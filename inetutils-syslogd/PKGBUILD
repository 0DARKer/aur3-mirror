pkgname=inetutils-syslogd
_pkgname=inetutils
pkgver=1.9.1
pkgrel=2
pkgdesc="A small syslogd"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/inetutils/"
license=('GPL3')
groups=('base')
depends=('readline' 'ncurses' 'pam')
options=('!emptydirs')
provides=('logger')
source=(http://ftp.gnu.org/gnu/inetutils/${_pkgname}-${pkgver}.tar.gz
        inetutils-syslogd.rc.d
        inetutils-syslogd.conf.d
        syslog.conf)
md5sums=('944f7196a2b3dba2d400e9088576000c'
         'cbd8ded38c7e91afedd2c137255ab28c'
         '8ca84ff5529755ab1dede5a855d621f6'
         'f732a7a038ca5eef05be22dd03c11503')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --libexec=/usr/sbin --localstatedir=/var \
    --sysconfdir=/etc --mandir=/usr/share/man --infodir=/usr/share/info \
    --without-wrap --with-pam \
    --disable-ftp --disable-ftpd \
    --disable-telnet --disable-telnetd \
    --disable-talk --disable-talkd \
    --disable-rlogin --disable-rlogind \
    --disable-rsh --disable-rshd \
    --disable-rexec --disable-rexecd \
    --disable-rcp --disable-hostname \
    --disable-tftp --disable-tftpd \
    --disable-ping --disable-ping6 \
    --disable-logger --enable-syslogd \
    --disable-inetd --disable-whois \
    --disable-uucpd --disable-ifconfig --disable-traceroute
  make
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/bin"
  rm -Rf "${pkgdir}/usr/share/info"
  install -D -m755 "${srcdir}/inetutils-syslogd.rc.d" "${pkgdir}/etc/rc.d/inetutils-syslogd"
  install -D -m644 "${srcdir}/inetutils-syslogd.conf.d" "${pkgdir}/etc/conf.d/inetutils-syslogd"
  install -D -m644 "${srcdir}/syslog.conf" "${pkgdir}/etc/syslog.conf"
}
