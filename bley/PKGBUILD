# Maintainer:   Lucky <archlinux@builds.lucky.li>

pkgname=bley
_pkgver=2.0.0-beta.1
pkgver="${_pkgver/-/.}"
pkgrel=1
pkgdesc="Intelligent greylisting daemon for Postfix"
url="http://bley.mx"
license=("BSD")
arch=("any")
depends=("python2" "python2-ipaddr" "python2-publicsuffix" "python2-pydns" "python2-pyspf" "python2-twisted")
optdepends=("sqlite: required for SQLite support"
            "python2-psycopg2: required for PostgreSQL support"
            "mysql-python: required for MySQL support"
            "python2-matplotlib: required for bleygraph")
makedepends=("python2-distribute")
source=("${url}/download/${pkgname}-${_pkgver}.tar.gz")
md5sums=("8ab73ef19e374d0da3becd1a3546b984")
sha1sums=("9249855fca23fc55be52d54a97cc1223a2c55388")

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
