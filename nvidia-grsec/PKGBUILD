# $Id: PKGBUILD 216984 2014-07-18 07:59:00Z tpowa $
# Maintainer : Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-grsec
pkgver=340.24
_extramodules=extramodules-3.15.5-grsec
pkgrel=2
pkgdesc="NVIDIA drivers for linux-grsec kernel"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-grsec>=3.15' 'linux-grsec<3.16' "nvidia-libgl" "nvidia-utils=${pkgver}")
makedepends=('linux-grsec-headers>=3.15' 'linux-grsec-headers<3.16')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia-grsec.install
options=(!strip)
source=("ftp://download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run"
        "ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run"
        "nvidia-drivers-340.24-pax-usercopy.patch"
        "nvidia-drivers-340.24-pax-constify.patch")
md5sums=('edd4c618265d22ef4d2b28baf070c6ff'
         'b71118e3e3ae9b227bf6f17a2cc8c726'
         '44f61839dbd7cdf6d18445c44bd004d3'
         'd1af855006f81c564a18389c928bab64')

[[ "$CARCH" = "i686" ]] && _pkg="NVIDIA-Linux-x86-${pkgver}"
[[ "$CARCH" = "x86_64" ]] && _pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    # patches here
    patch -Np1 -i ../nvidia-drivers-340.24-pax-usercopy.patch
    patch -Np1 -i ../nvidia-drivers-340.24-pax-constify.patch
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module

    cd uvm
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/uvm/nvidia-uvm.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-uvm.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/"*.ko
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-grsec.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-grsec.install"
}
