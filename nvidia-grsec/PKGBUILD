# $Id: PKGBUILD 216984 2014-07-18 07:59:00Z tpowa $
# Maintainer : Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-grsec
pkgver=343.36
_extramodules=extramodules-3.18.2-grsec
pkgrel=3
pkgdesc="NVIDIA drivers for linux-grsec kernel"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-grsec>=3.18' 'linux-grsec<3.19' "nvidia-libgl" "nvidia-utils=${pkgver}")
makedepends=('linux-grsec-headers>=3.18' 'linux-grsec-headers<3.19')
conflicts=('nvidia-96xx-grsec' 'nvidia-173xx-grsec')
license=('custom')
install=nvidia-grsec.install
options=(!strip)
source=("ftp://download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run"
        "ftp://download.nvidia.com/XFree86/Linux-x86_64/${pkgver}/NVIDIA-Linux-x86_64-${pkgver}-no-compat32.run"
        "nvidia-drivers-${pkgver}-pax-usercopy.patch"
        "nvidia-drivers-${pkgver}-pax-constify.patch"
        "nv-drm-${pkgver}.patch")
md5sums=('eae5d5407df0b371929a76f96aec4020'
         'dfdf53ee8787455fe92a760ffa6badcb'
         '4302daa0fddb7e2239815e5bf69c4dd7'
         'd1af855006f81c564a18389c928bab64'
         'ff8a5f979e4428f8c847423fb007042c')

[[ "$CARCH" = "i686" ]] && _pkg="NVIDIA-Linux-x86-${pkgver}"
[[ "$CARCH" = "x86_64" ]] && _pkg="NVIDIA-Linux-x86_64-${pkgver}-no-compat32"

prepare() {
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    # patches here
    patch -Np1 -i "../nvidia-drivers-${pkgver}-pax-usercopy.patch"
    patch -Np1 -i "../nvidia-drivers-${pkgver}-pax-constify.patch"
    patch -Np1 -i ../nv-drm-343.36.patch
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${_pkg}"/kernel
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module

    cd uvm
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 "${srcdir}/${_pkg}/kernel/uvm/nvidia-uvm.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia-uvm.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/"*.ko
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-grsec.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-grsec.install"
}
