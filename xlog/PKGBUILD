# Contributor: Vitaliy Berdinskikh ur6lad[at]archlinux.org.ua

pkgname=xlog
pkgver=2.0.5
pkgrel=1
pkgdesc="A logging program for amateur radio operators. #ham-radio"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/xlog/"
license=('GPL3')
depends=('pkg-config>=0.9.0' 'gtk2>=2.12.0' 'hamlib' 'libgnomeprint' 'shared-mime-info' 'desktop-file-utils')
optdepends=('cwdaemon: as external keyer' 'glabels: print log')
source=(http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
install=xlog.install

build() {
	cd $srcdir/$pkgname-$pkgver

	sed -i -e "s:icons:pixmaps:" data/pixmaps/Makefile.in

	./configure --prefix=/usr
	make

	sed -i -e \
	s:"Categories=Utility;Database;HamRadio;GTK":"Categories=Network;HamRadio;": \
	$srcdir/$pkgname-$pkgver/data/desktop/$pkgname.desktop
	sed -i -e s:'ApplicationHamRadio':'Application': \
	$srcdir/$pkgname-$pkgver/data/desktop/$pkgname.desktop
	sed -i -e s:'xlog-icon.png':'xlog-icon': $srcdir/$pkgname-$pkgver/data/desktop/$pkgname.desktop
	sed -i -e s:'text/x-xlog':'text/x-xlog;': $srcdir/$pkgname-$pkgver/data/desktop/$pkgname.desktop
}

package() {
	cd $srcdir/$pkgname-$pkgver

	make DESTDIR=$pkgdir install

	rm $pkgdir/usr/share/applications/mimeinfo.cache
	rm $pkgdir/usr/share/pixmaps/gnome-mime-text-x-xlog.png
}

md5sums=('dbdf0f29d5039165799ba0a8e53209be')
sha256sums=('d8290377acbb57d51182e511c4d11a9ea10c73f4d45434da210dd4a5c1b1b09f')
