# Contributor: Yichao Yu <yyc1992@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# See AUR page for current maintainer.

pkgname=fontconfig-git
pkgver=2.11.1.r1702
pkgrel=1
pkgdesc="A library for configuring and customizing font access"
arch=(i686 x86_64)
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat' 'freetype2')
makedepends=('gperf')
options=('!libtool')
conflicts=('fontconfig')
provides=('fontconfig')
install=fontconfig.install
source=("$pkgname::git://anongit.freedesktop.org/fontconfig"
        '29-replace-bitmap-fonts.conf')
sha256sums=('SKIP'
            '76eebd24e80a4f30eb2fe595d2df349bf44e781b711a8e5a10fc6c99e10c7421')

pkgver() {
  cd "$pkgname"
  printf "%s.r%s" \
    "$(git describe --abbrev=0 | sed 's/^v//')" \
    "$(git rev-list --count HEAD)"
}

build() {
  cd "$srcdir/$pkgname"

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install

  # Not in upstream, not in FC and not in Gentoo, although this probably should.
  # This satisfies pages like 
  # http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
  #
  install -dm755 "$pkgdir"/etc/fonts/{conf.d,conf.avail}
  install -m644 "$srcdir/29-replace-bitmap-fonts.conf" \
    "$pkgdir/etc/fonts/conf.avail"
  #
  #
  # Enable configuration below if you want...
  #
  #ln -s /etc/fonts/conf.avail/29-replace-bitmap-fonts.conf \
  #  "$pkgdir/etc/fonts/conf.d"

  #Install license
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
