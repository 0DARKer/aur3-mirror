# Contributor: joyfulgirl@archlinux.us
pkgname=dillo-hg
pkgver=2289
pkgrel=1
pkgdesc="A small, fast, FLTK-based web browser"
arch=('i686' 'x86_64')
url="http://www.dillo.org/"
license=('GPL3')
depends=('fltk' 'libxi' 'libxinerama' 'openssl' 'perl')
makedepends=('autoconf' 'automake' 'mercurial')
optdepends=('ttf-dejavu: Enable DejaVu Sans support')
provides=('dillo=2.0')
conflicts=('dillo' 'dillo-cvs' 'dillo2-cvs')
backup=('etc/dillo/dillorc' 'etc/dillo/dpidrc')
changelog=ChangeLog
source=()
md5sums=()

_hgroot="http://hg.dillo.org/dillo"
_hgrepo="dillo"

build() {
  cd $srcdir

  msg "Connecting to $_hgroot"
  if [ -d $_hgrepo ]; then
    cd $_hgrepo
    hg pull -u
    msg2 "Finished updating the local repository!"
  else
    hg clone $_hgroot $_hgrepo
    msg2 "Initial pull complete!"
  fi

  rm -rf ${srcdir}/${_hgrepo}-build
  cp -a ${srcdir}/${_hgrepo} ${srcdir}/${_hgrepo}-build
  cd ${srcdir}/${_hgrepo}-build

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/ \
      --enable-cookies \
      --enable-png \
      --enable-jpeg \
      --enable-gif \
      --enable-ssl \
      --enable-threaded-dns
  make
}

package() {
  cd ${srcdir}/${_hgrepo}-build
  make DESTDIR=$pkgdir install
}

# End of file
