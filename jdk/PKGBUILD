# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: Army <gmail.com: uli[dot]armbruster>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Guillaume ALAUX <Guillaume at alaux dot net>
# Contributor: Charles Ghislain <charlyghislain at gmail dot com>

pkgname=jdk
pkgver=7
pkgrel=3
arch=('i686' 'x86_64')
  [ "${CARCH}" = 'i686' ]   && _arch='i586'
  [ "${CARCH}" = 'x86_64' ] && _arch='x64'
url='http://jdk7.java.net/'
license=('custom')
source=("http://download.oracle.com/otn-pub/java/jdk/7/jdk-7-linux-${_arch}.tar.gz"
        'java-monitoring-and-management-console.desktop'
        'java-visualvm.desktop'
        'jdk.profile'
        'jdk.profile.csh'
        'derby-network-server'
        'derby-network-server.conf'
        'javaws-launcher')
[ "${CARCH}" = 'i686' ] && md5sums=('f97244a104f03731e5ff69f0dd5a9927')
[ "${CARCH}" = 'x86_64' ] && md5sums=('b3c1ef5faea7b180469c129a49762b64')
md5sums+=('81cf9b716263ba85f0528d3357dccb75'
         'ef27c6d4062f2f97c532a9e893b6adbe'
         'b83ab5742651b4234b7d2e20785c6693'
         '6f4cbf332816d2c4e9578ecd1d0dce7f'
         'a279e195e249000646895d93e199860d'
         '4bdff6982c66d24a879c424aaac3d04d'
         '45c15a6b4767288f2f745598455ea2bf')

package() {
  pkgdesc="Oracle's Java Development Kit"
  depends=('jre')
  provides=('java-environment=7')
  conflicts=('java-environment')
  install='jdk.install'

  #copy icon
  install -D -m644 ${srcdir}/jdk1.7.0/jre/lib/desktop/icons/hicolor/48x48/apps/sun-java.png \
    ${pkgdir}/usr/share/pixmaps/java.png

  # main files
  install -d ${pkgdir}/opt
  cp -r jdk1.7.0 ${pkgdir}/opt/java

  # remove jre folder provided by the jre package
  rm -r ${pkgdir}/opt/java/jre
  # can not package src.zip due to international use restrictions for certain countries
  rm ${pkgdir}/opt/java/src.zip
  # remove windows specific batch files
  find ${pkgdir}/opt/java -name '*\.bat' -delete
  # remove already installed icons, mime and .desktop files provided by jre
  rm -r ${pkgdir}/opt/java/lib/desktop

  # profiles
  install -D ${srcdir}/jdk.profile \
    ${pkgdir}/etc/profile.d/jdk.sh
  install -D ${srcdir}/jdk.profile.csh \
    ${pkgdir}/etc/profile.d/jdk.csh

  # licenses
  install -d ${pkgdir}/usr/share/licenses/jdk
  install -m644 \
    ${pkgdir}/opt/java/{COPYRIGHT,LICENSE,THIRDPARTYLICENSEREADME.txt} \
    ${pkgdir}/usr/share/licenses/jdk

  # desktop entries
  install -d ${pkgdir}/usr/share/applications
  install -m644 \
    ${srcdir}/java-{monitoring-and-management-console,visualvm}.desktop \
    ${pkgdir}/usr/share/applications

  # javadb (apache derby) daemon files FS#21186
  install -D ${srcdir}/derby-network-server \
    ${pkgdir}/etc/rc.d/derby-network-server
  install -D -m644 ${srcdir}/derby-network-server.conf \
    ${pkgdir}/etc/conf.d/derby-network-server
}

