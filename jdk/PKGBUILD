# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban

pkgname=jdk
pkgver=7u3
_build=b04
pkgrel=1
pkgdesc="Java 7 Development Kit"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=('custom')
depends=('jre' 'libx11')
provides=('java-environment=7')
conflicts=('java-environment')
backup=('etc/conf.d/derby-network-server'
        'etc/profile.d/jdk.csh'
        'etc/profile.d/jdk.sh')
install=jdk.install
_arch=i386; [ "$CARCH" == 'x86_64' ] && _arch=amd64
#source=("http://download.oracle.com/otn-pub/java/jdk/$pkgver-$_build/jdk-$pkgver-linux-i586.tar.gz"
source=("http://uni-smr.ac.ru/archive/dev/java/SDKs/oracle/j2se/7/jdk-$pkgver-linux-i586.tar.gz"
        'java-monitoring-and-management-console.desktop'
        'java-visualvm.desktop'
        'jdk.sh'
        'jdk.csh'
        'derby-network-server'
        'derby-network-server.conf'
        'javaws-launcher')
md5sums=('99a0fa02b608985c271e55122f0621bf'  # jdk-$pkgver-linux-i586.tar.gz
         'da10de5e6507c392fc9871076ef53ec6'  # java-monitoring-and-management-console.desktop
         '35fd89c5c170021d2183593335703703'  # java-visualvm.desktop
         'b83ab5742651b4234b7d2e20785c6693'  # jdk.sh
         '6f4cbf332816d2c4e9578ecd1d0dce7f'  # jdk.csh
         'a279e195e249000646895d93e199860d'  # derby-network-server
         '4bdff6982c66d24a879c424aaac3d04d'  # derby-network-server.conf
         '45c15a6b4767288f2f745598455ea2bf') # javaws-launcher
#[ "$CARCH" == 'x86_64' ] && source[0]="http://download.oracle.com/otn-pub/java/jdk/$pkgver-$_build/jdk-$pkgver-linux-x64.tar.gz" && md5sums[0]='969927251b558ffbc09ede1e89200d40'
[ "$CARCH" == 'x86_64' ] && source[0]="http://uni-smr.ac.ru/archive/dev/java/SDKs/oracle/j2se/7/jdk-$pkgver-linux-x64.tar.gz" && md5sums[0]='969927251b558ffbc09ede1e89200d40'

package(){
  msg2 "Creating required dirs"
  cd $(ls -1d jdk1.7.0_* | tail -1)
  mkdir -p "$pkgdir"/{opt/java/{,jre/lib/$_arch},etc/profile.d,usr/share/{applications,licenses/jdk}}

  msg2 "Fetching missing libraries from 'jre' to fix jconsole"
  mv jre/lib/$_arch/lib{saproc,attach}.so "$pkgdir"/opt/java/jre/lib/$_arch/

  msg2 "Removing already provided 'jre' and redundant .bat scripts"
  rm -r jre/ lib/visualvm/platform/docs/ # lib/desktop/
  find . -name '*\.bat' -delete

  msg2 "Moving stuff in place"
  mv * "$pkgdir"/opt/java/

  msg2 "Installing scripts, .desktop files, confs and licenses"
  cd "$srcdir"
  install -m755 jdk.{c,}sh "$pkgdir"/etc/profile.d/
  cp "$pkgdir"/opt/java/{COPYRIGHT,LICENSE,THIRDPARTYLICENSEREADME.txt} "$pkgdir"/usr/share/licenses/jdk/
  cp java-{visualvm,monitoring-and-management-console}.desktop "$pkgdir"/usr/share/applications/
  install -D derby-network-server "$pkgdir"/etc/rc.d/derby-network-server
  install -Dm644 derby-network-server.conf "$pkgdir"/etc/conf.d/derby-network-server
}