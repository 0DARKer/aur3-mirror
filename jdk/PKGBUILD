# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban

pkgname=jdk
pkgver=7u1
pkgrel=1
pkgdesc="The Java Development Kit"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=('custom')
depends=('jre')
provides=('java-environment=7')
conflicts=(java-environment)
install=jdk.install
source=("http://download.oracle.com/otn-pub/java/jdk/$pkgver-b08/jdk-$pkgver-linux-i586.tar.gz"
        'java-monitoring-and-management-console.desktop'
        'java-visualvm.desktop'
        'jdk.sh'
        'jdk.csh'
        'derby-network-server'
        'derby-network-server.conf'
        'javaws-launcher')
md5sums=('7267759f93bc6fb23046dd42d40d1c2f'
         '352ca9559e47e06dfcd401c271fb0f4c'
         '38c38a60fde0e064c5258a2406f03039'
         'b83ab5742651b4234b7d2e20785c6693'
         '6f4cbf332816d2c4e9578ecd1d0dce7f'
         'a279e195e249000646895d93e199860d'
         '4bdff6982c66d24a879c424aaac3d04d'
         '45c15a6b4767288f2f745598455ea2bf')
[ "$CARCH" == 'x86_64' ] && source[0]="http://download.oracle.com/otn-pub/java/jdk/$pkgver-b08/jdk-$pkgver-linux-x64.tar.gz" && md5sums[0]='9707049b591f47e5c3988a9f029c015e'

package(){
  cd $(ls -1d jdk1.7.0_* | tail -1)
  rm -r jre lib/desktop
  find . -name '*\.bat' -delete
  mkdir -p "$pkgdir"/opt/java
  mv * "$pkgdir"/opt/java/

  cd ..
  install -D jdk.sh "${pkgdir}"/etc/profile.d/jdk.sh
  install -m755 jdk.csh "$pkgdir"/etc/profile.d/
  install -Dm644 "$pkgdir"/opt/java/COPYRIGHT "$pkgdir"/usr/share/licenses/jdk/COPYRIGHT
  cp "$pkgdir"/opt/java/{LICENSE,THIRDPARTYLICENSEREADME.txt} "$pkgdir"/usr/share/licenses/jdk/
  install -Dm644 java-visualvm.desktop "$pkgdir"/usr/share/applications/java-visualvm.desktop
  cp java-monitoring-and-management-console.desktop "$pkgdir"/usr/share/applications/
  install -D derby-network-server "$pkgdir"/etc/rc.d/derby-network-server
  install -Dm644 derby-network-server.conf "$pkgdir"/etc/conf.d/derby-network-server
}