# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: rabyte <rabyte__gmail>

pkgname=dragonhunt
pkgver=3.56
pkgrel=4
pkgdesc="Extensible graphical RPG written in Python"
arch=('any')
url="http://emhsoft.com/dh.html"
license=('GPL' 'CCPL' 'custom')
depends=('python2' 'python-pygame')
source=("http://emhsoft.com/dh/Dragon_Hunt-${pkgver}.tar.gz"
	"${pkgname}.sh"
	"${pkgname}-editor.sh")
md5sums=('fa5aecc577b8ea7ece211c8f20ff1f0a'
         'ca6e9651f925abd99dec43dc03ef949f'
         'ca149aabc96ed54874018fa0550509bc')

package() {
  cd "${srcdir}/Dragon_Hunt-${pkgver}"

  # Install game files
  mkdir -p "${pkgdir}/opt/${pkgname}"
  rm code/.{cvsignore,directory}
  cp -r code modules "${pkgdir}/opt/${pkgname}"
  find "${pkgdir}/opt/${pkgname}" -type d -exec chmod 755 '{}' \;
  find "${pkgdir}/opt/${pkgname}" -type f -exec chmod 644 '{}' \;
  install -m777 -d "${pkgdir}/opt/${pkgname}/modules/"{Dark\ Ages,Dragon\ Hunt}/{data/maps,saves}
  install -m666 settings.txt "${pkgdir}/opt/${pkgname}"

  # Install docs
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 docs/* README.txt "${pkgdir}/usr/share/doc/${pkgname}"
  sed -n "/Graphic License/,/access to anyway/p" README.txt > LICENSE
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install launchers
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 "../${pkgname}-editor.sh" "${pkgdir}/usr/bin/${pkgname}-editor"
}
