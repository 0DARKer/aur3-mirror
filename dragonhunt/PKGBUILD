# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: rabyte <rabyte__gmail>

pkgname=dragonhunt
pkgver=3.56
pkgrel=3
pkgdesc="An extensible graphical RPG"
arch=('any')
url="http://emhsoft.com/dh.html"
license=('GPL' 'CCPL' 'custom')
depends=('python-pygame')
install=$pkgname.install
source=(http://emhsoft.com/dh/Dragon_Hunt-$pkgver.tar.gz
	$pkgname.sh
	$pkgname-editor.sh
	$pkgname-setup.sh
	$pkgname.patch)
md5sums=('fa5aecc577b8ea7ece211c8f20ff1f0a'
         '243751a413d2bca4938a93b846144532'
         '4aa3d27739103393666a492e2193be47'
         'f53126b7645aa74921a8d93b4eee5658'
         'b32983fda5e84c40f9cf8701c6feb3ad')

build() {
  cd "$srcdir"/Dragon_Hunt-$pkgver

  # fix path to the saves/settings directory
  patch -Np0 -i ../$pkgname.patch || return 1

  # remove some unneded files
  rm -f code/.{cvsignore,directory}

  # install data
  install -m755 -d "$pkgdir"/usr/share/$pkgname
  cp -rf code modules "$pkgdir"/usr/share/$pkgname/

  # set permissions
  find "$pkgdir"/usr/share/$pkgname -type d -exec chmod 755 {} +
  find "$pkgdir"/usr/share/$pkgname -type f -exec chmod 644 {} +

  # isntall docs
  install -m755 -d "$pkgdir"/usr/share/doc/$pkgname
  install -m755 -t "$pkgdir"/usr/share/doc/$pkgname \
    docs/{example_map,Items,tiles}.txt docs/*.html {README,settings}.txt

  # install license
  sed -n "/Graphic License/,/access to anyway/p" README.txt > LICENSE
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # install launchers
  install -Dm755 ../$pkgname.sh "$pkgdir"/usr/bin/$pkgname
  install -Dm755 ../$pkgname-editor.sh "$pkgdir"/usr/bin/$pkgname-editor
  install -Dm755 ../$pkgname-setup.sh "$pkgdir"/usr/bin/$pkgname-setup
}
