# Maintainer : Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
        
pkgname=python-libtorrent-rasterbar
pkgver=0.16.10
pkgrel=7
epoch=1 
pkgdesc="Python 3 bindings for the libtorrent library"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python' 'libtorrent-rasterbar')
makedepends=('boost')
options=('!libtool' '!emptydirs')
source=(http://libtorrent.googlecode.com/files/libtorrent-rasterbar-$pkgver.tar.gz)
sha512sums=('a8b5d762f2b50ded1f8589efeedf2d1e0b5cc39d71bcc1840658137c1058cd03fe249c6855c5c483304aafb74b616d241456fecf00dec50608aa82af9f795819')
        
build() {
  cd libtorrent-rasterbar-$pkgver
  ./configure --prefix=/usr \
      --enable-python-binding \
      --with-libgeoip=system \
      --with-boost-python=boost_python3

  # patch for a critical bug affecting 32bit systems https://bugs.archlinux.org/task/33429
  sed -i '/all_categories/s/0x7fffffff/0xfffffff/' include/libtorrent/alert.hpp

  cd bindings/python
  make
}

package() {
  cd libtorrent-rasterbar-$pkgver/bindings/python
  make DESTDIR="$pkgdir" install
  install -D "${srcdir}/libtorrent-rasterbar-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
