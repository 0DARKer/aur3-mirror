# Maintainer : Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
        
pkgname=python-libtorrent-rasterbar
pkgver=0.16.15
pkgrel=1
pkgdesc="Python 3 bindings for the libtorrent library"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python' 'libtorrent-rasterbar')
makedepends=('boost')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/project/libtorrent/libtorrent/libtorrent-rasterbar-${pkgver}.tar.gz")
sha512sums=('f55e75038a627399041384c2901aa36384e804d8ae2aa7edbbfd4ccc39dedf64170876d9fac03c64f537d4ff8b736ecaea0399211ba86ab1d08753828dc8f2a4')
        
build() {
  cd libtorrent-rasterbar-$pkgver
  ./configure --prefix=/usr \
      --enable-python-binding \
      --with-libgeoip=system \
      --with-boost-python=boost_python3
  cd bindings/python
  make
}

package() {
  cd libtorrent-rasterbar-$pkgver/bindings/python
  make DESTDIR="$pkgdir" install
  install -D "${srcdir}/libtorrent-rasterbar-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
