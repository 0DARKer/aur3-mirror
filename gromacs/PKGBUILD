# $Id: PKGBUILD 57440 2011-10-27 20:16:15Z lcarlier $
# Maintainer: Eduardo Martins Lopes "duca" <edumlopes@gmail.com>
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Ricardo <rikardo.horo@gmail.com>
# Contributor: Hector <hsearaDOTatDOTgmailDOTcom>

pkgname=gromacs
pkgver=4.6.2
pkgrel=1
pkgdesc='A versatile package to perform molecular dynamics, i.e. simulate the Newtonian equations of motion for systems with hundreds to millions of particles.'
url='http://www.gromacs.org/'
license=("GPL")
arch=('i686' 'x86_64')
depends=('fftw' 'lesstif' 'perl' 'libxml2' 'libsm' 'libx11')
makedepends=('cmake')
options=('!libtool')
source=(ftp://ftp.gromacs.org/pub/gromacs/${pkgname}-${pkgver}.tar.gz)
md5sums=('8a50f65d601a780e95962b680728efeb')

build() {
  mkdir -p ${srcdir}/{single,double}
  msg2 "Building the double precision files"
  cd ${srcdir}/double	
  cmake -DCMAKE_PREFIX=/usr/ -DCMAKE_INSTALL_PREFIX=/usr -DGMX_THREADS=ON -DGMX_SHARED_LIBS=ON -DGMX_DOUBLE=ON -DGMX_X11=ON -DGMX_LIBS_SUFFIX=_d ../${pkgname}-${pkgver}/
  make

  msg2 "Building the single precision files"
  cd ${srcdir}/single
  cmake -DCMAKE_PREFIX=/usr/ -DCMAKE_INSTALL_PREFIX=/usr/ -DGMX_THREADS=ON -DGMX_SHARED_LIBS=ON -DGMX_X11=ON ../${pkgname}-${pkgver}/
  make
}

package() {

  mkdir -p ${pkgdir}/etc/profile.d/
  mkdir -p ${pkgdir}/usr/share/bash-completion/completions
  msg2 "Making the single precision executables"
  cd ${srcdir}/single
  make  DESTDIR=${pkgdir} install

  # Cleaning up, kept the csh completion at default location
  msg2 "Making the double precision executables"
  cd ${srcdir}/double
  make DESTDIR=${pkgdir} install

  # installing completions in correct location and environment setup script
  install -D -m755 ${srcdir}/${pkgname}-${pkgver}/scripts/completion.bash "${pkgdir}/usr/share/bash-completion/completions/gromacs"
  mv ${pkgdir}/usr/bin/GMXRC.* ${pkgdir}/etc/profile.d/
  sed "s:/usr/bin:/etc/profile.d:" ${pkgdir}/usr/bin/GMXRC > ${pkgdir}/etc/profile.d/GMXRC
  chmod 755 ${pkgdir}/etc/profile.d/GMXRC
  rm -f ${pkgdir}/usr/bin/completion.*
  rm -f ${pkgdir}/usr/bin/GMXRC
}
