# Maintainer: Jiaxi Hu <sftrytry_AT_gmail_DOT_com>

pkgname='android-qt5'
pkgver=5.2.0
_pkgver=5.2
pkgrel=1
pkgdesc="Qt 5 for Android"
arch=('x86_64')
url='http://qt-project.org'
license=('GPL3' 'LGPL')
makedepends=('git' 'libgl' 'sqlite' 'zlib' 'python2' 'ruby' 'gperf' 'libxslt' 'fontconfig')
depends=('android-sdk' 'android-sdk-platform-tools' 'android-sdk-build-tools>=r19' 'android-ndk' 'jdk7-openjdk' 'android-platform-10' 'android-platform-11' 'android-platform-16' 'apache-ant')
groups=('qt-android' 'qt5-android')
options=('!libtool' '!strip')
optdepends=('qtcreator: Qt IDE with support for Android')
_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://download.qt-project.org/official_releases/qt/${_pkgver}/${pkgver}/single/${_pkgfqn}.tar.xz")
md5sums=('8f60b47ca9461831d940f579ee90517e')

build() {
  cd ${_pkgfqn}

  unset CC
  unset CXX
  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  unset CARCH
  unset CHOST
  unset QMAKESPEC
  unset QTDIR

  #5.2 seems to enable neon by default for optimization of latest devices.
  #If you want to use your old ones, like my xoom. You can disable it with 
  #-no-neon and recompile it.
  export ANDROID_BUILD_TOOLS_REVISION=19
  ./configure -confirm-license -opensource \
    -prefix /opt/android-qt5/${pkgver} \
    -docdir /usr/share/doc/android-qt5-${_pkgver} \
    -xplatform android-g++ \
    -nomake tests \
    -nomake examples \
    -android-ndk /opt/android-ndk \
    -android-sdk /opt/android-sdk \
    -android-toolchain-version 4.8 \
    -android-ndk-host linux-x86_64 \
    -android-arch armeabi-v7a \
    -android-ndk-platform android-19 \
    -no-pkg-config \
    -skip qttranslations \
    -skip qtwebkit \
    -skip qtserialport \
    -skip qtwebkit-examples \
    -no-warnings-are-errors
    
  make || return 1
}

package() {
  cd ${_pkgfqn}
  make INSTALL_ROOT=${pkgdir} install
}

