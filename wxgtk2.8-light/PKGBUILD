# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=wxgtk2.8-light
pkgver=2.8.12.1
pkgrel=2
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'libgl' 'libxxf86vm' 'libsm' 'sdl')
makedepends=('mesa' 'glu' 'libxt')
provides=('wxgtk2.8=2.8.12.1')
conflicts=('wxgtk2.8')
#source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2")
source=("http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2"
        'wxGTK-2.8-collision.patch')
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5'
          '575fb4688051c246777988332f55703df403d6b9')

prepare() {
  rm -fr build
  cp -R "wxPython-src-${pkgver}" build
  cd build
  patch -p1 -i ../wxGTK-2.8-collision.patch
}

build() {
  cd build
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,opengl,regex=builtin,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode} \
              --disable-{optimize,mediactrl,precomp-headers,webview}
  make
  make -C locale allmo
  make -C contrib/src
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
  make -C contrib/src DESTDIR="${pkgdir}" install
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
