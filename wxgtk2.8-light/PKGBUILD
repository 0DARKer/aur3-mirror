# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=wxgtk2.8-light
pkgver=2.8.12.1
pkgrel=3
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'libgl' 'libxxf86vm' 'libsm' 'sdl')
makedepends=('mesa' 'glu' 'libxt' 'git')
provides=("wxgtk2.8=${pkgver}")
conflicts=('wxgtk2.8')
source=("wxwidgets::git+https://github.com/wxWidgets/wxPython.git#tag=wxPy-${pkgver}"
        'wxGTK-2.8-collision.patch')
sha1sums=('SKIP'
          '75d2292a0058570aa6071b4bee6eef69e47f1208')
_gitname=wxwidgets

prepare() {
  cd "${_gitname}"
  patch -p1 -i ../wxGTK-2.8-collision.patch
}

build() {
  cd "${_gitname}"
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,opengl,regex=builtin,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode} \
              --disable-{optimize,mediactrl,precomp-headers,webview}
  make
  make -C locale allmo
  make -C contrib/src
}

package() {
  cd "${_gitname}"

  make DESTDIR="${pkgdir}" install
  make -C contrib/src DESTDIR="${pkgdir}" install
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
