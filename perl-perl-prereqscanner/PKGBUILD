# Contributor : Konrad Borowski <glitchmr@myopera.com>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>
# Maintainer : Kars Wang <jaklsy AT gmail.com>

pkgname='perl-perl-prereqscanner'
_dist='Perl-PrereqScanner'
pkgver='1.022'
pkgrel='1'
pkgdesc="a tool to scan your Perl code for its prerequisites"
arch=('any')
url="http://search.cpan.org/dist/${_dist}"
license=('PerlArtistic' 'GPL')
depends=('perl-cpan-meta-requirements'
'perl-list-moreutils'
'perl-moose'
'perl-ppi>=1.215'
'perl-params-util'
'perl-string-rewriteprefix>=0.005'
'perl-namespace-autoclean'
'perl>=5.008'
'perl-getopt-long-descriptive'
'perl-module-path')
options=('!emptydirs' 'purge')
source=("http://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/${_dist}-${pkgver}.tar.gz")
sha256sums=('4d9f1dd5ddeeca5246902fe7a9676ca3d2419b14516a758409aafa817deab94b')

build() (
  cd "$srcdir/$_dist-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
)

check() (
  cd "$srcdir/$_dist-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
)

package() (
  cd "$srcdir/$_dist-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

