# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Ansuransax <ansuransax at hotmail dot com>

pkgname=sec
pkgver=2.7.4
pkgrel=1
pkgdesc="A logfile monitoring tool with support for event correlation, written in perl"
arch=('any')
url="http://kodu.neti.ee/~risto/sec/"
license=('GPL')
depends=('perl')
source=("http://downloads.sourceforge.net/simple-evcorr/${pkgname}-${pkgver}.tar.gz"
	'sec.rc'
	'sec.service')

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# install binaries and scripts
	install -D -m0755 sec ${pkgdir}/usr/bin/sec
	install -D -m0755 contrib/convert.pl ${pkgdir}/usr/share/${pkgname}/contrib/convert.pl
	install -D -m0755 contrib/swatch2sec.pl ${pkgdir}/usr/share/${pkgname}/contrib/swatch2sec.pl
	  
	# install license, manpage, changelog and readme
	install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
	install -D -m0644 sec.man ${pkgdir}/usr/share/man/man1/sec.1
	install -D -m0644 ChangeLog ${pkgdir}/usr/share/${pkgname}/ChangeLog
	install -D -m0644 README ${pkgdir}/usr/share/${pkgname}/README

	# install initscript and systemd service
	install -D -m0755 ${srcdir}/sec.rc ${pkgdir}/etc/rc.d/sec
	install -D -m0644 ${srcdir}/sec.service ${pkgdir}/usr/lib/systemd/system/sec.service
}

sha256sums=('9970285caf14406c74a6899bfb1dc376a5ee3bfc95c6299ee3d44ad4790526f2'
            'fcf164553cd687c69bba521d6b63c034a3527385249c5649db7c008e3f6a3a59'
            '8aa9b458d3c95f76e5fe164b119c38108e640a970abcee4a1c76f70b134b98a9')
