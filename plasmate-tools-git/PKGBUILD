# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasmate-tools-git
pkgver=v1.0.16.gb428308
pkgrel=1
pkgdesc='Plasma tools that are installed with Plasmate and were in kde-workspace 4.9'
arch=('i686' 'x86_64')
url='http://www.kde.org/'
license=('LGPL')
depends=('kdepimlibs')
makedepends=('cmake' 'git' 'automoc4' 'boost')
conflicts=('plasmate-git' 'plasmate' 'kdebase-workspace<=4.9.80')
source=('git://anongit.kde.org/plasmate')
md5sums=('SKIP')

pkgver() {
  cd plasmate
  git describe --always | sed 's|-|.|g'
}

build() {
  mkdir -p build
  cd build

  cmake ../plasmate \
    -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
    -DCMAKE_BUILD_TYPE=Release

  cd engineexplorer
  make
  cd ../

  cd plasmoidviewer
  make
  cd ../
}

package() {
  cd build/engineexplorer
  make DESTDIR="${pkgdir}" install
  cd ../..

  cd build/plasmoidviewer
  make DESTDIR="${pkgdir}" install
}
