# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=shishi
pkgver=1.0.0
pkgrel=2
pkgdesc="a GNU implementation of the Kerberos 5 network security system"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/shishi/"
license=('GPL3')
depends=(
#  'gnutls'
  'libidn'
  'libgcrypt'
  'libgpg-error'
  'libtasn1'
  'pam'
)
makedepends=(
  'help2man'
)
backup=(
  'etc/shishi/shisa.conf'
  'etc/shishi/shishi.conf'
)
install="$pkgname.install"
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz"
        "ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz.sig"
        "shishid.service")
sha1sums=('076626b59459cbf34f2f5837cc9e482787889fe9'
          '6dd7178afd16db05230d454cf4a65518e028df47'
          '16c0fd94348c19df6c1ad82cda37c145341d64bf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # Disable STARTTLS due to out-of-date shishi code
  ./configure \
    --prefix=/usr         \
    --sysconfdir=/etc     \
    --localstatedir=/var  \
    --libdir=/usr/lib     \
    --with-system-asn1    \
    --disable-starttls    \
    ;
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -dm0700 "$pkgdir/var/shishi"
  make DESTDIR="$pkgdir/" install

  install -Dm0644 "$srcdir/shishid.service" "$pkgdir/lib/systemd/system/shishid.service"
}

# vim:set ts=2 sw=2 et:
