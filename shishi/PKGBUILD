# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=shishi
pkgver=1.0.1
pkgrel=2
pkgdesc="a GNU implementation of the Kerberos 5 network security system"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/shishi/"
license=('GPL3')
depends=(
  'gnutls'
  'libidn'
  'libgcrypt'
  'libgpg-error'
  'libtasn1'
  'pam'
)
makedepends=(
  'help2man'
)
backup=(
  'etc/shishi/shisa.conf'
  'etc/shishi/shishi.conf'
)
install="$pkgname.install"
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz"
        "ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz.sig"
        "shishid.service")
sha1sums=('0e914dbcc27516702acd668929b29c42f850daf5'
          'd2bf90a40e0d2f4f5bff428e3812bf277f977e7a'
          '16c0fd94348c19df6c1ad82cda37c145341d64bf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr         \
    --sysconfdir=/etc     \
    --localstatedir=/var  \
    --libdir=/usr/lib     \
    --with-system-asn1    \
    ;
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -dm0700 "$pkgdir/var/shishi"
  make DESTDIR="$pkgdir/" install

  install -Dm0644 "$srcdir/shishid.service" "$pkgdir/usr/lib/systemd/system/shishid.service"
}

# vim:set ts=2 sw=2 et:
