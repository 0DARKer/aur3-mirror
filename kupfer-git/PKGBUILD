# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkgbasename=kupfer
pkgname=$_pkgbasename-git
pkgver=20101113
pkgrel=1
pkgdesc="A program to change, speed up and make everything about files and programs more fun on your computer."
arch=(any)
url="http://kaizer.se/wiki/kupfer/"
license=('GPL')
depends=('pyxdg' 'pygtk' 'dbus-python'  'python-keybinder' 'python-keyring'
         'hicolor-icon-theme' 'shared-mime-info')
optdepends=('gnome-python-desktop: enables all plugins and gnome integration'
            'python-wnck: tracks running applications'
            'python-appindicator: ubuntu-style notification icon')
makedepends=('git')
provides=("$_pkgbasename")
conflicts=("$_pkgbasename")
install="$pkgname.install"
_wafver=1.5.8
source=("http://waf.googlecode.com/files/waf-$_wafver")
md5sums=('9697e84e0cd733928a52a72c6fb34467')

_gitroot="git://git.gnome.org/kupfer"
_gitname="kupfer"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # Placing waf builder
  cp ../waf-$_wafver waf

  # Python2 fixes
  export PYTHON="/usr/bin/python2"

  sed \
	-e "s_env python_&2_" \
	-e "s_bin/python_&2_" \
	-i `grep -rlE "(env python|bin/python)" .`

  python2 waf configure --prefix=/usr
  python2 waf
} 

package() {
  cd "$srcdir/$_gitname-build"

  python2 waf install -f --destdir="$pkgdir/"
}
