# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=kupfer
pkgname=$_pkg-git
pkgver=20110304
pkgrel=1
pkgdesc="A program to change, speed up and make everything about files and programs more fun on your computer."
arch=(any)
url="http://kaizer.se/wiki/kupfer/"
license=('GPL')
depends=('pyxdg' 'pygtk' 'dbus-python'  'python-keybinder' 'python-keyring'
         'hicolor-icon-theme' 'shared-mime-info')
optdepends=('gnome-python-desktop: enables all plugins and gnome integration'
            'python-wnck: tracks running applications'
            'python-appindicator: ubuntu-style notification icon')
makedepends=('git')
provides=("$_pkg")
conflicts=("$_pkg")
install="$pkgname.install"
_wafver=1.6.3
source=("http://waf.googlecode.com/files/waf-$_wafver")
md5sums=('94147318dd0b83cdf43c0e64b3ea6566')

_gitroot="git://git.gnome.org/kupfer"
_gitname="kupfer"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # Placing waf builder
  cp ../waf-$_wafver waf

  # Python2 fixes
  export PYTHON="/usr/bin/python2"

  sed -e "s_env python_&2_;s_bin/python_&2_" \
      -i `grep -rlE "(env python|bin/python)" .`

  python2 waf configure --prefix=/usr
  python2 waf
} 

package() {
  cd "$srcdir/$_gitname-build"

  python2 waf install -f --destdir="$pkgdir/"
}
