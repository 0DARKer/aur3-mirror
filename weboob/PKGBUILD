# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=weboob
pkgver=0.i
pkgrel=2
pkgdesc="Web Out Of Browsers provides several applications to interact with a lot of websites."
arch=('any')
url="http://weboob.org/"
license=('GPL')
depends=('phonon-qt4' 'which' 'python2-pyqt' 'python2-elementtidy' 'python2-prettytable'
         'python2-dateutil' 'python2-feedparser' 'python2-gdata' 'python2-html2text'
         'python2-imaging' 'python2-lxml' 'python2-mechanize' 'python2-simplejson'
         'python2-pysqlite' 'python2-yaml' 'python2-cssselect' 'python2-requests')
makedepends=('python2-distribute')
optdepends=('gnupg: check for repository authenticity'
            'python2-routes: contrib backends'
            'python2-webob: contrib backends'
            'python2-mako: contrib backends'
            'pywebkitgtk: contrib backends'
            'python2-pillow: PIL replacement'
            'python2-termcolor: color formatting'
            'python2-nose: test suite')
install="$pkgname.install"
source=("http://symlink.me/attachments/download/246/$pkgname-$pkgver.tar.gz"
        "https://symlink.me/projects/weboob/repository/revisions/4b4ec43ab76ebf7875bb989d7403eab11dcdb631/diff.diff")
md5sums=('a782b02d0be309d9e75d2dbc58d71769'
         '7f4f1dda7fb86784d1d84a931a1f51df')

prepare() {
  cd $pkgname-$pkgver

  # fix bug #1386
  # https://symlink.me/issues/1386
  patch -p1 -i ../${source[1]##*/}
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --qt --xdg --prefix=/usr --root="$pkgdir"
}
