# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=weboob
pkgver=0.5
pkgrel=2
pkgdesc="Web Out Of Browsers provides several applications to interact with a lot of websites."
arch=('any')
url="http://weboob.org/"
license=('GPL')
depends=('python2-qt' 'python2-elementtidy' 'python2-prettytable' 'python2-webob'
         'python-clientform' 'python-dateutil' 'python-feedparser' 'python-gdata'
         'python-html2text' 'python-html5lib' 'python-imaging' 'python-lxml'
         'python-mako' 'python-mechanize' 'python-nose' 'python-pysqlite' 'pygtk'
         'python-routes' 'python-yaml' 'pywebkitgtk' 'pyxdg' 'hicolor-icon-theme')
optdepends=('python-simplejson: simplejson backend')
conflicts=('weboob-git')
install="$pkgname.install"
source=("http://weboob.org/attachments/download/103/$pkgname-$pkgver.tar.gz")
md5sums=('076cbfc451b37136339f0b6b732b58d9')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fixes
  export PYTHON="python2"
  sed -i "s_env python_&2_" `grep -rl "env python" .`
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir"
}
