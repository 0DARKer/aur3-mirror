# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=weboob
pkgver=0.c
pkgrel=2
pkgdesc="Web Out Of Browsers provides several applications to interact with a lot of websites."
arch=('any')
url="http://weboob.org/"
license=('GPL')
depends=('python2-pyqt' 'python2-xdg' 'python2-elementtidy' 'python2-prettytable'
         'python2-dateutil' 'python2-feedparser' 'python2-gdata' 'python2-html2text'
         'python2-imaging' 'python2-lxml' 'python2-mechanize' 'python2-simplejson'
         'python2-pysqlite' 'python2-yaml' 'python2-cssselect' 'phonon' 'which')
optdepends=('gnupg: check for repository authenticity'
            'python2-routes: contrib backends'
            'python2-webob: contrib backends'
            'python2-mako: contrib backends'
            'pywebkitgtk: contrib backends'
            'python2-nose: test suite')
install="$pkgname.install"
source=("http://symlink.me/attachments/download/182/$pkgname-$pkgver.tar.gz"
        "prettytable.patch::http://git.symlink.me/?p=romain/weboob.git;a=patch;h=bad531679aacdc4a9051b2a87458f27251b6c9e2")
md5sums=('858edc5adb2ebdd2f42db01e1468ce36'
         'effbb0692d970a4f2dfb74aeee61c18f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2-prettytable 0.6 patch
  patch -p1 -i ../prettytable.patch

  # remove forced white background property
  sed -i "48,50d" weboob/applications/qvideoob/ui/video.ui
  sed -i "146,150d" weboob/applications/qvideoob/ui/main_window.ui

  # pyuic4 executable patch
  sed -i "s/pyuic4'/python2-&/g" setup.py
  find -name Makefile -exec sed -i 's/pyuic4/python2-pyuic4/g' '{}' \;
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir"
}
