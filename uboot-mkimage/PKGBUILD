# Maintainer: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>
pkgname=uboot-mkimage
pkgver=2013.04
pkgrel=1
pkgdesc="U-Boot mkimage utility, which encapsulates a compressed uImage Linux kernel image with header information, CRC32 checksum, etc, for use with the U-Boot bootloader"
arch=('i686' 'x86_64')
url="http://www.denx.de/wiki/U-Boot/WebHome"
license=('GPL')
depends=('glibc')

source=(http://ftp.denx.de/pub/u-boot/u-boot-${pkgver}.tar.bz2)
md5sums=('21bf962d69938ed4ed783b792b2b074e')

build() {
  cd "$srcdir/u-boot-${pkgver}"

  # Build tools only
  make tools
}

package() {
  install -Dm 755 $srcdir/u-boot-${pkgver}/tools/mkimage $pkgdir/usr/bin/mkimage
  install -Dm 644 $srcdir/u-boot-${pkgver}/doc/mkimage.1 $pkgdir/usr/share/man/man1/mkimage.1
}
