# Maintainer: Florian Bruhin <archlinux.org@the-compiler.org>

pkgname=stikked
pkgver=0.8.2
pkgrel=1
pkgdesc="An advanced and beautiful pastebin written in PHP"
arch=('any')
url="https://github.com/claudehohl/Stikked"
license=('GPL')
depends=('php')
conflicts=('stikked-git')
options=('!strip')
install=$pkgname.install
source=("https://github.com/claudehohl/Stikked/zipball/$pkgver")
md5sums=('88fbc39c43681e1cd5b01184b1b35388')

package() {
  cd "$srcdir"/claudehohl-Stikked-*/htdocs
  _instdir="$pkgdir/usr/share/webapps/Stikked"
  _cfgdir="$pkgdir/etc/webapps/stikked"
  mkdir -p "$_instdir" "$_cfgdir"

  cp -ra * .htaccess "$_instdir"
  mv "$_instdir/application/config/stikked.php" "$_cfgdir"
  ln -s /etc/webapps/stikked/stikked.php "$_instdir/application/config/stikked.php"
}

# vim:set ts=2 sw=2 et:
