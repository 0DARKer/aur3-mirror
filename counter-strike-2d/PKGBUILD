# Contributor: 404
# Contributor: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Josef Lusticky <evramp@gmail.com>
# Maintainer Jorge Barroso <jorge.barroso.11 at gmail dot com>

pkgname=counter-strike-2d
pkgver=0.1.2.4
_ver=0124
pkgrel=2
pkgdesc="More than just a freeware clone of the well known game Counter-Strike"
arch=('i686' 'x86_64')
url="http://www.cs2d.com/"
license=('custom')
changelog=$pkgname.ChangeLog
depends=('freetype2' 'libtxc_dxtn' 'glu')
optdepends=('openal: audio output'
            'ati-dri: video acceleration'
            'intel-dri: video acceleration'
            'nouveau-dri: video acceleration')
if [ "$CARCH" == "x86_64" ]; then
  depends=('lib32-freetype2' 'lib32-libtxc_dxtn' 'lib32-glu')
  optdepends=('lib32-openal: audio output'
              'lib32-ati-dri: video acceleration'
              'lib32-intel-dri: video acceleration'
              'lib32-nouveau-dri: video acceleration')
fi
makedepends=('curl')
install=cs2d.install
backup=(opt/cs2d/sys/autobuy.cfg  opt/cs2d/sys/autoexec.cfg
        opt/cs2d/sys/config.cfg   opt/cs2d/sys/controls.cfg
        opt/cs2d/sys/editor.cfg   opt/cs2d/sys/filters.cfg
        opt/cs2d/sys/mapcycle.cfg opt/cs2d/sys/more.cfg
        opt/cs2d/sys/server.cfg   opt/cs2d/sys/usgn.dat
        opt/cs2d/sys/usgn_pw.dat)
sha256sums=('4d489450f70858ba1d20e4f4809bfc27de9208352657fe89ac4d53f397ed05e5'
            '4e531c53c3b88ede2a48b768862d00c838cb3c865c95851ef1294c75f1bcc900'
            'ca0532412d11de05c9fc11208167b3ba64fa086c18850d15980f28c0fdd2b2a7'
            'b5128f97ccb4a8bc223080f3440f37a916c3c8e43c909a0c5880c6c9eb67561a'
            '8d2391510839dd46d7385be8eef0cd82e665e9bda5d8b9f883ce43953b9d0ba0')
        # hack for generated id, see also https://bbs.archlinux.org/viewtopic.php?id=141195
_url=http://www.unrealsoftware.de/get.php?get
grabcid() {
  local file=cs2d_${_ver}_linux.zip
  echo "$(curl -Ss "$_url=${file}&p=1" | 
            grep -o '"get.php[^"]\+"' | cut -d'"' -f2 | sed 's/&amp;/\&/g')"
}
_cid=$(grabcid) # this will hide the cmd line above from AUR interface
source=(cs2d_${_ver}_linux.zip::"$_url=cs2d_${_ver}_linux.zip&p=1&cid=${_cid}"
        cs2d_${_ver}_win.zip::"$_url=cs2d_${_ver}_win.zip&p=1&cid=${_cid}"
        "cs2d.desktop"
        "cs2d-fs.desktop"
        "cs2d.png")
options=(emptydirs)

package() {
  # create folders
  install -d "$pkgdir"/{opt/cs2d,usr/{share/{doc,licenses}/$pkgname,bin}}

  # data
  cp -R bots gfx help logos maps screens sfx sys "$pkgdir"/opt/cs2d

  # executable
  install -m755 CounterStrike2D "$pkgdir"/opt/cs2d
  ln -s /opt/cs2d/CounterStrike2D "$pkgdir"/usr/bin/cs2d

  # desktop launcher
  install -Dm644 cs2d.png "$pkgdir"/usr/share/pixmaps/cs2d.png
  install -Dm644 cs2d.desktop "$pkgdir"/usr/share/applications/cs2d.desktop

  # license
  sed -n 84,106p Readme.txt > "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # doc
  install -m644 *.txt "$pkgdir"/usr/share/doc/$pkgname

  # allow editing of configs
  chmod g+w "$pkgdir"/opt/cs2d/sys/*.{cfg,dat}
  chgrp games "$pkgdir"/opt/cs2d/sys/*.{cfg,dat}

  # allow logging and caching
  chmod -R g+w "$pkgdir"/opt/cs2d/sys/{logs,core}
  chgrp games -R "$pkgdir"/opt/cs2d/sys/{logs,core}
}
