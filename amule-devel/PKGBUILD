# Maintainer: Alessio Sergi <asergi [at] archlinux [dot] us>

pkgname=amule-devel
_realname=amule
pkgver=10400
pkgrel=1
pkgdesc="An eMule-like client for the eD2k and Kademlia networks"
arch=('i686' 'x86_64')
url="http://www.amule.org"
license=('GPL')
depends=('gd>=2.0.35' 'geoip' 'libupnp>=1.6.6' 'wxgtk>=2.8.10.1')
makedepends=('crypto++')
provides=($_realname=2.2.6)
conflicts=($_realname $_realname-adnza $_realname-adnza-svn \
           $_realname-base-git $_realname-dlp $_realname-remote \
           $_realname-remote-upnp $_realname-upnp)
source=(http://$_realname.sourceforge.net/tarballs/aMule-SVN-r$pkgver.tar.bz2)
md5sums=('123d61f0e87280f2c02d723548c59b55')

build() {
  cd $srcdir/aMule-SVN-r$pkgver

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-cas \
              --enable-wxcas \
              --enable-amule-daemon \
              --enable-amulecmd \
              --enable-amule-gui \
              --enable-alc \
              --enable-alcc \
              --enable-webserver \
              --disable-debug \
              --enable-optimize \
              --enable-ccache \
              --enable-geoip \
              --enable-upnp

  make
}

package() {
  cd $srcdir/aMule-SVN-r$pkgver
  make DESTDIR=$pkgdir install
}
