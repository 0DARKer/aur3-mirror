# Maintainer: Karol "Kenji Takahashi" Wozniak <wozniakk@gmail.com>
# Contributor: Mantas MikulÄ—nas <grawity@gmail.com>

pkgname=morituri
pkgver=0.1.3
pkgrel=1
pkgdesc="a CD ripper aiming for accuracy over speed, modelled after Exact Audio Copy"
arch=('i686' 'x86_64')
url="https://thomas.apestaart.org/morituri/trac/"
license=("GPL3")
depends=(
    "cdparanoia"
    "cdrdao"
    "gstreamer0.10"
    "gstreamer0.10-python"
    "python2-musicbrainz2"
)
optdepends=("python2-pycdio: for 'rip drive list'")
source=("http://thomas.apestaart.org/download/morituri/$pkgname-$pkgver.tar.bz2")
sha1sums=('f019fd7a9155765b5e18a805670f8fa5a95daea7')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    export PYTHON="python2"
    sed -i '27s/\<python\>/&2/' doc/Makefile.am
    sed -i '1s/\<python\>/&2/' bin/rip.in etc/bash_completion.d/bash-compgen
    autoreconf -i -f
    ./configure --prefix=/usr --sysconfdir=/etc
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm 0644 "README" "$pkgdir/usr/share/doc/morituri/README"
}

# vim: ft=sh:ts=4:sw=4:et
