# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-module-implementation
_lastauthor=D/DR/DROLSKY
_pkgname=Module-Implementation
pkgver=0.04
pkgrel=1
pkgdesc="Loads one of several alternate underlying implementations for a module"
arch=('any')
license=('PerlArtistic2')
options=('!emptydirs')
depends=('perl-try-tiny' 'perl-test-fatal' 'perl-test-taint' 'perl-module-runtime')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('97138574cc23fb78a749dc2bccf890c69bb6fab98ff4302cf905e84b9a3d5bad')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  if [ -f "Build.PL" ]; then
    perl Build.PL
    perl Build
  elif [ -f "Makefile.PL" ]; then
    perl Makefile.PL
    make
  else
    return 1
  fi
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build test
  elif [ -f "Makefile.PL" ]; then
    make test
  fi
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build install
  elif [ -f "Makefile.PL" ]; then
    make install
  fi

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
