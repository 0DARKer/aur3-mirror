# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Bailes <thepizzaking@gmail.com>

pkgname=clutter-gst
pkgver=1.3.4
pkgrel=1
pkgdesc="Gstreamer bindings for the clutter framework"
arch=('i686' 'x86_64')
url="http://clutter-project.org/"
license=('LGPL')
depends=('clutter>=1.0' 'gstreamer0.10-base')
options=('!libtool')
source=("http://www.clutter-project.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
md5sums=('4f74f78df215cba54444dd6256a29bd4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Disabling gobject introspection
  ./configure \
	--prefix=/usr \
	--disable-introspection

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}