# Maintainer: Devin J. Pohly <djpohly+arch@gmail.com>
pkgname=java-guava
_shortname=guava
pkgver=r09
pkgrel=1
pkgdesc="Google's core Java libraries"
arch=(any)
url='http://code.google.com/p/guava-libraries/'
license=('Apache')
depends=('java-runtime>=5')
source=("http://guava-libraries.googlecode.com/files/$_shortname-$pkgver.zip")
md5sums=('cc267a70d45a5954f92d361649c85d7f')

package() {
  cd "$srcdir/$_shortname-$pkgver"

  # Install jar
  install -Dm644 "$_shortname-$pkgver.jar" "$pkgdir/usr/share/java/$_shortname/$_shortname.jar"

  # Install documentation
  chmod -R a-x+X javadoc
  install -d "$pkgdir/usr/share/doc"
  cp -r javadoc "$pkgdir/usr/share/doc/$pkgname"
}
# vim:set ts=2 sw=2 et:
