# Maintainer: Wesley Merkel <ooesili@gmail.com>
pkgname=qfi
pkgver=0.3.1
pkgrel=2
pkgdesc='Quickly edit commonly used files'
arch=('any')
url="https://github.com/ooesili/$pkgname"
license=('GPL')
depends=('perl' 'sudo')
optdepends=('bash-completion: autocomplete targets and options')
options=('!emptydirs' 'purge' '!strip')
install=${pkgname}.install
source=("https://github.com/ooesili/qfi/archive/v${pkgver}.tar.gz"
        'qfi.install')
sha512sums=('92b864d9eb39a78c9dda0c4644438c8efc373d825dbbc6601d7cd69579d3bd8c4dc51dbaf10d243169f671ab76fdac0cd81d44fdf8c43df40bc0ef0daf003c4f'
            '43cc1cff7a723cbe87abf5ade91dd8ba2b29ec6805cc414ab2cb2f0b48b98d123fd436797bd2210dbff09c1c72e26ff06497820d928664e447f084d3ca9b80e5')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    # change environment variables that may cause unpredicatable behavior
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null

    # create and build script and build
    /usr/bin/perl Build.PL
    ./Build
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    # change environment variables that may cause unpredicatable behavior
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT

    # install
    ./Build install --installdirs=vendor --destdir="$pkgdir"
}
