# Maintainer: Stefan Tatschner <stefan@sevenbyte.org>

pkgname=pynote-git
_pkgname=pynote
pkgver=0.2.2.r27.gf350a02
pkgrel=1
pkgdesc="Manage notes on the commandline."
arch=('any')
url="https://github.com/rumpelsepp/pynote"
license=('MIT')
depends=('python' 'python-prettytable' 'python-pygments')
makedepends=('git' 'python-sphinx' 'python-babel')
conflicts=('pynote')
provides=('pynote')
source=("${_pkgname}::git+https://github.com/rumpelsepp/${_pkgname}.git")
md5sums=('SKIP')
install=pynote.install

pkgver() {
  cd "$srcdir/${_pkgname}"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  make docs
  make compile-locales
}

package() {
  cd "${srcdir}/${_pkgname}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

# disabled, see #364
#check() {
#  cd "$srcdir/${_pkgname}"
#  python tests.py
#}
