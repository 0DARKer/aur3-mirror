## Maintainer : shahid <helllamer@gmail.com>
## Long PKGBUILD description, todo, wiki:
## * https://bitbucket.org/helllamer/archlinux-pkgbuild/wiki/pkgbuild/bitcoin

## Wanna take part on development my PKGBUILDs? Feel welcome on my repo:
## * https://bitbucket.org/helllamer/archlinux-pkgbuild/

pkgname=bitcoin
pkgver=0.3.24
pkgrel=1
pkgdesc="Bitcoin is a peer-to-peer network based digital currency."
arch=('i686' 'x86_64')
url="http://www.bitcoin.org/"
depends=('wxgtk-2.9' 'expat' 'gcc-libs' 'boost-libs>=1.46' 'miniupnpc' 'openssl')
makedepends=('boost' 'gcc' 'make' 'findutils')
conflicts=('bitcoin-bin' 'bitcoin-git' 'bitcoin-daemon')
license=('MIT')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-linux.tar.gz"
	"${pkgname}.desktop"
	"makefile.archlinux")
md5sums=('7a9e5d45cccd21ef88b8cbbe20946599'
	 'e7520d64cad73a1d8a6f853c4e2ee43f'
	 '27d4d8a3b6c6cc1c4a9b16c7b408ffca')
s1=$srcdir/${pkgname}-${pkgver}
src=$s1/src/src/

build() {
	local amakefile=makefile.archlinux
	
	# copy correct makefile to src dir
	cp $srcdir/$amakefile $src
	
	cd $src

	# single-threaded build due to OOM issues reported
	make -j1 -f $amakefile bitcoin bitcoind
}

package() {
	# get compiled binaries
	mkdir -p $pkgdir/usr/bin
	install -D -m755 $src/bitcoin  $pkgdir/usr/bin/
	install -D -m755 $src/bitcoind $pkgdir/usr/bin/

	# install locales (FIXME bicycle with find|xargs)
	mkdir -p $pkgdir/usr/share/locale
	cd $s1/locale/
	find -name *.mo | xargs -I{} install -D -m644 {} $pkgdir/usr/share/locale/{}
	
	# add icon and .desktop file to pkg
	mkdir -p $pkgdir/usr/share/pixmaps
	install -D -m644 $src/xpm/bitcoin48.xpm $pkgdir/usr/share/pixmaps/

	mkdir -p $pkgdir/usr/share/applications
	install -D -m644 $srcdir/${pkgname}.desktop $pkgdir/usr/share/applications/ 
}
