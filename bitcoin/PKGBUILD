# Maintainer : shahid <helllamer@gmail.com>

pkgname=bitcoin
pkgver=0.3.21
pkgrel=3
pkgdesc="Bitcoin is a peer-to-peer network based digital currency."
arch=('i686' 'x86_64')
url="http://www.bitcoin.org/"
depends=('wxgtk-2.9' 'expat' 'gcc-libs' 'boost-libs>=1.46' 'miniupnpc' 'openssl')
makedepends=('boost' 'gcc' 'make' 'findutils')
conflicts=('bitcoin-bin')
license=('MIT')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-linux.tar.gz"
	"${pkgname}.desktop"
	"makefile.archlinux")
md5sums=('19e530a9b60e3a0987998a87b30d8cdc'
	 'e7520d64cad73a1d8a6f853c4e2ee43f'
	 '9522a59f38c8bd81c48f08ebb59e270e')

build() {
	local amakefile=makefile.archlinux
	cd $srcdir
	
	# copy correct makefile to src dir
	cp $srcdir/$amakefile $srcdir/${pkgname}-${pkgver}/src
	
	cd $srcdir/${pkgname}-${pkgver}/src

	# crunches for 0.3.20 package
	mkdir -p obj/nogui 2>/dev/null
	rm -f cryptopp/obj/*

	# single-threaded build due to OOM issues reported
	make -j1 -f $amakefile bitcoin bitcoind
}

package() {
	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/share/pixmaps
	mkdir -p $pkgdir/usr/share/applications
	
	# get compiled binaries
	install -D -m755 $srcdir/${pkgname}-${pkgver}/src/bitcoin  $pkgdir/usr/bin/
	install -D -m755 $srcdir/${pkgname}-${pkgver}/src/bitcoind $pkgdir/usr/bin/
	
	# strip shit from executables
	find $pkgdir/usr/bin/ -type f | xargs -L1 strip
	
	# add icon and .desktop file to pkg
	install -D -m644 $srcdir/${pkgname}-${pkgver}/src/xpm/bitcoin48.xpm $pkgdir/usr/share/pixmaps/
	install -D -m644 $srcdir/${pkgname}.desktop $pkgdir/usr/share/applications/ 
}
