# Maintainer : shahid <helllamer@gmail.com>

# TODO
#  1. Add autodetection for asm optimization. 
#     Need some mechanism to detect SSE2 on x86 and x86_64 instruction set.
#     Depending on autodetection result, pass following CFLAGS into makefile:
#     * -DFOURWAYSSE2 - SSE2 optimization for sha256.cpp
#  2. Add locales from src
#  3. To look into contrib directory. May there are something useful?
# 99. Make/grab init script for starting bitcoind from rc.

pkgname=bitcoin
pkgver=0.3.23
pkgrel=2
pkgdesc="Bitcoin is a peer-to-peer network based digital currency."
arch=('i686' 'x86_64')
url="http://www.bitcoin.org/"
depends=('wxgtk-2.9' 'expat' 'gcc-libs' 'boost-libs>=1.46' 'miniupnpc' 'openssl')
makedepends=('boost' 'gcc' 'make' 'findutils')
conflicts=('bitcoin-bin')
license=('MIT')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-linux.tar.gz"
	"${pkgname}.desktop"
	"makefile.archlinux")
md5sums=('c1f4309e585dba7fb288432cab9988b4'
	 'e7520d64cad73a1d8a6f853c4e2ee43f'
	 'c4969e8571717c4707791e64fcddbe04')
src=$srcdir/${pkgname}-${pkgver}/src/src/

build() {
	local amakefile=makefile.archlinux
	cd $srcdir
	
	# copy correct makefile to src dir
	cp $srcdir/$amakefile $src
	
	cd $src

	# check, if host can compile with SSE (TODO)

	# single-threaded build due to OOM issues reported
	make -j1 -f $amakefile bitcoin bitcoind
}

package() {
	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/share/pixmaps
	mkdir -p $pkgdir/usr/share/applications
	
	# get compiled binaries
	install -D -m755 $src/bitcoin  $pkgdir/usr/bin/
	install -D -m755 $src/bitcoind $pkgdir/usr/bin/
	
	# add icon and .desktop file to pkg
	install -D -m644 $src/xpm/bitcoin48.xpm $pkgdir/usr/share/pixmaps/
	install -D -m644 $srcdir/${pkgname}.desktop $pkgdir/usr/share/applications/ 
}
