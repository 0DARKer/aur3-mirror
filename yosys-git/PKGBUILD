# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

pkgname=yosys-git
_pkgbasename=yosys
pkgver=0.1.git20130727
pkgrel=2
pkgdesc="A framework for Verilog RTL synthesis with extensive Verilog-2005 support"
arch=('i686' 'x86_64')
url="http://www.clifford.at/yosys/"
license=('custom:ISC')
depends=('minisat-git' 'tcl' 'qt4')
makedepends=('git')
optdepends=()
source=(git+https://github.com/cliffordwolf/yosys.git#commit=0f38008ed34e36e798f521b4b6d4fea89ae31a3b
        yosys-Makefile-Arch-Linux.patch
        license.txt)
sha256sums=('SKIP'
            '4283b492c472b43d3b091f9f814b334cc7ead564029340156791c9548959f19b'
            'e09c086c53f9cc8ebecc207ae7d854f544e26f89baf65c002b7471313cde02b8')

prepare() {
  cd "${srcdir}/${_pkgbasename}"

  patch -Np0 -i ${srcdir}/yosys-Makefile-Arch-Linux.patch
}

build() {
  cd "${srcdir}/${_pkgbasename}"

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}"

  install -d "${pkgdir}/usr/bin"

  make DESTDIR="${pkgdir}/usr" install

  install -D -m 644 "${srcdir}/license.txt" "${pkgdir}/usr/share/licenses/yosys-git/license.txt"
}

# vim: set ts=2 sw=2 ft=sh noet:
