# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=sxemacs-git
pkgver=20120522
pkgrel=1
pkgdesc="A derivation of xemacs - git checkout"
arch=('i686' 'x86_64')
url="http://www.sxemacs.org/"
license=('GPL')
depends=('libffi' 'xbitmaps' 'libpng' 'libao' 'gpm' 'libtiff' 'libjpeg' 'xaw3d' 'openssl' 'libltdl' 'libxpm' 'jack' 'libmad' 'desktop-file-utils')
makedepends=('git')
provides=('sxemacs')
conflicts=('sxemacs' 'xemacs')
source=()
md5sums=()
install=sxemacs.install
_gitroot="http://git.sxemacs.org/sxemacs"
_gitname="sxemacs"
options=('!libtool')

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  [ -d "$srcdir/$_gitname-build" ] && rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  ./autogen.sh
  sed -i 2s+cl.info+cl-sxemacs.info+ info/cl.texi
  sed -i 2s+widget.info+widget-sxemacs.info+ info/widget.texi
  CFLAGS="$CFLAGS -I/usr/include/freetype2" ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
# remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.sxemacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1,ctags.sxemacs.1}
# remove conflict with other emacsen
  mv "$pkgdir"/usr/bin/{etags,etags.sxemacs}
  mv "$pkgdir"/usr/share/man/man1/{etags.1,etags.sxemacs.1}
  mv "$pkgdir"/usr/share/info/cl.info \
    "$pkgdir"/usr/share/info/cl-sxemacs.info
  mv "$pkgdir"/usr/share/info/widget.info \
    "$pkgdir"/usr/share/info/widget-sxemacs.info
}
