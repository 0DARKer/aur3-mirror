# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdelibs4support-git
pkgver=r258.cdb1baa
pkgrel=1
pkgdesc='KDE 4 Support'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdelibs4support'
license=('LGPL')
depends=('kunitconversion-git' 'kdesignerplugin-git' 'kemoticons-git' 'kitemmodels-git' 'kinit-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git' 'qt5-tools')
groups=('kf5')
conflicts=('kdelibs4support' 'kde4support-git')
provides=('kdelibs4support')
replaces=('kde4support-git')
source=('git://anongit.kde.org/kdelibs4support.git')
md5sums=('SKIP')

pkgver() {
  cd kdelibs4support
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kdelibs4support \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
