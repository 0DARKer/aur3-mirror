# Maintainer: Gustavo Castro <gustawho[at]gmail[dot]com>
# Submitter: 	acrox999 <acrox999[at]gmail[dot]com>
pkgname=gtk-theme-iris-dark-git
pkgver=67.60124a4
pkgrel=1
pkgdesc="Iris Dark is a flat Murrine theme that uses varying shades and tones to create distinction and a modern experience."
arch=('any')
url="http://thevirtualdragon.deviantart.com/art/Iris-Light-Beta-428948909"
license=('GPL3')
depends=('gtk3' 'gtk-engine-murrine')
makedepends=('git')
conflicts=('gtk-theme-iris-dark')
provides=('gtk-theme-iris-dark')
source=("iris-dark"::'git+https://github.com/xyl0n/iris.git')
sha256sums=('SKIP')
_realname=iris-dark

pkgver() {
        cd "${_realname}"
        echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
        install -dm755 "${pkgdir}/usr/share/themes/"
        cp -dpr --no-preserve=ownership "${srcdir}/${_realname}" "${pkgdir}/usr/share/themes/"
        find "${pkgdir}" -type d -exec chmod 755 "{}" \;
        find "${pkgdir}" -type f -exec chmod 644 "{}" \;
}
