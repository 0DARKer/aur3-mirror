# Maintainer: Maciej Mazur <mamciek@gmail.com>
# Contributor: Devin J. Pohly <djpohly+arch@gmail.com>
pkgname=neo4j
pkgver=1.8
pkgrel=1
pkgdesc="A fully transactional graph database implemented in Java"
arch=(any)
url="http://neo4j.org/"
license=(GPL)
depends=(bash java-runtime java-commons-io java-commons-logging java-commons-collections
         antlr2 slf4j)
backup=(etc/neo4j/neo4j.properties etc/neo4j/neo4j-server.properties etc/neo4j/logging.properties)
options=(!strip)
source=("http://dist.neo4j.org/$pkgname-community-$pkgver-unix.tar.gz" 
        neo4j-shell neo4j.rc neo4j.conf
        config.patch neo4j.patch
	neo4j.service)

md5sums=('7a76a75bac1a32c5291e8e7b238f7ca2'
         '43f0aaef9e403162430295491474c072'
         '1af96dfa52d42a1429d44484ed8ebba3'
         '36888b912754c660b4e54bc5b0606f53'
         '12eb3fd847e2c25743459b4f35daab1b'
         'faa056bbf0431ee2eb43a149fa3ad694'
         '7e15c7aff5a23f92ed6a64d9b972f9c0')

build() {
  cd "$srcdir/$pkgname-community-$pkgver"

  # Adjust configuration to match new directory structure
  patch -Np1 -i ../config.patch
  patch -Np1 -i ../neo4j.patch
}

package() {
  cd "$srcdir"

  install -d \
    "$pkgdir/usr/share/java" \
    "$pkgdir/usr/share/neo4j/bin" \
    "$pkgdir/usr/share/licenses/$pkgname" \
    "$pkgdir/var/lib/$pkgname/data" \
    "$pkgdir/var/log/$pkgname" \
    "$pkgdir/etc/$pkgname/ssl"

  install -D neo4j.rc "$pkgdir/etc/rc.d/neo4j"
  install -D neo4j.conf "$pkgdir/etc/conf.d/neo4j"
  install -D neo4j-shell "$pkgdir/usr/bin/neo4j-shell"
  install -D neo4j.service "$pkgdir/usr/lib/systemd/system/neo4j.service"

  cd "$pkgname-community-$pkgver"


  # Configuration in /etc, minus the wrapper (which we don't use)
  cp conf/* "$pkgdir/etc/$pkgname"
  rm "$pkgdir/etc/$pkgname/neo4j-wrapper.conf"
  rm "$pkgdir/etc/$pkgname/windows-wrapper-logging.properties"

  # Put JARs in lib, lib/system, and lib/plugins
  install -d "$pkgdir/usr/share/java/$pkgname/lib"
  cp -r lib "$pkgdir/usr/share/java/$pkgname"
  install -d "$pkgdir/usr/share/java/$pkgname/system/lib"
  cp -r system/lib "$pkgdir/usr/share/java/$pkgname/system"
  cp -r plugins "$pkgdir/usr/share/java/$pkgname/plugins"

  install -D bin/neo4j "$pkgdir/usr/bin/neo4j"
  install -D bin/utils "$pkgdir/usr/share/neo4j/bin/utils"

  # Factor out dependencies
  rm "$pkgdir/usr/share/java/$pkgname/system/lib/"{antlr,commons-{collections,io,logging},jcl-over-slf4j,slf4j-api}-*.jar

  cp LICENSE.txt LICENSES.txt NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname/"

  cd "$srcdir"
}

# vim:set ts=2 sw=2 et:
