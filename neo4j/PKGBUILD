# Maintainer: Devin J. Pohly <djpohly+arch@gmail.com>
pkgname=neo4j
pkgver=1.4.2
pkgrel=1
pkgdesc="A fully transactional graph database implemented in Java"
arch=(any)
url="http://neo4j.org/"
license=(custom)
depends=(bash java-runtime java-commons-io java-commons-logging jakarta-commons-collections
         antlr2 slf4j)
backup=(etc/neo4j.properties etc/neo4j-server.properties etc/logging.properties)
options=(!strip)
source=("http://dist.neo4j.org/$pkgname-community-$pkgver-unix.tar.gz"
        neo4j-shell neo4j.rc neo4j.conf
        config.patch)
md5sums=('7f45346984fd477804bfa2004da1b406'
         '73ec8b57f26ff07a2ca2a8f21a8622a6'
         '3f5cb200de5f0b144be1d19210512935'
         '634be326793c344626fa0626de9f3fa2'
         'dcf5c512217536b3bd8c87feda5a4121')

build() {
  cd "$srcdir/$pkgname-community-$pkgver"

  # Adjust configuration to match new directory structure
  patch -Np1 -i ../config.patch
}

package() {
  cd "$srcdir"

  install -d \
    "$pkgdir/usr/share/java" \
    "$pkgdir/usr/share/licenses/$pkgname" \
    "$pkgdir/var/lib/$pkgname/data" \
    "$pkgdir/var/log/$pkgname"

  install -D neo4j.rc "$pkgdir/etc/rc.d/neo4j"
  install -D neo4j.conf "$pkgdir/etc/conf.d/neo4j"
  install -D neo4j-shell "$pkgdir/usr/bin/neo4j-shell"

  cd "$pkgname-community-$pkgver"

  # Configuration in /etc, minus the wrapper (which we don't use)
  cp -r conf "$pkgdir/etc/neo4j"
  rm "$pkgdir/etc/neo4j/neo4j-wrapper.conf"

  # Put JARs in lib, lib/system, and lib/plugins
  cp -r lib "$pkgdir/usr/share/java/$pkgname"
  cp -r system/lib "$pkgdir/usr/share/java/$pkgname/system"
  cp -r plugins "$pkgdir/usr/share/java/$pkgname/plugins"

  # Factor out dependencies
  rm "$pkgdir/usr/share/java/$pkgname/system/"{antlr,commons-{collections,io,logging},{jcl,log4j}-over-slf4j,slf4j-{api,jdk14}}-*.jar

  cp LICENSE.txt LICENSES.txt NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
