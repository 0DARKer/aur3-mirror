# Maintainer: Samuel Mesa <samuelmesa [at] linuxmail.org>
# Contributor: James Duley <jagduley gmail>
# Contributor: A. Weiss <adam [at] archlinux.us>

pkgname=osgearth
pkgver=2.6
pkgrel=1
pkgdesc="A terrain rendering toolkit for OpenSceneGraph"
arch=('i686' 'x86_64')
url=('http://www.osgearth.org')
license=('LGPL')
depends=('openscenegraph' 'gdal' 'minizip' 'qt5-base')
makedepends=('cmake')
provides=('osgearth')
conflicts=('osgearth-qt5')
source=("https://github.com/gwaldron/osgearth/archive/${pkgname}-${pkgver}.tar.gz"
        "v8-fix.patch::https://github.com/gwaldron/osgearth/commit/54c9e794a838a0e61114fdf1c86fdb9e0d53d5ea.patch")
md5sums=('f3dadfa790971faa58e58689e624cfba'
         '15731efa5a947212196f45cf0ba68f01')


prepare() {
  cd ${srcdir}/${pkgname}-${pkgname}-${pkgver}
  
  # Don't use V8 if installed.
  patch -p1 -i ${srcdir}/v8-fix.patch
}         

build() {
  cd ${srcdir}/${pkgname}-${pkgname}-${pkgver}

  #Build
  if [[ -d "build" ]]; then
    (rm -rf build)
  fi

  mkdir build
  cd build

  cmake \
  -DLIB_POSTFIX= \
  -DCMAKE_INSTALL_PREFIX=/usr \
  ..

  make -j5
}

package() {
  cd ${srcdir}/${pkgname}-${pkgname}-${pkgver}/build
  make DESTDIR=$pkgdir install
  
  mkdir -p ${pkgdir}/usr/share/osgearth
  cp -r ${srcdir}/${pkgname}-${pkgname}-${pkgver}/tests ${pkgdir}/usr/share/osgearth
  cp -r ${srcdir}/${pkgname}-${pkgname}-${pkgver}/data ${pkgdir}/usr/share/osgearth
}
