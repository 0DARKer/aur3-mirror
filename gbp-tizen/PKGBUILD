# Maintainer: Junchun Guan <junchunx.guan@intel.com>

pkgname='gbp-tizen'
pkgver=0.6.15
pkgrel=1
pkgdesc="Tools from Debian to integrate the package build system with Git"
arch=(any)
url="https://honk.sigxcpu.org/piki/projects/git-buildpackage/"
license=('GPL2')
conflicts=('git-buildpackage')
provides=('git-buildpackage')
depends=('git'
         'man-db'
         'python2'
         'rpm-org')
makedepends=('python2-distribute')
source=(http://download.tizen.org/tools/archive/14.03.1/openSUSE_13.1/src/git-buildpackage-0.6.15-62.1.src.rpm)
sha256sums=('0c8b76c0cb5ec295c7d28db53e54a14ea39adfd569349eb292317e76dc31e45b')

prepare() {
  cd "$srcdir"
  tar -x -f git-buildpackage_$pkgver.tar.gz
  cd "$srcdir/git-buildpackage-$pkgver"

  for patch_file in $(find ../ -maxdepth 1 -name '*.patch' |sort)
  do
    patch -p1 -i $patch_file
  done
}

build() {
  cd "$srcdir/git-buildpackage-$pkgver"
  WITHOUT_NOSETESTS=1 \
      python2 setup.py build
}

package() {
  cd "$srcdir/git-buildpackage-$pkgver"
  WITHOUT_NOSETESTS=1 \
    python2 setup.py install \
    --root="$pkgdir" \
    --prefix=/usr \
    -O1
}

# vim:set ts=2 sw=2 et:
