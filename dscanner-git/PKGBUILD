# Maintainer: Liam Wigney <liamw950 at gmail doc com>

pkgname=dscanner-git
pkgver=r314.4bac267
pkgrel=1
pkgdesc="Swiss-army knife for D source code"
arch=('i686' 'x86_64')
url="https://github.com/Hackerpilot/Dscanner"
license=('Custom')
depends=('glibc')
makedepends=('dmd' 'git')
provides=('dscanner')
options=('!strip') #Because it's small anyway and symbols are useful as it is still in development
source=("$pkgname"::'git+https://github.com/Hackerpilot/Dscanner')
md5sums=('SKIP') #Only skip because sources are not static

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m755 dscanner "$pkgdir/usr/bin/dscanner"
  install -D -m644 LICENSE_1_0.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
