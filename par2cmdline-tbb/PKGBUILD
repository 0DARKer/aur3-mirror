# Contributor: jordz <jordz@archlinux.us>
# Maintainer: kevku <kevku@gmx.com>
pkgname=par2cmdline-tbb
pkgver=20100203
pkgrel=2
pkgdesc="This is a concurrent (multithreaded) version of par2cmdline 0.4"
arch=('i686' 'x86_64')
url="http://www.chuchusoft.com/par2_tbb/"
license=('GPL')
depends=('intel-tbb')
conflicts=('par2cmdline')
provides=('par2cmdline')
source=("http://www.chuchusoft.com/par2_tbb/par2cmdline-0.4-tbb-$pkgver.tar.gz"
        "par2cmdline-fix-dso-linking.patch"
	"par2cmdline-gcc-compile-fix.patch"
	"par2-tbb-fortify-source-fix.patch"
	"par2-tbb-packed.patch" )

md5sums=('b1052a08c3c6eac8a7a0605addb161e7'
         '0952a516e536f82ebf3d4d7082d00d45'
         'c8594097a07640bd62587677658c5526'
         'a18175c0bb8ff602204cea937e9c215f'
         'cef60986d47449cb69ef920ce4840b4c')

build() {
	cd ${srcdir}/par2cmdline-0.4-tbb-$pkgver
	patch < "${srcdir}/par2cmdline-fix-dso-linking.patch"
	patch < "${srcdir}/par2cmdline-gcc-compile-fix.patch"
	patch < "${srcdir}/par2-tbb-fortify-source-fix.patch"
	patch < "${srcdir}/par2-tbb-packed.patch"
	autoreconf
	./configure --prefix=/usr
	make
}

package(){
	cd ${srcdir}/par2cmdline-0.4-tbb-$pkgver
	make DESTDIR=${pkgdir} install
}
