# Maintainer: Chris Brannon <cmbrannon79 (at) gmail.com>
pkgname=edbrowse-cvs
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>
pkgver=20110126
pkgrel=1
pkgdesc="Editor, browser, and mail client based on /bin/ed"
arch=('i686' 'x86_64')
url="http://edbrowse.sourceforge.net/"
license=('GPL' 'openssl')
depends=('spidermonkey' 'openssl' 'pcre' 'curl>=7.17.0')
makedepends=('cvs')
provides=(edbrowse)
conflicts=(edbrowse)
source=(makefile.patch)

_cvsroot=":pserver:anonymous@edbrowse.cvs.sourceforge.net:/cvsroot/edbrowse"
_cvsmod="ebsmjs"

build() {
  cd "$srcdir"
  msg "Connecting to CVS server...."
  if [ -d $_cvsmod/CVS ]; then
    cd $_cvsmod
    cvs -z3 update -d
  else
    cvs -z3 -d $_cvsroot co -D $pkgver -f $_cvsmod
    cd $_cvsmod
  fi

  msg "CVS checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_cvsmod-build"
  cp -r "$srcdir/$_cvsmod" "$srcdir/$_cvsmod-build"
  cd "$srcdir/$_cvsmod-build"

  patch -p1 < "$srcdir/makefile.patch"
  make
}

package() {
  cd "$srcdir/$_cvsmod-build/src"
  make DESTDIR="$pkgdir" prefix=/usr install

  # setup.ebrc is a shell script that builds a config file for you.  It's
  # in doc/.
  install -m755 edbrowse ../doc/setup.ebrc "${pkgdir}/usr/bin"

  # How much of the documentation should be installed?
  cd ../doc
  install -dm755 "${pkgdir}/usr/share/doc/edbrowse"
  install -m644 usersguide.html philosophy.html sample.ebrc "$pkgdir/usr/share/doc/edbrowse"
}
md5sums=('9108d323e041398dce6f3b9e08b643e9')
