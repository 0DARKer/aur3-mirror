# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=gpac-svn
pkgver=4226
pkgrel=1
pkgdesc="MPEG-4 multimedia framework"
arch=('i686' 'x86_64')
url="http://gpac.sourceforge.net/"
license=('LGPL')
depends=('alsa-lib' 'zlib')
optdepends=('wxgtk: GUI Osmo4' 'openssl' 'ffmpeg' 'freetype2' 'libmad' 'libxml2: SVG parsing'
            'freeglut: OpenGL' 'faad2: AAC' 'xvidcore: MPEG4-ASP' 'libjpeg: JPEG image'
            'libpng: PNG image' 'libvorbis: OGG vorbis' 'libtheora: OGG theora'
            'a52dec: for A52 support' 'jack: for jack support' 'xulrunner: JavaScript')
makedepends=('subversion')
options=(!makeflags)
provides=('gpac')
conflicts=('gpac')
source=()
md5sums=()

_svntrunk="https://gpac.svn.sourceforge.net/svnroot/gpac/trunk/gpac"
_svnmod="gpac"

build() {
  cd "$srcdir"/
  msg "Connecting to $_svnmod.sourceforge.net SVN server...."
  if [ -d $_svnmod/.svn ]; then
    cd $_svnmod && svn up -r $pkgver
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir"/$_svnmod-build ]; then
  	rm -rf "$srcdir"/$_svnmod-build
  fi

  cp -r "$srcdir"/$_svnmod "$srcdir"/$_svnmod-build
  cd "$srcdir"/$_svnmod-build

  ./configure --prefix=/usr --mandir=/usr/share/man --X11-path=/usr --use-js=no
  make
}

package() {
  cd "$srcdir"/$_svnmod-build

  make DESTDIR="$pkgdir" install
}
