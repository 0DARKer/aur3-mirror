# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani at gmail dot com>
# Contributor: Michal Lisowski <lisu at riseup dot net>

pkgname=exfalso
pkgver=3.3.0
pkgrel=1
pkgdesc="GTK+ audio tag editor"
arch=('any')
url="https://code.google.com/p/quodlibet/"
license=('GPL2')
depends=('mutagen' 'gtk3' 'gtk-update-icon-cache' 'python2-gobject' 'python2-cairo')
#optdepends=('cddb-py: for "CDDB Lookup" plugin'
#             'python2-musicbrainz2: for "MusicBrainz Lookup" plugin')
makedepends=('intltool' 'gettext')
conflicts=('quodlibet')
install=exfalso.install
source=(https://bitbucket.org/lazka/quodlibet-files/raw/default/releases/quodlibet-${pkgver}.tar.gz)
sha512sums=('c7452f9eb86a8e0b98a6bb179a741b92ce58d2b22e624b9fe89602e6f36a2492380437048444b4556245ab46ddce0a62cb16b639947d012e7510b0c3b59c67e7')

prepare () {
  cd "${srcdir}/quodlibet-${pkgver}"

  # Use Python 2
  sed -i 's|#! /usr/bin/env python|#!/usr/bin/env python2|' quodlibet/ext/songsmenu/replaygain.py
}

build () {
  cd "${srcdir}/quodlibet-${pkgver}"

  python2 setup.py build
}

package() {
  cd "${srcdir}/quodlibet-${pkgver}"

  python2 setup.py install --root="${pkgdir}"

  # Remove Quod Libet
  rm    "${pkgdir}"/usr/bin/quodlibet
  rm    "${pkgdir}"/usr/share/appdata/quodlibet.appdata.xml
  rm    "${pkgdir}"/usr/share/applications/quodlibet.desktop
  rm -r "${pkgdir}"/usr/share/dbus-1/
  rm -r "${pkgdir}"/usr/share/gnome-shell/
  rm    "${pkgdir}"/usr/share/icons/hicolor/64x64/apps/quodlibet.png
  rm    "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/quodlibet.svg
  rm    "${pkgdir}"/usr/share/man/man1/quodlibet.1
  rm    "${pkgdir}"/usr/share/pixmaps/quodlibet.png

  # Remove Plugins
  rm -rf "$pkgdir"/usr/lib/python2.7/site-packages/quodlibet/ext/
}
