# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=hypertable
pkgver=0.9.6.3
pkgrel=1
pkgdesc='High performance, scalable, distributed storage and processing system'
arch=('x86_64' 'i686')
url='http://hypertable.org/'
license=('GPL2')
depends=('log4cpp' 'hyperic-sigar' 'thrift' 'gperftools' 'kfs' 'rrdtool' 'ceph')
source=('h096.zip::https://github.com/hypertable/hypertable/zipball/v0.9.6')
sha256sums=('4cfd559d09f3198013646122bf68f5393a640da1fbdfc5fd240f8f61bc062edb')
if [ "$CARCH" = "x86_64" ]
then
  _sigar=/usr/lib/libsigar-amd64-linux.so
else
  _sigar=/usr/lib/libsigar-x86-linux.so
fi

build() {
  cd "$srcdir"
  
  mv "$pkgname-$pkgname-"* "$pkgname-$pkgver"

  if [ -d build ]; then 
    rm -r build
  fi
  mkdir build
  cd build

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DThrift_LIB=/usr/lib/libthrift.so \
    -DSIGAR_LIBRARY=$_sigar \
    "../$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
