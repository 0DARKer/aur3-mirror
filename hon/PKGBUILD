# Contributor: Slash <demodevil5 [at] yahoo [dot] com>

#### To speed up packet creation.
#### If you are planning to distribute
#### This on some custon repo, you might
#### want to use the defaut slow but
#### much better xz compression.
PKGEXT=.pkg.tar.gz

pkgname=hon
pkgver=2.1.0.1
pkgrel=1
pkgdesc="Heroes of Newerth is a Real Time Strategy game heavily influcenced by DotA"
arch=('i686' 'x86_64')
url="http://www.heroesofnewerth.com/"
license=('custom:HoN')
depends=('libgl' 'libxml2' 'mesa' 'speex' 'curl' 'freetype2')
makedepends=('unzip')
options=(!strip)
install='hon.install'
source=('hon.desktop' "http://dl.heroesofnewerth.com/HoNClient-${pkgver}.sh")

package() {
    # Extract Files
    sh -c "unzip ${srcdir}/HoNClient-${pkgver}.sh; true"

    # Create Destination Directory
    mkdir -p "${pkgdir}/opt/hon/"

    # Install Game & Data Files
    cp -r "${srcdir}"/data/* "${pkgdir}/opt/hon/"

    # Install License (HoN TOS)
    install -D -m 0644 "${srcdir}/data/tos.txt" \
        "${pkgdir}/usr/share/licenses/${pkgname}/TOS"

    # Install License (S2 TOS)
    install -D -m 0644 "${srcdir}/meta/license.txt" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # Install Desktop Shortcut
    install -D -m 0644 "${srcdir}/hon.desktop" \
        "${pkgdir}/usr/share/applications/hon.desktop"

    # Install Client Launcher
    mkdir -p "${pkgdir}/usr/bin"
    ln -s /opt/hon/hon.sh "${pkgdir}/usr/bin/hon"

    ## Remove un-needed libs
    ## fmodex and linpng aren't compitable with Arch
    if [ "${CARCH}"=="x86_64" ]; then
	## 32bit's
	rm -r "${pkgdir}"/opt/hon/{libs-x86,hon_update-x86,hon-x86,libk2-x86.so}

	## System wide libs can be used.
	for lib in libcurl.so.4 libfreetype.so.6  libgcc_s.so.1  libspeexdsp.so.1  libspeex.so.1  libstdc++.so.6;
		do rm "${pkgdir}/opt/hon/libs-x86_64/${lib}"
	done
    else
        ## 64bit's
        rm -r "${pkgdir}"/opt/hon/{libs-x86_64,hon_update-x86_64,hon-x86_64,libk2-x86_64.so}

        ## System wide libs can be used. 
        for lib in libcurl.so.4 libfreetype.so.6  libgcc_s.so.1  libspeexdsp.so.1  libspeex.so.1  libstdc++.so.6;  
                do rm "${pkgdir}/opt/hon/libs-x86/${lib}"
	done
    fi

    # Set Ownership to root:games
    chown -R root:games "${pkgdir}/opt/hon"
    chmod -R g+w "${pkgdir}/opt/hon"
}


md5sums=('d3fa56f17b5eb21d4da01e8d628fbc97'
         '6774cfb4fbf0532266bb7ac07a3e55ea')
