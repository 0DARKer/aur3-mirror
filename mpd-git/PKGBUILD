#  Maintainer: sudokode <sudokode@gmail.com>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd-git
pkgver=20121031
pkgrel=3
pkgdesc='Flexible, powerful, server-side application for playing music'
url='http://mpd.wikia.com/wiki/Server'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libao' 'ffmpeg' 'mpg123' 'libmodplug' 'audiofile' 'libshout' 'libmad' 'curl' 'faad2'
         'sqlite' 'jack' 'libmms' 'wavpack' 'libmpcdec' 'avahi' 'libid3tag' 'opus')
conflicts='mpd'
makedepends=('git')
source=('mpd.init' 'mpd.tmpfile' 'mpd.service' 'mpd-system.socket' 'mpd-user.socket')
sha256sums=('97e7c2290d5bdfba891cebbf57b37ef997299d93db23c94dc4272b661b3752db'
            'c1683ba35774c85e16c70e89f7e2ed1c09619512b1a273daabbd5e34d40439bd'
            '36d6a97473a17a3ddcae633a610846bff2d84614a151ec37f2cba80312f3e23b'
            'a05936116d59d442a5f94858f30e759389719ed2ac6dbef25a512d952a156658'
            'a650e25f9e4490e6dc7172ff95bfb7a1df28175d365741c3d98af3849c4b5428')
backup=('etc/mpd.conf')
install=mpd.install

_gitroot="git://git.musicpd.org/master/mpd.git"
_gitname="mpd"

build() {
  cd "$srcdir"

  msg "Connecting to Git server..."

  if [ -d "$srcdir"/$_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "Git checkout done or server timeout."
  msg "Starting make..."

  # Copy Latest files to Build Directory
  cp -r "$srcdir"/$_gitname $srcdir/$_gitname-build
  cd "$srcdir"/$_gitname-build

  # Configure Source
  ./autogen.sh \
    --prefix=/usr \
    --enable-lastfm \
    --enable-jack \
    --enable-pulse \
    --enable-opus \
    --disable-sidplay \
    --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
  cd "$srcdir"/$_gitname-build

  make DESTDIR="$pkgdir" install

  sed \
    -e '/^#playlist_directory/c playlist_directory "/var/lib/mpd/playlists"' \
    -e '/^#db_file/c db_file "/var/lib/mpd/mpd.db"' \
    -e '/^#pid_file/c pid_file "/run/mpd/mpd.pid"' \
    -e '/^#state_file/c state_file "/var/lib/mpd/mpdstate"' \
    -e '/^#user/c user "mpd"' \
    -i doc/mpdconf.example

  install -Dm755 "$srcdir"/mpd.init "$pkgdir"/etc/rc.d/mpd
  install -d -g 45 -o 45 "$pkgdir"/var/lib/mpd/playlists
  install -Dm644 doc/mpdconf.example "$pkgdir"/etc/mpd.conf
  install -Dm644 "$srcdir"/mpd.tmpfile "$pkgdir"/usr/lib/tmpfiles.d/mpd.conf
  install -Dm644 "$srcdir"/mpd.service "$pkgdir"/usr/lib/systemd/user/mpd.service
  install -Dm644 "$srcdir"/mpd-system.socket "$pkgdir"/usr/lib/systemd/system/mpd.socket
  install -Dm644 "$srcdir"/mpd-user.socket "$pkgdir"/usr/lib/systemd/user/mpd.socket
}
