# Contributor: Kyle Keen <keenerd@gmail.com>
pkgname=cataclysm-git
pkgver=20111017
pkgrel=1
pkgdesc="A post-apocalyptic roguelike."
url="http://cataclysmrl.blogspot.com/"
arch=('i686' 'x86_64')
license="copyright"
depends=('ncurses')
makedepends=('git')
source=()
md5sums=()

_gitroot="git://github.com/Whales/Cataclysm.git"
_gitname="Cataclysm"

build() {
  cd "$srcdir"
  msg "Connecting to github..."

  if [ -d "$srcdir"/${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd "$srcdir/$_gitname"

  sed -i 's|"data/|"/usr/share/cataclysm/data/|g' *.cpp
  sed -i 's|"./data/|"/usr/share/cataclysm/data/|g' *.cpp
  sed -i 's|"save"|"/var/games/cataclysm/save"|g' *.cpp
  sed -i 's|"save/|"/var/games/cataclysm/save/|g' *.cpp

  # someday...
  #sed -i '1i #include <stdio.h>' {game,map,overmap}.cpp
  #sed -i 's|"save/|getenv("HOME") << "/.cataclysm/save/|g' *.cpp

  make PREFIX=/usr
}

package() {
  msg "Formerly, save games were in /usr/share/cataclysm/save"
  msg "/usr/share was the wrong place for this"
  msg "Please move your saves to /var/games/cataclysm/save"
  cd "$srcdir/$_gitname"
  install -Dm755 cataclysm "$pkgdir/usr/bin/cataclysm"
  install -dm755  "$pkgdir/usr/share/cataclysm/data"
  cp data/* "$pkgdir/usr/share/cataclysm/data"
  install -dm777 -g games "$pkgdir/var/games/cataclysm/save"
}
