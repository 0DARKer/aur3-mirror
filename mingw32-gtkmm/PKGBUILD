# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

_pkgname=gtkmm
pkgname=mingw32-${_pkgname}
pkgver=2.20.3
pkgrel=1
arch=(i686 x86_64)
pkgdesc="C++ interface for gtk2 (mingw32)"
depends=('mingw32-gtk2>=2.20' 'mingw32-pangomm>=2.26'
         'mingw32-glibmm>=2.24')
makedepends=(mingw32-gcc)
license=(LGPL)
options=(!strip)
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgname}/2.20/${_pkgname}-$pkgver.tar.bz2)
sha256sums=('311d2156f15cdbdce22cb2af6e606a3f621d7dfebea6f7b1be0132e81af90585')

build()
{
  cd ${srcdir}/${_pkgname}-$pkgver

  export CPPFLAGS="$CPPFLAGS -D_REENTRANT"
  export PKG_CONFIG_PATH="/usr/i486-mingw32/lib/pkgconfig"
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-documentation \
    --enable-static
  make
}

package() {
  cd ${srcdir}/${_pkgname}-$pkgver
  make install DESTDIR=${pkgdir}

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
}
