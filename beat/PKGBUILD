# Maintainer : Alucryd <alucryd at gmail dot com>

pkgname=beat
pkgver=01
pkgrel=11
pkgdesc="Delta patcher for the higan emulator"
arch=('i686' 'x86_64')
url="http://byuu.org/programming/beat/"
license=('GPL3')
source=("http://byuu.org/files/beat_v01.tar.xz" "beat.desktop")
sha256sums=('0ba71b22fa405929df3b300c3b80537bc8851854b27b291d3ad812ea1c45873d'
            'c12e60aa1c4877de06a458b2c8cc8d7ff940a0c9b397128a2dee1cb7de0329b9')

# UI (either gtk or qt)
_ui="gtk"

if [ "$_ui" == qt ] ; then
  depends=('qt>=4.7.0')
fi

if [ "$_ui" == gtk ] ; then
  depends=('gtk2')
fi

build() {
  cd "${srcdir}/beat"

# QT 4.8.0 fix
  if [ "$_ui" == qt ] ; then
    moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp
  fi

# Compile beat
  make compiler=gcc platform=x link='-s -lX11' phoenix=${_ui}
}

package() {
  cd "${srcdir}/beat"
  install -dm 755 "${pkgdir}/usr/bin"
  install -dm 755 "${pkgdir}/usr/share/applications"
  install -m 755 beat "${pkgdir}/usr/bin/beat"
  install -m 644 "../beat.desktop" "${pkgdir}/usr/share/applications/beat.desktop"
}

# vim: ts=2 sw=2 et:
