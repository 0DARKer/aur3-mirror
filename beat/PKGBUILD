# Maintainer : Alucryd <alucryd at gmail dot com>

pkgbase=beat
pkgname=beat
pkgver=01
pkgrel=9
arch=('i686' 'x86_64')
url="http://byuu.org/programming/beat/"
license=('GPL3')
source=("http://byuu.org/files/beat_v01.tar.xz" "beat.desktop")
sha256sums=('0ba71b22fa405929df3b300c3b80537bc8851854b27b291d3ad812ea1c45873d'
            'c12e60aa1c4877de06a458b2c8cc8d7ff940a0c9b397128a2dee1cb7de0329b9')

# UI (only one can be set to true)
_gtk=true
_qt=false

if [ "$_gtk" == true ]; then
	pkgname+=('beat-gtk')
fi

if [ "$_qt" == true ]; then
	pkgname+=('beat-qt')
fi

buildui() {
	make compiler=gcc platform=x phoenix=$1
	mv beat beat-$1
	make clean
}

installui() {
	cd "${srcdir}/beat"
	install -Dm 755 beat-$1 "${pkgdir}/usr/bin/beat"
	install -dm 755 "${pkgdir}/usr/share/applications"
	install -m 644 "../beat.desktop" "${pkgdir}/usr/share/applications/beat.desktop"
}

build() {
	cd "${srcdir}/beat"

# QT 4.8.0 fix
	if [ _qt == true ]; then
		moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp
	fi

# Linker fix
	sed -i 's|gtk+-2.0|gtk+-2.0 x11|g' phoenix/Makefile
	sed -i 's|QtCore QtGui|QtCore QtGui x11|g' phoenix/Makefile

# Compile GTK GUI
	if [ $_gtk == true ]; then
		buildui gtk
	fi

# Compile QT GUI
	if [ $_qt == true ]; then
		buildui qt
	fi
}

package_beat-gtk() {
	pkgdesc="Delta patcher for the higan emulator - GTK GUI"
	depends=('gtk2')
	provides=("beat-ui=${pkgver}-${pkgrel}")
	conflicts=('beat-qt')

	installui gtk
}

package_beat-qt() {
	pkgdesc="Delta patcher for the higan emulator - QT GUI"
	depends=('qt>=4.7.0')
	provides=("beat-ui=${pkgver}-${pkgrel}")
	conflicts=('beat-gtk')

	installui qt
}

package_beat() {
	pkgdesc="Delta patcher for the higan emulator - Dummy package for AUR helpers"
	depends=("beat-ui>=${pkgver}-${pkgrel}")
}

pkgdesc="Delta patcher for the higan emulator"
depends=()
