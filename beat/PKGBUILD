# Maintainer : Alucryd <alucryd at gmail dot com>

pkgbase=beat
pkgname=beat
true && pkgname=('beat-gtk' 'beat-qt')
pkgver=01
pkgrel=5
arch=('i686' 'x86_64')
url="http://byuu.org/programming/beat/"
license=('GPL3')
source=("http://byuu.org/files/beat_v01.tar.xz" "beat.desktop")
sha256sums=('0ba71b22fa405929df3b300c3b80537bc8851854b27b291d3ad812ea1c45873d'
            'e77f43bdf81452149964fa9069190e79ef89d535b90bafca31735369965c4ea4')

buildui() {
  make compiler=gcc platform=x phoenix=$1
  mv beat beat-$1
  make clean
}

installui() {
  cd "${srcdir}/beat"
  install -Dm 755 beat-$1 "${pkgdir}/usr/bin/beat-$1"
  install -dm 755 "${pkgdir}/usr/share/applications"
  install -m 644 "../beat.desktop" "${pkgdir}/usr/share/applications/beat-$1.desktop"
  sed -i "s|_ui|$1|" "${pkgdir}/usr/share/applications/beat-$1.desktop"
}

build() {
  cd "${srcdir}/beat"

# QT 4.8.0 fix
  moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp

# Linker fix
  sed -i 's|gtk+-2.0|gtk+-2.0 x11|g' phoenix/Makefile
  sed -i 's|QtCore QtGui|QtCore QtGui x11|g' phoenix/Makefile

# Compile GTK GUI
  buildui gtk

# Compile QT GUI
  buildui qt
}

package_beat-gtk() {
  pkgdesc="Delta patcher for the higan emulator - GTK GUI"
  depends=('gtk2')
  provides=('beat')

  installui gtk
}

package_beat-qt() {
  pkgdesc="Delta patcher for the higan emulator - QT GUI"
  depends=('qt>=4.7.0')
  provides=('beat')

  installui qt
}

pkgdesc="Delta patcher for the higan emulator"
depends=()
