# Maintainer: Lone_Wolf <lonewolf@xs4all.nl>
# Contributor : Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Adapted from offical archlinux mesa package

pkgname=(mesa-r600g-git)
pkgver=20110715
pkgrel=2
_realver=7.12
pkgdesc="Mesa R600 gallium for Radeon HD 2xxx and later - git version.If you live in the US, you should delete --enable-texture-float \ line."
arch=('x86_64')
depends=('libxt' 'libxxf86vm' 'libxdamage' 'xorg-server' 'libffi' 'libxv')
makedepends=('pkgconfig' 'python2' 'talloc' 'libxml2' 'imake' 'git' 'glproto' 'dri2proto>=2.6' 'llvm' 'xorg-server-devel' 'libxvmc' 'libvdpau')
optdepends=('libtxc_dxtn: S3TC support'
            'mesa-demos: glxinfo and glxgears')
provides=(mesa=${_realver} libgl=${_realver} ati-dri=${_realver})
replaces=(mesa libgl ati-dri)
conflicts=('xf86-video-ati<6.9.0-6' mesa libgl ati-dri)
url="http://mesa3d.sourceforge.net"
license=(custom)
source=(LICENSE)
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a')

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

build() {
  msg 'Connecting to git.freedesktop.org GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin --depth=1
  else
    git clone ${_gitroot} --depth=1
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd "${srcdir}"

  # Cleanup and prepare the build dir
  [ -d build ] && rm -rf build
  cp -r ${_gitname} build
  cd build
    ./autogen.sh --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r600,swrast \
    --with-dri-drivers=r600,swrast \
    --enable-gallium-llvm \
    --enable-texture-float \
    --enable-glx-tls \
    --enable-xcb \
    --enable-shared-dricore \
    --enable-gbm \
    --enable-gallium-gbm \
    --enable-xvmc \
    --enable-vdpau \
    --enable-gallium-g3dvl \
    --enable-shared-glapi
  make
}

package() {

# Mesa
  cd "${srcdir}/build" 
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/lib/libGL.so"*
  rm -f "${pkgdir}/usr/lib/libGLESv"*
  rm -f "${pkgdir}/usr/lib/libEGL"*
  rm -rf "${pkgdir}/usr/lib/egl"
  rm -f ${pkgdir}/usr/lib/pkgconfig/{glesv1_cm.pc,glesv2.pc,egl.pc}
  rm -rf "${pkgdir}/usr/lib/xorg"
  rm -f "${pkgdir}/usr/include/GL/glew.h"
  rm -f "${pkgdir}/usr/include/GL/glxew.h"
  rm -f "${pkgdir}/usr/include/GL/wglew.h"
  rm -f "${pkgdir}/usr/include/GL/glut.h"
  rm -rf ${pkgdir}/usr/include/{GLES,GLES2,EGL,KHR}
# libgl
  cd "${srcdir}/build" 
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  bin/minstall lib/libGL.so* "${pkgdir}/usr/lib/"
  bin/minstall lib/libdricore.so* "${pkgdir}/usr/lib/"
  bin/minstall lib/libglsl.so* "${pkgdir}/usr/lib/"
  cd src/mesa/drivers/dri
  make -C ${srcdir}/build/src/gallium/targets/dri-swrast DESTDIR="${pkgdir}" install
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"
# ati-dri
  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C ${srcdir}/build/src/gallium/targets/dri-r600 DESTDIR="${pkgdir}" install
#license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
