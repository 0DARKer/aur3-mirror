# Maintainer: Filip Brcic <brcha@gna.org>
# Contributor: Philip A Reimer <antreimer@gmail.com>

_qt_module=qtquickcontrols
pkgname="mingw-w64-qt5-quickcontrols"
pkgver="5.1.0"
pkgrel=1
arch=('any')
pkgdesc="Qt5 for Windows -- QtSerialPort module (mingw-w64)"
depends=('mingw-w64-qt5-declarative')
makedepends=('mingw-w64-gcc')
options=('!strip' '!buildflags')
license=('LGPL')
url="http://qt-project.org/wiki/QtSerialPort"
_pkgfqn="${_qt_module}-opensource-src-$pkgver"
source=("http://download.qt-project.org/official_releases/qt/5.1/${pkgver}/submodules/${_pkgfqn}.tar.xz")
md5sums=('b3825124a173a36f63c2f8380dc61e81')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}/${_pkgfqn}

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-qmake-qt5 ../${_qt_module}.pro
    make
    popd
  done
}

package() {
  cd ${srcdir}/${_pkgfqn}

  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make INSTALL_ROOT=$pkgdir install    
    popd

    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/share/qt5/qml/QtQuick/Controls/qtquickcontrolsplugin.dll
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/share/qt5/qml/QtQuick/Layouts/qquicklayoutsplugin.dll
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/share/qt5/qml/QtQuick/Controls/Private/qtquickcontrolsprivateplugin.dll
  done
}
