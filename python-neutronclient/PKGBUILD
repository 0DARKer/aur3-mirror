# Maintainer: Carl George < arch at cgtx.us >

_module="python-neutronclient"
pkgname=("python-neutronclient" "python2-neutronclient")
pkgver="2.4.0"
pkgrel="1"
pkgdesc="Python API Library for Quantum"
arch=("any")
url="https://github.com/openstack/python-neutronclient"
license=("Apache")
makedepends=("python-pbr" "python-setuptools" "python2-pbr" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz"
        "neutron.bash_completion"
        "neutron2.bash_completion")
sha256sums=('4c5c3a79216c682926f75ffd8db5f4338525a643ea02de37df7bf82a1e486b28'
            '55030291af97994acf6c957c3822ccfd685c5a9e3e03d3ad379d4f337066c297'
            '1011f25c51165f41e6f085a8e1c3937feac599dc87b2d29ffffa1d2172a2a7f9')

prepare() {
    sed -i '/argparse/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
    sed -i '/pbr/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-neutronclient() {
    depends=("python-babel>=1.3"
             "python-cliff>=1.10.0"
             "python-iso8601>=0.1.9"
             "python-keystoneclient>=1.1.0"
             "python-netaddr>=0.7.12"
             "python-oslo-i18n>=1.5.0"
             "python-oslo-serialization>=1.4.0"
             "python-oslo-utils>=1.4.0"
             "python-requests>=2.2.0"
             "python-simplejson>=2.2.0"
             "python-six>=1.9.0")
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 "${srcdir}/neutron.bash_completion" "${pkgdir}/etc/bash_completion.d/neutron"
}

package_python2-neutronclient() {
    depends=("python2-babel>=1.3"
             "python2-cliff>=1.10.0"
             "python2-iso8601>=0.1.9"
             "python2-keystoneclient>=1.1.0"
             "python2-netaddr>=0.7.12"
             "python2-oslo-i18n>=1.5.0"
             "python2-oslo-serialization>=1.4.0"
             "python2-oslo-utils>=1.4.0"
             "python2-requests>=2.2.0"
             "python2-simplejson>=2.2.0"
             "python2-six>=1.9.0")
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 "${srcdir}/neutron2.bash_completion" "${pkgdir}/etc/bash_completion.d/neutron2"
    mv "${pkgdir}/usr/bin/neutron" "${pkgdir}/usr/bin/neutron2"
}
