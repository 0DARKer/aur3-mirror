# Maintainer: hasardeur
pkgname=pptpclient-without-missing_window
pkgver=1.7.2
pkgrel=1
pkgdesc="Client for the proprietary Microsoft Point-to-Point Tunneling Protocol, PPTP - patched to remove MISSING_WINDOW checks."
arch=('i686' 'x86_64')
url="http://pptpclient.sourceforge.net/"
license=('GPL')
depends=('ppp' 'iproute2')
conflicts=('pptpclient')
backup=('etc/ppp/options.pptp')
source=("http://downloads.sourceforge.net/sourceforge/pptpclient/pptp-$pkgver.tar.gz" "adj_missing_windows.patch")
md5sums=('4c3d19286a37459a632c7128c92a9857'
	 '28d105d817107c7fe48fc2c7c27a3934')

build() {
  cd $srcdir/pptp-$pkgver

  #apply patch
  patch -p1 -i $srcdir/adj_missing_windows.patch 

  # use our CFLAGS and LDFLAGS
  sed -i -e "/CFLAGS  =/ c\CFLAGS = ${CFLAGS}" Makefile
  sed -i -e "/LDFLAGS =/ c\LDFLAGS = ${LDFLAGS}" Makefile

  # adjust ip path
  sed -i 's#/bin/ip#/usr/sbin/ip#' routing.c

  make 
}

package() {
  cd $srcdir/pptp-$pkgver
  make DESTDIR=$pkgdir install
}
