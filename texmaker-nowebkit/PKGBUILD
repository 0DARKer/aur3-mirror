# PKGBUILD derived from texmaker-oxygen-icons by damian01w
pkgname=texmaker-nowebkit
_pkgname=texmaker
pkgver=3.2.1
pkgrel=1
pkgdesc="The famous free cross-platform latex editor, stripped of browser/QtWebKit module."
arch=('i686' 'x86_64')
url="http://www.xm1math.net/texmaker/index.html"
license=('GPL')
depends=('qt>=4.7' 'ghostscript')
conflicts=('texmaker')
#optdepends=('ibus-qt: to avoid some trouble with accentued characters')
source=(http://www.xm1math.net/texmaker/$_pkgname-$pkgver.tar.bz2 \
	texmaker.desktop \
	browser.cpp \
	browser.h \
)
md5sums=('3d54fe9e5530f5cd8f9365bf9fb2e910'
         '48ca54ffd5eb98a83cbf3626205b8639'
         '445c4f825ef92a99fcd45445a7097d10'
         'aa331bfadd09e39042be09d2021ba046'
)

build() {
	cd $srcdir/$_pkgname-$pkgver

	# remove reference for WebKit from texmaker.pro
	sed 's/webkit//' <texmaker.pro >texmaker.pro.new
	mv -f texmaker.pro.new texmaker.pro
	
	# purge browser.h/.cpp
	cp -f $startdir/browser.* .
	
	# run qmake; generate Makefile
	qmake -unix PREFIX=//usr texmaker.pro
	
	make || return 1
}
package() {
	cd $srcdir/$_pkgname-$pkgver
	
	export INSTALL_ROOT=$pkgdir
	make install
	
	rm -f $pkgdir/usr/share/texmaker/{*.desktop,COPYING}
	
	install -D -m644 images/appicon.png \
		$pkgdir/usr/share/pixmaps/texmaker.png
		
	install -D -m644 $srcdir/$_pkgname.desktop \
		$pkgdir/usr/share/applications/$_pkgname.desktop
}

