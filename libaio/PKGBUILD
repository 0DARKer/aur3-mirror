# Maintainer: xduugu
# Contributor: jensp <jens@jenux.homelinux.org>
# Credit to encelo for updating the PKGBUILD

pkgname=libaio
pkgver=0.3.109
pkgrel=4
pkgdesc="The Linux-native asynchronous I/O facility (aio) library"
arch=('i686' 'x86_64')
url="http://git.kernel.org/?p=libs/libaio/libaio.git;a=summary"
license=('LGPL')
source=(ftp://ftp.kernel.org/pub/linux/libs/aio/libaio-$pkgver.tar.bz2)
depends=('man-pages')
md5sums=('14f3a22fc14118a48be80ab5ea974f49')
sha256sums=('b5cefce0a3cb49f8dca4d00e9480c0d9b45b75863bd44764156e322ee214e794')

build() {
	cd "$pkgname-$pkgver"
	sed -ri 's/^(version_dirty|dirty|new_changes|tag_valid) :=/#/' Makefile
	make
}

package() {
	cd "$pkgname-$pkgver"
	make prefix="$pkgdir/usr" install

	local file
	for file in man/*; do
		install -Dm644 "$file" "$pkgdir/usr/share/man/man${file##*.}/${file##*/}"
	done

	# fix file conflicts with package 'man-pages'
	cd "$pkgdir/usr/share/man/man3/"
	rm -f -- \
		aio_cancel.3 \
		aio_error.3 \
		aio_fsync.3 \
		aio_read.3 \
		aio_return.3 \
		aio_suspend.3 \
		aio_write.3 \
		aio_init.3 \
		lio_listio.3 \
		io.3
}

