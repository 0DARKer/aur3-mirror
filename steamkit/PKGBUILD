pkgname=steamkit
pkgdesc="SteamKit2 is a .NET library designed to interoperate with Valve's Steam network."
pkgver=1.5.0
pkgrel=1
arch=(any)
license=("LGPLv2.1")
options=(!emptydirs)
url="https://github.com/SteamRE/SteamKit"
source=("https://github.com/SteamRE/SteamKit/archive/SteamKit_$pkgver.tar.gz"
"steamkit2.pc.in"
"protobufdumper.sh"
"steamlanguageparser.sh"
"SteamKit2.snk")
depends=(protobuf-net)
makedepends=(mono)
sha1sums=('6dfef3ceffad9b0fab65a951bcb0644cd57f03ac'
          'e954ee52104005f007d142f406619402cd3f170e'
          'a94bb1fbab7aab665581d1b0989e82a368ccec15'
          '7a0f386a9a8cdc17736e5d489e614e2803151512'
          '2b81f3067957c98377a29ccf47dfa02306909f56')

build() {
	cd "${srcdir}/SteamKit-SteamKit_$pkgver/SteamKit2/SteamKit2"
	xbuild SteamKit2.csproj /p:Configuration=Release
	cd bin/Release
	monodis SteamKit2.dll --output=SteamKit2.il
	ilasm /dll /key:"$srcdir/SteamKit2.snk" SteamKit2.il
	cd ../../../../Resources/ProtobufDumper
	xbuild ProtobufDumper.sln /p:Configuration=Release
	cd ../SteamLanguageParser
	xbuild SteamLanguageParser.csproj /p:Configuration=Release /p:Platform=AnyCPU
}

package() {
	cd "${srcdir}/SteamKit-SteamKit_$pkgver/SteamKit2/SteamKit2/bin/Release"
	find . -name '*.dll*' -exec install -Dm644 {} "$pkgdir/usr/lib/steamkit2/"{} \;
	install -Dm644 license.txt "$pkgdir/usr/share/licenses/steamkit2/license.txt"
	cd ../../../../Resources
	find Protobufs -name '*.proto' -exec install -Dm644 {} "$pkgdir/usr/share/steamkit2/"{} \;
	find SteamLanguage -name '*.steamd' -exec install -Dm644 {} "$pkgdir/usr/share/steamkit2/"{} \;
	find Structs -name '*.hsl' -exec install -Dm644 {} "$pkgdir/usr/share/steamkit2/"{} \;
	cd ProtobufDumper/ProtobufDumper/bin/Release
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/lib/steamkit2/"{} \;
	cd ../../../../SteamLanguageParser/bin/Release
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/lib/steamkit2/"{} \;
	find "$pkgdir" -name '*.dll' -exec gacutil -i {} -root "$pkgdir/usr/lib" \;
	install -Dm644 "$srcdir/steamkit2.pc.in" "$pkgdir/usr/lib/pkgconfig/steamkit2.pc"
	sed -i "s,@VERSION@,$pkgver," "$pkgdir/usr/lib/pkgconfig/steamkit2.pc"
	install -Dm755 "$srcdir/protobufdumper.sh" "$pkgdir/usr/bin/protobufdumper"
	install -m755 "$srcdir/steamlanguageparser.sh" "$pkgdir/usr/bin/steamlanguageparser"
}
