# Maintainer: Noel Maersk <veox at wemakethings dot net>
# Contributor: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgname=cpuminer-git
_gitname=cpuminer
pkgver=276.dc20ebf
pkgrel=1
pkgdesc="Multi-threaded CPU miner for Bitcoin and Litecoin (git version)"
arch=('i686' 'x86_64')
url="https://github.com/pooler/cpuminer"
license=('GPLv2')
depends=('curl' 'jansson')
makedepends=('git' 'yasm')
source=('cpuminer::git+http://github.com/pooler/cpuminer.git'
        'rc-script.cpuminer'
        'cpuminer.conf')
md5sums=('SKIP'
         'b50db28641e7dd4554096639f19215b8'
         '96ee832cea2ff946bb890669cc7d34a0')
backup=(etc/conf.d/cpuminer.conf etc/cpuminer.json)

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/${_gitname}"

  # Use in-tree jansson
  # sed -e 's/^AC_CHECK_LIB(jansson, json_loads, request_jansson=false, request_jansson=true)$/request_jansson=true/' -i configure.ac

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/${_gitname}"

  make DESTDIR="$pkgdir/" install
  install -Dm600 "example-cfg.json" "$pkgdir/etc/cpuminer.json"
  install -Dm644 "$srcdir/cpuminer.conf" "$pkgdir/etc/conf.d/cpuminer.conf"
  install -Dm755 "$srcdir/rc-script.cpuminer" "$pkgdir/etc/rc.d/cpuminer"

  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
