# Maintainer: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Emmanuel 'guinness' Boudreault

pkgname=guile-devel
pkgver=2.0.2
pkgrel=1
pkgdesc="Guile is a portable, embeddable Scheme implementation written in C"
url="http://www.gnu.org/software/guile/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gmp>=4.3.1' 'libtool' 'ncurses>=5.7' 'texinfo'
         'libunistring' 'gc' 'libffi')
conflicts=('guile')
provides=("guile=$pkgver")
install=guile.install
options=('!libtool')
source=(ftp://ftp.gnu.org/pub/gnu/guile/guile-${pkgver}.tar.gz)
sha256sums=('1d36536f8bd398f8fbcb4fcc7b6aa6446cffe46f6e2a56cf5a0589c59be7b129')

build() {
  cd "${srcdir}/guile-${pkgver}"

  ./configure --prefix=/usr
  make LDFLAGS+="-lpthread"
}

package() {
  cd "${srcdir}/guile-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
