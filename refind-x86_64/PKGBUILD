# Maintainer: Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

# _GNU_EFI_LIB_DIR="/usr/lib"

_actualname="refind"
_pkgname="${_actualname}-x86_64"
pkgname="${_pkgname}"

pkgver="0.2.2"
pkgrel="3"
pkgdesc="Rod Smith's fork of rEFIt (U)EFI Boot Manager"
url="http://www.rodsbooks.com/refind/index.html"
arch=('any')
license=('GPL3' 'custom')

makedepends=('gnu-efi')
depends=('dosfstools' 'efibootmgr')
optdepends=('mactel-boot: For bless command in Apple Mac systems')

backup=('boot/efi/efi/arch_refind/refind.conf'
        'boot/efi/efi/arch_refind/refind_linux.conf')

options=('!strip' 'docs')
install="${_pkgname}.install"

source=("http://downloads.sourceforge.net/refind/refind-src-${pkgver}.zip"
        'refind_linux.conf'
        'refind_fix_linux_config_filename conflicts.patch'
        'refind_stop_2nd_program_crash.patch'
        'os_arch.icns')

sha256sums=('45071370d083f3eb46add92c45463d42e444d07d85e320bb675bae04d1ccb0e6'
            '9aac6e65018965ba182ec2d246d37fc5f9269ae96504956d8a51355c3ba1b62f'
            'd6451c8bad6dad43af50a7844519b4658f2a8e3a4e140a5fbd2639d99e400b20'
            '95b87ab3f5b0ad65212886f8987d3c773b7255b255d4c7c616684c2cfba164db'
            '589877dccc60f04c414fc5d002c5ca4e5ab687ac6912e866f6946f144a4bcde8')

build() {
	
	if [[ "${CARCH}" != "x86_64" ]]; then
		echo "${pkgname} package can be built only in a x86_64 system. Exiting."
		exit 1
	fi
	
	cd "${srcdir}/refind-${pkgver}/"
	
	## Patch from upstream git repo - rename linux.conf to refind_linux.conf
	patch -Np1 -i "${srcdir}/refind_fix_linux_config_filename conflicts.patch"
	echo
	
	## Patch from upstream git repo - fix system hang during 2nd program launch
	patch -Np1 -i "${srcdir}/refind_stop_2nd_program_crash.patch"
	echo
	
	make
	echo
	
}

package() {
	
	cd "${srcdir}/refind-${pkgver}/"
	
	## install the rEFInd x86_64 UEFI app
	install -d "${pkgdir}/boot/efi/efi/arch_refind/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/refind/refind.efi" "${pkgdir}/boot/efi/efi/arch_refind/refindx64.efi"
	
	## install the rEFInd config file
	install -D -m0644 "${srcdir}/refind-${pkgver}/refind.conf-sample" "${pkgdir}/boot/efi/efi/arch_refind/refind.conf"
	install -D -m0644 "${srcdir}/refind_linux.conf" "${pkgdir}/boot/efi/efi/arch_refind/refind_linux.conf"
	
	## install the rEFInd icons
	install -d "${pkgdir}/boot/efi/efi/arch_refind/icons/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/icons"/* "${pkgdir}/boot/efi/efi/arch_refind/icons/"
	install -D -m0644 "${srcdir}/os_arch.icns" "${pkgdir}/boot/efi/efi/arch_refind/icons/os_arch.icns" 
	
	## install the rEFInd docs
	install -d "${pkgdir}/usr/share/refind/docs/html/"
	install -d "${pkgdir}/usr/share/refind/docs/Styles/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/docs/refind"/* "${pkgdir}/usr/share/refind/docs/html/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/docs/Styles"/* "${pkgdir}/usr/share/refind/docs/Styles/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/README.txt" "${pkgdir}/usr/share/refind/docs/README.txt"
	install -D -m0644 "${srcdir}/refind-${pkgver}/NEWS.txt" "${pkgdir}/usr/share/refind/docs/NEWS.txt"
	rm -f "${pkgdir}/usr/share/refind/docs/html/.DS_Store" || true
	
	## rename linux.conf to refind_linux.conf
	sed 's|linux.conf|refind_linux.conf|g' -i "${pkgdir}/usr/share/refind/docs/html"/*.html
	sed 's|linux.conf|refind_linux.conf|g' -i "${pkgdir}/usr/share/refind/docs"/*.txt
	
	## install the rEFIt license file, since rEFInd is a fork of rEFIt
	install -d "${pkgdir}/usr/share/licenses/refind/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/refind/LICENSE"
	
}
