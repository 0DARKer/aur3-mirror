pkgname=fpc-i386-win32-rtl
pkgver=2.6.0
pkgrel=3
pkgdesc="Free Pascal runtime library for Win32."
arch=(any)
url="http://www.freepascal.org"
license=("GPL" "LGPL" "custom")
depends=("fpc=$pkgver")
makedepends=(ppcross386 mingw-w64-binutils)
options=(!strip !makeflags !buildflags)
source=("ftp://ftp.freepascal.org/pub/fpc/dist/$pkgver/source/fpcbuild-$pkgver.tar.gz")
md5sums=('17375e665a4e1311f85812fe2754f609')

build() {
  cd "${srcdir}/fpcbuild-$pkgver/fpcsrc/compiler"
  fpcmake -Tall
  cd "../.."
  make NOGDB=1 build OS_TARGET=win32 CPU_TARGET=i386
}

package() {
  cd "${srcdir}/fpcbuild-$pkgver"
  make NOGDB=1 PREFIX="${pkgdir}/usr" crossinstall OS_TARGET=win32 CPU_TARGET=i386
  rm -r "${pkgdir}/usr/bin"
  rm "$pkgdir/usr/lib/fpc/$pkgver/ppcross386"
  
  find $pkgdir -name '*.o' -o -name '*.a' | xargs -rtl1 i686-w64-mingw32-strip -g
  
  cat << 'EOM'
  ==> PLEASE NOTE:
  ==> Please be sure to copy fpcbuild-x.x.x.tar.gz to the extracted package folders
  ==> of any RTLs or other FPC cross compilers you intend to build, as Free Pascal's 
  ==> FTP server is very low bandwidth. Thanks!    
EOM
}