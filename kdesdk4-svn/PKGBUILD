# Contributor: Lawrence Lee <valheru@facticius.net>
pkgname=kdesdk4-svn
pkgver=1236499
pkgrel=1
pkgdesc="KDE4 Software Development Kit"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kdebase4-runtime-git' 'subversion' 'git' 'python2' 'kdepim4-runtime-git' 'kdebindings-python' 'antlr2')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'subversion')
source=('fix-python2-path.patch')
md5sums=('61c6cb8c25c9a3ff15fb8840b1e08df9')
conflicts=('kdesdk')
groups=('kde4-git')
install="kdesdk4-svn.install"

_svntrunk=svn://anonsvn.kde.org/home/kde/trunk/KDE/kdesdk
_svnmod=kdesdk

build() {
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up)
  else
    svn co $_svntrunk $_svnmod
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build
  fi 

  cp -r ${srcdir}/$_svnmod ${srcdir}/build
  cd ${srcdir}/build
  patch -Np1 -i ${srcdir}/fix-python2-path.patch
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
                -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
                $(find . -name '*.py')
  mkdir build
  cd build
  cmake ../ -DCMAKE_BUILD_TYPE=debugfull \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd ${srcdir}/build/build
  make DESTDIR=${pkgdir} install
}