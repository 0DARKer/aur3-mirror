# Maintainer: speps <speps at aur dot archlinux dot org>

_name=triceratops
pkgname=$_name-lv2
pkgver=0.0.6c
pkgrel=1
pkgdesc="Polyphonic synthesizer LV2 plugin"
arch=(i686 x86_64)
url="http://deliriumdecrypted.blogspot.com/"
license=('GPL')
groups=('lv2-plugins')
depends=('gtkmm' 'lv2')
makedepends=('lv2-c++-tools')
provides=("lv2-$_name" "$_name")
conflicts=("lv2-$_name" "$_name")
replaces=("$_name")
source=("http://downloads.sourceforge.net/project/$_name/$pkgname-v$pkgver.tar.gz")
md5sums=('551c2245cc87fd02fbcc80c9908cc0a7')

build() {
  cd "$srcdir/$pkgname-v${pkgver::-1}"

  # fix hardcoded lv2 path
  sed -i "s|getenv.*/\.|\"/usr/lib/|g" osc.c

  make
}

package() {
  cd "$srcdir/$pkgname-v${pkgver::-1}"
  make INSTALL_DIR="$pkgdir/usr/lib/lv2" install
}

# vim:set ts=2 sw=2 et:
