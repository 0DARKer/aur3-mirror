# Contributor: Mathieu Clabaut <mathieu.clabaut@gmail.com>
# Contributor: Kiril Zyapkov <kiril.zyapkov@gmail.com>
pkgname=python25
pkgver=2.5.6
pkgrel=3
pkgdesc="A high-level scripting language"
arch=(i686 x86_64)
license=('custom')
url="http://www.python.org"
depends=('db' 'bzip2' 'gdbm' 'openssl' 'zlib' 'gcc-libs' 'readline')
makedepends=('tk')
source=("http://www.python.org/ftp/python/$pkgver/Python-$pkgver.tar.bz2"
        '001_patch-svnversion.patch')
options=('!makeflags')
md5sums=('5d45979c5f30fb2dd5f067c6b06b88e4'
         'ffa026e28f375afc08cc7f50cfe7f8ab')

build() {
  export LD_LIBRARY_PATH=${LD_LIBRARY_PATH}:$srcdir/Python-$pkgver
  cd $srcdir/Python-$pkgver
  patch -p0 < ../001_patch-svnversion.patch
  ./configure --prefix=/usr --enable-shared --with-thread \
              --enable-ipv6 --enable-unicode
  make
}

package() {
  export LD_LIBRARY_PATH=${LD_LIBRARY_PATH}:$srcdir/Python-$pkgver
  cd $srcdir/Python-$pkgver
  make DESTDIR=$pkgdir altinstall
  #rm $pkgdir/usr/bin/python
  #rm $pkgdir/usr/bin/python-config

  # Rename all the scripts
  pushd $pkgdir/usr/bin
  mv idle idle2.5
  mv pydoc pydoc2.5
  mv smtpd.py smtpd2.5.py
  popd

  # license
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE
  #mv $pkgdir/usr/share/man/man1/python.1 $pkgdir/usr/share/man/man1/python25.1

}
