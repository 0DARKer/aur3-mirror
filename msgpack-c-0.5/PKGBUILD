# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>
# Contributor: Leen Jewel <leenjewel@gmail.com>

_pkgname=msgpack
pkgname=${_pkgname}-c-0.5
pkgver=0.5.9
pkgrel=2
pkgdesc="An efficient object serialization library. Legacy version."
arch=(
	"${CARCH}"
)
url="http://msgpack.org/"
license=(
	"Apache"
)

depends=(
	"gcc-libs"
)
optdepends=()
makedepends=()
checkdepends=(
	"gtest"
)
provides=(
	"${_pkgname}-c=${pkgver}"
)
conflicts=(
	"${_pkgname}-c"
)

source=(
	"https://github.com/${_pkgname}/${_pkgname}-c/releases/download/cpp-${pkgver}/${_pkgname}-${pkgver}.tar.gz"
	"fix_test_failure.patch"
)
sha512sums=(
	"9488bf8d4aefb44ce7554cfc1e6a5fee50b774ca173a7cb8a344f421da64d51d5c60d58fd7aaf93490a33447ad2150098c164ef6a11b6b042c3b1b4a38d06435"
	"807f625aabf834ab39a03744618543648e24c659925dc00bca20113905b384fca3d57de8c222eaa4daee52c97b75bdca1e4db2260c060ef8ef5615aff84e25ba"
)

prepare() {
	# upstream fix https://github.com/msgpack/msgpack-c/pull/95
	cd ${srcdir}/${_pkgname}-${pkgver}
	patch ./test/msgpack_test.cpp < ../fix_test_failure.patch
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ ${njobs} -ge 8 ]]; then
		njobs=$(( ${njobs} - 2 ))
	fi
	
	# Building package
	cd ${srcdir}/${_pkgname}-${pkgver}
	./configure --prefix=/usr
	make -j${njobs}
}

check() {
	cd ${srcdir}/${_pkgname}-${pkgver}
	make check
}

package() {
	# Installing package
	cd ${srcdir}/${_pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}