# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=ffmpeg-git
pkgver=20110629
pkgrel=1
pkgdesc="A versatile tool to encode and decode a multitude of video and audio formats"
arch=('i686' 'x86_64')
url="http://ffmpeg.org/"
license=('GPL')
depends=('alsa-lib' 'bzip2' 'faac' 'lame' 'libtheora' 'libva' 'libvdpau' 'libvorbis' 'libxfixes' 'sdl' 'x264-git' 'zlib')
makedepends=('git' 'yasm')
conflicts=('ffmpeg')
provides=("ffmpeg=$pkgver" "qt-faststart")
source=()
md5sums=()

_gitroot="git://git.videolan.org/ffmpeg"
_gitname="ffmpeg"

build() {
  cd $srcdir
  msg "Connecting to the GIT server"
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated"
  else
    git clone --depth 1 $_gitroot
  fi
  
  msg "GIT checkout done"
  msg "Starting make"
  
  rm -rf $srcdir/$_gitname-build
  cp -a $srcdir/$_gitname $srcdir/$_gitname-build
  
  cd $srcdir/$_gitname-build

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libx264 \
    --enable-nonfree \
    --enable-postproc \
    --enable-vaapi \
    --enable-vdpau \
    --enable-x11grab 
 
  make
  make tools/qt-faststart
  make doc/ff{mpeg,play,probe,server}.1
}

package() {
  cd $srcdir/$_gitname-build
  make DESTDIR="$pkgdir" install install-man
  install -D -m755 tools/qt-faststart "$pkgdir/usr/bin/qt-faststart" 
  rm -rf $srcdir/$_gitname-build
}
