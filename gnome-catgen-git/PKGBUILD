# Maintainer: Prurigro
# Contributor: Prurigro

_pkgname=gnome-catgen
pkgname=${_pkgname}-git
pkgver=20140702.r34.f41dd21
pkgrel=1
pkgdesc="A Gnome 3.12 application-overview folder configuration tool"
url="https://github.com/prurigro/${_pkgname}"
license=('MIT')
arch=('any')
depends=('dconf')
makedepends=('git')
source=("git://github.com/prurigro/${_pkgname}.git#branch=master")
sha512sums=('SKIP')

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd $_pkgname
    install -Dm755 $_pkgname "${pkgdir}"/usr/bin/${_pkgname}

    # Man Page
    install -Dm644 ${_pkgname}.1 "${pkgdir}"/usr/share/man/man1/${_pkgname}.1

    # Vim Syntax
    install -Dm644 vim/ftdetect/gnomecat.vim "${pkgdir}/usr/share/vim/vimfiles/ftdetect/gnomecat.vim"
    install -Dm644 vim/ftplugin/gnomecat.vim "${pkgdir}/usr/share/vim/vimfiles/ftplugin/gnomecat.vim"
    install -Dm644 vim/syntax/gnomecat.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/gnomecat.vim"

    # License
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
