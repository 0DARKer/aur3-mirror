# Maintainer: Prurigro
# Contributor: Prurigro

_pkgname=gnome-catgen
pkgname=${_pkgname}-git
pkgver=20140421.r16.fa6ad20
pkgrel=1
pkgdesc="A Gnome 3.12 application-overview folder configuration tool"
url="https://github.com/prurigro/${_pkgname}"
license=('MIT')
arch=('any')
depends=('glib2' 'dconf')
source=("${_pkgname}.1.gz"
        "git://github.com/prurigro/${_pkgname}.git#branch=master")
sha512sums=('8e88afa8f66da58bec0491b64fc348072e668cee2a96baa90ce73bb6312c326d60a68351968fdb07f8d0af70834432f8787ae220c1bb2dd5b14a1379ed0878e5'
            'SKIP')

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    install -Dm644 ${_pkgname}.1.gz "${pkgdir}"/usr/share/man/man1/${_pkgname}.1.gz
    cd $_pkgname
    install -Dm755 $_pkgname "${pkgdir}"/usr/bin/${_pkgname}
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
