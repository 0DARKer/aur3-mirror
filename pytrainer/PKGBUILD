# Maintainer: xduugu
# Contributor: D. Can Celasun <dcelasun at gmail>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=pytrainer
pkgver=1.8.0
pkgrel=4
pkgdesc='A tool to log your sport activities.'
arch=('any')
url='http://sourceforge.net/projects/pytrainer/'
license=('GPL')
depends=('python-lxml' 'python2-matplotlib' 'python-soappy' 'pywebkitgtk')
optdepends=('firefox: for browser support'
            'garmintools: "Garmin via garmintools" plugin'
            'gpsbabel: "Garmin via GPSBabel 1.3.5" plugin'
            'zenity: garmintools and gpsbabel plugins')
source=("http://downloads.sourceforge.net/project/$pkgname/PyTrainer%20$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname.sh
        pathfix.patch
        replace_gtkmozembed_with_webkit.patch)
md5sums=('f101632933e071cbe6651c10ac78f69d'
         'b39a8511bc63e6d65a615d26defbb05f'
         'c80aa188dce3a5675b15a703873bf3ad'
         '9a881e98a5a1755dc4c0c1e03246c60a')
sha256sums=('47a0b68321f516691c5a5bb2c4c1ccd767be0ac02eef232a49a22fa319a7abec'
            'a0bd23c619d6c5c9bc79e90dba2b5951b3405c08764ca927de385b894759449c'
            'b098e66cbc0e0f53e1ee222eb313417c7589928b66ce7b756c677f73b964fe1e'
            '5f8f9a4356bef089de0340f55cdc874748ff259d3bc63a296350e803b6b7f167')

package() {
  cd "$pkgname-$pkgver"

  patch -Np2 < "$srcdir/pathfix.patch"
  patch -Np1 < "$srcdir/replace_gtkmozembed_with_webkit.patch"

  # Fix python paths
  export PYTHON="/usr/bin/python2"
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
  -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
  $(find "$srcdir/" -name '*.py')

  # Install
  python2 setup.py install --prefix=/usr --root="$pkgdir"

  # install launcher
  mv "$pkgdir"/usr/{bin,share/$pkgname}/$pkgname
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
}
