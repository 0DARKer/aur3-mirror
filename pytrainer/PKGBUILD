# Maintainer: xduugu
# Contributor: D. Can Celasun <dcelasun at gmail>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=pytrainer
pkgver=1.9.1
pkgrel=1
pkgdesc='A tool to log your sport activities.'
arch=('any')
url='http://sourceforge.net/projects/pytrainer/'
license=('GPL')
depends=('python-lxml' 'python2-matplotlib' 'python2-migrate' 'pywebkitgtk')
optdepends=('garmintools: "Garmin via garmintools" plugin'
            'gdal: "Elevation correction" plugin'
            'gpsbabel: "Garmin via GPSBabel 1.3.5" plugin'
            'zenity: garmintools and gpsbabel plugins')
source=("http://downloads.sourceforge.net/project/$pkgname/pytrainer%20$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname.sh
        pathfix.patch)
md5sums=('0e4904ca4ba22bf20b7fd09f8d2fda8d'
         'b39a8511bc63e6d65a615d26defbb05f'
         'c80aa188dce3a5675b15a703873bf3ad')
sha256sums=('5c6e4d21cf0632cc13453f5a4c23e7489f2a519afb38141fa1cf36657d9d80c5'
            'a0bd23c619d6c5c9bc79e90dba2b5951b3405c08764ca927de385b894759449c'
            'b098e66cbc0e0f53e1ee222eb313417c7589928b66ce7b756c677f73b964fe1e')

package() {
  cd "$pkgname-$pkgver"

  patch -Np2 < "$srcdir/pathfix.patch"

  # Fix python paths
  export PYTHON="/usr/bin/python2"
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
  -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
  $(find "$srcdir/" -name '*.py')

  # Install
  python2 setup.py install --prefix=/usr --root="$pkgdir"

  # install launcher
  mv "$pkgdir"/usr/{bin,share/$pkgname}/$pkgname
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
}
