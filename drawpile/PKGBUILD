# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=drawpile
pkgver=0.9.3
pkgrel=1
pkgdesc='Collaborative drawing program specially made for use with pen tablets'
arch=('x86_64' 'i686')
url='http://drawpile.net/'
license=('GPL2')
depends=('qt5-base' 'cmake' 'gcc')
makedepends=('gendesk')
source=("http://drawpile.net/files/src/$pkgname-$pkgver.tar.gz")
sha256sums=('535a43fb316c2e5901f51df2a1b69ab9e3c19ba30ae8b88566e03f0cd73ac10d')

prepare() {
  cd "$pkgname-$pkgver"

  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --categories 'Graphics'
}

build() {
  cd "$pkgname-$pkgver"

  rm -rf build
  mkdir -p build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir" install
  install -Dm644 "src/client/ui/icons/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
