# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=drawpile
pkgver=0.8.0
pkgrel=1
pkgdesc='Collaborative drawing program specially made for use with pen tablets'
arch=('x86_64' 'i686')
url='http://drawpile.sourceforge.net/'
license=('GPL2')
depends=('qt5-base' 'cmake' 'gcc' 'boost')
makedepends=('gendesk')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        'https://raw.github.com/xyproto/Drawpile/ba7f701ab758e2184513e933648acb71642f0a95/CMakeLists.txt')
sha256sums=('089e468d17c07a47d6f844aa742e72190a01734168ff35e044088bf8cda0288e'
            '231719bb3e004b86093dca56cb5f3da4bb1bfaaf4acfe4ca9d0abd14a4d30f8b')

prepare() {
  cd "$pkgname-$pkgver"

  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --categories 'Graphics'
  cp -f -v "$srcdir/CMakeLists.txt" CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"

  rm -rf build
  mkdir -p build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-$pkgver/build" 

  make DESTDIR="$pkgdir" install

  cd ..

  install -Dm644 "src/client/ui/icons/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
