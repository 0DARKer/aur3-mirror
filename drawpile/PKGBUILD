# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=drawpile
pkgver=0.9.8
pkgrel=1
pkgdesc='Collaborative drawing program specially made for use with pen tablets'
arch=('x86_64' 'i686')
url='http://drawpile.net/'
license=('GPL2')
depends=('qt5-base' 'cmake')
makedepends=('gendesk' 'ninja')
source=("http://drawpile.net/files/src/$pkgname-$pkgver.tar.gz")
sha256sums=('2a6fc93d53c2a1f122b2ad78ee2dc0123f1bc927c448f446253ee38daf1416be')

prepare() {
  cd "$pkgname-$pkgver"

  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --categories 'Graphics'
}

build() {
  cd "$pkgname-$pkgver"

  # Ensure build is an empty directory
  rm -rf build
  mkdir -p build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -G Ninja
  ninja
}

package() {
  cd "$pkgname-$pkgver"

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "src/client/ui/icons/builtin/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
