# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=drawpile
pkgver=0.8.1
pkgrel=1
pkgdesc='Collaborative drawing program specially made for use with pen tablets'
arch=('x86_64' 'i686')
url='http://drawpile.sourceforge.net/'
license=('GPL2')
depends=('qt5-base' 'cmake' 'gcc')
makedepends=('gendesk')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('c42c163507ce1ea4c31e145fead86d9b3c6c34ad95f00dacde81ee51e43fbddb')

prepare() {
  cd "$pkgname-$pkgver"

  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --categories 'Graphics'
}

build() {
  cd "$pkgname-$pkgver"

  rm -rf build
  mkdir -p build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-$pkgver/build" 

  make DESTDIR="$pkgdir" install

  cd ..

  install -Dm644 "src/client/ui/icons/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
