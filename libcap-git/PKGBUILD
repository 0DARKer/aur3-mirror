# This is an example PKGBUILD file. Use this as a start to creating your own,
# and remove these comments. For more information, see 'man PKGBUILD'.
# NOTE: Please fill out the license field for your package! If it is unknown,
# then please put 'unknown'.

# See http://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines
# for more information on packaging from GIT sources.

# Maintainer: Your Name <youremail@domain.com>
pkgname=libcap-git
pkgver=20120210
pkgrel=1
pkgdesc="Library for getting and setting POSIX.1e capabilities"
arch=('i686' 'x86_64')
url="http://git.kernel.org/?p=linux/kernel/git/morgan/libcap.git;a=summary"
license=('LGPL2.1')
depends=('glibc' 'attr' 'pam')
makedepends=('git')
provides=('libcap=2.22')
conflicts=('libcap')
options=('!libtool')

_gitroot=git://git.kernel.org/pub/scm/linux/kernel/git/morgan/libcap.git
_gitname=libcap.git

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # What a clusterfuck:
 
  sed -i "s|--static||g" progs/Makefile

  for flags in {C,CXX,LD}FLAGS; do
    [[ ${!flags} ]] && printf '%s += %s\n' "$flags" "${!flags}"
  done >> Make.Rules

  make -j1 CC="${CC:-gcc}"
}

package() {
  cd "$srcdir/$_gitname-build"
  make prefix=/usr \
    DESTDIR="$pkgdir" LIBDIR="$pkgdir/lib" RAISE_SETFCAP=no install
  find "$pkgdir/lib" -name "*.a" -exec rm {} +
}

# vim:set ts=2 sw=2 et:
