# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Arch Haskell Team <>

pkgname=haskell-happstack-server
_hkgname=happstack-server
_licensefile=COPYING
pkgver=7.0.3
pkgrel=1
pkgdesc="Web related tools and services."
url="http://happstack.com"
license=('BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-base64-bytestring'
         'haskell-blaze-html'
         'haskell-bytestring'
         'haskell-containers'
         'haskell-directory'
         'haskell-extensible-exceptions'
         'haskell-filepath'
         'haskell-hslogger'
         'haskell-html'
         'haskell-monad-control'
         'haskell-mtl'
         'haskell-network'
         'haskell-old-locale'
         'haskell-old-time'
         'haskell-parsec'
         'haskell-process'
         'haskell-sendfile'
         'haskell-system-filepath'
         'haskell-text'
         'haskell-time'
         'haskell-transformers'
         'haskell-transformers-base'
         'haskell-utf8-string'
         'haskell-xhtml'
         'haskell-zlib')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('1e8367cb6c8a40ddddbdb092cded40820082f3c8b0a97c54860992056580251b')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}
}
