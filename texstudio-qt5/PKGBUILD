# Maintainer: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=texstudio-qt5
_srcname=texstudio
pkgver=2.6.6
pkgrel=1
pkgdesc="Fork of the LaTeX IDE TexMaker. Gives you an environment where you can easily create and manage LaTeX documents. Built with Qt5/Phonon."
arch=('i686' 'x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPLv2')
depends=('poppler-qt5' 'phonon-qt5' 'qt5-svg')
makedepends=('sed')
optdepends=('evince: pdf reader', 'okular: alternate pdf reader')
replaces=('texmakerx' 'texstudio')
provides=('texstudio')
install=$pkgname.install
source=("http://downloads.sourceforge.net/texstudio/${_srcname}-${pkgver}.tar.gz"
        'texstudio-2.6.4-phonon-build-fix.patch'
        'phonon-qt5-build-fix.patch'
        'archlinux-phonon.patch')
md5sums=('abf549b9c18776cf6f1025253a9519c6'
         'd44d6063e241e201e72e2db08ff91190'
         '36193bf84ea64b7cbab829fb0d57f56f'
         '2b4bd3232e9f92271856f7daa318e6c7')

build() {
  cd "${srcdir}/${_srcname}${pkgver}"
  # Patch to build PHONON.
  patch -p1 < "${srcdir}"/texstudio-2.6.4-phonon-build-fix.patch
  # Patch to build against phonon-qt5
  patch -p1 < "${srcdir}"/phonon-qt5-build-fix.patch
  # Patch include path for phonon detection on archlinux.
  patch -p1 < "${srcdir}"/archlinux-phonon.patch
  # Fix .desktop item.
  sed -i -e '/^Encoding/d' -e "/^Icon/s|=.*|=${_srcname}|" utilities/${_srcname}.desktop
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS+="${CFLAGS}" QMAKE_CXXFLAGS+="${CXXFLAGS}" \
    PHONON=true INCLUDEPATH+="/usr/include/poppler/qt5" \
    CONFIG-="debug" -o Makefile texstudio.pro
  make
}

package() {
  cd "${srcdir}/${_srcname}${pkgver}"
  make INSTALL_ROOT="$pkgdir" install
  for _i in 16 22 32 48 64 128; do
    install -D -m 0644 utilities/${_srcname}${_i}x${_i}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${_i}x${_i}/apps/${_srcname}.png
  done
  install -D -m 0644 utilities/${_srcname}.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/${_srcname}.svg
}
