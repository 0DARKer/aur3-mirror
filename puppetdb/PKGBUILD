# Puppetdb: Installer: Arch
# Maintainer: Niels Abspoel <aboe76 (at) Gmail (dot) com>

# RC style, reserved for later use
##pkgname=puppetdb
#_rc=1
#_pkgver=2.0.0
#pkgver=${_pkgver}_rc${_rc}

pkgname=puppetdb
pkgver=2.1.0
pkgrel=3
pkgdesc="Puppet data warehouse; it manages storage and retrieval of all platform-generated data"
arch=("any")
url="http://puppetlabs.com/projects/puppetdb/"
license=("APACHE")
depends=("ruby" "facter" "puppet" "jre7-openjdk" )
backup=('etc/puppetdb/conf.d/jetty.eni' 'etc/puppetdb/conf.d/database.ini' 'etc/puppetdb/conf.d/config.ini' 'etc/puppetdb/conf.d/repl.ini')
provides=("puppetdb")
install="puppetdb.install"
source=("http://downloads.puppetlabs.com/puppetdb/$pkgname-${pkgver//_/-}.tar.gz"
        "puppetdb.service"
        "puppetdb-sysconfig")
md5sums=('7c6a988c68790afbf6aea197e57e5676'
         'cc44a56414dc70fc01343c5ca4089038'
         '9407eb01415b37e2dcb52129ba41468b')

package () {
  cd "${srcdir}/${pkgname}-${pkgver//_/-}"
  unset DESTDIR
  export LANG=en_US.UTF-8

  # clean wrong ext/files/ and pkg dir
  rake clean
  # recreate ext/files and pkg dir
  rake package:bootstrap
  # install puppetdb 
  rake install PARAMS_FILE= DESTDIR=${pkgdir}

  # add logrotate
  install -Dm 644 $srcdir/${pkgname}-${pkgver//_/-}/ext/files/puppetdb.logrotate $pkgdir/etc/logrotate.d/puppetdb

  #puppetdb documentation
  mkdir -p $pkgdir/usr/share/doc/puppetdb/documentation
  cp -r $srcdir/${pkgname}-${pkgver//_/-}/documentation $pkgdir/usr/share/doc/puppetdb/documentation
  install -D -m644 $srcdir/${pkgname}-${pkgver//_/-}/*.md $pkgdir/usr/share/doc/puppetdb/

  #archlinux specific systemd because of java binary in other place.
  install -D -m644 "${startdir}/puppetdb.service" "${pkgdir}/usr/lib/systemd/system/puppetdb.service"
  install -D -m644 "${startdir}/puppetdb-sysconfig" "${pkgdir}/etc/sysconfig/puppetdb"

}
# vim: set ft=sh ts=2 sw=2 et:
