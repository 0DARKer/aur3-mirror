
# Put the desired install prefix HERE
_iprefix=/opt/compiz-git

pkgname=compiz-core++
pkgver=20101113
pkgrel=1
pkgdesc="Compiz is an extensible window manager utilizing opengl for effects and usability"
url="http://www.compiz.org/"
license=('GPL' 'LGPL' 'MIT')
arch=('i686' 'x86_64')
depends=('startup-notification' 'librsvg' 'libgl' 'dbus'
         'mesa' 'libxslt' 'fuse' 'boost' 'libstdc++5')
makedepends=('intltool' 'git' 'cmake')
conflicts=('compiz-core-09')
options=(!libtool !emptydirs)
source=()
md5sums=()
_gitroot="git://anongit.compiz.org/compiz/core"
_gitname="compiz-core"
MAKEFLAGS="-j1"

# This is a developement version
OPTIONS=(!strip)

# trick re-determining the build revision and, or entertain the hidden
# makepkg option --forcever, huh
if [ -z "${FORCE_VER}" ]; then
    msg "Determining latest build revision..."
    FORCE_VER=$(date +%Y%m%d)
fi

build()
{
	cd "${srcdir}"
	msg "Connecting to GIT server...."

	if [ -d "${_gitname}" ] ; then
		cd "${_gitname}" && git pull origin
		msg "The local files are updated."
	else
		git clone "${_gitroot}" "${_gitname}"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."
	cd "${srcdir}/${_gitname}"
	[[ -d build ]] || mkdir build
	cd build
	env cmake .. -DCMAKE_INSTALL_PREFIX="${_iprefix}" -DCMAKE_BUILD_TYPE=Debug -DCOMPIZ_DESTDIR="${pkgdir}"
	make
	make install
	if [[ -d "${pkgdir}/usr" ]]; then
		msg "Removing potentially conflicting files..."
		find "${pkgdir}/usr" -type f | sed "s:^${pkgdir}::"
		rm -rf "${pkgdir}/usr"
	fi
	_cmake_root=$(sed -n 's/CMAKE_ROOT:INTERNAL=//p' CMakeCache.txt)
	mkdir -p "${pkgdir}${_cmake_root}/Modules"
	make findcompiz_install || return 1
	mkdir -p "$pkgdir/usr/bin"
	msg "Making /usr/bin symlinks..."
	ln -s "${_iprefix}/bin/compiz" "$pkgdir/usr/bin/compiz-git"
	[[ -f "${pkgdir}${_iprefix}/bin/gtk-window-decorator" ]] && \
		ln -s "${_iprefix}/bin/gtk-window-decorator" "$pkgdir/usr/bin/gtk-window-decorator-git" || true
	[[ -f "${pkgdir}${_iprefix}/bin/kde4-window-decorator" ]] && \
		ln -s "${_iprefix}/bin/kde4-window-decorator" "$pkgdir/usr/bin/kde4-window-decorator-git" || true
}
