# Submitter: fsckd <fsckdaemon@gmail.com>
# Contributor: numeral <numerical@gmail.com>
# Maintainer: Kniyl <mathias.ettinger@gmail.com>

pkgname=python-mezzanine
pkgver=3.0.9
pkgrel=1
pkgdesc="An open source content management platform built using the Django framework"
arch=('any')
license=('BSD')
url="http://mezzanine.jupo.org/"
# explicit dependency over python-pillow since python2-pillow provides python-imaging
depends=('python'
         'python-future-old'
         'python-six'
         'python-requests-oauthlib'
         'python-oauthlib'
         'python-django'
         'python-pillow'
         'python-django-grappelli-safe'
         'python-django-filebrowser-safe'
         'python-bleach'
         'python-pytz'
         'python-south')
makedepends=('python-distribute')
optdepends=('mysql-python: for MySQL backend'
            'python-psycopg2: for PostgreSQL backend')
source=("http://pypi.python.org/packages/source/M/Mezzanine/Mezzanine-${pkgver}.tar.gz")
md5sums=('a4b15a937f180e8e10205b7f87f54813')
sha256sums=('c52bdb94752eb1b4dd3eb69c0a43dc630f3b05625fabc3775e094a706e9c70cf')

build() {
  cd "${srcdir}/Mezzanine-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/Mezzanine-${pkgver}"
  python setup.py install --root="${pkgdir}"
}
