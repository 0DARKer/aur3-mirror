# Maintainer: Kerrick Staley <mail@kerrickstaley.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>

pkgname=gst-editing-services-git
pkgver=20130130
pkgrel=1
pkgdesc='GStreamer editing services (Git version)'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://gstreamer.freedesktop.org/modules/gst-editing-services.html'
depends=('gst-plugins-base-libs')
conflicts=('gst-editing-services')
makedepends=('git' 'cvs' 'intltool' 'pkgconfig' 'gtk-doc')
provides=('gst-editing-services=99')
options=('!libtool')
groups=('gstreamer')

source=('Replace-AM_CONFIG_HEADER-with-AC_CONFIG_HEADERS.patch')
sha256sums=('58228d1f2a6dde3f6a8fbbe9aec6ca39bbc00f0e4888688e0c8685ea0f672388')

_gitroot='git://anongit.freedesktop.org/gstreamer/gst-editing-services'
_gitname='gst-editing-services'

build() {
    cd "$srcdir"
    
    msg 'Connecting to GIT server...'
    if [[ -d "$_gitname" ]]; then
        (cd "$_gitname" && git pull) || return 1
    else
        git clone --depth=1 "$_gitroot" "$_gitname" || return 1
    fi
    msg 'GIT checkout done'
    
    msg 'Starting make...'
    cd "$_gitname"
    
    # https://bugzilla.gnome.org/show_bug.cgi?id=692864
    patch -p1 < "$srcdir"/Replace-AM_CONFIG_HEADER-with-AC_CONFIG_HEADERS.patch
    
    ./autogen.sh --prefix=/usr || return 1
    make || return 1
}

package() {
    cd "$srcdir/$_gitname"
    make DESTDIR="$pkgdir" install || return 1
}
