# Maintainer: Carl George <carl@cgtx.us>

_module="docker-py"
pkgname=("python-${_module/-py/}" "python2-${_module/-py/}")
pkgver="0.5.3"
pkgrel="1"
pkgdesc="An API client for docker written in Python"
arch=("any")
url="https://github.com/dotcloud/docker-py"
license=("Apache")
makedepends=(
	"python-setuptools"
	"python2-setuptools"
)
checkdepends=(
	"python-tox"
	"python2-tox"
	"python-mock"
	"python2-mock"
	"python-coverage"
	"python2-coverage"
)
source=("https://github.com/docker/${_module}/archive/${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

check() {
	cd "${srcdir}/${_module}-${pkgver}"
	tox -e py34,flake8
	cd "${srcdir}/${_module}-${pkgver}-python2"
	tox -e py27,flake8
}

package_python-docker() {
	# https://github.com/dotcloud/docker-py/blob/master/requirements3.txt
	depends=("python-requests" "python-six")
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-docker() {
	# https://github.com/dotcloud/docker-py/blob/master/requirements.txt
	depends=("python2-requests" "python2-six" "python2-websocket-client")
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

sha256sums=('8b9b369e183132ec8a42b40e45defe4166572dce599e4618b04e14bc7e8a36c8')
