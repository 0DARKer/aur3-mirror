# Maintainer: Carl George < arch at cgtx.us >

_name="docker-py"

pkgname=("python-${_name%-py}" "python2-${_name%-py}")
pkgver="1.1.0"
pkgrel="3"
pkgdesc="An API client for docker written in Python"
arch=("any")
url="https://github.com/docker/docker-py"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_name:0:1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('6a07eb56b234719e89d3038c24f9c870b6f1ee0b58080120e865e2752673cd94')

package_python-docker() {
    depends=("python-requests>=2.5.2" "python-six>=1.3.0")
    cd "${srcdir}/${_name}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-docker() {
    depends=("python2-requests>=2.5.2" "python2-six>=1.3.0" "python2-websocket-client>=0.11.0")
    cd "${srcdir}/${_name}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1
}
