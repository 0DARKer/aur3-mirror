# Maintainer: Carl George <carl@cgtx.us>

_module="docker-py"
pkgname=("python-${_module/-py/}" "python2-${_module/-py/}")
pkgver="0.3.2"
pkgrel="1"
pkgdesc="Python client for Docker"
arch=("any")
url="https://github.com/dotcloud/docker-py"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://github.com/dotcloud/${_module}/archive/${pkgver}.tar.gz")
sha256sums=('57617f6ce1f3ccbdc83a4f8cc668ceb3328135570e07ece3436e9cd65178f6d3')

package_python-docker() {
	# https://github.com/dotcloud/docker-py/blob/master/requirements3.txt
	depends=("python-requests" "python-six")
	cd ${srcdir}/${_module}-${pkgver}
	python setup.py clean --all
	python setup.py install --root=${pkgdir} --optimize=1
}

package_python2-docker() {
	# https://github.com/dotcloud/docker-py/blob/master/requirements.txt
	depends=("python2-requests" "python2-six" "python2-websocket-client")
	cd ${srcdir}/${_module}-${pkgver}
	python setup.py clean --all
	python2 setup.py install --root=${pkgdir} --optimize=1
}
