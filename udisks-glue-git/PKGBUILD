# Maintainer: Christophe-Marie Duquesne <chmd@chmd.fr>
# Former maintainer: Mikhail Borisov <borisov.mikhail@gmail.com>

pkgname=udisks-glue-git
pkgdesc="A tool to associate udisks events to user-defined actions"
url="http://github.com/fernandotcl/udisks-glue"
depends=('glib2' 'confuse' 'dbus-glib')

# This package requires either udisks _or_ udisks2 to run, but it does not
# require both of them. Therefore, they are listed as optional
# dependencies (requiring any of them would penalize the users of the
# other).
optdepends=('udisks' 'udisks2')

makedepends=('git' 'gcc' 'automake' 'autoconf' 'pkg-config' 'sed')
provides=('udisks-glue')
conflicts=('udisks-glue')
license=('BSD')
pkgver=release.1.3.4.1.gd7bafe2
pkgrel=2
arch=('i686' 'x86_64')
source=('git+http://github.com/fernandotcl/udisks-glue.git')
md5sums=('SKIP')
_gitname="udisks-glue"

pkgver() {
    cd "$_gitname"
    git describe --always | sed 's|-|.|g'
}

build() {
    cd "$_gitname"

    # https://github.com/fernandotcl/udisks-glue/issues/6
    sed -i 's/-Werror//g' src/Makefile.am

    ./autogen.sh
    ./configure --prefix=/usr
    make
}

package() {
    cd $_gitname
    make install DESTDIR=$pkgdir
    install -Dm 0644 LICENSE $pkgdir/usr/share/licenses/udisks-glue/LICENSE
}
