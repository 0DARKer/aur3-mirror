pkgname=mingw-w64-stormlib
pkgver=8.20
pkgrel=1
pkgdesc="A pack of modules, written in C++, which are able to read and also to write files from/to the MPQ archives. (mingw-w64)"
arch=(any)
url="http://www.zezula.net/en/mpq/stormlib.html"
license=("custom")
makedepends=(mingw-w64-gcc cmake unzip)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("http://www.zezula.net/download/stormlib.zip"
"Toolchain-i686-w64-mingw32.cmake"
"Toolchain-x86_64-w64-mingw32.cmake")
md5sums=('7e36f97b3b056c76f2064de8910a8eb4'
         'da10ad00a161800e0b011a77c3efbf76'
         'adea9ccdeea1784c7cd30af195f2a8cb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/StormLib"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir "build-${_arch}" && pushd "build-${_arch}"
    PATH="/usr/${_arch}/bin:$PATH" cmake \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr/${_arch} \
      -DCMAKE_INSTALL_LIBDIR:PATH=/usr/${_arch}/lib \
      -DINCLUDE_INSTALL_DIR:PATH=/usr/${_arch}/include \
      -DLIB_INSTALL_DIR:PATH=/usr/${_arch}/lib \
      -DSYSCONF_INSTALL_DIR:PATH=/usr/${_arch}/etc \
      -DSHARE_INSTALL_PREFIX:PATH=/usr/${_arch}/share \
      -DBUILD_SHARED_LIBS:BOOL=ON \
      -DCMAKE_TOOLCHAIN_FILE=${srcdir}/Toolchain-${_arch}.cmake \
      -DWITH_STATIC:BOOL=ON \
      ..
    make
    popd
  done
}

package() {

  for _arch in ${_architectures}; do
    cd "$srcdir/StormLib/build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -m644 libStormLib.a "$pkgdir/usr/$_arch/lib"
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}