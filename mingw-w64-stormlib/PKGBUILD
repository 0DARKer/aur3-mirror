pkgname=mingw-w64-stormlib
pkgver=8.21
pkgrel=3
pkgdesc="A pack of modules, written in C++, which are able to read and also to write files from/to the MPQ archives. (mingw-w64)"
arch=(any)
url="http://www.zezula.net/en/mpq/stormlib.html"
license=("custom")
makedepends=(mingw-w64-gcc mingw-w64-cmake)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("https://github.com/stormlib/StormLib/archive/v$pkgver.tar.gz")
md5sums=('07017ebc369840a237607e999cb91bdf')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/StormLib-$pkgver"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir "build-${_arch}" && pushd "build-${_arch}"
    ${_arch}-cmake \
      -DBUILD_SHARED_LIBS:BOOL=ON \
      -DWITH_STATIC:BOOL=ON \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/StormLib-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -m644 libStormLib.a "$pkgdir/usr/$_arch/lib"
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
