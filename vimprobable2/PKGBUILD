# Maintainer: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=vimprobable2
pkgver=1.2.1
pkgrel=1
pkgdesc='Web browser that behaves like Vimperator'
arch=('i686' 'x86_64')
url='http://www.yllr.net/vimprobable/'
license=('MIT')
depends=('libwebkit' 'gtk2' 'libsoup')
source=(http://downloads.sourceforge.net/sourceforge/vimprobable/Releases/${pkgname}_${pkgver}.tar.bz2)
md5sums=('7eb643f2f5404cc5f611c7c3075b25da')

build() {
  cd $srcdir/$pkgname

  [ -e $srcdir/config.h ] && cp $srcdir/config.h .
  [ -e $srcdir/keymap.h ] && cp $srcdir/keymap.h .

  make
}

package() {
  cd $srcdir/$pkgname
  make PREFIX=/usr MANDIR=/usr/share/man DESTDIR=$pkgdir install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
