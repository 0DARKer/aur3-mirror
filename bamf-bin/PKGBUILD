# Contributor: Dan Serban
# Contributor: Ner0

pkgname=bamf-bin
pkgver=0.3.0
pkgrel=1
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library (Ubuntu prebuilt)"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL3')
provides=("bamf=$pkgver")
conflicts=('bamf' 'bamf-bzr' 'libbamf-bzr')

if [[ "$CARCH" == 'i686' ]]; then
  _arch=i386
  _soarch=i386
  md5sums=('600b0482d17ee631c4c4f32a3832577d' '69a5a448d605e2f4ea5a88c2402f176e' '1d8d0cbbe5ecae57af722839e3babe19')
elif [[ "$CARCH" == 'x86_64' ]]; then
  _arch=amd64
  _soarch=x86_64
  md5sums=('e003c238dd2067714330e4b1421571c5' '7b3979cff46f4d54d7d107d754b3620b' 'ff4c8a1bc9e51cba238486adbf77d117')
fi

source=("http://archive.ubuntu.com/ubuntu/pool/main/b/bamf/bamfdaemon_$pkgver-0ubuntu1_$_arch.deb"
	"http://archive.ubuntu.com/ubuntu/pool/main/b/bamf/libbamf3-0_$pkgver-0ubuntu1_$_arch.deb"
	"http://archive.ubuntu.com/ubuntu/pool/main/b/bamf/libbamf3-dev_$pkgver-0ubuntu1_$_arch.deb")
noextract=(${source[@]##*/})

package() {
  for i in *.deb; do
    bsdtar -xf "$i" data.tar.gz
    tar -xf data.tar.gz -C "$pkgdir/"
    rm -f data.tar.gz
  done

  mv "$pkgdir/usr/lib/$_soarch-linux-gnu"/* "$pkgdir/usr/lib/"
  rm -rf "$pkgdir/usr/lib/$_soarch-linux-gnu"

  sed -i "s/\/lib\/$_soarch-linux-gnu/\/lib/" "$pkgdir/usr/lib/pkgconfig/libbamf3.pc"
}
