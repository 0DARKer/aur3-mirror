# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Jekyll Wu <adaptee at gmail dot com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Rick W. Chen <stuffcorpse@archlinux.us>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Marek Kubica <marek@xivilization.net>

pkgname=phonon-mplayer-git
pkgver=20120714.7217499
pkgrel=1
pkgdesc="Mplayer backend for phonon, the multimedia API of KDE."
arch=("i686" "x86_64")
url="http://projects.kde.org/projects/kdesupport/phonon/phonon-mplayer"
license=("LGPL")
depends=("phonon" "mplayer")
makedepends=("cmake" "automoc4" "git")
provides=("phonon-mplayer" "phonon-backend")
conflicts=("phonon-mplayer")
source=('git://anongit.kde.org/phonon-mplayer.git'
        'fix_find_config.patch'
        'CMakeLists.txt.patch')
md5sums=('SKIP'
         '55a20d7146371fbf1f1139a2ed874604'
         'ab4c216d2b66e6763dd90a7160fbaf65')
_gitname="phonon-mplayer"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  [ ${CARCH} = 'x86_64' ] && CXXFLAGS+=" -fPIC"

  cd "${_gitname}"
  patch -Np1 -i "${srcdir}/fix_find_config.patch"
  patch -Np0 -i "${srcdir}/CMakeLists.txt.patch"
}

build() {
  cd "${_gitname}"
  # Build
  msg "Starting make..."
  _prefix="$(kde4-config --prefix)"
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX="${_prefix}" \
          -DPLUGIN_INSTALL_DIR="${_prefix}/lib/kde4" \
          -DSERVICES_INSTALL_DIR="${_prefix}/share/kde4/services"
  make
}


package () {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
