# Maintainer: Sara <sfauzia at comcast dot net>

pkgname=fbpad-git
pkgver=20111120
pkgrel=1
pkgdesc="a small linux framebuffer virtual terminal, with 256 colors"
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/fbpad.git"
license=('GPL')
optdepends=('ft2tf: ttf to tiny font converter')
makedepends=('git' 'ncurses')
provides=('fbpad')
conflicts=('fbpad')
source=('config.h'
        'fbpad-256.info')
install=${pkgname}.install
md5sums=('8ac6c298c096b89d1c88a2e2fa4ae4af'
         '18cab078300b77757aa7013d8f281dcd')

_gitroot=git://repo.or.cz/fbpad.git
_gitname=fbpad

build(){
  cd "${srcdir}"
  msg "Connecting to GIT server...."
    
  if [[ -d ${_gitname} ]]; then
    cd "${_gitname}"
    git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" -b bold
    cd "${_gitname}"
  fi
  msg "Checkout done or server timeout"
  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  cp ${srcdir}/config.h config.h

  msg "Starting make..."
  make
}

package(){
  install -Dm755 "${srcdir}/${_gitname}-build/${_gitname}" "${pkgdir}/usr/bin/${_gitname}"
  install -Dm644 "${srcdir}/${_gitname}-build/README" "${pkgdir}/usr/share/doc/${pkgname}/README"
  # Compile terminfo
  install -dm755 "$pkgdir/usr/share/terminfo"
  tic -x -o "${pkgdir}/usr/share/terminfo" "${_gitname}-256.info" 
}
 
# vim:set ts=2 sw=2 et:
