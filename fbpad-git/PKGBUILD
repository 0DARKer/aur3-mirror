# Contributor: Sara <sara at archlinux dot us>
# Maintainer: aksr <aksr at t-com dot me>
pkgname=fbpad-git
pkgver=0.r249.b8dfb44
pkgrel=2
pkgdesc="A small linux framebuffer virtual terminal."
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/fbpad.git"
#url="http://litcave.rudi.ir/"
license=('custom:BSD')
groups=()
depends=('bash' 'vim' 'mutt')
makedepends=('git' 'ft2tf' 'ttf-dejavu')
optdepends=()
provides=('fbpad')
conflicts=('fbpad')
replaces=()
backup=()
options=()
install=${pkgname}.install
source=('config.h'
        'fbpad-256.info'
        'LICENSE')
noextract=()
md5sums=('cdc8e8c4582768dd90b954a143f805d2'
         '185b9d6ec1c539213226a3e2509c7ccd'
         'ec67f29a7dec10f86ef31515ed657a91')
sha1sums=('85cbf626883ec0febb714c7226e4de0685985f69'
          'dbb816fe37faf0acb4e1a916d7493787c2b647fc'
          '76a535243054e1fdd9caaa46a1571cd381d74353')

_gitroot=git://repo.or.cz/fbpad.git
_gitname=fbpad

pkgver() {
  cd "$srcdir/$_gitname"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # Custom config.h
  cp "${srcdir}"/config.h config.h
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  install -Dm755 "${srcdir}/${_gitname}-build/${_gitname}" "${pkgdir}/usr/bin/${_gitname}"
  install -Dm644 "${srcdir}/${_gitname}-build/README" "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Compile terminfo
  mkdir -p  "${pkgdir}/usr/share/terminfo"
  tic -x -o "${pkgdir}/usr/share/terminfo" "$srcdir/${_gitname}-256.info" 

  # Setup fonts
  ft2tf /usr/share/fonts/TTF/DejaVuSansMono.ttf:6         >${srcdir}/font.ttf
  ft2tf /usr/share/fonts/TTF/DejaVuSansMono-Oblique.ttf:6 >${srcdir}/fonti.ttf
  ft2tf /usr/share/fonts/TTF/DejaVuSansMono-Bold.ttf:6    >${srcdir}/fontb.ttf

  # Install fonts
  install -Dm644 "${srcdir}"/font.ttf  "${pkgdir}/usr/share/${pkgname}/font.ttf"
  install -Dm644 "${srcdir}"/fonti.ttf "${pkgdir}/usr/share/${pkgname}/fonti.ttf"
  install -Dm644 "${srcdir}"/fontb.ttf "${pkgdir}/usr/share/${pkgname}/fontb.ttf"
}

# vim:set ts=2 sw=2 et:
