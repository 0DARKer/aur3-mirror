# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=translate-shell-git
pkgver=v0.8.23.2.g2b952ef
pkgrel=1
pkgdesc='A command-line interface and interactive shell for Google Translate.'
arch=('any')
url="http://www.soimort.org/translate-shell/"
license=('Public Domain')
depends=('gawk>=4.0')
makedepends=('git')
optdepends=(
    'fribidi: needed for displaying right-to-left (RTL) languages'
    'mplayer: needed for the Text-to-Speech functionality'
    'mpv: needed for the Text-to-Speech funcionality'
    'espeak: needed for the Text-to-Speech functionality'
    'rlwrap: needed for readline-style editing and history in the interactive mode'
    'emacs: for using the Emacs interface (with the -E flag)'
)
provides=('translate-shell')
conflicts=('translate-shell')
source=('git://github.com/soimort/translate-shell.git')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/translate-shell"
    git describe --always | sed 's|-|.|g'
}

build() {
    cd "$srcdir/translate-shell"
    make
}

package() {
    mkdir -p "$pkgdir/usr/bin/"
    cd "$srcdir/translate-shell"

    #make "INSTDIR=$pkgdir/usr/bin" install  # Makefile is buggy

    # Main executable
    install -D build/trans "$pkgdir/usr/bin/trans"
    ln -s /usr/bin/trans "$pkgdir/usr/bin/translate-shell"

    # Man page
    install -Dm644 man/trans.1 "$pkgdir/usr/share/man/man1/trans.1"
    install -Dm644 man/trans.1 "$pkgdir/usr/share/man/man1/translate-shell.1"

    # License
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
