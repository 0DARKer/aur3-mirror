# Maintainer : SpepS <dreamspepser at yahoo it>
# Contributor: Eric Forgeot < http://ifiction.free.fr >

pkgname=wxlua
pkgver=2.8.10.0
pkgrel=2
pkgdesc="A set of bindings to the wxWidgets library for the Lua programming language."
arch=('i686' 'x86_64')
url="http://wxlua.sourceforge.net"
license=('custom:wxWindows')
depends=('lua' 'desktop-file-utils' 'wxgtk')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/wxLua-$pkgver-src.tar.gz"
        "gcc4.6.patch")
md5sums=('7e0702c1f148a89c2ff6c1eed6a8e2fd'
         'b3a16e8657621fb7271d8e0de71af88c')

build() {

  cd "$srcdir/wxLua"

  # Let configure find system lua
  sed -i "s_\(lua\)5.1_\1_g;s_\(include\)/lua_\1_" \
	configure \
	build/autoconf/configure.ac \
	apps/Makefile.in \
	modules/Makefile.in

  # Prevent installing lua headers
  sed -i "1782d" modules/Makefile.in

  # let wxbind sources be installed
  # in a more suitable path
  sed -i "s|src/wxbind|include/wxbind|g" \
      modules/{Makefile.in,build/bakefiles/modules.bkl}

  # gcc 4.6 patch
  patch -p2 -i ../gcc4.6.patch

  ./configure \
	--prefix=/usr \
	--with-lua-prefix=/usr/lib/lua/5.1/ \
	--enable-systemlua

  make
}

package() {

  cd "$srcdir/wxLua"

  make DESTDIR="$pkgdir/" install

  # Install custom license file
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 docs/licence.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
