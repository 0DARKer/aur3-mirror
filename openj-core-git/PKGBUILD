pkgname=openj-core-git
pkgver=20120213
pkgrel=1
pkgdesc="Modern, high-level, general-purpose, high-performance programming language (core)"
arch=(i686 x86_64)
url="http://www.jsoftware.com/"
license=('GPL3')
depends=('readline')
makedepends=('git' 'cmake')
provides=('openj-core')

_gitroot=https://github.com/openj/core.git
_gitname=openj 

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  cd "$srcdir/$_gitname"
  rm -rf build
  mkdir build
  cd build

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/$_gitname/build"
  make DESTDIR="$pkgdir/" install

  # avoid an unfortunate conflict with OpenJDK
  mv "$pkgdir"/usr/bin/j{console,}
}

# vim:set ts=2 sw=2 et:
