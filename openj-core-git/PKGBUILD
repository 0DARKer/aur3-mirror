pkgname=openj-core-git
pkgver=20130609
pkgrel=1
pkgdesc="Modern, high-level, general-purpose, high-performance programming language (core)"
arch=(i686 x86_64)
url="http://www.jsoftware.com/"
license=('GPL3')
depends=('readline')
makedepends=('git' 'cmake')
provides=('openj-core')
source=('j::git+https://github.com/openj/core.git')
sha512sums=('SKIP')

pkgver() {
  date +%Y%m%d
}

build() {
  mkdir j/build; cd j/build
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd j/build

  make DESTDIR="$pkgdir/" install

  # avoid an unfortunate conflict with OpenJDK
  mv "$pkgdir"/usr/bin/j{console,}
}

# vim:set ts=2 sw=2 et:
