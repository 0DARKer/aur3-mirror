# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: BallÃ³ GyÃ¶rgy <ballogyor+arch at gmail dot com>

pkgname=indicator-appmenu-bzr
_bzrname=indicator-appmenu
pkgver=258
pkgrel=3
pkgdesc="Indicator to host the menus from an application"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-appmenu"
license=('GPL')
depends=('libindicator-bzr' 'libdbusmenu-gtk3' 'bamf' 'dconf')
makedepends=('bzr' 'intltool')
options=('!libtool')
provides=('indicator-appmenu')
conflicts=('indicator-appmenu')
install=$pkgname.install
source=("$_bzrname::bzr+http://bazaar.launchpad.net/~indicator-applet-developers/$_bzrname/trunk.14.04/"
        '0001_autoconf.patch')
md5sums=('SKIP'
         '117e556a82bb7aadab11d94d2331767b')

pkgver() {
  cd $_bzrname
  bzr revno
}

prepare() {
  cd "$srcdir"/$_bzrname

  patch -p1 -i "${srcdir}/0001_autoconf.patch"
}

build() {
  cd "$srcdir"/$_bzrname

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$_bzrname \
              --disable-static
  make
}

package() {
  cd "$srcdir"/$_bzrname

  make DESTDIR="$pkgdir/" install
}
