# Mantainer: 3ED <krzysztof1987@gmail.com>

pkgname=mixramp
pkgver=1.3
pkgrel=1
pkgdesc="A utility to analyse sound files and print out the volume profile of the start and end. The idea is to embed those profiles in metadata so players can do intelligent mixing between tracks. Supports FLAC and MP3."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mixramp/"
license=('GPL')
depends=('audiofile')
optdepends=('python2-eyed3: write mp3 tags'
            'mpg123: read mp3 tags'
            'flac: read and write flac tags'
            'mpd: player which support mixramp tags')
source=("${pkgname}-${pkgver}.tar.gz::http://sourceforge.net/projects/${pkgname}/files/${pkgname}_${pkgver}.tgz/download"
        "01-without-werror.patch")
sha256sums=('6d01a0ce0907a7fd225840a21d39144a113b551d176439964bdb5351aecf724c'
            'd1e4bcfe011aa996f4029e6c280f9c441d2ac8f99eb63b37829bc86dc566ddd5')

prepare() {
  cd "$srcdir/$pkgname"
  patch -p0 -i "$srcdir"/01-without-werror.patch
}
build() {
  cd "$srcdir/$pkgname"
  make
}
package() {
  cd "$srcdir/$pkgname"
  install -dm755 "$pkgdir"/usr/bin/
  install -m755 mixramp mixramp-tag "$pkgdir"/usr/bin/
}
