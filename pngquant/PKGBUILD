# Maintainer: Hannes Graeuler <hgraeule @ uos dot de>
pkgname=pngquant
pkgver=1.8.3
pkgrel=2
pkgdesc="pngquant is a command-line utility to quantize PNGs down to 8-bit paletted PNGs"
arch=('i686' 'x86_64')
url="http://pngquant.org/"
license=('BSD')
depends=('libpng' 'zlib')
source=("http://pngquant.org/pngquant-1.8.3-src.tar.bz2")
md5sums=('d1015f115bf396c9eed41a17ed8e898c')

build() {
    cd "$srcdir/pngquant-$pkgver"

    # patch libpng 1.6 issue
    # https://github.com/pornel/pngquant/issues/31
    sed 's/\([ (]\)png_image/\1rwpng_png_image/' -i rwpng.c rwpng.h

    make
}

package() {
    cd "$srcdir/pngquant-$pkgver"
    make PREFIX="/usr" DESTDIR="$pkgdir/" install
}

