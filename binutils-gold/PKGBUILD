# Maintainer: Humbert Julien <julroy67 [AT] gmail.com>
# Original PKGBUILD: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=binutils-gold
pkgver=2.20.1
pkgrel=4
_date=20100318
_pkgrealname=binutils
pkgdesc="A set of programs to assemble and manipulate binary and object files, GNU Gold enabled"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base')
depends=('glibc>=2.11.1-3' 'zlib')
options=('!libtool' '!distcc' '!ccache')
conflicts=('binutils')
provides=('binutils=2.20.1')
install=binutils.install
source=(ftp://ftp.archlinux.org/other/${_pkgrealname}/${_pkgrealname}-${pkgver}_${_date}.tar.bz2)
md5sums=('c153213a20a5075bd98d7f12fbd38a51')

mksource() {
  mkdir ${_pkgrealname}-${_date}
  cd ${_pkgrealname}-${_date}
  export _TAG=binutils-2_20-branch
  export 'CVSROOT=:pserver:anoncvs@sourceware.org:/cvs/src'
  cvs -z9 co -r $_TAG binutils || return 1
  cd src && tar -cvjf ../../binutils-${pkgver}_${_date}.tar.bz2 *
}

build() {
  unset CFLAGS CXXFLAGS
  export CFLAGS="-march=native -O2 -pipe"
  export CXXFLAGS="-march=native -O2 -pipe"
  cd ${srcdir}
  mkdir build && cd build

  CC="gcc -L`pwd`/bfd/.libs/"
  if [ "${CARCH}" = "x86_64" ]; then
    ../configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info --enable-shared --enable-64-bit-bfd --disable-multilib --enable-gold
  else
    ../configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info --enable-shared --enable-gold
  fi
  # fix man files install path
  sed -i -e "s:mandir \= \/usr\/share:mandir \= ${pkgdir}\/usr\/share:" ${srcdir}/build/Makefile || return 1
  # fix info files install path
  sed -i -e "s:infodir \= \/usr\/share:infodir \= ${pkgdir}\/usr\/share:" ${srcdir}/build/Makefile || return 1

  # This checks the host environment and makes sure all the necessary tools are available to compile Binutils.
  make configure-host || return 1

  make tooldir=${pkgdir}/usr || return 1
}

package() {
  cd ${srcdir}/build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install
  install -m644 ${srcdir}/include/libiberty.h ${pkgdir}/usr/include

  # Rebuild libiberty.a with -fPIC
  make -C libiberty clean
  make CFLAGS="$CFLAGS -fPIC" -C libiberty
  install -m644 libiberty/libiberty.a ${pkgdir}/usr/lib

  # Rebuild libbfd.a with -fPIC
  make -C bfd clean
  # hidden visability prevent 3rd party shared libraries exporting bfd non-stable API
  make CFLAGS="$CFLAGS -fPIC -fvisibility=hidden" -C bfd
  install -m644 bfd/libbfd.a ${pkgdir}/usr/lib

  rm -f ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # Remove these symlinks, with binutils prereleases they are not ABI stable.
  # Programs should compile static to the .a file.
  rm -f ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
  echo "INPUT ( /usr/lib/libbfd.a -liberty -lz )" >${pkgdir}/usr/lib/libbfd.so
  echo "INPUT ( /usr/lib/libopcodes.a -lbfd )" >${pkgdir}/usr/lib/libopcodes.so
}
