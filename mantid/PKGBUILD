# Maintainer: Harry Jeffery <harry|@|exec64|.|co|.|uk>

pkgname=mantid
pkgver=3.3.0
pkgrel=1
pkgdesc="Data analysis toolkit for neutron based instrument data"
url="http://www.mantidproject.org/"
arch=('x86_64' 'i686')
license=('GPL')
depends=(
  'libnexus'
  'qwt5'
  'qwtplot3d'
  'qscintilla'
  'boost'
  'poco'
  'muparser'
  'qtwebkit'
  'jsoncpp'
  'python2'
  'python2-numpy'
  'python2-sip'
  'python2-pyqt4'
  )
makedepends=('cmake')
source=("$pkgname::git://github.com/mantidproject/mantid.git#tag=v${pkgver}")
sha1sums=('SKIP')

build() {
  sed -i "s|jsoncpp/json/json.h|json/reader.h|" "${srcdir}/mantid/Code/Mantid/Build/CMake/FindJsonCPP.cmake"
  sed -i "s|<jsoncpp/|<|" "${srcdir}/mantid/Code/Mantid/Framework/DataHandling/src/DownloadInstrument.cpp"
  sed -i "s/QWT_LIBRARY NAMES/& qwt5 ) #/" "${srcdir}/mantid/Code/Mantid/Build/CMake/FindQwt.cmake"
  sed -i 's/HDF5_LIBRARIES/& "hdf5;hdf5_hl" ) #/' "${srcdir}/mantid/Code/Mantid/Build/CMake/hdf5-config.cmake"
  mkdir -p "${srcdir}/build"
  cd "${srcdir}/build"
  cmake -DPYTHON_EXECUTABLE=/usr/bin/python2 -DPYQT4_SIP_DIR=/usr/share/sip/PyQt4 -DNO_OPENCASCADE=1 "${srcdir}/mantid/Code/Mantid"
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
