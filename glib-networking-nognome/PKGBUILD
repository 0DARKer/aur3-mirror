# Maintainer: anonymous-arch <anonymous-arch@slopsbox.com>
pkgname=glib-networking-nognome
pkgver=2.28.7
pkgrel=3
pkgdesc="Network-related giomodules for glib without GNOME/gsetting dependencies"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates')
makedepends=('intltool')
provides=('glib-networking')
conflicts=('glib-networking')
options=('!libtool')
install=glib-networking.install
source=(http://download.gnome.org/sources/glib-networking/2.28/glib-networking-${pkgver}.tar.bz2)
sha256sums=('98bedfbd530c4b1b53c91025fe82290bafd289d249e4eb549c3b90d23a76021c')

build() {
  cd "$srcdir/glib-networking-$pkgver"
  ./configure \
    --prefix=/usr  --sysconfdir=/etc \
    --libexecdir=/usr/lib/glib-networking --disable-static \
    --without-gnome-proxy
  make
}

package() {
  cd "$srcdir/glib-networking-$pkgver"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
