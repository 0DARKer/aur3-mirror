# $Id: PKGBUILD 159094 2012-05-15 14:33:52Z ibiru $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=glib-networking-nognome
pkgver=2.32.3
pkgrel=1
pkgdesc="Network-related giomodules for glib without gnome proxy setting support"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'libgcrypt' 'ca-certificates')
makedepends=('intltool')
provides=('glib-networking')
conflicts=('glib-networking')
options=('!libtool')
install=glib-networking.install
source=(http://download.gnome.org/sources/${pkgname%-nognome}/${pkgver%.*}/${pkgname%-nognome}-$pkgver.tar.xz)
sha256sums=('39fe23e86a57bb7a8a67c65668394ad0fbe2d43960c1f9d68311d5d13ef1e5cf')

build() {
  cd "${pkgname%-nognome}-$pkgver"
  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/glib-networking --disable-static --without-gnome-proxy
  make
}

package() {
  cd "${pkgname%-nognome}-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
