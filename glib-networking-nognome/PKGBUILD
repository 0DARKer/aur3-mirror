# Maintainer: anonymous-arch <anonymous-arch@slopsbox.com>
pkgname=glib-networking-nognome
pkgver=2.30.0
pkgrel=1
pkgdesc="Network-related giomodules for glib"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'libgcrypt' 'ca-certificates')
makedepends=('intltool')
provides=('glib-networking')
conflicts=('glib-networking')
options=('!libtool')
install=glib-networking.install
source=(http://download.gnome.org/sources/${pkgname%-nognome}/${pkgver%.*}/${pkgname%-nognome}-${pkgver}.tar.xz)
sha256sums=('e28ca1e354d6519efb5c9c01c1f10e8a1a196710dfbcc5ea8f8aade3244e108c')

build() {
  cd "$srcdir/${pkgname%-nognome}-$pkgver"
  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/glib-networking --disable-static \
    --without-gnome-proxy
  make
}

package() {
  cd "$srcdir/${pkgname%-nognome}-$pkgver"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
