# $Id: PKGBUILD 146052 2012-01-04 17:56:55Z ibiru $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=glib-networking-nognome
pkgver=2.30.2
pkgrel=1
pkgdesc="Network-related giomodules for glib without gnome proxy setting support"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libproxy' 'gnutls' 'ca-certificates')
makedepends=('intltool')
provides=('glib-networking')
conflicts=('glib-networking')
options=('!libtool')
install=glib-networking.install
source=(http://download.gnome.org/sources/${pkgname%-nognome}/${pkgver%.*}/${pkgname%-nognome}-$pkgver.tar.xz)
sha256sums=('9f7542500939ca25896f0a74955cd3889758524583f6a15d02e00c9b815559bc')

build() {
  cd "$srcdir/${pkgname%-nognome}-$pkgver"
  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/glib-networking --disable-static \
    --without-gnome-proxy
  make
}

package() {
  cd "$srcdir/${pkgname%-nognome}-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
