# $Id: PKGBUILD 210447 2014-04-14 20:06:20Z jgc $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=glib-networking-nognome
pkgver=2.40.1
pkgrel=1
pkgdesc="Network-related giomodules for glib without gnome proxy setting support"
arch=(i686 x86_64)
url="http://www.gtk.org/"
license=(GPL2)
depends=(glib2 libproxy gnutls ca-certificates)
makedepends=(intltool)
provides=(glib-networking)
conflicts=(glib-networking)
install=glib-networking.install
source=(http://download.gnome.org/sources/${pkgname%-nognome}/${pkgver:0:4}/${pkgname%-nognome}-$pkgver.tar.xz)
sha256sums=('9fb3e54d049a480afdb814ff7452e7ab67e5d5f607ade230d7713f19922b5a28')

build() {
  cd ${pkgname%-nognome}-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/${pkgname%-nognome} --disable-static --without-gnome-proxy \
    --disable-installed-tests
  make
}

check() {
  cd ${pkgname%-nognome}-$pkgver
  make -k check
}

package() {
  cd ${pkgname%-nognome}-$pkgver
  make DESTDIR="$pkgdir" install testfiles_DATA=
}

# vim:set ts=2 sw=2 et:
