# $Id: PKGBUILD 182485 2013-04-10 19:12:19Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=glib-networking-nognome
pkgver=2.36.0
pkgrel=1
pkgdesc="Network-related giomodules for glib without gnome proxy setting support"
arch=(i686 x86_64)
url="http://www.gtk.org/"
license=(GPL2)
depends=(glib2 libproxy gnutls ca-certificates)
makedepends=(intltool)
provides=(glib-networking)
conflicts=(glib-networking)
options=('!libtool')
install=glib-networking.install
source=(http://download.gnome.org/sources/${pkgname%-nognome}/${pkgver%.*}/${pkgname%-nognome}-$pkgver.tar.xz)
sha256sums=('190d66fbaeb023ba4f43c315f23c5372c43be6cbe857596e00990211514650d9')

build() {
  cd ${pkgname%-nognome}-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/${pkgname%-nognome} --disable-static --without-gnome-proxy
  make
}

package() {
  cd ${pkgname%-nognome}-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
