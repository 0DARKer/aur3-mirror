pkgname=krb5-appl-svn
pkgver=3313
pkgrel=1
pkgdesc="Obsolete client applications from the MIT Kerberos suite"
arch=('i686' 'x86_64')
url="http://web.mit.edu/kerberos/"
license=('custom')
depends=('e2fsprogs')
makedepends=('perl' 'subversion')
provides=('krb5-appl')
conflicts=('krb5-appl')

_svntrunk='svn://anonsvn.mit.edu/krb5-appl/trunk'
_svnmod='krb5-appl'

build() {
  cd "$srcdir"
  msg "Connecting to SVN server..."

  if [[ -d "$_svnmod/.svn" ]]; then
     (cd "$_svnmod" && svn up -r "$pkgver")
  else
     #svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
     svn co "$_svntrunk" -r "$pkgver" "$_svnmod"
  fi

  msg "Starting build..."

  rm -rf "$_svnmod-build"
  cp -r "$_svnmod" "$_svnmod-build"
  cd "$_svnmod-build"

  export CFLAGS+=" -fPIC -fno-strict-aliasing -fstack-protector-all"
  export CPPFLAGS+=" -I/usr/include/et"
  ./autogen.sh
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --localstatedir=/var/lib \
              --program-prefix=k \
              ;
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir/$_svnmod/NOTICE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2:sw=2:et
