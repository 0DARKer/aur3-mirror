# Maintainer: Josh Kropf <josh@slashdev.ca>
# Contributor: Bartosz Chmura <chmurli at gmail dot com>
# Contributor: Mikkel Kroman <mk at maero dot dk>

pkgname=sublime-text
pkgver=2.2181
pkgrel=1
_build=2181
pkgdesc="sophisticated text editor for code, html and prose"
arch=('i686' 'x86_64')
url="http://www.sublimetext.com/2"
license=('custom')
depends=(libpng12 gtk2)
install=${pkgname}.install
_arch=' x64'
_archurl='%20x64'

[[ "${CARCH}" = i686 ]] && _arch='' && _archurl=''

source=(
	"http://c758482.r82.cf2.rackcdn.com/Sublime%20Text%202%20Build%20${_build}${_archurl}.tar.bz2"
	sublime-text.desktop
	subl
)

md5sums=(
	'f8f662886cd476adc8d66fbaefa7216e'
	'1466b8f9aabc9d607dbb0ebff8171907'
	'268580e79e52eb32bf59dd64241c5fd9'
)

[[ "${CARCH}" = i686 ]] && md5sums[0]='490a93e5fef9ffd1404915ba5ca1e9ed'

package () {
	cd ${srcdir}

	install -Dm644 ${pkgname}.desktop \
		${pkgdir}/usr/share/applications/${pkgname}.desktop

	install -dm755 ${pkgdir}/opt
	cp --preserve=mode -r "Sublime Text 2" \
		${pkgdir}/opt/${pkgname}

	for res in 128x128 16x16 256x256 32x32 48x48; do
		install -dm755 ${pkgdir}/usr/share/icons/hicolor/${res}/apps
		ln -s /opt/${pkgname}/Icon/${res}/sublime_text.png \
			${pkgdir}/usr/share/icons/hicolor/${res}/apps/sublime_text.png
	done

	install -Dm755 subl ${pkgdir}/usr/bin/subl
}
