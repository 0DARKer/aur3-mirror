# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=timeline
pkgver=0.15.0
pkgrel=1
pkgdesc="A cross-platform application for displaying and navigating events on a timeline."
arch=(any)
url="http://thetimelineproj.sourceforge.net/"
license=('GPL')
depends=('wxpython')
makedepends=('scons' 'python-markdown')
optdepends=('python2-pysvg: export in svg'
            'python-markdown: view help pages'
            'python2-icalendar: ics support')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/thetimelineproj/thetimelineproj/$pkgver/$pkgname-$pkgver.zip"
        "$pkgname.desktop")
md5sums=('abdbc8fcaa4bce45723644df408dad71'
         'df26b7f1fbe00c8a1722118363d8136c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # path fix
  sed -i "s|\(_ROOT = \).*|\1\"/usr/share/$pkgname\"|" timelinelib/paths.py

  # python2 fix
  sed -i "s|\(env python\).*|\12|" $pkgname.py

  scons
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # prepare dirs
  install -d "$pkgdir"/usr/{bin,lib/python2.7/site-packages/,share/{applications,locale,pixmaps,$pkgname}}

  # data and libs
  cp -a timeline* "$pkgdir/usr/lib/python2.7/site-packages"
  cp -a po/*[^pot] "$pkgdir/usr/share/locale"
# cp -a man "$pkgdir/usr/share" : gone
  cp -a icons "$pkgdir/usr/share/$pkgname"
# help_resources : gone

  # bin
  cat << EOF >> "$pkgdir/usr/bin/$pkgname"
#!/bin/bash
cd /usr/lib/python2.7/site-packages && python2 timeline.py
EOF
  chmod +x "$pkgdir/usr/bin/$pkgname"

  # desktop file and icon
  install -Dm644 icons/48.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications"
}

# vim:set ts=2 sw=2 et:
