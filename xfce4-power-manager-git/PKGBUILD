# Mantainer: Black_Codec <orso.f.regna@gmail.com>
# Contributor: Silvio Knizek <killermoehre@gmx.net>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Allen Choong <allencch at hotmail dot com>

pkgname=xfce4-power-manager-git
pkgver=1788.315f603
pkgrel=1
pkgdesc="Power Manager Xfce desktop environment"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://git.xfce.org/xfce/xfce4-power-manager/"
groups=('xfce4-git')
depends=('xfce4-panel' 'upower' 'udisks' 'libnotify' 'hicolor-icon-theme')
optdepends=()
makedepends=('git' 'xfce4-dev-tools' 'autoconf')
provides=('xfce4-power-manager')
conflicts=('xfce4-power-manager')
source=(git://git.xfce.org/xfce/xfce4-power-manager/)
md5sums=('SKIP')

pkgver(){
  cd $srcdir/xfce4-power-manager
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $srcdir/xfce4-power-manager
  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-debug \
    --enable-dpms \
    --disable-network-manager \
    --enable-polkit

  make
}

package() {
  cd $srcdir/xfce4-power-manager
  make DESTDIR=${pkgdir} install
}
