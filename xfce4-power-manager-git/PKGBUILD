# Contributor: Silvio Knizek <killermoehre@gmx.net>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=xfce4-power-manager-git
pkgver=20121206
pkgrel=1
pkgdesc="Power Manager Xfce desktop environment"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://git.xfce.org/apps/xfce4-power-manager"
groups=('xfce4-git')
depends=('xfce4-panel-git' 'upower' 'udisks' 'libnotify' 'hicolor-icon-theme') #xfce4-panel
optdepends=()
makedepends=('git' 'xfce4-dev-tools-git') #xfce4-dev-tools
provides=('xfce4-power-manager')
conflicts=('xfce4-power-manager')
source=()
md5sums=()

_gitname=xfce4-power-manager
_gitroot=git://git.xfce.org/xfce/xfce4-power-manager/

build() {
	cd $srcdir/
	msg "Getting sources..."

	if [[ -d $_gitname ]] ; then
		cd $_gitname && git pull origin
		msg "The local files are updated." && cd ../
	else
		git clone $_gitroot $_gitname
	fi

	[ -d $_gitname-build ] && rm -r $_gitname-build
	cp $_gitname $_gitname-build -r
	cd $_gitname-build

	msg "Starting build..."

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-debug \
		--enable-dpms \
		--disable-network-manager \
		--enable-polkit

	make
}

package() {
	cd $srcdir/$_gitname-build
	make DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
