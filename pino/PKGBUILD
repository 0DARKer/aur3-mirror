# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=pino
pkgver=0.2.11
pkgrel=2
pkgdesc="Fast, easy and free Twitter client"
arch=('i686' 'x86_64')
url="http://pino-app.appspot.com/"
license=('LGPL3')
depends=('libgee' 'libnotify' 'libwebkit' 'libunique' 'gtkspell' 'librsvg' 'hicolor-icon-theme')
makedepends=('vala' 'python2' 'intltool')
install=pino.install
source=(http://pino-twitter.googlecode.com/files/pino-$pkgver.tar.bz2)
md5sums=('301c1443e5fab366b8e43b691a2db28f')

build() {
  cd $srcdir/pino-0.2.10

  # Make it build with Vala 0.10
  sed -i 's|name = "AccountAct";|GLib.Object (name: "AccountAct");|' src/account_action.vala
  sed -i 's|type = WindowType.POPUP;|GLib.Object (type: WindowType.POPUP);|' src/more_window.vala

  python2 waf configure --prefix=/usr
  python2 waf build
}

package() {
  cd $srcdir/pino-0.2.10

  python2 waf install --destdir=$pkgdir
}
