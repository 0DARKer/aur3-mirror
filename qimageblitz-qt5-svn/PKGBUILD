# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=qimageblitz-qt5-svn
pkgver=1403251
pkgrel=1
pkgdesc="An interm image effect library, Qt5 version. (SVN version)"
arch=('x86_64')
url="http://www.kde-org"
license=('custom')
depends=('qt5-base')
makedepends=('cmake' 'subversion')
conflicts=('qimageblitz')
provides=('qimageblitz')
replaces=('qimageblitz')
source=("svn://anonsvn.kde.org/home/kde/trunk/kdesupport/qimageblitz")
sha1sums=('SKIP')
_svnname=qimageblitz

pkgver() {
  cd "${_svnname}"
  echo "$(svnversion)"
}

build() {
  cd "${srcdir}"
  rm -fr build
  mkdir -p build
  
  cd build
  cmake "../${_svnname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT4_BUILD=OFF
  make
}

package() {
  cd "${srcdir}/build"
  
  make DESTDIR="${pkgdir}" install
  install -Dm644 "../${_svnname}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
