# Maintainer: Gilrain <pierre.buard+aur gmail com>
_pkgname=stratum-mining-proxy
pkgname=stratum-mining-proxy-git
pkgver=7b5c080
pkgrel=1
pkgdesc="Getwork-compatible proxy for Stratum mining pools."
url="http://mining.bitcoin.cz/stratum-mining/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('python2-stratum')
makedepends=('git')
source=(${_pkgname}::git://github.com/slush0/${_pkgname}.git
	conf
	service)
md5sums=('SKIP'
	 '78479e0043a327ad35000a2bd9f495b7'
	 '4d765e3345adda05339814e73650de92')
backup=('etc/conf.d/mining-proxy.conf')

pkgver() {
  cd ${srcdir}/${_pkgname}/
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd ${srcdir}/${_pkgname}
  sed -i -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' $(find ${srcdir} -name '*.py')
}

build() {
  cd ${srcdir}/${_pkgname}
  python2 setup.py build --build-base="${srcdir}/${_pkgname}"

  cd ${srcdir}/${_pkgname}/midstatec
  make
}

package() {
  cd ${srcdir}/${_pkgname}
  python2 setup.py install --root ${pkgdir}

  install -Dm644 midstatec/midstate.so ${pkgdir}/usr/lib/midstate.so
  install -Dm644 README.md ${pkgdir}/usr/share/doc/${_pkgname}/README.md

  install -Dm644 ${srcdir}/conf ${pkgdir}/etc/conf.d/mining-proxy.conf
  install -Dm644 ${srcdir}/service ${pkgdir}/usr/lib/systemd/system/mining-proxy.service
}
