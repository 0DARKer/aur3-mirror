# Maintainer: Zom <zom[at]eevul[dot]org>
# Contributor: (archdria) Adria Arrufat <swiftscythe (at) gmail.com>

pkgname=kdemultimedia-kmix-git
_realname=kmix
_gitname=${_realname}
pkgver=2013.04.26
pkgrel=1
pkgdesc="KDE volume control program"
arch=('i686' 'x86_64')
url="http://www.kde.org/applications/multimedia/kmix/"
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'git' 'docbook-xsl')
provides=("kdemultimedia-kmix")
conflicts=("kdemultimedia-kmix")
source=("$_gitname::git://anongit.kde.org/${_gitname}")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  git log -1 --format="%cd" --date=short | sed 's|-|.|g'
}

build() {
  rm -rf ${srcdir}/build
  git clone ${srcdir}/${_gitname} ${srcdir}/build
  cd ${srcdir}/build

  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
