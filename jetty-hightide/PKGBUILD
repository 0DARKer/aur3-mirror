# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

_timestamp=v20121106

pkgname=jetty-hightide
pkgver=8.1.8
pkgrel=3
pkgdesc="An open source web container. It comes pre-integrated with a number of services: JNDI, an XA transaction service, a JMS message fabric, and a JDBC accessible database."
arch=('any')
url="http://jetty.codehaus.org/jetty/"
license=('Apache' 'EPL')
depends=('java-environment' 'logrotate' 'systemd')
makedepends=('libarchive')
conflicts=('jetty')
backup=(etc/default/jetty)
source=(http://dist.codehaus.org/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.${_timestamp}.tar.gz
		jetty.default jetty.logrotate jetty.service setuid.patch shell.patch)
options=(!strip)
install=$pkgname.install
changelog=${pkgname}.ChangeLog.markdown

build() {
	_distribution=${pkgname}-${pkgver}.${_timestamp}

	cd "$srcdir"

	patch -p1 < setuid.patch
	patch -p1 < shell.patch

	rm -r ${_distribution}/bin/*cygwin*
}

package() {
	_distribution=${pkgname}-${pkgver}.${_timestamp}

	install -dm755 "$pkgdir"/opt

	cd "$srcdir"
	
	cp -a ${_distribution} "$pkgdir"/opt/jetty
	install -D -m644 jetty.default "$pkgdir"/etc/default/jetty
	install -D -m644 jetty.logrotate "$pkgdir"/etc/logrotate.d/jetty
	install -D -m644 jetty.service "$pkgdir"/usr/lib/systemd/system/jetty.service
}

md5sums=('98bb335af8460ee5e3b253d9af5ed3f0'
         '7e6c8973cc1e78ee15c38e618aa341fe'
         '15e8a100480756299e186ec3fc38b25a'
         '770ae57c1e5f4fec7140ad2f4cef0fba'
         '2e00cef61002e986cf5e7f88374dc3b5'
         '7efa3e577bf30e2827264e9e4ad7b315')
sha256sums=('a0884de04d8d0e1273673398285a83cd66c9e2daaa0ac94cd05938b69b881486'
            '8627432a972b9f90f4fbb0fa87726e999c2712e0d0c034913b135fabafb3d7b8'
            'a139babe3f22ff0ccfefbe13e9cd8bd89999ef32d1f519c080bb09be117d527f'
            '867691883c10ac05230109d2108a8ec0885dca484a5a1c729506e30edc361089'
            '4a81459addcf0c4f2d1829bf6e61b5445787d55c7a9d97f9239f6dde5f50bdd1'
            '7b159de5009c71eeedf5fa44240f98bd07eaaee5f89dda0fd3d9b3267cea0800')
