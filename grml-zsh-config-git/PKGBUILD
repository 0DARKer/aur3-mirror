# Maintainer: Kevin Dodd <Jesin00@gmail.com>

pkgname=grml-zsh-config-git
pkgver=20121221
pkgrel=1
pkgdesc="grml's zsh setup - from git repositories"
arch=('any')
url='http://grml.org/zsh/'
license=('GPL2')
depends=('zsh' 'coreutils' 'inetutils' 'grep' 'sed' 'procps')
makedepends=('git' 'txt2tags')
conflicts=('grml-zsh-config')
provides=('grml-zsh-config' 'grmlzshrc')

_gitroot='https://github.com/grml/grml-etc-core.git'
_gitname='grml-etc-core'

build() {
	msg "Connecting to GIT server...."

	if [ -d "$_gitname" ] ; then
		if pushd "$_gitname"; then
			git pull origin
			popd
		fi
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	cd "${srcdir}/$_gitname/doc"
	make
}

package() {
	cd "${srcdir}/$_gitname"
	install -D -m644 etc/skel/.zshrc ${pkgdir}/etc/skel/.zshrc
	install -D -m644 etc/zsh/keephack ${pkgdir}/etc/zsh/keephack
	install -D -m644 etc/zsh/zshrc ${pkgdir}/etc/zsh/zshrc

	install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grmlzshrc.5
	ln -sf grmlzshrc.5.gz ${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz
}
