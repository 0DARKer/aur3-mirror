# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>

pkgname=virt-what
pkgver=1.14
pkgrel=2
license=('GPL')
arch=('i686' 'x86_64')
depends=('dmidecode')
pkgdesc="A small program that prints out a list of 'facts' about a virtual machine"
url="http://people.redhat.com/~rjones/virt-what/"
source=("http://people.redhat.com/~rjones/virt-what/files/${pkgname}-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure
	sed -i '/^sbindir/c sbindir = /usr/bin' Makefile
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make libexecdir=/usr/bin prefix=/usr DESTDIR=${pkgdir} install
}

sha256sums=('aaea74b406cf3fd0c3c0f626bc604de0e8efb8de8967206f2638c041686b5de5'
            'SKIP')
