# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>

pkgname=virt-what
pkgver=1.13
pkgrel=1
license=('GPL')
arch=('i686' 'x86_64')
depends=('dmidecode')
pkgdesc="A small program that prints out a list of 'facts' about a virtual machine"
url="http://people.redhat.com/~rjones/virt-what/"
source=("http://people.redhat.com/~rjones/virt-what/files/${pkgname}-${pkgver}.tar.gz"{,.sig})

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure
	sed -i '/^sbindir/c sbindir = /usr/bin' Makefile
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make libexecdir=/usr/bin prefix=/usr DESTDIR=${pkgdir} install
}

sha256sums=('2b758de0a151593a0393c691755ba798d98d81e2f1c66bd9cc9bfa8d58fc28a8'
            'SKIP')
