pkgname=ldraw-parts-library
pkgver=20120106
pkgrel=1
pkgdesc="A collection of LDraw-format CAD files representing many of LEGO bricks produced"
arch=(i686 x86_64)
url="http://www.ldraw.org/Article13.html"
license="CCPL: cc-by-2.0"
options=(!strip)
source=("http://www.ldraw.org/library/updates/complete.zip"
	"http://www.ldraw.org/library/official/LDConfig.ldr"
         "ldraw-parts-library.sh" "license")

package() {
  cd "${srcdir}/ldraw/"

  # Install data
  mkdir -p           "${pkgdir}/usr/share/ldraw"
  mv ../LDConfig.ldr "${pkgdir}/usr/share/ldraw/"
  mv p               "${pkgdir}/usr/share/ldraw/"
  mv parts           "${pkgdir}/usr/share/ldraw/"

  # Fix permissions
  find "${pkgdir}/usr/share/ldraw" -type f -exec chmod 644 {} +
  find "${pkgdir}/usr/share/ldraw" -type d -exec chmod 755 {} +

  # Install the old version 2.0 CC-by license
  install -D -m644 ../license                "${pkgdir}/usr/share/licenses/${pkgname}/license"

  install -D -m755 ../ldraw-parts-library.sh "${pkgdir}/etc/profile.d/ldraw-parts-library.sh"
}
md5sums=('be524ad78dbd3a242190c5605a0c8f9e'
         '801fe16ff6f00c02c473175b45e419c9'
         '2acda6add7ed39994a710bd70aa96fc1'
         '8fca376070b84bea4d4c42c736a378de')
