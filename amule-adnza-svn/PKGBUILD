# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: erm67

pkgname=amule-adnza-svn
pkgver=272
pkgrel=2
pkgdesc="aMule SVN patched for Fastweb Network (Italy)"
url="http://www.adunanza.net/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('wxgtk' 'gd' 'geoip' 'libupnp' 'crypto++')
makedepends=('subversion' 'cvs' 'perl')
conflicts=('amule' 'amule-adnza')
provides=('amule')
source=('wxfont.patch')
md5sums=('92556fd14c4b4180791e942140ec1d5a')

_svntrunk=https://amule-adunanza.svn.sourceforge.net/svnroot/amule-adunanza/trunk
_svnmod=amule-adunanza

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build
  
  patch -Np0 -i ${srcdir}/wxfont.patch

  ./autogen.sh
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-cas \
    --enable-wxcas \
    --enable-amule-daemon \
    --enable-amulecmd \
    --enable-amule-gui \
    --enable-alc \
    --enable-alcc \
    --enable-webserver \
    --disable-debug \
    --enable-optimize \
    --enable-ccache \
    --enable-geoip \
    --enable-upnp
  make
}

package() {
  cd ${srcdir}/$_svnmod-build
  make DESTDIR="${pkgdir}" install
}
