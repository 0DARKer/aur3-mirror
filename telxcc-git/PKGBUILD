# Maintainer: kfgz <kfgz at interia dot pl>
# Contributor: Gustavo Alvarez <sl1pkn07 at gmail dot com>
# Contributor: Konstantin Gribov <grossws at gmail dot com>

pkgname=telxcc-git
pkgrel=1
pkgver=v2.5.3.2.g25aa912
pkgdesc="TELeteXt Closed Captions decoder (produces subtitles in .SRT format from transport stream .TS files) (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/debackerl/telxcc"
license=('custom:Forers')
conflicts=('telxcc')
provides=('telxcc')
source=('git+https://github.com/petrkutalek/telxcc')
md5sums=('SKIP')
makedepends=('git')
_gitname="telxcc"

pkgver(){
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  sed 's|sudo||g' -i "${_gitname}"/Makefile
}

build() {
  cd "${_gitname}"
  make
}

package() {
  cd "${_gitname}"
  install -d "${pkgdir}"/usr/bin
  make DEST="${pkgdir}"/usr install
}