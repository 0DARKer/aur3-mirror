# Maintainer Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=telxcc-git
pkgver=2.4.4.f7e8f94
pkgrel=1
pkgdesc="TELeteXt Closed Captions decoder (produces subtitles in .SRT format from transport stream .TS files) (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/forers/telxcc"
license=('custom:Forers')
conflicts=('telxcc')
provides=('telxcc')
source=('git://github.com/forers/telxcc.git')
md5sums=('SKIP')
_gitname="telxcc"

pkgver() {
  cd "${_gitname}"
  echo "$(cat telxcc.c | grep TELXCC_VERSION | head -n1 | cut -d " " -f3 | tr -d '"').$(git log -1 --format="%h")"
}

build() {
  cd "${_gitname}"

  make
  make strip
  make man
}

package() {
  cd "${_gitname}"
  install -Dm755 telxcc "${pkgdir}/usr/bin/telxcc"
  install -Dm644 telxcc.1.gz "${pkgdir}/usr/share/man/man1/telxcc.1.gz"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
