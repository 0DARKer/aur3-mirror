# Maintainer: Jonne Ha√ü <me@jhass.eu>
pkgname=crystal
pkgver=0.5.3
pkgrel=1
pkgdesc="The Crystal Programming Language"
arch=('x86_64')
url="http://crystal-lang.org"
license=('Apache')
depends=('libunwind' 'gc' 'llvm' 'libatomic_ops' 'pcre')
checkdepends=('libyaml')
optdepends=('libyaml: For YAML support'
            'gmp: For BigInt support'
            'libpcl: For coroutine (Fiber) support')
conflicts=('crystal-git')
source=("https://github.com/manastech/crystal/releases/download/$pkgver/$pkgname-$pkgver-1-linux-x86_64.tar.gz"
        "https://github.com/manastech/crystal/archive/$pkgver.tar.gz"
        "crystal.sh")

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make release=1 \
       PATH="$PATH:$srcdir/$pkgname-$pkgver-1/bin" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src:$srcdir/$pkgname-$pkgver/libs" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="src:libs:/usr/lib/crystal/src:/usr/lib/crystal/libs"
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  echo -e "#!/bin/sh\nexec \"\${CRYSTAL_BIN:-$srcdir/$pkgname-$pkgver/.build/crystal}\" \"\$@\"" > bin/crystal
  make spec PATH="bin:$PATH" \
            CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src:$srcdir/$pkgname-$pkgver/libs" \
            CRYSTAL_CONFIG_VERSION="$pkgver"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # /usr/bin/crystal wrapper script applying CRYSTAL_BIN hack, let's hope we can drop that soon
  # /usr/lib/crystal/crystal compiled executable
  # /usr/lib/crystal/src compiler src & core libs
  # /usr/lib/cystal/libs stdlib
  # /usr/share/doc/crystal/samples samples
  install -Dm755 "$srcdir/crystal.sh" "$pkgdir/usr/bin/crystal"
  install -dm755 "$pkgdir/usr/lib/crystal"
  install -Dm755 ".build/crystal" "$pkgdir/usr/lib/crystal/crystal"
  cp -rv src "$pkgdir/usr/lib/crystal/"
  cp -rv libs "$pkgdir/usr/lib/crystal/"
  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -rv samples "$pkgdir/usr/share/doc/crystal/"
}
sha256sums=('7973e356f23c341d6ed40fda513b313bf21ddabf827d0faa092b2b1b7f6046c8'
            'fad43acb6a5aff2de174709410da87efd8c84c32af89da0afaa1dbd1e4b98968'
            '34988cd5cdf0d716b480aa6d55f54696da1ebe2e536e06d0d8eb6133b88486d9')
