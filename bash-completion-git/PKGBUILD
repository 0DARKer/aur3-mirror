# $Id$
##Git variant:
# Maintainer: Rick W. Chen <stuffcorpse@archlinux.us>
# Contributor: shining
##Original package:
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>
# Contributor: Evangelos Foutras <foutrelis@gmail.com>

pkgname=bash-completion-git
pkgver=20110511
pkgrel=1
pkgdesc="Programmable completion for the bash shell, git version"
arch=(any)
url="http://bash-completion.alioth.debian.org/"
license=('GPL')
depends=('bash')
makedepends=('git')
conflicts=('bash-completion')
provides=('bash-completion')

_gitroot="git://git.debian.org/git/bash-completion/bash-completion.git"
_gitname="bash-completion"

build() {
  cd ${srcdir}
  msg "Connecting to Debian GIT server...."

  if [ -d "${srcdir}/$_gitname" ] ; then
    cd "$_gitname" && git fetch origin
    msg "The local files are updated."
  else
    git clone --mirror "$_gitroot" "$_gitname"
  fi

  rm -fr "$srcdir/$_gitname-build"
  git clone --local "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  msg "GIT checkout done or server timeout"

  cd "$srcdir/$_gitname-build"

  # fixes for archlinux
  sed -i 's#/sbin/lsmod#/bin/lsmod#' bash_completion

  autoreconf -i
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install

  # hack
  chmod 644 "$pkgdir/usr/share/bash-completion/bash_completion"

  rm -fr "$srcdir/$_gitname-build"
}
# vim:set ts=2 sw=2 et:
