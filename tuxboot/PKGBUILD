# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
pkgname=tuxboot
pkgver=0.8.r0.ge46c51e
pkgrel=1
pkgdesc="A tool that helps you to create a bootable Live USB drive for Clonezilla live, DRBL live, GParted live and Tux2live"
url="http://www.tuxboot.org/"
license=('GPL')
arch=('i686' 'x86_64')
source=("git://git.code.sf.net/p/tuxboot/git")
md5sums=('SKIP')
depends=('syslinux' 'p7zip' 'qt4' 'mtools' 'polkit')
build() {
   cd git
   sed -i '/\[en_US\]/d' ${pkgname}.desktop
   sed -i '/^RESOURCES/d' ${pkgname}.pro
   lupdate-qt4 ${pkgname}.pro
   lrelease-qt4 ${pkgname}.pro
   qmake-qt4 "DEFINES += NOSTATIC" "RESOURCES -= tuxboot.qrc"
   make
}
pkgver() {
  cd git
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

package() {
   cd git
   install -Dm755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}
   install -Dm755 ${pkgname}-pkexec ${pkgdir}/usr/bin/${pkgname}-pkexec
   mkdir -p ${pkgdir}/usr/share/${pkgname}
   install -m644 i18n/${pkgname}_*.qm ${pkgdir}/usr/share/${pkgname}/
   install -Dm644 ${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
   install -Dm644 ${pkgname}.xpm  ${pkgdir}/usr/share/pixmaps/${pkgname}.xpm
}
