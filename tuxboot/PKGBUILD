# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
# based on the unetbootin PKGBUILD
pkgname=tuxboot
pkgver=0.1
pkgrel=1
pkgdesc="A tool that helps you to create a bootable Live USB drive for Clonezilla live, DRBL live, GParted live and Tux2live"
url="http://www.tuxboot.org/"
license=('GPL')
arch=('i686' 'x86_64')
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgname}-${pkgver}.src.tar.gz)
depends=('syslinux' 'p7zip' 'qt' 'mtools' )
optdepends=('gksu: for graphically requesting the root password'
            'kdebase-runtime: for graphically requesting the root password')
md5sums=('0f3ce403a9ddceba90ea52b0e39046bc')

build() {
   cd $pkgname-$pkgver
   sed -i '/^RESOURCES/d' ${pkgname}.pro
   lupdate ${pkgname}.pro
   lrelease ${pkgname}.pro
   qmake "DEFINES += NOSTATIC" "RESOURCES -= tuxboot.qrc" ${pkgname}.pro
   make
}

package() {
   cd $pkgname-$pkgver
   install -Dm755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}
   mkdir -p ${pkgdir}/usr/share/${pkgname}
   install -m644 i18n/${pkgname}_*.qm "${pkgdir}/usr/share/${pkgname}/"
   install -Dm644 ${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
   install -Dm644 ${pkgname}.xpm  ${pkgdir}/usr/share/pixmaps/${pkgname}.xpm
}
