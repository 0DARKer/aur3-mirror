# Maintainer: Simone Sclavi 'Ito' darkhado@gmail.com
# based on the unetbootin PKGBUILD
pkgname=tuxboot
pkgver=21
pkgrel=1
pkgdesc="Tuxboot helps you to create a bootable Live USB drive for Clonezilla live, DRBL live, GParted live and Tux2live"
url="http://www.tuxboot.org/"
license=('GPL2')
arch=('i686' 'x86_64')
source=(http://sourceforge.net/projects/${pkgname}/files/20110120/${pkgname}-${pkgver}.tar.gz)
md5sums='2bba1ab2f8d7a5cacca3f3e5a206c43f'
depends=('syslinux' 'p7zip' 'qt' 'mtools' 'libpng')
optdepends=('gksu: for graphically requesting the root password'
            'kdebase-runtime: for graphically requesting the root password')
conflicts=('unetbootin')

build() {
   cd ${srcdir}/${pkgname}-${pkgver} 
   sed -i '/^RESOURCES/d' unetbootin.pro
   lupdate unetbootin.pro
   lrelease unetbootin.pro
   qmake "DEFINES += NOSTATIC CLONEZILLA" "RESOURCES -= unetbootin.qrc" unetbootin.pro
   make
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver} 
   install -Dm755 ${pkgname} "${pkgdir}/usr/sbin/${pkgname}"
   mkdir -p "${pkgdir}/usr/share/unetbootin"
   install -m644 unetbootin_*.qm "${pkgdir}/usr/share/unetbootin/"
}
