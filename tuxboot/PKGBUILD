# Maintainer: Simone Sclavi 'Ito' darkhado@gmail.com
# based on the unetbootin PKGBUILD
pkgname=tuxboot
pkgver=23
pkgrel=3
pkgdesc="A tool that helps you to create a bootable Live USB drive for Clonezilla live, DRBL live, GParted live and Tux2live"
url="http://www.tuxboot.org/"
license=('GPL')
arch=('i686' 'x86_64')
source=(http://sourceforge.net/projects/${pkgname}/files/20110317/${pkgname}-${pkgver}.src.tar.gz)
md5sums=('4583cd1a24b63431506a2dc9dfe99ea6')
depends=('syslinux' 'p7zip' 'qt' 'mtools' )
optdepends=('gksu: for graphically requesting the root password'
            'kdebase-runtime: for graphically requesting the root password')
conflicts=('unetbootin')

build() {
   cd $pkgname-$pkgver
   sed -i '/^RESOURCES/d' unetbootin.pro
   lupdate unetbootin.pro
   lrelease unetbootin.pro
   qmake "DEFINES += NOSTATIC CLONEZILLA" "RESOURCES -= unetbootin.qrc" unetbootin.pro
   make
}

package() {
   cd $pkgname-$pkgver
   install -Dm755 ${pkgname} "${pkgdir}/usr/sbin/${pkgname}"
   mkdir -p "${pkgdir}/usr/share/unetbootin"
   install -m644 unetbootin_*.qm "${pkgdir}/usr/share/unetbootin/"

   ## fix and install desktop file
   sed -i -e 's#UNetbootin#Tuxboot#g' -e 's#bin/unetbootin#sbin/tuxboot#' unetbootin.desktop
   install -Dm644 unetbootin.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
