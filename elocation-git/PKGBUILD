# Maintainer: Doug Newgard <scimmia22 at outlook dot com>

pkgname=elocation-git
_pkgname=elocation
pkgver=0.1.0.116
pkgrel=1
pkgdesc="EFL based library to get location information"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('unknown')
depends=('efl-git')
makedepends=('git')
conflicts=('elocation-svn')
options=('!libtool')
source=("git://git.enlightenment.org/devs/stefan/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  
  for i in v_maj v_min v_mic; do
    local _$i=$(grep -m 1 $i configure.ac | sed 's/m4//' | grep -o "[[:digit:]]*")
  done
  
  echo $_v_maj.$_v_min.$_v_mic.$(git rev-list --count HEAD)
}

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" install
}
