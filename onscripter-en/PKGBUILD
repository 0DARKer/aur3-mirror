# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>

pkgname=onscripter-en
pkgver=20110413
pkgrel=2
pkgdesc="English version of the open source interpreter for the visual novel game scripting engine NScripter"
arch=('i686' 'x86_64')
url='http://onscripter.unclemion.com/'
license=('GPL')
depends=('sdl_mixer' 'sdl_image' 'sdl_ttf' 'bzip2' 'gcc-libs')
# Dependency 'smpeg' provides by 'sdl_mixer'
source=(http://onscripter.unclemion.com/pub/onscr/${pkgname}-${pkgver}-src.tar.bz2)
md5sums=('d2375ab3ba9c7dce20ae5c28734827f8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --no-werror
  make
}

package() {
  install -D -m755 "${srcdir}/${pkgname}-${pkgver}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -m644 BUGS CHANGES INSTALL README "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
