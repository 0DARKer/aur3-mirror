# Maintainer: h31 <h31mail@yandex.com>

pkgname=ffmpeg-vp8-svn
pkgver=r2
pkgrel=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix, with VP8 patches"
arch=('i686' 'x86_64')
url="http://ffmpeg.mplayerhq.hu/"
license=('GPL')
depends=('lame' 'sdl' 'zlib' 'x264>=20100207' 'libtheora' 'opencore-amr')
makedepends=('subversion' 'libvpx')
provides=("ffmpeg=20100519" "qt-faststart")
conflicts=('ffmpeg' 'ffmpeg-svn')
source=(http://webm.googlecode.com/files/mplayer-vp8-encdec-support-r2.tar.bz2)
md5sums=('68f7ed2efa00fec0eb09e6f6d7bd9a17')

build() {
  cd "$srcdir"

# No 'svn up', variables, etc because of fixed revision

  svn co svn://svn.ffmpeg.org/ffmpeg/trunk -r 23190 ffmpeg

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "ffmpeg-build"
  svn export ffmpeg ffmpeg-build
  cd "ffmpeg-build"

  cp ../mplayer-vp8-encdec-support/*.diff ../mplayer-vp8-encdec-support/ffmpeg-only/*.diff .
  for i in *.diff; do
    pn=$(basename $i)
    msg2 "Applying '$pn'"
    patch -p0 < "$i"  2>&1
    if [ "$?" -ne 0 ]; then
	error "Patching failed."
	return 1
    fi
  done

  "./configure" \
  --prefix=/usr \
  --enable-gpl \
  --enable-version3 \
  --enable-libmp3lame \
  --enable-libx264 \
  --enable-libopencore-amrnb \
  --enable-libopencore-amrwb \
  --enable-libtheora \
  --enable-postproc \
  --enable-shared \
  --enable-pthreads \
  --enable-libvpx-vp8 \
  --enable-x11grab \
  --arch=`uname -m` \
  || return 1

  make || return 1
  make doc/ff{mpeg,play,server}.1 || return 1

  make DESTDIR="$pkgdir" install || return 1
  make DESTDIR="$pkgdir" install-man || return 1

  make tools/qt-faststart || return 1
  cp -a tools/qt-faststart $pkgdir/usr/bin
}
