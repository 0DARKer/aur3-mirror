# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
#Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
pkgver=0.1.1
_ubuntu_ver=0ubuntu1
pkgrel=2
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
source=("http://archive.ubuntu.com/ubuntu/pool/main/u/unity-greeter/unity-greeter_${pkgver}.orig.tar.gz"
        "http://archive.ubuntu.com/ubuntu/pool/main/u/unity-greeter/unity-greeter_${pkgver}-${_ubuntu_ver}.debian.tar.gz"
        'unity-greeter.install'
        'fix_ld_x11.patch')
install=unity-greeter.install
depends=('gnome-doc-utils' 'lightdm' 'gnome-common' 'libindicator>=0.4.0' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
options=(!libtool)
makedepends=('gnome-settings-daemon' 'pkg-config' 'bzr' 'vala' 'autoconf')
sha512sums=('f501bb769f37c936f74323c37fc3e0609221f1a2538f5e1b1b918261347c5dc5b5359fca42d521a197e18560c9ef97d3579ef82a2a8788aaad69178d63239e12'
            '5778dcd3c739909dccecfb0d714357de5e5c34b8cb5c3ba9d22741b67aceeb2de024268a9c9d0d3e9575c1b3486d2d37ac24d5554d93027dc2229bc66aa02cf7'
            '1862eb7fb558abaf2fda4ca380d9cdb8543022f36a95fc0fba7c2f391400c4dfe250af07650d22547259368ab8b2ac3a9b1e91633a57e8308a351a741b41aca4'
            'e51c33aae21409305a68b6a7e02eef59b15789fbe3b59ae38621c71a8f90cfa36ba1b4f9bda8a375b33dbf94cf610830d07b388ab2cb318e8d4f1537f6fc555a')

build() {
  cd "${srcdir}/unity-greeter-${pkgver}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  # Fix the problem where GNU ld cannot find x11
  patch -Np0 -i "${srcdir}/fix_ld_x11.patch"
  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

