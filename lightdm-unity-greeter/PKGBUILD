# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
#Taken from M0Rf30 morf3089 at gmail dot com

pkgname=lightdm-unity-greeter
_ubuntu_rel=0ubuntu4
pkgver=0.2.0.${_ubuntu_rel}
pkgrel=1
pkgdesc="The greeter (login screen) application for Unity. It is implemented as a LightDM greeter."
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-greeter"
license=('GPL3' 'LGPL3')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}-${_ubuntu_rel}.debian.tar.gz"
        "https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname#*-}_${pkgver%.*}.orig.tar.gz"
        'unity-greeter.install')
install=unity-greeter.install
depends=('gnome-doc-utils' 'lightdm' 'gnome-common' 'libindicator' 'cantarell-fonts' 'light-themes' 'ubuntu-wallpapers' 'ttf-ubuntu-font-family')
options=(!libtool)
makedepends=('gnome-settings-daemon' 'pkg-config' 'bzr' 'vala' 'autoconf')
sha512sums=('d24c41bdcd920b8c9a25bbfd9c03318773ab3c14dfee69306144fb38a087f314a3dfc5e5d9f1e7d5fb9b8ababe774b61d6e23f162607e85e6a10496ce69ec0eb'
            '6b9aa7173294a8414127443403f503ed16adaba5e0b7e51225699f17bf0d864f5aec1e6c32e18f3a64015ddfefa3a639d70c32bd814274702feaadba818ff8ad'
            '1862eb7fb558abaf2fda4ca380d9cdb8543022f36a95fc0fba7c2f391400c4dfe250af07650d22547259368ab8b2ac3a9b1e91633a57e8308a351a741b41aca4')

build() {
  cd "${srcdir}/${pkgname#*-}-${pkgver%.*}"

  # Apply Ubuntu patches
  for i in $(cat "${srcdir}/debian/patches/series" | grep -v '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  CFLAGS="${CFLAGS} -lm -lX11"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm

   make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/unity-greeter-${pkgver%.*}"
  make DESTDIR="${pkgdir}" install
}

