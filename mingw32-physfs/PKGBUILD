# Maintainer: Andreas Krinke <andreas krinke gmx de>
# based on the PKGBUILD for physfs by:
#   Eric Belanger <eric@archlinux.org>
#   Bjorn Lindeijer <bjorn lindeijer nl>

pkgname=mingw32-physfs
pkgver=2.0.1
pkgrel=2
pkgdesc="A library to provide abstract access to various archives (mingw32)"
arch=('i686' 'x86_64')
url="http://icculus.org/physfs/"
license=('zlib')
depends=('mingw32-gcc' 'mingw32-zlib')
makedepends=('cmake')
source=(http://icculus.org/physfs/downloads/physfs-${pkgver}.tar.gz
        win32.cmake)
md5sums=('df00465fcfa80e87f718961c6dd6b928'
         'f6be7e20150164e8bd342e571d37db51')
sha1sums=('1e291e8717ce3bb921b74416ec7cbcf212efcaa1'
          'f12fe05f9a4521cc53372f6b981412c082325b13')

build() {
  # mingw32 has problems with --hash-style=gnu (default value)
  unset LDFLAGS

  cd "${srcdir}/physfs-${pkgver}"
  cp "${srcdir}/win32.cmake" .
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  export CXXFLAGS="$CXXFLAGS -fno-strict-aliasing"
  cmake . \
    -DCMAKE_TOOLCHAIN_FILE=win32.cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/i486-mingw32
  make all
  
}

package() {
  cd "${srcdir}/physfs-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -D -m644 LICENSE.txt "${pkgdir}/usr/i486-mingw32/share/licenses/${pkgname}/LICENSE"
}
