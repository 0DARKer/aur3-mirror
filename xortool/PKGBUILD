# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=xortool
pkgver=0.96
pkgrel=1
pkgdesc="A tool to analyze multi-byte XOR cipher"
url="https://github.com/hellman/xortool"
depends=('python2')
arch=('any')
license=('MIT')
source=(https://github.com/hellman/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('b762b286cfd61e94fbcfb2f6c85b8b63a89a881a691a112cd710b55e02dc2a8a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's|env python$|env python2|g'  -i xortool/*
}

build() {
  cd ${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python2 setup.py install -O1 --prefix="${pkgdir}/usr"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim: ts=2 sw=2 et:
