# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=kstart
pkgver=4.1
pkgrel=1
pkgdesc="Kerberos kinit supporting AFS and ticket refreshing"
arch=("i686" "x86_64")
url="http://www.eyrie.org/~eagle/software/kstart/"
license=("custom")
depends=("krb5")
source=("http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz"
        "http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz.asc")
sha1sums=('09d0a5186d68f6f7d59340d40a79f27b5489d891'
          'd3469036c8cc9489af5b3d41570f57ab454f7e98')
validpgpkeys=('E784364E8DDE7BB370FBD9EAD15D313882004173')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2:sw=2:et:ft=sh
