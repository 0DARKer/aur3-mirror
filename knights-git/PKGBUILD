# Maintainer: Antonis G. (greenmanalishi) <capoiosct gmail.com>

pkgname=knights-git
_pkgname=knights
pkgver=20111001
pkgrel=1
pkgdesc="Chess board with XBoard protocol support."
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/Knights?content=122046"
license=('GPL2')
depends=('kdegames-libkdegames')
optdepends=('gnuchess: for playing against the computer.')
makedepends=('automoc4' 'cmake' 'docbook-xsl' 'git')
groups=('kde' 'kdebase')
replaces=('kchess' 'knights-kde4')
conflicts=('knights-kde4')
source=()
sha1sums=()


_gitroot="git://anongit.kde.org/$_pkgname"
_gitname="$_pkgname"

build() {
  cd $srcdir
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf $srcdir/$_gitname-build
  git clone $srcdir/$_gitname $srcdir/$_gitname-build
  cd $srcdir/$_gitname-build

  cmake -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd $srcdir/$_gitname-build
  make DESTDIR=$pkgdir install
}
