# $Id: PKGBUILD 84561 2010-06-30 23:36:38Z dgriffiths $
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=curl-ares
pkgver=7.21.0
pkgrel=1
pkgdesc="An URL retrival utility and library with c-ares support"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('zlib' 'openssl' 'bash' 'ca-certificates' 'c-ares')
options=('!libtool')
conflicts=('curl')
provides=('curl=${pkgver}')
source=("http://curl.haxx.se/download/curl-${pkgver}.tar.bz2")
md5sums=('e1a2a773e93a39f3c04cab92c55bf197')

build() {
	cd ${srcdir}/curl-${pkgver}

	./configure \
	    --enable-ares=/usr/include \
	    --with-random=/dev/urandom \
	    --prefix=/usr \
	    --mandir=/usr/share/man \
	    --disable-dependency-tracking \
	    --enable-ipv6 \
	    --disable-ldaps \
	    --disable-ldap \
	    --enable-manual \
	    --enable-versioned-symbols \
	    --with-ca-path=/etc/ssl/certs \
	    --without-libidn || return 1
	make || return 1
}

package() {
	cd ${srcdir}/curl-${pkgver}

	make DESTDIR=${pkgdir} install || return 1
	install -Dm644 COPYING \
		${pkgdir}/usr/share/licenses/curl/COPYING || return 1
	install -Dm644 docs/libcurl/libcurl.m4 \
		${pkgdir}/usr/share/aclocal/libcurl.m4 || return 1
}
