# Maintainer: Stefano Avallone <stavallo@gmail.com>

pkgname=kdeplasma-addons-frameworks-git
pkgver=r6338.d1b9bac
pkgrel=1
pkgdesc="All kind of addons to improve your Plasma experience"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdeplasma-addons'
license=('LGPL')
depends=('knewstuff-git' 'plasma-framework-git' 'kdelibs4support-git' 'kjs-git' 'threadweaver-git' 'kidletime-git' 'qt5-declarative')
makedepends=('kf5umbrella-git' 'kdoctools-git' 'extra-cmake-modules' 'git')
source=('git://anongit.kde.org/kdeplasma-addons.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kdeplasma-addons
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdeplasma-addons \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
