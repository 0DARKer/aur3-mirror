# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

_perlmod=CGI-FormBuilder
_modnamespace=CGI
pkgname=perl-cgi-formbuilder
pkgver=3.08
pkgrel=5
pkgdesc="CGI::FormBuilder - Easily generate and process stateful forms"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
optdepends=('perl-cgi-session>=3.95: for CGI::FormBuilder::Multi'
            'perl-html-template>=2.06: for CGI::FormBuilder::Template::HTML'
            'perl-template-toolkit>=2.08: for CGI::FormBuilder::Template::TT2'
            'perl-text-template>=1.43: for CGI::FormBuilder::Template::Text')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tgz")
sha512sums=('6e184b9cd87b70012004a3893e11904629eb7e7f2770b51930d18d164970e11c2119b5dd8279a85129867802a5fd61741bd652f7180b60e0f4ce4e47df2c3918')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	make test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	make install DESTDIR="$pkgdir/"
}
