# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-cgi-formbuilder
_pkgname='CGI-FormBuilder'
pkgver=3.08
pkgrel=3
pkgdesc="CGI::FormBuilder - Easily generate and process stateful forms"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/${_pkgname}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.10.0')
source=("http://search.cpan.org/CPAN/authors/id/N/NW/NWIGER/${_pkgname}-${pkgver}.tgz")
sha512sums=('6e184b9cd87b70012004a3893e11904629eb7e7f2770b51930d18d164970e11c2119b5dd8279a85129867802a5fd61741bd652f7180b60e0f4ce4e47df2c3918')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
