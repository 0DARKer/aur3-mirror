# Maintainer: Claudio Kozicky <claudiokozicky@gmail.com>
# Contributor: Gadget3000 <gadget3000 at msn dot com>
# Contributor: Otto Allmendinger <otto.allmendinger@gmail.com>

pkgname=crayonphysicsdeluxe
pkgver=55
pkgrel=4
pkgdesc="A 2D physics puzzle / sandbox game, in which your drawings are transformed into objects"
arch=('i686' 'x86_64')
url="http://www.crayonphysics.com/"
license=('custom')
groups=('humblebundle3' 'humblebundle4' 'humbleintbundle' 'humblebundles')
depends=('gtk-update-icon-cache' 'hicolor-icon-theme' 'xdg-utils')
[ "$CARCH" = "i686" ] &&\
    depends+=('glu' 'qt4' 'sdl_image' 'sdl_mixer')
[ "$CARCH" = "x86_64" ] &&\
    depends+=('lib32-glu' 'lib32-qt4' 'lib32-sdl_image' 'lib32-sdl_mixer')
makedepends=('imagemagick')
install=$pkgname.install
source=("hib://crayon_physics_deluxe-linux-release$pkgver.tar.gz"\
    "crayon.sh")
md5sums=('3f799bba4fbca02fcee4b866e17b193d'\
    '4e3c5f5fe8fe1db8e36d6a83dc30625d')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

build() {
    # prepare .desktop file
    cd "$srcdir/CrayonPhysicsDeluxe"
    sed -e 's/Path=.*/Path=\/usr\/bin/' -e 's/Exec=.*/Exec=crayon/' -e 's/Icon=.*/Icon=kloonigames-crayon/'\
        -i kloonigames-crayon.desktop
}

package() {
    # install launch script
    cd "$srcdir"
    install -Dm755 crayon.sh "$pkgdir/usr/bin/crayon"

    # install game files
    cd CrayonPhysicsDeluxe
    find data -type f -exec install -Dm644 '{}' "$pkgdir/opt/$pkgname/{}" \;
    install -m644 autoexec.txt "$pkgdir/opt/$pkgname/autoexec.txt"
    install -Dm755 -t "$pkgdir/opt/$pkgname" crayon launcher
    install -Dm644 kloonigames-crayon.desktop "$pkgdir/usr/share/applications/kloonigames-crayon.desktop"

    # install legal files
    install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -m644 readme.html "${pkgdir}/usr/share/licenses/${pkgname}/README.html"

    # install icons
    install -Dm644 icon.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/kloonigames-crayon.png"
    for i in 16x16 22x22 32x32 48x48 64x64 128x128; do
        install -d "$pkgdir/usr/share/icons/hicolor/$i/apps"
        convert -quiet -resize $i icon.png "$pkgdir/usr/share/icons/hicolor/$i/apps/kloonigames-crayon.png"
    done
}
