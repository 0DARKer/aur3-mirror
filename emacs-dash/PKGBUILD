# Author: holos
# Maintainer: Sebastien Duthil <duthils@free.fr>

pkgname=emacs-dash
pkgver=2.9.0
pkgrel=1
pkgdesc='A modern list API for Emacs'
arch=('any')
url="https://github.com/magnars/dash.el"
license=('GPL')
makedepends=('emacs')
depends=('emacs')
source=("https://github.com/magnars/dash.el/archive/$pkgver.tar.gz")
sha256sums=('4bd5362c3eca1bdcec7bd9b77473bb257ed4d42b86a8fc326e9f595cd012479d')

build() {
  cd dash.el-"$pkgver"

  # byte compile
  emacs -batch -L . -f batch-byte-compile dash{,-functional}.el
}

package() {
  cd dash.el-"$pkgver"
  install -d "$pkgdir"/usr/share/emacs/site-lisp/dash
  install -Dm644 dash{,-functional}.{el,elc} "$pkgdir"/usr/share/emacs/site-lisp/dash
  gzip "$pkgdir"/usr/share/emacs/site-lisp/dash/*.el
}
