# Author: holos
# Maintainer: Sebastien Duthil <duthils@free.fr>

pkgname=emacs-dash
pkgver=2.7.0
pkgrel=1
pkgdesc='A modern list API for Emacs. No cl required'
arch=('any')
url="https://github.com/magnars/dash.el"
license=('GPL')
makedepends=('emacs')
depends=('emacs')
source=("https://github.com/magnars/dash.el/archive/$pkgver.tar.gz")
md5sums=('e253f4b0a80d99e8808ee88b1b01d8c3')

build() {
  cd "dash.el-$pkgver"

  # byte compile
  emacs -batch -f batch-byte-compile dash.el
  emacs -batch -l dash.elc -f batch-byte-compile dash-functional.el
}

package() {
  install -d "$pkgdir/usr/share/emacs/site-lisp/dash"
  install -Dm644 "$srcdir/dash.el-$pkgver/"*.{el,elc} "$pkgdir/usr/share/emacs/site-lisp/dash"
}
