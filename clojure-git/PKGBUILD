# Maintainer: Neer Sighted (neersighted) <neersighted@myopera.com>
# Contributor: Chris O'Donnell <christopher dot p dot odonnell at gmail dot com>
# Contributor: Sven-Hendrik Haase <sh at lutzhaase dot com>

pkgname=clojure-git
pkgver=1.5.0.RC17.1.g4ccb10e
pkgrel=1
pkgdesc='LISP dialect for the JVM (development version)'
url='http://clojure.org'
arch=('i686' 'x86_64')
license=('CPL')
install=clojure-git.install
source=('git://github.com/clojure/clojure.git'
        'clj'
        'clojure.sh')
md5sums=('SKIP'
         'bf6e1c6b03451be8932542cbffdf7d0b'
         '7e02402b3f875ec60c77da9dbe324786')
sha1sums=('SKIP'
          'a4b773889efc3fd645bd274d80114f236ad5e033'
          '640d333d0e7f6e75320e1f0b6af25e60ce20a26d')
sha256sums=('SKIP'
            '0d0f9e7db77f4b5f507ee18e140c29138b7bff5c4ca9d0b963dfd657c6e3c157'
            'b3fa6257dad10fb39851e87563269b80b2473623126595d9c279bbd90e97f052')
depends=('java-environment' 'sh')
makedepends=('git' 'apache-ant' 'maven')
optdepends=('rlwrap: friendlier shell with readline support')
provides=('clojure')
conflicts=('clojure')

pkgver() {
  cd "${srcdir}/clojure"

  git describe --always | sed 's/^v//;s/-/./g;s/^clojure.//'
}

prepare() {
  cd "${srcdir}/clojure"

  # Use maven to download and configure dependencies for ant.
  ./antsetup.sh
}

build() {
  cd "${srcdir}/clojure"

  # Build it!
  ant jar
}

check() {
  cd "${srcdir}/clojure"

  # Run the test suite.
  ant test
}

package() {
  cd "${srcdir}/clojure"

  # Install the program.
  install -Dm644 clojure.jar \
                 "${pkgdir}/usr/share/clojure/clojure.jar"
  install -Dm755 "${srcdir}/clj" \
                 "${pkgdir}/usr/bin/clj"

  # Install shell runtime files.
  install -Dm755 "${srcdir}/clojure.sh" \
                 "${pkgdir}/etc/profile.d/clojure.sh"
}
