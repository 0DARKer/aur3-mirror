# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgroot=clojure
pkgname=clojure-git
pkgver=latest
pkgrel=2
pkgdesc='LISP dialect for the JVM (development version)'
url='http://clojure.org'
arch=('i686' 'x86_64')
license=('CPL')
install=clojure-git.install
source=('git+https://github.com/clojure/clojure.git'
        'clj'
        'clojure.sh')
sha1sums=('SKIP'
          'a4b773889efc3fd645bd274d80114f236ad5e033'
          '640d333d0e7f6e75320e1f0b6af25e60ce20a26d')
depends=('java-environment' 'sh')
makedepends=('git' 'apache-ant' 'maven')
optdepends=('rlwrap: friendlier shell with readline support')
provides=('clojure')
conflicts=('clojure')

pkgver() {
  cd "${srcdir}/${pkgroot}"

  # Get the version number.
  git describe --always | sed 's/^v//;s/-/./g;s/^clojure.//'
}

prepare() {
  cd "${srcdir}/${pkgroot}"

  # Use maven to download and configure dependencies for ant.
  ./antsetup.sh
}

build() {
  cd "${srcdir}/${pkgroot}"

  # Build it!
  ant jar
}

check() {
  cd "${srcdir}/${pkgroot}"

  # Run the test suite.
  ant test
}

package() {
  cd "${srcdir}/${pkgroot}"

  # Install the program.
  install -Dm644 clojure.jar "${pkgdir}/usr/share/clojure/clojure.jar"
  install -Dm755 "${srcdir}/clj" "${pkgdir}/usr/bin/clj"

  # Install shell runtime files.
  install -Dm755 "${srcdir}/clojure.sh" "${pkgdir}/etc/profile.d/clojure.sh"
}

# vim: ft=sh ts=2 sw=2 et
