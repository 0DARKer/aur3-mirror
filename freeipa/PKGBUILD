# Maintainer: Fran√ßois Steinmetz <francois.steinmetz@gmail.com>

pkgname=freeipa
pkgver=2.1.4
pkgrel=3
pkgdesc="FreeIPA (identity, policy, audit) is an integrated security information management solution."
arch=('x86_64')
url="http://freeipa.org"
license=('GPL3')
depends=(
  '389-ds-base'
  'authconfig'
  'pyasn1'
  'python-nss'
  'python2'
  'python2-krbv'
  'python2-ldap'
  'python2-netaddr'
  'python2-pyopenssl'
  'sssd'
  'xmlrpc-c'
)
makedepends=()
backup=()
source=("http://freeipa.org/downloads/src/$pkgname-$pkgver.tar.gz"
        "missing-headers.patch"
        "ipaserver-encodecontroltuples.patch"
        "ipa-kpasswd.rcd"
        "ipa-kpasswd.service")
sha1sums=('0d0297d4861fb1eb681bf0972eea030e8503557f'
          '33e020e509665e9c3a44b8ba66d18534fc42cc9a'
          '862b4d6bf176261c5b34d2397ee834fcb01bed8b'
          'adfcff9c82674ea8d0f819571fd6007b91ed8d56'
          '965d92a7fca37303972d065cf7e5eedbba952331')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON=python2

  sed -i '1 s|\<python\>|python2|' \
    makeapi \
    install/tools/ipa* \
    ipa-client/ipa-install/ipa-client-install
  sed -i 's|\<python\>|python2|' \
    Makefile \
    ipapython/Makefile \
    ipapython/py_default_encoding/Makefile

  patch -p1 < "$srcdir/ipaserver-encodecontroltuples.patch"

  patch -p2 < "$srcdir/missing-headers.patch"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir/etc/rc.d/init.d"
  cd "$srcdir"
  install -Dm0644 ipa-kpasswd.service   "$pkgdir/lib/systemd/system/ipa-kpasswd.service"
  install -Dm0755 ipa-kpasswd.rcd       "$pkgdir/etc/rc.d/ipa-kpasswd"
}

# vim:set ts=2 sw=2 et:
