# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

pkgname=perl-sys-info-driver-linux
_pkgname=Sys-Info-Driver-Linux
pkgver=0.7900
pkgrel=2
pkgdesc="Sys::Info::Driver::Linux - Linux driver for Sys::Info"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-config-general' 'perl-sys-info-base>=0.7802' 'perl-test-sys-info>=0.20' 'perl-unix-processors')
url="http://search.cpan.org/dist/$_pkgname"
source=(http://search.cpan.org/CPAN/authors/id/B/BU/BURAK/$_pkgname-$pkgver.tar.gz)
sha512sums=('3a18a291f9af44a0807c2355bd012b7556fd71e5b926e2091d51b976ce05808f8526e94aeeb287b94ff07b3efbe5301ea3a034f9e23636d7916b32b9f2b95862')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
