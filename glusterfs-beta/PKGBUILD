# Maintainer : Kaushal M <kshlmster at gmail dot com>

_basename=glusterfs
_basever=3.4
pkgname=$_basename-beta
pkgver=$_basever.0beta1
pkgrel=1
pkgdesc="A cluster filesystem capable of scaling to several petabytes"
arch=(x86_64)
options=(debug)
provides=(glusterfs)
conflicts=(glusterfs)
url="http://www.gluster.org"
license=(GPL2 LGPL3)
depends=('fuse' 'libxml2' 'python2' 'libaio' 'lvm2' 'openssl' 'libibverbs' 'ncurses' 'readline')
source=(http://download.gluster.org/pub/gluster/glusterfs/${_basever}/${pkgver}/glusterfs-${pkgver}.tar.gz
        glusterd.service)

sha256sums=('57a25dda24aee47d31735549e4d31278a3445efbb2ab82cdd29e67e65a28000a'
            '334467b3943a36b4912e0f937d2750c9508ca678432964d5003e2e33b413d9bf')

build() {
        cd $srcdir/$_basename-$pkgver

        ./configure --prefix=/usr --sysconfdir=/etc  --localstatedir=/var --mandir=/usr/share/man \
                --libexecdir=/usr/lib/$_basename PYTHON=python2

        make
}

package() {
        cd $srcdir/$_basename-$pkgver

        make DESTDIR=$pkgdir install

        install -D -m 644 README \
                $pkgdir/usr/share/doc/glusterfs/
        install -D -m 644 INSTALL \
                $pkgdir/usr/share/doc/glusterfs/
        install -D -m 644 COPYING-* \
                $pkgdir/usr/share/doc/glusterfs/

        install -D -m 644 $srcdir/glusterd.service \
                $pkgdir/usr/lib/systemd/system/glusterd.service
}

