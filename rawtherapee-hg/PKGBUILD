# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
pkgname=rawtherapee-hg
pkgver=1116
pkgrel=1
pkgdesc="RAW photo editor"
arch=('i686' 'x86_64')
url="http://www.rawtherapee.com/"
license=('GPL')
depends=('libsigc++' 'gtkmm' 'libiptcdata' 'lcms2')
makedepends=('mercurial' 'cmake')
provides=('rawtherapee')
conflicts=('rawtherapee')
replaces=('rawtherapee-svn' 'rawtherapee-fork-git')
source=(rawtherapee.desktop rawtherapee.png)
md5sums=('83f14b57b27f066705b0aec7ad56c53d'
         '4e54e74684971a2f04d9396b9051570e')

_hgroot="https://rawtherapee.googlecode.com/hg/"
_hgrepo="rawtherapee"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u || return 1
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo || return 1
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir/$_hgrepo-build" ]; then
    rm -rf "$srcdir/$_hgrepo-build"
  fi
  mkdir "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr ../$_hgrepo
  make
}

package() {
  cd "$srcdir/$_hgrepo-build"
  make DESTDIR="$pkgdir/" install

  # remove unneded rtstart script
  # see http://code.google.com/p/rawtherapee/issues/detail?id=184
  rm "$pkgdir"/usr/bin/rtstart

  install -D -m644 "$srcdir"/rawtherapee.desktop "$pkgdir"/usr/share/applications/rawtherapee.desktop
  install -D -m644 "$srcdir"/rawtherapee.png "$pkgdir"/usr/share/pixmaps/rawtherapee.png
}
