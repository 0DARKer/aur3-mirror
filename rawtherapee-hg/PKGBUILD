# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
pkgname=rawtherapee-hg
pkgver=1385
pkgrel=1
pkgdesc="RAW photo editor"
arch=('i686' 'x86_64')
url="http://www.rawtherapee.com/"
license=('GPL')
depends=('bzip2' 'gtkmm' 'libiptcdata' 'lcms2' 'desktop-file-utils')
makedepends=('mercurial' 'cmake')
provides=('rawtherapee')
conflicts=('rawtherapee')
install=rawtherapee.install
source=(rawtherapee.desktop rawtherapee.png)
md5sums=('cdf38ea875375b63afdcfd78645d2228'
         '4e54e74684971a2f04d9396b9051570e')

_hgroot="https://rawtherapee.googlecode.com/hg/"
_hgrepo="rawtherapee"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir/$_hgrepo-build" ]; then
    rm -rf "$srcdir/$_hgrepo-build"
  fi
  mkdir "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../$_hgrepo
  make
}

package() {
  cd "$srcdir/$_hgrepo-build"
  make DESTDIR="$pkgdir/" install

  install -D -m644 "$srcdir"/rawtherapee.desktop "$pkgdir"/usr/share/applications/rawtherapee.desktop
  install -D -m644 "$srcdir"/rawtherapee.png "$pkgdir"/usr/share/pixmaps/rawtherapee.png
}
