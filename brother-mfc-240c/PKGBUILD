# Maintainer: libernux <dutchman55@gmx.com>
pkgname="brother-mfc-240c"
pkgver="1.0.1"
pkgrel=1
pkgdesc="LPR and CUPS driver for the Brother MFC-240c"
url="http://solutions.brother.com/linux/en_us/"
arch=("i686" "x86_64")
license=("GPL" 'custom:brother commercial license')
depends=("cups")
install="$pkgname.install"
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/mfc240clpr-$pkgver-$pkgrel.i386.rpm"
	"http://www.brother.com/pub/bsc/linux/dlf/mfc240ccupswrapper-$pkgver-$pkgrel.i386.rpm"
)
md5sums=(
	'e2564327cb7fc6501ca9657080855322'
	'207cf1eb6bbab8d2ecafd8a2b5bc8f4b'
)

build() {
	cd "$srcdir/usr/local/Brother/Printer/mfc240c/cupswrapper"
#  generate the ppd
	echo '#! /bin/sh' > genppd
	echo 'ppd_file_name=brmfc240c.ppd' >> genppd
	echo 'touch $ppd_file_name' >> genppd
	sed -n '/^printer_model/,/^device_model/p' cupswrappermfc240c >> genppd
	sed -n '/^cat <<ENDOFPPDFILE1/,/^ENDOFPPDFILE_END/p' cupswrappermfc240c >>genppd
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genppd
	chmod +x genppd
	./genppd
#  generate the wrapper
	echo '#! /bin/sh' > genwrapper
	echo 'brotherlpdwrapper=brlpdwrappermfc240c'  >> genwrapper
	echo 'touch $brotherlpdwrapper' >>genwrapper
	sed -n '/^printer_model/p' cupswrappermfc240c >>genwrapper
	sed -n '/^device_model/p' cupswrappermfc240c >>genwrapper
	sed -n '/cat <<!ENDOFWFILTER!/,/!ENDOFWFILTER!/p' cupswrappermfc240c >>genwrapper
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genwrapper
	chmod +x genwrapper
	./genwrapper
#  generate other
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/bin/brprintconf_mfc240c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc240c/lpd/filtermfc240c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc240c/lpd/brmfc240cfilter"
	install -d "$srcdir/usr/share"
	mv "$srcdir/usr/local/Brother/" "$srcdir/usr/share/brother"
	rm "$srcdir/usr/share/brother/Printer/mfc240c/cupswrapper/cupswrappermfc240c"
	rm "$srcdir/usr/share/brother/Printer/mfc240c/cupswrapper/genppd"
	rm "$srcdir/usr/share/brother/Printer/mfc240c/cupswrapper/genwrapper"
	rm "$srcdir/usr/share/brother/Printer/mfc240c/inf/setupPrintcapij"
	rm -rf "$srcdir/usr/local"
}

package() {
	cd "$pkgdir"
	cp -R "$srcdir/usr" "$pkgdir"
	install -m 644 -D "$srcdir/usr/share/brother/Printer/mfc240c/cupswrapper/brmfc240c.ppd" "$pkgdir/usr/share/cups/model/brmfc240c.ppd"
	install -m 755 -D "$srcdir/usr/share/brother/Printer/mfc240c/cupswrapper/brlpdwrappermfc240c" "$pkgdir/usr/lib/cups/filter/brlpdwrappermfc240c"
}