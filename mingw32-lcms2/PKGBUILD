pkgname=mingw32-lcms2
pkgver=2.3
pkgrel=2
pkgdesc="Lightweight color management development library/engine (mingw32)"
arch=('any')
depends=('mingw32-runtime' 'mingw32-libtiff')
makedepends=('mingw32-gcc')
options=('!libtool' '!strip')
conflicts=('mingw32-lcms')
license=('custom')
url="http://www.littlecms.com"
source=(http://downloads.sourceforge.net/sourceforge/lcms/lcms2-${pkgver}.tar.gz)
md5sums=('327348d67c979c88c2dec59a23a17d85')

build() {
  cd "${srcdir}/lcms2-${pkgver}"

  export CFLAGS="-O2 -mms-bitfields"
  export PKG_CONFIG_PATH="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS # mingw-ld chokes on --hash-style=gnu

  ./configure \
      --prefix=/usr/i486-mingw32 \
      --host=i486-mingw32 \
      --build=$CHOST

    make
}

package() {
  cd "${srcdir}/lcms2-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
