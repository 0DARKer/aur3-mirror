# Maintainer: Mateus Rodrigues Costa <charles [dot] costar [at] gmail [dot] com>
 
pkgname=chrome-remote-desktop
pkgver=35.0.1916.17
pkgrel=6
pkgdesc="Allows you to securely access your computer over the Internet through Chrome."
url="https://chrome.google.com/webstore/detail/gbchcmhmhahfdphkhkmpfmihenigjmpp"
arch=('i686' 'x86_64')
license=('BSD')
install=chrome-remote-desktop.install
depends=('python2' 'python2-psutil' 'gconf' 'gtk2' 'nss'
         'xorg-xdpyinfo' 'xorg-setxkbmap' 'xorg-server-xvfb' 'xorg-xauth')
source=("chrome-remote-desktop.service" 'arch.patch::http://pastebin.com/raw.php?i=JtmP5kDk')
md5sums=('SKIP' '6e31c2e0f6c033d4f9a210073c440dea')
_arch=i386
if [ "$CARCH" == i686 ]; then
  md5sums+=('a00b511e9d0e3289ba4225b3fd2b311c')
elif [ "$CARCH" == x86_64 ]; then
  _arch=amd64
  md5sums+=('ac95dbefad5f9fc642d769823985fac7')
fi
source+=(https://dl.google.com/linux/direct/${pkgname}_current_$_arch.deb)
 
package() {
  msg2 "Extracting data.tar.gz"
  bsdtar -xf data.tar.gz -C "$pkgdir/"
 
  msg2 "Patching Python script"
  sed -e '1s/python/python2/' -i "$pkgdir"/opt/google/chrome-remote-desktop/chrome-remote-desktop
 
  msg2 "Removing things that won't work"
  rm -R "$pkgdir"/etc/cron.daily/
  rm -R "$pkgdir"/etc/init.d/
  rm -R "$pkgdir"/etc/pam.d/
 
  msg2 "They forgot the LICENSE file, using a placeholder"
  install -Dm644 "$pkgdir"/usr/share/doc/$pkgname/copyright "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
 
  msg2 "Adding a systemd user service"
  mkdir -p "$pkgdir"/usr/lib/systemd/user/
  install -m644 $pkgname.service "$pkgdir"/usr/lib/systemd/user/
 
  msg2 "Creating symlinks for chromium compatibility"
  mkdir -p "$pkgdir"/etc/chromium/native-messaging-hosts
  ln -sr "$pkgdir"/etc/opt/chrome/native-messaging-hosts/* "$pkgdir"/etc/chromium/native-messaging-hosts
 
  msg2 "Patching for arch compatibility"
  cd "$pkgdir"
  patch -p1 < "$srcdir"/arch.patch
}
