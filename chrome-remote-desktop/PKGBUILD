# Maintainer: Mateus Rodrigues Costa <charles.costar [at] gmail [dot] com>

pkgname=chrome-remote-desktop
pkgver=35.0.1916.17
pkgrel=4
pkgdesc="Allows you to securely access your computer over the Internet through Chrome."
arch=('i686' 'x86_64')
url="https://chrome.google.com/webstore/detail/gbchcmhmhahfdphkhkmpfmihenigjmpp"
license=('custom:google')
depends=('xorg-server-xvfb' 'xorg-xauth' 'python2>=2.6' 'python2-psutil'
         'gconf>=2.31.1' 'gtk2>=2.14.0' 'nss>=3.14.3' 'libxtst')
source=('chrome-remote-desktop.service')
md5sums=('SKIP')
_arch=i386
if [ "$CARCH" == i686 ]; then
  _arch=i386
  md5sums+=('a00b511e9d0e3289ba4225b3fd2b311c')
elif [ "$CARCH" == x86_64 ]; then
  _arch=amd64
  md5sums+=('ac95dbefad5f9fc642d769823985fac7')
fi
source+=(https://dl.google.com/linux/direct/${pkgname}_current_$_arch.deb)

package() {
  msg2 "Extracting files"
  bsdtar -xf data.tar.gz -C "$pkgdir/"
  
  msg2 "Patching Python script"
  sed -e '1s/python/python2/' \
      -e '/^ *sudo_command =/ s/"gksudo.*"/"pkexec"/' \
      -i "$pkgdir"/opt/google/chrome-remote-desktop/chrome-remote-desktop
      
  msg2 "Removing things that won't work"
  rm -R "$pkgdir"/etc/cron.daily/
  rm -R "$pkgdir"/etc/init.d/
  rm -R "$pkgdir"/etc/pam.d/
  
  msg2 "Adding license file"
  cd "$pkgdir"/usr/share
  mkdir -p "licenses/${pkgname}"
  cp "doc/$pkgname/copyright" "licenses/$pkgname/copyright"
  
  msg2 "Adding a systemd user service"
  mkdir -p "$pkgdir"/usr/lib/systemd/user/
  install -m644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/user/
  
  msg2 "Adding a symlink for chromium"
  cd "$pkgdir/etc"
  mkdir -p chromium
  ln -sr opt/chrome/native-messaging-hosts chromium/native-messaging-hosts
}
