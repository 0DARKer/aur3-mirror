pkgname=libsolv
pkgver=0.6.11
pkgrel=1
pkgdesc="Library for solving packages and reading repositories"
arch=('i686' 'x86_64')
url="https://github.com/openSUSE/${pkgname}"
license=('BSD')
depends=('expat' 'xz' 'zlib')
makedepends=('cmake' 'perl' 'python' 'rpm-org' 'ruby' 'swig')
optdepends=('perl: for perl bindings'
            'python: for python bindings'
            'ruby: for ruby bindings')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
md5sums=('5c2a00c82e717e4928c17dca3be12863')

build() {
	cd "$pkgname-$pkgver"
	mkdir build
	cd build
	cmake -DCMAKE_BUILD_TYPE='Release'     \
	      -DCMAKE_INSTALL_PREFIX='/usr'    \
	      -DUSE_VENDORDIRS='ON'            \
	      -DARCHLINUX='1'                  \
	      -DENABLE_APPDATA='ON'            \
	      -DENABLE_ARCHREPO='ON'           \
	      -DENABLE_BZIP2_COMPRESSION='OFF' \
	      -DENABLE_COMPS='OFF'             \
	      -DENABLE_CUDFREPO='OFF'          \
	      -DENABLE_DEBIAN='ON'             \
	      -DENABLE_HAIKU='OFF'             \
	      -DENABLE_HELIXREPO='OFF'         \
	      -DENABLE_LZMA_COMPRESSION='ON'   \
	      -DENABLE_MDKREPO='OFF'           \
	      -DENABLE_PERL='ON'               \
	      -DENABLE_PUBKEY='OFF'            \
	      -DENABLE_PYTHON='ON'             \
	      -DENABLE_RPMDB='ON'              \
	      -DENABLE_RPMDB_BYRPMHEADER='OFF' \
	      -DENABLE_RPMMD='ON'              \
	      -DENABLE_RUBY='ON'               \
	      -DENABLE_SUSEREPO='OFF'          \
	      ..
	make
}

check() {
	cd "$pkgname-$pkgver"
	cd build
	make ARGS="-V" test
}

package() {
	cd "$pkgname-$pkgver"
	cd build
	make DESTDIR="$pkgdir/" install
	if [[ "$CARCH" == "x86_64" ]]; then
		mv "$pkgdir/"usr/lib64/* "$pkgdir/"usr/lib
		rmdir "$pkgdir/"usr/lib64
	fi
}
