# Maintainer: vertoe <vertoe AT qhor DOT net>
# Contributor: deusstultus <deusstultus at gmail dot com>
# Contributor: Viliam Kubis <viliam dot kubis at gmail dot com>
# Based on PKGBUILD from vertcoin-git maintained by Lothar_m <lothar_m at riseup dot net>

pkgname='darkcoin-git'
_gitname='darkcoin'
_gitbranch='master'
pkgver=5224.fd6c104
pkgrel=1
arch=('i686' 'x86_64')
url="https://www.darkcoin.io/"
depends=('qt4>=4.6' 'boost-libs>=1.46' 'miniupnpc>=1.6' 'qrencode>=3.4.3')
makedepends=('boost' 'automoc4' 'qrencode' 'miniupnpc' 'git')
license=('MIT')
pkgdesc="Darkcoin (DRK) is a privacy-centric digital currency based on the Bitcoin protocol. It allows you to remain anonymous while you make transactions, similar to cash. (Includes both the qt-client and the headless daemon.)"
provides=('darkcoin')
conflicts=('darkcoin' 'darkcoin-bin-rc' 'darkcoil')
source=("git://github.com/darkcoin/darkcoin.git")
sha512sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	git checkout "$_gitbranch" -q
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd "$srcdir/$_gitname"
	git checkout "$_gitbranch"

	# qt wallet
	qmake-qt4 USE_QRCODE=1 USE_UPNP=1
	make -j $(nproc)

	# headless daemon
	cd "$srcdir/$_gitname/src"
	make $MAKEFLAGS -f makefile.unix -j $(nproc)
}

package() {
	install -D -m755 "$srcdir/$_gitname/darkcoin-qt" "$pkgdir/usr/bin/darkcoin-qt"
	install -D -m755 "$srcdir/$_gitname/src/darkcoind" "$pkgdir/usr/bin/darkcoind"
	install -D -m644 "$srcdir/$_gitname/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D -m644 "$srcdir/$_gitname/share/pixmaps/bitcoin128.png" "$pkgdir/usr/share/pixmaps/darkcoin128.png"
	install -D -m644 "$srcdir/$_gitname/contrib/debian/darkcoin-qt.desktop" "$pkgdir/usr/share/applications/darkcoin.desktop"
}
