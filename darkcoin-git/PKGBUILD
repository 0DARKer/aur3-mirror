# Maintainer: Vertoe <vertoe AT qhor DOT net>
# Contributor: Viliam Kubis <viliam dot kubis at gmail dot com>
# Based on PKGBUILD from vertcoin-git maintained by Lothar_m <lothar_m at riseup dot net>

pkgname='darkcoin-git'
_gitname='darkcoin'
_gitbranch='master'
pkgver=5118.fda2f6b
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.darkcoin.io/"
depends=('qt4>=4.6' 'boost-libs>=1.46' 'miniupnpc>=1.6' 'qrencode>=3.4.3')
makedepends=('boost' 'automoc4' 'qrencode' 'miniupnpc' 'git')
license=('MIT')
pkgdesc="Privacy-Centric Cryptographic Currency with fully encrypted transactions and anonymous block transactions. (Includes both the qt-client and the headless daemon.)"
provides=('darkcoin')
conflicts=('darkcoin' 'darkcoin-bin-rc')
source=("git://github.com/darkcoinproject/darkcoin.git"
"https://raw.githubusercontent.com/darkcoinproject/darkcoin/master/COPYING"
"https://raw.githubusercontent.com/darkcoinproject/darkcoin/master/share/pixmaps/bitcoin128.png"
"https://raw.githubusercontent.com/darkcoinproject/darkcoin/master/contrib/debian/darkcoin-qt.desktop")
sha512sums=('SKIP'
'8aa23a60e346399b9a841258c45a6927ee7da6a033c39730b284bff5b3215e05497de027af57214393edec637718a986bf329937f59042918a9ea3c761db4089'
'f0bab4b845746dd56f368fbc439b87119c539eb3a4a6d937c5524c807f1b9d63d2c8a0c6680ede402c46fc5fbe6d13b95a107849e1d2e024e68a2a5ec9711e44'
'8df4301a0f582ee86da79bcc215d8cd230e1b573cfd0a928601ba23d17518edef3a9d8577afb5bea145a017b085777150ef0d70ef4dec2e4cf758e3780b6daad')

pkgver() {
	cd "$srcdir/$_gitname"
	git checkout "$_gitbranch" -q
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd "$srcdir/$_gitname"
	git checkout "$_gitbranch"

	# qt wallet
	qmake-qt4 USE_QRCODE=1 USE_UPNP=1
	make -j $(nproc)

	# headless daemon
	cd "$srcdir/$_gitname/src"
	make $MAKEFLAGS -f makefile.unix -j $(nproc)
}

package() {
	install -D -m755 "$srcdir/$_gitname/darkcoin-qt" "$pkgdir/usr/bin/darkcoin-qt"
	install -D -m755 "$srcdir/$_gitname/src/darkcoind" "$pkgdir/usr/bin/darkcoind"
	install -D -m644 "$srcdir/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D -m644 "$srcdir/bitcoin128.png" "$pkgdir/usr/share/pixmaps/darkcoin128.png"
	install -D -m644 "$srcdir/darkcoin-qt.desktop" "$pkgdir/usr/share/applications/darkcoin.desktop"
}

