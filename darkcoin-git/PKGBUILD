# Maintainer: Vertoe <vertoe AT qhor DOT net>
# Contributor: Viliam Kubis <viliam dot kubis at gmail dot com>
# Based on PKGBUILD from vertcoin-git maintained by Lothar_m <lothar_m at riseup dot net>

pkgname='darkcoin-git'
_gitname='darkcoin'
_gitbranch='master'
pkgver=5998.82283fb
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.darkcoin.io/"
depends=('qt4' 'boost' 'boost-libs' 'miniupnpc' 'openssl')
makedepends=('autoconf' 'automake' 'binutils' 'gcc' 'libtool' 'make' 'pkg-config' 'git' 'qrencode' 'automoc4')
license=('MIT')
pkgdesc="Privacy-Centric Cryptographic Currency with fully encrypted transactions and anonymous block transactions. (Includes the qt-client, the headless daemon and the command-line tool.)"
provides=('darkcoin')
conflicts=('darkcoin' 'darkcoin-bin-rc')
source=("git://github.com/darkcoinproject/darkcoin.git"
"https://raw.githubusercontent.com/darkcoin/darkcoin/master-v0.10/COPYING"
"https://raw.githubusercontent.com/darkcoin/darkcoin/master-v0.10/share/pixmaps/bitcoin128.png"
"https://raw.githubusercontent.com/darkcoin/darkcoin/master-v0.10/contrib/debian/darkcoin-qt.desktop")
sha512sums=('SKIP'
'8aa23a60e346399b9a841258c45a6927ee7da6a033c39730b284bff5b3215e05497de027af57214393edec637718a986bf329937f59042918a9ea3c761db4089'
'f0bab4b845746dd56f368fbc439b87119c539eb3a4a6d937c5524c807f1b9d63d2c8a0c6680ede402c46fc5fbe6d13b95a107849e1d2e024e68a2a5ec9711e44'
'8df4301a0f582ee86da79bcc215d8cd230e1b573cfd0a928601ba23d17518edef3a9d8577afb5bea145a017b085777150ef0d70ef4dec2e4cf758e3780b6daad')

pkgver() {
	cd "$srcdir/$_gitname"
	git checkout "$_gitbranch" -q
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd "$srcdir/$_gitname"
	git checkout "$_gitbranch"

	./autogen.sh
	./configure --with-incompatible-bdb --with-gui --with-daemon --with-cli --disable-debug --disable-tests
	make -j $(nproc)
	strip ./src/qt/darkcoin-qt
	strip ./src/darkcoind
	strip ./src/darkcoin-cli
}

package() {
	install -D -m755 "$srcdir/$_gitname/src/qt/darkcoin-qt" "$pkgdir/usr/bin/darkcoin-qt"
	install -D -m755 "$srcdir/$_gitname/src/darkcoind" "$pkgdir/usr/bin/darkcoind"
	install -D -m755 "$srcdir/$_gitname/src/darkcoin-cli" "$pkgdir/usr/bin/darkcoin-cli"
	install -D -m644 "$srcdir/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D -m644 "$srcdir/bitcoin128.png" "$pkgdir/usr/share/pixmaps/darkcoin128.png"
	install -D -m644 "$srcdir/darkcoin-qt.desktop" "$pkgdir/usr/share/applications/darkcoin.desktop"
}

