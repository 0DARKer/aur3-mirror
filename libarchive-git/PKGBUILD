# Maintainer: graysky <graysky AT arcnlinux DOT us>

pkgname=libarchive-git
pkgver=20121003
pkgrel=1
pkgdesc="Library that can create and read several streaming archive formats. Git version."
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz>=5.0.0' 'acl' 'openssl>=1.0.0' 'expat' 'lrzip')
makedepends=('git')
provides=('libarchive=3.0.4') # needed since several packages depend on a versioned libarchive
conflicts=('libarchive')

_gitroot=https://github.com/libarchive/libarchive.git
_gitname=libarchive

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

	cd "$srcdir/$_gitname-build"
	./build/autogen.sh
	./configure --prefix=/usr --without-xml2

	make
}

package() {
	cd "$srcdir/$_gitname-build"
	make DESTDIR="$pkgdir" install
	install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
