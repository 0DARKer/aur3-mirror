# Maintainer: graysky <graysky AT arcnlinux DOT us>

pkgname=libarchive-git
pkgver=20121002
pkgrel=1
pkgdesc="Library that can create and read several streaming archive formats. Git version."
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz>=5.0.0' 'acl' 'openssl>=1.0.0' 'expat' 'lrzip')
makedepends=('git')
provides=('libarchive=3.0.4') # needed since several packages depend on a versioned libarchive
conflicts=('libarchive')
source=(enable_lrzip.patch)
sha256sums=('2f1dba62c2cf548628450d10a6a0b4cc5bbd0a76bf3a28055e829c26924f1b6d')

_gitroot=https://github.com/libarchive/libarchive.git
_gitname=libarchive

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

	cd "$srcdir/$_gitname-build"
	./build/autogen.sh
	./configure --prefix=/usr --without-xml2
	#  ./configure --prefix=/usr --without-xml2 --disable-acl
	patch -Np1 -i $srcdir/enable_lrzip.patch

	make
}

package() {
	cd "$srcdir/$_gitname-build"
	make DESTDIR="$pkgdir" install
	install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
