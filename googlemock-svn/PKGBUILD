# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=googlemock-svn
pkgver=424
pkgrel=1
pkgdesc="Google Mock (GMock) - A library for writing and using C++ mock classes"
arch=('i686' 'x86_64')
url="http://code.google.com/p/googlemock/"
license=('BSD')
options=('!libtool')
depends=('googletest-svn')
makedepends=('python2')
optdepends=('python2: gmock_gen.py support')
conflicts=('googlemock')
provides=('googlemock')
source=()
sha1sums=()

_svntrunk=http://googlemock.googlecode.com/svn/trunk/
_svnmod=googlemock

_sitepackages=$(python2 -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())")

build() {
  cd "${srcdir}"
  msg "Connecting to SVN server...."

  if [[ -d "${_svnmod}/.svn" ]]; then
    (cd "${_svnmod}" && svn up -r "$pkgver")
  else
    svn co "${_svntrunk}" --config-dir ./ -r "$pkgver" "${_svnmod}"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_svnmod}-build"
  svn export "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  find -name "*.py" -exec sed -i 's/#!\/usr\/bin\/env python/#!\/usr\/bin\/env python2/' {} \;

  autoreconf -fvi
  ./configure --prefix=/usr --enable-external-gtest
  make
}

package() {
  cd "${srcdir}/${_svnmod}-build"

  mkdir -pm 0755 "${pkgdir}/usr/lib"
  install -m 0644 lib/.libs/libgmock{,_main}.so "${pkgdir}/usr/lib"

  mkdir -pm 0755 "${pkgdir}/usr/include/gmock/internal"
  install -m 0644 include/gmock/*.h "${pkgdir}/usr/include/gmock"
  install -m 0644 include/gmock/internal/*.h "${pkgdir}/usr/include/gmock/internal"

  mkdir -pm 0755 ${pkgdir}/usr/share/{doc,licenses}/${pkgname}
  install -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 0644 CHANGES CONTRIBUTORS README "${pkgdir}/usr/share/doc/${pkgname}"

  mkdir -pm 0755 "${pkgdir}/usr/bin/" "${pkgdir}/${_sitepackages}/cpp"
  install -m 0755 "scripts/generator/gmock_gen.py" "${pkgdir}/usr/bin"
  install -m 0644 scripts/generator/cpp/*.py "${pkgdir}/${_sitepackages}/cpp"

  mkdir -pm 0755 "${pkgdir}/usr/src/gmock/src"
  install -m 0644 src/*.cc "${pkgdir}/usr/src/gmock/src"
}
