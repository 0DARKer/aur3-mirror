# Maintainer: Dave Blair <mail@dave-blair.de>
# Contributor: Rustam Tsurik <rustam.tsurik@gmail.com>

pkgname=memsource-editor
_pkg=MemsourceEditor
_platform=ubuntu-12.04
pkgver=4.153
pkgrel=1
pkgdesc="A lightweight CAT tool"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.memsource.com/"

if [ "$CARCH" = "i686" ]; then
  depends=('qt4' 'bzip2' 'libsm' 'libx11' 'libxrender' 'freetype2' 'fontconfig' 'libpng12')
elif [ "$CARCH" = "x86_64" ]; then
  depends=('lib32-qt4' 'lib32-bzip2' 'lib32-libsm' 'lib32-libx11' 'lib32-libxrender' 'lib32-freetype2' 'lib32-fontconfig' 'lib32-libpng12')
fi

options=('!strip')
source=(http://download.memsource.com/production/updates/memsource-editor/linux/${_platform}/archive/install/${_pkg}-${pkgver}-${_platform}.run	memsource-editor.desktop memsource)
sha256sums=('ea13c5b083a974f433e785d9e9cdbc5e97e210eb7255cd916c819c47a54cfb5f'
            '749ee386e3324216f8a84763b98a968ecc3b679cd72c168bf8f4d7d124dbc13a'
            '4802b77ffefd2cdd7526fec28a690f8dc560fef74501806b3485648dcac830fe')

build() {
  mkdir -p "${srcdir}/build"
  cd "${srcdir}/build"
  chmod +x ../${_pkg}-${pkgver}-${_platform}.run

  mkdir -p "${srcdir}/build/Desktop"
  echo "XDG_DESKTOP_DIR=\"${srcdir}/build/Desktop\"" > "${srcdir}/build/user-dirs.dirs"
  export XDG_CONFIG_HOME="${srcdir}/build"
  export HOME="${srcdir}/build"

  ../${_pkg}-${pkgver}-${_platform}.run --mode unattended --prefix "${srcdir}/build/memsource-editor"
}

package() {
  cd "${srcdir}/build/memsource-editor"

  mkdir -p "${pkgdir}/opt/memsource-editor"
  cp -a fonts "${pkgdir}/opt/memsource-editor"
  cp -a lib "${pkgdir}/opt/memsource-editor"
  cp -a spelling "${pkgdir}/opt/memsource-editor"
  cp arch "${pkgdir}/opt/memsource-editor"
  cp cacert.pem "${pkgdir}/opt/memsource-editor"
  cp TranslationEditor "${pkgdir}/opt/memsource-editor"
  chmod +x "${pkgdir}/opt/memsource-editor/TranslationEditor"

  mkdir -p "${pkgdir}/usr/bin"
  cp "${srcdir}/memsource" "${pkgdir}/usr/bin"
  chmod +x "${pkgdir}/usr/bin/memsource"

  mkdir -p "${pkgdir}/usr/share/icons"
  cp app.png "${pkgdir}/usr/share/icons/memsource-editor.png"

  mkdir -p "${pkgdir}/usr/share/applications"
  cp "${srcdir}/memsource-editor.desktop" "${pkgdir}/usr/share/applications"
}
