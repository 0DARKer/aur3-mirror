# Maintainer: twa022 <twa022 at gmail dot com>

_pkgname=cinnamon-screensaver
pkgname=${_pkgname}-git
pkgver=75.07ef885
pkgrel=1
pkgdesc="Screensaver for cinnamon desktop"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/cinnamon-screensaver"
license=('GPL')
backup=(etc/pam.d/cinnamon-screensaver)
depends=('cinnamon-desktop' 'cinnamon-translations' 'dbus-glib' 'libgnomekbd')
makedepends=('intltool' 'gnome-common' 'git')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
options=('!emptydirs')
source=('git+https://github.com/linuxmint/cinnamon-screensaver.git'
        'cinnamon-screensaver.pam'
        'away-message-fixes.patch'
        'fix-systemd-lock.patch'
        'lock_screen_on_suspend.patch')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "${srcdir}/${_pkgname}"

  # Various fixes for away message handling
  patch -Np1 -i ../away-message-fixes.patch

  # Use the session path instead of the session id
  patch -Np1 -i ../fix-systemd-lock.patch

  # Lock screen on suspend with systemd
  patch -Np1 -i ../lock_screen_on_suspend.patch
}

build() {
  cd "${srcdir}/${_pkgname}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon-screensaver \
               --localstatedir=/var \
               --with-mit-ext --with-systemd
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../cinnamon-screensaver.pam "$pkgdir/etc/pam.d/cinnamon-screensaver"
}

sha256sums=('SKIP'
            'b6ea9e2eb586d94bcabb617a8f1c2958111df87afdbb51f645882bccdc15cbda'
            'f9f3d555b0c325322469be765b488da599fc850f8ff5a74f72fdf69f68e04c96'
            '14d3301a03883330bf044a40ea4dce74a829b03174833d21efe66ebeb940dc0c'
            '54f281eeb0e97c8c46250e287719051e0a580f6c0de384760aaa3532b42616f1')
