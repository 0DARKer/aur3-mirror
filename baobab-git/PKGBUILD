# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=baobab
pkgname=$_pkgname-git
pkgver=3.9.2.6.g5187d84
pkgrel=1
pkgdesc="A graphical directory tree analyzer"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=('dconf' "gtk3>=3.9.7" 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool' 'itstool' 'vala' 'gobject-introspection')
groups=('gnome-extra')
provides=('baobab' 'baobab=3.9.2')
conflicts=('baobab')
replace=('baobab')
install=$_pkgname.install
source=('git://git.gnome.org/baobab'
        '0001-Add-class-style-titlebar-to-the-headerbar-for-the-cs.patch')
sha256sums=('SKIP'
            '497f2923430e9cf66d4987cc42c556a84d91575798b251fab98f0f2aa44d1885')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe | sed 's/^v//;s/-/./g;s/_/./g;'
}

prepare() {
  cd "$srcdir/$_pkgname"
  patch -Np1 -i "$srcdir/0001-Add-class-style-titlebar-to-the-headerbar-for-the-cs.patch"
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install

  # https://bugs.archlinux.org/task/31861
  sed -i '/MimeType/d' "$pkgdir/usr/share/applications/baobab.desktop"
}

# vim:set ts=2 sw=2 et:
