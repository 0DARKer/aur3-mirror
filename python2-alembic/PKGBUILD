# Maintainer: googol <googol@posteo.de>
# Former Maintainer unknown (because the line in the PKGBUILD was accidentally removed).
pkgname=python2-alembic
pkgver=0.6.6
pkgrel=1
pkgdesc="A lightweight database migration tool for usage with SQLAlchemy"
arch=('any')
url="https://bitbucket.org/zzzeek/alembic"
license=('MIT')
groups=()
depends=('python2' 'python2-sqlalchemy' 'python2-mako')
source=(https://pypi.python.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz
        https://pypi.python.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz.asc)
sha256sums=('9665660251b8d4351a4aa8ef6d3740e94a4e699d53c720773a00d3650a8b7644'
            'e14f864321ac3c54a0e3908b34227a42c834c37f16fee948f9f07bbe2c97d770')

build() {
  cd "$srcdir"/alembic-${pkgver}
  python2 setup.py build
}

package() {
  cd "$srcdir"/alembic-${pkgver}
  python2 setup.py install --root="$pkgdir/" --prefix="/usr" --optimize=1
  # Avoid conflicts between python2-alembic and python-alembic
  mv $pkgdir/usr/bin/alembic $pkgdir/usr/bin/alembic2
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:

