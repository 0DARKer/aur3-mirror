# Maintainer: googol <googol@posteo.de>
pkgname=python2-alembic
pkgver=0.6.4
pkgrel=1
pkgdesc="A lightweight database migration tool for usage with SQLAlchemy"
arch=('any')
url="https://bitbucket.org/zzzeek/alembic"
license=('MIT')
groups=()
depends=('python2' 'python2-sqlalchemy' 'python2-mako')
source=(https://pypi.python.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz
        https://pypi.python.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz.asc)
sha256sums=('5a0a36c747c28f50366ac046da43f50055c84d1d9d8ed3c09b9b5b9a6c516832'
            '536577bd123b280b6823b8d08e72282150580e488a2d0c5bf0e77dffe33e0e14')

build() {
  cd "$srcdir"/alembic-${pkgver}
  python2 setup.py build
}

package() {
  cd "$srcdir"/alembic-${pkgver}
  python2 setup.py install --root="$pkgdir/" --prefix="/usr" --optimize=1
  # Avoid conflicts between python2-alembic and python-alembic
  mv $pkgdir/usr/bin/alembic $pkgdir/usr/bin/alembic2
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:

