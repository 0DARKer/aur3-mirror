# Maintainer: googol <googol@posteo.de>
pkgname=python2-alembic
pkgver=0.6.3
pkgrel=2
pkgdesc="A lightweight database migration tool for usage with SQLAlchemy"
arch=('any')
url="https://bitbucket.org/zzzeek/alembic"
license=('MIT')
groups=()
depends=('python2' 'python2-sqlalchemy' 'python2-mako')
source=(https://pypi.python.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz
        https://pypi.python.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz.asc)
sha256sums=('35dfad44254bd0731ac611de211957a37dcc8d53b67a134c187e0b32a8b43842'
            '890bd2503d3bc2ecc4620372d83cacf2c04a25349713bf95bc30014c33605da0')

build() {
  cd "$srcdir"/alembic-${pkgver}
  python2 setup.py build
}

package() {
  cd "$srcdir"/alembic-${pkgver}
  python2 setup.py install --root="$pkgdir/" --prefix="/usr" --optimize=1
  # Avoid conflicts between python2-alembic and python-alembic
  mv $pkgdir/usr/bin/alembic $pkgdir/usr/bin/alembic2
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:

