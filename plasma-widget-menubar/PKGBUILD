# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=plasma-widget-menubar
pkgver=0.1.17
pkgrel=1
pkgdesc="A Plasma widget to display menubar of application windows"
arch=('i686' 'x86_64')
url='https://launchpad.net/plasma-widget-menubar'
license=('GPL')
depends=('kdelibs' 'qjson')
makedepends=('cmake' 'automoc4')
optdepends=('appmenu-gtk2: support for GTK+ 2 apps'
            'appmenu-gtk3: support for GTK+ 3 apps'
            'appmenu-qt: support for Qt apps'
            'libreoffice-extension-menubar: support for LibreOffice')
source=("https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.bz2"{,.asc})
md5sums=('d1052e3751e94d8d8fb4ba6b86778db9'
         '853abb965f58e5557f21baebc3fbfc22')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
