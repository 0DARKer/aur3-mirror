# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=sword-svn
pkgver=2823
pkgrel=1
pkgdesc="Libraries for Bible programs - svn -version"
arch=('i686' 'x86_64')
url="http://www.crosswire.org/sword/"
license=('GPL')
depends=('curl' 'clucene' 'swig')
makedepends=('subversion')
provides=('sword')
conflicts=('sword')
source=('sword::svn+https://www.crosswire.org/svn/sword/trunk/' 'automake.patch')
md5sums=('SKIP' '24a4821088da78725b9374807d0d3508')
options=('!makeflags')
_svnmod=sword

pkgver() {
  cd $SRCDEST/${_svnmod}
  svnversion
}

prepare() {
  cd "$srcdir/$_svnmod"
  patch -p1 < $srcdir/automake.patch
}

build() {
  cd "$srcdir/$_svnmod"  
  ./autogen.sh
  CPPFLAGS+="-fpermissive" ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$_svnmod"
  make DESTDIR="$pkgdir/" install
  make DESTDIR="$pkgdir/" install_config
}
