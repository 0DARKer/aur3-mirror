# Maintainer: Lucio zara <pennegaatgmaildotcom>

pkgname=ha-glue
pkgver=1.0.6
pkgrel=5
pkgdesc="A set of libraries, tools and utilities suitable for the Heartbeat/Pacemaker cluster stack."
arch=('i686' 'x86_64')
url="http://linux-ha.org/wiki/Cluster_Glue"
license=('GPL')
depends=('net-snmp' 
	'python' 
	'libxml2' 
	'libtool' 
	'util-linux-ng' 
	'curl' 
	'glib2')
source=("http://hg.linux-ha.org/glue/archive/glue-${pkgver}.tar.bz2")
md5sums=('09df68da0073eb856415400ddc529707')
install='ha-glue.install'
options=('!libtool' 'docs')
build() {
	CLUSTER_USER=hacluster
	CLUSTER_GROUP=haclient
	PREFIX=/usr
	PREFIXETC=/etc
	PREFIXINIT=${PREFIXETC}/rc.d
	PREFIXVAR=/var
	PREFIXLIBEXEC=${PREFIX}/lib/${pkgname}
	LCRSODIR=$PREFIX/lib/lcrso
	DGID=666
	DUID=666
	if ! getent group ${CLUSTER_GROUP} >/dev/null;then
		echo -e "\nBEFORE COMPILE THIS SOFTWARE YOU MUST CREATE A CLUSTER GROUP, EXECUTE AS ROOT:"
		echo -e "groupadd -r -g ${DGID} ${CLUSTER_GROUP}\n"
		EXITCODE=1
	fi
	if ! getent passwd ${CLUSTER_USER} >/dev/null;then
		echo -e "\nBEFORE COMPILE THIS SOFTWARE YOU MUST CREATE A CLUSTER USER, EXECUTE AS ROOT:"
		echo -e "useradd -r -g ${CLUSTER_GROUP} -u ${DUID} -d /var/lib/heartbeat/cores/hacluster -s /sbin/nologin -c \"cluster user\" ${CLUSTER_USER}\n"
		EXITCODE=1
	fi
	if [[ $EXITCODE -eq 1 ]] ;then
		return 1 
	fi
	cd "${srcdir}/Reusable-Cluster-Components-glue-${pkgver}"
	./autogen.sh
	./configure 							\
		--prefix=${PREFIX} 					\
		--localstatedir=${PREFIXVAR} 		\
		--sysconfdir=${PREFIXETC}			\
		--with-daemon-user=${CLUSTER_USER} 	\
		--with-daemon-group=${CLUSTER_GROUP}\
		--libexecdir=${PREFIXLIBEXEC}		\
		--with-initdir=${PREFIXINIT}		\
		--enable-fatal-warnings=no
	make || return 1
	make DESTDIR="${pkgdir}" install || return 1
	rm ${pkgdir}/usr/include/heartbeat/compress.h 
	rm ${pkgdir}/usr/include/heartbeat/ha_msg.h
}

