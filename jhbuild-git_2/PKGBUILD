# Maintainer: Vasil Yonkov <bustervill at gmail dot com>
pkgname=jhbuild-git_2
pkgver=20130203
pkgrel=1
pkgdesc="A tool designed to ease building collections of source packages."
arch=('i686' 'x86_64')
url="https://live.gnome.org/Jhbuild"
license=('GPL')
depends=('python2')
makedepends=('autoconf'
             'automake-1.12'
             'libtool'
             'gettext'
             'pkgconfig'
             'rsync'
             'subversion'
             'git'
             'intltool'
             'gnome-doc-utils'
             'yelp-tools'
             'gnome-common-git')
optdepends=('docbook-xsl: required by jhbuild sanitycheck'
            'cvs: required by jhbuild sanitycheck')
provides=('jhbuild')
conflicts=('jhbuild' 'jhbuild-git')
install=jhbuild.install
source=(use_python2_when_building_modules.patch)
md5sums=(ddce421e6f124887580c17b56de4069e)

_gitroot="git://git.gnome.org/jhbuild"
_gitname="jhbuild"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."
  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth=1 $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  mkdir "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname" && ls -A | grep -v .git | xargs -d '\n' cp -r -t ../$_gitname-build
  cd "$srcdir/$_gitname-build"

  patch -p1 < "$srcdir/../use_python2_when_building_modules.patch"

  ./autogen.sh --prefix=/usr PYTHON=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
  install -d "$pkgdir/usr/share/jhbuild/"
  cp -dr modulesets "$pkgdir/usr/share/jhbuild/"
  sed -ir '1 s/python/python2/' "$pkgdir/usr/bin/jhbuild"
  install -d "$pkgdir/usr/lib/pkgconfig/"
  ln -sr "$pkgdir/usr/lib/pkgconfig/python-2.7.pc" "$pkgdir/usr/lib/pkgconfig/python.pc"
}

# vim:set ts=2 sw=2 et:
