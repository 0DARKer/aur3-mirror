# Maintainer: Moritz Maxeiner <moritz@ucworks.org>
pkgname=hts-engine
pkgver=1.07
pkgrel=2
pkgdesc="Engine to synthesize speech waveform from HMMs trained by hts."
arch=('i686' 'x86_64')
url="http://hts-engine.sourceforge.net/"
license=('BSD')
depends=('glibc')
source=(http://downloads.sourceforge.net/$pkgname/hts_engine_API-$pkgver.tar.gz
        64bitcompat.patch::"http://hts-engine.cvs.sourceforge.net/viewvc/hts-engine/hts_engine_API/src/lib/HTS_model.c?r1=1.22&r2=1.23&view=patch")
md5sums=('6fe76a74adeca1be0a71c41d5237423a'
         'ea14e6ad32bc7552799782aa92fa2b7a')
sha256sums=('3bc0e2e1ff81a828a1eb4b33af7d90ca1b5868f6f2234f8c98c5a05bdac97f1f'
            'b7337998c6b97fcf36e7ae5612742d98855b2e4bae34b487444c79da585fe7e7')

build() {

  if [ "${CARCH}" = "x86_64" ]; then
    cd "$srcdir/hts_engine_API-$pkgver/lib"
    patch -p0 -i ${srcdir}/64bitcompat.patch
  fi

  cd "$srcdir/hts_engine_API-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/hts_engine_API-$pkgver"
  make prefix="$pkgdir/usr" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
