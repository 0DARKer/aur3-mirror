# $Id$
# Maintainer: Radioactiveman <thomas-lange2@gmx.de>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious-git
pkgver=2014.04.23
pkgrel=1
pkgdesc='Lightweight, advanced audio player focused on audio quality'
url='http://audacious-media-player.org/'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('gtk3' 'glib2' 'libguess' 'libsm' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('pkg-config' 'python2' 'git')
optdepends=('unzip: zipped skins support')
provides=('audacious')
conflicts=('audacious')
source=('git://github.com/audacious-media-player/audacious.git')
install="$pkgname.install"
sha256sums=('SKIP')

pkgver() {
    date +%Y.%m.%d
}

build() {
    cd "${srcdir}/audacious"
    autoreconf -I m4
    ./configure --prefix=/usr --with-buildstamp="$(date +%x)"
    make
}

package() {
    cd "${srcdir}/audacious"
    make DESTDIR="${pkgdir}" install
    install -m755 -d "${pkgdir}/usr/share/licenses/audacious"
    ln -s "/usr/share/audacious/COPYING" "${pkgdir}/usr/share/licenses/audacious/LICENSE"
}
