# Maintainer: Radioactiveman <thomas-lange2@gmx.de>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious-git
pkgver=20110624
pkgrel=1
pkgdesc="Lightweight, GTK3-based advanced audio player focused on audio quality."
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL3')
depends=('gtk3' 'libmcs' 'dbus-glib' 'libguess' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('pkg-config' 'git')
optdepends=('unzip: zipped skins support')
provides=('audacious')
conflicts=('audacious' 'audacious-player-hg')
install=install

_gitroot="git://git.atheme.org/audacious"
_gitname="audacious"

build() {
    msg "Connecting to GIT server..."

    if [[ -d ${_gitname} ]]; then
        ( cd ${_gitname} && git pull origin )
        msg "The local files are updated."
    else
        git clone ${_gitroot}
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf ${_gitname}-build
    git clone ${_gitname}{,-build}
    cd ${_gitname}-build

    ./autogen.sh
    [[ ${CARCH} == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'
    ./configure --prefix=/usr ${extraopts}
    make
}

package() {
    cd "${srcdir}/${_gitname}-build"
    make DESTDIR="${pkgdir}" install
}
