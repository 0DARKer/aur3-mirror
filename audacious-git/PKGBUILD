# $Id$
# Maintainer: Radioactiveman <thomas-lange2@gmx.de>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious-git
pkgver=2014.12.10
pkgrel=1
pkgdesc='Lightweight, advanced audio player focused on audio quality'
url='http://audacious-media-player.org/'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('gtk2' 'libguess' 'libsm' 'gnome-icon-theme' 'desktop-file-utils')
       # 'qt5-base' 'libxkbcommon-x11')
makedepends=('python2' 'git')
optdepends=('unzip: zipped skins support')
provides=('audacious')
conflicts=('audacious')
install="$pkgname.install"
source=('git://github.com/audacious-media-player/audacious.git') # #branch=gtk3')
sha256sums=('SKIP')

pkgver() {
    date +%Y.%m.%d
}

build() {
    cd "${srcdir}/audacious"
    autoreconf -I m4
    ./configure --prefix=/usr --with-buildstamp="$(date +%x)" # --enable-qt
    make
}

package() {
    cd "${srcdir}/audacious"
    make DESTDIR="${pkgdir}" install
    install -m755 -d "${pkgdir}/usr/share/licenses/audacious"
    ln -s "/usr/share/audacious/COPYING" "${pkgdir}/usr/share/licenses/audacious/LICENSE"
}
