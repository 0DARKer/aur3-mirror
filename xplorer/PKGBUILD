# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=xplorer
pkgver=0.9.3
pkgrel=1
pkgdesc="A filemanager for X-Windows with fast and accurate file type detection"
arch=(i686 x86_64)
url="http://cxplorer.sourceforge.net/"
license=('GPL')
depends=('bzip2' 'libjpeg-turbo' 'libtiff' 'fontconfig' 'giflib' 'libxrender' 'libpng')
makedepends=('libtool')
options=('!libtool')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/cxplorer/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('714d57b5c070b2fb6ab9a5f7e0fa4e63'
         'd2950df2c3687f4e4d238a2cadc08a8b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 cxxx/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
