# Maintainer: Kwpolska <kwpolska@kwpolska.tk>
# Contributor (official repo maintainer): Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center-fixedicons
pkgver=3.2.2
pkgrel=2
pkgdesc="The Control Center for GNOME, with fixed sound applet icons (non-Shell)"
arch=('i686' 'x86_64')
depends=('gtk3' 'gsettings-desktop-schemas' 'gnome-menus' 'gnome-desktop' 'gnome-settings-daemon' 'upower' 'libgtop' 'cups-pk-helper' 'accountsservice' 'sound-theme-freedesktop' 'gnome-online-accounts' 'network-manager-applet')
optdepends=('mesa-demos: provides glxinfo for graphics information'
            'apg: adds password generation for user accounts'
            'gnome-color-manager: for color management tasks')
makedepends=('gnome-doc-utils' 'intltool')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-control-center.install
provides=('gnome-control-center')
conflicts=('gnome-control-center')
license=('GPL')
options=('!libtool' '!emptydirs')
source=(http://download.gnome.org/sources/gnome-control-center/3.2/gnome-control-center-${pkgver}.tar.xz gvc-applet.c.patch)
sha256sums=('e675f8cfde06775919eb0c9fa4ee8c61310702989eecac6083098beb5afd9369'
            '55839407b0407031341a9694f962a7fc1ec8126dde53abac30f0c097a05d1f9c')

build() {
  cd "${srcdir}/gnome-control-center-${pkgver}"
  patch panels/sound/gvc-applet.c ${srcdir}/gvc-applet.c.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --disable-update-mimedb

  #https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  #sound icon fix
  make
}

package() {
  cd "${srcdir}/gnome-control-center-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

