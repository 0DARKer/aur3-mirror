pkgname=mingw-w64-python2-native
pkgver=2.7.6
pkgrel=1
_pybasever=2.7
pkgdesc="A high-level scripting language (native)"
arch=('any')
license=('PSF')
url="http://www.python.org/"
provides=('mingw-w64-python2')
conflicts=('mingw-w64-python2')
depends=('mingw-w64-crt')
makedepends=('wine')
options=('staticlibs' '!buildflags' '!strip')
source=("http://www.python.org/ftp/python/${pkgver}/python-${pkgver}.msi"
        "http://www.python.org/ftp/python/${pkgver}/python-${pkgver}.amd64.msi")
md5sums=('ac54e14f7ba180253b9bae6635d822ea'
         'b73f8753c76924bc7b75afaa6d304645' )

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}"
  for _arch in ${_architectures}; do
    target=""
    if test "${_arch}" = x86_64-w64-mingw32
    then
      target=".amd64"
    fi
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    msiexec /a "${srcdir}"/python-${pkgver}${target}.msi /qb TARGETDIR=$PWD
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    install -d "$pkgdir"/usr/${_arch}/lib
    install -m644 libs/libpython27.a "$pkgdir"/usr/${_arch}/lib || echo "?!"
    install -d "$pkgdir"/usr/${_arch}/bin
    cp -r include "$pkgdir"/usr/${_arch}/
    install -m755 python27.dll "$pkgdir"/usr/${_arch}/bin
    install -m755 python.exe "$pkgdir"/usr/${_arch}/bin/python2.exe
    cp -r Lib "$pkgdir"/usr/${_arch}/ 
  done
}
