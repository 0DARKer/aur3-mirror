# Maintainer: libernux <dutchman55@gmx.com>
# Contributor: kimotori <kimotori@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
license=('GPL')
arch=('i686' 'x86_64')
pkgname=brscan
pkgver=0.2.4
pkgrel=1
pkgdesc="SANE drivers from Brother for USB scanners"
depends=('sane' 'libusb-compat')
makedepends=('sane')
if [ "$CARCH" == "i686" ]; then
  _filename="$pkgname-$pkgver-0.i386.deb"
  md5sums=('a89873d5306282fcf8dafe426ba9df33')
else
  _filename="$pkgname-$pkgver-0.amd64.deb"
  md5sums=('b3aa41e55ea17600eca87ab0f761e9b0')
fi
source="http://www.brother.com/pub/bsc/linux/dlf/$_filename"
url="http://welcome.solutions.brother.com/bsc/public_s/id/linux/en/index.html"
license=("GPL" 'custom:brother commercial license')
install="$pkgname.install"

package() {
  cd "$srcdir"
  ar -x ../$_filename
  cd "$pkgdir"
  tar -xzf $srcdir/data.tar.gz

  # On x86_64, install in "/usr/lib" instead of "/usr/lib64"
  if [ "$CARCH" == "x86_64" ]; then
    cd "$pkgdir/usr/"
    mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib"
    # some links are wrong now - recreate them
    for fn in lib/libbrcolm.so.1.0.1 \
        lib/libbrscandec.so.1.0.0 \
        lib/sane/libsane-brother.so.1.0.7
    do
        # break, if file does not exist
        if [ ! -f $fn ] ; then
            echo "ERROR: $fn does not exist"
            return 1
        fi
        base=`echo $fn | cut -d. -f1`.so
        major=`echo $fn | cut -d. -f3`
        ln -sf `basename $fn` $base
        ln -sf `basename $fn` $base.$major
    done
  fi
}