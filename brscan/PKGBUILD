# Maintainer: libernux <dutchman55@gmx.com>
# Contributor: kimotori <kimotori@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
arch=('i686' 'x86_64')
pkgname=brscan
pkgver=0.2.4
pkgrel=1
pkgdesc="SANE drivers from Brother for USB scanners"
depends=('sane' 'libusb-compat')
makedepends=('sane')
if [ "$CARCH" == "i686" ]; then
  _filename="$pkgname-$pkgver-0.i386.rpm"
  md5sums=('cd93d6b435adca6bc07f321fe716faea')
else
  _filename="$pkgname-$pkgver-0.x86_64.rpm"
  md5sums=('f1b3e42fab7bf9ad157428edfd4aa589')
fi
source="http://www.brother.com/pub/bsc/linux/dlf/$_filename"
url="http://welcome.solutions.brother.com/bsc/public_s/id/linux/en/index.html"
license=("GPL" 'custom:brother commercial license')
install="$pkgname.install"

package() {
  cp -R "$srcdir/usr" "$pkgdir"
  # On x86_64, install in "/usr/lib" instead of "/usr/lib64"
  if [ "$CARCH" == "x86_64" ]; then
    cd "$pkgdir/usr/"
    mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib"
    # some links are wrong now - recreate them
    for fn in lib/libbrcolm.so.1.0.1 \
        lib/libbrscandec.so.1.0.0 \
        lib/sane/libsane-brother.so.1.0.7
    do
        # break, if file does not exist
        if [ ! -f $fn ] ; then
            echo "ERROR: $fn does not exist"
            return 1
        fi
        base=`echo $fn | cut -d. -f1`.so
        major=`echo $fn | cut -d. -f3`
        ln -sf `basename $fn` $base
        ln -sf `basename $fn` $base.$major
    done
  fi
}