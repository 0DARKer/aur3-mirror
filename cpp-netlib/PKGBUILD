# $Id$
# Maintainer: imotec <info(at)imotec(dot)nl>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

# for customization
_pkgname=cpp-netlib
_ext=
_basedir=/usr
_boost=boost${_ext}
# note: depends=() needs separate customization

pkgname=${_pkgname}${_ext}
pkgver=0.11.0
pkgrel=2
pkgdesc="A library that provides application layer protocol support using modern C++ techniques"
arch=('i686' 'x86_64')
url="http://cpp-netlib.github.com/"
license=('custom')
depends=("boost${_ext}>=1.54.0")
optdepends=('openssl: for https')
makedepends=("cmake>=2.8.10")
#options=(staticlibs)
source=(
         http://commondatastorage.googleapis.com/$_pkgname-downloads/$pkgver/$_pkgname-$pkgver.tar.gz
         CMakeListsMimeRoundtrip.txt.diff
         )
md5sums=(
         '6fba4f2f64f19af9f172fd60ad8e76ab'
         '16d7a8c2fee08eeb1b4fc82e682aaf81'
         )

build() {
  msg "Applying patches..."
  cd "$srcdir/$_pkgname-$pkgver-final"
  patch -Np0 -i "${srcdir}/CMakeListsMimeRoundtrip.txt.diff"
  install -d $srcdir/build
  cd $srcdir/build
  cmake -DBOOST_ROOT=$_basedir -DCPP-NETLIB_BUILD_SHARED_LIBS=ON  -DCMAKE_INSTALL_PREFIX:PATH=${pkgdir}${_basedir} ../"$_pkgname-$pkgver-final"
  make
#  make test
}

package() {
  cd $srcdir/build
  make install

  if test "$CARCH" == x86_64; then
    mv ${pkgdir}/usr/lib64/* ${pkgdir}/usr/lib
    rmdir ${pkgdir}/usr/lib64
  fi
  mv ${pkgdir}/usr/lib/CMake ${pkgdir}/usr/lib/cmake

  #Install license
  install -d ${pkgdir}/usr/share/licenses/$pkgname
  cd "$srcdir/$_pkgname-$pkgver-final"
  install -Dm644 LICENSE_1_0.txt ${pkgdir}/usr/share/licenses/$pkgname/LICENSE_1_0.txt

}

