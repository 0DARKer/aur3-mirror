# Contributor: Antonio Rojas

pkgname=libkscreen-frameworks
pkgver=5.0.92
plasmaver=5.0.1
pkgrel=1
pkgdesc='KDE screen management software'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libkscreen'
license=('LGPL')
depends=('qt5-x11extras' 'libxrandr')
makedepends=('extra-cmake-modules')
replaces=('libkscreen2')
conflicts=('libkscreen2')
provides=('libkscreen2')
source=("http://download.kde.org/stable/plasma/$plasmaver/libkscreen-$pkgver.tar.xz")
md5sums=('a98fba8e13a9264fc5792957ad563fe8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libkscreen-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
