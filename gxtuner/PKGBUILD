# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gxtuner
pkgver=1.7
pkgrel=1
pkgdesc="A simple, small and lightweight guitar/bass tuner for jack."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/guitarix/"
license=('GPL')
depends=('gtk2' 'zita-resampler>=1.1.0' 'fftw' 'jack')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/guitarix/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('e6a193ea889300335a4be3f4a9bf3100')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # zita-resampler 1.1.0 patch
  sed -i 's/zita-\(resampler\)/&\/\1/' pitchtracker.h

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # prepare dirs
  install -d "$pkgdir"/usr/{bin,share/{applications,pixmaps}}

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
