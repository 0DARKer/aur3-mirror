# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gxtuner
pkgver=2.0
pkgrel=1
pkgdesc="A simple, small and lightweight guitar/bass tuner for jack."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/guitarix/"
license=('GPL')
depends=('gtk2' 'zita-resampler>=1.1.0' 'fftw' 'jack')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/guitarix/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('c9b81954c476651ff90dec84ff692be1')

build() {
  cd "$srcdir/$pkgname"

  # zita-resampler 1.1.0 patch
  sed -i 's/zita-\(resampler\)/&\/\1/' pitchtracker.h

  # DSO link fix
  LDFLAGS+=" -lX11" make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
