# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubikey-personalization-git
pkgver=1.12.0.9.g67061ee
pkgrel=1
pkgdesc="Yubico yubikey personalization library"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubikey-personalization"
license=('BSD')
depends=('libusb' 'yubico-c-client-git')
makedepends=('git')
provides=('ykpers' 'yubikey-personalization')
conflicts=('ykpers' 'yubikey-personalization')
source=('git://github.com/Yubico/yubikey-personalization.git')

pkgver() {
	cd yubikey-personalization/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd yubikey-personalization/

	autoreconf --install
	./configure --prefix=/usr
	make
}

check() {
	cd yubikey-personalization/

	make check
}

package() {
	cd yubikey-personalization/
  
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/yubikey-personalization/COPYING"
	install -D -m644 README "${pkgdir}/usr/share/doc/yubikey-personalization/README"
	make DESTDIR="${pkgdir}/" install
} 

sha256sums=('SKIP')
