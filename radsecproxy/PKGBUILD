# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=radsecproxy
pkgver=1.6.5
pkgrel=5
pkgdesc="a generic RADIUS proxy that in addition to to usual RADIUS UDP transport, also supports TLS (RadSec), as well as RADIUS over TCP and DTLS"
arch=('i686' 'x86_64')
url="https://software.uninett.no/radsecproxy/"
depends=('openssl')
optdepends=('freeradius: radius server')
license=('GPL')
backup=('etc/radsecproxy/radsecproxy.conf')
source=("https://software.uninett.no/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
	'0001-Close-a-server-writer-thread-if-SSL_write-fails.patch'
	'0002-Emit-an-error-log-line-if-client-writer-fails-writin.patch'
	'radsecproxy.service')
validpgpkeys=('8C4CD511095E982EB0EFBFA21E8BF34923291265')

prepare() {
	cd ${pkgname}-${pkgver}/

	patch -Np1 < "${srcdir}/0001-Close-a-server-writer-thread-if-SSL_write-fails.patch"
	patch -Np1 < "${srcdir}/0002-Emit-an-error-log-line-if-client-writer-fails-writin.patch"
}


build() {
	cd ${pkgname}-${pkgver}/

	./configure --prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/radsecproxy
	make
}

package() {
	cd ${pkgname}-${pkgver}/

	make DESTDIR="${pkgdir}" install

	install -D -m0644 radsecproxy.conf.5 "${pkgdir}"/usr/share/man/man5/radsecproxy.conf.5
	install -D -m0644 radsecproxy.conf-example "${pkgdir}"/etc/radsecproxy/radsecproxy.conf

	install -D -m0644 ${srcdir}/radsecproxy.service "${pkgdir}"/usr/lib/systemd/system/radsecproxy.service
}

sha256sums=('b0b7718c84a73ee2af48684cb5c9f3d76369c7e3a4ad3258b919769b4dc65e5f'
            'SKIP'
            '9364d404d12c2d8760e4fc749ba86e95d5c33f9b3695f573142573a650120019'
            '62cfda192032d74e3212e46cd6045cd682ac614ebb6d17c9138a4b151d65d5bd'
            '9f1c028311d716651fc33aa6d4a44676af03330b8297a2f095259203055bc923')
