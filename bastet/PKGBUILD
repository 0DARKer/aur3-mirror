# Maintainer: Jens Adam (byte/jra) <j_adam@web.de>

pkgname=bastet
pkgver=0.43
pkgrel=2
pkgdesc="Tetris(r) clone with 'bastard' block-choosing AI"
url="http://fph.altervista.org/prog/bastet.html"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('boost-libs' 'ncurses')
makedepends=('boost')
backup=('var/games/bastet.scores2')
source=("http://fph.altervista.org/prog/files/${pkgname}-${pkgver}.tgz")
md5sums=('b47090daa7b6d89b98b5b477cf155733')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}/
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/

  # install binary
  install -D bastet "${pkgdir}"/usr/bin/bastet

  # install manpage
  install -Dm644 bastet.6 "${pkgdir}"/usr/share/man/man6/bastet.6

  # prepare highscore file
  # (seems to work, so ignore the startup error message)
  install -dm775 -g games "${pkgdir}"/var/games/
  touch "${pkgdir}"/var/games/bastet.scores2
  chmod 664 "${pkgdir}"/var/games/bastet.scores2
  chown root:games "${pkgdir}"/var/games/bastet.scores2
}
