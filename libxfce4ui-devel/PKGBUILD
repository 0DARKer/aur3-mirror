# Maintainer:  cuihao <cuihao dot leo at gmail dot com>

# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: AndyRTR <andyrtr@archlinux.org>

# Original PKGBUILD (extra/libxfce4ui):
# $Id: PKGBUILD 145607 2011-12-23 17:37:26Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=libxfce4ui-devel
_pkgname=libxfce4ui
pkgver=4.9.1
pkgrel=1
pkgdesc="Commonly used Xfce widgets among Xfce applications (Development Version)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('gtk2' 'xfconf>=4.8' 'libsm' 'startup-notification')
makedepends=('intltool' 'gtk-doc' 'glade')
optdepends=('glade: for using the included glade module')
options=('!libtool')
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.9/$_pkgname-$pkgver.tar.bz2)
sha1sums=('7922794a52041a0d756220a719c1921377346877')

groups=('xfce4-devel')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname" "$_pkgname-git")

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/bin"
  rm -rf "$pkgdir/usr/share/applications"
  rm -rf "$pkgdir/usr/share/icons"
}

# vim:set ts=2 sw=2 et:
