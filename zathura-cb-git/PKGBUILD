# Maintainer: Moritz Lipp <mlq@pwmt.org>
pkgname=zathura-cb-git
pkgver=20120406
pkgrel=1
pkgdesc="Comic book support for zathura"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/zathura/plugins/zathura-cb"
license=('custom')
depends=('zathura-git')
makedepends=('git')
conflicts=('zathura-cb')
provides=('zathura-cb' 'zathura-cb-git')
optdepends=(
  'unrar: For *.cbr support'
  'unzip: For *.cbz support'
  'p7zip: For *.cb7 support'
  'tar:   For *.cbt support'
)

_gitname="zathura-cb"
_gitroot="git://pwmt.org/$_gitname.git"

build() {
  cd ${srcdir}
  msg "Connecting to GIT server..."

  if [ -d $_gitname ] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname" 
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  if [ -d "$srcdir/$_gitname-build" ]; then
    rm -rf "$srcdir/$_gitname-build"
  fi

  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build" || return 1
  cd "$srcdir/$_gitname-build" || return 1

  make
}

package () {
  cd "${srcdir}/$_gitname-build"
  make DESTDIR="$pkgdir" install
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
