# $Id: PKGBUILD 120599 2011-06-27 10:53:43Z TBastiani $
# Maintainer: Thomas Bastiani <thom@codehawks.eu>

pkgname=evolution-light
originalpkgname=evolution
pkgver=3.0.2
pkgrel=1
pkgdesc="A light version of Evolution without palm pilot, exchange, ldap, nntp and network manager support"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-desktop' 'evolution-data-server' 'gtkhtml4' 'libcanberra' 'libsm' 'gstreamer0.10' 'libunique3' 'libnotify' 'libpst' 'libytnef'
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('bogofilter: possible junk filter plugin'
            'spamassassin: possible junk filter plugin')
#groups=('gnome-extra')
options=('!libtool' '!emptydirs')
provides=('evolution')
conflicts=('evolution')
install=evolution.install
url=http://www.gnome.org/projects/evolution/
source=(http://ftp.gnome.org/pub/gnome/sources/${originalpkgname}/${pkgver%.*}/${originalpkgname}-${pkgver}.tar.bz2)
sha256sums=('6fdf6a39a89bb04c406225984e5b730e86a370024f30ef9cf019840be9fbe59a')

build() {
  cd "${srcdir}/${originalpkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib \
      --disable-scrollkeeper \
      --enable-nss=yes \
      --with-openldap=no \
      --enable-smime=yes \
      --enable-pilot-conduits=no \
      --enable-nntp=no \
      --enable-exchange=no \
      --enable-nm=no \
      --with-krb5=/usr \
      --disable-image-inline
  make
  make -C plugins/tnef-attachments
}

package() {
  cd "${srcdir}/${originalpkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  make -C plugins/tnef-attachments DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${originalpkgname}.schemas" --domain evolution-3.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
