# $Id: PKGBUILD 54769 2009-10-11 19:17:08Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-light
pkgver=2.28.0
_majorver=2.28
pkgrel=1
pkgdesc="A light version of evolution without palm pilot, exchange, ldap, nntp and network manager support"
arch=(i686 x86_64)
license=('GPL')
depends=('libnotify>=0.4.5' 'gtkhtml>=3.28.0' 'evolution-data-server>=2.28.0' 'gstreamer0.10>=0.10.24' 'libpst>=0.6.41' 'libytnef>=0.1.5' 'libgnomeui>=2.24.1' 'gnome-desktop>=2.28.0')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.18.0' 'intltool' )
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
provides=('evolution')
conflicts=('evolution')
install=evolution.install
url=http://www.gnome.org/projects/evolution/
source=(http://ftp.gnome.org/pub/gnome/sources/evolution/2.28/evolution-${pkgver}.tar.bz2)
sha256sums=('c93920a1e5e8391487b31a3d28c21e0a4b5131285fdafa6cad9fd6ee6cdf3ba1')

build() {
  cd "${srcdir}/evolution-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib \
      --disable-scrollkeeper \
      --enable-nss=yes \
      --with-openldap=no \
      --enable-smime=yes \
      --enable-pilot-conduits=no \
      --enable-nntp=no \
      --enable-exchange=no \
      --enable-nm=no \
      --with-krb5=/usr || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  cd plugins/tnef-attachments || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/evolution.schemas" --domain evolution-${_majorver} ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
