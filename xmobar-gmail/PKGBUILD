# Maintainer: Alessandro Pezzoni <alessandro_pezzoni@lavabit.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=xmobar-gmail
pkgver=0.18
pkgrel=1
pkgdesc="A Minimalistic Text Based Status Bar. With GMail monitor."
url="http://hackage.haskell.org/package/xmobar"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('gmp' 'libxft' 'libxinerama' 'wireless_tools' 'libxrandr')
makedepends=('ghc=7.6.3-1' 'haskell-x11' 'haskell-x11-xft' 'haskell-utf8-string'
	     'haskell-stm' 'haskell-parsec' 'haskell-mtl' 'haskell-regex-compat')
conflicts=('xmobar')
provides=('xmobar')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/xmobar/$pkgver/xmobar-$pkgver.tar.gz
       monitor_gmail.patch)

build() {
    cd ${srcdir}/xmobar-$pkgver

    msg "Applying GMail patch"
    patch -p1 -i ${srcdir}/monitor_gmail.patch

    runhaskell Setup configure --prefix=/usr --flags="with_utf8 with_xft with_iwlib with_gmail"
    runhaskell Setup build
}

package() {
    cd ${srcdir}/xmobar-$pkgver
    runhaskell Setup copy --destdir=${pkgdir}

    # A little cleanup
    rm ${pkgdir}/usr/share/doc/xmobar-${pkgver}/license
    rmdir ${pkgdir}/usr/share/doc/xmobar-${pkgver}

    install -D -m644 license ${pkgdir}/usr/share/licenses/$pkgname/LICENSE

    install -d ${pkgdir}/usr/share/doc/$pkgname/samples/Plugins
    install -m644 -t ${pkgdir}/usr/share/doc/$pkgname/ \
                     {readme,news}.md
    install -m644 -t ${pkgdir}/usr/share/doc/$pkgname/samples \
                     samples/{xmobar.config,xmonadpropwrite.hs}
    install -m644 -t ${pkgdir}/usr/share/doc/$pkgname/samples/Plugins \
                     samples/Plugins/{HelloWorld.hs,helloworld.config}
}

md5sums=('4d0502fa75cd51ff23e41e1711f25adb'
         '704293adc6776b7418288cfdaf8ec877')
