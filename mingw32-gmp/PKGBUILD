# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>

pkgname=mingw32-gmp
pkgver=5.0.1
pkgrel=2
arch=('any')
pkgdesc="A free library for arbitrary precision arithmetic (mingw32)"
depends=('mingw32-runtime')
makedepends=('mingw32-gcc')
options=(!strip)
license=('LGPL3')
url="http://gmplib.org/"
source=(ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.bz2)
md5sums=('6bac6df75c192a13419dfd71d19240a7')


build() {
  cd "${srcdir}/${pkgname#mingw32-}-${pkgver}"

  LDFLAGS=${LDFLAGS//-Wl,--hash-style=gnu} # mingw-ld chokes on --hash-style=gnu

  export ABI="32"

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --target=i486-mingw32 \
    --host=i486-mingw32 \
    --build=${CHOST} || exit 1

  make || exit 1
  make install DESTDIR=$pkgdir || exit 1

  cd $pkgdir
  find . -name '*.a' | xargs -rtl1 i486-mingw32-strip -g
  find . -name '*.la' -delete
}

# vim:set ts=2 sw=2 et:
