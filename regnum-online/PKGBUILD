# Maintainer: Frikilinux <frikilinux at frikilinux.com.ar>

pkgname=regnum-online
pkgver=1.6.6
pkgrel=1
pkgdesc="Cross-platform 3D Massively Multiplayer Online Role-playing Game (MMORPG)"
arch=('i686' 'x86_64')
_arch=32
url="http://www.regnumonline.com.ar"
license=('custom:Regnum Online License Agreement')
depends=('gtk2' 'mesa' 'openal' 'libjpeg' 'libvorbis' 'libtheora')
makedepends=()
install=regnum-online.install
options=()
sha256sums=('ad1019165308f05ac4ead8af1afdd6e9c832da612811b493944667479ee7c57a'
            'e11e273168d04adc3db8e8e5c23c127cb2ca5be7f99dbaf891313ff665b79596'
            '294f5308340ddc50d4e54a26964cbc18ba92d2bac95c97bc7c78a4eb1d71b21a'
            'a7ab3edda632f9ae4a80dfa1dd5ae360c85ab3b449f40303855fa7c804561b20'
            '8a8d907aee0008ee6af4b34e7e5f0779b7830c6d61b74dfe0772bc63c1e519be')

[ "$CARCH" = "x86_64" ] && sha256sums[0]='73e55d930bf71907da882aa54c7533102902e99de21f3ab252eab12a3a68bf2c'
[ "$CARCH" = "x86_64" ] && _arch=64
#[ "$CARCH" = "x86_64" ] && makedepends=('lib32-glibc')

source=(http://download01.regnumonlinegame.com/downloads/installer/RegnumOnlineInstall_$_arch
	regnum-online.desktop
	regnum-online
	LICENSE
	regnum.png)

build() {
        cd $srcdir
        mkdir -p reg-extract
        chmod +x RegnumOnlineInstall_$_arch
        ./RegnumOnlineInstall_$_arch --prefix "${srcdir}"/reg-extract --mode silent
}
package() {
        mkdir -p "${pkgdir}"/opt/regnum
        cd "${pkgdir}"/opt/regnum
        mv -f "${srcdir}"/reg-extract/* .
        rm -rf */
        rm -rf {uninstall,RegnumOnline.exe}
        find -type f -exec chmod 664 {} +
        chmod +x rolauncher
        chmod 2775 "${pkgdir}"/opt/regnum
        chown -R root:games "${pkgdir}"/opt/regnum
        install -Dm755 "${srcdir}"/regnum-online "${pkgdir}"/usr/bin/$pkgname
        install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
        install -Dm644 "${srcdir}"/regnum-online.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
        install -Dm644 "${srcdir}"/regnum.png "${pkgdir}"/usr/share/pixmaps/regnum.png
}

