# Contributor: Philipp Schmitt (attentah@NOSPAM.gmail.com)

pkgname=phoebetria
pkgver=1.3.1
pkgrel=4
pkgdesc="Controller software for the BitFenix Recon fan controller."
arch=('x86_64')
url="http://phoebetria.com/"
license=('GPL')
depends=('qt' 'rpmextract' 'systemd-tools' 'libusbx')
makedepends=('gcc')
install="$pkgname.install"
source=(http://downloads.sourceforge.net/project/phoebetria/$pkgname-fc17-$arch-$pkgver.src.rpm)
md5sums=('d9f5302341ec22b22528375ba76f7b73')


build() {
    cd "$srcdir"
    rpmextract.sh "$pkgname-fc17-x86_64-$pkgver.src.rpm"
    tar xvzf "$pkgname-$pkgver.tar.gz"
    cd "$pkgname-$pkgver"
    # Lowercase the binary name 
    sed -i 's/\(TARGET = \)P\(hoebetria\)/\1p\2/' Phoebetria.pro
    sed -i 's/\(Exec=\)P\(hoebetria\)/\1p\2/' phoebetria.desktop
    qmake && make 
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make -k check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make INSTALL_ROOT="$pkgdir/" install
}

