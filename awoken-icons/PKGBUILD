# Maintainer: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=awoken-icons
pkgver=2.1
pkgrel=1
pkgdesc="Simple and quite complete icon set, Token-style."
url="http://alecive.deviantart.com/art/AwOken-163570862"
arch=('any')
license=('CCPL:by-nc-sa')
depends=('librsvg')
optdepends=("imagemagick: To color the iconset."
            "zenity: To have a GUI for the configuration script."
            "xdg-utils: To be able to launch programs from the configuration script.")
source=(AwOken_Sources_$pkgver.zip::http://fc04.deviantart.net/fs71/f/2011/188/2/2/awoken_by_alecive-d2pdw32.zip)
md5sums=('88dbd1c9a7924a9b50809972a142bdc8')
sha1sums=('7a86e6d3c698a0c1f4ca5e8dd28b3b67b8c7dd48')
install=AwOken.install

build() {
	cd $srcdir/AwOken-$pkgver
	msg2 "Decompressing inner archives..."
	tar xfz AwOken.tar.gz
	tar xfz AwOkenDark.tar.gz
}

package() {
	cd "$srcdir/AwOken-$pkgver"
	install -dm755 "$pkgdir/usr/share/icons"

	msg2 "Installing files..."
	cp -r AwOken "$pkgdir/usr/share/icons"
	cp -r AwOkenDark "$pkgdir/usr/share/icons"

	msg2 "Adjusting permissions..."
	chmod 755 `find "$pkgdir/usr/share/icons" -type d`
	chmod 644 `find "$pkgdir/usr/share/icons" -type f`
	chmod 755 `find "$pkgdir/usr/share/icons" -name \*.sh` \
		"$pkgdir/usr/share/icons/AwOken/index.theme" \
		"$pkgdir/usr/share/icons/AwOkenDark/index.theme" 
	chmod 755 "$pkgdir/usr/share/icons/AwOken/awoken-icon-theme-customization" \
		"$pkgdir/usr/share/icons/AwOken/awoken-icon-theme-customization-clear" \
		"$pkgdir/usr/share/icons/AwOkenDark/awoken-icon-theme-customization-dark"

	# Symlink customization scripts in the user binaries directory:
	install -dm755 $pkgdir/usr/bin
	ln -s /usr/share/icons/AwOken/awoken-icon-theme-customization \
		"$pkgdir/usr/bin"
	ln -s /usr/share/icons/AwOken/awoken-icon-theme-customization-clear \
		"$pkgdir/usr/bin"
	ln -s /usr/share/icons/AwOkenDark/awoken-icon-theme-customization-dark \
		"$pkgdir/usr/bin"
}

# vim: set ft=sh ts=3 sw=3 tw=0:
