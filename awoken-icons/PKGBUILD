# Maintainer: Ner0
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=awoken-icons
pkgver=2.5
pkgrel=1
pkgdesc="Simple and quite complete icon set, Token-style."
url="http://alecive.deviantart.com/art/AwOken-163570862"
arch=('any')
license=('CCPL:by-sa')
depends=('librsvg')
optdepends=("imagemagick: To color the iconset."
            "zenity: To have a GUI for the configuration script."
            "xdg-utils: To be able to launch programs from the configuration script.")
source=("https://launchpad.net/~alecive/+archive/antigone/+files/awoken-icon-theme_$pkgver~ppa1~quantal1.tar.gz")
options=('!strip')
md5sums=('756495f366f9deca2b258c580cc61826')
install=awoken-icons.install
PKGEXT='.pkg.tar'

package() {
  install -dm755 "$pkgdir"/usr/{bin,share/icons}

  msg2 "Copying files..."
  cp -dr --no-preserve=ownership awoken-icon-theme-$pkgver*/AwOken* "$pkgdir/usr/share/icons/"

  msg2 "Adjusting permissions..."
  find "$pkgdir/" -type d -exec chmod 755 '{}' \;
  find "$pkgdir/" -type f -exec chmod 644 '{}' \;
  find "$pkgdir/" -name awoken-icon-theme-customization\* -exec chmod 755 '{}' \;

  # Symlink customization scripts in the user binaries directory:
  ln -sf /usr/share/icons/AwOken/awoken-icon-theme-customization* "$pkgdir/usr/bin/"
  ln -sf /usr/share/icons/AwOkenDark/awoken-icon-theme-customization-dark "$pkgdir/usr/bin/"
  ln -sf /usr/share/icons/AwOkenWhite/awoken-icon-theme-customization-white "$pkgdir/usr/bin/"
}

# vim: set ft=sh expandtab ts=3 sw=3 tw=0:
