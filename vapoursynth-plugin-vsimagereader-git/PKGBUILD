# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=vsimagereader
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20130112
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/chikuzen/${_plug}"
license=('LGPL2.1')
depends=('vapoursynth' 'libpng12' 'turbojpeg')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}" 'vapoursynth-plugin-vsbmpreader' 'vapoursynth-plugin-vsjpgreader' 'vapoursynth-plugin-vspngreader' 'vapoursynth-plugin-vstgareader')
makedepends=('git')

_gitroot="git://github.com/chikuzen/${_plug}.git"
_gitname="${_plug}"

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server..."

  if [ -d "${_gitname}" ] ; then
    cd "${_gitname}" && git reset --hard HEAD && git pull --depth=1
  else
    git clone --depth=1 "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -fr "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build/src"

  sh configure
  sed "s|CFLAGS =|CFLAGS = $CFLAGS|g" -i config.mak
  make
}

package(){
  cd "${srcdir}/${_gitname}-build"
  install -Dm755 "src/lib${_plug}.so" "${pkgdir}/usr/lib/vapoursynth/lib${_plug}.so"
  install -Dm644 readme.rst "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
}
