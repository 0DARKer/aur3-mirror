# Maintainer: Figue <ffigue at gmail>
# Contributor: Figue <ffigue at gmail>
# Contributor (Parabola): fauno <fauno@kiwwwi.com.ar>
# Thank you very much to the older contributors:
# Contributor: evr <evanroman at gmail>
# Contributor: Muhammad 'MJ' Jassim <UnbreakableMJ@gmail.com> 

pkgname=icecat
pkgver=12.0
pkgrel=1
pkgdesc="GNU version of the Firefox browser"
arch=(i686 x86_64)
url="http://www.gnu.org/software/gnuzilla/"
license=('GPL' 'MPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'nss>=3.13.3' 'libxt' 'hunspell' 'startup-notification' 'mime-types' 'dbus-glib' 'alsa-lib' 'sqlite' 'libnotify' 'desktop-file-utils' 'libvpx' 'libevent' 'hicolor-icon-theme')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'wireless_tools' 'yasm' 'mesa' 'autoconf2.13' 'libidl2' 'xorg-server-xvfb' 'imake')
install=icecat.install
source=(http://ftp.gnu.org/gnu/gnuzilla/${pkgver}/${pkgname}-${pkgver}.tar.xz
        mozconfig
        icecat.desktop
        icecat-safe.desktop
        mozilla-firefox-1.0-lang.patch
        vendor.js
        gcc47.patch)


build() {

  ICECATDIR="/usr/lib/${pkgname}-${pkgver}" && export ICECATDIR

  msg2 "Patching some files..."
  cd "${srcdir}/${pkgname}-${pkgver}/"
  patch -Np1 -i "${srcdir}/mozilla-firefox-1.0-lang.patch"
  patch -Np1 -i "$srcdir/gcc47.patch"
  #patch -Np1 -i "${srcdir}/libvpx.patch"

  # To fix session store bug
  # https://lists.gnu.org/archive/html/bug-gnuzilla/2012-03/msg00008.html
  #cp -v ${srcdir}/firefox.js browser/app/profile/firefox.js
  #cp -v ${srcdir}/package-manifest.in browser/installer/package-manifest.in

  # To fix build/unix/run-icecat.sh permission bug
  chmod -v +x build/unix/run-icecat.sh
 
  msg2 "Starting build..."
  cp "${srcdir}/mozconfig" .mozconfig

  # Fix PRE_RELEASE_SUFFIX
  sed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

#  export LDFLAGS="-Wl,-rpath,${ICECATDIR} -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"
  export LDFLAGS="$LDFLAGS -Wl,-rpath,${ICECATDIR}"
  export PYTHON="/usr/bin/python2"

  # PGO build
  LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
  LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
  kill $! || true
}

package () {

  cd "${srcdir}/${pkgname}-${pkgver}/"
  make -j1 -f client.mk DESTDIR="$pkgdir" install

  msg2 "Finishing..."
  install -m755 -d ${pkgdir}/usr/share/applications
  install -m755 -d ${pkgdir}/usr/share/pixmaps
  install -m644 ${srcdir}/${pkgname}-${pkgver}/browser/branding/unofficial/default48.png ${pkgdir}/usr/share/pixmaps/icecat.png
  install -m644 ${srcdir}/icecat.desktop ${pkgdir}/usr/share/applications/
  install -m644 ${srcdir}/icecat-safe.desktop ${pkgdir}/usr/share/applications/

  cd ${pkgdir}/usr/lib
  ln -s ${ICECATDIR} icecat

  # implement vendor.js setting the locale to match the os don't disable our languages extensions
  # https://projects.archlinux.org/svntogit/packages.git/commit/trunk/PKGBUILD?h=packages/firefox&id=281a95c2cca0db88904603d7808936f52797a690
  install -m644 "${srcdir}"/vendor.js "${pkgdir}/usr/lib/${pkgname}-${pkgver}/defaults/pref"

  # We don't want the development stuff
  rm -rv "$pkgdir"/usr/{include,lib/icecat-devel-$pkgver,share/idl}
}

md5sums=('04a5504c5c473354bcd20f7116360fee'
         '57bf282bc8c30a60b934de920b1a21a0'
         'e81ad01dbc16ba28bf92ba4b7c309ca7'
         'd93fe402b87cd000a869e1fd6badc6c9'
         'bd5db57c23c72a02a489592644f18995'
         '0d053487907de4376d67d8f499c5502b'
         '72aa5ba02b4065a40c1a114b74f77d1e')
