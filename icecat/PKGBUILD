# Maintainer: Figue <ffigue at gmail>
# Contributor: Figue <ffigue at gmail>
# Contributor (Parabola): fauno <fauno@kiwwwi.com.ar>
# Thank you very much to the older contributors:
# Contributor: evr <evanroman at gmail>
# Contributor: Muhammad 'MJ' Jassim <UnbreakableMJ@gmail.com> 

pkgname=icecat
pkgver=14.0
pkgrel=1
pkgdesc="GNU version of the Firefox browser"
arch=(i686 x86_64)
url="http://www.gnu.org/software/gnuzilla/"
license=('GPL' 'MPL' 'LGPL')
depends=('gtk2' 'mozilla-common' 'nss>=3.13.3' 'libxt' 'hunspell' 'startup-notification' 'mime-types' 'dbus-glib' 'alsa-lib' 'sqlite' 'libnotify' 'desktop-file-utils' 'libvpx' 'libevent' 'hicolor-icon-theme')
makedepends=('unzip' 'zip' 'diffutils' 'python2' 'wireless_tools' 'yasm' 'mesa' 'autoconf2.13' 'libidl2' 'imake')

# makedepends with PGO
#makedepends=('unzip' 'zip' 'diffutils' 'python2' 'wireless_tools' 'yasm' 'mesa' 'autoconf2.13' 'libidl2' 'xorg-server-xvfb' 'imake')

install=icecat.install
source=(http://ftp.gnu.org/gnu/gnuzilla/${pkgver}/${pkgname}-${pkgver}.tar.gz
        mozconfig
        icecat.desktop
        icecat-safe.desktop
	icecat-install-dir.patch
	shared-libs.patch
        vendor.js)

build() {

  ICECATDIR="/usr/lib/${pkgname}" && export ICECATDIR

  cd "${srcdir}/${pkgname}-${pkgver}/"

  # Patch to move files directly to /usr/lib/icecat. No more symlinks.
  patch -Np1 -i ../icecat-install-dir.patch

  # Patch for shared libs. Without it fails on package()
  patch -Np1 -i ../shared-libs.patch

  msg2 "ATTENTION"
  msg2 "ATTENTION"
  msg2 "ATTENTION: If you have IceCat installed the build will fail. Please, uninstall it first."
  msg2 "ATTENTION"
  msg2 "ATTENTION"
  msg2 "Starting build..."

  cp ../mozconfig .mozconfig

  # Fix PRE_RELEASE_SUFFIX
  sed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  export LDFLAGS="$LDFLAGS -Wl,-rpath,${ICECATDIR}"
  export PYTHON="/usr/bin/python2"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  # Enable PGO (no PGO this time)
#  export MOZ_PGO=1
#  export DISPLAY=:99
#  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

  make -f client.mk build

#  kill $! || true

}

package () {

  cd "${srcdir}/${pkgname}-${pkgver}/"
  make -j1 -f client.mk DESTDIR="${pkgdir}" install

  msg2 "Finishing..."
  install -m755 -d ${pkgdir}/usr/share/applications
  install -m755 -d ${pkgdir}/usr/share/pixmaps
  install -m644 ${srcdir}/${pkgname}-${pkgver}/browser/branding/unofficial/default48.png ${pkgdir}/usr/share/pixmaps/icecat.png
  install -m644 ${srcdir}/icecat.desktop ${pkgdir}/usr/share/applications/
  install -m644 ${srcdir}/icecat-safe.desktop ${pkgdir}/usr/share/applications/

  #cd ${pkgdir}/usr/lib
  #ln -s ${ICECATDIR} icecat

  # implement vendor.js setting the locale to match the os don't disable our languages extensions
  # https://projects.archlinux.org/svntogit/packages.git/commit/trunk/PKGBUILD?h=packages/firefox&id=281a95c2cca0db88904603d7808936f52797a690
  install -m644 "${srcdir}"/vendor.js "${pkgdir}${ICECATDIR}/defaults/pref"

  # We don't want the development stuff
  rm -rv "$pkgdir"/usr/{include,lib/icecat-devel,share/idl}
}

md5sums=('6bb02202cd568e8a4f43239b8f52a4a7'
         'a44df835cbcb775986df45c05538ad48'
         'e81ad01dbc16ba28bf92ba4b7c309ca7'
         'd93fe402b87cd000a869e1fd6badc6c9'
         '6afbdcb802609a91aa9e6b4a7fd63253'
         '7c735fa78c674496db1ca0f31a0c8be5'
         '0d053487907de4376d67d8f499c5502b')
