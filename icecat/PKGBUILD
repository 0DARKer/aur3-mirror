# Maintainer: Figue <ffigue at gmail>
# Contributor: Figue <ffigue at gmail>
# Contributor (Parabola): fauno <fauno@kiwwwi.com.ar>
# Thank you very much to the older contributors:
# Contributor: evr <evanroman at gmail>
# Contributor: Muhammad 'MJ' Jassim <UnbreakableMJ@gmail.com> 

pkgname=icecat
pkgver=3.6.15
_pkgver=3.6
pkgrel=1
_xulver=1.9.2.15
pkgdesc="GNU version of the Firefox browser"
arch=(i686 x86_64)
url="http://www.gnu.org/software/gnuzilla/"
license=('GPL' 'MPL' 'LGPL')
depends=("xulrunner=${_xulver}" 'desktop-file-utils')
makedepends=('zip' 'pkg-config' 'diffutils' 'libgnomeui>=2.24.1' 'python2' 'wireless_tools' 'autoconf2.13')
install=icecat.install
source=(http://ftp.gnu.org/gnu/gnuzilla/${pkgver}/${pkgname}-${pkgver}.tar.xz
        mozconfig
        icecat.desktop
        icecat-safe.desktop
        mozilla-firefox-1.0-lang.patch
	xulrunner-copy-stub.patch
	python2.7.patch
	)

build() {

  ICECATDIR="/usr/lib/${pkgname}-${_pkgver}" && export ICECATDIR

  msg2 "Patching some files..."
  cd "${srcdir}/${pkgname}-${pkgver}/"
  patch -Np1 -i "${srcdir}/mozilla-firefox-1.0-lang.patch"
  patch -Np0 -i "${srcdir}/xulrunner-copy-stub.patch"
  patch -Np0 -i "${srcdir}/python2.7.patch"
  
  msg2 "Starting build..."
  cp "${srcdir}/mozconfig" .mozconfig
  unset CFLAGS
  unset CXXFLAGS

  export LDFLAGS="-Wl,-rpath,${ICECATDIR}"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  msg2 "Build complete"

}

package () {

  cd "${srcdir}/${pkgname}-${pkgver}/"
  make -j1 DESTDIR="${pkgdir}" install

  msg2 "Finishing..."
  mv ${pkgdir}/usr/lib/${pkgname}-${pkgver} ${pkgdir}${ICECATDIR}
  cd ${pkgdir}${ICECATDIR}
  ln -s ${pkgname} firefox
  ln -s ${pkgname}-bin firefox-bin
  cd ${pkgdir}/usr/bin
  ln -sf ${ICECATDIR}/icecat icecat
  cd ${pkgdir}/usr/lib
  ln -s ${ICECATDIR} icecat

  # remove privacy_ext.xpi
  #rm -f "${pkgdir}${ICECATDIR}/extensions/privacy_ext.xpi"

  install -m755 -d ${pkgdir}/usr/share/applications
  install -m755 -d ${pkgdir}/usr/share/pixmaps
  install -m644 ${srcdir}/${pkgname}-${pkgver}/browser/branding/unofficial/default48.png ${pkgdir}/usr/share/pixmaps/icecat.png
  install -m644 ${srcdir}/icecat.desktop ${pkgdir}/usr/share/applications/
  install -m644 ${srcdir}/icecat-safe.desktop ${pkgdir}/usr/share/applications/

  ##copy default prefs manually, icecat fails to start otherwise
  cp -rf ${srcdir}/${pkgname}-${pkgver}/dist/bin/defaults/preferences ${pkgdir}${ICECATDIR}/defaults

}

md5sums=('5ee4cd447cf51afb0539d958f6f13c97'
         '956e4320e997fffd1e4b0342f641b44f'
         'e81ad01dbc16ba28bf92ba4b7c309ca7'
         'd93fe402b87cd000a869e1fd6badc6c9'
         'bd5db57c23c72a02a489592644f18995'
         '5a1938673a367b20ecfa009a5eb767aa'
         'ab3dc9aecae7f08b9492fb3c00a5fd28')
