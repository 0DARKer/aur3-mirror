# Maintainer: Thomas Kuther <tom@kuther.net>
pkgname=keystore-explorer
pkgver=4.1.1
pkgrel=1
pkgdesc="a free GUI replacement for the Java command-line utilities keytool, jarsigner and jadtool"
arch=('i686' 'x86_64')
url="http://www.lazgosoftware.com/kse/index.html"
license=('custom')
depends=('java-runtime' 'java-jce_ustrength7')
install=${pkgname}.install
source=(${pkgname}.desktop)
md5sums=('dfaa247ab726fe2e44368101399b508d')

_zipfile="kse-${pkgver//./}-manual.zip"

build() {
  if [ ! -e "${startdir}/${_zipfile}" ]; then
    echo
    echo "Please download ${_zipfile} from http://www.lazgosoftware.com/kse/downloads.html"
    echo "and put it into ${startdir}."
    echo
    return 1
  fi

  [[ ! -e "${srcdir}/${_zipfile}" ]] && ln -s ${startdir}/${_zipfile} ${srcdir}

  md5_want=225f680519c07259efe04bd0f4d12e1c
  md5_have=$(md5sum ${srcdir}/${_zipfile} | cut -d " " -f 1)
  if [ "$md5_want" != "$md5_have" ]; then
    echo
    echo "${srcdir}/${_zipfile}" checksum mismatch. Please download again.
    echo
    return 1
  fi

  cd "$srcdir"
  bsdtar -xf ${srcdir}/${_zipfile}
}


package() {
  cd "$srcdir"

  install -d "${pkgdir}/usr/lib/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/usr/share/applications"

  install -D -m644 *.jar "${pkgdir}/usr/lib/${pkgname}"
  install -D -m644 readme.txt "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644 license-*.txt "${pkgdir}/usr/share/licenses/${pkgname}"

  # install wrapper
  echo -e "#!/bin/sh\ncd /usr/lib/${pkgname}\njava -classpath kse.jar:bcprov.jar:binor.jar:jh.jar:lazgoprov.jar:looks.jar com.lazgosoftware.utilities.kse.KSE" > kse
  install -D -m755 kse "${pkgdir}/usr/bin"

  # install .desktop
  for i in 16 32 48 128 256; do
    jar xf kse.jar com/lazgosoftware/utilities/kse/images/kse-${i}x${i}.png
    install -D -m644 com/lazgosoftware/utilities/kse/images/kse-${i}x${i}.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/kse.png"
  done
  install -D -m644 ${pkgname}.desktop "${pkgdir}/usr/share/applications"
}


# vim:set ts=2 sw=2 et:
