# Maintainer:  Tim Hütz <timAThuetzDOTbiz>
# Contributor: Renato Garcia <fgar.renatoATgmailDOTcom>
#              Tim Hütz <timAThuetzDOTbiz>
pkgname=artoolkitplus
pkgver=2.3.0
pkgrel=5
pkgdesc="Extended version of ARToolKit's vision code that adds features, but breaks compatibility."
arch=('i686' 'x86_64')
url="https://launchpad.net/artoolkitplus"
license=('GPL')
depends=('gcc-libs')
makedepends=('bzip2' 'cmake')
options=()

source=( 'https://launchpad.net/artoolkitplus/trunk/2.3.0/+download/ARToolKitPlus-2.3.0.tar.bz2'
         'FindARTOOLKITPLUS.cmake'
         'cmake-config-script.patch'
         'throw-exception-instead-of-assert.patch'
       )

sha256sums=( '75eb8b2043b1b6698b7230a6396388713bf7b97933749d933abf01bca42a0c70'
             '97f2063121d7fab7acd259ab395e9393efec50c04f3e30bdb1d96686fdc4e077'
             '9c52d36a280589ad02c56d0b9b3316fdf994dcf2a1d7bcdf4d1e3f070766bafd'
             'ceefb4b8650bbb6e19d152ba12474f004ec065927e8adf12b871500793f27cc3'
           )

build() {
  cd "$srcdir/ARToolKitPlus-${pkgver}"
  cp $(readlink -f "$srcdir/FindARTOOLKITPLUS.cmake") .
  patch -p0 < ../cmake-config-script.patch
  patch -p0 < ../throw-exception-instead-of-assert.patch
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DARTK_USE_BOOST:BOOL=TRUE
  make
}

package()  {
  cd "$srcdir/ARToolKitPlus-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
