# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=drumgizmo
pkgver=0.9.5
pkgrel=1
pkgdesc="A drum LV2 plugin and stand-alone application"
arch=('i686' 'x86_64')
url="http://www.drumgizmo.org/"
license=('GPL3')
groups=('lv2-plugins')
depends=('lv2' 'libsmf' 'libao' 'jack' 'qt4')
source=("${url}releases/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('d6c1f1e080e53e01bc3b4eef13bc2b0d')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/rcc/$(QT_RCC)/' dgedit/Makefile.in
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
