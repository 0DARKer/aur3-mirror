# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=tomahawk-git
pkgver=20110310
pkgrel=1
pkgdesc="A Music Player App written in C++/Qt"
arch=('i686' 'x86_64')
url="http://tomahawk-player.org/"
license=('GPL3')
# qca and libidn are jreen's deps!
depends=('taglib>=1.6.2' 'boost>=1.30' 'clucene-git' 'libmad>=0.15.1b' 'libechonest-git' 'liblastfm>=0.3.3' 'qca' 'libidn')
makedepends=('git' 'cmake>=2.8.0')
provides=('tomahawk')
options=(!strip)
#install=tomahawk.install
changelog=ChangeLog

### This section can and should be adjusted to suit your needs
# How many jobs should 'make' use (for multicore CPUs)?
# Value is 2 by default because it doesn't hurt and can help.
_jobcount="2"
# What type of build do you want (release/debug etc)? Please don't change it unless you know what you're doing.
# Default is debugfull to help with providing backtrace.
# If you want to build a release type you can also remove a bang (!) sign from 'options' array above.
_buildtype="debugfull"
###

_gitroot="git://github.com/tomahawk-player/tomahawk.git"
_gitname="tomahawk"

build() {
  cd ${srcdir}
  msg "Connecting to GIT server..."

  if [ -d ${_gitname} ]; then
    cd ${_gitname} && git pull origin
  else
    git clone --depth 1 ${_gitroot}
  fi

  cd ${srcdir}/${_gitname}/thirdparty
  if [[ $(find -type d -empty|grep jreen|wc -l) -eq 1 ]]; then
    msg "Obtaining jreen..."
    cd ${srcdir}/${_gitname}
    git submodule init && git submodule update
  else
    msg "Updating jreen..."
    git pull origin
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf ${srcdir}/${_gitname}-build
  cp -r ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
  cd ${srcdir}/${_gitname}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DENABLE_JREEN=on -DCMAKE_BUILD_TYPE=${_buildtype}
  make -j ${_jobcount}
}

package() {
  cd ${srcdir}/${_gitname}-build
  make DESTDIR=${pkgdir} install
}
