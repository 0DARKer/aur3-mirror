# Maintainer: Christoph Drexler <chrdr at gmx dot at>
# Contributor: Jelle van der Waa <jellevdwaa@gmail.com>

pkgname=xmind
pkgver=3.2.1
_pkgdate=201011212218
pkgrel=1
pkgdesc="Brainstorming and Mind Mapping Software"
arch=('i686' 'x86_64')
[ "${CARCH}" = "i686" ] && _arch="i386"
[ "${CARCH}" = "x86_64" ] && _arch="amd64"
url="http://www.xmind.net"
license=('EPL' 'LGPL')
depends=('java-runtime' 'shared-mime-info')
optdepends=('lame: needed for some features')
install=xmind.install
source=(http://dl2.xmind.net/xmind-downloads/${pkgname}-${pkgver}.${_pkgdate}_${_arch}.deb)
[ "${CARCH}" = "i686" ] && md5sums=('8ffdf95dd1dfd5d37b5190562f2768a1')
[ "${CARCH}" = "x86_64" ] && md5sums=('d08060971bd6eb3531024e81950eee52')

build() {
  cd ${srcdir}
  ar x ${pkgname}-${pkgver}.${_pkgdate}_${_arch}.deb
  bsdtar -xf data.tar.gz
}

package() {
  install -d ${pkgdir}/usr/share/xmind/
  cp -a ${srcdir}/usr/local/xmind/ ${pkgdir}/usr/share/
  cp -a ${srcdir}/usr/share/ ${pkgdir}/usr/
  install -d ${pkgdir}/usr/bin/
  cd ${pkgdir}/usr/bin/
  ln -s ../../usr/share/xmind/xmind .

  # getting the config files to the right place...
  install -d ${pkgdir}/etc/xmind/
  mv ${pkgdir}/usr/share/xmind/configuration/* \
    ${pkgdir}/etc/xmind/
  cd ${pkgdir}/usr/share/xmind/configuration/
  ln -s ../../../../etc/xmind/* .
  mv ${pkgdir}/usr/share/xmind/xmind-bin.ini \
    ${pkgdir}/etc/xmind/
  cd ../
  ln -s ../../../etc/xmind/xmind-bin.ini .

  # putting the png file where it belongs to
  install -d ${pkgdir}/usr/share/pixmaps/
  mv ${pkgdir}/usr/share/xmind/xmind-logo-36.png \
    ${pkgdir}/usr/share/pixmaps/xmind.png
  sed -i s!Exec=.*!Exec=/usr/share/xmind/xmind! \
    ${pkgdir}/usr/share/applications/xmind.desktop
  sed -i s!/usr/local/xmind/xmind-logo-36.png!xmind.png! \
    ${pkgdir}/usr/share/applications/xmind.desktop
}
