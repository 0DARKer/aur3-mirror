_pkgbasename=ilmbase
pkgname=lib32-$_pkgbasename
pkgver=1.0.2
pkgrel=2
pkgdesc="IlmThread is a thread abstraction library for use with OpenEXR (32 bit)"
arch=('x86_64')
url="http://www.openexr.org"
license=('custom')
depends=('gcc-libs-multilib' "$_pkgbasename") #>=$pkgver")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://savannah.nongnu.org/download/openexr/$_pkgbasename-$pkgver.tar.gz)
md5sums=('26c133ee8ca48e1196fbfb3ffe292ab4')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32 CXX='g++ -m32'
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  
  make DESTDIR=$startdir/pkg install
  
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/include
  # Link to existing license folder, instead of creating new one
  mkdir -p ${pkgdir}/usr/share/licenses && cd ${pkgdir}/usr/share/licenses
  ln -sf ${_pkgbasename} ${pkgname}
}
