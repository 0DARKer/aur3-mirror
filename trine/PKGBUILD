# Maintainer: jimmy-6 <jakub.jarozek {at} gmail.com>
# PKGBUILD based on Braid package: https://aur.archlinux.org/packages.php?ID=44429

pkgname=trine
_gamepkg32="Trine.32.run"
_gamepkg64="Trine.64.run"
pkgver=1.0.8
pkgrel=1
pkgdesc="A physics-based fantasy action game with three playable characters and co-op mode (requires copy of the full game)"
url="http://trine-thegame.com"
license=('custom: "commercial"')
arch=('i686' 'x86_64')
depends=('libgl' 'libglade' 'libx86')
makedepends=('unzip')
source=('trine.sh' 'trine-launcher.sh' 'trine.desktop')
md5sums=('d0f5a14d6a206ef2a267a9548ae99812'
         'd2ced147e030537339abbaecc0a37ba7'
         '76fa5fec6813d75ca01fa73a40405fa6')

build() {
  cd ${srcdir}
  
  msg "You need a full copy of this game in order to install it"
  msg "Searching for ${_gamepkg32} or ${_gamepkg64} in dir: $(readlink -f `pwd`/..)"
  if [[ -f "../${_gamepkg32}" ]]; then
    msg "Found game package, installing..."
    ln -fs "../${_gamepkg32}" .
    unzip -u ${_gamepkg32} || true
  elif [[ -f "../${_gamepkg64}" ]]; then
    msg "Found game package, installing..."
    ln -fs "../${_gamepkg64}" .
    unzip -u ${_gamepkg64} || true
  else
    error "Game package not found, please type absolute path to ${_gamepkg32} or ${_gamepkg64}:"
    read pkgpath
    if [[ -f "${pkgpath}/${_gamepkg32}" ]]; then
      msg "Found game package, installing..."
      ln -fs "${pkgpath}/${_gamepkg32}" .
      unzip -u ${_gamepkg32} || true
    elif [[ -f "${pkgpath}/${_gamepkg64}" ]]; then
      msg "Found game package, installing..."
      ln -fs "${pkgpath}/${_gamepkg64}" .
      unzip -u ${_gamepkg64} || true
    else
      error "Unable to find game package."
      return 1
    fi
  fi
}

package(){

  # data
  mkdir -p ${pkgdir}/opt/Trine
  cd ${srcdir}
  cp -r binds config data dev profiles data1.fbz data4.fbz trine-launcher.glade trine-logo.png Trine.xpm \
        Trine_Manual_linux.pdf Trine_updates.txt ${pkgdir}/opt/Trine/
  
  # startup scripts
  install -Dm755 ${srcdir}/trine.sh ${pkgdir}/usr/bin/trine
  install -Dm755 ${srcdir}/trine-launcher.sh ${pkgdir}/usr/bin/trine-launcher

  # execs and libs
  if [ "${CARCH}" == "x86_64" ]; then
    cp -r lib64 ${pkgdir}/opt/Trine/
    install -Dm755 ${srcdir}/trine-bin64 ${pkgdir}/opt/Trine/trine-bin
    install -Dm755 ${srcdir}/trine-launcher64 ${pkgdir}/opt/Trine/trine-launcher
  else
    cp -r lib32 ${pkgdir}/opt/Trine/
    install -Dm755 ${srcdir}/trine-bin32 ${pkgdir}/opt/Trine/trine-bin
    install -Dm755 ${srcdir}/trine-launcher32 ${pkgdir}/opt/Trine/trine-launcher
  fi
  
  # desktop icon
  install -Dm644 ${srcdir}/trine.desktop ${pkgdir}/usr/share/applications/trine.desktop
}
