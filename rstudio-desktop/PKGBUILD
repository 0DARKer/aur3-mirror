# Maintainer:  TDY <tdy.aur@gmail.com>
# Contributor: Ben Ward <benjamin.ward@bathspa.org>

pkgname=rstudio-desktop
pkgver=0.97.248
_commit=2f428e3
pkgrel=1
pkgdesc="A powerful and productive user interface for R"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL')
depends=('r>=2.11.1' 'qtwebkit' 'boost-libs>=1.50' 'shared-mime-info')
makedepends=('git' 'cmake>=2.8' 'boost>=1.50' 'java-environment' 'apache-ant' 'unzip' 'openssl' 'pam')
install=rstudio.install
source=($pkgname-$pkgver.tgz::https://github.com/rstudio/rstudio/tarball/v$pkgver)
md5sums=('b0e6ce1738bac9d5ee9cc6dc4a4189c6')

build() {
  cd "$srcdir/rstudio-rstudio-$_commit/dependencies/common"
  sh install-gwt
  sh install-dictionaries
  sh install-mathjax

  rm -rf "$srcdir/rstudio-rstudio-$_commit/build"
  install -dm755 "$srcdir/rstudio-rstudio-$_commit/build"
  cd "$srcdir/rstudio-rstudio-$_commit/build"
  cmake -DRSTUDIO_TARGET=Desktop \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio ..
}

package() {
  cd "$srcdir/rstudio-rstudio-$_commit/build"
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
