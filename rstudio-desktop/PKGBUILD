# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Ben Ward <benjamin.ward@bathspa.org>

pkgname=rstudio-desktop
pkgver=0.97.312
_commit=165a43f
pkgrel=1
pkgdesc="A powerful and productive user interface for R"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL')
depends=('r>=2.11.1' 'qtwebkit' 'boost-libs>=1.50' 'shared-mime-info')
makedepends=('git' 'cmake>=2.8' 'boost>=1.50' 'java-environment<7' 'apache-ant' 'unzip' 'openssl' 'pam' 'wget')
install=rstudio.install
source=($pkgname-$pkgver.tgz::https://github.com/rstudio/rstudio/tarball/v$pkgver)
sha256sums=('dfa06adeaf9db77a72b7a599de42c99d9b1e108e7a4d6620c918006287d3aeae')

build() {
  cd "$srcdir/rstudio-rstudio-$_commit/dependencies/common"
  sh install-gwt
  sh install-dictionaries
  sh install-mathjax

  rm -rf "$srcdir/rstudio-rstudio-$_commit/build"
  install -dm755 "$srcdir/rstudio-rstudio-$_commit/build"
  cd "$srcdir/rstudio-rstudio-$_commit/build"
  cmake -DRSTUDIO_TARGET=Desktop \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio ..
}

package() {
  cd "$srcdir/rstudio-rstudio-$_commit/build"
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
