# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: Jonjo McKay <jonjo@jonjomckay.com>
#
# Package depends on chromaprint, ffmpeg, lame and bzip2. Dependency tree:
# chromaprint
# `-ffmpeg
#   |-lame
#   `-bzip2

pkgname=libgroove
pkgver=4.1.1
pkgrel=1
pkgdesc='Library that provides decoding and encoding of audio on a playlist.'
arch=(i686 x86_64)
url='https://github.com/andrewrk/libgroove'
license=(MIT)
depends=(sdl2 chromaprint)
makedepends=(cmake yasm)
conflicts=(libgroove-git)
options=('strip' ccache)
source=("https://github.com/andrewrk/libgroove/archive/${pkgver}.tar.gz")
sha256sums=('a852936e5da65e1729bc85b20e2a6ace71c3813abd77dac44759b3db8659bea6')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir build
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  cmake ../ -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  make -C "${srcdir}/${pkgname}-${pkgver}/build" DESTDIR="${pkgdir}/" install
  install -Dm 644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
