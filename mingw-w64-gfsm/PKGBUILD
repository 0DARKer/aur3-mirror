# Maintainer: Benoit Favre <benoit.favre@lif.univ-mrs.fr>
pkgname=mingw-w64-gfsm
_pkgname=gfsm
pkgver=0.0.15.1
_pkgver=0.0.15-1
pkgrel=3
pkgdesc="A weighted finite state transducer (WFST) library and associated command line tools. (mingw-w64)"
arch=(any)
url="http://kaskade.dwds.de/~moocow/mirror/projects/gfsm/"
license=('LGPL')
makedepends=(mingw-w64-configure mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-glib2)
options=(!strip !buildflags staticlibs)
source=(http://kaskade.dwds.de/~moocow/mirror/projects/gfsm/${_pkgname}-${_pkgver}.tar.gz)
md5sums=('e13ca735ee146d242b33d4de74fb6157')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/$_pkgname-$_pkgver"
  sh autogen.sh
}

build() {
  cd "$srcdir/$_pkgname-$_pkgver"
  for _arch in ${_architectures}; do
    mkdir build-${_arch} && pushd build-${_arch}
    ${_arch}-configure
    make
    popd
  done
}

package() {
  cd "$srcdir/$_pkgname-$_pkgver"
  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make DESTDIR="$pkgdir/" install
    find "$pkgdir" -name ${_arch}\*.exe | xargs ${_arch}-strip
    find "$pkgdir" -name ${_arch}\*.a | xargs ${_arch}-strip -g
    find "$pkgdir" -name ${_arch}\*.dll | xargs ${_arch}-strip --strip-unneeded
    popd
  done
}

# vim:set ts=2 sw=2 et:
