# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Matías Hernández <msdark(at)archlinux(dot)cl>
#
# fixes.patch (biloky):
#   2010.09.23 - some Info.plist does not contain a CFBundleName, use 
#                CFBundleDisplayName instead
#   2011.08.07 - fix gcc4.6 werror=sign-compare error (2012.03.21 fixed upstream)
#   2012.09.13 - updated fixes.patch to merge with upstream changes

pkgname=ideviceinstaller-git
pkgver=20120913
pkgrel=1
pkgdesc="Manage Applications of an iPhone or iPod Touch"
arch=('i686' 'x86_64')
url="http://cgit.sukimashita.com/"
license=('GPL')
groups=('system')
depends=('libimobiledevice' 'usbmuxd' 'libzip') 
makedepends=('git')
provides=(ideviceinstaller)
conflicts=()
replaces=()
backup=()
options=()
install=
noextract=()
source=(fixes.patch)
md5sums=('7ec419ca359878da5ce82a40d6265af7')

_gitroot="git://git.sukimashita.com/ideviceinstaller.git"
_gitname="ideviceinstaller"

build() {
  cd ${srcdir}
  msg "Connecting to GIT server...."
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done or server timeout"

  msg "Starting make..."
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build"
  patch -Np0 -i ../fixes.patch
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}
