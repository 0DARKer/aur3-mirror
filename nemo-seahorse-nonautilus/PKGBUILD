# Maintainer: Sebastian Lau <lauseb644@gmail.com>
# Old Maintainer: RubenKelevra <cyrond@gmail.com>

_mintrel=rebecca
_pkgname=nemo-seahorse
pkgname="${_pkgname}-nonautilus"
pkgver=2.4.0
pkgrel=1
pkgdesc="An extension for Nemo which allows encryption and decryption of OpenPGP files using GnuPG. (without nautilus dependency)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo-extensions"
license=('GPL2')
depends=('gconf' 'libcryptui' 'nemo')
makedepends=('intltool' 'gnupg')
groups=('nemo-extensions')
conflicts=('nemo-seahorse' 'seahorse-nautilus')
install=${pkgname}.install
options=('!libtool' '!emptydirs')

source=("${pkgname}.install"
	"http://packages.linuxmint.com/pool/main/n/$_pkgname/${_pkgname}_${pkgver}%2b${_mintrel}.tar.gz")
md5sums=('5ec9d9f22f605c92aa19e3bffa09fb87'
         'efd30c7bb680d520e4040ea56c2dc489')


prepare() {
  cd ${_pkgname}-${pkgver}+${_mintrel}

  msg2 "Adding support for gpg newer than version 2.0"
  sed -i "s/1.2 1.4 2.0/1.2 1.4 2.0 2.1/" configure
}

build() {
  cd ${_pkgname}-${pkgver}+${_mintrel}
    
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir="/usr/lib/${_pkgname}" \
    --disable-static \
    --disable-schemas-compile
  make
}

package() {
  cd ${_pkgname}-${pkgver}+${_mintrel}
  make DESTDIR="${pkgdir}" install
  # install the common license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/COPYING"
}

