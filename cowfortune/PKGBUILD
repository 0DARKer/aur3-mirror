# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=cowfortune
pkgver=0.1.1
pkgrel=1
pkgdesc="A configurable fortune cookie proclaiming cow (and a few other creatures)"
url="https://github.com/anthraxx/cowfortune"
arch=('any')
license=('MIT')
depends=('cowsay' 'fortune-mod')
optdepends=('lolcat')
backup=('etc/cowfortune/config' 'etc/cowfortune/whitelist' 'etc/cowfortune/blacklist')
install=cowfortune.install
source=("https://github.com/anthraxx/cowfortune/archive/v${pkgver}.tar.gz")
sha256sums=('e90a04548c4d88a77250326457f2d94fd373e32261f4cd3c1bd3ad385ba55755')

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm 644 README.md "${pkgdir}"/usr/share/doc/${pkgname}/README.md
}

# vim: ts=2 sw=2 et:
