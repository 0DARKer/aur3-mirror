# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: Zom <zom@eevul.org>
# Contributor: fettouhi <A.Fettouhi@gmail.com>

pkgname=packagekit-qt
pkgver=0.6.11
pkgrel=2
pkgdesc='Qt-specific libs for PackageKit.'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('glib2>=2.22.0' 'dbus-glib>=0.74' 'polkit>=0.92'
	 'pacman-glib' 'python2' 'sqlite3' 'packagekit')
makedepends=('intltool' 'qt' 'gobject-introspection')
options=('!libtool')
source=("http://www.packagekit.org/releases/PackageKit-${pkgver}.tar.bz2")
sha256sums=('7e1c17e5acd12a641d82c58ee87a7212af50f869179280cd2a68c98fbed93635')

build() {
	cd "${srcdir}/PackageKit-${pkgver}"
	
	sed -i 's/SUBDIRS = test/SUBDIRS =/' backends/Makefile.in
	
	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--enable-qt \
		--disable-managed \
		--disable-tests \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-debuginfo-install \
		--disable-pm-utils \
		--disable-dummy \
		--enable-pacman \
		--with-default-backend=pacman

	# we only need the stuff in this directory, the rest is already there
	cd ${srcdir}/PackageKit-${pkgver}/lib/packagekit-qt
	make -s || return 1
	make -s DESTDIR=${pkgdir} install || return 1
}
