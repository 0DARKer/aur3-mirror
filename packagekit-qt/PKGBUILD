# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: Zom <zom@eevul.org>
# Contributor: fettouhi <A.Fettouhi@gmail.com>

pkgname=packagekit-qt
pkgver=0.6.13
pkgrel=1
pkgdesc='A system designed to make installing and updating software on your computer (Qt version)'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('pacman-glib' 'polkit' 'sqlite3' 'networkmanager' 'qt')
makedepends=('intltool' 'python2' 'gtk-doc')
backup=('var/lib/PackageKit/transactions.db'
	'etc/PackageKit/pacman.d/pacman.conf'
	'etc/PackageKit/pacman.d/repos.list')
options=('!libtool')
source=("http://www.packagekit.org/releases/PackageKit-${pkgver}.tar.bz2")
sha256sums=('cec67a54cd615163402b1e5100a6a9be0bd1838896d6183407b4d9274de47249')

build() {
	cd "${srcdir}/PackageKit-${pkgver}"
	
	sed -i 's/SUBDIRS = test/SUBDIRS =/' backends/Makefile.in
	
	PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--enable-introspection=no \
		--enable-nls \
		--disable-gtk-doc \
		--enable-gtk-doc-html \
		--disable-gtk-doc-pdf \
		--enable-qt \
		--disable-service-packs \
		--enable-networkmanager \
		--disable-connman \
		--enable-man-pages \
		--disable-tests \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--enable-command-not-found \
		--enable-cron \
		--disable-debuginfo-install \
		--disable-device-rebind \
		--disable-pm-utils \
		--enable-alpm \
		--disable-dummy \
		--enable-pacman \
		--with-default-backend=pacman
	make
}

package() {
	cd "${srcdir}/PackageKit-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
