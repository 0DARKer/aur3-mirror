# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Tuxbubling <tuxbubling@jabber.fr>, alejandronova
pkgname=cdrtools
pkgver=3.01a03
pkgrel=1
pkgdesc="Original cdrtools supporting CD, DVD and BluRay burning"
arch=('i686' 'x86_64')
url="http://cdrecord.berlios.de/private/cdrecord.html"
license=('CDDL')
depends=('acl')
conflicts=('cdrkit')
provides=('cdrkit')
backup=('etc/default/cdrecord' 'etc/default/rscsi')
install=cdrtools.install
source=(http://ftp.berlios.de/pub/cdrecord/alpha/cdrtools-$pkgver.tar.gz)
md5sums=('d26f5b74096ddcb9c1c84c0504d7bc3b')
         
build() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}
  sed -i 's|/opt/schily|/usr|g' DEFAULTS/Defaults.linux
  sed -i 's|DEFINSGRP=.*|DEFINSGRP=root|' DEFAULTS/Defaults.linux
  
  export GMAKE_NOWARN=true
  make
}

package() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}
  export GMAKE_NOWARN=true

  make INS_BASE="$pkgdir"/usr INS_RBASE="$pkgdir" install
  rm -rf "$pkgdir"/usr/include
  rm -rf "$pkgdir"/usr/lib/{*.a,profiled}
  find "$pkgdir" -type d -perm 775 -exec chmod 755 '{}' ';'
  find "$pkgdir" -type f -perm 4711 -exec chmod 755 '{}' ';'
}

# vim:set ts=2 sw=2 et:
