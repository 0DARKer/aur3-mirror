# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=lucidor
pkgver=0.9.7
pkgrel=1
pkgdesc="A program for reading and handling EPUB e-books and OPDS catalogs"
arch=('i686' 'x86_64')
url="http://lucidor.org/lucidor/"
license=('GPL3')
depends=('xulrunner' 'desktop-file-utils')
install=$pkgname.install
source=($url${pkgname}_$pkgver.tar.gz)
md5sums=('3e0cbf4a3fe22ef9fddce22a239c1d97')

build()
{
    cd $srcdir/$pkgname-$pkgver
    _ver=$(sed -n 's/^MaxVersion=\([0-9]*\)\..*/\1/p' lucidor/application.ini)
    sed -i "s/^MaxVersion=.*/MaxVersion=$((_ver + 1)).*/" lucidor/application.ini
    make
}

package()
{
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install
}
