# Contributor: jordz <jordz@archlinux.us>

pkgname=nemiver
pkgver=0.8.1
pkgrel=1
pkgdesc="A C/C++ debugger for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('freetype2' 'libsigc++' 'gcc-libs' 'gdb' 'libgtksourceviewmm2' 'libgtop' 'vte' 'sqlite3' 'gconf' 'hicolor-icon-theme' 'gnome-doc-utils')
makedepends=('boost' 'intltool')
optdepends=('ghex: enables memory exploring')
url="http://www.gnome.org/projects/nemiver/"
install=nemiver.install
source=(http://ftp.acc.umu.se/pub/GNOME/sources/$pkgname/0.8/$pkgname-$pkgver.tar.bz2)
options=('!libtool' '!emptydirs')
md5sums=('feafc1a247804091f15b8497ce18db24') 

build() 
{	
	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc \
                  --localstatedir=/var --disable-static \
                  --enable-sourceviewmm2=yes --with-boost \
                  --disable-scrollkeeper --enable-symsvis=yes
	make
}

package() {
      cd $srcdir/$pkgname-$pkgver
	make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install
      install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
      gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain nemiver ${pkgdir}/etc/gconf/schemas/*.schemas
      rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
