# Maintainer: drrossum <d.r.vanrossum[at]gmx dot de>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: trapanator <trap[at]trapanator[dot]com>
# Contributor: tomprogrammer <Thomas-Bahn[at]gmx[dot]net>

pkgname=dkms-phc-intel
pkgver=0.3.2.12.10
_realver=pack-rev10
pkgrel=2
pkgdesc="frequency driver for Intel CPUs with undervolting feature"
url="http://www.linux-phc.org"
arch=('any')
license=('GPL')
provides=('linux-phc')
replaces=('linux-phc')
depends=('dkms')
backup=(etc/conf.d/phc-intel)
install=dkms-phc-intel.install
source=(phc-intel-$_realver.tar.bz2::$url/forum/download/file.php?id=145
        phc-intel.{conf,rc,sleep}
        dkms.conf)
sha256sums=('38ff886196fb941814255879657f51e50c37339c9af9579e59cf1d75ae2c618a'
            'ce08a5a4107be1d5723f1f169d515e67b6c77893f3994fc2d0d2ccf611307ed3'
            'db361cad4be7db4129bc0818adf13b2d36f941d77f3443112163d75a20b5a1d8'
            '3269294d901844d589e7c95d30baf74bcdf8863124290cca3f040b0ffac91ba5'
            '65dcfb1471e362ccd916acec106a9d372cf420fe1a16008d3baa19a885d5deba')

build() {
	cd phc-intel-$_realver
	sed -e 's,$(DESTDIR)/lib/modules/$(KERNELRELEASE)/extra$,$(DESTDIR)/lib/modules/$(KERNELRELEASE)/extramodules,' \
		-e 's/`uname -r`/\$(KERNELRELEASE)/' \
		-e 's,^\tinstall -m 644 -o root -g root phc-intel.modprobe,#\tinstall -m 644 -o root -g root phc-intel.modprobe,' -i Makefile
	sed -i 's,/sbin/modprobe phc-intel |,/sbin/modprobe phc-intel \&\& /etc/rc.d/phc-intel set |,' phc-intel.modprobe
}

package() {
	install -Dm644 phc-intel.conf "$pkgdir/etc/conf.d/phc-intel"
	install -Dm755 phc-intel.rc "$pkgdir/etc/rc.d/phc-intel"
	install -Dm755 phc-intel.sleep "$pkgdir/usr/lib/pm-utils/sleep.d/00phc-intel"

 	install -d ${pkgdir}/usr/src/${pkgname}-${pkgver}
	cp dkms.conf ${pkgdir}/usr/src/${pkgname}-${pkgver}
	cd phc-intel-$_realver
	install -Dm644 phc-intel.modprobe "$pkgdir/usr/lib/modprobe.d/phc-intel.conf"
	cp -R inc Makefile ${pkgdir}/usr/src/${pkgname}-${pkgver}
}
