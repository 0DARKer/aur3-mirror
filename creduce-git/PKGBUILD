pkgname=creduce-git
_gitname=creduce
pkgver=creduce.2.1.0.126.g849338d
pkgrel=2
pkgdesc='A C program reducer'
arch=('i686' 'x86_64')
url='https://github.com/csmith-project/creduce'
license=('custom:University of Illinois/NCSA Open Source License')
depends=('astyle' 'clang' 'delta' 'indent' 'llvm' 'perl-benchmark-timer'
         'perl-exporter-lite' 'perl-file-which' 'perl-getopt-tabular'
         'perl-regexp-common' 'perl-sys-cpu' 'zlib')
makedepends=('git')
source=('git://github.com/csmith-project/creduce.git')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}

build() {
	cd "$srcdir/$_gitname"
	./configure --prefix=/usr --libexecdir=/usr/lib/creduce
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir/" install
	install -Dm644 "COPYING" "$pkgdir/usr/share/licenses/$_gitname/COPYING"
}
