# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=binutils-msp430
pkgver=2.21
pkgrel=3
pkgdesc="A set of programs to assemble and manipulate binary and object files for the MSP430 architecture"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mspgcc4/"
license=('GPL')
depends=('zlib')
options=('!emptydirs' '!libtool')

_mspgcc4_release=20110312
_mspgcc4_path=mspgcc4-${_mspgcc4_release}
_gnu_mirror="http://ftpmirror.gnu.org"

source=("${_mspgcc4_path}.tar.bz2::http://sourceforge.net/projects/mspgcc4/files/mspgcc4/${_mspgcc4_path}.tar.bz2/download"
        "${_gnu_mirror}/binutils/binutils-${pkgver}.tar.bz2"
        "README-msp430-binutils.txt")
sha1sums=('8883f7932e395809ca32592c54112132ca140f9c'
          'ef93235588eb443e4c4a77f229a8d131bccaecc6'
          'e668250b0dae9eb248f767dfaec653792d95bb8d')
md5sums=('333a9b58d7a1db2138fb95015ab06a4f'
         'c84c5acc9d266f1a7044b51c85a823f5'
         '840d71adb5a7b65e66f5b294ab9b8803')

_builddir=${srcdir}/build

build() {
  rm -frv ${_builddir}
  cp -r ${srcdir}/binutils-${pkgver} ${_builddir}
  cd ${_builddir}
  patch -Np1 < ${srcdir}/${_mspgcc4_path}/binutils-${pkgver}.patch

  ./configure \
      --prefix=/usr \
      --program-prefix="msp430-" \
      --disable-multilib \
      --disable-nls \
      --enable-install-libbfd \
      --infodir=/usr/share/info \
      --libdir=/usr/lib \
      --mandir=/usr/share/man \
      --target=msp430

  # This checks the host environment and makes sure all the necessary
  # tools are available to compile Binutils.
  make configure-host

  make tooldir=/usr all
}

package() {
  cd ${_builddir}
  make DESTDIR=${pkgdir} tooldir=/usr install

  rm -f ${pkgdir}/usr/lib/libiberty.a
  rm -f ${pkgdir}/usr/man/man1/{dlltool,nlmconv,windres}*
  rm -f ${pkgdir}/usr/share/info/dir

  cd ${pkgdir}/usr/share/info
  for file in as bfd binutils configure gprof ld standards ; do
    mv ${file}.info "msp430-${file}.info"
  done

  for bin in addr2line ar as c++filt gprof ld nm objcopy \
             objdump ranlib readelf size strings strip
  do
    rm -f ${pkgdir}/usr/bin/${bin}
  done

  _docdir=${pkgdir}/usr/share/doc/${pkgname}
  install -Dm644 ${srcdir}/README-msp430-binutils.txt ${_docdir}/README-msp430-binutils.txt
  install -Dm644 ${srcdir}/binutils-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # rm -fr ${_builddir}
}

# vim:set sts=2 ts=2 sw=2 et:
