# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=binutils-msp430
pkgver=2.21.1a
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files for the MSP430 architecture"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mspgcc4/"
license=('GPL')
depends=('zlib')
options=('!emptydirs' '!libtool')

_mspgcc_ver=20110716
_gnu_mirror="http://ftpmirror.gnu.org"
_sf_base="http://sourceforge.net/projects/mspgcc/files"
_patches_base="${_sf_base}/Patches/LTS/${_mspgcc_ver}"

_patches=("msp430-binutils-2.21.1-${_mspgcc_ver}-sf3143071.patch"
          "msp430-binutils-2.21.1-${_mspgcc_ver}-sf3379341.patch"
          "msp430-binutils-2.21.1-${_mspgcc_ver}-sf3386145.patch"
          "msp430-binutils-2.21.1-${_mspgcc_ver}-sf3400711.patch"
          "msp430-binutils-2.21.1-${_mspgcc_ver}-sf3400750.patch")

source=("${_sf_base}/mspgcc/mspgcc-${_mspgcc_ver}.tar.bz2"
        "${_gnu_mirror}/binutils/binutils-${pkgver}.tar.bz2"
        "${_patches[0]}::${_patches_base}/${_patches[0]}/download"
        "${_patches[1]}::${_patches_base}/${_patches[1]}/download"
        "${_patches[2]}::${_patches_base}/${_patches[2]}/download"
        "${_patches[3]}::${_patches_base}/${_patches[3]}/download"
        "${_patches[4]}::${_patches_base}/${_patches[4]}/download"
        "001_ld_makefile_patch.patch")
sha1sums=('db625da1295fa7b64fe687420d908e532e72439c'
          '525255ca6874b872540c9967a1d26acfbc7c8230'
          '99d644a420029fd38196214799da67c8d0b034b9'
          'e58a37dbd1163e85cf8ed5463e20d0ecd7c75a62'
          'a5297097d59c452b0b172c60ed946efdaa22e56d'
          'c407ed31ebea9ff6266f6392204ed0986af8c388'
          '9bb23e90f3f01e3a218ce9ce831416ee408a3818'
          '7a5d78fd94fd99dd544816db75a14c326c494e68')

_builddir="${srcdir}/build"

build() {
  _patch_name="msp430-binutils-2.21.1-${_mspgcc_ver}.patch"
  (cd "${srcdir}/binutils-2.21.1" &&
    patch -p1 < "${srcdir}/mspgcc-${_mspgcc_ver}/${_patch_name}" &&
    patch -p0 < "${srcdir}/001_ld_makefile_patch.patch" &&
    for patch in ${_patches[@]} ; do
      msg "Applying ${patch}"
      patch -p1 < "${srcdir}/${patch}"
    done)

  rm -frv ${_builddir}
  mkdir -p ${_builddir} && cd ${_builddir}

  "${srcdir}/binutils-2.21.1/configure" \
      --prefix=/usr \
      --program-prefix="msp430-" \
      --disable-multilib \
      --disable-nls \
      --enable-install-libbfd \
      --infodir=/usr/share/info \
      --libdir=/usr/msp430/lib \
      --mandir=/usr/share/man \
      --target=msp430

  # This checks the host environment and makes sure all the necessary
  # tools are available to compile Binutils.
  make configure-host

  make tooldir=/usr all
}

check() {
  cd ${_builddir}

  # do not abort on errors - manually check log files
  make -k -j1 check || true
}

package() {
  cd ${_builddir}
  make DESTDIR=${pkgdir} tooldir=/usr install

  rm -f ${pkgdir}/usr/lib/libiberty.a
  rm -f ${pkgdir}/usr/man/man1/{dlltool,nlmconv,windres}*
  rm -f ${pkgdir}/usr/share/info/dir

  cd ${pkgdir}/usr/share/info
  for file in as bfd binutils configure gprof ld standards ; do
    mv ${file}.info "msp430-${file}.info"
  done

  for bin in addr2line ar as c++filt gprof ld nm objcopy \
             objdump ranlib readelf size strings strip
  do
    rm -f ${pkgdir}/usr/bin/${bin}
  done

  install -Dm644 "${srcdir}/binutils-2.21.1/COPYING" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sts=2 ts=2 sw=2 et:
