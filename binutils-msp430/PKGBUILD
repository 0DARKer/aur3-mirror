# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=binutils-msp430
pkgver=2.21.1
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files for the MSP430 architecture"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mspgcc4/"
license=('GPL')
depends=('zlib')
options=('!emptydirs' '!libtool')

_mspgcc_ver=20110716
_gnu_mirror="http://ftpmirror.gnu.org"

source=("http://sourceforge.net/projects/mspgcc/files/mspgcc/mspgcc-${_mspgcc_ver}.tar.bz2"
        "${_gnu_mirror}/binutils/binutils-${pkgver}.tar.bz2"
        "001_ld_makefile_patch.patch")
sha1sums=('db625da1295fa7b64fe687420d908e532e72439c'
          'f188490772cc902ec44a76545887bff60dbfa22d'
          '7a5d78fd94fd99dd544816db75a14c326c494e68')

_builddir="${srcdir}/build"

build() {
  _patch_name="msp430-binutils-${pkgver}-${_mspgcc_ver}.patch"
  (cd "${srcdir}/binutils-${pkgver}" &&
    patch -p1 < "${srcdir}/mspgcc-${_mspgcc_ver}/${_patch_name}" &&
    patch -p0 < "${srcdir}/001_ld_makefile_patch.patch")

  rm -frv ${_builddir}
  mkdir -p ${_builddir} && cd ${_builddir}

  "${srcdir}/binutils-${pkgver}/configure" \
      --prefix=/usr \
      --program-prefix="msp430-" \
      --disable-multilib \
      --disable-nls \
      --enable-install-libbfd \
      --infodir=/usr/share/info \
      --libdir=/usr/msp430/lib \
      --mandir=/usr/share/man \
      --target=msp430

  # This checks the host environment and makes sure all the necessary
  # tools are available to compile Binutils.
  make configure-host

  make tooldir=/usr all
}

check() {
  cd ${_builddir}

  # do not abort on errors - manually check log files
  make -k -j1 check || true
}

package() {
  cd ${_builddir}
  make DESTDIR=${pkgdir} tooldir=/usr install

  rm -f ${pkgdir}/usr/lib/libiberty.a
  rm -f ${pkgdir}/usr/man/man1/{dlltool,nlmconv,windres}*
  rm -f ${pkgdir}/usr/share/info/dir

  cd ${pkgdir}/usr/share/info
  for file in as bfd binutils configure gprof ld standards ; do
    mv ${file}.info "msp430-${file}.info"
  done

  for bin in addr2line ar as c++filt gprof ld nm objcopy \
             objdump ranlib readelf size strings strip
  do
    rm -f ${pkgdir}/usr/bin/${bin}
  done

  install -Dm644 "${srcdir}/binutils-${pkgver}/COPYING" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sts=2 ts=2 sw=2 et:
