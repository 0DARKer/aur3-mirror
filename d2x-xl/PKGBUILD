pkgname=d2x-xl
pkgver=1.16.16
pkgrel=3
pkgdesc="D2X is a port of Descent 2 to OpenGL. It is an open source project and has received a lot of enhancements compared to Descent 2."
arch=(i686 x86_64)
url="http://www.descent2.de"
license=("Other License, Public Domain")
depends=(openmotif sdl sdl_image sdl_mixer sdl_net mesa directfb d2x-xl-data)
makedepends=(p7zip)
conflicts=(d2x-xl-svn)
provides=(d2x-xl-svn)
install=d2x-xl.install
source=("http://downloads.sourceforge.net/d2x-xl/d2x-xl-src-$pkgver.7z"
"d2x-xl.desktop")
md5sums=('29e5e2e5a2dc68ae1d67c42d78e7ee7c'
         '1f59d6bf831506a5083c1c1582407a13')
optdepends=("innoextract: Manually extract game data from the game's GOG.com installer"
"wine: To run the setup installer from GOG.com"
"d2x-xl-hires-meta: Descent 1 and 2 high resolution models, textures, and sounds")

build() {
  cd "$srcdir"
  7z x -y "d2x-xl-makefiles.7z"
  sed -i "s,AM_CONFIG_HEADER,AC_CONFIG_HEADERS,g" "$srcdir/configure.ac"
  chmod +x autogen.sh
  ./autogen.sh \
    --prefix=/usr
  chmod +x configure
  ./configure \
    --prefix=/usr \
    --enable-release \
    --disable-debug
}

package() {
  cd "$srcdir"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/d2x-xl.desktop" "$pkgdir/usr/share/applications/d2x-xl.desktop"
  install -Dm644 "${srcdir}/d2x-xl-icon-64x64.png" "${pkgdir}/usr/share/icons/${pkgname}.png"
  install -Dm644 INSTALL "$pkgdir/usr/share/licenses/$pkgname/INSTALL"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/"
}
# vim:syntax=sh

