# Contributor: Jaroslav Lichtblau <tu@dragonlord.cz>
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=tome2-git
pkgver=20121104
pkgrel=1
pkgdesc="A dungeon crawler similar to Angband, based on the works of Tolkien."
arch=('i686' 'x86_64')
url="http://forums.te4.org/viewforum.php?f=28"
_watch="https://gitorious.org/tome2/tome2"
license=('custom')
depends=('libx11' 'ncurses' 'jansson')
makedepends=('git' 'cmake')
source=(LICENSE)

md5sums=('74183fd3880704df6ab64e4c2887b852')

_gitroot="git://gitorious.org/tome2/tome2.git"
_gitname="tome2"

build() {
    cd "$srcdir"
    msg "Connecting to gitorius..."

    if [ -d "$srcdir/$_gitname" ] ; then
        cd $_gitname && git pull --depth 1 origin
        msg "The local files are updated."
    else
        git clone --depth 1 "$_gitroot"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    cd "$srcdir/$_gitname"
    sed -i 's/tome ${LIBS})/tome ${LIBS} m)/' src/CMakeLists.txt
    rm -rf build
    mkdir build
    cd build
    cmake -DSYSTEM_INSTALL:BOOL=true -DCMAKE_INSTALL_PREFIX='/usr/' ../
    make
}

package() {
    # todo: permissions for scores, more tome->tome2 renames
    cd "$srcdir/$_gitname/build"
    make DESTDIR="$pkgdir" install
    mv "$pkgdir/usr/bin/tome" "$pkgdir/usr/bin/tome2"

    # license
    install -D -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

