# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Christian Theune <ct@gocept.com>
# Maintainer: M Rawash <mrawash@gmail.com>

pkgname=gnotime
pkgver=2.3.0
pkgrel=1
pkgdesc="A utility to track time spent on activities"
arch=('i686' 'x86_64')
license=('GPL')
url="http://gttr.sourceforge.net/"
depends=('gtkhtml' 'guile' 'qof>=0.7.2' 'gconf' 'desktop-file-utils')
makedepends=('perlxml' 'gnome-doc-utils')
options=('!libtool' '!emptydirs')
install=gnotime.install
source=(http://downloads.sourceforge.net/gttr/${pkgname}-${pkgver}.tar.gz
        description-column.patch)
md5sums=('dd92b8c8a1f45e33000a3e14f227bcf8'
         '1af7f0d9d9f085dcd440a66609e67e99')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  
  #patch fixes description column text
  patch -Np0 -i ${srcdir}/description-column.patch || return 1

  sed -i -e 's/\/usr\/bin\/tr/\/bin\/tr/g' configure
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static --disable-scrollkeeper
	      
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install

  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas \
    ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas

  chmod +x ${startdir}/pkg/usr/bin/gnotime-remote
}
