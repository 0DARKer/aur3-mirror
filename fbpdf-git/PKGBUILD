# Maintainer: Sara <sara at archlinux dot us>
pkgname=fbpdf-git
pkgver=20110307
pkgrel=1
pkgdesc="A small framebuffer pdf viewer based on MuPDF."
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/fbpdf.git"
license=('GPL')
depends=('mupdf') 
makedepends=('git')
provides=('fbpdf')
conflicts=('fbpdf')
source=('http://mupdf.com/download/mupdf-0.8-source.tar.gz' \
        'fbpdf.1')
md5sums=('ce105056957706dd62990966a2717cc1'
         '09f5b4eda2a44844ff2b9566398da8be')

_gitroot=git://repo.or.cz/fbpdf.git
_gitname=fbpdf

build(){
	cd "${srcdir}"
    msg "Connecting to GIT server...."
	
	if [[ -d ${_gitname} ]]; then
		cd "${_gitname}"
		git pull origin
		msg "The local files are updated."
	else
		git clone "${_gitroot}" 
		cd "${_gitname}"
	fi
	msg "Checkout done or server timeout"
    rm -rf "${srcdir}/${_gitname}-build"
    git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
    cd "${srcdir}/${_gitname}-build"
    mkdir lib
    mkdir include

    # we need mupdf header and lib files, which are not shipped in Arch by default
    tar -zxvf "${srcdir}/mupdf-0.8-source.tar.gz"
    cd mupdf-0.8 && make
    cp build/debug/libmupdf.a "${srcdir}/${_gitname}-build/lib"
    cp mupdf/mupdf.h "${srcdir}/${_gitname}-build/include"
    cp fitz/fitz.h "${srcdir}/${_gitname}-build/include"
    cd -

    msg "Starting make..."
    make
}

package(){
    install -Dm755 "${srcdir}/${_gitname}-build/${_gitname}" "${pkgdir}/usr/bin/${_gitname}"
    # Install man page
    install -Dm644 ${srcdir}/${_gitname}.1 "$pkgdir/usr/share/man/man1/${_gitname}.1"
}
