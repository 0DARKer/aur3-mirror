# Contributor: Sara <sara at archlinux dot us>

_pkgname=fbpdf
pkgname=fbpdf-git
pkgver=0.65.384cc96
pkgrel=1
epoch=1
pkgdesc="A small framebuffer pdf viewer, based on MuPDF"
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/fbpdf.git"
license=('custom:BSD')
depends=('mupdf')
makedepends=('git')
provides=('fbpdf')
conflicts=('fbpdf')
source=('git://repo.or.cz/fbpdf.git'
	'fbpdf.1'
        'LICENSE')
md5sums=('SKIP'
	'ae7299de0fcd8e945940b014ba335c1b'
         'ec67f29a7dec10f86ef31515ed657a91')


pkgver() {
  cd $_pkgname
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

build(){
  cd "${srcdir}/${_pkgname}"

  make fbpdf
}

package(){
  install -Dm755 "${srcdir}/${_pkgname}/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
  # Install man page
  install -Dm644 "${srcdir}/${_pkgname}.1" "$pkgdir/usr/share/man/man1/${_pkgname}.1"
}

# vim:set ts=2 sw=2 et:
