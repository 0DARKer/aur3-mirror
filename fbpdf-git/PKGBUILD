# Maintainer: Sara <sfauzia at comcast dot net>

pkgname=fbpdf-git 
true && pkgname=(fbpdf-git fbdjvu-git)
pkgver=20110910
pkgrel=1
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/fbpdf.git"
license=('GPL')
makedepends=('djvulibre' 'mupdf' 'git')
source=('fbpdf.1' 'fbdjvu.1')
md5sums=('64986ce7d11ae9b168eae77695d3eac8'
         'd5324f99dfff37de6d7b933479e616a3')

_gitroot=git://repo.or.cz/fbpdf.git
_gitname=fbpdf

build() {
    cd "${srcdir}"
    msg "Connecting to GIT server...."
    
    if [[ -d ${_gitname} ]]; then
        cd "${_gitname}"
        git pull origin
        msg "The local files are updated."
    else
        git clone "${_gitroot}" -b reload
        cd "${_gitname}"
    fi
    msg "Checkout done or server timeout"
    rm -rf "${srcdir}/${_gitname}-build"
    git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
}

package_fbpdf-git() {
    pkgdesc+=" for pdfs, based on MuPDF."
    depends=('mupdf')
    provides=('fbpdf')
    conflicts=('fbpdf')

    cd "${srcdir}/${_gitname}-build"

    msg "Starting make..."
    make fbpdf
    install -Dm755 "${srcdir}/${_gitname}-build/${_gitname}" "${pkgdir}/usr/bin/${_gitname}"
    # Install man page
    install -Dm644 ${srcdir}/${_gitname}.1 "$pkgdir/usr/share/man/man1/${_gitname}.1"
}

package_fbdjvu-git() {
    pkgdesc+=" for DjVu files."
    depends=('djvulibre')
    provides=('fbdjvu')
    conflicts=('fbdjvu')

    cd "${srcdir}/${_gitname}-build"

    msg "Starting make..."
    make fbdjvu
    install -Dm755 "${srcdir}/${_gitname}-build/fbdjvu" "${pkgdir}/usr/bin/fbdjvu"
    # Install man page
    install -Dm644 ${srcdir}/fbdjvu.1 "$pkgdir/usr/share/man/man1/fbdjvu.1"
}

# Package description is here so that the AUR reads it properly
pkgdesc="A small framebuffer document viewer"
