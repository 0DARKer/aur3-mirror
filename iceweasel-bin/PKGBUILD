# Maintainer: superlex
# Contributor: Jorge Barroso <jorge.barroso.11@gmail.com>

# We're getting this from Debian Sid (Unstable)
# packages.debian.org/sid/iceweasel

_debname=iceweasel
_debver=29.0.1
_xulver=29
_debrel=2
_debrepo=http://ftp.de.debian.org/debian/pool/main/i/
 
pkgname=iceweasel-bin
pkgver=${_debver}.deb${_debrel}
pkgrel=1
pkgdesc="Debian Browser based on Mozilla Firefox (bin version)"
arch=('i686' 'x86_64')
url=("http://www.geticeweasel.org")
license=('MPL' 'GPL' 'LGPL')
conflicts=("$_debname")
provides=("$_debname"="$_debver")

depends=( 'gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types' 'dbus-glib' 'alsa-lib'
         'libnotify' 'desktop-file-utils' 'hicolor-icon-theme' 'libvpx' 'libevent' 'nss>=3.14.1' 'hunspell' 'sqlite')

optdepends=('iceweasel-sync: Speed up Iceweasel using tmpfs'
	    'mozplugger: A Mozilla & Firefox multimedia plugin.'
	    'mozilla-searchplugins: extra search plugins'
	    'iceweasel-extension-archsearch: Iceweasel Arch search engines (AUR, Pkgs, BBS, Wiki, etc.)'
            'iceweasel-extension-archforumsearch-it: Iceweasel search engines for Arch Linux Italian forum'
	    'iceweasel-i18n-it: Language Pack for Iceweasel'
	    'iceweasel-i18n-es: Spanish language pack for Iceweasel'
            'iceweasel-i18n-fr: French language pack for Iceweasel')

install=iceweasel.install
 
 if [ "$CARCH" = "x86_64" ]; then
    _debarch=amd64
    sha1sums=('842ccdd106cabc91ca8214fb3046a461386fed73'
              '802f1c34a18659d298897ce0787fbb5dc5dcbf07')
else
    _debarch=i386
    sha1sums=('23389787cc879bd380aa233d4f00441cffa0d587'
              '06ff9e398a5e1d082d7c6dd21a94121258b68f34')
 fi
 
source=("${_debrepo}/${_debname}/${_debname}_${_debver}-${_debrel}_${_debarch}.deb"
        "${_debrepo}/${_debname}/xulrunner-${_xulver}_${_debver}-${_debrel}_${_debarch}.deb")

noextract=("xulrunner-${_xulver}_${_debver}-${_debrel}_${_debarch}.deb")


build(){
      mkdir -p "${srcdir}/xul_dir"
      bsdtar -xf "xulrunner-${_xulver}_${_debver}-${_debrel}_${_debarch}.deb" -C "${srcdir}/xul_dir"
      tar Jxvf "${srcdir}/xul_dir/data.tar.xz" -C "${srcdir}/xul_dir"
}

package(){
      msg2 "Installing Iceweasel..."
      tar Jxvf "${srcdir}"/data.tar.xz -C "$pkgdir"/
      
      msg2 "Installing xulrunner..."
      rm -v "${pkgdir}/usr/lib/iceweasel/xulrunner"
      rm -v "${srcdir}"/xul_dir/usr/lib/xulrunner-${_xulver}/{chrome,defaults,icons}
      cp -R "${srcdir}"/xul_dir/usr/lib/xulrunner-${_xulver} "${pkgdir}/usr/lib/$_debname/xulrunner"         
      cp -R "${srcdir}"/xul_dir/usr/share/xulrunner-${_xulver}/{chrome,defaults,icons} "${pkgdir}/usr/lib/$_debname/xulrunner/"      

      msg2 "Cleaning up unwanted files..."
      rm -rv "${pkgdir}"/usr/lib/{mozilla,mime}
      rm -rv "${pkgdir}"/usr/share/{bug,mozilla,doc}
      rm -v "${pkgdir}"/usr/bin/firefox
}
 
# vim:set ts=2 sw=2 et:
