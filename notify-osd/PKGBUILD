# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Johannes Pfau <johannespfau at gmail dot com>

pkgname=notify-osd
pkgver=0.9.31
pkgrel=1
pkgdesc="Canonical's on-screen-display notification agent, implementing the freedesktop.org Desktop Notifications Specification with semi-transparent click-through bubbles"
arch=('i686' 'x86_64')
url="https://launchpad.net/notify-osd"
license=('GPL')
depends=('gconf' 'libwnck3' 'libnotify>=0.7.0')
provides=('notification-daemon')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/oneiric/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('f34ea518927f824094190b06e185ccde')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/gtk+-3.0 >= 3.1.6/gtk+-3.0 >= 3.0.0/' configure{,.in}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
