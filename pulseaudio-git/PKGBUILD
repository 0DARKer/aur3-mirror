# Maintainer: Clément Démoulins <clement@archivel.fr>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Lorenzo Nizzi Grifi Gargiolli <lorenzo.nizzi.grifi@gmail.com>

pkgname="pulseaudio-git"
_pkgname="pulseaudio"
pkgdesc="A featureful, general-purpose sound server"
pkgver=20130408
pkgrel=1
arch=(i686 x86_64)
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
makedepends=(git libasyncns attr libxtst libsm libsndfile
             libtool rtkit speex libsamplerate tdb udev dbus-core xcb-util
             avahi bluez gconf intltool jack lirc-utils pkgconfig openssl
             json-c fftw orc sh)
depends=('rtkit' 'libtool' 'speex' 'libsamplerate' 'tdb' 'udev' 'fftw' 'orc'
         'json-c' 'xcb-util' 'libasyncns' 'libxtst' 'libsm'
         'libsamplerate' 'json-c')
optdepends=('alsa-plugins: ALSA support'
            'avahi: zeroconf support'
            'bluez: bluetooth support'
            'gconf: configuration through gconf'
            'jack: jack support'
            'lirc-utils: infra-red support'
            'openssl: RAOP support')
backup=(etc/pulse/{daemon.conf,default.pa,system.pa,client.conf})
provides=('pulseaudio=0.9.23' 'libpulse=0.9.23')
conflicts=('pulseaudio' 'libpulse')
replaces=('pulseaudio' 'libpulse')
options=(!emptydirs !libtool !makeflags)

source=(git://anongit.freedesktop.org/pulseaudio/pulseaudio
        ${_pkgname}.xinit)
md5sums=('SKIP'
         'eb1cfae28eb2e4d3fdbe45a90e4318ab')

pkgver() {
  git log -1 --format="%cd" --date=short | tr -d '-'
}

build() {
  cd $srcdir/${_pkgname}

 ./autogen.sh --prefix=/usr \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var \
              --with-database=tdb \
              --disable-hal \
              --disable-rpath \
              --disable-default-build-tests

  make
}

package() {
  cd $srcdir/${_pkgname}
  make DESTDIR="$pkgdir" install

  # Lower resample quality
  sed -e '/resample-method/iresample-method=speex-float-0' \
      -i "$pkgdir/etc/pulse/daemon.conf"

  # Disable cork-request module, can result in e.g. media players unpausing
  # when there's a Skype call incoming
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i "$pkgdir/usr/bin/start-${_pkgname}-x11"

  # Make module-console-kit optional
  sed -e $'/module-console-kit/{i.nofail\n;a.fail\n;}' \
      -i "$pkgdir/etc/pulse/default.pa"

  install -Dm755 "$srcdir/${_pkgname}.xinit" "$pkgdir/etc/X11/xinit/xinitrc.d/${_pkgname}"

  rm "$pkgdir/etc/dbus-1/system.d/${_pkgname}-system.conf"
}

