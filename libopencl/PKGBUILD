# Contributor: Vojtech "kralyk" Kral

pkgname='libopencl'
pkgver=1.2
pkgrel=1
_amdstream_ver=2.7

pkgdesc='OpenCL library and ICD loader'
arch=('i686' 'x86_64')
url="http://developer.amd.com/sdks/AMDAPPSDK/Pages/default.aspx"
license=("custom")
install='install'

provides=('libcl')
conflicts=('libcl' 'nvidia-utils')

#Architecture resolution & sum
_arch="${CARCH/i6/x}"
_bits=${_arch/x86/32}
_bits=${_bits/32_/}
[ "$CARCH" = 'x86_64' ] && sha256sums=('01b24a65775ba934678c28638aabefff70013c4e00e6b8f621ab6bfbd6207121') \
                        || sha256sums=('cb84eba67fda2c20c2a85fdacbb472483e327cc463ba45552e8fee1edeb146ec')

#Sources
source=("http://developer.amd.com/Downloads/AMD-APP-SDK-v${_amdstream_ver}-lnx${_bits}.tgz")


package()
{
  #Extract files
  _inner="AMD-APP-SDK-v2.7-RC-lnx${_bits}"
  bsdtar xf "${_inner}.tgz" "${_inner}/lib/${_arch}/libOpenCL.so.1" "${_inner}/docs/opencl/LICENSES"

  #Install libOpenCL
  install -d -m 755 "${pkgdir}/usr/lib"
  install -m 755 "${_inner}/lib/${_arch}/libOpenCL.so.1" "${pkgdir}/usr/lib"
  ln -s 'libOpenCL.so.1' "${pkgdir}/usr/lib/libOpenCL.so"

  #Install license
  install -d -m 755 "${pkgdir}/usr/share/licenses/libopencl"
  install -m 644 "${_inner}/docs/opencl/LICENSES" "${pkgdir}/usr/share/licenses/libopencl"
}
