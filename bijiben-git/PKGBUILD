# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=bijiben
pkgname=$_pkgname-git
pkgver=BIJIBEN.3.9.5
pkgrel=1
pkgdesc="Note editor designed to remain simple to use"
arch=('i686' 'x86_64')
url="https://live.gnome.org/Bijiben"
license=('GPL')
depends=("gtk3>=3.9.3" 'libtracker-sparql' 'libzeitgeist' 'webkitgtk3' 'dconf' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'itstool')
options=('!libtool')
install=$_pkgname.install
conflicts=('bijiben')
replaces=('bijiben')
provides=('bijiben' 'bijiben=3.9.5')
source=('git://git.gnome.org/bijiben')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe | sed 's/^v//;s/-/./g;s/_/./g;'
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$_pkgname --disable-static \
	--disable-schemas-compile --disable-update-mimedb
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
