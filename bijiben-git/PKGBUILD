# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=bijiben
pkgname=$_pkgname-git
pkgver=3.11.5
pkgrel=1
pkgdesc="Note editor designed to remain simple to use"
arch=('i686' 'x86_64')
url="https://live.gnome.org/Bijiben"
license=('GPL')
depends=("gtk3" 'libtracker-sparql' 'libzeitgeist' 'webkitgtk3' 'dconf' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'itstool')
options=('!libtool')
install=$_pkgname.install
conflicts=('bijiben')
replaces=('bijiben')
provides=('bijiben=3.11.5')
source=('git://git.gnome.org/bijiben'
        '0001-Revert-build-support-tracker-0.18.patch')
sha256sums=('SKIP'
            '749a683c57aaaf5d416543bef72c7d8c4069e104458e3426a6dc55d53f2c12d3')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed -E 's/^BIJIBEN_//;s/_/./g;s/([^-]*-g)/r\1/;s|-|.|g'
}

prepare() {
  cd "$srcdir/$_pkgname"
  patch -Np1 -i "$srcdir/0001-Revert-build-support-tracker-0.18.patch"
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$_pkgname --disable-static \
	--disable-schemas-compile --disable-update-mimedb
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
