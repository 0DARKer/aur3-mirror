#Maintainer: <SpeedVin at archlinux dot us>
pkgname=xorg-xinit-git
pkgver=20121020
pkgrel=1
pkgdesc="X.Org initialisation program "
arch=('i686' 'x86_64')
license=('GPL')
url="http://xorg.freedesktop.org/"
depends=('libx11-git' 'xorg-xauth')
makedepends=('pkgconfig')
backup=('etc/skel/.xinitrc'
        'etc/skel/.xsession'
        'etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
conflicts=('xorg-xinit')
provides=('xorg-xinit')
replaces=('xorg-xinit')
source=(xinitrc
        xsession
        xserverrc
        06_move_serverauthfile_into_tmp.diff
        fs25361.patch)
md5sums=('f165d9a52284ef8cd4c8a129463a2b49'
         '7873d97db30d6c279ed37e3559e7c59d'
         'bb550d3b9a2d2b6cbe6e2667a3a7cd03'
         'acc5bf60b1c34de488535c5ebe6c9ff8'
         'b21cfc58c32c44929dd6c1c539890110')

_gitroot="git://anongit.freedesktop.org/xorg/app/xinit"
_gitname="xinit"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -Np1 <"$srcdir/06_move_serverauthfile_into_tmp.diff"
  patch -Np1 <"$srcdir/fs25361.patch"

  sed -i -e 's/XSLASHGLOB.sh/XSLASHGLOB/' xinitrc.cpp

  sh autogen.sh --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  make -C "$srcdir/$_gitname-build" DESTDIR="$pkgdir" install

  install -Dm755 "$srcdir/xinitrc" "$pkgdir/etc/skel/.xinitrc"
  install -Dm755 "$srcdir/xsession" "$pkgdir/etc/skel/.xsession"
  install -Dm755 "$srcdir/xserverrc" "$pkgdir/etc/X11/xinit/xserverrc"
}
