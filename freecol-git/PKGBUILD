# Maintainer: Lone_Wolf lonewolf@xs4all.nl
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Nelson Menon <nelsonmenon@gmail.com insanatorium.wordpress.com>

pkgname=freecol-git
pkgver=0.10100.117db9a
pkgrel=2
pkgdesc="A turn-based strategy game based on Colonization, git version build against java7 "
arch=('any')
url="http://www.freecol.org/"
license=('GPL')
depends=('java-environment=7' 'bash')
makedepends=('git' 'apache-ant' 'junit' 'texlive-core')
source=("$pkgname::git://git.code.sf.net/p/freecol/git#branch=master"
        'freecol-git.sh')
conflicts=('freecol-svn')
replaces=('freecol-svn')
sha512sums=('SKIP'
            'df01bdf3769c9bb03fe0f264dd69b3511d04adcba4890f097b46d34b1a90fb104bdf41e0df233c990f2ce5db135d863e112c2c8e712d027179969e2ad7dfef68')
#_gitroot="git://git.code.sf.net/p/freecol/git"
#_gitname="freecol-git"
# git clone git://git.code.sf.net/p/freecol/git freecol-git

pkgver() {
  cd "${pkgname}"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd "${pkgname}"
  ant -Djava.target.version=1.7 package print-manual desktop-entry
}

package() {
  cd "${pkgname}"
  install -Dm644 FreeCol.jar "$pkgdir/usr/share/java/$pkgname/FreeCol.jar"
  # install manual
  install -Dm644 doc/FreeCol.pdf "$pkgdir/usr/share/doc/$pkgname/FreeCol.pdf"
  # install icon and .desktopfile
  install -Dm644 packaging/common/freecol.xpm "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  sed -e 's:Name=FreeCol:Name=Freecol-git:' \
      -e 's:Exec=freecol:Exec=/usr/bin/freecol-git:' \
      -e 's:Icon=data/freecol.png:Icon=/usr/share/pixmaps/freecol-git.xpm:' \
      <dist/base/freecol/freecol.desktop >"$pkgname".desktop
  install -Dm644 "$pkgname".desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  # copy necessary files and correct some permissions
  cp -r data jars "$pkgdir/usr/share/java/$pkgname/"
  find "$pkgdir/usr/share/java/$pkgname" -type d -exec chmod 755 '{}' \;
  find "$pkgdir/usr/share/java/$pkgname" -type f -exec chmod 644 '{}' \;
  # install shell script to start application
  install -Dm755 "$srcdir/freecol-git.sh" "$pkgdir/usr/bin/$pkgname"
  # clean up git leftovers
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}

