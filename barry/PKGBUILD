# Contributor: Fritz <V155> Reichwald <fritz.reichwald at gmail.com>
# Contributor: Hauke Wesselmann <hauke at h-dawg.de>

pkgname=barry
pkgver=0.18.4
pkgrel=1
pkgdesc="A GPL C++ library for interfacing with the RIM BlackBerry Handheld"
url='http://www.netdirect.ca/software/packages/barry'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libusbx' 'zlib' 'libglademm' 'libtar' 'libxml++')
optdepends=('fuse: for mounting the BB database with bfuse'
            'sdl: for grabbing screenshots with bwatch'
            'ppp: for tethering with pppob')
conflicts=('barry-git')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('840572f21bb0b5b37ca2c39e8c72da83')


build() {
# Build the package
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-gui
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR=$pkgdir install

# Install PPP scripts for pppob
  mkdir -p $pkgdir/etc/ppp/peers/ $pkgdir/etc/chatscripts/
  find $srcdir/$pkgname-$pkgver/ppp/ -name *.chat -exec cp {} $pkgdir/etc/chatscripts/ \;
  find $srcdir/$pkgname-$pkgver/ppp/ ! -name *.chat ! -name README -exec cp {} $pkgdir/etc/ppp/peers/ \;

# Remove libtool files
  find "$pkgdir/" -name "*.la" -exec rm {} \;
}
