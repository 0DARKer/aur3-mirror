pkgname=upstart
pkgver=0.6.5
pkgrel=1
pkgdesc=""
arch=(i686 x86_64)
url="http://upstart.ubuntu.com/"
license=('GPL')
depends=('libnih')
makedepends=('python' 'pkgconfig')
conflicts=('sysvinit')
replaces=('sysvinit')
options=(emptydirs)
source=( http://upstart.ubuntu.com/download/0.6/${pkgname}-${pkgver}.tar.gz
ftp://ftp.cistron.nl/pub/people/miquels/sysvinit/sysvinit-2.86.tar.gz )
md5sums=('f9466bba72b655c2408353b64105853f'
'7d5d61c026122ab791ac04c8a84db967')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --exec-prefix=/ --sysconfdir=/etc || return 1
  make || return 1

  cd ${srcdir}/sysvinit-2.86 || return 1
  make -C src || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1

  # manually install sulogin killall5 and the pidof symlink from sysvinit
  cd ${srcdir}/sysvinit-2.86
  install -d 755 ${pkgdir}/bin
  install -m 755 src/killall5 ${pkgdir}/sbin/killall5
  install -m 755 src/sulogin ${pkgdir}/sbin/sulogin
  ln -sf ../sbin/killall5 ${pkgdir}/bin/pidof
  for i in killall5.8 pidof.8 sulogin.8 ; do
    install -m 644 man/$i ${pkgdir}/usr/share/man/man8/$i
    gzip -9 ${pkgdir}/usr/share/man/man8/$i
  done


  # Cleanup some residue empty folders
  rm ${pkgdir}/etc/init/*
}
