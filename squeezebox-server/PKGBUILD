# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=squeezebox-server
pkgver=7.5.5
pkgrel=1
pkgdesc='Streaming audio server supported by Logitech'
arch=('i686' 'x86_64')
url='http://www.mysqueezebox.com/download'
license=('GPL')
depends=('perl')
replaces=('squeezecenter' 'squeezeboxcenter')
source=("http://downloads.slimdevices.com/SqueezeboxServer_v${pkgver}/squeezeboxserver-${pkgver}.tgz"
        'conf.d'
        'rc.d')
backup=('etc/conf.d/squeezebox-server')
sha1sums=('7adbfa6f120ffa225f226491cface4a84a17771a'
          'edaa6af06eae98d253a690fdf5abf6a6f07796d0'
          '999fb59f99dd87fffbf27532a501c086b9db087e')

install=install

build() {
	cd "${srcdir}"/squeezeboxserver-*-*
	rm -r Bin/{arm-linux,darwin,i386-freebsd-64int,powerpc-linux}
	rm -r CPAN/arch/*/{arm-linux-gnueabi-thread-multi,darwin-thread-multi-2level}
	[ ${CARCH} = i686   ] && rm -r CPAN/arch/5.12/x86_64-linux-thread-multi
	[ ${CARCH} = x86_64 ] && rm -r CPAN/arch/5.12/i386-linux-thread-multi
	rm -r CPAN/arch/5.{8,10}
}

package() {
	cd "${srcdir}"/squeezeboxserver-*-*
	install -d "${pkgdir}/opt/${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}/"
	install -D -m755 ../rc.d "${pkgdir}/etc/rc.d/${pkgname}"
	install -D -m644 ../conf.d "${pkgdir}/etc/conf.d/${pkgname}"
}
