# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=squeezebox-server
pkgver=7.6.0
pkgrel=1
pkgdesc='Streaming audio server supported by Logitech'
arch=('i686' 'x86_64')
url='http://www.mysqueezebox.com/download'
license=('GPL')
depends=('perl')
replaces=('squeezecenter' 'squeezeboxcenter')
source=("http://downloads.slimdevices.com/SqueezeboxServer_v${pkgver}/squeezeboxserver-${pkgver}.tgz"
        'conf.d'
        'rc.d')
backup=('etc/conf.d/squeezebox-server')
sha1sums=('efefffe9a7f237d193c759d578721fc8862769e5'
          'edaa6af06eae98d253a690fdf5abf6a6f07796d0'
          '999fb59f99dd87fffbf27532a501c086b9db087e')

install=install

build() {
	cd "${srcdir}"/squeezeboxserver-*-*
	rm -r CPAN/arch/5.{8,10}
	rm -r Bin/{arm-linux,darwin,i386-freebsd-64int,powerpc-linux}
	rm -r CPAN/arch/5.12/{arm-linux-gnueabi-thread-multi,darwin-thread-multi-2level,powerpc-linux-thread-multi}
	[[ ${CARCH} = i686   ]] && rm -r CPAN/arch/5.12/x86_64-linux-thread-multi || true
	[[ ${CARCH} = x86_64 ]] && rm -r CPAN/arch/5.12/i386-linux-thread-multi || true
}

package() {
	cd "${srcdir}"/squeezeboxserver-*-*
	install -d "${pkgdir}/opt/${pkgname}"
	cp -a * "${pkgdir}/opt/${pkgname}/"
	install -D -m755 ../rc.d "${pkgdir}/etc/rc.d/${pkgname}"
	install -D -m644 ../conf.d "${pkgdir}/etc/conf.d/${pkgname}"
}
