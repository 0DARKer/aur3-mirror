# Maintainer: Martin Stolpe <martin (dot) stolpe [at] gmail (dot) com>
pkgname=perl-xmltwig-git
pkgver=3.44
pkgrel=1
pkgdesc="A perl module for processing huge XML documents in tree mode."
arch=('any')
url=https://github.com/mirod/xmltwig
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0')
makedepends=('git')
options=(!emptydirs)

_gitroot=git://github.com/mirod/xmltwig.git
_gitname=xmltwig

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/${_gitname}-build"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
