# Heavily based on 'opencv' by Ray Rashif & Tobias Powalowski
# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
pkgname=opencv-old
_pybin=python2
_pydir=python2.7
pkgver=2.1.0
pkgrel=1
pkgdesc="Older version of the Intel(R) Open Source Computer Vision Library, currently required by sikuli-ide"
arch=('i686' 'x86_64')
license=('BSD')
url="http://opencv.willowgarage.com"
depends=('jasper' 'python2-numpy' 'gstreamer0.10-base'
          'gtk2' 'xine-lib' 'libdc1394' 'v4l-utils')
makedepends=('pkg-config' 'cmake')
options=('!libtool')
source=(http://downloads.sourceforge.net/opencvlibrary/OpenCV-$pkgver.tar.bz2
        libpng-1.4.patch v4l-mmap.patch opencv-old.sh)
md5sums=('1d71584fb4e04214c0085108f95e24c8'
         '301f9108f2ee875d889c279adadb254e'
         'c0e2d8ecba3b56974ea2169f61e4905f'
         'bc539a317dbe2a62ddead21e66abf3e3')

build() {
  cd "$srcdir/OpenCV-$pkgver"
  # fix ffmpeg-related C++ issue
  # see http://code.google.com/p/ffmpegsource/source/detail?r=311
  export CXXFLAGS="$CXXFLAGS -D__STDC_CONSTANT_MACROS"
    
  # libpng 1.4 compatibility
  patch -Np1 -i ../libpng-1.4.patch

  # fix v4l issue
  patch -Np0 -i ../v4l-mmap.patch

  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/opt/opencv-old \
          -DCMAKE_SKIP_RPATH=ON \
          -DWITH_XINE=ON \
          -DWITH_UNICAP=OFF \
          -DPYTHON_EXECUTABLE=/usr/bin/$_pybin \
          -DPYTHON_INCLUDE_DIR=/usr/include/$_pydir \
          -DPYTHON_LIBRARY=/usr/lib/lib$_pydir.so

  make
}

package() {
  cd "$srcdir/OpenCV-$pkgver"

  make DESTDIR="$pkgdir/" install

  # install license file
  install -Dm644 "$srcdir/OpenCV-$pkgver/doc/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 $srcdir/opencv-old.sh $pkgdir/etc/profile.d/opencv-old.sh
}

# vim:set ts=2 sw=2 et:
