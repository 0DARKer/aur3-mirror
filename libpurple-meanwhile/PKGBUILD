# Maintainer: Vinicius de Avila Jorge <vinicius.avila.jorge@gmail.com>

pkgname=('libpurple-meanwhile')
pkgver=2.10.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://pidgin.im/"
license=('GPL')
pkgdesc="IM library extracted from Pidgin. Enable protocol Lotus Sametime."
makedepends=('startup-notification' 'gtkspell' 'libxss' 'nss' 'libsasl' 'libsm'
             'libidn' 'python2' 'hicolor-icon-theme' 'gstreamer0.10'
             'farsight2' 'avahi' 'tk' 'ca-certificates' 'intltool'
             'networkmanager')
depends=('farsight2' 'libsasl' 'libidn' 'dbus-glib' 'nss' 'meanwhile')
optdepends=('avahi: Bonjour protocol support'
            'dbus-python: for purple-remote and purple-url-handler'
            'telepathy-haze: Empathy support'
            'telepathy-salut: Empathy support')
conflicts=('libpurple')
provides=('libpurple')
options=('!libtool')
source=(http://downloads.sourceforge.net/pidgin/pidgin-$pkgver.tar.bz2
        nm09-more.patch)
sha256sums=('2f28bddc5edcd714d607d74126c8958ae7c258602b3929e3e6e783d3cb1beac8'
            '641f133335d05d40a20e025d6c573c69537153a69ca986cdcc36dfa63f599d9a')

build() {
  cd "$srcdir/pidgin-$pkgver"

  # Further fixes for NetworkManager 0.9
  # http://developer.pidgin.im/ticket/13859
  patch -Np1 -i "$srcdir/nm09-more.patch"

  # Use Python 2
  sed -i 's/env python$/&2/' */plugins/*.py \
    libpurple/purple-{remote,notifications-example,url-handler}

  # Pidgin doesn't explicitly link to libm
  LDFLAGS+=' -Wl,--copy-dt-needed-entries'

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-schemas-install \
    --disable-gnutls \
    --enable-cyrus-sasl \
    --disable-doxygen \
    --enable-nm \
    --with-python=/usr/bin/python2 \
    --with-system-ssl-certs=/etc/ssl/certs
    make libpurple
}

package(){

  cd "$srcdir/pidgin-$pkgver"

  for _dir in libpurple share/sounds share/ca-certs m4macros po; do
    make -C "$_dir" DESTDIR="$pkgdir" install
  done
}

# vim:set ts=2 sw=2 et:
