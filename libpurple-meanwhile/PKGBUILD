# Maintainer: Vinicius de √Åvila jorge <vinicius.avila.jorge@gmail.com>

pkgname=('libpurple-meanwhile')
pkgver=2.10.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://pidgin.im/"
license=('GPL')
pkgdesc="IM library extracted from Pidgin. Enable protocol Lotus Sametime"
makedepends=('startup-notification' 'gtkspell' 'libxss' 'nss' 'libsasl' 'libsm'
             'python2' 'hicolor-icon-theme' 'silc-toolkit' 'gstreamer0.10'
             'farsight2' 'avahi' 'tk' 'ca-certificates' 'intltool'
             'networkmanager')
depends=('farsight2' 'libsasl' 'dbus-glib' 'silc-toolkit' 'nss' 'meanwhile')
optdepends=('avahi: Bonjour protocol support'
            'dbus-python: for purple-remote and purple-url-handler')
conflicts=('libpurple')
provides=('libpurple')
source=(http://downloads.sourceforge.net/pidgin/pidgin-${pkgver}.tar.bz2
	nm09-more.patch
	pidgin-2.10.0-silc-remote-crash-fix.diff)
md5sums=('e1453c9093c4f32beec19abd14069a3f'
	'a673659d86c7a65aa710f7c8c7feda82'
	'42909374e6f5ca66fa4b3448b95124f5')

build() {
  cd "$srcdir/pidgin-$pkgver"

  # Further fixes for NetworkManager 0.9
  # http://developer.pidgin.im/ticket/13859
  patch -Np1 -i "$srcdir/nm09-more.patch"

  # Fix remote crash in SILC plugin
  # http://developer.pidgin.im/ticket/14636
  patch -Np0 -i "$srcdir/pidgin-2.10.0-silc-remote-crash-fix.diff"

  # Use Python 2
  sed -i 's/env python$/&2/' */plugins/*.py \
    libpurple/purple-{remote,notifications-example,url-handler}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-schemas-install \
    --disable-gnutls \
    --enable-cyrus-sasl \
    --disable-doxygen \
    --enable-nm \
    --with-python=/usr/bin/python2 \
    --with-system-ssl-certs=/etc/ssl/certs
    make
}

package(){
  cd "$srcdir/pidgin-$pkgver"

  for _dir in libpurple share/sounds share/ca-certs m4macros po; do
    make -C "$_dir" DESTDIR="$pkgdir" install
  done
}
