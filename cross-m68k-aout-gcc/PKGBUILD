# Contributor: SebRmv <sbriais at-symbol free dot fr> 
# PKGBUILD based on cross-mipsel-linux-gnu-gcc
# (made by: Vojtech Horky <vojta . horky at-symbol seznam . cz>)
pkgname=cross-m68k-aout-gcc
_pkgname=gcc
_target="m68k-aout"
pkgver=3.3.6
pkgrel=1
pkgdesc="The GNU Compiler Collection for the Motorola M68000 architecture"
url="http://www.gnu.org/software/binutils/"
arch=('i686')
license=('GPL')
depends=('mpfr' 'sh' "cross-m68k-aout-binutils" 'cloog' 'ppl')
options=('!ccache' '!distcc' '!emptydirs' '!libtool' '!strip')
source=("ftp://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/${_pkgname}-core-${pkgver}.tar.bz2")
sha1sums=('79e4bfa5d6bc212f60707c4b990f453eac279f5b')
_sysroot="/usr/lib/cross-${_target}"


build() {
	cd ${srcdir}/${_pkgname}-${pkgver}

	export CFLAGS=`echo "$CFLAGS" | sed 's/-D_FORTIFY_SOURCE=[0-9]*//'`
	
	./configure \
		"--prefix=${_sysroot}" \
		"--bindir=/usr/bin" "--program-prefix=${_target}-" \
		"--target=${_target}" "--build=$CHOST" "--host=$CHOST" \
		--with-gnu-as --with-gnu-ld \
		--disable-nls --disable-threads \
		--enable-languages=c \
		--disable-libgcj \
		--disable-shared \
		|| return 1
	
	make all-gcc "inhibit_libc=true" || return 1
	
	make DESTDIR=${pkgdir} install-gcc || return 1
	
	msg "Removing duplicit files..."
	# remove these files as they are already in the system
	# (with native gcc)
	rm -Rf ${pkgdir}${_sysroot}/{man,info}
	# remove conflicting binaries
	find ${pkgdir}/usr/bin/ -type f -not -name 'm68k-aout*' -delete
	
	msg "Creating out-of-path executables..."
	# symlink executables to single directory with no-arch-prefix name
	mkdir -p ${pkgdir}/usr/bin/cross/${_target}/;
	cd ${pkgdir}/usr/bin/cross/${_target}/;
	for bin in ${pkgdir}/usr/bin/${_target}-*; do
		bbin=`basename "$bin"`;
		ln -s "/usr/bin/${bbin}" `echo "$bbin" | sed "s#^${_target}-##"`;
	done
}

