# Contributor: Junmin Huang <junmin(dot)h(at)gmail(dot)com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=emhacks
pkgver=2008
pkgrel=2
pkgdesc="Useful Emacs Lisp libraries, including gdiff, jjar, jmaker, swbuff and tabbar"
arch=('any')
url="http://emhacks.sourceforge.net/"
license=('GPL2')
depends=(emacs)
install=emhacks.install
source=(emhacks_${pkgver}.tar.gz::http://emhacks.cvs.sourceforge.net/viewvc/emhacks/?view=tar)
md5sums=('27dce642cdaf55ca0d1a58689e69f244')

build() {
  cd $srcdir/$pkgname/$pkgname

  # remove files included in Emacs>=22 or not useful on GNU/Linux
  rm -r findstr* overlay-fix* recentf* ruler-mode* tree-widget*
  for _i in gdiff-setup jjar jmaker swbuff tabbar
  do
    [ -f ${_i}.el.gz ] && rm ${_i}.el.gz
    emacs -batch -q -f batch-byte-compile ${_i}.el
    gzip ${_i}.el
  done
}
package() {
  install -d $pkgdir/usr/share/emacs/site-lisp/$pkgname
  install -Dm644 $srcdir/$pkgname/$pkgname/* \
    $pkgdir/usr/share/emacs/site-lisp/$pkgname
}
