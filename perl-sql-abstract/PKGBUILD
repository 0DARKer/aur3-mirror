# Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-sql-abstract'
pkgver='1.77'
pkgrel=1
pkgdesc="Generate SQL from Perl data structures"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-accessor-grouped>=0.10005' 'perl-getopt-long-descriptive>=0.091' 'perl-hash-merge>=0.12')
makedepends=('perl-test-deep>=0.101' 'perl-test-exception' 'perl-test-warn')
url='http://search.cpan.org/dist/SQL-Abstract'
source=("http://search.cpan.org/CPAN/authors/id/R/RI/RIBASUSHI/SQL-Abstract-${pkgver}.tar.gz")
md5sums=('4e7af7304a5e6c89e1e23582c7d6b657')
sha512sums=('5f76f6dbe53115cc60bd9217d1e18316daeb4a26509027acbb5e02946667b5b6327a9c8210da7d3031698339718f2856ba25d004a6762fc3a6a9f6e3ec2fa41c')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/SQL-Abstract-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
