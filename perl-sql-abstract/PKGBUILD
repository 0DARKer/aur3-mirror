# Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-sql-abstract'
pkgver='1.74'
pkgrel=2
pkgdesc="Generate SQL from Perl data structures"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-accessor-grouped>=0.10005' 'perl-getopt-long-descriptive>=0.091' 'perl-hash-merge>=0.12')
makedepends=('perl-test-deep>=0.101' 'perl-test-exception' 'perl-test-warn')
url='http://search.cpan.org/dist/SQL-Abstract'
source=('http://search.cpan.org/CPAN/authors/id/R/RI/RIBASUSHI/SQL-Abstract-1.74.tar.gz')
md5sums=('4eca363f44ab195e75f45f307c37660e')
sha512sums=('648e05541c7bbed3808bd50ede70f19652894947f8ad6e67ac6531aa3f54e20d8ab6789464ea30a02231f6d1dd37c5f98bef4b9010021ffb1b5fb5e55a5bdc0c')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/SQL-Abstract-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
