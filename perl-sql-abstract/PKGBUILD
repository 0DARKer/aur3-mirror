# Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-sql-abstract'
pkgver='1.73'
pkgrel=2
pkgdesc="Generate SQL from Perl data structures"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.6.2' 'perl-class-accessor-grouped>=0.10002' 'perl-getopt-long-descriptive>=0.086' 'perl-hash-merge>=0.12')
makedepends=('perl-test-deep>=0.106' 'perl-test-exception' 'perl-test-warn')
url='http://search.cpan.org/dist/SQL-Abstract'
source=('http://search.cpan.org/CPAN/authors/id/F/FR/FREW/SQL-Abstract-1.73.tar.gz')
md5sums=('d72edac94a2bf487fcd19460c4d3a7d3')
sha512sums=('aec2f7a60bcb362e9da0291af797fe6b6353f34746e425915ade0ae5610c95222be2b6800637f63b630071cf0228d3adecb7a9c173168851cc792f1cd5de9e3b')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/SQL-Abstract-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
