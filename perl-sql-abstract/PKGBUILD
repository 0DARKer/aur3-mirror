# Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-sql-abstract'
pkgver='1.75'
pkgrel=1
pkgdesc="Generate SQL from Perl data structures"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-accessor-grouped>=0.10005' 'perl-getopt-long-descriptive>=0.091' 'perl-hash-merge>=0.12')
makedepends=('perl-test-deep>=0.101' 'perl-test-exception' 'perl-test-warn')
url='http://search.cpan.org/dist/SQL-Abstract'
source=("http://search.cpan.org/CPAN/authors/id/R/RI/RIBASUSHI/SQL-Abstract-${pkgver}.tar.gz")
md5sums=('db8d33a441141679541c1d44ae756de7')
sha512sums=('25f9bd211d55acc1c56e13b13cc96f219d581899891b06e86934fa0836b81635a635e4da5651ab5335bf3301c26d128590a3a13ba11b3984a2732c87500a0be0')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/SQL-Abstract-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
