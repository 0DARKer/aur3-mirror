# $Id: PKGBUILD 163991 2012-07-23 14:58:38Z andyrtr $
# Maintainer: Jubei-Mitsuyoshi <jubei.house.of.five.masters@gmail.com
# Contributor: Link Dupont <link@subpop.net>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

pkgname='dbus-core-lsd'
_axepkgname='dbus'
pkgver=1.6.4
pkgrel=2
groups=('lsd')
arch=('i686' 'x86_64')
pkgdesc="Freedesktop.org message bus system ,without systemd compile options, part of the -lsd innitiative"
url="http://www.freedesktop.org/Software/dbus"
license=('GPL' 'custom')
depends=('expat' 'coreutils' 'filesystem' 'shadow' 'udev>=182') # shadow for install scriptlet FS#29341
provides=("dbus-core=$pkgver")
conflicts=('dbus-core')
makedepends=('libx11')
options=(!libtool)
install=dbus.install
source=("http://dbus.freedesktop.org/releases/dbus/$_axepkgname-$pkgver.tar.gz"
        'dbus')
md5sums=('5ec43dc4554cba638917317b2b4f7640'
         'f0364f3f5dc5f653bb05d39aa36e3264')

build() {
  cd $_axepkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify --disable-dnotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts 
      
  make
}

#sysd compile options removed
 #     --with-systemdsystemunitdir=/usr/lib/systemd/system 
 #    --enable-systemd

package(){
  cd $_axepkgname-$pkgver
  make DESTDIR="$pkgdir" install

  rm -f "$pkgdir/usr/bin/dbus-launch"
  rm -f "$pkgdir/usr/share/man/man1/dbus-launch.1"
  rm -rf "$pkgdir/var/run"
  
  # remove systemd units, services and such
  rm -rf "$pkgdir/usr/lib/systemd"
  rm -rf "$pkgdir/etc/dbus-1/system.d"

  install -m755 -d "$pkgdir/etc/rc.d"
  install -m755 ../dbus "$pkgdir/etc/rc.d/"

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|' "$pkgdir/etc/dbus-1/system.conf"

  install -dm755 "$pkgdir/usr/share/licenses/dbus-core"
  install -m644 COPYING "$pkgdir/usr/share/licenses/dbus-core/"
}
