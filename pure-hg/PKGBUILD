# -*- shell-script -*-
# Maintainer: Alastair Pharo  asppsa at gmail dot com
# Maintainer: Albert Graef <aggraef@gmail.com>

pkgname=pure-hg
pkgver=5324.57cb0f212469
pkgrel=1
pkgdesc="Pure is a modern-style functional programming language based on term rewriting."
arch=("i686" "x86_64")
license=('LGPL3')
url="http://purelang.bitbucket.org/"
depends=('llvm' 'gmp' 'mpfr' 'readline')
makedepends=('autoconf' 'automake' 'bison' 'flex' 'gcc' 'make' 'mercurial')
optdepends=("pure-docs-hg: online documentation"
            "w3m: access to help in interactive mode"
            "emacs-pure-mode: editing Pure files from Emacs"
            "texmacs-pure: embedding Pure sessions in TeXmacs")
provides=('pure=0.58')
conflicts=('pure')
source=("hg+https://bitbucket.org/purelang/pure-lang"
	"https://bitbucket.org/purelang/pure-lang/downloads/llvm-3.3-tools.tar.bz2")
md5sums=(SKIP '0a3c4154e7d20f1d078d994ec47be1b7')

pkgver() {
  cd $srcdir/pure-lang
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd $srcdir/pure-lang/pure
  autoreconf
  ./configure --enable-release --prefix=/usr --without-elisp --without-texmacs
  make || return 1
  make check || return 1
}

package() {
  cd "$srcdir/pure-lang/pure"
  make DESTDIR=$pkgdir install || return 1
  cd "$srcdir/llvm-3.3-tools/$CARCH"
  cp llc opt $pkgdir/usr/lib/pure || return 1
}

# vim:set ts=2 sw=2 et:
