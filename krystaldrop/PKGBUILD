# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Piotr Beling <qwak@stud.ics.p.lodz.pl>

pkgname=krystaldrop
pkgver=0.7.2
pkgrel=2
pkgdesc="A free clone of the excellent NeoGeo's Magical Drop puzzle game"
arch=('i686' 'x86_64')
url="http://krystaldrop.sourceforge.net/"
license=('GPL2')
depends=('libgl' 'libxml2' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
source=("http://downloads.sourceforge.net/${pkgname}/src_${pkgver}.tgz"
        "http://downloads.sourceforge.net/${pkgname}/art_${pkgver}.tgz"
        "00-assert.patch"
        "01-deps.patch"
        "02-gcc41.patch"
        "03-gcc43.patch"
        "04-glibc2.10.patch"
        "05-icu.patch"
        "${pkgname}.desktop")
md5sums=('68af5fccb41bb22bb583da8139dd3bc0'
         '2a0a8049fc5525f1c2cd8ed6656572c4'
         'e357d37949c20a3547715fa8eb3d6105'
         '2f8888ebfa635ef9dd07d6e5c0ccd3ea'
         '53a26432544a07c94b0855d70e315fef'
         '5f0c4fbb7cf651248569168e779ff9da'
         'c6aa310d9fff856e3e0a9c9d346d083d'
         '3b516776ce89d77d833a55555de0ac62'
         '014ba61f21a079486a756f479cd781e6')

build() {
  cd "${srcdir}/${pkgname}"

  # Copy data files
  cp -r ../art .

  # Apply Gentoo patches
  patch -Np1 -i ../00-assert.patch
  patch -Np1 -i ../01-deps.patch
  patch -Np1 -i ../02-gcc41.patch
  patch -Np1 -i ../03-gcc43.patch
  patch -Np0 -i ../04-glibc2.10.patch
  patch -Np0 -i ../05-icu.patch

  # Fix installation paths
  sed -e "s_/etc_/etc/${pkgname}_" \
      -e "s_/usr/games_/usr/bin_" \
      -e "s_/usr/share/games_/usr/share_" \
      -i Makefile

  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make INSTALL_PREFIX="${pkgdir}" install

  # Install a desktop entry
  install -Dm644 art/drop.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install a man page and readme
  install -Dm644 doc/kdrop.6 "${pkgdir}/usr/share/man/man6/${pkgname}.6"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
