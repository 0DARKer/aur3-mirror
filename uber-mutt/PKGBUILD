# Contributor: Patrick Brisbin <pbrisbin@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=uber-mutt
pkgver=1.5.20
patchdate='20090619'
pkgrel=1
pkgdesc="A small but very powerful text-based mail client patched by the best."
arch=('i686' 'x86_64')
url='http://lunar-linux.org/index.php?page=mutt-sidebar'
license=('GPL')
depends=('openssl' 'gdbm' 'mime-types' 'libsasl' 'slang' 'gnupg' 'gpgme')
conflicts=('mutt')
provides=('mutt')
source=(ftp://ftp.mutt.org/mutt/devel/mutt-${pkgver}.tar.gz
	http://lunar-linux.org/~tchan/mutt/patch-${pkgver}.sidebar.$patchdate.txt
	http://dbg.download.sourcemage.org/grimoire/codex/stable/mail/mutt/patches/trash_folder/trash_folder-1.5.18.patch.bz2
	muttrc.example
	mutt-unmailbox.patch
	mutt_ssl.patch
	mutt-attach.patch)
md5sums=('027cdd9959203de0c3c64149a7ee351c'
         '5786519489877c92e4fff68cf547e869'
         '9d95c08295f8ec5171f774f776ef8413'
         '336d1d8e290a0595dbe2cd92d720ffc9'
         'fa8e03a49a2fa7b294dc8237d928cdb7'
         '3f54850315502ad47405421339ffae60'
         '659d26f6dae386d55a58c5311e57632e')

build() {
	cd ${srcdir}/mutt-${pkgver}

	# forgotten attatchment detector
	patch -p1 < ${srcdir}/mutt-attach.patch || return 1

	# patch a segfault bug in 1.5.20 -- remove for next release
	patch -p1 < ${srcdir}/mutt-unmailbox.patch || return 1

	# patch for openssl-1
	patch -Np1 < ${srcdir}/mutt_ssl.patch || return 1

	# patch to add sidebar support
	patch -p1 < ${srcdir}/patch-${pkgver}.sidebar.$patchdate.txt

	# patch to add trash folder
	patch -p1 < ${srcdir}/trash_folder-1.5.18.patch

	./configure --prefix=/usr --sysconfdir=/etc --enable-pop \
		    --enable-imap --enable-smtp --enable-pgp --enable-hcache \
		    --enable-gpgme --with-ssl=/usr --with-sasl --without-idn \
		    --with-regex --with-slang || return 1

	make || return 1
}

package() {
	cd ${srcdir}/mutt-${pkgver}

	make DESTDIR=${pkgdir} install

	# remove unneeded or conflicting files
	rm -f ${pkgdir}/usr/bin/{flea,muttbug}
	rm -f ${pkgdir}/usr/share/man/man1/{flea,muttbug}.1
	rm -f ${pkgdir}/etc/mime.types*

	# install example muttrc file
	install -Dm644 ${srcdir}/muttrc.example ${pkgdir}/etc/muttrc.example
}
