# Maintainer: xduugu
# Contributor: Vitaliy Berdinskikh <skipper13@archlinux.org.ua>
# Contributor: Andrea Agosti <cifvts@gmail.com>
pkgname=instantclient-basiclite
pkgver=11.2.0.1
pkgrel=4
pkgdesc="Lighter replacement for the Oracle client (files to run OCI, OCCI and JDBC-OCI programs)"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technology/tech/oci/instantclient/"
license=('custom:OTN')
depends=('gcc-libs' 'libaio')
provides=("instantclient-basic=$pkgver")
conflicts=('instantclient-basic')
install=$pkgname.install
#
# grab the source from
# i686  : http://www.oracle.com/technology/software/tech/oci/instantclient/htdocs/linuxsoft.html
# x86_64: http://www.oracle.com/technology/software/tech/oci/instantclient/htdocs/linuxx86_64soft.html
#
source=($pkgname-linux32-$pkgver.zip LICENSE)
md5sums=('ae2966345030aa2d78fe3d143c8d83ff'
         '2d62e22e4f2d7e6d220fafde0f692a7d')
sha256sums=('63d8ba24a419854fcf9d5f807dbf3e5aa35a08756679e44d35cd7fda76c83eb2'
            'f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')

if [[ $CARCH = x86_64 ]]; then
	source[0]=oracle-${pkgname%%-*}$(echo $pkgver | sed -r 's/^([0-9]+\.[0-9]+).*$/\1/')-${pkgname#*-}-${pkgver}.0-1.$CARCH.zip
	md5sums=('885664cec6413c2c7e7e7928b76e7478'
           '2d62e22e4f2d7e6d220fafde0f692a7d')
	sha256sums=('4cd2fd8da357cb3ede3d0aed1f4a94b623f13e8fd1a2cdd6b3d701d3dc84a07f'
              'f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')
fi

build() {
	local dest=$pkgdir/opt/${pkgname%%-*}

	# copy files
	local pkgver=${pkgver/./_}
	install -d "${dest%/*}" &&
	cp -a "$srcdir/${pkgname%%-*}_${pkgver%%.*}" "$dest" || return 1

	# create required symlinks
	cd "$dest" || return 1
	local lib link
	for lib in *.so*; do
		link=$lib
		while [[ ${link#*.} != so ]]; do
			link=${link%.*}
			ln -s $lib $link
		done
	done

	# fix permissions
	chmod a=rx,u+w * &&
	chmod a-x *_README *.jar || return 1

	# set ld path
	install -d "$pkgdir/etc/ld.so.conf.d" &&
	echo "/opt/instantclient" > "$pkgdir/etc/ld.so.conf.d/$pkgname.conf" || return 1

	install -Dm644 "$srcdir/LICENSE" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
}

# vim:set ts=2 sw=2 noet:
