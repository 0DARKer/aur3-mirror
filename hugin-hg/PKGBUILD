# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=hugin-hg
pkgver=4082
pkgrel=1
pkgdesc="A frontend to the panorama-tools"
arch=('i686' 'x86_64')
url="http://hugin.sourceforge.net/"
license=('GPL')
depends=('openexr' 'exiv2' 'wxgtk' 'boost-libs' 'libpano13-svn' 'enblend-enfuse' 'perl-exiftool' 'glew' 'shared-mime-info')
makedepends=('mercurial' 'cmake' 'boost')
optdepends=('autopano-sift-c: automatic control point generator')
provides=('hugin' 'hugin-svn')
conflicts=('hugin' 'hugin-svn')
replaces=('hugin-svn')
install=hugin.install
source=()
md5sums=()

_hgroot="http://hugin.hg.sourceforge.net:8000/hgroot/hugin/hugin"
_hgrepo="hugin"

build() {
  cd "$srcdir"

  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u || return 1
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo || return 1
  fi  

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir"/$_hgrepo-build ]; then
        rm -rf "$srcdir"/$_hgrepo-build
  fi

  mkdir -p "$srcdir"/$_hgrepo-build
  cd "$srcdir"/$_hgrepo-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_FLAGS="$CXXFLAGS" -DCMAKE_SHARED_LINKER_FLAGS="-lpthread" ../$_hgrepo
  make || return 1
  make DESTDIR="$pkgdir" install || return 1
}

# vim:set ts=2 sw=2 et:
