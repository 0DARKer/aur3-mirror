# Maintainer: graysky <graysky AT archlinux DOT us>
pkgname=parallel-git
pkgver=20130317
pkgrel=1
pkgdesc='A shell tool for executing jobs in parallel. Git version.'
arch=('any')
url='http://www.gnu.org/software/parallel'
license=('GPL3')
depends=('perl')
makedepends=('git')
provides=('parallel')
conflicts=('parallel')

_gitroot=http://git.savannah.gnu.org/r/parallel.git
_gitname=parallel

build() {
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  
  cd "$srcdir/$_gitname-build"
  source /etc/profile.d/perlbin.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$_gitname-build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
