# Maintainer: Andrey Vihrov <andrey.vihrov at gmail.com>

pkgname=quake3-edawn
pkgver=1.3.1
pkgrel=3
pkgdesc="A free Quake III Arena mod with numerous improvements"
arch=('any')
url="http://edawn-mod.org/"
license=('unknown')
depends=('quake3')
source=("http://edawn-mod.org/binaries/edawn-${pkgver}.zip")
md5sums=('df59864a44e7ea59ba8331af5fff0c19')

package() {
  local _q3dir="/opt/quake3"

  # Engines other than quake3e may have problems loading files
  # without the .cfg extension
  cd "$srcdir/edawn"
  for f in {bans,crontab,motd,rotation,voteconf}.txt maps/*.{add,set} ; do
    mv "$f" "$f.cfg"
  done

  cd "$srcdir"
  install -d "$pkgdir/$_q3dir"
  cp -dR edawn "$pkgdir/$_q3dir"
}

# vim:set ts=2 sw=2 et:
