# Maintainer: Xiao-Long Chen
# Contributor: LookTJ/Taylor Lookabaugh <jesus {dot} christ {dot} i {dot} love {at} gmail {dot} com>
# Contributor: alessandro_ufms

pkgname=caffeine
pkgver=403
pkgrel=2
pkgdesc="A system applet that allows you to inhibit the screensaver and the sleep power saving mode in an easy way"
arch=('any')
url="https://launchpad.net/caffeine"
license=('GPL')
depends=('dbus-python' 'gtk2' 'pygobject' 'python-notify' 'python-xlib' 'python2' 'python2-gconf' 'pyxdg' 'kaa-metadata')
makedepends=('bzr')
options=('!libtool' '!emptydirs')
install=caffeine.install

_bzrtrunk="lp:caffeine"
_bzrmod="caffeine"

build()
{
  cd "${srcdir}"
  msg "Connecting to Bazaar server...."

  if [[ -d "${_bzrmod}" ]]; then
    cd "${_bzrmod}" && bzr pull "${_bzrtrunk}" -r "${pkgver}"
    msg "The local files are updated."
  else
    bzr branch "${_bzrtrunk}" "${_bzrmod}" -q -r "${pkgver}"
  fi  

  rm -rfv "${srcdir}/${_bzrmod}-build"
  cp -rv "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
  cd "${srcdir}/${_bzrmod}-build"

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  msg "Nothing to build."
}

package() {
  cd "${srcdir}/${_bzrmod}-build"
  python2 setup.py install --root="${pkgdir}"
}

