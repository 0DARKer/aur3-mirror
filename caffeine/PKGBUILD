# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: LookTJ/Taylor Lookabaugh <jesus {dot} christ {dot} i {dot} love {at} gmail {dot} com>
# Contributor: alessandro_ufms

pkgname=caffeine
pkgver=410
pkgrel=1
pkgdesc="A system applet that allows you to inhibit the screensaver and the sleep power saving mode in an easy way"
arch=('any')
url="https://launchpad.net/caffeine"
license=('GPL')
depends=('dbus-python' 'gtk2' 'python2-gobject' 'python-notify' 'python-xlib' 'python2' 'dconf' 'pyxdg' 'kaa-metadata' 'hicolor-icon-theme')
makedepends=('bzr')
options=('!libtool' '!emptydirs')
install=caffeine.install

_bzrtrunk="lp:caffeine"
_bzrmod="caffeine"

build()
{
  cd "${srcdir}"
  msg "Connecting to Bazaar server...."

  if [[ -d "${_bzrmod}" ]]; then
    cd "${_bzrmod}" && bzr pull "${_bzrtrunk}" -r "${pkgver}"
    msg "The local files are updated."
  else
    bzr branch "${_bzrtrunk}" "${_bzrmod}" -q -r "${pkgver}"
  fi  
  msg "Bazaar checkout done or server timeout"

  # export without .bzr directory
  cd "${srcdir}"
  rm -rf "${_bzrmod}-build"
  bzr export "${_bzrmod}-build" -q -d "${_bzrmod}"
 
  # use python2 for version 2, instead of python (version 3)
  cd "${srcdir}/${_bzrmod}-build"
  for i in utils core procmanager main applicationinstance __init__ flash_detect; do
    sed 's/python/python2/' -i caffeine/${i}.py
  done
}

package() {
  cd "${srcdir}/${_bzrmod}-build"
  python2 setup.py install --root="${pkgdir}"
}

