# Maintainer: Army
_pkgname=paulstretch
pkgname=$_pkgname-git
pkgver=20110309.22
pkgrel=1
pkgdesc="a program for stretching the audio"
arch=('i686' 'x86_64' 'any')
url="http://hypermammut.sourceforge.net/paulstretch"
license=('GPL2')
depends=('fltk' 'mxml' 'portaudio' 'audiofile' 'fftw' 'libmad')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+git://github.com/paulnasca/paulstretch_cpp.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-list --count master)"
}

prepare() {
  cd "$srcdir/$_pkgname"
  sed -i '23i#include <string.h>' Input/MP3InputS.cpp
  sed -i '9s/-o/-lz -logg -lvorbis -o/' compile_linux_fftw.sh
}

build() {
  cd "$srcdir/$_pkgname"
  ./compile_linux_fftw.sh
}

package() {
  cd "$srcdir/$_pkgname"
  install -Dm755 "$_pkgname" "$pkgdir/usr/bin/$_pkgname"
}

# vim:set ts=2 sw=2 et:
