# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Paul Burton <paulburton89@gmail.com>
pkgname=xf86-video-via-chrome9
pkgver=92.008.d9c5be.20140915
pkgrel=1
pkgdesc="VIA chrome9 Xorg driver"
arch=('i686' 'x86_64')
url="http://linux.via.com.tw"
license=('MIT')
depends=('xorg-server-devel' 'xproto' 'libdrm' 'mesa-libgl' 'libxvmc' 'udev')
makedepends=('autoconf' 'libtool')
source=('5.76.52.92-sourcecode-008-d9c5be-20140915.7z::http://linux.via.com.tw/support/beginDownload.action?eleid=741&fid=1041'
'xf86drmvia.c.patch')
md5sums=('f02433a9573ee07db0acc5723f75d8a3'
         'cb77b59fca35386bd053183eb25c0d8b')
backup=('etc/X11/xorg.conf.via-chrome9')
_dir=5.76.52.92-sourcecode-008-d9c5be-20140915/XServer

prepare() {
  cd "$srcdir"/$_dir/src
  patch -p0 -i $srcdir/xf86drmvia.c.patch
}

build() {
  cd "$srcdir"/$_dir
  
  # comment out below line because error occured after autogen.sh
	# bash autogen.sh

	# compile driver
  make

	# extract license
	sed -n '/Copyright/,/DEALINGS IN THE SOFTWARE/p' src/via_common.c | sed 's/^ \* *//' >LICENSE
}

package() {
	cd "$srcdir"/$_dir

	# install driver
	install -dm755 "${pkgdir}/usr/lib/xorg/modules/drivers"
	install -m755 "src/.libs/via_drv.so" "${pkgdir}/usr/lib/xorg/modules/drivers/"

	# install sample xorg.conf
	install -dm755 "${pkgdir}/etc/X11"
	install -m644 "Misc/generic/xorg.conf" "${pkgdir}/etc/X11/xorg.conf.via-chrome9"

	# install license
	install -dm755 "${pkgdir}/usr/share/licenses/xf86-video-via-chrome9"
	install -m644 "LICENSE" "${pkgdir}/usr/share/licenses/xf86-video-via-chrome9/"
}
