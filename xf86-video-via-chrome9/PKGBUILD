# Maintainer: Paul Burton <paulburton89@gmail.com>
pkgname=xf86-video-via-chrome9
pkgver=92.006.75396
pkgrel=1
pkgdesc="VIA chrome9 Xorg driver"
arch=('i686' 'x86_64')
url="http://linux.via.com.tw"
license=('MIT')
depends=('xorg-server-devel' 'xproto' 'libdrm' 'mesa-libgl' 'libxvmc' 'udev' 'via-chrome9')
makedepends=('autoconf' 'libtool')
source=('5.76.52.92-006-75396-20130304.zip::http://linux.via.com.tw/support/beginDownload.action?eleid=701&fid=1001'
        'via_chrome9_xorg_114.patch')
md5sums=('80521c39dccd3c60cdec9e8a96cd2c79'
         '69c2508b091d2f555a3a006a1457dfec')

build() {
	cd "$srcdir/XServer"

	# patch for recent Xorg & autoconf
	patch -p1 <"${srcdir}/via_chrome9_xorg_114.patch"

	# compile driver
	bash autogen.sh
	make

	# extract license
	sed -n '/Copyright/,/DEALINGS IN THE SOFTWARE/p' src/via_common.c | sed 's/^ \* *//' >LICENSE
}

package() {
	cd "$srcdir/XServer"

	# install driver
	install -dm755 "${pkgdir}/usr/lib/xorg/modules/drivers"
	install -m755 "src/.libs/via_drv.so" "${pkgdir}/usr/lib/xorg/modules/drivers/"

	# install sample xorg.conf
	install -dm755 "${pkgdir}/etc/X11"
	install -m644 "Misc/xorg.conf" "${pkgdir}/etc/X11/xorg.conf.via-chrome9"

	# install license
	install -dm755 "${pkgdir}/usr/share/licenses/xf86-video-via-chrome9"
	install -m644 "LICENSE" "${pkgdir}/usr/share/licenses/xf86-video-via-chrome9/"
}
