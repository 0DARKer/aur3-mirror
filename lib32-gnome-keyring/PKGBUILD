_pkgbasename=gnome-keyring
pkgname=lib32-$_pkgbasename
pkgver=3.0.3
pkgrel=1
pkgdesc="GNOME Password Management daemon (32 bit, library only)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL' 'LGPL')
depends=('lib32-gtk3' "$_pkgbasename>=$pkgver")
makedepends=('intltool' 'gcc-multilib' 'dconf' 'lib32-libtasn1')
options=('!libtool' '!emptydirs' '!makeflags')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('a06c34e474d93da62bf96a816e58cb5730e181d6523730f041f34c58a0d5e94e')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libdir=/usr/lib32 \
      --libexecdir=/usr/lib32/gnome-keyring \
      --with-pam-dir=/usr/lib32/security \
      --with-root-certs=/etc/ssl/certs \
      --disable-schemas-compile \
      CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/{etc,usr/{bin,include,share}}
}
