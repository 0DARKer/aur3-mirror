_pkgbasename=gnome-keyring
pkgname=lib32-$_pkgbasename
pkgver=2.32.1
pkgrel=2
pkgdesc="GNOME Password Management daemon (32 bit, library only)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL' 'LGPL')
depends=('lib32-gtk2>=2.22.0' 'dconf' "$_pkgbasename=$pkgver")
makedepends=('intltool' 'pkgconfig' 'python2' 'gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.32/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('31fecec1430a97f59a6159a5a2ea8d6a1b44287f1e9e595b3594df46bf7f18f9')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib32/gnome-keyring \
      --with-pam-dir=/usr/lib32/security --with-root-certs=/etc/ssl/certs \
      --disable-schemas-compile --libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{../etc,bin,include,share}
}
