# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gnome-keyring
pkgname=lib32-${_pkgbase}
pkgver=3.4.1
pkgrel=1
pkgdesc="GNOME Password Management daemon (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL' 'LGPL')
depends=('lib32-gtk3' 'lib32-p11-kit' 'lib32-gcr' "${_pkgbase}") # 'libcap-ng'
makedepends=('intltool' 'gcc-multilib')
options=('!libtool' '!emptydirs')
_ver=${pkgver%.*}
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/${_ver}/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('9e90267721198cbe6ebc08f6390d24901d9c0de39b180cd2ad1bbcdd7b30b249')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-pam-dir=/usr/lib32/security \
      --with-root-certs=/etc/ssl/certs \
      --disable-schemas-compile  \
      --libexecdir=/usr/lib32/${_pkgbase} \
      --libdir=/usr/lib32
  make
}

package() {
  cd "${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}

