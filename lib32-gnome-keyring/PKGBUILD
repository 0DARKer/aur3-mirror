_pkgbasename=gnome-keyring
pkgname=lib32-$_pkgbasename
pkgver=3.0.2
pkgrel=1
pkgdesc="GNOME Password Management daemon (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL' 'LGPL')
depends=('lib32-gtk3') # "$_pkgbasename=$pkgver")
makedepends=('intltool' 'gcc-multilib' 'dconf')
options=('!libtool' '!emptydirs' '!makeflags')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('ed834e5a6d87fe8a90c7c79153af3d8979740d5f8eacad6a8991ef147c924af7')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libdir=/usr/lib32 \
      --libexecdir=/usr/lib32/gnome-keyring \
      --with-pam-dir=/usr/lib32/security \
      --with-root-certs=/etc/ssl/certs \
      --disable-schemas-compile \
      CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/{etc,usr/{bin,include,share}}
}

