# Maintainer: josephgbr <rafael.f.f1 at gmail.com>

_pkgbase=gnome-keyring
pkgname=lib32-$_pkgbase
pkgver=3.2.2
pkgrel=1
pkgdesc="GNOME Password Management daemon (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL' 'LGPL')
depends=('lib32-gtk3' 'lib32-libgcrypt' 'lib32-glib2' 'lib32-pam'
	 'lib32-p11-kit' "$_pkgbase") # 'dconf' 'hicolor-icon-theme' 'desktop-file-utils' 'libtasn1' 'libcap-ng'
makedepends=('intltool' 'python2' 'gcc-multilib' 'lib32-libtasn1' 'lib32-libcap-ng')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/3.2/${_pkgbase}-${pkgver}.tar.bz2)
sha256sums=('4758053ca7a0e02bc06889778b459fd70d1521244961e04b3b1b60247b2ed844')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "$srcdir/$_pkgbase-$pkgver"
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-pam-dir=/lib32/security --with-root-certs=/etc/ssl/certs \
      --disable-schemas-compile --disable-update-mime \
      --libexecdir=/usr/lib32/$_pkgbase \
      --libdir=/usr/lib32
      
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/{etc,usr/{bin,include,share}}
}

