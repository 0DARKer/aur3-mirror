# $Id: PKGBUILD 179342 2013-03-05 15:24:06Z andrea $
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Maintainer: Ivan Shapovalov <intelfx100@gmail.com>

pkgname=kdebase-workspace-plymouth
_pkgname=kde-workspace
pkgver=4.10.1
pkgrel=1
pkgdesc="Provides the interface and basic tools for the KDE workspace: patched for plymouth interaction"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
provides=("kdebase-workspace=$pkgver")
conflicts=('kdebase-workspace')
# note on libxdamage:
# 	not detected by namcap because libgl depends on it
#	but nvidia providing libgl does not depend on libxdamage
depends=('kdepim-runtime' 'lm_sensors' 'libraw1394' 'libqalculate'
         'qimageblitz' 'polkit-kde' 'xorg-xprop' 'libxdamage'
         'libxklavier' 'xorg-xsetroot' 'libxcomposite' 'libxinerama'
         'xorg-xrdb' 'libxres' 'xorg-xrandr' 'xorg-xmessage' 'libusb-compat'
         'kde-base-artwork' 'xcb-util-renderutil' 'xcb-util-image' 'ttf-font')
makedepends=('cmake' 'automoc4' 'boost' 'kdebindings-python2' 'networkmanager')
optdepends=('kde-wallpapers: wallpapers for KDE Plasma Workspaces'
            'appmenu-qt: menu applications over dbus')
install="${pkgname}.install"
backup=('usr/share/config/kdm/kdmrc')
source=("http://download.kde.org/stable/${pkgver}/src/${_pkgname}-${pkgver}.tar.xz"
        'kde.pam' 'kde-np.pam' 'kscreensaver.pam' 'kdm.service' 'kdm.logrotate'
        'etc-scripts.patch' 'terminate-server.patch' 'kdm-xinitrd.patch' 'kdm-plymouth.patch')
md5sums=('8c791507cb2fdf42daebc280bdfe1b1c'
         '6589fb28bd20d5ec1b4a7b9db9fc4209'
         '5a035421a9bfaa353caf04250c2b285c'
         '367a3538f54db71f108b34cfa31088ac'
         'c4b5f1b342b3dad38642318ca077daee'
         '08455692088f82811ea093c80a91840b'
         '8e526f7b4e7f8f02bc7c0c0214314109'
         '814350c52c135d6f7bdada1e29223d38'
         '43790b855557d9bb7b53b02e77a14f11'
         '7ebd9f0b7874816f1c33028d3becb134')

build() {
        cd ${_pkgname}-${pkgver}

        # reads the shell scripts in /etc/kde/
        patch -p0 -i "${srcdir}"/etc-scripts.patch
        # FS#26120
        patch -p1 -i "${srcdir}"/kdm-xinitrd.patch

        # KDEBUG#202629
        patch -p0 -i "${srcdir}"/terminate-server.patch

		# plymouth integration
		patch -p1 -i "${srcdir}"/kdm-plymouth.patch

        cd ../

        mkdir build
        cd build
        cmake ../${_pkgname}-${pkgver} \
          -DCMAKE_BUILD_TYPE=Release \
          -DKDE4_BUILD_TESTS=OFF \
          -DCMAKE_SKIP_RPATH=ON \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DWITH_Xmms=OFF \
          -DWITH_Googlegadgets=OFF \
          -DWITH_libgps=OFF \
          -DPYTHON_EXECUTABLE=/usr/bin/python2 \
          -DWITH_CkConnector=OFF \
		  -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4
        make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install

	install -D -m644 "${srcdir}"/kde.pam "${pkgdir}"/etc/pam.d/kde
	install -D -m644 "${srcdir}"/kde-np.pam "${pkgdir}"/etc/pam.d/kde-np
	install -D -m644 "${srcdir}"/kscreensaver.pam "${pkgdir}"/etc/pam.d/kscreensaver

	install -d -m755 "${pkgdir}"/usr/share/xsessions/
	ln -sf /usr/share/apps/kdm/sessions/kde-plasma{,-safe}.desktop \
      "${pkgdir}"/usr/share/xsessions/
	install -d -m755 "${pkgdir}"/etc/kde/{env,shutdown}

	install -d -g 135 -o 135 "${pkgdir}"/var/lib/kdm
    install -D -m644 "${srcdir}"/kdm.service \
      "${pkgdir}"/usr/lib/systemd/system/kdm.service
    install -Dm644 "${srcdir}"/kdm.logrotate "${pkgdir}"/etc/logrotate.d/kdm
}
