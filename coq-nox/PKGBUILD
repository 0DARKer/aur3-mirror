# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname=coq-nox
pkgver=8.4pl4
pkgrel=4
pkgdesc='Formal proof management system. Headless version, without CoqIDE.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
options=('!emptydirs')
depends=('ocaml' 'camlp4')
provides=('coq')
conflicts=('coq')
optdepends=('coq-doc')
source=("http://coq.inria.fr/distrib/V$pkgver/files/coq-$pkgver.tar.gz"
  "ocaml-4.02.patch")
md5sums=('6a9f61cf0ece644b170f722fbc8cf2a1'
         'b5168185706d2da445eb4dc22286c8dd')
sha1sums=('4dfc3a1ae65f5c480ddc4387d21549a526183e00'
          'f91a15857865cc6f64f717c5bf065704173786bf')
sha256sums=('06c3aeab7819eed8f35ce794c887a70cf3b4f6b71ee52cd3110fb4e526717f01'
            '91aca7acf21d103f66b5c0385fe54429fd8e2591e148018b6f4420cc5b545d49')

build() {
  cd "coq-$pkgver"

  patch -p1 < ../ocaml-4.02.patch

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -configdir '/etc/xdg/coq/' \
    -opt \
    -with-doc no \
    -coqide no \
    -usecamlp4

  make world
}

package() {
  cd "coq-$pkgver"

  make COQINSTALLPREFIX="$pkgdir" install
}
