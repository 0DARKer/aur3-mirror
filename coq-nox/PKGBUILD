# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname=coq-nox
pkgver=8.4pl2
pkgrel=1
pkgdesc='Formal proof management system. Headless version, without CoqIDE.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
options=('!emptydirs')
depends=('ocaml')
provides=('coq')
conflicts=('coq')
makedepends=('camlp5-transitional')
optdepends=('coq-doc')
source=("http://coq.inria.fr/distrib/V$pkgver/files/coq-$pkgver.tar.gz")
md5sums=('7fd98da8db35a89b9718333a31af6153')

build() {
  cd "coq-$pkgver"

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -configdir '/etc/xdg/coq/' \
    -opt \
    -with-doc no\
    -coqide no

  make world
}

package() {
  cd "coq-$pkgver"

  make COQINSTALLPREFIX="$pkgdir" install
}
