# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname=coq-nox
pkgver=8.4pl3
pkgrel=1
pkgdesc='Formal proof management system. Headless version, without CoqIDE.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
options=('!emptydirs')
depends=('ocaml')
provides=('coq')
conflicts=('coq')
optdepends=('coq-doc')
source=("http://coq.inria.fr/distrib/V$pkgver/files/coq-$pkgver.tar.gz")
md5sums=('40a3057ae296bbb228e1e6537f44ccb1')
sha1sums=('b7d7f49412b0b9827bc461a78b5340e69cc0d3f4')
sha256sums=('97583d637f981c5554007f4e99ce6420ebc737186b1d021bd71766fd891cfb38')

build() {
  cd "coq-$pkgver"

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -configdir '/etc/xdg/coq/' \
    -opt \
    -with-doc no \
    -coqide no \
    -usecamlp4

  make world
}

package() {
  cd "coq-$pkgver"

  make COQINSTALLPREFIX="$pkgdir" install
}
