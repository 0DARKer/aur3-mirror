# Maintainer: Thiago Perrotta <perrotta dot thiago at poli dot ufrj dot br>
# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=translate-shell
pkgver=0.8.23
pkgrel=3
pkgdesc="Google Translate to serve as a command-line tool."
arch=('any')
url="http://www.soimort.org/$pkgname"
license=('Public Domain')
depends=('gawk>=4.0')
makedepends=('git')
optdepends=(
#   'zsh: for interpreting the wrapper script'
    'fribidi: needed for displaying right-to-left (RTL) languages'
    'mplayer: needed for the Text-to-Speech functionality'
    'mpv: needed for the Text-to-Speech funcionality'
    'espeak: needed for the Text-to-Speech functionality'
    'rlwrap: needed for readline-style editing and history in the interactive mode'
    'emacs: for using the Emacs interface (with the -E flag)'
)
install=$pkgname.install
source=("https://github.com/soimort/$pkgname/archive/v$pkgver.tar.gz"
        "patch-gawk-4.1.2")

md5sums=('f7fa96b113fb23e2cb1f0dd331882db3'
         'b2cbd17e160a51be19a43a97deca51af')
sha256sums=('e725b5a126f37b752de91cae6cf63726874612ce18a6a51d047d204085d08318'
            '91afab590da2d33a06467fc0060248cb8df8a923003566a7d7a78612a61f635c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 < ../patch-gawk-4.1.2
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$pkgdir/usr/bin/"

  # Main executable
  make "INSTDIR=$pkgdir/usr/bin" install
  ln -s /usr/bin/trans "$pkgdir/usr/bin/$pkgname"

  # Man page
  install -Dm644 man/trans.1 "$pkgdir/usr/share/man/man1/trans.1"
  install -Dm644 man/trans.1 "$pkgdir/usr/share/man/man1/$pkgname.1"
  
  # License
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
