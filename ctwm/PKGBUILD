# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Stefan Husmann <stefan.husmann@t-online.de>
# Contributor: flixie <69one@gmx.net>
pkgname=ctwm
pkgver=3.8.1
pkgrel=1
pkgdesc="Richard's Tab Window Manager"
arch=('x86_64' 'i686')
url="http://ctwm.free.lp.se/"
license=('custom:MIT' 'custom:X')
depends=('glibc' 'libjpeg-turbo' 'libxmu' 'libxpm')
makedepends=('imake' 'setconf')
backup=('etc/X11/ctwm/system.ctwmrc')
source=("http://ctwm.free.lp.se/dist/$pkgname-$pkgver.tar.gz"
        "Imakefile.local"
        "copyright")
sha256sums=('293657511eaa3f0f147bae2f50ed3caf6bbaa64dd142dfdbd9d6576d89c6970d'
            'ba8b25071ba0cdf03299dd827296b0fd8a6e549dac6f1ce8d50d9f26afd17041'
            '5242dcee1ffc736abd3e7022648c51039da7f4d998a2eb1eaee57367816886c1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  setconf Imakefile CONFDIR /etc/X11/ctwm
  setconf Imakefile PIXMAPDIR /etc/X11/ctwm/images
  install -Dm644 $srcdir/Imakefile.local $srcdir/$pkgname-$pkgver
  xmkmf
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR=$pkgdir install
  make DESTDIR=$pkgdir install.man
  install -Dm644 "$srcdir/copyright" \
    "$pkgdir/usr/share/licenses/ctwm/copyright"
}

# vim:set ts=2 sw=2 et:
