# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbase=libgusb
pkgname=lib32-${_pkgbase}
pkgver=0.1.3
pkgrel=3
pkgdesc="GLib wrapper around libusb1 (32 bit)"
arch=('x86_64')
url="https://gitorious.org/gusb/"
license=('LGPL2.1')
depends=('lib32-systemd-tools' 'lib32-libusbx' "${_pkgbase}")
makedepends=('gcc-multilib')
source=(http://people.freedesktop.org/~hughsient/releases/${_pkgbase}-${pkgver}.tar.xz)
options=('!libtool')
sha256sums=('4eebb56700d9366c513bfa8f9321a60388f61bade0236a90d66854caea97f0e6')

build() {
  export CC='gcc -m32'
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}
