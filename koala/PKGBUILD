# Maintainer: JokerYu <dayushinn@gmail.com>
pkgname=koala
pkgver=2.0.2
pkgrel=4
pkgdesc="A GUI application for Less, Sass, Compass and CoffeeScript compilation."
arch=("i686" "x86_64")
url="http://koala-app.com/"
license=("Apache")
depends=("gconf" "gtk2" "nss" "libudev.so.0")
optdepends=(
  "lessc: Use System LESS compiler" 
  "ruby-sass: Use System Sass compiler" 
  "ruby-compass: Use System Compass compiler" 
  "coffee-script: Use System CoffeeScript compiler"
)
makedepends=("nodejs" "node-webkit")
options=(!strip)
conflicts=("koala-deb")
source=(
  "https://github.com/oklai/koala/archive/v2.0.2.tar.gz" 
  "koala.png" 
  "Koala.desktop"
)
md5sums=(
  "c3a6a867445a4232d5b03296d2d1dca9" 
  "cde974f0b5bc366486ec0e1bd4c98ca3" 
  "7f421e6abd56bf11160df22674a3fa7f"
)
install=koala.install

build() {
  cd ${pkgname}-${pkgver}/src

  msg "disable frameless window"
  sed -i 's/"frame": false/"frame": true/g' package.json

  msg "installing node modules"
  npm install clean-css@1.0.12 coffee-script@1.6.3 less@1.6.1 uglify-js@2.3.6

  msg "preparing nw file"
  zip -r ../${pkgname}.nw *

  cd ../
  cat /usr/bin/nw ${pkgname}.nw > ${pkgname} && chmod +x ${pkgname}
  msg "build done"
}

package() {
  msg "copying files"
  mkdir -p ${pkgdir}/usr/share/{applications,${pkgname}}
  install -Dm 644 Koala.desktop ${pkgdir}/usr/share/applications
  install -Dm 644 {koala.png,/usr/lib/node-webkit/nw.pak} ${pkgdir}/usr/share/${pkgname}
  install -Dm 755 ${pkgname}-${pkgver}/${pkgname} ${pkgdir}/usr/share/${pkgname}
}