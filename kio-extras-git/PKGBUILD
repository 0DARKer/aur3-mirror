# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kio-extras-git
pkgver=r5653.e6793e0
pkgrel=1
pkgdesc="Additional components to increase the functionality of KIO"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kio-extras'
license=('LGPL')
depends=('kdelibs4support-git' 'khtml-git' 'kdnssd-git' 'libssh' 'smbclient' 'exiv2' 'openexr')
makedepends=('extra-cmake-modules' 'git' 'openslp' 'kdoctools-git')
conflicts=('kio-extras')
provides=('kio-extras')
source=('git://anongit.kde.org/kio-extras.git')
md5sums=('SKIP')

pkgver() {
  cd kio-extras
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kio-extras/ \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
