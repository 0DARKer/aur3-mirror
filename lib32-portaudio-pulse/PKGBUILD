_pkgbasename=portaudio
pkgname=lib32-$_pkgbasename-pulse
pkgver=19_20071207
pkgrel=1
pkgdesc="A free, cross-platform, open source, audio I/O library (with pulse support, 32 bit)"
arch=('x86_64')
url="http://www.portaudio.com/"
license=('custom')
depends=('lib32-jack' 'lib32-alsa-lib' 'lib32-libpulse' "$_pkgbasename")
makedepends=('gcc-multilib')
conflicts=("lib32-$_pkgbasename")
provides=("lib32-$_pkgbasename")
options=('!libtool')
source=(http://www.portaudio.com/archives/pa_stable_v$pkgver.tar.gz portaudio-19-alsa_pulse.patch)
md5sums=('d2943e4469834b25afe62cc51adc025f'
         '8ce32fcd7555f2705657fc2017c7664d')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cd "${srcdir}/${_pkgbasename}"
  patch -Nup1 < ../portaudio-19-alsa_pulse.patch
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package () {
  cd "${srcdir}/${_pkgbasename}"
  make DESTDIR=$pkgdir install
  rm -rf "${pkgdir}"/usr/include
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
