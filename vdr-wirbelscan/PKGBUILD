# Maintainer: David Spicer <azleifel at gmail dot com>
# Contributor: Maik Opitz <maik@fjordblau.de>

pkgname=vdr-wirbelscan
_pluginname=wirbelscan
pkgver=0.0.7
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="A channel search plugin for VDR"
url="http://wirbel.htpc-forum.de/wirbelscan/index2.html"
license=('GPL2')
depends=('vdr>=1.7.42')
source=(http://wirbel.htpc-forum.de/${_pluginname}/${pkgname}-${pkgver}.tgz
       receiver-api-fixes.patch)
md5sums=('b07d57cecbb0dc3cbf1d13f3d47bc7f3'
         'eb847c8762796211ad7a51275b38f238')

package() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  # Patches
  patch -Np1 -i "${srcdir}/receiver-api-fixes.patch"

  # VDR directory environment
  _vdrCONFDIR="/var/lib/vdr"
  _vdrINCDIR="/usr/include"
  _vdrLIBDIR="/usr/lib/vdr"
  _vdrLOCDIR="/usr/share/locale"

  # Create install directories
  install -d -m755 "${pkgdir}${_vdrLIBDIR}"
  install -d -m755 "${pkgdir}${_vdrLOCDIR}"
  
  make LIBDIR="${pkgdir}${_vdrLIBDIR}" \
       LOCALEDIR="${pkgdir}${_vdrLOCDIR}" \
       VDRDIR="${_vdrINCDIR}/vdr" \
       all

  # Install plugin service interface header
  install -d -m755 "${pkgdir}${_vdrINCDIR}/vdr"
  install -m644 wirbelscan_services.h "${pkgdir}${_vdrINCDIR}/vdr"
  
  # Install documents
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  for _file in README HISTORY SERVICES.html TODO; do
    install -m644 ${_file} "${pkgdir}/usr/share/doc/${pkgname}/${_file}"
  done
}
