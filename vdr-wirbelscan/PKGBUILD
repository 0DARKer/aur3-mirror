# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: Maik Opitz <maik@fjordblau.de>

pkgname=vdr-wirbelscan
_pluginname=wirbelscan
pkgver=0.0.6
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A channel search plugin for VDR"
url="http://wirbel.htpc-forum.de/wirbelscan/index2.html"
license=('GPL2')
depends=('gcc-libs' 'vdr')
source=(http://wirbel.htpc-forum.de/${_pluginname}/${pkgname}-${pkgver}.tgz
        fix-vdr-1.6.0-build.patch)
md5sums=('705473eb41bc3c8bc7d2cf54ff66d4e2'
         '233a4a582549957d1833504eef77544b')

build() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  # Fix directory permissions to make cleaning up easier
  chmod 755 clean_code
  chmod 755 patches

  # Fix build for vdr 1.6.0-2
  patch -Np1 -i "${srcdir}/fix-vdr-1.6.0-build.patch"

  # Assumed VDR directory environment (VDRDIR/Make.config):
  # MANDIR       = /usr/share/man
  # BINDIR       = /usr/bin
  # LOCDIR       = /usr/share/locale
  # PLUGINLIBDIR = /usr/lib/vdr
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr
  
  install -d -m755 "${pkgdir}/usr/include/vdr"
  install -d -m755 "${pkgdir}/usr/lib/vdr"
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -d -m755 "${pkgdir}/usr/share/locale"
  
  make VDRDIR="/usr/include/vdr" \
       LIBDIR="${pkgdir}/usr/lib/vdr" \
       LOCALEDIR="${pkgdir}/usr/share/locale" all

  # Install plugin service interface header
  install -m644 wirbelscan_services.h "${pkgdir}/usr/include/vdr/"
  
  # Install documents
  for _file in README HISTORY SERVICES.html TODO; do
    install -m644 "${_file}" "${pkgdir}/usr/share/doc/${pkgname}/${_file}"
  done
}
