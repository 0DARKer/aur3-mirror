# Maintainer: Luca Cesari <luca.cesari@bewq.org>
# Contributor: Stebalien

_pkgname=pycarddav
pkgname=$_pkgname-git
pkgver=0.6.1.22.g50f862b
epoch=1
pkgrel=1
pkgdesc="A CLI CardDAV client"
arch=('any')
url="http://lostpackets.de/pycarddav/"
license=('Expat/MIT')
conflicts=('pycarddav')
provides=('pycarddav')
depends=('python2' 'python2-vobject' 'python2-requests' 'python2-lxml'
'python2-urwid' 'python2-xdg')
optdepends=('python2-ndg-httpsclient: SSL Server Name Indication (SNI) support')
makedepends=('asciidoc' 'python2-distribute')

source=('git+https://github.com/geier/pycarddav.git')

pkgver() {
  cd pycarddav
  git describe --always | sed -e 's|-|.|g' -e 's|^v||'
}

build() {
  # Compile documentation
  cd $_pkgname/doc/man
  make
}

package() {
  cd $_pkgname

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # Manuals
  install -Dm644 doc/man/pycard-import.1 "$pkgdir/usr/share/man/man1/pycard-import.1"
  install -Dm644 doc/man/pycardsyncer.1 "$pkgdir/usr/share/man/man1/pycardsyncer.1"
  install -Dm644 doc/man/pc_query.1 "$pkgdir/usr/share/man/man1/pc_query.1"

  # Other
  install -Dm644 pycard.conf.sample "$pkgdir/usr/share/doc/$pkgname/pycard.conf.sample"
  install -Dm644 README.rst "$pkgdir/usr/share/doc/$pkgname/README.rst"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
sha256sums=('SKIP')
