# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth-svn
pkgver=438
pkgrel=1
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth (SVN version)"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL')
depends=('ffmpeg' 'python' 'qt' 'libass')
makedepends=('cython' 'subversion' 'waf' 'yasm' 'python-sphinx')
optdepends=('ffmpegsource: Avisynth/Vapoursynth plugin for easy frame accurate access')
provides=('vapoursynth')
conflicts=('vapoursynth')
source=('exprfilter.diff' 'vapoursynth.diff')
md5sums=('932afb4166a1025626b99516b2224e31'
         'e74c163b774b67e4d99705e066239dc4')

_svntrunk='http://vapoursynth.googlecode.com/svn/trunk/'
_svnmod='vapoursynth'

build() {
  cd "${srcdir}"

  msg "Connecting to SVN server...."

  if [ -d "${srcdir}/${_svnmod}" ] ; then
    cd "${_svnmod}" && svn update
  else
    svn co "${_svntrunk}" "${_svnmod}"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -fr "${srcdir}/${_svnmod}-build"
  cp -R "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd  "${srcdir}/${_svnmod}-build"

  mkdir doc/_static
  patch --silent -Np0 -i "${srcdir}/exprfilter.diff"
  patch --silent -Np0 -i "${srcdir}/vapoursynth.diff"
  waf configure --prefix="/usr" --docs="true"
  waf build "${MAKEFLAGS}"
  python setup.py build
}

package() {
  cd  "${srcdir}/${_svnmod}-build"
  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
}
