# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth-svn
pkgver=447
pkgrel=1
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth (SVN version)"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL')
depends=('ffmpeg' 'python' 'qt4' )
makedepends=('cython' 'subversion' 'waf' 'yasm' 'python-sphinx')
optdepends=('ffmpegsource: Avisynth/Vapoursynth plugin for easy frame accurate access')
provides=('vapoursynth')
conflicts=('vapoursynth')
source=('vapoursynth::svn+http://vapoursynth.googlecode.com/svn/trunk/'
        'lut_lut2-fix-r0.patch'
        'test_regression.py')
md5sums=('SKIP'
         'a5f630c97598b22e9b1dd5645b894508'
         '44ccc8b054c5a2f7d3304667bf30e816')
_svnmod='vapoursynth'

pkgver() {
  cd "${SRCDEST}/${_svnmod}"
  svnversion
}

prepare() {
  cd "${_svnmod}"
  mkdir doc/_static
  sed 's|vapoursynth.h|VapourSynth.h|g' -i include/VSScript.h
  patch -Np1 -i "${srcdir}/lut_lut2-fix-r0.patch"
}

build() {
  cd "${_svnmod}"

  waf configure --prefix="/usr" --docs="true"
  waf build ${MAKEFLAGS}
  python setup.py build
}

package() {
  cd "${_svnmod}"
  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 test/test.py "${pkgdir}/usr/share/vapoursynth/test/test.py"
  install -Dm644 "${srcdir}/test_regression.py" "${pkgdir}/usr/share/vapoursynth/test/test_regression.py"
}
