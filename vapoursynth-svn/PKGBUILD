# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth-svn
pkgver=441
pkgrel=1
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth (SVN version)"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL')
depends=('ffmpeg' 'python' 'qt' 'libass')
makedepends=('cython' 'subversion' 'waf' 'yasm' 'python-sphinx')
optdepends=('ffmpegsource: Avisynth/Vapoursynth plugin for easy frame accurate access')
provides=('vapoursynth')
conflicts=('vapoursynth')

_svntrunk='http://vapoursynth.googlecode.com/svn/trunk/'
_svnmod='vapoursynth'

build() {
  cd "${srcdir}"

  msg "Connecting to SVN server...."

  if [ -d "${_svnmod}" ] ; then
    cd "${_svnmod}" && svn update
  else
    svn co "${_svntrunk}" "${_svnmod}"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -fr "${srcdir}/${_svnmod}-build"
  cp -R "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  mkdir doc/_static
  waf configure --prefix="/usr" --docs="true"
  waf build ${MAKEFLAGS}
  python setup.py build
}

package() {
  cd  "${srcdir}/${_svnmod}-build"
  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
}
