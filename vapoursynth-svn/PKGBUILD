# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth-svn
pkgver=451
pkgrel=1
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth (SVN version)"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL')
depends=('ffmpeg' 'python' 'qt4' )
makedepends=('cython' 'subversion' 'waf' 'yasm' 'python-sphinx')
optdepends=('ffmpegsource: Avisynth/Vapoursynth plugin for easy frame accurate access')
provides=('vapoursynth')
conflicts=('vapoursynth')
source=('vapoursynth::svn+http://vapoursynth.googlecode.com/svn/trunk/')
md5sums=('SKIP')
_svnmod='vapoursynth'

pkgver() {
  cd "${SRCDEST}/${_svnmod}"
  svnversion
}

prepare() {
  cd "${_svnmod}"
  mkdir doc/_static
}

build() {
  cd "${_svnmod}"

  waf configure --prefix="/usr" --docs="true"
  waf build ${MAKEFLAGS}
  python setup.py build
}

package() {
  cd "${_svnmod}"
  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
  for i in $(find sdk -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/vapoursynth/${i}"; done
  install -Dm644 1test4.py "${pkgdir}/usr/share/vapoursynth/test/1test4.py"
  install -Dm644 test/test.py "${pkgdir}/usr/share/vapoursynth/test/test.py"
  install -Dm644 test/filter_test.py "${pkgdir}/usr/share/vapoursynth/test/filter_test.py"
}
