# Maintainer : Dobroslaw Kijowski [dobo] <dobo90_at_gmail.com>
# Contributor: Valentin Hăloiu <vially.ichb+aur [at] gmail.com>
# Contributor: Étienne Deparis <etienne [at] depar.is>
# Contributor: Yannik Stein <yannik.stein [at] gmail.com>
# Submitter: vbPadre <vbpadre [at] gmail.com>

pkgname=ajenti
pkgver=1.2.17.2
pkgrel=1
pkgdesc='An easy server administration frontend.'
arch=(any)
url='http://ajenti.org/'
license=(LGPL)
depends=(python2-gevent-socketio python2-lxml python2-passlib python2-psutil python2-daemon
         python2-catcher python2-reconfigure python2-pyopenssl python2-pillow python2-exconsole
         python2-ldap)
#makedepends=(coffee-script lessc yuicompressor)
install=ajenti.install
source=(https://pypi.python.org/packages/source/a/${pkgname}/${pkgname}-${pkgver}.tar.gz
        ajenti.service
        detach_process.patch)
md5sums=(c390ac0002949f05c29eb78fa2e688b5
         74e13666013eea439b08e6fcbeef3a6d
         8db0af90db94afb49df910ceb2970b92)
backup=(etc/ajenti/config.json)

prepare() {
  # systemd expects forking process to detach form parent. let's make it
  patch -p0 < detach_process.patch

  cd ${srcdir}/${pkgname}-${pkgver}
  
  msg2 'Replacing python shebang by python2 shebang...'
  find . -type f -exec sed -i \
    -e '1s|^#!/usr/bin/env python$|#!/usr/bin/env python2|' \
    -e '1s|^#!/usr/bin/python$|#!/usr/bin/env python2|' \
    "{}" \;
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  python2 setup.py build
  #make clean
  #make tgz

  #cd ${srcdir}/${pkgname}-${pkgver}/dist
  #tar xzf ${pkgname}-${pkgver}.tar.gz
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # cd ${srcdir}/${pkgname}-${pkgver}/dist/${pkgname}-${pkgver}
  
  python2 setup.py install --root=${pkgdir} --optimize=1
 
  # Fix config file permissions
  chmod 0640 ${pkgdir}/etc/ajenti/config.json

  rm -r ${pkgdir}/etc/init.d

  install -D -m 644 ${srcdir}/ajenti.service ${pkgdir}/usr/lib/systemd/system/ajenti.service
}
