# Contributor: Jed Brown <jed@59A2.org>
pkgname=(parmetis)
_prefix=/usr
pkgver=3.2.0
pkgrel=2
pkgdesc="A parallel graph partitioning library"
url="http://glaros.dtc.umn.edu/gkhome/metis/parmetis/overview"
arch=('i686' 'x86_64')
license="custom"
depends=()
makedepends=()
conflicts=()
replaces=()
provides=(metis)
backup=()
source=(http://glaros.dtc.umn.edu/gkhome/fetch/sw/parmetis/ParMetis-$pkgver.tar.gz)
md5sums=('d97fea8c8776e1f9340ff90d9992e929')

build() {
  cd $srcdir/ParMetis-$pkgver
  cat > Makefile.in <<EOF
CC= ${_prefix}/bin/mpicc
OPTFLAGS= -O2 -fPIC
COPTIONS= -DNDEBUG
LD= ${_prefix}/bin/mpicc
AR= ar rv
RANLIB= ar -ts
EOF
  make
  ${_prefix}/bin/mpicc -o libmetis.so -shared METISLib/*.o -lm
  ${_prefix}/bin/mpicc -o libparmetis.so -shared ParMETISLib/*.o libmetis.so -lm
}

package () {
  cd $srcdir/ParMetis-$pkgver
  install -m755 -d $pkgdir${_prefix}/lib
  install -m644 lib{metis,parmetis}.{a,so} $pkgdir${_prefix}/lib
  install -m755 -d $pkgdir${_prefix}/include/metis
  install -m644 METISLib/*.h $pkgdir${_prefix}/include/metis
  install -m644 parmetis.h $pkgdir${_prefix}/include
  install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}
