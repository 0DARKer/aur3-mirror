# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>
# Contributor: Ido Rosen <ido@kernel.org>
# Contributor: Brett Hoerner <brett@bretthoerner.com>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>
# Contributor: Mathieu Clabaut <mathieu.clabaut@gmail.com>
# Contributor: helios <aur@wiresphere.de>
# Contributor: George Ornbo <gornbo@gmail.com>
# Contributor: Niklas Heer <niklas.heer@me.com>

pkgname=vagrant
pkgver=1.5.1
pkgrel=1
pkgdesc="Build and distribute virtualized development environments"
arch=('any')
url="http://vagrantup.com"
license=('MIT')
depends=('ruby' 'ruby-childprocess' 'ruby-erubis' 'ruby-i18n' 
         'ruby-log4r' 'ruby-net-ssh-2.7' 'ruby-net-scp' 'ruby-wdm'
         'ruby-archive-tar-minitar' 'ruby-json-1.7' 'ruby-bundler'
         'ruby-listen-2.4' 'ruby-timers-1.1' 'ruby-rb-kqueue')
options=('!emptydirs')
source=(https://github.com/mitchellh/$pkgname/archive/v$pkgver.tar.gz)
md5sums=('f635262bcdcd352bdd68d855fce00e61')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  gem build $pkgname.gemspec
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  local _gemdir="$( ruby -rubygems -e'puts Gem.default_dir' )"

  gem install --no-user-install --ignore-dependencies \
    -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $pkgname-$pkgver.gem

  rm "$pkgdir/$_gemdir"/cache/$pkgname-$pkgver.gem

  install -Dm644 "$pkgdir/$_gemdir"/gems/$pkgname-$pkgver/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # suppress warning about using official packages and set GEM_HOME
  sed -i '0,/^$/s/^$/\nENV\["VAGRANT_INSTALLER_ENV"\] \= "1"\
ENV\["GEM_HOME"\] \= "\/usr\/lib\/ruby\/gems\/2.1.0\/"\n/' \
    "$pkgdir"/usr/bin/$pkgname
}
