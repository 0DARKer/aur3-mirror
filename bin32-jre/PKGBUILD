# Maintainer: Rustam Tsurik <rustam.tsurik@gmail.com>

pkgname=bin32-jre
pkgver=7u21
pkgrel=1
pkgdesc="32-bit Oracle Java with the Mozilla plugin for Arch x86_64"
arch=('x86_64')
_arch=i386
license=('custom')
url="http://www.java.com"
depends=('lib32-glibc' 'lib32-libgl' 'lib32-libx11' 'lib32-libxtst' 'mozilla-common' 'lib32-libxi')
makedepends=('diffutils' 'fakeroot' 'pkgconfig')
install=bin32-jre.install
bin32name=jdk-${pkgver}-dlj-linux-i586
source=("jre-$pkgver-linux-i586.tar.gz::http://javadl.sun.com/webapps/download/AutoDL?BundleId=76851"
        'java-policy-settings.desktop'
        'jre.sh'
        'jre.csh'
        'javaws-launcher')
md5sums=('d1df6cbb7c2b5cc7e9dd05b3e8e838f9'
         '6614b04176b9b7dfe26f22e9ce846801'
         '7cd3dc10e7a37468cad4053a067dcd01'
         'cc90df2df6fe80fab885a80036d420a1'
         '45c15a6b4767288f2f745598455ea2bf')

package() {
  # Create required dirs
  cd $(ls -1d jre1.7.0_*/ | tail -1)
  mkdir -p "$pkgdir"/{opt/java/jre,usr/{share/{,licenses/jre},lib/mozilla/plugins},etc/{.java/.systemPrefs,profile.d}}

  # Re-order folders a bit
  mv lib/desktop/{applications,icons,mime} "$pkgdir"/usr/share/

  # Remove empty and redundant dirs from 'lib'
  rm -r plugin
  rmdir lib/{applet,desktop}

  # Move stuff in place
  mv * "$pkgdir"/opt/java/jre

  # Symlink the plugin
  cd "$srcdir"
  ln -s /opt/java/jre/lib/$_arch/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/libnpjp2.so

  # Install scripts, .desktop file and licenses
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -Dm644 java-policy-settings.desktop "$pkgdir"/usr/share/applications/java-policy-settings.desktop
  install -m755 jre.{c,}sh "$pkgdir"/etc/profile.d/
  cp "$pkgdir"/opt/java/jre/{COPYRIGHT,LICENSE,THIRDPARTYLICENSEREADME.txt} "$pkgdir"/usr/share/licenses/jre/

  # Tweak the javaws .desktop file
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "$pkgdir"/usr/share/applications/sun-javaws.desktop
}
