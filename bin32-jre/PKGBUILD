# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=bin32-jre
_major=8
_minor=20
_build=b26
pkgver=${_major}u$_minor
pkgrel=1
pkgdesc="Java Runtime Environment (32-bit)"
arch=('x86_64')
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
license=('custom')
depends=('lib32-libxrender' 'lib32-libxtst' 'lib32-gtk2') #  'shared-mime-info' 'xdg-utils' 'desktop-file-utils' 'hicolor-icon-theme'
optdepends=('lib32-alsa-lib: sound'
            'ttf-dejavu: fonts')
provides=("java-runtime-32=$_major" "java-runtime-headless-32=$_major" "java-web-start-32=$_major")
conflicts=("${provides[@]}" "${provides[@]/$_major/7}")
backup=('opt/java32/jre/lib/security/java.policy')
#install=jre.install
source=("http://download.oracle.com/otn-pub/java/jdk/$pkgver-$_build/jre-$pkgver-linux-i586.tar.gz"
        'javaws-launcher'
        'jre.csh'
        'jre.sh')
md5sums=(`curl -sL ${url/i*}/javase8-binaries-checksum-2133161.html | sed -nr "s|.*>${source##*/}</td> *<td>(<*[^<]*).*|\1|p"`
         '224bcf5e1f5d14bfa493ed243a5a5287'  # javaws-launcher
         '92dec202858f2bf7729c6805b1bd3ae4'  # jre.csh
         '85ba1d2b39d5e6efad89ef98d0f19909') # jre.sh
## Alternative mirrors, if your local one is throttled:
#source[0]="http://ftp.wsisiz.edu.pl/pub/pc/pozyteczne%20oprogramowanie/java/jre-$pkgver-linux-i586.tar.gz"
#source[0]="http://uni-smr.ac.ru/archive/dev/java/JRE/$_major/JRE-$_major.$_minor/jre-$pkgver-linux-i586.tar.gz"

DLAGENTS=('http::/usr/bin/curl -LC - -b "oraclelicense=a" -O')

package() {
  msg2 "Creating required dirs"
  cd jre1.$_major.0_$_minor
  mkdir -p "$pkgdir"/{opt/java32/jre,usr/{lib32/mozilla/plugins,share/licenses/$pkgname},etc/profile.d}

  msg2 "Removing undesired stuff" # desktop file, icons, man pages
  rm -r plugin/ man/ lib/desktop/*

  msg2 "Moving stuff in place"
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/$pkgname/
  mv * "$pkgdir"/opt/java32/jre/

  msg2 "Symlinking plugin"
  ln -s /opt/java32/jre/lib/i386/libnpjp2.so "$pkgdir"/usr/lib32/mozilla/plugins/

  msg2 "Installing additions"
  cd "$srcdir"
  install -m755 jre.{,c}sh "$pkgdir"/etc/profile.d/
  install -m755 javaws-launcher "$pkgdir"/opt/java32/jre/bin/
  
  msg2 "Tweaking profile.d files for 32-bit in a 64-bit system"
  sed -i -e '/J2REDIR/d;/JAVA_HOME/d;s#opt/java#&32#' "$pkgdir"/etc/profile.d/*
  
  msg2 "Renaming files for 32-bit in a 64-bit system"
  for bin in $(ls "$pkgdir"/opt/java32/jre/bin/); do
    mv "$pkgdir"/opt/java32/jre/bin/$bin "$pkgdir"/opt/java32/jre/bin/${bin}32
  done
  ln -fs jcontrol32 "$pkgdir"/opt/java32/jre/bin/ControlPanel32
  mv "$pkgdir"/etc/profile.d/{,bin32-}jre.sh
  mv "$pkgdir"/etc/profile.d/{,bin32-}jre.csh

  # Copy/paste from system clipboard to unsigned Java applets has been disabled since 6u24:
  # - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java
  # - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html
  msg2 "Enabling copy+paste to unsigned applets"
  _line=$(awk '/permission/{a=NR}; END{print a}' "$pkgdir"/opt/java32/jre/lib/security/java.policy)
  sed "$_line a\\\\n \
        // (AUR) Allow unsigned applets to read system clipboard, see:\n \
        // - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java\n \
        // - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html\n \
        permission java.awt.AWTPermission \"accessClipboard\";" \
     -i "$pkgdir"/opt/java32/jre/lib/security/java.policy     
}
