# Contributor: Leslie P. Polzer <polzer@gnu.org>
# Contributor: bender02 at gmx dot com

pkgname=mongoose
pkgver=2.11
pkgrel=1
pkgdesc="Small and quick-to-use web server; https/php/cgi support"
arch=('i686' 'x86_64')
license=('MIT')
url="http://code.google.com/p/mongoose/"
optdepends=('php-cgi: for php support')
backup=('etc/mongoose/mongoose.conf')
source=(http://mongoose.googlecode.com/files/$pkgname-${pkgver}.tgz
        mongoose.conf
        rc.d.mongoose)
md5sums=('f6985da7513d354cc18b21b7670d23c1'
         'd09f86f35f156393324ed4e9cdaa3246'
         '2aea8278222400289576a626fd1ef33d')

build() {
  cd $srcdir/$pkgname

  make linux
}

package() {
  cd $srcdir

  # sample rc.d script and config file courtesy of
  # Alexandr Leykin: al at instrument dot pl dot ua
  install -D -m755 rc.d.mongoose $pkgdir/etc/rc.d/mongoose
  install -D -m644 mongoose.conf $pkgdir/etc/mongoose/mongoose.conf

  cd $srcdir/$pkgname

  install -D -m755 mongoose $pkgdir/usr/bin/mongoose
  install -D -m644 mongoose.1 $pkgdir/usr/share/man/man1/mongoose.1

  # install the sources + examples to /usr/share/mongoose
  # comment out if you don't want that to happen
  install -d $pkgdir/usr/share
  tar -x -f $srcdir/$pkgname-$pkgver.tgz -C $pkgdir/usr/share/
}

