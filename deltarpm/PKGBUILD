# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>

pkgname=deltarpm
pkgver=3.6
pkgrel=1
pkgdesc='Create deltas between rpms'
arch=('i686' 'x86_64')
license=('BSD')
url="http://gitorious.org/${pkgname}/${pkgname}"
depends=('rpm-org' 'zlib')
makedepends=('python2' 'python3')
optdepends=('python2: for python2 module'
            'python3: for python3 module')
source=("ftp://ftp.suse.com/pub/projects/${pkgname}/${pkgname}-3.6.tar.bz2")
md5sums=('2cc2690bd1088cfc3238c25e59aaaec1')

build() {
    cd "${pkgname}-${pkgver}"
    make zlibbundled='' zlibldflags='-lz' zlibcppflags='' all
    make PYTHONS="python2 python3" python
}

package() {
    cd "${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}"      \
         prefix="/usr"            \
         mandir="/usr/share/man"  \
         PYTHONS="python2 python3" \
         install

    install -D -m644 LICENSE.BSD "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.BSD"
}
