# Maintainer: ava1ar <mail(at)ava1ar(dot)info>

pkgname=system76-driver
pkgver=14.04.7
pkgrel=1
pkgdesc="System76 Driver provides drivers, restore, and regression support for System76 computers"
arch=('any')
url="https://launchpad.net/system76-driver"
license=('GPL')
install="${pkgname}.install"
depends=('python>=3.4' 'python-gobject' 'python-dbus' 'dmidecode' 'pm-utils')
makepdepends=('patch')
optdepends=('pulseaudio: To apply microphone fix'
            'grub: To apply brigtness button fix'
)
source=("https://launchpad.net/~system76-dev/+archive/stable/+files/${pkgname}_${pkgver}.tar.gz"
	'system76.service'
	'galu1.patch'
	'grub.patch'
	'gtk.patch'
	'daemon.patch'
)
sha1sums=('fc230e507ca943040f5f465dae0f927cc4c4c2fe'
	  'cf763432441c3da0563eee041145a02109f2bb3a'
	  '86950ecc36d78b9b554d5af07251219b6c7a7a83'
	  'dd95fd7317786c9fc9b0233c248d192832a83fe0'
	  '880dbe3665e4fcaa87bb3f2f0c9a9ad6e67d68d5'
	  '775e2472a60c505de84df34a30d02943b31307ca'
)

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	# installing system76-daemon/system76-driver-pkexec/systemd service/python module
	install -m644 -D system76-daemon ${pkgdir}/usr/lib/${pkgname}/system76-daemon
	install -m755 -D system76-driver-pkexec ${pkgdir}/usr/bin/system76-driver-pkexec
	install -m644 -D ${startdir}/system76.service ${pkgdir}/usr/lib/systemd/system/system76.service
	python setup.py install --prefix=/usr --root=${pkgdir} --optimize=1
	
	# patching
	cd ${pkgdir}
	# galu1 model-specific patch
	patch --no-backup-if-mismatch -Np1 -i ${srcdir}/galu1.patch 
	# making grub optional - do not fail if no /etc/default/grub file found
	patch --no-backup-if-mismatch -Np1 -i ${srcdir}/grub.patch 
	# enabling "Restore System" button if all changes applied
	patch --no-backup-if-mismatch -Np1 -i ${srcdir}/gtk.patch 
	# generic daemon changes
	patch --no-backup-if-mismatch -Np1 -i ${srcdir}/daemon.patch

	# removing tests
	rm -rf ${pkgdir}/usr/lib/python3.4/site-packages/system76driver/tests/
} 
