# Maintainer: Michele Mocciola <mickele>

pkgname=salome-hexablock
pkgver=7.4.0
pkgrel=1
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation - HEXABLOCK Module"
url="http://www.salome-platform.org"
depends=('salome-kernel=7.4.0' 'salome-gui=7.4.0' 'salome-geom=7.4.0' 'python2' 'python2-numpy' 'boost' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'hdf5' 'graphviz' 'libxml2')
makedepends=('doxygen' 'python2-sphinx')
arch=('i686' 'x86_64')
license=('LGPL')
source=(http://files.salome-platform.org/Salome/Salome${pkgver}/src${pkgver}.tar.gz ${pkgname}.profile)

_source=HEXABLOCK_SRC_${pkgver}
_installdir=/opt/salome/hexablock

prepare() {
  cd "$srcdir/$_source"

  # python -> python2
  for _FILE in `grep -Rl "/usr/bin/env python" * `
  do
	sed -e "s|/usr/bin/env python|/usr/bin/env python2|" -i ${_FILE}
  done
}

build() {
  source /etc/salome/profile.d/salome-kernel.sh
  source /etc/salome/profile.d/salome-gui.sh
  source /etc/salome/profile.d/salome-geom.sh

  rm -rf "$srcdir/$_source/build"
  mkdir -p "$srcdir/$_source/build"
  cd "$srcdir/$_source/build"

  cmake .. \
     -DCMAKE_INSTALL_PREFIX=$_installdir \
     -DPYTHON_EXECUTABLE=/usr/bin/python2 \
     -DSPHINX_EXECUTABLE=/usr/bin/sphinx-build2
     
  make
}

package() {
  cd "$srcdir/$_source/build"

  make DESTDIR="$pkgdir/" install

  install -D -m755 "$srcdir/$pkgname.profile" \
                   "$pkgdir/etc/salome/profile.d/$pkgname.sh"
}
md5sums=('18055915a14e0921563072f830614e7f'
         '961b38ff24b084af92f2d7f26edd1f99')
