# Maintainer: megadriver <megadriver at gmx dot com>
# This is basically the old evince-gtk PKGBUILD by pizzapunk
# This one DOES conflict with evince from [extra], on purpose :)

pkgname=evince2-light
pkgver=2.32.0
pkgrel=2
pkgdesc="Simply a document viewer (GTK2 version, GNOME free!)"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/evince/"
license=('GPL')
depends=('gtk2' 'poppler-glib' 'libspectre' 'djvulibre' 't1lib' 'gsfonts' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool')
provides=('evince')
conflicts=('evince')
options=('!libtool')
install=evince2-light.install
source=(http://ftp.gnome.org/pub/gnome/sources/evince/2.32/evince-$pkgver.tar.bz2)
md5sums=('ebc3ce6df8dcbf29cb9492f8dd031319')

build() {
  cd "$srcdir"/evince-$pkgver

  # Don't depend on gnome-icon-theme
  sed -i '/gnome-icon-theme/d' configure

  ./configure --prefix=/usr \
              --libexecdir=/usr/lib/evince \
              --disable-static \
              --disable-schemas-compile \
              --disable-tests \
              --disable-nautilus \
              --disable-scrollkeeper \
              --disable-help \
              --enable-t1lib \
              --enable-pixbuf \
              --enable-impress \
              --without-keyring \
              --without-gconf
  make
}

package() {
  cd "$srcdir"/evince-$pkgver

  make DESTDIR="$pkgdir" install

  rm -rf $pkgdir/usr/share/GConf
}
