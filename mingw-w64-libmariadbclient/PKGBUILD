# Maintainer: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-libmariadbclient
epoch=1
pkgver=5.5.39
pkgrel=1
pkgdesc="MariaDB client libraries (mingw-w64)"
arch=(any)
url="http://mariadb.org"
license=("GPL")
makedepends=(mingw-w64-gcc mingw-w64-tools)
depends=(mingw-w64-crt)
options=(!strip !buildflags staticlibs)
source=("http://ftp.osuosl.org/pub/mariadb/mariadb-${pkgver}/win32-packages/mariadb-${pkgver}-win32.zip"
        "http://ftp.osuosl.org/pub/mariadb/mariadb-${pkgver}/winx64-packages/mariadb-${pkgver}-winx64.zip")
md5sums=('11dca746b4e590a4214c415185df8537'
         '603be3911673ca4c1d302c049ed0dd60')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  ln -s mariadb-${pkgver}-win32 i686-w64-mingw32
  ln -s mariadb-${pkgver}-winx64 x86_64-w64-mingw32
  for _arch in ${_architectures}; do
    cd "${srcdir}/${_arch}/lib"
    gendef libmysql.dll
    ${_arch}-dlltool -k --input-def libmysql.def --dllname libmysql.dll --output-lib libmysql.dll.a
  done
}

package() {
  for _arch in ${_architectures} ; do
    cd "${srcdir}/${_arch}"
    mkdir -p "${pkgdir}/usr/${_arch}/"{bin,include,lib}
    cp -R include "${pkgdir}/usr/${_arch}"
    install -Dm644 lib/libmysql.dll "${pkgdir}/usr/${_arch}/bin/libmysql.dll"
    install -Dm644 lib/libmysql.dll.a "${pkgdir}/usr/${_arch}/lib/libmysql.dll.a"
    ${_arch}-ranlib "${pkgdir}/usr/${_arch}/lib/libmysql.dll.a"
    # calling strip on libmysql.dll does not work
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/lib/"*.dll.a
  done
}
