# Contributor: Thomas Bächler <thomas@archlinux.org>
# Maintainer: Clément DEMOULINS <clement@archivel.fr>

pkgname=openvpn-dev
_pkgname=openvpn
pkgver=2.3_alpha2
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network)"
arch=('i686' 'x86_64')
url="http://openvpn.net/"
license=('custom')
depends=('openssl' 'lzo2')
provides=('openvpn')
conflicts=('openvpn')
optdepends=('openresolv: scripts can use this tool to manage resolv.conf')

source=(http://swupdate.openvpn.net/community/releases/${_pkgname}-${pkgver}.tar.xz
        openvpn.rc openvpn.conf)
md5sums=('6d9a0f3fe2893ce0c13f47512072068b'
         'c9f7a8c0be2448877275d5115543afb3'
         'b0938f65016c904dab336f019f81e126')

backup=('etc/conf.d/openvpn')

build() {
  cd $srcdir/${_pkgname}-${pkgver}

  # Build and install openvpn
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\"" ./configure \
    --prefix=/usr \
    --enable-password-save \
    --enable-systemd \
    --mandir=/usr/share/man
  make
}

package() {
  cd $srcdir/${_pkgname}-${pkgver}

  # Install openvpn
  make DESTDIR=$pkgdir install
  install -d -m755 $pkgdir/etc/openvpn
  install -m 755 -D $srcdir/openvpn.rc $pkgdir/etc/rc.d/openvpn
  install -m 644 -D $srcdir/openvpn.conf $pkgdir/etc/conf.d/openvpn

  # Install examples (config-files and scripts)
  install -d -m755 $pkgdir/usr/share/openvpn/examples
  cp -r sample/sample-config-files $pkgdir/usr/share/openvpn/examples/config-files
  cp -r sample/sample-scripts $pkgdir/usr/share/openvpn/examples/scripts
  cp -r contrib $pkgdir/usr/share/openvpn/examples/scripts
  find $pkgdir/usr/share/openvpn -type f -exec chmod 644 {} \;
  find $pkgdir/usr/share/openvpn -type d -exec chmod 755 {} \;

  # Install license
  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING

  # Install easy-rsa
  #cd $srcdir/${_pkgname}-${pkgver}
  #make -C easy-rsa/2.0 install DESTDIR=$pkgdir PREFIX=usr/share/openvpn/easy-rsa
}
# vim:set ts=2 sw=2 et:
