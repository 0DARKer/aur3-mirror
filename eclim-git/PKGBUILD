# Maintainer: Alexandre Fonseca <alexandrejorgefonseca_at_gmail>
# Contributor: Dave Reisner <d@falconindy.com>
# Contributor: Andrea Fagiani <andfagiani_at_gmail_dot_com>

pkgname=eclim-git
pkgver=20130630
pkgrel=1
pkgdesc="Brings Eclipse functionality to Vim"
url="http://eclim.org/"
license=('GPL3')
arch=(i686 x86_64)
depends=('vim' 'eclipse>=4.2')
makedepends=('apache-ant' 'python2-sphinx')
optdepends=('eclipse-pdt: Eclipse PHP Development Tools support'
            'eclipse-cdt: Eclipse C/C++ Plugin support'
            'eclipse-dltk-core: Eclipse Dynamic Languagues Toolkit support'
            'eclipse-dltk-ruby: Eclipse Ruby support'
            'eclipse-wtp-wst: Eclipse Web Developer Tools support')
conflicts=('eclim' 'eclim-juno-git')
source=(resources.patch)
install=eclim.install
md5sums=('15d96cc259f9f91b68c25e1f950b9ca8')

_gitroot="git://github.com/ervandew/eclim.git"
_gitname="eclim"
_eclipseLocal="/home/$USER/.eclipse/`ls -v /home/$USER/.eclipse | tail -n 1`"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    ( cd $_gitname && git pull origin; )
    msg "The local files are updated."
  else
    git clone "$_gitroot"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -Np0 < "$srcdir"/resources.patch

  # Get the ANT_HOME environment variable
  source /etc/profile.d/apache-ant.sh

  export ECLIM_ECLIPSE_HOME="/usr/share/eclipse"

  msg2 "Building..."
  ant -Declipse.local=$_eclipseLocal \
      -Dvim.files=/usr/share/vim/vimfiles \
      build

}

package() {
  cd "$srcdir/$_gitname-build"

  msg2 "Deploying..."
  ant -Declipse.local=$_eclipseLocal \
      -Declipse.dest=$pkgdir/usr/share/eclipse \
      -Dvim.files=$pkgdir/usr/share/vim/vimfiles \
      deploy vimdocs

  # fix eclim paths
  find "$pkgdir" -type f -print0 | xargs -0 sed -i -e "s|$pkgdir||g"
  find "$pkgdir" -type f -print0 | xargs -0 sed -i -e "s|$srcdir||g"

  # delete Windows stuff
  find "$pkgdir" -regex ".*bat\|.*cmd\|.*exe" -delete
}

