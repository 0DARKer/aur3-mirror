# Maintainer: mutantmonkey <mutantmonkey@mutantmonkey.in>
pkgname=calypso-git
pkgver=20120819
pkgrel=1
pkgdesc="Simple CalDAV/CardDAV server based on Radicale that uses Git"
arch=('any')
url="http://keithp.com/blogs/calypso/"
license=('GPL3')
depends=('python2' 'python2-vobject' 'git')
makedepends=('git')
provides=('calypso')
conflicts=('calypso')
backup=('etc/calypso/config')
source=('calypso.rc')
sha256sums=('fce3e4325cb4c1d7bbc67cc1891b1307392a367c4f7933832172a92efff78abe')

_gitroot=git://keithp.com/git/calypso
_gitname=calypso

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i 's/\/usr\/bin\/python/\/usr\/bin\/python2/g' calypso.py
}

package() {
  cd "$srcdir/$_gitname-build"
  python2 setup.py install --root="$pkgdir/"
  install -Dm644 "$srcdir/calypso/config" "$pkgdir/etc/calypso/config"
  install -Dm744 "$srcdir/calypso.rc" "$pkgdir/etc/rc.d/calypso"
}

# vim:set ts=2 sw=2 et:
