# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Maintainer: Glaucous <glakke1 at gmail dot com>

pkgname=lib32-apitrace-git
pkgver=20130320
pkgrel=1
pkgdesc="Graphics API Tracing from git (32-bit)"
arch=('x86_64')
url="https://github.com/apitrace/apitrace"
license=('BSD')
makedepends=('cmake' 'git' 'lib32-mesa-libgl' 'mesa-libgl' 'lib32-procps-ng' 'gcc-multilib')
provides=('lib32-apitrace')
conflicts=('lib32-apitrace')

_gitroot='git://github.com/apitrace/apitrace.git'
_gitname='apitrace'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin && cd ..
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf apitrace-build
  cp -r "${_gitname}" apitrace-build

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${srcdir}"/apitrace-build
  
  sed -i -e 's#lib/${CMAKE_PROJECT_NAME}#lib32/${CMAKE_PROJECT_NAME}#g' CMakeLists.txt
  sed -i -e 's#/lib/apitrace/wrappers#/lib32/apitrace/wrappers#g' cli/cli_resources.cpp
  
  cmake . -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE='/usr/bin/python2.7' -DENABLE_GUI="no"
  make -C build
}

package() {
  depends=('python2' 'lib32-libgl' 'lib32-procps-ng' 'apitrace-git')

  cd "${srcdir}/apitrace-build"
  
  make  -C build DESTDIR="${pkgdir}/" install

  mv -v ${pkgdir}"/usr/bin/apitrace" ${pkgdir}"/usr/bin/apitrace32"
  rm -r ${pkgdir}/usr/share/doc
  rm ${pkgdir}/usr/bin/*retrace
  
  install -m755 -d ${pkgdir}"/usr/share/licenses/apitrace"
  ln -s apitrace "$pkgdir/usr/share/licenses/apitrace/"${pkgname}
}

# vim:set ts=2 sw=2 et:
