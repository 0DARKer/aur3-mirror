# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Maintainer: Glaucous <glakke1 at gmail dot com>

pkgname=lib32-apitrace-git
pkgver=20130208
pkgrel=1
pkgdesc="Graphics API Tracing from git (32-bit)"
arch=('x86_64')
url="https://github.com/apitrace/apitrace"
license=('BSD')
depends=('python2' 'lib32-libgl')
makedepends=('cmake>=2.8' 'git' 'lib32-mesa' 'gcc-multilib')
optdepends=('qt: GUI support' 'qjson: GUI support')
provides=('apitrace')
conflicts=('apitrace' 'apitrace-git')
source=()
md5sums=()

_gitroot='git://github.com/apitrace/apitrace.git'
_gitname='apitrace'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'


  cd "${srcdir}"
  cmake -H"${_gitname}" -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE='/usr/bin/python2.7'
  make -C build
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install

  mkdir -p "${pkgdir}"/usr/share/licenses/apitrace
  msg "Linking license"
  cd "${pkgdir}"/usr/share/licenses/apitrace
  ln -s ../../doc/apitrace/LICENSE.txt LICENSE
}

# vim:set ts=2 sw=2 et:
