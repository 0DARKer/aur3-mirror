# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=lib32-vogl-git
pkgver=418.b3a52c2
pkgrel=1
pkgdesc="An OpenGL debugger"
arch=('x86_64')
url="https://github.com/ValveSoftware/vogl"
license=('MIT')
# if building the glxspheres64 demo, add following dependencies:
#     'glu' 'freeglut' 'libxxf86vm' 'sdl2'
depends=('lib32-qt4' 'lib32-libjpeg-turbo' 'lib32-tinyxml' 'lib32-libunwind' 'lib32-sdl2')
makedepends=('git' 'cmake' 'gcc-multilib')
optdepends=('steam: use steamlauncher32 to trace games from Steam')
options=('!buildflags' '!makeflags')
source=('git+https://github.com/ValveSoftware/vogl.git' \
        'https://bitbucket.org/raddebugger/vogl_chroot/raw/HEAD/bin/src/sl.cpp' \
        'steamlauncher_libraries.diff' 'vogleditor_paths.diff')
md5sums=('SKIP'
         'SKIP'
         '7991a02e0ea5903a86436e4a17453ea2'
         'a8cc334db199f2844f156264f01447d9')

pkgver() {
  cd "$srcdir/vogl"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/vogl"
  # disable building of glxspheres64 demo to keep the dependencies minimal
  sed -i 's|add_subdirectory.*glxspheres.*|#&|' CMakeLists.txt

  # allow the steam launcher to use the system wide vogl libraries
  cd "$srcdir"
  patch --follow-symlinks -Np0 < steamlauncher_libraries.diff || true

  # use our system-wide steamlauncherbinary
  cd "$srcdir/vogl"
  patch -Np1 < "$srcdir"/vogleditor_paths.diff

  # disable some optimizations for vogl_entrypoints.cpp to avoid memory exhaustion
  sed -i 's|-fno-var-tracking|"-fno-var-tracking -fvisibility=default"|' src/voglcommon/CMakeLists.txt
  # make sure the 32bit libraries are used
  sed -i 's|${QT_QTCORE_LIBRARY}|QtCore|' src/vogleditor/CMakeLists.txt
  sed -i 's|${QT_QTGUI_LIBRARY}|QtGui|' src/vogleditor/CMakeLists.txt
}

build() {
  mkdir -p "$srcdir/build"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export LDFLAGS="-m32"

  # build vogl
  cd "$srcdir/build"
  cmake ../vogl \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=True \
    -DCMAKE_PREFIX_PATH="/usr/lib32" \
    -DBUILD_X64=Off
  make

  # build steamlauncher
  g++ $CXXFLAGS -o steamlauncher ../sl.cpp
}

package() {
  cd "$srcdir/vogl"

  install -d -m755 "$pkgdir"/usr/{bin,lib32}

  install -m644 vogl_build/bin/libvogltrace32.so "$pkgdir"/usr/lib32/libvogltrace.so
  install -m755 vogl_build/bin/vogl* "$pkgdir"/usr/bin

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # install steamlauncher
  install -D -m755 "$srcdir"/build/steamlauncher "$pkgdir"/usr/bin/steamlauncher32
}
