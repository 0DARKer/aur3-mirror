# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Comete <la_comete@tiscali.fr>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python-vpython
pkgver=5.71
pkgrel=5
pkgdesc="A Python module that offers real-time 3D output"
arch=('i686' 'x86_64')
url="http://www.vpython.org"
depends=('gtkglextmm' 'libglademm' 'python2' 'python2-numpy' 'boost-libs' 'wxpython' 'tk' 'fonttools' 'python2-polygon' 'python2-ttfquery')
makedepends=('boost')
license=('custom')
options=(!libtool)
source=(http://www.vpython.org/contents/download/visual-${pkgver}_release.tar.bz2
        idle.patch boostmt.patch)
md5sums=('34f41db0755466c20aa142c62d0b545a'
         'ab545d81b5c2744fc7a86b65ef52556d'
         'bc61912ad69381db0852c29e7a0ff93f')

build() {
  cd "${srcdir}"/visual-${pkgver}_release
  patch -p1 < "${srcdir}"/idle.patch
  patch -p1 < "${srcdir}"/boostmt.patch
  rm ./src/gtk2/random_device.cpp
  sed -i "s/random_device.lo//g" ./src/Makefile.in
  sed -i "s/random_device.o//g" ./src/gtk2/makefile
  pythondir=`python2 -c "from distutils import sysconfig; print sysconfig.get_python_lib()"`
  export PYTHON=/usr/bin/python2
  unset PYTHONPATH # ignore developers PYTHONPATH
  ./configure --prefix=/usr \
              --with-example-dir=$pythondir/visual/demos
  make || return 1
}

package() {
  cd "${srcdir}"/visual-${pkgver}_release
  make DESTDIR="${pkgdir}" install
  install -D -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

