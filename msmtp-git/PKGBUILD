# Contributor: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Andres P <aepd87@gmail.com>

pkgname=msmtp-git
pkgver=rel.1.4.30.2.g716e47e
pkgrel=1
pkgdesc="A mini smtp client (git version)"
arch=(i686 x86_64)
url="http://msmtp.sourceforge.net"
license=('GPL3')
provides=('msmtp' 'smtp-forwarder')
conflicts=('msmtp')
depends=('gsasl' 'libidn')
makedepends=('git' 'libgnome-keyring')
optdepends=('libgnome-keyring: External Authentication')
install=msmtp.install
source=("git://msmtp.git.sourceforge.net/gitroot/msmtp/msmtp"
        "fix-itemx-references-without.patch")
sha256sums=('SKIP'
            '25e1c50e05d9e71cd6575770062efb971ea88689208d896de9e87d12f88245d2')
_gitname="msmtp"

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}


build() {
	cd "$srcdir/$_gitname"
	git apply ../fix-itemx-references-without.patch
	mkdir -p m4
	autoreconf -i
	./configure --prefix=/usr --sysconfdir=/etc \
		--with-libgsasl \
		--with-libidn \
		--with-gnome-keyring \
		--with-ssl=gnutls
	make
	make -C doc html
}

package() {
	cd "$srcdir/$_gitname"

	make DESTDIR="$pkgdir/" install
	make DESTDIR="$pkgdir/" -C doc install-html

	# Installing example configs and scripts to /usr/share/doc/msmtp
	# as they are not installed by default (Debian and Gentoo do it this way)
	install -d "${pkgdir}/usr/share/doc/msmtp"
	cp -r scripts/{find_alias,msmtp-gnome-tool,msmtpqueue,msmtpq,set_sendmail} "${pkgdir}/usr/share/doc/msmtp/"
	install -D -m644 doc/*.example "${pkgdir}/usr/share/doc/msmtp/"

	install -D -m644 scripts/vim/msmtp.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/msmtp.vim"
}
