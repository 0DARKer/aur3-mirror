# Maintainer: Andres P <aepd87@gmail.com>

pkgname=msmtp-git
pkgver=20100618
pkgrel=1
pkgdesc='An SMTP client'
arch=(i686 x86_64)
license=(GPL3)
url=http://msmtp.sourceforge.net
depends=(gnutls libidn)
makedepends=(git)
provides=(msmtp smtp-forwarder)
conflicts=(msmtp)
install=msmtp.install

_gitroot=git://msmtp.git.sourceforge.net/gitroot/msmtp/msmtp
_gitname=msmtp
#_gittag=rel-1-4-20

build() {
  msg 'Connecting to GIT server...'

  if [[ -d $_gitname ]]; then
    ( cd $_gitname; git pull origin )
    msg 'The local files are updated.'
  else
    git clone $_gitroot $_gitname
  fi

  rm -rf $_gitname-build
  git clone $_gitname{,-build}
  cd $_gitname-build

  #git checkout $_gittag

  autoreconf
  ./configure         \
    --prefix=/usr     \
    --sysconfdir=/etc \
    #
  
  make
}

package() {
  cd $_gitname-build
  
  make DESTDIR=$pkgdir install

  install -Dm644 scripts/vim/msmtp.vim \
    $pkgdir/usr/share/vim/vimfiles/syntax/msmtp.vim
  for _i in msmtp{Q,q}; do
    install -D scripts/msmtpq/$_i $pkgdir/usr/bin/$_i
  done

  install -Dm644 scripts/msmtpq/README \
    $pkgdir/usr/share/doc/msmtp/README.msmtpq
  for _i in msmtprc-{system,user}.example; do
    install -Dm644 doc/$_i $pkgdir/usr/share/doc/msmtp/$_i
  done
}

