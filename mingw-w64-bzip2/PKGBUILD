pkgname=mingw-w64-bzip2
pkgver=1.0.6
pkgrel=7
pkgdesc="A high-quality data compression program (mingw-w64)"
arch=(any)
url="http://sources.redhat.com/bzip2"
license=("custom")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt)
options=(!strip !buildflags staticlibs)
source=("http://www.bzip.org/${pkgver}/bzip2-${pkgver}.tar.gz"
"bzip2-1.0.5-autoconfiscated.patch"
"bzip2-use-cdecl-calling-convention.patch"
"mingw32-bzip2-1.0.5-slash.patch")
sha1sums=('3f89f861209ce81a6bab1fd1998c0ef311712002'
          '4d512b19b802d328037bf957c18866471c092e84'
          'f73d45bb02741cf0cfa995ac46547b34aedf5343'
          '809c3a95f6f6c26463d804b345d62eed8420c6a6')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	cd "$srcdir/bzip2-$pkgver"
	patch -p1 -i "$srcdir/"bzip2-1.0.5-autoconfiscated.patch
	patch bzlib.h < "$srcdir/"bzip2-use-cdecl-calling-convention.patch
	patch bzip2.c < "$srcdir/"mingw32-bzip2-1.0.5-slash.patch
	chmod +x autogen.sh
	./autogen.sh
}

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
			--prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm "$pkgdir/usr/${_arch}/bin/bz"{diff,grep,more}
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}