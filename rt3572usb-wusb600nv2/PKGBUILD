# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=rt3572usb-wusb600nv2
pkgver=2.5.0.0
pkgrel=14
__date=2011_0427
pkgdesc='Linksys WUSB600Nv2 wireless adapter driver'
arch=('i686' 'x86_64')
url='http://www.ralinktech.com/en/04_support/support.php?sn=501'
license=('GPL' 'GPL3' 'custom:ralink-firmware')
backup=('etc/Wireless/RT2870STA/RT2870STA.dat')
source=(http://mirror.thebasementserver.com/soft/ralink/linux/${__date}_RT3572_Linux_STA_v${pkgver}.DPO.bz2
        patch.config
        patch.wusb600nv2
        rt2870-firmware-update.pl
        rt3572build
        rt3572sta.conf)
md5sums=('6a857c9d74987c7f3fa61b15ade179cc'
         'a8c2c1049b32821235ff65941d5d627d'
         'ff9a0de8b1f44c3344e197fe1f3feb65'
         'ccff67ac30e1334a42a30721884662d5'
         '9c7daed83b1fbf9f3f7ab343a09b02b2'
         'dd59211aa37f17a15bd182b9dace69c4')

depends=('linux-firmware' 'perl-libwww' 'perl-archive-zip' 'usbutils')
makedepends=('linux-headers')
install=rt3572usb-wusb600nv2.install

build() {

    cd ${__date}_RT3572_Linux_STA_v${pkgver}.DPO
    patch -Np1 -i ../patch.config
    patch -Np1 -i ../patch.wusb600nv2
    sed -i -e 's#usb_buffer_alloc#usb_alloc_coherent#g' \
           -e 's#usb_buffer_free#usb_free_coherent#g' \
           include/os/rt_linux.h
    ##
    ## If you're building for kernel 3.2.4 uncomment the sed lines
    ## below and BEFORE running makepkg type as root:
    ##
    ##  ln -sf /usr/src/linux-$(uname -r) /usr/src/linux
    ##
    ## after successfully building the package you can
    ## safely remove '/usr/src/linux' symlink
    ##
    #sed -i 's#LINUX_SRC = /lib/modules/$(shell uname -r)/build#LINUX_SRC = /usr/src/linux#' \
    #    Makefile
    make
}

package() {
   ## rt3572build related
   install -D -m 744 rt3572build ${pkgdir}/usr/sbin/rt3572build
   install -D -m 644 ${__date}_RT3572_Linux_STA_v${pkgver}.DPO.bz2 ${pkgdir}/usr/share/rt3572usb/${__date}_RT3572_Linux_STA_v${pkgver}.DPO.bz2
   cp patch.{config,wusb600nv2} ${pkgdir}/usr/share/rt3572usb

   install -D -m 755 rt2870-firmware-update.pl ${pkgdir}/usr/bin/rt2870-firmware-update
   install -D -m 644 rt3572sta.conf ${pkgdir}/etc/modprobe.d/rt3572sta.conf
   cd ${__date}_RT3572_Linux_STA_v${pkgver}.DPO
   gzip os/linux/rt3572sta.ko 
   install -D -m 644 os/linux/rt3572sta.ko.gz ${pkgdir}/usr/share/rt3572usb/rt3572sta.ko.gz
   install -D -m 644 RT2870STA.dat ${pkgdir}/etc/Wireless/RT2870STA/RT2870STA.dat
   install -D -m 644 RT2870STACard.dat ${pkgdir}/etc/Wireless/RT2870STA/RT2870STACard.dat
   install -D -m 644 LICENSE\ ralink-firmware.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.ralink-firmware.txt
}

