# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>
# Based on the libcanberra package trunk

_pkgbase=libcanberra
pkgname=libcanberra-gtk3
true && pkgname=(libcanberra-gtk3 libcanberra-pulse-gtk3 libcanberra-gstreamer-gtk3)
pkgver=0.28
pkgrel=1
url=http://0pointer.de/lennart/projects/libcanberra
arch=(i686 x86_64)
license=('LGPL')
depends=('libvorbis>=1.3.1' 'libtool>=2.2.10' 'alsa-lib>=1.0.23' 'tdb>=1.2.1')
makedepends=('gtk-doc' 'libpulse' 'gstreamer0.10>=0.10.30' 'gtk2>=2.21.8' 'gtk3')
options=(!emptydirs)
source=(${url}/${_pkgbase}-${pkgver}.tar.gz
        libcanberra-gtk-module.sh libcanberra-quit-add.patch)
md5sums=('c198b4811598c4c161ff505e4531b02c'
         'a54799e624aac814b9343ab05f25c38b'
         'bc4c4392d0c71023252c96f80d1b0a38')

build() {
  cd ${_pkgbase}-${pkgver}
  patch -Np1 -i ../libcanberra-quit-add.patch
  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var --disable-static --with-builtin=dso
      --enable-null --disable-oss --enable-alsa --enable-gstreamer --enable-pulse
  make
}

package_libcanberra-pulse-gtk3() {
  pkgdesc="PulseAudio plugin for libcanberra"
  depends=("${_pkgbase}=${pkgver}-${pkgrel}" 'libpulse')
  provides=("${_pkgbase}-pulse=${pkgver}")
  conflicts=("${_pkgbase}-pulse")
  groups=('pulseaudio-gnome')

  cd ${_pkgbase}-${pkgver}

  mkdir -p "${pkgdir}/usr/lib/${_pkgbase}-${pkgver}"
  mv pulse-plugin/* "${pkgdir}/usr/lib/${_pkgbase}-${pkgver}"
}

package_libcanberra-gstreamer-gtk3() {
  pkgdesc="GStreamer plugin for libcanberra"
  depends=("${_pkgbase}=${pkgver}-${pkgrel}" 'gstreamer0.10>=0.10.30')
  provides=("${_pkgbase}-gstreamer=${pkgver}")
  conflicts=("${_pkgbase}-gstreamer")

  cd ${_pkgbase}-${pkgver}

  mkdir -p "${pkgdir}/usr/lib/${_pkgbase}-${pkgver}"
  mv gstreamer-plugin/* "${pkgdir}/usr/lib/${_pkgbase}-${pkgver}"
}

package_libcanberra-gtk3() {
  pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification"
  install=libcanberra-gtk3.install
  provides=("${_pkgbase}=${pkgver}-${pkgrel}")
  conflicts=("${_pkgbase}")
  optdepends=("${_pkgbase}-pulse-gtk3: PulseAudio driver"
              "${_pkgbase}-gstreamer-gtk3: GStreamer driver")

  cd ${_pkgbase}-${pkgver}
  make -j1 DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}"/usr/lib/libcanberra-gtk*.la
  rm -f "${pkgdir}"/usr/lib/gtk-{2,3}.0/modules/*.la

  install -m755 -d "${pkgdir}/usr/share/gconf"
  mv "${pkgdir}/etc/gconf/schemas" "${pkgdir}/usr/share/gconf/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "../libcanberra-gtk-module.sh" "${pkgdir}/etc/X11/xinit/xinitrc.d/40-libcanberra-gtk-module"

  # Split plugins
  for _p in pulse gstreamer; do
    mkdir $_p-plugin
    mv "${pkgdir}"/usr/lib/${_pkgbase}-${pkgver}/${_pkgbase}-$_p.* $_p-plugin
  done
}