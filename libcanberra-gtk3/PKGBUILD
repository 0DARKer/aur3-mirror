# Maintainer: Adam Hani Schakaki (krzd) <krzd@krzd.net>

_pkg=libcanberra
pkgname=libcanberra-gtk3
pkgver=0.26
pkgrel=1
pkgdesc='A small and lightweight implementation of the XDG Sound Theme Specification'
arch=('i686' 'x86_64')
url="http://0pointer.de/lennart/projects/${_pkg}"
license=('LGPL')
depends=('libvorbis>=1.3.1' 'gstreamer0.10>=0.10.30' 'libtool>=2.2.10' 'gtk2>=2.21.8' 'alsa-lib>=1.0.23' 'tdb>=1.2.1' 'gtk3')
makedepends=('gtk-doc')
provides=("${_pkg}=${pkgver}")
conflicts=("${_pkg}" "${_pkg}-pulse")
install=libcanberra.install
options=(!emptydirs)
source=("${url}/${_pkg}-${pkgver}.tar.gz"
        'libcanberra-gtk-module.sh')
md5sums=('ee2c66ada7c851a4e7b6eb1682285a24'
         'a54799e624aac814b9343ab05f25c38b')

build() {
  cd "${srcdir}/${_pkg}-${pkgver}"
  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
      --disable-static \
      --enable-alsa --enable-null --enable-gstreamer --disable-oss \
      --with-builtin=dso --enable-gtk3
  make
}
package() {
  cd "${srcdir}/${_pkg}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/lib/libcanberra-gtk.la"
  rm -f "${pkgdir}/usr/lib/gtk-2.0/modules/"*.la
  
  install -m755 -d "${pkgdir}/usr/share/gconf" 
  mv "${pkgdir}/etc/gconf/schemas" "${pkgdir}/usr/share/gconf/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/libcanberra-gtk-module.sh" "${pkgdir}/etc/X11/xinit/xinitrc.d/40-libcanberra-gtk-module"
}
