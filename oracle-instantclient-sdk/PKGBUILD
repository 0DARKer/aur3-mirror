# Contributor: Adam Nielsen <malvineous@shikadi.net>
# Contributor: Vitaliy Berdinskikh <skipper13@archlinux.org.ua>
# Contributor: Andrea Agosti <cifvts@gmail.com>
# Contributor: Viliam Pucik <viliam.pucik@gmail.com>
# Maintainer: Adam Nielsen <malvineous@shikadi.net>

_pkgname=instantclient-sdk
pkgname=oracle-${_pkgname}
pkgver=12.1.0.1.0
pkgrel=1
pkgdesc="Additional header files for developing Oracle applications with Instant Client"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technetwork/database/features/instant-client/"
license=('custom:OTN')
depends=(oracle-instantclient-basic=$pkgver)
replaces=('instantclient-sdk')
options=(!strip)

# These are the only files we want to include in the source package
source=(LICENSE)
md5sums=('2d62e22e4f2d7e6d220fafde0f692a7d')
sha256sums=('f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')

# Skip the next bit if we're building a source package for AUR, as we don't
# want these files included.
if [ $SOURCEONLY -ne 1 ]; then

	# Add the main file, depending on which architecture we're building for
	case "$CARCH" in
		i686)
			source[1]="${_pkgname}-linux-$pkgver.zip"
			md5sums[1]='e33beaaf88256e19f8c59e07d7033159'
			sha256sums[1]='0188e4fb3f0fa1f8ea73185ac8a56a79b0d0dc4127bbbd15090a2846b17421d6'
			;;
		x86_64)
			source[1]="${_pkgname}-linux.x64-$pkgver.zip"
			md5sums[1]='e8682f754ea63b9c5f17bd22ba158a75'
			sha256sums[1]='1713c3e8350cb580664a44388312fc473efafe9afffaa038ab835fe4fbb94f52'
			;;
	esac

	# Warn the user if they haven't downloaded the file already
	if [ ! -f "${source[1]}" ]; then
		error "You need to download ${source[1]}"
		echo
		msg2 "This software cannot be downloaded automatically.  You will need to sign up"
		msg2 "for an Oracle account and download the software from Oracle directly.  Place"
		msg2 "the downloaded file in the same directory as the PKGBUILD and re-run makepkg."
		msg2 ""
		msg2 "The source .zip files can be downloaded from:"
		msg2 ""
		msg2 "i686   - http://www.oracle.com/technetwork/topics/linuxsoft-082809.html"
		msg2 "x86_64 - http://www.oracle.com/technetwork/topics/linuxx86-64soft-092277.html"
		echo
		exit 1
	fi

fi

package() {
	# Put all .h files into /usr/include
	cd "$srcdir/instantclient_12_1/"
	install -d "$pkgdir/usr/include"
	install -m 644 -t "$pkgdir/usr/include" sdk/include/*.h
	# But we don't want this one, it is unused and it conflicts with another
	rm "$pkgdir/usr/include/ldap.h"
}
