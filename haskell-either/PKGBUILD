# Maintainer: Andr√© van Delden <andre.van.deldenX@Xuni-bremen.de>

_hkgname=either
pkgname=haskell-either
pkgver=4.3.0.1
pkgrel=1

pkgdesc="An either monad transformer."

url="http://hackage.haskell.org/package/${_hkgname}"
license=('BSD3')
arch=('i686' 'x86_64')
depends=('ghc'
         'haskell-exceptions>=0.5' 'haskell-exceptions<0.7'
         'haskell-free>=4.9' 'haskell-free<5'
         'haskell-monad-control>=0.3.2' 'haskell-monad-control<0.4'
         'haskell-monadrandom>=0.1'
         'haskell-mtl>=2.0' 'haskell-mtl<2.3'
         'haskell-semigroupoids>=4.0'
         'haskell-semigroups>=0.8.3.1' 'haskell-semigroups<1'
         'haskell-transformers>=0.2' 'haskell-transformers<0.5'
         'haskell-transformers-base>=0.4')
options=('strip')
#options=('strip' 'staticlibs')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install="${pkgname}.install"
sha512sums=('20bcf1cc532ab128e9b2018ff77b2231b156c5871eb4e19efc47da8eed829ee0aca348b7c61dacfa20d87d4537a1aa58e38eb61122a6a6ca0e6c960a01b66c26')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
