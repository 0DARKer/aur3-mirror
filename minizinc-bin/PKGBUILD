# $Id: PKGBUILD 318 2012-02-29 08:11:53Z shtrom $
# Contributor: Olivier Mehani <shtrom-aur@ssji.net>
_appname=minizinc
pkgname=${_appname}-bin
pkgver=1.6
pkgrel=1
pkgdesc="MiniZinc is a modelling language for constraint programming problems (upstream binaries)"
arch=(i686 x86_64)
url="http://www.minizinc.org/downloads/"
license=('custom:BSD-Nicta')
conflicts=(minizinc)
provides=(minizinc)
optdepends=('vim: a versatile editor (for which Zinc support files are provided)')
_targetspec=i686-pc-linux-gnu
md5sums=('56a77b9e72621df13290d4f0e1fa35e0')
if [ $CARCH = x86_64 ]; then
  _targetspec=x86_64-unknown-linux-gnu
md5sums=('284bd4152c75e7068645904a3ec91980')
fi
source=(http://www.minizinc.org/downloads/release-${pkgver}/${_appname}-${pkgver}-${_targetspec}.tar.gz)

package() {
  mkdir -p ${pkgdir}/usr/{bin,lib,share/{vim/vimfiles/syntax/,doc/,{,licenses/}${_appname}/}}

  find ${srcdir}/${_appname}-${pkgver}/bin -type f -exec install -D -m 0755 {} ${pkgdir}/usr/bin/ \;
  cp -R ${srcdir}/${_appname}-${pkgver}/lib/minizinc/ ${pkgdir}/usr/lib/
  cp -R ${srcdir}/${_appname}-${pkgver}/{benchmarks,examples,fcts,scripts,tests,xml} \
    ${pkgdir}/usr/share/${_appname} 
  cp -R ${srcdir}/${_appname}-${pkgver}/doc ${pkgdir}/usr/share/doc/${_appname}
  mv ${pkgdir}/usr/bin/mzn2fzn ${pkgdir}/usr/bin/mzn2fzn-actual
  mv ${pkgdir}/usr/share/doc/${_appname}/man/ ${pkgdir}/usr/

  cat > ${pkgdir}/usr/bin/mzn2fzn << 'EOF'
#!/bin/sh

export MZN_STDLIB_DIR="/usr/lib/minizinc"
exec "/usr/bin/mzn2fzn-actual" "$@"
EOF

  chmod a+x ${pkgdir}/usr/bin/mzn2fzn
  chmod -R a+r ${pkgdir}
  find ${pkgdir} -type d -exec chmod a+x {} \;

  install -D -m 0644 ${srcdir}/${_appname}-${pkgver}/tools/vim/zinc.vim \
    ${pkgdir}/usr/share/vim/vimfiles/syntax/
  install -D -m 0644 ${srcdir}/${_appname}-${pkgver}/COPYING \
    ${pkgdir}/usr/share/licenses/${_appname}/COPYING
}

# vim:set ts=2 sw=2 et:
