# Maintainer: Stefan J. Betz <info at stefan-betz dot net>
# Contributor: p2k <Patrick dot Schneider at uni-ulm dot de>
# Contributor: Jonathan Liu <net147@gmail.com>
pkgname=rabbitmq
pkgver=2.7.0
pkgrel=1
pkgdesc="Highly reliable and performant enterprise messaging implementation of AMQP written in Erlang/OTP"
arch=('i686' 'x86_64')
url="http://rabbitmq.com"
license=('MPL')
depends=('erlang')
backup=('etc/rabbitmq/rabbitmq.conf' 'etc/rabbitmq/rabbitmq-env.conf')
source=("http://www.rabbitmq.com/releases/${pkgname}-server/v${pkgver}/${pkgname}-server-generic-unix-${pkgver}.tar.gz"
        "rabbitmq-env.conf"
        "rabbitmq-server"
        "rabbitmqctl"
        "rabbitmq-server-rc.d")
install="${pkgname}.install"
md5sums=('68d01e0f811c013823f423819a2cabfb'
         'b2fde4dc69c3a75f99a21463b5c9f832'
         '481bd6ce748a168a75ce1b65373aaad1'
         '481bd6ce748a168a75ce1b65373aaad1'
         '062af9637fc3f81ffeb9e428daa8c536')

package() {
  cd "${srcdir}"
  install -d "${pkgdir}/usr/lib/rabbitmq"/{bin,lib}
  install -d "${pkgdir}/var/log/rabbitmq"
  install -d "${pkgdir}/var/lib/rabbitmq/mnesia"
  cp -R "${pkgname}_server-${pkgver}" "${pkgdir}/usr/lib/rabbitmq/lib/"

  for x in rabbitmqctl rabbitmq-env rabbitmq-server; do
    ln -sf "../lib/${pkgname}_server-${pkgver}/sbin/$x" "${pkgdir}/usr/lib/rabbitmq/bin/"
  done

  install -D rabbitmq-env.conf "${pkgdir}/etc/rabbitmq/rabbitmq-env.conf"
  sed -i "s#^\(RABBITMQ_PLUGINS_DIR\)=.*\$#\1=/usr/lib/${pkgname}/lib/${pkgname}_server-${pkgver}/plugins#" \
     "${pkgdir}/etc/rabbitmq/rabbitmq-env.conf"
  install -D rabbitmq-server "${pkgdir}/usr/sbin/rabbitmq-server"
  install -D rabbitmqctl "${pkgdir}/usr/sbin/rabbitmqctl"
  install -D rabbitmq-server-rc.d "${pkgdir}/etc/rc.d/rabbitmq-server"
}

# vim:set ts=2 sw=2 et:
