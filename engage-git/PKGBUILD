# Contributor: fancris3 <fancris3 at aol.com>
# Contributor: fbianconi <fbianconi at gmail.com>

# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=engage-git
_gitname=engage
pkgver=3b3bc48
pkgrel=1
pkgdesc="Enlightenment e17 module: A taskbar and app-launcher dock, which makes use of composite."
arch=('i686' 'x86_64')
url="http://git.enlightenment.org/enlightenment/modules/engage.git"
license=('MIT')
depends=('enlightenment17')
makedepends=('git')
provides=('engage')
options=('!libtool')
conflicts=('e-modules-extra' 'engage-svn')
source=("$_gitname::git://git.enlightenment.org/enlightenment/modules/engage.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$_gitname"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$_gitname"
  make DESTDIR="$pkgdir/" install

  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  install -D -m644 COPYING-PLAIN $pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN
}
