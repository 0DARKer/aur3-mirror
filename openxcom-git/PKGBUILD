# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Stefan Karner <stefan.karner at student.tuwien.ac.at>

pkgbase=openxcom-git
pkgname=('openxcom-git' 'openxcom-docs-git')
_gitname=OpenXcom
pkgver=1.0_893_g8f9f926
pkgrel=1
arch=('i686' 'x86_64')
url="http://openxcom.org/"
license=('GPL3')
makedepends=('git' 'xmlto' 'docbook-xml' 'docbook-xsl' 'doxygen' 'sdl_mixer' 'sdl_gfx'
             'sdl_image' 'sdl' 'yaml-cpp51' 'libgl' 'gcc-libs' 'glibc' 'boost' 'glu')
source=(git+"https://github.com/SupSuper/${_gitname}.git"
        "openxcom.sh")
sha256sums=('SKIP'
            '1596f65076ce80ca42c24f8c301f029ba933f204af476cddc42ff1412d973d9b')

pkgver() {
  cd $_gitname
  git describe --tags | sed -e 's:v::' -e 's:-:_:g'
}

prepare() {
  cd $_gitname
  ./autogen.sh
}

build() {
  cd $_gitname
  ./configure --prefix=/usr --bindir=/usr/share/openxcom/data YAML_LIBS="-L/opt/yaml-cpp51/lib -lyaml-cpp" YAML_CFLAGS="-I/opt/yaml-cpp51/include"
  make
}

package_openxcom-git() {
  pkgdesc="An open-source reimplementation of the famous X-COM game (git-version)"
  depends=('sdl_mixer' 'sdl_gfx' 'sdl_image' 'sdl' 'yaml-cpp51' 'libgl' 'gcc-libs' 'glibc')
  optdepends=('openxcom-data-steam: pacman-tracked X-COM data files from Steam')
  provides=('openxcom')
  conflicts=('openxcom')
  install="${pkgname}.install"

  cd $_gitname

  # disable subdirs -- we install docs separately
  sed -i 's:^SUBDIRS = docs$::' Makefile

  make DESTDIR="$pkgdir" install
  
  # Install wrapper script
  install -Dm755 "$srcdir/openxcom.sh" "$pkgdir/usr/bin/openxcom"
}

package_openxcom-docs-git() {
  pkgdesc="Documentation for the open-source reimplementation of the famous X-COM game (git-version)"
  arch=('any')
  cd $_gitname
  make -C docs DESTDIR="$pkgdir" install
}
