# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Andrea 'dedalus' Turconi <andy.dedalus@gmail.com>
# Contributor: 'lazork'

# NOTE: build will fail with suitesparse installed

pkgname=freefem++
pkgver=3.23
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut')
makedepends=('gcc-fortran' 'wget')
options=(!makeflags)
source=("http://www.freefem.org/ff++/ftp/${pkgname}-${pkgver}.tar.gz"
        "disable-doc.patch")
sha1sums=('9001f6013cd14e7b0353fb5a1bc7b38ee5389fab'
          'df4df1a42143047f216d10a4e61fd9f78d79a903')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"

	sed -i "1i#include <unistd.h>" src/Graphics/getprog-unix.hpp
	patch -Np1 -i ../disable-doc.patch
}

build() {
	cd "$srcdir/$pkgname-$pkgver"

	./configure \
		--prefix=/usr \
		--enable-download \
		--disable-pdf

	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
