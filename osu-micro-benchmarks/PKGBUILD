# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=osu-micro-benchmarks
pkgver=4.0
pkgrel=1
pkgdesc="A suite of micro-benchmarks for testing various MPI operations (with optional CUDA support)."
arch=('i686' 'x86_64')
url="http://mvapich.cse.ohio-state.edu/benchmarks/"
license=('BSD')
depends=('openmpi')
optdepends=('cuda-toolkit: optional makedepends that enables CUDA support in the benchmarks')
changelog=ChangeLog
source=("http://mvapich.cse.ohio-state.edu/benchmarks/$pkgname-$pkgver.tar.gz")
sha512sums=('81ccc1cf0b027772f09b0dcdecf132cdfd3036b91b739812cbe7a507a943fea18a7b41b8522b88eaac94c8c35d5d9f4b27afb841567819544bb2497f0c0d78bf')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr

	#
	# For CUDA support, use the following switches during the 'configure' process.
	# Read the documentation for more information regarding CUDA support.
	#
	#./configure --prefix=/usr --enable-cuda --with-cuda-include=/path/to/cuda/include --with-cuda-lib=/path/to/cuda/lib

	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
