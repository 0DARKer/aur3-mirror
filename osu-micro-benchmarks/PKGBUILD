# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=osu-micro-benchmarks
pkgver=4.2
pkgrel=1
pkgdesc="A suite of micro-benchmarks for testing various MVAPICH2 MPI operations (with compile-time optional CUDA support)."
arch=('i686' 'x86_64')
url='http://mvapich.cse.ohio-state.edu/benchmarks/'
license=('BSD')
depends=('mvapich2')
# Uncomment the next line if you need CUDA support
#depends=('mvapich2' 'cuda' 'nvidia-utils')
optdepends=('cuda: optional make dependency that enables CUDA support'
            'nvidia-utils: optional make dependency that enables CUDA support')
changelog=ChangeLog
source=("http://mvapich.cse.ohio-state.edu/benchmarks/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8825a6a33269b85c86a3d5a8bc193b65d907041336532f5d0ab3ba6f90aea38c9a4b34b857646f2cde1e76e57a001bc2ee1dce9e506791dfdc2b8e783504c30c')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	if [ ${CARCH} == 'x86_64' ]; then
		_libdir='lib64'
	else
		_libdir='lib'
	fi

	#####################################################
	#
	# Without CUDA support
	#
	# Use the following configure command.
	#
	./configure --prefix=/usr --libexecdir=/usr/lib \
		CC=/opt/mvapich2/bin/mpicc
	#
	#
	#####################################################


	#####################################################
	#
	# With CUDA support
	#
	# Use the following configure command, and make sure that
	# the packages "cuda" and "nvidia-utils" are installed
	# BEFORE compiling.
	#
	# Read the documentation for more information:
	# http://mvapich.cse.ohio-state.edu/svn/mpi-benchmarks/branches/4.2/README
	#
	#./configure --prefix=/usr --libexecdir=/usr/lib \
	#	--enable-cuda \
	#	--with-cuda-include=/opt/cuda/include \
	#	--with-cuda-libpath=/opt/cuda/${_libdir} \
	#	LD_LIBRARY_PATH="${LD_LIBRARY_PATH}:/opt/cuda/${_libdir}" \
	#	CC=/opt/mvapich2/bin/mpicc
	#
	#####################################################

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}/" install
}
