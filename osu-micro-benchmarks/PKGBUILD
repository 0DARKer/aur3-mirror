# Maintainer: Jason St. John <jstjohn .. purdue . edu>
pkgname=osu-micro-benchmarks
pkgver=3.5.1
pkgrel=1
pkgdesc="A suite of micro-benchmarks for testing various MPI operations (with optional CUDA support)."
arch=('i686' 'x86_64')
url="http://mvapich.cse.ohio-state.edu/benchmarks/"
license=('BSD')
depends=('openmpi')
optdepends=('cuda-toolkit: optional makedepends that enables CUDA support in the benchmarks')
changelog=ChangeLog.osu-micro-benchmarks
source=("http://mvapich.cse.ohio-state.edu/benchmarks/$pkgname-$pkgver.tar.gz")
sha512sums=('1ea04b887ac77a9fe8e4fb2014afa7d8534cda793bc6035bf137ccc60f3b8341dd37287a039be0589818f6e8e46988298ae7311e149b64334f4e3538e2f4fdfe')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr

  #
  # For CUDA support, use the following switches during the 'configure' process.
  # Read the documentation for more information regarding CUDA support.
  #
  #./configure --prefix=/usr --enable-cuda --with-cuda-include=/path/to/cuda/include --with-cuda-lib=/path/to/cuda/lib

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
