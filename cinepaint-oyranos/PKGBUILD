# Maintainer: tobias [tobias.archlinux.org]
# Contributor: tobias [tobias.justdreams.de] Milan Knizek [knizek.volny.cz]

pkgname=cinepaint-oyranos
_pkgname=cinepaint
pkgver=0.25.1
_pkgver=0.25.0
pkgrel=2
pkgdesc="Sophisticated graphics manipulation programm supporting > 8bit pictures"
arch=('i686' 'x86_64')
license=('LGPL' 'GPL' 'MIT')
url="http://www.cinepaint.org"
depends=('gtk2' 'openexr' 'lcms' 'libxpm' 'fltk' 'ftgl' 'libxxf86vm' 'oyranos')
makedepends=('python2' 'gutenprint>=5.2.9')
optdepends=('python2: for python plug-ins' 
            'gutenprint: for print plug-ins'
            'ghostscript: for pdf plug-ins'
           )
conflicts=(cinepaint)
options=('!libtool')
install=cinepaint.install
source=(https://api.opensuse.org/public/source/multimedia:color_management/cinepaint/${_pkgname}-${_pkgver}.tar.gz
        LICENSE
        cinepaint-libpng15.patch
        autogen.sh
        cinepaint_desktop.diff
        cinepaint-0.25-add_libraw_plugin.patch
        cinepaint-0.25-autotools_update.patch
        cinepaint-0.25-dcraw-8.98.patch
        cinepaint-0.25-fix_default_profile_preselect.patch
        cinepaint-0.25-fix_non_compiz_BadAccess2.patch
        cinepaint-0.25-gcc4_4.patch
        cinepaint-0.25-gtk_x11_link.patch
        cinepaint-0.25-check_profile_name.patch
        cinepaint-0.25-image_x_dcraw.patch
        cinepaint-0.25-libm.patch
        cinepaint-0.25-link_order.patch
        cinepaint-0.25-main_monitor_net_color.patch
        cinepaint-0.25-net_color_rename.patch
        cinepaint-0.25-net_color_rename2.patch
        cinepaint-0.25-oyranos_api16.patch
        cinepaint-0.25-oyranos_cms.patch
        cinepaint-0.25-oyranos_header.patch
        cinepaint-0.25-oyranos_include.patch
        cinepaint-0.25-pnm_buffer.patch
        cinepaint-0.25-rawphoto_empty_space_filename.patch
        cinepaint-0.25-resize_n_fullscreen.patch
        cinepaint-0.25-string_format.patch
        cinepaint-0.25-tiff_cmyk_without_ICC.patch
        cinepaint-0.25-use_first_screen.patch
        cinepaint-0.25-version1.patch
        )

build() {
 cd "${srcdir}/${_pkgname}-${_pkgver}"
 chmod +w Makefile.am

# Patches by Kai-Uwe Behrmann (www.oyranos.org/git/cinepaint.git)
 patch -p1 -i ../cinepaint-0.25-oyranos_header.patch
 patch -p0 -i ../cinepaint_desktop.diff
 patch -p1 -i ../cinepaint-0.25-pnm_buffer.patch
 patch -p1 -i ../cinepaint-0.25-gcc4_4.patch
 patch -p1 -i ../cinepaint-0.25-oyranos_api16.patch
 patch -p0 -i ../cinepaint-0.25-libm.patch
 patch -p0 -i ../cinepaint-0.25-oyranos_cms.patch
 patch -p0 -i ../cinepaint-0.25-link_order.patch
 patch -p0 -i ../cinepaint-0.25-tiff_cmyk_without_ICC.patch
 patch -p0 -i ../cinepaint-0.25-version1.patch
 patch -p0 -i ../cinepaint-0.25-dcraw-8.98.patch
 patch -p0 -i ../cinepaint-0.25-oyranos_include.patch
 patch -p1 -i ../cinepaint-0.25-autotools_update.patch
 patch -p1 -i ../cinepaint-0.25-gtk_x11_link.patch
 patch -p1 -i ../cinepaint-0.25-string_format.patch
 patch -p1 -i ../cinepaint-0.25-rawphoto_empty_space_filename.patch
 patch -p1 -i ../cinepaint-0.25-main_monitor_net_color.patch
 patch -p1 -i ../cinepaint-0.25-check_profile_name.patch
 patch -p1 -i ../cinepaint-0.25-image_x_dcraw.patch
 patch -p1 -i ../cinepaint-0.25-use_first_screen.patch
 patch -p1 -i ../cinepaint-0.25-fix_non_compiz_BadAccess2.patch
 patch -p1 -i ../cinepaint-0.25-resize_n_fullscreen.patch
 patch -p1 -i ../cinepaint-0.25-net_color_rename.patch
 patch -p1 -i ../cinepaint-0.25-add_libraw_plugin.patch
 patch -p1 -i ../cinepaint-0.25-fix_default_profile_preselect.patch
 patch -p1 -i ../cinepaint-0.25-net_color_rename2.patch

# Patches from Arch package repository
 patch -p1 -i ../cinepaint-libpng15.patch
 find plug-ins/pygimp -type f -exec sed -i 's#env python#env python2#' {} +

# Czech translation fails due too long string
 sed -i 's/BarvovýProstor/BarvovýProst./g' po/cs.po

# Better to regenerate the stuff
 aclocal
 aclocal # When not run twice, compilation fails occassionally
 cp ../autogen.sh autogen.sh
 sh autogen.sh
 autoconf

# Use --enable-debug if CinéPaint crashes when a) opening preferences dialog and
#                                              b) converting to another ICC profile
# With debugging symbols, it does not crash :-)
# Better solution is to identify, which ICC profile causes the crash and remove it

 LIBS+="-lstdc++ -lm -lX11" ./configure --prefix=/usr --libdir=/usr/lib --mandir=/usr/share/man \
  --enable-gtk2 --enable-python --enable-pygimp --with-python=/usr/bin/python2

# Remove double comma ",,", otherwise linking fails
 sed -i 's/-Wl,,/-Wl,/' lib/fl_i18n/Makefile plug-ins/bracketing_to_hdr/Makefile plug-ins/collect/Makefile plug-ins/pdf/Makefile
 make
}

package() {
 cd "${srcdir}/${_pkgname}-${_pkgver}"
 make DESTDIR="${pkgdir}" install
 sed -i -e "s|-I$srcdir/${_pkgname}-${_pkgver}||" -e "s|-I$srcdir/${_pkgname}-${_pkgver}/lib||" -e "/libcinepaint.la/d" \
  -e "s|$srcdir/${_pkgname}-${_pkgver}/lib/.libs/\$dlname||" "${pkgdir}/usr/bin/cinepainttool"
 sed -i "s/cinepaint.png/cinepaint/" "${pkgdir}/usr/share/applications/cinepaint.desktop"
 install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}

md5sums=('ce60a3006949496d20ca89e8c728cff4'
         '169085743f667c250ea2e4022efecf5f'
         '682de65ad358512d00a8b76730453664'
         '7eefcec93caa99a923621cf7c67b7732'
         'a366d9e1cbefaef6e7b025e9bcf02bf2'
         '7c35f265947419ec95065ab519d0ab2a'
         '24672c619e996d6bf8ab524ea7f94dce'
         '57b1747f57c8e2c5bd7da51f5afe4fc5'
         'ac64cfc1b95da2a848fd89294212d296'
         '0aa29fb5964f82510373c9a002752392'
         'be95dc5e7e291b5f6fa84c08fd5375c2'
         '96b33d1959eaab4869dfff6ed357e1ef'
         '6c142331699e9bc26642ab039f9d8233'
         'a56c0e37188daa7080f59ad78641c6cd'
         'abbbaa034d2b973c4c93a24354413bf0'
         'd5a68dea54c901e52d5032c654c11c8a'
         'd092247a0580c406315eee66dbb028b3'
         'e3e5e36b0b44c207b7940db4856a050e'
         'bfb7e3841456c1339eeeca241d839692'
         '35ddf7ff80e8ae9d770f15b2e02b1065'
         'b87116e2dac5b1b0da408635370786a4'
         'd3aa6dd79c3001df319471055df45234'
         '197d212667b91241f158add5c760c387'
         'c5b43655a36c6b349d5450561afd676a'
         'a719d8df2e6a9977db1877d90394b4c0'
         'd192e0ca6c11b4972716aac9f39302a4'
         '371a6af8b5322c48e6bc33baa0c65c2c'
         '570494cfec7c71a38d3d021b1bbdd3a2'
         'e37d0d298f089e8c847b2f80a56ea821'
         '89d33ea6948112ccfe9d25184d6db347')
