# Maintainer: Ruthger Gubbels <ruthger@ruthger.nl>

pkgname='communigatepro'
pkgver='6.1c2'
pkgrel='1'
pkgdesc='Unified Communications platform'
arch=('x86_64')
url='http://www.stalker.com/'
license=('custom')
makedepends=('rpmextract')
optdepends=('communigatepro-av: Spam and AV scanner for Communigate Pro')
provides=('smtp-server' 'imap-server' 'pop3-server')
conflicts=('smtp-server' 'imap-server' 'pop3-server')
options=(!strip)
install='install'
source=(
  'ftp://ftp.stalker.com/pub/CommuniGatePro/6.1/CGatePro-Linux-6.1c-2.x86_64.rpm'
  'cgatepro.service'
  'LICENCE'
)
sha512sums=('9bb9c3b7d12c563e636a7ee67bfbfb8939046e04cb1444dbe26345f3a665a0be222817da436bb32d2408377c4032d17a9445703a5ef2ed81caf8f8de6df86fec'
            '37525c11c8cc57b4bc0e95a2cbc641d7ab4a9fd6718090f3b2459c6707101f9ae2848def9fa48b318634365e58b2872abaf85ebfe93f87f91458cc7989eb2d5d'
            'a220724c78e715c577dca1001437942e29d32c4f6e1d3e4d7e99b5ced198dac45966084be3f23b2f64522b6d005d7faa50c14e76cf71297500618ec9ed3cd066')

build() {
  rpmextract.sh *.rpm
  rm opt/CommuniGate/Startup -f
}

package() {
  # Licence
  install -Dm644 LICENCE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

  # Systemd service
  install -Dm644 cgatepro.service "$pkgdir"/usr/lib/systemd/system/cgatepro.service

  # Program directory and files
  install -dm755 "$pkgdir"/opt/CommuniGate
  cp "$srcdir"/opt/CommuniGate/* "$pkgdir"/opt/CommuniGate -r
  chmod 755 "$pkgdir"/opt/CommuniGate -R

  # Data directories
  install -dm755 "$pkgdir"/var/lib/CommuniGate
  install -dm755 "$pkgdir"/var/lib/CommuniGate/Settings
}

