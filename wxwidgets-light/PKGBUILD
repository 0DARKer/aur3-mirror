#Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase="wxwidgets-light"
pkgname=('wxbase-light' 'wxgtk-light')
pkgver=3.0.2
pkgrel=1
pkgdesc="wxWidgets suite for Base and GTK2 toolkits (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
makedepends=('git' 'glu')
_pkgver=$(echo ${pkgver} |tr . _)
source=("wxwidgets::git+https://github.com/wxWidgets/wxWidgets.git#tag=WX_${_pkgver}")
md5sums=('SKIP')
_gitname=wxwidgets

prepare() {
  rm -fr "${_gitname}"-{base,gtk}
  cp -R "${_gitname}" "${_gitname}-base"
  cp -R "${_gitname}" "${_gitname}-gtk"
}


build() {
  pushd "${_gitname}-base" &> /dev/null
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-regex=builtin \
              --enable-unicode \
              --disable-{precomp-headers,visibility,gui}
  make
  make -C locale allmo
  popd &> /dev/null
  pushd "${_gitname}-gtk" &> /dev/null
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,regex=builtin,opengl,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode} \
              --disable-{mediactrl,precomp-headers,webview,visibility}
  make
  popd &> /dev/null
}

package_wxbase-light() {
  pkgdesc="wxWidgets Base (GNOME/GStreamer free!)"
  depends=('bash' 'expat' 'zlib')
  provides=("wxbase=${pkgver}")
  conflicts=('wxbase')
  options=('!emptydirs')

  make -C "${_gitname}-base" DESTDIR="${pkgdir}" install
  install -D -m644 "${_gitname}-base/docs/licence.txt" "${pkgdir}/usr/share/licenses/wxbase-light/LICENSE"
}

package_wxgtk-light() {
  pkgdesc="wxWidgets GTK2 Toolkit (GNOME/GStreamer free!)"
  depends=('wxbase-light' 'gtk2' 'libxxf86vm' 'libsm' 'sdl' 'libgl')
  provides=("wxgtk=${pkgver}")
  conflicts=('wxgtk')
  options=('!emptydirs')

  make -C "${_gitname}-gtk" DESTDIR="${pkgdir}" install
  install -D -m644 "${_gitname}-gtk/docs/licence.txt" "${pkgdir}/usr/share/licenses/wxgtk-light/LICENSE"

  rm -fr "${pkgdir}/usr/bin/wx-config"
  rm -fr "${pkgdir}/usr/lib/"*baseu*
  rm -fr "${pkgdir}/usr/share/"{aclocal,bakefile}
  for i in $(diff -srq "${pkgdir}/usr/include/wx-3.0/wx/" "${pkgdir}/../wxbase-light/usr/include/wx-3.0/wx/" | grep identical | awk '{print $2}'); do rm -fr ${i}; done
}

