# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase="wxwidgets-light"
pkgname=('wxbase-light' 'wxgtk-light' 'wxcommon-light')
pkgver=3.0.2
_pkgver=$(echo ${pkgver} |tr . _)
pkgrel=2
pkgdesc="wxWidgets suite for Base and GTK2 toolkits (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
makedepends=('git' 'glu')
source=("wxwidgets::git+https://github.com/wxWidgets/wxWidgets.git#tag=WX_${_pkgver}"
        'config.conf'
        'wx-config.sh')
sha1sums=('SKIP'
          '55436b5c57eae798f716e3e486c6834d6274cec5'
          '47df8d82e6c9671212428ea769ff4a3e3f725917')
_gitname=wxwidgets

prepare() {
  mkdir -p build-{base,gtk}
}

build() {
  pushd build-base &> /dev/null
  ../wxwidgets/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --with-regex=builtin \
      --enable-unicode \
      --disable-{precomp-headers,gui}
  make
  make -C ../wxwidgets/locale allmo
  popd &> /dev/null
  pushd build-gtk &> /dev/null
  ../wxwidgets/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,regex=builtin,opengl,sdl} \
      --without-{libnotify,gnomevfs,gnomeprint} \
      --enable-{graphics_ctx,unicode} \
      --disable-{mediactrl,precomp-headers,webview}
  make
  make -C ../wxwidgets/locale allmo
  popd &> /dev/null
}

package_wxbase-light() {
  pkgdesc="wxWidgets Base (GNOME/GStreamer free!)"
  depends=('wxcommon-light' 'expat' 'zlib')
  provides=("wxbase=${pkgver}")
  conflicts=('wxbase')
  options=('!emptydirs')

  make -C build-base DESTDIR="${pkgdir}" install

  rm -fr "${pkgdir}/usr/bin/wx-config"
  rm -fr "${pkgdir}/usr/include"
  rm -fr "${pkgdir}/usr/share/"

  install -Dm644 wxwidgets/docs/licence.txt "${pkgdir}/usr/share/licenses/wxbase-light/LICENSE"
}

package_wxgtk-light() {
  pkgdesc="wxWidgets GTK2 Toolkit (GNOME/GStreamer free!)"
  depends=('wxcommon-light' 'wxbase-light' 'gtk2' 'libxxf86vm' 'libsm' 'sdl' 'libgl')
  provides=("wxgtk=${pkgver}")
  conflicts=('wxgtk')
  options=('!emptydirs')

  make -C build-gtk DESTDIR="${pkgdir}" install

  rm -fr "${pkgdir}/usr/bin/wx-config"
  rm -fr "${pkgdir}/usr/include"
  rm -fr "${pkgdir}/usr/lib/"*baseu*
  rm -fr "${pkgdir}/usr/share"

  install -Dm644 wxwidgets/docs/licence.txt "${pkgdir}/usr/share/licenses/wxgtk-light/LICENSE"
}

package_wxcommon-light() {
  pkgdesc="wxWidgets common (GNOME/GStreamer free!)"
  depends=('bash')
  provides=("wxcommon=${pkgver}")
  conflicts=('wxcommon')
  options=('!emptydirs')
  backup=('etc/wx/config')

  make -C build-base DESTDIR="${pkgdir}" install
  make -C build-gtk DESTDIR="${pkgdir}" install

  rm -fr "${pkgdir}/usr/bin"
  rm -fr "${pkgdir}/usr/lib"

  install -Dm644 wxwidgets/docs/licence.txt "${pkgdir}/usr/share/licenses/wxcommon-light/LICENSE"

  install -Dm644 config.conf "${pkgdir}/etc/wx/config"
  install -Dm755 wx-config.sh "${pkgdir}/usr/bin/wx-config"
}