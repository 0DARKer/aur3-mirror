# Maintainer: Jason Lenz <Jason@Lenzplace.org>
_pkgname=sleepyhead
pkgname=$_pkgname-git
pkgver=0.9.8.0.gd24fd55
pkgrel=1
pkgdesc="Open-source, cross platform, sleep tracking software with a focus on monitoring CPAP treatment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/sleepyhead"
license=('GPL')
depends=(
  'qt5-base'
  'qt5-serialport'
  'qt5-webkit'
)
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(
  "$_pkgname::git+git://git.code.sf.net/p/sleepyhead/code"
  'moduleupdates.patch'
)
sha256sums=(
  'SKIP'
  27fe965d3555a494fe0e69ec6d6368643e5c719ad4e2e537aed3238d0eb174fa
)

pkgver() {
  cd "$_pkgname"
  _major=$(sed -rn 's/.*major_version = ([0-9]+).*/\1/p' < sleepyhead/version.h)
  _minor=$(sed -rn 's/.*minor_version = ([0-9]+).*/\1/p' < sleepyhead/version.h)
  _rev=$(sed -rn 's/.*revision_number = ([0-9]+).*/\1/p' < sleepyhead/version.h)
  _build=$(cat sleepyhead/scripts/build_number)
  _gitversion=$(git rev-parse --short HEAD)
  echo $_major.$_minor.$_rev.$_build.g$_gitversion
}

build() {
  cd "$_pkgname"
  patch -Np1 -i "$srcdir/moduleupdates.patch"
  ./configure
  make
}

package() {
 install -D "$srcdir/$_pkgname/sleepyhead/SleepyHead" "$pkgdir/usr/bin/SleepyHead"
}

# vim:set ts=2 sw=2 et:
