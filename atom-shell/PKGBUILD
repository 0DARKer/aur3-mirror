# Maintainer: /dev/rs0 <rs0@secretco.de.com>
# Contributor: Daniel Perez <tuvistavie@gmail.com>

pkgname=atom-shell
pkgver=0.21.3
pkgrel=1
pkgdesc="Framework for writing cross-platform desktop applications using JavaScript, HTML and CSS."

arch=('i686' 'x86_64')
license=('MIT')
url="https://github.com/atom/atom-shell"

optdepends=('nodejs')
depends=('libgcrypt15' 'libnotify' 'gconf' 'alsa-lib' 'nss' 'libxtst' 'gtk2' 'libgnome-keyring')
[[ $CARCH == 'x86_64' ]] && _arch='x64' || _arch='ia32'
[[ $CARCH == 'x86_64' ]] && \
    sha256sums=('2dd810b7462f914466fa83d30c6e07285cd6732e90c29799aaaee67f6be76758') || \
    sha256sums=('e7c4c6f288a5162855908b3402d8d87ea9d9026c952fddaed2beba73cf286b60')
source=("atom-shell.zip::https://github.com/atom/atom-shell/releases/download/v${pkgver}/atom-shell-v${pkgver}-linux-${_arch}.zip")

package() {
  rm "${srcdir}/atom-shell.zip"

  install -d "${pkgdir}/usr/share/atom-shell"
  cp -a "${srcdir}/." "${pkgdir}/usr/share/atom-shell"

  install -d "${pkgdir}/usr/bin"
  ln -s "/usr/share/atom-shell/atom" "${pkgdir}/usr/bin/atom-shell"

  install -Dm644 "${pkgdir}/usr/share/atom-shell/LICENSE" "${pkgdir}/usr/share/licenses/atom-shell/LICENSE"
  rm "${pkgdir}/usr/share/atom-shell/LICENSE"
  ln -sf "/usr/lib/libgcrypt.so.11" "${pkgdir}/usr/share/atom-shell/libgcrypt.so.11"
  ln -sf "/usr/lib/libnotify.so.4" "${pkgdir}/usr/share/atom-shell/libnotify.so.4"
  ln -sf "/usr/lib/libudev.so" "${pkgdir}/usr/share/atom-shell/libudev.so.0"

  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
  chmod 755 "${pkgdir}/usr/share/atom-shell/atom"
}
