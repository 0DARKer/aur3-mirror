# Maintainer: Michael RÃ¼egg <rueegg.michael@gmail.com>

is_64bit_arch() { [ $CARCH = "x86_64" ]; }

is_64bit_arch && _arch=-x86_64
_eclipse_name=kepler
_eclipse_release=R
_eclipse_timestamp=20130605-2000
pkgname=eclipse-rcp
pkgver=4.3.0
pkgrel=1
pkgdesc="Eclipse for RCP and RAP Developers"
arch=('i686' 'x86_64')
url="http://www.eclipse.org"
license=('EPL')
depends=('java-environment>=6' 'gtk2' 'xulrunner' 'webkitgtk2')
provides=('eclipse')
install=${pkgname}.install
source=(eclipse.sh eclipse.desktop eclipse.svg "http://www.eclipse.org/downloads/download.php?file=/technology/epp/downloads/release/\
    ${_eclipse_name}/${_eclipse_release}/${pkgname}-${_eclipse_name}-${_eclipse_release}-linux-gtk${_arch}.tar.gz&r=1")
changelog=${pkgname}.Changelog
md5sums=('00598e0866353f7a7b1a5ed65dc01610'
         '4c37bbe1cc5f8eb23dad2021b4d4b7e8'
         '5e9975a49de88815a731cbd4c77a136e'
         'fcf69d3448763c7b913a1c4e1b9d279d')
is_64bit_arch && md5sums[3]='f33f8ed107437b041741b036d0313ba2'

package() {
	local _icon_path=/usr/share/eclipse/plugins/org.eclipse.platform_${pkgver}.v${_eclipse_timestamp}
	install -m755 -d $pkgdir/usr/{bin,share/applications}
	install -m755 -d $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48,256x256,scalable}/apps
	cd $srcdir
	mv eclipse $pkgdir/usr/share
	install -D -m 755 eclipse.sh $pkgdir/usr/bin/eclipse
	install -D -m 644 eclipse.desktop $pkgdir/usr/share/applications
	install -D -m 644 eclipse.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/eclipse.svg
	ln -s ${_icon_path}/eclipse16.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png
	ln -s ${_icon_path}/eclipse32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png
	ln -s ${_icon_path}/eclipse48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png
	ln -s ${_icon_path}/eclipse256.png ${pkgdir}/usr/share/icons/hicolor/256x256/apps/eclipse.png
}

