# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ethereum-serpent
pkgver=1.5.6
pkgrel=1
pkgdesc="Serpent compiler"
arch=('i686' 'x86_64')
depends=('python2')
makedepends=('gcc' 'make' 'python2-setuptools')
url="https://www.ethereum.org"
license=('WTFPL')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/e/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz)
md5sums=('0df1ec2920172c92f4400d301d4f444a')
sha256sums=('e110d41163eb78e706fd44edd0c4b1d729da526cc188c20406b8616496c9ebdd')
provides=('ethereum-serpent' 'serpent')
conflicts=('serpent')

prepare() {
  cd "$srcdir/${pkgname#python2-}-$pkgver"

  msg 'Fixing Makefile...'
  sed -i "s@/usr/local@/usr@g" Makefile

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Installing...'
  python2 setup.py install --root="$pkgdir" --optimize=1
}
