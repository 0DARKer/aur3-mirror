# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ethereum-serpent
pkgver=1.4.13
pkgrel=1
pkgdesc="Serpent contract programming language tools"
arch=('i686' 'x86_64')
depends=('boost' 'boost-libs' 'crypto++' 'gmp' 'leveldb' 'python2' 'python2-bitcoin' 'python2-miniupnpc' 'python2-pysha3' 'readline')
makedepends=('cmake' 'gcc' 'glibc' 'python2-setuptools')
url="https://www.ethereum.org"
license=('WTFPL')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/e/${pkgname#python2-}/${pkgname#python2-}-$pkgver.tar.gz
        setup.py.patch)
md5sums=('f63621f6499080a445f6f0bb1bf34425'
         '9c5b300ce6edec2a416c68a6184a2346')
sha256sums=('ce0457b7b61dfd00a5fbede891fc525e2d074c0b40a7d76f7cfa400251195b91'
            '98d4d93e07791113f91faa56bcb7d5c12c85fd401b40af9af88b932a54b9e883')
provides=('ethereum-serpent' 'llc' 'sc')
conflicts=('ethereum' 'llc' 'sc')

prepare() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Fixing CMakeLists.txt...'
  sed -i "s@jsonrpc/rpc.h@rpc/rpc.h@" CMakeLists.txt

  msg 'Fixing setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch
  sed -i "s@/usr/lib/python%d.%d@$pkgdir/usr/lib/python%d.%d@" setup.py
  sed -i "s@-DCMAKE_INSTALL_PREFIX=/usr@-DCMAKE_INSTALL_PREFIX=$pkgdir/usr@" setup.py

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Building...'
  python2 setup.py build
}

package() {
  cd $srcdir/${pkgname#python2-}-$pkgver

  msg 'Installing...'
  install -dm 755 "$pkgdir/usr/lib/python2.7/lib-dynload"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
