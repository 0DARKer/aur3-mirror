# Maintainer: Ivy Foster <joyfulgirl@archlinux.us>
pkgname=emacs-bbdb-git  
pkgver=20111024
pkgrel=1
pkgdesc="The Insidious Big Brother Database (from git)"
url="http://savannah.nongnu.org/projects/bbdb/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('emacs')
optdepends=('vm: The vm mailer for emacs')
provides=('bbdb=3.0')
conflicts=('bbdb')
install=${pkgname}.install
changelog=ChangeLog
source=()
md5sums=()
options=(!zipman)

_gitroot="git://git.savannah.nongnu.org/bbdb.git"
_gitname="bbdb"

build () {
  cd $srcdir
  msg "Connecting to the git server..."

  if [[ -d $srcdir/$_gitname ]]; then
    cd $_gitname
    git pull origin master
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "git checkout done"
  msg "Starting make..."

  rm -rf $srcdir/$_gitname-build
  git clone $srcdir/$_gitname $srcdir/$_gitname-build

  cd $srcdir/$_gitname-build/lisp
  
  # Optionally remove vm from the list of dependencies.
  # If the user has vm installed from AUR, find it in /usr/share/emacs...
  if [[ -n "$(pacman -Q vm 2>/dev/null)" ]]; then
    sed -i 's:^\(VMDIR = \)~:\1/usr/share:' Makefile
    make bbdb autoloadsc vm
  else
    sed -i 's:^\(VMDIR =\).*$::' Makefile
    make bbdb autoloadsc
  fi
}

package () {
  cd $srcdir/$_gitname-build/lisp
  sed -i -e 's:/\(lisp/bbdb\):site-\1:g' \
         -e 's:^\(INSTALL = \)/usr:\1:' Makefile
  make PACKAGEDIR=${pkgdir}/usr/share/emacs/ install-pkg

  # Some extra documentation
  cd $srcdir/$_gitname-build
  install -d $pkgdir/usr/share/doc/$pkgname
  install -m644 -t $pkgdir/usr/share/doc/$pkgname \
    ChangeLog README TODO
}
