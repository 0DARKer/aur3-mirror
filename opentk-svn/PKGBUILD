pkgname=opentk-svn
pkgver=3123
pkgrel=1
pkgdesc="An advanced, cross-platform, C-sharp OpenGL, OpenAL and OpenCL wrapper for Mono/.Net."
arch=(any)
license=('MIT')
depends=(mono)
makedepends=(mono subversion)
options=(!strip)
url='http://www.opentk.com/'
conflicts=(opentk)
provides=(opentk)
_svntrunk='https://opentk.svn.sourceforge.net/svnroot/opentk/trunk'
_svnmod='opentk'

build() {
  cd "${srcdir}"

  if [ -d "${_svnmod}/.svn" ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver $_svnmod
  fi

  msg 'SVN checkout done or server timeout'

  rm -rf "${_svnmod}-build"
  cp -r "$_svnmod" "${_svnmod}-build"
  cd "${_svnmod}-build"

  xbuild OpenTK.sln /p:Configuration=Release
}

package() {
  cd "${srcdir}/${_svnmod}-build/Binaries/OpenTK/Release/"
  
  gacutil -i OpenTK.Compatibility.dll -root "$pkgdir/usr/lib"
  gacutil -i OpenTK.dll -root "$pkgdir/usr/lib"
  gacutil -i OpenTK.GLControl.dll -root "$pkgdir/usr/lib"
}
