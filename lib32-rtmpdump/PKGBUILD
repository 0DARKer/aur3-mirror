# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=rtmpdump
pkgname=lib32-${_pkgbase}
pkgver=2.4
pkgrel=1
pkgdesc="A tool to download rtmp streams (32 bit)"
arch=('x86_64')
url="http://rtmpdump.mplayerhq.hu/"
license=('GPL2' 'LGPL2.1')
depends=('lib32-openssl' "${_pkgbase}")
makedepends=('gcc-multilib')
#source=(http://rtmpdump.mplayerhq.hu/download/${_pkgbase}-${pkgver}.tgz)
source=(ftp://ftp.archlinux.org/other/${_pkgbase}/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('2c3b76d46d56180176ef099b8ad50a1608bef1dac35d8cb0b21ceac31b5c0656')

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  
  # prefix = /usr/local -> /usr
  # CC     = gcc -> gcc -m32
  sed -e '/^prefix\=/s/\/local//' \
  	  -e '/^CC\=/s/gcc/gcc -m32/' \
  	  -i Makefile librtmp/Makefile
  	  
  make -j1
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  install -d -m755 "${pkgdir}/usr/lib32"
  make prefix=/usr mandir=/usr/share/man libdir=/usr/lib32 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,sbin,share}
}
