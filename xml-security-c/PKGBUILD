# Maintainer: kevku <kevku@gmx.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>

pkgname='xml-security-c'
pkgver='1.7.3'
pkgrel='1'
pkgdesc='Apache C++ XML security libraries'
arch=('i686' 'x86_64')
url='http://santuario.apache.org/'
license=('GPL')
depends=('xalan-c>=1.11_pre1153059' 'nss')
source=('1.7.0-forgot.ye.bracket.patch'
        '1.6.1-nss-compilation-fix.patch'
        "http://www.apache.org/dist/santuario/c-library/$pkgname-$pkgver.tar.gz"
        "http://www.apache.org/dist/santuario/c-library/$pkgname-$pkgver.tar.gz.asc")
sha256sums=('85a1b0172cc8753254fe717e0e55fd57604de3978da65eba7843e76b54f633e4'
            '5873240498e46d2372737829321b7b1b4615abbceeb756264ac9e63c2a556563'
            'e5226e7319d44f6fd9147a13fb853f5c711b9e75bf60ec273a0ef8a190592583'
            'SKIP')
validpgpkeys=('DCAA15007BED9DE690CD9523378B845402277962')

prepare(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np2 -i $srcdir/1.7.0-forgot.ye.bracket.patch
  patch -Np1 -i $srcdir/1.6.1-nss-compilation-fix.patch
  mkdir -p xsec/yes/lib
}


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --with-openssl \
    --with-xerces \
    --with-xalan \
    --with-nss \
    --disable-static
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}
