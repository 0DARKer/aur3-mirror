# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>
# Contributor: Kirill "reflexing" Churin <reflexing@reflexing.ru>

pkgname=keepass
pkgver=2.15
pkgrel=3
pkgdesc="KeePass Password Safe, the free, open source, light-weight and easy-to-use password manager."
arch=('any')
url="http://keepass.info/"
license=('GPL2')
depends=('mono>=2.6' 'desktop-file-utils' 'xdg-utils')
optdepends=('xdotool: if you want to use auto-type.')
install=keepass.install
source=(http://downloads.sourceforge.net/project/keepass/KeePass%202.x/${pkgver}/KeePass-${pkgver}-Source.zip
        01_change_signing_key.patch
        02_avoid_broken_res.patch
        04_no_sgen.patch
        06_add-translation-search-paths.patch  
        07_fix-quicksearch-losing-results-on-defocus.patch
        08_work-around-quicksearch-crash.patch
        09_do-not-strip-leading-slash-from-command-line.patch
        13-remove-ToolsVersion-3.5.patch
        keepass
        keepass.1
        keepass.desktop
        keepass.xml
        KeePass_6_256x256x32.png
        KeePass_7_48x48x32.png
        KeePass_8_32x32x32.png
        KeePass_9_16x16x32.png)

md5sums=('4cbf5a7fe26579d3886b2ebb9852dfd7'
         'ace68c644bebc5105f6c41b921ea9779'
         '337a8e9cb44cd74e470acd7d96326fe8'
         '6aa0f7aabe54f208a6dbe560a767099d'
         '1379e1dbd2d803a3909ed3f3f909c03e'
         '46569df19ba8a40c85d8ec4e6838f0c9'
         'ec94548043d1332c08a967b39caadee1'
         'b9e26c0ef63f6c103f50cf2275d249e2'
         '9f54cf7b0442c79e31ab43e9720cac87'
         'daa5d6c01c11cf38c6f5cc207333aa9a'
         'a2a0dff1ebf0aaf6cbfb6f8566f4a010'
         '0ff6c4e6b12b77d1954292efcd2e6b97'
         '1d7545e79e7aa1107a9f78cb46ced251'
         '490859f348e492b5e33739262ff07f00'
         '5b060d5376e94f6743e787a46f53e9a3'
         '17044c8d69ad5275a2a57843b19ad89d'
         '4dfb6d0d6535c6d57ee88a4de7119095')

build() {
  cd "${srcdir}"
  
  patch -p1 -i 01_change_signing_key.patch
  patch -p1 -i 02_avoid_broken_res.patch
  patch -p1 -i 04_no_sgen.patch
  patch -p1 -i 06_add-translation-search-paths.patch  
  patch -p1 -i 07_fix-quicksearch-losing-results-on-defocus.patch
  patch -p1 -i 08_work-around-quicksearch-crash.patch
  patch -p1 -i 09_do-not-strip-leading-slash-from-command-line.patch
  patch -p1 -i 13-remove-ToolsVersion-3.5.patch
  
  sn -k mono.snk
  cp mono.snk KeePass/mono.snk
  cp mono.snk KeePassLib/mono.snk
  
  xbuild /target:KeePass /property:Configuration=Release
}

package() {
  cd ${srcdir}
  
  install -dm755 ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/usr/share/keepass/XSL
  
  install -Dm755 keepass ${pkgdir}/usr/bin/keepass
  install -Dm755 Build/KeePass/Release/KeePass.exe ${pkgdir}/usr/share/keepass/KeePass.exe
  install -Dm755 Ext/KeePass.config.xml ${pkgdir}/usr/share/keepass/KeePass.config.xml
  install -Dm755 Ext/KeePass.exe.config ${pkgdir}/usr/share/keepass/KeePass.exe.config

  install -Dm755 KeePass/Resources/Icons/LockOverlay.ico ${pkgdir}/usr/share/keepass/LockOverlay.ico
  install -Dm755 KeePass/Resources/Icons/QuadLocked.ico ${pkgdir}/usr/share/keepass/QuadLocked.ico
  install -Dm755 KeePass/Resources/Icons/QuadNormal.ico ${pkgdir}/usr/share/keepass/QuadNormal.ico
  install -Dm755 KeePass/Resources/Images/KeePass.ico ${pkgdir}/usr/share/keepass/KeePass.ico

  install -m644 Ext/XSL/* ${pkgdir}/usr/share/keepass/XSL
  
  install -Dm644 keepass.1 ${pkgdir}/usr/share/man/man1/keepass.1
  
  # Proper installation of .desktop file
  desktop-file-install -m 0644 --dir ${pkgdir}/usr/share/applications/ keepass.desktop
  
  # Install Icons (got from source package with "icotool -x KeePass.ico")
  for size in 16 32 48 256; do
    install -m644 -D \
    KeePass_*_${size}x${size}x32.png \
    ${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/keepass.png
  done
  
  # Needed for postinst with xdg-utils
  install -Dm644 keepass.xml ${pkgdir}/usr/share/mime/packages/keepass.xml
}
