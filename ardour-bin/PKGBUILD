# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=ardour
pkgname=$_name-bin
pkgver=3.0beta3_11482
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=(i686 x86_64)
url="http://$_name.org/"
license=('GPL')
depends=('jack' 'desktop-file-utils')
options=('!strip')
install="$pkgname.install"
if [ "$CARCH" = 'i686' ]; then
_arch=_32 && md5sums=('f3b1cfd1bb3f1d5d48e1e95768e9f13c'); else
_arch=_64 && md5sums=('14c899f77ddd30bc974a47b5be5f01e9'); fi
source=("http://$_name.org/files/${_name/a/A}${_arch}bit-$pkgver-dbg.tar"
        "$pkgname.desktop")
md5sums+=('cc5b92f67b90408c0d1a3df682f491e7')

build() {
  return 0
}

package() {
  cd "$srcdir/${_name/a/A}${_arch}bit-$pkgver-dbg"

  # unpack
  install -d "$pkgdir/opt"
  bsdtar --no-same-owner -jxvf \
    ${_name/a/A}_x86${_arch/_32}-$pkgver-dbg.tar.bz2 \
    -C "$pkgdir/opt"

  # install to /opt as does not conflict
  # with other ardour installations and libs
  cd "$pkgdir/opt"
  mv ${_name/a/A}_x86${_arch/_32}-$pkgver-dbg $_name

  # rm uninstaller and sanity check
  rm $_name/bin/{*.uninstall.sh,sanityCheck}

  # launcher
  install -d "$pkgdir/usr/bin"
  cat << EOF >> "$pkgdir/usr/bin/$pkgname"
#!/bin/bash
cd /opt/$_name/bin && ./${_name}3
EOF
  chmod +x "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
