# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Kurnevsky Evgeny <kurnevsky@gmail.com>

pkgname=scilab
pkgver=5.5.0
pkgrel=6
pkgdesc='A scientific software package for numerical computations.'
arch=('i686' 'x86_64')
url='http://www.scilab.org/'
license=('custom:CeCILL')
depends=('java-environment' 'shared-mime-info'  'desktop-file-utils' 'hicolor-icon-theme'
'hdf5' 'arpack' 'fftw' 'curl' 'tk' 'openmpi')
makedepends=('apache-ant'  'ocaml' 'gcc-fortran')
[[ $CARCH == 'x86_64' ]] && {
  source=("http://www.scilab.org/download/$pkgver/prerequirements-scilab-$pkgver-x86_64-src.tar.gz"
          "http://www.scilab.org/download/$pkgver/$pkgname-$pkgver-src.tar.gz")
  md5sums=('b2fda0ea03665695513bea9012f0ca90'
           'f35486e34735fce2e128e7f1f44839b5')
}
[[ $CARCH == 'i686' ]] && {
  source=("http://www.scilab.org/download/$pkgver/prerequirements-scilab-$pkgver-src.tar.gz"
          "http://www.scilab.org/download/$pkgver/$pkgname-$pkgver-src.tar.gz")
  md5sums=('0ae2b1eb08e510ce087527f767ab5494'
           'f35486e34735fce2e128e7f1f44839b5')
}
install=$pkgname.install

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  [[ $CARCH == 'x86_64' ]] && ./configure \
    --prefix=/usr \
    --enable-build-localization \
    --with-mpi \
    --without-matio \
    --without-umfpack\
    --with-fftw \
    --with-modelica \
    --disable-static-system-lib \
    --with-gfortran # Needed for 64 bits, don't hurt for 32 bits


  [[ $CARCH == 'i686' ]] && ./configure \
    --prefix=/usr \
    --enable-build-localization \
    --with-mpi \
    --without-matio \
    --without-umfpack \
    --with-fftw \
    --with-modelica \
    --disable-static-system-lib

  cp etc/classpath.xml.vc etc/classpath.xml

  make all doc
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install install-html

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING-FR ${pkgdir}/usr/share/licenses/${pkgname}/COPYING-FR
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING-BSD ${pkgdir}/usr/share/licenses/${pkgname}/COPYING-BSD

  # Install thirdparty jar files
  cd ${srcdir}/${pkgname}-${pkgver}/thirdparty/
  install -dm755 ${pkgdir}/usr/share/scilab/thirdparty
  for file in `ls *.jar *.README *.LICENSE`
  do
    install -m644 ${file} ${pkgdir}/usr/share/scilab/thirdparty/${file}
  done

  # Install thirdparty libraries
  cd ${srcdir}/${pkgname}-${pkgver}/lib/thirdparty
  thirdparty_libs='libgluegen2-rt.so libjogl_desktop.so libnativewindow_awt.so libnativewindow_x11.so'
  for file in ${thirdparty_libs}
  do
    install -m644 ${file} ${pkgdir}/usr/lib/scilab/${file}
  done
}
