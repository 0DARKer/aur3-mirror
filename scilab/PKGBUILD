# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>
# Paulo Matias

pkgname=scilab
pkgver=5.4.1
pkgrel=4
pkgdesc='A scientific software package for numerical computations.'
arch=('i686' 'x86_64')
url='http://www.scilab.org/'
license=('custom:CeCILL')
depends=('libxml2' 'pcre' 'lapack' 'fftw' 'bash' 'tk' 'hdf5' 'java-environment' 'arpack' 'shared-mime-info' 'umfpack' 'ocaml')
makedepends=('apache-ant' 'antlr2' 'awk')
[[ $CARCH == x86_64 ]] && \
  source=("http://www.scilab.org/download/$pkgver/prerequirements-scilab-$pkgver-x86_64-src.tar.gz" 
  "http://www.scilab.org/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz" 'scilab.desktop')

[[ $CARCH == i686 ]] && \
  source=("http://www.scilab.org/download/$pkgver/prerequirements-scilab-${pkgver}-src.tar.gz" \
"http://www.scilab.org/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz" \
  'scilab.desktop')
[[ $CARCH == x86_64 ]] && md5sums=('1a217691457cf5d65a1887333fd99b33'
  '5f8746053d645c0d7f220d42d3463f2d'
  'ad6286f324891fe8b86c60e4012a36b7')
[[ $CARCH == i686 ]] && md5sums=('1515c76852d71c7194ade1dc2a8bf8dc'
  '5f8746053d645c0d7f220d42d3463f2d'
  'ad6286f324891fe8b86c60e4012a36b7')
 
install=$pkgname.install
options=('!libtool' '!emptydirs')

build() {
  LDFLAGS=${LDFLAGS/ -Wl,--as-needed}
  cd $srcdir/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --enable-build-localization \
    --without-matio \
    --with-umfpack \
    --with-fftw \
    --with-modelica \
    --with-gfortran  # Needed for 64 bits, don't hurt for 32 bits
  cp etc/classpath.xml.vc etc/classpath.xml
  make all doc
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install-html

  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm644 ${srcdir}/scilab-${pkgver}/COPYING \
	${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm644 ${srcdir}/${pkgname}.desktop \
	${pkgdir}/usr/share/applications/${pkgname}.desktop
  sed -i 's#/jni##' ${pkgdir}/usr/share/scilab/etc/librarypath.xml
  cd $srcdir/${pkgname}-${pkgver}/thirdparty
  for _i in *.jar
  do
    install -Dm644 ${_i} $pkgdir/usr/share/scilab/thirdparty/${_i}
  done
}
