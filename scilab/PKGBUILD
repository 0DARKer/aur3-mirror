# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Kurnevsky Evgeny <kurnevsky@gmail.com>

pkgname=scilab
pkgver=5.5.1
pkgrel=3
pkgdesc='A scientific software package for numerical computations.'
arch=('i686' 'x86_64')
url='http://www.scilab.org/'
license=('BSD' 'custom:CeCILL')
depends=('java-environment' 
         'shared-mime-info'  'desktop-file-utils' 'gtk-update-icon-cache'
         'suitesparse'  'arpack' 'fftw' 'openmpi'
         'libmatio' 'hdf5'
         'tk' 'curl')
makedepends=('apache-ant'  'ocaml' 'gcc-fortran')
conflicts=('scilab-git' 'scilab-bin')

source=("${url}/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz")
md5sums=('ce3dcf7b95abd0fc57e51bf8c3ebd805')

if test "$CARCH" == x86_64; then
  source+=("${url}/download/${pkgver}/prerequirements-scilab-${pkgver}-x86_64-src.tar.gz")
  md5sums+=('192e6c8f971ea7f8c81bc9ad3a29be6c')
elif test "$CARCH" == i686; then
  source+=("${url}/download/${pkgver}/prerequirements-scilab-${pkgver}-src.tar.gz")
  md5sums+=('62e7c8b46a45deb2463391f8a43bb5f7')
fi

install=$pkgname.install

_thirdparty_libs="libgluegen2-rt.so libjogl_desktop.so \
                  libnativewindow_awt.so libnativewindow_x11.so"

prepare(){
  # Move some libraries to avoid conflicts with those on the system.
  cd ${srcdir}/${pkgname}-${pkgver}/lib

  rm -rf thirdparty_old

  mv thirdparty thirdparty_old
  mkdir -p thirdparty

  cd thirdparty_old
  cp -p ${_thirdparty_libs} ../thirdparty
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure \
    --prefix=/usr \
    --with-gcc \
    --with-gfortran \
    --with-mpi \
    --with-matio \
    --with-umfpack \
    --with-fftw \
    --with-modelica \
    --with-install-help-xml \
    --enable-build-help \
    --enable-build-localization \
    --disable-static-system-lib

  cp etc/classpath.xml.vc etc/classpath.xml

  make all doc
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install install-data install-html

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING-FR ${pkgdir}/usr/share/licenses/${pkgname}/COPYING-FR
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING-BSD ${pkgdir}/usr/share/licenses/${pkgname}/COPYING-BSD

  # Install thirdparty jar files
  cd ${srcdir}/${pkgname}-${pkgver}/thirdparty/
  install -dm755 ${pkgdir}/usr/share/scilab/thirdparty
  _jar_files=$(find . -maxdepth 1 -regex ".*\.jar" | xargs)
  for jar in ${_jar_files}; do
    install -m644 ${jar} ${pkgdir}/usr/share/scilab/thirdparty/${jar}
  done

  # Install thirdparty libraries
  cd ${srcdir}/${pkgname}-${pkgver}/lib/thirdparty
  install -dm755 ${pkgdir}/usr/lib/scilab
  for lib in ${_thirdparty_libs};  do
    install -m644 ${lib} ${pkgdir}/usr/lib/scilab/${lib}
  done
}
