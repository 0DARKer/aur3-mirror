# Maintainer: eolianoe <eolianoe [at] gmail [DoT] computations.>
# Contributor: Kurnevsky Evgeny <kurnevsky@gmail.com>

pkgname=scilab
pkgver=5.5.0
pkgrel=3
pkgdesc='A scientific software package for numerical computations.'
arch=('i686' 'x86_64')
url='http://www.scilab.org/'
license=('custom:CeCILL')
depends=('java-environment' 'shared-mime-info'  'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('apache-ant' 'suitesparse' 'ocaml' 'tcl' 'tk' 'gcc-fortran' 'hdf5' 'arpack' 'openmpi')
[[ $CARCH == 'x86_64' ]] && {
  source=("http://www.scilab.org/download/$pkgver/prerequirements-scilab-$pkgver-x86_64-src.tar.gz" 
          "http://www.scilab.org/download/$pkgver/$pkgname-$pkgver-src.tar.gz")
  md5sums=('b2fda0ea03665695513bea9012f0ca90'
           'f35486e34735fce2e128e7f1f44839b5')
}
[[ $CARCH == 'i686' ]] && {
  source=("http://www.scilab.org/download/$pkgver/prerequirements-scilab-$pkgver-src.tar.gz"
          "http://www.scilab.org/download/$pkgver/$pkgname-$pkgver-src.tar.gz")
  md5sums=('0ae2b1eb08e510ce087527f767ab5494'
           'f35486e34735fce2e128e7f1f44839b5')
}
install=$pkgname.install

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Temporary fix for umfpack (suitesparse). 
  sed -i "s/umf_divcomplex/SuiteSparse_divcomplex/" configure 
  sed -i "s/-lamd/-lamd -lsuitesparseconfig/" configure
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  [[ $CARCH == 'x86_64' ]] && ./configure \
    --prefix=/usr \
    --enable-build-localization \
    --without-matio \
    --with-umfpack \
    --with-fftw \
    --with-modelica \
    --disable-static-system-lib \
    --with-mpi \
    --with-gfortran # Needed for 64 bits, don't hurt for 32 bits

  [[ $CARCH == 'i686' ]] && ./configure \
    --prefix=/usr \
    --enable-build-localization \
    --without-matio \
    --with-umfpack \
    --with-fftw \
    --with-modelica \
    --with-mpi \
    --disable-static-system-lib

  cp etc/classpath.xml.vc etc/classpath.xml
  
  make all doc
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install install-html

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING-FR ${pkgdir}/usr/share/licenses/${pkgname}/COPYING-FR
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING-BSD ${pkgdir}/usr/share/licenses/${pkgname}/COPYING-BSD

  install -d ${pkgdir}/usr/share/scilab/thirdparty
  cp -dr ${srcdir}/${pkgname}-${pkgver}/thirdparty/* ${pkgdir}/usr/share/scilab/thirdparty
  install -d ${pkgdir}/usr/share/scilab/modules/tclsci
  cp -dr ${srcdir}/${pkgname}-${pkgver}/modules/tclsci/* ${pkgdir}/usr/share/scilab/modules/tclsci
  install -d ${pkgdir}/usr/share/scilab/lib
  cp -dr ${srcdir}/${pkgname}-${pkgver}/lib/* ${pkgdir}/usr/share/scilab/lib
}
