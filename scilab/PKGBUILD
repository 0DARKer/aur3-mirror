# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>
# Paulo Matias

pkgname=scilab
pkgver=5.4.1
pkgrel=7
pkgdesc='A scientific software package for numerical computations.'
arch=('i686' 'x86_64')
url='http://www.scilab.org/'
license=('custom:CeCILL')
depends=('pcre' 'fftw' 'tk' 'hdf5' 'java-environment' 'arpack' 'shared-mime-info'  'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('apache-ant' 'antlr2' 'awk' 'umfpack' 'ocaml')
[[ $CARCH == 'x86_64' ]] && \
  source=("http://www.scilab.org/download/$pkgver/prerequirements-scilab-$pkgver-x86_64-src.tar.gz" 
  "http://www.scilab.org/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz" 'scilab.desktop')

[[ $CARCH == 'i686' ]] && \
  source=("http://www.scilab.org/download/$pkgver/prerequirements-scilab-${pkgver}-src.tar.gz" \
"http://www.scilab.org/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz" \
  'scilab.desktop')
[[ $CARCH == 'x86_64' ]] && md5sums=('1a217691457cf5d65a1887333fd99b33'
  '5f8746053d645c0d7f220d42d3463f2d'
  'ad6286f324891fe8b86c60e4012a36b7')
[[ $CARCH == 'i686' ]] && md5sums=('1515c76852d71c7194ade1dc2a8bf8dc'
  '5f8746053d645c0d7f220d42d3463f2d'
  'ad6286f324891fe8b86c60e4012a36b7')
 
install=$pkgname.install
options=('!libtool' '!emptydirs' '!makeflags')

build() {
  LDFLAGS="-L$srcdir/bin -L$SCI_SRCDIR/thirdparty -L$SCI_SRCDIR/lib/thirdparty -L$SCI_SRCDIR/bin"

  cd $srcdir/$pkgname-$pkgver
  [[ $CARCH == 'x86_64' ]] && ./configure \
    --prefix=/usr \
    --enable-build-localization \
    --without-matio \
    --with-umfpack \
    --with-fftw \
    --with-modelica \
    --with-gfortran  # Needed for 64 bits, don't hurt for 32 bits

  [[ $CARCH == 'i686' ]] && ./configure \
    --prefix=/usr \
    --enable-build-localization \
    --without-matio \
    --with-umfpack \
    --with-fftw \
    --with-modelica 

  cp etc/classpath.xml.vc etc/classpath.xml
  make all doc
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install-html

  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm644 ${srcdir}/scilab-${pkgver}/COPYING \
	${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm644 ${srcdir}/${pkgname}.desktop \
	${pkgdir}/usr/share/applications/${pkgname}.desktop
  sed -i 's#/jni##' ${pkgdir}/usr/share/scilab/etc/librarypath.xml
  mkdir -p $pkgdir/usr/share/scilab/thirdparty
  cp -R $srcdir/${pkgname}-${pkgver}/thirdparty/* $pkgdir/usr/share/scilab/thirdparty
  install -Dm644 $srcdir/${pkgname}-${pkgver}/bin/libarpack.so $pkgdir/usr/share/scilab/bin/libarpack.so
  install -Dm644 $srcdir/${pkgname}-${pkgver}/bin/libgluegen2-rt.so $pkgdir/usr/share/scilab/bin/libgluegen2-rt.so
  install -Dm644 $srcdir/${pkgname}-${pkgver}/bin/libjogl_cg.so $pkgdir/usr/share/scilab/bin/libjogl_cg.so
  install -Dm644 $srcdir/${pkgname}-${pkgver}/bin/libjogl_desktop.so $pkgdir/usr/share/scilab/bin/libjogl_desktop.so
  install -Dm644 $srcdir/${pkgname}-${pkgver}/bin/libnativewindow_awt.so $pkgdir/usr/share/scilab/bin/libnativewindow_awt.so
  install -Dm644 $srcdir/${pkgname}-${pkgver}/bin/libnativewindow_x11.so $pkgdir/usr/share/scilab/bin/libnativewindow_x11.so
  install -Dm644 $srcdir/${pkgname}-${pkgver}/bin/libnewt.so $pkgdir/usr/share/scilab/bin/libnewt.so
}
