# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Kurnevsky Evgeny <kurnevsky@gmail.com>
# Contributor: Victor Dmitriyev <mrvvitek@gmail.com>

pkgname=scilab
pkgver=5.5.1
pkgrel=5
pkgdesc='A scientific software package for numerical computations.'
arch=('i686' 'x86_64')
url='http://www.scilab.org'
license=('BSD' 'custom:CeCILL')
depends=('shared-mime-info'  'desktop-file-utils' 'gtk-update-icon-cache'
         'suitesparse'  'arpack' 'fftw'
         'libmatio' 'tk' 'curl'
         'beanshell2' 'eclipse-ecj' 'java-flexdock'   'fop-hyph'
         'java-freehep-vectorgraphics' 'jeuclid-core' 'jgraphx' 'javahelp2'
         'jing' 'jlatexmath-fop' 'jrosetta' 'jgoodies-looks' 'java-qdox'
         'scirenderer' 'java-skinlf' 'java-testng' 'xalan-java' 'docbook-xsl')
makedepends=('apache-ant'  'ocaml' 'gcc-fortran')
conflicts=('scilab-git' 'scilab-bin')

source=("${url}/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
        "jogl_2.2.0.patch")
sha256sums=('c056223e12f17434a17ce881d13651bdb1b79a793bde028a978ea8dc20806729'
            '6ad6ce7cd64f34c032d4d0e399364497447e97e05933150bb1375147e770e9aa')

changelog=${pkgname}.changelog
install=${pkgname}.install

prepare(){
  cd "${srcdir}"
  # Upstream patch from this bugreport http://bugzilla.scilab.org/show_bug.cgi?id=13586
  patch -R "${pkgname}-${pkgver}/modules/scirenderer/src/org/scilab/forge/scirenderer/implementation/jogl/JoGLCanvas.java" < jogl_2.2.0.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --with-gcc \
    --with-gfortran \
    --with-mpi \
    --with-matio \
    --with-umfpack \
    --with-fftw \
    --with-modelica \
    --with-external-scirenderer=/usr/share/java/scirenderer/scirenderer.jar \
    --with-install-help-xml \
    --enable-build-help \
    --enable-build-localization \
    --disable-static-system-lib

  make all doc
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install install-data install-html

  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING-FR" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING-FR"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING-BSD" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING-BSD"
}
