# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=spideroak-beta
_realname=spideroak
pkgver=4.1.9848
pkgrel=1
pkgdesc="Secure file backup, sync and sharing client (beta channel)"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
conflicts=('${_realname}')
options=('!strip')
install=${pkgname}.install

_arch='x86_64'
[[ "${CARCH}" = 'i686' ]] && _arch='i386'
source=("${pkgname}_${pkgver}_${_arch}.deb"::"https://${_realname}.com/directdownload?platform=ubuntulucid&arch=${_arch}&beta=yes"
        "terms.txt")
md5sums=('8cb949000128fab7d5624a7930f44057'
         '9338d3464e8ce876d7222099c22a205b')
[[ "${CARCH}" = 'i686' ]] && md5sums[0]='958f177e2b971ea37c176d064acb8535'

package() {
  cd "${srcdir}"

  # unpack bundled files
  bsdtar xf data.tar.gz

  # install config files
  install -d -m755 "${pkgdir}/etc/"{dbus-1/system.d,sysctl.d}
  install -m644 "etc/dbus-1/system.d/${_realname}.dbus.conf" \
    "${pkgdir}/etc/dbus-1/system.d/${_realname}.dbus.conf"
  install -m644 "etc/sysctl.d/30-${_realname}.conf" \
    "${pkgdir}/etc/sysctl.d/30-${_realname}.conf"

  # clean up a bit
  rm -f usr/lib/SpiderOak/lib{gcc_s,stdc++,z}.so.*
  rm -f usr/lib/SpiderOak/*/*/*.exe

  # install app in /opt
  install -d -m755 "${pkgdir}/opt/SpiderOak"
  cp -r usr/lib/SpiderOak/* "${pkgdir}/opt/SpiderOak/"

  # change /usr to /opt in start script file
  sed -i 's:/usr/lib:/opt:g' usr/bin/SpiderOak

  # install start script file
  install -d -m755 "${pkgdir}/usr/bin"
  install -m755 usr/bin/SpiderOak "${pkgdir}/usr/bin/${_realname}"

  # fix desktop file according to the desktop entry spec
  sed -i \
    -e "s:Encoding=UTF-8::" \
    -e "s:SpiderOak;::" \
    -e "/^Name=/s:Backup:Beta:" \
    -e "/^Icon=/s:=.*$:=${_realname}:" \
    -e "/^Exec=/s:=.*$:=${_realname}:" \
    "usr/share/applications/${_realname}.desktop"

  # install desktop and pixmap files
  install -d -m755 "${pkgdir}/usr/share/"{applications,pixmaps}
  install -m644 "usr/share/applications/${_realname}.desktop" \
    "${pkgdir}/usr/share/applications/${_realname}.desktop"
  install -m644 "usr/share/pixmaps/${_realname}.png" \
    "${pkgdir}/usr/share/pixmaps/${_realname}.png"

  # install custom license file
  install -d -m755 "${pkgdir}/usr/share/licenses/${_realname}"
  install -m644 terms.txt "${pkgdir}/usr/share/licenses/${_realname}/terms.txt"
}

# vim:set ts=2 sw=2 et:
