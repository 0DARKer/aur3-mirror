# Maintainer: Tom Kwok <contact@tomkwok.com>
# Contributor: Martin F. Schumann <mfs@mfs.name>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Limao Luo <luolimao@gmail.com>
# Contributor: ava1ar (mail(dot)avatar(at)gmail(dot)com)

pkgname=spideroak-beta
_pkgname=spideroak
_PkgName=SpiderOak
pkgver=5.1.5
pkgrel=1
#_beta="yes" 
_beta="" # (no beta verison now) 
pkgdesc="Secure file backup, sync and sharing client (Beta version)"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
depends=('libpng12' 'curl' 'libdbusmenu-qt')
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
options=('!strip')
install=${pkgname}.install
source=(terms.txt)
sha1sums=('40d996ad01a0ca61a98264758ae8456f8f2e7f5e')

if [ "$CARCH" == x86_64 ]; then
        source+=("${_pkgname}_${pkgver}_x86_64.tgz"::"https://spideroak.com/getbuild?platform=slackware&arch=x86_64&beta=${_beta}")
        sha1sums+=('18ee94794832d388b9fcd4ad34d3d980f1def926')
elif [ "$CARCH" == i686 ]; then
        source+=("${_pkgname}_${pkgver}_i386.tgz"::"https://spideroak.com/getbuild?platform=slackware&arch=i386&beta=${_beta}")
        sha1sums+=(SKIP)
fi

package() {
    # install config files
    install -Dm644 ${srcdir}/etc/dbus-1/system.d/${_PkgName}.dbus.conf ${pkgdir}/etc/dbus-1/system.d/${pkgname}.dbus.conf

    # install app in /opt
    install -dm755 ${pkgdir}/opt/
    cp -r ${srcdir}/opt/* ${pkgdir}/opt/

    # install start script file
    install -Dm755 ${srcdir}/usr/bin/${_PkgName} ${pkgdir}/usr/bin/${_PkgName}
    cd $pkgdir/usr/bin
    ln -s $_PkgName $_pkgname
    cd $srcdir

    # install desktop and pixmap files
    install -Dm644 ${srcdir}/usr/share/applications/${_PkgName}.desktop ${pkgdir}/usr/share/applications/${_PkgName}.desktop
    install -Dm644 ${srcdir}/usr/share/pixmaps/${_PkgName}.png ${pkgdir}/usr/share/pixmaps/${_PkgName}.png

    # fix desktop file
    sed -i \
	-e "/Encoding=UTF-8/d" \
	-e "/^Name=/s: Backup::" \
	-e "/^Comment=/s:${_PkgName} ::" \
	-e "/^Categories=/s:${_PkgName};::" \
	-e "/^Icon=/s:=.*$:=${_PkgName}:" \
	-e "/^Exec=/s:=.*$:=${_PkgName}:" \
	${pkgdir}/usr/share/applications/${_PkgName}.desktop

    # install xdg menu
    install -Dm755 $srcdir/etc/xdg/menus/applications-merged/${_PkgName}.menu $pkgdir/etc/xdg/menus/applications-merged/${_PkgName}.menu

    # install man page
    install -Dm644 ${srcdir}/usr/share/man/man1/${_PkgName}.1.gz ${pkgdir}/usr/share/man/man1/${_PkgName}.1.gz

    # install custom license file
    install -Dm644 terms.txt ${pkgdir}/usr/share/licenses/${pkgname}/terms.txt
}
