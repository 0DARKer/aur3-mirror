# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=spideroak-beta
_pkgname=spideroak
pkgver=4.2.9912
pkgrel=1
pkgdesc="Secure file backup, sync and sharing client (beta channel)"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
provides=(${_pkgname})
conflicts=(${_pkgname})
options=('!strip')
install=${pkgname}.install

_arch='x86_64'
[[ "${CARCH}" = 'i686' ]] && _arch='i386'
source=("${pkgname}_${pkgver}_${_arch}.deb"::"https://${_pkgname}.com/directdownload?platform=ubuntulucid&arch=${_arch}&beta=yes"
        "terms.txt")
md5sums=('d04a68c1ec3bcc7a526aab58825786bb'
         '9338d3464e8ce876d7222099c22a205b')
[[ "${CARCH}" = 'i686' ]] && md5sums[0]='a0bcaadffcbe6c12a72e6d413ff8fbce'

package() {
  cd "${srcdir}"

  # unpack bundled files
  bsdtar xf data.tar.gz

  # install config files
  install -d -m755 "${pkgdir}/etc/"{dbus-1/system.d,sysctl.d}
  install -m644 "etc/dbus-1/system.d/${_pkgname}.dbus.conf" \
    "${pkgdir}/etc/dbus-1/system.d/${_pkgname}.dbus.conf"
  install -m644 "etc/sysctl.d/30-${_pkgname}.conf" \
    "${pkgdir}/etc/sysctl.d/30-${_pkgname}.conf"

  # clean up a bit
  rm -f usr/lib/SpiderOak/lib{gcc_s,stdc++,z}.so.*
  rm -f usr/lib/SpiderOak/*/*/*.exe

  # install app in /opt
  install -d -m755 "${pkgdir}/opt/SpiderOak"
  cp -r usr/lib/SpiderOak/* "${pkgdir}/opt/SpiderOak/"

  # change /usr to /opt in start script file
  sed -i 's:/usr/lib:/opt:g' usr/bin/SpiderOak

  # install start script file
  install -d -m755 "${pkgdir}/usr/bin"
  install -m755 usr/bin/SpiderOak "${pkgdir}/usr/bin/SpiderOak"

  # fix desktop file (according to the desktop entry spec)
  sed -i \
    -e "/Encoding/d" \
    -e "/^Name=/s:Backup:Beta:" \
    -e "/^Comment=/s:SpiderOak ::" \
    -e "/^Categories=/s:SpiderOak;::" \
    -e "/^Icon=/s:=.*:=${_pkgname}:" \
    -e "/^Exec=/s:=.*:=SpiderOak:" \
    "usr/share/applications/${_pkgname}.desktop"

  # install desktop and pixmap files
  install -d -m755 "${pkgdir}/usr/share/"{applications,pixmaps}
  install -m644 "usr/share/applications/${_pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -m644 "usr/share/pixmaps/${_pkgname}.png" \
    "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"

  # install custom license file
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 terms.txt "${pkgdir}/usr/share/licenses/${pkgname}/terms.txt"
}

# vim:set ts=2 sw=2 et:
