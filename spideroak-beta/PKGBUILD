# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=spideroak-beta
_pkgname=spideroak
pkgver=4.3.9925
pkgrel=1
pkgdesc="Secure file backup, sync and sharing client (beta channel)"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!strip')
install=$pkgname.install

[[ "$CARCH" = 'i686' ]] && _arch='i386' || _arch='x86_64'
source=("${pkgname}_${pkgver}_${_arch}.deb"::"https://$_pkgname.com/directdownload?platform=ubuntulucid&arch=$_arch&revision=${pkgver##4.3.}&code="
        "terms.txt")
sha1sums=('e9313a0b3ad1559212bb16ccbcc8531002ec8612'
          'f287fdbad966ac9ae4951a1932e9be7e82112728')
[[ "$CARCH" = 'i686' ]] && sha1sums[0]='25f68b1c80e3639e72c2b994bfe45adb11205cad'

package() {
  cd "$srcdir"

  # unpack bundled files
  tar zxf data.tar.gz

  # install config files
  install -d -m 755 "$pkgdir"/etc/{dbus-1/system.d,sysctl.d}
  install -m 644 etc/dbus-1/system.d/"$_pkgname".dbus.conf \
    "$pkgdir/etc/dbus-1/system.d/$_pkgname.dbus.conf"
  install -m 644 etc/sysctl.d/30-"$_pkgname".conf \
    "$pkgdir/etc/sysctl.d/30-$_pkgname.conf"

  # clean up a bit
  rm -f usr/lib/SpiderOak/lib{gcc_s,stdc++,z}.so.*
  rm -f usr/lib/SpiderOak/*/*/*.exe

  # install app in /opt
  install -d -m 755 "$pkgdir"/opt/SpiderOak
  cp -r usr/lib/SpiderOak/* "$pkgdir"/opt/SpiderOak/

  # change /usr to /opt in start script file
  sed -i 's:/usr/lib:/opt:g' usr/bin/SpiderOak

  # install start script file
  install -d -m 755 "$pkgdir"/usr/bin
  install -m 755 usr/bin/SpiderOak "$pkgdir"/usr/bin/SpiderOak

  # fix desktop file
  sed -i \
    -e "/Encoding=UTF-8/d" \
    -e "/^Name=/s:Backup:Beta:" \
    -e "/^Comment=/s:SpiderOak ::" \
    -e "/^Categories=/s:SpiderOak;::" \
    -e "/^Icon=/s:=.*$:=$_pkgname:" \
    -e "/^Exec=/s:=.*$:=SpiderOak:" \
    usr/share/applications/"$_pkgname".desktop

  # install desktop and pixmap files
  install -d -m 755 "$pkgdir"/usr/share/{applications,pixmaps}
  install -m 644 usr/share/applications/"$_pkgname".desktop \
    "$pkgdir/usr/share/applications/$_pkgname.desktop"
  install -m 644 usr/share/pixmaps/"$_pkgname".png \
    "$pkgdir/usr/share/pixmaps/$_pkgname.png"

  # install custom license file
  install -d -m 755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m 644 terms.txt "$pkgdir/usr/share/licenses/$pkgname/terms.txt"
}

# vim:set ts=2 sw=2 et:
