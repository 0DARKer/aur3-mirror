# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Limao Luo <luolimao@gmail.com>
# Maintainer: ava1ar (mail(dot)avatar(at)gmail(dot)com)

pkgname=spideroak-beta
_pkgname=spideroak
_PkgName=SpiderOak
pkgver=4.8.0
pkgrel=2
pkgdesc="Secure file backup, sync and sharing client"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
depends=(qt krb5 python2)
provides=($_pkgname)
conflicts=($_pkgname)
options=('!strip')
install=$pkgname.install
_arch="$CARCH"
[ "$CARCH" = 'i686' ] && _arch='i386'
source=("${_pkgname}_${pkgver}_${_arch}.rpm"::"https://spideroak.com/directdownload?platform=fedora&arch=$_arch&beta=yes&code="
        "terms.txt")
sha1sums=('9c6c643d6f7f8d6176e63f6da6e092a3125d976a'
          'f287fdbad966ac9ae4951a1932e9be7e82112728')
[ "$CARCH" = 'i686' ] && sha1sums[0]='4bfe6d31217dbcfefcf71b7f9aca623e96e59bc0'

package() {
    # install config files
    install -Dm644 $srcdir/etc/dbus-1/system.d/$_PkgName.dbus.conf $pkgdir/etc/dbus-1/system.d/$_pkgname.dbus.conf

    # install app in /opt
    install -d $pkgdir/opt/
    cp -r $srcdir/opt/* $pkgdir/opt/

    # install start script file
    install -Dm755 $srcdir/usr/bin/SpiderOak $pkgdir/usr/bin/SpiderOak

    # install desktop and pixmap files
    install -Dm644 $srcdir/usr/share/applications/$_PkgName.desktop $pkgdir/usr/share/applications/$_pkgname.desktop
    install -Dm644 $srcdir/usr/share/pixmaps/$_PkgName.png $pkgdir/usr/share/pixmaps/$_pkgname.png

    # fix desktop file
    sed -i \
	-e "/Encoding=UTF-8/d" \
	-e "/^Name=/s: Backup::" \
	-e "/^Comment=/s:$_PkgName ::" \
	-e "/^Categories=/s:$_PkgName;::" \
	-e "/^Icon=/s:=.*$:=$_pkgname:" \
	-e "/^Exec=/s:=.*$:=$_PkgName:" \
	$pkgdir/usr/share/applications/$_pkgname.desktop

    # install custom license file
    install -Dm644 terms.txt $pkgdir/usr/share/licenses/$_pkgname/terms.txt
}
