# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Peter Johansson <zeronightfall at gmail dot com>

pkgname=starfighter
pkgver=1.2
pkgrel=2
pkgdesc="A 2D mission-based space shooter"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/pr-starfighter"
license=('GPL')
depends=('gcc-libs' 'sdl_image' 'sdl_mixer')
makedepends=('python2')
source=(http://downloads.sourceforge.net/pr-starfighter/$pkgname-$pkgver.tar.gz
        $pkgname.patch
        $pkgname.png
        $pkgname.desktop)
md5sums=('6d2301539945a04b3109869fe11f362f'
         '5874aa5d326408be5cdb804aa26978f8'
         'bd726a4084b19042dc51953e6230dc0a'
         'edb308677fe38a6defe76aba97212b2a')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -Np1 -i "$srcdir"/$pkgname.patch

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  chmod 644 "$pkgdir"/usr/share/doc/$pkgname/{LICENSE,index.html}

  install -Dm644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
