# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

pkgname=sbagen
pkgver=1.4.5
_river_ver=1.4.1
_rhybag_ver=0.1.1
_binauralanalysis_ver=20040521
pkgrel=3
pkgdesc="A binural brainwave generator"
url="http://uazu.net/${pkgname}"
license=('GPL')
depends=('bash' 'perl' 'lib32-glibc')
makedepends=('libmad' 'libvorbis')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
options=('!strip')
install=${pkgname}.install

source=("http://uazu.net/${pkgname}/${pkgname}-${pkgver}.tgz"
        "http://uazu.net/${pkgname}/${pkgname}-river-${_river_ver}.tgz"
        "http://uazu.net/${pkgname}/rhybag-${_rhybag_ver}.tgz"
        "http://uazu.net/${pkgname}/binaural-analysis-${_binauralanalysis_ver}.tgz")
sha512sums=('35a8cdf0ca59cef01200a102c6f718b163006eec6c3450e7ee32bac0454be2db4d9a4df2f1a52d6cc2beda1d45bf9a7816a2c9dfae710eef14dbe5667cec9bf9'
            'f61c9308c671dc9cef9b9e60ba37cc5a9dc6ac45b5ab34d0818e08f6006eeba0ede4756e0c5375b11963e04f4e1e801119af8df8726c867e3be399983b12bc30'
            '19a432b3d465881a113b0aad3f774062952b3f0229ae77e668b0c43d47c94d595cae1b7178c7a30fbc7d7f05da1cc9c0255ad0de7daba8023da2fd7fc6057e2b'
            'ca60751ed67bf87aa75465fc1f70a57f3ae4e704a0ff5f745d955b65dd624394be74bc19470b8d5d25df13c70241efac355b66a49773d0584623157151e0b074')

prepare() {
    cd ${pkgname}-${pkgver}
    while read -r file; do
        sed -i -re 's|(river[12]\.ogg)|/usr/share/sbagen/media/\1|g' "$file"
    done < <(grep -ir "river1\.ogg" examples/ | sed 's|:.*$||')
}

build() {
    # Remove compiler flags set by makepkg.conf
    unset CFLAGS

    # Compile sbagen
    cd ${pkgname}-${pkgver}
    rm sbagen
    cc -DOGG_DECODE -DMP3_DECODE -Wa,--noexecstack -DT_LINUX -Wall -m32 -O3 -s -lm -lpthread -Wl,-z,noexecstack sbagen.c libs/linux-libmad.a libs/linux-libvorbisidec.a -o sbagen

    # Compile rhybag
    cd ../rhybag-${_rhybag_ver}
    cc -O2 -s -lm main.c -o rhybag 2>&1 1>&0 | ./strip_warnings > mk.err

    cd ../binaural-analysis
    rm anarange downsample
    ./mk
}

package() {
    ### Install rhybag
    install -Dm755 rhybag-${_rhybag_ver}/rhybag "${pkgdir}"/usr/bin/rhybag

    ### Install river sounds
    install -Dm644 ${pkgname}-${_river_ver}/river1.ogg "${pkgdir}"/usr/share/${pkgname}/media/river1.ogg
    install -Dm644 ${pkgname}-${_river_ver}/river2.ogg "${pkgdir}"/usr/share/${pkgname}/media/river2.ogg

    ### Install Binaural analysis tools
    install -Dm755 binaural-analysis/anarange "${pkgdir}"/usr/bin/anarange
    install -Dm755 binaural-analysis/downsample "${pkgdir}"/usr/bin/downsample

    ### Install sbagen
    cd ${pkgname}-${pkgver}
    install -Dm755 ${pkgname} "${pkgdir}"/usr/bin/${pkgname}

    # Install supplimentary files
    find . -type f -name '[a-z]*.txt' -exec install -Dm644 '{}' "${pkgdir}"/usr/share/${pkgname}/doc/'{}' \;
    find examples -type f -exec install -Dm644 '{}' "${pkgdir}"/usr/share/${pkgname}/'{}' \;
    find scripts -type f -exec install -Dm644 '{}' "${pkgdir}"/usr/share/${pkgname}/'{}' \;
}
