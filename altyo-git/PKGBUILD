# Maintainer: willemw <willemw12@gmail.com>

_pkgname=altyo
pkgname=$_pkgname-git
pkgver=r349.a7bfa7e
pkgrel=1
pkgdesc="Vala / GTK3 drop-down terminal emulator"
arch=('i686' 'x86_64')
url="https://github.com/linvinus/AltYo"
license=('GPL3')
depends=('hicolor-icon-theme' 'vte3')
makedepends=('git' 'vala')
provides=($_pkgname)
conflicts=($_pkgname)
install=$pkgname.install
source=($pkgname::git://github.com/linvinus/AltYo.git)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

