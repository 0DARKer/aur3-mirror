# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=tophat
pkgver=2.0.13
pkgrel=1
pkgdesc="fast splice junction mapper for RNA-Seq reads"
arch=('x86_64' 'i686')
url="http://ccb.jhu.edu/software/tophat/index.shtml"
license=('PerlArtistic')
depends=('boost-libs' 'bowtie2' 'python2')
makedepends=('boost' 'clang')
source=("http://ccb.jhu.edu/software/tophat/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('df5c22c85d7c631ee37f61e91f81ae4d')

prepare() {
  cd $srcdir/$pkgname-$pkgver/src

  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      -i bed_to_juncs \
         contig_to_chr_coords \
         sra_to_solid \
         tophat.py \
         tophat-fusion-post
}

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure CC=clang CXX=clang++ --prefix=/usr

  make -j1
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install
}
