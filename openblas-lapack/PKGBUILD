# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>

pkgname=openblas-lapack
_pkgname=OpenBLAS
pkgver=0.2.13
pkgrel=2
pkgdesc="Optimized BLAS library based on GotoBLAS2 1.13 BSD (providing blas, lapack, and cblas)"
arch=('i686' 'x86_64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran')
provides=('openblas' 'blas=3.5.0' 'lapack=3.5.0' 'cblas')
conflicts=('openblas' 'blas' 'lapack' 'cblas')
options=(!emptydirs)
source=(${_pkgname}-v${pkgver}.tar.gz::https://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha256sums=('5f1f986a1900949058b578c61c2b65b4de324f1968ec505daeb526b9f9af14ab')

_config="USE_OPENMP=0 USE_THREAD=0 MAJOR_VERSION=3 NO_LAPACK=0"

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  NPROC=`grep "physical id" /proc/cpuinfo|sort|uniq|wc -l`
  NCORE4PROC=`grep "cores" /proc/cpuinfo|sort|tail -n 1|sed -e 's/cpu cores.*: //'`
  let NCORE=NPROC*NCORE4PROC

  make ${_config} NUM_THREADS=$NCORE libs netlib shared

}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make ${_config} tests
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make ${_config} PREFIX="${pkgdir}/usr" install

  # Install license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Move cmake files to a more suitable destination
  mv "${pkgdir}/usr/cmake/" "${pkgdir}/usr/lib/cmake/"
  # Remove reference to ${pkgdir}
  sed -i -e "s%${pkgdir}%%" "${pkgdir}/usr/lib/cmake/OpenBLASConfig.cmake"

  # Symlink to provide blas, cblas and lapack
  cd "${pkgdir}/usr/lib/"
  # BLAS
  ln -sf libopenblas.so libblas.so
  ln -sf libopenblas.so libblas.so.3
  ln -sf libopenblas.so libblas.so.3.5.0
  # CBLAS
  ln -sf libopenblas.so libcblas.so
  # LAPACK
  ln -sf libopenblas.so liblapack.so
  ln -sf libopenblas.so liblapack.so.3
  ln -sf libopenblas.so liblapack.so.3.5.0
}

# vim:set ts=2 sw=2 et:
