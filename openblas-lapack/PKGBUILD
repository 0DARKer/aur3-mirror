#Maintainer: Jiaxi Hu <sftrytry _AT_ gmail _DOT_ com>
pkgname=openblas-lapack
_pkgname=OpenBLAS
pkgver=0.2.12
pkgrel=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD, providing optimized blas, lapack, and cblas."
arch=('i686' 'x86_64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran')
provides=('openblas' 'blas=3.4.2' 'lapack' 'cblas')
conflicts=('openblas' 'blas' 'lapack' 'cblas')
options=(!makeflags)
source=(${_pkgname}-v${pkgver}.tar.gz::http://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
md5sums=('5df7f175b2db6a2b02ca4ff932e39bc7')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  unset CFLAGS
  unset CXXFLAGS
  NPROC=`grep "physical id" /proc/cpuinfo|sort|uniq|wc -l`
  NCORE4PROC=`grep "cores" /proc/cpuinfo|sort|tail -n 1|sed -e 's/cpu cores.*: //'`
  let NCORE=NPROC*NCORE4PROC
  #make USE_OPENMP=1 NO_LAPACK=0 NUM_THREADS=$NCORE LIBPREFIX=libblas \
  #  MAJOR_VERSION=3 NO_CBLAS=0 NO_AFFINITY=1
  make USE_OPENMP=1 NO_LAPACK=0 NUM_THREADS=$NCORE \
    MAJOR_VERSION=3 NO_AFFINITY=1
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  NPROC=`grep "physical id" /proc/cpuinfo|sort|uniq|wc -l`
  NCORE4PROC=` grep "cores" /proc/cpuinfo|sort|tail -n 1|sed -e 's/cpu cores.*: //'`
  let NCORE=NPROC*NCORE4PROC
  #make PREFIX="$pkgdir/usr" NUM_THREADS=$NCORE LIBPREFIX=libblas \
  #  MAJOR_VERSION=3 install
  make PREFIX="$pkgdir/usr" NUM_THREADS=$NCORE \
    MAJOR_VERSION=3 install
  #rm -f "$pkgdir/usr/include/cblas.h" "$pkgdir"/usr/include/lapacke*
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/lib/"
  ln -sf libopenblas_*-r$pkgver.a libopenblas.a
  ln -sf libopenblas_*-r$pkgver.so libopenblas.so
  ln -sf libopenblas_*-r$pkgver.so libopenblas.so.0
  ln -sf libopenblas.so libblas.so.3
  ln -sf libopenblas.a libblas.a
  ln -sf libblas.so.3 libblas.so
  ln -sf libopenblas.so libcblas.so.3
  ln -sf libopenblas.a libcblas.a
  ln -sf libcblas.so.3 libcblas.so
  ln -sf libopenblas.so liblapack.so.3
  ln -sf libopenblas.a liblapack.a
  ln -sf liblapack.so.3 liblapack.so
}

# vim:set ts=2 sw=2 et:
