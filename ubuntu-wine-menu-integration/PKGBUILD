# Maintainer: Faheem Pervez <trippin1@gmail.com>
pkgname=ubuntu-wine-menu-integration
pkgver=1.3.28
pkgrel=5
pkgdesc="XDG etc. files from Ubuntu's Wine package to integrate it into DEs' app launchers"
arch=('any')
url="http://packages.ubuntu.com/wine1.3"
license=('LGPL2.1')
depends=('wine' 'xdg-utils' 'hicolor-icon-theme')
makedepends=('xz')
install="${pkgname}.install"
source=("http://se.archive.ubuntu.com/ubuntu/pool/universe/w/wine1.3/wine1.3_${pkgver}-0ubuntu1_i386.deb"
	'01-fix-browse-drive-c-kde.patch')
md5sums=('4f0c5bff46bcc333e6811a5c00685431'
	 '5bed5667677f102b7ab9ab3b415f83c1')

package() {
	cd "${pkgdir}"
	unlzma -c "${srcdir}"/data.tar.lzma | tar --preserve-permissions -xf - ./etc/xdg/ ./usr/share/desktop-directories ./usr/share/applications/ ./usr/share/icons --exclude=wine.desktop
	mkdir --mode=0755 "${pkgdir}"/etc/xdg/menus/kde-applications-merged
	for i in `basename "${pkgdir}"/etc/xdg/menus/applications-merged/*`; do
		ln -sf /etc/xdg/menus/applications-merged/"$i" "${pkgdir}"/etc/xdg/menus/kde-applications-merged/"$i"
	done

	patch -p1 -i "${srcdir}"/01-fix-browse-drive-c-kde.patch
}
