# Mantainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Diego <cdprincipe@at@gmail@dot@com>
# Contributor : Jekyll wu <adapte [at] gmail [dot] com>
# Contributor: Carbon Jiao <http://archlive-pkg.googlecode.com>
# Contributor: Mateusz Loskot <mateusz@loskot.net>

pkgname=libfm-git
pkgver=071481a
pkgrel=1
pkgdesc='A glib/gio-based library providing some file management utilities and related-widgets missing in gtk+/glib.'
url='http://wiki.lxde.org/en/PCManFM'
arch=('i686' 'x86_64')
license=('GPL')
options=('!libtool')
install='libfm.install'
provides=('libfm')
conflicts=('libfm')
depends=('gtk2' 'menu-cache' 'shared-mime-info' 'desktop-file-utils')
makedepends=('git' 'intltool' 'pkgconfig' 'autoconf' 'automake' 'perl' 'gtk-doc' 'vala')
source=("git://github.com/lxde/libfm.git")
sha256sums=('SKIP')
_gitname="libfm"

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed 's|-|.|g'
}

build() {
	cd "$srcdir/$_gitname"

	./autogen.sh

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-udisks \
		--enable-gtk-doc

	make LDFLAGS+=' -lgobject-2.0 -lglib-2.0'
}

package() {
	cd "$srcdir/$_gitname"
	  make DESTDIR="$pkgdir" install
}
