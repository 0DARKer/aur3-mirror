# Mantainer: Diego <cdprincipe@at@gmail@dot@com>
# Contributor : Jekyll wu <adapte [at] gmail [dot] com>
# Contributor: Carbon Jiao <http://archlive-pkg.googlecode.com>
# Contributor: Mateusz Loskot <mateusz@loskot.net>

_pkgname=libfm
pkgname=libfm-git
pkgver=1.1.0.383.ga6c5b3d
pkgrel=1
pkgdesc='Library for file management'
url='http://pcmanfm.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
options=('!libtool')
install=libfm.install
depends=('gtk2' 'menu-cache' 'shared-mime-info' 'udisks' 'desktop-file-utils')
makedepends=('git' 'intltool' 'pkgconfig' 'autoconf' 'automake<=1.13.4' 'perl' 'gtk-doc' 'vala')
provides=('libfm')
conflicts=('libfm')
source=(git://pcmanfm.git.sourceforge.net/gitroot/pcmanfm/libfm)
md5sums=('SKIP')

pkgver() {
  cd $srcdir/$_pkgname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $srcdir/$_pkgname

  sh ./autogen.sh

  ### temporary workaround for building failure
  sed -i '/_\$(V)/d;/AM_DEFAULT_VERBOSITY/d' gtk-doc.make
  sed -i 's,done \\,done ; \\,' gtk-doc.make
  autoreconf -fiv

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-udisks \
    --enable-gtk-doc \
    --with-gnu-ld 
  
  make LDFLAGS+=' -lgobject-2.0 -lglib-2.0'
}

package() {
  cd $srcdir/$_pkgname
  make DESTDIR=$pkgdir install

  # Temporary fix to FS#32361
  #rm $pkgdir/usr/include/libfm
  #mv $pkgdir/usr/include/libfm-1.0/ $pkgdir/usr/include/libfm 
}
