# Maintainer: Cedric Staub <cs+aur@cssx.cc>

pkgname=dnssec-trigger
pkgver=0.11
pkgrel=1
pkgdesc="Reconfigures the local unbound DNS server to use DNSSEC enabled forwarders"
arch=('i686' 'x86_64')
url="http://www.nlnetlabs.nl/projects/dnssec-trigger/"
license=('BSD')
depends=('ldns' 'gtk2' 'unbound')
backup=('etc/dnssec-trigger/dnssec-trigger.conf')
source=(
    http://www.nlnetlabs.nl/downloads/dnssec-trigger/$pkgname-$pkgver.tar.gz
    dnssec-triggerd.service dnssec-triggerd-keygen.service)
sha256sums=(
    'c22cff6a51f0ae8e07393ab7935d44faaabfe3d8341ba8bb85189391dcdfd9fb'
    '1e291140b627da22585d85e07d9a0feb3614c9f90528db2b22fe0b8a4301cf51'
    'f549b3e5b4c14bbdb2280719899b59a88d4b806446f5d28a723fe01f17b4f970'
    )

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --with-keydir=/etc/dnssec-trigger
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  mkdir -p "$pkgdir/usr/lib/systemd/system"
  cp "$srcdir/dnssec-triggerd.service" "$pkgdir/usr/lib/systemd/system"
  cp "$srcdir/dnssec-triggerd-keygen.service" "$pkgdir/usr/lib/systemd/system"
  mkdir -p "$pkgdir/etc/dnssec-trigger"
}
