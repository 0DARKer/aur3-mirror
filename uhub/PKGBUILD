# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# contributor: Yoran Heling <info@yorhel.nl>
# Contributor: Lex Black <autumn-wind@web.de>

pkgname=uhub
pkgver=0.4.1
pkgrel=3
pkgdesc="A hub for the ADC network."
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.uhub.org/"
makedepends=('cmake')
depends=('openssl' 'sqlite')
source=("http://www.extatic.org/downloads/uhub/${pkgname}-${pkgver}-src.tar.bz2"
        'uhub.service'
        'uhub.tmpfiles')
md5sums=('ace53f95c62ac6e6df1f86a91e38bb02'
         '09688e786abaa18ce3125155d5bac15d'
         'f9b69851435abd8ba73be1310595a029')
install="uhub.install"
backup=('etc/uhub/motd.txt'
        'etc/uhub/plugins.conf'
        'etc/uhub/rules.txt'
        'etc/uhub/uhub.conf'
        'etc/uhub/users.conf')
install=uhub.install

prepare() {
  cd "${srcdir}"
  rm -fr uhub
  mv "uhub-${pkgver}" uhub
}

build() {
  cd uhub
  cmake . -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd uhub
  make DESTDIR="${pkgdir}" install
  install -Dm644 doc/users.conf "${pkgdir}/etc/uhub/users.conf"
  install -Dm644 doc/rules.txt "${pkgdir}/etc/uhub/rules.txt"
  touch "${pkgdir}/etc/uhub/motd.txt"

  install -Dm644 "${srcdir}/uhub.service" "${pkgdir}/usr/lib/systemd/system/uhub.service"
  install -Dm644 "${srcdir}/uhub.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/uhub.conf"

  install -o424 -g424 -dm770 "${pkgdir}/var/lib/uhub"
  install -o424 -g424 -dm770 "${pkgdir}/var/log/uhub"

  install -Dm644 doc/init.d.RedHat/etc/logrotate.d/uhub "${pkgdir}/etc/logrotate.d/uhub"

  install -Dm644 doc/uhub.1 "${pkgdir}/usr/share/man/man1/uhub.1"
  install -Dm644 doc/uhub-passwd.1  "${pkgdir}/usr/share/man/man1/uhub-passwd.1"

  install -Dm644 doc/architecture.txt "${pkgdir}/usr/share/doc/uhub/architecture.txt"
  install -Dm644 doc/extensions.txt "${pkgdir}/usr/share/doc/uhub/extensions.txt"
  install -Dm644 doc/getstarted.txt "${pkgdir}/usr/share/doc/uhub/getstarted.txt"
}
