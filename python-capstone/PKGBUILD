# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=python-capstone
_pkgname=capstone
pkgver=3.0.2
pkgrel=1
pkgdesc='Python bindings for multi-platform, multi-architecture capstone disassembly framework'
arch=('any')
url='http://www.capstone-engine.org/index.html'
license=('BSD')
depends=('capstone' 'python')
source=(https://github.com/aquynh/capstone/archive/${pkgver}.tar.gz)
sha512sums=('931259ef9d2972f175a8594c0bca2788e6f0b792b415b3dffce6357dd0c7720c67df933994c26236a52d47845e39af1d99c5e2384a5da0d224c94bda523e92bd')

build() {
  cd ${_pkgname}-${pkgver}/bindings/python
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}/bindings/python
  python test.py
}

package() {
  cd ${_pkgname}-${pkgver}/bindings/python
  python setup.py install -O1 --root="${pkgdir}" --prefix=usr
  install -Dm 644 ../../LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
