# Maintainer: Det
# Contributor: Ng Oon-Ee
# Based on nvidia-beta: https://aur.archlinux.org/packages/nvidia-beta/

pkgname=nvidia-beta-all
pkgver=334.16
pkgrel=1
pkgdesc="NVIDIA drivers for all kernels (beta)"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('nvidia-libgl-beta' "nvidia-utils-beta=${pkgver}")
makedepends=('linux-headers')
conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
provides=('nvidia')
license=('custom:NVIDIA')
install=${pkgname}.install
options=('!strip')

_arch='x86'
_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
if [ "${CARCH}" = 'x86_64' ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
fi

#source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
        'nvidia-linux-3.13.patch'
        'nvidia-linux-3.14.patch')
md5sums=(`curl -s ${source}.md5 | cut -d " " -f1` # ${_pkg}.run
         '9761c3f33c47f4ce29d31d6fbaba7559'       # nvidia-linux-3.13.patch
         '4b30d667e50f963f7cd2a118ffe17523')      # nvidia-linux-3.14.patch

prepare() {
    # Remove previous builds
    if [ -d ${_pkg} ]; then
        rm -rf ${_pkg}
    fi

    # Extract
    msg2 "Self-Extracting ${_pkg}.run.."
    sh ${_pkg}.run --extract-only

    cd ${_pkg}

    # Use separate source directories
    for _extramod in $(find /usr/lib/modules/extramodules-*/version -printf '%h\n'); do
        _kernel=$(cat ${_extramod}/version)
        cp -r kernel kernel-${_kernel}
    done
}

build() {
    # Build for all kernels
    for _extramod in $(find /usr/lib/modules/extramodules-*/version -printf '%h\n'); do
        _kernel=$(cat ${_extramod}/version)

        msg "Building module for ${_kernel}..."
        cd "${srcdir}"/${_pkg}/kernel-${_kernel}

        # Patch
        if [[ ${_kernel} = 3.13* ]]; then
            msg2 "Updating for linux 3.13..."
            patch -p2 -i "${srcdir}"/nvidia-linux-3.13.patch
        elif [[ ${_kernel} = 3.14* ]]; then
            msg2 "Updating for linux 3.14..."
            patch -p2 -i "${srcdir}"/nvidia-linux-3.14.patch
        fi

        # Build
        msg2 "Starting make module..."
        make SYSSRC=/usr/lib/modules/${_kernel}/build module
    done
}

package() {
    # Install for all kernels
    for _extramod in $(find /usr/lib/modules/extramodules-*/version -printf '%h\n'); do
        _kernel=$(cat ${_extramod}/version)

        # Install
        install -Dm644 ${_pkg}/kernel-${_kernel}/nvidia.ko "${pkgdir}"/${_extramod}/nvidia.ko

        # Compress
        gzip -9 "${pkgdir}"/${_extramod}/nvidia.ko
    done

    # Blacklist Nouveau
    install -d "${pkgdir}"/usr/lib/modprobe.d/
    echo "blacklist nouveau" >> "${pkgdir}"/usr/lib/modprobe.d/nvidia.conf
}