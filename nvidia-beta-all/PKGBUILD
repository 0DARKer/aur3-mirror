# Maintainer: Det
# Contributor: Ng Oon-Ee
# Based on nvidia-beta: https://aur.archlinux.org/packages/nvidia-beta/

# Try to make sure people don't miss this
echo
warning "343.xx drops support for pre-GeForce 400 "Fermi" GPUs. See:
             - https://devtalk.nvidia.com/default/topic/766018/
             - http://www.phoronix.com/scan.php?page=news_item&px=MTc1NjU"
echo

pkgname=nvidia-beta-all
pkgver=343.13
pkgrel=1
pkgdesc="NVIDIA drivers for all kernels (beta)"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('custom:NVIDIA')
depends=('nvidia-libgl-beta' "nvidia-utils-beta=$pkgver")
makedepends=('linux-headers')
provides=('nvidia')
conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
options=('!strip')
install=$pkgname.install

# Arch/Installer name
_arch='x86_64'
_pkg="NVIDIA-Linux-$_arch-$pkgver-no-compat32"
if [[ "$CARCH" = 'i686' ]]; then
  _arch='x86'
  _pkg="NVIDIA-Linux-$_arch-$pkgver"
fi

# Source
source=("http://us.download.nvidia.com/XFree86/Linux-$_arch/$pkgver/$_pkg.run")

# MD5
_md5=$(echo $source.md5 | sed 's_http://us._ftp://_')
if ! md5sums=$(curl -s $_md5 | cut -d " " -f1); then
  md5sums=('b71118e3e3ae9b227bf6f17a2cc8c726')  # NVIDIA-Linux-x86_64-$pkgver-no-compat32
  [[ "$CARCH" = 'i686' ]] && md5sums=('edd4c618265d22ef4d2b28baf070c6ff')  # NVIDIA-Linux-x86-$pkgver
fi

# Patch
if [[ -f *.patch ]]; then
  source+=('nvidia-linux-3.16.patch')
  md5sums+=('bf3ad68be85b212f00e9efc0246ce8f5')
fi

prepare() {
  # Remove previous builds
  if [[ -d $_pkg ]]; then
    rm -rf $_pkg
  fi

  # Extract
  msg2 "Self-Extracting $_pkg.run.."
  sh $_pkg.run -x
  cd $_pkg

  for _kernel in $(cat /usr/lib/modules/extramodules-*/version); do
    # Use separate source directories
    cp -r kernel kernel-$_kernel

    # Patch
    if [[ -f "$srcdir"/*.patch ]]; then
      _major_patch=$(ls "$srcdir"/*.patch | grep -Po "\d+\.\d+")
      if [[ $_kernel =~ $_major_patch ]]; then
        msg2 "Updating for linux $_kernel..."
        cd kernel-$_kernel
        patch -p2 -i "$srcdir"/nvidia-linux-$_major_patch.patch
        cd -
      fi
    fi
  done
}

build() {
  # Build for all kernels
  for _kernel in $(cat /usr/lib/modules/extramodules-*/version); do
    cd "$srcdir"/$_pkg/kernel-$_kernel

    # Main module
    msg2 "Building Nvidia module for $_kernel..."
    make SYSSRC=/usr/lib/modules/$_kernel/build module

    # UVM
    cd uvm
    msg2 "Building UVM module for $_kernel...: https://devblogs.nvidia.com/parallelforall/unified-memory-in-cuda-6/"
    make SYSSRC=/usr/lib/modules/$_kernel/build module
  done
}

package() {
  # Install for all kernels
  for _extramod in $(find /usr/lib/modules/extramodules-*/version -printf '%h\n'); do
    _kernel=$(cat $_extramod/version)

    # Install
    install -Dm644 $_pkg/kernel-$_kernel/nvidia.ko \
                   "$pkgdir"/$_extramod/nvidia.ko

    # Unified Memory
    install -Dm644 $_pkg/kernel-$_kernel/uvm/nvidia-uvm.ko \
                   "$pkgdir/$_extramod/nvidia-uvm.ko"

    # Compress
    gzip "$pkgdir"/$_extramod/nvidia*.ko
  done

  # Blacklist Nouveau
  install -d "$pkgdir"/usr/lib/modprobe.d/
  echo "blacklist nouveau" >> "$pkgdir"/usr/lib/modprobe.d/nvidia.conf
}