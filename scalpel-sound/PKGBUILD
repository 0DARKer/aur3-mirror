# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkgname=scalpel
pkgname=$_pkgname-sound
pkgver=0.8.0
pkgrel=2
pkgdesc="A fast and lightweight destructive audio editor written in Python."
arch=(i686 x86_64)
url="http://scalpelsound.online.fr/"
license=('GPL')
depends=('python2-numpy' 'python-pyalsaaudio' 'pygtk')
makedepends=('cython')
install="$pkgname.install"
source=("${url}releases/$_pkgname-$pkgver.tar.gz"
        "$pkgname.desktop"
        "http://www.openclipart.org/image/128px/svg_to_png/pitr_scissors_half-open_icon.png")
md5sums=('f30c812c8c95fb0ac703b62f242e513d'
         '745243c79051808fde763f5c5f4fc732'
         '678d3c0e9ba0ada3fa46a5817b9cbb83')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Numpy include fix
  sed -i "s|'])|'], include_dirs=['/usr/lib/python2.7/site-packages/numpy/core/include'])|g" setup.py

  # Python2 fixes
  sed -i "s|\(env python\).*|\12|g" `grep -rl "env python" .`
  export PYTHON="python2"

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir"

  # Rename scalpel to scalpel-sound
  mv "$pkgdir/usr/bin/$_pkgname" "$pkgdir/usr/bin/$pkgname"

  # Install desktop file and pixmaps
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 ../pitr_scissors_half-open_icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
