# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=plasma-desktop-git
pkgver=r1.88ae0b6
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('qt5-x11extras' 'plasma-workspace-git' 'kwin-git' 'plasma-framework-git' 'ki18n-git' 'kcmutils-git' 'knewstuff-git' 'kde4support-git' 'knotifyconfig-git' 'kdesu-git' 'krunner-git' 'kactivities-frameworks-git' 'libusb' 'fontconfig')
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git')
conflicts=('kde-workspace<0')
source=('git://anongit.kde.org/plasma-desktop.git')
md5sums=('SKIP')

pkgver() {
  cd plasma-desktop
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../plasma-desktop/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
