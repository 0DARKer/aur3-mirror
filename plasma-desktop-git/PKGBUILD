# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=plasma-desktop-git
pkgver=r1136.11d6fa6
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('qt5-graphicaleffects' 'libxkbfile' 'libusb-compat' 'libcanberra' 'systemsettings-git' 'ksysguard-git' 
'powerdevil-git' 'oxygen-git' 'kmenuedit-git' 'kinfocenter-git' 'polkit-kde-frameworks-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
optdepends=('plasma-nm-git: Network manager applet' 'kdebase-kdepasswd: to change the user password from systemsettings')
conflicts=('plasma-desktop' 'kdebase-workspace' 'kdebase-kdepasswd')
provides=('plasma-desktop')
source=('git://anongit.kde.org/plasma-desktop.git')
md5sums=('SKIP')

pkgver() {
  cd plasma-desktop
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-desktop \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
