# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=plasma-desktop-git
pkgver=r301.cb80e47
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('qt5-graphicaleffects' 'libxkbfile' 'libusb-compat' 'libcanberra' 'systemsettings-git' 'ksysguard-git' 
'powerdevil-git' 'oxygen-git' 'kmenuedit-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git')
conflicts=('plasma-desktop' 'kde-workspace<0')
provides=('plasma-desktop')
source=('git://anongit.kde.org/plasma-desktop.git')
md5sums=('SKIP')

pkgver() {
  cd plasma-desktop
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-desktop \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# fix conflicts
  rm -fr ${pkgdir}/usr
}
