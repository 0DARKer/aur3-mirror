# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=plasma-desktop-git
pkgver=r687.d48ad31
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('kemoticons-git' 'kitemmodels-git' 'qt5-graphicaleffects' 'libxkbfile' 'libusb-compat' 'libcanberra' 'systemsettings-git' 'ksysguard-git' 
'powerdevil-git' 'oxygen-git' 'kmenuedit-git' 'kinfocenter-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
optdepends=('plasma-nm-git: Network manager applet')
conflicts=('plasma-desktop' 'kde-workspace<0')
provides=('plasma-desktop')
source=('git://anongit.kde.org/plasma-desktop.git')
md5sums=('SKIP')

pkgver() {
  cd plasma-desktop
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-desktop \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# move conflicting policy file to /opt
  mv "$pkgdir"/usr/share/polkit-1 "$pkgdir"/opt/kf5/share
  rm -fr "$pkgdir"/usr
}
