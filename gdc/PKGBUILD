# Contributor: Selman Ulug <selman.ulug@gmail.com>
# Contributor: Andrew O'Neil <ajoneil@gmail.com>
pkgname=gdc
pkgver=0.24
_gccver=4.1.2
pkgrel=1
pkgdesc="GDC, Digital Mars D Programing Language (DMD) frontend for GCC"
arch=(i686 x86_64)
url="http://dgcc.sourceforge.net"
license="GPL"
provides=('gdc')
depends=('gcc>=4.1.2')
conflicts=('gdc-svn')
source=(http://optusnet.dl.sourceforge.net/sourceforge/dgcc/$pkgname-$pkgver-src.tar.bz2
        ftp://gcc.gnu.org/pub/gcc/releases/gcc-${_gccver}/gcc-core-${_gccver}.tar.bz2
        gcc_pure64.patch)
md5sums=('aa4433729015d8b6e7183ab9528ba77c' '2af3fb599635219171c6ae1f3034888a' '1ceaa49e3a1d9f984ecc2893c43f7425')

build() {
  cd $startdir/src
  cp -r $startdir/src/d $startdir/src/gcc-${_gccver}/gcc
  cd $startdir/src/gcc-${_gccver}
  # make patches in dgcc-build if necessary
  # apply gdc patches
  ./gcc/d/setup-gcc.sh
  export MAKEFLAGS="-j1"
  # Don't install libiberty 
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  if [ "${CARCH}" = "x86_64" ]; then
      patch -Np1 -i ../gcc_pure64.patch || return 1
  fi
  # Don't run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
  mkdir ../gcc-build
  cd ../gcc-build
  ../gcc-${_gccver}/configure --prefix=/usr --enable-shared \
       --enable-languages=d --enable-threads=posix --enable-__cxa_atexit \
       --disable-multilib --libdir=/usr/lib --libexecdir=/usr/lib \
       --enable-clocale=gnu
  make all-target-libphobos || return 1
  make DESTDIR=$startdir/pkg install-target-libphobos || return 1
  cd gcc
  make DESTDIR=${startdir}/pkg lang.install-normal lang.install-common lang.install-man
  install -Dm 755 cc1d $startdir/pkg/usr/lib/gcc/${CHOST}/${_gccver}/
}
