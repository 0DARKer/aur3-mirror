# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Based on r8168 [community] & r8168-ck (https://aur.archlinux.org/packages.php?ID=56657)

pkgname=r8168-openvz
_realname=r8168
pkgver=8.032.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards for linux-openvz"
url="http://www.realtek.com.tw"
license=("GPL")
arch=("i686" "x86_64")
depends=("glibc" "linux-openvz>=2.6.32" "linux-openvz<2.6.33")
makedepends=("linux-openvz-headers")
source=("http://r8168.googlecode.com/files/${_realname}-${pkgver}.tar.bz2")
install="${_realname}.install"
md5sums=("942ce02459a181093f77bb4d5cc75d45")
sha256sums=("f24ed6ba1a0a6e8cae27c49b0bddbecbd430ad1d7ddd1c5d28f2091202aee9c3")

_extramodules="extramodules-2.6.32-openvz"
_kernver="$(cat "/usr/lib/modules/${_extramodules}/version" || true)"

build() {
	cd "${srcdir}/${_realname}-${pkgver}"
#  make clean BASEDIR="/usr/lib/modules/${_kernver}" modules  # r8168-ck
#  make clean SYSSRC="/usr/src/linux-${_kernver}" modules # r8168
# r8168-lts
  make -C "/usr/lib/modules/${_kernver}/build" SUBDIRS="${srcdir}/${_realname}-${pkgver}/src" EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" modules
}

package() {
	cd "${srcdir}/${_realname}-${pkgver}/src"
	install -Dm644 "${_realname}.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/${_realname}.ko"
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
}
