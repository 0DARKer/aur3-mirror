# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=abe
pkgver=1.1
pkgrel=7
pkgdesc="A scrolling, platform-jumping, key-collecting, ancient pyramid exploring game."
arch=('i686' 'x86_64')
url="http://abe.sourceforge.net"
license=('GPL')
depends=('sdl_mixer' 'libxmu' 'libxi')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz \
  abe.desktop logo.png)
md5sums=('5537920e1746708e1a631d84d3500f5c' \
  '7fa5c0aa79b1d94bbac4e9e6100f9fe0' \
  '8a41e3aec62321221204ade2ee21aec8')

build() {
  cd $srcdir/$pkgname-$pkgver
  # Fix base paths
  sed 's|BASE_DIR .|BASE_DIR /usr/share/abe|' -i src/Directories.h
  # Fix some settings in the menu not working
  sed 's|return 0|return 1|' -i src/Menu.c
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -m755 -D src/abe $pkgdir/usr/bin/abe
  mkdir -p $pkgdir/usr/share/{abe,applications}
  cp -r images/ maps/ sounds/ $pkgdir/usr/share/abe
  install -m644 $srcdir/logo.png $pkgdir/usr/share/abe/images
  install -m644 $srcdir/abe.desktop $pkgdir/usr/share/applications
}
