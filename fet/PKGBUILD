# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=fet
pkgver=5.14.5
pkgrel=1
pkgdesc="Free Timetabling Software automatically schedule the timetable of a school, high-school or university."
arch=(i686 x86_64)
url="http://lalescu.ro/liviu/fet/"
license=('GPL')
depends=('qt')
install="$pkgname.install"
source=("${url}download/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop")
md5sums=('ef2482845d950dea1a72254281293e35'
         '8d951b500de581eb660472aa56ba6ba3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # prepare dest dirs
  install -d "$pkgdir"/usr/{bin,share/{doc/,}$pkgname}

  # bin
  install -Dm755 $pkgname "$pkgdir/usr/bin"

  # translations
  install -d "$pkgdir/usr/share/$pkgname/translations"
  install -Dm644 translations/*.qm "$pkgdir/usr/share/$pkgname/translations"

  # examples
  cp -a examples "$pkgdir/usr/share/$pkgname"

  # man
  install -Dm644 doc/$pkgname.1 "$pkgdir/usr/share/man/man1/$pkgname.1"

  # docs
  cp -a doc/[^f]* "$pkgdir/usr/share/doc/$pkgname"

  # desktop
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
