# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: speps <speps at aur dot archlinux dot org> 

pkgname=fet
pkgver=5.23.2
pkgrel=1
pkgdesc="Atomatically schedule the timetable of a school, high-school or university managing resources"
url="http://lalescu.ro/liviu/fet"
license=('GPL')

arch=('i686' 'x86_64')
depends=('qt5-base')
install="$pkgname.install"

source=("${url}/download/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop")
md5sums=('599e1f867b9fbcc3e2f66d5af8c74a34'
         '8d951b500de581eb660472aa56ba6ba3')

#_language=( ar ca da de el es fa fr gl he hu id it lt mk ms nl pl pt_BR ro ru si sk sq sr tr uk untranslated uz vi )
_language=( untranslated de ) # do we really need 'untranslated' ?

#_examples=( Algeria Argentina Belize Brazil Bulgaria Denmark Egypt Germany Greece Hong-Kong Hungary India Indonesia Iran Italy Malta Morocco Namibia Romania Saudi-Arabia South-Africa Spain Sudan United-Kingdom )
_examples=( Germany )

build() {
  cd $pkgname-$pkgver
  qmake-qt5
  make 
}

package() {
  cd $pkgname-$pkgver

  # bin
  install -d "$pkgdir"/usr/bin "$pkgdir"/usr/share/doc/fet
  install -m755 fet fet-cl "$pkgdir"/usr/bin

  # desktop
  install -Dm644 ../fet.desktop \
    "$pkgdir"/usr/share/applications/fet.desktop

  # man
  install -Dm644 doc/fet.1 "$pkgdir"/usr/share/man/man1/fet.1

  # translations
  for _lang in ${_language[@]}; do
    install -Dm644 translations/fet_${_lang}.qm "${pkgdir}"/usr/share/fet/translations/fet_${_lang}.qm
  done
  
  # examples
  for _lang in ${_examples[@]}; do
    install -d "$pkgdir"/usr/share/fet/examples/${_lang}
    install -m644 `find ./examples/${_lang} -type f` "${pkgdir}"/usr/share/fet/examples/${_lang}
  done

  # docs  
  install -m644 README TODO ChangeLog  "$pkgdir"/usr/share/doc/fet
  cd doc
  for _doc in `find . -type f ! -iname fet.1`; do
      install -Dm644 $_doc "${pkgdir}"/usr/share/doc/fet/$_doc
  done
}
