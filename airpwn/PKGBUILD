# Contributor: nofxx <x@nofxx.com>
# Contributor: sergeantspoon <sergeant.spoon@gmail.com>
pkgname=airpwn
pkgver=1.4
pkgrel=3
pkgdesc="Airpwn is a tool for generic packet injection on an 802.11 network."
arch=('i686' 'x86_64')
url="http://airpwn.sourceforge.net"
license=('GPL')
depends=('pcre' 'libpcap' 'openssl' 'lorcon' 'wireless_tools' 'libnet')
makedepends=('python2' 'autoconf')
optdepends=('iw: to put wireless interfaces into monitor mode')
provides=(airpwn)
conflicts=(airpwn)
source=("http://downloads.sourceforge.net/airpwn/$pkgname-$pkgver.tgz"
        "link-libcrypto.patch")
md5sums=('c5538f5eadc2afa0bf3e873c4bfa2513'
         '921260502b46bd140d74448f5526e533')

build() {
    cd $srcdir/$pkgname-$pkgver

    # Patch configure to link libcrypto
    patch -Np1 < $srcdir/link-libcrypto.patch

    # Fix airpwn files so we can use it with a Python version other than 2.4.
    PYTHON_VERSION1=$(python2 -c 'import sys; print sys.version[:3]')
    PYTHON_VERSION2=$(echo $PYTHON_VERSION1 | tr . _)

    for FILE in configure.ac conf.h config.h.in; do
        sed -i "s|python2.4|python${PYTHON_VERSION1}|g" $FILE
    done

    sed -i "s|Python2.4|Python${PYTHON_VERSION1}|g" configure.ac
    sed -i "s|PYTHON2_4|PYTHON${PYTHON_VERSION2}|g" config.h.in

    autoreconf

    ./configure --prefix=/usr
    make || return 1
}

package() {
    cd $srcdir/$pkgname-$pkgver

    make DESTDIR=$pkgdir install || return 1

    install -m 0755 mac80211_prep.sh $pkgdir/usr/sbin || return 1
    install -m 0755 madwifing_prep.sh $pkgdir/usr/sbin || return 1
    install -m 0755 madwifiold_prep.sh $pkgdir/usr/sbin || return 1
}
