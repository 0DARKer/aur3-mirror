# This is a fork of Samsagax's nvidia-bumblebee, supporting PAE kernels instead of normal kernels
# Maintainer: wyy1326 <wyy1326[at]gmail[dot]com>
# Contributor: Thomas Baechler <thomas[at]archlinux[dot]org>

pkgname=nvidia-bumblebee-pae
pkgver=295.49
_kernel_ver=`uname -r | cut -c1-3`
_extramodules="extramodules-${_kernel_ver}-pae"
pkgrel=1
pkgdesc="NVIDIA drivers for linux. Packaged for Bumblebee. PAE version"
arch=('i686')
url="http://www.nvidia.com/"
depends=('linux-pae' "nvidia-utils-bumblebee=${pkgver}")
provides=("nvidia=${pkgver}")
makedepends=('linux-pae-headers')
conflicts=('nvidia' 'nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

_arch='x86'
_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
md5sums=('fd998f870d20b30b8d636ffce79d9e6c')

build() {
    _kernver="$(cat /lib/modules/${_extramodules}/version || true)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nouveau_blacklist.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
