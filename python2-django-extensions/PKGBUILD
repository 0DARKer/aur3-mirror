# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
#
# namcap warns that python2-django and python2-six are unnecessary dependencies.
# This is untrue.

pkgname=python2-django-extensions
_pkgname=django-extensions
pkgver=1.3.2
pkgrel=1
pkgdesc='A collection of custom extensions for the Django Framework.'
arch=(any)
url='http://github.com/django-extensions/django-extensions'
license=(MIT)
depends=(python2-django python2-six)
makedepends=(python2-setuptools)
optdepends=(
    'graphviz: to graph Django models'
    'python2-werkzeug: to use the Werkzeug debugger in the embedded web server'
)
conflicts=(django-extensions)
options=(!emptydirs)
source=(
    LICENSE
    "https://github.com/${_pkgname}/${_pkgname}/archive/${pkgver}.tar.gz"
)
sha256sums=('85f87e274f2beeceaf94955674d8323d9fc1ebd6fc35d4afcdd38b544ca0cb20'
            '3f0a5a7df9bac5cb7689f6c0c3e57e4adb76fa4fd5a5a8bd50ad792b608a6acb')

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
