# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=codeslayer
pkgver=2.1.0
pkgrel=1
pkgdesc="A source code editor that boasts a clean interface but powerful features."
arch=(i686 x86_64)
url=http://code.google.com/p/$pkgname/
license=(GPL2)
depends=(desktop-file-utils gtksourceview3 hicolor-icon-theme)
makedepends=(intltool)
install=$pkgname.install
source=(http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.gz
    $pkgname.desktop)
options=(!buildflags !libtool)
sha256sums=('38310a6221451acc81051039fa1098d14d0cd1bedd96b01aaa1a4754a113f33d'
    '5d8b46ba9a71492766bcb6c908a7030bbb22b73e2743f5c2412f0a9ba15e0348')
sha512sums=('e7417f57b28c758bd58f107ebebbe2065d523e452daa152fe445d3944af354e017e5adfd782515b40647348865555606cbdd4ea26af54426eddf3a4d3126a671'
    'bcc72aa995aa46a958590a0525ea54f88ede1aa71c93ea138b18119b790286cb84ee832a69f50a4f6fb7bc23d6ab659f69b3d5cf5e60635c4e8655bcadc09293')

build() {
    cd "$srcdir"/$pkgname-$pkgver/
    ./configure --prefix=/usr --mandir=/usr/share/man
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    make DESTDIR="$pkgdir" install
    desktop-file-install ../$pkgname.desktop --dir "$pkgdir"/usr/share/applications/
}
