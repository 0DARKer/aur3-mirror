# Maintainer: eolianoe <eolianoe At GoogleMAIL DoT CoM>
# Contributor: Kibeom Kim <kkb110@gmail.com>

pkgname=getfem++
_pkgname=getfem
pkgver=4.3
pkgrel=1
pkgdesc="Generic C++ finite element library."
arch=('i686' 'x86_64')
url="http://download.gna.org/getfem/html/homepage/"
license=('LGPL3')
depends=('python2-numpy' 'python2-scipy' 'mumps-seq'
'boost' 'qhull' 'qd' 'muparser' 'metis')
source=("http://download.gna.org/getfem/stable/${_pkgname}-${pkgver}.tar.gz")

md5sums=('5fd1af782923e0ec073cb07ecebba966')
conflicts=('gmm')

prepare(){
  cd ${srcdir}/${_pkgname}-${pkgver}

  # Use shared library instead of static
  sed -i 's/libqd.a/libqd.so/g' configure

  # Fix qhull include
  sed -i 's/qhull\/qhull.h/libqhull\/qhull_a.h/g' configure
  sed -i 's/qhull\/qhull.h/libqhull\/qhull_a.h/g' src/getfem_mesher.cc
  sed -i 's/<qhull/<libqhull/g' src/getfem_mesher.cc

  # Use python2 
  sed -i 's/env\ python/env\ python2/g' bin/extract_doc
  sed -i 's/python\ setup.py/python2\ setup.py/g' interface/src/python/Makefile.in
}

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}

  export PYTHON=python2
  export PYTHON_VERSION=2.7

  # For mumps
  export LIBS="-lmumps_common_seq -lpord_seq -lmpiseq"

  ./configure --prefix=/usr \
    --enable-shared --disable-static \
    --with-pic \
    --enable-qhull \
    --enable-qd \
    --enable-mumps \
    --enable-boost \
    --disable-openmp \
    --enable-superlu \
    --enable-muparser \
    --enable-metis \
    --enable-python \
    --disable-matlab \
    --disable-scilab

  make
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}

  rm -r ${pkgdir}/usr/getfem_toolbox
}
