# Maintainer: Oleksandr "post-factum" Natalenko <pfactum@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sebastian Joergensen <sebjorg.gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas.archlinux.org>

pkgname=ntfs-3g
pkgver=2011.1.15
pkgrel=1
pkgdesc="Read and write NTFS driver"
url="http://www.tuxera.com/community/ntfs-3g-download/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse')
makedepends=('pkgconfig')
options=('!libtool')
provides=("$pkgname=$pkgver")
source=("http://tuxera.com/opensource/ntfs-3g-2011.1.15.tgz"
	'25-ntfs-config-write-policy.fdi')
md5sums=('15a5cf5752012269fa168c24191f00e2'
	'9dca69d78310f8635877984167eb515f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ac_cv_path_LDCONFIG=/bin/true ./configure \
    --prefix=/usr \
    --with-fuse=external \
    --disable-static \
    --enable-posix-acls

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  ln -s /bin/ntfs-3g "${pkgdir}/sbin/mount.ntfs"

  install -m644 -D "$srcdir/25-ntfs-config-write-policy.fdi" \
    "$pkgdir/usr/share/hal/fdi/policy/10osvendor/25-ntfs-config-write-policy.fdi"
}
