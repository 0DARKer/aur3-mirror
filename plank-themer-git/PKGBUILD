# Maintainer: Que Quotion <quequotion@mailinator.com>

pkgname=plank-themer-git
_gitname=plank-themer
pkgver=6b66d2d
pkgrel=1
pkgdesc="A small program that allows you to change Plank Dock themes without any additional consumption of resources"
arch=('i686' 'x86_64')
url="http://rhoconlinux.github.io/plank-themer/"
license=('LGPL')
depends=('bash' 'plank')
makedepends=('bash')
provides=("plank-themer=${pkgver}" "plank-themer-git=${pkgver}")
conflicts=()
replaces=('plank-themer')
source=('git://github.com/rhoconlinux/plank-themer'
	'Makefile'
	'plank-themer.sh')
sha512sums=('SKIP'
            'b6562a7b69f4991d0e49c4bc9c79d5627fefcca6c95224acb28b17a0f7e20f0c65c7edd819641bd0ffe2ec063da4b2b3b6a7ac8d1229f21966874c8537e783bc'
            'f5637581a9373b878f5ba3b55cec156ab6a82c159f3af2f872a183cde4817a17ada0d8745d319fcdbb68d1a49417247b08d732e0fc0c77b679ac8b4c3f65924d')

pkgver() {

	cd "${_gitname}"
	git describe --always | sed 's/-/./g'

}


prepare() {

	rm -rf ${_gitname}/themes-repo/Themes/Pantheon # Conflicts with plank-theme-pantheon(-bzr); probably not as up-to-date.
	cp "$srcdir/Makefile" ${_gitname}
	cp "$srcdir/plank-themer.sh" ${_gitname}

}

build() {

	cd ${_gitname}
	make DESTDIR="$pkgdir"

}

package() {

	cd ${_gitname}
	make DESTDIR="$pkgdir" install

}
