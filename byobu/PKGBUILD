# -*- shell-script -*-
# Maintainer: Baptiste Jonglez <zerstorer at free dot fr>
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=byobu
pkgver=3.33
pkgrel=1
pkgdesc="Enhanced profile and configuration utilities for GNU screen"
arch=("i686" "x86_64")
url="https://launchpad.net/byobu"
license=("GPL3")
depends=("screen" "libnewt" "python2")
makedepends=("gettext" "patch")
install="${pkgname}.install"
source=("${url}/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
        "${pkgname}4arch.diff")
md5sums=('7ce0706b459144ab45e860b539213571'
         '14e6cf3849123dfe191715646a5ab2f4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Adapt byobu for Arch
  patch -uNp1 -i "${srcdir}/${pkgname}4arch.diff"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="$pkgdir"

  # Install default notifications configuration
  install -D -m644 etc/byobu/statusrc "${pkgdir}/etc/${pkgname}/statusrc"
  install -m644 etc/byobu/socketdir "${pkgdir}/etc/${pkgname}"

  # Remove Makefile crap
  find "${pkgdir}" -name "Makefile.*" -exec rm -f {} \;

}
