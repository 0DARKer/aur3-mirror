# -*- shell-script -*-
# Maintainer: Baptiste Jonglez <zerstorer at free dot fr>
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=byobu
pkgver=4.46
pkgrel=1
pkgdesc="Enhanced profile and useful notifications for tmux (or optionally screen)"
arch=("any")
url="https://launchpad.net/byobu"
license=("GPL3")
depends=("tmux" "libnewt" "python2")
makedepends=("gettext" "patch")
optdepends=("screen")
install="${pkgname}.install"
source=("${url}/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
    "${pkgname}.diff" # for the python2 issue & arch-specific config
    "statusrc"
    "status")
md5sums=('fa63ae4c4afbfe492bf62f29286af5e2'
         'eb18b4e7471a4c18dc5b515702781c07'
         '114937599258f95d608fc6b459c0aaff'
         '5eae11eacf0a19683751e711a541af2c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix python -> python2 & /etc config
  patch -uNp1 -i "${srcdir}/${pkgname}.diff"

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="$pkgdir"

  # Install custom configuration for arch
  install -D -m644 ${srcdir}/statusrc ${srcdir}/status "${pkgdir}/usr/share/${pkgname}/status/"
}
