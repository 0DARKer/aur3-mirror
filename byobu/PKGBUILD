# -*- shell-script -*-
# Maintainer: Baptiste Jonglez <zerstorer at free dot fr>
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=byobu
pkgver=4.34
pkgrel=1
pkgdesc="Enhanced profile and configuration utilities for GNU screen (preliminary tmux support)"
arch=("any")
url="https://launchpad.net/byobu"
license=("GPL3")
depends=("screen" "libnewt" "python2")
makedepends=("gettext" "patch")
optdepends=("tmux")
install="${pkgname}.install"
source=("${url}/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
    "${pkgname}.diff" #for the python2 issue
    "statusrc"
    "socketdir")
md5sums=('0eb0e3b2bbcb05d2e4211fd96bc028f3'
         'f15b4da9701e72ece3c1d3e1c00f8770'
         '0206b2c855e6d310ee7997cdce862b41'
         '0846a9f14f899e934f82e64798faaff2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix python -> python2
  patch -uNp1 -i "${srcdir}/${pkgname}.diff"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="$pkgdir"

  # Install custom configuration for arch
  #install -D -m644 ${srcdir}/statusrc ${srcdir}/status "${pkgdir}/usr/share/${pkgname}/status/"
  install -D -m644 ${srcdir}/statusrc "${pkgdir}/usr/share/${pkgname}/status/"
  install -D -m644 ${srcdir}/socketdir "${pkgdir}/etc/${pkgname}/socketdir"
  install -m644 ${srcdir}/${pkgname}-${pkgver}/etc/${pkgname}/backend "${pkgdir}/etc/${pkgname}/"
}
