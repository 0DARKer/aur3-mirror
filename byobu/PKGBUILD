# -*- shell-script -*-
# Maintainer: Baptiste Jonglez <zerstorer@free.fr>
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=byobu
pkgver=3.29
pkgrel=1
pkgdesc="Enhanced profile and configuration utilities for GNU screen"
arch=("i686" "x86_64")
url="https://launchpad.net/byobu"
license=("GPL")
depends=("screen" "libnewt" "python2")
makedepends=("gettext" "patch")
install="${pkgname}.install"
source=("${url}/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
        "${pkgname}4arch.diff")
md5sums=('10ce12db8d52f2c50ca9c94c53938c6d'
         'a18cb6da72b0bf89dabebce60eec5e25')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Adapt byobu for Arch Linux, and only expand on the old build
# procedure, until autotools support is polished
  patch -uNp1 -i "${srcdir}/${pkgname}4arch.diff"

# Generate and install locales
  for _i in po/*.po
  do
    _lang=${_i[@]//po\//}
    _lang=${_lang[@]//.po/}
    install -d "${pkgdir}/usr/share/locale/${_lang}/LC_MESSAGES/"
    msgfmt "${_i}" -o "${pkgdir}/usr/share/locale/${_lang}/LC_MESSAGES/${pkgname}.mo"
  done

# Install man pages
  install -d "${pkgdir}/usr/share/man/man1/"
  install -m644 usr/share/man/man1/*.1 "${pkgdir}/usr/share/man/man1"

# Install documentation (also required by help function)
  install -D -m644 usr/share/doc/byobu/help.txt "${pkgdir}/usr/share/doc/${pkgname}/help.txt"
  install -m644 README AUTHORS "${pkgdir}/usr/share/doc/${pkgname}"

# Install default notifications configuration
  install -D -m644 etc/byobu/statusrc "${pkgdir}/etc/${pkgname}/statusrc"
  install -m644 etc/byobu/socketdir "${pkgdir}/etc/${pkgname}"

# Install launcher and icon(s)
  install -D -m644 icons/byobu_multi.svg "${pkgdir}/usr/share/${pkgname}/pixmaps/${pkgname}_multi.svg"
  install -D -m644 usr/share/byobu/pixmaps/byobu.svg "${pkgdir}/usr/share/${pkgname}/pixmaps/${pkgname}.svg"
  install -D -m644 usr/share/applications/byobu.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

# Install ec2 rates
  install -d "${pkgdir}/usr/share/${pkgname}/ec2/"
  install -m644 usr/share/byobu/ec2/* "${pkgdir}/usr/share/${pkgname}/ec2"

# Install profiles
  install -d "${pkgdir}/usr/share/${pkgname}/profiles/"
  install -m644 usr/share/byobu/profiles/* "${pkgdir}/usr/share/${pkgname}/profiles"

# Install keybindings
  install -d "${pkgdir}/usr/share/${pkgname}/keybindings/"
  install -m644 usr/share/byobu/keybindings/* "${pkgdir}/usr/share/${pkgname}/keybindings"

# Install windows example
  install -D -m644 usr/share/byobu/windows/common "${pkgdir}/usr/share/${pkgname}/windows/common"

# Install libraries
  install -d "${pkgdir}/usr/lib/${pkgname}/"
  install -m755 usr/lib/byobu/* "${pkgdir}/usr/lib/${pkgname}"

# Install executables
  install -d "${pkgdir}/usr/bin/"
  install -m755 usr/bin/* "${pkgdir}/usr/bin"

# Remove Makefile crap
  find "${pkgdir}" -name "Makefile.*" -exec rm -f {} \;
}
