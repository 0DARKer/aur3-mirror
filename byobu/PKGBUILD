# -*- shell-script -*-
# Maintainer: Baptiste Jonglez <zerstorer at free dot fr>
# Contributor: Adrian C. <anrxc..sysphere.org>

pkgname=byobu
pkgver=4.44
pkgrel=1
pkgdesc="Enhanced profile and configuration utilities for GNU screen (preliminary tmux support)"
arch=("any")
url="https://launchpad.net/byobu"
license=("GPL3")
depends=("tmux" "libnewt" "python2")
makedepends=("gettext" "patch")
optdepends=("screen")
install="${pkgname}.install"
source=("${url}/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
    "${pkgname}.diff" #for the python2 issue
    "statusrc"
    "status"
    "socketdir"
    "backend")
md5sums=('95677ff82fce9c4839e8d626d90b0dc6'
         'f15b4da9701e72ece3c1d3e1c00f8770'
         '114937599258f95d608fc6b459c0aaff'
         '5eae11eacf0a19683751e711a541af2c'
         '0846a9f14f899e934f82e64798faaff2'
         '9f19a0102abedf38ec2174039d0d0d6d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix python -> python2
  patch -uNp1 -i "${srcdir}/${pkgname}.diff"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="$pkgdir"

  # Install custom configuration for arch
  install -D -m644 ${srcdir}/statusrc ${srcdir}/status "${pkgdir}/usr/share/${pkgname}/status/"
  install -D -m644 ${srcdir}/socketdir "${pkgdir}/etc/${pkgname}/socketdir"
  install -m644 ${srcdir}/backend "${pkgdir}/etc/${pkgname}/"
}
