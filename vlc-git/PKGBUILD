# Maintainer: heaven <aheaven87 at gmail dot com>
# Contributor: graysky <graysky at archlinux dot us>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: MacWolf <macwolf at archlinux dot de>

pkgname='vlc-git'
pkgver=2.2.r54383.g5389383
pkgrel=1
pkgdesc='A multi-platform MPEG, VCD/DVD, and DivX player. Development GIT Version.'
arch=('i686' 'x86_64')
url='http://www.videolan.org/vlc/'
license=('LGPL2.1' 'GPL2')
depends=('a52dec' 'libdvbpsi' 'libxpm' 'libdca' 'qt4' 'libproxy'
         'sdl_image' 'libdvdnav' 'libtiger' 'libmatroska'
         'zvbi' 'taglib' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp'
         'libshout' 'libmad' 'libmpeg2' 'libmodplug' 'libass'
         'xcb-util-keysyms')
makedepends=('live-media' 'libnotify' 'libbluray' 'flac' 'kdelibs'
             'fluidsynth' 'libdc1394' 'libavc1394' 'lirc-utils'
             'libcaca' 'librsvg' 'portaudio' 'libgme' 'xosd'
             'projectm' 'twolame' 'aalib' 'libmtp' 'libdvdcss'
             'gnome-vfs' 'libgoom2' 'libtar' 'vcdimager' 'opus' 'libssh2'
             'mesa' 'git' 'smbclient' 'libva')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin'
            'ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'kdelibs: KDE Solid hardware integration'
            'libva-vdpau-driver: vdpau back-end for nvidia'
            'libva-intel-driver: back-end for intel cards'
            'libbluray: for Blu-Ray support'
            'flac: for Free Lossless Audio Codec plugin'
            'portaudio: for portaudio support'
            'twolame: for TwoLAME mpeg2 encoder plugin'
            'projectm: for ProjectM visualisation plugin'
            'libcaca: for colored ASCII art video output'
            'libgme: for libgme plugin'
            'librsvg: for SVG plugin'
            'gnome-vfs: for GNOME Virtual File System support'
            'libgoom2: for libgoom plugin'
            'vcdimager: navigate VCD with libvcdinfo'
            'xosd: for xosd support'
            'aalib: for ASCII art plugin'
            'libmtp: for MTP devices support'
            'fluidsynth: for synthesizer MIDI FluidSynth'
            'smbclient: for SMB access plugin'
            'libcdio: for audio CD playback support'
            'ttf-freefont: for subtitle font '
            'ttf-dejavu: for subtitle font'
            'opus: for opus support'
            'libssh2: for sftp support')
conflicts=('vlc' 'vlc-stable-git' 'vlc-dev')
provides=('vlc')
source=('git://git.videolan.org/vlc.git')
backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool' '!emptydirs')
md5sums=('SKIP')
install='vlc-git.install'

_gitroot='vlc'

pkgver() {
  cd "${srcdir}/$_gitroot"
  echo "2.2.r$(git rev-list --count master).g$(git log -1 --format="%h")"
}

build() {
  cd "${srcdir}/$_gitroot"

  msg 'Generating necessary files...'

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c
  sed -i -e 's:truetype/ttf-dejavu:TTF:g' modules/visualization/projectm.cpp

  ./bootstrap

  msg 'Done. Configuring VLC...'

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-lirc \
              --enable-realrtsp \
              --enable-aa \
              --enable-vcdx \
              --enable-wma-fixed \
              --enable-merge-ffmpeg \
              --disable-rpath \
              --disable-lua \
              RCC=/usr/bin/rcc-qt4

  msg 'Done. Starting make...'

  make
  #make CFLAGS="$CFLAGS -fPIC"
}

package() {
    cd "$srcdir/$_gitroot"
    make PREFIX=/usr DESTDIR="$pkgdir" install
}
