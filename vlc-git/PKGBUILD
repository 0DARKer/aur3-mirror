# Maintainer: heaven <aheaven87 at gmail dot com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: MacWolf <macwolf at archlinux dot de>

pkgname='vlc-git'
pkgver=20120326
pkgrel=1
pkgdesc='A multi-platform MPEG, VCD/DVD, and DivX player. Development GIT Version.'
arch=('i686' 'x86_64')
url='http://www.videolan.org/vlc/'
license='GPL'

depends=('a52dec' 'libdvbpsi' 'libxpm' 'libdca' 'qt' 'libproxy'
         'sdl_image' 'libdvdnav' 'libtiger' 'lua' 'libmatroska'
         'zvbi' 'taglib' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp'
         'libshout' 'libmad' 'libmpeg2' 'libmodplug' 'libass'
         'xcb-util-keysyms' 'ttf-freefont')
makedepends=('live-media' 'libnotify' 'libbluray' 'flac' 'fluidsynth'
             'libdc1394' 'libavc1394' 'lirc-utils' 'libcaca' 'librsvg'
             'libgme' 'xosd' 'twolame' 'aalib'
             'libmtp' 'libdvdcss' 'libtar' 'vcdimager' 'ncurses' 'git')
optdepends=('libnotify: for notification plugin'
            'ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libdc1394: for IEEE 1394 plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'kdelibs: KDE Solid hardware integration'
            'vdpau-video: vdpau back-end for Nvidia'
            'libva-driver-intel: back-end for Intel cards'
            'xvba-video: backend for Ati/AMD cards'
            'libbluray: for Blu-Ray support'
            'flac: for Free Lossless Audio Codec plugin'
            'twolame: for TwoLAME mpeg2 encoder plugin'
            'libcaca: for colored ASCII art video output'
            'libgme: for libgme plugin'
            'librsvg: for SVG plugin'
            'gnome-vfs: GNOME Virtual File System support'
            'vcdimager: navigate VCD with libvcdinfo'
            'xosd: for xosd support'
            'aalib: for ASCII art plugin'
            'libmtp: for MTP devices support'
            'fluidsynth: for synthesizer MIDI FluidSynth'
            'smbclient: for SMB access plugin')

backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool' '!emptydirs')

conflicts='vlc'
provides='vlc'
install='vlc-git.install'

source=()
md5sums=()

_gitroot='git://git.videolan.org/vlc.git'
_gitname='vlc'
_buildir=${_gitname}-build

build() {
	cd ${srcdir}

	msg 'Connecting to GIT server...'

	if [ -d ${_gitname} ]; then
		cd ${_gitname} && git pull origin
		cd ..
	else
		git clone ${_gitroot}
	fi

	msg 'GIT checkout done or server timeout.'

	if [ -d ${_buildir} ]; then
		msg 'Cleaning previous build...'
		rm -rf ${_buildir}
	fi

	git clone ${_gitname} ${_buildir}
	cd ${_buildir}
	
	msg 'Generating necessary files...'
	
	sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c
	./bootstrap

	msg 'Generating done.'
	msg 'Configuring VLC...'

	./configure --prefix=/usr \
		--disable-rpath \
		--enable-pvr \
		--enable-faad \
		--enable-lirc \
		--enable-ncurses \
		--enable-realrtsp \
		--enable-dca \
		--enable-merge-ffmpeg \
		--enable-nls \
		--enable-dbus-control \
		--enable-sftp \
		--enable-xosd \
          --enable-aa \
          --enable-vcdx

	CFLAGS="$CFLAGS -fPIC"

	msg 'Starting make...'

	make || return 1
	make DESTDIR=${pkgdir} install || return 1

	rm -rf ${srcdir}/${_buildir}
}
