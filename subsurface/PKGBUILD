# Maintainer: Francois Boulogne <fboulogne at april dot org>
pkgname=subsurface
pkgver=3.1.1
pkgrel=1
pkgdesc="A divelog software written by Linus"
arch=('i686' 'x86_64')
url="http://subsurface.hohndel.org/"
license=('GPL2')
depends=('libdivecomputer>=0.4.1' 'libxml2' 'gtk2' 'glib2' 'gconf' 'hicolor-icon-theme' 'osm-gps-map' 'libzip' 'libsoup')
optdepends=()
makedepends=('make')
install=subsurface.install
provides=()
conflicts=()
source=("http://subsurface.hohndel.org/downloads/Subsurface-${pkgver}.tgz")
md5sums=('8576733d3fa0f1b546fe24c777c199a4')


build() {
  cd "$srcdir/Subsurface-${pkgver}"
  #cd "$srcdir"

  #Prevent pacman/namcap warning
  sed -i -e "s|\$(INSTALL) -d -m 644 \$(MANDIR)|\$(INSTALL) -d -m 755 \$(MANDIR)|" Makefile
  #gtk_update is handle by the PKGBUILD
  sed -i -e "s|\$(gtk_update_icon_cache)| |" Makefile

  make
}

package() {
  cd "$srcdir/Subsurface-${pkgver}"
  #cd "$srcdir"
  make DESTDIR="${pkgdir}" install
} 

# vim:set ts=2 sw=2 et:
