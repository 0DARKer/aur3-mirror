# Maintainer: DerFlob <derflob at derflob dot de>

pkgname=tilda-git
pkgver=20120531
pkgrel=1
pkgdesc="Linux terminal based on classic terminals from first person shooter games"
arch=('i686' 'x86_64')
url="https://github.com/tsloughter/tilda"
license=('GPL')
depends=('vte' 'confuse' 'libglade')
makedepends=('git' 'gawk')
provides=('tilda')
conflicts=('tilda')
source=(fix.patch)
md5sums=('ce869d9401a0d2993878a6b3739fdecf')
sha256sums=('6e8605d85925e5b00040aea85bb99ab77eeb8278c38e40ae4f966ab962ac56c3')

_gitroot='git://github.com/tsloughter/tilda.git'
_gitname='tilda'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -Np1 -i "${srcdir}/fix.patch"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
