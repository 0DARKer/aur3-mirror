pkgname=sdl-fullscreen-toggle
pkgver=1.2.15
pkgrel=1
pkgdesc='Simple DirectMedia Layer'
arch=(i686 x86_64)
url=http://home.roadrunner.com/~nwmovies/
license=(LGPL2.1)
depends=(sh)
provides=(sdl=$pkgver)
conflicts=(sdl)
options=(staticlibs)
source=(
${url}fullscreen-toggle-1211.patch
http://libsdl.org/release/SDL-$pkgver.tar.gz
http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/media-libs/libsdl/files/libsdl-$pkgver-const-xdata32.patch
http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/media-libs/libsdl/files/libsdl-$pkgver-gamma.patch
http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/media-libs/libsdl/files/libsdl-$pkgver-joystick.patch
http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/media-libs/libsdl/files/libsdl-$pkgver-resizing.patch
http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/media-libs/libsdl/files/libsdl-$pkgver-sdl-config.patch
)
sha256sums=(9fb14803530a966507da61ce9f6a462d03ad14e05d5793a4657e68f6543aa405
            d6d316a793e5e348155f0dd93b979798933fb98aa1edebcc108829d6474aad00
            b0560f37c8927d00cfb034580f15d08ebfd31bca38f470761957c02590b87f9e
            bdf3983a5854a9f46a9ada06dccc62b743418a7a770fd6614797c37d438a625c
            fa912a36cf6a0926e91a34efa756d3514bb42c1de834f416a52c4fa3ba777dd3
            0e4c2c4a11dbf611e317be4e866f99bde619eb1ca3942524e96b7ef94d8180f4
            1c73cc815ae4f6fdaee3dc899a9f38e9f29f88e3997070c9fba226b36dbed015)

_confopts=(
  --disable-rpath
  --disable-static
  --prefix=/usr
  --with-x
)

prepare() {
  cd SDL-$pkgver

  patch -Np1 < $srcdir/fullscreen-toggle-1211.patch

  patch -Np0 < $srcdir/libsdl-$pkgver-sdl-config.patch
  patch -Np1 < $srcdir/libsdl-$pkgver-resizing.patch
  patch -Np0 < $srcdir/libsdl-$pkgver-joystick.patch
  patch -Np1 < $srcdir/libsdl-$pkgver-gamma.patch
  patch -Np1 < $srcdir/libsdl-$pkgver-const-xdata32.patch

  ./autogen.sh
}

build() {
  cd SDL-$pkgver

  ./configure ${_confopts[@]}

  make
}

package() {
  cd SDL-$pkgver

  make DESTDIR=$pkgdir install

  install -d $pkgdir/usr/share/doc/SDL/{html,images}
  install -m644 docs/html/* $pkgdir/usr/share/doc/SDL/html
  install -m644 \
    {Borland,docs/index,VisualC}.html BUGS COPYING CREDITS INSTALL README* TODO WhatsNew \
    $pkgdir/usr/share/doc/SDL
  sed 's/docs\///g' docs.html > $pkgdir/usr/share/doc/SDL/docs.html
  install -m644 docs/images/* $pkgdir/usr/share/doc/SDL/images
}
