# Maintainer: Malte Wessel <muunleit AT lavabit DOT com>

pkgname=atomzombiesmasher
pkgver=1.62
pkgrel=1
pkgdesc="RTS, evacuating civilians out of zombie-contaminated cities. (Commercial)"
arch=('any')
url="http://blendogames.com/atomzombiesmasher/"
license=("custom:commercial")
conflicts=("atomzombiesmasherdemo")
source=("http://blendogames.com/files/atomzombiepatch_v${pkgver//./_}_linux.tar")
md5sums=('afe99d029aac1071363a59423a4f181a')

# If you get another file from Blendo Games, please change it here
_gamepkg="${pkgname}_v1_23.tar.gz"
_gamemd5="3286a655fd6a34cf376d69a185b5e53f"


build() {
	# Check if game-archive is in build directory, than validate
	cd $startdir
	if [[ ! -f $_gamepkg ]]; then
		msg "!! AtomZombieSmasher is a commercial game."
		msg "!! You need a full copy of this game in order to install it."
		msg "!! Please copy $_gamepkg to $startdir " && return 1
	else
		if [[ "$(md5sum $_gamepkg | awk '{print $1}')" == "$_gamemd5" ]]; then
			msg "Check: $_gamepkg is the correct."
		else
			msg "!! Check: $_gamepkg didn't match md5sum. Exiting!" && return 1
		fi
	fi

	# Extract game-archive
	msg "Extracting archive..."
	tar xaf $startdir/$_gamepkg -C $srcdir 

	cd $srcdir/$pkgname
	sed -i "s/\.\/data/\/opt\/$pkgname\/data/g" AtomZombieSmasher

	# Copy from patch to game
	msg "Patching to version $pkgver ..."
	cp -a $srcdir/data $srcdir/$pkgname/
	cp -a $srcdir/readme.htm $srcdir/$pkgname/
}

package() {
	# Create pkgdir folders
	install -d $pkgdir/usr/bin
	install -d $pkgdir/usr/share/doc/$pkgname
	install -g games -d $pkgdir/opt/$pkgname

	# Copy game
	cp -a $srcdir/$pkgname/* $pkgdir/opt/$pkgname
	mv $pkgdir/opt/$pkgname/readme.htm $pkgdir/usr/share/doc/$pkgname
		
	# executable link
	ln -s /opt/$pkgname/AtomZombieSmasher $pkgdir/usr/bin/$pkgname
}

# vim:set ts=2 sw=2 et:
