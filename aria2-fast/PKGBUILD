# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=aria2-fast
pkgver=1.15.1
pkgrel=1
pkgdesc='Aria2 Download utility with little patch to by-pass some limit such as min-split-size'
arch=('i686' 'x86_64')
url='http://aria2.sourceforge.net/'
license=('GPL')
depends=('gnutls' 'libxml2' 'sqlite' 'c-ares' 'ca-certificates')
checkdepends=('cppunit')
conflicts=('aria2')
source=("http://downloads.sourceforge.net/aria2/aria2-${pkgver}.tar.bz2" "aria2.patch")

build() {
  cd aria2-${pkgver}

  patch -Np1 < ../aria2.patch

  ./configure \
    --prefix=/usr \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

  make -j4
}

check() {
  cd aria2-${pkgver}

  make check
}

package() {
  cd aria2-${pkgver}

  make DESTDIR=${pkgdir} install

  # add bash completion (aria2 automatically installs to a temporary target directory)
  install -d ${pkgdir}/usr/share/bash-completion/completions
  install -m644 ${pkgdir}/usr/share/doc/aria2/bash_completion/aria2c \
    ${pkgdir}/usr/share/bash-completion/completions
  rm -rf ${pkgdir}/usr/share/doc/aria2/bash_completion
}

md5sums=('a749a1f7cfe03a7518630d2310267c42'
         '3dbbb8e0c5196b4605a1a9a824d7628c')
