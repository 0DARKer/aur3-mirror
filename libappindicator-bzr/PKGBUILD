# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: willemw <willemw12@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libappindicator-bzr
_bzrname=libappindicator
pkgver=270
pkgrel=3
pkgdesc="Library to allow applications to export a menu into the Unity Menu bar (GTK+ 3 library)"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
depends=('libdbusmenu-gtk3' 'libindicator-bzr')
makedepends=('bzr' 'dbus-glib' 'gobject-introspection' 'gtk-sharp-2' 'perl-xml-libxml' 'pygtk' 'vala')
provides=('libappindicator' 'libappindicator3')
options=('!libtool')
source=("$_bzrname::bzr+http://bazaar.launchpad.net/~indicator-applet-developers/$_bzrname/trunk.14.04/")
md5sums=('SKIP')

pkgver() {
  cd $_bzrname
  bzr revno
}

build() {
  cd "$srcdir"/$_bzrname
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static --with-gtk=3
  make -j1
}

package(){
  cd "$srcdir"/$_bzrname
  make -C src DESTDIR="$pkgdir/" install
  make -C bindings/vala DESTDIR="$pkgdir/" install
}

