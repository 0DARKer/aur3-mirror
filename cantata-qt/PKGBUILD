# Maintainer: Spyros Stathopoulos <foucault.online at gmail dot com>
# Contributor: Ulrich Keller <uhkeller at gmail dot com>
# Contributor: James Thomas <dairy_water at yahoo dot co dot uk>
# Contributor: Anton Larionov <diffident dot cat at gmail dot com>

_pkgname=cantata
pkgname="${_pkgname}-qt"
pkgver=1.3.0.1
pkgrel=1
pkgdesc="QT4 client for the music player daemon (MPD) (without kdelibs)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/cantata/"
license=('GPL')
depends=('qtwebkit' 'taglib-extras' 'ffmpeg' 'mpg123' 'phonon' 'libcddb'
'libmusicbrainz5' 'libmtp')
optdepends=('libmtp: to support MTP devices'
'oxygen-icons: to fill in otherwise missing icons')
makedepends=('cmake' 'automoc4')
provides=('cantata')
conflicts=('cantata')
install=${pkgname}.install
source=("https://drive.google.com/uc?export=download&id=0Bzghs6gQWi60czZxNXJtdWllc0E")
sha1sums=('2c72d5fa7c63cd9f9e85edf2aa24dfaa88c064fb')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_KDE=NO \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_PREFIX_PATH=/usr/lib/qt4
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  make DESTDIR=${pkgdir} install
}

# vim:ts=2:sw=2:et
