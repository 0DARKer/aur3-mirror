# Maintainer: Vitaly Bogdanov <vsbogd at gmail dot com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=bubbros
pkgver=1.6.2
pkgrel=1
pkgdesc="Bubble Bobble clone for up to 10 players"
arch=('i686' 'x86_64')
license=('MIT')
url="http://bub-n-bros.sourceforge.net"
depends=('python2' 'patch')
source=(http://downloads.sourceforge.net/bub-n-bros/$pkgname-$pkgver.tar.gz
        bub-n-bros-1.6-home.patch
        bub-n-bros-1.6-python25.patch
        bub-n-bros-1.6-python2.patch
        bubnbros.desktop)
install=bubnbros.install
md5sums=('2249e5c71033029016037f6b3d695326'
         'f37690393ccd1779902f4328b5e0e500'
         '2168c2dd263ef3d94d80a34d87cba496'
         '904fa364689717ea84cf31814c77b928'
         'f3b91347f8a47838bb389a6badcac7d7')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i ../bub-n-bros-1.6-python2.patch
#   patch -Np1 -i ../bub-n-bros-1.6-python25.patch
  patch -Np0 -i ../bub-n-bros-1.6-home.patch

  cd $srcdir/$pkgname-$pkgver/bubbob
  python2 setup.py build_ext -i || return 1
  cd $srcdir/$pkgname-$pkgver/display
  python2 setup.py build_ext -i || return 1
  cd $srcdir/$pkgname-$pkgver/bubbob/images
  python2 buildcolors.py || return 1

}

package () {
  cd $srcdir/$pkgname-$pkgver/
  install -D -m755 BubBob.py $pkgdir/usr/share/bubbros/BubBob.py
  cp -r {bubbob,common,display,java,http2,metaserver} $pkgdir/usr/share/bubbros
  
  install -d $pkgdir/usr/bin
  ln -s /usr/share/bubbros/BubBob.py $pkgdir/usr/bin/bubnbros

  install -d $pkgdir/usr/share/{applications,pixmaps}
  install -m644 $srcdir/bubnbros.desktop \
    $pkgdir/usr/share/applications/
  install -m644 $srcdir/$pkgname-$pkgver/http2/data/bob.png \
    $pkgdir/usr/share/pixmaps/bubnbros.png
}

