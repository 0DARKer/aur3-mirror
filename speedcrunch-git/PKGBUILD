# Contributor: Timur Antipin < kosmocap (at) gmail.ru >

pkgname=speedcrunch-git
pkgver=20100304
pkgrel=1
pkgdesc="A simple but powerful calculator using QT"
arch=('i686' 'x86_64')
url="http://speedcrunch.org/"
license=('GPL2')
depends=('qt>=4.3.0')
makedepends=('make' 'cmake' 'git')
conflicts=('speedcrunch' 'speedcrunch-alpha')

_gitroot="git://gitorious.org/speedcrunch/mainline.git"
_gitname="mainline"

source=()
md5sums=()

build() {
  cd ${srcdir}
  msg "Connecting to GIT server...."

  if [ -d "${srcdir}/${_gitname}" ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build/src"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
