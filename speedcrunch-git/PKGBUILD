pkgname=speedcrunch-git
pkgver=20130329
pkgrel=1
pkgdesc="A simple but powerful calculator using Qt"
arch=('i686' 'x86_64')
url="http://speedcrunch.org/"
license=('GPL2')
depends=('qt4')
makedepends=('git' 'cmake' 'python2')
conflicts=('speedcrunch' 'speedcrunch-alpha')
provides=('speedcrunch')

_gitroot="https://github.com/speedcrunch/SpeedCrunch.git"
_gitname="speedcrunch"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."
  if [ -d "$_gitname" ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth=1 $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"

  cd "$srcdir"
  rm -rf $_gitname-build && cp -r $_gitname $_gitname-build
  mkdir -p $_gitname-build/build && cd $_gitname-build/build

  # force link with libX11, otherwise build fails
  sed -e 's|(${PROGNAME} ${QT_LIBRARIES})|(${PROGNAME} ${QT_LIBRARIES} X11)|' \
      -i ../src/CMakeLists.txt

  msg "Starting make..."
  cmake ../src -DCMAKE_INSTALL_PREFIX=/usr -DQT_QMAKE_EXECUTABLE=qmake4
  make
  
  # fix make install issue
  lrelease-qt4 ../src/speedcrunch.pro

  # need to create book manually
  msg "Creating book..."
  cd "$srcdir"/$_gitname-build/src/book
  sed -e 's|"python |"python2 |g' -i make.sh
  bash make.sh &> "$srcdir"/$_gitname-build/src/book/make.sh.log
}

package() {
  cd $_gitname-build/build
  make DESTDIR="$pkgdir" install
}
