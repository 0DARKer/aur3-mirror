# Contributor: Timur Antipin < kosmocap (at) gmail.ru >

pkgname=speedcrunch-git
pkgver=20120203
pkgrel=1
pkgdesc="A simple but powerful calculator using QT"
arch=('i686' 'x86_64')
url="http://speedcrunch.org/"
license=('GPL2')
depends=('qt>=4.6.0')
makedepends=('cmake' 'python2')
conflicts=('speedcrunch' 'speedcrunch-alpha')
provides=('speedcrunch')

_gitroot="git://git.gitorious.org/speedcrunch/mainline.git"
_gitname="speedcrunch"


build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d "$srcdir/$_gitname" ] ; then
        (cd $_gitname && git pull origin)
        msg "The local files are updated."
    else
        git clone --depth=1 $_gitroot $_gitname
    fi
    msg "GIT checkout done or server timeout"

    rm -rf $_gitname-build && cp -r $_gitname $_gitname-build
    mkdir -p $_gitname-build/build && cd $_gitname-build/build

    # force link with libX11 - build fails otherwise
    sed -e 's|(${PROGNAME} ${QT_LIBRARIES})|(${PROGNAME} ${QT_LIBRARIES} X11)|g' \
        -i ../src/CMakeLists.txt

    msg "Starting make..."
    cmake ../src -DCMAKE_INSTALL_PREFIX=/usr
    make
    
    # need to create books manually
    cd "$srcdir"/$_gitname-build/src/books
    sed -e 's|"python |"python2 |g' -i make.sh
    /bin/bash make.sh

}

package(){
    cd "$srcdir/${_gitname}-build/build"
    make DESTDIR="$pkgdir" install
}

# vim: set et sw=4 ts=4 sts=4:
