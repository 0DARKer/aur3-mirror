# marc.marrash@gmail.com
# Check-out the latest nightly's here: http://developer.songbirdnest.com/builds/trunk/latest/
# If you do so change songbird-launcher.sh, songbird.desktop and md5sums

pkgname=songbird-nightly-bin
pkgver=1.7.0a
_pkgver=1461
pkgrel=2
pkgdesc="Digital desktop media player mashed-up with web browser, powered by Mozilla (latest official nightly build binary)."
url="http://www.songbirdnest.com/"
license=('GPL2')
arch=('i686' 'x86_64')
install=songbird.install
depends=('libxmu' 'gstreamer0.10-base' 'gstreamer0.10-good-plugins' 'gstreamer0.10-python')
optdepends=('gstreamer0.10-bad-plugins' 'gstreamer0.10-ugly-plugins')
conflicts=('songbird-nightly' 'songbird-svn')
provides=('songbird')
source=(http://developer.songbirdnest.com/builds/trunk/$_pkgver/Songbird_$pkgver-"$_pkgver"_linux-$CARCH.tar.gz
        songbird.desktop 
        songbird-launcher.sh) 
md5sums=('7b1a284107f9569320b3f052cdb34ca6')
[ "$CARCH" == "x86_64" ] && md5sums=('2a970199c96495c155162b56e29a58d6')
md5sums=(${md5sums[*]} 
         'e8e6327f94596e02b474d06109943a70'
         'afc2ad4ad4954e98af3703a1a07cd574')


build() {
    cd $srcdir

    # Create directory and copy everything
    install -d $pkgdir/opt/songbird-nightly
    cp -a $srcdir/Songbird_build-$_pkgver $pkgdir/opt/songbird-nightly || return 1

    # Create some chrome.manifest
    touch $pkgdir/opt/songbird-nightly/Songbird_build-$_pkgver/extensions/albumart@songbirdnest.com/chrome.manifest
    touch $pkgdir/opt/songbird-nightly/Songbird_build-$_pkgver/extensions/{f13b157f-b174-47e7-a34d-4815ddfdfeb8}/chrome.manifest
    touch $pkgdir/opt/songbird-nightly/Songbird_build-$_pkgver/extensions/gonzo@songbirdnest.com/chrome.manifest
    touch $pkgdir/opt/songbird-nightly/Songbird_build-$_pkgver/extensions/inspector@mozilla.org/chrome.manifest
    touch $pkgdir/opt/songbird-nightly/Songbird_build-$_pkgver/extensions/purplerain@songbirdnest.com/chrome.manifest

    # Fix permissions
    find $pkgdir/opt/songbird-nightly/Songbird_build-$_pkgver -type d -exec chmod 755 {} +
    find $pkgdir/opt/songbird-nightly/Songbird_build-$_pkgver -type f -exec chmod 644 {} +
    chmod 755 $pkgdir/opt/songbird-nightly/Songbird_build-$_pkgver/songbird{,-bin}
    chmod 755 $pkgdir/opt/songbird-nightly/Songbird_build-$_pkgver/xulrunner/xulrunner{,-bin}

    # Install bin, icon and desktop
    install -Dm 755 $srcdir/songbird-launcher.sh $pkgdir/usr/bin/songbird
    install -Dm 644 $srcdir/Songbird_build-$_pkgver/chrome/icons/default/default.xpm \
        $pkgdir/usr/share/pixmaps/Songbird.xpm
    install -Dm 644 $srcdir/songbird.desktop \
        $pkgdir/usr/share/applications/Songbird.desktop
}
