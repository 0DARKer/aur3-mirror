# Maintainer: Steven Hiscocks <steven [at] hiscocks [dot] me [dot] uk>
# Contributor:  Andre Wayand <aur-sogo@awayand.sleepmail.com>
pkgname=sogo
pkgver=2.0.3a
pkgrel=1
pkgdesc="groupware server built around OpenGroupware.org (OGo) and the SOPE application server"
arch=('i686' 'x86_64')
url="http://www.sogo.nu/"
license=('GPL')
depends=('sope=2.0.3a' 'gnustep-base' 'libmemcached' 'memcached')
makedepends=('gcc-objc')
options=()
optdepends=(
	'postgresql: run database server for sogo locally'
	'mysql: run database server for sogo locally'
	'openldap: run directory server for sogo locally'
	'postfix: run smtp server for sogo locally'
	'dovecot: run imap server for sogo locally'
	'courier-imap: run imap server for sogo locally'
	'nginx: webserver to provide web interface locally'
	'apache: webserver to provide web interface locally'
	'lighttpd: webserver to provide web interface locally'
	'funambol: sync mobile devices with sogo contacts, events, tasks via SyncML'
)
backup=(etc/conf.d/sogo etc/logrotate.d/sogo)
install=sogo.install
source=(
  http://www.sogo.nu/files/downloads/SOGo/Sources/SOGo-${pkgver}.tar.gz
  sogo_configure.patch
  sogo.conf.d
  sogo.logrotate.d
  sogo.rc.d
  sogo.service
  sogo.tmpfiles
)
md5sums=('549fd8a0cec2c529b5b84c6c19c1d202'
         '5fbd694ee94639697c5f635013d89327'
         '50e67376e9c1af5ea91947cc0542c997'
         '6e9809e4373a6cc674c286ba8d24571c'
         '04188562938eae8a9b0141bae0bfc4da'
         '2f54776402c5419c79e6687b75545d53'
         '04dc24e79e11b62933843cd1f9f60792')

build() {
  cd "$srcdir/SOGo-${pkgver}"
  patch configure ../sogo_configure.patch
  ./configure --prefix=$(gnustep-config --variable=GNUSTEP_SYSTEM_ROOT)
  make
}

package() {
  cd "${srcdir}/SOGo-${pkgver}"
  make install DESTDIR="${pkgdir}"
  install -D -m 0644 "${srcdir}"/sogo.conf.d "${pkgdir}"/etc/conf.d/sogo
  install -D -m 0644 "${srcdir}"/sogo.logrotate.d "${pkgdir}"/etc/logrotate.d/sogo
  install -D -m 0755 "${srcdir}"/sogo.rc.d "${pkgdir}"/etc/rc.d/sogo
  install -D -m 0644 "${srcdir}"/sogo.service "${pkgdir}"/usr/lib/systemd/system/sogo.service
  install -D -m 0644 "${srcdir}"/sogo.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/sogo.conf
  install -d "${pkgdir}"/var/log/sogo
  install -d "${pkgdir}"/etc/sogo
}
