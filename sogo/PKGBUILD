# Maintainer: Steven Hiscocks <steven [at] hiscocks [dot] me [dot] uk>
# Contributor:  Andre Wayand <aur-sogo@awayand.sleepmail.com>
pkgname=sogo
pkgver=2.0.7
pkgrel=2
pkgdesc="groupware server built around OpenGroupware.org (OGo) and the SOPE application server"
arch=('i686' 'x86_64')
url="http://www.sogo.nu/"
license=('GPL')
depends=('sope=2.0.7' 'gnustep-base' 'libmemcached' 'memcached')
makedepends=('gcc-objc')
options=('!strip')
optdepends=(
	'postgresql: run database server for sogo locally'
	'mariadb: run database server for sogo locally'
	'openldap: run directory server for sogo locally'
	'postfix: run smtp server for sogo locally'
	'dovecot: run imap server for sogo locally'
	'courier-imap: run imap server for sogo locally'
	'nginx: webserver to provide web interface locally'
	'apache: webserver to provide web interface locally'
	'lighttpd: webserver to provide web interface locally'
	'funambol: sync mobile devices with sogo contacts, events, tasks via SyncML'
)
backup=(etc/logrotate.d/sogo etc/sogo/sogo.conf)
install=sogo.install
source=(
  http://www.sogo.nu/files/downloads/SOGo/Sources/SOGo-${pkgver}.tar.gz
  sogo_configure.patch
  sogo.logrotate.d
  sogo.service
  sogo.tmpfiles
)
md5sums=('c666706e006a1b4312789cddce5da2ee'
         '5fbd694ee94639697c5f635013d89327'
         '6e9809e4373a6cc674c286ba8d24571c'
         'c037d83ac8f7632a7de0d7e3ffb19bea'
         '04dc24e79e11b62933843cd1f9f60792')

prepare() {
  cd "$srcdir/SOGo-${pkgver}"
  patch configure ../sogo_configure.patch
}

build() {
  cd "$srcdir/SOGo-${pkgver}"
  ./configure --prefix=$(gnustep-config --variable=GNUSTEP_SYSTEM_ROOT) --disable-debug
  make
}

package() {
  cd "${srcdir}/SOGo-${pkgver}"
  make install DESTDIR="${pkgdir}" GNUSTEP_SYSTEM_ADMIN_TOOLS="/usr/bin"
  install -D -m 0644 "${srcdir}"/sogo.logrotate.d "${pkgdir}"/etc/logrotate.d/sogo
  install -D -m 0644 "${srcdir}"/sogo.service "${pkgdir}"/usr/lib/systemd/system/sogo.service
  install -D -m 0644 "${srcdir}"/sogo.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/sogo.conf
  install -D -m 0600 "${srcdir}"/SOGo-${pkgver}/Scripts/sogo.conf "${pkgdir}"/etc/sogo/sogo.conf
  install -d "${pkgdir}"/var/log/sogo
}
