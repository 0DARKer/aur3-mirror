# Contributor: Carlo Casta <carlo.casta at gmail dot com>

pkgname=openflipper
pkgver=1.0.2
_pkgname=OpenFlipper
pkgrel=1
pkgdesc="An Open Source Geometry Processing and Rendering Framework"
arch=('i686' 'x86_64')
url="http://www.openflipper.org"
license=('LGPL')
depends=('qt' 'glut' 'glew')
makedepends=('cmake')
optdepends=('freetype2' 'ftgl')
provides=(openmesh=2.0)
conflicts=(openmesh)
source=(http://www.openflipper.org/fileadmin/openflipper-files/${pkgver}/${_pkgname}-${pkgver}.tar.bz2
        openflipper.desktop)
md5sums=('fe6ba06777a86889021e6b5c43db16ec'
         '514fbb8d8f51bdc000e1bac884f21287')

build()
{
	cd "$srcdir/$_pkgname-$pkgver"

	sed -i -e "s/acg_qt4 (REQUIRED 4.5)/acg_qt4 (REQUIRED)/g" CMakeLists.txt || return 1

	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr        \
          -DDISABLE_PLUGIN_TEXTURECONTROL=On \
	      -DCMAKE_BUILD_TYPE=Release  ..

	make
}

package()
{
	cd "${srcdir}/${_pkgname}-${pkgver}/build"
	make DESTDIR=$pkgdir install || return 1

	# move openmesh libs in /usr/lib instead of /usr/lib/OpenFlipper
	mv Build/lib/OpenFlipper/libOpenMesh{Core,Tools}.{so*,a} $pkgdir/usr/lib/
	mv Build/lib/OpenFlipper/libACG.so $pkgdir/usr/lib/

	# install .desktop file
	install -d $pkgdir/usr/share/applications || return 1
	install $srcdir/openflipper.desktop $pkgdir/usr/share/applications/ || return 1
}
