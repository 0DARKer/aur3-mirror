# Maintainer: Sean Greenslade <sean at seangreenslade dot com>
# Contributor: jsteel <mail at jsteel dot org>
# Contributor: andreas_baumann <abaumann at yahoo dot com>

pkgname=mutt-slang
_pkgname=mutt
provides=mutt
conflicts=mutt
pkgver=1.5.23
pkgrel=1
pkgdesc='Small but very powerful text-based mail client'
url='http://www.mutt.org'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gpgme' 'slang' 'libidn' 'krb5')
backup=('etc/Muttrc')
install=$pkgname.install
source=(https://bitbucket.org/$_pkgname/$_pkgname/downloads/$_pkgname-$pkgver.tar.gz)
sha1sums=('8ac821d8b1e25504a31bf5fda9c08d93a4acc862')

build() {
  cd "$srcdir"/$_pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --enable-gpgme --enable-pop \
    --enable-imap --enable-smtp --enable-hcache --with-slang=/usr \
    --with-regex --with-gss=/usr --with-ssl=/usr --with-sasl --with-idn

  make
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/bin/{flea,muttbug}
  rm "$pkgdir"/usr/share/man/man1/{flea,muttbug}.1
  rm "$pkgdir"/etc/mime.types{,.dist}

  install -Dm644 contrib/gpg.rc "$pkgdir"/etc/Muttrc.gpg.dist
}
