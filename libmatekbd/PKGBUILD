pkgname=libmatekbd
pkgver=2011.10.29
pkgrel=2
pkgdesc="MATE keyboard library"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libxklavier' 'mate-conf') #  'dconf'
makedepends=('intltool' 'pkgconfig')
groups=('mate')
url="http://matsusoft.com.ar/projects"
options=('!emptydirs' '!libtool')
install=libmatekbd.install
source=(http://sourceforge.net/projects/matede/files/${pkgver}/${pkgname}.tar.gz/download)
sha256sums=('6e03df86918647fa57f4fe27163f812879815b629309ef709e4d80f3cb9c1134')

build() {
	cd "${srcdir}/${pkgname}"
	./configure --prefix=/usr --sysconfdir=/etc --disable-static || return 1
	make || return 1
}

package() {
	cd "${srcdir}/${pkgname}"
	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
