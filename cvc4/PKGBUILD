
# Contributor: Philipp Sieweck

pkgname=cvc4
pkgver=1.3
pkgrel=1
pkgdesc="CVC4: An automatic theorem prover"
arch=(i686 x86_64)
url="http://cvc4.cs.nyu.edu/web/"
depends=('libantlr3c' 'gmp')
optdepends=('boost: for running cvc4 in portfolio mode'
            'readline: for an improved interaction support'
            # 'glpk' -- incompatible :(
            'cln: Alternative to gmp; can offer mor performance')
license=('custom')
source=("http://cvc4.cs.nyu.edu/builds/src/cvc4-$pkgver.tar.gz")
md5sums=('a8c2bf10b7fa581a8de283072f4137b6')
options=(!libtool)

build() {
    cd "$srcdir"/cvc4-$pkgver
    if [[ -r /usr/lib/libcln.so ]]; then WITH_CLN="--with-cln"; fi
    #if [[ -r /usr/lib/libglpk.so ]]; then WITH_GLPK="--with-glpk"; fi
    ./configure --prefix=/usr --enable-optimization --disable-doxygen-doc $WITH_CLN $WITH_GLPK
    make
}

package() {
    cd "$srcdir"/cvc4-$pkgver
    make prefix="$pkgdir"/usr install
    install -D COPYING "$pkgdir"/usr/share/licenses/cvc4/LICENCE

    dirs=("usr/share/perl5" "usr/lib/pyshared" "usr/lib/php" "usr/lib/jni"
          "usr/lib/csharp" "usr/share/java" "usr/share/php" "usr/share/csharp"
          "usr/lib/ruby" "usr/lib/tcltk" "usr/lib/perl5" "usr/share/pyshared"
          "usr/lib/ocaml/cvc4" "usr/lib/ocaml")

    for d in "${dirs[@]}"; do rmdir "$pkgdir/$d"; done
}
