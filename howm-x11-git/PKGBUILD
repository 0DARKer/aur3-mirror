# Maintainer: Alad Wenter
# base64 -d <<< YWxhZEBhcmNobGludXguaW5mbwo

pkgname=howm-x11-git
_gitname=howm
pkgver=8b026ec
pkgrel=6
pkgdesc='A lightweight, tiling X11 window manager that mimics vi by offering operators, motions and modes.'
arch=('i686' 'x86_64')
url='https://github.com/HarveyHunt/howm'
license=('GPL')
depends=('xcb-util-wm' 'sxhkd' 'cottage-git')
makedepends=('git')
checkdepends=('linux-headers')
provides=('howm-x11')
conflicts=('howm-x11')
options=('docs' '!strip' 'debug')
source=('git+https://github.com/HarveyHunt/howm#branch=develop')
install=howm.install
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git rev-parse --short HEAD
}

build() {
  cd "$_gitname"
  make debug
}

check() {
  cd "$_gitname"
  find /usr/lib/modules -name checkpatch.pl -print -quit | xargs -i cp {} .
  make check
}

package() {
  cd "$_gitname"
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/xsessions" "$pkgdir/usr/share/howm/examples"  

  find "examples/" -type f -exec install -m644 {} "$pkgdir/usr/share/howm/examples" \;
  install -m644 howm.xsession.desktop "$pkgdir/usr/share/xsessions"
  install -m755 "bin/debug/howm" "$pkgdir/usr/bin"
  install -m755 "examples/howmrc" "$pkgdir/usr/share/howm/examples"
}

# vim:set ts=2 sw=2 et:
