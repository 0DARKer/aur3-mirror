# Maintainer: Alad Wenter
# base64 -d <<< YWxhZEBhcmNobGludXguaW5mbwo

pkgbase=howm-x11-git
pkgname=('howm-x11-git' 'cottage-git')
pkgver=8b026ec
pkgrel=4
pkgdesc='A lightweight, tiling X11 window manager that mimics vi by offering operators, motions and modes.'
arch=('i686' 'x86_64')
url='https://github.com/HarveyHunt/howm'
license=('GPL')
makedepends=('git')
options=('docs' '!strip' 'debug')
source=('git+https://github.com/HarveyHunt/howm#branch=develop'
	'git+https://github.com/Harveyhunt/cottage#branch=master'
	'https://raw.githubusercontent.com/torvalds/linux/master/scripts/checkpatch.pl'
	'https://raw.githubusercontent.com/torvalds/linux/master/scripts/spelling.txt')
install=howm.install
sha256sums=('SKIP'
            'SKIP'
            '12f81fca3a770e425fc0226e2bdfad1141391c928dc40ba9e95eb4a42024f9e3'
            '1916fc0139856cc382b37de46375c0a7d3d4c50bd8b954f62eb0f5e2ceefd343')

pkgver() {
  cd "$srcdir/howm"
  git rev-parse --short HEAD
}

build() {
  cd "$srcdir/howm"
  make debug
  
  cd "$srcdir/cottage"
  make debug
}

check() {
  _checkpatch() {
    install -m755 "$srcdir/checkpatch.pl" .
    install -m644 "$srcdir/spelling.txt" .
    make check
  }
  
  cd "$srcdir/howm"
  _checkpatch
  
  cd "$srcdir/cottage"
  _checkpatch
}

package_howm-x11-git() {
  pkgdesc='A lightweight, tiling X11 window manager that mimics vi by offering operators, motions and modes.'
  depends=('xcb-util-wm' 'sxhkd' 'cottage-git')
  provides=('howm-x11')
  conflicts=('howm-x11')

  cd "$srcdir/howm"
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/xsessions" "$pkgdir/usr/share/howm"
  install -m755 "bin/debug/howm" "$pkgdir/usr/bin"
  install -m644 howm.xsession.desktop "$pkgdir/usr/share/xsessions"
  cp -r "examples/" "$pkgdir/usr/share/howm/"
}

package_cottage-git() {
  pkgdesc='Use howm commands, operators and set configuration values through a UNIX socket.'
  provides=('cottage')
  conflicts=('cottage')
  
  cd "$srcdir/cottage"
  mkdir -p "$pkgdir/usr/bin"
  install -m755 "bin/debug/cottage" "$pkgdir/usr/bin"
}

# vim:set ts=2 sw=2 et:
