# Contributor: David Spicer <azleifel at gmail dot com>

pkgname=vdr-trayopenng
_pluginname=trayopenng
pkgver=1.0.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin that opens and closes the CD/DVD tray"
url="http://www.uli-eckhardt.de/vdr/trayopenng.en.shtml"
license=('GPL2')
depends=('vdr>=1.7.42' 'util-linux')
source=(http://www.uli-eckhardt.de/vdr/download/$pkgname-$pkgver.tgz)
md5sums=('21414947ac307e2322536eb5ddfebd5f')

prepare() {
  cd "$srcdir/$_pluginname-$pkgver"

  # Fix locale file installation directory
  sed -i 's:LOCDIR = \$(DESTDIR):LOCDIR = :' Makefile

  # Fix parsing of command-line short options
  sed -i 's|getopt_long(argc, argv, \"d:\",|getopt_long(argc, argv, \"c:d:\",|' trayopenng.cc
}


build() {
  cd "$srcdir/$_pluginname-$pkgver"
  make
}

package() {
  cd "$srcdir/$_pluginname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Install documents
  install -d -m755 "$pkgdir/usr/share/doc/$pkgname"
  for _file in README HISTORY; do
    install -m644 $_file "$pkgdir/usr/share/doc/$pkgname"
  done
}
