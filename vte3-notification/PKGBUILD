# $Id$
# Maintainer: Fernando Fernandez <fernando@softwareperonista.com.ar>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=vte3-notification
pkgver=0.40.0
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with GTK3"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!emptydirs')
makedepends=('intltool' 'gobject-introspection' 'gtk3' 'vala')
depends=('gtk3' 'vte-common')
provides=(vte3=$pkgver)
conflicts=(vte3)
url="http://www.gnome.org"
source=(http://download.gnome.org/sources/vte/${pkgver::4}/vte-$pkgver.tar.xz
        vte291-command-notify.patch)
sha256sums=('54d96181626f2641c78e30ccf991ea124b8713646231e52a973dea3adf02d852'
            'c1d6be7fab5997a6908aac9f5864335293c4c1052847fdf10f406b54aacdc650')

prepare () {
  cd vte-$pkgver

  patch -p1 -i ../vte291-command-notify.patch
}

build() {
  cd "vte-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --enable-gnome-pty-helper
  make
}

package(){
  cd "vte-$pkgver"
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/lib/vte/gnome-pty-helper
  rm "$pkgdir"/etc/profile.d/vte.sh
}
