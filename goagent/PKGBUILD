# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: cuihao <cuihao dot leo at gmail dot com>
# Contributor: Guten <ywzhaifei@gmail.com> 

pkgname=goagent
pkgver=2.0.12
pkgrel=2
pkgdesc="A gae proxy forked from gappproxy/wallproxy"
arch=("i686" "x86_64")
url="http://goagent.googlecode.com"
license=("GPL2")
depends=('python2' 'python2-pyopenssl' 'python2-gevent')
optdepends=(
  'python26: (AUR) upload GAE code using /opt/server/uploader.zip'
)
conflicts=('systemd-goagent-units')
replaces=('systemd-goagent-units')
source=(
  "$pkgname.daemon"
  "$pkgname.service"
  "$pkgname-$pkgver.tar.gz::https://github.com/goagent/goagent/tarball/v$pkgver"
)
backup=('etc/goagent')
install='goagent.install'

build() {
  cd "$srcdir"
  
  rm -rf $pkgname
  mv goagent-goagent-* $pkgname
}

package() {
  cd "$srcdir/$pkgname"

  # python2 fix
  sed -i -re "1s/python2?/python2/" local/*.py
  chmod +x local/proxy.py

  mkdir -p "$pkgdir/opt/goagent"
  cp -r local server "$pkgdir/opt/goagent"
  
  # remove windows-only files
  rm -f "$pkgdir/opt/goagent/"*/*.{vbs,dll,exe,manifest,bat}

  # remove CA.crt CA.key for security issues
  rm -f "$pkgdir/opt/goagent/local/CA.crt" "$pkgdir/opt/goagent/local/CA.key"

  # config file
  install -Dm644 "${pkgdir}/opt/goagent/local/proxy.ini" "${pkgdir}/etc/goagent"
  ln -sf "/etc/goagent" "${pkgdir}/opt/goagent/local/proxy.ini"

  # rc.d daemon
  install -Dm755 "${srcdir}/goagent.daemon" "${pkgdir}/etc/rc.d/goagent"

  # systemd service
  install -Dm644 "${srcdir}/goagent.service" "${pkgdir}/usr/lib/systemd/system/goagent.service"
}

# vim:set ts=2 sw=2 et:
md5sums=('94e66bb9673157bc7fcfc2e7d01be389'
         'a0223e4e436a4d5cc17f76fc1fbbc140'
         'c9f1110a28672dece6eb8d1d81206b1c')
