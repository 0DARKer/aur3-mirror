# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Gadget3000 <gadget3000 at msn dot com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

pkgname=gmail-notify
pkgver=1.6.1.1
pkgrel=3
pkgdesc="A Linux alternative for the notifier program released by Google"
arch=('i686' 'x86_64')
url="http://gmail-notify.sourceforge.net"
license=('GPL')
depends=('python2' 'gtk2' 'pygtk')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname.patch
        pytrayicon.so)
md5sums=('7d62330064b3cc70c674cb721f36975a'
         '1d4d0c39af6f63febc16cac95f0f937e'
         '003fd6111aa2c4924308f79c10ae0f09')

build() {
  cd "$srcdir"/$pkgname

  patch < "$srcdir"/gmail-notify.patch

  # change python to python2
  for i in $(find "$srcdir" | grep "\.py"); do
    sed "s/\#\!\/usr\/bin\/python/\#\!\/usr\/bin\/python2/" -i "$i"
    sed "s/\#\!\/usr\/bin\/env python/\#\!\/usr\/bin\/env python2/g" -i "$i"
  done
}

package() {
  install -dm755 "$pkgdir"/usr/bin
  install -dm755 "$pkgdir"/usr/share/gmail-notify

  cp -r "$srcdir"/$pkgname/*  "$pkgdir"/usr/share/gmail-notify/
  ln -s /usr/share/$pkgname/notifier.py "$pkgdir"/usr/bin/gmail-notify

  if [ "$CARCH" = "x86_64" ]; then
    cp -f "$srcdir"/pytrayicon.so "$pkgdir"/usr/share/gmail-notify/
  fi
}
