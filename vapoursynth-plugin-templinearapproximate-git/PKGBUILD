# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=templinearapproximate
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20131110.839a15f
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://bitbucket.org/mystery_keeper/${_plug}-vapoursynth"
license=('LGPL2.1')
depends=('vapoursynth')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
makedepends=('git')
source=("${_plug}::git+https://bitbucket.org/mystery_keeper/${_plug}-vapoursynth.git")
md5sums=('SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"

  echo "all:
	  gcc -shared -std=gnu99 -fPIC -O2 -march=native -o "${_plug}.so" src/main.c -I/usr/include/vapoursynth" > Makefile
}


build() {
  cd "${_gitname}"

  make
}

package(){
  cd "${_gitname}"
  install -Dm755 "${_plug}.so" "${pkgdir}/usr/lib/Vapoursynth/${_plug}.so"
  install -Dm644 TempLinearApproximate-readme.txt "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
}
