# Contributors: Matt Kemp <matt@mattikus.com>, David Zaragoza <david@zaragoza.com.ve>

pkgname=mod_gnutls
pkgver=0.5.10
pkgrel=3
pkgdesc="Apache module for the GnuTLS library"
arch=('i686' 'x86_64')
url="http://modgnutls.sourceforge.net/"
license=('Apache')
depends=('apache>=2.0.42' 'gnutls>=2.12.6')
install=$pkgname.install
source=(http://modgnutls.sourceforge.net/downloads/$pkgname-$pkgver.tar.bz2
        gnutls-3.0.5.patch)
md5sums=('53fd571080b16333d3a4550b8477bf3c'
         'f42b94c5f49a0a6ba52f1a8dddf74754')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --with-apxs=/usr/sbin/apxs
  patch -p1 -i "${srcdir}/gnutls-3.0.5.patch"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -ir 's/@${APXS_BIN} -i -n gnutls mod_gnutls.so/mkdir -p $(DESTDIR)$(AP_LIBEXECDIR)\n\t@${APXS_BIN} -i -S LIBEXECDIR=$(DESTDIR)$(AP_LIBEXECDIR) -n gnutls mod_gnutls.so/' src/Makefile
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
