# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=mp
pkgver=5.2.2
pkgrel=1
pkgdesc="A text editor for programmers."
arch=(i686 x86_64)
url="http://triptico.com/software/mp.html"
license=('GPL')
makedepends=('gtk3' 'qt')
optdepends=('gtk3: gtk frontend'
            'qt: qt frontend')
options=('!emptydirs')
source=(http://triptico.com/download/$pkgname-$pkgver.tar.gz)
md5sums=('0e0b973d6a02aed2f7b8c33db17c8ab2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i "s|ncursesw/||" config.sh mpv_curses.c

  ./config.sh --prefix=/usr \
              --without-gtk \
              --without-qt4
  make
  cp mp-5 mp-curses
  make clean

  ./config.sh --prefix=/usr \
              --without-curses \
              --without-qt4
  make
  cp mp-5 mp-gtk
  make clean

  ./config.sh --prefix=/usr \
              --without-curses \
              --without-gtk
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir/" install

  mv "$pkgdir/usr/bin/mp-5" "$pkgdir/usr/bin/mp-qt"
  install -Dm755 mp-{curses,gtk} "$pkgdir/usr/bin"
}

# vim:set ts=2 sw=2 et: