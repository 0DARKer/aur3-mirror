# Maintainer: Arthur Zamarin <arthurzam@gmail.com> 

_pkgname=aftp-server
pkgname=${_pkgname}-git
pkgver=v1.0.r4.gab8ea92
pkgrel=1
pkgdesc="Very fast file transfering server"
arch=(i686 x86_64)
url="https://github.com/arthurzam/${pkgname}"
license=('GPL2')
depends=('openssl')
source=("${_pkgname}::git+git://github.com/arthurzam/${_pkgname}.git")
sha1sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}
 
build() {
  cd "${srcdir}/${_pkgname}"
  make
  gzip man.1
}
 
package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="$pkgdir" install
  install -Dm 644 man.1.gz "$pkgdir/usr/share/man/man1/aftp_server.1.gz"
}

