# Maintainer: Frank Vanderham <twelve.eighty (at) gmail.>
# Contributor: Hsin-Yi Chen (hychen) <hycehn (at) canonical.com>
# Contributor: Alberto Milone <alberto.milone (at) canonical.com>
# Contributor: Jasmine Hassan <jasmine.aura (at) gmail.com>
# Contributor: b3niup
# Upstream code branches: https://code.launchpad.net/ubuntu/+source/bcmwl
# Also see: https://bbs.archlinux.org/viewtopic.php?id=145884
pkgname=wireless-bcm43142-dkms
_pkgname=broadcom-wl
pkgver=6.20.155.1
pkgrel=7
pkgdesc="Broadcom 802.11 Linux STA wireless driver BCM43142."
url="https://bbs.archlinux.org/viewtopic.php?id=145884"
arch=('i686' 'x86_64')
license=('custom')
depends=('dkms' 'linux-headers')
conflicts=('wireless-bcm43142-oneiric-dkms')
source=(
	"https://launchpad.net/ubuntu/raring/+source/bcmwl/6.20.155.1+bdcom-0ubuntu6/+files/bcmwl_6.20.155.1+bdcom.orig.tar.gz"
	"https://launchpad.net/ubuntu/raring/+source/bcmwl/6.20.155.1+bdcom-0ubuntu6/+files/bcmwl_6.20.155.1+bdcom-0ubuntu6.diff.gz"
	'wireless-bcm43142-dkms.conf'
	'dkms.conf'
	'001-Arch-Makefile.patch'
	'002-Module-License.patch'
	'003-tx-power-38.patch'
	'004-RefData-38.patch'
	'005-user-ioctl.patch'
	'006-put-bss-39.patch'
)
sha1sums=(
	'0277417c7da443472048bf465b68f4f6128e1422'
	'bea80e1a25d88fd20f6c402745e01ae93a2454fe'
	'a37c5dd9ab279372f0c68595bb2a8f1fe694cd13'
	'83447f2fc56f260394e6fc407b6e9086d3bf8b21'
	'03b453e9a4c719663b222c879fb238f22690eb59'
	'05669db9310026f98d1030413ef5bb482594dd11'
	'6e97985e13c9ccc3e6e9be25187e5fdcfcf20f66'
	'a376ed809f301e2449a929a0358b9f8c4ad81a29'
	'8738daa02b273ff7791d0fb57720eb7bc0b85c63'
	'dd1b70974782d163fb6c37ef928c7026751b1ed8'
)

backup=('etc/modprobe.d/wireless-bcm43142-dkms.conf')
install=install

build() {
	cd "${srcdir}"

	# apply diff patch to create the debian/ folder
	patch -p1 -i bcmwl_6.20.155.1+bdcom-0ubuntu6.diff

	# apply source patches
	# MODULE_LICENSE patch is handled differently to deal with GPL usage
	# patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../debian/patches/0001-MODULE_LICENSE.patch 
	patch -d ./bcmwl-6.20.155.1+bdcom -p1 -i ../debian/patches/0002-Makefile.patch
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../debian/patches/0003-Make-up-for-missing-init_MUTEX.patch
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../debian/patches/0004-Add-support-for-Linux-3.2.patch
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../debian/patches/0005-add-support-for-linux-3.4.0.patch
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../debian/patches/0006-add-support-for-linux-3.8.0.patch
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../debian/patches/0007-nl80211-move-scan-API-to-wdev.patch

	# patch to replace use of dkpg with uname to detect 64 vs. 32 bit
	patch -p1 -i 001-Arch-Makefile.patch

	# patch for MODULE_LICENSE
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../002-Module-License.patch

	# patch for 3.8 kernels change in set_tx_power and get_tx_power
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../003-tx-power-38.patch

	# patch for 3.8 kernels change in __refdata requirements
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../004-RefData-38.patch

	# patch for spurious log messages
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../005-user-ioctl.patch

	# patch for kernel 3.9 put-bss
	patch -d ./bcmwl-6.20.155.1+bdcom/src -p1 -i ../../006-put-bss-39.patch

	# move folders to create Arch specific folder layout
	mv bcmwl-6.20.155.1+bdcom/src/* ./

	# move Makefile to main src/ folder
	mv ./bcmwl-6.20.155.1+bdcom/Makefile ./

	# delete the debian folders and files not needed for packaging
	rm -R debian/
	rm -R bcmwl-6.20.155.1+bdcom/
	rm bcmwl_6.20.155.1+bdcom.orig.tar.gz
	rm bcmwl_6.20.155.1+bdcom-0ubuntu6.diff.gz
	rm bcmwl_6.20.155.1+bdcom-0ubuntu6.diff
	rm BOM.txt

}

package() {
	cd "${srcdir}"
	
	mkdir -p ${pkgdir}/usr/src/${_pkgname}-${pkgver}

	cp -RL * ${pkgdir}/usr/src/${_pkgname}-${pkgver}
	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 wireless-bcm43142-dkms.conf "${pkgdir}"/etc/modprobe.d/wireless-bcm43142-dkms.conf
}
