# Contributor: Jonathan Wiersma <archaur at jonw dot org>
pkgname=eclipse-pdt
_pkgver=3.0.0
_reltype=M
_reldate=201105240530
pkgver=${_pkgver}_${_reltype}${_reldate}
pkgrel=1
pkgdesc="PHP Development Tools for Eclipse"
arch=('i686' 'x86_64')
url="http://www.eclipse.org/pdt/"
license=('EPL')
depends=( 'eclipse-dltk-core-index' 'eclipse-dtp' 'eclipse-wtp' )
optdepends=('eclipse-rse' 'eclipse-pdt-debugger' 'php' 'xdebug' 'php-zend-debugger')
install=eclipse-pdt.install
_mirror="http://www.eclipse.org/downloads/download.php?r=1&file="
source=("$_mirror/tools/pdt/downloads/drops/${_pkgver}/${_reltype}${_reldate}/pdt-SDK-${_reltype}${_reldate}.zip")
md5sums=('f1ec04c3dfc14c7fbd27bb4294178593')

package() {
	cd $srcdir
	install -dm755 $pkgdir/usr/share/eclipse/dropins/${pkgname#eclipse-}/
	find eclipse -type f -exec install -Dm644 {} \
		$pkgdir/usr/share/eclipse/dropins/${pkgname#eclipse-}/{} \;
}

