# Maintainer: Andrey Semisokhin <customs.rnd@gmail.com>
pkgname=gpredict-git
pkgver=v1.3.r242.gb445bee
pkgrel=2
pkgdesc="Gpredict is a real-time satellite tracking and orbit prediction application."
arch=('i686' 'x86_64')
license=('GPL')
depends=('curl>=7.19' 'gtk2>=2.18' 'glib2>=2.22' 'goocanvas1>=0.15')
makedepends=('git' 'intltool')
provides=('gpredict')
conflicts=('gpredict')
url="http://gpredict.oz9aec.net/"
source=('git://gpredict.git.sourceforge.net/gitroot/gpredict/gpredict')
md5sums=('SKIP')

_gitname=gpredict

pkgver() {
	cd "$_gitname"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "$_gitname"
	./autogen.sh
	./configure --prefix=/usr
	make
}

package()
{
	cd "$_gitname"
	make DESTDIR=${pkgdir} install
}

