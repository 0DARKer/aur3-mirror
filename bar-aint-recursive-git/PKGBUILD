# Maintainer: ledti <queryvgmail.com>

pkgname=bar-aint-recursive-git
pkgver=106.3919a00
pkgrel=1
pkgdesc="A lightweight xcb based bar."
arch=('i686' 'x86_64')
url="https://github.com/LemonBoy/bar"
license=('MIT')
depends=('libxcb')
makedepends=('git')
provides=('bar-aint-recurisve')
conflicts=('bar-aint-recursive')
source=('bar::git+https://github.com/LemonBoy/bar.git#branch=master')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/bar"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/bar"
  make
}

package() {
  cd "$srcdir/bar"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  
  # rename binary to avoid conflicting with 'bar' package in community:
  mv "$pkgdir/usr/bin/bar" "$pkgdir/usr/bin/bar-aint-recursive"
}
