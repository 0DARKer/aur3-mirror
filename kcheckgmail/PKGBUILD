# Maintainer: birdflesh <antkoul at gmail dot com>
# Contributor: Carl Mueller  archlinux at carlm e4ward com

pkgname=kcheckgmail
pkgver=0.6.0
pkgrel=3
pkgdesc="System tray application for checking a Gmail account for new messages"
arch=('i686' 'x86_64')
url="http://kcheckgmail.sourceforge.net/"
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
	fix-docbook-build.patch)
md5sums=('12d375119d7048d6649532b0b0cae32b'
	 '0000a3ee650c5e5f1b43e44690d34b30')

build() {
  cd ${srcdir}
  patch -Np0 -i ${srcdir}/fix-docbook-build.patch
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}