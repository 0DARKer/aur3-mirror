# $Id: PKGBUILD 169916 2012-10-30 22:34:57Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Lubosz Sarnecki <lubosz@gmail.com>

_realname=gnome-terminal
pkgname=$_realname-git

pkgver=20130210
_realver=3.7.3
pkgrel=1
pkgdesc="The GNOME Terminal Emulator. Git Version"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gconf' 'vte3' 'gsettings-desktop-schemas' 'libsm')
makedepends=('gnome-doc-utils' 'intltool' 'gtk2' 'itstool' 'docbook-xsl')
options=('!emptydirs')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-terminal.install

provides=($_realname)
conflicts=($_realname)

_gitname=gnome-terminal
_gitroot=git://git.gnome.org/${_gitname}

build() {
    cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$_realname.schemas" --domain gnome-terminal "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
