# Maintainer: Olivier Le Moal <mail at olivierlemoal dot fr>

pkgname=dirb
pkgver=2.21
pkgrel=1
pkgdesc="DIRB is a web content scanner, brute forceing for hidden files"
url="http://dirb.sourceforge.net/"
arch=('i686' 'x86_64')
license=("GPL2")
depends=('curl')
_name="dirb221"
noextract=('dirb221.tar.gz')
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${_name}.tar.gz")
sha512sums=('b2bbca6ed4a59cc4b84f9d07e53d31eac4814c005b4eaca2f98691fc3d3a57943208da06c70c4e3e9aef943025f019dc1486fbefed73bd234944db40b10c248e')
build () {
	# Ugly workaround with permissions issues
	bsdtar -xf "${noextract}"
	chmod -R 750 "${srcdir}"
	cd "${srcdir}/${_name}"
	./configure --prefix=/usr
	make
}
package() {
	cd "${srcdir}/${_name}"
	make DESTDIR="${pkgdir}" install
	mkdir -p "${pkgdir}/usr/share/${pkgname}/"
	cp -r "${srcdir}/${_name}/wordlists" "${pkgdir}/usr/share/${pkgname}/"
	# Avoid file conflict with icu
	mv "${pkgdir}/usr/bin/gendict" "${pkgdir}/usr/bin/gendict_dirb"
	chmod -R 755 "${pkgdir}/usr/share/${pkgname}/wordlists"
}
