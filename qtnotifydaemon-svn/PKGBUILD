pkgname=qtnotifydaemon-svn
pkgver=19
pkgrel=1
pkgdesc="Configurable and flexible Qt notification daemon"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/qtnotifydaemon"
license=('GPL3')
depends=('qt')
makedepends=('subversion')
source=('org.freedesktop.Notifications.service')
md5sums=('3ddb9114a6de013592449a88a12aef46')
install="${pkgname}.install"

_svntrunk="https://qtnotifydaemon.svn.sourceforge.net/svnroot/qtnotifydaemon"
_svnmod="qtnotifydaemon"

build()  {
  cd ${srcdir}

  msg2 "Connecting to $_svntrunk SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi
  msg2 "SVN checkout done or server timeout"

  msg2 "Starting make..."
  if [[ -d "${srcdir}/build" ]]; then
    msg3 "Cleaning the previous build directory..."
    rm -rf "${srcdir}/build"
  fi
  mkdir "${srcdir}/build"
  cd "${srcdir}/build"
  qmake ${srcdir}/qtnotifydaemon/qtnotifydaemon.pro
  make
}

package() {
  install -Dm 755 ${srcdir}/build/qtnotifydaemon ${pkgdir}/usr/bin/qtnotifydaemon
  install -Dm 644 org.freedesktop.Notifications.service ${pkgdir}/usr/share/dbus-1/services/org.freedesktop.Notifications.service
}