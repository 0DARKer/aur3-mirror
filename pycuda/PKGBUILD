# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
pkgname=pycuda
pkgver=0.94.2
pkgrel=1
pkgdesc="Python wrapper for Nvidia CUDA"
arch=('i686' 'x86_64')
url="http://mathema.tician.de/software/pycuda"
license=('MIT/X Consortium')
source=(http://pypi.python.org/packages/source/p/pycuda/${pkgname}-${pkgver}.tar.gz)
depends=('boost' 'cuda-toolkit' 'python-numpy' 'pytools' 'python2')
makedepends=('ctags' 'python2-distribute')
conflicts=('pycuda-git')
sha1sums=('aff473d0cea2842657c38eef6c5bb6eaa3e0d8e1')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   python2 ./configure.py --boost-inc-dir=/usr/include \
                  --boost-lib-dir=/usr/lib \
                  --boost-python-libname=boost_python-mt \
                  --boost-thread-libname=boost_thread-mt \
                  --cuda-root=/usr \
                  --boost-compiler=gcc \
                  --cuda-enable-gl \
                  --cuda-inc-dir=/usr/include/cuda
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   python2 setup.py install --prefix=/usr --root="${pkgdir}"
}
# vim: set noexpandtab tabstop=8 shiftwidth=8 textwidth=132 autoindent
