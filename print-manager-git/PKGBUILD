# Maintainer: vicky91 <vickypaiers@gmail.com>

pkgname=print-manager-git
pkgver=20120810
pkgrel=1
pkgdesc="A kde tool for managing print jobs and printers"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/base/print-manager"
license=('GPL')
depends=('kdebase-workspace' 'libcups')
makedepends=('cmake' 'automoc4' 'git')
provides=('print-manager')
conflicts=('print-manager')

_gitroot='git://anongit.kde.org/print-manager'
_gitname='print-manager'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/build"
  mkdir "$srcdir/build"
  cd "$srcdir/build"

  cmake ../${_gitname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` \
    -DCUPS_1_6=ON
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
