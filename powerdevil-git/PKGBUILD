# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=powerdevil-git
pkgver=r1517.767556b
pkgrel=1
pkgdesc='KDE power management tools'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('plasma-workspace-git' 'libxrandr')
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git')
conflicts=('powerdevil' 'kde-workspace<0')
provides=('powerdevil')
source=('git://anongit.kde.org/powerdevil.git')
md5sums=('SKIP')

pkgver() {
  cd powerdevil
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../powerdevil/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# fix conflicts
  rm -fr ${pkgdir}/usr/*
}
