#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013.5.13
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python3-threaded_servers pyalpm)
backup=(etc/pacserve/pacserve.service.conf etc/pacserve/pacserve-ports.conf)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.5.13.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.5.13.tar.xz.sig
)
sha512sums=(
  5114d5bbdfbbdb60f14078240cbd5551cdd0938e3de6c36290225c874662832f174a78d2105b50f1df025c82713afe8b1cdef5c27534d8fc695204f4643f8d03
  6821cc6cd9fd77bffeed30939418a7202fd27efb0707048193ce155aaff499bf93dce0b00b09b9236c0c3f71068d1dfd6c9ebf87745d1d3080288b0aa8751c16
)
md5sums=(
  96c8633f7e661c1e8d64c31fef9a545f
  2178489e846181f14d9e3999a6156601
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"


  for foo_ in etc/*/*
  do
    install -Dm644 "$foo_" "$pkgdir/$foo_"
  done

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done
}

# vim: set ts=2 sw=2 et:
