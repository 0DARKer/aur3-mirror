#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2012.12.24.1
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/conf.d/pacserve)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2012.12.24.1.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2012.12.24.1.tar.xz.sig
)
sha512sums=(
  ae369e0dd550b46b87f2d9cd65024ca489b209941c9689eb0f26062449bcb52a6f298c79ef34b7fe641b5cc86123bd5135a8b131aef9d109f53e6e78e2712a77
  e512aba5516748f0c6fae365fbfb914cc65d77f6d3ba5ebd7a96b78504817c99a8d398d49966a84f0cc81b492cec23a72238d4b2f95efbc3a7d5b4d9d6c46cd4
)
md5sums=(
  1715c65b17a925955b36ca16f4d13778
  e1e6b277bdd85b987969dc943000768d
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"

  for foo_ in {conf,pacman}.d
  do
    install -Dm644 "$foo_/$pkgname" "$pkgdir/etc/$foo_/$pkgname"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
  install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
