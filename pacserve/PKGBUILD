#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013.4.14
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/pacserve/pacserve.service.conf etc/pacserve/pacserve-ports.conf)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.14.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.14.tar.xz.sig
)
sha512sums=(
  f2cce097626656bf4ea09d94c667735793fc354bf760c16aba7db028ad58756894dd7a80f183952f39889a01f94aad00af54191af5c21e1f327544be610b7494
  9401ddea257405b02f34b1fa43ff116f85c2cdac59bdb06f5128ea26618ad8c6c34926e1c2e8dae75c4b93a90769e1b45402f439b43b9dc9c4ed65debe05e416
)
md5sums=(
  83f50e65b1bb1ff09ac41c1af0f235aa
  f83a36fe1473173367c45076a333e986
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"


  for foo_ in etc/*/*
  do
    install -Dm644 "$foo_" "$pkgdir/$foo_"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
#   install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
