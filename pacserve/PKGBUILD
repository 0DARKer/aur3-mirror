#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013.4.6.1
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/conf.d/pacserve)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.6.1.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.6.1.tar.xz.sig
)
sha512sums=(
  24a95ac9d06374a24982116d8337c09976161d00334bc6a995650b1a3db51727f64d50869074fef047ea5bfd24dcad37d02f86b0635847f3ad7ce55feb87e53b
  a0838ac0d0b3ed2de3421358ccffe93ba90ebfaaaf1df6eba48905cd820be22e8706fff9b75d2ee0970ec9a5bbeb47ef3f404fa4c2121837381103973286c472
)
md5sums=(
  6c42684d2f49cf35eb59c02a61908401
  6827bd1f45c601e341b157a15da8dc3f
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"

  for foo_ in {conf,pacman}.d
  do
    install -Dm644 "$foo_/$pkgname" "$pkgdir/etc/$foo_/$pkgname"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
  install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
