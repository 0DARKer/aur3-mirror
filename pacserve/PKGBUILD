#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013.5.12
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python3-threaded_servers pyalpm)
backup=(etc/pacserve/pacserve.service.conf etc/pacserve/pacserve-ports.conf)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.5.12.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.5.12.tar.xz.sig
)
sha512sums=(
  a424d755622cc636886e08a8f58bb4a7e5de577ca87521ce3e31a8ac522dfdf396b8abbef658c2494061c57b6ac59fa455037796f5e680b93d84f5493187e82a
  04cd98ad24d41ca219643de2105655ad5d31a894a57c4a0c70acc2dc1a0d19db92f25d2615d927e1eb1d97f9ade25c5057a1f2ce430a782671e3eb07609ef187
)
md5sums=(
  b5a5867128d9ac56a01a1ca44f858e99
  3e61c3e2385daaa1eca68265ac23de92
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"


  for foo_ in etc/*/*
  do
    install -Dm644 "$foo_" "$pkgdir/$foo_"
  done

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done
}

# vim: set ts=2 sw=2 et:
