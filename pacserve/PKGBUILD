#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013.5.18
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python3-threaded_servers pyalpm)
backup=(etc/pacserve/pacserve.service.conf etc/pacserve/pacserve-ports.conf)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.5.18.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.5.18.tar.xz.sig
)
sha512sums=(
  bdf87b89ec52d01de812ba231836b437074950aa1840dc5ca23798e8ee61d0a749da89e95e46cf22557a02975d327b56e4eedaf85106f62d615968a2716d3320
  a14aa70dd266069d3920586bbffa48eb0137c4ee8500a87a54bf340b5092e65e412f556d77727bacb4184639e83e3fa9485940acf0d5cc36bff78c71517840e7
)
md5sums=(
  6f04d4692660f249c767047382eaee73
  abf40e2dae23094eeaa130dc36de2ebf
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"


  for foo_ in etc/*/*
  do
    install -Dm644 "$foo_" "$pkgdir/$foo_"
  done

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done
}

# vim: set ts=2 sw=2 et:
