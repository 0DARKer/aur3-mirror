#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/conf.d/pacserve)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.tar.xz.sig
)
sha512sums=(
  cd4d12205e9ecaa329caa040877a9d461a4ce35ce58aee228ece594ecbe731377e965d871a68768784d8081a2d98a094574a390fca395ed9c1cd30e4bdd2bf2b
  886c02b3350237bf8fa28e89a2da81a1ecfa7fa5541fb3a886381e06ff1e5119e5c5e77df50545607acf76c404a4f58e151e5f5049c9c6ebe1d0b1168c530bd7
)
md5sums=(
  219216846952a2ba1f525b711a1e7cb4
  dc125aae2cd6f8b2339059e3d843eb7c
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"

  for foo_ in {conf,pacman}.d
  do
    install -Dm644 "$foo_/$pkgname" "$pkgdir/etc/$foo_/$pkgname"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
  install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
