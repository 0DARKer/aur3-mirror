#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2012.12.24.2
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/conf.d/pacserve)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2012.12.24.2.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2012.12.24.2.tar.xz.sig
)
sha512sums=(
  d81004652848f7860f29399ce8e0835796e8d449f03b9946eeada7d2ecba0f4efa3741ee73c3f7bdf8273e11a0506a1127c53b0e345800dbacde7cd603087fa9
  339edd01389bafd68fc8daa8340beef04f08712fc6664120abdb0a1c9dbb792de31c814415083055be0aea78429aba35fc06eb5ca5a079167825f1982b06082f
)
md5sums=(
  827666b79bf9bdf1916dfc4a96126ac5
  3f3f4161e8d1fe82cd09c98b96163304
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"

  for foo_ in {conf,pacman}.d
  do
    install -Dm644 "$foo_/$pkgname" "$pkgdir/etc/$foo_/$pkgname"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
  install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
