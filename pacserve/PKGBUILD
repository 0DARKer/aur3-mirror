#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013.5.31
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python3-threaded_servers pyalpm)
backup=(etc/pacserve/pacserve.service.conf etc/pacserve/pacserve-ports.conf)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.5.31.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.5.31.tar.xz.sig
)
sha512sums=(
  ef4c09a30dd5162aece4cf0b250eb319a42b59703b937774b4faa9c56f3ba7ccef81d2ca0688e5c30898ee8e059a2f1c9db207982401a4b7f684d8cb50c5dc57
  69b77db3693c22b0e60ff07a1e3fb3725930c096e2ee0556dffb9abcf424bace05d46246ddcfeeb41dda49c2c6e8070698ff251b5ffa10655ac24ce4ea014b45
)
md5sums=(
  2ed623240401da1b71753e4c8ee6a2bc
  731e6c15ba947f42ad523e0392863768
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"


  for foo_ in etc/*/*
  do
    install -Dm644 "$foo_" "$pkgdir/$foo_"
  done

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done
}

# vim: set ts=2 sw=2 et:
