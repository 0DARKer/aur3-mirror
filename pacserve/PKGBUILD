#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2012.11.27.1
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/conf.d/pacserve)
source=(
  http://xyne.archlinux.ca/projects/pacserve/archives/pacserve-2012.11.27.1.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/archives/pacserve-2012.11.27.1.tar.xz.sig
)
sha512sums=(
  52e877d81d2d8e2aee38804bbeb4356d4b4df382f48e599e3d2586fb80257c67d73dd749862c692e5072325536136011ca9ca49c0efa9e9eb7ec20d52cf33d27
  25fc335944f90a574156d22f4b2fe0a93deec9cab69acd679f7484557dd2ab56c94c2cb654428de322cef7f893187c82554987386cd86518f22f9f3c32a5bc58
)
md5sums=(
  729fbc1723dfa61606ffba4babb46eee
  761eb236d0d01749e6f3f0a1d9d3056b
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"

  for foo_ in {conf,pacman}.d
  do
    install -Dm644 "$foo_/$pkgname" "$pkgdir/etc/$foo_/$pkgname"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
  install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
