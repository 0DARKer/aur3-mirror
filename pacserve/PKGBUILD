#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013.4
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/conf.d/pacserve)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.tar.xz.sig
)
sha512sums=(
  30b4ed23c70157d58f4b5d7eb740707218597bbcf105225522945b8682e2ee2a9e141d179a0a4a1a55dca91b49d1693d9889d4d8ef0958eb38fa1ddfbde0d996
  c0f5dbeff549ac5d675234ab0c5489f6463e302fb7b0595c907c03402830b003dc46b69bc6aa01a29e512b96fa62dda52a42bd0b6f9fa593778000142dfc853e
)
md5sums=(
  4eb09e6c517964fdfb1ada4a22218c35
  715a8755a765ed2eee0d431c11a3caa4
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"

  for foo_ in {conf,pacman}.d
  do
    install -Dm644 "$foo_/$pkgname" "$pkgdir/etc/$foo_/$pkgname"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
  install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
