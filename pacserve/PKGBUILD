#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2012.12
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/conf.d/pacserve)
source=(
  http://xyne.archlinux.ca/projects/pacserve/archives/pacserve-2012.12.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/archives/pacserve-2012.12.tar.xz.sig
)
sha512sums=(
  41d9daee73585b2ab096258aa65f5eecdd1d690f1442b19dd4193de8d63ff78f8d8164ae99c9534ad490b8d2a3b3dc2a163551ae89803fa88c76e8357a61371f
  eaa625d8381657118e4fdc045ac3d0974f96a052f1d066621c9b5e13a35949b2fdea3a3962b75e8967425b60b6cf92126b9f321b434c5f6f18e00ab0673fdfaa
)
md5sums=(
  e44faf3737f51d7651cb15823591d2eb
  362a47152ed9ba991221d77159ed2efd
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"

  for foo_ in {conf,pacman}.d
  do
    install -Dm644 "$foo_/$pkgname" "$pkgdir/etc/$foo_/$pkgname"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
  install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
