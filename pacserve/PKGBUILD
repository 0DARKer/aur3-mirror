#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013.4.6
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/conf.d/pacserve)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.6.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.6.tar.xz.sig
)
sha512sums=(
  f327258302dd3e3a89b5eca5e611056aaa256175305836a0ad9b1e2d41726b2e5e10eaf61db5c5808d47409a8cae79d9b0cc3c2b4147095cc76f9a7384b736c1
  8b7733df19fbf7449257ee4dc1d3111a52458c7061038048e9990ba562434e54ed3f37fa6d3c5d1639d011a4c576f86bdf880a39f0861f7509ccb9a99d643d9c
)
md5sums=(
  470b51666de5b72fe8cb851225f251c3
  184827e6dd0598f1ada2d2fb5af2e1a2
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"

  for foo_ in {conf,pacman}.d
  do
    install -Dm644 "$foo_/$pkgname" "$pkgdir/etc/$foo_/$pkgname"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
  install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
