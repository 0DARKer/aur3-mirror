#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pacserve
pkgver=2013.4.20
pkgrel=1
pkgdesc='Easily share Pacman packages between computers. A replacement for PkgD.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pacserve"
depends=(python2 python2-xynehttpserver)
backup=(etc/pacserve/pacserve.service.conf etc/pacserve/pacserve-ports.conf)
source=(
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.20.tar.xz
  http://xyne.archlinux.ca/projects/pacserve/src/pacserve-2013.4.20.tar.xz.sig
)
sha512sums=(
  61a33ea8a0d6d180943c9ca8fcccf85da81ca8f4816cae02b5dade85ecb0e07f5884dd6f877180df8c4770ee1b2907983ee14adf8343594029d204d319ee34d0
  2572c19dce65158135ef5d05fcda65602fb88a87b2719ff4af9fed26ea0bc1e0e46fb072ece5cd02873bd8705183820412b22c3cb92f89041ee36d2e66df2174
)
md5sums=(
  53fba9b90cd5f63bf0856c1a3e8d1f8b
  6b7c551615a15a30f6c8d0b12b5cd0a2
)
install=pacserve.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"


  for foo_ in etc/*/*
  do
    install -Dm644 "$foo_" "$pkgdir/$foo_"
  done

  # rc.d daemon, to be removed when Arch fully drops support for it
#   install -Dm755 "rc.d/${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"

  for bin_ in bin/*
  do
    install -Dm755 "$bin_" "$pkgdir/usr/bin/${bin_##*/}"
  done

  for service_ in systemd/*.service
  do
    install -Dm644 "$service_" "$pkgdir/usr/lib/systemd/system/${service_##*/}"
  done

  for man_ in man/*
  do
    install -Dm644 "$man_" "$pkgdir/usr/share/man/man1/${man_##*/}"
  done
}

# vim: set ts=2 sw=2 et:

# vim: set ts=2 sw=2 et:
