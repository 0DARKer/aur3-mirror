# Contributor: grimi <grimi at poczta dot fm>
# Contributor: Sebastian Wolf <fatmike303 at gmail dot com>

pkgname=vice-sdl
_pkgname=vice
pkgver=2.4
pkgrel=1
pkgdesc="The Versatile Commodore 8-bit Emulator, SDL version"
arch=('i686' 'x86_64')
license=('GPL')
url="http://vice-emu.sourceforge.net"
depends=('alsa-lib' 'sdl' 'gcc-libs' 'giflib' 'lame' 'libjpeg' 'libpng' 'libxrandr')
makedepends=('xorg-font-utils' 'pkg-config')
provides=("vice=$pkgver")
conflicts=('vice' 'vice-gnomeui' 'vice-gtkglext' 'vice-svn')
replaces=('vice')
options=('!makeflags')
install=vice-sdl.install
source=(	http://downloads.sourceforge.net/project/vice-emu/releases/${_pkgname}-${pkgver}.tar.gz
			vice-2.4-x11video.patch
			vice-2.4-no-fc-cache-no-lib64.patch
			vice-2.4-notexi-notxt.patch
			vice-2.4-zlib-1.2.7.patch)
md5sums=(	'b017647a0c159bbe43cdb81762d1c577'
			'ae22d60bb34ea0ed325c601211a47cbc'
			'f3d369b3dea44cc8761d08f7664d9438'
			'9058d0f8e75e61dd4bf41097f4eb2f6f'
			'849d1f90c5e8ca429ff2ee9936f44a65')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	patch -Np1 -i "${srcdir}/vice-2.4-x11video.patch"
	patch -Np1 -i "${srcdir}/vice-2.4-no-fc-cache-no-lib64.patch"
	patch -Np1 -i "${srcdir}/vice-2.4-notexi-notxt.patch"
	patch -Np1 -i "${srcdir}/vice-2.4-zlib-1.2.7.patch"

	autoreconf
	./configure --prefix=/usr --enable-fullscreen --enable-sdlui --with-sdlsound --enable-ethernet --disable-ffmpeg

	make -j1
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" realdocdir="/usr/share/doc/vice" install
	ln -s /usr/share/doc/vice "${pkgdir}/usr/lib/vice/doc"
}

