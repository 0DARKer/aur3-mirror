# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
pkgver=1.6.0
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet, stable version"
arch=('any')
depends=('python2' 'python2-ecdsa' 'python2-pyqt' 'python2-slowaes' 'qt')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QRcode reading support')
url="http://electrum.ecdsa.org/"
license=('GPL3')
source=(http://electrum.bitcoin.cz/download/Electrum-$pkgver.tar.gz)
sha256sums=('63b9de5adabc4fdf958d20acc090dd8b343390c99d87077e0b507d7c21084479')
provides=('electrum')
conflicts=('electrum')

build() {
  cd "$srcdir"/Electrum-$pkgver
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  python2 mki18n.py
  pyrcc4 icons.qrc -o lib/icons_rc.py
  python2 setup.py sdist --format=zip,gztar
}

package() {
  cd "$srcdir"/Electrum-$pkgver
  python2 setup.py install --root=$pkgdir
}
