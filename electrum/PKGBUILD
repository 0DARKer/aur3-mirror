# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
pkgver=1.5.7
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet, stable version"
arch=('any')
depends=('python2' 'python2-ecdsa' 'python2-pyqt' 'python2-slowaes' 'qt')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QRcode reading support')
url="http://electrum.ecdsa.org/"
license=('GPL3')
source=(http://electrum.bitcoin.cz/download/Electrum-$pkgver.tar.gz)
sha256sums=('3e20e6ec871efea848dfd94879a64caa9c16081287855a2dd1c095e1044b86d8')
provides=('electrum')
conflicts=('electrum')

build() {
  cd "$srcdir"/Electrum-$pkgver
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  python2 mki18n.py
  pyrcc4 icons.qrc -o lib/icons_rc.py
  python2 setup.py sdist --format=zip,gztar
}

package() {
  cd "$srcdir"/Electrum-$pkgver
  python2 setup.py install --root=$pkgdir
}
