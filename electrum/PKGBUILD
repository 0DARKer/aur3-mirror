# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
pkgver=1.5.3
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet, stable version"
arch=('any')
depends=('python2' 'qt' 'python2-pyqt' 'python2-ecdsa' 'python2-slowaes')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QRcode reading support')
url="http://electrum.ecdsa.org/"
license=('GPL3')
source=(http://electrum.ecdsa.org/Electrum-$pkgver.tar.gz)
sha256sums=('bdc176631cbffa54e359c040189e9e40060584f2c63d20ebfa7e58cfb74c458f')
provides=('electrum')
conflicts=('electrum')

build() {
  cd "$srcdir"/Electrum-$pkgver
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  python2 mki18n.py
  pyrcc4 icons.qrc -o lib/icons_rc.py
  python2 setup.py sdist --format=zip,gztar
}

package() {
  cd "$srcdir"/Electrum-$pkgver
  python2 setup.py install --root=$pkgdir
}
