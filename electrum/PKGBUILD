# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.7
pkgrel=2
pkgdesc="Lightweight Bitcoin wallet, stable version"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        electrum.install)
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

build() {
  cd "$srcdir"/$_realname-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  python2 mki18n.py
  pyrcc4 icons.qrc -o lib/icons_rc.py
  python2 setup.py sdist --format=zip,gztar
}

package() {
  cd "$srcdir"/$_realname-$pkgver
  python2 setup.py install --root=$pkgdir
}

md5sums=('4580c7ab656174a20334062b7fc1201f'
         '1d956e96d1410b5d0feb5e7cf497ac25'
         '49a3b6b4985a90c3b7da229c160735e3')
sha256sums=('8d7c83144b78180598b66d5434d133e083bdab95a2a820a87c9e292aaa4a8749'
            'ca9f4072d405320733366972d7cce4f0be9029eb9ef6004a6e17a591e615640c'
            'dfaab558fff85c026427627610a66531a5bfcfedcf54c4d058d14ed6c27db547')
