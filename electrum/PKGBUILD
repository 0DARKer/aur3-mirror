# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.7.1
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet, stable version"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        electrum.install)
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

build() {
  cd "$srcdir"/$_realname-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  python2 mki18n.py
  pyrcc4 icons.qrc -o lib/icons_rc.py
  python2 setup.py sdist --format=zip,gztar
}

package() {
  cd "$srcdir"/$_realname-$pkgver
  python2 setup.py install --root=$pkgdir
}

md5sums=('0ca7739a45c41f18b8fd16e9a6c9fa37'
         'c42b437ab2eb4e2f6142268720a440ed'
         '49a3b6b4985a90c3b7da229c160735e3')
sha256sums=('b1c39612b56d830f2cbfb4cc68d707ba9acf38f50928bc7ff9458eb4d2e5a90b'
            'eaf2a48f3b64f02b131f2529fa13bb0d02eff71c2b75c68529e5bf6bca713c70'
            'dfaab558fff85c026427627610a66531a5bfcfedcf54c4d058d14ed6c27db547')
