# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.6.2
pkgrel=8
pkgdesc="Lightweight Bitcoin wallet, stable version"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt<=4.9.6' 'python2-sip<=4.14.2' 'python2-slowaes' 'qt4' 'sip<=4.14.2')
makedepends=('gcc' 'make')
optdepends=('zbar-v4l: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc)
md5sums=('4e40d5c6c13e7e7d09b559cc6f4eb982'
         'ba226a4223a41a7671e5b0ec3f3856c2')
sha256sums=('2b7f55cd8998c9529ceb5526e2c8274e701c1b9419fe6fbc4976bfd5d9cb4427'
            '65bec1799706cc7076dab22ab574972f603b94e52fc69034bc26c8d7450026dc')
provides=('electrum')
conflicts=('electrum-git')

build() {
  cd "$srcdir"/$_realname-$pkgver
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  python2 mki18n.py
  pyrcc4 icons.qrc -o lib/icons_rc.py
  python2 setup.py sdist --format=zip,gztar
}

package() {
  cd "$srcdir"/$_realname-$pkgver
  python2 setup.py install --root=$pkgdir
}
