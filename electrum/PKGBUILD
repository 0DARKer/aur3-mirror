# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=electrum
_realname=Electrum
pkgver=1.7.3
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('gettext' 'python2' 'python2-ecdsa' 'python2-pyqt' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gcc' 'make')
optdepends=('python2-zbar: QR code reading support')
url="http://electrum.org/"
license=('GPL3')
source=(http://download.electrum.org/download/$_realname-$pkgver.tar.gz
        http://download.electrum.org/download/$_realname-$pkgver.tar.gz.asc
        electrum.install)
provides=('electrum')
conflicts=('electrum-git')
install=electrum.install

build() {
  cd "$srcdir"/$_realname-$pkgver

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'

  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/icons_rc.py
  python2 setup.py sdist --format=zip,gztar
}

package() {
  cd "$srcdir"/$_realname-$pkgver
  python2 setup.py install --root=$pkgdir
}

md5sums=('22a35b0816f7c6c59fbeddb50e832ada'
         '8f284732adfc7b4b9b81b98544ebc73c'
         '49a3b6b4985a90c3b7da229c160735e3')
sha256sums=('bd16afafa70c6c104eb82723bf4555405774c48f6723593e95ef49de0b8f3e51'
            '029f1ed6e8893747bf24d300c0b1f06d1f31ae74658d92c1e36acbd9baba1774'
            'dfaab558fff85c026427627610a66531a5bfcfedcf54c4d058d14ed6c27db547')
