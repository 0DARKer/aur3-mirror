# Maintainer: Idares <idares@seznam.cz>

pkgname=nagios
pkgver=3.4.1
pkgrel=3
pkgdesc="Nagios is an open source host, service and network monitoring program."
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.nagios.org"
depends=('gd' 'freetype2' 'libtool' 'glib2')
source=("http://downloads.sourceforge.net/nagios/$pkgname-$pkgver.tar.gz"
        "rc.nagios"
        "nagios.install"
        "nagios.service")
backup=('etc/httpd/conf/extra/nagios.conf')
install='nagios.install'

_nagios_user="nagios"
_nagios_group="nagios"
_instdir="usr/share/nagios"
_bindir="usr/bin"
_vardir="var/nagios"
_confdir="etc/nagios"
_httpdconfdir="etc/httpd/conf/extra"
_checkresultdir="var/nagios/spool/checkresults"
_perldir="$_instdir/bin"

getent group $_nagios_group > /dev/null || _nagios_group=30
getent passwd $_nagios_user > /dev/null || _nagios_user=30

build() {
	cd $srcdir/$pkgname

	./configure \
		--with-nagios-user=$_nagios_user \
		--with-nagios-group=$_nagios_group \
		--prefix="/$_instdir" \
		--bindir="/$_bindir" \
		--localstatedir="/$_vardir" \
		--sysconfdir="/$_confdir" \
		--with-httpd-conf="/$_httpdconfdir" \
		--with-checkresultdir="/$_checkresultdir" \
		--enable-embedded-perl

	make all
}

package() {
	cd $srcdir/$pkgname

	make \
		prefix=$pkgdir/$_instdir \
		BINDIR=$pkgdir/$_bindir \
		LOGDIR=$pkgdir/$_vardir \
		CFGDIR=$pkgdir/$_confdir \
		HTTPD_CONF=$pkgdir/$_httpdconfdir \
		CHECKRESULTDIR=$pkgdir/$_checkresultdir \
		install install-config

	install -D -m 755 daemon-init $pkgdir/etc/nagios/
	install -D -m 644 sample-config/httpd.conf $pkgdir/$_httpdconfdir/nagios.conf
	install -D -m 755 $srcdir/rc.nagios $pkgdir/etc/rc.d/nagios
	install -D -m 644 $srcdir/nagios.service $pkgdir/usr/lib/systemd/system/nagios.service

	mkdir $pkgdir/var/nagios/rw
	chown $_nagios_user.$_nagios_group $pkgdir/var/nagios/rw
	chmod 755 $pkgdir/var/nagios/rw

	find $pkgdir/etc/nagios -name '*cfg' -exec mv "{}" "{}.sample" \; > /dev/null

	mkdir -p -m 755 $pkgdir/$_perldir
	mv $pkgdir/$_bindir/p1.pl $pkgdir/$_perldir/
	sed -i "s:^p1_file=.*:p1_file=/$_perldir/p1.pl:" $pkgdir/etc/nagios/nagios.cfg.sample
}

md5sums=('2fa8acfb2a92b1bf8d173a855832de1f'
         'c5c98b2e609794c6ab2622dcf01ba373'
         '0bd071fe9c74210f48729be4e2ae1c9f'
         'ea17f3e79c4b0ed8f6223b1b63e026de')
sha1sums=('728d3a7f601cfbc0686afa27cb02a2cb212cc5ac'
          '73614f71041fecb0670f2d9ac118c01660d01e5b'
          '39d61e6a7f1a53d6700e46475c0c8c5b18b8a98f'
          'b6782b7d64cd20beae1669cd16868919fc314430')

