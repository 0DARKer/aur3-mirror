# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>
 
pkgname=graphene-git
pkgver=0.99.1.127.7c755ce
pkgrel=1
pkgdesc='A thin layer of graphic data types'
arch=('i686' 'x86_64')
url='https://github.com/ebassi/graphene/'
depends=('glib2')
provides=('graphene='$pkgver)
conflicts=('graphene')
options=('!libtool')
license=('MIT')
 
source=('git+https://github.com/ebassi/graphene.git')
md5sums=('SKIP')
 
_gitname='graphene'
 
pkgver() {
  cd $_gitname
  
  major=$(grep "major_version]" configure.ac | sed 's/m4_define(\[graphene_major_version\], \[//' | sed 's/\])//')
  minor=$(grep "minor_version]" configure.ac | sed 's/m4_define(\[graphene_minor_version\], \[//' | sed 's/\])//')
  micro=$(grep "micro_version]" configure.ac | head -1 | sed 's/m4_define(\[graphene_micro_version\], \[//' | sed 's/\])//')
  hash=$(git log --pretty=format:'%h' -n 1)
  revision=$(git rev-list --count HEAD)
  
  echo $major.$minor.$micro.$revision.$hash  
}
 
build() {
  cd $_gitname
  ./autogen.sh --prefix=/usr
  make
}
 
package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
}
