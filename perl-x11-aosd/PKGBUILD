# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-x11-aosd
_prename=X11
_pkgname=X11-Aosd
pkgver=0.03
pkgrel=1
pkgdesc="libaosd binding for Cairo powered on screen display"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('libaosd' 'perl' 'gtk2-perl' 'perl-test-simple' 'perl-extutils-depends')
url='http://search.cpan.org/dist/X11-Aosd/'
source=(http://search.cpan.org/CPAN/modules/by-module/${_prename}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('7cbf1d75704d5a7104f8fd3c5369a2ed70b73758fd8a2715813366625c902e33')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  export PERL_MM_USE_DEFAULT=1

  perl Makefile.PL INSTALLDIRS=vendor
  make
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
