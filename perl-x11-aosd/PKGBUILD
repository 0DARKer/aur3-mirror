# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-x11-aosd
_lastauthor=J/JR/JRED
_pkgname=X11-Aosd
pkgver=0.03
pkgrel=4
pkgdesc="libaosd binding for Cairo powered on screen display"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('libaosd' 'gtk2-perl' 'perl-extutils-depends' 'perl-extutils-pkgconfig')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('7cbf1d75704d5a7104f8fd3c5369a2ed70b73758fd8a2715813366625c902e33')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  if [ -f "Build.PL" ]; then
    perl Build.PL
    perl Build
  elif [ -f "Makefile.PL" ]; then
    perl Makefile.PL
    make
  else
    return 1
  fi
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build test
  elif [ -f "Makefile.PL" ]; then
    make test
  fi
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build install
  elif [ -f "Makefile.PL" ]; then
    make install
  fi

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
