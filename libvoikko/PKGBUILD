#Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>
#Contributor:	Jussi Timperi	<gmail.com: jussi.timperi>

pkgname=libvoikko
pkgver=3.5
pkgrel=1
pkgdesc="A library of free Finnish language tools using Malaga"
arch=('i686' 'x86_64')
url="http://voikko.sourceforge.net"
license=('GPL2')
depends=('suomi-malaga-voikko>=1.8' 'gcc-libs')
makedepends=('python2')
options=(!libtool)
source=(http://downloads.sourceforge.net/voikko/$pkgname-$pkgver.tar.gz)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --with-dictionary-path=/usr/lib/suomi-malaga
  sed 's|python $(srcdir)|python2 $(srcdir)|g' -i src/Makefile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

md5sums=('ed12fa993bd10f703d4fbe2f0e9b4e77')
