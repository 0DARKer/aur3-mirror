# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jean-Francois Chevrette <jfchevrette@gmail.com>
# Contributor: Jonathas Rodrigues <jonathas@archlinux.us>
# Contributor: Karl Kochs <captainhaggy@i2pmail.org>

_pkgname=turpial
pkgname=$_pkgname-git
pkgver=1.3.2.b2.920.g5e136a4
pkgrel=1
pkgdesc="A multi-interface Twitter client written in Python"
arch=(any)
url=http://code.google.com/p/turpial/
license=(GPL3)
depends=(gstreamer0.10-python hspell libturpial-git notification-daemon pygtk python2-babel
    python2-gtkspell python2-notify python2-pygame python2-simplejson pywebkitgtk)
makedepends=(setuptools git)
provides=($_pkgname)
conflicts=($_pkgname)
source=($pkgname::git://github.com/Turpial/Turpial.git#branch=development)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $pkgname/
    git describe | sed 's/-/./g'
}

build() {
    cd $pkgname/
    python2 setup.py build
}

package() {
    cd $pkgname/
    python2 setup.py install --root="$pkgdir" --optimize=1
}
