# Maintainer: ava1ar (mail(dot)avatar(at)gmail(dot)com)

pkgname=chromium-libpdf-stable
pkgver=24.0.1312.69
pkgrel=1
_verbld=${pkgver}-180721
_channel='stable'
arch=('i686' 'x86_64')
_arch="$CARCH"
[ "$CARCH" = 'i686' ] && _arch='i386'
pkgdesc="Google Chrome's PDF plugin for Chromium (stable version)"
url="http://www.google.com/chrome"
license=('custom:chrome')
depends=('chromium')
source=(http://dl.google.com/linux/chrome/rpm/stable/${_arch}/google-chrome-${_channel}-${_verbld}.${_arch}.rpm
license.html::http://www.google.com/chrome/intl/en/eula_text.html)
sha1sums=('f8b62ac85dc76bbccc6eed900852c9300915bb2f' 'cdda47faa791d56089f92854957070b1ec135c2e')
[ "$CARCH" = 'i686' ] && sha1sums[0]='a7c2245f9c937ec4a83efe4bbe5bed22fb94e4ad'

package() {
    install -d "${pkgdir}/usr/lib/chromium"
    install -m644 opt/google/chrome/libpdf.so "${pkgdir}/usr/lib/chromium"
    install -Dm644 "$srcdir/license.html" "$pkgdir/usr/share/licenses/$pkgname/license.html"
}
