# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname='osmesa'
pkgver=7.11
pkgrel=1
pkgdesc="Mesa 3D off-screen rendering library"
arch=('i686' 'x86_64')
url="http://mesa3d.sourceforge.net"
license=('custom')
depends=("mesa")
makedepends=('imake' 'libxml2' 'python2')
options=('!makeflags')
source=("ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2"
        "llvmpipe-libs.patch"
        "LICENSE")
md5sums=('ff03aca82d0560009a076a87c888cf13'
         '1f505bc159fe8526863b87eb0a2ba915'
         '5c65a0fe315dd347e09b1f2826a1df5a')

build() {
  cd "${srcdir}/Mesa-${pkgver}"
  patch -Np1 -i "${srcdir}/llvmpipe-libs.patch"
  find . -name '*.py' -exec sed -i -r "s|#\s*!\s*(/usr)?/bin/(env\s*)?python\s*$|#!/usr/bin/env python2|" {} +
  sed -i -e "s|PYTHON2 = python|PYTHON2 = python2|" configs/{default,autoconf.in}
  sed -i -e "s|python|python2|" src/gallium/{auxiliary,drivers/llvmpipe}/{Makefile,SConscript}
  sed -i -e "s|python|python2|" src/mesa/drivers/dri/common/xmlpool/Makefile
  ./configure --prefix=/usr --disable-egl --disable-glu --disable-glw --disable-glut --with-driver=osmesa
  make
}

package() {
  cd "${srcdir}/Mesa-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -fr "${pkgdir}"/usr/include &>/dev/null
  install -D -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
