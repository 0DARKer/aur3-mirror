# Contributor: Jonathan Liu <net147@gmail.com>
pkgname='osmesa'
pkgver=7.10.1
pkgrel=1
pkgdesc="Mesa 3D off-screen rendering library"
arch=('i686' 'x86_64')
url="http://mesa3d.sourceforge.net"
license=('custom')
depends=("mesa")
makedepends=('glproto>=1.4.12' 'pkgconfig' 'libdrm>=2.4.23' 'libxxf86vm>=1.1.0' 'libxdamage>=1.1.3' 'expat>=2.0.1' 'libx11>=1.3.5' 'libxt>=1.0.8' 
             'gcc-libs>=4.5' 'dri2proto=2.3' 'python2' 'talloc' 'libxml2' 'imake')
options=('!makeflags')
source=("ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2"
        "LICENSE")
md5sums=('efe8da4d80c2a5d32a800770b8ce5dfa'
         '5c65a0fe315dd347e09b1f2826a1df5a')

build() {
  cd "${srcdir}/Mesa-${pkgver}"
  find . -name '*.py' -exec sed -i -r "s|#\s*!\s*(/usr)?/bin/(env\s*)?python\s*$|#!/usr/bin/env python2|" {} +
  sed -i -e "s|PYTHON2 = python|PYTHON2 = python2|" configs/{default,autoconf.in}
  sed -i -e "s|python|python2|" src/gallium/{auxiliary,drivers/llvmpipe}/{Makefile,SConscript}
  sed -i -e "s|python|python2|" src/mesa/drivers/dri/common/xmlpool/Makefile
  ./configure --prefix=/usr --disable-egl --disable-glu --disable-glw --disable-glut --with-driver=osmesa || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -fr "${pkgdir}"/usr/include &>/dev/null
  install -D -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}

# vim:set ts=2 sw=2 et:
