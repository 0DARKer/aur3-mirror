pkgname=lxde-common-qt-git
_gitname=lxde-common-qt
pkgver=6d3efa5
pkgrel=1
pkgdesc="Default configuration and data files required to run LXDE-Qt"
url="http://lxde.git.sourceforge.net"
arch=('any')
license=('GPL2')
depends=('libx11' 'glib2' 'lxde-icon-theme' 'lxsession')
optdepends=('pcmanfm-qt-git')
makedepends=('automake' 'git')
conflicts=(lxde-common, lxde-common-git)
backup=(etc/xdg/lxsession/LXDE-Qt/desktop.conf
        etc/xdg/lxsession/LXDE-Qt/autostart)
install='lxde-common-qt-git.install'
source=("lxde-common-qt::git://lxde.git.sourceforge.net/gitroot/lxde/lxde-common-qt")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
    cd $srcdir/$_gitname
    autoreconf --install
    ./configure --sysconfdir=/etc --prefix=/usr
    make
}

package() {
    cd $srcdir/$_gitname
    make DESTDIR=$pkgdir install
    install -Dm644 lxde-logout-qt.desktop \
      $pkgdir/usr/share/applications/lxde-logout-qt.desktop
}
