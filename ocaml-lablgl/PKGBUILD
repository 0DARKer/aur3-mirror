# Contributor: Florian Dejonckheere <florian@floriandejonckheere.be>
# Contributor: Bart≈Çomiej Piotrowski <spam@bpiotrowski.pl>
# Contributor: Nikolay Amiantov <nikoamia@gmail.com>

pkgname=ocaml-lablgl
pkgver=1.05
pkgrel=1
pkgdesc='OCaml interface to OpenGL'
arch=('i686' 'x86_64')
license=('BSD')
url='https://forge.ocamlcore.org/projects/lablgl/'
depends=('glu' 'glut' 'libxi' 'libxmu' 'libgl' 'ocaml' 'tcl' 'tk' 'mesa')
options=('!strip' '!makeflags')
source=(https://forge.ocamlcore.org/frs/download.php/1254/lablgl-${pkgver}.tar.gz)
sha1sums=('6451294c1591e17db38874e654f319f8ecf401c9')

build() {
	cd "lablgl-${pkgver}"
	sed 17d Makefile.config.linux.mdk > Makefile.config
	make lib
}

package() {
	cd "lablgl-${pkgver}"
	make install BINDIR="${pkgdir}/usr/bin/" \
		LIBDIR="${pkgdir}/usr/lib/ocaml/" \
		DDLDIR="${pkgdir}/usr/lib/ocaml/stublibs/" \
		INSTALLDIR="${pkgdir}/usr/lib/ocaml/lablGL/"
	# Hack for wrongly placed stublibs library
	mv "${pkgdir}/usr/lib/ocaml/stublibs" /tmp/temp.so
	mkdir -p "${pkgdir}/usr/lib/ocaml/stublibs"
	mv /tmp/temp.so "${pkgdir}/usr/lib/ocaml/stublibs/dlllablgl.so"
	install -D COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
