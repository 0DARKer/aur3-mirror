# Maintainer: Albert Chang <albert.chang@gmx.com>
# Contributor: Thomas Mudrunka <harvie@@email..cz> You can also contact me on http://blog.harvie.cz/

pkgname=riak
pkgver=1.1.2
pkgrel=1
pkgdesc='NOSQL database engine providing decentralized key-value store, flexible map/reduce engine and HTTP/JSON query interface'
arch=('i686' 'x86_64')
license=('APACHE')
url='http://riak.basho.com/'
makedepends=('erlang' 'git')
backup=('opt/riak/etc/app.config' 'opt/riak/etc/vm.args')
install='riak.install'
source=('http://downloads.basho.com.s3-website-us-east-1.amazonaws.com/riak/1.1/1.1.2/riak-1.1.2.tar.gz'
        'riak.rc'
        'riak'
        'riak-admin')
md5sums=('2820cc52942c778656d9dc839247dbb4'
         '942e198d5dfa52ac36360e3f79b65930'
         'ea204ebd9a06a3812a00f4dcbd37c227'
         'af9a1757a5011d23712555adc98c133f')

fixerldep() {
  find . -type f -name 'rebar.config' | xargs sed -i -e 's/R13B04|R14/R13B04|R14|R15/;' -e 's/R14B0\[234\]/R14B0[234]|R15/'
}

build() {
  cd ${pkgname}-${pkgver}

  # Fix R15 dependencies
  fixerldep

  msg 'Cleaning...'
  make distclean

  msg 'Building...'
  # need to unset LDFLAGS because: ld: unrecognized option '-Wl,--hash-style=gnu'
  unset LDFLAGS
  make rel || (fixerldep; make rel)

  cd rel/riak

  install -d ${pkgdir}/opt/riak

  msg 'Packing...'
  cp -r ./* ${pkgdir}/opt/riak

  [ -d "${pkgdir}/usr/lib/erlang" ] && {
    msg 'Removing files already shipped with erlang package...'
    pacman -Ql erlang | cut -d ' ' -f 2- | while read i; do
      [ -d "$i" ] || {
        rm -f ${pkgdir}/"$i";
        echo -ne "$i                             \r";
      }
    done; echo;
  }

  chmod -R 755 ${pkgdir}/opt/riak/bin

  msg 'ArchLinux integration...'

  install -d ${pkgdir}/var/log
  ln -s /opt/riak/log ${pkgdir}/var/log/riak
  install -d ${pkgdir}/etc/rc.d
  ln -s /opt/riak/etc ${pkgdir}/etc/riak
  install -d ${pkgdir}/usr/share/doc/
  cp -r ${srcdir}/riak-${pkgver}/doc/man ${pkgdir}/usr/share/
  cp -r ${srcdir}/riak-${pkgver}/doc ${pkgdir}/usr/share/doc/riak
  rm -r ${pkgdir}/usr/share/doc/riak/man

  # install launch files
  install -d ${pkgdir}/usr/bin/
  install ${srcdir}/riak ${pkgdir}/usr/bin
  install ${srcdir}/riak-admin ${pkgdir}/usr/bin

  # install daemon
  install ${srcdir}/riak.rc ${pkgdir}/etc/rc.d/riak
}
