# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Thomas Mudrunka <harvie@@email..cz> You can also contact me on http://blog.harvie.cz/

pkgname=riak
pkgver=0.14.1
pkgrel=2
pkgdesc='NOSQL database engine providing decentralized key-value store, flexible map/reduce engine and HTTP/JSON query interface'
arch=('i686' 'x86_64')
license=('Apache 2')
url='http://riak.basho.com/'
makedepends=('erlang' 'git')
backup=('opt/riak/etc/app.config' 'opt/riak/etc/vm.args')
install='riak.install'
source=("http://downloads.basho.com/riak/CURRENT/riak-${pkgver}.tar.gz"
        'riak.rc'
        'riak'
        'riak-admin')
md5sums=('f85721775ad1732f20de2c40e0e41d80'
         '942e198d5dfa52ac36360e3f79b65930'
         '62eb96c695aee753ed98c27ae00e0578'
         '313a9c5fa4cd27fdad6a66775b930f21')
prefixpath="/opt/${pkgname}"

build() {
  cd ${pkgname}-${pkgver}
  msg Cleaning...;
  make distclean

  msg Building...;
  LDFLAGS='' make rel

  cd rel/riak
  #cd rel/overlay
  mkdir -p ${pkgdir}${prefixpath}

  msg Packing...;
  cp -r ./* ${pkgdir}${prefixpath}

  [ -d "${pkgdir}/usr/lib/erlang" ] && {
    msg 'Removing files already shipped with erlang package...';
    pacman -Ql erlang | cut -d ' ' -f 2- | while read i; do
      [ -d "$i" ] || {
        rm -f ${pkgdir}/"$i";
        echo -ne "$i                             \r";
      }
    done; echo;
  }

  chmod -R 755 ${pkgdir}${prefixpath}/bin

  msg 'ArchLinux integration...';

  mkdir -p ${pkgdir}/var/log
  ln -s ${prefixpath}/log ${pkgdir}/var/log/riak
  mkdir -p ${pkgdir}/etc/rc.d
  ln -s ${prefixpath}/etc ${pkgdir}/etc/riak
  mkdir -p ${pkgdir}/usr/share/doc/
  cp -r ${srcdir}/riak-${pkgver}/doc/man ${pkgdir}/usr/share/
  cp -r ${srcdir}/riak-${pkgver}/doc ${pkgdir}/usr/share/doc/riak
  rm -r ${pkgdir}/usr/share/doc/riak/man

  # install launch files
  install -d ${pkgdir}/usr/bin/
  install ${srcdir}/riak \
    ${pkgdir}/usr/bin
  install ${srcdir}/riak-admin \
    ${pkgdir}/usr/bin

  # install daemon
  install ${srcdir}/riak.rc \
    ${pkgdir}/etc/rc.d/riak
}
