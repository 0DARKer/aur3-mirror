# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: pressh <pressh at gmail dot com>

pkgname=xscrabble
pkgver=2.12
pkgrel=3
pkgdesc="An X11 implementation of the Scrabble board game"
arch=('i686' 'x86_64')
url="http://www.happypenguin.org/show?XScrabble"
license=('GPL')
depends=('bzip2' 'libxaw' 'zlib')
makedepends=('imake')
optdepends=('xscrabble-en: English language files'
            'xscrabble-fr: French language files')
source=("ftp://ftp.ac-grenoble.fr/ge/educational_games/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.desktop")
md5sums=('733e90def2c1d60c0c223f3a0c908db9'
         'b2c6472d28a51cfd8a50e42cf3a32871')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix installation paths
  sed -i "s_/etc/X11/app-defaults_/usr/share/X11/app-defaults_" build
  sed -i "s_/usr/games_/usr/bin_" build
  sed -i "s_/usr/share/games/scrabble_/usr/share/xscrabble_" build XScrabble

  # Uncomment this line if you want to use the French language files by default
#  sed -i "s_en/_fr/_" XScrabble

  # Compile
  ./build bin
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install game files
  mkdir -p "${pkgdir}/usr/bin"
  install -m755 src/{xscrab,xscrabble} "${pkgdir}/usr/bin"
  install -Dm644 XScrabble "${pkgdir}/usr/share/X11/app-defaults/XScrabble"

  # Install pixmap and .desktop file
  install -Dm644 "src/${pkgname}.xpm" "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 CHANGES README "${pkgdir}/usr/share/doc/${pkgname}"
}
