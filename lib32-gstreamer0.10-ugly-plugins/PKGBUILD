# $Id: PKGBUILD 178131 2013-02-16 22:48:50Z ioni $
# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: GordonGR <gordongr@freemail.gr>

pkgname="lib32-gstreamer0.10-ugly-plugins"
_pkgname="gstreamer0.10-ugly-plugins"
pkgver=0.10.19
pkgrel=3
arch=('x86_64')
license=('LGPL')
pkgdesc="GStreamer Multimedia Framework Ugly Plugins (gst-plugins-ugly), lib32"
depends=("lib32-gstreamer0.10-ugly" "${_pkgname}" 'lib32-libdvdread' 'lib32-lame' 'lib32-libmpeg2' 'lib32-a52dec' 'lib32-libmad' 'lib32-libsidplay' 'lib32-libcdio' 'lib32-x264' 'lib32-opencore-amr')
makedepends=('pkgconfig' 'lib32-gstreamer0.10-base')
url="http://gstreamer.freedesktop.org/"
options=(!libtool)
source=(${url}/src/gst-plugins-ugly/gst-plugins-ugly-${pkgver}.tar.xz
        opencore-amr.patch
        cdio-cd-text-api.patch)
md5sums=('ba26045c8c8c91f0d48d327ccf53ac0c'
         'd8feb6c99bfaff8be6b2c48ea4e98e47'
         '6c1c665f864387f3a77d32231fedeaab')

prepare() {
export CC="gcc -m32"
export CXX="g++ -m32"
export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
cd "${srcdir}/gst-plugins-ugly-${pkgver}"
patch -Np0 -i "${srcdir}/opencore-amr.patch"
patch -Np1 -i "${srcdir}/cdio-cd-text-api.patch"
}

build() {
cd "${srcdir}/gst-plugins-ugly-${pkgver}"
./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
  --disable-static --enable-experimental \
  --disable-twolame \
  --with-package-name="GStreamer Ugly Plugins (Archlinux)" \
  --with-package-origin="http://www.archlinux.org/" \
  --libdir=/usr/lib32 --libexecdir=/usr/lib32
  make
  sed -e 's/gst-libs gst ext/gst-libs gst/' -i Makefile
}

check() {
  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  make check
}

package() {
cd "${srcdir}/gst-plugins-ugly-${pkgver}"
make -C ext DESTDIR="${pkgdir}" install
cd "$pkgdir/usr"
rm -rf share/
}
