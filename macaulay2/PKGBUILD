# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=macaulay2
pkgver=15943
pkgrel=1
pkgdesc="Software system for algebraic geometry and commutative algebra"
arch=('i686' 'x86_64')
url="http://www.math.uiuc.edu/Macaulay2/"
license=('GPL')
depends=('gcc-fortran' 'gc>=7.2' 'gdbm' 'gmp' 'mpfr' 'mpir' 'readline' 'pari' 
      'ntl' 'lapack' 'scscp' 'singular-factory' 'frobby' 'glpk' 'cddlib' 'gfan')
makedepends=('subversion' 'unzip')
source=('svn://svn.macaulay2.com/Macaulay2/release-branches/1.5/M2' 'files' 'install-sh' 'config.sub' 'config.guess')

pkgver() {
  cd "$SRCDEST"/M2
  svnversion | tr -d [A-z]
}

build() {
  export CPPFLAGS="$CPPFLAGS -I/usr/include/singular -I/usr/include"
  export LDFLAGS="$LDFLAGS -L/usr/lib/singular -L/usr/lib"
  
  cd "$srcdir"/M2
  # Fix the broken build system... why doesn't it come with the file list?
  # One can generate the other three with automake --add-missing
  # However, automake would fail afterwards since M2 is not an automake program
  mkdir config
  cp ../files config/
  cp ../install-sh config/
  cp ../config.sub config/
  cp ../config.guess config/
  make

  # Build Macaulay2
  rm -rf BUILD/normal
  mkdir -p BUILD/normal
  cd BUILD/normal
  ../../configure LIBS="-lgmp" --prefix=/usr \
    --libexecdir='${prefix}'/lib/Macaulay2 \
    --enable-shared --enable-download \
    --enable-pari --enable-frobby \
    --with-unbuilt-programs=gfan

  sed -i -r 's/^\tobjdump(.*)/#\tobjdump\1/g' Macaulay2/bin/Makefile
  sed -i -r 's/^\techo(.*)/#\techo\1/g' Macaulay2/bin/Makefile
  make -j1
}

package() {
  cd "$srcdir"/M2/BUILD/normal
  make DESTDIR="$pkgdir" install

  cd "$pkgdir"/usr/share
  mv info info-mac
  mkdir info
  mv info-mac info/Macaulay2
  rm -rf "$pkgdir/usr/lib/Macaulay2/*Linux*/lib"
}

md5sums=('SKIP' 'cda8ec431acd6f7e0aea7f5fb418ee2c' '6e5fe73723cd40a28adc5b7b5650c8d1' 'c502a1505a963ef440c39095775bf2b0' '9c01fa8c4554cb2c7b92c95dfa0dbfcf')
