# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=macaulay2
pkgver=11633.5c7d042
pkgrel=1
pkgdesc="Software system for algebraic geometry and commutative algebra"
arch=('i686' 'x86_64')
url="http://www.math.uiuc.edu/Macaulay2/"
license=('GPL')
depends=('gcc-fortran' 'gc>=7.2' 'gdbm' 'gmp' 'mpfr' 'mpir' 'readline'  
      'ntl' 'lapack' 'scscp' 'singular-factory' 'frobby' 'glpk' 'cddlib'
      'gfan' 'gtest')
makedepends=('git' 'unzip' 'emacs')
source=('git+https://github.com/Macaulay2/M2')

pkgver() {
  cd "${srcdir}"/M2
  echo `git rev-list --count master`.`git rev-parse --short master`
}

build() {
  cd "$srcdir"/M2/M2
  sed -i -e '/lgfortran/d' configure.ac

  # Some provided examples will make M2 crash if the -q option is used.
  # Removing -q works, but it might require ~/.Macaulay2/init.m2 to be present.
  sed -i -e 's/" -q"/""/' Macaulay2/m2/html.m2
  sed -i -e 's/ -q//' Macaulay2/packages/Macaulay2Doc/test/Makefile.test.in

  make
  # Not sure why it crashes with the newest pari.
  ./configure LIBS="-lgmp" --prefix=/usr \
    --libexecdir='${prefix}'/lib/Macaulay2 \
    --enable-shared --enable-download \
    --enable-frobby --enable-pari \
    --enable-build-libraries=pari \
    --with-unbuilt-programs=gfan

  make -j1
}

package() {
  cd "$srcdir"/M2/M2
  make DESTDIR="$pkgdir" install

  cd "$pkgdir"/usr/share
  mv info info-mac
  mkdir info
  mv info-mac info/Macaulay2
  #rm -rf "$pkgdir/usr/lib/Macaulay2/*Linux*/lib"
}
md5sums=('SKIP')
