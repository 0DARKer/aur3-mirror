# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=ossec-agent
pkgver=2.5.1
pkgrel=1
pkgdesc="Open Source Host-based Intrusion Detection System"
arch=('i686' 'x86_64')
url="http://www.ossec.net/"
license=('GPL3')
depends=('glibc')
backup=('var/ossec/etc/ossec.conf')
install=ossec.install
options=('emptydirs')
source=(http://www.ossec.net/files/ossec-hids-$pkgver.tar.gz ossec.rc \
        install-remove_root.diff config)
md5sums=('94a7cabbba009728510a7a3e290ab200'
         'c5d59932a40c8782867627f29f99ace4'
         '87db29a06b6f5532f7f1d15c7d402d7d'
         'ff3c9d0cec6fbd907e4c301625cb4112')

_instdir=/var/ossec

build() {
  cd "$srcdir/ossec-hids-$pkgver"

  export USER_NO_STOP=yes
  export USER_LANGUAGE=en
  export USER_INSTALL_TYPE=agent
  export USER_DIR=$_instdir

  . "$srcdir/config" # load configuration

  # remove control if user is root
  patch -Np0 < "$srcdir"/install-remove_root.diff || return 1

  # fix placement of ossec-init.conf
  mkdir -p $pkgdir/etc
  sed -i "s|^OSSEC_INIT.*|OSSEC_INIT=\"$pkgdir/etc/ossec-init.conf\"|" src/init/shared.sh
  # change the install location
  sed -i "s|^DIR=.*|DIR=$pkgdir/$_instdir|" src/InstallServer.sh

  # change user names to user id's, appropriate users are created by the ossec.install
  sed -i -e 's|^USER=.*|USER=524|' -e 's|^USER_MAIL=.*|USER_MAIL=525|' \
    -e 's|^USER_REM=.*|USER_REM=526|' src/InstallServer.sh
  # change group name to gid, group is created by ossec.install
  sed -i 's|^GROUP=.*|GROUP=525|' src/InstallServer.sh

  ./install.sh

  # install rc initscript
  install -D -m755 "$srcdir"/ossec.rc "$pkgdir"/etc/rc.d/ossec
}
