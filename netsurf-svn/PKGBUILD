# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
pkgname=netsurf-svn
pkgver=12495
pkgrel=1
pkgdesc="Lightweight and fast web browser"
arch=('x86_64' 'i686')
url="http://www.netsurf-browser.org/"
license=("GPL")
depends=('libmng' 'curl' 'libglade' 'lcms' 'librsvg' 'lemon' 'cairo' 'desktop-file-utils')
makedepends=('re2c' 'curl' 'libnsbmp' 'libnsgif' 'libcss>=0.0.2' 'hubbub' 'libparserutils')
provides=('netsurf')
conflicts=('netsurf')
source=("$pkgname.desktop"
        "$pkgname.install"
        "http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png")
md5sums=('f9f63cd5cced618a5e05119a4194ba26'
         '69445d7ccf7df8c7ff134f04f36342b0'
         '4f28912e5df4b826b819e175265bcc9c')
install=("$pkgname.install")
_svntrunk=svn://svn.netsurf-browser.org/trunk/netsurf
_svnmod=netsurf-svn

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"

  cp -rT $_svnmod $_svnmod-build
  ln -sf $_svnmod-build netsurf # will not compile otherwise
  cd netsurf

  msg2 "Compiling Netsurf..."
  make

  msg2 "Creating wrapperscript..."
  echo "#!/bin/sh" > netsurf.sh
  echo "NETSURFRES=/usr/share/netsurf/res /usr/bin/nsgtk \$*" >> netsurf.sh
}

package() {
  cd "$srcdir/netsurf"

  msg2 "Packaging executables..."
  mkdir -p "$pkgdir/usr/bin"
  install nsgtk "$pkgdir/usr/bin"
  install -Dm755 netsurf.sh "$pkgdir/usr/bin/netsurf"
  install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/netsurf"

  msg2 "Packaging resources..."
  rm -rf gtk/res/.svn gtk/res/docs/.svn
  cp -RL gtk/res "$pkgdir/usr/share/netsurf"

  msg2 "Packaging application shortcut and icon"
  install -D -m644 "../$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -D -m644 "../icon_48x48_netsurf.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  msg2 "Cleaning up..."
  find "$pkgdir" -name ".svn" -print0 | xargs -0 rm -rf
}

# vim:set ts=2 sw=2 et:
