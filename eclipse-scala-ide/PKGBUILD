# Maintainer: Kristof Jozsa <kjozsa@fsdev.hu>

pkgname=eclipse-scala-ide
pkgver=2.1.0M3
pkgrel=4
pkgdesc="Scala IDE for Eclipse"
arch=('any')
url="http://www.scala-ide.org/"
license=('custom')
depends=('eclipse>=3.8')

source=(\
  http://download.scala-ide.org/sdk/e38/scala210/dev/update-site.zip
  LICENSE
)

md5sums=('c84ef5a2f5577231f7705a99eb599aae' '58b225f304aaf42c8b8738894a10cb96')

package() {
  install -D -m0644 $srcdir/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  _dest="${pkgdir}"/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse
  cd "${srcdir}"/site
  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf "${srcdir}"/site/${_feature}
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
