# Maintainer : Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=asterisk
pkgver=11.4.0
pkgrel=1
pkgdesc="A complete PBX solution"
arch=('i686' 'x86_64')
url="http://www.asterisk.org"
license=('GPL')
depends=('alsa-lib' 'speex' 'popt' 'libvorbis' 'curl') 
makedepends=('postgresql' 'unixodbc' 'sqlite3')
optdepends=('libpri' 'libss7' 'openr2' 'iksemel' 'radiusclient-ng' 'dahdi')
source=(http://downloads.asterisk.org/pub/telephony/asterisk/releases/asterisk-${pkgver}.tar.gz \
	${pkgname}.service \
	${pkgname}.logrotated)
install=${pkgname}.install
sha256sums=('081ba8de5f349ff23de8e000d8cda38a2ca34858174e9379b2662a732557e9d4'
	'5966a4b89a859caef73b1302e0cdac3057bc906aaf0b656d1032e05412235867'
	'caa24cfec5c6b4f8cea385269e39557362acad7e2a552994c3bc24080e3bdd4e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin
  make
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} samples
  
  mkdir -p ${pkgdir}/usr/share/doc/asterisk/examples
  for i in ${pkgdir}/etc/asterisk/*; do mv $i ${pkgdir}/usr/share/doc/asterisk/examples/; done
  
  mv ${pkgdir}/var/run ${pkgdir}
  sed -i -e "s/\/var\/run/\/run\/asterisk/" ${pkgdir}/usr/share/doc/asterisk/examples/asterisk.conf
  
  install -D -m 644 ${srcdir}/asterisk.logrotated ${pkgdir}/etc/logrotate.d/asterisk
  install -D -m 644 ${srcdir}/asterisk.service ${pkgdir}/usr/lib/systemd/system/asterisk.service
 }
