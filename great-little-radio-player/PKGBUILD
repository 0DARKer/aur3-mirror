# Maintainer: blast beat

pkgname=great-little-radio-player
pkgver=1.4.7
pkgrel=1
pkgdesc="A robust internet radio station streamer"
arch=('i686' 'x86_64')
url="https://sites.google.com/site/glrpgreatlittleradioplayer"
license=('GPL3')
depends=('qt4' 'phonon' 'freetype2')
optdepends=('phonon-gstreamer')
source=(http://sourceforge.net/projects/glrp/files/v1.4.7/greatlittleradioplayer_${pkgver}.tar.gz)
sha1sums=('df0609e756ea5ead3a402c84c7e4c9e9272254ac')

prepare() {
  cd greatlittleradioplayer_${pkgver}
  
  # Fix includes
  sed -i '/^#include/s|Phonon/|phonon/|' mainwindow.{h,cpp}
  sed -i '/^#include <phonon\/phonon>/d' mainwindow.cpp
}

build() {
  cd greatlittleradioplayer_${pkgver}
  qmake-qt4 PREFIX=/usr
  make
}

package() {
  cd greatlittleradioplayer_${pkgver}

  install -D GreatLittleRadioPlayer \
    "$pkgdir/opt/extras.ubuntu.com/glrp/GreatLittleRadioPlayer"

  cp -r changelog.txt glrp.svg stations.csv language styles \
    "$pkgdir/opt/extras.ubuntu.com/glrp/"

  install -Dm644 extras-greatlittleradioplayer.desktop \
    "$pkgdir/usr/share/applications/extras-greatlittleradioplayer.desktop"
}

