# Contributor: Jakob Gruber <jakob.gruber@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Filip Dvorak <fila@pruda.com>
# Contributor: 3ED_0 (AUR)
# Maintainer: SanskritFritz (gmail)

pkgname=firehol
pkgver=2.0.0_pre9
_pkgver=2.0.0-pre9
pkgrel=1
pkgdesc="The iptables stateful packet filtering firewall builder."
url="http://firehol.sourceforge.net"
arch=('any')
license=('GPL')
depends=('iptables' 'gawk' 'iproute')
backup=('etc/firehol/firehol.conf' 'etc/firehol/fireqos.conf')
install='firehol.install'
source=("http://firehol.org/download/releases/v${_pkgver}/${pkgname}-${_pkgver}.tar.xz"
        "firehol.service"
        "fireqos.service")

build() {
  cd "$srcdir/$pkgname-$_pkgver"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib
  
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"

  make install DESTDIR="$pkgdir"

  install -d -m755 "$pkgdir"/usr/lib/systemd/system/
  install -m644 "$srcdir"/fire{hol,qos}.service "$pkgdir"/usr/lib/systemd/system/

  touch "$pkgdir"/etc/firehol/fire{hol,qos}.conf
}
md5sums=('1d3dbf392b7502e7815c9992a0dd9bdb'
         '6c6571af548273e1f172313e366532df'
         'ae9fc18b19a69149108e9f4ab9ba5de9')
