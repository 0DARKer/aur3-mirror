# Maintainer: Yosef Or Boczko <yoseforb@gnome.org>

_pkgname=atomix
pkgname=$_pkgname-git
pkgver=3.15.4.r3.g2be6350
pkgrel=1
_realver=3.15.4
pkgdesc="Build molecules out of single atoms"
arch=('i686' 'x86_64')
license=('GPLv2')
depends=('gtk3' "glib2" 'libxml2' "gdk-pixbuf2")
makedepends=('intltool' 'git' 'gnome-common')
conflicts=('gnome-games' 'atomix')
replaces=('gnome-games' 'atomix')
provides=("atomix=$_realver")
options=('!emptydirs' '!libtool')
url="https://wiki.gnome.org/Apps/Atomix"
groups=('gnome-extra')
source=('git://git.gnome.org/atomix')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed -E 's/^ATOMIX_//;s/_/./g;s/([^-]*-g)/r\1/;s|-|.|g'
}

prepare() {
  cd "$srcdir/$_pkgname"
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
