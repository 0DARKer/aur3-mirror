# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com
# Maintainer: Nano77 <nanoarch77@gmail.com>

pkgname=gnome-shell-native-theme
_pkgname=gnome-shell
pkgver=3.2.2.1
pkgrel=1
pkgdesc="GnomeShell patched to using theme without extension"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeShell"
license=('GPL2')
depends=('mutter' 'gjs' 'libcroco' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'libpulse' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'nautilus' 'telepathy-mission-control' 'unzip')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('network-manager-applet: shell integration for networkmanager' 'gdm3setup-native-theme: Select the GDM GnomeShell Theme')
conflicts=('gnome-shell')
provides=('gnome-shell=$pkgver')
options=('!libtool' '!emptydirs')
install=gnome-shell.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/${pkgver%.*.*}/$_pkgname-$pkgver.tar.xz
        gnome-shell-native-theme.patch)
sha256sums=('209363084ef2d58777d70d8430e4659b058cf854c4b9766f3019d69ffea34d6f'
            'd591aec9f486d05a49bcc11b5f949f78fb7029226d6e009d91cd040a70974520')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  patch -Np1 -i ${srcdir}/gnome-shell-native-theme.patch

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-shell \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" --domain gnome-shell "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
