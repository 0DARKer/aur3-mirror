# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Flamelab <panosfilip@gmail.com
# Maintainer: Nano77 <nanoarch77@gmail.com>

pkgname=gnome-shell-native-theme
pkgver=3.2.1
pkgrel=3
pkgdesc="GnomeShell patched to using theme without extension"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeShell"
license=('GPL2')
depends=('mutter' 'gjs' 'libcroco' 'gnome-bluetooth' 'gnome-desktop' 'gnome-menus' 'libpulse' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'nautilus' 'telepathy-mission-control')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('network-manager-applet: shell integration for networkmanager' 'gdm3setup-native-theme: Select the GDM GnomeShell Theme')
conflicts=('gnome-shell')
provides=('gnome-shell=3.2.1')
options=('!libtool' '!emptydirs')
install=gnome-shell.install
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-shell/3.2/gnome-shell-$pkgver.tar.xz
	gnome-shell-native-theme.patch)
sha256sums=('ac05e3133bd279c84812d8d3d8e362f8b33563cdc3e7a6aefa9c0b1c2aeab0eb'
	'd591aec9f486d05a49bcc11b5f949f78fb7029226d6e009d91cd040a70974520')

build() {
  cd "${srcdir}/gnome-shell-${pkgver}"

  patch -Np1 -i ${srcdir}/gnome-shell-native-theme.patch

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-shell \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/gnome-shell-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/gnome-shell.schemas" --domain gnome-shell ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
