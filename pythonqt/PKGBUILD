# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=PythonQt
pkgname=pythonqt
pkgver=2.0.1
pkgrel=1
pkgdesc="A dynamic Python binding for the Qt framework to embed Python code into C++ Qt applications."
arch=(i686 x86_64)
url="http://pythonqt.sourceforge.net/"
license=('LGPL')
depends=('qt' 'python2')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$_pkg-$pkgver/$_pkg$pkgver.zip")
md5sums=('4a9a639ed88fe2bfede08649c100da6a')

build() {
  cd "$srcdir/$_pkg$pkgver"

  # set python version to 2.7
  sed -i "s|2.6|2.7|" build/python.prf

  # remove cocoa references
  sed -i "s| *Cocoa.*,||" \
    generated_cpp/com_trolltech_qt_core/com_trolltech_qt_core0.h

  qmake
  make
}

package() {
  cd "$srcdir/$_pkg$pkgver"

  # bins
  install -d "$pkgdir/usr/bin"
  install -Dm755 lib/{CPP*,Py*} "$pkgdir/usr/bin"

  # libs
  install -d "$pkgdir/usr/lib"
  cp -a lib/lib* "$pkgdir/usr/lib"

  # headers
  install -d "$pkgdir/usr/include/$_pkg/gui"
  install -Dm644 src/$_pkg*.h "$pkgdir/usr/include/$_pkg"
  install -Dm644 src/gui/*.h "$pkgdir/usr/include/$_pkg/gui"
  install -Dm644 extensions/PythonQt_QtAll/PythonQt_QtAll.h \
    "$pkgdir/usr/include/$_pkg"

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 doxygen/{header.html,PythonQt.jpg,stylesheet.css} \
    "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
