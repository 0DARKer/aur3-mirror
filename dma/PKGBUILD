# Contributor: Michael Krauss <hippodriver@gmx.net>
# $Id: PKGBUILD,v 1.2 2010/12/25 18:46:11 andres Exp $

pkgname="dma"
pkgver=0.0.2010.06.17
pkgrel=4
pkgdesc="DragonFly BSD mail transport agent"
url="http://www.dragonflybsd.org/"
license=('BSD')
makedepends=('ed')
depends=('openssl')
backup=('etc/dma/auth.conf' 'etc/dma/dma.conf' 'etc/dma/virtusertable')
install="dma.install"
arch=('i686' 'x86_64')
source=("http://devel.ringlet.net/mail/dma/dma-$pkgver.tar.gz")
md5sums=('929fbfc814a0b52900533e034d6d67e4')

build() {
	cd $pkgname-$pkgver

	CPPFLAGS+=" -DNEED_REALLOCF -DNEED_STRLCPY -DNEED_GETPROGNAME" \
		make -f Makefile.plain
}

package() {
	cd $pkgname-$pkgver

	install -Dm755 dma $pkgdir/usr/sbin/dma
	install -Dm644 dma.8 $pkgdir/usr/share/man/man8/dma.8

	install -dm755 $pkgdir/etc/dma
	install -dm755 $pkgdir/var/spool/dma
	install -dm755 $pkgdir/usr/share/licenses/$pkgname

	for i in auth.conf dma.conf virtusertable; do
		install -Dm644 $i $pkgdir/etc/dma/$i
	done

	ed dma.c > $pkgdir/usr/share/licenses/$pkgname/LICENSE <<-\!
		/^\/\*/,/^ \*\//s/^...\?//
		.,$d
		a

		.
		,p
		Q
	!
}
