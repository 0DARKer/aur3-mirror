# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=mingw32-libxml2
pkgver=2.8.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="libxml2 (mingw32)"
depends=('mingw32-gettext' 'mingw32-zlib')
makedepends=('mingw32-gcc')
options=('!buildflags' '!strip')
license=('LGPL')
url="http://www.xmlsoft.org/"
source=("ftp://xmlsoft.org/libxml/libxml2-${pkgver}.tar.gz")
md5sums=('c62106f02ee00b6437f0fb9d370c1093')

build()
{
  cd "${srcdir}/libxml2-${pkgver}"

  unset LDFLAGS
  export CFLAGS="${CFLAGS} -mms-bitfields"

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build="${CHOST}" \
    --without-python \
    --without-threads

  make
}

package()
{
  cd "${srcdir}/libxml2-${pkgver}"

  make install DESTDIR="${pkgdir}"

  rm -rf "${pkgdir}/usr/i486-mingw32"/{share/{doc,gtk-doc},man}
  rm -f "${pkgdir}/usr/i486-mingw32/lib/charset.alias"
}
