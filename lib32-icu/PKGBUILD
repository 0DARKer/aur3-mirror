# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=icu
pkgname=lib32-$_pkgbasename
pkgver=4.6.1
pkgrel=1
pkgdesc="International Components for Unicode library (32 bit)"
arch=('x86_64')
url="http://www.icu-project.org/"
license=('custom:icu')
depends=('lib32-gcc-libs')
makedepends=('gcc-multilib' 'links')
source=("http://download.icu-project.org/files/icu4c/${pkgver}/icu4c-${pkgver//./_}-src.tgz"
	'icu.8198.revert.icu5431.patch')
md5sums=('da64675d85f0c2191cef93a8cb5eea88'
         'ebd5470fc969c75e52baf4af94a9ee82')

build() {
  export CFLAGS="$CFLAGS -m32"
  export CXXFLAGS="$CXXFLAGS -m32"
  
  cd ${srcdir}/icu/source
  
  # fix Malayalam encoding https://bugzilla.redhat.com/show_bug.cgi?id=654200
  patch -Rp3 -i ${srcdir}/icu.8198.revert.icu5431.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
  	--mandir=/usr/share/man --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/icu/source
  
  make -j1 DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,sbin,share}
  # Install license
  links -dump ${srcdir}/icu/license.html > LICENSE
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${_pkgbasename}/LICENSE
}
