# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

pkgname=lib32-icu
pkgver=4.8
pkgrel=1
pkgdesc="International Components for Unicode library (32 bit)"
arch=('x86_64')
url="http://www.icu-project.org/"
license=('custom:icu')
depends=('lib32-gcc-libs' 'icu')
makedepends=('gcc-multilib' 'links')
source=("http://download.icu-project.org/files/icu4c/${pkgver}/icu4c-${pkgver//./_}-src.tgz"
	'icu.8198.revert.icu5431.patch')
md5sums=('e78194c49d7fd8131353aa180d11b789'
         'ebd5470fc969c75e52baf4af94a9ee82')

build() {  
  cd ${srcdir}/icu/source
  
  # fix Malayalam encoding https://bugzilla.redhat.com/show_bug.cgi?id=654200
  patch -Rp3 -i ${srcdir}/icu.8198.revert.icu5431.patch
  
  ./configure --prefix=/usr --sysconfdir=/etc \
  	--mandir=/usr/share/man --libdir=/usr/lib32 \
  	CC='gcc -m32' CXX='g++ -m32' \
  	PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  	
  make
}

package() {
  cd ${srcdir}/icu/source
  
  make -j1 DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,sbin,share}
  
  # Install license
  install -d m644 ${pkgdir}/usr/share/licenses/$pkgname
  links -dump ${srcdir}/icu/license.html > ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
