# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=opennebula-unstable
_pkgname=opennebula
pkgver=3.5.80
pkgrel=1
pkgdesc="Unstable, development version of OpenNebula â€” a virtual management infrastructure as a service (IaaS) toolkit for cloud computing (NOTE: Read the PKGBUILD!)"
arch=('i686' 'x86_64')
url="http://opennebula.org/software:rnotes:rn-rel3.6beta"
license=('Apache')
depends=('ruby>=1.8.7' 'xmlrpc-c-abyss>=1.06' 'openssl>=0.9.8' 'sqlite3>=3.6' 'openssh' 'libxml2>=2.7'
  'curl' 'libxslt' 'expat' 'cdrkit')
makedepends=('xmlrpc-c-abyss>=1.06' 'pkgconfig' 'scons>=0.98')
optdepends=('nfs-utils: for using the shared file system storage model'
  'mysql>=5.1: optional replacement for SQLite as the DB back-end'
  'libmysqlclient>=5.1: required if using MySQL instead of SQLite')
provides=('opennebula')
install=opennebula-unstable.install
changelog=ChangeLog
source=(http://dev.opennebula.org/packages/$_pkgname-$pkgver/$_pkgname-$pkgver.tar.gz 
	oned
  chown_fix.patch
  var_run.patch)
sha512sums=('f65f37f775cf93837e64ea3fba6f345f130277fb777245eac764714dec292b109738597eef148edb010434334a69d4b570a6f7b87cc2ee4503c073b75362af59'
            'c72d136d8b83e3c97d94e5898c0982f3f66c32caac1dcd4a8fd88b06a99bd72447e96642d6129ace6502ada15668c9f32345f370b9bc21e68097edb9b1ecc7bd'
            'be6615f761f13623f642633294f763474bad302911b2b6d2c59e9abff3c89c6cb4bef440437dd255eb1ca4b809944b249acc5133439c314d98e38735dca09eff'
            'c5ab132bf2f13ac055678b1b1bf940bdf8b141331c0d5dee326f532b818b2f59c247488cb95eee5d4ded5b6cb95ad745897bda6b0c215b87dfc90a8fbf825029')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  #############################################################################
  ##                                                                         ##
  ## It is highly recommended that you read the documentation and tweak the  ##
  ##   PKGBUILD accordingly:                                                 ##
  ## http://opennebula.org/documentation:rel3.6:compile                      ##
  ##                                                                         ##
  ## This package assumes a self-contained install. If you do NOT want a     ##
  ##   self-contained install, then remove `-d /srv/cloud/one` from the      ##
  ##   package() function and MAKE SURE you properly change the appropriate  ##
  ##   sections of opennebula.install                                        ##
  ##                                                                         ##
  #############################################################################

  # This builds the vanilla OpenNebula package. Tweak this line as desired.
  scons
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  install -D -m755 "$srcdir"/oned "$pkgdir"/etc/rc.d/one

  # Patch upstream install script to not attempt to chown the install
  # directories because `makepkg` will otherwise fail on a fresh installation.
  # We do our own chown in post_install().
  patch < "$srcdir/chown_fix.patch"
  
  # Patch upstream install script to use /run/one instead of /var/run/one.
  patch < "$srcdir/var_run.patch"

  # This checks to see whether OpenNebula is currently installed.
  if [[ ("$(pacman -Qq ${pkgname})" == "${pkgname}") || ("$(pacman -Qq ${_pkgname})" == "${_pkgname}") ]]; then
    # Use -k when running ./install.sh to keep previous configuration files
    DESTDIR="$pkgdir/" ./install.sh -k -u oneadmin -g cloud -d /srv/cloud/one
  else
    # Do not use -k when running ./install.sh for new installations
    DESTDIR="$pkgdir/" ./install.sh -u oneadmin -g cloud -d /srv/cloud/one
  fi
}

# vim:set ts=2 sw=2 et:
