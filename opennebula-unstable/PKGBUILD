# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=opennebula-unstable
_pkgname=opennebula
pkgver=3.9.90
pkgrel=2
pkgdesc="Bleeding edge version of OpenNebula â€” an IaaS toolkit for cloud computing (NOTE: Read the PKGBUILD!)"
arch=('i686' 'x86_64')
url='http://opennebula.org/software:rnotes:rn-rel4.0'
license=('Apache')
depends=('ruby>=1.8.7'
         'xmlrpc-c>=1.06'
         'openssl>=0.9.8'
         'sqlite3>=3.6'
         'openssh'
         'libxml2>=2.7'
         'curl'
         'libxslt'
         'expat'
         'cdrkit'
         'log4cpp>=1.0')
makedepends=('xmlrpc-c>=1.06'
             'pkgconfig'
             'scons>=0.98')
optdepends=('nfs-utils: for using the shared file system storage model'
            'mariadb>=5.1: optional replacement for SQLite as the DB back-end'
            'libmariadbclient>=5.1: required if using MariaDB/MySQL instead of SQLite')
provides=('opennebula')
install=opennebula-unstable.install
changelog=ChangeLog
source=("http://dev.opennebula.org/packages/$_pkgname-$pkgver/$_pkgname-$pkgver-2.tar.gz"
        'opennebula.service'
        'chown_fix.patch')

build() {
	cd "$srcdir/$_pkgname-$pkgver-2"
	##########################################################################
	##                                                                      ##
	## It is highly recommended that you read the documentation and tweak   ##
	##     the PKGBUILD accordingly:                                        ##
	## http://opennebula.org/documentation:rel4.0:compile                   ##
	##                                                                      ##
	## This package assumes a self-contained install. If you do NOT want a  ##
	##     self-contained install, then remove `-d /srv/cloud/one` from the ##
	##     package() function and MAKE SURE you properly change the         ##
	##     appropriate sections of opennebula-unstable.install              ##
	##                                                                      ##
	##########################################################################

	# This builds the vanilla OpenNebula package. Tweak this line as desired.
	scons
}

package() {
	cd "$srcdir/$_pkgname-$pkgver-2"

	install -D -m644 "$srcdir"/opennebula.service "$pkgdir"/usr/lib/systemd/system/opennebula.service

	# Patch upstream install script to not attempt to chown the install
	# directories because `makepkg` will otherwise fail on a fresh installation.
	# We do our own chown in post_install().
	patch < "$srcdir/chown_fix.patch"

	# This checks to see whether OpenNebula is currently installed. To avoid
	# a potentially scary message, errors are sent to /dev/null
	if [[ ("$(pacman -Qq ${pkgname} 2>/dev/null)" == "${pkgname}") || ("$(pacman -Qq ${_pkgname} 2>/dev/null)" == "${_pkgname}") ]]; then
		# Use -k when running ./install.sh to keep previous configuration files
		DESTDIR="$pkgdir/" ./install.sh -k -u oneadmin -g cloud -d /srv/cloud/one
	else
		# Do not use -k when running ./install.sh for new installations
		DESTDIR="$pkgdir/" ./install.sh -u oneadmin -g cloud -d /srv/cloud/one
	fi
}
sha512sums=('8c7300b564082d94dc592098acdb07bdb4fc893cef0db4e0f259dd5a252cde34f1d7c98d598e4fdd87a52f0b31847e002406935a75d28f58788e0241e2e3a136'
            '00db19d355a789fb27320d16917804354fc824c99a851e755a58943824377f87f158e0e560773957acfc3f959a2937462391271d309aff4e6aabb50cb964009d'
            'd0c8f36f8c89fc35819de51710b01c96118a1f2e56c4bc80bdda1971c7aaa402946586edc1d825cbd4e587fb9858de4c31d0b05777360900da3e30c7339f6506')
