# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=opennebula-unstable
_pkgname=opennebula
pkgver=3.3.0
pkgrel=2
pkgdesc="Unstable, development version of OpenNebula -- a virtual management infrastructure as a service (IaaS) tool for cloud computing (NOTE: Read the PKGBUILD!)"
arch=('i686' 'x86_64')
url="http://opennebula.org/"
license=('Apache')
depends=('ruby>=1.8.7' 'openssl' 'sqlite3' 'openssh' 'libxml2'
  'curl' 'libxslt' 'expat' 'cdrkit')
makedepends=('xmlrpc-c-abyss' 'pkgconfig' 'scons')
optdepends=('nfs-utils: for using the shared file system storage model'
  'mysql: optional replacement for SQLite as the DB back-end'
  'libmysqlclient: required if using MySQL instead of SQLite')
provides=('opennebula')
install=opennebula-unstable.install
changelog=ChangeLog
source=(http://dev.opennebula.org/packages/$_pkgname-$pkgver/$_pkgname-$pkgver.tar.gz 
	oned)
sha512sums=('5d5bf9acf1664867f794ebabf6db95cb49167ba6e6c825ea7322eeb7aa568ad50b01f59d6770285b0f9907ea1eb2bc13bd1ac7bc673eb30132d5619493ce54d3'
            'c72d136d8b83e3c97d94e5898c0982f3f66c32caac1dcd4a8fd88b06a99bd72447e96642d6129ace6502ada15668c9f32345f370b9bc21e68097edb9b1ecc7bd')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  #############################################################################
  ##                                                                         ##
  ## It is highly recommended that you read the documentation and tweak the  ##
  ##   PKGBUILD accordingly:                                                 ##
  ## http://opennebula.org/documentation:rel3.4:compile                      ##
  ##                                                                         ##
  ## This package assumes a self-contained install. If you do NOT want a     ##
  ##   self-contained install, then remove `-d /srv/cloud/one` from the      ##
  ##   package() function and MAKE SURE you properly change the appropriate  ##
  ##   sections of opennebula.install                                        ##
  ##                                                                         ##
  #############################################################################

  # This builds the vanilla OpenNebula package. Tweak this line as desired.
  scons
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  install -D -m755 "$srcdir"/oned "$pkgdir"/etc/rc.d/one

  # This checks to see whether OpenNebula is currently installed.
  if [[ ("$(pacman -Qq ${pkgname})" == "${pkgname}") || ("$(pacman -Qq ${_pkgname})" == "${_pkgname}") ]]; then
    # Use -k when running ./install.sh to keep previous configuration files
    DESTDIR=$pkgdir ./install.sh -k -u oneadmin -g cloud -d /srv/cloud/one
  else
    # Do not use -k when running ./install.sh for new installations
    DESTDIR=$pkgdir ./install.sh -u oneadmin -g cloud -d /srv/cloud/one
  fi
}

# vim:set ts=2 sw=2 et:
