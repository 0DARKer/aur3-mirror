# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=opennebula-unstable
_pkgname=opennebula
pkgver=4.4.0
pkgrel=1
pkgdesc="Bleeding edge version of OpenNebula â€” an IaaS toolkit for cloud computing (NOTE: Read the PKGBUILD!)"
arch=('i686' 'x86_64')
url='http://opennebula.org/software:rnotes:rn-rel4.4'
license=('Apache')
depends=('ruby>=1.8.7'
         'xmlrpc-c>=1.31'
         'openssl>=0.9.8'
         'sqlite3>=3.6'
         'openssh'
         'libxml2>=2.7'
         'curl'
         'libxslt'
         'expat'
         'cdrkit'
         'log4cpp>=1.0')
makedepends=('xmlrpc-c>=1.31'
             'pkgconfig'
             'scons>=0.98')
optdepends=('nfs-utils: for using the shared file system storage model'
            'mariadb>=5.1: optional replacement for SQLite as the DB back-end'
            'libmariadbclient>=5.1: required if using MariaDB/MySQL instead of SQLite')
provides=('opennebula')
install=opennebula-unstable.install
changelog=ChangeLog
source=("http://downloads.opennebula.org/packages/${_pkgname}-${pkgver}/${_pkgname}-${pkgver}.tar.gz"
        'opennebula.service'
        'chown_fix.patch')
sha512sums=('b131ea5903323e03e7e498e37a96ef6d721a8a5f18919763e8e932e42e1ebab2f8454cbff3dab476084d5b873ed42f1eb3dbe808067bcbdd8a6f2528a9e30098'
            '00db19d355a789fb27320d16917804354fc824c99a851e755a58943824377f87f158e0e560773957acfc3f959a2937462391271d309aff4e6aabb50cb964009d'
            '9fcb103c228fcadfa4c6e6c2246600e9581d34405cb76888f8f52c3bd321e8c210d8d8ccf51e4dbf01203f432532a5b33fe90bf97b5f4f2383a984a03a4c4ca4')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	# Patch upstream install script to not attempt to chown the install
	# directories because `makepkg` will otherwise fail on a fresh installation.
	# We do our own chown in post_install().
	patch < "${srcdir}/chown_fix.patch"
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	##########################################################################
	##                                                                      ##
	## It is highly recommended that you read the documentation and tweak   ##
	##     the PKGBUILD accordingly:                                        ##
	## http://opennebula.org/documentation:rel4.4:compile                   ##
	##                                                                      ##
	## This package assumes a self-contained install. If you do NOT want a  ##
	##     self-contained install, then remove `-d /srv/cloud/one` from the ##
	##     package() function and MAKE SURE you properly change the         ##
	##     appropriate sections of opennebula-unstable.install              ##
	##                                                                      ##
	##########################################################################

	# This builds the vanilla OpenNebula package. Tweak this line as desired.
	scons new_xmlrpc=yes
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	install -D -m644 "${srcdir}/opennebula.service" "${pkgdir}/usr/lib/systemd/system/opennebula.service"

	# This checks to see whether OpenNebula is currently installed. To avoid
	# a potentially scary message, errors are sent to /dev/null
	if [[ ("$(pacman -Qq ${pkgname} 2>/dev/null)" == "${pkgname}") || ("$(pacman -Qq ${_pkgname} 2>/dev/null)" == "${_pkgname}") ]]; then
		# Use -k when running ./install.sh to keep previous configuration files
		# Note: It is highly recommended to not keep the oned.conf file.
		DESTDIR="${pkgdir}/" ./install.sh -k -u oneadmin -g cloud -d /srv/cloud/one
	else
		# Do not use -k when running ./install.sh for new installations
		DESTDIR="${pkgdir}/" ./install.sh -u oneadmin -g cloud -d /srv/cloud/one
	fi
}
