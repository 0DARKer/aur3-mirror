# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=opennebula-unstable
_pkgname=opennebula
pkgver=3.3.80
pkgrel=1
pkgdesc="Unstable, development version of OpenNebula -- a virtual management infrastructure as a service (IaaS) tool for cloud computing (NOTE: Read the PKGBUILD!)"
arch=('i686' 'x86_64')
url="http://opennebula.org/"
license=('Apache')
depends=('ruby>=1.8.7' 'openssl' 'sqlite3' 'openssh' 'libxml2'
  'curl' 'libxslt' 'expat' 'cdrkit')
makedepends=('xmlrpc-c-abyss' 'pkgconfig' 'scons')
optdepends=('nfs-utils: for using the shared file system storage model'
  'mysql: optional replacement for SQLite as the DB back-end'
  'libmysqlclient: required if using MySQL instead of SQLite')
provides=('opennebula')
install=opennebula-unstable.install
changelog=ChangeLog
source=(http://dev.opennebula.org/packages/$_pkgname-$pkgver/$_pkgname-$pkgver.tar.gz 
	oned
  http://dev.opennebula.org/projects/opennebula/repository/revisions/one-3.4/raw/src/onedb/3.3.0_to_3.3.80.rb)
sha512sums=('0957a4e4265fa3964514baed615bb5d4c48a96626d1378e2b522400b5bfab4714fa4fbbf2f534b587b77e4c320b9224336c5a7a74bedb501afbc187c3665a529'
            'c72d136d8b83e3c97d94e5898c0982f3f66c32caac1dcd4a8fd88b06a99bd72447e96642d6129ace6502ada15668c9f32345f370b9bc21e68097edb9b1ecc7bd'
            '6b3a51a8274372d4d0b11dda0d7dff0987eb01e7b5fa35db0dfa1f58ac0b667f7a9f91ba64e004300595990d6abd1fd3e5bbe70e366c2a713bc98bd47e0e752b')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  #############################################################################
  ##                                                                         ##
  ## It is highly recommended that you read the documentation and tweak the  ##
  ##   PKGBUILD accordingly:                                                 ##
  ## http://opennebula.org/documentation:rel3.4:compile                      ##
  ##                                                                         ##
  ## This package assumes a self-contained install. If you do NOT want a     ##
  ##   self-contained install, then remove `-d /srv/cloud/one` from the      ##
  ##   package() function and MAKE SURE you properly change the appropriate  ##
  ##   sections of opennebula.install                                        ##
  ##                                                                         ##
  #############################################################################

  # This builds the vanilla OpenNebula package. Tweak this line as desired.
  scons
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  install -D -m755 "$srcdir"/oned "$pkgdir"/etc/rc.d/one

  # This checks to see whether OpenNebula is currently installed.
  if [[ ("$(pacman -Qq ${pkgname})" == "${pkgname}") || ("$(pacman -Qq ${_pkgname})" == "${_pkgname}") ]]; then
    # Use -k when running ./install.sh to keep previous configuration files
    DESTDIR=$pkgdir ./install.sh -k -u oneadmin -g cloud -d /srv/cloud/one
  else
    # Do not use -k when running ./install.sh for new installations
    DESTDIR=$pkgdir ./install.sh -u oneadmin -g cloud -d /srv/cloud/one
  fi

  #############################################################################
  ##                                                                         ##
  ## Version 3.3.80 contains two minor bugs in the onedb migrator file. The  ##
  ## updated .rb file needs to be copied to                                  ##
  ##   /usr/lib/one/ruby/onedb/3.3.0_to_3.3.80.rb                            ##
  ##                                                                         ##
  ## You must ensure that this file goes in the proper location depending    ##
  ## on your deployment type (self-contained vs. system-wide)!               ##
  ##                                                                         ##
  ## Read docs: http://opennebula.org/documentation:rel3.4:upgrade           ##
  ##                                                                         ##
  #############################################################################

  # Is -m644 the appropriate mode? If the file does not work for you, try changing the modes.
  # Unfortunately, I don't have a 3.3.80 setup available to test this on.
  install -m644 "$srcdir"/3.3.0_to_3.3.80.rb "$pkgdir"/usr/lib/one/ruby/onedb/3.3.0_to_3.3.80.rb
}

# vim:set ts=2 sw=2 et:
