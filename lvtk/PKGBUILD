# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=lvtk
pkgver=1.0.2
pkgrel=1
pkgdesc="A set C++ wrappers around the LV2 C API"
arch=(i686 x86_64)
url="http://lvtoolkit.org/"
license=('GPL')
depends=('lv2')
makedepends=('python2' 'boost' 'gtkmm')
optdepends=('gtkmm: lv2 sample plugins')
source=("${url}wp-content/uploads/2012/10/$pkgname-$pkgver.tar.gz"
        "$pkgname-boost.patch")
md5sums=('9994b493e4a81e8873a403c728d472cf'
         '356ef2212995ed590b2b56517f1af187')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # boost 1.50 patch
  patch -p1 -i "$srcdir/${source[1]}"

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir/" python2 waf install
}

# vim:set ts=2 sw=2 et:
