# Contributor: Splex
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Jérémie Astor <astor.jeremie@wanadoo.fr>

_pkgname=xombrero
pkgname=xombrero-git
pkgver=20130405
pkgrel=2
pkgdesc="minimalist web browser"
arch=('i686' 'x86_64')
url="http://opensource.conformal.com/wiki/xombrero"
license=('custom:ISC')
depends=('webkitgtk3' 'libbsd' 'desktop-file-utils')
makedepends=('git')
provides=('xombrero')
conflicts=('xxxterm' 'xombrero' 'xombrero3')
source=('git://opensource.conformal.com/xombrero.git#branch=master')
md5sums=('SKIP')


pkgver() {
	cd $_pkgname
	git log -1 --date=short | grep -Eo '[[:digit:]]{1,4}\-[[:digit:]]{1,2}\-[[:digit:]]{1,2}' | sed -e 's#-##g'
	#git describe | sed 's/^v//;s/-/./g'
}

build() {
        cd "$srcdir/$_pkgname"
        sed -i 's|/etc/ssl/cert.pem|/etc/ssl/certs/ca-certificates.crt|' xombrero.conf
        sed -i 's|https://www.cyphertite.com|http://archlinux.org|' xombrero.{c,conf}
        sed -i 's|/usr/local|/usr|' xombrero.{h,conf}
        cd "linux"
        sed -i 's|LIBS= gtk+-2.0|& javascriptcoregtk-1.0|' Makefile
        sed -i 's/gnutls/& libbsd/' Makefile
        make javascript.h
        make PREFIX="/usr"
}
 
package() {
        cd "$srcdir/$_pkgname/"
        make PREFIX="/usr" DESTDIR="$pkgdir" install -C linux GTK-VERSION=gtk3
        install -Dm644 xombrero.conf "$pkgdir/etc/xombrero.conf"
}