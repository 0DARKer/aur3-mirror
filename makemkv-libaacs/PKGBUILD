# Contributor: Olaf Bauer <hydro@freenet.de>
# Maintainer: Frank Phillips <frankalso@gmail.com>

pkgname=makemkv-libaacs
pkgver=1.8.7
pkgrel=1
pkgdesc="MakeMKV with libaacs emulation"
arch=('i686' 'x86_64')
url="http://www.makemkv.com/forum2/viewtopic.php?f=3&t=7009"
license=('LGPL' 'MPL' 'custom')
depends=('qt4' 'ffmpeg')
provides=('makemkv' 'libaacs')
conflicts=('makemkv' 'libaacs' 'libaacs-git')
if [ "$CARCH" = "x86_64" ]; then
  optdepends=('lib32-glibc: dts support')
fi
install=makemkv.install
source=(http://www.makemkv.com/download/makemkv-bin-${pkgver}.tar.gz
        http://www.makemkv.com/download/makemkv-oss-${pkgver}.tar.gz)
md5sums=('1709dcdfbe6a8bb37b9b995fdf02052f'
         '5f04541e1ce74884b482703006378a00')

build() {
  cd $srcdir/makemkv-oss-${pkgver}
  make -f makefile.linux
}

package() {
  cd $srcdir/makemkv-oss-${pkgver}
  make -f makefile.linux DESTDIR="$pkgdir" install

  cd $srcdir/makemkv-bin-${pkgver}
  install -d $pkgdir/usr/bin/
  install -t $pkgdir/usr/bin/ bin/i386/mmdtsdec
  if [ "$CARCH" = "x86_64" ]; then
    install -t $pkgdir/usr/bin/ bin/amd64/makemkvcon
  else
    install -t $pkgdir/usr/bin/ bin/i386/makemkvcon
  fi
  install -d $pkgdir/usr/share/MakeMKV
  install -t $pkgdir/usr/share/MakeMKV src/share/makemkv_*.mo.gz src/share/*.mmcp.xml
  
  install -d $pkgdir/usr/lib
  ln -s libmmbd.so.0 $pkgdir/usr/lib/libaacs.so.0
  ln -s libmmbd.so.0 $pkgdir/usr/lib/libbdplus.so.0

  install -Dm 644 src/eula_en_linux.txt $pkgdir/usr/share/licenses/makemkv/eula_en_linux.txt
}
