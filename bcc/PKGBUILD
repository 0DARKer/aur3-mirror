# Maintainer: Andre Bartke <dev@bartke.cc>
 
_gccver=4.4.2

pkgname=bcc
pkgver=1.0.42
pkgrel=1
pkgdesc="Gaisler BCC cross-compiler for SPARC V8 LEON2 and LEON3 processors"
arch=('i686' 'x86_64')
url="http://www.gaisler.com/index.php/products/operating-systems/bcc"
license=('GPL')
options=('!strip')
conflicts=("bcc-dev")
source=("ftp://ftp.gaisler.com/bcc/bin/linux/sparc-elf-${_gccver}-${pkgver}.tar.bz2")
 
if test "$CARCH" == x86_64; then
  depends=("lib32-glibc" "lib32-zlib")
fi

package() {
  mkdir -p $pkgdir/usr/lib/cross-sparc-unknown-elf
  cp -r $srcdir/sparc-elf-$_gccver/* $pkgdir/usr/lib/cross-sparc-unknown-elf

  msg "Cleaning-up cross compiler tree..."
  rm -rf ${pkgdir}/usr/lib/cross-sparc-unknown-elf/{info,man}

  msg "Creating out-of-path executables..."
  mkdir -p ${pkgdir}/usr/lib/cross-sparc-unknown-elf/sparc-elf/bin/
  rm -f ${pkgdir}/usr/lib/cross-sparc-unknown-elf/sparc-elf/bin/*
  cd ${pkgdir}/usr/lib/cross-sparc-unknown-elf/sparc-elf/bin/
  for bin in ${pkgdir}/usr/lib/cross-sparc-unknown-elf/bin/sparc-elf-*; do
    bbin=`basename "$bin"`;
    ln -s "/usr/lib/cross-sparc-unknown-elf/bin/${bbin}" `echo "$bbin" | sed "s#^sparc-elf-##"`;
  done

  msg "Creating /usr/bin symlinks..."
  mkdir -p $pkgdir/usr/bin
  for bin in ${pkgdir}/usr/lib/cross-sparc-unknown-elf/bin/sparc-elf-*; do
    bbin=`basename "$bin"`;
    ln -s "/usr/lib/cross-sparc-unknown-elf/bin/${bbin}" "${pkgdir}/usr/bin/${bbin}";
  done
}

md5sums=('6466171e5b2ddf0f824b28da0b346549')

# vim: set ts=2 sw=2 ft=sh noet:
