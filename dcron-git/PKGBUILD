# Maintainer: Jim Pryor <profjim@jimpryor.net>

pkgname=dcron-git
pkgver=20100203
pkgrel=1
pkgdesc="dillon's lightweight cron daemon"
arch=(i686 x86_64)
license=('GPL')
url="http://www.jimpryor.net/linux/dcron"
backup=(var/spool/cron/root etc/logrotate.d/crond)
depends=('glibc')
provides=('cron' 'dcron=4.4')
conflicts=('dcron' 'yacron-git')
# to update the manpages, you need pandoc or haskell-pandoc
makedepends=('git')
source=()

_gitroot="git://git.jimpryor.net/dcron.git"
_gitname="dcron"

build() {
	cd "$srcdir"

	if [[ -d "$_gitname" ]]; then
		msg "Updating Git repository"
		(cd "$_gitname"; git checkout master; git pull origin) 
	else
		msg "Checking out fresh Git repository"
		git clone "$_gitroot" "$_gitname"
	fi
	msg "Copying files"
	rsync -a --exclude='.git' --delete "$_gitname/" "${_gitname}-build"
	cd "${_gitname}-build"
	msg "Starting build..."

	# by default, any member of group "users" can edit their own crontab
	make PREFIX=/usr CRONTAB_GROUP=users CRONTABS=/var/spool/cron CRONSTAMPS=/var/spool/cronstamps || return 1
	make man # this is allowed to fail, we just use existing manpages
	make DESTDIR="$pkgdir" install || return 1


	install -d -m755 "$pkgdir/etc/cron."{hourly,daily,weekly,monthly} || return 1
	install -D -m755 extra/crond.rc "$pkgdir/etc/rc.d/crond" || return 1
	install -D -m755 extra/run-cron "$pkgdir/usr/sbin/run-cron" || return 1
	install -D -m0600 extra/root.crontab "$pkgdir/var/spool/cron/root" || return 1
	install -D -m644 extra/crond.logrotate "$pkgdir/etc/logrotate.d/crond" || return 1
	# install -D -m755 extra/prune-cronstamps "$pkgdir/etc/cron.monthly/prune-cronstamps" || return 1

}
