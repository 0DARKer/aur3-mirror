# Maintainer: Jim Pryor <profjim@jimpryor.net>

pkgname=dcron-git
_lastver=4.5
pkgver=20110501
pkgrel=1
pkgdesc="dillon's lightweight cron daemon"
arch=(i686 x86_64)
license=('GPL')
url="http://www.jimpryor.net/linux/dcron"
backup=(var/spool/cron/root etc/conf.d/crond)
depends=('glibc')
provides=('cron' "dcron=$_lastver")
conflicts=('dcron' 'cronnie')
# to update the manpages, you need pandoc or haskell-pandoc
makedepends=('git')
source=()
optdepends=('smtp-server: sending cron job output via email')

_gitroot="git://git.jimpryor.net/dcron.git"
_gitname="dcron"

build() {
	cd "$srcdir"

	if [[ -d "$_gitname" ]]; then
		msg "Updating Git repository"
		(cd "$_gitname"; git checkout master; git pull origin) 
	else
		msg "Checking out fresh Git repository"
		git clone "$_gitroot" "$_gitname"
	fi
	msg "Copying files"
	rsync -a --exclude='.git' --delete "$_gitname/" "${_gitname}-build"
	cd "${_gitname}-build"
	msg "Starting build..."

	# tweak manpages
	sed -i -e 's=/var/spool/cron/crontabs=/var/spool/cron=; s=/var/spool/cron/cronstamps=/var/spool/cronstamps=' crond.8
	sed -i -e 's=/var/spool/cron/crontabs=/var/spool/cron=; s=/var/spool/cron/cronstamps=/var/spool/cronstamps=' crontab.1

	# by default, any member of group "users" can edit their own crontab
	make \
		PREFIX=/usr \
		VERSION="$_lastver git-$(cd ../${_gitname}; git rev-parse HEAD | cut -c-7)" \
		CRONTAB_GROUP=users \
		CRONTABS=/var/spool/cron \
		CRONSTAMPS=/var/spool/cronstamps
	make man || true # this is allowed to fail, we just use existing manpages
	make DESTDIR="$pkgdir" install

    # install standard configuration and scripts
	install -d -m755 "$pkgdir/etc/cron."{hourly,daily,weekly,monthly}

	sed -i -e 's=/var/spool/cron/cronstamps=/var/spool/cronstamps=' extra/prune-cronstamps
    install -D -m755 extra/prune-cronstamps "$pkgdir/etc/cron.d/prune-cronstamps"
	install -D -m755 extra/crond.rc "$pkgdir/etc/rc.d/crond"
    install -D -m755 extra/crond.conf "$pkgdir/etc/conf.d/crond"
    install -D -m755 extra/crontab.vim "$pkgdir/usr/share/vim/vimfiles/ftplugin/crontab.vim"
	install -D -m755 extra/run-cron "$pkgdir/usr/sbin/run-cron"
	install -D -m0600 extra/root.crontab "$pkgdir/var/spool/cron/root"

}
