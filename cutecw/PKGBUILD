# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=cutecw
pkgver=1.0
pkgrel=1
pkgdesc="Learning morse code is hard; this software breaks it into phases to make learning easier."
arch=(i686 x86_64)
url="http://www.hamtools.org/cutecw/"
license=('GPL')
depends=('qt' 'hicolor-icon-theme')
install="$pkgname.install"
source=("${url}releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('b0ddf2637d27b00253501693dcd2f6a5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Prefix fix
  sed -i "s|/local||" $pkgname.pro

  # Build
  qmake PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Desktop file fix
  sed -i "s|/opt||;s|X-Win.*|Categories=Education;|" $pkgname.desktop

  make INSTALL_ROOT="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
