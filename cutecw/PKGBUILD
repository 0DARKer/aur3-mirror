# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=cutecw
pkgver=1.0
pkgrel=2
pkgdesc="Learning morse code is hard; this software breaks it into phases to make learning easier."
arch=(i686 x86_64)
url="http://www.hamtools.org/cutecw/"
license=('GPL')
depends=('qt4')
install="$pkgname.install"
source=("${url}releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('b0ddf2637d27b00253501693dcd2f6a5')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # strip local path
  sed -i "s|/local||" $pkgname.pro

  # right bin path and category
  sed -i "s|/opt||;s|X-Win.*|Categories=Education;|" $pkgname.desktop  
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  qmake-qt4 PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
