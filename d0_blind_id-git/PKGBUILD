# Maintainer: MrBougo <bougospam at gmail dot com>
pkgname=d0_blind_id-git
pkgver=20110110
pkgrel=1
pkgdesc="Cryptographic library for identification with Schnorr ID scheme and Blind RSA Signatures"
arch=('i686' 'x86_64')
url="http://github.com/divVerent/d0_blind_id"
license=('BSD')
depends=('gmp')
makedepends=('git')
conflicts=()
options=('!libtool')

_gitroot="git://git.xonotic.org/xonotic/d0_blind_id.git"
_gitname="d0_blind_id"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sh autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
} 
