# Maintainer: Mantas MikulÄ—nas <grawity@gmail.com>
pkgname=libverto
pkgver=0.2.6
pkgrel=1
pkgdesc="Main event loop abstraction library"
arch=(i686 x86_64)
url="https://fedorahosted.org/libverto/"
license=(MIT)
makedepends=(
# Supported event loops
  'glib2'
  'libev'
  'libevent'
  'tevent'
)
source=("https://fedorahosted.org/releases/l/i/libverto/libverto-$pkgver.tar.gz")
sha1sums=('f46adfb54fedd46c36f4c820cb03dfa2c5b71747')

prepare() {
  cd "$srcdir/libverto-$pkgver"
  autoreconf -i -f
}

build() {
  cd "$srcdir/libverto-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/libverto-$pkgver"
  make DESTDIR="$pkgdir" install
  sed -i '
    /^prefix=/aexec_prefix=${prefix}
    /^exec_prefix/d
  ' "$pkgdir/usr/lib/pkgconfig"/*.pc
  install -Dm0644 "COPYING" "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2:sw=2:et
