# Maintainer: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=peerflix-git
_gitname=peerflix
pkgver=0.20.3.r0.gda0afce
pkgrel=1
pkgdesc="Streaming torrent client for node.js"
arch=('any')
url="https://github.com/mafintosh/peerflix"
license=('MIT')
depends=('nodejs')
makedepends=('git')
provides=('peerflix')
conflicts=('peerflix')
source=('git://github.com/mafintosh/peerflix.git')
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "${srcdir}/${_gitname}"

  # Temporarily removing optional dependency that breaks npm install
  sed -i '/airplay-js/d' package.json
}

package() {
  cd "${srcdir}/${_gitname}"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_gitname}/LICENSE"
  npm install --user root -g --prefix "${pkgdir}/usr"
}

# vim:set ts=2 sw=2 et:
