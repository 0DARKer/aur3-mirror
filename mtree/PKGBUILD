pkgname="mtree"
pkgver="2010Q3"
pkgrel=2
pkgdesc="NetBSD mtree"
arch=('i686' 'x86_64')
url="http://www.netbsd.org/"
license=('BSD')
makedepends=('cvs' 'libnbcompat')
source=('fileflags.diff')
md5sums=('6959c69cff70ac5ee14ab96666b73902')

__cvsroot="anoncvs@anoncvs.NetBSD.org:/cvsroot"
__cvsmod="pkgsrc/pkgtools/$pkgname/files"

build() {
	if [ -d $__cvsmod/CVS ]; then
		CVSROOT=$__cvsroot CVS_RSH=ssh cvs -f -z3 up -r pkgsrc-$pkgver -Pd $__cvsmod
	else
		CVSROOT=$__cvsroot CVS_RSH=ssh cvs -f -z3 co -r pkgsrc-$pkgver -P $__cvsmod
	fi

	rm -rf build
	cp -r $__cvsmod build
	cd build

	# http://old.nabble.com/pkg-40766%3A-sysutls-mtree-fails-to-build-on-Linux-(with-patch-)-tt22242834.html#a22242834
	patch -p0 < $srcdir/fileflags.diff

	./configure --prefix=/usr

	make LIBS=-lnbcompat
}

package() {
	cd build

	install -Dm755 $pkgname $pkgdir/usr/sbin/$pkgname
	install -Dm644 $pkgname.8 $pkgdir/usr/share/man/man8/$pkgname.8
}
