# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Shahar Weiss <sweiss4@gmx.net>

pkgname=winetools
pkgver=0.9jo
pkgrel=6
pkgdesc="A menu driven installer for installing about 90 Windows programs with the Linux operating system using Wine."
arch=('i686')
url="http://www.von-thadden.de/Joachim/WineTools"
license=('GPL')
depends=('wine' 'xdialog')
source=(http://www.sfr-fresh.com/linux/misc/${pkgname}-${pkgver}-III.tar.gz \
        findwine.patch)

md5sums=('e15874cabdd48342e7de5a1823ee3c98'
         '621eeed192701be22aae6ac164b1d04a')

build() {
  cd ${srcdir}/$pkgname-$pkgver-III

  # Fix up paths in the shell scripts
  sed 's:BASEDIR="/usr/local/winetools":BASEDIR="/usr/share/winetools":' -i ./wt${pkgver} || return 1
  sed 's:BASEDIR="/usr/local/winetools":BASEDIR="/usr/share/winetools":' -i ./findwine || return 1
  sed 's:DIALOG="/usr/local/winetools/Xdialog":DIALOG="/usr/share/winetools/Xdialog":' -i ./findwine || return 1
  sed 's:TEXTDOMAINDIR=/usr/local/share/locale":TEXTDOMAINDIR=/usr/share/locale":' -i ./findwine || return 1

  # Installing WineTools to /usr/share/winetools...
  patch -Np0 < ${srcdir}/findwine.patch || return 1
  install -d -m755 ${pkgdir}/usr/share/winetools ${pkgdir}/usr/bin || return 1
  cp -R * ${pkgdir}/usr/share/winetools || return 1

  # Installing translations...
  cd ${pkgdir}/usr/share/winetools/po || return 1
  for i in $( ls  *.po|cut -f1 -d.); do
    # Create the folder for the translation, then install it.
    install -d -m755 ${pkgdir}/usr/share/locale/$i/LC_MESSAGES || return 1
    msgfmt $i.po -o ${pkgdir}/usr/share/locale/$i/LC_MESSAGES/wt2.mo || return 1
  done

  cd ${pkgdir}/usr/bin || return 1

  # Make binaries symlinks
  ln -sf ../share/winetools/wt${pkgver} wt${pkgver} || return 1
  ln -sf ../share/winetools/wt${pkgver} winetools || return 1
  ln -sf ../share/winetools/findwine findwine || return 1
}
