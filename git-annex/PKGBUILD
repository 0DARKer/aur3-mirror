# Maintainer: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Elena ``of Valhalla'' Grandi <elena.valhalla@gmail.com>

pkgname=git-annex
pkgver=3.20110906
pkgrel=2
pkgdesc='manage files with git, without checking their contents into git'
arch=('i686' 'x86_64')
url="http://git-annex.branchable.com/"
license=(GPL3)
depends=(git uuid openssh rsync pcre curl)
options=(!makeflags)
# optional build dependency: hS3
makedepends=(ikiwiki ghc
             haskell-http
             haskell-pcre-light
             haskell-utf8-string
             haskell-monad-control
             haskell-dataenc
             haskell-sha
             haskell-missingh
             haskell-hslogger
             haskell-json
             haskell-quickcheck
             haskell-testpack)
_gitroot='git://git.kitenet.net/git-annex'
_gitrepo='git-annex'

build() {
  cd $srcdir

  if [ -d $_gitrepo ]; then
    (cd $_gitrepo && git checkout HEAD && git pull .) || return 1
  else
    git clone $_gitroot $_gitrepo || return 1
  fi
  echo git checkout "$pkgver"
  (cd $_gitrepo && git checkout "$pkgver") || return 1

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  [ -d $_gitrepo-build ] && rm -rf $_gitrepo-build
  cp -r $_gitrepo $_gitrepo-build
  cd $_gitrepo-build

  sed -i -e 's|\$(bins): sources|\$(bins): sources Remote/S3.o|' Makefile
  make || return 1
  # haskell-testpack is required only to run the tests
  make test || return 1
}

package() {
  cd $srcdir/$_gitrepo-build
  make DESTDIR=$pkgdir install
}
