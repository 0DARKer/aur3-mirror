# Maintainer: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Elena ``of Valhalla'' Grandi <elena.valhalla@gmail.com>

# Building the documentation requires ikiwiki.
# If you don't want to build the documentation turns this to 0.
_build_docs=1

pkgname=git-annex
pkgver=3.20120106
pkgrel=2
_githash=c9aab2a5b65d1559133ef9cc140bd030f79bb2f4
pkgdesc='manage files with git, without checking their contents into git'
arch=('i686' 'x86_64')
url="http://git-annex.branchable.com/"
license=(GPL3)
depends=(git util-linux openssh rsync pcre curl)
options=(!makeflags)
# optional build dependency: hS3
makedepends=(ghc
             haskell-http
             haskell-pcre-light
             haskell-utf8-string
             haskell-monad-control
             haskell-dataenc
             haskell-sha
             haskell-missingh
             haskell-hslogger
             haskell-json
             haskell-quickcheck
             haskell-testpack)
[ $_build_docs = 1 ] && makedepends=(ikiwiki "${makedepends[@]}")
_gitroot=git://git.kitenet.net/git-annex
_gitrepo=git-annex

build() {
  cd $srcdir

  if [ -d $_gitrepo ] ; then
    cd $_gitrepo
    git fetch origin || return 1
    git reset --hard origin/master || return 1
    msg "The local files are updated."
  else
    # --depth=1 should only be used when no merge is done
    git clone "${_gitroot}" $_gitrepo || return 1
  fi
  cd "${srcdir}/$_gitrepo"
  git checkout $_githash || return 1
  git merge origin/no-s3 || return 1
  rsync -aH --del "${srcdir}/$_gitrepo/" "${srcdir}/build/" || return 1
  cd "${srcdir}/build"

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  sed -i -e 's|\$(bins): sources|\$(bins): sources Remote/S3.o|' Makefile
  make || return 1
  # haskell-testpack is required only to run the tests
  make test || return 1
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
