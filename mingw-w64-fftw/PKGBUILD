pkgname=mingw-w64-fftw
pkgver=3.3.3
pkgrel=1
pkgdesc="A library for computing the discrete Fourier transform (DFT) (mingw-w64)"
arch=(any)
url="http://www.fftw.org"
license=("GPL")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("$url/fftw-$pkgver.tar.gz")
md5sums=('0a05ca9c7b3bfddc8278e7c40791a1c2')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}-d"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}-d"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-sse2 \
      --enable-static \
      --enable-shared \
      --enable-threads=win32 \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make

    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}-ld"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}-ld"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-long-double \
      --enable-static \
      --enable-shared \
      --enable-threads=win32 \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make

    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}-f"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}-f"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-float \
      --enable-sse \
      --enable-static \
      --enable-shared \
      --enable-threads=win32 \
      --with-our-malloc \
      --with-g77-wrappers \
      --with-windows-f77-mangling
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}-d"
    make DESTDIR="$pkgdir" install
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}-ld"
    make DESTDIR="$pkgdir" install
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}-f"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm "$pkgdir/usr/${_arch}/bin/fftw-wisdom-to-conf"
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
