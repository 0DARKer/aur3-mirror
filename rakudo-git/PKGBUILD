# Contributor: xduugu
# Maintainer: Daenyth
pkgname=rakudo-git
pkgver=20090823
pkgrel=1
pkgdesc="A Perl 6 compiler for the Parrot virtual machine"
arch=('i686' 'x86_64')
url="http://rakudo.org/"
license=('custom:Artistic License 2.0')
depends=('parrot-svn')
makedepends=('git' 'perl')
provides=("rakudo=$pkgver")

_gitroot="git://github.com/rakudo/rakudo.git"
_gitname="rakudo"

build() {
  msg "Connecting to GIT server...."

  if [ -d "$_gitname" ] ; then
    cd "$_gitname" && git pull origin || return 1
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname" || return 1
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  perl Configure.pl &&
  make &&
  make DESTDIR="$pkgdir" install || return 1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1

  rm -rf "$srcdir/$_gitname-build"
} 

# vim:set ts=2 sw=2 et:
