# Contributor: leepesjee <lpeschier at xs4all.nl>
pkgname=gluplot
pkgver=1.9
pkgrel=1
pkgdesc="A data plotting OpenGL/GLU/glut based 2D/3D graphical library program, supporting high quality plot output to PS, EPS and PDF files."
arch=('i686' 'x86_64')
url="http://wwwinfo.jinr.ru/programs/jinrlib/gluplot/indexe.html"
license=('GPL')
depends=('fltk' 'freeglut' 'libxpm')
source=(http://wwwinfo.jinr.ru/programs/jinrlib/gluplot/$pkgname-$pkgver.tar.gz)
md5sums=('0f60883595a13c5656c3274990fd0c37')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|tolower|::tolower|' ./src/plot/glp_plot_gl.cpp
  sed -i '/^using namespace std;/i #include <cstdlib>' ./src/test/terrain.cpp

  ./configure --prefix=/usr --enable-fltk
  sed -i 's|mandir = ${datarootdir}|mandir = ${DESTDIR}/${datarootdir}|' ./docs/Makefile
  sed -i 's|docdir = ${datarootdir}|docdir = ${DESTDIR}/${datarootdir}|' ./docs/html/Makefile

  make || return 1
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
