# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Benjamin Andresen <benny(at)klapmuetz(dot)org>
#              Sylvester Johansson <syljo361(at)gmail(dot)org>
pkgname=ocaml-ounit
pkgver=1.1.1
pkgrel=1
arch=('i686' 'x86_64')
license=('MIT')
pkgdesc="Unit testing framework for OCaml"
url="http://ounit.forge.ocamlcore.org"
depends=('ocaml')
makedepends=('ocaml-findlib')
options=('!strip')
source=("http://forge.ocamlcore.org/frs/download.php/762/ounit-$pkgver.tar.gz")
md5sums=('b28e4c25ad5fc9080639d47fd51c7c24')

build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"
  make CONFIGUREFLAGS="--prefix /usr --destdir $pkgdir" build doc
}

package() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"
  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
