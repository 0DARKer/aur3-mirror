# Maintainer: Bart Verhoeven <nepherte at archlinux dot us>

pkgname=mercury-compiler
pkgver=10.04.2
pkgrel=1
pkgdesc="stable release of the mercury language compiler"
arch=('i686' 'x86_64')
url="http://www.cs.mu.oz.au/research/mercury/"
license=('GPL')
depends=('gcc' 'make')
makedepends=('flex' 'bison')
source=(http://www.mercury.csse.unimelb.edu.au/download/files/$pkgname-$pkgver.tar.gz)
md5sums=('6bc8b1c0897c266bcb5afa128d2c2a3b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --infodir=/usr/share/info --disable-debug-grades \
              --disable-inefficient-grades 

  # Uncomment PARRALEL in Makefile and change -j accordingly to speed up
  # complilation
  make 
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make INSTALL_PREFIX="$pkgdir/usr" INSTALL_MAN_DIR="$pkgdir/usr/share/man" \
       INSTALL_INFO_DIR="$pkgdir/usr/share/info" \
       MERCURY_COMPILER="$pkgdir/usr/bin/mercury_compile" \
       MERCURY_CONFIG_DIR="$pkgdir/usr/lib/mercury" install
}

# vim:set ts=2 sw=2 et:
