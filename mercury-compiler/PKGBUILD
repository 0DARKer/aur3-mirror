# Maintainer: Bart Verhoeven <bartverhoeven@live.com>

pkgname=mercury-compiler
pkgver=11.07.2
pkgrel=3
pkgdesc="Stable release of the mercury language compiler."
arch=('i686' 'x86_64')
url="http://www.mercurylang.org/"
license=('GPL')
depends=('gcc' 'make')
makedepends=('flex' 'bison')
source=(http://dl.mercurylang.org/release/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8afee5732d6d52343fbba8d6b9dd3f87b819673743a408a564eede17956ea659')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --infodir=/usr/share/info --disable-debug-grades \
              --disable-inefficient-grades

  # Change -j accordingly to speed up compilation
  make PARALLEL="$MAKEFLAGS"
}

package() {
  cd $srcdir/$pkgname-$pkgver

  # Change -j accordingly to speed up compilation
  make INSTALL_PREFIX="$pkgdir/usr" \
       INSTALL_MAN_DIR="$pkgdir/usr/share/man" \
       INSTALL_INFO_DIR="$pkgdir/usr/share/info" \
       MERCURY_COMPILER="$pkgdir/usr/bin/mercury_compile" \
       MERCURY_CONFIG_DIR="$pkgdir/usr/lib/mercury" \
       PARALLEL="$MAKEFLAGS" install
}

# vim:set ts=2 sw=2 et:
