# $Id: PKGBUILD 54241 2011-08-15 08:53:08Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Bob Finch <w9ya@arrl.net>

pkgname=dosemu-svn
pkgver=2053
pkgrel=1
pkgdesc="DOS emulator"
arch=('i686' 'x86_64')
url="http://www.dosemu.org/"
license=('GPL' 'custom')
depends=('gpm' 'slang' 'sdl' 'libxxf86vm' 'libsndfile' 'sh' 'xorg-mkfontdir' 'xorg-bdftopcf')
makedepends=('subversion')
conflicts=('dosemu')
provides=('dosemu')
backup=('etc/dosemu/dosemu.conf'
	'etc/dosemu/dosemu.users'
	'etc/dosemu/global.conf')
source=(http://downloads.sourceforge.net/sourceforge/dosemu/dosemu-freedos-1.0-bin.tgz)
noextract=('dosemu-freedos-1.0-bin.tgz')
md5sums=('2e09774fe91cff4372cb4a393eb467f5')

_svntrunk='https://dosemu.svn.sourceforge.net/svnroot/dosemu/trunk'
_svnmod="$pkgname"

build() {
  cd "$srcdir"
  msg "Connecting to SVN server..."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr --with-fdtarball="$srcdir/dosemu-freedos-1.0-bin.tgz" --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
  install -D -m 644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  install -D -m 644 COPYING.DOSEMU $pkgdir/usr/share/licenses/$pkgname/COPYING.DOSEMU
}
