# Maintainer: prettyvanilla <prettyvanilla@posteo.at>

pkgname=kup
pkgver=0.4.2
pkgrel=1
pkgdesc="A KDE-based frontend for the bup backup software"
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/Kup+Backup+System?content=147465"
license=('GPL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
optdepends=('bup: support for incremental backups'
            'rsync: support for synced backups')
install=kup.install
source=(http://kde-apps.org/CONTENT/content-files/147465-$pkgname-$pkgver.tar.gz)
md5sums=('7c0cfc0b1b1a13bc85df6e443947c021')

build() {
  cd $pkgname-$pkgver

  [ -d build ] && rm -rf build
  mkdir build
  cd build

  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
