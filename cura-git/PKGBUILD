# Maintainer: Adam Goldsmith <contact@adamgoldsmith.name>
 
pkgname=cura-git
pkgver=14.11.RC7.5.ga7c2ea3
pkgrel=1
pkgdesc="A full software solution for 3D printing aimed at RepRaps and the Ultimaker."
arch=('i686' 'x86_64')
license=('GPL2')
url="https://github.com/daid/Cura"
provides=('cura')
conflicts=('cura')
depends=('python2' 'wxpython' 'python2-opengl' 'python2-pyserial' 'python2-numpy' 'python2-power-git')
makedepends=('git')
source=('git+https://github.com/daid/Cura.git'
        'git+https://github.com/Ultimaker/CuraEngine.git')
md5sums=('SKIP' 'SKIP')

_gitname="Cura"
_gitbranch="master"

pkgver() {
  cd "$_gitname"
  git describe --tags | sed 's/-/./g'
}

build() {
  cd CuraEngine
  make 
}

package() {
  cd "$_gitname"
  mkdir -p "$pkgdir/usr/share/cura/"
  mkdir -p "$pkgdir/usr/share/applications/"
  mkdir -p "$pkgdir/usr/bin/"
  cp -a Cura "$pkgdir/usr/share/cura/"
  cp -a resources "$pkgdir/usr/share/cura/"
  cp -a plugins "$pkgdir/usr/share/cura/"
  cp -a ../CuraEngine/build/CuraEngine "$pkgdir/usr/share/cura/"
  cp scripts/linux/cura.py "$pkgdir/usr/share/cura/"
  cp -a scripts/linux/debian_amd64/usr/bin/cura "$pkgdir/usr/bin"
  sed -i 's:/usr/bin/python:/usr/bin/python2:' "$pkgdir/usr/bin/cura"
  cp -a scripts/linux/debian_amd64/usr/share/applications/cura.desktop "$pkgdir/usr/share/applications/"
  sed -i 's:/usr/bin/python:/usr/bin/python2:' "$pkgdir/usr/share/applications/cura.desktop"
  git describe --abbrev=0 --tags > "$pkgdir/usr/share/cura/Cura/version"
}

