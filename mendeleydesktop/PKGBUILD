##maintainer Meow < meow at linux at cn >

pkgname=mendeleydesktop
pkgver=1.1.2
pkgrel=3
pkgdesc="Academic software for managing and sharing research papers (desktop client)"
url=http://www.mendeley.com/release-notes/
arch=(i686 x86_64)
depends=(python2 qt)
license=(custom:mendeley_eula)
install=mendeleydesktop.install
_arch=$CARCH
[[ $CARCH = i686 ]] && _arch=i486

source=("http://s3.amazonaws.com/mendeley-desktop-download/linux/$pkgname-$pkgver-linux-$_arch.tar.bz2"
	 'mendeleydesktop.install')
md5sums=('97421155f7e0b91e2887948646e9f80e'
	 'b2b77e6b80aa0fe1fc518beb18574d13')
sha256sums=('e387d44fe6ef403b973572b9fe63088d942fee0b0e0ec2a1d3315e11eaf377f1'
	    '8889586c811ce05e0d8f94ceede4cdc5b878bd138a7c64ca3fd68af0399a7955')

if [[ $CARCH = x86_64 ]]; then
	md5sums[0]='d2cebfdacab1ef82c1c887510524001e'
	sha256sums[0]='23b972ac17ee841d676c5bffd3a951da632b51aa73e0d69d1188ebe6330ea1e1'
fi

package() {
	cd "$pkgname-$pkgver-linux-$_arch"

	rm -f share/doc/mendeleydesktop/*.txt
	install -dm755 "$pkgdir/opt/$pkgname/"{,bin}
	mv lib share "$pkgdir/opt/$pkgname/"
	ln -s "../lib/mendeleydesktop/libexec/mendeleydesktop.$_arch" "$pkgdir/opt/$pkgname/bin/$pkgname"

	sed -i '1s@^#!/usr/bin/python$@&2@' bin/mendeleydesktop
	install -Dm755 "bin/mendeleydesktop" "$pkgdir/usr/bin/mendeleydesktop"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	mkdir -p "$pkgdir"/usr/share/applications
	ln -s /opt/$pkgname/share/applications/mendeleydesktop.desktop "$pkgdir"/usr/share/applications/
	for size in 16 22 32 48 64 128;do
	    mkdir -p "$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps
	    ln -s /opt/$pkgname/share/icons/hicolor/${size}x${size}/apps/${pkgname}.png \
		  "$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps
	done
}
