##maintainer Meow < meow at linux dot cn >

pkgname=mendeleydesktop
pkgver=1.3
pkgrel=2
pkgdesc="Academic software for managing and sharing research papers (desktop client)"
url=http://www.mendeley.com/release-notes/
arch=(i686 x86_64)
depends=(python2 qt)
license=(custom:mendeley_eula)
install=mendeleydesktop.install

_arch=$CARCH

[[ $CARCH = i686 ]] && _arch=i486

source=("http://download.mendeley.com/linux/$pkgname-$pkgver-linux-$_arch.tar.bz2"
        'mendeleydesktop.install')
md5sums=('df8b69ee4a12fe65643d06c6b9925240'
         '16358cd53dc258a72efcaeab5a415217')

if [[ $CARCH = x86_64 ]]; then
    md5sums[0]='4d0f77a37df5cb4912d0daee22a639b5'
fi

package() {
    cd "$pkgname-$pkgver-linux-$_arch"

    rm -f share/doc/mendeleydesktop/*.txt

    install -dm755 "$pkgdir/opt/$pkgname/"
    mv bin lib share "$pkgdir/opt/$pkgname/"
    #ln -s "../lib/mendeleydesktop/libexec/mendeleydesktop.$_arch" "$pkgdir/opt/$pkgname/bin/$pkgname"
    cd "$pkgdir"
    sed -i '1s@^#!/usr/bin/python$@&2@' opt/"$pkgname"/bin/mendeleydesktop
    #install -Dm755 "bin/mendeleydesktop" "$pkgdir/usr/bin/mendeleydesktop"
    install -dm755 "$pkgdir"/usr/bin
    ln -s /opt/"$pkgname"/bin/mendeleydesktop "$pkgdir/usr/bin/mendeleydesktop"

    cd "$srcdir/$pkgname-$pkgver-linux-$_arch"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    install -dm755 "$pkgdir"/usr/share/applications
    ln -s /opt/"$pkgname"/share/applications/mendeleydesktop.desktop "$pkgdir"/usr/share/applications/

    #force mendeley using bundled qt because which under qt 4.8 crashes at start point
    sed -i 's/^Exec.*$/& --force-bundled-qt/' "$pkgdir"/opt/"$pkgname"/share/applications/mendeleydesktop.desktop
    for size in 16 22 32 48 64 128;do
        install -dm755 "$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps
        ln -s /opt/"$pkgname"/share/icons/hicolor/${size}x${size}/apps/"${pkgname}".png \
          "$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps
    done
}
