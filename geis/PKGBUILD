# Maintainer: Nicolas Qui√©not <niQo @ AUR >
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=geis
pkgver=2.2.16
pkgrel=1
pkgdesc="Implementation of the GEIS (Gesture Engine Interface and Support) interface."
arch=('i686' 'x86_64')
url="https://launchpad.net/geis"
license=('GPL' 'LGPL')
depends=('grail' 'dbus' 'python-gobject')
makedepends=('xorg-server-devel')
provides=('utouch-geis')
conflicts=('utouch-geis')
replaces=('utouch-geis')
options=('!emptydirs' '!libtool')
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ## add -Wno-format in CFLAGS see FS#34658
  CFLAGS="$CFLAGS -Wno-format" ./configure --prefix=/usr --disable-static
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}/" install
}

md5sums=('550c78b5185b835b80dba14537c2c83d')
