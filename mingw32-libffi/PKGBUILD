# Maintainer: Mikael Eriksson <mikael_eriksson@miffe.org>

pkgbase=libffi
pkgname=mingw32-libffi
pkgver=3.0.9
pkgrel=1
pkgdesc="A portable, high level programming interface to various calling conventions (mingw32)"
arch=('any')
depends=('mingw32-runtime')
makedepends=('mingw32-gcc')
options=('!libtool' 'force')
license=('MIT')
url="http://sourceware.org/libffi"
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz)
md5sums=('1f300a7a7f975d4046f51c3022fa5ff1')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  export CFLAGS="-O2 -mms-bitfields"
  export PKG_CONFIG_PATH="/usr/i486-mingw32/lib/pkgconfig/"

  # mingw-ld chokes on --hash-style=gnu
  unset LDFLAGS

  ./configure \
      --prefix=/usr/i486-mingw32 \
      --host=i486-mingw32 \
      --build=$CHOST

  make || return 1
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}

# vim:set ts=2 sw=2 et:
