# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=lebiniou
pkgver=3.9.2
pkgrel=1
pkgdesc="A program which creates images from sound, in an intelligent way."
arch=(i686 x86_64)
url="http://biniou.net/"
license=('GPL')
depends=("$pkgname-data" 'libpulse' 'libcaca' 'fftw'
         'ffmpeg' 'libxml2' 'sdl_ttf' 'glib2')
install="$pkgname.install"
source=("http://dl.biniou.net/biniou/tar/$pkgname-$pkgver.tar.gz")
md5sums=('0d232a0293477b922a5676f94d7df51b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export LDFLAGS="${LDFLAGS//,--as-needed}"

  # set schemes path to /etc/lebiniou
  sed -i "s|\(SCHEMES_FILE=\"\).*|\1/etc/lebiniou/schemes.xml\"|" \
    configure*

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-esd \
              --enable-sndfile \
              --enable-caca \
              --enable-diskwriter \
              --enable-erlang \
              --enable-webcam
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
