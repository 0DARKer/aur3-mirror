# Maintainer: Billy Wayne McCann <thebillywayne ~et~ gmail.com>
# Former Maintainer: Thorsten Bonck <thorsten at bonck.net>
# Former Maintainer: Hector Mtz-Seara <hseara__*a*t*__gmail*.*com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Michal Bozon <michal.bozon__at__gmail.com>

pkgname=molden
pkgver=5.1
pkgrel=3
pkgdesc="Molecular builder and viewer of quantum chemistry calculations"
arch=('i686' 'x86_64')
url="http://www.cmbi.ru.nl/molden/molden.html"
license=('custom')
install=${pkgname}.install
groups=('science' 'chemistry')
depends=('glu' 'libxmu' 'mesa')
makedepends=('gcc-fortran')
provides=('molden' 'gmolden' 'ambfor' 'ambmd')
source=(ftp://ftp.cmbi.ru.nl/pub/molgraph/$pkgname/$pkgname$pkgver.tar.gz)
sha1sums=('7b10d6b52952ad55e3d0e2cc7fa0d5a724b7cb03')

# contatenated $pkgname and $pkgver
_pkgnv=${pkgname}${pkgver}


build() {
    cd "$srcdir"/$_pkgnv
  
    # rename the CopyRight file
    mv -f CopyRight LICENSE
  
    # make
    make molden gmolden ambfor/ambfor ambfor/ambmd || return 1
}

package() {
    # install binaries
    install -m755 -d "$pkgdir"/usr/bin
    install -m755 -t "$pkgdir"/usr/bin "$srcdir"/$_pkgnv/molden
    install -m755 -t "$pkgdir"/usr/bin "$srcdir"/$_pkgnv/gmolden
    install -m755 -t "$pkgdir"/usr/bin "$srcdir"/$_pkgnv/ambfor/{ambfor,ambmd}

    # install licenses 
    install -m755 -d "$pkgdir"/usr/share/licenses/$pkgname
    install -m644 -t "$pkgdir"/usr/share/licenses/$pkgname "$srcdir"/$_pkgnv/COMMERCIAL_LICENSE
    
    # install desktop file and icon
    install -m755 -d "$pkgdir"/usr/share/applications
    install -m644 -t "$pkgdir"/usr/share/applications/ ../$pkgname.desktop
    install -m755 -d "$pkgdir"/usr/share/pixmaps
    install -m644 -t "$pkgdir"/usr/share/pixmaps/ ../$pkgname.png 
}
