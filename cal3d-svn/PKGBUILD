# Maintainer: Christian BÃ¼hler <christian.buehler@ipoplan.de>
pkgname=cal3d-svn
pkgver=560
pkgrel=1
pkgdesc="3d character animation library"
arch=('x86' 'x86_64')
url="https://gna.org/projects/cal3d"
license=('LGPL')
makedepends=('subversion')
conflicts=('cal3d')
provides=('cal3d')
source=(svn://svn.gna.org/svn/cal3d/trunk/cal3d)
md5sums=('SKIP')

pkgver() {
  cd "$SRCDEST/cal3d"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$srcdir/cal3d"

  # http://gna.org/bugs/?11499
  sed -i 's/AM_USE_UNITTESTCPP/# AM_USE_UNITTESTCPP/' configure.in

  # http://gna.org/bugs/?20724
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.in
}

build() {
  cd "$srcdir/cal3d"
  autoreconf --install --force
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/cal3d"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
