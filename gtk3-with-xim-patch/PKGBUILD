# $Id: PKGBUILD 119847 2011-04-15 22:42:57Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gtk3-with-xim-patch
pkgver=3.0.11
pkgrel=1
pkgdesc="The GTK+ Toolkit (v3) with an XIM patch, so that XIM based input methods like fcitx can run with gtk3. See the discussion at https://bugzilla.gnome.org/show_bug.cgi?id=645993"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'gnutls' 'krb5' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info')
makedepends=('gobject-introspection')
options=('!libtool' '!docs')
conflicts=('gtk3')
provides=('gtk3')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.0/gtk+-${pkgver}.tar.xz
        settings.ini
		xim.patch)

build() {
    cd "${srcdir}/gtk+-${pkgver}"
	patch -p1 < ../xim.patch
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile
    make
}

package() {
    cd "${srcdir}/gtk+-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}
sha256sums=('e2e38b316c4657df0cf376c3b6aad63158a9068c557ad41033a677f29967001b'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            '0925baa15c5a42bc47e4d705afe0c718bc3c707bfd8f0f9e407b7bb2261c2f1e')
