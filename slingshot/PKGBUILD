# Maintainer: jsteel <jsteel at vorx dot com>
# Contributor: Erdbeerkaese
# Contributor: Arkham <arkham at archlinux dot us>

pkgname=slingshot
pkgver=0.8.1p
pkgrel=6
pkgdesc="A 2D strategy game in the gravity fields of several planets."
arch=(any)
url="http://slingshot.wikispot.org/"
license=('GPL')
depends=('python-pygame>=1.7.1')
source=(http://downloads.sourceforge.net/slingshot-game/Slingshot-$pkgver-linux.tar.gz "$pkgname.sh" planet.patch player.patch slingshot.patch)
md5sums=('cdfcf0c85dc7d1b203344c24f591e7b5'
         'e342a8de1bc701d457c6bbe4fe11a9ee'
         '1e3064465764c355f30aefebc091c742'
         '614003b3cf68f02e3b90c869903d128d'
         '68333e7f92fc4197d99fd3d870347037')
build() {
    cd "$srcdir/Slingshot-$pkgver-linux"
    tar -xzf "$pkgname"_files.tar.gz
    cd "$pkgname"
    patch < ../../planet.patch
    patch < ../../player.patch
    patch < ../../slingshot.patch
}

package() {
    cd "$srcdir/Slingshot-$pkgver-linux"
    install -d "$pkgdir/usr/share/$pkgname/data"
    for file in "$pkgname"/*.py ; do
        install -D -m 644 "$file" "$pkgdir/usr/share/$pkgname"
    done
    for file in "$pkgname"/data/* ; do
        install -D -m 644 "$file" "$pkgdir/usr/share/$pkgname/data"
    done
    install -D -m 644 readme "$pkgdir/usr/share/doc/$pkgname/readme"
    # Install .desktop and bin
    sed -i 's,/opt/,/usr/share/,' slingshot.desktop
    install -Dm 644 slingshot.desktop \
                    "$pkgdir/usr/share/applications/slingshot.desktop"
    install -D -m 755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
}
