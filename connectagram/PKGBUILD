# Maintainer: Graeme Gott <graeme@gottcode.org>

pkgname=connectagram
pkgver=1.1.1
pkgrel=1
pkgdesc="A word unscrambling game"
arch=('i686' 'x86_64')
url="http://gottcode.org/${pkgname}/"
license=('GPL3')
if [ -e /usr/bin/qmake-qt5 ] || [ ! -e /usr/bin/qmake-qt4 ]; then
  depends=('qt5-base')
else
  depends=('qt4')
fi
install=${pkgname}.install
source=(http://gottcode.org/${pkgname}/${pkgname}-${pkgver}-src.tar.bz2)
md5sums=('84b17b2a3b76c76311c51ed728bf1030')
sha256sums=('734c610a59d043a13edb6d7301cc9e72704663128cd95370ba22ae12da4796fb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ -e /usr/bin/qmake-qt5 ]; then
    qmake-qt5 PREFIX=/usr
  else
    qmake-qt4 PREFIX=/usr
  fi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/" install
}
