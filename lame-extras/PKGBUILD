# Maintainer: TDY <tdy@gmx.com>

pkgname=lame-extras
pkgver=3.98.4
pkgrel=1
pkgdesc="Supplementary wrappers for LAME (auenc, lameid3, mlame, and mugeco)"
arch=('any')
url="http://lame.sourceforge.net/"
license=('LGPL')
depends=('lame' 'perl-mp3-info')
provides=('mlame=3.98.2')
conflicts=('mlame')
install=$pkgname.install
source=(http://downloads.sourceforge.net/${pkgname/-*}/${pkgname/-*}-$pkgver.tar.gz)
md5sums=('8e9866ad6b570c6c95c8cba48060473f')

build() {
  cd "$srcdir/${pkgname/-*}-$pkgver/misc"
  sed -i '1 i\#!/usr/bin/env perl' lameid3.pl
  sed -n '9,18 s,#[ ]*,,p' <lameid3.pl >lameid3.txt
  install -Dm755 lameid3.pl "$pkgdir/usr/bin/lameid3"
  install -Dm644 lameid3.txt "$pkgdir/usr/share/doc/lame-extras/README.lameid3"
  install -Dm755 auenc "$pkgdir/usr/bin/auenc"
  install -Dm755 mlame "$pkgdir/usr/bin/mlame"
  install -Dm755 mugeco.sh "$pkgdir/usr/bin/mugeco"
}
