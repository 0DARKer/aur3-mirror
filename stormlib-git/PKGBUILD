# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_pkgname=stormlib
pkgname=$_pkgname-git
pkgver=v8.22.6.g870acd5
pkgrel=1
pkgdesc="A C/C++ API to read and write MPQ files with support for merged archives, patch MPQs and more."
arch=('i686' 'x86_64')
url="http://www.zezula.net/en/mpq/stormlib.html"
license=('MIT')
depends=('bzip2' 'zlib')
makedepends=('git' 'cmake')
provides=($_pkgname)
conflicts=($_pkgname)
source=("git://github.com/stormlib/StormLib.git")
sha256sums=('SKIP')
_gitname="StormLib"

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_gitname"
	cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir" install
}
