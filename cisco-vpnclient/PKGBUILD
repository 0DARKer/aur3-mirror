# Maintainer: Alexander De Sousa <archaur :dot: xandy21 :at: spamgourmet :dot: com>
# Contributor: Vasco Costa <vasco dot costa at geekslot dot com>
# Contributor: Michael Jones <jmjones at sisna dot com>
# Contributor: Valerio Mariani <valerio dot mariani at hispeed dot ch>

pkgname=cisco-vpnclient
_pkgname=vpnclient
pkgver=4.8.02.0030_k9
_pkgver=4.8.02.0030-k9
pkgrel=4
pkgdesc="Client to access Cisco VPNs (Virtual Private Network)."
arch=(i686 x86_64)
_kernver=`uname -r`
url="http://www.cisco.com/en/US/products/sw/secursw/ps2308/"
license=("custom")
install=$pkgname.install

if [ "`uname -m`" = "i686" ]; then
   depends=('kernel26>=2.4.0')
else
   depends=('kernel26>=2.4.0' 'lib32-gcc-libs')
fi

makedepends=('kernel26-headers>=2.4.0')
source=(http://projects.tuxx-home.at/ciscovpn/clients/linux/4.8.02/$_pkgname-linux-x86_64-$_pkgver.tar.gz
        fixes.patch)
md5sums=('3ccc9641fd7dae29bfc90556ae5c4b74'
         'd15a8a84a6a6ea3ed2da3bf16c1eb287')

build() {
	cd "$srcdir/$_pkgname"
	
	msg2 "Applying patches..."
	patch <../fixes.patch || return 1
	
	msg2 "Building..."
	KBUILD_NOPEDANTIC=1 make || return 1
}

package() {
	cd "$srcdir/$_pkgname"

	msg2 "Installing files and creating symlinks..."
	install -d -m755 "$pkgdir/opt/$pkgname/bin"
	install -D -m755 $_pkgname cvpnd ipseclog cisco_cert_mgr "$pkgdir/opt/$pkgname/bin/"
	install -D -m755 libvpnapi.so "$pkgdir/opt/$pkgname/lib/libvpnapi.so"
	install -D -m644 vpnapi.h "$pkgdir/opt/$pkgname/include/vpnapi.h"
	install -D -m644 $_pkgname.ini "$pkgdir/opt/$pkgname/$_pkgname.ini"
	install -D -m644 sample.pcf "$pkgdir/opt/$pkgname/Profiles/sample.pcf"
	install -d -m755 "$pkgdir/opt/$pkgname/Certificates"
	install -D -m644 cisco_ipsec.ko "$pkgdir/lib/modules/$_kernver/CiscoVPN/cisco_ipsec.ko"
	install -D -m644 license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
	install -d -m755 "$pkgdir/usr/bin"
	install -d -m755 "$pkgdir/etc"
	ln -sf "/opt/$pkgname/bin/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
	ln -sf "/opt/$pkgname/bin/cisco_cert_mgr" "$pkgdir/usr/bin/cisco_cert_mgr"
	ln -sf "/opt" "$pkgdir/etc/opt"
	
	msg2 "Updating install file..."
	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=$_kernver/g" "$startdir/$pkgname.install"
}
