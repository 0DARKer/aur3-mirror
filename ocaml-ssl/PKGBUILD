# Contributor: Denis Wernert <denis@wernert.info>
pkgname=ocaml-ssl
pkgver=0.5.0
pkgrel=1
pkgdesc="OCaml SSL Library"
arch=('i686' 'x86_64')
url="http://savonet.sourceforge.net/"
license=('LGPL2.1')
depends=('openssl')
makedepends=('ocaml' 'ocaml-findlib')
source=(ocaml-ssl-0.5.0.tar.gz::http://sourceforge.net/projects/savonet/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz/download)
options=(!libtool !strip zipman !makeflags staticlibs)
md5sums=('72e5cca19b8da552e8b57b9138841f43')


build() {
  cd $pkgname-$pkgver
  ./configure --prefix /usr
  make
}

package() {
  OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
  cd $pkgname-$pkgver
  mkdir -p $OCAMLFIND_DESTDIR/stublibs
  OCAMLFIND_INSTFLAGS="-destdir $OCAMLFIND_DESTDIR -ldconf /dev/null" make install
}

# vim:set ts=2 sw=2 et:
