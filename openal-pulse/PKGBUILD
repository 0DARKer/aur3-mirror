# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=openal-pulse
pkgver=1.12.854
pkgrel=1
pkgdesc="OpenAL audio library for use with opengl (with PulseAudio support)"
arch=('i686' 'x86_64')
url="http://www.openal.org/"
license=('LGPL')
depends=('glibc')
makedepends=('alsa-lib' 'sdl' 'pkgconfig' 'cmake' 'pulseaudio')
conflicts=('openal')
provides=("openal=$pkgver-$pkgrel")
options=('!libtool')
backup=(etc/openal/alsoft.conf)
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-${pkgver}.tar.bz2
        alsoft.conf)
md5sums=('fbf36451fdebd6466edbdc0ee7db9603'
         '4298ddc7ac16fae0e0087f48286cf381')

build() {
  cd ${srcdir}/openal-soft-${pkgver}/build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release .. || return 1
  make || return 1
}

package() {
  cd ${srcdir}/openal-soft-${pkgver}/build
  make DESTDIR=${pkgdir}/ install  || return 1

  install -Dm644 "$srcdir/alsoft.conf" "$pkgdir/etc/openal/alsoft.conf"
}
