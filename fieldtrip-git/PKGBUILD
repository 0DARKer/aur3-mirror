# Maintainer: cornholio <vigo.the.unholy.carpathian@gmail.com>

pkgname=fieldtrip-git
pkgver=20140126
pkgrel=1
pkgdesc="A Matlab software toolbox for MEG and EEG analysis."
arch=('i686' 'x86_64')
license=('GPL2')
provides=('fieldtrip')
depends=('fltk' 'make' 'java-environment')
if [[ "$CARCH" == 'i686' ]]; then
	depends+=('gcc-libs' 'libxft' 'libxext' 'ncurses')
else
	depends+=('gcc-libs-multilib' 'lib32-libxft' 'lib32-libxext' 'lib32-ncurses')
fi
optdepends=('spm8' 'suitesparse')
url="http://fieldtrip.fcdonders.nl/"
source=('git://github.com/fieldtrip/fieldtrip.git')
sha1sums=('SKIP')
	   
pkgver() {
	cd "${srcdir}/fieldtrip"
	git log -1 --format="%cd" --date=short | sed 's|-||g'
}

build() {
	
	cd "${srcdir}/fieldtrip/realtime/src"
	find . -name \*.cc -exec sed -i "s^#include <Fl/^#include <FL/^g" "{}" \;
	find . -name \*.cc -exec sed -i "s^#include <FL/\(.*\).h>^#include <FL/\1.H>^g" "{}" \;
	./rebuild.sh clean
	./rebuild.sh all

	cd "${srcdir}/fieldtrip/peer/src"
	make

	cd "${srcdir}/fieldtrip/external/vgrid"
	make

}

package() {

	# Remove Windows stuff and things covered by other packages
	rm -r "${srcdir}/fieldtrip/external/spm2"
	rm -r "${srcdir}/fieldtrip/external/spm8"
	rm -r "${srcdir}/fieldtrip/peer/src/win32"
	rm -r "${srcdir}/fieldtrip/realtime/bin/win32"
	rm -r "${srcdir}/fieldtrip/realtime/bin/raspberrypi"
	rm -r "${srcdir}/fieldtrip/realtime/src/buffer/src/win32"
	rm -r "${srcdir}/fieldtrip/realtime/src/external/pthreads-win32"
	rm -r "${srcdir}/fieldtrip/realtime/src/external/pthreads-win64"
	rm -r "${srcdir}/fieldtrip/realtime/src/acquisition/tobi/lib/ticpp/win"
	rm -r "${srcdir}/fieldtrip/external/dmlt/external/gpstuff/SuiteSparse"

	# Remove object files
	find "${srcdir}/fieldtrip" -name \*.o -exec rm "{}" \;
	
	# Remove files for other architectures
	find "${srcdir}/fieldtrip" -name \*.mexmac\* -exec rm "{}" \;
	find "${srcdir}/fieldtrip" -name \*.mexw\* -exec rm "{}" \;
	find "${srcdir}/fieldtrip" -name \*.mexarmhf -exec rm "{}" \;
	find "${srcdir}/fieldtrip" -name \*.mexsol -exec rm "{}" \;
	find "${srcdir}/fieldtrip" -name \*.hpux -exec rm "{}" \;
	if [[ "$CARCH" == 'i686' ]]; then
	find "${srcdir}/fieldtrip" -name \*.mexa64\* -exec rm "{}" \;
	find "${srcdir}/fieldtrip" -type d -name glnxa64 -exec rm -r "{}" \;
	fi
	if [[ "$CARCH" == 'x86_64' ]]; then
	find "${srcdir}/fieldtrip" -name \*.mexglx\* -exec rm "{}" \;
	find "${srcdir}/fieldtrip" -type d -name glnx86 -exec rm -r "{}" \;
	fi

	mkdir -p "${pkgdir}/opt"
	cp -r "${srcdir}/fieldtrip" "${pkgdir}/opt/"
	rm -r "${pkgdir}/opt/fieldtrip/.git"

}
