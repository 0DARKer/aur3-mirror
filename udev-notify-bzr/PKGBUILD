# Maintainer: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=udev-notify-bzr
pkgver=22
pkgrel=1
pkgdesc="Visual hardware notifications for Linux."
arch=('i686' 'x86_64')
url="https://launchpad.net/udev-notify"
license=('GPL3')
depends=('notification-daemon' 'python2' 'python2-udev' 'python-notify')
makedepends=('bzr')
source=()
md5sums=()

_bzrtrunk="https://code.launchpad.net/udev-notify/trunk"
_bzrmod="udev-notify"

build() {
	cd "$srcdir"
	msg "Connecting to Bazaar server...."

	if [ -d $_bzrmod ] ; then
		cd $_bzrmod && bzr --no-plugins pull $_bzrtrunk -r $pkgver
		msg "The local files are updated."
	else
		bzr --no-plugins branch $_bzrtrunk $_bzrmod -q -r $pkgver
	fi

	msg "Bazaar checkout done or server timeout"
	msg "Starting make..."

	rm -rf "$srcdir/$_bzrmod-build"
	cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
	cd "$srcdir/$_bzrmod-build"

	sed 's|\./tools/.*||g' -i Makefile
	make -s

	# Using system pyudev:
	rm -r build/debian/usr/share/udev-notify/pyudev
}

package() {
	cd "$srcdir/$_bzrmod-build"

	cp -r build/debian/* "$pkgdir"
	sed 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' -i \
		"$pkgdir/usr/share/udev-notify/udev-notify.py"
}

# vim: set ft=sh ts=3 sw=3 tw=0:
