# Based on PKGBUILD by: Nathan Jones <nathanj@insightbb.com>
# Contributor: Johann Kl√§hn <kljohann@gmail.com>

pkgname=ledger-git
pkgver=20101218
pkgrel=1
pkgdesc="CLI double-entry accounting tool"
depends=('gmp' 'pcre' 'expat' 'mpfr>=2.4.0' 'boost' 'libedit')
makedepends=('git' 'sed' 'python2')
url="https://github.com/jwiegley/ledger/wiki"
license=('BSD')
arch=('i686' 'x86_64')
provides=('ledger')
conflicts=('ledger')
options=('!strip') # for debugging purposes
install=ledger.install
source=()
md5sums=()

_gitroot="git://github.com/jwiegley/ledger.git"
_gitname="ledger"

build() {
  cd $srcdir

  msg "Connecting to GIT server..."
  if [[ -d $_gitname ]]; then
    (cd $_gitname && git pull origin)
  else
    git clone $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf $_gitname-build
  cp -r $_gitname $_gitname-build
  cd $_gitname-build

  #patch -p1 -i ../automake.patch
  find -iname '*.py' -execdir sed -i 's/^#!.*python/#!\/usr\/bin\/python2/' '{}' \;
  sed -i 's/^#!.*python/#!\/usr\/bin\/python2/' ./acprep

  ./acprep submodule
  CPPFLAGS=-DBOOST_IOSTREAMS_USE_DEPRECATED ./acprep debug make --no-python -- --prefix=/usr

  #make check
}

package () {
  cd "$srcdir/$_gitname-build"
  make DESTDIR=$pkgdir install

  rm -rf $pkgdir/usr/share/locale

  # handle the dir file in the install script
  rm -rf $pkgdir/usr/share/info/dir

  install -D -m644 doc/LICENSE $pkgdir/usr/share/licenses/custom/ledger/license.txt
}
