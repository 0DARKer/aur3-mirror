# Contributor: henning mueller <henning@orgizm.net>

pkgname=transmission-rss
pkgver=0.1.10
pkgrel=1
pkgdesc='Adds torrents from rss feeds to transmission web frontend.'
arch=(any)
license=(GPL)
url=http://rubygems.org/gems/transmission-rss
depends=(ruby)
optdepends=(
  'ruby-libglade: Gtk configuration editor'
)
source=(
  https://rubygems.org/downloads/$pkgname-$pkgver.gem
  $pkgname.install
)

package() {
  cd $srcdir

  export RBENV_VERSION=system

  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "$pkgdir$_gemdir" \
    $pkgname-$pkgver.gem

  mkdir -p $pkgdir/usr/bin
  ln -s $_gemdir/bin/$pkgname \
    $pkgdir/usr/bin/$pkgname

  install -D $pkgdir/$_gemdir/gems/$pkgname-$pkgver/README.rdoc \
    $pkgdir/usr/share/doc/$pkgname/README
}

sha256sums=('227cd41d719c6c57b21bf3ee4fcdf2c2bb2aa843dcc72d27c20f27c60a45e4a7'
            '8d88a4858d6ce3f84fe851534d4bbf2e0ca8ecb81e25c095e128046d08fb764e')
