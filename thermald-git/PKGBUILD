pkgname=thermald-git
_gitname=thermal_daemon
pkgver=83.760f473
pkgrel=1
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('i686' 'x86_64')
url="https://github.com/01org/thermal_daemon"
license=('GPL2')
depends=('dbus>=1.1' 'dbus-glib>=0.94' 'glib2>=2.22' 'libxml2>=2.4')
makedepends=('git')
conflicts=('thermald')
provides=('thermald')
source=('git://github.com/01org/thermal_daemon.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
}
