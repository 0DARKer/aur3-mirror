# Contributor: mariusz myswiat <my.swiat@o2.pl>
# Maintainer: Andrew Crerar <asc9003 [at] rit [dot] edu>

pkgname=gtk+-git
_gitname=gtk+
pkgver=20130428
pkgrel=3
pkgdesc="GTK+ is a multi-platform toolkit for creating graphical user interfaces. (Git version)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk+-git.install
license=('LGPL')
depends=('libxcomposite' 'pango' 'glib2-git' 'colord' 'at-spi2-atk' 'libxinerama' 'libxcursor' 'libcups' 'libxkbcommon')
provides=('gtk+' 'gtk3')
options=('!libtool')
conflicts=('gtk3-svn' 'gtk3-git' 'gtk3')
source=('gtk+::git://git.gnome.org/gtk+')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname
  ./autogen.sh --prefix=/usr \
	       --sysconfdir=/etc \
	       --localstatedir=/var \
	       --disable-schemas-compile \
	       --enable-x11-backend \
	       --enable-broadway-backend \
	       --enable-wayland-backend

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}

