# Maintainer:  Eric Biggers <ebiggers3 at gmail dot com>

pkgname=velvet
pkgver=1.2.06
pkgrel=1
pkgdesc="Sequence assembler for very short reads"
arch=("i686" "x86_64")
url="http://www.ebi.ac.uk/~zerbino/velvet/"
license=("GPL3")
depends=("zlib")
options=(!makeflags) # Parallel make fails?
source=("http://www.ebi.ac.uk/~zerbino/velvet/velvet_1.2.06.tgz")

build() {
	cd "${pkgname}_${pkgver}"
	make
}

package() {
	cd "${pkgname}_${pkgver}"
	mkdir -p "${pkgdir}"/usr/share/doc/velvet
	mkdir -p "${pkgdir}"/usr/bin
	mkdir -p "${pkgdir}"/usr/share/man/man1
	install -t "${pkgdir}"/usr/share/doc/velvet -m644 Manual.pdf README.txt CREDITS.txt
	install -t "${pkgdir}"/usr/bin -m755 velvetg velveth 

	mkdir -p "${pkgdir}"/usr/share/velvet
	cp -r contrib "${pkgdir}"/usr/share/velvet

	for program in velvetg velveth; do
		help2man --no-info --version-option "" ./$program > \
				"${pkgdir}"/usr/share/man/man1/$program.1
	done
}

md5sums=("eedc6e5b6099bbd2b9e01d9def0250c8")
