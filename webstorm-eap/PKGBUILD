# Maintainer : Dustin Falgout <dustin@falgout.us>
# Contributor: Eduard Kracmar <edke.kraken[at]gmail[dot]com>

pkgname=webstorm-eap
_pkgname=WebStorm
_pkgver=8.0
_buildver=134.697
pkgver=${_pkgver}.${_buildver}
epoch=1
pkgrel=1
pkgdesc="IDE for the Web developers and coders, built on top of IntelliJ IDEA platform. 30-Day Trial."
arch=('i686' 'x86_64')
url="http://www.jetbrains.com/webstorm/"
license=('custom')
depends=('java-environment')
conflicts=('webstorm')
provides=('webstorm')
source=(http://download.jetbrains.com/webstorm/WebStorm-EAP-${_buildver}.tar.gz)
md5sums=('fc4564404de926b6d2db9af7d0a41ba2')

package() {
  cd ${srcdir}
  mkdir -p ${pkgdir}/opt/${pkgname}
  cp -R ${srcdir}/${_pkgname}-${_buildver}/* ${pkgdir}/opt/${pkgname}
  if [[ $CARCH = 'i686' ]]; then
     rm -f ${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so
     rm -f ${pkgdir}/opt/${pkgname}/bin/fsnotifier64
     rm -f ${pkgdir}/opt/${pkgname}/bin/libnotifywrapper64.so
     rm -R -f ${pkgdir}/opt/${pkgname}/lib/libpty/linux/x86_64
  fi
  if [[ $CARCH = 'x86_64' ]]; then
     rm -f ${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux.so
     rm -f ${pkgdir}/opt/${pkgname}/bin/fsnotifier
     rm -R -f ${pkgdir}/opt/${pkgname}/lib/libpty/linux/x86
     rm -f ${pkgdir}/opt/${pkgname}/bin/libnotifywrapper.so
  fi

(
cat <<EOF
[Desktop Entry]
Version=${pkgver}
Name=WebStorm
Icon=webstorm
GenericName=IDE For Web Developmers
Comment=30-day free trial
Exec=/opt/${pkgname}/bin/webstorm.sh
Terminal=false
Type=Application
Categories=Development
EOF
) > ${srcdir}/webstorm.desktop

  mkdir -p ${pkgdir}/usr/bin/
  mkdir -p ${pkgdir}/usr/share/applications/
  mkdir -p ${pkgdir}/usr/share/pixmaps/
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m 644 ${srcdir}/webstorm.desktop ${pkgdir}/usr/share/applications/
  install -m 644 ${pkgdir}/opt/${pkgname}/bin/webide.png ${pkgdir}/usr/share/pixmaps/webstorm.png
  install -m 644 ${srcdir}/${_pkgname}-${_buildver}/license/${_pkgname}_license.txt ${pkgdir}/usr/share/licenses/${pkgname}/${_pkgname}_license.txt
  ln -s /opt/$pkgname/bin/webstorm.sh "$pkgdir/usr/bin/webstorm"
}
