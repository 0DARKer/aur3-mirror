# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com> 
pkgname=glaxium-fedora
__pkgalias=glaxium
pkgver=0.5
pkgrel=2
__patch=8
__fc=fc13
pkgdesc="Glaxium space-ship shoot-em-up style game with Fedora's patches"
arch=('i686' 'x86_64')
url="https://admin.fedoraproject.org/community/?package=glaxium#package_maintenance/package_overview"
license=('GPL2')
makedepends=('rpmextract')
depends=('sdl_mixer' 'freeglut' 'mesa' 'libpng')
conflicts=('glaxium')
source=(            
http://kojipkgs.fedoraproject.org/packages/${__pkgalias}/${pkgver}/${__patch}.${__fc}/src/${__pkgalias}-${pkgver}-${__patch}.${__fc}.src.rpm
)
md5sums=('7cb45f6dac16ee8a97108ccbf4a15b85')  

build() {
    cd $srcdir
    rpmextract.sh ${__pkgalias}-${pkgver}-${__patch}.${__fc}.src.rpm
    tar -zxvf ${__pkgalias}_${pkgver}.tar.gz
    cd ${__pkgalias}_${pkgver}
    patch -Np1 -i ../${__pkgalias}-0.5-fixes.patch
    patch -Np1 -i ../${__pkgalias}_0.5-allow-running-when-dsp-busy.patch
    patch -Np1 -i ../${__pkgalias}_0.5-glutInit.patch
    patch -Np1 -i ../${__pkgalias}_0.5-rh553067.patch
    ./configure --prefix=/usr
    make
}

package(){
    cd ${srcdir}/${__pkgalias}_${pkgver}
    install -Dm755 glaxium $pkgdir/usr/bin/glaxium
    mkdir -p $pkgdir/usr/share/games/glaxium/{samples,textures}
    install -m644 samples/* $pkgdir/usr/share/games/glaxium/samples
    install -m644 textures/* $pkgdir/usr/share/games/glaxium/textures

    cd $srcdir
    mkdir -p $pkgdir/usr/share/{applications,pixmaps}
    install -m644 glaxium.png $pkgdir/usr/share/pixmaps
    sed 's:glaxium-wrapper:glaxium:' -i glaxium.desktop
    install -m644 glaxium.desktop $pkgdir/usr/share/applications
}           


  
