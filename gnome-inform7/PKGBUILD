# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=gnome-inform7
pkgver=6L02
pkgrel=1
pkgdesc="GNOME port of the Inform 7 IDE for the Inform 7 programming language for interactive fiction"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/gnome-inform7"
license=('GPL3')
depends=('aspell-en' 'desktop-file-utils' 'goocanvas1' 'gst-plugins-bad'
         'gst-plugins-base' 'gst-plugins-good' 'gtksourceview2'
         'gtkspell' 'webkitgtk2')
makedepends=('autoconf' 'automake' 'graphviz' 'intltool' 'pkgconfig'
             'texlive-core')
options=('!libtool')
install="${pkgname}.install"
source=("http://inform7.com/download/content/${pkgver}/I7_${pkgver}_Linux_all.tar.gz"
        "http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/Gnome_UI_source_${pkgver}.tar.gz")
md5sums=('4a69207cdee272b0de176a78b8163403'
         'd29cced8f4c044307d3530b690b338e0')
[ "${CARCH}" = "i686" ]   && _arch='i386'
[ "${CARCH}" = "x86_64" ] && _arch='x86_64'

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  bsdtar -C src/ni --strip-components 3 -xf "${srcdir}/inform7-${pkgver}/inform7-compilers_${pkgver}_${_arch}.tar.gz" share/inform7/Compilers/ni
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --enable-manuals
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
