# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Lari Tikkanen <lartza at wippies.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Joao Rodrigues <gothicknight (at) gmail (dot) com>

pkgname=atunes
pkgver=3.0.9
pkgrel=1
pkgdesc="A full-featured audio player and manager"
arch=('i686' 'x86_64')
url="http://www.atunes.org/"
license=('GPL')
depends=('java-environment>=6' 'mplayer')
if [[ "$CARCH" == 'i686' ]]; then
  depends+=('gcc-libs' 'libx11')
elif [[ "$CARCH" == 'x86_64' ]]; then
  depends+=('lib32-gcc-libs' 'lib32-libx11')
fi
optdepends=('cdrkit: for ripping CDs via cdda2wav'
            'vorbis-tools: for ripping Ogg Vorbis via oggenc'
            'lame: for ripping MP3 via lame')
install=atunes.install
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/aTunes%20${pkgver}/${pkgname}-${pkgver}-bin.tar.gz
        ${pkgname}.desktop)
md5sums=('31a8c017619a919d5a5affe9a56cc3f3'
         'e00f8bcc3df45734295a37815934ba14')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "21 c\cd /usr/share/java/${pkgname}" aTunes.sh
  install -Dm755 aTunes.sh "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 ${pkgname}.png "$pkgdir/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -dm755 "${pkgdir}/usr/share/java/${pkgname}/lib"
  install -m644 lib/* "${pkgdir}/usr/share/java/${pkgname}/lib"
  install -dm755 "${pkgdir}/usr/share/java/${pkgname}/settings"
  install -dm755 "${pkgdir}/usr/share/java/${pkgname}/settings/spring"
  install -m644 settings/*.* "${pkgdir}/usr/share/java/${pkgname}/settings"
  install -m644 settings/spring/*.* "${pkgdir}/usr/share/java/${pkgname}/settings/spring"
  install -m644 *.{ico,jar,so} "${pkgdir}/usr/share/java/${pkgname}"
}
