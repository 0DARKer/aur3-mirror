# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Lari Tikkanen <lartza at wippies.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Joao Rodrigues <gothicknight (at) gmail (dot) com>

pkgname=atunes
pkgver=3.0.1
pkgrel=1
pkgdesc="A full-featured audio player and manager"
arch=('i686' 'x86_64')
url="http://www.atunes.org/"
license=('GPL')
depends=('java-environment>=6' 'mplayer')
optdepends=('cdrkit: for ripping CDs via cdda2wav'
            'vorbis-tools: for ripping Ogg Vorbis via oggenc')
install=atunes.install
source=(http://downloads.sourceforge.net/$pkgname/${pkgver}/${pkgname}-${pkgver}-bin.tar.gz
        $pkgname.desktop)
md5sums=('e86d78fb6210bea2f8a395e7b1d80e8d'
         'e00f8bcc3df45734295a37815934ba14')

package() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "21 c\cd /usr/share/java/$pkgname" aTunes.sh
  install -Dm755 aTunes.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -dm755 "$pkgdir/usr/share/java/$pkgname/lib"
  install -m644 lib/* "$pkgdir/usr/share/java/$pkgname/lib"
  install -dm755 "$pkgdir/usr/share/java/$pkgname/settings"
  install -dm755 "$pkgdir/usr/share/java/$pkgname/settings/spring"
  install -m644 settings/*.* "$pkgdir/usr/share/java/$pkgname/settings"
  install -m644 settings/spring/*.* "$pkgdir/usr/share/java/$pkgname/settings/spring"
  install -m644 *.{ico,jar,so} "$pkgdir/usr/share/java/$pkgname"
}
