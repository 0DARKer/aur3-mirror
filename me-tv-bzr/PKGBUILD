
#Maintainer: Gianluca Boiano (aka M0Rf30)

pkgname=me-tv-bzr
pkgver=49
pkgrel=2
pkgdesc="A digital television (DVB) viewer for GNOME."
arch=('i686' 'x86_64')
url="https://launchpad.net/me-tv"
license=('GPL')
depends=('gtkmm' 'gconfmm' 'libxml++' 'libunique' 'sqlite3' 'vlc' 'gstreamer0.10-base' 'gnome-common')
makedepends=('intltool' 'bzr')
provides=('me-tv')
conflicts=('me-tv')
install='me-tv-bzr.install'
_bzrtrunk="lp:me-tv"
_bzrmod=development

build() {
  cd "${srcdir}"
  msg "Connecting to the server..."

  if [ ! -d ./${_bzrmod} ]; then
    bzr co ${_bzrtrunk} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi

  mkdir -p "${_bzrmod}-build"
  cd "${_bzrmod}"
  # Missing unistd.h includes :| https://bugs.launchpad.net/me-tv/+bug/1020690
  for file in common/common.cc server/thread.cc; do
      sed -i '1 i\
          #include <unistd.h>' $file
  done
  #fix aclocale
  sed -i '/AM_PROG_CC_STDC/d' configure.ac
  
  msg "Starting make..."
  cd "../${_bzrmod}-build/"
  NOCONFIGURE=1 ../"$_bzrmod"/autogen.sh
  ../"$_bzrmod"/configure CXXFLAGS="$CXXFLAGS -D__KERNEL_STRICT_NAMES" --prefix=/usr
    
  make
}
 
package() {     
  cd "${srcdir}/${_bzrmod}-build"

  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
  --domain me-tv "${pkgdir}/usr/etc/gconf/schemas/${pkgname}.schemas"

  rm -rf "${pkgdir}/usr/etc/"
}

