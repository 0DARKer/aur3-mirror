# Maintainer: Gicu Gorodenco <cyclopsihus 'at' gmail 'dot' com>
# Contributor: rafael ff1 <rafael.f.f1 'at' gmail 'dot' com> 
_pkgbasename=gtkmm
pkgname=lib32-$_pkgbasename
pkgver=2.22.0
pkgrel=1
pkgdesc="C++ bindings for gtk2 (32 bit, library only)"
arch=('x86_64')
url="http://gtkmm.sourceforge.net/"
license=('LGPL')
depends=('lib32-gtk2>=2.22.1' 'lib32-pangomm>=2.26.2' 'lib32-atkmm>=2.22.1' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/2.22/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('8478d80d8d4793c2e1c459a71bad6a767a43eaf2664da06a9bd65a2b98bb07b1')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
  # Cleanup for  a lib32 package
  rm -rf ${pkgdir}/usr/{include,lib32/gtkmm*,lib32/gdkmm*}
  # Install license
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
