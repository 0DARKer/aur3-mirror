# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: herb <herb@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=planner
pkgver=0.14.4
pkgrel=6
pkgdesc="Gnome Project Management System"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Planner"
license=('GPL')
depends=('libgnomeui>=2.24.4' 'evolution-data-server>=2.32.0' 'libxslt>=1.1.26' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('pkg-config' 'intltool' 'gnome-doc-utils>=0.20.1' 'pygtk>=2.22')
options=('!libtool' '!makeflags' '!emptydirs')
install=planner.install
source=(http://ftp.gnome.org/pub/GNOME/sources/planner/${pkgver%.*}/planner-${pkgver}.tar.bz2)
md5sums=('8fcbd1b55dd037662f7710a28a96bb92')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	./configure --prefix=/usr --sysconfdir=/etc \
			--localstatedir=/var --disable-update-mimedb \
			--enable-python --enable-eds
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
	install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
	gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain planner ${pkgdir}/etc/gconf/schemas/*.schemas
	rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
