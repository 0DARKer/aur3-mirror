pkgname=mingw-w64-fontconfig
pkgver=2.10.1
pkgrel=1
pkgdesc="A library for configuring and customizing font access (mingw-w64)"
arch=(any)
url="http://www.fontconfig.org/release"
license=("custom")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt "mingw-w64-expat>=2.0.1" "mingw-w64-freetype>=2.3.11")
options=(!libtool !strip !buildflags)
source=("http://www.fontconfig.org/release/fontconfig-$pkgver.tar.gz")
md5sums=('c94e380eea42f2f23ca9537035ef1899')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  export CFLAGS="$CFLAGS -mms-bitfields"
  mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-i686-w64-mingw32"
  cd "${srcdir}/${pkgname}-${pkgver}-build-i686-w64-mingw32"
  ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
    --prefix=/usr/i686-w64-mingw32 \
    --build=$CHOST \
    --host=i686-w64-mingw32 \
    --with-arch=i686
  make
  mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-x86_64-w64-mingw32"
  cd "${srcdir}/${pkgname}-${pkgver}-build-x86_64-w64-mingw32"
  ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
    --prefix=/usr/x86_64-w64-mingw32 \
    --build=$CHOST \
    --host=x86_64-w64-mingw32 \
    --with-arch=x86_64
  make
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/"{etc,share}
  done
}