pkgname=mingw-w64-fontconfig
pkgver=2.11.0
pkgrel=4
pkgdesc="A library for configuring and customizing font access (mingw-w64)"
arch=(any)
url="http://www.fontconfig.org/release"
license=("custom")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt "mingw-w64-expat>=2.1.0-4" "mingw-w64-freetype>=2.3.11" "mingw-w64-bzip2>=1.0.6-4" mingw-w64-libiconv)
options=(staticlibs !strip !buildflags)
source=("http://www.fontconfig.org/release/fontconfig-$pkgver.tar.bz2"
"fontconfig-disable-migration-testcase-on-win32.patch")
sha256sums=('cb0e0ef6f03bc8568e95653840bb07d5859dc89d7ce2cade9d94fcccf8c1a467'
            '08218f7d55348c136e63737b35aaabff4dae2dfa4d29cde8187c234ed59d44b9')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	cd "$srcdir/fontconfig-$pkgver"
	patch -p1 -i ../fontconfig-disable-migration-testcase-on-win32.patch
	autoreconf -fi
}

build() {
	cd "$srcdir/fontconfig-$pkgver"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    export CFLAGS="-O2 -mms-bitfields"
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-iconv \
      --with-arch=${_arch%-w64-mingw32}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/"{etc,share}
  done
}
