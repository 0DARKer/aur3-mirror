# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=geogebra
pkgver=3.2.47.0
_pkgver=3-2-47-0
pkgrel=2
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet"
arch=('any')
url='http://www.geogebra.org/'
license=('custom') 
# Application and source code under GPL / Language files under CCPL
depends=('java-runtime' 'hicolor-icon-theme' 'shared-mime-info')
optdepends=('geogebra-thumbnail-kde')
install=('geogebra.install')
source=(http://geogebra.googlecode.com/files/GeoGebra-FreeBSD-Installer-${_pkgver}.tar.gz
	http://www.geogebra.org/download/license.txt
	geogebra.sh)
noextract=('*.jar')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Installing application
  install -d -m755 ${pkgdir}/usr/share/java/${pkgname}/unsigned
  install -D -m644 *.jar ${pkgdir}/usr/share/java/${pkgname}
  install -D -m644 unsigned/*.jar ${pkgdir}/usr/share/java/${pkgname}/unsigned

  # Intalling launcher
  install -D -m755 $srcdir/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}

  # Copying icons
  cp -R icons ${pkgdir}/usr/share/
  
  # Creating menu item
  install -D -m644 ${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop

  # Installing mime-type
  install -D -m644 ${pkgname}.xml ${pkgdir}/usr/share/mime/packages/${pkgname}.xml

  # Installing license
  cd ${srcdir}
  install -D -m644 license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('a9de7fa7fae5d978d7530100f24c011c'
         '39e2fbaf507a7b8db047152332d695c5'
         'd73e46f95be0edfbb9a9857ad6563e76')
