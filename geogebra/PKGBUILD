# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=geogebra
pkgver=4.0.0.0
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet"
arch=('any')
url='http://www.geogebra.org/'
license=('custom')
# Application and source code under GPL / Language files under CCPL:by-sa
depends=('java-runtime' 'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils' 'xdg-utils')
optdepends=('kde-thumbnailer-geogebra: generates thumbnails of GeoGebra files in KDE'
	    'geogebra-prim: adds a menu entry for the primary school version')
install=('geogebra.install')
source=(http://geogebra.googlecode.com/files/GeoGebra-Unixlike-Installer-${pkgver}.tar.gz
	$pkgname.patch
	$pkgname.desktop.patch)
noextract=('*.jar')
md5sums=('5f62e0246e92002503d7cf074c7bb504'
         '77625adf901f2356e38f1256ad29b75b'
         '534c4b9b618b6c75c8cf8abde6856232')

package() {
  cd $srcdir/$pkgname-$pkgver

  # Installing application
  install -d -m755 $pkgdir/usr/share/java/$pkgname/unsigned
  install -D -m644 *.jar $pkgdir/usr/share/java/$pkgname
  install -D -m644 unsigned/*.jar $pkgdir/usr/share/java/$pkgname/unsigned
  patch -p0 < $srcdir/$pkgname.patch
  patch -p0 < $srcdir/$pkgname.desktop.patch
  install -d -m755 $pkgdir/usr/bin
  install -D -m755 $pkgname $pkgdir/usr/bin/$pkgname

  # Copying icons
  for _size in "16x16" "22x22" "24x24" "32x32" "36x36" "48x48" "64x64" "72x72" "96x96" "128x128" "192x192" "256x256" "scalable"
  do
    install -d -m755 $pkgdir/usr/share/icons/hicolor/${_size}/{apps,mimetypes}
    install -D -m644 icons/hicolor/${_size}/apps/$pkgname.* $pkgdir/usr/share/icons/hicolor/${_size}/apps
    install -D -m644 icons/hicolor/${_size}/mimetypes/*.* $pkgdir/usr/share/icons/hicolor/${_size}/mimetypes
  done
  
  # Creating menu item
  install -D -m644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop

  # Installing mime-type
  install -D -m644 $pkgname.xml $pkgdir/usr/share/mime/packages/$pkgname.xml

  # Installing license
  install -D -m644 _LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

