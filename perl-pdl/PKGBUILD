# CPAN Name  : PDL
# Contributor: Anton Leontiev <bunder /at/ t-25.ru>
# Generator  : CPANPLUS::Dist::Arch 1.15

# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: carltonf <xiong[c05]@gmail.com>
# Contributor: Colin Pitrat <colin.pitrat@gmail.com>

pkgname=perl-pdl
pkgver=2.4.9
pkgrel=2
pkgdesc="The Perl Data Language, a perl extension designed for scientific and bulk numeric data processing and display"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/PDL"
license=("PerlArtistic" "GPL")
depends=("perl>=5.8.1" "perl-inline>=0.43" "gsl")
makedepends=("fftw2" "hdf4" "perl-opengl>=0.63" "plplot" "proj" "perl-extutils-f77")
optdepends=(
	"fftw2: for PDL::FFTW support"
	"hdf4: for HDF files support"
	"perl-astro-fits-header: improved FITS files support"
#	"perl-convert-uu: for the case when something wrong with Unicode support"  # It is necessary on *BSD systems
#	"perl-extutils-f77: for PDL::Slatec and PDL::Minuit support"               # It is not necessary at runtime
	"perl-opengl>=0.63: for PDL::Graphics::TriD support"
	"plplot: for PDL::Graphics::PLplot support"
	"proj: for PDL::GIS::Proj and PDL::Transform::Proj4 support")
source=(http://search.cpan.org/CPAN/authors/id/C/CH/CHM/PDL-2.4.9.tar.gz gsl_sf_laguerre.patch)
options=(!emptydirs)
changelog=("changelog")
md5sums=("459e127d3ad7d80a95d6006373b1da6c" "f535659688fff07c9c520742e2881acc")

build() {
	cd "$srcdir/PDL-2.4.9"
	patch -p1 -i../gsl_sf_laguerre.patch
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "$srcdir/PDL-2.4.9"
	make test
}

package() {
	cd "$srcdir/PDL-2.4.9"
	make install DESTDIR="$pkgdir"
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
