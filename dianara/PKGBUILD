# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=dianara
pkgver=0.2
pkgrel=1
pkgdesc="A Qt Diasora client"
arch=(i686 x86_64)
url="http://dianara.nongnu.org/"
license=('GPL')
depends=('qjson')
install="$pkgname.install"
source=("http://download.savannah.gnu.org/releases/$pkgname/$pkgname-v$pkgver.tar.gz")
md5sums=('f292e0599cf3a4c9e9709c3feb933653')

build() {
  cd "$srcdir"

  # translation path fix
  sed -i 's|:/translations|/usr/share/locale|' src/main.cpp

  # resources path fix
  sed -e 's|:/icon/64x64|/usr/share/pixmaps|' \
      -e 's|:/images|/usr/share/dianara|' \
      -i src/mainwindow.cpp

  [ -d bld ] || mkdir bld && cd bld
  qmake ..
  make
}

package() {
  cd "$srcdir"

  # bin
  install -Dm755 bld/$pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 icon/64x64/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # translations
  install -d "$pkgdir/usr/share/locale"
  install -Dm644 translations/*.qm \
    "$pkgdir/usr/share/locale"
}

# vim:set ts=2 sw=2 et:
