# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org> ([core] package)

pkgname=openvpn-git
pkgver=20111121
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network) - git checkout"
arch=(i686 x86_64)
url="http://openvpn.net/index.php/open-source.html"
depends=('openssl' 'lzo2' 'iproute2')
conflicts=('openvpn')
provides=('openvpn')
license=('custom')
backup=(usr/share/openvpn/easy-rsa/vars
        usr/share/openvpn/easy-rsa/openssl-1.0.0.cnf
        etc/conf.d/openvpn-tapdev)
source=(openvpn.rc
        openvpn-tapdev.rc
        openvpn-tapdev.conf)

_gitroot="git://openvpn.git.sourceforge.net/gitroot/openvpn/openvpn.git"
_gitname="openvpn"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Build openvpn
  autoreconf -vi
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\"" ./configure \
    --prefix=/usr \
    --enable-password-save \
    --mandir=/usr/share/man \
    --enable-iproute2
  make

  # Build plugins
  for plug in auth-pam down-root; do
    cd ${srcdir}/${_gitname}-build/plugin/${plug}
    make
  done
}

package() {
  cd ${srcdir}/${_gitname}-build
  # Install openvpn
  make DESTDIR=${pkgdir} install
  install -d -m755 ${pkgdir}/etc/openvpn
  # Install examples
  install -d -m755 ${pkgdir}/usr/share/openvpn
  cp -r sample-config-files ${pkgdir}/usr/share/openvpn/examples
  find ${pkgdir}/usr/share/openvpn -type f -exec chmod 644 {} \;
  find ${pkgdir}/usr/share/openvpn -type d -exec chmod 755 {} \;
  # Install license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
  # Install plugins
  for plug in auth-pam down-root; do
    cd ${srcdir}/${_gitname}-build/plugin/${plug}
    install -D -m755 openvpn-${plug}.so ${pkgdir}/usr/lib/openvpn/openvpn-${plug}.so
    cd -
  done
  # Install contrib
  install -d -m755 ${pkgdir}/usr/share/openvpn/contrib
  cp -r contrib ${pkgdir}/usr/share/openvpn
  # Install easy-rsa
  cd ${srcdir}/${_gitname}-build
  make -C easy-rsa/2.0 install DESTDIR=${pkgdir} PREFIX=usr/share/openvpn/easy-rsa
  rm -f ${pkgdir}/usr/share/openvpn/easy-rsa/openssl-0.9.?.cnf
  # Install rc scripts
  install -D -m755 ${srcdir}/openvpn.rc ${pkgdir}/etc/rc.d/openvpn
  install -D -m755 ${srcdir}/openvpn-tapdev.rc ${pkgdir}/etc/rc.d/openvpn-tapdev
  install -D -m644 ${srcdir}/openvpn-tapdev.conf ${pkgdir}/etc/conf.d/openvpn-tapdev
}
md5sums=('a3809b9727f0c2af2d0770f5c7442db2'
         'd2c48e970088d679dd3c2afd914ff731'
         '722f483c9e3ce2ec66d3301aaf7cf3d5')
