# Maintainer: Oleg Plakhotniuk <olegus EIGHT AT gmail DOT com>

pkgname=w3c-css-validator
pkgver=2.1
pkgrel=1
pkgdesc="A tool for checking if CSS files conform with standards"
arch=('any')
url="http://jigsaw.w3.org/css-validator"
license=('custom')
depends=('java-environment' 'bash')
makedepends=('cvs' 'apache-ant')
source=('deps.patch' 'w3c-css-validator')
md5sums=('29321bafb103b10bb19f0aadf935347a'
         '83492dcf81e490dd0588a3626023aadc')

_cvsroot=":pserver:anonymous:anonymous@dev.w3.org:/sources/public"
_cvsrev="nytrulce"
_cvsmod="2002/css-validator"

build() {
  cd "$srcdir"
  msg "Connecting to $_cvsroot CVS server...."

  if [[ -d "$_cvsmod/CVS" ]]; then
    cd "$_cvsmod"
    cvs -z3 update -d
  else
    cvs -z3 -d "$_cvsroot" co -r "$_cvsrev" "$_cvsmod"
    cd "$_cvsmod"
  fi

  msg "CVS checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_cvsmod-build"
  cp -r "$srcdir/$_cvsmod" "$srcdir/$_cvsmod-build"
  cd "$srcdir/$_cvsmod-build"

  # fix dependencies (https://www.w3.org/Bugs/Public/show_bug.cgi?id=10347)
  patch -up1 -i "$srcdir/deps.patch"

  ant jar
}

package() {
  cd "$srcdir/$_cvsmod-build"
  install -d "$pkgdir/usr/share/java/$pkgname" "$pkgdir/usr/bin"
  install "$srcdir/w3c-css-validator" "$pkgdir/usr/bin"
  install -m644 css-validator.jar "$pkgdir/usr/share/java/$pkgname"
  for name in lib/*.jar ; do
    install -m644 "$name" "$pkgdir/usr/share/java/$pkgname"
  done
  install -D -m644 COPYRIGHT.html "$pkgdir/usr/share/licenses/$pkgname/LICENSE.html"
}

# vim:set ts=2 sw=2 et:
