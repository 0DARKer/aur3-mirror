# Maintainer: Alexander Minges <alexander.minges@gmail.com>
pkgname=coot
pkgver=0.7.1
pkgrel=1
pkgdesc="Crystallographic Object-Oriented Toolkit for model building, completion and validation"
arch=('i686' 'x86_64')
url="http://lmb.bioch.ox.ac.uk/coot/"
license=('GPL')
depends=('guile' 'guile-lib' 'net-http' 'gtkglext' 'ccp4-libs' 'clipper' 'goocanvas1' 'gsl' 'libgnomecanvas'
         'freeglut' 'libgl' 'gtk2' 'cairo' 'ssmlib' 'zlib' 'curl' 'python2' 'pygtk' 'gtkglarea' 'guile-gtk' 'guile-gui')
source=(http://www2.mrc-lmb.cam.ac.uk/personal/pemsley/coot/source/releases/$pkgname-$pkgver.tar.gz
	coot-configure.in.patch
	coot-unistd.patch
	coot-user-manual.texi.patch
	coot-makefile.patch
	coot-ssm1.3.patch)
sha256sums=('2310d48f7581283ee20eeb355d5fcef90e8d36bc032684b7b47f7c61e89ea0a9'
            '2477b806a87d32fe39c816e8429810b6466e945c1d586a809961c357f3734093'
            '11e5a8c38a077a881fc3b666c8ea06c376804312aa7e700a2b8a74b3b791e7a8'
            '2f5da6f7683d6d2b8add8c2cf4e821aaa839a214585d5ab480f38486e3fe9857'
            'c6b36d90900d6b4586391fa9e9738c09237761bf1e96b981d5a0f7c387e22570'
            'a5dbe1a75b549b04ca0c87f616416425cf648f36b52ea57e712f842cf60dbcb7')

build() {

## Nasty workaround for hardcoded python dependency ##
  cd $srcdir
  mkdir python2
  cd python2
  
  ln -s /usr/bin/python2 ./python
  ln -s /usr/bin/python2-config ./python-config
  
  export PATH=$srcdir/python2:${PATH}
## End workaround
  
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 -i "$srcdir/coot-configure.in.patch"
  patch -Np0 -i "$srcdir/coot-unistd.patch"
  patch -Np0 -i "$srcdir/coot-user-manual.texi.patch"
  patch -Np0 -i "$srcdir/coot-makefile.patch"
  patch -Np0 -i "$srcdir/coot-ssm1.3.patch"
  
  aclocal -I macros
  libtoolize --automake --copy
  autoconf
  automake --copy --add-missing --gnu
  
  CXXFLAGS="${CXXFLAGS} -fpermissive"
  CFLAGS="${CFLAGS} -fpermissive"

  ./configure --prefix=/usr \
              --includedir=/usr/include/coot \
              --with-goocanvas-prefix=/usr \
              --with-mmdb-prefix=/usr \
              --with-ssmlib-prefix=/usr \
              --with-clipper-prefix=/usr \
              --with-glut-prefix=/usr \
              --with-gsl-prefix=/usr \
              --with-guile \
              --with-guile-gtk \
              --with-guile-gtk-prefix=/usr \
              --with-gtk2 \
              --with-python \
              --with-pygtk \
              --disable-openmp \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
} 
