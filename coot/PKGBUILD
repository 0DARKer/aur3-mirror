# Maintainer: Alexander Minges <alexander.minges@gmail.com>
pkgname=coot
pkgver=0.7.1_r4596
_revision=4596
_pkgver=0.7.1-pre-revision-$_revision
pkgrel=1
pkgdesc="Crystallographic Object-Oriented Toolkit for model building, completion and validation"
arch=('i686' 'x86_64')
url="http://lmb.bioch.ox.ac.uk/coot/"
license=('GPL')
depends=('guile' 'guile-lib' 'net-http' 'gtkglext' 'ccp4-libs' 'clipper' 'goocanvas' 'openmpi' 'gsl'
         'freeglut' 'libgl' 'gtk2' 'cairo' 'ssmlib' 'zlib' 'curl' 'python2' 'pygtk' 'gtkglarea' 'guile-gtk' 'guile-gui')
source=(http://www2.mrc-lmb.cam.ac.uk/groups/murshudov/coot/source/pre-releases/$pkgname-$_pkgver.tar.gz)
sha256sums=('83a09266a0de3ef6abbed098cca535b1452a521f2d59a55278207e50bcb7cb8c')

build() {

## Nasty workaround for hardcoded python dependency ##
  cd $srcdir
  mkdir python2
  cd python2
  
  ln -s /usr/bin/python2 ./python
  ln -s /usr/bin/python2-config ./python-config
  
  export PATH=$srcdir/python2:${PATH}
## End workaround
  
  cd "$srcdir/$pkgname-${_pkgver/-revision-$_revision}"

  ./configure --prefix=/usr \
              --with-goocanvas-prefix=/usr \
              --with-mmdb-prefix=/usr \
              --with-ssmlib-prefix=/usr \
              --with-clipper-prefix=/usr \
              --with-glut-prefix=/usr \
              --with-gsl-prefix=/usr \
              --with-guile \
              --with-guile-gtk \
              --with-guile-gtk-prefix=/usr \
              --with-gtk2 \
              --with-python \
              --with-pygtk \
              --disable-openmp
  make
}

package() {
  cd "$srcdir/$pkgname-${_pkgver/-revision-$_revision}"

  make DESTDIR="$pkgdir/" install
} 
