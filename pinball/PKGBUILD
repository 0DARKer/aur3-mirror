# Contributor: Rob McCathie <archaur at rmcc dot com dot au>
# Contributor: Renato Garcia <fgar.renato@gmail.com>
# Contributor: dale <dale@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=pinball
pkgver=0.3.1
pkgrel=6
pkgdesc="The Emilia Pinball Project strives to fulfil your needs for a great pinball game in Linux"
arch=('i686' 'x86_64')
url="http://pinball.sourceforge.net/"
license=('GPL2')
depends=('sh' 'sdl_mixer' 'sdl_image' 'mesa' 'gcc-libs')
options=('!makeflags' 'libtool')
install=pinball.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'gcc45.patch'
        'pinball-0.3.1-cstddef.patch'
        'pinball-0.3.1-hiscore.patch'
        'pinball-0.3.1-lacomment.patch'
        'pinball-0.3.1-strictproto.patch'
        'pinball-0.3.1-sys-ltdl.patch')
md5sums=('f28e8f49e0db8e9491e4d9f0c13c36c6'
         'f488fbaec1834cdcb902674d99a2045d'
         'dbd34c8601d25314d6981e2e075f9a3e'
         '7e9be8fbee00bce123b90333a4ee2547'
         'd73a4410fb351e1b80699b556b192b59'
         '473cce9f5edb23153ccbec221cf3e294'
         '42bda2dd8d31582930f07c5fbce808d3')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ## Since adding the Fedora patches i've commented this out, but left it included in the source package in case it's of use later.
  #patch -Np1 -i "${srcdir}"/gcc45.patch
  
  ## The below patches are from Fedora, u pulled them out of: pinball-0.3.1-22.fc20.src.rpm
  ## ( http://redhat.download.fedoraproject.org/pub/fedora-secondary/development/20/source/SRPMS/p/pinball-0.3.1-22.fc20.src.rpm )
  patch -Np0 -i "${srcdir}"/pinball-0.3.1-cstddef.patch
  patch -Np1 -i "${srcdir}"/pinball-0.3.1-hiscore.patch
  patch -Np0 -i "${srcdir}"/pinball-0.3.1-lacomment.patch
  patch -Np0 -i "${srcdir}"/pinball-0.3.1-strictproto.patch
  patch -Np1 -i "${srcdir}"/pinball-0.3.1-sys-ltdl.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-sdltest
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
