# Maintainer: Felix Bier <flx.bier@googlemail.com>

pkgname=gnome-globalmenu-git
pkgver=20110724
pkgrel=1
pkgdesc="OSX/Unity-style global menu for GTK applications in the GNOME Shell"
arch=('i686' 'x86_64')
url=http://code.google.com/p/gnome2-globalmenu/
license=('GPL2')
depends=('gnome-shell')
makedepends=('git' 'intltool' 'vala')
conflicts=('gnome-globalmenu')
provides=('gnome-globalmenu')
install="$pkgname.install"
source=("module.c"
        "header.h"
        "header-gtk2.h")
md5sums=('c4655323c7edc8ae97edcc1aaf9128b5'
         'a934fa25f3a3a012abc8cc94edf1aecf'
         '7d24bded82f7ac12911b7b4d4dfcaa99')

_gitroot="git://github.com/gnome-globalmenu/gnome-globalmenu.git"
_gitname="gnome-globalmenu"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server:"
  msg "$_gitroot"

  if [[ -d $_gitname ]] ; then
    cd "$_gitname"
    git pull
  else
    git clone "$_gitroot" --depth=1

    cd "$_gitname"
    git checkout gnome-3
  fi

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cp ../{module.c,header.h,header-gtk2.h} ./src

  autoreconf \
  --force \
  --install \
  --verbose

  ./autogen.sh \
  --prefix=/usr \
  --sysconfdir=/etc \
  --libexecdir=/usr/lib

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make \
  DESTDIR=${pkgdir} \
  GTK2_MODULES_DIR=/usr/lib/gtk-2.0/modules \
  GTK3_MODULES_DIR=/usr/lib/gtk-3.0/modules \
  GLIB_COMPILE_SCHEMAS=/bin/true \
  install

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/$_gitname-build/globalmenu.sh" "${pkgdir}/etc/profile.d/"

  ln -sfv libglobalmenu-gtk2.so "${pkgdir}/usr/lib/gtk-2.0/modules/libglobalmenu-gtk.so"
}

