# $Id: PKGBUILD 153632 2012-03-16 22:34:32Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Nicky726 <nicky726@gmail.com>

pkgname=selinux-udev
_origname=udev
pkgver=181
pkgrel=5
pkgdesc="The userspace dev tools (udev) with SELinux support"
depends=('selinux-usr-libselinux' 'selinux-util-linux' 'libusb-compat' 'glib2' 'kmod' 'pciutils' 'usbutils' 'acl')
conflicts=("${_origname}")
provides=("${_origname}=${pkgver}-${pkgrel}")
install=udev.install
arch=(i686 x86_64)
license=('GPL')
makedepends=('gobject-introspection' 'gperf' 'libxslt' 'usbutils' 'kmod')
source=(ftp://ftp.kernel.org/pub/linux/utils/kernel/hotplug/$_origname-$pkgver.tar.xz
	0001-reinstate-TIMEOUT-handling.patch
	initcpio-hooks-udev
	initcpio-install-udev)
url="http://git.kernel.org/?p=linux/hotplug/udev.git;a=summary"
backup=(etc/udev/udev.conf)
groups=('selinux' 'selinux-system-utilites')
options=(!makeflags !libtool)

build() {
  cd $srcdir/$_origname-$pkgver

  patch -p1 -i ../0001-reinstate-TIMEOUT-handling.patch

  ./configure --prefix=/usr \
              --with-rootprefix= \
              --sysconfdir=/etc \
              --libdir=/usr/lib \
              --libexecdir=/lib \
              --with-systemdsystemunitdir=/lib/systemd/system \
              --enable-udev_acl \
              --with-selinux

  make
}
  
package() {
  cd $srcdir/$_origname-$pkgver
  make DESTDIR=${pkgdir} install

  # install the mkinitpcio hook
  install -D -m644 ../initcpio-hooks-udev ${pkgdir}/usr/lib/initcpio/hooks/udev
  install -D -m644 ../initcpio-install-udev ${pkgdir}/usr/lib/initcpio/install/udev

  # udevd moved, symlink to make life easy for restarting udevd manually
  ln -s ../../lib/udev/udevd ${pkgdir}/usr/bin/udevd

  # the path to udevadm is hardcoded in some places
  install -d ${pkgdir}/sbin
  ln -s ../usr/bin/udevadm ${pkgdir}/sbin/udevadm

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  for i in $pkgdir/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' $i
  done
}
md5sums=('0d7af750702620a871b9f9b98d8ad859'
         'f758b3e62ab77a1a6b7be12bb900cb2d'
         'a4dd853050bf2e0ae6b2e3d2c75499c2'
         'ee0bfe91a20fff12cc25ab1d1e024853')
