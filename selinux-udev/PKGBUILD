# $Id: PKGBUILD 134147 2011-08-02 11:38:00Z tomegun $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Nicky726 <nicky726@gmail.com>

pkgbase="selinux-udev"
pkgname=('selinux-udev')
true && pkgname=('selinux-udev' 'selinux-udev-compat')
_origname="udev"
pkgver=173
pkgrel=3
arch=(i686 x86_64)
url="http://www.kernel.org/pub/linux/utils/kernel/hotplug/udev.html"
license=('GPL')
groups=('selinux' 'selinux-system-utilities')
# older initscripts versions required start_udev
options=(!makeflags !libtool)
makedepends=('glibc' 'selinux-coreutils' 'selinux-util-linux' 'pciutils' 'libusb-compat' 'glib2' 'kernel26-selinux' 'gperf' 'libxslt' 'gobject-introspection')
source=(http://www.kernel.org/pub/linux/utils/kernel/hotplug/$_origname-$pkgver.tar.bz2
	bluetooth.patch
        81-arch.rules)

build() {
  cd $srcdir/$_origname-$pkgver
  # fix https://bugs.archlinux.org/task/25356 (submitted upstream)
  patch -Np1 -i ../bluetooth.patch
  ./configure --sysconfdir=/etc\
              --with-rootlibdir=/lib\
              --libexecdir=/lib/udev\
              --sbindir=/sbin\
              --with-systemdsystemunitdir=/lib/systemd/system\
              --disable-rule-generator\
              --enable-udev_acl\
              --with-selinux
  make
}
  
package_selinux-udev() {
  pkgdesc="The SELinux aware userspace dev tools (udev)"
  depends=('glibc' 'selinux-coreutils' 'selinux-util-linux' 'libusb-compat' 'glib2'
           'module-init-tools>=3.11' 'pciutils')
  install=udev.install
  backup=(etc/udev/udev.conf)
  conflicts=('pcmcia-cs' 'hotplug' 'initscripts<2009.07' "${_origname}")
  replaces=('devfsd')
  provides=("${_origname}=${pkgver}-${pkgrel}")
  
  cd $srcdir/$_origname-$pkgver
  make DESTDIR=${pkgdir} install
  # Install our rule for permissions and symlinks
  install -D -m644 $srcdir/81-arch.rules $pkgdir/lib/udev/rules.d/81-arch.rules

  # create framebuffer blacklist
  mkdir -p $pkgdir/lib/modprobe.d/
  for mod in $(find /lib/modules/*/kernel/drivers/video -name '*fb.ko.gz' -exec basename {} .ko.gz \;); do 
  	echo "blacklist $mod" 
  done | sort -u > $pkgdir/lib/modprobe.d/framebuffer_blacklist.conf

  # these static devices are created for convenience, to autoload the modules if necessary
  # /dev/loop0
  mknod -m 0660 ${pkgdir}/lib/udev/devices/loop0 b 7 0
  chgrp disk ${pkgdir}/lib/udev/devices/loop0

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  for i in $pkgdir/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' $i
  done
}

package_selinux-udev-compat() {
  pkgdesc="The SELinux aware userspace dev tools (udev) - additional rules for older kernels"
  depends=('selinux-udev')
  provides=("${_origname}-compat=${pkgver}-${pkgrel}")
  conflicts=("${_origname}-compat")
  cd $srcdir/$_origname-$pkgver
  install -d -m755 ${pkgdir}/lib/${_origname}/rules.d
  install -D -m644 ${srcdir}/${_origname}-${pkgver}/rules/misc/30-kernel-compat.rules ${pkgdir}/lib/udev/rules.d/30-kernel-compat.rules
  # create static devices in /lib/udev/devices/
  mkdir -p ${pkgdir}/lib/udev/devices/{pts,shm}

  mknod -m 0600 ${pkgdir}/lib/udev/devices/console c 5 1
  mknod -m 0666 ${pkgdir}/lib/udev/devices/null c 1 3
  mknod -m 0660 ${pkgdir}/lib/udev/devices/zero c 1 5
  mknod -m 0666 ${pkgdir}/lib/udev/devices/kmsg c 1 11

  ln -snf /proc/self/fd ${pkgdir}/lib/udev/devices/fd
  ln -snf /proc/self/fd/0 ${pkgdir}/lib/udev/devices/stdin
  ln -snf /proc/self/fd/1 ${pkgdir}/lib/udev/devices/stdout
  ln -snf /proc/self/fd/2 ${pkgdir}/lib/udev/devices/stderr
  ln -snf /proc/kcore ${pkgdir}/lib/udev/devices/core

  # these static devices are created for convenience, to autoload the modules if necessary
  # /dev/net/tun
  mkdir ${pkgdir}/lib/udev/devices/net
  mknod -m 0666 ${pkgdir}/lib/udev/devices/net/tun c 10 200
  # /dev/fuse
  mknod -m 0666 ${pkgdir}/lib/udev/devices/fuse c 10 229 
  # /dev/ppp
  mknod -m 0600 ${pkgdir}/lib/udev/devices/ppp c 108 0
}
md5sums=('91a88a359b60bbd074b024883cc0dbde'
         '36cb9bfb55a8d931b7498d2e46730745'
         'ec529eb1ddaabb70c61b38f80bb8462a')
