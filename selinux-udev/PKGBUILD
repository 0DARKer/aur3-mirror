# $Id: PKGBUILD 148429 2012-01-31 19:55:51Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Nicky726 <nicky726@gmail.com>

pkgbase="selinux-udev"
pkgname=('selinux-udev')
true && pkgname=('selinux-udev' 'selinux-udev-compat')
_origname="udev"
pkgver=180
pkgrel=1
arch=(i686 x86_64)
url="http://git.kernel.org/?p=linux/hotplug/udev.git;a=summary"
license=('GPL')
groups=('selinux' 'selinux-system-utilites')
options=(!makeflags !libtool)
makedepends=('gobject-introspection' 'gperf' 'libxslt' 'usbutils' 'kmod')
source=(ftp://ftp.kernel.org/pub/linux/utils/kernel/hotplug/$_origname-$pkgver.tar.xz)

build() {
  cd $srcdir/$_origname-$pkgver

  ./configure --prefix=/usr \
              --with-rootprefix= \
              --sysconfdir=/etc \
              --bindir=/sbin \
              --libdir=/usr/lib \
              --with-rootlibdir=/lib \
              --libexecdir=/lib \
              --with-systemdsystemunitdir=/lib/systemd/system \
              --enable-udev_acl \
              --with-selinux

  make
}
  
package_selinux-udev() {
  pkgdesc="The SELinux aware userspace dev tools (udev)"
  depends=('selinux-usr-libselinux' 'selinux-util-linux' 'libusb-compat' 'glib2' 'kmod' 'pciutils' 'usbutils' 'pciutils')
  install=udev.install
  backup=(etc/udev/udev.conf)
  conflicts=('pcmcia-cs' 'hotplug' 'initscripts<2009.07' "${_origname}")
  replaces=('devfsd')
  provides=("${_origname}=${pkgver}-${pkgrel}")
  
  cd $srcdir/$_origname-$pkgver
  make DESTDIR=${pkgdir} install

  # create framebuffer blacklist
  install -d -m755 ${pkgdir}/lib/modprobe.d/
  for mod in $(find /lib/modules/*/kernel/drivers/video -name '*fb.ko.gz' -exec basename {} .ko.gz \;); do 
  	echo "blacklist $mod" 
  done | sort -u > $pkgdir/lib/modprobe.d/framebuffer_blacklist.conf

  # /dev/loop0 is created for convenience, to autoload the module if necessary
  # this is no longer needed when util-linux-2.21 is released as /dev/loop-control
  # will be used instead. In that case move this to udev-compat
  install -d -m755 ${pkgdir}/lib/udev/devices/
  mknod ${pkgdir}/lib/udev/devices/loop0 b 7 0
  chgrp disk ${pkgdir}/lib/udev/devices/loop0
 
  # udevd moved, symlink to make life easy for restarting udevd manually
  ln -s /lib/udev/udevd ${pkgdir}/sbin/udevd

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  for i in $pkgdir/lib/udev/rules.d/*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
               s#GROUP="tape"#GROUP="storage"#g;
               s#GROUP="cdrom"#GROUP="optical"#g' $i
  done
}

package_selinux-udev-compat() {
  pkgdesc="The SELinux aware userspace dev tools (udev) - additional rules for older kernels"
  depends=('selinux-udev')
  provides=("${_origname}-compat=${pkgver}-${pkgrel}")
  conflicts=("${_origname}-compat")

  cd $srcdir/$_origname-$pkgver
  install -d -m755 ${pkgdir}/lib/${_origname}/rules.d
  install -D -m644 ${srcdir}/${_origname}-${pkgver}/rules/misc/30-kernel-compat.rules ${pkgdir}/lib/udev/rules.d/30-kernel-compat.rules

  # create static nodes to be compatible with on-demand module
  # loading in the most recent kernel
  #
  # the list of nodes is generated from /lib/modules/`most recent kernel`/modprobe.devname
  # excluding any devices not included in the LTS kernel and any entries in the
  # modprobe.devname file of the LTS kernel (if it exists).

  install -d -m755 ${pkgdir}/lib/udev/devices
  cd ${pkgdir}/lib/udev/devices

  install -d -m755 net
  mknod net/tun c 10 200
  mknod ppp c 108 0
#  mknod loop-control c 10 237 -- does not exist in old kernels
  mknod uinput c 10 223
  install -d -m755 mapper
  mknod mapper/control c 10 236
  install -d -m755 snd
  mknod snd/timer c 116 33
  mknod snd/seq c 116 1
  mknod btrfs-control c 10 234
  mknod autofs c 10 235
  mknod fuse c 10 229
  install -d -m755 cpu
  mknod cpu/microcode c 10 184
  
}
md5sums=('1cf124ab5a124ea35d4748ea414926ec')
