#Maintainer: Ivan Shapovalov <intelfx100@gmail.com>
pkgname=acpica
pkgver=20130418
pkgrel=2
pkgdesc="Provides an operating system (OS)-independent reference implementation of the ACPI"
url="http://www.acpica.org"
arch=('i686' 'x86_64')
license=("custom:intel")
depends=('glibc')
makedepends=('bison' 'gcc')
provides=('iasl')
conflicts=('iasl')
source=("http://www.acpica.org/sites/acpica/files/${pkgname}-unix-${pkgver}.tar.gz"
        "LICENSE")
md5sums=('53936808b460cb7963dc8e7f9e02a3bb'
         '2c0478c2c880b4b323fe1d081f6d8951')

build() {
	case "$CARCH" in
		i686)
			BITS=32
			;;
		x86_64)
			BITS=64
			;;
		*)
			echo "Unknown architecture. Please, edit PKGBUILD by hand." >&2
			return 1
			;;
	esac

	cd "${srcdir}/${pkgname}-unix-${pkgver}"
	make BITS="$BITS"
}

package() {
	cd "${srcdir}/${pkgname}-unix-${pkgver}"
	make DESTDIR="${pkgdir}" install

	## License
	install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
