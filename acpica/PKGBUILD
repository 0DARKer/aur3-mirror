#Maintainer: Ivan Shapovalov <intelfx100@gmail.com>
pkgname=acpica
pkgver=20121018
pkgrel=1
pkgdesc="Provides an operating system (OS)-independent reference implementation of the ACPI"
url="http://www.acpica.org"
arch=('i686' 'x86_64')
depends=('glibc')
makedepends=('bison' 'gcc')
license=("custom:intel")
source=("http://www.acpica.org/download/${pkgname}-unix-${pkgver}.tar.gz"
        "LICENSE"
		"Makefile.config.patch")

md5sums=('07b8137a6870e3dad862e5752c87de75'
         '2c0478c2c880b4b323fe1d081f6d8951'
         '32024db0f38174e8bd6b19eb9da39168')

build() {
	case "$CARCH" in
		i686)
			BITS=32
			;;
		x86_64)
			BITS=64
			;;
		*)
			echo "Unknown architecture. Please, edit PKGBUILD by hand." >&2
			return 1
			;;
	esac

	cd "${srcdir}/${pkgname}-unix-${pkgver}"
	patch -p0 -i "${srcdir}/Makefile.config.patch"
	make BITS="$BITS"
}

package() {
	cd "${srcdir}/${pkgname}-unix-${pkgver}"
	make DESTDIR="${pkgdir}" install

	## License
	install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}
