# Maintainer: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/
# Thanks to ridikulus_rat for PKGBUILD improvements
pkgname="spacefm"
pkgver=0.5.5
pkgrel=1
pkgdesc="A multi-panel tabbed file manager"
arch=('i686' 'x86_64')
url=("http://spacefm.sourceforge.net/")
license=('GPL2')

makedepends=('intltool' 'gettext')
depends=('gtk2' 'shared-mime-info' 'desktop-file-utils' 'startup-notification')
optdepends=('ktsuss: perform as root functionality'
            'gksu: perform as root functionality'
            'udisks: automount and device manager' 'wget' 'lsof' 'eject')

options=('!strip')
install="spacefm.install"

source=("http://downloads.sourceforge.net/project/spacefm/spacefm-${pkgver}.tar.xz")
md5sums=('bc18e18882458d9019a36d30fa00518c')
sha256sums=('485f7ba1d14e02ec5e1450ecd46afa3e2a372470f106a47fdfa718884e442aa1')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    echo
    
    # NOTE: To enable hal support add --enable-hal to
    # configure line below. See README for details.
    # NOTE: To disable inotify and use fam or gamin instead, add:
    # --disable-inotify to configure line below.
    # NOTE: To add a custom su program (mysu in this example), add:
    # --with-preferable-sudo="mysu"  to configure line below.
    
    CFLAGS+="-g" ./configure --prefix="/usr"
    echo
    CFLAGS+="-g" make
    echo
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    echo
    make DESTDIR="${pkgdir}/" install
    echo
}

