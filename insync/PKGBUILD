# Maintainer: Zhengyu Xu <xzy3186@gmail.com>

pkgname=insync
pkgver=1.2.1
pkgrel=1
pkgdesc="An unofficial Google Drive client that runs on Linux, with support for various desktops"
url="https://support.insynchq.com/forums/discussion/3549/new-insync-version-1-2-1"
license=('custom:insync')
options=(!strip)

depends=('xdg-utils' 'glibc' 'gobject-introspection')
optdepends=('insync-nautilus: file-manager integration for nautilus'
'insync-nemo: file-manager integration for nemo'
'insync-dolphin: file-manager integration for dolphin'
'insync-thunar: file-manager integration for thunar'
'insync-caja: file-manager integration for caja'
)

arch=('i686' 'x86_64')
if [ "$CARCH" = 'i686' ]; then
   source=("${pkgname}-${pkgver}.deb::http://s.insynchq.com/builds/${pkgname}_${pkgver}.35067-trusty_i386.deb")
   sha256sums=('5cf657d795d181dc3e18209fd3fbacebbf845bcbb1e7855402e7f729f26bfa77')
elif [ "$CARCH" = "x86_64" ]; then
   source=("${pkgname}-${pkgver}.deb::http://s.insynchq.com/builds/${pkgname}_${pkgver}.35067-trusty_amd64.deb")
   sha256sums=('18ee82439ceec946685ef24c6069e7a2beb88d443a218c3607314a7098549cfb')
fi
noextract=("${pkgname}-${pkgver}.deb")

source=("${source[@]}"
        "insync.service")
sha256sums=("${sha256sums[@]}"
            "995d9131bd454015ff11414d002b8927b7397835969e27061008264c0c5f225d")

package() {
   cd $srcdir
   ar x ${pkgname}-${pkgver}.deb
   tar xvf data.tar.gz
   cp -rp usr $pkgdir

   cd $pkgdir
   for file in $(grep -R "/usr/bin/python" . | cut -f1 -d :)
   do
      sed -i "s|usr/bin/python$|usr/bin/python2|g" $file
   done
   mkdir -p ${pkgdir}/usr/lib/systemd/system
   sed "s/_OPTION/ start/g" ${srcdir}/insync.service >${pkgdir}/usr/lib/systemd/system/insync@.service
}
