# Maintainer: Zhengyu Xu <xzy3186@gmail.com>

pkgname=insync
pkgver=1.0.20
pkgrel=2
pkgdesc="An unofficial Google Drive client that runs on Linux, with support for various desktops"
url="https://support.insynchq.com/forums/discussion/2080/new-insync-version-1-0-20-with-linux-cli"
license=('custom:insync')
options=(!strip)

depends=('xdg-utils' 'glibc' 'gobject-introspection')
optdepends=('insync-nautilus: file-manager integration for nautilus'
'insync-nemo: file-manager integration for nemo'
'insync-dolphin: file-manager integration for dolphin'
'insync-thunar: file-manager integration for thunar'
'insync-caja: file-manager integration for caja'
)

arch=('i686' 'x86_64')
if [ "$CARCH" = 'i686' ]; then
   source=("${pkgname}-${pkgver}.deb::http://s.insynchq.com/builds/${pkgname}_${pkgver}_i386.deb")
   sha256sums=('c0953cf709cbd3ad96c25bf569d04139d5a419468e4a7511118d717b254a2938')
elif [ "$CARCH" = "x86_64" ]; then
   source=("${pkgname}-${pkgver}.deb::http://s.insynchq.com/builds/${pkgname}_${pkgver}_amd64.deb")
   sha256sums=('632d8cb63faea49a32832aa79c122751a29dc99b29d935ba636bfb68072969fe')
fi
noextract=("${pkgname}-${pkgver}.deb")

source=("${source[@]}"
        "insync.service")
sha256sums=("${sha256sums[@]}"
            "0e302d4f1ce95a3838381094817b542505b8b9fe024aa01cca227d6184bb1f92")

package() {
   cd $srcdir
   ar x ${pkgname}-${pkgver}.deb
   tar xvf data.tar.gz
   cp -rp usr $pkgdir

   cd $pkgdir
   for file in $(grep -R "/usr/bin/python" . | cut -f1 -d :)
   do
      sed -i "s|usr/bin/python$|usr/bin/python2|g" $file
   done
   mkdir -p ${pkgdir}/usr/lib/systemd/system
   sed "s/_OPTION//g" ${srcdir}/insync.service >${pkgdir}/usr/lib/systemd/system/insync@.service
}
