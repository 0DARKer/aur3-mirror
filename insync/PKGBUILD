# Maintainer: Zhengyu Xu <xzy3186@gmail.com>

pkgname=insync
pkgver=1.0.28
pkgrel=1
pkgdesc="An unofficial Google Drive client that runs on Linux, with support for various desktops"
url="https://support.insynchq.com/forums/discussion/2699/new-insync-version-1-0-28"
license=('custom:insync')
options=(!strip)

depends=('xdg-utils' 'glibc' 'gobject-introspection')
optdepends=('insync-nautilus: file-manager integration for nautilus'
'insync-nemo: file-manager integration for nemo'
'insync-dolphin: file-manager integration for dolphin'
'insync-thunar: file-manager integration for thunar'
'insync-caja: file-manager integration for caja'
)

arch=('i686' 'x86_64')
if [ "$CARCH" = 'i686' ]; then
   source=("${pkgname}-${pkgver}.deb::http://s.insynchq.com/builds/${pkgname}_${pkgver}.31731_i386.deb")
   sha256sums=('da3c0f8ba97a41b7fb59b4e69a89b1f182f093b6af6acaed0decef3ae4bdfb5d')
elif [ "$CARCH" = "x86_64" ]; then
   source=("${pkgname}-${pkgver}.deb::http://s.insynchq.com/builds/${pkgname}_${pkgver}.31731_amd64.deb")
   sha256sums=('071df9dfb6a7375829fefae0ba6b875dc1645f2f70a85975d3218c9b766cab43')
fi
noextract=("${pkgname}-${pkgver}.deb")

source=("${source[@]}"
        "insync.service")
sha256sums=("${sha256sums[@]}"
            "995d9131bd454015ff11414d002b8927b7397835969e27061008264c0c5f225d")

package() {
   cd $srcdir
   ar x ${pkgname}-${pkgver}.deb
   tar xvf data.tar.gz
   cp -rp usr $pkgdir

   cd $pkgdir
   for file in $(grep -R "/usr/bin/python" . | cut -f1 -d :)
   do
      sed -i "s|usr/bin/python$|usr/bin/python2|g" $file
   done
   mkdir -p ${pkgdir}/usr/lib/systemd/system
   sed "s/_OPTION/ start --synchronous-full/g" ${srcdir}/insync.service >${pkgdir}/usr/lib/systemd/system/insync@.service
}
