# Contributor: Chris Brannon <cmbrannon79@gmail.com>
pkgname=emacspeak-svn
pkgver=6244
pkgrel=1
pkgdesc="Emacs extension that provides spoken output"
arch=(i686 x86_64)
url="http://emacspeak.sourceforge.net/"
license=('GPL' 'custom')
makedepends=('subversion')
depends=(emacs tcl tclx espeak)
optdepends=(  'eflite: software speech via the FLite TTS engine')
provides=(emacspeak)
conflicts=(emacspeak)
install='emacspeak-svn.install'
source=('verbatim.txt')
md5sums=('d6248e590e7ab9a510356fb38cea1373')

_svntrunk='http://emacspeak.googlecode.com/svn/trunk/'
_svnmod=emacspeak

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  make config || return 1
  make || return 1
  make DESTDIR="$pkgdir" prefix="/usr" install || return 1
  gzip -9nf "${pkgdir}"/usr/share/info/* || return 1
  rm -f "$pkgdir/usr/share/info/dir"
  cd "$pkgdir/usr/share/emacs/site-lisp/emacspeak/servers/linux-espeak" || return 1

  # Yep, we're going to compile in $pkgdir.  There is method to my madness!
  make TCL_VERSION=8.5 || return 1
  rm -f tclespeak.o
}
