# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=screengrab-git
pkgver=1.0.132.gc73eef9
pkgrel=1
pkgdesc="Crossplatform tool for grabbing screenshots of your desktop."
arch=("i686" "x86_64")
url="http://screengrab.doomer.org/"
license=("GPL2")
depends=("qt5-base")
makedepends=("git" "cmake")
provides=("screengrab")
conflicts=("screengrab")
source=("git+https://github.com/QtDesktop/screengrab")
sha256sums=("SKIP")
_gitname="screengrab"

pkgver() {
	cd "$srcdir/$_gitname"
	echo "$(git describe --always | sed 's/-/./g')"
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$_gitname" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make install DESTDIR="$pkgdir"
}
