# Maintainer: oke3 < Sekereg [at] gmx [dot] com >

pkgname=nunit-bzr
_pkgname=nunit
pkgver=3384
pkgrel=1
pkgdesc="A test framework for all .Net languages, running on Microsoft .NET and Mono."
arch=('i686' 'x86_64')
url="http://www.nunit.com/"
license=('custom')
depends=('mono')
makedepends=('bzr')
provides=('nunit')
conflicts=('nunit')

_bzrtrunk=lp:nunitv2
_bzrmod=nunit

build() {
    cd "$srcdir"
    msg "Connecting to Bazaar server...."

    if [[ -d "$_bzrmod" ]]; then
        cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r "$pkgver" && cd "$srcdir"
        msg "The local files are updated."
    else
        bzr branch "$_bzrtrunk" "$_bzrmod" -q -r "$pkgver"
    fi

    msg "Bazaar checkout done or server timeout"
    msg "Starting build..."

    rm -rf "$_bzrmod-build"
    cp -r "$_bzrmod" "$_bzrmod-build"
    cd "$_bzrmod-build"

    sed -i 's|NANT="tools/nant-0.90/bin/NAnt.exe"|NANT="tools/nant-0.91/bin/NAnt.exe"|' build

    ./build release mono-2.0
}

package() {
    cd "$srcdir/$_bzrmod-build"

    ./package release mono-2.0

    mkdir -p $pkgdir/usr/{lib/$_pkgname/{framework,lib,tests},share/{$_pkgname,doc/$_pkgname/{files/,img/},licenses/$_pkgname/}}

    cp -r packages/*/bin/* $pkgdir/usr/lib/$_pkgname/
    cp -r packages/*/doc/* $pkgdir/usr/share/doc/$_pkgname/
    cp -r packages/*/samples/ $pkgdir/usr/share/$_pkgname/
    cp -r packages/*/license.txt $pkgdir/usr/share/licenses/$_pkgname/
}
