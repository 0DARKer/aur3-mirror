# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: Florian LÃ©ger <florian6 dot leger at laposte dot net>
# Contributor: osily <ly50247@gmail.com>

pkgname=flashplayer-standalone
pkgver=11.2.202.394
pkgrel=1
pkgdesc="Macromedia standalone flash player (nonfree)"
arch=(i686 x86_64)
url=http://www.adobe.com/support/flashplayer/downloads.html
license=(custom)
# License matrix: http://www.adobe.com/products/eulas/
depends=(desktop-file-utils lib32-curl lib32-gtk2 lib32-libxt lib32-nss)
[[ $CARCH == "i686" ]] && depends=(${depends[@]/lib32-/})
options=(!strip)
install=$pkgname.install
source=(http://fpdownload.macromedia.com/pub/flashplayer/updaters/11/flashplayer_11_sa.i386.tar.gz
    license.pdf::http://www.adobe.com/products/eulas/pdfs/PlatformClients_PC_WWEULA-MULTI-20110809_1357.pdf
    flashplayer.desktop)
sha256sums=('6e429786c882bbc9b360f6c5fce794da22c26f825c3a8902593d725e3d6cd6cc'
    'd30b03c0f70f7c4a7837751286bab579d5033f2e7a1548279a9c09ea905b2849'
    'e0f3061f6d8e12a4ca543c7d80562563eac79afbf9da149b43b9c6d7be621dd4')
sha512sums=('47d43a6ced9638ce77d17520f2e4db9b98b6680fc8ae82b2792380d06181c2915cbdca853944c9a2f00ba639793b81a9f2e672e59fa9873a8ff78f51206fa06b'
    'a70fd7127f0f9d64f2c872d1e4634d93e24fdea165abfbe20724e68c1b7297df46362cb449b5a78d0eb4a68e43426965359ca93fd4ba4848d2542e5505bcd2fe'
    'e4139b265896ebd276b4c7931d5718db7ac8b25e5e86decb2161c94d2b6b410dc7160667cda3a330ece40e64e7809d7afc5ddcc94e09352e84359fa4c7fe3e2d')

package() {
    desktop-file-install flashplayer.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 license.pdf "$pkgdir"/usr/share/licenses/$pkgname/license.pdf
    install -Dm755 flashplayer "$pkgdir"/usr/bin/flashplayer
}
