# Contributor: 3ED <krzysztof1987@gmail.com>

pkgname=festival-pl-em
pkgver=0.1
pkgrel=1
pkgdesc="Polish diphone female voice for Festival. (Polski zenski glos)"
url="http://festlang.berlios.de/docu/doku.php?id=languages"

license="custom"
depends=('festival')
install="festival-pl-em.install"
source=("http://repository.slacky.eu/slackware-11.0/utilities/festvox/non-mbrola/pol/src/festival_polish_voice.tgz" "festival-pl.sh")
arch=('any')
md5sums=('93a4cae62976609a27e0380b042fea94' 'ec62346157afc21f00e5dcf5c159f52b')

build() {
  msg "Creating folders.."
  install -v -d -m755 \
    "$pkgdir/usr/bin/" \
    || return 1
  install -v -d -m755 \
    "$pkgdir/usr/share/festival/voices/polish/cstr_pl_em_diphone/"{festvox/,group/} \
    || return 1
  install -v -d -m755 \
    "$pkgdir/usr/share/licenses/$pkgname/" \
    || return 1

  msg "Copying files.."
  install -v -D -m755 \
    "$srcdir/festival-pl.sh" \
    "$pkgdir/usr/bin/festival-pl" \
    || return 1
  install -v -D -m644 \
    "$srcdir/cstr_pl_em_diphone/festvox/"{cstr_pl*,diphlist.scm} \
    "$pkgdir/usr/share/festival/voices/polish/cstr_pl_em_diphone/festvox/" \
    || return 1
  install -v -D -m555 \
    "$srcdir/cstr_pl_em_diphone/festvox/number_decl.scm" \
    "$pkgdir/usr/share/festival/voices/polish/cstr_pl_em_diphone/festvox/" \
    || return 1
  install -v -D -m644 \
    "$srcdir/cstr_pl_em_diphone/group/emlpc.group" \
    "$pkgdir/usr/share/festival/voices/polish/cstr_pl_em_diphone/group/" \
    || return 1
  install -v -D -m644 \
    "$srcdir/cstr_pl_em_diphone/"{przeczytajto.txt,README} \
    "$pkgdir/usr/share/festival/voices/polish/cstr_pl_em_diphone/" \
    || return 1

  ln -s "/usr/share/festival/voices/polish/cstr_pl_em_diphone/README" \
    "$pkgdir/usr/share/licenses/$pkgname/cstr_pl_em_diphone" || return 1
  ln -s "/usr/share/licenses/common/GPL3/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/festival-pl-skrypt" || return 1
}
