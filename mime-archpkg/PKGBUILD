# Contributor: grimi <grimi at poczta dot fm>

pkgname=mime-archpkg
pkgver=0.3.2
pkgrel=1
pkgdesc="mime type for archlinux packages"
arch=('any')
url="http://www.archlinux.org"
license=('GPL')
depends=('shared-mime-info' 'xdg-utils')
makedepends=('imagemagick' 'librsvg')
install=$pkgname.install
source=($pkgname archpkg-hicolor.svg archpkg-gnome.svg archpkg-Tango.svg archpkg-oxygen.svg archpkg-nuoveXT2.svg archpkg-Faenza.svg)
md5sums=('7668a09780ae7e855a174c5d24bc83d2'
         '81eed13d733ffd3b9d4f32e331e1a9cc'
         '24dcbbcf5bd306eb776ed9a17417254c'
         '9af358da055b637d46045b49e15e2c95'
         'b801e32a795540aaaf5a65de4eb35564'
         '1556724cf3149da49c46fe8e313dac99'
         '30e169ad1de7eadfe53df8fda4c2b5b1')


# comment that variable to install all icons
# normally install for themes that already installed
INSTALLED_ONLY=1


package() {

  # function to make things shorter
  put_icons() {
    # args: $1 = icon name(nopref:archpkg, nosuff:.svg) , $2 = sizes
    local i iname="application-x-archpkg" ipath="${pkgdir}/usr/share/icons"
    if [[ -z ${INSTALLED_ONLY} ]] || [[ -n ${INSTALLED_ONLY} && -f /usr/share/icons/${1}/index.theme ]]; then
      if [[ $1 == Faenza ]]; then
        install -Dm644 archpkg-${1}.svg "${ipath}"/$1/mimetypes/scalable/${iname}.svg
      else
        install -Dm644 archpkg-${1}.svg "${ipath}"/$1/scalable/mimetypes/${iname}.svg
      fi
      for i in ${2}; do
        convert 2>/dev/null -background none -resize ${i}x${i} archpkg-${1}.svg archpkg.png
        if [[ $1 == Faenza ]]; then
          install -Dm644 archpkg.png "${ipath}"/$1/mimetypes/${i}/${iname}.png
        else
          install -Dm644 archpkg.png "${ipath}"/$1/${i}x${i}/mimetypes/${iname}.png
        fi
      done
    fi
  }

  # mime type
  install -Dm644 ${pkgname} "${pkgdir}"/usr/share/mime/packages/archpkg.xml
  # hicolor icons
  put_icons hicolor '16 22 24 32 48 64 72 96 128 192 256'
  # gnome icons
  put_icons gnome '16 22 24 32 48 256'
  # tango icons
  put_icons Tango '16 22 24 32 48 64 96 128 256'
  # oxygen icons
  put_icons oxygen '16 22 24 32 48 64 128 256'
  # lxde icons
  put_icons nuoveXT2 '16 22 24 32 36 48 64 72 96 128'
  # Faenza icons
  put_icons Faenza '16 22 24 32 48 64 96'

}


# vim:set ts=2 sw=2 et:

