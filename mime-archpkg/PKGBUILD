# Contributor: grimi <grimi at poczta dot fm>

pkgname=mime-archpkg
pkgver=0.3.4.3
pkgrel=1
pkgdesc="mime type for archlinux packages"
arch=('any')
url="http://www.archlinux.org"
license=('GPL')
depends=('shared-mime-info' 'xdg-utils')
makedepends=('librsvg')
install=$pkgname.install
source=($pkgname archpkg-{hicolor,gnome,Tango,oxygen,nuoveXT2,Faenza,Faience,NITRUX,Numix}.svg)
md5sums=('1ff726f4e5e0dc7115aafb5f1b691455'
         '81eed13d733ffd3b9d4f32e331e1a9cc'
         '24dcbbcf5bd306eb776ed9a17417254c'
         '9af358da055b637d46045b49e15e2c95'
         'b801e32a795540aaaf5a65de4eb35564'
         '1556724cf3149da49c46fe8e313dac99'
         '30e169ad1de7eadfe53df8fda4c2b5b1'
         '336b1853d04aa42c580cf800e8fce9f3'
         '06ff47b7c8929b64aa5003248e68e2d8'
         '952cb088bc32686851b81d2671704b98')


# comment that variable to install all icons
# normally install for themes that already installed
INSTALLED_ONLY=1



package() {

  # function to make things shorter
  put_icons() {
    # args: $1 = icon name(nopref:archpkg, nosuff:.svg) , $2 = sizes
    # args: $3 != 1:YxY/folder; = 1:folder/Y , $4 = 1:svg suff, else png
    local i iname="application-x-archpkg" ipath="${pkgdir}/usr/share/icons" suff="png"
    if [[ $4 == 1 ]]; then suff="svg"; fi
    if [[ -z ${INSTALLED_ONLY} ]] || [[ -n ${INSTALLED_ONLY} && -f /usr/share/icons/${1}/index.theme ]]; then
      if [[ $3 == 1 ]]; then
        install -Dm644 archpkg-${1}.svg "${ipath}"/$1/mimetypes/scalable/${iname}.svg
      else
        install -Dm644 archpkg-${1}.svg "${ipath}"/$1/scalable/mimetypes/${iname}.svg
      fi
      for i in ${2}; do
        rsvg-convert -w ${i} -h ${i} -f ${suff} -o archpkg.${suff} archpkg-${1}.svg
        if [[ $3 == 1 ]]; then
          install -Dm644 archpkg.${suff} "${ipath}"/$1/mimetypes/${i}/${iname}.${suff}
        else
          install -Dm644 archpkg.${suff} "${ipath}"/$1/${i}x${i}/mimetypes/${iname}.${suff}
        fi
      done
    fi
  }

  # mime type
  install -Dm644 ${pkgname} "${pkgdir}"/usr/share/mime/packages/archpkg.xml
  # hicolor icons
  put_icons hicolor '16 22 24 32 48 64 72 96 128 192 256'
  # gnome icons
  put_icons gnome '16 22 24 32 48 256'
  # tango icons
  put_icons Tango '16 22 24 32 48 64 96 128 256'
  # oxygen icons
  put_icons oxygen '16 22 24 32 48 64 128 256'
  # lxde icons
  put_icons nuoveXT2 '16 22 24 32 36 48 64 72 96 128'
  # Faenza icons
  put_icons Faenza '16 22 24 32 48 64 96' 1
  # Faience icons
  put_icons Faience '16 22 24 32 48 64 96' 1
  # NITRUX icons
  put_icons NITRUX '16 22 24 32' 1
  # Numix icons
  put_icons Numix '16 22 24 32 64 128 256' 0 1
}


# vim: set et sts=2 sw=2 ts=2 :

