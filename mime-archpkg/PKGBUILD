# Contributor: grimi <grimi at poczta dot fm>

pkgname=mime-archpkg
pkgver=0.3.1
pkgrel=1
pkgdesc="mime type for archlinux packages"
arch=('any')
url="http://www.archlinux.org"
license=('GPL')
depends=('shared-mime-info')
makedepends=('imagemagick' 'librsvg')
conflicts=('kdemod-shaman')
install=$pkgname.install
source=($pkgname archpkg-hi.svg archpkg-gnome.svg archpkg-tango.svg archpkg-oxy.svg archpkg-lxde.svg)
md5sums=('7668a09780ae7e855a174c5d24bc83d2'
         '81eed13d733ffd3b9d4f32e331e1a9cc'
         '24dcbbcf5bd306eb776ed9a17417254c'
         '9af358da055b637d46045b49e15e2c95'
         'b801e32a795540aaaf5a65de4eb35564'
         '1556724cf3149da49c46fe8e313dac99')


package() {

  # function to make things shorter
  put_icons() {
     # args: $1 = icon name(nopref:archpkg, nosuff:.svg) , $2 = icon theme , $3 = sizes
     local iname=application-x-archpkg
     local ipath="${pkgdir}/usr/share/icons"
     install -Dm644 archpkg-${1}.svg "${ipath}"/$2/scalable/mimetypes/${iname}.svg
     for i in ${3}
     do
        convert 2>/dev/null -background none -resize ${i}x${i} archpkg-${1}.svg archpkg.png
        install -Dm644 archpkg.png "${ipath}"/$2/${i}x${i}/mimetypes/${iname}.png
     done
  }

  # mime type
  install -Dm644 $pkgname "${pkgdir}"/usr/share/mime/packages/archpkg.xml

  # hicolor icons
  put_icons hi hicolor '16 22 24 32 48 64 72 96 128 192 256'

  # gnome icons
  put_icons gnome gnome '16 22 24 32 48 256'

  # tango icons
  put_icons tango Tango '16 22 24 32 48 64 96 128 256'

  # oxygen icons
  put_icons oxy oxygen '16 22 24 32 48 64 128 256'

  #  lxde icons (nuoveXT2)
  put_icons lxde nuoveXT2 '16 22 24 32 36 48 64 72 96 128'

}


