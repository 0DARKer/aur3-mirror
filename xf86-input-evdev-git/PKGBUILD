# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-evdev

_pkgname=xf86-input-evdev
pkgname=$_pkgname-git
pkgver=20130326
pkgrel=1
pkgdesc="X.org evdev input driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/$_pkgname/"
license=('custom')
depends=('glibc' 'mtdev' 'systemd-tools')
makedepends=('git' 'resourceproto' 'scrnsaverproto' 'xorg-server-devel')
conflicts=('X-ABI-XINPUT_VERSION!=19' "$_pkgname")
groups=('xorg' 'xorg-drivers')
source=("git://anongit.freedesktop.org/xorg/driver/$_pkgname")
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git log -1 --format="%cd" --date=short | sed 's/-//g'
}

build() {
  cd $_pkgname
  msg2 "Starting autogen.sh..."
  ./autogen.sh --prefix=/usr
  msg2 "Starting make..."
  make
}

package() {
  cd $_pkgname
  provides=$([ -d .git ] && git describe | sed 's/-/=/3')
  msg2 "Starting make install..."
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}