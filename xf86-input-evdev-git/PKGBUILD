# Maintainer: Det <who@cares.com>
# Contributor: <Eugeny Borisov flekst@gmail.com>
# Based on [extra]'s xf86-input-evdev

_pkgname=xf86-input-evdev
pkgname=$_pkgname-git
pkgver=20130406
pkgrel=1
pkgdesc="X.org evdev input driver - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/driver/$_pkgname/"
license=('custom')
depends=('glibc' 'mtdev' 'systemd-tools')
makedepends=('git' 'resourceproto' 'scrnsaverproto' 'xorg-server-devel')
provides=("$_pkgname=99")
conflicts=('X-ABI-XINPUT_VERSION<19' 'X-ABI-XINPUT_VERSION>=20' "$_pkgname" 'xorg-server<1.14.0')
options=('!libtool' '!makeflags')
groups=('xorg' 'xorg-drivers')
source=("git://anongit.freedesktop.org/xorg/driver/$_pkgname")
md5sums=('SKIP')
_gitname=$_pkgname

pkgver() {
  cd $_gitname
  date +%Y%m%d
}

build() {
  cd $_gitname
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}