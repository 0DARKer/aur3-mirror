# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
pkgname=gitorious-git
pkgver=20101117
pkgrel=1
pkgdesc="Gitorious aims to provide a great way of doing distributed opensource code collaboration."
arch=('any')
url="http://gitorious.org/gitorious"
license=('AGPLv3')
depends=('apache-activemq' 'sphinx' 'mysql' 'ruby' 'mysql-ruby' 'ruby-chronic' 'ruby-diff-lcs' 'ruby-echoe' 'ruby-eventmachine' 'ruby-fastthread' 'ruby-gemcutter' 'ruby-gem_plugin' 'ruby-geoip' 'ruby-highline' 'ruby-mongrel' 'ruby-oauth' 'ruby-mime-types' 'ruby-net-scp' 'ruby-openid' 'ruby-rack10' 'ruby-rdiscount13' 'imagemagick' 'ruby-stomp' 'ruby-thin' 'ruby-uuid' 'ruby-yadis')
makedepends=('git')
source=(gitorious.rc.d database.yml gitorious.yml sphinx-cmd.patch environment-fix.patch)
install=gitorious.install
backup=(etc/webapps/gitorious/database.yml etc/webapps/gitorious/gitorious.yml)
md5sums=('4de256a43539b906eff97aa218c7aec5'
         '6a6efd4a0c9deaaefe841c57176a1bee'
         'c1b329f5e923b31b979dc96300f5261a'
         'f46e84a467b492a6d5de936e1595e028'
         'a73714fab5974a932ea489a218ace0ff')

_gitroot="git://gitorious.org/gitorious/mainline.git"
_gitname="gitorious"

build() {
	cd $startdir/src
	msg "Connecting to GIT server...."

	if [ -d $startdir/src/$_gitname ] ; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot $_gitname || return 1
	fi

	msg "GIT checkout done or server timeout"

	rm -rf $startdir/src/$_gitname-build
	cp -r $startdir/src/$_gitname $startdir/src/$_gitname-build
	cd $startdir/src/$_gitname-build/

	patch -p1 -i "$srcdir/sphinx-cmd.patch" || return 1
	patch -p1 -i "$srcdir/environment-fix.patch" || return 1

	rm -rf .git .gitmodules
	find . -type f -name .gitignore -delete

	_gitorious="/usr/share/webapps/gitorious"
	_etc="$pkgdir/etc/webapps/gitorious"
	install -d "$pkgdir/usr/share/webapps"
	install -d "$_etc"

	cd ..
	mv $_gitname-build "${pkgdir}${_gitorious}"

	install -D -m0644 "$srcdir/database.yml" "$_etc/database.yml"
	install -D -m0644 "$srcdir/gitorious.yml" "$_etc/gitorious.yml"
	install -D -m0644 "${pkgdir}${_gitorious}/config/broker.yml.example" "${pkgdir}${_gitorious}/config/broker.yml"

	ln -s "/etc/webapps/gitorious/database.yml" "${pkgdir}${_gitorious}/config/"
	ln -s "/etc/webapps/gitorious/gitorious.yml" "${pkgdir}${_gitorious}/config/"

	install -d "$pkgdir/usr/bin"
	ln -s "${_gitorious}/script/gitorious" "$pkgdir/usr/bin/gitorious"

	install -D -m0755 "$srcdir/gitorious.rc.d" "$pkgdir/etc/rc.d/gitorious"
}
