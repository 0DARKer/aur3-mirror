# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
pkgname=gitorious-git
pkgver=20110204
pkgrel=1
pkgdesc="Gitorious aims to provide a great way of doing distributed opensource code collaboration."
arch=('any')
url="http://gitorious.org/gitorious"
license=('AGPLv3')
depends=('apache-activemq' 'sphinx' 'mysql' 'ruby-enterprise' 'ruby-enterprise-rake' 'ruby-enterprise-bundler' 'imagemagick')
makedepends=('git')
optdepends=('memcached: High performance cache')
source=(gitorious.rc.d database.yml gitorious.yml sphinx-cmd.patch environment-fix.patch Gemfile Gemfile.lock)
install=gitorious.install
backup=(etc/webapps/gitorious/database.yml etc/webapps/gitorious/gitorious.yml)

_gitroot="git://gitorious.org/gitorious/mainline.git"
_gitname="gitorious"

build() {
	cd $srcdir
	msg "Connecting to GIT server...."

	if [ -d $srcdir/$_gitname ]; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot $_gitname
	fi

	msg "GIT checkout done or server timeout"

	rm -rf $srcdir/$_gitname-build
	cp -r $srcdir/$_gitname $srcdir/$_gitname-build
	cd $srcdir/$_gitname-build/

	cp "$srcdir/Gemfile"* .
	/opt/ruby-enterprise/bin/bundle install --deployment
	rm -rf "vendor/bundle/ruby/1.8/cache"

	patch -p1 -i "$srcdir/sphinx-cmd.patch"
	patch -p1 -i "$srcdir/environment-fix.patch"

	rm -rf .git .gitmodules
	find . -type f -name .gitignore -delete
	
	find script -type f -exec sed -i 's|^#!/usr/bin/env ruby|#!/opt/ruby-enterprise/bin/ruby|' {} \;
}

package() {
	cd $srcdir

	_gitorious="/usr/share/webapps/gitorious"
	_etc="$pkgdir/etc/webapps/gitorious"
	install -d "$pkgdir/usr/share/webapps"
	install -d "$_etc"

	mv $_gitname-build "${pkgdir}${_gitorious}"

	install -D -m0644 "$srcdir/database.yml" "$_etc/database.yml"
	install -D -m0644 "$srcdir/gitorious.yml" "$_etc/gitorious.yml"
	install -D -m0644 "${pkgdir}${_gitorious}/config/broker.yml.example" "${pkgdir}${_gitorious}/config/broker.yml"

	ln -s "/etc/webapps/gitorious/database.yml" "${pkgdir}${_gitorious}/config/"
	ln -s "/etc/webapps/gitorious/gitorious.yml" "${pkgdir}${_gitorious}/config/"

	install -d "$pkgdir/usr/bin"
	ln -s "${_gitorious}/script/gitorious" "$pkgdir/usr/bin/gitorious"

	install -D -m0755 "$srcdir/gitorious.rc.d" "$pkgdir/etc/rc.d/gitorious"
}

md5sums=('f81db03860eb6c42dd94ab18d7bc1df8'
         '6a6efd4a0c9deaaefe841c57176a1bee'
         'c1b329f5e923b31b979dc96300f5261a'
         'f46e84a467b492a6d5de936e1595e028'
         '412b48988d53046ece8292852b3c965b'
         '47f07b3e4763c992c0edbdb60588e8ed'
         '82191a1b7a08ac66fbb7554c6ad588da')
