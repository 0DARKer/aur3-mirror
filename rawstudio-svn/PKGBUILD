# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: Dan Serban
# Contributor: ra_fi

pkgname=rawstudio-svn
_svnmod=rawstudio
pkgver=4345  
pkgrel=1
pkgdesc="An open source raw-image converter written in GTK"
arch=(i686 x86_64)
license=('GPL')
url="http://rawstudio.org/"
depends=('exiv2' 'libgphoto2' 'flickcurl' 'libjpeg' 'fftw' 'gconf' 'lensfun' 'raptor1' 'lcms' 'gtk2')
makedepends=('subversion' 'automake-1.12')
provides=('rawstudio')
conflicts=('rawstudio')
source=("$_svnmod::svn+https://rawstudio.org/svn/rawstudio/trunk/"
        'autogen.patch')
md5sums=('SKIP'
         'a61a5a14bfe5376fce768d63e2339a2a')

pkgver() {
  cd "$SRCDEST/$_svnmod/"
  svnversion | tr -d [A-z]
}
prepare() {
  cd "$_svnmod/"
  cp -a "$SRCDEST/$_svnmod/.svn" .
  patch -p1 -i "$srcdir/autogen.patch"
}

build() {
  cd "$_svnmod"
  ./autogen.sh  --prefix=/usr
  make
}

package() {
  cd "$_svnmod"
  make DESTDIR="${pkgdir}" install
  mkdir "${pkgdir}"/usr/lib/rawstudio
  mv "${pkgdir}"/usr/share/rawstudio/plugins "${pkgdir}"/usr/lib/rawstudio
  ln -s ../../lib/rawstudio/plugins "${pkgdir}"/usr/share/rawstudio/plugins  
}

