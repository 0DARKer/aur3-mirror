# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: Dan Serban
# Contributor: ra_fi

pkgname=rawstudio-svn
pkgver=4329
pkgrel=2
pkgdesc="An open source raw-image converter written in GTK"
arch=(i686 x86_64)
license=(GPL)
url="http://rawstudio.org/"
depends=('exiv2' 'libgphoto2' 'flickcurl' 'libjpeg' 'fftw' 'gconf' 'lensfun' 'raptor1' 'lcms' 'gtk2')
makedepends=('subversion' 'automake-1.12')
conflicts=('rawstudio')
source=('autogen.patch')
md5sums=('a61a5a14bfe5376fce768d63e2339a2a')

_svntrunk=https://rawstudio.org/svn/rawstudio/trunk/
_svnmod=rawstudio

build() {
  cd $srcdir

  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r $pkgver)
  else
    svn co ${_svntrunk} --config-dir ./ -r $pkgver ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -r $srcdir/${_svnmod}-build || true
  cp -r ${_svnmod} ${_svnmod}-build
# 
  cd ${_svnmod}-build
  patch -p1 -i ../autogen.patch
  ./autogen.sh  --prefix=/usr
  make
}


package() {
  cd $srcdir/${_svnmod}-build
  make DESTDIR="${pkgdir}" install
  mkdir "${pkgdir}"/usr/lib/rawstudio
  mv "${pkgdir}"/usr/share/rawstudio/plugins "${pkgdir}"/usr/lib/rawstudio
  ln -s ../../lib/rawstudio/plugins "${pkgdir}"/usr/share/rawstudio/plugins  
}

