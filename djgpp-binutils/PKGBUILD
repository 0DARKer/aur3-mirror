pkgname=djgpp-binutils
pkgver=2.22
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (DJGPP)"
arch=(i686 x86_64)
url="http://www.gnu.org/software/binutils/"
license=("GPL")
source=("ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2")
md5sums=('ee0f10756c84979622b992a4a61ea3f5')

build() {
  cd "${srcdir}/binutils-$pkgver"
  ./configure \
    --prefix=/usr \
    --target=i586-msdosdjgpp \
    --host=$CHOST \
    --build=$CHOST \
    --disable-nls
  make
}

package()
{
  cd "${srcdir}/binutils-$pkgver"
  make DESTDIR=$pkgdir install
  rm -r "$pkgdir/usr/lib"
  rm -r "$pkgdir/usr/share/"{info,man}
}
