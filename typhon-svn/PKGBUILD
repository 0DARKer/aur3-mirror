# Maintainer: Alessandro Pazzaglia <jackdroido at gmail dot com>

pkgname=typhon-svn
pkgver=139
pkgrel=2
pkgdesc="A slim and themeable OpenGL-based dashboard and emulator launcher"
arch=('i686' 'x86_64')
url="http://code.google.com/p/typhon-launcher/"
license=('GPL3')
depends=('ftgl' 'sfml' 'opencv')
makedepends=('subversion' 'cmake')

_svntrunk="http://typhon-launcher.googlecode.com/svn/trunk/"
_svnmod="typhon-svn"

build() {
    cd "$srcdir"

    msg "Starting SVN checkout ..."

    if [ -d $_svnmod/.svn ]
    then
        cd $_svnmod && svn up -r $pkgver
    else
        svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    rm -rf "$srcdir/$_svnmod-build"
    cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"

    msg "Starting make ..."

    cd "$srcdir/$_svnmod-build/build"
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DNOMMD=ON -DDEBUG=ON ..
    make
}

package() {
    cd "$srcdir/$_svnmod-build/build"
    make DESTDIR="$pkgdir/" install
}
