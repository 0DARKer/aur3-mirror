# Maintainer :  kiefer <jorgelmadrid@gmail.com>

pkgname=gtkhotkey
pkgver=0.2.1
pkgrel=4
pkgdesc="Platform independent hotkey handling for Gtk+ applications"
arch=('i686' 'x86_64')
url="https://launchpad.net/gtkhotkey"
license=('LGPL3')
depends=('gtk2')
makedepends=('intltool')
options=('!libtool')
source=("http://launchpad.net/${pkgname}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
md5sums=('bfdc73e68e9adbe0d506d31a25862914')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # doc path fix
  sed -i "/gtkhotkeydocdir/s/\${prefix}/\/usr\/share/g" Makefile.*

  ./configure --prefix=/usr \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}