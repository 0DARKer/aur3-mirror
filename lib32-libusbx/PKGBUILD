#	AUR Package Maintainer: 3V0LU710N <db_eee-at-hotmail-dot-com>

_pkgbase=libusbx
pkgname=lib32-${_pkgbase}
pkgver=1.0.11
pkgrel=3
pkgdesc="Library to enable user space application programs to communicate with USB devices. (32-bit). libusb fork."
arch=('x86_64')
url="http://libusbx.sourceforge.net/"
license=('LGPL')
depends=('lib32-glibc' "${_pkgbase}")
makedepends=('gcc-multilib')
provides=("lib32-libusb=${pkgver}")
conflicts=('lib32-libusb')
source=(http://downloads.sourceforge.net/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2)
options=(!libtool)
md5sums=('9aaab6aee72f65900cc731ecbffb4cf4')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "libusbx-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package () {
  cd "${srcdir}/libusbx-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}
