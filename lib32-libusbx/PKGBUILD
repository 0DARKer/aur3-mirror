#	AUR Package Maintainer: 3V0LU710N <db_eee-at-hotmail-dot-com>

_pkgbase=libusbx
pkgname=lib32-${_pkgbase}
pkgver=1.0.16
pkgrel=4
pkgdesc="Library to enable user space application programs to communicate with USB devices (32-bit)."
arch=('x86_64')
url="http://libusbx.sourceforge.net/"
license=('LGPL')
depends=('lib32-glibc' "${_pkgbase}" 'lib32-systemd')
makedepends=('gcc-multilib')
provides=("lib32-libusb=${pkgver}")
conflicts=('lib32-libusb')

source=(http://downloads.sourceforge.net/${_pkgbase}/${_pkgbase}-${pkgver}.tar.bz2
	0001-linux-Use-a-separate-lock-to-serialize-start-stop-vs.patch
	0002-hotplug-Remove-use-of-pthread_cancel-from-linux_udev.patch)

options=(!libtool)
md5sums=('7f5715d624cd6c26b30a317eb6c2fe5e'
	'dad04322621fe7cabd2fe631c6d9929f'
	'259238c92c92f8b3df5dcad0db8aaf2f')

prepare() {
  cd "${srcdir}/libusbx-${pkgver}"
  # fix #36418
  patch -Np1 -i ../0001-linux-Use-a-separate-lock-to-serialize-start-stop-vs.patch
  patch -Np1 -i ../0002-hotplug-Remove-use-of-pthread_cancel-from-linux_udev.patch
}

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "libusbx-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make
}

package () {
  cd "${srcdir}/libusbx-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}
