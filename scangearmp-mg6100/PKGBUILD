# Maintainer: Fortunato Ventre (voRia) <vorione AT gmail DOT com>
#
# Here is a list of printer names and IDs:
#
#   name - id
#-----------
#  mp250 - 356
#  mp280 - 370
#  mp495 - 369
# mg5100 - 373
# mg5200 - 374
# mg6100 - 376
# mg8100 - 377
#
# Change the following variables accordingly:
_name=mg6100
_id=376

pkgname=scangearmp-${_name}
pkgver=1.60
pkgrel=6
_pkgver=1.60-1
pkgdesc="Canon Scanner Driver (${_name} series)"
url="http://support-my.canon-asia.com/contents/MY/EN/0100303302.html"
arch=('i686' 'x86_64')
license=('custom')
depends=('sane' 'gtk2' 'gimp')
source=(http://gdlp01.c-wss.com/gds/3/0100003033/01/scangearmp-source-${_pkgver}.tar.gz
       fix_png15.patch)
md5sums=('15782d670f9d5c5904e00610508114f3'
         '6609d7fe171e67451658a3665442972c')

if [ "$CARCH" == "x86_64" ]; then  
	_libdir=libs_bin64
else
	_libdir=libs_bin32
fi

build() {
	# Apply patch
	cd ${srcdir}/scangearmp-source-${_pkgver}
	patch -p1 -i ${srcdir}/fix_png15.patch || return 1
	
	cd ${srcdir}/scangearmp-source-${_pkgver}/scangearmp
	./autogen.sh --prefix=/usr LDFLAGS="-L`pwd`/../com/${_libdir}" LIBS=-lm
	# Force the use of system's libtool
	rm libtool
	ln -s `which libtool` .
	# Build package
	make clean || return 1
	make || return 1	
}

package() {
	# Install package
	cd ${srcdir}/scangearmp-source-${_pkgver}/scangearmp
	make install DESTDIR=${pkgdir} || return 1

	# Install common libraries
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${_libdir}/libcncpcmcm.so.8.0.1 ${pkgdir}/usr/lib/
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${_libdir}/libcncpmsimg.so.1.0.2 ${pkgdir}/usr/lib/
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${_libdir}/libcncpmslld.so.1.0.1 ${pkgdir}/usr/lib/
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${_libdir}/libcncpmsui.so.1.6.0 ${pkgdir}/usr/lib/
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${_libdir}/libcncpnet.so.1.2.0 ${pkgdir}/usr/lib/
  
	# Install model specific libraries
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/${_id}/${_libdir}/libcncpmsimg${_id}.so.1.6.0 ${pkgdir}/usr/lib/
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/${_id}/${_libdir}/libcncpmslld${_id}c.so.1.04.1 ${pkgdir}/usr/lib/
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/${_id}/${_libdir}/libcncpmslld${_id}.so.1.6.0 ${pkgdir}/usr/lib/

	# Create symbolic links
	cd ${pkgdir}/usr/lib/
	ln -s libcncpcmcm.so.8.0.1 libcncpcmcm.so
	ln -s libcncpmsimg.so.1.0.2 libcncpmsimg.so
	ln -s libcncpmslld.so.1.0.1 libcncpmslld.so
	ln -s libcncpmsui.so.1.6.0 libcncpmsui.so
	ln -s libcncpnet.so.1.2.0 libcncpnet.so
	ln -s libcncpmsimg${_id}.so.1.6.0 libcncpmsimg${_id}.so
	ln -s libcncpmslld${_id}c.so.1.04.1 libcncpmslld${_id}c.so
	ln -s libcncpmslld${_id}.so.1.6.0 libcncpmslld${_id}.so

	# Make scangearmp usable from gimp
	install -d -m 755 ${pkgdir}/usr/lib/gimp/2.0/plug-ins/
	ln -s /usr/bin/scangearmp ${pkgdir}/usr/lib/gimp/2.0/plug-ins/

	# Install model specific .tbl and .dat files
	install -d -m 755 ${pkgdir}/usr/lib/bjlib/
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/${_id}/*.tbl ${pkgdir}/usr/lib/bjlib/
	install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/${_id}/*.DAT ${pkgdir}/usr/lib/bjlib/
	
	# Install .ini file
	install -m 666 ${srcdir}/scangearmp-source-${_pkgver}/com/ini/canon_mfp_net.ini ${pkgdir}/usr/lib/bjlib/

	# Fix and install udev rules
	sed -i -e 's/SYSFS/ATTR/g' ${srcdir}/scangearmp-source-${_pkgver}/scangearmp/etc/80-canon_mfp.rules
	install -D -m 644 ${srcdir}/scangearmp-source-${_pkgver}/scangearmp/etc/80-canon_mfp.rules ${pkgdir}/etc/udev/rules.d/80-canon_mfp.rules

	# Install license files
	cd ${srcdir}/scangearmp-source-${_pkgver}
	install -d -m 755 ${pkgdir}/usr/share/licenses/${pkgname}/
	install -m 644 LICENSE-scangearmp-${pkgver}EN.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-scangearmp-${pkgver}EN.txt
	install -m 644 LICENSE-scangearmp-${pkgver}FR.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-scangearmp-${pkgver}FR.txt
	install -m 644 LICENSE-scangearmp-${pkgver}JP.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-scangearmp-${pkgver}JP.txt
	install -m 644 LICENSE-scangearmp-${pkgver}SC.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-scangearmp-${pkgver}SC.txt

  # Remove unneeded files
  rm ${pkgdir}/usr/lib/libsane-canon_mfp.a
  rm ${pkgdir}/usr/lib/libsane-canon_mfp.la
}

# vim:set ts=2 sw=2 et:
