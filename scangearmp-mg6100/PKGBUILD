# Contributor: Fortunato Ventre (voRia) <vorione AT gmail DOT com>
pkgname=scangearmp-mg6100
pkgver=1.60
pkgrel=4
_pkgver=1.60-1
pkgdesc="Canon Scanner Driver (for MG6100 series)"
url="http://support-my.canon-asia.com/contents/MY/EN/0100303302.html"
arch=('i686' 'x86_64')
license=('custom')
depends=('sane' 'gtk2' 'gimp')
source=(http://gdlp01.c-wss.com/gds/3/0100003033/01/scangearmp-source-${_pkgver}.tar.gz
	fix_scanfile.patch)
md5sums=('15782d670f9d5c5904e00610508114f3'
         '58402336995427d698bc5f31d2cc6e5c')

build() {
  if [ "$CARCH" == "x86_64" ]; then  
    libdir=libs_bin64
  else
    libdir=libs_bin32
  fi

  # Apply patch
  cd ${srcdir}/scangearmp-source-${_pkgver}
  patch -p1 -i ${srcdir}/fix_scanfile.patch

  cd ${srcdir}/scangearmp-source-${_pkgver}/scangearmp
  ./autogen.sh --prefix=/usr LDFLAGS="-L`pwd`/../com/${libdir}" LIBS=-lm
  # Force the use of system's libtool
  rm libtool
  ln -s `which libtool` .
  # Build package
  make clean || return 1
  make || return 1
  # Install package
  make install DESTDIR=${pkgdir} || return 1

  #Install common libraries
  install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpcmcm.so.8.0.1 ${pkgdir}/usr/lib/
  install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpmsimg.so.1.0.2 ${pkgdir}/usr/lib/
  install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpmslld.so.1.0.1 ${pkgdir}/usr/lib/
  install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpmsui.so.1.6.0 ${pkgdir}/usr/lib/
  install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/com/${libdir}/libcncpnet.so.1.2.0 ${pkgdir}/usr/lib/
  # Install mg6100 series specific libraries
  install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/376/${libdir}/libcncpmsimg376.so.1.6.0 ${pkgdir}/usr/lib/
  install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/376/${libdir}/libcncpmslld376c.so.1.04.1 ${pkgdir}/usr/lib/
  install -m 755 ${srcdir}/scangearmp-source-${_pkgver}/376/${libdir}/libcncpmslld376.so.1.6.0 ${pkgdir}/usr/lib/
  # Create symbolic links
  cd ${pkgdir}/usr/lib/
  ln -s libcncpcmcm.so.8.0.1 libcncpcmcm.so
  ln -s libcncpmsimg.so.1.0.2 libcncpmsimg.so
  ln -s libcncpmslld.so.1.0.1 libcncpmslld.so
  ln -s libcncpmsui.so.1.6.0 libcncpmsui.so
  ln -s libcncpnet.so.1.2.0 libcncpnet.so
  ln -s libcncpmsimg376.so.1.6.0 libcncpmsimg376.so
  ln -s libcncpmslld376c.so.1.04.1 libcncpmslld376c.so
  ln -s libcncpmslld376.so.1.6.0 libcncpmslld376.so
  
  # Make scangearmp usable from gimp
  install -d -m 755 ${pkgdir}/usr/lib/gimp/2.0/plug-ins/
  ln -s /usr/bin/scangearmp ${pkgdir}/usr/lib/gimp/2.0/plug-ins/

  # Install .tbl and .dat files for mg6100 series
  install -D -m 755 ${srcdir}/scangearmp-source-${_pkgver}/376/cnc174ad.tbl ${pkgdir}/usr/lib/bjlib/
  install -D -m 755 ${srcdir}/scangearmp-source-${_pkgver}/376/cnc_3760.tbl ${pkgdir}/usr/lib/bjlib/
  install -D -m 755 ${srcdir}/scangearmp-source-${_pkgver}/376/CNC6100.DAT ${pkgdir}/usr/lib/bjlib/
  install -D -m 755 ${srcdir}/scangearmp-source-${_pkgver}/376/CNC6100P.DAT ${pkgdir}/usr/lib/bjlib/

  # Install udev rules
  install -D -m 644 ${srcdir}/scangearmp-source-${_pkgver}/scangearmp/etc/80-canon_mfp.rules ${pkgdir}/etc/udev/rules.d/80-canon_mfp.rules

  # Install .ini file
  install -D -m 666 ${srcdir}/scangearmp-source-${_pkgver}/com/ini/canon_mfp_net.ini ${pkgdir}/usr/lib/bjlib/

  # Install license file
  cd ${srcdir}/scangearmp-source-${_pkgver}
  install -D LICENSE-scangearmp-${pkgver}EN.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-scangearmp-${pkgver}EN.txt

  # Remove unneeded files
  rm ${pkgdir}/usr/lib/libsane-canon_mfp.a
  rm ${pkgdir}/usr/lib/libsane-canon_mfp.la

  # Fix udev rules
  sed -i -e 's/SYSFS/ATTR/g' ${pkgdir}/etc/udev/rules.d/80-canon_mfp.rules
}
