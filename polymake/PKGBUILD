# Maintainer: Stephan Friedrichs <stephan.friedrichs@tu-bs.de>

pkgname=polymake
pkgver=2.11
pkgrel=4
pkgdesc='A tool to study the combinatorics and the geometry of convex polytopes and polyhedra'
arch=('i686' 'x86_64')
url='http://polymake.org/'
license=('GPL2')
depends=( \
	'gmp' 'mpfr' \
	'perl>=5.8.1' 'perl-term-readline-gnu' \
	'perl-xml-libxml' 'perl-xml-libxslt' 'perl-xml-writer' \
	)
makedepends=('boost')
optdepends=( \
	'java-environment>=5: jreality ' \
	'libgl: jreality' \
	'nvidia-cg-toolkit: jreality' \
	'jack: jreality' \
	)
source=("http://polymake.org/lib/exe/fetch.php/download/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('232dd41f39ad7f67508c513ced2642f84f9e116b3613a790b57900a3dfce286d')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
