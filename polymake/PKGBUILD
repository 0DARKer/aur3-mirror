# Maintainer: Stephan Friedrichs <deduktionstheorem@googlemail.com>

pkgname=polymake
pkgver=2.11
pkgrel=1
pkgdesc='A tool to study the combinatorics and the geometry of convex polytopes and polyhedra'
arch=('i686' 'x86_64')
url='http://polymake.org/'
license=('GPL2')
depends=( \
	'perl>=5.8.1' 'gmp' 'mpfr' \
	'readline>=5.0' 'perl-term-readline-gnu' \
	'libxml2' 'libxslt' 'perl-xml-libxml' 'perl-xml-libxslt' 'perl-xml-writer' \
	)
optdepends=( \
	'java-environment>=5: Visualization' \
	'jack: Visualization' \
	'lib32-libgl: Visualization' \
	)
options=('!emptydirs')
source=("http://polymake.org/lib/exe/fetch.php/download/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('5a39ec5898791acb1f83a248caf14092f8000ecf5609f427daf03334ba24bf54')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr --docdir=/usr/share/doc/${pkgname}
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}
