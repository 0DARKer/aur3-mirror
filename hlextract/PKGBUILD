# Contributer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=hlextract
pkgver=2.4.5
_pkgver=245
pkgrel=2
pkgdesc="Extract Tool and Lib for open Half-Life and other games package formats"
arch=('i686' 'x86_64')
url="http://nemesis.thewavelength.net/index.php?c=108"
license=('GPL' 'LGPL')
provides=('hlextract' 'hllib')
source=("http://nemesis.thewavelength.net/files/files/hllib${_pkgver}.zip"
        "hllib-${pkgver}.patch")
sha1sums=('67af380bd39027b2b0ea76ba1ed30f32b786b178'
          '6d543630eed34c5f8c3682bf805f5a11086b77fe')
noextract=("hllib${_pkgver}.zip")

prepare() {
  cd "${srcdir}"
  rm -fr build
  mkdir build
  cd build
  bsdtar -xf "${srcdir}/hllib${_pkgver}.zip"
  # Damm DOS
  find -type f -exec perl -pi -e 's/\r\n?/\n/g' "{}" \;

  patch -p0 -i "${srcdir}/hllib-${pkgver}.patch"

  sed 's|-fs $(PREFIX)/lib/|-fs |g' -i HLLib/Makefile
  sed 's|-m 0755|-Dm 0755|g' -i HLExtract/Makefile
}

build() {
  cd "${srcdir}/build"
  make -C HLLib PREFIX=/usr
  make -C HLExtract PREFIX=/usr
}

package() {
  cd "${srcdir}/build"
  make -C HLLib PREFIX="${pkgdir}/usr" install
  make -C HLExtract PREFIX="${pkgdir}/usr" install
  install -Dm644 Readme.txt "${pkgdir}/usr/share/doc/${pkgname}/Readme.txt"
}
