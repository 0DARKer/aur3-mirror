# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Linux port fixes by Ondřej Hošek (RavuAlHemio) https://github.com/RavuAlHemio

pkgname=hlextract
pkgver=2.4.5
_pkgver=245
pkgrel=3
pkgdesc="Extract Tool and Lib for open Half-Life and other games package formats"
arch=('i686' 'x86_64')
url="http://nemesis.thewavelength.net/index.php?c=108"
license=('GPL' 'LGPL')
provides=('hlextract' 'hllib')
makedepends=('git')
source=("http://nemesis.thewavelength.net/files/files/hllib${_pkgver}.zip"
        "hllib-${pkgver}v2.patch")
sha1sums=('2b2b9900e7cc412c6d36224d469c9a6b0477011f'
          'SKIP')
noextract=("hllib${_pkgver}.zip")

prepare() {
  cd "${srcdir}"
  rm -fr build
  mkdir build
  cd build
  bsdtar -xf "${srcdir}/hllib${_pkgver}.zip"
  # Damm DOS
  find -type f -exec perl -pi -e 's/\r\n?/\n/g' "{}" \;

  patch -p0 -i "${srcdir}/hllib-${pkgver}v2.patch"

  sed 's|-fs $(PREFIX)/lib/|-fs |g' -i HLLib/Makefile
}

build() {
  cd "${srcdir}/build"
  make -C HLLib PREFIX=/usr
  make -C HLExtract PREFIX=/usr
}

package() {
  cd "${srcdir}/build"
  make -C HLLib PREFIX="${pkgdir}/usr" install
  make -C HLExtract PREFIX="${pkgdir}/usr" install
  install -Dm644 Readme.txt "${pkgdir}/usr/share/doc/${pkgname}/Readme.txt"
}
