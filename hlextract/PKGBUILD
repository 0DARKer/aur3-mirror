# Contributer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=hlextract
pkgver=2.4.3
_pkgver=243
pkgrel=1
pkgdesc="Extract Tool and Lib for open BSP, GCF, NCF, PAK, VPK, WAD, XZP (Half-Life and other games)"
arch=('i686' 'x86_64')
url="http://nemesis.thewavelength.net/index.php?c=108"
license=('GPL' 'LGPL')
depends=('gcc-libs')
makedepends=('patch')
provides=('hlextract' 'hllib')
source=("http://nemesis.thewavelength.net/files/files/hllib${_pkgver}.zip"
        'libhl2-2.4.3-abuild.patch'
        'libhl2-2.4.3-Makefile.patch'
        'libhl2-2.4.3-switch_default.patch')
md5sums=('e1b0a85639577effb1e2650d49eb7d06'
         '20149670616857b7b6095720d62e4a89'
         '32831aaefd3a2fbc8c810a3b02418c48'
         '3b3b5edb53e4891ef2d0e5ca38302779')

build() {
     cd "${srcdir}"
     rm -fr "${srcdir}/build"
     mkdir -p 	"${srcdir}/build/lib"
     cp -R "${srcdir}/"{HLLib,HLExtract,Readme.txt} "${srcdir}/build"
     cp -R "${srcdir}/lib/HLLib.h" "${srcdir}/build/lib/HLLib.h"
     cd "${srcdir}/build"
     find -type f -exec perl -pi -e 's/\r\n?/\n/g' '{}' +
     patch --silent -p1 -f -i "${srcdir}/libhl2-2.4.3-abuild.patch"
     patch --silent -p1 -f -i "${srcdir}/libhl2-2.4.3-Makefile.patch"
     patch --silent -p1 -f -i "${srcdir}/libhl2-2.4.3-switch_default.patch"
     make -C HLLib PREFIX=/usr CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing -fPIC -funroll-loops -fvisibility=hidden"
     ln -s HLLib.h lib/hl.h
     ln -s "libhl.so.${pkgver}" HLLib/libhl.so
     gcc HLExtract/Main.c -o HLExtract/hlextract -lhl -L./HLLib -I./lib
}

package() {
     cd "${srcdir}/build"
     make -C HLLib PREFIX="${pkgdir}/usr" install
     install -Dm755 HLExtract/hlextract "${pkgdir}/usr/bin/hlextract"
     install -Dm644 Readme.txt "${pkgdir}/usr/share/doc/${pkgname}/Readme.txt"
}
