# Contributer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=hlextract
pkgver=2.4.5
_pkgver=245
pkgrel=1
pkgdesc="Extract Tool and Lib for open Half-Life and other games package formats"
arch=('i686' 'x86_64')
url="http://nemesis.thewavelength.net/index.php?c=108"
license=('GPL' 'LGPL')
provides=('hlextract' 'hllib')
source=("http://nemesis.thewavelength.net/files/files/hllib${_pkgver}.zip"
        "hllib-${pkgver}.patch")
md5sums=('166d83e73a57932d6a1b7a70d7072dd0'
         '87bfca118e1882459d08c4cd00d4116f')
noextract=("hllib${_pkgver}.zip")

prepare() {
  cd "${srcdir}"
  rm -fr build
  mkdir build
  cd build
  bsdtar -xf "${srcdir}/hllib${_pkgver}.zip"
  # Damm DOS
  find -type f -exec perl -pi -e 's/\r\n?/\n/g' "{}" \;

  patch -p0 -i "${srcdir}/hllib-${pkgver}.patch"
}

build() {
  cd "${srcdir}/build"
  make -C HLLib PREFIX=/usr
  ln -s "libhl.so.${pkgver}" HLLib/libhl.so
  gcc HLExtract/Main.c -o HLExtract/hlextract -lhl -L./HLLib -I./lib
}

package() {
  cd "${srcdir}/build"
  make -C HLLib PREFIX="${pkgdir}/usr" install
  install -Dm755 HLExtract/hlextract "${pkgdir}/usr/bin/hlextract"
  install -Dm644 Readme.txt "${pkgdir}/usr/share/doc/${pkgname}/Readme.txt"
}
