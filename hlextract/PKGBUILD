# Contributer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=hlextract
pkgver=2.4.4
_pkgver=244
pkgrel=1
pkgdesc="Extract Tool and Lib for open BSP, GCF, NCF, PAK, VPK, WAD, XZP (Half-Life and other games)"
arch=('i686' 'x86_64')
url="http://nemesis.thewavelength.net/index.php?c=108"
license=('GPL' 'LGPL')
depends=('gcc-libs')
makedepends=('patch')
provides=('hlextract' 'hllib')
source=("http://nemesis.thewavelength.net/files/files/hllib${_pkgver}.zip"
        'libhl2-2.4.4-abuild.patch'
        'libhl2-2.4.4-Makefile.patch'
        'libhl2-2.4.4-switch_default.patch')
md5sums=('854017f1365ea7162b77037d406d46c5'
         '673eb49f7d8c04670f11930aee23c147'
         '897b7e45d2d74e51efe36fcf682f9176'
         'ef0f0ff670922f21e6194de36eec701d')

build() {
     cd "${srcdir}"
     rm -fr "${srcdir}/build"
     mkdir -p 	"${srcdir}/build/lib"
     cp -R "${srcdir}/"{HLLib,HLExtract,Readme.txt} "${srcdir}/build"
     cp -R "${srcdir}/lib/HLLib.h" "${srcdir}/build/lib/HLLib.h"
     cd "${srcdir}/build"
     find -type f -exec perl -pi -e 's/\r\n?/\n/g' "{}" +
     patch --silent -p1 -f -i "${srcdir}/libhl2-2.4.4-abuild.patch"
     patch --silent -p1 -f -i "${srcdir}/libhl2-2.4.4-Makefile.patch"
     patch --silent -p1 -f -i "${srcdir}/libhl2-2.4.4-switch_default.patch"
     make -C HLLib PREFIX=/usr CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing -fPIC -funroll-loops -fvisibility=hidden"
     ln -s HLLib.h lib/hl.h
     ln -s "libhl.so.${pkgver}" HLLib/libhl.so
     gcc HLExtract/Main.c -o HLExtract/hlextract -lhl -L./HLLib -I./lib
}

package() {
     cd "${srcdir}/build"
     make -C HLLib PREFIX="${pkgdir}/usr" install
     install -Dm755 HLExtract/hlextract "${pkgdir}/usr/bin/hlextract"
     install -Dm644 Readme.txt "${pkgdir}/usr/share/doc/${pkgname}/Readme.txt"
}
