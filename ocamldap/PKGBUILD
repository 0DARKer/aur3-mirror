# Maintainer: Taylor Venable <taylor@metasyntax.net>

pkgname='ocamldap'
pkgver=2.1.8
pkgrel=1
pkgdesc='An implementation of the Light Weight Directory Access Protocol in Objective Caml'
arch=('i686' 'x86_64')
url='http://ocamldap.sourceforge.net/'
license=('LGPL')
depends=('ocaml' 'ocaml-ssl' 'ocamlnet')
makedepends=('ocaml-findlib')
source=("http://sourceforge.net/projects/ocamldap/files/ocamldap/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
options=('!strip' '!makeflags')
md5sums=('6b42715182758844a63a9bc0e11deb73')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make all opt
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p "${pkgdir}/$(ocamlfind printconf destdir)"

  env OCAMLFIND_DESTDIR="${pkgdir}/$(ocamlfind printconf destdir)" \
      OCAMLFIND_LDCONF='ignore' \
      make install

  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
