pkgname=mendeleydesktop-dev
pkgver=0.9.8.2
pkgrel=1
url="http://www.mendeley.com/"
pkgdesc="The desktop client for managing and sharing research papers - development version."
license=('custom:mendeley_eula')
arch=('i686' 'x86_64')
[ $CARCH = 'x86_64' ] && _arch='x86_64'
[ $CARCH = 'i686' ]   && _arch='i486'
source=("http://www.mendeley.com/downloads/linux/${pkgname%%-*}-${pkgver/_/-}-linux-$_arch.tar.bz2")
[ $CARCH = 'x86_64' ] && md5sums=('80efddadbd7f3f28b07a639adba810d5')
[ $CARCH = 'i686' ]   && md5sums=('28eb4997615b3f4be4549fbaaf407b95')
depends=('qt' 'openssl' 'sqlite3' 'openssl-compatibility' 'libpng12')
makedepends=('cpio')
provides=('mendeleydesktop')
conflicts=('mendeleydesktop')

build() {
        cd $srcdir/${pkgname%%-*}-${pkgver/_/-}-linux-$_arch

        mkdir -p $pkgdir/opt/mendeleydesktop/{bin,lib,plugins}
        mkdir -p $pkgdir/usr/{bin,lib}

        rm -f INSTALL README

        # install libs
        mv lib/libMendeley.so lib/libMendeley.so.0.9 \
           $pkgdir/usr/lib/ 
        install -m755 lib/libMendeley.so.${pkgver%%_*} \
                      lib/libPDFNetC.so \
                      $pkgdir/usr/lib/ 
        install -m755 lib/libpng.so.3 lib/libssl.so.0 \
                      lib/libQtGui.so.4 \
                      lib/libQtSvg.so.4 \
                      lib/libQtNetwork.so.4  \
                      lib/libQtXml.so.4 \
                      lib/libQtCore.so.4 \
                      lib/libQtSql.so.4 \
		      lib/libQtWebKit.so.4 \
		      lib/libQtXmlPatterns.so.4 \
                      $pkgdir/opt/mendeleydesktop/lib

        # install actual executables with pointer to sqlite plugin
        cat > $pkgdir/opt/mendeleydesktop/bin/qt.conf <<__EOF__
[Paths]
plugins=/opt/mendeleydesktop/plugins
__EOF__
        install -m755 \
           lib/mendeleydesktop/libexec/{Updater,mendeleydesktop.$_arch} \
           $pkgdir/opt/mendeleydesktop/bin 
        tar -cf - -C lib/mendeleydesktop/plugins . | \
          tar -C $pkgdir/opt/mendeleydesktop/plugins/ -xpf - 
        ln -s /usr/share/mendeleydesktop/ $pkgdir/opt/mendeleydesktop/bin/share

        # install launcher and fix up path
        sed -i "s~\"\$LOCAL_LIB_PATH\"/mendeleydesktop/libexec/~LD_LIBRARY_PATH=/opt/mendeleydesktop/lib:\$LD_LIBRARY_PATH /opt/mendeleydesktop/bin/~" \
               bin/mendeleydesktop 
        install -D -m755 bin/mendeleydesktop \
                         $pkgdir/usr/bin/mendeleydesktop 

        # install license and resources
        install -D -m644 LICENSE \
                         $pkgdir/usr/share/licenses/${pkgname%%-*}/LICENSE 
        rm -f LICENSE

        find share/ | cpio -p -dum $pkgdir/usr 
        install -Dm644 share/icons/hicolor/48x48/apps/mendeleydesktop.png \
                       $pkgdir/usr/share/pixmaps/mendeleydesktop.png
        rm -rf share/{applications,icons}

}
