# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-frameworks-git
pkgver=v4.10.90.r77.g4f55d1f
pkgrel=1
pkgdesc="Core components for the KDE's Activities"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
depends=('kdelibs-frameworks-git')
makedepends=('extra-cmake-modules-git' 'git')
source=('git://anongit.kde.org/kactivities#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kactivities
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities \
    -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/opt/kf5
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
