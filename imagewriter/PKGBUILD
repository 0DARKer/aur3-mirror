# Maintainer: Vindex17 <vindex17@outlook.it>

pkgname=imagewriter
pkgver=1.10
_release=4.1.3
pkgrel=13
pkgdesc="A graphical utility for writing raw disk images & hybrid isos to USB keys"
arch=('i686' 'x86_64')
url='https://github.com/mbarringer/imagewriter'
license=('GPL2')
depends=('qt4' 'udisks2')
optdepends=('xdg-su: NEEDED by non-KDE users!'
	    'kdebase-runtime: provides kdesu')
source=("http://download.opensuse.org/repositories/openSUSE:/13.1/standard/src/$pkgname-$pkgver-${_release}.src.rpm")
sha512sums=('791f57bc9582dc86690d88ad225f3e10093d7cf488ef78bbdf3377ccb3c311aa09d8087e16620cfae02a68c2b9add71a0582480c230a98a2226d7e7d28e03570')

prepare() {
	cd $srcdir
	tar xvzf $pkgname-$pkgver.tar.gz
	if [ -e /usr/bin/kdesu ]
	  then sed 's/\/usr\/bin\/xdg-su -c \/usr\/bin\//kdesu /' -i $pkgname-$pkgver/$pkgname.desktop
	  else sed 's/\/usr\/bin\/xdg-su -c \/usr\/bin\//xdg-su -s /' -i $pkgname-$pkgver/$pkgname.desktop
	fi
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	qmake-qt4 PREFIX="$pkgdir/usr" DEFINES=USEUDISKS2 $pkgname.pro
	make CFLAGS="$CFLAGS -DKIOSKHACK"
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
