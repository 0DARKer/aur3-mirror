# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=imagewriter
pkgver=1.9
_opensuse_rel=6.1
pkgrel=3
pkgdesc="SUSE Studio Imagewriter - Graphical image writer application"
url="http://kiwi.berlios.de"
arch=('x86_64' 'i686')
license=('GPLv2')
depends=('qt')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/imagewriter-${pkgver}-${_opensuse_rel}.src.rpm"
        'build_fix.patch')
sha512sums=('b117974dd298f266917bdf1fc853a70e6e7719cc83677bd289bb5929ab76b19fe997cb20077c089ac6f2e313694b7ebd083e1d344a53deedeb548aa7d8cc784b'
            '42034f630d0cd097a94e16ea6428c558ec00f16051330cb1bbaf0a2c5c496a6ece997328fba5ffed79cf13aa13299e787c1b82e4a29a7b92438bc63b36d278e0')

build() {
  cd "${srcdir}"
  rm -vf imagewriter.spec
  tar zxvf imagewriter-${pkgver}.tar.gz
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/build_fix.patch"
  qmake PREFIX=/usr imagewriter.pro
  make CFLAGS="${CFLAGS} -DKIOSKHACK -Wno-unused-parameter"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
