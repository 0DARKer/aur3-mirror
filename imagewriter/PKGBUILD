# Maintainer: Vindex17 <vindex17@outlook.it>

pkgname=imagewriter
pkgver=1.10
_release=1.7
pkgrel=12
pkgdesc="A graphical utility for writing raw disk images & hybrid isos to USB keys"
arch=('i686' 'x86_64')
url='https://github.com/mbarringer/imagewriter'
license=('GPL2')
depends=('qt4' 'udisks2')
optdepends=('xdg-su: NEEDED by non-KDE users!'
	    'kdebase-runtime: provides kdesu')
source=("http://download.opensuse.org/repositories/SUSE:/SUSEStudio/openSUSE_Tumbleweed/src/$pkgname-$pkgver-${_release}.src.rpm")
sha512sums=('845875c86591fbf414666cd1721793ebcf79bd2466b41ce398d384248a96378d8d69a422afe034d5397718275d32026582aae199de7b19db3ca59162583772af')

prepare() {
	cd $srcdir
	tar xvzf $pkgname-$pkgver.tar.gz
	if [[ -e /usr/bin/kdesu ]]
	  then sed 's/\/usr\/bin\/xdg-su -c \/usr\/bin\//kdesu /' -i $pkgname-$pkgver/$pkgname.desktop
	  else sed 's/\/usr\/bin\/xdg-su -c \/usr\/bin\//xdg-su -s /' -i $pkgname-$pkgver/$pkgname.desktop
	fi
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	qmake-qt4 PREFIX="$pkgdir/usr" DEFINES=USEUDISKS2 $pkgname.pro
	make CFLAGS="$CFLAGS -DKIOSKHACK"
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
