# Maintainer: Alexander Rødseth <rodseth@gmail.com>
pkgname=tiddlywiki
pkgver=2.6.3
pkgrel=1
pkgdesc="Personal wiki where everything is in one html-file"
arch=('x86_64' 'i686')
url="http://www.tiddlywiki.com/"
license=('BSD')
depends=('xdg-utils' 'pcre' 'gc')
makedepends=('shedskin')
source=('http://www.tiddlywiki.com/empty.html'
        'tiddlywiki.py')
md5sums=('2440d0dddd6107d41cf1ac802352d4ec'
         'a7220d1c7f6d9c6ead1222a97b40563a')

build() {
  cd "$srcdir"

  msg2 "Compiling wrapperscript..."
  shedskin $pkgname.py
  make CPPFLAGS="-march=native -fomit-frame-pointer"

  msg2 "Extracting license..."
  cat empty.html | head -39 | tail -29 > COPYING
}

package() {
  cd "$srcdir"

  msg2 "Packaging data files..."
  mkdir -p "$pkgdir/usr/share/tiddlywiki"
  cp "$srcdir/empty.html" "$pkgdir/usr/share/tiddlywiki/"
  install -Dm644 empty.html "$pkgdir/usr/share/tiddlywiki/empty.html"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

  msg2 "Packaging license..."
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
