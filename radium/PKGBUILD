# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=radium
pkgver=1.9.42
pkgrel=1
pkgdesc="A graphical music editor and a next generation tracker."
arch=('i686' 'x86_64')
url="http://users.notam02.no/~kjetism/radium/"
license=('GPL')
depends=('calf-kxstudio-git' 'liblrdf' 'libxaw' 'qt4')
source=("http://archive.notam02.no/arkiv/src/$pkgname-$pkgver.tar.gz")
md5sums=('4767fc7d42464590d6feaf254f41cfb8')

prepare() {
  cd $pkgname-$pkgver

  # python2 shebang
  sed -i "s|\#\!.*python|&2|" `grep -rl "\#\!.*python" .`

  # force install
  sed -i '/exit/d' Makefile

  # do not look for libiberty.h
  sed -i '/libiberty.h/d' check_dependencies.sh \
                          crashreporter/backtrace-symbols.c
}

build() {
  cd $pkgname-$pkgver
  make packages
  BUILDTYPE=RELEASE ./build_linux.sh -j7
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
