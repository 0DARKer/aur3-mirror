# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=radium
pkgver=1.9.35
pkgrel=1
pkgdesc="A graphical music editor and a next generation tracker."
arch=('i686' 'x86_64')
url="http://users.notam02.no/~kjetism/radium/"
license=('GPL')
depends=('calf-kxstudio-git' 'liblrdf' 'libxaw' 'qt4')
source=("http://archive.notam02.no/arkiv/src/$pkgname-$pkgver.tar.gz")
md5sums=('665e33375fc26c9900dec642ec37194c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 shebang
  sed -i "s|\#\!.*python|&2|" `grep -rl "\#\!.*python" .`

  # force install
  sed -i '/exit/d' Makefile

  # do not look for libiberty.h
  sed -i '/libiberty.h/d' check_dependencies.sh \
                          crashreporter/backtrace-symbols.c
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make packages
  BUILDTYPE=RELEASE ./build_linux.sh -j7
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
