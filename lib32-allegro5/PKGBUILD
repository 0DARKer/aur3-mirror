_pkgbasename=allegro
pkgname=lib32-${_pkgbasename}5
pkgver=5.0.2
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming (32 bit)"
arch=('x86_64')
url="http://alleg.sourceforge.net/"
license=('custom:allegro')
depends=('lib32-alsa-lib' 'lib32-gtk2' 'lib32-libjpeg-turbo' 'lib32-libpulse'
	 'lib32-libxxf86vm' 'lib32-nvidia-utils' 'lib32-openal' 'lib32-physfs')
makedepends=('cmake' 'gcc-multilib')
replaces=('lib32-allegro')
options=('!makeflags')
conflicts=('lib32-allegro')
source=(http://downloads.sourceforge.net/alleg/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('49fee5e580e4b46f72ced6e1a7fd2443')

build() {
  cd "${srcdir}"
  rm -rf build
  mkdir build && cd build  
  cmake "../${_pkgbasename}-${pkgver}" \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DLIB_SUFFIX="32" \
	-DCMAKE_C_FLAGS="$CFLAGS -m32" \
	-DCMAKE_CXX_FLAGS="$CXXFLAGS -m32" \
	-DWANT_DOCS=OFF
  make
}

package() {
  cd "${srcdir}/build"  
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include}
  install -Dm644 "../${_pkgbasename}-${pkgver}/LICENSE.txt" \
  		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
