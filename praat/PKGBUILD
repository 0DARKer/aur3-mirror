# Maintainer: jpate <j.k.pate@sms.ed.ac.uk>
pkgname=praat
pkgver=5.3.84
pkgrel=1
pkgdesc="A tool for 'Doing Phonetics by computer'"
arch=('x86_64' 'i686')
url="http://www.fon.hum.uva.nl/praat/"
license=('GPL')
depends=( 'alsa-lib' 'gtk2' )
makedepends=( 'pkg-config' )
optdepends=( 'ttf-sil-fonts' )
source=("http://www.fon.hum.uva.nl/praat/praat5384_sources.tar.gz")
md5sums=('8b4acfb7b7b86b66da70328b1f4eadbd')

prepare() {
  cd "$srcdir/sources_5384/"

  cp makefiles/makefile.defs.linux.alsa makefile.defs

  sed -i 's/-lasound/-lasound -lpthread/' makefile.defs
}

build() {
  cd "$srcdir/sources_5384/"

  make
}

package() {
  cd "$srcdir/sources_5384/"

  install -Dm755  praat "$pkgdir/usr/bin/praat"
}

