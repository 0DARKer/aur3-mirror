# Maintainer: jpate <jkpate@jkpate.net>
pkgname=praat
pkgver=5.4.01
pkgrel=1
pkgdesc="A tool for 'Doing Phonetics by computer'"
arch=('x86_64' 'i686')
url="http://www.fon.hum.uva.nl/praat/"
license=('GPL')
depends=( 'alsa-lib' 'gtk2' )
makedepends=( 'pkg-config' )
optdepends=( 'ttf-sil-fonts' )
source=("http://www.fon.hum.uva.nl/praat/praat5401_sources.tar.gz")
md5sums=('92019d42037d3daf7bb8407f3fe02d2d')

prepare() {
  cd "$srcdir/sources_5401/"

  cp makefiles/makefile.defs.linux.alsa makefile.defs

  sed -i 's/-lasound/-lasound -lpthread/' makefile.defs
}

build() {
  cd "$srcdir/sources_5401/"

  make
}

package() {
  cd "$srcdir/sources_5401/"

  install -Dm755  praat "$pkgdir/usr/bin/praat"
}

