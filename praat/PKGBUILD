# Maintainer: jpate <jkpate@jkpate.net>
pkgname=praat
pkgver=5.4.08
pkgrel=1
pkgdesc="A tool for 'Doing Phonetics by computer'"
arch=('x86_64' 'i686')
url="http://www.fon.hum.uva.nl/praat/"
license=('GPL')
depends=( 'alsa-lib' 'gtk2' )
makedepends=( 'pkg-config' )
optdepends=( 'ttf-sil-fonts' )
source=("http://www.fon.hum.uva.nl/praat/praat5408_sources.tar.gz")
md5sums=('3ffb75ac6232a97c426df7ce692a7e1f')

prepare() {
  cd "$srcdir/sources_5408/"

  cp makefiles/makefile.defs.linux.alsa makefile.defs

  sed -i 's/-lasound/-lasound -lpthread/' makefile.defs
}

build() {
  cd "$srcdir/sources_5408/"

  make
}

package() {
  cd "$srcdir/sources_5408/"

  install -Dm755  praat "$pkgdir/usr/bin/praat"
}

