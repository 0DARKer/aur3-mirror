# Maintainer: jpate <j.k.pate@sms.ed.ac.uk>
pkgname=praat
pkgver=5.3.72
pkgrel=1
pkgdesc="A tool for 'Doing Phonetics by computer'"
arch=('x86_64' 'i686')
url="http://www.fon.hum.uva.nl/praat/"
license=('GPL')
depends=( 'alsa-lib' 'gtk2' )
makedepends=( 'pkg-config' )
optdepends=( 'ttf-sil-fonts' )
source=("http://www.fon.hum.uva.nl/praat/praat5372_sources.tar.gz")
md5sums=('d9de44ffa18a894f27a1c91d3b336480')

prepare() {
  cd "$srcdir/sources_5372/"

  cp makefiles/makefile.defs.linux.alsa makefile.defs

  sed -i 's/-lasound/-lasound -lpthread/' makefile.defs
}

build() {
  cd "$srcdir/sources_5372/"

  make
}

package() {
  cd "$srcdir/sources_5372/"

  install -Dm755  praat "$pkgdir/usr/bin/praat"
}

