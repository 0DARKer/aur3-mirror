# Maintener: SÃ©bastien Martinez (Garrik) garrik@garrik.info
# Based on the PKGBUILD for pypy-hg by Sven-Hendrik Haase (svenstaro) <sh@lutzhaase.com>


pkgname=pypy-dsu
pkgver=2.4.0
pkgrel=1
pkgdesc="The pypy interpreter with DSU enhancements"
url="http://bitbucket.org/smartinezgd/pymoult/wiki/pypy-dsu"
arch=('i686' 'x86_64')
depends=('libffi')
provides=('python2' 'pypy')
options=(!buildflags)
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module')
license=('custom:MIT')

# Why use python2 or a previous version of pypy to do the translation when we
# could just as easily use the upstream binary?
source=('https://bitbucket.org/smartinezgd/pymoult/downloads/pypy-dsu-2.4-src.tar.gz')
md5sums=('115b2a4b8ecc2f129bff028443a473cb')

build(){
  cd "${srcdir}/pypy-dsu-35d3aaccd714/pypy/goal"
  export CFLFAGS="-O1"
  export CXXFLAGS="-O1"

  python2 ../../rpython/bin/rpython -Ojit targetpypystandalone

}

package() {
  cd "${srcdir}/pypy-dsu-35d3aaccd714/pypy/goal"

  install -Dm755 pypy-c "${pkgdir}/opt/pypy-dsu/pypy-c"
  mkdir -p "${pkgdir}/opt/pypy-dsu/lib-python"

  cd "${srcdir}/pypy-dsu-35d3aaccd714/"
  cp -r lib-python/2.7 "${pkgdir}/opt/pypy-dsu/lib-python/"
  cp -r lib_pypy "${pkgdir}/opt/pypy-dsu/"
  cp -r include "${pkgdir}/opt/pypy-dsu/"

  mkdir -p "${pkgdir}/usr/bin"
  ln -s /opt/pypy-dsu/pypy-c "${pkgdir}/usr/bin/pypy-dsu"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/pypy-dsu/LICENSE"
}
# vim: ts=2 sw=2 et:
