# Maintainer: Maik Broemme <mbroemme@libmpq.org>
pkgname="dahdi-tools"
pkgdesc="DAHDI tools for Asterisk (Digium, OpenVox, Allo and Yeastar cards)"
pkgver=2.10.0.1
pkgrel=1
arch=("i686" "x86_64")
url="http://www.asterisk.org/"
license=("LGPLv2")
depends=("dahdi-linux" "libusb" "perl")
source=(
  "http://downloads.asterisk.org/pub/telephony/dahdi-tools/${pkgname}-${pkgver}.tar.gz"
  "dahdi-tools-2.10.0.1-allo.patch"
  "dahdi-tools-2.10.0.1-openvox.patch"
  "dahdi-tools-2.10.0.1-yeastar.patch"
)
sha256sums=(
  "b28691b8160d95aaefb7efe4c7afbc906dcc0eaf77e728924a5edc705f7e66c7"
  "5176ab6f92da173e667b5f9b7c82131173dff61ae69ed3e51c743ac955a40cde"
  "fcc641ade28a1bd46a4c2abe45d6e19c25d08484421ddb3365bcb07f37cc183b"
  "b420dd3d4c0f08d0a696acc545f35e2157d9a50073581497b4d4093b7a23f32f"
)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/dahdi-tools-2.10.0.1-allo.patch"
  patch -Np1 -i "${srcdir}/dahdi-tools-2.10.0.1-openvox.patch"
  patch -Np1 -i "${srcdir}/dahdi-tools-2.10.0.1-yeastar.patch"
  sed 's,$(prefix)/sbin,$(prefix)/bin,' -i xpp/Makefile
  ./configure --with-dahdi --sbindir=/usr/bin
  make DESTDIR="${pkgdir}" all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
