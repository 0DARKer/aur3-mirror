# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=libdesktop-agnostic-bzr
_realname=libdesktop-agnostic
pkgver=405
pkgrel=1
pkgdesc="Desktop agnostic library for Glib-based projects"
arch=('i686' 'x86_64')
url="https://launchpad.net/libdesktop-agnostic"
license=('GPL2' 'LGPL2.1')
depends=('glib2' 'gtk2' 'pygtk' 'pygobject')
makedepends=('bzr' 'gconf' 'gobject-introspection' 'intltool' 'vala')
optdepends=('gconf: gconf configuration backend')
provides=(${_realname})
conflicts=(${_realname})
backup=(etc/xdg/${_realname}/desktop-agnostic.ini)
options=('!libtool')
source=('gir.patch')
md5sums=('69bf989b9204fe886c958b279822696c')

_bzrtrunk="lp:${_realname}"
_bzrmod=${_realname}

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  # python2 fix
  sed -i '1s|^#!.*python$|&2|' waf

  # fix to turn on introspection
  patch -Np1 -i "${srcdir}/gir.patch"

  export PYTHON="/usr/bin/python2"
  ./waf configure --prefix=/usr --sysconfdir=/etc \
                  --config-backends=gconf,keyfile \
                  --vfs-backends=gio \
                  --desktop-entry-backends=gio,glib
  ./waf
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  ./waf install --destdir="${pkgdir}" --sysconfdir="${pkgdir}"/etc
}

