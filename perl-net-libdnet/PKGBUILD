# Maintainer: Max Pray a.k.a. <synthead@gmail.com>
# Contributor: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=perl-net-libdnet
_cpanname="Net-Libdnet"
pkgver=0.96
pkgrel=2
pkgdesc="Binding for Dug Song's libdnet"
arch=('i686' 'x86_64')
url="http://search.cpan.org/~gomor/Net-Libdnet/"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.5.0' 'perl-class-gomor' 'libdnet')
makedepends=('perl-extutils-makemaker')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/G/GO/GOMOR/$_cpanname-$pkgver.tar.gz")
md5sums=('e359aa9ecfe76c5f3c2e8c4005e8940a')

# Function to change to the working directory and set
# environment variables to override undesired options.
prepareEnvironment() {
	cd "$srcdir/$_cpanname-$pkgver"
	export \
		PERL_MM_USE_DEFAULT=1 \
		PERL_AUTOINSTALL=--skipdeps \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
		PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
		MODULEBUILDRC=/dev/null
}

build() {
	prepareEnvironment
	/usr/bin/perl Makefile.PL
	make
}

check() {
	prepareEnvironment
	make test
}

package() {
	prepareEnvironment
	make install

	# Remove "perllocal.pod" and ".packlist".
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
