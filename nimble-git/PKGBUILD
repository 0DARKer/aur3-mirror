# Maintainer: Alexander F RÃ¸dseth <rodseth@gmail.com>

pkgname=nimble-git
pkgver=184
pkgrel=1
pkgdesc='Package manager for the Nimrod programming language'
arch=('x86_64' 'i686')
url='https://github.com/nimrod-code/nimble'
license=('BSD')
makedepends=('nimrod>=0.9.4')
source=("$pkgname::git+https://github.com/nimrod-code/nimble.git")
md5sums=('SKIP')

pkgver () {
  cd "$pkgname"

  git rev-list --count master
}

build() {
  cd "$pkgname/src/"

  nimrod c -d:release nimble.nim
}

package() {
  cd "$pkgname"

  install -Dm755 "src/nimble" "$pkgdir/usr/bin/nimble"
  install -Dm644 "license.txt" "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

# vim:set ts=2 sw=2 et:
