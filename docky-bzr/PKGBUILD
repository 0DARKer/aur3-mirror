# Contributor: Alessio 'Bl@ster' Biancalana <dottorblaster@gmail.com>
# Contributor: Alessio Sergi <sergi.alessio {at} gmail.com>

pkgname=docky-bzr
_realname=docky
pkgver=1815
pkgrel=1
pkgdesc="The finest dock no money can buy"
arch=('i686' 'x86_64')
url="https://launchpad.net/docky"
license=('GPL3')
depends=('dbus-sharp-glib' 'dockmanager-bzr' 'gnome-sharp' 'gio-sharp-git' \
         'gnome-desktop-sharp' 'gnome-keyring-sharp' 'gtk2' \
         'hicolor-icon-theme' 'mono-addins' 'notify-sharp-svn' \
         'xdg-utils')
makedepends=('bzr' 'intltool')
provides=(${_realname})
conflicts=(${_realname})
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=()
md5sums=()

_bzrtrunk=lp:${_realname}
_bzrmod=${_realname}

build() {
  export MONO_SHARED_DIR=${srcdir}/.wabi
  mkdir -p ${MONO_SHARED_DIR}

  cd ${srcdir}

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_bzrmod}-build
  cp -r ${_bzrmod} ${_bzrmod}-build
  cd ${_bzrmod}-build

  ./autogen.sh && ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${srcdir}/${_bzrmod}-build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  install -dm755 ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${_realname}.schemas \
                        ${_realname} ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

#  rm -rf ${srcdir}/${_bzrmod}-build
}
