# Maintainer: Alessio 'Bl@ster' Biancalana <dottorblaster at archlinux dot us>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=docky-bzr
_pkgname=docky
pkgver=1829
pkgrel=1
pkgdesc="The finest dock no money can buy"
arch=('i686' 'x86_64')
url="https://launchpad.net/docky"
license=('GPL3')
depends=('gconf-sharp' 'gnome-keyring-sharp' 'hicolor-icon-theme' \
         'libgnome-desktop-sharp' 'mono-addins' 'notify-sharp' \
         'rsvg2-sharp' 'wnck-sharp' 'xdg-utils')
makedepends=('bzr' 'intltool' 'libgnome-sharp' 'gio-sharp')
optdepends=('dockmanager: helpers support')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!emptydirs')
install=$pkgname.install

_bzrtrunk="https://code.launchpad.net/~$_pkgname-core/$_pkgname/trunk"
_bzrmod="$pkgname"

build() {
  cd "$srcdir"

  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr --no-plugins pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr --no-plugins branch "$_bzrtrunk" "$_bzrmod" -q -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schema-install
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

  install -d -m 755 "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$_pkgname.schemas" \
    "$_pkgname" "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
