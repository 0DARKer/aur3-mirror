# Maintainer: Ner0
# Contributor: Alessio 'Bl@ster' Biancalana <dottorblaster at archlinux dot us>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=docky-bzr
pkgver=1829
pkgrel=1
pkgdesc="Full fledged dock application that makes opening common applications and managing windows easier and quicker"
url="http://wiki.go-docky.com/"
arch=('any')
license=('GPL3')
depends=('gconf-sharp' 'libgnome-desktop-sharp' 'gnome-keyring-sharp' 'mono-addins' 'notify-sharp' 'rsvg2-sharp' 'wnck-sharp' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('bzr' 'intltool' 'libgnome-sharp' 'gio-sharp')
install=$pkgname.install

_bzrtrunk=lp:docky
_bzrmod=docky

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir/$_bzrmod-build"
  make DESTDIR="$pkgdir/" install
}
