# Maintainer: Rene Peinthor <peinthor@gmail.com>
pkgname=blender-beta
pkgver=2.56a
pkgrel=3
pkgdesc="Blender beta version build from source"
arch=('i686' 'x86_64')
url="http://blender.org/"
depends=('libpng' 'mesa' 'openexr' 'python=3.2' 'libsamplerate' 'ffmpeg' 'openal' 'freetype2' 'libxi' 'libtiff')
makedepends=('python' 'yasm' 'cmake')
provides=('blender=$pkgver')
conflicts=('blender')
license=('GPL')
install=blender.install
#source=(http://download.blender.org/source/blender-$pkgver-beta.tar.gz blender.desktop)
source=(http://rp.oldsch00l.com/tmp/source/blender-$pkgver-beta-source.tar.bz2 python32.diff blender.desktop)
md5sums=('55624e1a5ebbb2b2ef919f6f09b3d862'
	 '01584eeb3aa5053d9fbd365ccb2ea76e'
         '634b532b58d5ef51bcbb7859b318e81d')

extractedSrcDir="blender-$pkgver-beta-source"

build() {
  cd "$srcdir"/$extractedSrcDir

  msg "Patching for Python 3.2"
  patch -p0 < ../python32.diff

  msg "Starting make..."
  ##########
  [ -e "$srcdir"/blender-$pkgver-beta-build ] || mkdir -p "$srcdir"/blender-$pkgver-beta-build
  cd "$srcdir"/blender-$pkgver-beta-build
  cmake -D PYTHON_LIB=/usr/lib/python3.2/config-3.2mu/libpython3.2mu.so -D PYTHON_INC=/usr/include/python3.2mu -DWITH_CODEC_SNDFILE:BOOL=ON -DWITH_CODEC_FFMPEG:BOOL=ON -DWITH_PYTHON_INSTALL:BOOL=OFF -DCMAKE_INSTALL_PREFIX=/usr "$srcdir"/$extractedSrcDir
  make
  ##########
  # now compile some plugins
  cp -rf "$srcdir"/$extractedSrcDir/release/plugins/* \
    "$srcdir"/$extractedSrcDir/source/blender/blenpluginapi/
  cd "$srcdir"/$extractedSrcDir/source/blender/blenpluginapi
  chmod 755 bmake
  sed -i 's/\r$//' bmake
  make
}

package() {
  cd "$srcdir"/blender-$pkgver-beta-build
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/blender.desktop \
    "$pkgdir"/usr/share/applications/blender.desktop

  # install plugins
  install -d -m755 "$pkgdir"/usr/share/blender/*/plugins/{sequence,texture}
  cp "$srcdir"/$extractedSrcDir/source/blender/blenpluginapi/sequence/*.so \
    "$pkgdir"/usr/share/blender/*/plugins/sequence/
  cp "$srcdir"/$extractedSrcDir/source/blender/blenpluginapi/texture/*.so \
    "$pkgdir"/usr/share/blender/*/plugins/texture/
}
