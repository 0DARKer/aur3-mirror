# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=gnome-music-git
pkgver=207.eb90541
pkgrel=1
pkgdesc="Play and organize your music collection"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('grilo-plugins' 'gstreamer' 'libgee' 'gjs' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'gobject-introspection' 'gnome-common' 'git')
provides=('gnome-music')
conflicts=('gnome-music')
install=gnome-music-git.install
options=(!libtool)
source=('git://git.gnome.org/gnome-music' add_icons_bgo697929.patch)
md5sums=('SKIP' '96127850f0032eb884e63b60ab900895')

pkgver() {
  cd "$srcdir"/gnome-music
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir"/gnome-music
  patch -p1 -i $srcdir/add_icons_bgo697929.patch
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "$srcdir"/gnome-music
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
