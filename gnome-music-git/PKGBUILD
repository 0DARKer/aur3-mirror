# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=gnome-music
pkgname=$_pkgname-git
pkgver=7d5db80
pkgrel=1
pkgdesc="Music player and management application for GNOME"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=("gtk3>=3.9.8" 'glib2' 'python-dbus' 'python-gobject' "grilo>=0.2.7" 'grilo-plugins' 'python-dbus-common')
makedepends=('git' 'intltool' 'gobject-introspection' 'gnome-common')
provides=('gnome-music')
conflicts=('gnome-music')
install=gnome-music.install
source=(git://git.gnome.org/$_pkgname)
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir/" install
}
