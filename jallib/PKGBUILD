# Maintainer: Matias De la Puente <mfpuente.ar@gmail.com>
pkgname=jallib
pkgver=0.7.1
pkgrel=1
pkgdesc="JAL V2 libraries"
arch=('any')
url="http://code.google.com/p/jallib/"
license=('BSD' 'ZLIB')
depends=('jalv2')
options=(!strip)
source=(http://jallib.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1995fc7dd8e6da19ed8f4d8df3cd2f7c54b2616c41f4b453b36bb547d1a30447')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # install libraries
  install -dm755 "${pkgdir}/usr/share/${pkgname}/lib"
  install -Dm644 -t "${pkgdir}/usr/share/${pkgname}/lib/" lib/*
  
  # install doc
  install -dm755 "${pkgdir}/usr/share/${pkgname}/doc/html"
  install -Dm644 -t "${pkgdir}/usr/share/${pkgname}/doc/html/" doc/html/*
  
  # install samples
  install -dm755 "${pkgdir}/usr/share/${pkgname}/sample"
  install -Dm644 -t "${pkgdir}/usr/share/${pkgname}/sample/" sample/*
  
  # install licenses
  install -Dm644 LICENSE.bsd "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.bsd"
  install -Dm644 LICENSE.zlib "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.zlib"
}

