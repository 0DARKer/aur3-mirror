# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_p=python-sipsimple
pkgname=python2-sipsimple
pkgver=0.18.2
pkgrel=1
pkgdesc="Python SDK for development of SIP end-points"
license=('custom:MIT' 'LGPL')
arch=('i686' 'x86_64')
url="http://download.ag-projects.com/SipClient"
depends=('cython' 'alsa-lib' 'util-linux' 'python2-backports-darcs' 'python-gnutls'
         'python2-xcaplib' 'python2-msrplib' 'python2-eventlet08')
makedepends=('subversion')
options=('!makeflags')
source=("http://download.ag-projects.com/SipClient/$_p-$pkgver.tar.gz")
md5sums=('4d0b56904da5ba143a7c87ed439f2567')

build() {
  cd "$srcdir/$_p-$pkgver"

  # fix application module import
  sed -i "s|\(application\.python\)\( import Null\)|\1\.util\2|g" `grep -rl "application\.python import Null" .`
  sed -i "s|, limit|\nfrom application.python import limit|" `grep -rl ", limit" .`

  python2 setup.py build_ext --pjsip-clean-compile
}

package() {
  cd "$srcdir/$_p-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
