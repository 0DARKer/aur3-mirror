# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=python2-sipsimple
pkgver=0.34.0
pkgrel=1
pkgdesc="Python SDK for development of SIP end-points"
license=('custom:MIT' 'LGPL')
arch=('i686' 'x86_64')
url="http://download.ag-projects.com/SipClient"
depends=('alsa-lib' 'util-linux' 'python2-backports' 'python2-dateutil'
         'python2-dnspython' 'python2-eventlib' 'python2-msrplib' 'python2-xcaplib')
makedepends=('cython2' 'subversion')
options=('!makeflags')
source=("http://download.ag-projects.com/SipClient/${pkgname/2}-$pkgver.tar.gz"
        "sipsimple-cython.patch")
md5sums=('f0c1c1d882f303167efaf53c3afd8011'
         '586d604d600ddbea7c3f5a27c42126e4')

prepare() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  patch -p1 -i ../${source[1]} # cython 0.19 patch
}

build() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py build_ext --pjsip-clean-compile
}

package() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  # license
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
