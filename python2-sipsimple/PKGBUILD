# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=python2-sipsimple
pkgver=0.31.1
pkgrel=1
pkgdesc="Python SDK for development of SIP end-points"
license=('custom:MIT' 'LGPL')
arch=('i686' 'x86_64')
url="http://download.ag-projects.com/SipClient"
depends=('alsa-lib' 'python2-application' 'python2-backports' 'python2-dateutil'
         'python2-dnspython' 'python2-eventlib' 'python2-gnutls' 'python2-lxml'
         'python2-msrplib' 'python2-xcaplib' 'twisted')
makedepends=('cython2' 'subversion')
options=('!makeflags')
source=("http://download.ag-projects.com/SipClient/${pkgname/2}-$pkgver.tar.gz")
md5sums=('1c213f64b46e649d124ba9456c4c7438')

build() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py build_ext --pjsip-clean-compile
}

package() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  # license
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
