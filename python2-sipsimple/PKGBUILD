# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=python2-sipsimple
_pkgname=python-sipsimple
pkgver=0.18.0
pkgrel=1
pkgdesc="Python SDK for development of SIP end-points"
license=('MIT')
arch=('i686' 'x86_64')
url="http://download.ag-projects.com/SipClient"
depends=('cython')
makedepends=('subversion')
options=('!makeflags')
source=("http://download.ag-projects.com/SipClient/${_pkgname}-${pkgver}.tar.gz"
        'http://devel.ag-projects.com/repositories/python-sipsimple/patches/pjsip-3368-disable_assert_on_invite_answer.patch'
        'http://devel.ag-projects.com/repositories/python-sipsimple/patches/pjsip-3368-evsub_timer_functions.patch'
        'http://devel.ag-projects.com/repositories/python-sipsimple/patches/pjsip-3368-remove_hdr_by_name.patch')
md5sums=('be320b3c6c40d1fe4f15b5ae7b368b6e'
         'fbb6144c214c8849402f0c8595bb595e'
         '936830b09a30132400f846a5a41dcd91'
         'b867dd7750c01f153ec2e7ec44126a88')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  cp "$srcdir/pjsip-3368-disable_assert_on_invite_answer.patch" patches/
  cp "$srcdir/pjsip-3368-evsub_timer_functions.patch" patches/
  cp "$srcdir/pjsip-3368-remove_hdr_by_name.patch" patches/
  python2 setup.py build_ext --pjsip-clean-compile
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
