# Maintainer: Westminboy <westminboy@gmail.com>

pkgname=networkmanager-pptp-lite
_pkgname=networkmanager-pptp
pkgver=0.9.8.0
pkgrel=1
pkgdesc="NetworkManager VPN plugin for pptp "
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=${pkgver}" 'pptpclient' 'gtk2')
makedepends=('intltool')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
options=('!libtool')
provides=($_pkgname)
conflicts=($_pkgname)
install=networkmanager-pptp.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-pptp/0.9/NetworkManager-pptp-${pkgver}.tar.xz)
sha256sums=('34d0981462d8948ace0dd4f49478c86017b53516afbbf62e2a4c6c0adc679cc8')

build() {
  cd NetworkManager-pptp-${pkgver}
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --libexecdir=/usr/lib/networkmanager \
      --with-gtkver=2 \
      --disable-maintainer-mode \
      --disable-static \
      --enable-more-warnings=no
  make
}

package() {
  cd NetworkManager-pptp-${pkgver}
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}
