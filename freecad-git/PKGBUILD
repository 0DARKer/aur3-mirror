# Maintainer: Giuseppe Borzi <gborzi@ieee.org>
# Contributor : Omar Lakhdar <omar_lakhdar@hotmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Joaquim Coimbra <joaca_rj@yahoo.com.br>
# Contributor: Michele Mocciola <mickele>
# Contributor : FoolEcho <Archlinux.fr>
# Thanks to Werner Mayer for his support

pkgname=freecad-git
pkgver=20130217
pkgrel=1
pkgdesc="A general purpose 3D CAD modeler"
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/mediawiki/free-cad/"
license=('GPL')
depends=('boost-libs' 'opencascade' 'pivy-hg' 'python2-pyqt' 'xerces-c' 'libspnav' 'python2-matplotlib')
replaces=('freecad-svn')
makedepends=('boost' 'eigen3' 'gcc-fortran' 'git' 'swig1' 'cmake' 'coin>=3.1.3-9')
options=(!libtool)
source=("${pkgname}.desktop")
md5sums=('7ed9bb582fa7ef80b2d0aff10583b9c9')

_gitname="freecad"
_gitroot="git://free-cad.git.sourceforge.net/gitroot/free-cad/free-cad"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  # fix boost*-mt libs
  sed -i -e 's/-lboost\(.*\)-mt/-lboost\1/' acinclude.m4
  # M_PI instead of PI
  for i in `grep -rl "\<PI\>" src/3rdParty/salomesmesh/*`; do
     sed -i -e 's/\<PI\>/M_&/g' $i
  done
  sed -i -e 's/\<PI\>/M_&/g' src/Mod/Part/App/TopoShape.cpp
  sed -i -e 's/\<PI\>/M_&/g' src/Mod/Fem/App/AppFemPy.cpp
  # M_PI instead of Standard_PI
  for i in `grep -rl "\<Standard_PI\>" src/*`; do
     sed -i -e 's/Standard_PI/M_PI/g' $i
  done
  # fix OC version check
  sed -i -e '212s/>= 0x060502/< 0x060500/' src/Mod/Part/App/TopoShapeFacePyImp.cpp

  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr/lib/${pkgname} . \
        -DCMAKE_INSTALL_DATADIR:PATH=/usr/share/${pkgname} \
        -DCMAKE_INSTALL_INCLUDEDIR:PATH=/usr/include/${pkgname} \
        -DCMAKE_INSTALL_DOCDIR:PATH=/usr/share/doc/${pkgname} \
        -DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2 \
        -DFREECAD_USE_EXTERNAL_PIVY:BOOL=ON \
        -DOCC_INCLUDE_DIR:PATH=/opt/opencascade/inc \
        -DCMAKE_C_FLAGS:STRING="$CFLAGS -fpermissive" \
        -DCMAKE_CXX_FLAGS:STRING="$CXXFLAGS -fpermissive"

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR=${pkgdir} install
  sed -i -e 's_/usr/bin/python_&2_' "$pkgdir/usr/lib/freecad-git/Mod/Robot/MovieTool.py"
  sed -i -e 's_/usr/bin/env python_&2_' "$pkgdir/usr/lib/freecad-git/Mod/Test/unittestgui.py"

  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -dm755 "${pkgdir}/usr/bin"
  ln -sf /usr/lib/${pkgname}/bin/FreeCAD "${pkgdir}/usr/bin/${pkgname}"
  ln -sf /usr/lib/${pkgname}/bin/FreeCADCmd "${pkgdir}/usr/bin/freecadcmd-git"

  # Fix hard boost dep
  BOOSTVER=`pacman -Q boost|sed -e 's_-.*__' -e 's_boost __'`
  cd "${pkgdir}/usr/lib/${pkgname}/lib"
  for i in _program_options _signals _regex _filesystem _system; do
     ln -s /usr/lib/libboost$i.so libboost$i.so.$BOOSTVER
  done
}
