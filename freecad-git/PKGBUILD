# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=freecad-git
pkgver=0.r2740.gae90c15
pkgrel=1
pkgdesc="A general purpose 3D CAD modeler - git checkout"
arch=('i686' 'x86_64')
url="http://www.freecadweb.org/"
license=('GPL')
depends=('opencascade>=6.6.0' 'boost-libs' 'python2-matplotlib' 'python2-pyqt4' 'xerces-c' 'freetype2' 'wget' 'ode' 'coin' 'soqt')
makedepends=('boost' 'eigen3' 'gcc-fortran' 'git' 'swig' 'cmake' 'coin>=3.1.3-9' 'zlib' 'libspnav')
optdepends=('python2-matplotlib' 'pycollada-git' 'pivy-hg' 'libspnav' 'gts')
provides=('freecad')
conflicts=('freecad')
install=freecad.install
source=('freecad::git://git.code.sf.net/p/free-cad/code'
	'freecad.desktop'
	'freecad.xml')

pkgver() {
	cd freecad/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd freecad/

	mkdir -p build
	cd build/

	cmake -DCMAKE_INSTALL_PREFIX:PATH=/opt/freecad .. \
		-DOCC_INCLUDE_DIR:PATH=/opt/opencascade/inc/ \
		-DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2 \
		-DFREECAD_USE_EXTERNAL_PIVY:BOOL=ON

      make
}

package() {
	cd freecad/build/

	make DESTDIR="${pkgdir}" install

	install -d -m0755 "${pkgdir}/usr/bin"
	ln -sf /opt/freecad/bin/FreeCAD "${pkgdir}/usr/bin/freecad"
	ln -sf /opt/freecad/bin/FreeCADCmd "${pkgdir}/usr/bin/freecadcmd"

	desktop-file-install \
		--dir="${pkgdir}/usr/share/applications" \
		"${srcdir}/freecad.desktop"

	install -D -m644 "${srcdir}/freecad.xml" "${pkgdir}/usr/share/mime/packages/freecad.xml"
}

sha256sums=('SKIP'
            '886688f1f3624fafe92710890bda7504d1e0181c2c5239b313066eb412a78e0f'
            '248918de7d3c2145b5cc4fbbc9e224d22f4a6ca7ead2680e8c3a32e91772482a')
