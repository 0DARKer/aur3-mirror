# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-kiwisolver
pkgver=0.1.2
pkgrel=1
pkgdesc="A fast implementation of the Cassowary constraint solver"
arch=('i686' 'x86_64')
url="https://github.com/nucleic/kiwi"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
options=(!emptydirs)

source=("https://github.com/nucleic/kiwi/archive/$pkgver.tar.gz")
md5sums=('1a2f5f850e7c6be578e1ed974aef6442')

build() {
  cd "$srcdir/kiwi-$pkgver/py"

  rm -rf kiwi
  rm -rf build
  cp -rf ../kiwi .
  cp ../README.rst .
  cp ../COPYING.txt .
  cp ../releasenotes.rst .

  python2 setup.py build
}

package() {
  cd "$srcdir/kiwi-$pkgver/py"

  python2 setup.py install --root="$pkgdir"/ --optimize=1
}

