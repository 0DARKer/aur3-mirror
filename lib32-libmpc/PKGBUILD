# Contributor: Vadym Abramchuk <abramm@gmail.com>

pkgname=lib32-libmpc
pkgver=0.9
pkgrel=6
pkgdesc="Library for the arithmetic of complex numbers with arbitrarily high precision (32-bit)"
arch=('x86_64')
url="http://www.multiprecision.org/"
groups=('lib32')
license=('LGPL')
depends=('libmpc' 'lib32-mpfr>=3.0.0')
options=('!libtool')
install=libmpc.install
source=(http://www.multiprecision.org/mpc/download/mpc-${pkgver/_/-}.tar.gz
        libmpc-0.9-configure_cflags_egrep_issue.patch)
md5sums=('0d6acab8d214bd7d1fbbc593e83dd00d'
         '35d5bb02dc6c1153e581b7c34a738a08')

build() {
  cd "${srcdir}/mpc-${pkgver}"

  # http://lists.gforge.inria.fr/pipermail/mpc-discuss/2011-February/000805.html
  patch -Np1 -i $srcdir/libmpc-0.9-configure_cflags_egrep_issue.patch
  autoreconf
     
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export LDFLAGS="-m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export ABI=32
   
  ./configure --prefix=/usr --libdir='/usr/lib32' --with-mpfr-lib='/usr/lib32/'\
  --with-gmp-lib='/usr/lib32'

  make
}

check() {
  cd "${srcdir}/mpc-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/mpc-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{share,include}
}
