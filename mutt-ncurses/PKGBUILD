# Maintainer: SpepS <archspepser at yahoo dot it>
# Contributor Ismael Carnales <icarnales@gmail.com>

_pkgbasename=mutt
pkgname=$_pkgbasename-ncurses
pkgver=1.5.21
pkgrel=1
pkgdesc="A small but very powerful text-based mail client, ncurses version."
arch=('i686' 'x86_64')
url="http://www.mutt.org/"
license=('GPL')
depends=('mime-types' 'gpgme')
makedepends=('gnupg')
provides=("$_pkgbasename")
conflicts=("$_pkgbasename")
install="${_pkgbasename}.install"
source=("ftp://ftp.mutt.org/mutt/devel/${_pkgbasename}-${pkgver}.tar.gz")
md5sums=('a29db8f1d51e2f10c070bf88e8a553fd')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  ./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--enable-pop \
	--enable-imap \
	--enable-smtp \
	--enable-pgp \
	--enable-gpgme \
	--enable-hcache \
	--without-idn \
	--with-regex \
	--with-sasl \
	--with-ssl=/usr \
	--with-curses=/usr

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Remove unneeded bug filer scripts
  rm -f \
	"${pkgdir}"/usr/bin/{flea,muttbug} \
	"${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1 \
	"${pkgdir}"/etc/mime.types*

  install -Dm644 contrib/gpg.rc ${pkgdir}/etc/Muttrc.gpg.dist
}
