# Maintainer: boenki <boenki at gmx dot de>

pkgname=postler-git
pkgver=20110107
pkgrel=1
pkgdesc="A lean mail interface"
arch=('i686' 'x86_64')
url="http://git.xfce.org/apps/postler"
license=('LGPL2.1')
depends=('libwebkit' 'libunique' 'libcanberra' 'libnotify' 'libindicate')
makedepends=('git' 'vala' 'intltool' 'librsvg' 'gettext' 'python2' 'msmtp')
optdepends=('dexter: provides contact completion'
            'lynx: enables HTML to text conversion')
install="$pkgname.install"

_gitroot=$url
_gitname="postler"

build() {
  cd $srcdir
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    msg "Updating local files..."
    git --git-dir=$_gitname/.git pull -q
    msg "The local files are now up-to-date."
  else
    msg "Cloning into postler..."
    git clone -q $_gitroot $_gitname
    msg "GIT checkout done."
  fi

  msg "Cloning into postler-build..."
  rm -rf $_gitname-build
  git clone -q $_gitname $_gitname-build

  cd $_gitname-build

  msg "Patching the buildscript to use python2..."
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|g" waf

  msg "Configuring..."
  ./waf configure --prefix=/usr

  msg "Building..."
  ./waf build
}

package() {
  cd $srcdir/$_gitname-build
  ./waf --destdir=$pkgdir install
}