# Contributor: Anton Leontiev <bunder /at/ t-25.ru>
pkgname=perl-pdl-git
pkgver=20111114
pkgrel=1
pkgdesc="The Perl Data Language, a perl extension designed for scientific and bulk numeric data processing and display"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/PDL"
license=("PerlArtistic" "GPL")
depends=("perl>=5.8.1" "perl-inline>=0.43" "gsl")
makedepends=("fftw2" "hdf4" "perl-opengl>=0.63" "plplot" "proj" "perl-extutils-f77")
optdepends=(
	"fftw2: for PDL::FFTW support"
	"hdf4: for HDF files support"
	"perl-astro-fits-header: improved FITS files support"
#	"perl-convert-uu: for the case when something wrong with Unicode support"  # It is necessary on *BSD systems
#	"perl-extutils-f77: for PDL::Slatec and PDL::Minuit support"               # It is not necessary at runtime
	"perl-opengl>=0.63: for PDL::Graphics::TriD support"
	"plplot: for PDL::Graphics::PLplot support"
	"proj: for PDL::GIS::Proj and PDL::Transform::Proj4 support")
conflicts=("perl-pdl")
provides=("perl-pdl")
options=(!emptydirs)

_gitroot=git://pdl.git.sourceforge.net/gitroot/pdl/pdl
_gitname=pdl

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}


check() {
	cd "$srcdir/$_gitname-build"
	make test
}

package() {
  cd "$srcdir/$_gitname-build"
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

