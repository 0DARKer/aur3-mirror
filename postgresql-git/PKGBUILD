# Maintainer: Marti Raudsepp <marti@juffo.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=postgresql-git
pkgver=20101109
pkgrel=1
pkgdesc="git build of the PostgreSQL database (includes both server and libs)"
arch=(i686 x86_64)
license=('custom:PostgreSQL')
backup=('etc/conf.d/postgresql' 'etc/pam.d/postgresql')
url="http://www.postgresql.org/developer/alpha"
options=(!strip) # to facilitate debugging of testing builds
depends=('libxml2' 'readline>=6.0' 'openssl' 'bash')
makedepends=('python2' 'perl')
conflicts=('postgresql-libs' 'postgresql' 'postgresql-testing')
provides=("postgresql-libs=9.1alpha2" "postgresql=9.1alpha2")
source=(postgresql.rc postgresql.confd postgresql.pam)
md5sums=('86e0161343ce80e544aba999f284bc88'
         'df6ddf9e2ab4700a3415f17c0f4f4172'
         '96f82c38f3f540b53f3e5144900acf17')

# Slow git server is slow, use Github mirror instead
#_gitroot="git://git.postgresql.org/git/postgresql.git"
_gitroot="git://github.com/postgres/postgres.git"
_gitname="postgresql"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./configure --prefix=/usr --mandir=/usr/share/man --with-libxml \
	--with-openssl --with-perl \
	--with-python PYTHON=/usr/bin/python2 --with-pam \
	--with-system-tzdata=/usr/share/zoneinfo --enable-nls \
	--datadir=/usr/share/postgresql --enable-thread-safety

  make
  make DESTDIR=$pkgdir install

  # build all contrib modules
  make -C contrib
  make -C contrib DESTDIR=$pkgdir install

  install -D -m755 $srcdir/postgresql.rc $pkgdir/etc/rc.d/postgresql
  install -D -m644 COPYRIGHT $pkgdir/usr/share/licenses/postgresql/LICENSE
  install -D -m644 $srcdir/postgresql.confd $pkgdir/etc/conf.d/postgresql
  install -D -m644 $srcdir/postgresql.pam $pkgdir/etc/pam.d/postgresql
}
