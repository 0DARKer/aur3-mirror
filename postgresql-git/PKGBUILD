# Maintainer: Marti Raudsepp <marti@juffo.org>

pkgname=postgresql-git
pkgver=20120918
pkgrel=1
_ver=9.3
pkgdesc="Beta version of the PostgreSQL database (includes both server and libs)"
arch=(i686 x86_64)
license=('custom:PostgreSQL')
backup=('etc/conf.d/postgresql' 'etc/pam.d/postgresql')
url="http://www.postgresql.org/developer/alpha"
options=(!strip) # to facilitate debugging of testing builds
depends=('libxml2' 'readline' 'openssl')
makedepends=('python2' 'perl')
optdepends=('python2: PL/Python procedure support')
conflicts=('postgresql-libs' 'postgresql' 'postgresql-testing')
provides=("postgresql-libs=$_ver" "postgresql=$_ver")
source=(postgresql.rcd postgresql.confd postgresql.pam postgresql.logrotate
		postgresql.service postgresql-initdb)
install=postgresql.install

# Slow git server is slow, use Github mirror instead
#_gitroot="git://git.postgresql.org/git/postgresql.git"
_gitroot="git://github.com/postgres/postgres.git"
_gitname="postgresql"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./configure --prefix=/usr \
  --mandir=/usr/share/man \
  --datadir=/usr/share/postgresql \
  --with-libxml \
  --with-openssl \
  --with-perl \
  --with-python PYTHON=/usr/bin/python2 \
  --with-pam \
  --with-system-tzdata=/usr/share/zoneinfo \
  --enable-nls \
  --enable-thread-safety
  #--with-krb5 \
  #--with-tcl \

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR=$pkgdir install

  # build all contrib modules
  make -C contrib
  make -C contrib DESTDIR=$pkgdir install

  # license
  install -D -m644 COPYRIGHT $pkgdir/usr/share/licenses/$pkgname/LICENSE

  # install launch script
  install -D -m755 $srcdir/postgresql.rcd $pkgdir/etc/rc.d/postgresql
  install -D -m644 $srcdir/postgresql.service $pkgdir/usr/lib/systemd/system/postgresql.service
  install -D -m755 $srcdir/postgresql-initdb $pkgdir/usr/lib/systemd/scripts/postgresql-initdb

  # install conf files
  install -D -m644 $srcdir/postgresql.confd $pkgdir/etc/conf.d/postgresql
  install -D -m644 $srcdir/postgresql.pam $pkgdir/etc/pam.d/postgresql
  install -D -m644 $srcdir/postgresql.logrotate $pkgdir/etc/logrotate.d/postgresql
}
md5sums=('1ddd1df8010549f237e7983bb326025e'
         'a54d09a20ab1672adf08f037df188d53'
         '96f82c38f3f540b53f3e5144900acf17'
         'd28e443f9f65a5712c52018b84e27137'
         '55732c5b05ee3b2977606fd55ff4d082'
         '1cb3132acb0ca61d87a0d6ab9a6dbda2')
