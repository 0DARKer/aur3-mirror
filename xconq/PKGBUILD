# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: roberto <roberto@archlinux.org>

pkgname=xconq
pkgver=7.5.0pre20050612
pkgrel=1
pkgdesc="An open source engine for turn-based strategy games"
arch=('i686' 'x86_64')
url="http://sourceware.org/xconq/"
license=('GPL')
depends=('gcc-libs' 'sdl' 'tk' 'libxaw')
makedepends=('perl' 'wget')
install="${pkgname}.install"

build() {
  cd "${srcdir}"

  # Download the tarball with wget, because it always has a different md5
  wget "http://${pkgname}.cvs.sourceforge.net/viewvc/${pkgname}/${pkgname}.tar.gz"
  tar xfz "${pkgname}.tar.gz"
  cd "${pkgname}"

  # Configure
  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-alternate-gameuser=root \
              --enable-alternate-gamegroup=root \
              --enable-alternate-x11-app-defaults-dir=/usr/share/X11/app-defaults || return 1

  # Replace space characters on tabs in Makefiles
  sed -i "s/\s\{8\}/\t/" {curses,images,kernel,lib,sdl,tcltk,x11}/Makefile

  # Compile xconq with Tcl/Tk, SDL and curses interfaces
  make || return 1
  make all-sdlconq || return 1
  make all-cconq || return 1
}

package() {
  cd "${srcdir}/${pkgname}"

  # Install game files
  mkdir -p "${pkgdir}/var/lib/${pkgname}"
  make prefix="${pkgdir}/usr" \
       mandir="${pkgdir}/usr/share/man" \
       infodir="${pkgdir}/usr/share/info" \
       scoresdir="${pkgdir}/var/lib" \
       install install-sdlconq install-cconq || return 1

  # Fix some issues
  chgrp games "${pkgdir}/var/lib/${pkgname}"
  chmod 775 "${pkgdir}/var/lib/${pkgname}"
  chmod 755 "${pkgdir}/usr/bin/"{c,x,sdl}conq
  rmdir "${pkgdir}/usr/share/info"

  # Install manuals
  cd doc
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  perl texi2html -split_section xconq.texi
  install -m644 xconq*.html "${pkgdir}/usr/share/doc/${pkgname}"
}
