# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena
pkgver=0.2.56
pkgrel=1
pkgdesc="Milena is a Polish speech synthesizer for Linux"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=('milena-data' 'mbrola-voices-pl1' 'enca' 'antiword' 'odt2txt' 'poppler' 'libao' 'sox')
optdepends=('milena-basewords: for milena_book'
            'lame: for milena_book'
            'vorbis-tools: for milena_book'
            'faac: for milena_book'
            'amrwb: for milena_nokia'
            'ImageMagick: for milena_nokia')
source=(http://tts.polip.com/files/$pkgname-dist-$pkgver.tar.gz
        10-makefile-fix.patch
        20-build_without_data_archive.patch
        40-archlinux_package.patch)
install=milena.install
sha256sums=('1a23bc4ee55054bdd1675f592d0de36983cafa3d0cc926b18ac2ef9573e4f1b5'
            '691f506f2dedfebc4b4619ec445ef6cbdcdb35844bc1c38aca44ba667901e362'
            'e98b6b1e8d5105ddf04877b85488570573e83508f3d6f3b863a8a56443f18401'
            'f866b4ceeb34ccf12fc572133aa2c6150debc3231167c733279eddc5e44cf063')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  for (( i=0; i < ${#source[@]}; i++ )); do
    if [ "${source[i]##*.}" = "patch" ]; then
      msg2 "Applying: ${source[i]##*/}"
      patch -Np1 -i "${srcdir}/${source[i]##*/}"
    fi
  done

  msg2 "Compiling source.."

  make prefix="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" prefix="/usr" install
}

# vim:set ts=2 sw=2 et:
