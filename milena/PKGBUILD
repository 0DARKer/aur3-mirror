# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena
pkgver=0.2.55
pkgrel=1
pkgdesc="Milena is a Polish speech synthesizer for Linux"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=('milena-data' 'mbrola-voices-pl1' 'enca' 'antiword' 'odt2txt' 'poppler' 'libao' 'sox')
optdepends=('milena-basewords: for milena_book'
            'lame: for milena_book'
            'vorbis-tools: for milena_book'
            'faac: for milena_book'
            'amrwb: for milena_nokia'
            'ImageMagick: for milena_nokia')
source=(http://tts.polip.com/files/$pkgname-dist-$pkgver.tar.gz
        10-makefile-fix.patch
        20-build_without_data_archive.patch
        40-archlinux_package.patch)
install=milena.install
md5sums=('0da36f213d48e6e316b50959e86204b0'
         'a25f53a1f1e1b059a04331cbac684af2'
         'c53db59fdd039c67e0f85f8ac60cafe6'
         '9c73a7dba78623edd0255393b586aa49')
sha256sums=('96c6488cd7a4390cc8eeb02713ad4a50a0b752b11670b281401a1a16a5b500e6'
            '691f506f2dedfebc4b4619ec445ef6cbdcdb35844bc1c38aca44ba667901e362'
            'e98b6b1e8d5105ddf04877b85488570573e83508f3d6f3b863a8a56443f18401'
            'f866b4ceeb34ccf12fc572133aa2c6150debc3231167c733279eddc5e44cf063')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  for (( i=0; i < ${#source[@]}; i++ )); do
    if [ "${source[i]##*.}" = "patch" ]; then
      msg2 "Applying: ${source[i]##*/}"
      patch -Np1 -i "${srcdir}/${source[i]##*/}"
    fi
  done

  msg2 "Compiling source.."

  make prefix="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" prefix="/usr" install
}

# vim:set ts=2 sw=2 et:
