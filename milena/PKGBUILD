# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena
pkgver=0.2.53
pkgrel=1
pkgdesc="Milena is a Polish speech synthesizer for Linux"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=('milena-data' 'mbrola-voices-pl1' 'enca' 'antiword' 'odt2txt' 'poppler' 'libao' 'sox')
optdepends=('milena-basewords: for milena_book'
            'lame: for milena_book'
            'vorbis-tools: for milena_book'
            'faac: for milena_book'
            'amrwb: for milena_nokia'
            'ImageMagick: for milena_nokia')
source=(http://tts.polip.com/files/$pkgname-dist-$pkgver.tar.gz
        10-makefile-fix.patch
        20-build_without_data_archive.patch
        40-archlinux.patch)
install=milena.install
sha256sums=('6d4d2da821bd7612ddf76a408d613e393d547bc1a1e3ce446fbcf8071a963885'
            'd90fcabcb84fc91f3fba9f7ca4b18c025ffc61656d0f0ee754d8c14293052e4d'
            '3809875be5c69e5ecf2ad39a2f70ca7b627db09085880e01bbc0482045e75e86'
            '2de9e21293ac13e4134aeb3d699c962791bfa561979ad2008f4f98077deeee55')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  for (( i=0; i < ${#source[@]}; i++ )); do
    test "${source[i]}" = "${source[i]%.patch}" \
      || patch -Np1 -i "${srcdir}/${source[i]##*/}"
  done

  make prefix="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" prefix="/usr" install
}

# vim:set ts=2 sw=2 et:
