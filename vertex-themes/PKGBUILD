# Maintainer: CanalGuada <dguadal at free dot fr>
# Author: horst3180 @ deviantart

pkgname=vertex-themes
pkgver=20150410
pkgrel=1
pkgdesc='Vertex Gtk2, Gtk3, Metacity, Xfwm, Cinnamon and GNOME Shell themes (GNOME 3.16 version)'
_gnomever=3.16
_releasever=20150410
arch=('any')
url='http://horst3180.deviantart.com/art/Vertex-Theme-470663601'
license=('GPL3')
depends=('gtk-engine-murrine')
provides=('vertex-themes')
conflicts=('vertex-themes-git')
source=("${pkgname}-${_releasever}.tar.gz::https://github.com/horst3180/Vertex-theme/archive/${_releasever}.tar.gz")
sha256sums=('fa7c2d59544de7bd2794ba7ebaa19d98df7f74a08e0d88a4cf332cf9aadbc158')

build() {
  cd Vertex-theme-${_releasever}

  sed -i '228,229d' ./build.sh
  chmod +x ./build.sh
  ./build.sh
}
package() {
  cd Vertex-theme-${_releasever}
  _theme="Vertex-Gnome-Shell"
  [[ "x${_gnomever}" == "x3.16" ]] && _theme="${_theme}-3.16"

  for theme in ${_theme} "Vertex_alt_metacity" "Vertex-Cinnamon"; do
    find "$theme" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/{}" \;
  done

  _path="Gnome"
  case "${_gnomever}" in
    3.10)
      _path="${_path}-3.10_Ubuntu-14.04"
      ;;
    3.12)
      _path="${_path}-3.12_Ubuntu-14.10"
      ;;
    3.14)
      _path="${_path}-3.14_Ubuntu-15.04"
      ;;
    3.16)
      _path="${_path}-3.16"
      ;;
  esac
  cd ${_path}
  for theme in "Vertex" "Vertex-Dark" "Vertex-Light"; do
    find "$theme" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/{}" \;
  done

  cd ../Chrome
  mv Vertex-light.crx Vertex-Light.crx
  for theme in "Vertex" "Vertex-Light"; do
    mkdir -p "$pkgdir/usr/share/themes/$theme/Chrome/" 
    cp -dr --no-preserve='ownership' "$theme.crx" "$pkgdir/usr/share/themes/$theme/Chrome/"
  done

  cd ../Firefox
  for theme in "Vertex" "Vertex-Dark" "Vertex-Light"; do
    cd "$theme"
    find "chrome" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/$theme/Firefox/{}" \;
    cd -
  done

  cd ..
  cp -dr --no-preserve='ownership' {LICENSE,README} "$pkgdir"/usr/share/themes/Vertex/
}

# vim: ts=2 sw=2 et:
