# Maintainer: Vinícius dos Santos Oliveira <vini.ipsmaker@gmail.com>
pkgname=tufao-plugin
pkgver=0.2.5
pkgrel=2
pkgdesc="A QtCreator plugin to work with Tufão."
arch=('i686' 'x86_64')
url="https://github.com/vinipsmaker/tufao-qtcreatorplugin"
license=('LGPL3')

_qtcreatorversion='3.0.1'

depends=("qtcreator=${_qtcreatorversion}")
makedepends=(
  # For qtcreator
  'qt5-quick1'
  'qt5-tools')
source=("http://download.qt-project.org/official_releases/qtcreator/$(echo ${_qtcreatorversion} | grep -Eoe '[[:digit:]]+\.[[:digit:]]+')/${_qtcreatorversion}/qt-creator-opensource-src-${_qtcreatorversion}.tar.gz"
        "https://github.com/vinipsmaker/tufao-qtcreatorplugin/archive/${pkgver}.tar.gz")
md5sums=('1d736f4b3c25c25cf070c400a92e50c4'
         'bd8adbce49698a65bd4f208379c9f1f7')

prepare() {
  cd "$srcdir"
  [ -d tufao ] || mv "tufao-qtcreatorplugin-${pkgver}" tufao
}

build() {
  if [ -z "$QTC_BUILD" ]; then
    if [ -z "$QTC_SOURCE" ]; then
      export QTC_SOURCE="$srcdir/qt-creator-opensource-src-${_qtcreatorversion}"
    fi
    cd "$QTC_SOURCE"

    export QTC_BUILD="${QTC_SOURCE}/build"

    mkdir -p "$QTC_BUILD"
    cd "$QTC_BUILD"
    qmake "$QTC_SOURCE/qtcreator.pro"
    make
  fi

  cd "$srcdir/tufao"
  qmake
  make
}

package() {
  if [ -z "$QTC_BUILD" ]; then
    if [ -z "$QTC_SOURCE" ]; then
      export QTC_SOURCE="$srcdir/qt-creator-opensource-src-${_qtcreatorversion}"
    fi

    export QTC_BUILD="$QTC_SOURCE/build"
  fi

  cd "${QTC_BUILD}/lib/qtcreator/plugins"
  mkdir -p "$pkgdir/usr/lib/qtcreator/plugins"
  cp -r TufaoTeam "$pkgdir/usr/lib/qtcreator/plugins"
}
