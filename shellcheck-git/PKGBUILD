# Maintainer: Josh McGee <josh.s.mcgee@gmail.com>
# Contributor: Earnestly <zibeon@gmail.com>

pkgname=shellcheck-git
pkgver=231.e84d5ab
pkgrel=1
pkgdesc="A shell script static analysis tool"
arch=(i686 x86_64)
url=http://www.vidarholen.net/contents/shellcheck
license=(AGPL3)
makedepends=(git ghc haskell-regex-compat haskell-parsec haskell-quickcheck)
checkdepends=(haskell-quickcheck)
provides=(shellcheck)
conflicts=(shellcheck)
source=("$pkgname::git+https://github.com/koalaman/shellcheck.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf '%s.%s\n' "$(git rev-list --count HEAD)" \
                   "$(git rev-parse --short HEAD)"
}

build() {
  # QuickCheck is not useful after compilation
  (cd "$srcdir/$pkgname"; ./test/runQuack)
  cd "$pkgname"
  ghc -O9 -fforce-recomp --make shellcheck.hs
}

package() {
  cd "$pkgname"
  install -Dm755 shellcheck "$pkgdir/usr/bin/${pkgname%-git}"
}

# vim:set ts=2 sw=2 et:
