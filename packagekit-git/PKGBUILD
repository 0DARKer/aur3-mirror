#! /bin/bash
# Maintainer: Jorge Barroso <jorge.barroso.11 at gmail dot com>
# Contributor: onestep_ua <onestep@ukr.net>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: lh <jarryson#gmail.com>
# Based on packagekit PKGBUILD by Jonathan Conder <jonno.conder@gmail.com>

pkgname=packagekit-git
pkgver=PACKAGEKIT_0_9_3.r5.g534d89b
pkgrel=1
pkgdesc="A system designed to make installing and updating software on your computer easier"
arch=('i686' 'x86_64')
backup=('var/lib/PackageKit/transactions.db'
        'etc/PackageKit/alpm.d/pacman.conf'
        'etc/PackageKit/alpm.d/repos.list')
url="http://packagekit.org/"
license=('GPL')
depends=('dbus-glib' 'polkit' 'pacman' 'shared-mime-info' 'sqlite' 'udev' 'bash-completion')
makedepends=('dbus-glib' 'gobject-introspection' 'gtk-doc' 'intltool'
             'networkmanager' 'pacman'  'pm-utils' 'polkit' 'python2' 'qt4'
             'shared-mime-info' 'sqlite' 'udev')
optdepends=('networkmanager: Network management daemon')
source=(git+https://git.gitorious.org/packagekit/packagekit.git)
install='packagekit.install'
options=('!libtool')
provides=('packagekit')
sha512sums=('SKIP')

_gitname="packagekit"

pkgver() {
  cd ${_gitname}
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd ${_gitname}
  sed -i 's@bin/python@bin/python2@' 'lib/python/packagekit/'*.py
  
 export PYTHON=/usr/bin/python2
  ./autogen.sh --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libexecdir=/usr/lib/PackageKit \
              --enable-strict \
              --disable-static \
              --disable-gtk-doc \
              --disable-tests \
              --disable-local \
              --disable-browser-plugin \
              --disable-gstreamer-plugin \
              --disable-gtk-module \
              --disable-command-not-found \
              --disable-cron \
              --disable-debuginfo-install \
              --enable-pm-utils \
              --disable-dummy \
              --enable-alpm \
              --with-default-backend=alpm
  make -s CFLAGS='-D_FILE_OFFSET_BITS=64'
}

package() {
  cd ${_gitname}

  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/include/PackageKit/packagekit-qt"*
  rm -rf "$pkgdir/usr/lib/libpackagekit-qt"*
  rm -rf "$pkgdir/usr/lib/pkgconfig/packagekit-qt"*
  rm -rf "$pkgdir/usr/lib/cmake"

  rm -rf "$pkgdir/usr/lib/python"*
  rm -rf "$pkgdir/usr/share/PackageKit/website"

  mkdir -p "$pkgdir/usr/share/bash-completion/completions"
  mv "$pkgdir/etc/bash_completion.d/pk-completion.bash" \
     "$pkgdir/usr/share/bash-completion/completions/pkcon"
  rmdir "$pkgdir/etc/bash_completion.d"
  touch "$pkgdir/var/log/PackageKit"
}

# vim:set ts=2 sw=2 et:
