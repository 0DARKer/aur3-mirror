# Maintainer:  TDY <tdy@gmx.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=terminator-bzr
pkgver=1352
pkgrel=1
pkgdesc="A tool for arranging multiple terminals in a single window"
arch=('i686' 'x86_64')
url="http://www.tenshu.net/terminator/"
license=('GPL')
depends=('hicolor-icon-theme' 'pygtk' 'python2-keybinder2' 'python2-notify'
         'vte' 'xdg-utils')
makedepends=('bzr' 'desktop-file-utils' 'gettext' 'intltool')
optdepends=('gnome-python: gnome-terminal profile support')
provides=('terminator')
conflicts=('terminator')
install=terminator.install
source=($pkgname::bzr+http://bazaar.launchpad.net/~gnome-terminator/terminator/trunk)
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s" "$(bzr revno)"
}

build() {
  cd "$srcdir/$pkgname"
  find -type f -name '*.py' -exec sed -i '1s,python,&2,' '{}' \;
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname"
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  rm -f "$pkgdir/usr/share/icons/hicolor/icon-theme.cache"
}

# vim:set ts=2 sw=2 et:
