# Maintainer:  TDY <tdy@gmx.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=terminator-bzr
pkgver=1352
pkgrel=1
pkgdesc="A tool for arranging multiple terminals in a single window"
arch=('i686' 'x86_64')
url="http://www.tenshu.net/terminator/"
license=('GPL')
depends=('hicolor-icon-theme' 'pygtk' 'python2-keybinder2' 'python2-notify'
         'vte' 'xdg-utils')
makedepends=('bzr' 'desktop-file-utils' 'gettext' 'intltool')
optdepends=('gnome-python: gnome-terminal profile support')
provides=('terminator=0.13')
conflicts=('terminator')
install=terminator.install

_bzrtrunk=http://bazaar.launchpad.net/~gnome-terminator/terminator/trunk
_bzrmod=terminator

build() {
  cd "$srcdir"

  msg "Connecting to $_bzrtrunk..."
  if [[ -d $_bzrmod ]]; then
    cd "$_bzrmod" && bzr --no-plugins pull "$_bzrtrunk" -r "$pkgver"
    msg2 "Local files updated"
  else
    bzr --no-plugins branch "$_bzrtrunk" "$_bzrmod" -q -r "$pkgver"
    msg2 "BZR checkout done"
  fi

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  msg "Starting make..."
  find -type f -name '*.py' -exec sed -i '1s,python,&2,' '{}' \;
  python2 setup.py build
}

package() {
  cd "$srcdir/$_bzrmod-build"
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  rm -f "$pkgdir/usr/share/icons/hicolor/icon-theme.cache"
}
