# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=terminator-bzr
pkgver=1520
pkgrel=1
pkgdesc="A tool for arranging multiple terminals in a single window"
arch=('i686' 'x86_64')
url="http://gnometerminator.blogspot.com/"
license=('GPL')
depends=('hicolor-icon-theme' 'pygtk' 'python2-keybinder2' 'python2-notify'
         'vte' 'xdg-utils')
makedepends=('bzr' 'desktop-file-utils' 'gettext' 'intltool')
optdepends=('gnome-python: gnome-terminal profile support')
provides=('terminator=0.13')
conflicts=('terminator')
install=terminator.install
source=(terminator::bzr+http://bazaar.launchpad.net/~gnome-terminator/terminator/trunk)
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname/-*}
  bzr revno
}

build() {
  cd ${pkgname/-*}
  find -type f -name '*.py' -exec sed -i '1s,python,&2,' '{}' \;
  python2 setup.py build
}

package() {
  cd ${pkgname/-*}
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  rm -f "$pkgdir/usr/share/icons/hicolor/icon-theme.cache"
}
