# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-enaml
pkgver=0.6.8
pkgrel=1
pkgdesc="Enaml is not a Markup Language"
arch=('any')
url="https://github.com/enthought/enaml"
license=('BSD')
depends=('python2-traits' 'python2-casuarius' 'python2-ply')
optdepends=('python2-pyside: only if using the Qt backend'
            'wxpython: only if using the Wx backend')
makedepends=('python2-distribute' 'cython2')
options=(!emptydirs)

source=("https://github.com/enthought/enaml/archive/${pkgver}.tar.gz")
md5sums=('49ed97629e0cdb3be31f6e1a3d472193')

build() {
  cd "$srcdir"/enaml-$pkgver

  python2 setup.py build
}

package() {
  cd "$srcdir"/enaml-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

