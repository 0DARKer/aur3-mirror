# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-enaml
pkgver=0.6.3
_githubtag=14c95b6
pkgrel=1
pkgdesc="Enaml is not a Markup Language"
arch=('any')
url="https://github.com/enthought/enaml"
license=('BSD')
depends=('python2-traits' 'python2-casuarius' 'python2-ply')
optdepends=('python2-pyside: only if using the Qt backend'
            'wxpython: only if using the Wx backend')
makedepends=('python2-distribute' 'cython2')
options=(!emptydirs)

source=("https://github.com/enthought/enaml/tarball/${pkgver}")
md5sums=('47c214a564821765428823d83443d168')

build() {
  cd "$srcdir/enthought-enaml-${_githubtag}"

  python2 setup.py build
}

package() {
  cd "$srcdir/enthought-enaml-${_githubtag}"

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

