# Contributor: Jan Cholasta <grubber@grubber.cz>
# Maintainer: Jan Cholasta <grubber@grubber.cz>

pkgname=gzdoom-svn
pkgver=860
pkgrel=1
pkgdesc="Doom source port based on ZDoom with an OpenGL renderer."
arch=('i686' 'x86_64')
url="http://grafzahl.drdteam.org/"
license=('custom')
depends=('bzip2' 'gtk2' 'sdl' 'mesa')
makedepends=('nasm' 'cmake' 'subversion')
optdepends=('doom1-wad: Doom shareware IWAD (game data)'
            'harmony-wad: Harmony IWAD (game data)'
            'heretic1-wad: Heretic shareware IWAD (game data)'
            'hexen1-wad: Hexen 1 Demo IWAD (game data)'
            'strife0-wad: Strife shareware IWAD (game data)'
            'urbanbrawl-wad: Urban Brawl: Action Doom 2 IWAD (game data)')
provides=('gzdoom')
conflicts=('gzdoom')
source=(http://www.fmod.org/index.php/release/version/fmodapi42816linux.tar.gz \
        ${pkgname}-sharedir.patch \
        gzdoom.desktop \
        gzdoom.png)
md5sums=('e6d1a9c8565051a983af7bcc5c4d9cfb'
         '0d0b49ae39e1cc827a1098290378c4f9'
         '3f5920d839086c9ad04ed1338c3fb546'
         '7e1518eeda9bab34b7222a04c690697d')

_svntrunk="http://mancubus.net/svn/hosted/gzdoom/trunk/"
_svnmod="${pkgname}"

_fmodver=4.28.16
_fmod64=

if [ "$CARCH" = "x86_64" ]; then
  makedepends[0]='yasm'
  source[0]=http://www.fmod.org/index.php/release/version/fmodapi42816linux64.tar.gz
  md5sums[0]='b6953f9331c72f9dfa95b31a41aa77b2'
  _fmod64=64
fi

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  patch -p1 < ../${pkgname}-sharedir.patch || return 1

  cp ../fmodapi${_fmodver//./}linux${_fmod64}/api/lib/libfmodex${_fmod64}-${_fmodver}.so libfmodex-${pkgname}.so || return 1

  cmake -DFMOD_INCLUDE_DIR=../fmodapi${_fmodver//./}linux${_fmod64}/api/inc -DFMOD_LIBRARY=libfmodex-${pkgname}.so -DCMAKE_SKIP_RPATH=TRUE . || return 1
  make || return 1
}

package() {
  cd "$srcdir"/${_svnmod}-build

  install -m755 -D gzdoom "$pkgdir"/usr/bin/gzdoom
  install -m644 -D gzdoom.pk3 "$pkgdir"/usr/share/games/gzdoom/gzdoom.pk3
  install -m644 -D docs/BUILDLIC.TXT "$pkgdir"/usr/share/licenses/${pkgname}/BUILDLIC.TXT
  install -m644 -D docs/doomlic.txt "$pkgdir"/usr/share/licenses/${pkgname}/doomlic.txt

  install -m755 -D libfmodex-${pkgname}.so "$pkgdir"/usr/lib/libfmodex-${pkgname}.so

  install -m644 -D ../gzdoom.png "$pkgdir"/usr/share/pixmaps/gzdoom.png
  install -m644 -D ../gzdoom.desktop "$pkgdir"/usr/share/applications/gzdoom.desktop
}
