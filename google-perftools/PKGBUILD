# Maintainer: Schnouki <thomas.jost@gmail.com>
# Contributor: JaDa <jada@archlinux.us>

pkgname=google-perftools
pkgver=1.7
pkgrel=2
pkgdesc="Fast, mutli-threaded malloc() and nifty performance analysis tools by google."
arch=('i686' 'x86_64')
url="http://code.google.com/p/google-perftools/"
license=('BSD')
depends=('perl')
[[ "$CARCH" == "x86_64" ]] && depends=('perl' 'libunwind>=0.99')
optdepends=(
    'graphviz: for pprof graphical output routines'
    'gv: for pprof --gv mode'
)
source=(http://google-perftools.googlecode.com/files/$pkgname-$pkgver.tar.gz
        tcmalloc.patch)
md5sums=('5839cab3723e68a86ed327ebb54d54bc'
         '8446ec55b5ebf46fa4652083c35f2ca6')
sha256sums=('65326e3328a84c97d9de79d3f7dfdda6c78d9310217bfb8ac0a0845759e69b15'
            'de88299f23a2ea702237714ca6d329fcc1a467386847e89e1a9d6fb76071c01f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Patch for compatibility with glibc 2.14 -- see
  # http://code.google.com/p/google-perftools/issues/detail?id=348
  # Adapted from https://gist.github.com/1057210
  patch -p0 < "$srcdir/tcmalloc.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
