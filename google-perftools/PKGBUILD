# Maintainer: Schnouki <thomas.jost@gmail.com>
# Contributor: JaDa <jada@archlinux.us>
# Contributor: Joaquim Pedro (osmano807) <osmano807@gmail.com>

pkgname=google-perftools
pkgver=1.8.3
pkgrel=2
pkgdesc="Fast, mutli-threaded malloc() and nifty performance analysis tools by google."
arch=('i686' 'x86_64')
url="http://code.google.com/p/google-perftools/"
license=('BSD')
depends=('perl')
[[ "$CARCH" == "x86_64" ]] && depends=('perl' 'libunwind>=1.0.1')
optdepends=(
    'graphviz: for pprof graphical output routines'
    'gv: for pprof --gv mode'
)
source=(http://google-perftools.googlecode.com/files/$pkgname-$pkgver.tar.gz)
md5sums=('70c95322c9bac97e67f0162e4cc55996')
sha256sums=('6ad744b34abb24312631740d9912f4667993b02e5f59b91246c31a2a911a9d59')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
