# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=libav-git
pkgver=20110412
pkgrel=1
pkgdesc="FFmpeg fork"
arch=('i686' 'x86_64')
url="http://libav.org/"
license=('GPL')
depends=('alsa-lib' 'bzip2' 'faac' 'lame' 'libtheora' 'libva' 'libvdpau' 'libvorbis' 'libxfixes' 'sdl' 'x264-git' 'zlib')
makedepends=('git' 'yasm')
conflicts=('ffmpeg')
provides=("ffmpeg=$pkgver")
source=()
md5sums=()

_gitroot="git://git.libav.org/libav.git"
_gitname="libav"

build() {
  cd $srcdir
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  
  msg "GIT checkout done"
  msg "Starting make..."
  
  rm -rf $srcdir/$_gitname-build
  git clone $srcdir/$_gitname $srcdir/$_gitname-build
  
  cd $srcdir/$_gitname-build

  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libx264 \
    --enable-nonfree \
    --enable-postproc \
    --enable-x11grab 
 
  make
  make doc/ff{mpeg,play,probe,server}.1

  make DESTDIR="$pkgdir" install install-man
  
  rm -rf $srcdir/$_gitname-build
}
