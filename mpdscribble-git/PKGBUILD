# Maintainer: Somasis <somasissounds@gmail.com>
# Original Maintainer: evr <evanroman at gmail>
# Contributor: KRTac
# Contributor: BjÃ¶rn Pettersson <bjorn@hygiena.org>

pkgname=mpdscribble-git
pkgver=release.0.22.r9.g979e96a
pkgrel=1
pkgdesc="A mpd client which submits information to a scrobbler, such as last.fm."
arch=('i386' 'x86_64' 'armv5h' 'armv6h' 'armv7h')
url="http://mpd.wikia.com/wiki/Client:Mpdscribble"
license=('GPL')
depends=('curl' 'glib2' 'libmpdclient')
makedepends=('gcc' 'git' 'make')
conflicts=('mpdscribble')
provides=('mpdscribble')
install='mpdscribble.install'
source=("git+git://git.musicpd.org/master/mpdscribble.git" "mpdscribble.install")
md5sums=('SKIP' '8349de9fd06cd5d25b8e52446ea96e9a')

pkgver() {
    cd "$srcdir/mpdscribble"
    git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/mpdscribble"
    msg "Starting autogen.sh and configuring..."
    ./autogen.sh --prefix="/usr" --sysconfdir="/etc"
    msg "Starting make..."
    make || return 1
}

package() {
    cd "$srcdir/mpdscribble"
    msg "Starting make install..."
    make DESTDIR="$pkgdir" install || return 1
    if [ -f "$pkgdir/etc/mpdscribble.conf" ];then
        rm "$pkgdir/etc/mpdscribble.conf"
    fi
    install -D -m644 "$srcdir/mpdscribble/doc/mpdscribble.conf" "$pkgdir/etc/mpdscribble.conf.example"
    mkdir -p "$pkgdir/var/cache/mpdscribble"
    touch "$pkgdir/var/cache/mpdscribble/mpdscribble.cache"
}
