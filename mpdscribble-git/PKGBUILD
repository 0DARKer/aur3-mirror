# Maintainer: Somasis <somasissounds@gmail.com>
# Original Maintainer: evr <evanroman at gmail>
# Contributor: KRTac
# Contributor: BjÃ¶rn Pettersson <bjorn@hygiena.org>

pkgname=mpdscribble-git
pkgver=0.22.r9.g979e96a
pkgrel=1
pkgdesc="A mpd client which submits information to a scrobbler, such as last.fm."
arch=('i686' 'x86_64' 'armv5h' 'armv6h' 'armv7h')
url="http://mpd.wikia.com/wiki/Client:Mpdscribble"
license=('GPL')
depends=('curl' 'glib2' 'libmpdclient')
makedepends=('git')
conflicts=('mpdscribble')
provides=('mpdscribble')
install='mpdscribble.install'
source=("git://git.musicpd.org/master/mpdscribble.git" "mpdscribble.install")
md5sums=('SKIP' '8349de9fd06cd5d25b8e52446ea96e9a')

pkgver() {
    cd "$srcdir/mpdscribble"
    git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g;s/release\.//'
}

build() {
    cd "$srcdir/mpdscribble"
    ./autogen.sh --prefix="/usr" --sysconfdir="/etc"
    make
}

package() {
    cd "$srcdir/mpdscribble"
    make DESTDIR="$pkgdir" install
    if [ -f "$pkgdir/etc/mpdscribble.conf" ];then
        rm "$pkgdir/etc/mpdscribble.conf"
    fi
    install -D -m644 "$srcdir/mpdscribble/doc/mpdscribble.conf" "$pkgdir/etc/mpdscribble.conf.example"
    mkdir -p "$pkgdir/var/cache/mpdscribble"
    touch "$pkgdir/var/cache/mpdscribble/mpdscribble.cache"
}
