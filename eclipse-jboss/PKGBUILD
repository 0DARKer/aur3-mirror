#Maintainer: Raimar Buehmann <raimar _at_ buehmann _dot_ de>

pkgname=eclipse-jboss
_mainver=4.2
pkgver=${_mainver}.1.Final
pkgrel=1
pkgdesc='Developer tools for JBoss application server'
arch=('any')
url='http://tools.jboss.org/'
license=('LGPL2')
depends=('eclipse>=4.3.0')
options=('!strip')
source=("http://downloads.sourceforge.net/project/jboss/JBossTools/jbosstools${_mainver}.x/jbosstools-${pkgver}_2014-12-14_19-43-15-B320-updatesite-core.zip")
md5sums=(5f79cdfbf91d39af36a9eccafea92828)

package() {
  # remove features and plug-ins containing sources
  #rm features/*.source_*
  #rm plugins/*.source_*
  # remove gz files
  rm plugins/*.pack.gz
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse
  install -d $_dest
  # extract features (otherwise features are not recognized)
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done
  # copy plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
