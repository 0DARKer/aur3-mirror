#Maintainer: Raimar Buehmann <raimar _at_ buehmann _dot_ de>

pkgname=eclipse-jboss
pkgver=4.1.2.Final
pkgrel=1
pkgdesc='Developer tools for JBoss application server'
arch=('any')
url='http://tools.jboss.org/'
license=('LGPL2')
depends=('eclipse>=4.3.0')
options=('!strip')
source=('http://sourceforge.net/projects/jboss/files/JBossTools/JBossTools4.1.x/jbosstools-Update-4.1.2.Final_2014-03-18_15-46-19-B706.zip')
sha256sums=('070e91e1a31f27421315de675b6719d9b26d7ab86859a2319c46a89a5ba5a81a')

package() {
  # remove features and plug-ins containing sources
  #rm features/*.source_*
  #rm plugins/*.source_*
  # remove gz files
  rm plugins/*.pack.gz
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse
  install -d $_dest
  # extract features (otherwise features are not recognized)
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done
  # copy plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
