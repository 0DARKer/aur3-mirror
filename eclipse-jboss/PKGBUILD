#Maintainer: Raimar Buehmann <raimar _at_ buehmann _dot_ de>

pkgname=eclipse-jboss
_mainver=4.2
pkgver=${_mainver}.2.Final
pkgrel=1
pkgdesc='Developer tools for JBoss application server'
arch=('any')
url='http://tools.jboss.org/'
license=('LGPL2')
depends=('eclipse>=4.3.0')
options=('!strip')
source=("jbosstools-${pkgver}-updatesite-core.zip::http://downloads.sourceforge.net/project/jboss/JBossTools/jbosstools${_mainver}.x/jbosstools-${pkgver}_2015-01-13_13-28-49-B326-updatesite-core.zip?r=&ts=1422475726&use_mirror=optimate")
md5sums=('92cf73c6dc5973ba348ba4468d771e76')

package() {
  # remove features and plug-ins containing sources
  #rm features/*.source_*
  #rm plugins/*.source_*
  # remove gz files
  rm plugins/*.pack.gz
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse
  install -d $_dest
  # extract features (otherwise features are not recognized)
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done
  # copy plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
