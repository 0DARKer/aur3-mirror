# Maintainer: Kirill "reflexing" Churin <reflexing@reflexing.ru>
# Contributor: Wido <widowild [at] myopera [dot] com>

pkgname=desura
pkgver=1
pkgrel=12
pkgdesc="a community driven digital distribution service for gamers, putting the best games, mods and downloadable content from developers at gamers fingertips (BETA)"
url="http://www.desura.com/"
license=('custom: "commercial"')
arch=('i686' 'x86_64')
install=${pkgname}.install
options=()
depends=('gtk2' 'libjpeg6' 'libpng12' 'lsb-release' 'gconf')
optdepends=('nvidia-utils: If you have nvidia graphics'
            'catalyst-utils: If you have ATI graphics'
            'glew1.5: if you have install M.A.R.S - a ridiculous shooter'
            'java-runtime: adds java support.'
            'mono: Free implementation of the .NET platform')
[ "$CARCH" = "x86_64" ] &&  optdepends=('lib32-nvidia-utils: If you have nvidia graphics'
                                        'lib32-catalyst-utils: If you have ATI graphics'
                                        'lib32-curl: if you have install TRAUMA '
                                        'lib32-nss: if you have install TRAUMA'
                                        'glew1.5: if you have install M.A.R.S - a ridiculous shooter'
                                        'java-runtime: adds java support.'
                                        'mono: Free implementation of the .NET platform')

source=(http://www.desura.com/desura-${CARCH}.tar.gz ${pkgname}.sh ${pkgname}.desktop)
md5sums=('38a3aa1b69189418e823059072da7739'
         '6f8d2745304da5822762ae84f23e38a2'
         '53660264c03d9df633a6732205b35a96')

[ "$CARCH" = "x86_64" ] && md5sums=('29f124803927c86df2e5bab94ec9e26f'
                                    '6f8d2745304da5822762ae84f23e38a2'
                                    '53660264c03d9df633a6732205b35a96')

package(){
  cd ${srcdir}

  # Change Permissions
  install -dm775 -g users ${pkgdir}/opt/${pkgname}
  install -dm775 -g users ${pkgdir}/opt/${pkgname}/{bin,data,docs,lib}

  # Install Desura
  case $CARCH in
    i686) install -Dm755 ${srcdir}/${pkgname}/${pkgname} \
      ${pkgdir}/opt/${pkgname}/${pkgname};;
    x86_64) install -Dm755 ${srcdir}/${pkgname} \
      ${pkgdir}/opt/${pkgname}/${pkgname};;
  esac

  # Install Launcher
  install -Dm 755 ${srcdir}/${pkgname}.sh \
    ${pkgdir}/usr/bin/${pkgname}.sh

  # Install Desktop
  install -Dm 644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
