# Maintainer: Kirill "reflexing" Churin <reflexing@reflexing.ru>
# Contributor: Wido <widowild [at] myopera [dot] com>

pkgname=desura
pkgver=1
pkgrel=14
pkgdesc="A community-driven digital distribution service for gamers (BETA)"
url="http://desura.com/"
license=('custom: "commercial"')
arch=('i686' 'x86_64')
install=${pkgname}.install
options=()
depends=(lsb-release gconf desktop-file-utils)

if [[ $CARCH == i686 ]]; then
  depends+=(gtk2 libjpeg6 libpng12 libx11 libxpm libxcb libxau libxdmcp)
  optdepends=('nvidia-utils: If you have nvidia graphics'
              'catalyst-utils: If you have ATI graphics'
              'glew1.5: if you have install M.A.R.S - a ridiculous shooter'
              'adobe-air: for Air-based games'
              'java-runtime: adds java support'
              'mono: Free implementation of the .NET platform')

  md5sums=('38a3aa1b69189418e823059072da7739'
           '6f8d2745304da5822762ae84f23e38a2'
           '3725d79a96a318c078a4e9c738f27ac4')
else
  depends+=(lib32-{gtk2,libjpeg6,libpng12,libx11,libxpm,libxcb,libxau,libxdmcp})
  optdepends=('lib32-nvidia-utils: If you have nvidia graphics'
              'lib32-catalyst-utils: If you have ATI graphics'
              'lib32-curl: if you have install TRAUMA '
              'lib32-nss: if you have install TRAUMA'
              'glew1.5: if you have install M.A.R.S - a ridiculous shooter'
              'bin32-adobe-air: for Air-based games'
              'java-runtime: adds java support'
              'mono: Free implementation of the .NET platform')

  md5sums=('3cee8a2935bc1cd61523aaaf23de8e2c'
           '6f8d2745304da5822762ae84f23e38a2'
           '3725d79a96a318c078a4e9c738f27ac4')
fi

source=(http://www.desura.com/desura-${CARCH}.tar.gz ${pkgname}.sh ${pkgname}.desktop)

package(){
  cd ${srcdir}

  # Change permissions
  install -dm775 -g users ${pkgdir}/opt/${pkgname}
  install -dm775 -g users ${pkgdir}/opt/${pkgname}/{bin,data,docs,lib}

  # Install Desura
  install -Dm755 ${srcdir}/${pkgname}/${pkgname} \
    ${pkgdir}/opt/${pkgname}/${pkgname}

  # Install launcher
  install -Dm 755 ${srcdir}/${pkgname}.sh \
    ${pkgdir}/usr/bin/${pkgname}.sh

  # Install desktop file
  desktop-file-install -m 0644 --dir ${pkgdir}/usr/share/applications/ ${pkgname}.desktop
}
