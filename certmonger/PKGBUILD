# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Run tests? They take a bit of time.
run_tests=false

pkgname=certmonger
pkgver=0.61
pkgrel=1
pkgdesc="Certificate status monitor and PKI enrollment client"
arch=('i686' 'x86_64')
url="http://certmonger.fedorahosted.org"
license=('GPL')
depends=('curl' 'dbus' 'libxml2' 'nspr' 'nss' 'openssl' 'systemd' 'talloc' 'tevent' 'xmlrpc-c')
checkdepends=('diffutils' 'dos2unix' 'expect')
backup=('etc/certmonger/certmonger.conf'
        'etc/tmpfiles.d/certmonger.conf')
install=${pkgname}.install
source=("http://fedorahosted.org/released/certmonger/certmonger-${pkgver}.tar.gz"
        '0001_Fix_includes.patch')
sha512sums=('cbedb364b7b55decf9c485b8fcbe4fcc01a62a6b6a8e63d1f1c436286ee12aa9982679ada69e11cf10c098c56a6e59f60dbd6666fd1205326dd1468f403e2e85'
            'fc9b3ff4405d72d71697784ad41ed8f0bb2f91336de6d0fd4747b9e18d54a7ba070a73314477367089216e178dab90692e3a3c434508688c6bf56b10de6481b3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_Fix_includes.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/${pkgname} \
    --localstatedir=/var \
    --enable-systemd \
    --enable-tmpfiles \
    --with-tmpdir=/run/certmonger

  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ "x${run_tests}" == "xtrue" ]; then
    make check
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
