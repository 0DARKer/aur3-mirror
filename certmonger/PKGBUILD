# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

# Run tests? They take a bit of time.
run_tests=false

pkgname=certmonger
pkgver=0.65
pkgrel=1
pkgdesc="Certificate status monitor and PKI enrollment client"
arch=('i686' 'x86_64')
url="http://certmonger.fedorahosted.org"
license=('GPL')
depends=('curl' 'dbus' 'libxml2' 'nspr' 'nss' 'openssl' 'systemd' 'talloc' 'tevent' 'xmlrpc-c')
checkdepends=('diffutils' 'dos2unix' 'expect')
backup=('etc/certmonger/certmonger.conf'
        'etc/tmpfiles.d/certmonger.conf')
install=${pkgname}.install
source=("http://fedorahosted.org/released/certmonger/certmonger-${pkgver}.tar.gz"
        '0001_Fix_includes.patch')
sha512sums=('0649e7644970605e60103189e8623253d24d18fcae5813233c2f2f66fa86bed6df2fc5fc95aa168390fafc018fa7a86af8de6b07a4c05a86d9711896445951d8'
            'fc9b3ff4405d72d71697784ad41ed8f0bb2f91336de6d0fd4747b9e18d54a7ba070a73314477367089216e178dab90692e3a3c434508688c6bf56b10de6481b3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/0001_Fix_includes.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/${pkgname} \
    --localstatedir=/var \
    --enable-systemd \
    --enable-tmpfiles \
    --with-tmpdir=/run/certmonger

  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ "x${run_tests}" == "xtrue" ]; then
    make check
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
