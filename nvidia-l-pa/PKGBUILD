# 
# Maintainer: Ninez
# Contributors: Det, Ng Oon-Ee, Dan Vratil
# Based on [extra]'s nvidia: https://www.archlinux.org/packages/extra/x86_64/nvidia/
#
# Please note the depends() array. I have included nvidia-utils-beta, since that is what i 
# am using, but thought i should track Archlinux nvidia packages by default. 
#
# So you can switch to nvidia-beta by commenting/uncommenting the correct line.

pkgname=nvidia-l-pa
pkgver=331.20
_major=$(uname -r | cut -d '.' -f-2)
_extramodules=extramodules-3.12-l-pa
pkgrel=3
pkgdesc="NVIDIA driver for linux (for Linux-l-pa)"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
#depends=('linux-l-pa>=3.10' 'linux-l-pa<3.13' "nvidia-utils-beta=${pkgver}")
depends=('linux-l-pa>=3.10' 'linux-l-pa<3.13' "nvidia-utils=${pkgver}")
makedepends=('linux-l-pa-headers>=3.10' 'linux-l-pa-headers<3.13')
conflicts=('nvidia-96xx' 'nvidia-173xx' 'nvidia')
provides=()
license=('custom:NVIDIA')
install=${pkgname}.install
options=('!strip')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
fi

#source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
         'nvidia-rt_explicit.patch'
         'nvidia-rt_no_wbinvd.patch'
         'nvidia-rt_mutexes.patch')

md5sums=('28295eed56c2ca996401c0093279620f'
         '5e2a92db7409a7c7c827d52f1d83d8a9'
         '6db443ac6bc1ce0676388fb8d939b081'
         '64ed8433143d337632d8a624f02ae086')

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

    # Remove previous builds
    if [ -d ${_pkg} ]; then
        rm -rf ${_pkg}
    fi

    # Extract
    sh ${_pkg}.run --extract-only
    cd ${_pkg}/kernel

    msg2 "apply nvidia-325xx-rt.patch"
    patch -Np1 -i $srcdir/nvidia-rt_explicit.patch

    msg2 "apply nvidia-rt-no-wbinvd.patch"
    patch -Np1 -i $srcdir/nvidia-rt_no_wbinvd.patch

    msg2 "apply nvidia-rt_mutexes.patch"
    patch -Np1 -i $srcdir/nvidia-rt_mutexes.patch

    # Build module
    msg2 "Starting make module..."
    make IGNORE_PREEMPT_RT_PRESENCE=1 SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    # Install/compress module
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"

    # Write $_extramodules to .install
    sed -i "s/_extramodules='.*'/_extramodules='${_extramodules}'/" "${startdir}/${install}"

    # Blacklist Nouveau
    install -d "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
}
