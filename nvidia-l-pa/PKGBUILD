# Maintainer:  Jordan Johnston <triplesquarednine@gmail.com>
# Contributor:  Morgan Cox <morgancoxuk@gmail.com>
# Contributor: Joakim Hernberg <jbh@alchemy.lu>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-l-pa
pkgver=313.18
_extramodules=extramodules-3.6-l-pa
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=1
pkgdesc="NVIDIA drivers for linux-l-pa."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=()
makedepends=('linux-l-pa')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
            "patch-3.0-rt-nvidia.patch")

elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
            "patch-3.0-rt-nvidia.patch")
fi

md5sums=('fa17a260793a38b4b8ae367db2e03b39'
         'f09b5fb3edea108ddacd15d8fd4fc6d3')

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    patch -Np1 -i $srcdir/patch-3.0-rt-nvidia.patch
    make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-nvidia-l-pa.conf"
    echo "options nvidia NVreg_EnableMSI=1" >> "${pkgdir}/etc/modprobe.d/nvidia-l-pa-msi.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
