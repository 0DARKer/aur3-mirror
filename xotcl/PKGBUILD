# Maintainer: Uwe Koloska <kolewu [at] koloro [dot] de>

pkgname=xotcl
pkgver=1.6.7
pkgrel=2
pkgdesc="Extended Object Tcl"
arch=('i686' 'x86_64')
url='http://xotcl.org'
license=('MIT')
depends=('tcl' 'gdbm')

source=(http://media.wu-wien.ac.at/download/${pkgname}-${pkgver}.tar.gz
    ${pkgname}-${pkgver}.patch
    tclstub.patch)
md5sums=('ff136c3237eb11fad257576ee02579db'
         '435a22e866188ce8699211ba8eefc69d'
         'd301d942577eec35a098a04a571ca6ee')
build() {
  cd $pkgname-$pkgver
  # Apply patch to tcl.m4 and configure scripts to delete 'generic' from pathes to tclInt.h
  msg "Patch tcl.m4 and configure files..."
  patch -Np1 -i ../${pkgname}-${pkgver}.patch
  
  msg "Apply patch for tclstub ..."
  patch -Np1 -i ../tclstub.patch
  
  msg "Starting make..."
  # FIXME: xotclsh and xowish create a 'Segmentation fault' right at the start
  #   but since they are both marked as deprecated, this may not be a big loss
  # ./configure --prefix=/usr --with-all --enable-threads
  ./configure --prefix=/usr --with-gdbm=/usr/include --with-actiweb --enable-threads
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # fix some installation flaws that I will try to sort out later
  rm -f "${pkgdir}/usr/lib/lib${pkgname}${pkgver}.so"
  ln -s ${pkgname}${pkgver}/lib${pkgname}${pkgver}.so "${pkgdir}/usr/lib/"
  # there are no manpages installed
  rm -rf "${pkgdir}/usr/share/man"
  # and no binaries, too
  rm -rf "${pkgdir}/usr/bin"

  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
