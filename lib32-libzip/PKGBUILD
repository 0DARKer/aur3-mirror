# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Changes: Chris Osgood <archlinux at functionalfuture dot com>

pkgname=lib32-libzip
pkgver=0.10.1
pkgrel=1
pkgdesc="A C library for reading, creating, and modifying zip archives (32-bit)"
url="http://www.nih.at/libzip/index.html"
license=('GPL2')
groups=('lib32')
arch=('x86_64')
depends=('libzip' 'lib32-zlib' 'lib32-glibc')
options=('!libtool')
source=("http://www.nih.at/libzip/libzip-${pkgver}.tar.gz"
        'fix-headers.patch')
md5sums=('dd817b1dd4d582bef6715586311a8c08'
         '249395bd3a426c4c4e993e9d4753e1dd')

build() {
  cd "${srcdir}/libzip-${pkgver}"

  patch -p1 -i "${srcdir}/fix-headers.patch"
  CC='gcc -m32' CFLAGS='-D_FILE_OFFSET_BITS=64' ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libzip-${pkgver}"
  make DESTDIR=${pkgdir} install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
