# Maintainer: kfgz <kfgz at interia dot pl>
# Contributor: Thomas Karmann <thomas at krmnn dot de>

pkgname=faumachine
pkgver=20100527
pkgrel=1
pkgdesc="FAUmachine is an Open Source Virtual Machine"
arch=('i686' 'x86_64')
url="http://www3.informatik.uni-erlangen.de/Research/FAUmachine/"
license=('GPL')
depends=('fauhdlc' 'faucc' 'faumachine-bios' 'gtk2' 'libtheora' 'zenity' 'make')
makedepends=('autoconf' 'automake' 'pkgconfig' 'xmlto' 'python2')
optdepends=('slirp: for network support')
source=(http://www3.informatik.uni-erlangen.de/Research/FAUmachine/downloads/${pkgname}-${pkgver}.tar.gz)
md5sums=('1c648b945fd71d979079d00754b832a1')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  unset LDFLAGS
  sed -i "s|bin/python|bin/python2|g" scripts/check_license.py
  ./autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-build-bios
  sed -i "s|bin/python|bin/python2|g" config.status
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
