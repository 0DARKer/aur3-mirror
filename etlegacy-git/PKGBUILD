# Maintainer: Babets
pkgname=etlegacy-git
pkgver=20120628
pkgrel=1
pkgdesc="ET: Legacy is a project that aims to create a fully compatible client/server for the popular online FPS game Wolfenstein: Enemy Territory."
arch=('i686' 'x86_64')
url="http://etlegacy.com/"
license=('GPL3')

[ "$CARCH" == "i686" ] && depends=('sdl' 'libjpeg' 'mesa' 'etlegacy-data')
[ "$CARCH" == "x86_64" ] && depends=('lib32-sdl' 'lib32-libjpeg' 'lib32-mesa' 'etlegacy-data')

[ "$CARCH" == "i686" ] && makedepends=('git' 'cmake' 'curl' 'libvorbis' 'openal' 'freetype2')
[ "$CARCH" == "x86_64" ] && makedepends=('git' 'cmake' 'lib32-curl' 'lib32-libvorbis' 'lib32-openal' 'lib32-freetype2')

[ "$CARCH" == "i686" ] && optdepends=('curl' 'libvorbis' 'openal' 'freetype2')
[ "$CARCH" == "x86_64" ] && optdepends=('lib32-curl' 'lib32-libvorbis' 'lib32-openal' 'lib32-freetype2')
provides=('enemy-territory' 'etlegacy')
conflicts=('etlegacy')
replaces=('etlegacy')
source=(etlegacy.desktop etlegacy.launcher etlegacy-dedicated.launcher)
md5sums=('0bddd57669a52e1ee38ac76391151990'
         '1d96cc1fd83d57004a52e6a1583279d8'
         'e761b78452b1afc2f9a40bde3169bba9')

#git clone git://github.com/etlegacy/etlegacy.git
_gitroot=git://github.com/etlegacy/etlegacy.git
_gitname=etlegacy

build() {
  cd "$srcdir"
   msg "Connecting to GIT server..."
   if [ -d $_gitname ] ; then
     cd $_gitname && git pull origin
     msg "The local files are updated."
   else
     git clone --depth=1 $_gitroot $_gitname
   fi
   msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  cp -R "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i 's@"games/etlegacy"@"share/etlegacy"@g' CMakeLists.txt

  mkdir build
  cd build

  cmake -D CMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/$_gitname-build/build"
  make DESTDIR="$pkgdir/" install

  cd ../..

  # install icon and desktop file
  install -Dm 644 $_gitname-build/misc/wolfet.ico $pkgdir/usr/share/pixmaps/wolfet.ico
  install -Dm 644 $_gitname.desktop $pkgdir/usr/share/applications/$_gitname.desktop

  # install launch scripts
  install -Dm 755 $_gitname.launcher $pkgdir/usr/bin/$_gitname
  install -Dm 755 $_gitname-dedicated.launcher $pkgdir/usr/bin/$_gitname-dedicated
}

# vim:set ts=2 sw=2 et:
