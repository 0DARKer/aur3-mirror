# Maintainer: Spyhawk
# Contributor: Babets 
# - requires multilib-devel
# - other optional deps
#   (32 bits): 'openal' 'ncurses' 'freetype2'
#   (64 bits): 'lib32-openal' 'lib32-ncurses' 'lib32-freetype2'

pkgname=etlegacy-git
pkgver=v2.70rc1.1027.g6c61275
pkgrel=1
pkgdesc="Fully compatible client and server for the popular online FPS game Wolfenstein: Enemy Territory"
arch=('i686' 'x86_64')
url="http://www.etlegacy.com/"
license=('GPL3')
if [[ "$CARCH" == "i686" ]]; then
  depends=('enemy-territory-data' 'alsa-plugins' 'sdl' 'libjpeg' 'glu' 'libsm' 'curl' 'libvorbis' 'gcc-libs' 'lua')
else
  depends=('enemy-territory-data' 'lib32-alsa-plugins' 'lib32-sdl' 'lib32-libjpeg' 'lib32-glu' 'lib32-libsm' 'lib32-curl' 'lib32-libvorbis' 'lib32-gcc-libs' 'lib32-lua')
fi
makedepends=('git' 'cmake' 'zip')
provides=('etlegacy' 'etlegacy-mod')
conflicts=('etlegacy' 'etlegacy-mod')
install=etlegacy.install
source=('git://github.com/etlegacy/etlegacy.git')
md5sums=('SKIP')
_gitname=etlegacy

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --always | sed 's/-/./g'
}

build() {
  cd "$srcdir/$_gitname"

  # options
  cmakeopts=(
    '-D CMAKE_BUILD_TYPE="Release"'
    '-D CMAKE_INSTALL_PREFIX=/usr'
    '-D CMAKE_LIBRARY_PATH=/usr/lib32'
    '-D INSTALL_DEFAULT_BASEDIR=/usr/share/etlegacy'
    '-D INSTALL_DEFAULT_BINDIR=bin'
    '-D INSTALL_DEFAULT_MODDIR=share/etlegacy'
    '-D BUILD_SERVER=1'
    '-D BUILD_CLIENT=1'
    '-D BUILD_MOD=1'
    '-D BUILD_PAK3_PK3=1'
    '-D BUNDLED_LIBS=0'
    '-D BUNDLED_SDL=0'
    '-D BUNDLED_CURL=0'
    '-D BUNDLED_JPEG=0'
    '-D BUNDLED_LUA=0'
    '-D BUNDLED_OGG_VORBIS=0'
    '-D CROSS_COMPILE32=1'
    '-D FEATURE_CURL=1'
    '-D FEATURE_OGG_VORBIS=1'
    '-D FEATURE_OPENAL=0'
    '-D FEATURE_FREETYPE=0'
    '-D FEATURE_TRACKER=0'
    '-D FEATURE_LUA=1'
    '-D FEATURE_MULTIVIEW=0'
    '-D FEATURE_ANTICHEAT=1'
    '-D FEATURE_CURSES=0'
    '-D FEATURE_AUTOUPDATE=1'
    '-D FEATURE_RENDERER2=0'
    '-D FEATURE_IPV6=0'
    '-D FEATURE_OMNIBOT=1'
    '-D INSTALL_OMNIBOT=0'
  )

  cmake ${cmakeopts[@]} .
  make
}

package() {
  cd "$srcdir/$_gitname"

  # files
  make DESTDIR="$pkgdir/" install

  # data files
  mkdir -p $pkgdir/usr/share/etlegacy/etmain
  ln -s /usr/share/enemy-territory/etmain/{pak0,pak1,pak2}.pk3 $pkgdir/usr/share/$_gitname/etmain/

  # doc and man pages
  mkdir -p $pkgdir/usr/share/{doc/$_gitname,man/man6}
  mv $pkgdir/usr/share/$_gitname/{README.md,COPYING.txt} $pkgdir/usr/share/doc/$_gitname/
  install -Dm 644 docs/game/anticheat.html $pkgdir/usr/share/doc/$_gitname/
  install -Dm 644 docs/linux/man/man6/{etl,etlded}.6 $pkgdir/usr/share/man/man6/

  # desktop file and icon
  mkdir -p $pkgdir/usr/share/{pixmaps,applications,mime/packages}
  install -Dm 644 misc/ETL.xpm $pkgdir/usr/share/pixmaps/etlegacy.xpm
  install -Dm 644 misc/etlegacy.desktop $pkgdir/usr/share/applications/
  install -Dm 644 misc/etlegacy.xml $pkgdir/usr/share/mime/packages/
}

# vim:set ts=2 sw=2 et:
