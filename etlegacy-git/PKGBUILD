# Maintainer: Babets
pkgname=etlegacy-git
pkgver=20121231
pkgrel=1
pkgdesc="ET: Legacy is a project that aims to create a fully compatible client/server for the popular online FPS game Wolfenstein: Enemy Territory."
arch=('i686' 'x86_64')
url="http://etlegacy.com/"
license=('GPL3')

[ "$CARCH" == "i686" ] && depends=('sdl' 'libjpeg' 'glu' 'libsm' 'curl' 'libvorbis' 'etlegacy-data')
[ "$CARCH" == "x86_64" ] && depends=('lib32-sdl' 'lib32-libjpeg' 'lib32-glu' 'lib32-libsm' 'lib32-curl' 'lib32-libvorbis' 'lib32-gcc-libs'  'etlegacy-data')
# gcc-libs not needed on i686 because is a dependency of libgl which is a dependency of glu

[ "$CARCH" == "i686" ] && makedepends=('git' 'cmake' 'zip')
[ "$CARCH" == "x86_64" ] && makedepends=('git' 'cmake' 'zip')

[ "$CARCH" == "i686" ] && optdepends=('lua')
[ "$CARCH" == "x86_64" ] && optdepends=('lua')
# lua not tested
provides=('etlegacy')
conflicts=('etlegacy')
replaces=('etlegacy')
source=(etlegacy.desktop)
md5sums=('9bb374b5cdf1540f47d695f8086cc21a')

#git clone git://github.com/etlegacy/etlegacy.git
_gitroot=git://github.com/etlegacy/etlegacy.git
_gitname=etlegacy

build() {
  cd "$srcdir"
   msg "Connecting to GIT server..."
   if [ -d $_gitname ] ; then
     cd $_gitname && git pull origin
     msg "The local files are updated."
   else
     git clone --depth=1 $_gitroot $_gitname
   fi
   msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  cp -R "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  mkdir build
  cd build

  #sed -i 's/\(INSTALL_DEFAULT_BASEDIR[ ]*\)""/\1 ${CMAKE_INSTALL_PREFIX}\/${INSTALL_DEFAULT_MODDIR}/g' ../CMakeLists.txt
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_DEFAULT_MODDIR=share/etlegacy -D INSTALL_DEFAULT_BASEDIR=/usr/share/etlegacy \
	-DCMAKE_BUILD_TYPE="Release" \
	-DBUILD_CLIENT=1 \
	-DBUILD_SERVER=1 \
	-DBUILD_MOD=1 \
	-DBUILD_MOD_PK3=1 \
	-DBUILD_PAK3_PK3=1 \
	-DBUNDLED_SDL=0 \
	-DBUNDLED_JPEG=0 \
	-DBUNDLED_LUA=0 \
	-DBUNDLED_CURL=0 \
	-DCROSS_COMPILE32=1 \
	-DFEATURE_CURL=1 \
	-DFEATURE_OGG_VORBIS=1 \
	-DFEATURE_FREETYPE=0 \
	-DFEATURE_OPENAL=0 \
	-DFEATURE_TRACKBASE=1 \
	-DFEATURE_OMNIBOT=0 \
	-DFEATURE_ANTICHEAT=0 \
  ..

  make
}

package() {
  cd "$srcdir/$_gitname-build/build"
  make DESTDIR="$pkgdir/" install

  # install icon
  cd ..
  install -Dm 644 misc/etl.svg $pkgdir/usr/share/pixmaps/etl.svg

  #install desktop file
  cd ..
  install -Dm 644 $_gitname.desktop $pkgdir/usr/share/applications/$_gitname.desktop
}

# vim:set ts=2 sw=2 et:
