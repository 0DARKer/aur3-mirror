# Maintainer: Spyhawk
# Contributor: Babets 
# - requires multilib-devel
# - other optional deps
#   (32 bits): 'lua' 'openal' 'ncurses' 'freetype2'
#   (64 bits): 'lib32-lua' 'lib32-openal' 'lib32-ncurses' 'lib32-freetype2'

pkgname=etlegacy-git
pkgver=20130327
pkgrel=1
pkgdesc="Fully compatible client and server for the popular online FPS game Wolfenstein: Enemy Territory"
arch=('i686' 'x86_64')
url="http://www.etlegacy.com/"
license=('GPL3')
if [[ "$CARCH" == "i686" ]]; then
  depends=('enemy-territory-data' 'sdl' 'libjpeg' 'glu' 'libsm' 'curl' 'libvorbis' 'gcc-libs')
else
  depends=('enemy-territory-data' 'lib32-sdl' 'lib32-libjpeg' 'lib32-glu' 'lib32-libsm' 'lib32-curl' 'lib32-libvorbis' 'lib32-gcc-libs')
fi
makedepends=('git' 'cmake' 'zip')
provides=('etlegacy')
conflicts=('etlegacy')
source=('etlegacy.desktop')
md5sums=('215a3e6a1228730fcc0289aa6298e9f5')
_gitroot=git://github.com/etlegacy/etlegacy.git
_gitname=etlegacy

#source=('git://github.com/etlegacy/etlegacy.git#branch=master'
#        'etlegacy.desktop')
#md5sums=('SKIP'
#         '215a3e6a1228730fcc0289aa6298e9f5')
#_gitname=etlegacy

#pkgver() {
#  cd "$srcdir/$_gitname"
#  git describe --always | sed 's/-/./g'
#}

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."
  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth=1 $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"

  cd "$srcdir/$_gitname"

  cmake -D CMAKE_INSTALL_PREFIX=/usr \
    -D INSTALL_DEFAULT_BASEDIR=/usr/share/etlegacy \
    -D INSTALL_DEFAULT_BINDIR=bin \
    -D INSTALL_DEFAULT_MODDIR=share/etlegacy \
    -D CMAKE_BUILD_TYPE="Release" \
    -D BUILD_SERVER=1 \
    -D BUILD_CLIENT=1 \
    -D BUILD_MOD=1 \
    -D BUILD_PAK3_PK3=1 \
    -D BUNDLED_LIBS=0 \
    -D BUNDLED_SDL=0 \
    -D BUNDLED_CURL=0 \
    -D BUNDLED_JPEG=0 \
    -D BUNDLED_LUA=0 \
    -D BUNDLED_OGG_VORBIS=0 \
    -D CROSS_COMPILE32=1 \
    -D FEATURE_CURL=1 \
    -D FEATURE_OGG_VORBIS=1 \
    -D FEATURE_OPENAL=0 \
    -D FEATURE_FREETYPE=0 \
    -D FEATURE_TRACKER=1 \
    -D FEATURE_LUA=0 \
    -D FEATURE_MULTIVIEW=0 \
    -D FEATURE_ANTICHEAT=1 \
    -D FEATURE_CURSES=0 \
    -D FEATURE_AUTOUPDATE=1 \
    -D FEATURE_RENDERER2=0 \
    -D FEATURE_IPV6=0 \
    -D FEATURE_OMNIBOT=0\
    -D INSTALL_OMNIBOT=0 \
    .

  make
}

package() {
  cd "$srcdir/$_gitname"

  # files
  make DESTDIR="$pkgdir/" install

  # data files
  ln -s /usr/share/enemy-territory/etmain/{mp_bin,pak0,pak1,pak2}.pk3 $pkgdir/usr/share/$_gitname/etmain/

  # doc and man pages
  mkdir -p $pkgdir/usr/share/{doc/$_gitname,man/man6}
  mv $pkgdir/usr/share/$_gitname/{README.md,COPYING.txt} $pkgdir/usr/share/doc/$_gitname/
  install -Dm 644 docs/game/anticheat.html $pkgdir/usr/share/doc/$_gitname/
  install -Dm 644 docs/linux/man/man6/{etl,etlded}.6 $pkgdir/usr/share/man/man6/

  # desktop file and icon
  mkdir -p $pkgdir/usr/share/{pixmaps,applications}
  install -Dm 644 misc/ETL.xpm $pkgdir/usr/share/pixmaps/etlegacy.xpm
  install -Dm 644 ../etlegacy.desktop $pkgdir/usr/share/applications/
}

# vim:set ts=2 sw=2 et:
