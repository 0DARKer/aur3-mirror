# Contributor: JD Horelick <jdhore1@gmail.com>
# Maintainer: Mantas MikulÄ—nas <grawity@gmail.com>
pkgname=charybdis
pkgver=3.4.0
pkgrel=1
pkgdesc="A highly-scalable IRCv3-compliant IRC daemon"
arch=('i686' 'x86_64')
url="http://www.atheme.org/project/charybdis"
license=('GPL2')
depends=(
  'bison'
  'flex'
  'openssl'
  'zlib'
  )
source=("http://www.stack.nl/~jilles/irc/$pkgname-$pkgver.tbz2")
install=$pkgname.install
sha1sums=('0d08e43533ce1a8aca62433e0ff5c5cd8d640e1c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  rundir=/run \
  ./configure \
    --prefix=/usr                     \
    --sysconfdir=/etc/charybdis       \
    --localstatedir=/var              \
    --libexecdir=/usr/lib             \
    --enable-ipv6                     \
    --with-rundir=/run                \
    --with-program-prefix=charybdis-  \
    ;
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  
  mkdir -p "$pkgdir/usr/share/doc/charybdis"
  mv "$pkgdir/etc/charybdis/example.conf"   "$pkgdir/usr/share/doc/charybdis"
  mv "$pkgdir/etc/charybdis/reference.conf" "$pkgdir/usr/share/doc/charybdis"

  mkdir -p "$pkgdir/usr/lib/tmpfiles.d"
  echo 'd /run/charybdis 0755 charybdis charybdis -' \
    > "$pkgdir/usr/lib/tmpfiles.d/charybdis.conf"
  rm -rf "$pkgdir/run"
}

# vim: ts=2:sw=2:et
