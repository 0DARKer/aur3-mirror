# Maintainer: Moritz Lipp <mlq@pwmt.org>

pkgname=freerct-git
pkgver=r1020.f498ebe
pkgrel=1
pkgdesc="Open Source RollerCoaster Tycoon 1 and 2 implementation"
arch=('i686' 'x86_64')
url="http://freerct.org"
license=('GPL2')
depends=('sdl2' 'sdl2_ttf' 'flex' 'libzip' 'bison' 'libpng' 'ttf-freefont')
makedepends=('git' 'cmake')
conflicts=('freerct')
provides=('freerct')
source=('freerct::git+https://github.com/FreeRCT/FreeRCT.git'
        'freerct.cfg'
        'freerct.sh')
md5sums=('SKIP'
         '9418fedbf4487127964abc96b4eb71e9'
         '5a11b1373f5d0d9b252c63aa3a77c742')
_gitname=freerct

build() {
  cd "$srcdir/$_gitname"
  cmake .
  make
}

package() {
  cd "$srcdir/$_gitname"

  mkdir -p "$pkgdir/opt/freerct/src/rcdgen"
  mkdir -p "$pkgdir/usr/bin"

  # install executeables
  install -D -m755 "./src/freerct" "$pkgdir/opt/freerct/src/freerct"
  install -D -m755 "./src/rcdgen/rcdgen" "$pkgdir/opt/freerct/src/rcdgen/rcdgen"

  install -D -m755 "$srcdir/freerct.sh" "$pkgdir/usr/bin/freerct"

  # install rcd and graphics
  cp -r "./graphics" "$pkgdir/opt/freerct/"

  # install freerct.cfg
  install -D -m664 "$srcdir/freerct.cfg" "$pkgdir/opt/freerct/src"

  # install LICENSE
  install -D -m664 LICENSE-gpl-2.0.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

# vim:set ts=2 sw=2 et:
