# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=ffmbc
pkgver="0.6_rc8"
pkgrel=1
pkgdesc="FFmpeg customized for broadcast and professional usage"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmbc/"
license=('GPL')
depends=('bzip2' 'faac' 'lame' 'libtheora' 'libva' 'libvdpau' 'libvorbis' 'x264-git' 'zlib')
makedepends=('yasm')
conflicts=('ffmpeg')
source=("http://ffmbc.googlecode.com/files/${pkgname//ff/FF}-${pkgver//_/-}.tar.gz")
sha1sums=('c1f0a489819744f8d294387b2c68efe98b7e9c5e')

build() {
  cd ${srcdir}/${pkgname//ff/FF}-${pkgver//_/-}

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-nonfree \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libx264
 
  make
  make doc/ff{mbc,play,probe}.1
}

package() {
  cd ${srcdir}/${pkgname//ff/FF}-${pkgver//_/-}
  make DESTDIR=${pkgdir} install install-man
}
