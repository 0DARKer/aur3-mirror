# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=ffmbc
pkgver="0.7_rc7"
pkgrel=2
pkgdesc="FFmpeg customized for broadcast and professional usage"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmbc/"
license=('GPL2' 'custom')
depends=('faac' 'lame' 'libass' 'x264-git' 'zlib')
makedepends=('yasm')
source=("http://ffmbc.googlecode.com/files/FFmbc-${pkgver//_/-}.tar.bz2")
sha1sums=('79d125cd5d420e61120e2a66b018b0be096ad088')

build() {
  cd "${srcdir}/FFmbc-${pkgver//_/-}"

  ./configure \
    --prefix=/usr \
    --incdir=/usr/include/ffmbc \
    --libdir=/usr/lib/ffmbc \
    --shlibdir=/usr/lib/ffmbc \
    --disable-ffplay \
    --disable-ffprobe \
    --enable-gpl \
    --enable-nonfree \
    --enable-libass \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libx264
 
  make
  make doc/ffmbc.1
}

package() {
  cd "${srcdir}/FFmbc-${pkgver//_/-}"
  make DESTDIR="${pkgdir}" install install-man
}
