# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=ffmbc
pkgver="0.7_rc4"
pkgrel=2
pkgdesc="FFmpeg customized for broadcast and professional usage"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmbc/"
license=('GPL')
depends=('bzip2' 'faac' 'lame' 'libtheora' 'libva' 'libvdpau' 'libvorbis' 'x264-git' 'zlib')
makedepends=('yasm')
conflicts=('ffmpeg')
source=("http://ffmbc.googlecode.com/files/FFmbc-${pkgver//_/-}.tar.bz2")
sha1sums=('2d47a9694bf51bde61657341c6dc7b793ae4491c')

build() {
  cd ${srcdir}/FFmbc-${pkgver//_/-}

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-nonfree \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libx264
 
  make
  make doc/ff{mbc,play,probe}.1
}

package() {
  cd ${srcdir}/FFmbc-${pkgver//_/-}
  make DESTDIR=${pkgdir} install
}
