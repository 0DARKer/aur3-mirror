# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=ffmbc
pkgver="0.6.1"
pkgrel=1
pkgdesc="FFmpeg customized for broadcast and professional usage"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmbc/"
license=('GPL')
depends=('bzip2' 'faac' 'lame' 'libtheora' 'libva' 'libvdpau' 'libvorbis' 'x264-git' 'zlib')
makedepends=('yasm')
conflicts=('ffmpeg')
source=("http://ffmbc.googlecode.com/files/FFmbc-${pkgver}.tar.gz")
sha1sums=('2dc65b84178b8b73cd2f78de8b73311c057823eb')

build() {
  cd ${srcdir}/FFmbc-${pkgver}

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-nonfree \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libx264
 
  make
  make doc/ff{mbc,play,probe}.1
}

package() {
  cd ${srcdir}/FFmbc-${pkgver}
  make DESTDIR=${pkgdir} install install-man
}
