# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=ffmbc
pkgver="0.6_rc3"
pkgrel=1
pkgdesc="FFmpeg customized for broadcast and professional usage"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmbc/"
license=('GPL')
depends=('bzip2' 'faac' 'lame' 'libtheora' 'libva' 'libvdpau' 'libvorbis' 'sdl' 'x264-git' 'zlib')
makedepends=('yasm')
conflicts=('ffmpeg')
provides=('qt-faststart')
source=("http://ffmbc.googlecode.com/files/${pkgname//ff/FF}-${pkgver//_/-}.tar.gz")
sha1sums=('315d5ad4fffa0677d023425d3a9f31bc455fd8de')

build() {
  cd ${srcdir}/${pkgname//ff/FF}-${pkgver//_/-}

  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-nonfree \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libx264
 
  make
  make tools/qt-faststart
  make doc/ff{mbc,play,probe,server}.1

  make DESTDIR=${pkgdir} install install-man
  install -D -m755 tools/qt-faststart ${pkgdir}/usr/bin/qt-faststart
}
