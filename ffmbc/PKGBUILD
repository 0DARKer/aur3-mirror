# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=ffmbc
pkgver="0.7_rc6"
pkgrel=1
pkgdesc="FFmpeg customized for broadcast and professional usage"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmbc/"
license=('GPL2' 'custom')
depends=('faac' 'lame' 'libass' 'x264-git' 'zlib')
makedepends=('yasm')
conflicts=('ffmpeg')
source=("http://ffmbc.googlecode.com/files/FFmbc-${pkgver//_/-}.tar.bz2")
sha1sums=('67f34eaedb053b32958ccdecb84a1eba871ac2cd')

build() {
  cd "${srcdir}/FFmbc-${pkgver//_/-}"

  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --enable-nonfree \
    --enable-libass \
    --enable-libfaac \
    --enable-libmp3lame \
    --enable-libx264
 
  make
  make doc/ff{mbc,play,probe}.1
}

package() {
  cd "${srcdir}/FFmbc-${pkgver//_/-}"
  make DESTDIR="${pkgdir}" install
}
