# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=bkbtl
pkgver=beta8
pkgrel=2
pkgdesc="An emulator of the BK-0010/0011 computers"
arch=('i686' 'x86_64')
url="http://code.google.com/p/bkbtl/"
license=('LGPL')
depends=('qt')
makedepends=('subversion')
source=("${pkgname}.png"
        "${pkgname}.desktop")
md5sums=('631ba1510ba4ba08c4e7a10217928467'
         '0fbbb8ebdd8650883f29696995f8142a')

build() {
  cd "${srcdir}"

  _svntrunk="http://bkbtl.googlecode.com/svn/trunk/src/QtBkBtl"
  _svnroms="http://bkbtl.googlecode.com/svn/trunk/lib/roms"
  _svnrev=131
  _svnmod="bkbtl-read-only"

  # Download source code and ROM's from SVN
  svn co "${_svntrunk}" -r "${_svnrev}" "${_svnmod}"
  svn co "${_svnroms}"

  # Set path to ROM file
  cd "${pkgname}-read-only"
  sed -i "s|= _T(\"|= _T(\"/usr/share/${pkgname}/|" Emulator.cpp

  # Compile
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}-read-only"

  # Install program files
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  install -m644 ../roms/*.rom "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 QtBkBtl "${pkgdir}/usr/bin/${pkgname}"

  # Install pixmap and .desktop file
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
