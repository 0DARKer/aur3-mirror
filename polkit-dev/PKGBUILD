# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on the polkit package trunk

_pkgbase=polkit
pkgname=polkit-dev
pkgver=0.101
pkgrel=1
pkgdesc="Application development toolkit for controlling system-wide privileges"
arch=(i686 x86_64)
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/PolicyKit"
depends=('glib2' 'pam' 'expat')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'docbook-xsl')
replaces=('policykit')
conflicts=("${_pkgbase}")
provides=("${_pkgbase}=${pkgver}")
options=('!libtool')
source=(http://hal.freedesktop.org/releases/${_pkgbase}-${pkgver}.tar.gz
        polkit.pam)
md5sums=('f925ac93aba3c072977370c1e27feb7f'
         '6564f95878297b954f0572bc1610dd15')

build() {
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/polkit-1 \
      --disable-static --enable-gtk-doc --disable-manpages
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m644 ../polkit.pam "${pkgdir}/etc/pam.d/polkit-1"
}
