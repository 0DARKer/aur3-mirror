# Maintainer: Gilrain <pierre.buard+aur gmail com>
pkgname=pology-svn
pkgver=1350070
pkgrel=1
pkgdesc='A framework for custom processing of PO files.'
arch=('any')
license=('GPL3')
url='http://techbase.kde.org/Localization/Tools/Pology'
depends=('python2' 'gettext')
makedepends=('subversion' 'cmake' 'libxml2' 'docbook-xsl' 'epydoc')
options=(!makeflags)
optdepends=('python2-dbus: to communicate with external applications'
	    'python2-pyenchant: spell checking functionality (needs a dictionary)'
	    'hunspell-lang: a spell-checking dictionary for your language(s)'
	    'aspell-lang: a spell-checking dictionary for your language(s)'
	    'ispell-lang: a spell-checking dictionary for your language(s)'
	    'languagetool: used by the check-grammar sieve'
	    'apertium: used by the pomtrans script'
	    'git: for processing files under version control'
	    'subversion: for processing files under version control')
provides=('pology')
source=('pology::svn://anonsvn.kde.org/home/kde/trunk/l10n-support/pology')
md5sums=('SKIP')
changelog=changelog.svn

pkgver() {
  cd "$SRCDEST"/pology
  svnversion
}

prepare() {
  cd ${srcdir}/pology
  sed -i -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' $(find ${srcdir} -name '*.py')
}

build() {
  cd ${srcdir}/pology
  mkdir -p build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DDOCBOOK_XSL_DIR=/usr/share/xml/docbook/xsl-stylesheets-1.78.1/
  make PREFIX=/usr
}

package() {
  cd ${srcdir}/pology/build
  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/share/apps/katepart/syntax
  ln -s /usr/share/pology/syntax/kate/synder.xml ${pkgdir}/usr/share/apps/katepart/syntax/synder.xml

  install -d ${pkgdir}/etc/bash_completion.d
  ln -s /usr/share/pology/completion/bash/pology ${pkgdir}/etc/bash_completion.d/pology
}
