# Maintainer leper <blubblub@mail.ru>

pkgname=smokinguns
pkgver=1.1
pkgrel=5
pkgdesc='A semi-realistic simulation of the "old west" great atmosphere built on id Tech 3.'
url="http://www.smokin-guns.org"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('sdl' 'openal' 'curl' 'libjpeg' 'speex' 'libgl' 'smokinguns-data')
conflicts=('smokinguns-bin')
source=("release_${pkgver}.tar.gz::http://smokinguns.svn.sourceforge.net/viewvc/smokinguns/tags/release_${pkgver}/?view=tar"
        "$pkgname.desktop"
        'x86_64.patch')
md5sums=('ddedc782f5816b937cde1a7f2e5e260b'
         'c0622e33e16efbd67af1b1c92c4be2e6'
         '71aada35a20a59743cd359f2ffe4c66d')

build() {
  cd "$srcdir/release_${pkgver}"

  # Patch for x64 (backported from ioquake3 trunk)
  if [ "$CARCH" == "x86_64" ] ; then
    patch -p0 < "$srcdir/x86_64.patch"
    # TODO These should be removed as a part of the patch
    rm code/asm/ftola.s
    rm code/asm/snapvectora.s
  fi

  # Set basedir
  echo "DEFAULT_BASEDIR = /usr/share/${pkgname}" >> "Makefile.local"
  # Use system libraries
  echo "USE_INTERNAL_ZLIB=0" >> "Makefile.local"
  echo "USE_INTERNAL_JPEG=0" >> "Makefile.local"
  echo "USE_INTERNAL_SPEEX=0" >> "Makefile.local"
  # Use system headers
  echo "USE_LOCAL_HEADERS=0" >> "Makefile.local"

  make
}

package() {
  install -Dm 755 "$srcdir/release_$pkgver/build/release-linux-${CARCH//i.86/i386}/$pkgname.${CARCH//i.68/i386}" "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "$srcdir/release_$pkgver/build/release-linux-${CARCH//i.86/i386}/${pkgname}_dedicated.${CARCH//i.86/i386}" "$pkgdir/usr/bin/${pkgname}_dedicated"

  install -Dm 644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm 644 "$srcdir/release_$pkgver/misc/$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
