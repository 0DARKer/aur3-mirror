# Maintainer leper <blubblub@mail.ru>

pkgname=smokinguns
pkgver=1.1
pkgrel=4
pkgdesc='A semi-realistic simulation of the "old west" great atmosphere built on id Tech 3.'
url="http://www.smokin-guns.org"
arch=('x86_64')
license=('GPL2')
depends=('sdl' 'openal' 'curl' 'libjpeg' 'speex' 'libgl' 'smokinguns-data')
makedepends=('subversion')
conflicts=('smokinguns-bin')
replaces=('westernquake3')
source=('x86_64.patch')
md5sums=('71aada35a20a59743cd359f2ffe4c66d')

svntrunk=https://smokinguns.svn.sourceforge.net/svnroot/smokinguns/tags/release_${pkgver}
svnmod=tag

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$svnmod/.svn" ]]; then
    (cd "$svnmod" && svn up)
  else
    svn co "$svntrunk" --config-dir ./ "$svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$svnmod-build"
  cp -r "$srcdir/$svnmod" "$srcdir/$svnmod-build"
  cd "$srcdir/$svnmod-build"

  # Patch for x64 (backported from ioquake3 trunk)
  if [ "$CARCH" == "x86_64" ] ; then
    patch -p0 < "$srcdir/x86_64.patch"
    # TODO These should be removed as a part of the patch
    rm code/asm/ftola.s
    rm code/asm/snapvectora.s
  fi

  # Set basedir
  echo "DEFAULT_BASEDIR = /usr/share/${pkgname}" >> "$srcdir/$svnmod-build/Makefile.local"
  # Use system libraries
  echo "USE_INTERNAL_ZLIB=0" >> "$srcdir/$svnmod-build/Makefile.local"
  echo "USE_INTERNAL_JPEG=0" >> "$srcdir/$svnmod-build/Makefile.local"
  echo "USE_INTERNAL_SPEEX=0" >> "$srcdir/$svnmod-build/Makefile.local"
  # Use system headers
  echo "USE_LOCAL_HEADERS=0" >> "$srcdir/$svnmod-build/Makefile.local"

  make
}

package() {
  cd "${srcdir}/${svnmod}-build"

  # TODO Check if $arch works on i686 as the folder could be called i386
  # and fix it if it doesn't work.
  install -Dm 755 "$srcdir/$svnmod-build/build/release-linux-$arch/$pkgname.$arch" "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "$srcdir/$svnmod-build/build/release-linux-$arch/${pkgname}_dedicated.$arch" "$pkgdir/usr/bin/${pkgname}_dedicated"
}

# vim:set ts=2 sw=2 et:
