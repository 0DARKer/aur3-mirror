# Contributor: Leon Vayman <vleon1@gmail.com>

pkgname=smokinguns
pkgver=1.1_Beta2
pkgrel=1
pkgdesc="A Western game using Quake3 engine."
url="http://www.smokin-guns.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl' 'openal' 'curl')
makedepends=('unzip' 'svn')
source=(http://www.quake3.fr/fichier/stuff/quake3/mods/Smokin_Guns_1.0.zip
	smokinguns.desktop
	smokinguns.sh
	smokinguns_dedicated.sh)

md5sums=('6e6a980bd057e9241c66b7324729c7b9'
	 'cf3b5675d47ab566a7f632a79c3f0c9a'
	 '6cd5b719ec16a1def01dfb6deeaa8e2e'
	 '59e17d682e4e7579b46c3b2fc8d6005a')

_svntrunk=https://smokinguns.svn.sourceforge.net/svnroot/smokinguns/branches/1.1/
_svnmod=smokinguns

build() {

  #Grab the svn engine sources
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up) || return 1
  else
    svn co $_svntrunk --config-dir ./ $_svnmod || return 1
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  rm -r $srcdir/$_svnmod-build
  cp -r $srcdir/$_svnmod $srcdir/$_svnmod-build
  cd $srcdir/$_svnmod-build

  #Build the smokinguns engine
  sed -i "s:./install:$pkgdir/opt/smokinguns:" Makefile.local
  make || return 1
  make copyfiles || return 1

  #Fix the naming conversion
  mv $pkgdir/opt/smokinguns/smokinguns.* $pkgdir/opt/smokinguns/smokinguns.bin
  mv $pkgdir/opt/smokinguns/smokinguns_dedicated.* $pkgdir/opt/smokinguns/smokinguns_dedicated.bin

  #Install the data files
  cd "$srcdir/Smokin' Guns"
  install -d -m755 $pkgdir/opt/smokinguns/smokinguns $pkgdir/opt/smokinguns/baseq3 $pkgdir/usr/bin $pkgdir/usr/share/pixmaps/ $pkgdir/usr/share/applications/
  install -D -m644 smokinguns/*.pk3 $pkgdir/opt/smokinguns/smokinguns/
  install -D -m644 smokinguns/*.cfg $pkgdir/opt/smokinguns/smokinguns/
  install -D -m644 baseq3/*.pk3 $pkgdir/opt/smokinguns/baseq3/

  #Install the binary files
  install -m755 $srcdir/smokinguns.sh $pkgdir/usr/bin/smokinguns
  install -m755 $srcdir/smokinguns_dedicated.sh $pkgdir/usr/bin/smokinguns_dedicated

  #Install the desktop file and the icon
  install -m644 sg_48.png $pkgdir/usr/share/pixmaps/smokinguns.png
  install -m644 $srcdir/smokinguns.desktop $pkgdir/usr/share/applications/

}

# vim:set ts=2 sw=2 et: