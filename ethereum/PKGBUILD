# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=ethereum
pkgver=0.5
pkgrel=1
pkgdesc="Next-generation distributed cryptographic ledger"
arch=('i686' 'x86_64')
depends=('boost' 'boost-libs' 'crypto++' 'gmp' 'leveldb' 'miniupnpc' 'ncurses' 'openssl' 'python2' 'qt5-base' 'qt5-webkit' 'snappy')
makedepends=('automake' 'cmake' 'gcc' 'glibc' 'yasm')
url="https://code.ethereum.org/"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/ethereum/cpp-$pkgname/tar.gz/poc-5)
sha256sums=('9c58110d2de0bba3c3226ae57d3690c95ed8c90bccf9af1cf9a393dbf644b791')
provides=('alethzero' 'cpp-ethereum' 'ethereum' 'ethereum-serpent' 'lllc' 'sc' 'testeth' 'walleth')
conflicts=('alethzero' 'cpp-ethereum' 'ethereum' 'ethereum-serpent' 'lllc' 'sc' 'secp256k1' 'testeth' 'walleth')

prepare() {
  cd $srcdir/cpp-$pkgname-poc-5

  msg 'Fixing cryptopp...'
  find . -type f -print0 | xargs -0 sed -i 's@#include <sha3.h>@#include <cryptopp/sha3.h>@g'
  find . -type f -print0 | xargs -0 sed -i 's@#include <sha.h>@#include <cryptopp/sha.h>@g'
  find . -type f -print0 | xargs -0 sed -i 's@#include <ripemd.h>@#include <cryptopp/ripemd.h>@g'

  msg 'Fixing CMakeLists.txt...'
  sed -i "s@jsonrpc/rpc.h@rpc/rpc.h@" CMakeLists.txt

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

build() {
  cd $srcdir/cpp-$pkgname-poc-5

  msg 'Building ethereum and alethzero...'
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCMAKE_THREAD_LIBS_INIT=pthread ./
  make
}

package() {
  cd $srcdir/cpp-$pkgname-poc-5

  msg 'Installing...'
  make DESTDIR="$pkgdir" install
  install -Dm 755 alethzero/alethzero $pkgdir/usr/bin/alethzero
  install -Dm 755 test/testeth        $pkgdir/usr/bin/testeth
  install -Dm 755 walleth/walleth     $pkgdir/usr/bin/walleth
}
