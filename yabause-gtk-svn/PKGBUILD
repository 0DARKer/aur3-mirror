pkgname=yabause-gtk-svn
pkgver=3100
pkgrel=1
pkgdesc="A Sega Saturn emulator. (svn)"
arch=(i686 x86_64)
license=("GPL")
depends=(gtkglext sdl freeglut mini18n-svn)
makedepends=(subversion)
url="http://yabause.org"
conflicts=(yabause yabause-qt yabause-qt-svn yabause-svn)
provides=(yabause)
replaces=(yabause-svn)
source=("rwx.patch")
md5sums=('2f6491ed2cbe88d78253617cfa68b22b')

_svntrunk="https://yabause.svn.sourceforge.net/svnroot/yabause/trunk"
_svnmod="yabause"

_optimal_make_jobs() {
  if [ -r /proc/cpuinfo ]; then
    local core_count=$(grep -Fc processor /proc/cpuinfo)
  else
    local core_count=0
  fi
  if [ $core_count -gt 1 ]; then
    echo -n $[$core_count-1]
  else
    echo -n 1
  fi
}

build() {
  cd "${srcdir}"

  if [ -d "${_svnmod}/.svn" ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver $_svnmod
  fi

  msg 'SVN checkout done or server timeout'

  rm -rf "${_svnmod}-build"
  cp -r "$_svnmod" "${_svnmod}-build"
  cd "${_svnmod}-build/yabause"
  patch -Np1 -i "${srcdir}"/rwx.patch
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DYAB_PORTS=gtk -DYAB_NETWORK=ON \
    -DYAB_OPTIMIZED_DMA=on \
    -DYAB_PERKEYNAME=ON
  make -j$(_optimal_make_jobs)
}

package() {
  cd "${srcdir}/${_svnmod}-build/yabause"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
