# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=auralquiz
pkgver=0.8.1
pkgrel=2
pkgdesc="A simple music quiz game, using your own music files."
arch=(i686 x86_64)
url="http://jancoding.wordpress.com/auralquiz/"
license=('GPL')
depends=('phonon-qt4')
install="$pkgname.install"
source=("http://qt-apps.org/CONTENT/content-files/139127-$pkgname-v$pkgver-src.tar.gz")
md5sums=('0eee47ac30728dd616fb9991f52996d6')

prepare() {
  cd $pkgname-v$pkgver-src
  [ -d b ] || mkdir b

  # locale path fix
  sed -i 's|:/translations|/usr/share/locale|' src/main.cpp  
}

build() {
  cd $pkgname-v$pkgver-src/b
  qmake-qt4 ..
  make
}

package() {
  cd $pkgname-v$pkgver-src

  # bin
  install -Dm755 b/$pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # man
  install -Dm644 manual/$pkgname.6 \
    "$pkgdir/usr/share/man/man6/$pkgname.6"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 icon/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # locales
  install -d "$pkgdir/usr/share/locale"
  install -Dm644 translations/*.qm \
    "$pkgdir/usr/share/locale"
}

# vim:set ts=2 sw=2 et:
