# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=eko
pkgver=0.7.1
pkgrel=1
pkgdesc="A simple sound editor based on Qt, libsndfile, and libsample rate."
arch=(i686 x86_64)
url="http://eko.sourceforge.net/"
license=('GPL')
depends=('qt4' 'jack')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop")
md5sums=('a7856ec123b9c4dc78d7e60c602ef84f'
         'a10ec56761ce95a8f8e7c9fa464ad0bb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  qmake-qt4 PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 icons/${pkgname}_icon.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
