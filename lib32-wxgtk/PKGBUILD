# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: Devin Cofer <ranguvar@archlinux.us>
# Contributor: Yggdrasil <tetzank@web.de>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Bob Finch <w9ya@arrl.net>
# Contributor: J. Santiago "Angelus" Hirschfeld <jsantiagoh@yahoo.com.ar>

_pkgname=wxgtk
pkgname=lib32-$_pkgname
pkgver=2.8.12
pkgrel=3
pkgdesc="GTK+ implementation of wxWidgets API for GUI (lib32)"
arch=(x86_64)
url=http://wxwidgets.org/
license=('custom:wxWindows')
depends=(lib32-gtk2 lib32-libgl lib32-libsm lib32-libxxf86vm lib32-sdl wxgtk)
makedepends=(ccache gcc-multilib lib32-glu lib32-libxt)
options=(ccache)  # Required to build libwx_gtk2u_mmedia
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-$pkgver.tar.bz2)
sha256sums=('3b0ac1d2d017683851841501c8e1b744b97242d684a1668ded61809b0504f707')
sha512sums=('e867148a573885fd3996db634daaf83929650a64ceacade4939841eadeb14b7de6259fb0d50a0b7fee440238bf606013f06b3f93f18ca11d16277c861379db3f')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    rm -rf wxWidgets-build/
    cp -r wxWidgets-$pkgver/ wxWidgets-build/
    cd wxWidgets-build/

    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib32 \
        --with-gtk=2 \
        --with-opengl \
        --enable-unicode \
        --enable-graphics_ctx \
        --disable-optimize \
        --disable-mediactrl \
        --with-regex=builtin \
        --with-libpng=sys \
        --with-libxpm=sys \
        --with-libjpeg=sys \
        --with-libtiff=sys \
        --with-sdl \
        --disable-precomp-headers
    make
    make -C locale allmo
    make -C contrib/src
}

package() {
    cd wxWidgets-build/
    make DESTDIR="$pkgdir" install
    make -C contrib/src DESTDIR="$pkgdir" install

    cd "$pkgdir"/
    rm -rf usr/{include,share,bin/wxrc{,-2.8}}
    mv usr/bin/wx-config{,32}
    install -d usr/share/licenses/
    ln -s $_pkgname usr/share/licenses/$pkgname
}
