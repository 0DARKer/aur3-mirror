# Maintainer: Devin Cofer <ranguvar@archlinux.us>
# Contributor: Yggdrasil <tetzank@web.de>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Bob Finch <w9ya@arrl.net>
# Contributor: J. Santiago "Angelus" Hirschfeld <jsantiagoh@yahoo.com.ar>

pkgname=lib32-wxgtk
_pkgname32=wxgtk
pkgver=2.8.11
pkgrel=2
pkgdesc="GTK+ implementation of wxWidgets API for GUI (lib32)"
arch=('x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')

makedepends=('gcc-multilib' 'gconf' 'ccache' 'lib32-mesa')
depends=('lib32-glibc' 'wxgtk' 'lib32-gtk2' 'lib32-libgl' 'lib32-libxxf86vm' 'lib32-libsm')

source=("http://downloads.sourceforge.net/wxwindows/wxGTK-$pkgver.tar.bz2")
sha256sums=('0dab1ac107d50f132e3b292711dc637f3d19d166c19821cbb62ab7d5acdabc5c')

options=('ccache')  # Required to build libwx_gtk2u_mmedia
build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"

	cd "$srcdir/wxGTK-$pkgver"
	./configure --prefix=/usr --libdir=/usr/lib32 --with-gtk=2 \
	            --with-opengl --enable-unicode --enable-graphics_ctx \
	            --disable-optimize --disable-mediactrl --with-libpng=sys \
	            --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys
	make
	make -C locale allmo
	cd contrib/src && make
}
package() {
	cd "$srcdir/wxGTK-$pkgver"
	make DESTDIR="$pkgdir" install

	cd contrib/src
	make DESTDIR="$pkgdir" install

	rm -rf "$pkgdir"/usr/{include,share,bin}
}
