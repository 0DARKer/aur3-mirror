# Contributor: Stefan J. Betz <info@stefan-betz.net>
# Maintainer: Abakus <java5@arcor.de>
# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=opie
pkgver=2.32
pkgrel=4
pkgdesc="S/KEY Client utilities"
arch=('i686' 'x86_64')
license=('GPL')
url="http://packages.debian.org/sid/opie-server"
replaces=('opie-client' 'opie-server')
source=("http://ftp.de.debian.org/debian/pool/main/o/opie/opie_$pkgver.dfsg.1.orig.tar.gz"
        "http://ftp.de.debian.org/debian/pool/main/o/opie/opie_$pkgver.dfsg.1-0.2+squeeze1.diff.gz")
sha1sums=('111e543d61c94c005b41283adbf3789053d98fce'
          '675cded4faa1136ec8ae6a1a9e7906d47de11cf3')

build() {
  # Yes, I'm forced to do this because rebuilds error out otherwise.
  cd "$srcdir"
  rm -rf "opie-build"
  cp -a "opie-$pkgver.dfsg.1" "opie-build"
  cd "opie-build"

  patch -p1 < "../opie_$pkgver.dfsg.1-0.2+squeeze1.diff"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-retype ;

  # And I'm forced to do this because all builds error out otherwise.
  make -j1 all

  # I'm not forced to do this, but what the hell. Fixes roff syntax.
  sed -i 's/\(^\.BR opiegen(1)\) /&\n/' opiegen.1
}

package() {
  cd "$srcdir/opie-build"

  install -d "$pkgdir/etc"
  make install \
    LOCALBIN="$pkgdir/usr/bin" \
    KEY_FILE="$pkgdir/etc/opiekeys" \
    LOCK_DIR="$pkgdir/etc/opielocks" ;
  install -Dm0755 "opiegen" "$pkgdir/usr/bin/opiegen"

  for page in *.[0-9]; do
    install -Dm0644 "$page" "$pkgdir/usr/share/man/man${page##*.}/$page"
  done
}

# vim: ts=2:sw=2:et
