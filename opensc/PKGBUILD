#Maintainer: kevku <arc.mission at gmail>
pkgname=opensc
pkgver=0.12.0
pkgrel=1
pkgdesc="Access smart cards that support cryptographic operations"
arch=('x86_64' 'i686')
url="http://www.opensc-project.org/opensc"
license=("LGPL")
backup=('etc/opensc.conf')
depends=('openssl' 'pcsclite')
options=('!libtool')
source=("http://www.opensc-project.org/files/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('630fa3b8717d22a1d069d120153a0c52')
 
build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-pcsc --disable-static
  make
}
 
package(){
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR=${pkgdir} install 
  install -Dm644 etc/opensc.conf ${pkgdir}/etc/opensc.conf
} 
