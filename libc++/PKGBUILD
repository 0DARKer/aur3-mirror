# Maintainer: MThinkCpp <mtc.maintainer[at]outlook.com>
pkgname='libc++'
pkgver=3.3
pkgrel=1
pkgdesc='C++ Standard Library implementation - libc++ is a new implementation of the C++ standard library, targeting C++11.'
url='http://libcxx.llvm.org'
license=('custom: University of Illinois "BSD-Like"')
arch=('i686' 'x86_64')

depends=('glibc' 'gcc-libs>=4.8') ##gcc-libs for libsupc++ binary
makedepends=('clang>=3.2' 'subversion' 'cmake' 'gcc') #gcc provides libsupc++ headers

source=("http://www.llvm.org/releases/${pkgver}/libcxx-${pkgver}.src.tar.gz") #Extracts to a folder ${srcdir}/libcxx.src
md5sums=('SKIP')

build() {
#get gcc libsupc++ include directories (and save to variables)
  _gcc_include_dir=/usr/include/c++/$(gcc -dumpversion)
  _gcc_platform_include_dir=${_gcc_include_dir}/$(gcc -dumpmachine)

#Build libc++ (see http://libcxx.llvm.org for more detail on the instructions)
#Build is done with clang
  mkdir "${srcdir}/libcxx-${pkgver}.src/build"
  cd "${srcdir}/libcxx-${pkgver}.src/build"
  CC=clang CXX=clang++ cmake -G "Unix Makefiles" -DLIBCXX_CXX_ABI=libsupc++ -DLIBCXX_LIBSUPCXX_INCLUDE_PATHS="${_gcc_include_dir};${_gcc_platform_include_dir}"\
			     -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr "${srcdir}/libcxx-${pkgver}.src"
  make
}
package() {
#license
  install -Dm644 "${srcdir}/libcxx-${pkgver}.src/LICENSE.TXT" "$pkgdir/usr/share/licenses/$pkgname/license.txt"
#Install libc++ to the ${pkgdir}
  cd "${srcdir}/libcxx-${pkgver}.src/build"
  make DESTDIR="${pkgdir}" install
}