# Maintainer: XZS <d dot f dot fischer at web dot de>
pkgname=python-grip-git
pkgver=2.0.1
pkgrel=1
pkgdesc="Preview GitHub Markdown files like Readme locally before committing them."
arch=('any')
url="https://github.com/joeyespo/grip"
license=('MIT')
depends=('python-requests' 'python-flask' 'python-docopt'
         'python-markdown' 'python-pygments' 'python-path-and-address')
makedepends=('python-setuptools' 'git')
options=(!emptydirs)
source=("$pkgname::git://github.com/joeyespo/grip.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  grep -Po "(?<=version=').*(?=',)" setup.py
}

prepare () {
  cd "$srcdir/$pkgname"
  sed -i 's/Werkzeug==0.9.6/Werkzeug>=0.6.4/' requirements.txt
  sed -i 's|Flask(\(.*\))|Flask(\1, instance_path=os.path.expanduser("~/.cache/grip"))|' grip/server.py
}

package() {
  cd "$srcdir/$pkgname"
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
