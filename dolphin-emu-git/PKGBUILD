# Maintainer: Lightning <sgsdxzy@gmail.com>
# Contributor: Joe Amenta <amentajo@msu.edu>
# Contributor:  Devin Cofer <ranguvar@archlinux.us>
# Contributor: Hans-Kristian Arntzen <maister@archlinux.us>
# Contributor: Zephyr
# Special thanks to Zhou Xin <soul916@hotmail.com> for rewriting this to use cmake

pkgname=dolphin-emu-git
pkgver=20111022
pkgrel=1
pkgdesc="A GameCube and Wii emulator (Dev tree Git version)"
arch=('i686' 'x86_64')
url="http://www.dolphin-emulator.com/"
license=('GPL2')

makedepends=('git' 'cmake' 'glproto' 'opencl-headers')
depends=('sdl' 'libxxf86vm' 'libao' 'portaudio' 'wxgtk>=2.8' 'wxgtk<2.9' 'wiiuse' 'nvidia-cg-toolkit' 'glew' 'mesa' 'libgl')
provides=("dolphin-emu=${pkgver}-${pkgrel}")
conflicts=('dolphin-emu' 'dolphin-emu-svn-bin' 'dolphin-emu-svn')

source=('dolphin-emu.desktop'
	'Dolphin_Logo.png')

sha256sums=('1cf1cc034ad03cf068d39fd3bac0103dfb663695966a6846e530d8b8cb363470'
	    '4ef79175305df7bb9201931870c4bedc5eda9c94b65311973dfd41c1128784d1')
_gitroot=http://code.google.com/p/dolphin-emu/
_gitname=dolphin-emu


build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."


  rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  mkdir -p "${srcdir}/${_gitname}-build/build"
  cd "${srcdir}/${_gitname}-build/build"

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make

}
package() {
	cd "${srcdir}/${_gitname}-build/build"

	make DESTDIR=${pkgdir} install

	install -Dm644 "${srcdir}/dolphin-emu.desktop" "${pkgdir}/usr/share/applications/dolphin-emu.desktop"
	install -Dm644 "${srcdir}/Dolphin_Logo.png" "${pkgdir}/usr/share/icons/dolphin-emu.png"
}
