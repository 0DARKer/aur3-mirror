# Maintainer: Alexey D. <lq07829icatm at rambler.ru>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kate-git
pkgver=r14371.6dc9832
pkgrel=1
pkgdesc='An advanced editor component which is used in numerous KDE applications requiring a text editing component'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/applications/kate'
license=('LGPL')
depends=('kinit-git' 'ktexteditor-git' 'kitemmodels-git' 'kwallet-git' 'knewstuff-git')
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git')
provides=('kate')
conflicts=('kate')
source=('git://anongit.kde.org/kate.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kate
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kate \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
