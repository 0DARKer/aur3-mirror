# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=calfbox
pkgver=0.0.3.ac1243b
pkgrel=1
pkgdesc="Open source music instrument"
arch=(i686 x86_64)
url="http://repo.or.cz/w/calfbox.git"
license=('GPL3')
depends=('python' 'util-linux' 'fluidsynth' 'libsmf' 'libusbx')
source=("http://repo.or.cz/w/calfbox.git/snapshot/ac1243b033985ca9a6c445ec64164d54af387169.tar.gz")
md5sums=('53530e5feaa5cf4d692557792eb064a6')

build() {
  cd "$srcdir/$pkgname"
  ./autogen.sh
  ./configure --prefix=/usr
  make

  # python module
  python setup.py build
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install

  # python module
  python setup.py install --root="$pkgdir/"
}

# vim:set ts=2 sw=2 et: