# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-font-ttf
_pkgname=Font-TTF
pkgver=0.48
pkgrel=3
pkgdesc="Font::TTF - Perl module for TrueType Font hacking"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/${_pkgname}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.10.0')
source=("http://search.cpan.org/CPAN/authors/id/M/MH/MHOSKEN/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('3458472a2985d14e07820d911fb1bf5ef6f9deac9d6ba8da3ea6f3576fb5df37487a7ca8c378cff2d97a7b0dd79655173a404b718ca7a089e9dc0c9be28e9157')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
