# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Francois Charette <firmicus@gmx.net>

_perlmod=Font-TTF
_modnamespace=Font
pkgname=perl-font-ttf
pkgver=1.03
pkgrel=2
pkgdesc="Font::TTF - Perl module for TrueType Font hacking"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('Artistic2.0')
depends=('perl-io-string')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('9f8ed60137646cf9e6e8c1fb73dd02cf00056bb3fbcdca85adc9d46c922c2f647b97c4f55e16c09ee7b6d68ee321988b10eb17cdfd5d5be638121b76aac293da')

build() {
	cd "${srcdir}/${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${srcdir}/${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}/"
}
