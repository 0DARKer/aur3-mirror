# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Francois Charette <firmicus@gmx.net>

_perlmod=Font-TTF
_modnamespace=Font
pkgname=perl-font-ttf
pkgver=1.05
pkgrel=1
pkgdesc="Font::TTF - Perl module for TrueType Font hacking"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('Artistic2.0')
depends=('perl-io-string')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('7288a2de63fc92c36f3706f365b2ea886e37361b13400a14f940961b0263f6492b02f73048c653bdcd5c5d1bb714977f3c5326b7217dcf3683b16d5048cd1e63')

build() {
	cd "${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}"
}
