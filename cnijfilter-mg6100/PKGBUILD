# Maintainer: Fortunato Ventre (voRia) <vorione AT gmail DOT com>
#
# Here is a list of printer names and IDs (taken from the README file):
#
#   name - id
#-----------
#  mp250 - 356
#  mp280 - 370
#  mp495 - 369
# mg5100 - 373
# ip4800 - 375
# mg5200 - 374
# mg6100 - 376
# mg8100 - 377
#
# Change the following variables accordingly:
_name=mg6100
_id=376

pkgname=cnijfilter-${_name}
pkgver=3.40
pkgrel=6
_pkgver=3.40-1
pkgdesc="Canon IJ Printer Driver (${_name} series)"
url="http://support-au.canon.com.au/contents/AU/EN/0100302002.html"
arch=('i686' 'x86_64')
license=('custom')
depends=('popt' 'gtk2' 'cups')
source=(http://gdlp01.c-wss.com/gds/0/0100003020/01/cnijfilter-source-${_pkgver}.tar.gz
	fix_includes.patch
	fix_png.patch
	fix_mg6100_ppd.patch)
md5sums=('609975a05d6050fcca88f312d3f35c6a'
         '5c92d9bec6eb153200d1b4474c0939fb'
         '5f665042df2175da3629667aaf258782'
         'dd51ed81963669cd34cbef741d1a9724')

if [ "$CARCH" == "x86_64" ]; then  
	_libdir=libs_bin64
else
	_libdir=libs_bin32
fi

build() {
	## Apply patches
	cd ${srcdir}/cnijfilter-source-${_pkgver}
	patch -p1 -i ${srcdir}/fix_includes.patch || return 1
	patch -p1 -i ${srcdir}/fix_png.patch || return 1
	if [ "${_name}" == "mg6100" ]; then
		patch -p1 -i ${srcdir}/fix_mg6100_ppd.patch || return 1
	fi
  
	## Compile model specific stuff
	# ppd file
	cd ${srcdir}/cnijfilter-source-${_pkgver}/ppd
	./autogen.sh --prefix=/usr --enable-ppdpath=/usr/share/cups/model --program-suffix=${_name}
	make clean || return 1
	make || return 1
	# cnijfilter
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cnijfilter
	./autogen.sh --prefix=/usr --enable-libpath=/usr/lib/bjlib --enable-binpath=/usr/bin --program-suffix=${_name}
	make clean || return 1
	make || return 1
	# printui
	cd ${srcdir}/cnijfilter-source-${_pkgver}/printui
	./autogen.sh --prefix=/usr --datadir=/usr/share --program-suffix=${_name}
	make || true # Needed to avoid errors while building locales
	make clean || return 1
	make || return 1
	# lgmon
	cd ${srcdir}/cnijfilter-source-${_pkgver}/lgmon
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin --program-suffix=${_name}
	make clean || return 1
	make || return 1
	# cngpijmon
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cngpijmon
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin --datadir=/usr/share --program-suffix=${_name}
	make clean || return 1
	make || return 1
	
	## Compile common stuff
	# libs
	cd ${srcdir}/cnijfilter-source-${_pkgver}/libs
	./autogen.sh --prefix=/usr
	make clean || return 1
	make || return 1
	# cngpij
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cngpij
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin
	make clean || return 1
	make || return 1
	# pstocanonij
	cd ${srcdir}/cnijfilter-source-${_pkgver}/pstocanonij
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin
	make clean || return 1
	make || return 1
	# backend
	cd ${srcdir}/cnijfilter-source-${_pkgver}/backend
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin
	make clean || return 1
	make || return 1
	# backendnet
	cd ${srcdir}/cnijfilter-source-${_pkgver}/backendnet
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin LDFLAGS="-L../../com/${_libdir}"
	make clean || return 1
	make || return 1
	# sm sub process
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cngpijmon/cnijnpr
	./autogen.sh --prefix=/usr LIBS=-ldl
	make clean || return 1
	make || return 1
}

package() {
	## Install model specific stuff
	# ppd file
	cd ${srcdir}/cnijfilter-source-${_pkgver}/ppd
	make install DESTDIR=${pkgdir} || return 1
	# cnijfilter
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cnijfilter
	make install DESTDIR=${pkgdir} || return 1
	# printui
	cd ${srcdir}/cnijfilter-source-${_pkgver}/printui
	make install DESTDIR=${pkgdir} || return 1
	# lgmon
	cd ${srcdir}/cnijfilter-source-${_pkgver}/lgmon
	make install DESTDIR=${pkgdir} || return 1
	# cngpijmon
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cngpijmon
	make install DESTDIR=${pkgdir} || return 1
	
	## Install common stuff
	# libs
	cd ${srcdir}/cnijfilter-source-${_pkgver}/libs
	make install DESTDIR=${pkgdir} || return 1
	# cngpij
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cngpij
	make install DESTDIR=${pkgdir} || return 1
	# pstocanonij
	cd ${srcdir}/cnijfilter-source-${_pkgver}/pstocanonij
	make install DESTDIR=${pkgdir} || return 1
	# backend
	cd ${srcdir}/cnijfilter-source-${_pkgver}/backend
	make install DESTDIR=${pkgdir} || return 1
	# backendnet
	cd ${srcdir}/cnijfilter-source-${_pkgver}/backendnet
	make install DESTDIR=${pkgdir} || return 1
	# sm sub process
	cd ${srcdir}/cnijfilter-source-${_pkgver}/cngpijmon/cnijnpr
	make install DESTDIR=${pkgdir} || return 1
	
	## Install model specific libraries
	install -d ${pkgdir}/usr/lib/
	install -m 755 ${srcdir}/cnijfilter-source-${_pkgver}/${_id}/${_libdir}/*so.* ${pkgdir}/usr/lib/
	install -d ${pkgdir}/usr/lib/bjlib/
	install -m 644 ${srcdir}/cnijfilter-source-${_pkgver}/${_id}/database/* ${pkgdir}/usr/lib/bjlib/
	
	## Install common libraries
	install -m 755 ${srcdir}/cnijfilter-source-${_pkgver}/com/${_libdir}/*so.* ${pkgdir}/usr/lib/
	install -m 666 ${srcdir}/cnijfilter-source-${_pkgver}/com/ini/cnnet.ini ${pkgdir}/usr/lib/bjlib/
	
	## Make symbolic links for libraries
	cd ${pkgdir}/usr/lib/
	ln -s libcnnet.so.1.2.0 libcnnet.so
	ln -s libcnbpcmcm${_id}.so.8.0.1 libcnbpcmcm${_id}.so
	ln -s libcnbpcnclapi${_id}.so.3.5.0 libcnbpcnclapi${_id}.so
	ln -s libcnbpcnclbjcmd${_id}.so.3.3.0 libcnbpcnclbjcmd${_id}.so
	ln -s libcnbpcnclui${_id}.so.3.6.0 libcnbpcnclui${_id}.so
	ln -s libcnbpess${_id}.so.3.3.3 libcnbpess${_id}.so
	ln -s libcnbpess${_id}.so.3.3.3 libcnbpo${_id}.so
	
	## Install license files
	cd ${srcdir}/cnijfilter-source-${_pkgver}
	install -D LICENSE-cnijfilter-${pkgver}EN.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-cnijfilter-${pkgver}EN.txt
	install -D LICENSE-cnijfilter-${pkgver}FR.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-cnijfilter-${pkgver}FR.txt
	install -D LICENSE-cnijfilter-${pkgver}JP.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-cnijfilter-${pkgver}JP.txt
	install -D LICENSE-cnijfilter-${pkgver}SC.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-cnijfilter-${pkgver}SC.txt
}

# vim:set ts=2 sw=2 et:
