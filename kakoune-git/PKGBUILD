# Maintainer: Guillaume ALAUX <Guillaume at ALAUX dot net>
# Contributor: Shizeeg Unadequatov <shizeeque@gmail.com>

pkgname=kakoune-git
pkgver=r2524.c148966
pkgrel=1
pkgdesc='Code editor heavily inspired by Vim'
arch=('i686' 'x86_64')
url='https://github.com/mawww/kakoune'
license=('custom:unlicense')
depends=('ncurses' 'boost')
optdepends=('xorg-xmessage: for better debugging messages'
            'xdotool: if used in kak files')
makedepends=('git')

source=("$pkgname::git+https://github.com/mawww/kakoune.git"
        '01_ncurses.patch')
sha256sums=('SKIP'
            '09e1ef4d02f87246e2a6bbbcb9546b34700c60f8d61396e85bf41a60a8f5c00a')

pkgver() {
  cd "${srcdir}/${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 < "${srcdir}"/01_ncurses.patch
}

build() {
  cd "${srcdir}/${pkgname}/src"
  make
}

package() {
  cd "${srcdir}/${pkgname}/src"
  make install DESTDIR="${pkgdir}" PREFIX=/usr
  install -D ../UNLICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
