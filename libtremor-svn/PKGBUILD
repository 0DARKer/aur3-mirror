# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: rabyte <rabyteNOSPAM_at_moonblade.dot.org>

pkgname=libtremor-svn
pkgver=18153
pkgrel=1
pkgdesc="An integer-only, fully Ogg Vorbis compliant software decoder library"
arch=('i686' 'x86_64')
url="http://www.xiph.org/vorbis/"
license=('BSD')
depends=('libogg')
makedepends=('subversion')
provides=('libtremor')
conflicts=('libtremor')
options=('!libtool')

_svntrunk=http://svn.xiph.org/trunk/Tremor
_svnmod=tremor

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"
  
  # fix for automake 1.13
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.in

  #
  # BUILD HERE
  #
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
