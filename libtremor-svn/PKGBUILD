# Contributor: rabyte <rabyteNOSPAM_at_moonblade.dot.org>

pkgname=libtremor-svn
pkgver=15687
pkgrel=1
pkgdesc="An integer-only, fully Ogg Vorbis compliant software decoder library"
arch=('i686' 'x86_64')
url="http://www.xiph.org/vorbis/"
license=('BSD')
depends=('glibc')
makedepends=('subversion' 'autoconf' 'automake' 'libtool')
provides=('libtremor')
conflicts=('libtremor')
options=('!libtool')

_svntrunk=http://svn.xiph.org/trunk/Tremor
_svnmod=tremor

build() {
  [ -d $_svnmod/.svn ] && {
	cd $_svnmod
	svn up -r $pkgver
	cd ..
  } || {
	svn co $_svntrunk $_svnmod -r $pkgver --config-dir ./
  }

  msg "SVN checkout done or server timeout"

  rm -rf $_svnmod-build
  cp -rf $_svnmod/ $_svnmod-build/
  cd $_svnmod-build

  sed 's|-O2 -Wall||g' -i configure.in
  
  ./autogen.sh --prefix=/usr

  msg "Starting make..."

  make || return 1
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING

  rm -rf ../$_svnmod-build
}
