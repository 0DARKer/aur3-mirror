# Maintainer: Eric Anderson <ejona86 at gmail dot com>

pkgname=swordandsworcery
pkgver=1.0
pkgrel=3
pkgdesc="An exploratory action adventure videogame with an emphasis on audiovisual style (game purchased separately)"
url="http://www.swordandsworcery.com/"
license=('custom')
arch=('i686' 'x86_64')
if [ "$CARCH" == "x86_64" ]; then
    depends=('lib32-mesa' 'lib32-curl' 'lib32-sdl' 'lib32-gcc-libs')
else
    depends=('mesa' 'curl' 'sdl' 'gcc-libs')
fi
options=(!strip)
_gamepkg="${pkgname}-linux-${pkgver}-1338343971.tar.gz"
source=("${pkgname}.desktop"
        'http://www.swordandsworcery.com/storage/icon_512.png'
        "${_gamepkg}")
md5sums=('cc5a308dbd2d19c3b183fab755f2d396'
         '071d74203e593188ee28c0913d57d4ff'
         '3e9eaf4ada5cf0ae01ae525a9225da94')
_gamedirname=SwordAndSworceryEP
PKGEXT='.pkg.tar'

package(){
    cd ${srcdir}

    mkdir -p "${pkgdir}/opt/"
    cp -R "${srcdir}/${_gamedirname}" "${pkgdir}/opt/"

    # Use system-provided versions
    rm "${pkgdir}/opt/SwordAndSworceryEP/lib/libstdc++.so.6.0.16"
    rm "${pkgdir}/opt/SwordAndSworceryEP/lib/libstdc++.so.6"
    rm "${pkgdir}/opt/SwordAndSworceryEP/lib/libcurl.so"
    rm "${pkgdir}/opt/SwordAndSworceryEP/lib/libSDL-1.2.so.0"

    install -D -m644 "${srcdir}/icon_512.png" \
        "${pkgdir}/opt/${_gamedirname}/icon_512.png"

    install -D -m644 "${srcdir}/${pkgname}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    install -D -m644 "${srcdir}/${_gamedirname}/eula.txt" \
        "${pkgdir}/usr/share/licenses/${pkgname}/eula.txt"

    mkdir -p "${pkgdir}/usr/bin"
    ln -s "/opt/${_gamedirname}/run.sh" "${pkgdir}/usr/bin/${pkgname}"
}
