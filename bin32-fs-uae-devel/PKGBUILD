# Maintainer: grimi <grimi at poczta dot fm>

_pkgname=fs-uae
_pkgver=1.3.24

pkgname=bin32-fs-uae-devel
pkgver=1.3.24
pkgrel=1
pkgdesc="An Amiga multi-platform emulator based on UAE/WinUAE, with a focus on emulating games (development/32-bit version)."
arch=("x86_64")
url="http://fengestad.no/wp/fs-uae/download-beta"
license=("GPL2")
depends=("lib32-sdl" "lib32-libpng" "lib32-glib2" "lib32-openal" "lib32-zlib" "lib32-mesa" "lib32-gettext" "xdg-utils")
makedepends=("libtool-multilib")
provides=("fs-uae" "fs-uae-devel")
conflicts=("fs-uae" "fs-uae-devel")
install="${pkgname}.install"
source=(http://fengestad.no/${_pkgname}/files/${_pkgver}/${_pkgname}-${_pkgver}.tar.gz)
md5sums=('411a152d13bc5e5c5cc3c9cbe0454b57')






build() {
   cd ${_pkgname}-${_pkgver}

   mv share/fs-uae/shaders/{Scale4xHQ,scale4xhq}.shader

   sed 's|\(sdl-config\)|\1-32|g' -i Makefile libfsemu/Makefile

   make CC="gcc -m32" CXX="g++ -m32" CFLAGS="${CFLAGS/x86-64/i686}" CXXFLAGS="${CXXFLAGS/x86-64/i686}"
}


package() {
   cd ${_pkgname}-${_pkgver}

   make install prefix="${pkgdir}/usr"
}


