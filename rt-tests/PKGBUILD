# Maintainer: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=0.89
pkgrel=2

pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
url="http://rt.wiki.kernel.org/"
arch=('i686' 'x86_64')
license=('GPL2')

depends=('python2' 'numactl')

source=("ftp://ftp.kernel.org/pub/linux/kernel/people/clrkwllms/$pkgname-$pkgver.tar.gz")
md5sums=('f3dcd23f3026a18512e968b4419d79a0')

prepare() {
  cd "$pkgname"
  sed -i s/'shell python'/'shell python2'/g Makefile
  sed -i s/_SC_NPROCESSORS_CONF/_SC_NPROCESSORS_ONLN/g src/cyclictest/cyclictest.c
  sed -i s/_SC_NPROCESSORS_CONF/_SC_NPROCESSORS_ONLN/g src/cyclictest/rt_numa.h
  sed -i s/'mpath = os.path.join(path, m) + ".ko"'/'mpath = os.path.join(path, m) + ".ko.gz"'/g src/hwlatdetect/hwlatdetect.py
}

build() {
  cd "$pkgname"
  make
}

package() {
  cd "$pkgname"
  make prefix=/usr DESTDIR="$pkgdir" install
}
