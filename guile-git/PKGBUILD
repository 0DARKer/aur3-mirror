pkgname=guile-git
_gitname=guile
pkgver=0
pkgrel=1
pkgdesc="A portable, embeddable Scheme implementation (Git snapshot)"
arch=(i686 x86_64)
license=(gpl)
makedepends=(git)
provides=(guile)
depends=(gc 'gmp>=4.3.1' gperf libffi libtool
         libunistring 'ncurses>=5.7' texinfo)
conflicts=(guile guile-devel)
options=('!strip')
source=("git://git.sv.gnu.org/$_gitname.git")
url="http://www.gnu.org/software/guile/"
md5sums=(SKIP)

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  ./autogen.sh
  ./configure --prefix=/usr --disable-error-on-warning
  make LDFLAGS+="-lpthread"
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir/" install
}