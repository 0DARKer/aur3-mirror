# Maintainer: Karol "Kenji Takahashi" Wozniak <wozniakk@gmail.com>
# Contributor: SeeSchloss <seeschloss@seos.fr>

pkgname=museekd-svn
pkgver=1209
pkgrel=4
pkgdesc="Museek+'s daemon. Museek+ is a fork of Hyriand's C++ daemon/client Soulseek P2P client, with many enhancements."
url="http://www.museek-plus.org/"
depends=('libogg' 'libvorbis' 'libxml++' 'pkgconfig' 'libevent')
optdepends=(
    'pyqt3: musetup-qt script'
    'pygtk: musetup-gtk script'
)
makedepends=('subversion' 'cmake' 'swig')
license=('GPL2')
arch=('i686' 'x86_64')
source=(
    'python2.patch'
    'museekd@.service'
)
md5sums=('18ef7c92283ea4cf1c9906f50a58746e'
         '785d5178f387777394b2bc3924311df8')
conflicts=('museekd' 'museekplus-svn' 'museek-plus')
provides=('museekd')
install=museekd.install

_svntrunk=http://www.museek-plus.org/svn/museek+/trunk/sources
_svnmod=museek+
_revnumber=$pkgver

build() {
    cd $srcdir

    if [ -d $_svnmod/.svn ]; then
        (cd $_svnmod && svn up -r $pkgver)
    else
        svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"
    msg "Starting make..."

    cp -r $_svnmod $_svnmod-build
    cd $_svnmod-build

    patch -p1 -i "${srcdir}/python2.patch"

    # fix for systemd service
    sed -i '139d' "museekd/main.cpp"
    sed -i '138a\   char *home = getenv("HOME"); \
    std::string configPath; \
    if(home != NULL) { \
        configPath = std::string(std::string(home) + "/.museekd/config.xml"); \
    }' "museekd/main.cpp"

    cmake -DNO_MUSEEQ=1 -DNO_PYMUCIPHER=1 -DCLIENTS=1 -DPREFIX=/usr
    make
}

package() {
    cd $srcdir/$_svnmod-build

    make DESTDIR="$pkgdir" install
    install -Dm755 scripts/museekd.rc $pkgdir/etc/rc.d/museekd
    install -Dm755 $srcdir/museekd@.service $pkgdir/usr/lib/systemd/system/museekd@.service

    rm -rf ${srcdir}/$_svnmod-build
}
