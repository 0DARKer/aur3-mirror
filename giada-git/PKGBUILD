# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
#
# reason for the git release: bleeding edge full feature build ;)
#
pkgname=giada-git
_gitname=giada
pkgver=760.70c4f59
pkgrel=1
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers (git)"
arch=('i686' 'x86_64')
url="http://www.giadamusic.com/"
license=('GPL3')
depends=('libxpm' 'fltk' 'libpulse' 'jack' 'rtmidi>=2.1.0')
makedepends=('git')
install="giada.install"
source=("git+https://github.com/monocasual/giada.git"
    "giada-logo.png"
    "giada.desktop"
    'http://www.music.mcgill.ca/~gary/rtaudio/release/rtaudio-4.1.1.tar.gz'
    'rtaudio-4.1.1-giada.patch')

md5sums=('SKIP'
         'e712157099a1be39f2adcfc4630d961e'
         '135bef9e2df07b7550a2f4e4358b6933'
         '99763d3187fbe24ca959a1b711c17984'
         'f5a1e15f67528dea0ab6336fdab49312')
         
provides=('giada')
conflicts=('giada')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  patch -p1 -i "${srcdir}"/rtaudio-4.1.1-giada.patch
  (cd rtaudio-4.1.1; aclocal; autoconf -f)
  # replace
  rm -fr $_gitname/src/rtaudio-mod
  mv rtaudio-4.1.1 $_gitname/src/rtaudio-mod
  cd $_gitname; ./autogen.sh
}


build() {
  cd "$_gitname"
  CPPFLAGS+=" -I./rtaudio-mod" \
  ./configure --prefix=/usr --target=linux --enable-vst 
  (cd src; make libs)
  make
}

package() {
    cd "$_gitname"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/giada.desktop" "$pkgdir/usr/share/applications/giada.desktop"
    install -Dm644 "$srcdir/giada-logo.png" "$pkgdir/usr/share/pixmaps/giada.png"
}


