# Manteiner: Aguilera Dario <dario_21_06@hotmail.com>

pkgname=nautilus-elementary-ur
_realname=nautilus
pkgver=2.30.1
pkgrel=1
pkgdesc="The Elementary GNOME shell and file manager version, with undo/redo patch"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif>=0.6.19' 'exempi>=2.1.1' 'gnome-icon-theme>=2.30.0' 'desktop-file-utils' 'gvfs>=1.4.3' 'libunique>=1.1.6' 'gnome-desktop>=2.30.0')
makedepends=('pkgconfig' 'intltool')
replaces=('gnome-volume-manager' 'nautilus')

url="http://www.gnome.org"
groups=('gnome')
provides=('nautilus')
conflicts=('nautilus' 'nautilus-elementary')
options=('!libtool' '!emptydirs')
install=nautilus.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/2.30/${_realname}-${pkgver}.tar.bz2
        elementary.patch
        undo-redo.diff
        nautilus-condrestart.patch)
sha256sums=('83df19591c4d0d359fe67d16b3f73f71e6a2aa5ba7d477739276c7562f221144'
            '16b26921b5a64fd10ebe4712ec9dfab3e9c61cbd73779c498ee3e8553d669e1f'
            'b4e3a2bde91a4a8f29a36139101d719c90411c9df307f08267a4664ee4c4ec31'
            'f825292ef26274d3044272ca9f25095391cc49d30fb0056e7ed7346a1b4d106c')

build() {
  cd "${srcdir}"
  patch -p0 -i elementary.patch || return 1
  patch -p0 -i undo-redo.diff || return 1
  cd "${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/nautilus-condrestart.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static --libexecdir=/usr/lib/nautilus --disable-update-mimedb || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_realname}.schemas" --domain nautilus ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
