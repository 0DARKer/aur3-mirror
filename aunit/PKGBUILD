# Maintainer: Aay Jay Chan <aayjaychan at gmail dot com>
# Contributor: Christopher LÃ¼bbemeier <christopher dot luebbemeier at googlemail dot com>

pkgname=aunit
pkgver=2013
pkgrel=2
pkgdesc="GNAT GPL release of Ada unit testing framework"
arch=('i686' 'x86_64')
url="http://libre.adacore.com/tools/aunit/"
license=('GPL')
options=('staticlibs')
optdepends=(
    'gcc-ada: GNAT project files support'
    'gprbuild: GNAT project files support'
)
makedepends=('gprbuild')
changelog="ChangeLog"
source=("$pkgname-gpl-$pkgver-src.tgz::http://mirrors.cdn.adacore.com/art/93340c5991d85586950787ef76920fc2b901c5c4")
sha256sums=('da5a27caf3c2d0fe8bbddeffe495d1be9e26cf5dae5d3cdb2256e48f4cdb0a55')

build() {
    cd "$srcdir/$pkgname-gpl-$pkgver-src"
    make
}

check() {
    cd "$srcdir/$pkgname-gpl-$pkgver-src/test"
    make test
}

package() {
    cd "$srcdir/$pkgname-gpl-$pkgver-src"
    make install DESTDIR="$pkgdir/" INSTALL="$pkgdir/usr/"
}