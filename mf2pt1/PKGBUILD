# Maintainer: Patrick McCarty <pnorcks at gmail dot com>

pkgname=mf2pt1
pkgver=2.4.4
pkgrel=12
pkgdesc="Produce PostScript Type 1 fonts from Metafont source"
arch=('i686' 'x86_64')
url="http://www.ctan.org/tex-archive/support/mf2pt1/"
license=('LPPL')
depends=('perl' 't1utils' 'texlive-core')
makedepends=('texinfo')
optdepends=('fontforge: support for autohinting Type 1 fonts')
install=$pkgname.install
source=(http://www.ctan.org/get/support/$pkgname.zip)
md5sums=('aeabf4ea3308a1e4ca45019a58047afa')


build() {
  cd $srcdir/$pkgname

  mv mf2pt1.pl mf2pt1
  mpost -progname=mpost -ini mf2pt1 \\dump
  makeinfo mf2pt1.texi
  pod2man --center="User Commands" --date="27 January 2008" \
    --release="v2.4.4" mf2pt1 > mf2pt1.1
}

package() {
  cd $srcdir/$pkgname

  install -Dm755 mf2pt1 $pkgdir/usr/bin/mf2pt1

  install -d $pkgdir/usr/share/texmf-var/web2c/metapost
  install -m644 -t $pkgdir/usr/share/texmf-var/web2c/metapost mf2pt1.mem mf2pt1.log

  install -Dm644 mf2pt1.info $pkgdir/usr/share/info/mf2pt1.info
  install -Dm644 mf2pt1.1 $pkgdir/usr/share/man/man1/mf2pt1.1

  install -d $pkgdir/usr/share/doc/mf2pt1
  install -m644 -t $pkgdir/usr/share/doc/mf2pt1 ChangeLog README mf2pt1.pdf
}
