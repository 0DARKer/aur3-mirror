# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=mf2pt1
pkgver=2.4.4
pkgrel=13
pkgdesc="Produce PostScript Type 1 fonts from Metafont source"
arch=('i686' 'x86_64')
url="http://www.ctan.org/tex-archive/support/mf2pt1/"
license=('LPPL')
depends=('perl' 't1utils' 'texlive-core')
makedepends=('texinfo')
optdepends=('fontforge: support for autohinting Type 1 fonts')
install=$pkgname.install
source=(http://www.ctan.org/get/support/$pkgname.zip workaround.patch)
sha256sums=('a3eb840854693e38be27486878717bab274d2be34cc0abf7f20c55a453f64715'
            'd2216edae16989cce7f93249949c160cc1a9ba3c7c9a1b2c0af6c30f97c2ee7b')

build() {
  cd $srcdir/$pkgname

  patch -p1 -i $srcdir/workaround.patch
  mv mf2pt1.pl mf2pt1
  mpost -progname=mpost -ini mf2pt1.mp
  makeinfo mf2pt1.texi
  pod2man --center="User Commands" --date="27 January 2008" \
    --release="v2.4.4" mf2pt1 > mf2pt1.1
}

package() {
  cd $srcdir/$pkgname

  install -D -m755 mf2pt1 $pkgdir/usr/bin/mf2pt1
  install -D -m644 mf2pt1.mem $pkgdir/var/lib/texmf/web2c/metapost/mf2pt1.mem
  install -D -m644 mf2pt1.log $pkgdir/var/lib/texmf/web2c/metapost/mf2pt1.log
  install -D -m644 mf2pt1.info $pkgdir/usr/share/info/mf2pt1.info
  install -D -m644 mf2pt1.1 $pkgdir/usr/share/man/man1/mf2pt1.1

  mkdir -p $pkgdir/usr/share/doc/mf2pt1
  install -D -m644 ChangeLog $pkgdir/usr/share/doc/mf2pt1/ChangeLog
  install -D -m644 README $pkgdir/usr/share/doc/mf2pt1/README
  install -D -m644 mf2pt1.pdf $pkgdir/usr/share/doc/mf2pt1/mf2pt1.pdf
}
