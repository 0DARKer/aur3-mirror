# Maintainer: Stefan Seering <stefanarch@gmx.de>
pkgname=teamspeak3-plugin-massmover
pkgver='0.56.19'
pkgrel='7'
pkgdesc='Plugin for Teamspeak 3. Allows moving several users at once.'
arch=('i686' 'x86_64')
url='http://addons.teamspeak.com/directory/plugins/miscellaneous/TS3MassMover.html'
license=('Unknown')
makedepends=('teamspeak3') # the teamspeak package contains C header files
depends=('teamspeak3')
source=('TS3MassMover-v0.56.19.zip::http://addons.teamspeak.com/index.php?option=com_mtree&task=att_download&link_id=44&cf_id=24')
sha1sums=('62dde6cfbcc162cd7c1885205ae8489ebe33a905')
replaces=('teamspeak-plugin-massmover')

build() {
  cd -- "$srcdir/source"
  c++ ${CXXFLAGS} ${LDFLAGS} -shared -I /opt/teamspeak3/pluginsdk/include -fPIC -o TS3MassMover.so plugin.cpp
}

package() {
  cd -- "$srcdir/source"
  mkdir -p -- "$pkgdir/opt/teamspeak3/plugins"
  cp TS3MassMover.so "$pkgdir/opt/teamspeak3/plugins"
}

# vim:set ts=2 sw=2 et:
