# P7Zip-GUI: Installer: Arch
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>

realname=p7zip
pkgname=${realname}-gui
pkgver=9.13
pkgrel=2
pkgdesc="The GUI component of the P7Zip compression utility"
arch=("i686" "x86_64")
license=("GPL")
url="http://${realname}.sourceforge.net"
depends=("wxgtk" "${realname}=${pkgver}")
makedepends=("make")
optdepends=("q7z: A P7Zip GUI, which attempts to simplify data compression and backup")
options=(!emptydirs)
source=(http://downloads.sourceforge.net/sourceforge/${realname}/${realname}_${pkgver}_src_all.tar.bz2)

build() {
	cd ${srcdir}/${realname}_${pkgver}

	#Arch64 fix
	if [ "${CARCH}" == "x86_64" ]; then
		cp makefile.linux_amd64 makefile.machine
	else
		cp makefile.linux_x86_ppc_alpha_gcc_4.X makefile.machine
	fi

	sed -i "s|usr/local|usr|g" makefile || return 1

	make 7zG 7zFM OPTFLAGS="${CXXFLAGS}" || return 1
}

package() {
	cd ${srcdir}/${realname}_${pkgver}

	install -m755 -D bin/7zG ${pkgdir}/usr/lib/${realname}/7zG
	install -m755 -D bin/7zFM ${pkgdir}/usr/lib/${realname}/7zFM
	
	install -d ${pkgdir}/usr/bin/
	cp ${startdir}/extra/7z* ${pkgdir}/usr/bin/
	
	install -m755 -D GUI/${realname}ForFilemanager ${pkgdir}/usr/bin/${realname}ForFilemanager
	install -m755 -D GUI/${realname}_32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/${realname}.png
	ln -s /usr/lib/${realname}/7zCon.sfx ${pkgdir}/usr/lib/${realname}/7z.sfx
	
	# Desktop
	install -d ${pkgdir}/usr/share/applications/
	cp ${startdir}/extra/7zFM.desktop ${pkgdir}/usr/share/applications/
	
	# KDE
	install -d ${pkgdir}/usr/share/kde4/services/ServiceMenus/
	sed -i "s|\[Desktop Entry\]|[Desktop Entry]\nType=Service\nX-KDE-ServiceTypes=KonqPopupMenu/Plugin|g" GUI/kde/* || return 1
	sed -i "/Actions=/s|$|;|g" GUI/kde/* || return 1
	cp GUI/kde/* ${pkgdir}/usr/share/kde4/services/ServiceMenus/
	
	# Help
	find "GUI/help" -type d -exec chmod 755 {} ';'
	find "GUI/help" -type f -exec chmod 644 {} ';'
	cp -r GUI/help ${pkgdir}/usr/lib/${realname}/
}

sha1sums=('81da0729561ce123c0a82656ec96a04ad5bfa522')
