_pkgbasename=libexif
pkgname=lib32-$_pkgbasename
pkgver=0.6.20
pkgrel=1
pkgdesc="A library to parse an EXIF file and read the data from those tags (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://sourceforge.net/projects/libexif"
depends=('lib32-glibc' "$_pkgbasename")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://downloads.sf.net/sourceforge/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('19844ce6b5d075af16f0d45de1e8a6a3')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{include,share,lib32/*.a}
  # Install license
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
