# Maintainer: OmeGa <omega at mailoo dot org>

pkgname=firefox-extension-omnibar
_addons_id=8823
_addons_file=174569
pkgver=0.7.16.20121026
pkgrel=2
pkgdesc="Integrates the location bar and the search bar into one."
arch=('any')
url="https://addons.mozilla.org/firefox/addon/${_addons_id}"
license=('MPL')
depends=('firefox')
makedepends=('unzip')
source=(https://addons.mozilla.org/firefox/downloads/file/${_addons_file}/omnibar-${pkgver}.xpi)
noextract=("omnibar-${pkgver}.xpi")
sha1sums=('3bc20841e643eb9348178a9dd6b32eb989454eac')

build() {
  cd ${srcdir}
  [[ -d ${pkgname}.extract ]] || \
    mkdir ${pkgname}.extract
  cd ${pkgname}.extract
  unzip ../omnibar-${pkgver}.xpi
}
  
package() {
  cd ${srcdir}/${pkgname}.extract 
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf) || return 1
  local dstdir=${pkgdir}/usr/lib/firefox/extensions/${emid}
  install -d ${dstdir}
#  sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>20.*</em:maxVersion>#' install.rdf
  cp -R * ${dstdir}
  find ${pkgdir} -type d -exec chmod 0755 {} \;
  find ${pkgdir} -type f -exec chmod 0644 {} \;
}

# vim:set ts=2 sw=2 et:
