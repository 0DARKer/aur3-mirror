# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Jakub 'Icar' Jaróżek <machine.head.fan@gmail.com>

pkgname=xfburn-svn
pkgver=7715
pkgrel=1
pkgdesc="A simple CD/DVD burning tool based on libburnia libraries"
arch=('i686' 'x86_64')
url="http://www.xfce.org/projects/xfburn/"
license=('GPL')
depends=('desktop-file-utils' 'gstreamer0.10-base>=0.10.2' 'libburn>=0.4.2'
         'libisofs>=0.6.2' 'thunar')
makedepends=('pkgconfig>=0.9' 'subversion' 'xfce4-dev-tools')
provides=('xfburn')
conflicts=('xfburn')
install=xfburn.install

_svntrunk=http://svn.xfce.org/svn/goodies/xfburn/trunk
_svnmod=xfburn

build() {
  cd "$srcdir"

  msg "Connecting to the SVN server..."
  if [ -d "$_svnmod/.svn" ]; then
    cd $_svnmod && svn up -r $pkgver
    msg2 "Local files updated"
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    msg2 "SVN checkout done"
  fi

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  msg "Starting make..."
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --enable-gstreamer \
      --enable-thunar-vfs \
      --enable-dbus \
      --enable-hal
  make || return 1
  make DESTDIR="$pkgdir" install
}
