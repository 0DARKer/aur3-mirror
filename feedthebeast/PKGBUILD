# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Maxence Sartiaux <makz@sartiaux.be>

pkgname=feedthebeast
pkgver=1.3.0
pkgrel=1
pkgdesc="Feed The Beast minecraft launcher"
arch=(any)
url=http://feed-the-beast.com/
license=(Apache)
depends=(java-runtime xorg-server-utils openal ttf-dejavu)
install=$pkgname.install
_datesum=$(echo -n "mcepoch1$(curl -s http://www.creeperrepo.net/getdate)" | md5sum | awk '{print $1}')
source=($pkgname
    $pkgname.jar::http://www.creeperrepo.net/direct/FTB2/$_datesum/launcher^FTB_Launcher.jar
    $pkgname.desktop)
noextract=($pkgname.jar)
sha256sums=('35ebb0e4fc8a581c2aa6e939527f238800f4059bb5253c67eb3d478c463010b0'
    '4dfa9c9ed249956f3f80507312994b2173a7bfec7b1a74587316fe715d44f8d7'
    '92512859a7456c91244774b5b860f463305772c010ad58a7476bbafcd6874eb3')
sha512sums=('fc80ee6e419763266e25b824f6bd9220bc6eb3bc78781b35bd399e4b6ef4b7bc7bc9bb9967e5ba52b4e392f12b5383c30d73cadc1691c55b5ca566aef3ecd20b'
    '28591977b99d4fb13224c06b06936b0c6b14e259ccb3e1fcc97544295aa608e4b8e7efb6c240a107d1358be836eb9c0aa41f1e868459ce08717593579fa12069'
    '646d3712c2dab78dee960d435df8f0251e9e8aefdd04e5c2414d41e48e99d018ff0919ea2e75932a8054835e15fa5d04e934ab60d81528d8ff62131b69963a8a')

package() {
    install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
    install -Dm644 $pkgname.jar "$pkgdir"/usr/share/$pkgname/$pkgname.jar
    desktop-file-install $pkgname.desktop --dir "$pkgdir"/usr/share/applications/
    bsdtar -xf $pkgname.jar image/logo_ftb.png
    install -Dm644 image/logo_ftb.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
