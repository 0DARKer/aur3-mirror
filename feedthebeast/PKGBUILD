# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Maxence Sartiaux <makz@sartiaux.be>

pkgname=feedthebeast
pkgver=1.3.2
pkgrel=1
pkgdesc="Feed The Beast minecraft launcher"
arch=(any)
url=http://feed-the-beast.com/
license=(Apache)
depends=(java-runtime xorg-server-utils openal ttf-dejavu)
install=$pkgname.install
_datesum=$(echo -n "mcepoch1$(curl -s http://www.creeperrepo.net/getdate)" | md5sum | awk '{print $1}')
source=($pkgname
    $pkgname.jar::http://www.creeperrepo.net/direct/FTB2/$_datesum/launcher^FTB_Launcher.jar
    $pkgname.desktop)
noextract=($pkgname.jar)
sha256sums=('35ebb0e4fc8a581c2aa6e939527f238800f4059bb5253c67eb3d478c463010b0'
    '58f9a9995a09ef1ea3207212abb2e59183eddea41fbdff31b9f78aa9a17d7c7a'
    '92512859a7456c91244774b5b860f463305772c010ad58a7476bbafcd6874eb3')
sha512sums=('fc80ee6e419763266e25b824f6bd9220bc6eb3bc78781b35bd399e4b6ef4b7bc7bc9bb9967e5ba52b4e392f12b5383c30d73cadc1691c55b5ca566aef3ecd20b'
    '7da6bbb0fd8888a5de270bb0ca951a27a626dda7adff632d284f03d98537eb1e02aa25775499d1b79291fabd5b46d6245ab4064848d71a631ee47ccbd70c3984'
    '646d3712c2dab78dee960d435df8f0251e9e8aefdd04e5c2414d41e48e99d018ff0919ea2e75932a8054835e15fa5d04e934ab60d81528d8ff62131b69963a8a')

package() {
    install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
    install -Dm644 $pkgname.jar "$pkgdir"/usr/share/$pkgname/$pkgname.jar
    desktop-file-install $pkgname.desktop --dir "$pkgdir"/usr/share/applications/
    bsdtar -xf $pkgname.jar image/logo_ftb.png
    install -Dm644 image/logo_ftb.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
