# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Maxence Sartiaux <makz@sartiaux.be>

pkgname=feedthebeast
pkgver=1.2.9
pkgrel=1
pkgdesc="Feed The Beast minecraft launcher"
arch=(any)
url=http://feed-the-beast.com/
license=(Apache)
depends=(java-runtime xorg-server-utils openal ttf-dejavu)
install=$pkgname.install
_datesum=$(echo -n "mcepoch1$(curl -s http://www.creeperrepo.net/getdate)" | md5sum | awk '{print $1}')
source=($pkgname
    $pkgname.jar::http://www.creeperrepo.net/direct/FTB2/$_datesum/launcher^FTB_Launcher.jar
    $pkgname.desktop)
noextract=($pkgname.jar)
sha256sums=('35ebb0e4fc8a581c2aa6e939527f238800f4059bb5253c67eb3d478c463010b0'
    '47c970d7089fd05f38737c40b8bc004969c4e5c76a63272d83a5b098da88c6ee'
    '92512859a7456c91244774b5b860f463305772c010ad58a7476bbafcd6874eb3')
sha512sums=('fc80ee6e419763266e25b824f6bd9220bc6eb3bc78781b35bd399e4b6ef4b7bc7bc9bb9967e5ba52b4e392f12b5383c30d73cadc1691c55b5ca566aef3ecd20b'
    '4e67d184c06c15bc981b17b6a297885c505b778e529f5da0a6317c3e2b3a77b392c767b4ea1e35aae7b29104a0181b1ebc31ef7a956015addc5f2f241251d68d'
    '646d3712c2dab78dee960d435df8f0251e9e8aefdd04e5c2414d41e48e99d018ff0919ea2e75932a8054835e15fa5d04e934ab60d81528d8ff62131b69963a8a')

package() {
    install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
    install -Dm644 $pkgname.jar "$pkgdir"/usr/share/$pkgname/$pkgname.jar
    desktop-file-install $pkgname.desktop --dir "$pkgdir"/usr/share/applications/
    bsdtar -xf $pkgname.jar image/logo_ftb.png
    install -Dm644 image/logo_ftb.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
