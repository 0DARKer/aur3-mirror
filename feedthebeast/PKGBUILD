# Maintainer:  Ainola <opp310@alh.rqh> (ROT13 it)
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Maxence Sartiaux <makz@sartiaux.be>

pkgname=feedthebeast
pkgver=1.4.6_303
pkgrel=1
epoch=1
pkgdesc="Feed The Beast offers many different styles of modpacks to the community, catering to all tastes."
arch=('any')
url=('http://feed-the-beast.com/')
license=('Apache')
depends=('java-runtime' 'java-environment' 'hicolor-icon-theme' 'xorg-xrandr' 'ttf-dejavu')
makedepends=('libicns')
install=("${pkgname}.install")
source=("$pkgname"
        "${pkgname}.jar"::"http://www.creeperrepo.net/FTB2/maven/net/ftb/FTB_Launcher/${pkgver/_/-}/FTB_Launcher-${pkgver/_/-}.jar"
        "${pkgname}.desktop"
        "${pkgname}.install")
noextract=("${pkgname}.jar")
sha256sums=('35ebb0e4fc8a581c2aa6e939527f238800f4059bb5253c67eb3d478c463010b0'
            'a0e61f87d03fd16bbc74d5860c87fd4952cb87961b21be940b080a52a9f33b2b'
            '24ef17485069ada6a3ffb8f14d5537467767f4b71c57160696c25ccdaf6e60f8'
            'dfb83978fea9eddd28c2d90ab9eefa9554d5ae03dfc540554a829ac8ee8db83a')

package() {
    install -Dm755 "$pkgname" "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 "${pkgname}.jar" "${pkgdir}/usr/share/${pkgname}/${pkgname}.jar"
    install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    # icons are trapped inside .icns format, free them.
    bsdtar -xf "${pkgname}.jar" app/icon.icns
    icns2png -x app/icon.icns
    for size in 16 32 48 128 256; do
        install -Dm644 "icon_${size}x${size}x32.png" \
          "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}.png"
    done
}
