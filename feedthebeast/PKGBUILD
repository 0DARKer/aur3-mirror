# Maintainer:  Ainola <opp310@alh.rqh> (ROT13 it)
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Maxence Sartiaux <makz@sartiaux.be>

pkgname=feedthebeast
pkgver=10404
pkgrel=1
pkgdesc="Feed The Beast offers many different styles of modpacks to the community, catering to all tastes."
arch=(any)
url=http://feed-the-beast.com/
license=(Apache)
depends=(java-runtime xorg-server-utils openal ttf-dejavu)
makedepends=(desktop-file-utils libicns)
install=$pkgname.install
_datesum=$(echo -n "mcepoch1$(date '+%d%m%y')" | md5sum | awk '{print $1}')
source=($pkgname
        $pkgname.jar::http://www.creeperrepo.net/direct/FTB2/$_datesum/launcher^FTB_Launcher.jar
        $pkgname.desktop
        $pkgname.install)
noextract=($pkgname.jar)
sha256sums=('35ebb0e4fc8a581c2aa6e939527f238800f4059bb5253c67eb3d478c463010b0'
            '22678830facbac6d6e17ff2c29f33688069f0d60ee4effeeceef255309e50138'
            '24ef17485069ada6a3ffb8f14d5537467767f4b71c57160696c25ccdaf6e60f8'
            '4f345b7840f9a5490224f4a029c4c5f68ddaa29f15d1583f64a879b5728ea8c7')

package() {
    install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
    install -Dm644 "$pkgname.jar" "$pkgdir/usr/share/$pkgname/$pkgname.jar"
    desktop-file-install "${pkgname}.desktop" --dir "$pkgdir/usr/share/applications/"

    # icons are trapped inside .icns format, free them.
    bsdtar -xf "$pkgname.jar" app/icon.icns
    icns2png -x app/icon.icns
    for size in 16 32 48 128 256; do
        install -Dm644 "icon_${size}x${size}x32.png" \
          "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}.png"
    done
}
