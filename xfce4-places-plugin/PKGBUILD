# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=xfce4-places-plugin
pkgver=1.2.0
pkgrel=2
pkgdesc="Menu with quick access to folders, documents, and removable media"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-places-plugin"
license=('GPL2')
depends=('libnotify' 'libxfcegui4' 'xfce4-panel')
makedepends=('intltool')
source=("http://archive.xfce.org/src/panel-plugins/${pkgname}/1.2/${pkgname}-${pkgver}.tar.bz2"
        "01_port-to-exo-1.patch"
        "02_port-to-gio.patch")
md5sums=('f2d8c13340b3d52c5a7f6e2b9cdc55e3'
         '2090b5be8877b14526d125e15545414c'
         '61da0537ebc4bc8dae3b8fa413428394')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # configure fix
  sed -i -e "s/exo-0.3/exo-1/g" configure

  patch -Np1 -i ${srcdir}/01_port-to-exo-1.patch
  patch -Np1 -i ${srcdir}/02_port-to-gio.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var \
              --disable-static \
              --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

