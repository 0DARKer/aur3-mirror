# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=xfce4-places-plugin
pkgver=1.3.0
pkgrel=3
pkgdesc="Places menu plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-places-plugin"
license=('GPL2')
depends=('libnotify' 'xfce4-panel')
makedepends=('intltool')
source=("http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2"
        "01_use-preferred-filemanager.patch"
        "02_remove-warnings.patch"
        "03_fix-save-config-file.patch")
sha1sums=('b59204735262a9ff4d5caf9ee3e5eabc4b974898'
          'da3b99b4f7e9804bf119eaedc2b16d63adb5e6f5'
          '0b93aa2c39a9f7075d21931e7bcff8bdbd34b870'
          'fb7efbbd88b4257abfcf74a19429c41487bd9395')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir"/01_use-preferred-filemanager.patch
  patch -Np1 -i "$srcdir"/02_remove-warnings.patch
  patch -Np1 -i "$srcdir"/03_fix-save-config-file.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var \
              --disable-static \
              --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
