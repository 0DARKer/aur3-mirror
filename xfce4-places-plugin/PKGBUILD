# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=xfce4-places-plugin
pkgver=1.2.0
pkgrel=4
pkgdesc="Places menu for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-places-plugin"
license=('GPL2')
depends=('libnotify' 'libxfcegui4' 'xfce4-panel')
makedepends=('intltool' 'xfce4-dev-tools')
source=("http://archive.xfce.org/src/panel-plugins/${pkgname}/1.2/${pkgname}-${pkgver}.tar.bz2"
        "01_port-to-exo-1.patch"
        "02_use-preferred-filemanager.patch"
        "03_port-to-gio.patch"
        "04_fix-segfault.patch")
md5sums=('f2d8c13340b3d52c5a7f6e2b9cdc55e3'
         '2090b5be8877b14526d125e15545414c'
         '77b5da689f542f28e1c11663d3b85566'
         '61da0537ebc4bc8dae3b8fa413428394'
         'f678120c4070b58b66f29eabc7803888')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/01_port-to-exo-1.patch"
  patch -Np1 -i "${srcdir}/02_use-preferred-filemanager.patch"
  patch -Np1 -i "${srcdir}/03_port-to-gio.patch"
  patch -Np1 -i "${srcdir}/04_fix-segfault.patch"

  export LDFLAGS="${LDFLAGS} -lX11"

  NOCONFIGURE=1 xdt-autogen
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var \
              --disable-static \
              --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
