# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=xfce4-places-plugin
pkgver=1.3.0
pkgrel=2
pkgdesc="Places menu plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-places-plugin"
license=('GPL2')
depends=('libnotify' 'xfce4-panel')
makedepends=('intltool')
source=("http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2"
        "01_use-preferred-filemanager.patch"
        "02_fix-gtk-warning.patch")
sha1sums=('b59204735262a9ff4d5caf9ee3e5eabc4b974898'
          '4f6ab0b10a5bbe83b97e63ce0595be749eb4df5d'
          'f4ea8edf82f52c0b2fbef242eb37db3ef54fe792')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir"/01_use-preferred-filemanager.patch
  patch -Np1 -i "$srcdir"/02_fix-gtk-warning.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var \
              --disable-static \
              --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
