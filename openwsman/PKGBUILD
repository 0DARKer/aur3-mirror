pkgname=openwsman
pkgver=2.5.2
pkgrel=1
pkgdesc="Opensource Implementation of WS-Management"
arch=('i686' 'x86_64')
url="https://${pkgname}.github.io/"
license=('BSD')
depends=('libxml2' 'sblim-sfcc')
makedepends=('cmake' 'swig'
             'perl'
             'python2'
             'ruby' 'ruby-docs')
optdepends=('perl: for perl bindings'
            'python2: for python bindings'
            'ruby: for ruby bindings')
backup=("etc/${pkgname}/${pkgname}.conf"
        "etc/${pkgname}/${pkgname}_client.conf"
        "etc/${pkgname}/ssleay.cnf"
        "etc/pam.d/${pkgname}")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Openwsman/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('625500ea2e2a48016e2226edee4efd61')

build() {
    cd "$pkgname-$pkgver"
    cmake -DCMAKE_BUILD_TYPE=Release             \
          -DCMAKE_INSTALL_PREFIX=/usr            \
          -DCMAKE_SKIP_RPATH=ON                  \
          -DPACKAGE_ARCHITECTURE=${CARCH}        \
          -DLIB=lib                              \
          -DBUILD_RUBY_GEM=OFF                   \
          -DBUILD_JAVA=OFF                       \
          -DPYTHON_EXECUTABLE=/usr/bin/python2.7
    make
}

# TODO: find out why tests fail and report bugs upstream if necessary
#check() {
#    cd "$pkgname-$pkgver"
#    sed -e 's/python/python2/' -i bindings/python/tests/run
#    make ARGS="-V" test
#}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install

    install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
    mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
    install -dm755 "$pkgdir/usr/share/doc/${pkgname}"
    mv "bindings/ruby/html/" "$pkgdir/usr/share/doc/${pkgname}/rdoc"
}
