pkgname=openwsman
pkgver=2.4.12
pkgrel=1
pkgdesc="Opensource Implementation of WS-Management"
arch=('i686' 'x86_64')
url="https://${pkgname}.github.io/"
license=('BSD')
depends=('libxml2' 'sblim-sfcc')
makedepends=('cmake' 'swig'
             'perl'
             'python2'
             'ruby' 'ruby-docs')
optdepends=('perl: for perl bingdings'
            'python2: for python bingdings'
            'ruby: for ruby bingdings')
backup=("etc/${pkgname}/${pkgname}.conf"
        "etc/${pkgname}/${pkgname}_client.conf"
        "etc/${pkgname}/ssleay.cnf"
        "etc/pam.d/${pkgname}")
source=("https://github.com/Openwsman/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('4893a41c6fd89b33095c315c79638a75')

prepare() {
	cd "$pkgname-$pkgver"

    # Support ruby 2.2 (paste from Fedora SPEC file)
    cd bindings/ruby
    sed -e 's|rdoc2_1|rdoc2_2|' < rdoc2.1 > rdoc2.2
    chmod 0755 rdoc2.2
    ln -sf /usr/bin/rdoc rdoc2_2.rb
}

build() {
	cd "$pkgname-$pkgver"
    cmake -DCMAKE_BUILD_TYPE=Release             \
          -DCMAKE_INSTALL_PREFIX=/usr            \
          -DLIB=lib                              \
          -DPYTHON_EXECUTABLE=/usr/bin/python2.7
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install

    install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
    mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}
