# custom variables
_hkgname=Agda
_licensefile=LICENSE

# PKGBUILD options/directives
pkgname=haskell-agda
pkgver=2.2.10
pkgrel=1
pkgdesc="A dependently typed functional programming language and proof assistant"
url="http://wiki.portal.chalmers.se/agda/"
license=("OtherLicense")
arch=('i686' 'x86_64')
makedepends=()
depends=("ghc=7.0.3-2"
         "sh"
         "alex"
         "haskell-quickcheck=2.4.0.1"
         "haskell-array=0.3.0.2"
         "haskell-binary=0.5.0.2"
         "haskell-bytestring=0.9.1.10"
         "haskell-containers=0.4.0.0"
         "haskell-directory=1.1.0.0"
         "haskell-filepath=1.2.0.0"
         "haskell-haskeline=0.6.3.2"
         "haskell-haskell-src-exts=1.11.1"
         "haskell-mtl=2.0.1.0"
         "haskell-old-time=1.0.0.6"
         "haskell-pretty=1.0.1.2"
         "haskell-process=1.0.1.5"
         "haskell-syb=0.3"
         "haskell-xhtml=3000.2.0.1"
         "haskell-zlib=0.5.3.1")
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        "cabal.patch")
install="${pkgname}.install"
sha256sums=("5cd3b69a744b86b74344b790350d8a6f2b62f206f5a53a9cfad22cc3ca3309ae"
            "37ae08cd9a94ef619d22f82b62f985962010f57590ff06743ab3cac00e87bd31")

# PKGBUILD functions
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    #NP
    sed -i -e 's/\(import Data.Generics\)/\1 hiding (everythingBut)/' \
              src/full/Agda/Utils/Generics.hs \
              src/full/Agda/Interaction/Highlighting/Generate.hs
    patch Agda.cabal ${srcdir}/cabal.patch 
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}
}
