# Maintainer: Andrey Vihrov <andrey.vihrov at gmail.com>
# Contributor: megadriver <megadriver at gmx dot com>
# Based on clearlooks-phenix-gtk-theme-git, also in the AUR

pkgname=clearlooks-phenix-gtk-theme
pkgver=3.0.16
pkgrel=3
pkgdesc="GTK3 theme visually close to Clearlooks"
arch=('any')
url="http://www.jpfleury.net/en/software/clearlooks-phenix.php"
license=('GPL3')
depends=('gtk-engines')
conflicts=('clearlooks-phenix-gtk-theme-git')
source=("clearlooks-phenix-$pkgver.tar.gz::https://gitorious.org/projets-divers/clearlooks-phenix/archive/bad04e592a9e62202b9146d36ce5169d91ec0f1c.tar.gz"
        "001-gtk-3.8.patch"
        "002-menubar-bg.patch"
        "003-menu-padding.patch")
sha256sums=('d35b525e2a3c1394edb6354c401dc09d6edabdd342e35a42fb6c0848d66669e9'
            '51e74345d52627dd2cf84c58d5cca3d70faa665ce2b5c89eff8f37549b7824d6'
            'cf7f575eb20e1e5cdf1e794e86b783772319183a421f3aeb419087d0bb02f48d'
            'f14c3e355530c68483dd3459f88ec319a31c8ffc7b047e633e69c2c017a03ba5')

prepare() {
  cd projets-divers-clearlooks-phenix

  patch -p1 < "$srcdir/001-gtk-3.8.patch"
  patch -p1 < "$srcdir/002-menubar-bg.patch"
  patch -p1 < "$srcdir/003-menu-padding.patch"
}

package() {
  cd projets-divers-clearlooks-phenix

  mkdir -p "$pkgdir/usr/share/themes/Clearlooks-Phenix"
  cp -dR gtk-2.0 gtk-3.0 metacity-1 openbox-3 wallpapers xfwm4 index.theme \
    "$pkgdir/usr/share/themes/Clearlooks-Phenix"

  mkdir -p "$pkgdir/usr/share/doc"
  cp -dR doc "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
