# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=rssh
pkgver=2.3.3
pkgrel=5
pkgdesc='Restricted secure shell allowing only scp, sftp, cvs, rsync or rdist'
url='http://www.pizzashack.org/rssh/'
license=('custom:rssh')
arch=('i686' 'x86_64')
backup=('etc/rssh.conf')
depends=('openssh')
source=("http://downloads.sourceforge.net/sourceforge/rssh/rssh-${pkgver}.tar.gz"
        'env-breach.patch'
	'fail-logging.patch'
	'info-to-debug.patch'
	'man-page-spelling.patch'
	'mkchroot.patch'
	'mkchroot-arch.patch'
        'rsync.patch'
	'destdir.patch')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	patch -Np1 < ${srcdir}/env-breach.patch

	# from debian package (2.3.3-4)
	# http://ftp.de.debian.org/debian/pool/main/r/rssh/rssh_2.3.3-4.debian.tar.gz
	patch -Np1 < ${srcdir}/fail-logging.patch
	patch -Np1 < ${srcdir}/info-to-debug.patch
	patch -Np1 < ${srcdir}/man-page-spelling.patch
	patch -Np1 < ${srcdir}/mkchroot.patch

	patch -Np1 < ${srcdir}/mkchroot-arch.patch

	patch -Np1 < ${srcdir}/rsync.patch
	patch -Np1 < ${srcdir}/destdir.patch

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/rssh \
		--mandir=/usr/share/man \
		--sysconfdir=/etc \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
	install -D -m0644 LICENSE "${pkgdir}"/usr/share/licenses/rssh/LICENSE

	install -D -m0755 mkchroot.sh "${pkgdir}"/usr/bin/mkchroot
}

sha256sums=('1940912c2485f8531e4461de06bd2aebc607d2a89805debb7ac81002fa6fd07f'
            'd407531c9717306dbd5e1b3bbb587b5ce5d9f6b6440edb51d2d9f6c64401d4b6'
            'd30f2f4fdb1b57f94773f5b0968a4da3356b14a040efe69ec1e976c615035c65'
            '86f6ecf34f62415b0d6204d4cbebc47322dc2ec71732d06aa27758e35d688fcd'
            '455b3bbccddf1493999d00c2cd46e62930ef4fd8211e0b7d3a89d8010d6a5431'
            'f7fd8723d2aa94e64e037c13c2f263a52104af680ab52bfcaea73dfa836457c2'
            'ac8894c4087a063ae8267d2fdfcde69c2fe6b67a8ff5917e4518b8f73f08ba3f'
            '80da4ce86d3129fd1a757e93abcc59623291cc4ece2a299bce641e293bd8d51b'
            '7fa03644f81dc37d77cc7e2cad994f17f91b2b8a49b1a74e41030a4ac764385e')
