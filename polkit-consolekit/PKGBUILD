# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=polkit-consolekit
pkgver=0.107
pkgrel=1
pkgdesc="PolyciKit with ConsoleKit support for non-systemd systems"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.freedesktop.org/wiki/Software/PolicyKit"
depends=('glib2' 'pam' 'expat' 'js')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection')
provides=('polkit')
conflicts=('polkit')
options=('!libtool')
install=polkit.install
source=(http://www.freedesktop.org/software/polkit/releases/polkit-$pkgver.tar.gz \
        polkit.pam fix-empty-wheel.patch)
md5sums=('0e4f9c53f43fd1b25ac3f0d2e09b2ae1'
         '6564f95878297b954f0572bc1610dd15'
         '58ebac48b59f2599dd5d11564565c44e')

build() {
  cd "$srcdir/polkit-$pkgver"

  # https://bugs.archlinux.org/task/31717
  patch -Np1 -i ../fix-empty-wheel.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-1 \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-libsystemd-login=no \
      --disable-static --enable-gtk-doc
  make
}

package() {
  cd "$srcdir/polkit-$pkgver"
  make DESTDIR="$pkgdir" install

  chown 102 "$pkgdir/etc/polkit-1/rules.d"
  chown 102 "$pkgdir/usr/share/polkit-1/rules.d"

  install -m644 "$srcdir/polkit.pam" "$pkgdir/etc/pam.d/polkit-1"
}
