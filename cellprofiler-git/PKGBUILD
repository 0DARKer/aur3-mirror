# Maintainer: Michael Schubert <mschu.dev at gmail>

pkgname=cellprofiler-git
pkgver=20120903
pkgrel=1
pkgdesc="Analysis software for cellular images"
arch=('i686' 'x86_64')
url="http://www.cellprofiler.org/"
license=('GPL')
depends=('python2-libtiff-svn' 'python2-numpy' 'python2-scipy' 'wxpython' 'python2-decorator' 'mysql-python' 'python2-matplotlib' 'cython2' 'python-imaging')
makedepends=('git' 'python2-pytz' 'python2-nose' 'python2-h5py')
optdepends=('ilastik' 'python2-nuageux' 'python2-pytz' 'python2-nose' 'python2-h5py')
provides=('cellprofiler')
conflicts=('cellprofiler')

_gitroot="https://github.com/CellProfiler/CellProfiler.git"
_gitname="CellProfiler"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
    mkdir "$srcdir/$_gitname/build"
  fi

  cd "$srcdir/$_gitname"

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  sed -i "s/\tpython /\tpython2 /" Makefile.CP2
  sed -i 's/command = "python"/command = "python2"/' install.py
  sed -i '/subprocess.Popen/s|"python"|"python2"|' CellProfiler.py
  if [ $CARCH == 'i686' ]; then
    sed -i "s/'amd64'/'i386'/" cellprofiler/utilities/setup.py
  fi
  sed -i 's/\["java","-cp", path, "findlibjvm"\]/"dirname `find $JAVA_HOME -name libjvm.so -print -quit`", shell=True/' cellprofiler/utilities/jutil.py

  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname"
  python2 setup.py install --root="$pkgdir" --skip-build --optimize=1

  pydir=`python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()"`
  cp -r cellprofiler "$pkgdir/$pydir/"
  cp -r bioformats "$pkgdir/$pydir/cellprofiler/"
  cp -r contrib "$pkgdir/$pydir/cellprofiler/"
  cp -r imagej "$pkgdir/$pydir/cellprofiler/"
  cp -r killjavabridge "$pkgdir/$pydir/cellprofiler/"
  cp -r plugins "$pkgdir/$pydir/cellprofiler/"
  install -m755 CellProfiler.py "$pkgdir/$pydir/cellprofiler/"

  mkdir -p "$pkgdir/usr/share"
  mv "$pkgdir/usr/cellprofiler" "$pkgdir/usr/share/"

mkdir -p "$pkgdir/usr/bin"
cat > "$pkgdir/usr/bin/cellprofiler" << EOF
#!/bin/sh
python2 "$pydir/cellprofiler/CellProfiler.py" --do-not-build --do-not-fetch
EOF
chmod 755 "$pkgdir/usr/bin/cellprofiler"

  find "$pkgdir/$pydir/cellprofiler" -name "*.egg" \
    -exec mv {} "$pkgdir/$pydir/" \;

  rm -rf `find "$pkgdir" -name "tests" -type d`

  python2 -m compileall "$pkgdir/$pydir/cellprofiler/"
}

