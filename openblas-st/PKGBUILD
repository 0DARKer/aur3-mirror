# Maintainer: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
pkgname=openblas-st
_pkgname=xianyi-OpenBLAS
pkgver=0.2.6
pkgrel=1
pkgdesc="A single-threaded optimized BLAS library based on GotoBLAS2 1.13 BSD "
arch=('i686' 'x86_64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran')
options=(!makeflags !strip)
source=(${_pkgname}-v${pkgver}.tar.gz::http://github.com/xianyi/OpenBLAS/tarball/v${pkgver})
md5sums=('ee14728318215b627bb56e0f4f90b4df')

build() {
  cd "$srcdir/$_pkgname-87b4d0c"

  unset CFLAGS
  unset CXXFLAGS
  make USE_THREAD=0 NO_LAPACK=1 LIBPREFIX=lib$pkgname
}

package() {
  cd "$srcdir/$_pkgname-87b4d0c"

  make PREFIX="$pkgdir/usr" USE_THREAD=0 LIBPREFIX=lib$pkgname install
  mkdir "$pkgdir/usr/include/$pkgname"
  mv $pkgdir/usr/include/*.h "$pkgdir/usr/include/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cd "$pkgdir/usr/lib/"
  ln -sf libopenblas-st_*-r$pkgver.a libopenblas-st.a
  ln -sf libopenblas-st_*-r$pkgver.so libopenblas-st.so
  ln -sf libopenblas-st_*-r$pkgver.so libopenblas-st.so.0
}

# vim:set ts=2 sw=2 et:
