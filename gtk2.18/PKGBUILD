# $Id: PKGBUILD 73091 2010-03-22 16:41:13Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk2.18
pkgver=2.18.9
pkgrel=2
pkgdesc="The GTK+ Toolkit (v2)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk2.install
depends=('atk>=1.28.0' 'pango>=1.26.2' 'libxcursor' 'libxinerama' 'libxrandr>=1.3.0' 'libxi>=1.2.1' 'libxcomposite' 'libxdamage' 'heimdal>=1.3.1' 'gnutls>=2.8.6' 'shared-mime-info' 'cairo>=1.8.10' 'libtiff>=3.9.2-2' 'libcups>=1.4.2-3')
makedepends=('pkgconfig' 'gtk-doc')
replaces=('gtkprint-cups' 'gail' 'gtk2')
conflicts=('gtkprint-cups' 'gail' 'gtk2')
provides=('gail=1.22.3' 'gtk2')
options=('!libtool' '!docs')
backup=(etc/gtk-2.0/gtkrc)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.18/gtk+-${pkgver}.tar.bz2
        xid-collision-debug.patch
	avoid_spurious_notifications.patch)
sha256sums=('5dcd8c406acbb8779c0b081a089fa87dfd7ab4d7d4c6075db478997ce96aa9b4'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
	    'fd656e194d3e734d21b6afd0d8cf9e002679d8f41da73ec41790341a0d355798')

build() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -i "${srcdir}/xid-collision-debug.patch" || return 1
  patch -Np1 -i "${srcdir}/avoid_spurious_notifications.patch" || return 1

  CXX=/bin/false ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-xinput=yes \
      --without-libjasper \
      --with-included-loaders=png || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc" || return 1
}
