# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Based on libtorrent [community]

pkgname=libtorrent-git
_pkgname="${pkgname%-*}"
pkgver=0.13.4.r99.g99e33c0
pkgrel=1
pkgdesc="BitTorrent library written in C++"
url="http://libtorrent.rakshasa.no"
license=("GPL")
arch=("i686" "x86_64")
depends=("libsigc++" "openssl")
makedepends=("cppunit" "git")
options=("!libtool")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=("git://github.com/rakshasa/${_pkgname}.git")
md5sums=("SKIP")

pkgver() {
  cd "${_pkgname}"
  git describe --long | sed -r "s/([^-]*-g)/r\1/;s/-/./g"
}

build() {
  cd "${srcdir}/${_pkgname}"

  ./autogen.sh
#  export LIBS="${LIBS} -lpthread"
  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
  ./configure --prefix=/usr --disable-debug

  make
}

package() {
  cd "${srcdir}/${_pkgname}"

  make DESTDIR="${pkgdir}" install
}
