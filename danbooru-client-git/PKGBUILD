# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: archtux <antonio dot arias99999 at gmail dot com>

pkgname=danbooru-client-git
pkgver=v0.2.0.7.g2c455a3
pkgrel=1
pkgdesc="Application to access Danbooru-based image boards. (GIT Version)"
url="http://www.dennogumi.org/projects/danbooru-client"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('kdeclarative' 'kfilemetadata5')
makedepends=('git' 'cmake')
conflicts=('danbooru-client')
provides=('danbooru-client')
source=("git+https://git.dennogumi.org/KDE/danbooru-client.git")
sha1sums=('SKIP')
install=danbooru-client-git.install

pkgver() {
  cd danbooru-client
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../danbooru-client \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}