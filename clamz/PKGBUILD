# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=clamz
pkgver=0.4
pkgrel=1
pkgdesc="Command-line program to download MP3 files from Amazon.com's music store."
arch=('i686' 'x86_64')
url="http://code.google.com/p/clamz/"
license=('GPL3')
depends=('libgcrypt' 'curl' 'expat' 'shared-mime-info')
source=(http://clamz.googlecode.com/files/clamz-${pkgver}.tar.gz)
md5sums=('ab7661340ee27d206f36064cfbd5bfa4')
install=clamz.install

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# Fix issue with update-mime-database
	sed -i 's|$(UPDATE_DESKTOP_DATABASE)\ $(DESTDIR)$(applications_dir)||' Makefile.in || return 1
	sed -i 's|$(UPDATE_MIME_DATABASE)\ $(DESTDIR)$(mime_dir)||' Makefile.in || return 1

	./configure --prefix=/usr/ || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install || return 1
}
