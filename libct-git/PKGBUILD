# Maintainer: Maik Broemme <mbroemme@parallels.com>
pkgname=libct-git
pkgver=20140522.8d379d0
pkgrel=1
pkgdesc="libct - a containers management library"
arch=("i686" "x86_64")
url="https://github.com/xemul/libct"
license=("LGPLv2")
depends=("protobuf-c")
makedepends=("asciidoc")
conflicts=("libct")
source=(
  "libct-git::git://github.com/xemul/libct.git"
  "libctd.service"
  "libctd.socket"
)
sha256sums=(
  "SKIP"
  "2757761fa41683d9e0a9bfc6c2ec1595dfa8b947141cf1df2130450a9fd699e2"
  "92e4009e3d8758dc18184b679430d186527750e91ea6d56314cda65888294219"
)

pkgver() {
  cd "${srcdir}/libct-git"
  git log -1 --date=short --format="%cd.%h" | tr -d '-'
}

build() {
  cd "${srcdir}/libct-git"
  export LDFLAGS="${LDFLAGS//-Wl,}"
  make
  make docs
}

package() {
  cd "${srcdir}/libct-git"
  install -D -m 0644 ../libctd.service "${pkgdir}/usr/lib/systemd/system/libctd.service"
  install -D -m 0644 ../libctd.socket "${pkgdir}/usr/lib/systemd/system/libctd.socket"
  install -D -m 0644 Documentation/libct.1 "${pkgdir}/usr/share/man/man1/libct.1"
  install -D -m 0644 src/include/uapi/libct-errors.h "${pkgdir}/usr/include/libct/libct-errors.h"
  install -D -m 0644 src/include/uapi/libct.h "${pkgdir}/usr/include/libct/libct.h"
  install -D -m 0755 src/libctd/libctd "${pkgdir}/usr/sbin/libctd"
  install -D -m 0755 src/libct.so "${pkgdir}/usr/lib/libct.so.0.1.0"
  ln -s libct.so.0.1.0 "${pkgdir}/usr/lib/libct.so.0"
  ln -s libct.so.0.1.0 "${pkgdir}/usr/lib/libct.so"
}
