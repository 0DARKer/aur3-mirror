# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>
# Based on owncloud-git PKGBUILD by Alexander Ovsyannikov

pkgname=owncloud-beta
_pkgname=owncloud
pkgver=7.0.0beta1
pkgrel=1
pkgdesc="A cloud server to store your files centrally on a hardware controlled by you"
arch=('any')
url="http://owncloud.org/"
license=('GPL')
depends=('php-gd' 'php-intl')
optdepends=('php-apache: to use the Apache web server'
	    'php-sqlite: to use the SQLite database backend'
	    'php-pgsql: to use the PostgreSQL database backend'
	    'mariadb: to use the MySQL database backend'
	    'smbclient: to mount SAMBA shares'
	    'php-mcrypt'
	    'php-imagick: file preview'
	    'ffmpeg: file preview'
	    'libreoffice-common: file preview')
makedepends=()
options=('!strip')
source=("http://download.owncloud.org/community/testing/${_pkgname}-${pkgver}.tar.bz2"{,.asc}
	'apache.example.conf')
backup=('etc/webapps/owncloud/apache.example.conf')
sha512sums=('4ec6c6d42a2abb42c346eb9e2d7748a8c79be5e4a1b791508df1b08d5b3b05c46d058c4194b50b9631046246379e12ca8e383fb732335d69bd4f2f89e4a6ee47'
	    'SKIP'
	    '5daf80e6e774bac34cc47f697f94b7d6286aac49a9d39ad26a37fd0e18cf4ddc3dc438d7f88458f00058c9732c8ffe1e27682db4f58fd6d8c4bf8a4e84e46f31')

package() {
  # install license
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  cp ${srcdir}/${_pkgname}/COPYING-* ${pkgdir}/usr/share/licenses/${pkgname}

  # install project
  install -d ${pkgdir}/usr/share/webapps/
  cp -a ${srcdir}/${_pkgname} ${pkgdir}/usr/share/webapps/${pkgname}

  # install apache config file
  install -d  ${pkgdir}/etc/webapps/${pkgname}
  install -m 644 ${srcdir}/apache.example.conf  ${pkgdir}/etc/webapps/${pkgname}

  # move config to /etc
  mv ${pkgdir}/usr/share/webapps/${pkgname}/config ${pkgdir}/etc/webapps/${pkgname}/config
  chown -R http:http ${pkgdir}/etc/webapps/${pkgname}/config
  ln -s /etc/webapps/${pkgname}/config ${pkgdir}/usr/share/webapps/${pkgname}/config
}
