# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
#contributor

pkgname=xterm-vt220-tolbar
_pkgname=xterm
pkgver=291
pkgrel=7
pkgdesc="X Terminal Emulator, use VT220 for terminal w/ enabled tolbar"
arch=('i686' 'x86_64')
conflicts=('xterm')
provides=('xterm')
url="http://invisible-island.net/xterm/"
license=('custom')
depends=('libxft' 'libxaw')
source=(ftp://invisible-island.net/${_pkgname}/${_pkgname}.tar.gz
        LICENSE)
md5sums=('9925cd564c84746442e640f0e32f0493'
         '10ecc3f8ee91e3189863a172f68282d2')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --libdir=/etc \
      --mandir=/usr/share/man \
      --with-app-defaults=/usr/share/X11/app-defaults/ \
      --with-x \
      --disable-full-tgetent \
      --disable-imake \
      --with-terminal-is=vt220 \
      --enable-ansi-color \
      --enable-88-color \
      --enable-256-color \
      --enable-broken-osc \
      --enable-broken-st \
      --enable-load-vt-fonts \
      --enable-i18n \
      --enable-toolbar \
      --enable-wide-chars \
      --enable-doublechars \
      --enable-warnings \
      --enable-tcap-query \
      --enable-logging \
      --enable-dabbrev \
      --enable-freetype \
      --enable-luit \
      --enable-mini-luit \
      --enable-16bit-chars \
      --enable-narrowproto \
      --enable-exec-xterm \
      --with-tty-group=tty \
      --with-utmp-setgid=utmp
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  chmod 0755 "${pkgdir}/usr/bin/xterm"

  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
