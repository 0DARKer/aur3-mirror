# Old Maintainer: lagrange <flkazemakase@gmail.com>
# Maintainer: mid-kid <esteve.varela@gmail.com>

pkgname=python-kivy
pkgver=1.8.0
pkgrel=3
pkgdesc="A software library for rapid development of hardware-accelerated multitouch applications."
arch=('i686' 'x86_64')
url="http://kivy.org"
license=(MIT)
depends=(python-pygame libgl)
makedepends=(cython gstreamer)
optdepends=("gstreamer: GstPlayer")
source=("https://pypi.python.org/packages/source/K/Kivy/Kivy-$pkgver.tar.gz")
md5sums=('fc36e08e0cd903a249dca957bdf1e2b0')

build() {
    cd "$srcdir/Kivy-$pkgver"
    export USE_X11=1
    export USE_MESAGL=1

    python setup.py build
}

package() {
    cd "$srcdir/Kivy-$pkgver"
    python setup.py install --root=$pkgdir
    install -Dm644 ./LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

