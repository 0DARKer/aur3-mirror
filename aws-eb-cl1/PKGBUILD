# Contributor: Douglas Creager (dcreager AT dcreager DOT NET)
# Contributor: Josh David Miller (josh AT joshdavidmiller DOT COM)
# Contributor: David Pretty (david DOT pretty AT gmail DOT COM)

pkgname=aws-eb-cl1
pkgver=2.6.3
pkgrel=1
pkgdesc="The API and CLI tools that provide access to Amazon Elastic Beanstalk"
arch=( 'any' )
url="http://aws.amazon.com/code/6752709412171743"
source=( https://s3.amazonaws.com/elasticbeanstalk/cli/AWS-ElasticBeanstalk-CLI-2.6.3.zip )
depends=( 'python' )
license=( 'APACHE' )
md5sums=( 'b26e88ff4f5f923a409cc467733a063c')

build() {
  # Fix the location of the dev tools
  sed -i 's/LinuxClimbUpDepth\s=.*$/LinuxClimbUpDepth = 0/g' \
    $srcdir/AWS-ElasticBeanstalk-CLI-$pkgver/eb/linux/python3/scli/constants.py
  sed -i 's/LinuxRepoScript\s=.*$/LinuxRepoScript = \x27AWSDevTools\/AWSDevTools-RepositorySetup.sh\x27/g' \
    $srcdir/AWS-ElasticBeanstalk-CLI-$pkgver/eb/linux/python3/scli/constants.py
}

package() {
  # Install the files into /opt since they depend on a non-standard directory
  # structure.
  mkdir -p $pkgdir/opt
  cp -r $srcdir/AWS-ElasticBeanstalk-CLI-$pkgver/eb/linux/python3 $pkgdir/opt/aws-eb-cli
  cp -r $srcdir/AWS-ElasticBeanstalk-CLI-$pkgver/AWSDevTools/Linux $pkgdir/opt/aws-eb-cli/AWSDevTools

  # Link the CLI program into /usr/bin
  mkdir -p $pkgdir/usr/bin
  ln -s ../../opt/aws-eb-cli/eb $pkgdir/usr/bin
}