# Maintainer: Carl George < arch at cgtx.us >

_module="supernova"
pkgname=("supernova" "supernova2")
pkgver="1.0.7"
pkgrel="4"
pkgdesc="Use novaclient with multiple OpenStack nova environments the easy way"
arch=("any")
url="https://github.com/major/${_module}"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools")
source=("${url}/archive/v${pkgver}.tar.gz"
        "supernova-completion.bash"
        "supernova2-completion.bash")

prepare() {
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
	#sed -i 's_supernova_supernova2_g' \
	#	"${srcdir}/${_module}-${pkgver}-python2/contrib/supernova-completion.bash"
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

package_supernova() {
	conflicts=("python-supernova" "python-supernova-git" "supernova-git")
	depends=("python-novaclient" "python-keyring")
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	#install -Dm644 contrib/supernova-completion.bash "${pkgdir}/etc/bash_completion.d/supernova"
	install -Dm644 "${srcdir}/supernova-completion.bash" "${pkgdir}/etc/bash_completion.d/supernova"
}

package_supernova2() {
	conflicts=("python2-supernova" "python2-supernova-git" "supernova2-git")
	depends=("python2-novaclient" "python2-keyring")
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	mv "${pkgdir}/usr/bin/supernova" "${pkgdir}/usr/bin/supernova2"
	mv "${pkgdir}/usr/bin/supernova-keyring" "${pkgdir}/usr/bin/supernova2-keyring"
	#install -Dm644 contrib/supernova-completion.bash "${pkgdir}/etc/bash_completion.d/supernova2"
	install -Dm644 "${srcdir}/supernova2-completion.bash" "${pkgdir}/etc/bash_completion.d/supernova2"
}

sha256sums=('e0336590e8b4a73404acbfd7379285c631daf628baaba64c9f42d839982d9949'
            '979f234a7f4c3dfa4b78cb57fcf6a80df47104f1832894b255097777ea7bd1cf'
            'f9c24db174b4e4e5c4d340c42f037831cc9b5aff398918e1c6c5a6ffe9a9ee6b')
