# $Id: PKGBUILD 169847 2012-10-30 22:23:55Z heftig $
# Maintainer: jtts
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbase=at-spi2-core
pkgname=lib32-$_pkgbase
pkgver=2.6.2
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi (32-bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('lib32-dbus-core' 'lib32-glib2' 'lib32-libxtst' $_pkgbase)
makedepends=('gcc-multilib' 'intltool')
options=('!libtool')
source=(http://download.gnome.org/sources/$_pkgbase/${pkgver%.*}/$_pkgbase-$pkgver.tar.xz)
sha256sums=('0d5321ce9fdd7d036b072ef28cba14088394606e1687183ec5d0ae6adea8c87e')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd $_pkgbase-$pkgver

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libdir=/usr/lib32 --libexecdir=/usr/lib32/at-spi2-core \
    --enable-introspection=no \
    --disable-xevie
  make
}

package() {
  cd $_pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  # cleanup for lib32 package
  rm -rf "${pkgdir}"/{etc,bin,sbin,usr/{bin,lib,include,share},var}
}

# vim:set ts=2 sw=2 et:
