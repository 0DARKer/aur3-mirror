# Maintainer: jtts
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=at-spi2-core
pkgname=lib32-$_pkgbasename
pkgver=2.8.0
pkgrel=2
pkgdesc="Protocol definitions and daemon for D-Bus at-spi (32-bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('lib32-dbus-core' 'lib32-glib2' 'lib32-libxtst' $_pkgbasename)
makedepends=('gcc-multilib' 'intltool')
options=('!libtool')
source=(http://download.gnome.org/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('1861a30fc7f583d5a567a0ba547db67ce9bd294f0d1c9f7403c96a10a481c458')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd $_pkgbasename-$pkgver

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libdir=/usr/lib32 --libexecdir=/usr/lib32/at-spi2-core \
    --enable-introspection=no \
    --disable-xevie
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  # cleanup for lib32 package
  rm -rf "${pkgdir}"/{etc,bin,sbin,usr/{bin,lib,include,share},var}
}

# vim:set ts=2 sw=2 et:
