# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=latexila-devel
_pkgname=latexila
pkgver=2.5.0
pkgrel=1
pkgdesc="Integrated LaTeX environment for GNOME (development version)"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/latexila/"
license=('GPL')
depends=('gtksourceview3' 'libgee' 'gtkspell3' 'gsettings-desktop-schemas' 'dconf' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('cmake' 'itstool' 'vala')
conflicts=("$_pkgname")
install=$_pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz)
sha256sums=('e03d68f13e12e92dd0e446edcb3c278da54d23db8d3c3d7c09fbdf78e0bc68b6')

build() {
  cd "$srcdir/$_pkgname-$pkgver/build"

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DWITH_GNOME=ON -DBUILD_VALA=ON
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"

  make DESTDIR="$pkgdir/" install
}
