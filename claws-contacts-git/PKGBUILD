# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=claws-contacts-git
pkgver=61.g4e246e9
pkgrel=1
pkgdesc="A GTK+ based contacts manager / address book - git checkout"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org/"
depends=('gtk2')
makedepends=('git')
conflicts=('claws-contacts' 'claws-contacts-cvs')
provides=('claws-contacts' 'claws-contacts-cvs')
options=(!libtool)
install=claws-contacts.install
source=('claws-contacts::git+http://git.claws-mail.org/readonly/contacts.git')

pkgver() {
	cd claws-contacts/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd claws-contacts/

	./autogen.sh \
		--prefix=/usr \
		--disable-static
	for MAKEFILE in $(find -name Makefile); do
		sed -i '/^LIBS =/s/$/ -lm/' ${MAKEFILE}
	done
	make
}

package() {
	cd claws-contacts/

	make DESTDIR=${pkgdir} install
}

sha256sums=('SKIP')
