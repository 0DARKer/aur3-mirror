# Contributor: J.W. Lee <arcicube at gmail dot com>

pkgname=hunspell-ko
pkgver=0.5.5
pkgrel=1
pkgdesc="Korean hunspell dictionary"
arch=('any')
url="http://code.google.com/p/spellcheck-ko/"
license=('GPL' 'LGPL' 'MPL')
optdepends=('hunspell: the spell checking libraries and apps')
source=(http://spellcheck-ko.googlecode.com/files/ko-aff-dic-$pkgver.zip)
md5sums=('30bb07e298f22ae01f3abf337708097d')

package() {
  cd "${srcdir}/ko-aff-dic-${pkgver}"
  
  install -dm755 ${pkgdir}/usr/share/hunspell
  install -m644 ko.dic ko.aff ${pkgdir}/usr/share/hunspell

  pushd ${pkgdir}/usr/share/hunspell
  ln -s ko.aff ko_KR.aff
  ln -s ko.dic ko_KR.dic
  popd

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd ${pkgdir}/usr/share/myspell/dicts
    for file in ${pkgdir}/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename ${file}) .
    done
  popd
}