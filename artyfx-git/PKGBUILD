# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=artyfx-git
pkgver=1.2.r5.g802579b
pkgrel=1
pkgdesc="A LV2 plugin bundle of artistic realtime effects"
arch=('i686' 'x86_64')
url="http://openavproductions.com/artyfx/"
license=('GPL')
groups=('lv2-plugins')
depends=('lv2' 'ntk-git')
provides=('artyfx')
conflicts=('artyfx')
source=("$pkgname::git+https://github.com/harryhaaren/openAV-ArtyFX.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed -r 's/^release.//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname/b
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd $pkgname/b
  make DESTDIR="$pkgdir/" install
}
