# Maintainer: monson <holymonson@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

_pkgname=cgminer
pkgname=cgminer-git
pkgver=20120405
pkgrel=1
pkgdesc="Multi-threaded multi-pool CPU and GPU miner for bitcoin, forked from cpuminer."
url='http://forum.bitcoin.org/index.php?topic=28402.0'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ncurses' 'curl' 'libcl')
makedepends=('opencl-headers' 'git')
optdepends=('opencl-nvidia: OpenCL implementation for NVIDIA'   \
            'intel-opencl-sdk: OpenCL implementation for Intel' \
            'amdstream: OpenCL implementation for AMD')
source=("$_pkgname.conf.d"
        "$_pkgname")
backup=("etc/conf.d/$_pkgname" "etc/$_pkgname.conf")

md5sums=('fe4a243fabe24608f5c05e40f0f118f6'
         'ee39698273671fee0e98d4af16014c36')
	 
_gitroot="https://github.com/ckolivas/cgminer.git"
_gitname="cgminer-git"

[ $CARCH == 'x86_64' ] && makedepends+=('yasm')

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd "$srcdir"/$_gitname

  # Use in-tree jansson since it is not compatible with jansson 2.0
  #sed -e 's/^AC_CHECK_LIB(jansson, json_loads, request_jansson=false, request_jansson=true)$/request_jansson=true/' -i configure.ac

  ./autogen.sh || return 1
  # Here you may want to use custom CFLAGS
  #export CFLAGS="-O2 -march=native -mtune=native -msse2"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "$srcdir"/$_gitname

  make DESTDIR="$pkgdir" install

  install -Dm755 "$srcdir"/$_pkgname "$pkgdir"/etc/rc.d/$_pkgname
  install -Dm644 "$srcdir"/$_pkgname.conf.d "$pkgdir"/etc/conf.d/$_pkgname.conf.d
  sed 's#/usr/local/bin#/usr/bin#g' example.conf > $_pkgname.conf
  install -Dm644 $_pkgname.conf "$pkgdir"/etc/$_pkgname.conf
}
