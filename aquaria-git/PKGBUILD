# Maintainer: Sam S. <smls75@gmail.com>

pkgname=aquaria-git
pkgver=1.1.3+g262.8da8b54
_basever=1.1.3
pkgrel=1
pkgdesc="A 2D fantasy action-adventure game (improved Open Source build of the engine)"
url='http://www.bit-blot.com/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('aquaria-data' 'sdl' 'openal' 'gcc-libs')
makedepends=('cmake')
conflicts=('aquaria' 'aquaria-hib-hg' 'aquaria-hib-git')

source=('git://github.com/fgenesis/Aquaria_clean.git' "$pkgname.desktop")
md5sums=('SKIP' '8677297d97cee364ca63652503ed6e62')

_gitname=Aquaria_clean

pkgver() {
  cd $_gitname
  # Automatically generate package version from git commit number
  echo $_basever+g$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  cmake -DAQUARIA_DEFAULT_DATA_DIR=/usr/share/aquaria
  make
}

package() {
  cd $pkgdir

  # Install executable
  install -Dm755 "$srcdir/$_gitname/aquaria" usr/bin/aquaria

  # Install some additional game files needed by the executable
  mkdir -p usr/share/aquaria/override
  cp -r "$srcdir/$_gitname"/{game_scripts,files}/* usr/share/aquaria/override

  # Install desktop entry
  install -Dm644 "$srcdir/$pkgname.desktop" usr/share/applications/$pkgname.desktop
}
