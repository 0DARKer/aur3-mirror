# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=gsignond-git
_pkgname=${pkgname%-*}
pkgver=0.0.3.r433.917677a
pkgrel=2
pkgdesc='GLib based Single Sign-On daemon'
arch=('i686' 'x86_64')
url='https://01.org/gsso'
license=('LGPL2.1')
depends=('dbus' 'glib2' 'sqlite')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("${pkgname%-*}::git+https://code.google.com/p/accounts-sso.gsignond/"
	"makefile.patch")
sha256sums=('SKIP'
	    'e4727c3c7964f24e53d070021c1094c30c62d1c44c8834be2f8ddbe88436ad94')

pkgver() {
  cd ${pkgname%-*}

  local v_ver=$(awk -F , '/^AC_INIT/ {gsub(/[\[\] -]/, ""); print $2}' configure.ac)
  printf "$v_ver.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${pkgname%-*}

  patch -p1 -i $srcdir/makefile.patch
  
  if [ -f = "gtk-doc.make" ]
  then
    rm gtk-doc.make
  fi
    touch gtk-doc.make

  #aclocal
  #autoheader
  #libtoolize --copy --force
  #autoconf
  #automake --add-missing --copy
  autoreconf --install --force
}

build() {
  cd ${pkgname%-*}

  ./configure --prefix=/usr --sysconfdir=/etc --enable-dbus-type=session
  make
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
