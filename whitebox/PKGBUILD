# Maintainer: SaultDon <sault.don gmail>
pkgname=whitebox
pkgver=3.1.0
pkgrel=2
_pkgname=Whitebox_${pkgver//./_}
pkgdesc="A Geographic information system (GIS) and remote sensing package intended for advanced geospatial analysis and data visualization."
arch=('any')
url="http://www.uoguelph.ca/~hydrogeo/Whitebox/index.html"
license=('GPL3')
depends=('java-environment' 'bash')
provides=("$pkgname=$pkgver")
conflicts=("$pkgname-svn")
install=$pkgname.install
changelog=$pkgname.changelog
source=("$pkgname-$pkgver.zip::http://www.uoguelph.ca/~hydrogeo/Whitebox/$_pkgname.zip"
	"$pkgname.sh"
	"$pkgname.desktop"
	"http://www.uoguelph.ca/~hydrogeo/Whitebox/gpl-3_0.txt")
noextract=($pkgname-$pkgver.zip)
sha1sums=('d3995dc4c1b30425d97d08c91e4576ec276a4b77'
          '8869864e89cc2c7f90dfe55fec59349e5bcdb361'
          'fd3f308e69ce14ff62f7365a09bcf76e02b86be9'
          '7713a1753ce88f2c7e6b054ecc8e4c786df76300')

package(){

	msg2 "Installing license..."
	install -Dm644 $srcdir/gpl-3_0.txt \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	msg2 "Installing desktop entry..."
	install -Dm644 $srcdir/$pkgname.desktop \
	  "$pkgdir/usr/share/applications/$pkgname.desktop"

	msg2 "Installing package..."
	install -d $pkgdir/opt

	install -Dm755 $srcdir/$pkgname.sh \
	  "$pkgdir/opt/$pkgname/$pkgname.sh"

	bsdtar --no-same-owner \
	       -s /$_pkgname/$pkgname/ \
	       --exclude .DS_Store \
	       --directory $pkgdir/opt \
	       -xf $pkgname-$pkgver.zip $_pkgname

        msg2 "Installing icon..."
        install -Dm644 $pkgdir/opt/$pkgname/resources/Images/wbGAT.png \
          "$pkgdir/usr/share/pixmaps/$pkgname.png"
	
	msg2 "Making required folders/files writeable..."
	find $pkgdir/opt/$pkgname/resources -exec chmod a+w '{}' \;
	find $pkgdir/opt/$pkgname/resources -type f -name '*.jar' -exec chmod go-w '{}' \;
	find $pkgdir/opt/$pkgname/resources -type f -name '*.java' -exec chmod go-w '{}' \;
	find $pkgdir/opt/$pkgname/resources/Images -exec chmod go-w '{}' \;
}
