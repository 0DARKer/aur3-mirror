# Maintainer: SaultDon <sault.don gmail>
pkgname=whitebox
pkgver=3.1.3
pkgrel=1
_pkgname="Whitebox_${pkgver//./_}"
pkgdesc="A Geographic information system (GIS) and remote sensing package intended for advanced geospatial analysis and data visualization."
arch=('any')
url="http://www.uoguelph.ca/~hydrogeo/Whitebox/index.html"
license=('GPL3')
depends=('java-runtime' 'bash')
provides=("$pkgname=$pkgver")
conflicts=("$pkgname-svn")
install=$pkgname.install
changelog=$pkgname.changelog
source=("$pkgname-$pkgver.zip::http://www.uoguelph.ca/~hydrogeo/Whitebox/WhiteboxGAT.zip"
	"$pkgname.sh"
	"$pkgname.desktop")
noextract=($pkgname-$pkgver.zip)
sha1sums=('669a0f8d3207fb109d77340fb76957720d2d499c'
          '8869864e89cc2c7f90dfe55fec59349e5bcdb361'
          'fd3f308e69ce14ff62f7365a09bcf76e02b86be9')

package(){

	msg2 "Installing desktop entry..."
	install -Dm644 $srcdir/$pkgname.desktop \
	  "$pkgdir/usr/share/applications/$pkgname.desktop"

	msg2 "Installing package..."
	install -d $pkgdir/opt

	install -D $srcdir/$pkgname.sh \
	  "$pkgdir/opt/$pkgname/$pkgname.sh"

	bsdtar --no-same-owner \
	       -s /$_pkgname/$pkgname/ \
	       --exclude .DS_Store \
           --exclude __MACOSX \
	       --directory $pkgdir/opt \
	       -xf $pkgname-$pkgver.zip $_pkgname

	msg2 "Installing license..."
	install -d "$pkgdir/usr/share/licenses/$pkgname"
    sed -n '/GNU GENERAL PUBLIC LICENSE/,/why-not-lgpl.html>./p' "$pkgdir/opt/$pkgname/ReleaseNotes.txt" > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    msg2 "Installing icon..."
    install -Dm644 $pkgdir/opt/$pkgname/resources/Images/wbGAT.png \
      "$pkgdir/usr/share/pixmaps/$pkgname.png"
	
	msg2 "Making required folders/files writeable..."
    find $pkgdir/opt/$pkgname -exec chmod 777 '{}' \;
	find $pkgdir/opt/$pkgname -type f -name '*.jar' -exec chmod go-w '{}' \;
	find $pkgdir/opt/$pkgname -type f -name '*.java' -exec chmod go-w '{}' \;
	find $pkgdir/opt/$pkgname/resources/Images -exec chmod go-w '{}' \;
}
