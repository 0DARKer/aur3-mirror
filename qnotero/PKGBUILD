# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=qnotero
pkgver=0.46
pkgrel=1
pkgdesc="A Qt application which provides access to your Zotero references"
arch=(any)
url="http://www.cogsci.nl/software/gnotero"
license=('GPL')
depends=('python2-pyqt' 'pdftk' 'desktop-file-utils')
optdepends=(
  'gnote: for integration with gnote'
  'python-levenshtein: for integration with gnote'
)
provides=("gnotero")
replaces=("gnotero")
source=(http://files.cogsci.nl/software/${pkgname}/${pkgname}_${pkgver}.tar.gz)
md5sums=('66cb0cb5fe47b6078ed42d3e22f2f353')
sha256sums=('85c9d0d70d939d2bad00d6cea25d99bc348184bf9abe27c8b36a844720425262')
install=qnotero.install

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root=$pkgdir

  # Patch to use python2
  find $pkgdir -type f \( -name '*.py' -or -executable \) -exec \
    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    \{\} +
}
