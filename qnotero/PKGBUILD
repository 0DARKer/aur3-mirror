# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=qnotero
pkgver=0.47
pkgrel=1
pkgdesc="A Qt application which provides access to your Zotero references"
arch=(any)
url="http://cogsci.nl/software/qnotero"
license=('GPL')
depends=('python2-pyqt' 'pdftk' 'desktop-file-utils')
optdepends=(
  'gnote: for integration with gnote'
  'python-levenshtein: for integration with gnote'
)
provides=("gnotero")
replaces=("gnotero")
source=(http://files.cogsci.nl/software/${pkgname}/${pkgname}_${pkgver}.tar.gz)
md5sums=('a28e389e8d3d93ba0c49ea380b548eda')
sha256sums=('254bb80e6da28bdc0b2ba49fc31f4b10864d8873ac5403a721d8350422cd472b')
install=qnotero.install

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root=$pkgdir

  # Patch to use python2
  find $pkgdir -type f \( -name '*.py' -or -executable \) -exec \
    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    \{\} +
}
