# Maintainer: Adam Reichold <adamreichold@myopera.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=qpdfview-bzr
pkgver=1479
pkgrel=1
pkgdesc='A tabbed PDF viewer using the poppler library. (development version)'
arch=('i686' 'x86_64')
url='https://launchpad.net/qpdfview'
license=('GPL2')
depends=('libcups' 'qt5-svg' 'desktop-file-utils')
optdepends=('poppler-qt5: for PDF support' 'libspectre: for PostScript support' 'djvulibre: for DjVu support' 'mupdf: for PDF, XPS and CBZ support')
makedepends=('bzr' 'qt5-tools')
conflicts=('qpdfview')
install='qpdfview.install'
source=('qpdfview::bzr+http://bazaar.launchpad.net/~adamreichold/qpdfview/trunk/')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/qpdfview"
  
  bzr revno
}

build() {
  cd "$srcdir/qpdfview"

  if ! pkg-config --exists poppler-qt5; then
    local config="$config without_pdf"
  fi
  
  if ! pkg-config --exists libspectre; then
    local config="$config without_ps"
  fi
  
  if ! pkg-config --exists ddjvuapi; then
    local config="$config without_djvu"
  fi

  if [ -f /usr/lib/libmupdf.a  ]; then
    local config="$config with_fitz"
  fi

  lrelease-qt5 qpdfview.pro qpdfview.pro
  qmake-qt5 "CONFIG+=$config" qpdfview.pro
  make
}

package() {
  cd "$srcdir/qpdfview"

  make "INSTALL_ROOT=$pkgdir" install
}
