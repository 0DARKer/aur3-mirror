# Maintainer: Adam Reichold <adamreichold@myopera.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=qpdfview-bzr
pkgver=960
pkgrel=1
pkgdesc='A tabbed PDF viewer using the poppler library. (development version)'
arch=('i686' 'x86_64')
url='https://launchpad.net/qpdfview'
license=('GPL2')
depends=('file' 'libcups' 'poppler-qt')
optdepends=('libspectre: for PostScript support' 'djvulibre: for DjVu support')
makedepends=('bzr')
conflicts=('qpdfview')
install='qpdfview.install'
_bzrtrunk='lp:qpdfview'
_bzrmod='qpdfview'

build() {
  cd "$srcdir"

  msg 'Obtaining source code...'

  if [ -d $_bzrmod ] ; then
    cd $_bzrmod && bzr pull $_bzrtrunk
  else
    bzr branch $_bzrtrunk $_bzrmod && cd $_bzrmod
  fi

  msg 'Building program...'
  
  if ! pkg-config --exists libspectre; then
    local config="$config without_ps"
  fi
  
  if ! pkg-config --exists ddjvuapi; then
    local config="$config without_djvu"
  fi

  qmake "CONFIG+=$config" qpdfview.pro
  make
}

package() {
  cd "$srcdir/$_bzrmod"

  make INSTALL_ROOT=$pkgdir install
}
