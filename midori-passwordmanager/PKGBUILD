# Maintainer: Giuseppe Borzi <gborzi@ieee.org>

pkgname=midori-passwordmanager
_pkgname=midori
pkgver=0.3
_pkgver=0.4.8
pkgrel=9
pkgdesc="A password manager for Midori"
arch=('i686' 'x86_64')
url="http://www.twotoasts.de/index.php?/pages/midori_summary.html"
license=('GPL2')
depends=('midori' 'gnome-keyring' 'libgnome-keyring')
makedepends=('intltool' 'vala')
optdepends=('seahorse: to edit/delete passwords')
source=(http://archive.xfce.org/src/apps/$_pkgname/${_pkgver%.*}/$_pkgname-$_pkgver.tar.bz2 passwordmanager.c passwordmanager.js wscript.diff README lpins.c)
md5sums=('ce1b01fa5aa33d2ee50196df0c71e96f'
         '94c6ec8ddd50ca4d226d8413ef85c0f2'
         'f9a91b8d929d4cb6407a769603fca619'
         'eda46a64b52c60b9d0c45be419c20c01'
         'defadec00ebd02506fb1106f7fe5f2ae'
         'd11b1f035e4a044e2727ef226db9482b')

build() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"

  patch -Np1 -i "${srcdir}/wscript.diff"
  # python2 
  export PYTHON=/usr/bin/python2
  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" wscript waf
  cp "$srcdir/passwordmanager.c" extensions

  [ -z $MAKEFLAGS ] && JOBS=1 || JOBS=${MAKEFLAGS/-j/}
  ./configure --prefix=/usr --jobs=$JOBS \
	--enable-addons \
	--enable-unique \
	--enable-nls 
  make
  cc `pkg-config --cflags --libs gnome-keyring-1` "$srcdir/lpins.c" -o lpins
}

package() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  install -Dm644 _build/default/extensions/libpasswordmanager.so \
     "${pkgdir}/usr/lib/midori/libpasswordmanager.so"
  install -Dm644 "${srcdir}/passwordmanager.js" \
     "${pkgdir}/usr/share/midori/res/passwordmanager.js"
  install -Dm755 lpins "${pkgdir}/usr/bin/lpins"
  install -Dm644 "${srcdir}/README" "${pkgdir}/usr/share/doc/$pkgname/README"
}
