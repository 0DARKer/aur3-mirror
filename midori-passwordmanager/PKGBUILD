# Maintainer: Giuseppe Borzi <gborzi@ieee.org>

pkgname=midori-passwordmanager
_pkgname=midori
pkgver=0.2
_pkgver=0.3.6
pkgrel=1
pkgdesc="A password manager for Midori"
arch=('i686' 'x86_64')
url="http://www.twotoasts.de/index.php?/pages/midori_summary.html"
license=('GPL2')
depends=('midori' 'gnome-keyring')
optdepends=('seahorse: to edit/delete passwords')
source=(http://archive.xfce.org/src/apps/$_pkgname/0.3/$_pkgname-$_pkgver.tar.bz2 passwordmanager.c passwordmanager.js wscript.diff README lpins.c)
md5sums=('c4cb0686601b1c470c317de3d3f8e8fd'
         '490cf9fa452487edc771a88da2f8ef20'
         'dfb07d7d700803d7553d3a0b2dfc982b'
         'ff94238eccfbb8dacb94ad5c77e19829'
         'defadec00ebd02506fb1106f7fe5f2ae'
         '5adde08b2f7480f8c3e577f9bbe5068a')

build() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"

  patch -Np1 -i "${srcdir}/wscript.diff"
  # python2 
  export PYTHON=/usr/bin/python2
  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" wscript waf
  cp "$srcdir/passwordmanager.c" extensions

  ./configure --prefix=/usr --jobs=${MAKEFLAGS/-j/} \
	--enable-addons \
	--disable-hildon \
	--enable-unique \
	--enable-nls \
      --disable-vala
  make
  cc `pkg-config --cflags --libs gnome-keyring-1` "$srcdir/lpins.c" -o lpins
}

package() {
  cd "${srcdir}/${_pkgname}-${_pkgver}"
  install -Dm644 _build_/default/extensions/libpasswordmanager.so \
     "${pkgdir}/usr/lib/midori/libpasswordmanager.so"
  install -Dm644 "${srcdir}/passwordmanager.js" \
     "${pkgdir}/usr/share/midori/res/passwordmanager.js"
  install -Dm755 lpins "${pkgdir}/usr/bin/lpins"
  install -Dm644 "${srcdir}/README" "${pkgdir}/usr/share/doc/$pkgname/README"
}
