# Maintainer: Padfoot <padfoot at exemail dot com dot au>

pkgname=lightdm-gtk-greeter
pkgver=1.1.5
pkgrel=5
pkgdesc="GTK+ greeter for LightDM"
arch=('i686'
      'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3'
         'LGPL3')
depends=('lightdm>=1.2.0'
         'gtk3')
makedepends=('gtk-doc'
             'gnome-common'
             'gnome-doc-utils'
             'gobject-introspection'
             'intltool'
             'patch')
optdepends=('xorg-server-xephyr: run lightdm in test mode')
conflicts=('lightdm-gtk-greeter-osk'
           'lightdm-gtk-greeter-bzr')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
install='lightdm-gtk-greeter.install'
source=("https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        "lightdm-gtk-greeter.conf"
        "lightdm-gtk-greeter-login.patch")
md5sums=('e42cedadbebd5b45733b32db54b83a44'
         'd8b7a9bb10dee7597af9e8d310446829'
         '9250a9dba9f2aa558ef9c4e24f025220')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i $srcdir/lightdm-gtk-greeter-login.patch

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm \
    --disable-static
    
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make DESTDIR="${pkgdir}" install

  cp ../lightdm-gtk-greeter.conf $pkgdir/etc/lightdm
}
