# Maintainer: Maxime Morel <maxime@mmorel.eu>

pkgname=armv6l-unknown-linux-gnueabihf
pkgver=7.9.1
pkgrel=1
pkgdesc="The GNU Debugger for the armv6 (raspberry pi 1) target"
arch=('x86_64' 'i686')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('gdb')
makedepends=('texinfo')
source=(http://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.xz)
sha1sums=('04ba2906279b16b5f99c4f6b25942843a3717cdb')

prepare() {
  cd gdb-$pkgver

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd gdb-$pkgver

  #guile support has a severe bug https://sourceware.org/bugzilla/show_bug.cgi?id=17247
  ./configure --prefix=/usr --disable-nls \
    --with-system-readline \
    --with-python=/usr/bin/python2 \
    --target=armv6l-unknown-linux-gnueabihf
  make
}

package() {
  cd gdb-$pkgver
  make DESTDIR=$pkgdir install

  # resolve conflicts with binutils and gdb
  rm -rf $pkgdir/usr/include/
  rm -rf $pkgdir/usr/share/
}
