# Contributor: N3ON <archlinux@alunamation.com>
# Contributor: Emiliano Vavassori <syntaxerrormmm@gmail.com>

pkgname="pencil-svn"
pkgver=259
pkgrel=1
pkgdesc="Animation/drawing software, that lets you create traditional hand-drawn animation using both bitmap and vector graphics"
url="http://pencil-animation.org/"
license=("GPL2")
arch=("i686" "x86_64")
depends=("qt" "phonon" "ffmpeg")
makedepends=("subversion" "mesa")
source=("pencil.desktop" "pencil.sh")
md5sums=("01a9978f3326c5dc0abe946a00e23fca" "67292fa3b8775b3be75e57fafdc699ab")

# _svntrunk="https://pencil-planner.svn.sourceforge.net/svnroot/pencil-planner/trunk/"
# _svnmod="pencil"
_svntrunk="https://pencil-planner.svn.sourceforge.net/svnroot/pencil-planner/branches/v0.5/"
_svnmod="pencil-v0.5"

build() {
	msg "Connecting to SVN server..."
	if [ -d ${_svnmod}/.svn ]; then
		(cd "${_svnmod}" && svn cleanup && svn update -r ${pkgver})
	else
		svn co ${_svntrunk} ${_svnmod} -r ${pkgver} --config-dir ./
	fi

	rm -rf build
	cp -rf ${_svnmod} build
	cd build

	# Compile fixs
	sed -e "18d" -e "26d" -i src/main.cpp
	cat <<-EOF | sed --file=- -i src/structure/layersound.h
		s|^#include <phonon>$|#include <phonon/MediaObject>\n\
		#include <phonon/AudioOutput>|
	EOF

	qmake \
		"QMAKE_CFLAGS+=-fpermissive" \
		"QMAKE_CXXFLAGS+=-fpermissive"
	make

	install -d "${pkgdir}/opt/pencil/plugins"
	install -m755 release/plugins/* "${pkgdir}/opt/pencil/plugins"
	install -D -m755 release/Pencil "${pkgdir}/opt/pencil/Pencil"
	install -D -m644 icons/icon.png "${pkgdir}/usr/share/pixmaps/pencil.png"
	install -D -m755 "${srcdir}/pencil.sh" "${pkgdir}/usr/bin/pencil"
	install -D -m644 "${srcdir}/pencil.desktop" \
		"${pkgdir}/usr/share/applications/pencil.desktop"
}

# vim: set noet ff=unix:
