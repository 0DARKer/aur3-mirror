# Maintainer: Chris Brannon <cmbrannon (at) cox.net>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>
# Contributor: Chuck Boucher <jofywyxaxukyvofa (at) tempomail.fr>
pkgname=edbrowse
pkgver=3.4.6
pkgrel=2
pkgdesc="Editor, browser, and mail client based on /bin/ed"
arch=('i686' 'x86_64')
url="http://edbrowse.sourceforge.net/"
depends=('spidermonkey' 'openssl' 'pcre' 'curl>=7.17.0' 'readline')
license=('GPL' 'openssl')
source=("http://www.eklhad.net/${pkgname}/${pkgname}-${pkgver}.zip" makefile.patch
        offbyone.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 < "$srcdir/makefile.patch"
  patch -p1 < "$srcdir/offbyone.patch"
  cd src
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"

  make DESTDIR="$pkgdir" prefix=/usr install
  # setup.ebrc is a shell script that builds a config file for you.  It's
  # in doc/.
  install -m755 ../doc/setup.ebrc "${pkgdir}/usr/bin"

  # How much of the documentation should be installed?
  cd ../doc
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 usersguide.html philosophy.html sample.ebrc "$pkgdir/usr/share/doc/edbrowse"
}
md5sums=('104efd4090539c94d3c321033a91b631'
         '4f3792bdb1999cc7868339bdde57b475'
         '6b86a513ec1dca1685c3e556c3e4fe3a')
