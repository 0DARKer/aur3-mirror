# Originally taken and inspired from Crakra (CCR):
# http://chakraos.org/ccr/pkgbuild_view.php?p=cutegram
# Original uploader to CCR: gnastyle
# Contributor from Chakra: FranzMari
# Contributor from Chakra: danyf90 <daniele.formihelli@gmail.com>
# Contributorfrom Chakra: totoloco <totoloco@gmx.com>

# Arch Linux (AUR):
# Maintainer: Llumex03
# Contributor: Thiago Perrotta <echo dGhpYWdvcGVycm90dGE5NUBnbWFpbC5jb20K | base64 -d >

pkgname=cutegram-git
pkgver=0
pkgrel=2
pkgdesc="Telegram client by Aseman Land"
arch=('i686' 'x86_64')
url="http://aseman.co/cutegram"
license=('GPL')
depends=('qt5-base' 'qt5-declarative' 'qt5-multimedia' 'qt5-quick1'
         'qt5-graphicaleffects' 'qt5-quickcontrols' 'libqtelegram-bzr')
makedepends=('git' 'cmake')
source=("${pkgname}"::"git+https://github.com/Aseman-Land/Cutegram.git")
md5sums=('SKIP')

pkgver() {
	cd "${srcdir}/${pkgname}"
	git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "${srcdir}/${pkgname}"
	mkdir -p build && cd build
	qmake-qt5 -r .. PREFIX=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}/build"
	make INSTALL_ROOT="${pkgdir}" install
}