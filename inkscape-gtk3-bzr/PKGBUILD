# Contributor: Splex
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=inkscape-gtk3-bzr
pkgver=12274
pkgrel=1
pkgdesc="An Open Source vector graphics editor, using Scalable Vector Graphics (SVG) file format, built with experimental gtk3 enabled"
url="https://launchpad.net/inkscape"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gc' 'poppler-glib' 'libxslt' 'gsl' 'popt' 'gtkspell3' 'imagemagick' 'desktop-file-utils' 'hicolor-icon-theme' 'gdl' 'gtkmm3' 'python2' 'libexif')
optdepends=('python2-numpy: some extensions'
            'python-lxml: some extensions and filters'
            'uniconvertor: reading/writing to some proprietary formats')
makedepends=('boost' 'pkg-config' 'intltool' 'bzr' 'gettext' 'autoconf' 'pango' 'fontconfig')
provides=('inkscape')
conflicts=('inkscape')
options=('!libtool')
source=('inkscape::bzr+http://bazaar.launchpad.net/~inkscape.dev/inkscape/trunk/' 'g_type_init.patch')
md5sums=('SKIP' '065afabec67f51c5358b58a9c4fc4009')
install=inkscape-bzr.install
_bzrmod="inkscape"

pkgver() {
  cd $srcdir/$_bzrmod
  bzr revno
}

prepare() {
  cd "$srcdir/$_bzrmod"
  patch -p1 < $srcdir/g_type_init.patch
}

build() {
  cd "$srcdir/$_bzrmod"
  ./autogen.sh
  ./configure LIBS='-lpangoft2-1.0 -lfontconfig' \
    --prefix=/usr \
    --without-gnome-vfs \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking \
    --enable-gtk3-experimental \
    --enable-dbusapi
     
  #fix for inkscape to use python2 with the python 3 package installed.
  sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
  sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
  sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
  sed -i 's|python -c |python2 -c|g' configure share/extensions/uniconv*.py
  sed -i 's|"python"|"python2"|g' src/main.cpp
  sed -i 's|"python"|"python2"|' share/extensions/ink2canvas/svg.py
  sed -i 's|"python"|"python2"|' share/extensions/ink2canvas/canvas.py
  make 
}

package() {
  cd "$srcdir/$_bzrmod"
  make DESTDIR=$pkgdir install
}
