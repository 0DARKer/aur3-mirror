# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=ropper-git
_gitname=Ropper
pkgver=1.4.1.91.212d5da
pkgrel=1
pkgdesc="Show information about binary files and find gadgets to build rop chains for different architectures"
arch=('any')
url="https://github.com/sashs/Ropper"
license=('GPL2')
depends=('python2-capstone')
makedepends=('git' 'python2-setuptools')
provides=('ropper')
conflicts=('ropper')
source=(git+https://github.com/sashs/${_gitname})
sha512sums=('SKIP')

pkgver() {
  cd ${_gitname}
  printf "%s.%s.%s" "$(python2 setup.py --version)" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_gitname}
  sed -ri 's/(ropper)\.py/\1/g' ropperapp/options.py
  sed -ri 's/env (python)/\12/g' ropperapp/printer/rawprinter.py ropperapp/loaders/raw.py
}

build() {
  cd ${_gitname}
  python2 setup.py build
}

package() {
  cd ${_gitname}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
