# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hilton Medeiros <medeiros.hilton AT gmail DOT com>

pkgname=ariamaestosa
pkgver=1.4.7
pkgrel=1
pkgdesc='MIDI editor, player, tracker and composer'
arch=('x86_64' 'i686')
url='http://ariamaestosa.sourceforge.net/'
license=('GPL2')
depends=('wxgtk2.9' 'mesa' 'alsa-lib' 'hicolor-icon-theme' 'xdg-utils'
         'desktop-file-utils')
optdepends=('timidity++: for midi playback'
            'timidity-freepats: for midi playback')
makedepends=('scons' 'setconf')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/AriaSrc-$pkgver.tar.bz2")
sha256sums=('84f9abcf9e1715b08e518230b29ac4b97aa71265405c388603ae713ae2afcfa6')

build() {
  cd "$srcdir/AriaSrc-$pkgver"

  msg2 'Preparing...'
  mv Resources/Documentation Resources/score .
  sed -i '/--rpath/d' SConstruct
  sed -i 's:,webview::' SConstruct

  msg2 'Compiling...'
  scons \
    config=release \
    prefix="$pkgdir/usr"

  setconf Freedesktop/Aria.desktop Exec "$pkgname"
  sed -i -e "/^Version/s:1.2.2:1.2.3:" -e "/^Icon/s:/local::" \
    Freedesktop/Aria.desktop
}

package() {
  cd "$srcdir/AriaSrc-$pkgver"

  msg2 'Packaging application...'
  scons \
    config=release \
    prefix="$pkgdir/usr" \
    install

  mkdir -p "$pkgdir/usr/share/doc"
  cp -r Documentation "$pkgdir/usr/share/doc/$pkgname"
  cp -r score "$pkgdir/usr/share/Aria"

  msg2 'Packaging icons...'
  install -Dm644 aria32.png \
    "$pkgdir/usr/share/icons/hicolor/32x32/apps/$pkgname.png"
  install -Dm644 aria48.png \
    "$pkgdir/usr/share/icons/hicolor/48x48/apps/$pkgname.png"
  install -Dm644 aria64.png \
    "$pkgdir/usr/share/icons/hicolor/64x64/apps/$pkgname.png"
  install -Dm644 aria128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/$pkgname.png"

  msg2 'Packaging application shortcut...'
  install -Dm644 Freedesktop/Aria.desktop \
    "$pkgdir/usr/share/applications/Aria.desktop"
  install -m644 Freedesktop/*.png Freedesktop/*.xml "$pkgdir/usr/share/Aria/"

  msg2 'Packaging license...'
  install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"

  msg2 'Cleaning up...'
  mv "$pkgdir/usr/bin/Aria" "$pkgdir/usr/bin/$pkgname"
  mv "$pkgdir/usr/share/Aria" "$pkgdir/usr/share/$pkgname"
  mv "$pkgdir/usr/share/applications/Aria.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
