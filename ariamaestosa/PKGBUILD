# Maintainer: Hilton Medeiros <medeiros.hilton AT gmail DOT com>

pkgname=ariamaestosa
_pkgname=AriaSrc
pkgver=1.2.3
pkgrel=1
pkgdesc="An open source MIDI composer, tracker/editor and player"
arch=('i686' 'x86_64')
url="http://ariamaestosa.sourceforge.net/"
license=('GPL2')
depends=('wxgtk>=2.8' 'mesa' 'alsa-lib' 'hicolor-icon-theme' 'xdg-utils' 'desktop-file-utils')
optdepends=('timidity++: for midi playback'
            'timidity-freepats: for midi playback')
makedepends=('scons')
install=ariamaestosa.install
source=("http://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.bz2")
md5sums=('b5cdc0fe9d0134baf86629bdbb894076')

build() {
  cd $srcdir/$_pkgname-$pkgver

  pushd libjdkmidi
  ./configure && make
  popd

  sed -i "/--rpath/d" SConstruct
  scons CXXFLAGS="$CXXFLAGS" prefix=$pkgdir/usr
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  # To make 'install' not recompile everything we give it the same variables.
  install -d $pkgdir/usr
  scons CXXFLAGS="$CXXFLAGS" prefix=$pkgdir/usr install

  install -Dm644 aria32.png $pkgdir/usr/share/icons/hicolor/32x32/apps/$pkgname.png
  install -Dm644 aria48.png $pkgdir/usr/share/icons/hicolor/48x48/apps/$pkgname.png
  install -Dm644 aria64.png $pkgdir/usr/share/icons/hicolor/64x64/apps/$pkgname.png
  install -Dm644 aria128.png $pkgdir/usr/share/icons/hicolor/128x128/apps/$pkgname.png

  cd Freedesktop
  sed -i -e "/^Version/s:1.2.2:1.2.3:" -e "/^Icon/s:/local::" Aria.desktop
  install -Dm644 Aria.desktop $pkgdir/usr/share/applications/Aria.desktop
  install -m644 *.png *.xml $pkgdir/usr/share/Aria/
}
