# Maintainer : zhulik <zhulik.gleb@gmail.com>

pkgname=mathgl-lite
pkgver=2.1
pkgrel=1
pkgdesc="A library for scientific data visualization, lite build"
arch=('i686' 'x86_64')
url='http://mathgl.sourceforge.net'
license=('GPL3')
depends=('qt' 'gsl' 'freeglut' 'mesa')
makedepends=('cmake')
provides=('udav')
conflicts=('udav' 'mathgl')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/project/mathgl/mathgl/mathgl%20$pkgver/mathgl-$pkgver.tar.gz")
md5sums=('025bc5a87b057f3736a94ce17874ef64')

build() {
  cd "$srcdir/mathgl-$pkgver"

  [[ -d bld ]] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DPYTHON_EXECUTABLE=/usr/bin/python2 \
           -Denable-all=OFF \
           -Denable-doc=OFF \
           -Denable-double=ON \
           -Denable-fltk=OFF \
           -Denable-gif=OFF \
           -Denable-glut=ON \
           -Denable-gsl=ON \
           -Denable-hdf4=OFF \
           -Denable-hdf5=OFF \
           -Denable-hdf5_18=OFF \
           -Denable-jpeg=OFF \
           -Denable-langall=OFF \
           -Denable-lgpl=OFF \
           -Denable-ltdl=ON \
           -Denable-octave=OFF \
           -Denable-opengl=ON \
           -Denable-pdf=OFF \
           -Denable-pthread=ON \
           -Denable-python=OFF \
           -Denable-qt=ON \
           -Denable-wx=OFF
  make
}

package() {
  cd "$srcdir/mathgl-$pkgver/bld"

  make DESTDIR="$pkgdir/" install
}
