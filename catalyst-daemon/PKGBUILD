# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: B. M. Kwapinski <lord.xml@web.de> (autofglrx daemon idea and most important code)
# Great Contributor: Shen Miren <dickeny@gmail.com> (build_module/hook idea and code)
# Contributor: aidanlinz
# Contributor: Rip-Rip


pkgname=catalyst-daemon
pkgver=11.10
pkgrel=2
pkgdesc="AMD/ATI drivers. Auto re-compile the fglrx module (while system's start) if kernel was updated"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')
depends=('catalyst-utils' 'gcc-libs' 'gcc>4.0.0' 'make' 'patch')
replaces=('catalyst' 'catalyst-hook')
conflicts=('catalyst-test' 'nvidia' 'xf86-video-ati' 'xf86-video-radeonhd' 'ati-dri' 'catalyst-hook' 'catalyst' 'catalyst-generator')
provides=("catalyst=${pkgver}")
install=${pkgname}.install

#patch_file="fglrx-2.6.36.patch"
#patch_md5="eb58dbf993dbf8d77924055cc8caaeb6"
#patch_ver="2.6.36"

source=(
    http://www2.ati.com/drivers/linux/ati-driver-installer-${pkgver/./-}-x86.x86_64.run
    autofglrx.sh
    catalyst_build_module
    ati_make.sh
    makefile_compat.patch)
#    ${patch_file})

md5sums=('d33cada9b3a23726fbd41ffd02636358'
         '8f1e38f645124924342f929ad2a4ec22'
         '8325563ec43fa746563fd2eaba330577'
	 '210fd25311893117fa6bd609eda086e1'
	 '3e1b82bd69774ea808da69c983d6a43b')
#         ${patch_md5})


build() {
  ## Unpack archive
    msg "Unpacking archive ..."
    /bin/sh ./ati-driver-installer-${pkgver/./-}-x86.x86_64.run --extract archive_files
}

package() {
    # License
      install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}" || return 1
      install -m644 "${srcdir}/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1

      if [ "${CARCH}" = "i686" ]; then
	cd "${srcdir}/archive_files/arch/x86/usr" || return 1
	_lib=lib
      elif [ "${CARCH}" = "x86_64" ]; then
	cd "${srcdir}/archive_files/arch/x86_64/usr" || return 1
	_lib=lib64
      fi

    # Prepare modules source files
      _archdir=x86_64
      test "${CARCH}" = "i686" && _archdir=x86
      cd "${srcdir}/archive_files/"
      install -m755 -d "${pkgdir}/usr/share/ati/build_mod" || return 1
      install -m644 common/lib/modules/fglrx/build_mod/*.c \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m644 common/lib/modules/fglrx/build_mod/*.h \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m644 common/lib/modules/fglrx/build_mod/2.6.x/Makefile \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m644 arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m755 -d "${pkgdir}/usr/bin" || return 1
      install -m755 "${srcdir}/catalyst_build_module" "${pkgdir}/usr/bin" || return 1

    # modified ati's make.sh script
      install -m755 "${srcdir}/ati_make.sh" "${pkgdir}/usr/share/ati/build_mod/" || return 1

    # makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
    # works only in combination with ati_make.sh script
      install -m644 "${srcdir}/makefile_compat.patch" "${pkgdir}/usr/share/ati/build_mod/" || return 1


    # optional patch
#      for ver in ${patch_ver}; do
#      install -m644 "${srcdir}/${patch_file}" \
#                "${pkgdir}/usr/share/ati/build_mod/${ver}.patch" || return 1
#      done

    # fglrx daemon
      install -m755 -d "${pkgdir}/etc/rc.d" || return 1
      install -m755 "${srcdir}/autofglrx.sh" "${pkgdir}/etc/rc.d/autofglrx" || return 1
}
