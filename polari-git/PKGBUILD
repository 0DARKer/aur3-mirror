# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=polari
pkgname=$_pkgname-git
pkgver=3.11.2.43.gb5d9127
_realver=3.11.2
pkgrel=1
pkgdesc="An IRC Client for GNOME"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/Polari"
license=('GPL2')
groups=('gnome')
depends=('gjs' "gtk3>=3.9.12" 'telepathy-glib' 'telepathy-idle' 'gnome-icon-theme-symbolic'
	 'telepathy-mission-control' 'telepathy-logger')
makedepends=('git' 'intltool' 'gobject-introspection' 'gnome-common')
options=('!libtool')
conflicts=('polari')
replaces=('polari')
provides=("polari=$_realver")
install=polari.install
source=('git://git.gnome.org/polari')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
