# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=polari
pkgname=$_pkgname-git
pkgver=133.2c832a0
_realver=0.0.1
pkgrel=1
pkgdesc="An IRC Client for GNOME"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/Polari"
license=('GPL2')
groups=('gnome')
depends=('gjs' "gtk3>=3.9.11" 'telepathy-glib')
makedepends=('git' 'intltool' 'gobject-introspection' 'gnome-common')
options=('!libtool')
conflicts=('polari')
replaces=('polari')
provides=('polari' "polari=$_realver")
install=polari.install
source=('git://git.gnome.org/polari'
        '0001-Add-Hebrew-translation.patch')
sha256sums=('SKIP'
            '96eb0ddeb688d1e8b008344198827891a8894b8aedf982d02f8afedb4d993ec9')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/$_pkgname"
  patch -Np1 -i "$srcdir/0001-Add-Hebrew-translation.patch"
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
