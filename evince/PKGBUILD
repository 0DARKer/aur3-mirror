
pkgname=evince-gtk2
pkgbase=evince
pkgver=2.32.0
pkgrel=1
pkgdesc="Simply a document viewer (GTK2 version, GNOME free!)"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/evince/"
license=('GPL')
depends=('gtk2' 'poppler-glib' 'libspectre' 'djvulibre' 't1lib' 'gsfonts' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool')
provides=('evince')
conflicts=('evince')
options=('!libtool')
install=evince-gtk2.install
source=(http://ftp.gnome.org/pub/gnome/sources/evince/2.32/$pkgbase-$pkgver.tar.bz2 )
md5sums=('ebc3ce6df8dcbf29cb9492f8dd031319')

build() {
  cd $srcdir/$pkgbase-$pkgver

  # Don't depend on gnome-icon-theme
  sed -i '/gnome-icon-theme/d' configure

  ./configure --prefix=/usr \
              --libexecdir=/usr/lib/evince \
              --disable-static \
              --disable-schemas-compile \
              --disable-nls \
              --disable-dbus \
              --disable-schemas-install \
              --disable-tests \
              --disable-nautilus \
              --disable-thumbnailer \
              --disable-previewer \
              --disable-scrollkeeper \
              --disable-help \
              --enable-t1lib \
              --enable-pixbuf \
              --enable-impress \
              --without-keyring \
              --without-gconf
  make
}

package() {
    cd $srcdir/$pkgbase-$pkgver

    make DESTDIR="$pkgdir" install

    rm -rf ${pkgdir}/usr/share/gtk-doc
    rm -rf ${pkgdir}/usr/share/GConf
#    rm -rf ${pkgdir}/usr/share/locale

}
