#Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
pkgname=gtkterm
pkgver=0.99.6
pkgrel=1
pkgdesc="Serial port terminal"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/gtkterm/"
license=('GPL')
depends=('vte' 'hicolor-icon-theme')
install=gtkterm.install
source=(https://fedorahosted.org/released/gtkterm/${pkgname}-${pkgver}.tar.gz
        gtkterm.desktop
        gtkterm.png
        gtkterm-0.99.5-scrollback.patch
        gtkterm-0.99.6-ubuntu-fixes.patch
        )
md5sums=('49fca8132e36781378c1f5c6b60291cb'
         '905e043707ed71b1e3cc3420a54ecde1'
         '9e25fa8914f415a1d9f6d60801752b6a'
         'e6b1674c98642758390f53a458c3d527'
         'c44c5a8da625f8c3549522eb53eb064e')
build() {
    cd $pkgname-$pkgver
    patch -Np1 -i ../gtkterm-0.99.5-scrollback.patch
    patch -Np1 -i ../gtkterm-0.99.6-ubuntu-fixes.patch
    ./configure --prefix=/usr --mandir=/usr/share/man
    # Hack fix broken Makefile
    sed -i 's|@MKINSTALLDIRS@|../mkinstalldirs|g' po/Makefile
    make
}

package() {
    install -Dm644 gtkterm.desktop ${pkgdir}/usr/share/applications/gtkterm.desktop
    install -Dm644 gtkterm.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/gtkterm.png
    cd $pkgname-$pkgver
    make install DESTDIR=${pkgdir}
}
