# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=fotoxx
pkgver=12.01
pkgrel=1
pkgdesc="A program for improving image files made with a digital camera"
url="http://kornelix.squarespace.com/fotoxx"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gimp-ufraw' 'perl-exiftool' 'xdg-utils' 'desktop-file-utils')
optdepends=('mashup: for composing pages to print from within fotoxx' 'brasero: for burning')
source=("http://kornelix.squarespace.com/storage/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('891def0a4243790ce0e8be4a7f2196dc')
install=fotoxx.install

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's+xdg-deskto+#xdg-deskto+' Makefile
  make PREFIX=/usr 
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir PREFIX=/usr install
  cp $pkgdir/usr/share/applications/kornelix-$pkgname.desktop \
    $pkgdir/usr/share/applications/$pkgname.desktop 
  rm $pkgdir/usr/share/applications/kornelix-$pkgname.desktop 
}
