# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=flacon-qt5-git
pkgver=v1.0.1.2.gafe2338
pkgrel=1
pkgdesc="Extracts individual tracks from one big audio file containing the entire album of music and saves them as separate audio files. Qt5 Build (Git Version)"
arch=('x86_64' 'i686')
url="http://flacon.github.io/"
license=('LGPL2.1')
depends=('qt5-base' 'uchardet' 'shntool')
makedepends=('cmake' 'git' 'qt5-tools')
optdepends=('faac: For AAC support'
            'flac: For FLAC support'
            'lame: For MP3 support'
            'mac: For APE support'
            'mp3gain: For MP3 Replay Gain support'
            'vorbis-tools: For OGG support'
            'ttaenc: For TrueAudio support'
            'vorbisgain: For OGG Replay Gain support'
            'wavpack: For WavPack support')
conflicts=('flacon')
provides=('flacon')
install=flacon.install
source=('git+https://github.com/flacon/flacon.git')
sha1sums=('SKIP')

pkgver() {
  cd flacon
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../flacon \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT5=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
