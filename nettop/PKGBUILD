# Maintainer: Lara Maia <lara@craft.net.br>
# Contribuitor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=nettop
pkgver=0.2.3
pkgrel=5
pkgdesc="A program which looks like top, but is for network packets"
arch=('i686' 'x86_64')
url="https://web.archive.org/web/20090511201805/http://srparish.net/scripts/"
license=('BSD')
depends=('slang' 'libpcap')
makedepends=('patch' 'gcc')
source=(http://fossies.org/linux/privat/old/$pkgname-$pkgver.tar.bz2
        $pkgname-$pkgver-compile.diff)
md5sums=('c5412b675ea8639104544b971ef9bcea'
         '49a7768aeb22df656947448b5ef6e734')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -Np0 -i "${srcdir}"/$pkgname-$pkgver-compile.diff

  ./configure --prefix=/usr --sysconfdir=/etc/nettop
  make
}
  
package(){  
  cd "${srcdir}"/$pkgname-$pkgver

  install -d "${pkgdir}"/usr/bin \
             "${pkgdir}"/usr/share/{doc,licenses}/${pkgname}

  make prefix="${pkgdir}"/usr install

# License
  cat "${srcdir}"/$pkgname-$pkgver/${pkgname}.c | tail -n+3 | head -n26 > \
      "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE

  install -D -m644 ChangeLog README THANKS "${pkgdir}"/usr/share/doc/${pkgname}
}
