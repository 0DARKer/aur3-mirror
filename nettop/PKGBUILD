# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=nettop
pkgver=0.2.3
pkgrel=4
pkgdesc="A program which looks like top, but is for network packets"
arch=('i686' 'x86_64')
url="http://srparish.net/scripts/"
license=('BSD')
depends=('slang' 'libpcap')
makedepends=('patch' 'gcc')
source=(http://www.sourcefiles.org/Networking/Tools/Scripts/$pkgname-$pkgver.tar.gz
        $pkgname-$pkgver-compile.diff)
md5sums=('e76602717e79a26b0ab15ab6799fb1ef'
         '49a7768aeb22df656947448b5ef6e734')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -Np0 -i "${srcdir}"/$pkgname-$pkgver-compile.diff

  ./configure --prefix=/usr --sysconfdir=/etc/nettop
  make
}
  
package(){  
  cd "${srcdir}"/$pkgname-$pkgver

  install -d "${pkgdir}"/usr/bin \
             "${pkgdir}"/usr/share/{doc,licenses}/${pkgname}

  make prefix="${pkgdir}"/usr install

# License
  cat "${srcdir}"/$pkgname-$pkgver/${pkgname}.c | tail -n+3 | head -n26 > \
      "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE

  install -D -m644 ChangeLog README THANKS "${pkgdir}"/usr/share/doc/${pkgname}
}
