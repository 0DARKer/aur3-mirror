# Maintainer: moostik <mooostik_at_gmail.com>
# Contributor: Knut Franke - Knut.Franke*gmx.de
# Contributor: Olaf Leidinger <leidola@newcon.de>
# Contributor: Robal <nigrantis.tigris*.gmail.com>

pkgname=scidavis
pkgver=0.2.4
pkgrel=7
pkgdesc="Free application for Scientific Data Analysis and Visualization, started as a fork of QtiPlot"
arch=("i686" "x86_64")
depends=('qt-assistant-compat' 'qwtplot3d'
         'gsl' 'muparser' 'python2-pyqt'
	 'mesa' 'shared-mime-info' 'qwt5')
license=("GPL")
url="http://scidavis.sourceforge.net/"
source=("http://prdownloads.sourceforge.net/sourceforge/scidavis/scidavis-$pkgver.tar.bz2"
        "scidavis.pro.patch"
        "scidavis.install"
        "qt-assistant-compat.patch"
        "scidavis-python2.patch"
	"qwt5.patch")
install="scidavis.install"

build() {
  cd $srcdir/scidavis-$pkgver/scidavis 

  patch < $srcdir/scidavis.pro.patch 
  patch < $srcdir/qwt5.patch
  patch -p2 < $srcdir/qt-assistant-compat.patch
  patch < $srcdir/scidavis-python2.patch

  # There appears to be a problem with qmake not generating the install target for translation files,
  # at least with Qt 4.4.3. Target is generated if .qm files exist at qmake run time, though.
  (lupdate scidavis.pro && lrelease scidavis.pro)

  qmake
  make
}

package () {
  cd $srcdir/scidavis-$pkgver/scidavis
  make INSTALL_ROOT=$pkgdir install

  _pydir=`python2 -c "from distutils import sysconfig; print sysconfig.get_python_lib()"`
  python2 `dirname $_pydir`/py_compile.py $pkgdir/etc/scidavisrc.py
  python2 `dirname $_pydir`/py_compile.py $pkgdir/usr/share/scidavis/scidavisUtil.py

}

md5sums=('c5650475c5e0b22f9ee5dc49630a0e89'
         '5973f8d7e6af56bb3d50e6ac680ee3fb'
         '6afcf9cffa82ab640711071e71f8f706'
         '450b7139f913210dd8a0ffbde03e86be'
         'ff0a7cb32e8ed439f008bfd114cbf611'
         '159aad53f13328123d42bfb7a4adf1b8')
