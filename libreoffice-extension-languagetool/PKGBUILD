# Maintainer: 3ED <krzysztof1987 /at/ gmail /dot/ com>

pkgname=libreoffice-extension-languagetool
_pkgname=LanguageTool
pkgver=1.4
pkgrel=2
pkgdesc="An Open Source style and grammar checker"
arch=('any')
url="http://www.languagetool.org/"
license=('LGPL')
groups=('libreoffice-extensions')
depends=('libreoffice')
makedepends=('unzip')
provides=('languagetool')
conflicts=('languagetool')
source=(http://www.languagetool.org/download/${_pkgname}-${pkgver}.oxt languagetool.sh)
sha256sums=('509a0b1170b2662208930472f7b229142f88a96baa2245b2566f7001772f13da'
            '76143c859f05d93e9eec71fe95ea0e11e0c2a0aff74060b214e9d448bf393181')

package() {
  cd "$srcdir"
  
  _DEST="$pkgdir/usr/lib/libreoffice/share/extensions/$_pkgname"

  msg2 "Installing files.." # by install.. umask will be ignored, etc
  find -type d -exec install -dm755 "${_DEST}/{}" \;
  find -type f -exec install -m644 "{}" "${_DEST}/{}" \;

  install -dm755 "$pkgdir/usr/bin"
  install -m755 "$srcdir/languagetool.sh" "$pkgdir/usr/bin/languagetool"
}
