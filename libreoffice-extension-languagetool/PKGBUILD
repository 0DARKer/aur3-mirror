# Maintainer: 3ED <krzysztof1987 /at/ gmail /dot/ com>
# Contributor: jtts

pkgname=libreoffice-extension-languagetool
_pkgname=languagetool
pkgver=2.1
pkgrel=1
pkgdesc="An Open Source style and grammar checker (22 languages)"
arch=('any')
url="http://www.languagetool.org/"
license=('LGPL')
groups=('libreoffice-extensions')
depends=('libreoffice-common')
makedepends=("unzip")
source=("http://www.languagetool.org/download/LanguageTool-${pkgver}.oxt")
noextract=(${source[@]##*/})
sha256sums=('ea3c7bf3134c28e75ecbb55a33149370f0234606b4be4acc5c54fc40cf462e32')

package() {
  _DESTDIR="${pkgdir}/usr/lib/libreoffice/share/extensions/${_pkgname}/"

  install -dm755 "${_DESTDIR}"
  unzip -q "$srcdir"/LanguageTool-${pkgver}.oxt -d "${_DESTDIR}"
  chmod -R g-w,o-w "${_DESTDIR}"
}
