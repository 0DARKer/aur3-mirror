# Maintainer: 3ED <krzysztof1987 /at/ gmail /dot/ com>
# Contributor: jtts

pkgname=libreoffice-extension-languagetool
_pkgname=languagetool
pkgver=2.2
pkgrel=1
pkgdesc="An Open Source style and grammar checker (29 languages)"
arch=('any')
url="http://www.languagetool.org/"
license=('LGPL')
groups=('libreoffice-extensions')
depends=('libreoffice-common')
makedepends=("unzip")
source=("http://www.languagetool.org/download/LanguageTool-${pkgver}.oxt")
noextract=(${source[@]##*/})
sha256sums=('2a34804e7d4851646736e05853bcb2e5cc5ad60c7ab2e970efd326f5906ca738')

package() {
  _DESTDIR="${pkgdir}/usr/lib/libreoffice/share/extensions/${_pkgname}/"

  install -dm755 "${_DESTDIR}"
  unzip -q "$srcdir"/LanguageTool-${pkgver}.oxt -d "${_DESTDIR}"
  chmod -R g-w,o-w "${_DESTDIR}"
}
