# Maintainer: 3ED <krzysztof1987 /at/ gmail /dot/ com>

pkgname=libreoffice-extension-languagetool
_pkgname=LanguageTool
pkgver=1.4
pkgrel=4
pkgdesc="An Open Source style and grammar checker"
arch=('any')
url="http://extensions.services.openoffice.org/en/project/languagetool"
license=('LGPL')
groups=('libreoffice-extensions')
depends=('libreoffice-common' 'java-environment')
#depends=('libreoffice-common' 'languagetool')
makedepends=('unzip')
provides=('languagetool')
conflicts=('languagetool')
source=(http://www.languagetool.org/download/${_pkgname}-${pkgver}.oxt languagetool.sh)
sha256sums=('509a0b1170b2662208930472f7b229142f88a96baa2245b2566f7001772f13da'
            '76143c859f05d93e9eec71fe95ea0e11e0c2a0aff74060b214e9d448bf393181')

package() {
  cd "$srcdir"
  
  _DEST="$pkgdir/usr/lib/libreoffice/share/extensions/$_pkgname" #this is right destination for libreoffice!

  msg2 "Installing files.." # by install.. umask will be ignored, etc
  find -type d -exec install -dm755 "${_DEST}/{}" \;
  find -type f -exec install -m644 "{}" "${_DEST}/{}" \;
  # NOT FOR LINKS:      (find -type l ..)
  # Immediately after installation package "languagetool" takes 46MB of disk space,
  # and the package "libreoffice-extension-languagetool" only 30MB because the file
  # "LanguageTool-1.4.oxt" is not installed (no needed)..

  install -dm755 "$pkgdir/usr/"{bin,share}
  install -m755 "$srcdir/languagetool.sh" "$pkgdir/usr/bin/languagetool"

  ln -s "/usr/lib/libreoffice/share/extensions/$_pkgname" "$pkgdir/usr/share/languagetool"

## alternativly may be that
#  _DEST="$pkgdir/usr/lib/libreoffice/share/extensions/" #this is right destination for libreoffice!
#  install -dm755 "${_DEST}"
#  ln -s "/usr/share/languagetool" "${_DEST}/$_pkgname"
}
