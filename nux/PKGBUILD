# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=nux
pkgver=1.0.2
pkgrel=1
pkgdesc="Graphical user interface toolkit for applications that mixes OpenGL hardware acceleration with high quality visual rendering"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('LGPL')
depends=('gdk-pixbuf2' 'glew' 'pango' 'libsigc++' 'libxcomposite')
makedepends=('boost' 'doxygen')
options=('!libtool')
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        nux-0.9.40-fix-build-with-gcc46.patch
        nux-0.9.40-dont-redefine-glewmx.patch)
md5sums=('030fb1dbcad7e591e4a9813f3f54be4b'
         '9f56af1226e79d1a4984e4e919d8e23a'
         '09cb992c197add27d9e678eb0b9e47e8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/nux-0.9.40-fix-build-with-gcc46.patch"
  patch -Np1 -i "$srcdir/nux-0.9.40-dont-redefine-glewmx.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-tests --enable-documentation
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
