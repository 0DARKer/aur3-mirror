# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: Artyom Olshevskiy <siasia@siasia>

pkgname=java-service-wrapper
pkgver=3.5.19
pkgrel=1
pkgdesc="Enables a Java Application to be run as a Windows Service or Unix Daemon"
url="http://wrapper.tanukisoftware.com"
arch=('i686' 'x86_64' 'armv6h')
license=('GPL2')
conflicts=('java-service-wrapper-bin')
makedepends=('apache-ant' 'java-environment')
source=("http://wrapper.tanukisoftware.com/download/${pkgver}/wrapper_${pkgver}_src.tar.gz")
sha256sums=('3194e256fa0472d18303c1a3bcafb878ae0d302a8541e768aeb7ee2f4dc90ce4')

build() {
    cd "${srcdir}/wrapper_${pkgver}_src"
    [[ "$CARCH" = "x86_64" ]] && _arch=64 || _arch=32
    source /etc/profile.d/apache-ant.sh
    source /etc/profile.d/jdk.sh
    ant -Dbits=${_arch} jar compile-c-unix
}

package() {
    cd "${srcdir}/wrapper_${pkgver}_src"
    install -Dm644 doc/wrapper-community-license-1.1.txt "${pkgdir}/usr/share/licenses/java-service-wrapper/LICENSE"
    install -Dm755 bin/wrapper       "${pkgdir}/usr/bin/java-service-wrapper"
    install -Dm644 lib/libwrapper.so "${pkgdir}/usr/lib/java-service-wrapper/libwrapper.so"
    install -Dm644 lib/wrapper.jar   "${pkgdir}/usr/share/java/wrapper-${pkgver}.jar"
    ln -s /usr/share/java/wrapper-${pkgver}.jar "${pkgdir}/usr/share/java/wrapper.jar"
}
