# Maintainer: Filip Brcic <brcha@gna.org>

pkgname='android-qt5-qtbase-git'
pkgver=20130322
pkgrel=1
pkgdesc="Qt 5 for Android: qtbase module"
arch=('i686' 'x86_64')
url='http://qt-project.org/wiki/Qt5ForAndroid'
license=('GPL3' 'LGPL')
makedepends=('git' 'libgl' 'sqlite' 'zlib' 'python2' 'ruby' 'gperf' 'libxslt' 'fontconfig')
depends=('android-sdk' 'android-ndk' 'jdk7-openjdk')
groups=('qt-android' 'qt5-android')
options=('!libtool' '!strip')
optdepends=('android-ndk-necessitas: NDK optimized for Qt on Android with gold linker'
            'qtcreator: Qt IDE with support for Android (just setup the paths correctly)')

_gitroot="git://gitorious.org/qt/qtbase.git"
_gitname=qt5-qtbase
_gitbranch=dev

build() {
  if [ ! -d "${_gitname}" ]; then
    git clone -b ${_gitbranch} --depth 1 ${_gitroot} ${_gitname} && cd ${_gitname}
  else
    cd ${_gitname} && git reset --hard && git pull origin && git clean -dfx
  fi
  
  msg "GIT checkout done."

  unset CC
  unset CXX
  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  unset CARCH
  unset CHOST
  unset QMAKESPEC
  unset QTDIR

  ./configure -confirm-license -opensource \
    -developer-build \
    -xplatform android-g++ \
    -android-ndk /opt/android-ndk \
    -android-sdk /opt/android-sdk \
    -prefix /opt/android-qt5 \
    -docdir /usr/share/doc/android-qt5 \
    -nomake docs \
    -nomake examples \
    -nomake tests \
    -no-rpath

  make
}

package() {
  cd ${_gitname}
  make INSTALL_ROOT="${pkgdir}" install

  # Strip by hand, system strip doesn't work on arm binaries
  
  # Linux binaries in bin
  strip ${STRIP_BINARIES} ${pkgdir}/opt/android-qt5/bin/{moc,qdoc,qmake,rcc,uic} # can't use * because of syncqt
  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/lib/*.so* \
    ${pkgdir}/opt/android-qt5/plugins/*/*.so \
    ${pkgdir}/opt/android-qt5/plugins/platforms/android/*.so
  # arm static libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86/bin/arm-linux-androideabi-strip ${STRIP_STATIC} \
    ${pkgdir}/opt/android-qt5/lib/libQt5PlatformSupport.a
  # linux static libs in lib
  strip ${STRIP_STATIC} ${pkgdir}/opt/android-qt5/lib/libQt5{Bootstrap,OpenGLExtensions}.a
}
