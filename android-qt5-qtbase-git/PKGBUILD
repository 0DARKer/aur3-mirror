# Maintainer: Filip Brcic <brcha@gna.org>

pkgname='android-qt5-qtbase-git'
_basever="5.2.0"
pkgver=5.2.0_13408.c5a6b89
pkgrel=1
pkgdesc="Qt 5 for Android: qtbase module"
arch=('i686' 'x86_64')
url='http://qt-project.org/wiki/Qt5ForAndroid'
license=('GPL3' 'LGPL')
makedepends=('git' 'libgl' 'sqlite' 'zlib' 'python2' 'ruby' 'gperf' 'libxslt' 'fontconfig')
depends=('android-sdk' 'android-sdk-platform-tools' 'android-ndk-necessitas' 'jdk7-openjdk' 'android-platform-10')
groups=('qt-android' 'qt5-android')
options=('!libtool' '!strip')
optdepends=('qtcreator: Qt IDE with support for Android')
source=("${pkgname}::git://gitorious.org/qt/qtbase.git#branch=dev")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${pkgname}
  echo ${_basever}_$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  #git describe --always | sed 's|-|_|g' | sed 's|^v||'
}

build() {
  cd ${srcdir}/${pkgname}

  unset CC
  unset CXX
  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  unset CARCH
  unset CHOST
  unset QMAKESPEC
  unset QTDIR

  ./configure -confirm-license -opensource \
    -developer-build \
    -xplatform android-g++ \
    -android-ndk /opt/android-ndk \
    -android-sdk /opt/android-sdk \
    -prefix /opt/android-qt5 \
    -docdir /usr/share/doc/android-qt5 \
    -nomake docs \
    -nomake examples \
    -nomake tests \
    -no-rpath

  make
}

package() {
  cd ${srcdir}/${pkgname}
  make INSTALL_ROOT="${pkgdir}" install

  # Strip by hand, system strip doesn't work on arm binaries
  
  # Linux binaries in bin
  strip ${STRIP_BINARIES} ${pkgdir}/opt/android-qt5/bin/{moc,qdoc,qmake,rcc,uic} # can't use * because of syncqt
  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/lib/*.so* \
    ${pkgdir}/opt/android-qt5/plugins/*/*.so \
    ${pkgdir}/opt/android-qt5/plugins/platforms/android/*.so
  # arm static libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_STATIC} \
    ${pkgdir}/opt/android-qt5/lib/libQt5PlatformSupport.a
  # linux static libs in lib
  strip ${STRIP_STATIC} ${pkgdir}/opt/android-qt5/lib/libQt5{Bootstrap,OpenGLExtensions}.a
}
