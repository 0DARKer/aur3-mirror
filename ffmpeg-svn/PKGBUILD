# Contributor: raubkopierer <mail[dot]sensenmann[at]googlemail[dot]com>

pkgname=ffmpeg-svn
pkgver=25148
pkgrel=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=('i686' 'x86_64')
url="http://ffmpeg.mplayerhq.hu/"
license=('GPL')
depends=('lame' 'sdl' 'zlib' 'x264-git>=20100605' 'libtheora' 'opencore-amr' 'libvpx-git' 'libvorbis')
makedepends=('subversion')
provides=("ffmpeg=`date +%Y%m%d`" "qt-faststart")
conflicts=('ffmpeg')
source=()
md5sums=()

_svntrunk=svn://svn.ffmpeg.org/ffmpeg/trunk
_svnmod=ffmpeg

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_svnmod-build"
  mkdir "$_svnmod-build"
  cd "$_svnmod-build"

  "$srcdir/$_svnmod/configure" \
  --prefix=/usr \
  --enable-gpl \
  --enable-version3 \
  --enable-libmp3lame \
  --enable-libx264 \
  --enable-libopencore-amrnb \
  --enable-libopencore-amrwb \
  --enable-libtheora \
  --enable-postproc \
  --enable-shared \
  --enable-x11grab \
  --enable-libvorbis \
  --enable-libvpx \
  --arch=`uname -m` \
  || return 1

  make || return 1
  make doc/ff{mpeg,play,server}.1 || return 1

  make DESTDIR="$pkgdir" install || return 1
  make DESTDIR="$pkgdir" install-man || return 1

  make tools/qt-faststart || return 1
  cp -a tools/qt-faststart $pkgdir/usr/bin
}
