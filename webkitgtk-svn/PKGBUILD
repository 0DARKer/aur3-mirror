# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

pkgname=webkitgtk-svn
pkgver=151500
pkgrel=8
pkgdesc="GTK+ Web content engine library"
conflicts=('webkitgtk')
provides=("webkitgtk")
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=(libwebp libxt libxslt sqlite libsoup enchant libgl 
	 geoclue gtk3 gst-plugins-base-libs libsecret harfbuzz-icu)
makedepends=(subversion gtk2 gperf gobject-introspection python mesa 
	     ruby gtk-doc)
optdepends=('gtk2: Netscape plugin support')

options=('!libtool' '!emptydirs')
source=('webkitgtk3::svn+https://svn.webkit.org/repository/webkit/trunk')
md5sums=('SKIP')
_svnmod="webkitgtk3"

pkgver() {
  cd $SRCDEST/${_svnmod}
  svnversion
}

build() {
  cd "$srcdir"/$_svnmod
  ./autogen.sh
  ./configure --prefix=/usr --libexecdir=/usr/lib

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make 
  # ./Tools/Scripts/update-webkit 
  # ./Tools/Scripts/build-webkit --gtk3
}

package() {
  cd "$srcdir"/$_svnmod
  make -j1 DESTDIR="$pkgdir/" install 
  install -Dm644 Source/WebKit/LICENSE \
    "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
