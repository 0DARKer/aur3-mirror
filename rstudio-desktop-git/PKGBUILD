# Artem Klevtsov <a.a.klevtsov@gmail.com>
pkgname=rstudio-desktop-git
_gitname=rstudio
pkgver=0.98.996
pkgrel=1
pkgdesc="A powerful and productive integrated development environment (IDE) for R programming language"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('gmp4' 'libffi5' 'r>=2.11.1' 'qtwebkit' 'shared-mime-info' 'boost-libs>=1.5')
makedepends=('git' 'cmake>=2.8' 'boost>=1.5' 'jdk7-openjdk' 'apache-ant' 'unzip' 'openssl' 'pango' 'pam' 'wget')
optdepends=(
    'git: for git support'
    'subversion: for subversion suuport'
    'openssh-askpass: for a git ssh access')
provides=('rstudio-desktop' 'rstudio-desktop-bin')
conflicts=('rstudio-desktop' 'rstudio-desktop-bin')
install="${pkgname}.install"

source=('git://github.com/rstudio/rstudio.git')
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_gitname}
  git describe --tags --abbrev=0 | sed 's|v||g'
}

build() {
  cd "${srcdir}/${_gitname}/dependencies/common"
  ./install-gwt
  ./install-dictionaries
  ./install-mathjax
  ./install-pandoc
  ./install-packages

  if [ -d "${srcdir}/${_gitname}/build" ]; then
    rm -rf "${srcdir}/${_gitname}/build"
  fi
  mkdir "${srcdir}/${_gitname}/build"
  cd "${srcdir}/${_gitname}/build"

  cmake -DRSTUDIO_TARGET=Desktop \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio ..
}

package() {
  cd "${srcdir}/${_gitname}/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
