# Maintainer: Artem Klevtsov <a.a.klevtso@gmail.com>

pkgname=rstudio-desktop-git
_gitname=rstudio
pkgver=0.99.122
pkgrel=1
pkgdesc="A powerful and productive integrated development environment (IDE) for R programming language"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('boost-libs>=1.5' 'r>=2.11.1' 'hicolor-icon-theme' 'shared-mime-info' 'qt5-webkit')
makedepends=('git' 'cmake>=2.8' 'boost>=1.5' 'jdk7-openjdk' 'apache-ant' 'unzip' 'openssl' 'pango' 'libcups' 'qt5-svg' 'pam' 'wget')
optdepends=('git: for git support'
	    'subversion: for subversion suuport'
	    'openssh-askpass: for a git ssh access')
provides=('rstudio-desktop' 'rstudio-desktop-bin')
conflicts=('rstudio-desktop' 'rstudio-desktop-bin')
install="${pkgname}.install"

source=('git://github.com/rstudio/rstudio.git')
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_gitname}
  git describe --tags --abbrev=0 | sed 's|v||g'
}

prepare() {
  cd "${srcdir}/${_gitname}/src"
  # specify that namespace core is in the global namespace and not
  # relative to some other namespace (like its ::core not ::boost::core)
  find . \( -name *.cpp -or -name *.hpp \) -exec sed \
    -e 's@<core::@< ::core::@g' -e 's@\([^:]\)core::@\1::core::@g' -i {} \;

  cd "${srcdir}/${_gitname}/dependencies/common"
  ./install-gwt
  ./install-dictionaries
  ./install-mathjax
  ./install-pandoc
  ./install-libclang
  ./install-packages
}

build() {
  rm -rf "${srcdir}/${_gitname}/build"
  mkdir "${srcdir}/${_gitname}/build"
  cd "${srcdir}/${_gitname}/build"

  cmake -DRSTUDIO_TARGET=Desktop \
	-DCMAKE_BUILD_TYPE=Release \
	-DQT_QMAKE_EXECUTABLE=/usr/lib/qt4/bin/qmake \
	-DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio ..
}

package() {
  cd "${srcdir}/${_gitname}/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
