#
# Maintainer: JT Wilkinson <jowilkin at tu dot archserver dot org>
#
# Note: Apache ant and java must be on the path for make to succeed.
#       If either was installed as a dependency during the build, you
#       will need to log out and then log back in for the changes to
#       your path and JAVA_HOME environment variable to take effect.
#
pkgname=rstudio-desktop-git
_gitname=rstudio
pkgver=0.98.111
pkgrel=1
pkgdesc="A new integrated development environment (IDE) for R programming language"
arch=('i686' 'x86_64')
url="http://www.rstudio.org/"
license=('AGPL3')
depends=('r>=2.11.1' 'qt4' 'qtwebkit' 'boost-libs>=1.5' 'shared-mime-info')
makedepends=('git' 'cmake>=2.8' 'boost>=1.5' 'jdk7-openjdk' 'apache-ant' 'unzip' 'openssl' 'pam' 'wget')
optdepends=('git: for git support'
'subversion: for subversion suuport'
'openssh-askpass: for a git ssh access')
provides=('rstudio-desktop' 'rstudio-desktop-bin ')
conflicts=('rstudio-desktop' 'rstudio-desktop-bin ')
install="${pkgname}.install"

source=('git://github.com/rstudio/rstudio.git')
md5sums=('SKIP')

pkgver() {
  cd $srcdir/$_gitname
  git describe --tags --abbrev=0 | sed 's|v||g'
}

build() {
  cd "$srcdir/$_gitname/dependencies/common"
  ./install-dictionaries
  ./install-mathjax
  ./install-gwt

  if [ -d "$srcdir/$_gitname/build" ]; then
    rm -rf "$srcdir/$_gitname/build"
  fi

  mkdir "$srcdir/$_gitname/build"
  cd "$srcdir/$_gitname/build"

  cmake -DRSTUDIO_TARGET=Desktop -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr/lib/rstudio ..
}

package() {
  cd "$srcdir/$_gitname/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
