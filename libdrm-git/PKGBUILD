# Arch libdrm Maintainers:
#	Jan de Groot
#	Andreas Radke

pkgname=libdrm-git
pkgver=20120207
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libpciaccess')
provides=('libdrm=2.4.31')
makedepends=('cairo' 'udev')
options=('!libtool')
url="http://dri.freedesktop.org/"
conflicts=('libdrm')
source=('no-pthread-stubs.patch')
sha256sums=('64982850d1494b810ef111fec16c827857bb25502cc51faeea0f0bc5cddc5fd3')

_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='drm'

build() {

  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -Np1 -i "$srcdir/no-pthread-stubs.patch"
  
  ./autogen.sh
  ./configure --prefix=/usr \
      --enable-udev \
      --enable-intel \
      --enable-radeon \
      --enable-vmwgfx-experimental-api \
      --enable-nouveau-experimental-api
  make V=1
}

check() {
  cd "$srcdir/$_gitname-build"
  make -k check
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
