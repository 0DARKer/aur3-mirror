# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=xfirepower
pkgver=0.84
pkgrel=1
pkgdesc="A multiplayer tank game which is playable over the Internet"
arch=('i686' 'x86_64')
url="http://www.happypenguin.org/show?Firepower"
license=('custom')
depends=('libxpm' 'ttf-dejavu')
makedepends=('imake')
install=${pkgname}.install
source=("http://www.ibiblio.org/pub/linux/games/multiplayer/tank/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.patch"
        "LICENSE")
md5sums=('7f2251554c339727f6ee7351b97c0b38'
         '65bd6ae6f194369298e74c6052ed7242'
         'caf918cb2ce53b45eb4d40a075db2c16')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "../${pkgname}.patch"

  # Compile server
  cd server
  make || return 1

  # Compile client
  cd ../client
  xmkmf || return 1
  make Makefiles || return 1
  # We haven't man, so skip this step
  sed -i "/all.*(MANNEWSUFFIX)/d" Makefile
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install binaries
  install -Dm755 server/xfireserv "${pkgdir}/usr/bin/xfireserv"
  install -Dm755 client/xfire "${pkgdir}/usr/bin/xfire"

  # Install maps and motd file
  mkdir -p "${pkgdir}/usr/share/${pkgname}/maps"
  sed -i "s_maps/_/usr/share/xfirepower/maps/_" server/maps.list
  install -Dm644 server/maps.list "${pkgdir}/usr/share/${pkgname}/maps.list"
  install -Dm644 server/.motd "${pkgdir}/usr/share/${pkgname}/motd"
  install -Dm644 server/maps/*.map "${pkgdir}/usr/share/${pkgname}/maps/"

  # Install documentation and license file
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 TODO "${pkgdir}/usr/share/doc/${pkgname}/TODO"
  install -Dm644 server/CHANGES "${pkgdir}/usr/share/doc/${pkgname}/CHANGES"
  install -Dm644 server/maps/README "${pkgdir}/usr/share/doc/${pkgname}/README_MAPS"
  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
