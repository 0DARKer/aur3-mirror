# Maintainer: Junchun Guan <junchunx.guan@gmail.com>
pkgbase=mic
pkgname=(mic mic-native)
pkgver=0.24.3
pkgrel=1
pkgdesc="Image Creator for Linux Distribution"
arch=(any)
url="https://download.tizen.org/tools/latest-release"
license=('GPL2')
makedepends=('python2' 'python2-docutils')
source=(http://download.tizen.org/tools/archive/14.03.1/openSUSE_13.1/src/mic-0.24.3-14.1.src.rpm)
md5sums=('7886a5d4d0b44d55f8f106b2a5db3319')

build() {
  cd "$srcdir"
  tar -x -f "$pkgname"_"$pkgver".tar.gz
  cd "$srcdir/$pkgbase-${pkgver}"

  python2 setup.py build
  sed -i 's/rst2man/rst2man2/' Makefile
  make man
}

package_mic() {
  depends=('python2' 'urlgrabber' 'rpm-org' 'python2-lxml' 'cpio' 'bzip2' 'gzip')
  cd "$srcdir/$pkgbase-${pkgver}"
  python2 setup.py install --prefix=/usr --root="$pkgdir"

  install -d -m755 "$pkgdir/usr/share/man/man1"
  install -m644 doc/mic.1 "$pkgdir/usr/share/man/man1"
  install -d -m755 "$pkgdir/etc/bash_completion.d"
  install -m755 etc/bash_completion.d/$pkgbase.sh "$pkgdir/etc/bash_completion.d"
  install -d -m755 "$pkgdir/etc/zsh_completion.d"
  install -m755 etc/zsh_completion.d/_$pkgbase "$pkgdir/etc/zsh_completion.d"
}

package_mic-native() {
  depends=('mic' 'util-linux' 'psmisc' 'e2fsprogs' 'dosfstools>=2.11' 'device-mapper' 'syslinux' 'squashfs-tools' 'cdrkit' 'multipath-tools' 'mtools')
  cd "$srcdir/$pkgbase-${pkgver}"
  install -d -m755 "$pkgdir/usr/bin"
  install -m755 tools/mic "$pkgdir/usr/bin/mic-native"
  sed -i '1 c\#!/usr/bin/python2' "$pkgdir/usr/bin/mic-native"
}
