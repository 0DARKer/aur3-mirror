# Contributor: bubla <matej.tyc@gmail.com>

pkgname=mingw-w64-libtiff
pkgver=4.0.1
pkgrel=1
pkgdesc="Library for manipulation of TIFF images (mingw-w64)"
arch=('any')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('mingw-w64-crt' 'mingw-w64-libjpeg>=7' 'mingw-w64-zlib')
makedepends=('mingw-w64-gcc')
optdepends=('freeglut: for using tiffgt')
options=('!libtool' '!buildflags' '!strip')
source=(ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz)
md5sums=('fae149cc9da35c598d8be897826dfc63')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do 
    cd "${srcdir}/tiff-${pkgver}"
    mkdir -p build-${_arch}
    cd build-${_arch}

    ../configure --prefix=/usr/${_arch} --host=${_arch} --sysconfdir=/usr/${_arch}/etc --mandir=/usr/${_arch}/share/man
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/tiff-${pkgver}/build-${_arch}"

    make DESTDIR="${pkgdir}" install

    install -D -m644 ../COPYRIGHT "${pkgdir}/usr/${_arch}/share/licenses/${pkgname}/LICENSE"
  done
}
