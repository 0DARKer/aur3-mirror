# Maintainer: Filip Brcic <brcha@gna.org>
# Contributor: bubla <matej.tyc@gmail.com>

pkgname=mingw-w64-libtiff
pkgver=4.0.1
pkgrel=2
pkgdesc="Library for manipulation of TIFF images (mingw-w64)"
arch=('any')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('mingw-w64-crt' 'mingw-w64-libjpeg-turbo' 'mingw-w64-zlib')
makedepends=('mingw-w64-gcc')
optdepends=('freeglut: for using tiffgt')
options=('!libtool' '!buildflags' '!strip')
source=(ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz)
md5sums=('fae149cc9da35c598d8be897826dfc63')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/tiff-${pkgver}"

  # Use build system's libtool.m4, not the one in the package.
  rm -f libtool.m4

  libtoolize --force --copy
  aclocal -I . -I m4
  automake --add-missing --copy
  autoconf
  autoheader

  CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4 -fno-strict-aliasing"

  for _arch in ${_architectures}; do 
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --sysconfdir=/usr/${_arch}/etc \
      --mandir=/usr/${_arch}/share/man \
      --enable-static \
      --enable-shared
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/tiff-${pkgver}/build-${_arch}"

    make DESTDIR="${pkgdir}" install

    # Remove docs
    rm -rf ${pkgdir}/usr/${_arch}/share/doc
    rm -rf ${pkgdir}/usr/${_arch}/share/man

    # Remove binaries
    rm -rf ${pkgdir}/usr/${_arch}/bin/*.exe

    # Strip
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/bin/*.dll
    mv ${pkgdir}/usr/${_arch}/lib/*.dll.a ${pkgdir}/
    ${_arch}-strip --strip-unneeded ${pkgdir}/*.dll.a
    ${_arch}-strip --strip-debug ${pkgdir}/usr/${_arch}/lib/*.a
    mv ${pkgdir}/*.dll.a ${pkgdir}/usr/${_arch}/lib/
  done
}
