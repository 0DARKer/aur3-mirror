# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: G_Syme
# Contributor: encelo

pkgname=gaphor
pkgver=0.17.0
pkgrel=1
pkgdesc="An easy to use modeling environment for UML design"
arch=('any')
url="http://gaphor.sourceforge.net"
license=('GPL')
depends=('gaphas' 'python2-zope-component' 'python2-distribute' 'etk-docking')
makedepends=('python2-nose' 'python2-sphinx')
install="$pkgname.install"
source=("http://pypi.python.org/packages/source/g/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('d29e686d6d1743775c4ba43b652196fe')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # zope-component fix
  sed -i "/self/s/\.\(registry\)/\.global\1/" \
    gaphor/services/componentregistry.py

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir/"

  # desktop
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 iconsrc/$pkgname.svg \
    "$pkgdir/usr/share/pixmaps/$pkgname.svg"

  # python2 fix
  sed -i "s/\(#\!.*python\).*/\12/" \
    `grep -rl "#\!.*python" "$pkgdir"`
}
