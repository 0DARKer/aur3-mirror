# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: G_Syme
# Contributor: encelo

pkgname=gaphor
pkgver=0.16.0
pkgrel=2
pkgdesc="An easy to use modeling environment for UML design"
arch=('any')
url="http://gaphor.sourceforge.net"
license=('GPL')
depends=('gaphas' 'zope-component')
makedepends=('python2-distribute' 'python2-nose' 'python-sphinx')
install="$pkgname.install"
source=("http://pypi.python.org/packages/source/g/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('b209637ce4e9f3a6705c5eab0bc18eb1'
         'd46ba3c07279dd53a47386432de1d975')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  sed -i "s|\(env python\).*|\12|;s|\(bin/python\).*|\12|" \
      `grep -Erl "(env python|bin/python)" .`

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir/"

  # desktop and pixmaps
  install -Dm644 ../$pkgname.desktop \
          "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 iconsrc/$pkgname.svg \
          "$pkgdir/usr/share/pixmaps/$pkgname.svg"
}
