# Maintainer: ruario 
pkgname=opera-beta # Set to opera if you want to install as your main/stable build
_bigrelease=11.50
_buildver=1027
_betalevel=1
pkgver=${_bigrelease}b${_betalevel}
pkgrel=1
pkgdesc="A fast and secure web browser and Internet suite. Beta release version."
url="http://www.opera.com/browser/next/"
depends=('gcc-libs' 'libxt' 'freetype2' 'libxext')
optdepends=('gtk2: GTK integration'
	    'kdebase-runtime: KDE4 integration'
	    'gstreamer0.10-base-plugins: HTML5 open codecs support'
	    'gstreamer0.10-good: HTML5 open codecs support'
	    'gstreamer0.10-ffmpeg: HTML5 not so open codecs support'
	    'gstreamer0.10-bad-plugins: HTML5 not so open codecs support')
install=opera-beta.install
options=(!strip !zipman)
license=('custom:opera')
arch=('i686' 'x86_64')
_arch=i386
[ "$CARCH" = "x86_64" ] && _arch=x86_64
source=(http://ftp.opera.com/pub/opera/linux/${_bigrelease/./}b${_betalevel}/opera-next-${_bigrelease}-${_buildver}.${_arch}.linux.tar.xz)
sha256sums=('3cbef29f6ddc4c437bf0df75e654fb5266d562586c8361dad380e1d83226b7a4')
[ "$CARCH" = "x86_64" ] && sha256sums=('f9f8ca5edc63e61487911f39a7dfdd7c39ccbcb6c765adbb59e2878812831e62')

# Uncomment the following line, if you want your User Agent to include Arch Linux.
#_opdistro="Arch Linux"
 
package() {

	# 'Install' Opera into $pkgdir
	opera-next-${_bigrelease}-${_buildver}.${_arch}.linux/install --prefix /usr --name ${pkgname} --repackage "${pkgdir}/usr"
	install -D -m 644 "${pkgdir}/usr/share/${pkgname}/defaults/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

	# Insert an Arch User Agent string if set
	if [ -n "${_opdistro}" ]
	then
		mkdir -p "${pkgdir}/usr/share/${pkgname}/custom/defaults"
		echo "[ISP]" > "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
		echo "Id=${_opdistro}" >> "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
		chmod 644 "${pkgdir}/usr/share/${pkgname}/custom/defaults/operaprefs.ini"
	fi

}
