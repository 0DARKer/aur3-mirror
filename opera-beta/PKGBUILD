# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=opera-beta
pkgver=25.0.1614.18
pkgrel=1
pkgdesc='A fast and secure web browser and Internet suite - beta stream'
arch=('x86_64')
url='http://www.opera.com/browser/'
license=('custom:opera')
depends=('alsa-lib' 'nss' 'gtk2' 'gconf' 'libxss' 'libxtst' 'desktop-file-utils')
install=opera.install
options=(!strip)

if [[ "${CARCH}" == 'i686' ]]; then
	# no i686 at this time!
	_arch='i386'
	source=()
	sha256sums=()
elif [[ "${CARCH}" == 'x86_64' ]]; then
	_arch="${CARCH}"
	source=("http://get.geo.opera.com/pub/opera-beta/${pkgver}/linux/opera-beta_${pkgver}_amd64.deb")
	# alternative download
	#source=("ftp://ftp.opera.com/pub/opera-beta/${pkgver}/linux/opera-beta_${pkgver}_amd64.deb")
	sha256sums=('fc4e5be23ae9339ab46040e14ed7a899680b11bcbabb51544717fb4bd9bc3a2c')
fi

prepare() {
	cd ${srcdir}/

	# extract the tarball
	tar xJf data.tar.xz

	# fix opera to use libudev.so.1
	sed -i -e 's/libudev.so.0/libudev.so.1/g' usr/lib/x86_64-linux-gnu/opera-beta/opera-beta
}

package() {
	cd ${srcdir}/

	# copy to pkgdir
	cp -a usr/ "${pkgdir}/"

	# set suid bit for Opera sandbox
	chmod 4755 "${pkgdir}/usr/lib/x86_64-linux-gnu/opera-beta/opera_sandbox"
}

