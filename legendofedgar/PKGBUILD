#Maintainer: Jesse Jaara <gmail.com: jesse.jaara>

pkgname=legendofedgar
pkgver=0.98
pkgrel=2
pkgdesc="2D platformer, where you venture across the world, battle fearsome creatures and solve puzzles, whilst trying to rescue your father."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/legendofedgar/"
license=('GPL')
depends=('sdl_image' 'sdl_mixer' 'sdl_ttf' 'sdl_net')
install=edgar.install
source=("http://sourceforge.net/projects/legendofedgar/files/${pkgver}/edgar-${pkgver}-2.tar.gz")

build() {
	cd "${srcdir}/edgar-${pkgver}"

	sed -e 's|$(PREFIX)/share/games/edgar/|$(PREFIX)/share/edgar/|' \
	    -e 's|-lz|-lz -lm|' -i makefile

	make
}

package() 
{
	cd "${srcdir}/edgar-${pkgver}"

	make DESTDIR="${pkgdir}" BIN_DIR="${pkgdir}/usr/bin/" DATA_DIR="${pkgdir}/usr/share/edgar/" install
}
md5sums=('0e76d7ef6cc3be96651aa8e7a90a71dc')
