# Maintainer: Jesse Jaara <jesse.jaara@gmail.com>

pkgname=legendofedgar
pkgver=0.85
pkgrel=1
pkgdesc="2D platformer. You take on the role of Edgar as he ventures across the world, battling fearsome creatures and solving puzzles whilst trying to find and rescue his father."
arch=('i686' 'x86_64')
url="http://www.parallelrealities.co.uk/projects/edgar.php"
license=('GPL')
depends=('sdl_image' 'sdl_mixer' 'sdl_ttf' 'sdl_net')
source=("http://www.parallelrealities.co.uk/download/edgar/edgar-${pkgver}-1.tar.gz")

build() {
	cd "${srcdir}/edgar-${pkgver}"

	sed -e 's|$(PREFIX)/share/games/edgar/|$(PREFIX)/share/edgar/|' \
	    -e 's|install -o root -g games|install|' -e 's|-lz|-lz -lm|g' \
	    -i makefile

	make
}

package() 
{
	cd "${srcdir}/edgar-${pkgver}"

	make DESTDIR="${pkgdir}" BIN_DIR="${pkgdir}/usr/bin/" DATA_DIR="${pkgdir}/usr/share/edgar/" install

	mkdir ${pkgdir}/usr/share/pixmaps
	cp ${srcdir}/edgar-${pkgver}/icons/edgar.png ${pkgdir}/usr/share/pixmaps/
}
md5sums=('eea9249f6fa266efca91b8158374c55e')
