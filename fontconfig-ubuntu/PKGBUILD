# Maintainer: Tevin Zhang <mail2tevin {at} gmail {dot} com>
# Contributor: Andrea Fagiani <andfagiani {at} gmail {dot} com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Paul Bredbury <brebs@sent.com>

# Installation order:  freetype2-ubuntu fontconfig-ubuntu libxft-ubuntu cairo-ubuntu
pkgname=fontconfig-ubuntu
pkgver=2.8.0
_ubver=2.8.0-3ubuntu9
pkgrel=10
pkgdesc="A library for configuring and customizing font access, with Ubuntu's LCD rendering patches."
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntu/precise/+source/fontconfig"
license=('custom')
depends=('expat>=2.0.1' 'freetype2-ubuntu>=2.3.11')
conflicts=('fontconfig')
provides=("fontconfig=$pkgver")
options=('!libtool')
install=fontconfig.install
source=(http://archive.ubuntu.com/ubuntu/pool/main/f/fontconfig/fontconfig_$pkgver.orig.tar.gz
        http://archive.ubuntu.com/ubuntu/pool/main/f/fontconfig/fontconfig_$_ubver.diff.gz)

md5sums=('77e15a92006ddc2adbb06f840d591c0e'
         '9efc0828a5a38431d6a77f78383f8d7d')

build() {
  cd $srcdir/fontconfig-$pkgver

  patch -Np1 -i ../fontconfig_$_ubver.diff

  for _f in $(cat debian/patches/series) ; do
     patch -Np1 -i "debian/patches/$_f"
  done

  # Enable Position Independent Code for prelinking
  export CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
  make DESTDIR=$pkgdir install

  rm -f $pkgdir/etc/fonts/conf.d/*.conf

  # License
  install -Dm0644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING

  # Docs
  install -Dm0644 debian/changelog $pkgdir/usr/share/doc/fontconfig/changelog
}

