# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Dmitrij D. Czarkoff <czarkoff@gmail.com>

pkgname=gnubg-cli
_pkgname=gnubg
pkgver=20120811
pkgrel=1
pkgdesc="Backgammon (command line interface)"
arch=('i686' 'x86_64')
url="http://www.gnubg.org"
license=('GPL')
depends=('python2' 'glib2')
makedepends=('bison' 'flex')
conflicts=('gnubg' 'gnubg-cvs')
source=($url/media/sources/$_pkgname-source-SNAPSHOT-$pkgver.tar.gz)
md5sums=('c963ec6b48b32a2da414290f09cbf61d')

build() {
  cd "$srcdir"/$_pkgname
  ./autogen.sh
  ./configure --prefix=/usr --bindir=/usr/bin --sysconfdir=/etc --mandir=/usr/share/man --without-gtk
  make
}

package() {
  cd "$srcdir"/$_pkgname
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/share/icons
}
