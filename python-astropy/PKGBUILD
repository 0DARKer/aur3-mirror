# Contributor: Médéric Boquien <mboquien@free.fr>
# Maintainer: Médéric Boquien <mboquien@free.fr>
pkgname=python-astropy
pkgver=0.4.4
pkgrel=3
pkgdesc="A community python library for astronomy"
arch=('i686' 'x86_64')
url="http://www.astropy.org/"
license=('BSD')
depends=('python>=3.1' 'python-numpy>=1.5' 'python-scipy' 'python-h5py' 'cfitsio' 'expat' 'wcslib' 'erfa')
conflicts=('python-pyfits' 'python-vo')
makedepends=('cython')
source=("http://pypi.python.org/packages/source/a/astropy/astropy-${pkgver}.tar.gz" "wcslib.patch")
md5sums=('68da956109c5968aaa8fdab8256049ba'
         'b1b67ffcfbe14e65dbc289b2f5e42632')

prepare() {
  cd ${srcdir}/astropy-${pkgver}
  patch -Np1 -i ../wcslib.patch
}

build() {
  cd ${srcdir}/astropy-${pkgver}
  python setup.py build --use-system-libraries --offline 
}

package() {
  cd ${srcdir}/astropy-${pkgver}

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" licenses/*
  python setup.py install --offline --root=${pkgdir} --prefix=/usr --optimize=1
}

