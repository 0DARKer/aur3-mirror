# Contributor: Médéric Boquien <mboquien@free.fr>
# Maintainer: Médéric Boquien <mboquien@free.fr>
pkgname=python-astropy
pkgver=0.3
pkgrel=1
pkgdesc="A community python library for astronomy"
arch=('i686' 'x86_64')
url="http://www.astropy.org/"
license=('BSD')
depends=('python>=3.1' 'python-numpy>=1.5' 'python-scipy' 'python3-h5py' 'cfitsio' 'expat' 'wcslib' 'erfa')
conflicts=('python-pyfits' 'python3-vo')
makedepends=('cython')
source=("http://pypi.python.org/packages/source/a/astropy/astropy-${pkgver}.tar.gz")
md5sums=('d9aee73db94d86da1c67607d854c72b7')

build() {
  cd ${srcdir}/astropy-${pkgver}
  python setup.py build --use-system-libraries
}

package() {
  cd ${srcdir}/astropy-${pkgver}

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" licenses/*
  python setup.py install --root=${pkgdir} --prefix=/usr --optimize=1
}

