# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: F0ruD <fzerorubigd[at]gmail[dot]com>

pkgname=winswitch
pkgver=0.12.12
pkgrel=1
pkgdesc="Window-Switch is a client server tool to start and control virtual desktops"
arch=('any')
url="http://winswitch.org/"
license=('GPL3')
depends=('pygtk' 'pycrypto' 'twisted' 'python2-pyasn1' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('nautilus: nautilus sendto'
            'thunar: thunar sendto'
            'parti-all: xpra support'
            'nss-mdns: mdns support'
            'nxproxy: proxy support'
            'python-netifaces: firewall detection'
            'java-environment: graphical server signatures')
options=('!emptydirs')
install="$pkgname.install"
source=("${url}src/$pkgname-$pkgver.src.tar.bz2" add_build_info.py)
md5sums=('192634338399abdf83f220bf642c4c22'
         '3791e96b54836c16939653b1147ff634')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # missing definition
  cp ../${source[1]} .

  # replace libexec path
  sed -i "s|\(%s/\)libexec|\1share|" setup.py
  sed -i "s|\(usr/\)libexec|\1share|g" `grep -Erl /usr/libexec .`

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir/"

  # python2 fixes
  sed -i "s/env python/&2/" `grep -rl "env python" "$pkgdir"`
}
