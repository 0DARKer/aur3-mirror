# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libdvdcss
pkgname=lib32-${_pkgbase}
pkgver=1.2.11
pkgrel=1
pkgdesc="A portable abstraction library for DVD decryption (32 bit)"
url="http://www.videolan.org/libdvdcss"
arch=('x86_64')
license=('GPL')
depends=('lib32-glibc' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://download.videolan.org/pub/${_pkgbase}/${pkgver}/${_pkgbase}-${pkgver}.tar.bz2)
sha512sums=('32dec992cf2f352a09c4edc39ea16101fc9005f3aecc43460aa1b5834c104d3f2c1efc81051339fa18fd4f33cfa949fd17ffe84414519a92b91d0d8a1ae4c467')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/include"
}
