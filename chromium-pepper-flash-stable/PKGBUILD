# Maintainer: ava1ar <mail(at)ava1ar(dot)info>

pkgname=chromium-pepper-flash-stable
pkgdesc="Google Chrome's Pepper Flash plugin for Chromium (stable version)"
pkgver=11.7.700.203
pkgrel=2
epoch=1
_verbld=28.0.1500.71-209842
_channel='stable'
arch=('i686' 'x86_64')
url="http://www.google.com/chrome"
license=('custom:chrome')
depends=('chromium')
provides=("chromium-pepper-flash=${pkgver}")
optdepends=('pulseaudio-alsa: For PulseAudio users')
install=chromium-pepper-flash.install
source=(license.html::http://www.google.com/chrome/intl/en/eula_text.html)
sha1sums=('SKIP')
if [ "$CARCH" == x86_64 ]; then
        source+=(https://dl.google.com/linux/chrome/rpm/stable/x86_64/google-chrome-${_channel}-${_verbld}.x86_64.rpm)
        sha1sums+=('c3ee26996d5f37cf48534121e3b374a8baf3f332')
elif [ "$CARCH" == i686 ]; then
        source+=(https://dl.google.com/linux/chrome/rpm/stable/i386/google-chrome-${_channel}-${_verbld}.i386.rpm)
        sha1sums+=('5d5f1535d854f8d13d2936c4f0dfabeb86bc63e6')
fi

package() {
	install -d "${pkgdir}/usr/lib/PepperFlash"
	install -m644 opt/google/chrome/PepperFlash/* "${pkgdir}/usr/lib/PepperFlash"
	sed -i "s/flashver=.*/flashver=${pkgver}/" "${startdir}/chromium-pepper-flash.install"
	install -Dm644 "${srcdir}/license.html" "${pkgdir}/usr/share/licenses/${pkgname}/license.html"
}
