# Maintainer: ava1ar (mail(dot)avatar(at)gmail(dot)com)

pkgname=chromium-pepper-flash-stable
pkgver=11.6.602.171
pkgrel=2
_verbld=25.0.1364.152-185281
_channel='stable'
arch=('i686' 'x86_64')
_arch="$CARCH"
[ "$CARCH" = 'i686' ] && _arch='i386'
pkgdesc="Google Chrome's Pepper Flash plugin for Chromium (stable version)"
url="http://www.google.com/chrome"
license=('custom:chrome')
depends=('chromium')
optdepends=('pulseaudio-alsa: For PulseAudio users')
source=(http://dl.google.com/linux/chrome/rpm/stable/${_arch}/google-chrome-${_channel}-${_verbld}.${_arch}.rpm 
license.html::http://www.google.com/chrome/intl/en/eula_text.html)
install=chromium-pepper-flash.install
sha1sums=('a4c529b19058a45f704b7c093fa7a027c215e10a' 'cdda47faa791d56089f92854957070b1ec135c2e')
[ "$CARCH" = 'i686' ] && sha1sums[0]='a14eecfcc7f2a5aedf066f239b7cb151c3d03a5b'

package() {
  install -d "$pkgdir/usr/lib/PepperFlash"
  install -m644 opt/google/chrome/PepperFlash/* "$pkgdir/usr/lib/PepperFlash"
  sed -i "s/flashver=.*/flashver=$pkgver/" "$startdir/chromium-pepper-flash.install"
  install -Dm644 "$srcdir/license.html" "$pkgdir/usr/share/licenses/$pkgname/license.html"
}
