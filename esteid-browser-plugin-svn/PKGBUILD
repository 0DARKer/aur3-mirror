# Maintainer: Kevin <arc.mission at gmail>
pkgname=esteid-browser-plugin-svn
_pkgname=esteid-browser-plugin
pkgver=3774
pkgrel=1
pkgdesc="Estonian ID-card Firefox plugin & extension (Community dev version)"
arch=('x86_64' 'i686')
url="http://code.google.com/p/esteid"
license=('LGPL')
depends=('boost' 'smartcardpp-svn' 'firefox>=3.6' 'gtkmm')
makedepends=('cmake' 'subversion' 'unzip')
conflicts=('sk-esteidpkcs11loader-svn' 'sk-esteidfirefoxplugin-svn' 'esteid-browser-plugin-hg-svn')
source=("http://firebreath.googlecode.com/files/firebreath-1.3.2a.tar.bz2")
sha1sums=('2e3c12ffa5e1153d4d99d6c84076a6dc3d607ae6')

_svntrunk=https://esteid.googlecode.com/svn/$_pkgname/trunk
_svnmod=$_pkgname

build() {
  cd "$srcdir/"
  
  if [ -d $srcdir/firebreath-1.3.2a/projects/$_svnmod/.svn ]; then
    (cd $srcdir/firebreath-1.3.2a/projects/$_svnmod && svn up -r $pkgver)
  else
    mkdir $srcdir/firebreath-1.3.2a/projects
    cd "$srcdir/firebreath-1.3.2a/projects"
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/plugin-build"
  cp -r "$srcdir/firebreath-1.3.2a" "$srcdir/plugin-build"
  cd "$srcdir/plugin-build/"
  sed -i 's|(bits == 64) ? "/usr/lib64/" :||g' projects/esteid-browser-plugin/Mozilla/chrome/content/config.js
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DLIB_SUFFIX="" -DWITH_SYSTEM_BOOST=YES -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "$srcdir/plugin-build/"
  make DESTDIR="$pkgdir/" install
  mkdir -p $pkgdir/usr/lib/firefox-3.6/extensions/\{aa84ce40-4253-11da-8cd6-0800200c9a66\}/
  unzip *.xpi -d $pkgdir/usr/lib/firefox-3.6/extensions/\{aa84ce40-4253-11da-8cd6-0800200c9a66\}/
}
