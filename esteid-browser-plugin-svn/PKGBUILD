# Maintainer: kevku <kevku@gmx.com>
pkgname=esteid-browser-plugin-svn
pkgver=3782
pkgrel=1
pkgdesc="Estonian ID-card Firefox plugin & extension (Community dev version)"
arch=('x86_64' 'i686')
url="http://code.google.com/p/esteid"
license=('LGPL')
depends=('boost-libs' 'smartcardpp-svn' 'firefox>=4.0' 'gtkmm')
makedepends=('cmake' 'subversion' 'unzip' 'boost')
conflicts=('sk-esteidpkcs11loader-svn' 'sk-esteidfirefoxplugin-svn')
source=("http://firebreath.googlecode.com/files/firebreath-1.3.2a.tar.bz2")
md5sums=('e37f2db6ff387f59ccc2760c90a41938')

_svntrunk=https://esteid.googlecode.com/svn/${pkgname%-svn}/trunk
_svnmod=${pkgname%-svn}

build() {
  cd "$srcdir/"
  
  if [ -d $srcdir/firebreath-1.3.2a/projects/$_svnmod/.svn ]; then
    (cd $srcdir/firebreath-1.3.2a/projects/$_svnmod && svn up -r $pkgver)
  else
    mkdir $srcdir/firebreath-1.3.2a/projects
    cd "$srcdir/firebreath-1.3.2a/projects"
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/plugin-build"
  cp -r "$srcdir/firebreath-1.3.2a" "$srcdir/plugin-build"
  cd "$srcdir/plugin-build/"
  sed -i 's|(bits == 64) ? "/usr/lib64/" :||g' projects/esteid-browser-plugin/Mozilla/chrome/content/config.js
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DLIB_SUFFIX="" -DWITH_SYSTEM_BOOST=YES -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "$srcdir/plugin-build/"
  make DESTDIR="$pkgdir/" install
  mkdir -p $pkgdir/usr/lib/firefox-4.0/extensions/\{aa84ce40-4253-11da-8cd6-0800200c9a66\}/
  unzip *.xpi -d $pkgdir/usr/lib/firefox-4.0/extensions/\{aa84ce40-4253-11da-8cd6-0800200c9a66\}/
}
