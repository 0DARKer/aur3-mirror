# Maintainer: Juhani Numminen <juhaninumminen0@gmail.com>
pkgname=pentobi
pkgver=9.0
pkgrel=1
pkgdesc="computer opponent for the board game Blokus"
arch=('i686' 'x86_64')
url="http://pentobi.sourceforge.net"
license=('GPL3')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'qt5-base' 'qt5-svg'
         'shared-mime-info')
makedepends=('cmake' 'extra-cmake-modules' 'kio' 'qt5-tools')
optdepends=('kio: KDE thumbnailer')
install=${pkgname}.install
source=("http://dl.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'kde-frameworks-5.patch')
sha256sums=('29d76f5e360c4a89dd2384c6e15eb38e6858122b680589129959785f9e7668f2'
            '9ecf2ecbbe1170a469a9c9627ae5ceaff77bf7bbad92ce7f4d45692f19628cb0')

prepare() {
  mkdir -p build
  cd "${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/kde-frameworks-5.patch"
}

build() {
  cd build
  cmake "../${pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT5=ON \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DPENTOBI_BUILD_KDE_THUMBNAILER=ON
  make VERBOSE=1
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
}
