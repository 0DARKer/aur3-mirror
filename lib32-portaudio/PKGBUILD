_pkgbasename=portaudio
pkgname=lib32-$_pkgbasename
pkgver=19_20071207
pkgrel=5
pkgdesc="A free, cross-platform, open source, audio I/O library (32 bit)"
arch=('x86_64')
url="http://www.portaudio.com/"
license=('custom')
depends=('lib32-jack' 'lib32-alsa-lib' "$_pkgbasename")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://www.portaudio.com/archives/pa_stable_v$pkgver.tar.gz)
md5sums=('d2943e4469834b25afe62cc51adc025f')

build() {
  cd "${srcdir}/${_pkgbasename}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC="gcc -m32"
  make
}

package () {
  cd "${srcdir}/${_pkgbasename}"
  make DESTDIR=$pkgdir install
  rm -rf "${pkgdir}"/usr/{include,lib32/*.a}
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
