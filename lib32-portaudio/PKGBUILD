# Maintainer: josephgbr <rafael.f.f1 at gmail.com>
# Contributor: Xavius <xavierelf at gmail.com>

_pkgbasename=portaudio
pkgname=lib32-$_pkgbasename
pkgver=19_20110326
pkgrel=1
pkgdesc="A free, cross-platform, open source, audio I/O library (32 bit)"
arch=('x86_64')
url="http://www.portaudio.com/"
license=('custom')
depends=('lib32-jack' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://www.portaudio.com/archives/pa_stable_v$pkgver.tgz)
md5sums=('8f266ce03638419ef46e4efcb0fabde6')

build() {
  cd "$srcdir/$_pkgbasename"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC="gcc -m32"
  make
}

package () {
  cd "$srcdir/$_pkgbasename"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  rm -rf "$pkgdir/usr/include"
}
