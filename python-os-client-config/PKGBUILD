# Maintainer: Carl George < arch at cgtx dot us >

_module="os-client-config"
pkgname=("python-${_module}" "python2-${_module}")
pkgver="0.8.0"
pkgrel="1"
pkgdesc="Unified config handling for client libraries and programs"
arch=("any")
url="https://github.com/openstack/${_module}"
license=("Apache")
makedepends=("python-pbr" "python-setuptools" "python2-pbr" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('6f8bace5f8669a92a80b1611d53676ab24696bc7b68ed5dba3ba4243b5e7ef81')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-os-client-config() {
    depends=("python-yaml>=3.1.0")
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-os-client-config() {
    depends=("python2-yaml>=3.1.0")
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
