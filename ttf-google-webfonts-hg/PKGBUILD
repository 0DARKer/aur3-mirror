# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Michalis Georgiou <mechmg93@gmail.comr>
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=ttf-google-webfonts-hg
pkgver=2723
pkgrel=2
pkgdesc="Various TrueType fonts from the Google Webfonts Project."
arch=('any')
url="http://code.google.com/webfonts"
license=('various')
depends=('fontconfig' 'xorg-fonts-encodings')
makedepends=('mercurial')
provides=('ttf-google-webfonts')
conflicts=('ttf-google-webfonts'
           'jsmath-fonts'
           'lohit-fonts'
           'otf-goudy'
           'ttf-andika'
           'ttf-anonymous-pro'
           'ttf-cantarell'
           'ttf-cardo'
           'ttf-chromeos-fonts'
           'ttf-droid'
           'ttf-inconsolata'
           'ttf-kimberly_geswein_print'
           'ttf-nova'
           'ttf-oldstandard'
           'oldstand-font'
           'ttf-pt-mono'
           'ttf-ptsans'
           'ttf-pt-sans'
           'ttf-roboto'
           'ttf-sortsmillgoudy'
           'ttf-source-sans-pro'
           'ttf-ubuntu-font-family'
           'ttf-vollkorn')
# Uncomment the following line if you want
#replaces=('ttf-google-webfonts')
install=ttf.install
source=()

_hgroot="https://googlefontdirectory.googlecode.com/hg/"
_hgrepo="googlefontdirectory"

build() {
  cd "$srcdir"
  msg2 "Connecting to Mercurial server...."
  if [ -d $_hgrepo ] ; then
    cd $_hgrepo && hg pull -u
    msg2 "Local files are up to date."
  else
    hg clone $_hgroot $_hgrepo
  fi
  msg2 "Mercurial checkout done or server timeout."
}

package() {
  cd "$srcdir/$_hgrepo"
  install -dm755 "$pkgdir/usr/share/fonts/TTF"
  find . -maxdepth 3 -type f -name \*.ttf -exec install -Dm644 '{}' \
    "$pkgdir/usr/share/fonts/TTF" \;
}
