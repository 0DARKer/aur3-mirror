# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Åukasz Potocki <hrabia dot p at gmail dot com>

pkgname=pcmanfm-svn
pkgver=935
pkgrel=1
pkgdesc="An extremely fast, lightweight, yet feature-rich file manager with tabbed browsing"
arch=('i686' 'x86_64')
url="http://pcmanfm.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'fam' 'gtk2' 'hal' 'shared-mime-info'
         'startup-notification')
makedepends=('intltool' 'pkgconfig' 'subversion')
provides=('pcmanfm')
conflicts=('pcmanfm')
install=pcmanfm.install

_svntrunk=https://pcmanfm.svn.sourceforge.net/svnroot/pcmanfm/trunk
_svnmod=pcmanfm

build() {
  cd "$srcdir"

  msg "Connecting to $_svnmod.svn.sourceforge.net..."
  if [[ -d $_svnmod/.svn ]]; then
    cd $_svnmod && svn up -r $pkgver && cd ..
    msg2 "Local files updated"
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    msg2 "SVN checkout done"
  fi

  rm -rf $_svnmod-build
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  msg "Starting make..."
  ./autogen.sh
  ./configure --sysconfdir=/etc --prefix=/usr
  make || return 1
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install
}
