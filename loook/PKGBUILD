# Maintainer: efreak4u <efreak4u@gmx.net>
pkgname=loook
pkgver=0.7.2
pkgrel=1
pkgdesc="Loook is for Full-text search in Open Document Files. Written in Python3"
url="http://mechtilde.de/Loook/"
arch=('any')
license=('GPLv3')
depends=('python' 'tk')
source=("http://mechtilde.de/Loook/Downloads/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c3736cd9ce12c0a0e224d81b52d9efc53bc25e05928314553a0b60ee7116918a')

package() {
    # Enter Source Directory
    cd "${srcdir}/${pkgname}-${pkgver}"

    # Create Destination Directories
    install -d "${pkgdir}"/usr
    install -d "${pkgdir}"/usr/{bin,lib/loook,share}
    install -d "${pkgdir}"/usr/share/{man/man1,man/de/man1,applications}

    # Copy Loook and create Symlink in /usr/bin
    install -m 755 loook.py "${pkgdir}/usr/lib/loook/loook.py"
    ln -sv /usr/lib/loook/loook.py "${pkgdir}/usr/bin/loook"

    # Copy Messages
    cp -a locale/ "${pkgdir}/usr/share/"
    
    # Copy Man Pages
    install -m 644 man/loook.1 "${pkgdir}/usr/share/man/man1/loook.1"
    install -m 644 man/de/loook.1 "${pkgdir}/usr/share/man/de/man1/loook.1"

    # Copy .desktop file for executing Loook from application menu
    install -m 644 loook.desktop "${pkgdir}/usr/share/applications/loook.desktop"
}
