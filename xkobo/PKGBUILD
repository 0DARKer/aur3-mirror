# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>

pkgname=xkobo
pkgver=1.11
pkgrel=5
pkgdesc='The original game by Akira Higuchi'
arch=('i686' 'x86_64')
url='http://happypenguin.org/show?XKobo'
license=('GPL')
depends=('libxext' 'gcc-libs' 'ttf-dejavu')
makedepends=('imake')
source=("http://www.ibiblio.org/pub/X11/contrib/games/${pkgname}-${pkgver}.tar.gz"
        "01-modernize.patch"
        "02-${pkgname}.patch")
md5sums=('b350678e49ad1a319a73d3e23ba5ec06'
         '95ecd16a58144671fad93c3a481fdb18'
         '7f410c47a36199c2560e4939e3d2241a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "../01-modernize.patch"
  patch -p0 -i "../02-${pkgname}.patch"
  xmkmf -a
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make "DESTDIR=${pkgdir}" install
  install -Dm644 "${pkgname}.man" "${pkgdir}/usr/share/man/man6/${pkgname}.6"
}

# vim:set ts=2 sw=2 et:
