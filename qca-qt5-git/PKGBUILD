# Maintainer: Antonio Rojas

pkgname=qca-qt5-git
pkgver=r2131.bfe0784
pkgrel=1
pkgdesc="Qt Cryptographic Architecture"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://delta.affinix.com/qca/"
depends=('qt5-base' 'nss')
makedepends=('cmake' 'git')
source=("git://anongit.kde.org/qca.git")
md5sums=('SKIP')

pkgver() {
  cd qca
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../qca \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQCA_SUFFIX=qt5 \
    -DQCA_INSTALL_IN_QT_PREFIX=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
