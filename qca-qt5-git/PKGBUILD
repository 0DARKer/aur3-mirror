# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Antonio Rojas

pkgname=qca-qt5-git
pkgver=r2216.c32bc6f
pkgrel=1
pkgdesc="Qt Cryptographic Architecture"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://delta.affinix.com/qca/"
depends=('qt5-base' 'botan' 'pkcs11-helper')
makedepends=('cmake' 'git')
conflicts=('qca-qt5')
provides=('qca-qt5')
source=("git://anongit.kde.org/qca.git#branch=qt5")
md5sums=('SKIP')

pkgver() {
  cd qca
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  rm -fr build
  mkdir -p build
}

build() {
  cd build
  cmake ../qca \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQCA_SUFFIX="qt5" \
    -DQCA_INSTALL_IN_QT_PREFIX=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
