# Contributor: Libernux <dutchman55@gmx.com>
pkgname="brother-hl2140"
pkgver="2.0.2"
pkgrel=1
pkgdesc="LPR and CUPS driver for the Brother HL2140"
url="http://solutions.brother.com/linux/en_us/"
arch=('i686' 'x86_64')
license=('custom:brother commercial license')
depends=('cups')
install='brother-hl2140.install'
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/brhl2140lpr-$pkgver-$pkgrel.i386.rpm"
	"http://www.brother.com/pub/bsc/linux/dlf/cupswrapperHL2140-$pkgver-$pkgrel.i386.rpm"
	'cupswrapper-license.txt'
	'lpr-license.txt'
)
md5sums=(
	'0c4bbdc943c987c101266c832d6c8b7a'
	'931035c9b7a8e7ee667efefba4bba168'
	'97ad0cffd216059e9d1d3121899d8646'
	'5e87a3dc0f3e3438c088eda0f3565f0d'
)
build() {
	cd "$srcdir/usr/local/Brother/cupswrapper"
#  generate the ppd
	echo '#! /bin/sh' > genppd
	echo 'ppd_file_name=HL2140.ppd' >> genppd
	echo 'touch $ppd_file_name' >> genppd
	sed -n '/^cat <<ENDOFPPDFILE/,/^ENDOFPPDFILE/p' cupswrapperHL2140-2.0.2 >>genppd
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genppd
	chmod +x genppd
	./genppd
#  generate the wrapper
	echo '#! /bin/sh' > genwrapper
	sed -n '/^printer_model/,/^device_model/p' cupswrapperHL2140-2.0.2 >>genwrapper
	echo 'brotherlpdwrapper=brlpdwrapperHL2140' >>genwrapper
	echo 'touch $brotherlpdwrapper' >>genwrapper
	sed -n '/cat <<!ENDOFWFILTER!/,/!ENDOFWFILTER!/p' cupswrapperHL2140-2.0.2 >>genwrapper
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genwrapper
	chmod +x genwrapper
	./genwrapper
#  generate other
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/bin/brprintconflsr2"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/cupswrapper/brcupsconfig3"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/inf/braddprinter"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/lpd/filterHL2140"
	install -d "$srcdir/usr/share"
	mv "$srcdir/usr/local/Brother/" "$srcdir/usr/share/brother"
	rm "$srcdir/usr/share/brother/cupswrapper/cupswrapperHL2140-2.0.2"
	rm "$srcdir/usr/share/brother/cupswrapper/genppd"
	rm "$srcdir/usr/share/brother/cupswrapper/genwrapper"
	rm "$srcdir/usr/share/brother/inf/setupPrintcap"
	rm -rf "$srcdir/usr/local"
}
package() {
	cd "$pkgdir"
	cp -R "$srcdir/usr" "$pkgdir"
	cp -R "$srcdir/var" "$pkgdir"
	install -m 644 -D "$srcdir/usr/share/brother/cupswrapper/HL2140.ppd" "$pkgdir/usr/share/cups/model/HL2140.ppd"
	install -m 755 -D "$srcdir/usr/share/brother/cupswrapper/brlpdwrapperHL2140" "$pkgdir/usr/lib/cups/filter/brlpdwrapperHL2140"
	install -m 644 -D "$startdir"/cupswrapper-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/cupswrapper-licence.txt"
	install -m 644 -D "$startdir"/lpr-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/lpr-licence.txt"
}