# Maintainer: LEW21 <lew21@xtreeme.org>

pkgname=mesa-full
pkgver=20110827
_realver=7.11
pkgrel=1
pkgdesc="Full Mesa 3D graphics library with all its components, built from the git master branch. If you live in the US, you should delete --enable-texture-float \ line."
arch=(i686 x86_64)
url="http://mesa3d.org/"
license=('LGPL')
depends=('libdrm>=2.4.24' 'dri2proto>=2.3' 'glproto>=1.4.12' 'libxxf86vm' 'libxdamage' 'expat>=2.0.1' 'llvm')
makedepends=('pkgconfig')
optdepends=('libtxc_dxtn: S3TC support'
'mesa-demos: glxinfo and glxgears')
provides=("mesa=${_realver}" "libgl=${_realver}" "libglapi=${_realver}" "libgles=${_realver}" "libegl=${_realver}" "ati-dri=${_realver}" "intel-dri=${_realver}" "nouveau-dri=${_realver}")
conflicts=('mesa' 'libgl' 'libglapi' 'libgles' 'libegl' 'ati-dri' 'intel-dri' 'nouveau-dri' 'mach64-dri' 'mga-dri' 'r128-dri' 'savage-dri' 'tdfx-dri' 'unichrome-dri')

_gitroot="git://anongit.freedesktop.org/git/mesa/mesa"
_gitname="mesa"

build() {
	msg "Connecting to git.freedesktop.org GIT server...."

	if [ -d $_gitname ] ; then
		cd $_gitname && (git pull origin; cd ..)
		msg "The local files are updated."
	else
		git clone $_gitroot --depth=1
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf $_gitname-build
	cp -rH $_gitname $_gitname-build
	cd $_gitname-build

	# Classic r300, r600 & swrast are disabled - their Gallium versions are better.
	# Classic nouveau is for different hardware than Gallium nouveau, so both are enabled.

	# For Wayland support:
	# - install libwayland-git
	# - add wayland to --with-egl-platforms

	./autogen.sh --prefix=/usr --with-dri-driverdir=/usr/lib/xorg/modules/dri \
		--with-dri-drivers=i915,i965,nouveau,radeon,r200 \
		--with-gallium-drivers=r300,r600,nouveau,svga,swrast \
		--with-egl-platforms=drm,x11 \
		--enable-gallium-llvm \
		--enable-gallium-egl \
		--enable-shared-dricore \
		--enable-shared-glapi \
		--enable-egl \
		--enable-gles1 \
		--enable-gles2 \
		--enable-openvg \
		--enable-glx-tls \
		--enable-xcb \
		--enable-texture-float \

	make
}

package() {
	cd ${srcdir}/$_gitname-build

	make DESTDIR="${pkgdir}" install

	install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
	ln -sf libglx.xorg ${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so
}
