# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>                                                                                                                                                        
# Maintainer: Aliaksandr Stelmachonak <mail.avatar at gmail.com>

pkgname=networkmanagement-plasmoid-svn
pkgver=1153428
pkgrel=1
pkgdesc="KDE4 Network Management plasmoid, svn version"
arch=('i686' 'x86_64')
url="http://plasma.kde.org/"
license=('GPL')
depends=('kdelibs>=4.3.0' 'kdebase-workspace>=4.3.0' 'networkmanager>=0.7.1')
makedepends=('cmake' 'subversion' 'python' 'automoc4')
provides=('kdeplasma-applets-networkmanagement')
conflicts=('kdeplasma-applets-networkmanagement')
install=networkmanagement-plasmoid-svn.install
source=()
md5sums=()

_svntrunk=svn://anonsvn.kde.org/home/kde/trunk/extragear/base/networkmanagement/
_svnmod=networkmanagement

build() {
  cd ${srcdir}
  rm -rf ${srcdir}/build

  if [ -d ${_svnmod} ]; then
    msg "Updating SVN tree ..."
    cd ${_svnmod}
    svn revert -R .
    svn up -r ${pkgver} || return 1
    msg "SVN update finished, starting build ..."
  else
    msg "Doing SVN checkout ..."
    svn co ${_svntrunk} ${_svnmod} -r ${pkgver} || return 1
    cd ${_svnmod}
    msg "SVN checkout finished, starting build ..."
  fi

  mkdir -p ${srcdir}/build
  cd ${srcdir}/build

  cmake ../${_svnmod} -DCMAKE_INSTALL_PREFIX=/usr -DDBUS_SYSTEM_POLICY_DIR=/etc/dbus-1/system.d -DCMAKE_BUILD_TYPE=Release
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
