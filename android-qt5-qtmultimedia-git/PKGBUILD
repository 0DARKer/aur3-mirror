# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=android-qt5-qtmultimedia-git
_basever="5.1.0"
pkgver=5.1.0_925.46bf67b
pkgrel=1
pkgdesc="Qt 5 for Android: qtmultimedia module"
groups=('qt-android' 'qt5-android')
arch=('i686' 'x86_64')
url="https://qt.gitorious.org/qt/qtmultimedia"
license=('LGPL')
depends=('android-qt5-qtbase-git' 'android-qt5-qtxmlpatterns-git' 'android-qt5-qtdeclarative-git' 'android-qt5-qtjsbackend-git'
         'android-platform-11')
makedepends=('git')
options=('!libtool' '!strip')
source=("${pkgname}::git://gitorious.org/qt/qtmultimedia.git")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${pkgname}
  echo ${_basever}_$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  #git describe --always | sed 's|-|_|g' | sed 's|^v||'
}

build() {
  cd ${srcdir}/${pkgname}

  /opt/android-qt5/bin/qmake
  make
}

package() {
  cd ${srcdir}/${pkgname}
  
  make INSTALL_ROOT="${pkgdir}" install

  # Strip by hand, system strip doesn't work on arm binaries
  
  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/lib/*.so* \
    ${pkgdir}/opt/android-qt5/plugins/*/*.so \
    ${pkgdir}/opt/android-qt5/qml/*/*.so
}
