# Maintainer: Vincent Hourdin <aur at vinvin dot tf>
pkgname=goqat
pkgver=0.9.8
pkgrel=1
pkgdesc="Operates any CCD camera from Quantum Scientific Imaging (QSI) on the Linux platform."
url="http://canburytech.net/GoQat/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('goocanvas1')
optdepends=('qsiapi: QSI CDD camera support'
'ds9: displays CCD images (FITS format)'
'xpa: communication mechanism used to bind to DS9'
'libparapin: parallel port support'
'grace: plotting support'
'sextractor: analyse GoQats video recordings'
'libunicapgtk: uniform API for all kinds of video capture devices'
'libraw1394: support of firewire cameras or coverters from The Imaging Source'
'v4l-utils: use v4l devices (webcams) for autoguiding')
source=("http://canburytech.net/GoQat/download/${pkgname}-${pkgver}.tar.gz")
sha1sums=('ed39820c75fe5272de6b571d25c04cd8203a446c')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's/prefix/datarootdir/g' Makefile.am	# fix install dir
	sed -i 's/prefix/datarootdir/g' src/Makefile.am	# fix install dir
	truncate -s -290 Makefile.am			# remove post install hooks
	aclocal && autoconf && autoheader && automake
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

	# do the install hooks here since they cannot be executed in the DESTDIR environment
	chmod +x ${pkgdir}/usr/share/GoQat/data/*.pl ${pkgdir}/usr/share/GoQat/data/*.sh
	sed -i 's,@PREFIX\@,/usr/share,' "${pkgdir}/usr/share/GoQat/data/DemoTasks.txt"
}

