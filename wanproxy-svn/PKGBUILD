# Maintainer: kurych <kurych[at]mail[dot]ru>
pkgname=wanproxy-svn
pkgver=942
pkgrel=3
pkgdesc="Portable TCP proxy which makes TCP connections send less data, which improves TCP performance and throughput over lossy links, slow links and long links"
arch=('i686' 'x86_64')
url="http://wanproxy.org/"
license=('BSD')
makedepends=('subversion')
#options=()
backup=('etc/wanproxy/wanproxy.conf' 'etc/conf.d/wanproxy')
source=('wanproxy-fix-make.patch'
        'wanproxy.conf.d'
        'wanproxy.rc.d')
md5sums=('1dd0b1ddf8d26545852d53db59153ff8'
         'a3d57b2758b121b433fcbb6d4d4118b6'
         '0f40af0c583ed5b9d2da3d462678786c')

_svntrunk="http://wanproxy.org/svn/trunk"
_svnmod="${pkgname}"

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  patch -p1 -i "$srcdir/wanproxy-fix-make.patch"
  cd "$srcdir/$_svnmod-build/programs/wanproxy"
  make NDEBUG=1
}

package() {
  install -D -m755 "$srcdir/$_svnmod-build/programs/wanproxy/wanproxy" "${pkgdir}/usr/bin/wanproxy"
  install -D -m644 "$srcdir/$_svnmod-build/programs/wanproxy/wanproxy.conf" "${pkgdir}/etc/wanproxy/wanproxy.conf"
  install -D -m644 "$srcdir/wanproxy.conf.d" "${pkgdir}/etc/conf.d/wanproxy"
  install -D -m755 "$srcdir/wanproxy.rc.d" "${pkgdir}/etc/rc.d/wanproxy"
}

# vim:set ts=2 sw=2 et:
