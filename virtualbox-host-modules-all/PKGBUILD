# $Id$
# Maintainer: Ng Oon-Ee <ngoonee.talk@gmail.com
# Based off virtualbox-modules package, listed maintainers at the time below
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Sébastien Luttringer <seblu@aur.archlinux.org>

pkgname=virtualbox-host-modules-all
pkgdesc='Host kernel modules for VirtualBox'
pkgver=4.2.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
depends=('linux>=3.6' 'linux<3.7' 'virtualbox-host-source=4.2.4')
install=virtualbox-host-modules-all.install
makedepends=('linux-headers'
             "virtualbox-host-source>=$pkgver")

build() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf

if [ "$USE_PACMAN_VERSION" = "0" ]; then
  _KERNELS=`file /boot/* | grep 'Linux kernel.*boot executable' | sed 's/.*version \([^ ]\+\).*/\1/'`
else
  _PACKAGES=`pacman -Qsq linux`
  _KERNELS=`pacman -Ql $_PACKAGES | grep /modules.alias.bin | sed 's/.*\/lib\/modules\/\(.*\)\/modules.alias.bin/\1/g'`
fi

# Loop through all detected kernels
for _kernver in $_KERNELS;
do  
  # build host modules
  msg2 "Host modules for ${_kernver}"
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
done
}

package() {
if [ "$USE_PACMAN_VERSION" = "0" ]; then
  _KERNELS=`file /boot/* | grep 'Linux kernel.*boot executable' | sed 's/.*version \([^ ]\+\).*/\1/'`
else
  _PACKAGES=`pacman -Qsq linux`
  _KERNELS=`pacman -Ql $_PACKAGES | grep /modules.alias.bin | sed 's/.*\/lib\/modules\/\(.*\)\/modules.alias.bin/\1/g'`
fi

# Loop through all detected kernels
for _kernver in $_KERNELS;
do  
  _extraver=extramodules-${_kernver%.*}-${_kernver##*[0-9]-}

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "$srcdir/dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
done
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

# vim:set ts=2 sw=2 et:
