# Maintainter: Christoph Drexler <chrdr at gmx dot at>

pkgname=pcsc-cyberjack
pkgver=3.99.5_SP02
_pkgver=3.99.5final.SP02
pkgrel=1
pkgdesc="PCSC Driver for Reiner SCT cyberjack Cardreaders"
arch=('i686' 'x86_64')
url="http://support.reiner-sct.de/"
license=('LGPL')
replaces=('ctapi-cyberjack')
conflicts=('ctapi-cyberjack')
depends=('gcc-libs' 'glibc' 'libusb' 'pcsclite')
makedepends=('pkg-config')
options=('!libtool' '!docs')
source=("http://support.reiner-sct.de/downloads/LINUX/V${pkgver}/${pkgname}_${_pkgver}.tar.gz"
	99-cyberjack.rules)
md5sums=('7a0ac862a98fbeda8c8a27d4544969ac'
         '54d70581a387f9a5b62eb6cbfeb44024')

install=cyberjack.install

build() {
	# patch according to http://ludovicrousseau.blogspot.com/2011/06/new-version-of-pcsc-lite-173.html
	sed -i "s/FEATURE_MCT_READERDIRECT/FEATURE_MCT_READER_DIRECT/" "$srcdir/${pkgname}-${_pkgver}/ifd/ifd.cpp"

	cd "${srcdir}/${pkgname}-${_pkgver}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc/$pkgname  \
		--enable-udev \
		--with-usbdropdir=$(pkg-config libpcsclite --variable=usbdropdir)

	make

}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	make DESTDIR=$pkgdir install

	install -Dm644 "${srcdir}/99-cyberjack.rules" "${pkgdir}/lib/udev/rules.d/99-cyberjack.rules"
}