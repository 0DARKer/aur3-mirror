# Maintainter: Christoph Drexler <chrdr at gmx dot at>

pkgname=pcsc-cyberjack
pkgver=3.99.5
pkgrel=1
pkgdesc="PCSC Driver for Reiner SCT cyberjack Cardreaders"
arch=('i686' 'x86_64')
url="http://support.reiner-sct.de/"
license=('LGPL')
replaces=('ctapi-cyberjack')
conflicts=('ctapi-cyberjack')
depends=('gcc-libs' 'glibc' 'libusb' 'pcsclite')
makedepends=('pkg-config')
options=('!libtool' '!docs')
source=("http://support.reiner-sct.de/downloads/LINUX/V${pkgver}/${pkgname}_${pkgver}final.tar.gz")
md5sums=('db9ac6ce2a0e1b91ee73294b2d5b82b8')

install=(pcsc-cyberjack.install)

build() {
	cd "${srcdir}/${pkgname}-${pkgver}final"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc/$pkgname  \
		--enable-udev \
		--with-usbdropdir=$(pkg-config libpcsclite --variable=usbdropdir)

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}final"
	make DESTDIR=$pkgdir install
}
