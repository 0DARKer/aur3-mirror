# Maintainter: Christoph Drexler <chrdr at gmx dot at>

pkgname=pcsc-cyberjack
pkgver=3.99.5_SP03
_pkgver=3.99.5final.SP03
pkgrel=2
pkgdesc="PCSC Driver for Reiner SCT cyberjack Cardreaders"
arch=('i686' 'x86_64')
url="http://support.reiner-sct.de/"
license=('LGPL')
replaces=('ctapi-cyberjack')
conflicts=('ctapi-cyberjack')
depends=('gcc-libs' 'glibc' 'libusb' 'pcsclite')
makedepends=('pkg-config')
options=('!libtool' '!docs')
source=("http://downloads.reiner-sct.de/LINUX/V${pkgver}/${pkgname}_${_pkgver}.tar.gz"
        gcc-4.7.patch)
md5sums=('fdbd506ed755d20e16ae7fdb6c2fe9bf'
         '46f39556a130d4080bee3e73c1ca44f2')

build() {
	cd "${srcdir}/${pkgname}-${_pkgver}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc/$pkgname  \
		--enable-udev \
		--with-usbdropdir=$(pkg-config libpcsclite --variable=usbdropdir)

	patch -p0 < "${srcdir}/gcc-4.7.patch"

	make
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	make DESTDIR=$pkgdir install
}
