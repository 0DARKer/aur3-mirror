# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=pcsc-cyberjack
pkgver=3.99.5_SP05
_pkgver=3.99.5final.SP05
pkgrel=3
pkgdesc="PCSC Driver for Reiner SCT cyberjack Cardreaders"
arch=('i686' 'x86_64')
url="http://support.reiner-sct.de/"
license=('LGPL')
replaces=('ctapi-cyberjack')
conflicts=('ctapi-cyberjack')
depends=('libusb' 'pcsclite')
options=('!libtool' '!docs')
source=("http://support.reiner-sct.de/downloads/LINUX/V${pkgver}/${pkgname}-${_pkgver}.tar.bz2")
md5sums=('0538c937922f7010d70d80a0a8194fd0')

build() {
	cd "${srcdir}/${pkgname}-${_pkgver}"

	autoreconf -i

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc/${pkgname}  \
		--enable-udev \
		--with-usbdropdir=$(pkg-config libpcsclite --variable=usbdropdir)

	make
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	make DESTDIR=$pkgdir install
}
