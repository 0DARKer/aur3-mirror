# Maintainer: Bart Verhoeven <nepherte at archlinux dot us>

pkgname=mercury-compiler-rotd
pkgver=20100917
_rotd=2010-09-17
pkgrel=1
pkgdesc="daily snapshot of the mercury language compiler"
arch=('i686' 'x86_64')
url="http://www.cs.mu.oz.au/research/mercury/"
license=('GPL')
depends=('gcc' 'make')
makedepends=('flex' 'bison')
provides=(mercury-compiler)
conflicts=(mercury-compiler)
changelog=$pkgname.changelog
source=(http://www.mercury.cs.mu.oz.au/download/files/beta-releases/rotd/${pkgname}-${_rotd}-unstable.tar.gz)
md5sums=('59e80b894859287697b80df467e6106c')

build() {
  cd $srcdir/$pkgname-$_rotd

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --infodir=/usr/share/info --disable-debug-grades \
              --disable-inefficient-grades 

  # Uncomment PARALLEL in Makefile and change -j  accordingly to speed 
  # up compilation
  make 
}

package() {
  cd $srcdir/$pkgname-$_rotd

  make INSTALL_PREFIX="$pkgdir/usr" INSTALL_MAN_DIR="$pkgdir/usr/share/man" \
       INSTALL_INFO_DIR="$pkgdir/usr/share/info" \
       MERCURY_COMPILER="$pkgdir/usr/bin/mercury_compile" \
       MERCURY_CONFIG_DIR="$pkgdir/usr/lib/mercury" install
}

# vim:set ts=2 sw=2 et:
