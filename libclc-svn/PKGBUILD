# Maintainer: Armin K. <krejzi at email dot com>

pkgname=libclc-svn
pkgver=197978
pkgrel=1
pkgdesc="Library requirements of the OpenCL C programming language"
arch=('i686' 'x86_64')
url="http://libclc.llvm.org/"
license=('MIT')
makedepends=('clang' 'subversion' 'python2')
source=("${pkgname}::svn+http://llvm.org/svn/llvm-project/libclc/trunk")
md5sums=(SKIP)

pkgver() {
  cd ${pkgname}
  svnversion | tr -d [A-z]
}

prepare() {
  cd ${pkgname}
  # disable nvptx target, fixes build with latest clang/llvm
  sed -i "/default_targets/s:'nvptx--nvidiacl', 'nvptx64--nvidiacl', ::" configure.py
}

build() {
  cd ${pkgname}
  python2 ./configure.py --prefix=/usr
  make
}

package() {
  cd ${pkgname}
  make install DESTDIR="$pkgdir"
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

