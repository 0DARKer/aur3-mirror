# Maintainer: Carl Reinke <mindless2112 gmail com>
pkgname=utserver
pkgver=27079
pkgrel=1
pkgdesc="uTorrent server, BitTorrent client with web UI"
arch=('i686' 'x86_64')
url="http://www.utorrent.com/downloads/linux"
license=(custom)
makedepends=('unzip')
conflicts=('utorrent-server')
install=$pkgname.install

if [[ $CARCH == i686 ]]
then
    source=(
        http://download.utorrent.com/linux/utorrent-server-3.0-ubuntu-10.10-${pkgver}.tar.gz
        utserver utserver.conf utserver.conf.d utserver.rc.d
    )
    md5sums=(
        '145f795dd4a8ec9e1432efdde508d4f3'
        'cc2abac1ce3d0efe475f16b70257bace'
        '3ded0803577a244ff18269412983dc32'
        'b06944473a02fddfb56b6864aa85e255'
        '64c1b47703aac29300bd137c266573a3'
    )
    depends=('glibc' 'gcc-libs' 'bash' 'openssl098' 'zlib')
else 
    source=(
        http://download.utorrent.com/linux/utorrent-server-3.0-ubuntu-12.04-${pkgver}.x64.tar.gz
        utserver utserver.conf utserver.conf.d utserver.rc.d
    )
    md5sums=(
        'baac6a08d0653eb7d2880e61a92627f8'
        'cc2abac1ce3d0efe475f16b70257bace'
        '3ded0803577a244ff18269412983dc32'
        'b06944473a02fddfb56b6864aa85e255'
        '64c1b47703aac29300bd137c266573a3'
    )
    depends=('glibc' 'gcc-libs' 'bash' 'openssl' 'zlib')
fi

package()
{
    install -dm755 $pkgdir/srv/utserver/{downloads,settings,torrents}
    install -dm755 $pkgdir/usr/share/utserver/webui
    install -dm755 $pkgdir/usr/share/doc/utserver
    install -dm755 $pkgdir/var/log/utserver
    
    cd $srcdir/utorrent-server-v3_0
    install -Dm755 utserver $pkgdir/usr/share/utserver/utserver
    install -m644 docs/* $pkgdir/usr/share/doc/utserver
    unzip webui.zip -d $pkgdir/usr/share/utserver/webui
    
    cd $srcdir
    install -Dm755 utserver $pkgdir/usr/bin/utserver
    install -Dm755 utserver.conf $pkgdir/etc/utserver.conf
    install -Dm755 utserver.conf.d $pkgdir/etc/conf.d/utserver
    install -Dm755 utserver.rc.d $pkgdir/etc/rc.d/utserver
}
