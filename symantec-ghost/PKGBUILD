pkgname=symantec-ghost
pkgver=11.5
pkgrel=1
pkgdesc='Symantec Ghost for Linux'
arch=('i686' 'x86_64')
url='http://www.symantec.com/ghost-solution-suite'
license=('custom')
options=(!strip)
depends=('lib32-gcc-libs' 'lib32-libx11')
[[ "${CARCH}" == i686 ]] && depends=("${depends[@]/lib32-/}")
optdepends=('polkit: to run ghost directly from menu')
makedepends=('glibc')
source=('GSSB2174.zip'
	'ghost.desktop'
	'http://upload.wikimedia.org/wikipedia/en/5/58/Norton_Ghost_icon.png'
	'ghost.polkit'
	'readme.txt')
sha1sums=('aa5876a0f34543c134bd57f3458800d1ff0674b3'
	  'dcd427c4850b782a7d2848d46ffbab84c5cdeb65'
	  'efae361218502f3f088007c4075d1f0962d557c4'
	  'b391ce33c2e44fb2876551df60f4fd91e02efc53'
	  '00a09a95e9930e9aa8dc004c27886049fbdf87e5')

prepare() {
	iconv -f cp1251 -t utf8 readme.txt -o README.txt
	cp --attributes-only --preserve=timestamps readme.txt README.txt
}

package() {
	install -pDm744 ghost "${pkgdir}/usr/bin/ghost"
	install -pDm644 Norton_Ghost_icon.png "${pkgdir}/usr/share/icons/ghost.png"
	install -pDm644 ghost.desktop "${pkgdir}/usr/share/applications/ghost.desktop"
	install -pDm644 ghost.polkit "${pkgdir}/usr/share/polkit-1/actions/org.archlinux.pkexec.ghost.policy"
	install -pDm644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	ln -rs "${pkgdir}/usr/share/doc/${pkgname}/README" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
