msg2 "Hint: Put 'GSSB2174.zip' in the current directory by yourself, as this file is proprietary and should only be downloaded by symantec's customers"
msg2 "Note: Before using symantec-ghost, you should read the 'readme.txt'.\n\n"

pkgname=symantec-ghost
pkgver=11.5
pkgrel=2
pkgdesc='Symantec Ghost for Linux'
arch=('i686' 'x86_64')
url='http://www.symantec.com/ghost-solution-suite'
license=('custom')
options=(!strip)
depends=('lib32-gcc-libs' 'lib32-libx11')
[[ "${CARCH}" == i686 ]] && depends=("${depends[@]/lib32-/}")
optdepends=('polkit: to run ghost directly from menu')
makedepends=('glibc')
source=('ghost.desktop'
	'http://upload.wikimedia.org/wikipedia/en/5/58/Norton_Ghost_icon.png'
	'ghost.polkit'
	'ftp://ftp.norton.com/public/english_us_canada/products/symantec_ghost_solution_suite/2.5/manuals/readme.txt'
	'GSSB2174.zip')

prepare() {
	iconv -f cp1251 -t utf8 readme.txt -o README.txt
	cp --attributes-only --preserve=timestamps readme.txt README.txt
}

package() {
	install -pDm744 ghost "${pkgdir}/usr/bin/ghost"
	install -pDm644 Norton_Ghost_icon.png "${pkgdir}/usr/share/icons/ghost.png"
	install -pDm644 ghost.desktop "${pkgdir}/usr/share/applications/ghost.desktop"
	install -pDm644 ghost.polkit "${pkgdir}/usr/share/polkit-1/actions/org.archlinux.pkexec.ghost.policy"
	install -pDm644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
	install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
	ln -rs "${pkgdir}/usr/share/doc/${pkgname}/README" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
sha1sums=('dcd427c4850b782a7d2848d46ffbab84c5cdeb65'
          'efae361218502f3f088007c4075d1f0962d557c4'
          'b391ce33c2e44fb2876551df60f4fd91e02efc53'
          '00a09a95e9930e9aa8dc004c27886049fbdf87e5'
          'aa5876a0f34543c134bd57f3458800d1ff0674b3')
md5sums=('3d43174dbf38c169b167b753be48b081'
         '5fe8f55c43d151457d7c8fa90ca34014'
         '70339c3d04ef9ac7c318c2d0c2ca3531'
         'e2991b9c0c72d28692c67868a42771ea'
         '82dc10b16892bc74049c3b45134ca8e7')
sha256sums=('7d0b4a0f3c15927d8b9c788f61ea05d6dd591fea0db99e8cb5cacaba5dab218c'
            '849459ed3ff52e76547eb1f90d2963226a4bd9d9875473785cbf3922ab3ca0ac'
            '852c2208a95080923e7534146e86d0b50ff790a254161fad900029c7e44181e3'
            '39b5bca2401df511364904039fb8f692be3095f57df434245cfc36893416b176'
            'a477be351ba22d7f11bbeebfa2a40cad81d394e6c0c95657e3e4d2705876b228')
