#Maintainer: Lone_Wolf <lonewolf@xs4all.nl>
# Contributor: Steven She <mintcoffee@gmail.com>
# Contributor: vbPadre <vbPadre@gmail.com>
pkgname=cndrvcups-lb
pkgver=2.80
pkgrel=2
pkgdesc="Canon UFR II Printer Driver build from source for LBP, iR & MF printers"
arch=('i686' 'x86_64')
url="http://support-au.canon.com.au/contents/AU/EN/0100270808.html"
license=('custom')
install=${pkgname}.install
depends=('cndrvcups-common-lb')
if [[ $CARCH == "i686" ]]; then
  depends+=('libxml2')
  _lib32dir="lib"
else
  depends+=('lib32-libxml2')
  _lib32dir="lib32"
fi
makedepends=('autoconf' 'automake')
conflicts=('cndrvcups-lb-bin')
source=(Linux_UFRII_PrinterDriver_V280_uk_EN.tar.gz::'http://pdisp01.c-wss.com/gdl/WWUFORedirectTarget.do?id=MDEwMDAwMjcwODEw&cmp=ABS&lang=EN'
        'how-to.txt')
options=('!emptydirs' '!strip' '!libtool')
sha512sums=('f4e3a6856ddc3993547bfa317f2f14c4e6d278211c1a7c770ef4203838c19cc0cc134ecc1a87973ada45082d872dc56910397df09922a754d8c4440aa2a6281f'
            '736e1785c443c4d129c8801a127410012889f46691259e8a7f6a54106a0647beb5b6267aabb78b3ed0a1c7a9d8ce216e159515d3aad425812e5be52c8b58e4ee')
         
# build instructions are adapted from upstream cndrvcups-lb.spec file
prepare() {
    cd "${srcdir}"/Linux_UFRII_PrinterDriver_V280_uk_EN/Sources
    tar xf "${pkgname}"-"${pkgver}"-1.tar.gz -C "${srcdir}"
}

build() {
    
    cd "${srcdir}"/"${pkgname}"-"${pkgver}"/ppd
    autoreconf -vi
    ./autogen.sh --prefix=/usr
        
    cd "${srcdir}"/"${pkgname}"-"${pkgver}"/pstoufr2cpca
    autoreconf -vi
    ./autogen.sh --prefix=/usr --libdir=/usr/lib
    cd ..

    cd "${srcdir}"/"${pkgname}"-"${pkgver}"/cpca
    autoreconf -vi
    ./autogen.sh --prefix=/usr --enable-progpath=/usr/bin --libdir=/usr/lib --disable-static
        
    cd "${srcdir}"/"${pkgname}"-"${pkgver}"/cngplp
    aclocal
    autoreconf -vi
    ./autogen.sh --prefix=/usr --libdir=/usr/lib
    
    cd files
    autoreconf -vi
    ./autogen.sh --prefix=/usr
    cd "${srcdir}"/"${pkgname}"-"${pkgver}"
    make
}

package() {
  
    cd "${srcdir}"/"${pkgname}"-"${pkgver}"
    mkdir -p "${pkgdir}"/usr/{bin,include,lib}
    if [[ $CARCH=="x86_64" ]]; then
      mkdir -p "${pkgdir}"/usr/"${_lib32dir}"
    fi
    mkdir -p "${pkgdir}"/usr/lib/cups/filter
    mkdir -p "${pkgdir}"/usr/share/{cups/model,caepcm,cngplp,cnpkbidi,ufr2filter}
    make install DESTDIR="${pkgdir}"

    install -m 4755 libs/cnpkmoduleufr2  "${pkgdir}"/usr/bin

    install -m 755 libs/libcanonufr2.la  "${pkgdir}"/usr/"${_lib32dir}"
    install -s -m 755 libs/libcanonufr2.so.1.0.0  "${pkgdir}"/usr/"${_lib32dir}"
    install -s -m 755 libs/libufr2filter.so.1.0.0   "${pkgdir}"/usr/"${_lib32dir}"
    install -s -m 755 libs/libEnoJBIG.so.1.0.0   "${pkgdir}"/usr/"${_lib32dir}"
    install -s -m 755 libs/libEnoJPEG.so.1.0.0   "${pkgdir}"/usr/"${_lib32dir}"
    install -s -m 755 libs/cnpkbidi   "${pkgdir}"/usr/bin
    install -s -m 755 libs/libcaiocnpkbidi.so.1.0.0   "${pkgdir}"/usr/"${_lib32dir}"

    install -m 644 data/CNC* "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CNL6*  "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CNL8*  "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CNL9*  "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CNLC*  "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CNLA*  "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CNLB*  "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CNLD*  "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CNLF*  "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CNLG*    "${pkgdir}"/usr/share/caepcm
    install -m 644 data/CnLB*  "${pkgdir}"/usr/share/caepcm
    install -m 644 libs/cnpkbidi_info* "${pkgdir}"/usr/share/cnpkbidi
    install -m 644 libs/ThLB*  "${pkgdir}"/usr/share/ufr2filter

    install -m 755 libs/libcnlbcm.so.1.0     "${pkgdir}"/usr/"${_lib32dir}"

    cd "${pkgdir}"/usr/"${_lib32dir}"
    ln -sf libcanonufr2.so.1.0.0 libcanonufr2.so
    ln -sf libcanonufr2.so.1.0.0 libcanonufr2.so.1
    ln -sf libufr2filter.so.1.0.0 libufr2filter.so
    ln -sf libufr2filter.so.1.0.0 libufr2filter.so.1
    ln -sf libEnoJBIG.so.1.0.0 libEnoJBIG.so
    ln -sf libEnoJBIG.so.1.0.0 libEnoJBIG.so.1
    ln -sf libEnoJPEG.so.1.0.0 libEnoJPEG.so
    ln -sf libEnoJPEG.so.1.0.0 libEnoJPEG.so.1
    ln -sf libcaiocnpkbidi.so.1.0.0 libcaiocnpkbidi.so
    ln -sf libcaiocnpkbidi.so.1.0.0 libcaiocnpkbidi.so.1
    ln -sf libcnlbcm.so.1.0     libcnlbcm.so.1
    ln -sf libcnlbcm.so.1.0     libcnlbcm.so

    cd "${srcdir}"/"${pkgname}"-"${pkgver}"
    install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    install -m644 L*-ufr2-"${pkgver}"* "${pkgdir}"/usr/share/licenses/"${pkgname}/"
    install -m644 LICENSE-lipslx-"${pkgver}".txt "${pkgdir}"/usr/share/licenses/"${pkgname}/"
    install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
    install -m644 "${srcdir}"/Linux_UFRII_PrinterDriver_V280_uk_EN/Documents/guide-ufr2-2.8xUK.tar.gz "${pkgdir}"/usr/share/doc/"${pkgname}"/guide-ufr2-2.8xUK.tar.gz
}
