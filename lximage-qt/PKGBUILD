# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=lximage-qt
pkgver=0.3.0
pkgrel=2
pkgdesc="LXQt image viewer"
arch=('i686' 'x86_64')
url='http://lxqt.org/'
license=('LGPL')
groups=('lxqt')
depends=('pcmanfm-qt')
makedepends=('cmake' 'qt5-tools')
source=("http://lxqt.org/downloads/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('6ebd303f268fcef069e6f340583669be')
install=$pkgname.install

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
