# Maintainer: Trent McPheron <twilightinzero@gmail.com>
# Based on PKGBUILD by Mikhail Vorozhtsov <mikhail.vorozhtsov@gmail.com>
pkgname=libdesktop-agnostic-gconf
pkgver=20100405
pkgrel=1
pkgdesc="Desktop Agnostic Library for Glib-based Projects. Uses GConf as configuration backend, but no other GNOME dependencies."
arch=('i686' 'x86_64')
url="https://launchpad.net/libdesktop-agnostic"
license=('GPL')
depends=('glib2' 'gtk2' 'pygtk' 'pygobject' 'gconf')
makedepends=('bzr' 'python' 'gobject-introspection' 'vala')
provides=('libdesktop-agnostic')
options=('!libtool')
source=()
md5sums=()

_bzrmod=libdesktop-agnostic
_bzrbranch="lp:~awn-core/libdesktop-agnostic/various-fixes"

build() {
  msg "Connecting to the server..."

  rm -rf fake-bzr-home || return 1
  mkdir fake-bzr-home || return 1
  export BZR_HOME="${srcdir}/fake-bzr-home"

  if test -d ${_bzrmod}; then
    (cd ${_bzrmod} \
       && bzr clean-tree --force --ignored --unknown \
       && bzr revert \
       && bzr switch --force "${_bzrbranch}") \
      || return 1
  else
    bzr checkout "${_bzrbranch}" ${_bzrmod} || return 1
  fi

  msg "BZR synchronization done"
  msg "Starting make..."

  cd "${srcdir}/${_bzrmod}" || return 1

  ./waf configure \
    --prefix=/usr \
    --config-backends=gconf \
    --vfs-backends=gio \
    --desktop-entry=glib || return 1
  ./waf || return 1
}

package() {
  cd "${srcdir}/${_bzrmod}" || return 1

  ./waf install --destdir="${pkgdir}" || return 1 
  mv "${pkgdir}/usr/etc" "${pkgdir}/etc" || return 1
}
