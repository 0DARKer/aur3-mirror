# Maintainer: Ã‰tienne Deparis <etienne [at] depar.is>

pkgname=galileo
pkgver=0.4.1
pkgrel=2
pkgdesc="Utility to securely synchronize a Fitbit tracker with the Fitbit server"
license=("LGPL")
url="https://pypi.python.org/pypi/galileo"
depends=('python2-pyusb-beta' 'python2-requests')
makedepends=('python2-setuptools')
source=("http://pypi.python.org/packages/source/g/$pkgname/$pkgname-$pkgver.tar.gz"
  "https://bitbucket.org/benallard/galileo/raw/082c494e68ff8d4f7fcc60c9aa05a8e5c33578f5/99-fitbit.rules")
md5sums=('c13ea621136731d86ce2035d51dc996c'
         'bc8f0e72228397e5d9c3552d07b76e43')
arch=('any')
options=(!emptydirs)

build() {
  cd $srcdir/$pkgname-$pkgver

  find . -type f -exec sed -i \
    -e'1s|^#!/usr/bin/env python$|#!/usr/bin/env python2|' \
    -e '1s|^#!/usr/bin/python$|#!/usr/bin/env python2|' \
    "{}" \;

  # Temporary fix, due to python2-pyusb-beta 2 incompatibility
  sed -i "s/params = (endpoint, data, interface, timeout)/params = (endpoint, data, timeout)/" $pkgname/dongle.py
  sed -i "s/self.dev.read(0x82, length, self.CtrlIF.bInterfaceNumber,/self.dev.read(0x82, length,/" $pkgname/dongle.py
  sed -i "s/data = self.dev.read(0x81, DM.LENGTH, self.DataIF.bInterfaceNumber,/data = self.dev.read(0x81, DM.LENGTH,/" $pkgname/dongle.py
}

package(){
  cd $srcdir/$pkgname-$pkgver
  python2 setup.py install --root=$pkgdir

  install -dm 755 $pkgdir/etc/udev/rules.d
  install -Dm 644 $srcdir/99-fitbit.rules $pkgdir/etc/udev/rules.d/99-fitbit.rules
}
