# Maintainer: Ã‰tienne Deparis <etienne [at] depar.is>

pkgname=galileo
pkgver=0.4.2
pkgrel=1
pkgdesc='Utility to securely synchronize a Fitbit tracker with the Fitbit server'
license=('LGPL3')
url='https://bitbucket.org/benallard/galileo'
depends=('python2-pyusb-beta' 'python2-requests')
makedepends=('python2-setuptools')
source=("https://bitbucket.org/benallard/${pkgname}/get/${pkgver}.tar.gz")
sha256sums=('9dab45f7115d2559324ee5af8d25219822f0389bbd153f0e80ade37743850e6c')
arch=('any')
options=(!emptydirs)

prepare() {
  cd $srcdir/benallard-galileo-630b4debcd4f

  find . -type f -exec sed -i \
    -e'1s|^#!/usr/bin/env python$|#!/usr/bin/env python2|' \
    -e '1s|^#!/usr/bin/python$|#!/usr/bin/env python2|' \
    "{}" \;

  sed -i 's/logging: verbose/#logging: verbose/' galileorc.sample
  sed -i "26,35s/^\(.*\)$/\#\1/" galileorc.sample
}

package(){
  cd $srcdir/benallard-galileo-630b4debcd4f
  python2 setup.py install --root=$pkgdir

  install -d -m755                  $pkgdir/etc/udev/rules.d
  install -D -m644 99-fitbit.rules  $pkgdir/etc/udev/rules.d/99-fitbit.rules
  install -D -m644 doc/galileo.1    $pkgdir/usr/share/man/man1/galileo.1
  install -D -m644 doc/galileorc.5  $pkgdir/usr/share/man/man5/galileorc.5
  install -D -m644 galileorc.sample $pkgdir/etc/galileo/config
}
