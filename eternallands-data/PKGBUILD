# Based on eternallands package
# Maintainer (yuan_modu): Atila Satilmis <atila.satilmis@gmail.com>

pkgname=eternallands-data
pkgver=1.9.3
pkgrel=2
pkgdesc="Eternal Lands is a FREE 3D fantasy MMORPG (Official game data)"
arch=('any')
license=('custom')
url="http://www.eternal-lands.com/"
makedepends=('unzip')
conflicts=('eternallands')
options=('!strip')
source=('http://www.eternal-lands.com/el_linux_193.zip')
md5sums=('a1d0c7c5983d7c33ee0edc7a4e4867d6')

package() {
  cd "${srcdir}/el_linux"
  
  mkdir -p "${pkgdir}/usr/share/eternallands/"
  mkdir -p "${pkgdir}/usr/share/licenses/eternallands/"
  
  install -m644 license.txt "${pkgdir}/usr/share/licenses/eternallands/LICENSE.data.txt"
  find \( -name *.bmp -or -name *.elm \) -exec gzip {} \;
  for dir in 2dobjects 3dobjects actor_defs animations languages maps meshes \
             particles shaders skeletons skybox textures; do
	  cp -R ${dir} "${pkgdir}/usr/share/eternallands/"
  done 
  for file in *.ini *.txt *.lst *.xml; do
    install -m644 ${file} "${pkgdir}/usr/share/eternallands/"
  done
}
