# Based on the eternallands package from aur
# Maintainer (yuan_modu): Atila Satilmis <atila.satilmis@gmail.com>

pkgname=eternallands-data
pkgver=1.9.3
pkgrel=3
pkgdesc="Eternal Lands is a FREE 3D fantasy MMORPG (Official game data)"
arch=('any')
license=('custom')
url="http://www.eternal-lands.com/"
conflicts=('eternallands')
options=('!strip')
source=('http://www.eternal-lands.com/el_linux_193.zip')
md5sums=('a1d0c7c5983d7c33ee0edc7a4e4867d6')

package() {
  cd "${srcdir}/el_linux"
  
  mkdir -p "${pkgdir}"/usr/share/{eternallands,licenses/eternallands}
  
  install -m644 license.txt \
    "${pkgdir}/usr/share/licenses/eternallands/LICENSE.data.txt"
  
  _folder=('2dobjects' '3dobjects' 'actor_defs' 'animations' 'languages' 'maps' \
            'meshes' 'particles' 'shaders' 'skeletons' 'skybox' 'textures')
  for _f in ${_folder[@]}; do
    cp -a ${_f} "${pkgdir}/usr/share/eternallands/"
  done
  
  _file=('*.ini' '*.txt' '*.lst' '*.xml')
  for _f in ${_file[@]}; do
    install -m644 ${_f} "${pkgdir}/usr/share/eternallands/"
  done
  # Client complaining about this file atm.
  # So i did remove it here to replace with the one from git.
  _gitdata=('spells.xml')
  for _f in ${_gitdata[@]}; do
    rm -f "${pkgdir}/usr/share/eternallands/${_f}"
  done
}
