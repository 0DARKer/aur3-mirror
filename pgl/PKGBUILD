# Maintainer: Pierre Buard <pierre.buard+aur gmail com>
pkgname=pgl
pkgver=2.1.3
pkgrel=1
pkgdesc='A privacy oriented firewall application (GUI).'
url='http://phoenixlabs.org/'
arch=('i686' 'x86_64')
depends=('libnfnetlink' 'libnetfilter_queue' 'iptables' 'wget' 'qt')
optdepends=('zlib: to directly load gz compressed blocklists'
	    'p7zip: for blocklists packed as .7z'
	    'unzip: for blocklists packed as .zip'
	    'networkmanager: to run a white-listing script when detecting new network interfaces')
license=('GPL')
replace=('pgl-nogui')
conflicts=('blockcontrol' 'moblock' 'pgl-cli')
backup=('etc/pgl/allow.p2p'
	'etc/pgl/blocklists.list'
	'etc/pgl/pglcmd.conf')
source=(http://downloads.sourceforge.net/project/peerguardian/PeerGuardian%20Linux/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('b478f01e133626d724e3acf676cfcbfa')

build() {
    cd $srcdir/$pkgname-$pkgver/
    make INITDIR='/etc/rc.d' QTDIR='/usr/share/qt' QTINC='/usr/include'
}

package() {
    cd $srcdir/$pkgname-$pkgver/
    make INITDIR='/etc/rc.d' QTDIR='/usr/share/qt' QTINC='/usr/include' DESTDIR=$pkgdir install

    # Man
    gzip $srcdir/$pkgname-$pkgver/docs/pgld.1
    install -Dm644 $srcdir/$pkgname-$pkgver/docs/pgld.1.gz $pkgdir/usr/share/man/man1/pgld.1.gz
    ln -s /usr/share/man/man1/pgld.1.gz $pkgdir/usr/share/man/man1/pglcmd.wd.1.gz
    # Texts
    install -d $pkgdir/usr/share/doc/pgl/
    install -Dm644 $srcdir/$pkgname-$pkgver/docs/{AUTHORS,BUGS,README,README.blocklists,THANKS,TODO} $pkgdir/usr/share/doc/pgl/
    install -Dm644 $srcdir/$pkgname-$pkgver/debian/copyright $pkgdir/usr/share/licenses/pgl/LICENSE
}