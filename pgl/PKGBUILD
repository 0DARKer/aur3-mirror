# Maintainer: Pierre Buard <pierre.buard+aur gmail com>
pkgname=pgl
pkgver=2.2.2
pkgrel=6
pkgdesc='PeerGuardian is a privacy oriented firewall application (GUI).'
url='http://sourceforge.net/projects/peerguardian/'
arch=('i686' 'x86_64')
depends=('cron' 'logrotate' 'wget' 'libnetfilter_queue' 'iptables' 'qt4' 'polkit-qt')
optdepends=('unzip: for zipped blocklists'
	    'p7zip: for 7z blocklists'
	    'net-tools: to whitelist local IP addresses'
	    'networkmanager: to whitelist newly started network interfaces'
	    'smtp-forwarder: to send reports'
	    'tcptraceroute: to diagnose connection problems')
license=('GPL')
replace=('pgl-nogui')
conflicts=('blockcontrol' 'moblock' 'pgl-cli')
backup=('etc/pgl/allow.p2p'
	'etc/pgl/blocklists.list'
	'etc/pgl/pglcmd.conf'
	'etc/logrotate.d/pglcmd'
	'etc/logrotate.d/pgld')
source=(http://downloads.sourceforge.net/project/peerguardian/PeerGuardian%20Linux/${pkgver}/${pkgname}-${pkgver}.tar.gz
	service)
md5sums=('c623c92802281f69e51d140c8b1bb171'
	 'ac4e6b929e8085165dc7cc947e8acd2f')
install=install
changelog=changelog.gui

build() {
    # makes sure qt4 is used
    export QMAKE=qmake-qt4
    export MOC=moc-qt4
    export UIC=uic-qt4
    export RCC=rcc-qt4

    cd ${srcdir}/${pkgname}-${pkgver}/
    ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
		--with-piddir=/run --without-initddir
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}/
    make DESTDIR=${pkgdir} install

    install -Dm644 ${srcdir}/service ${pkgdir}/usr/lib/systemd/system/pgl.service

    # integrates logs in systemd journal
    sed -i '/^LOG_SYSLOG/s/0/1/g' ${pkgdir}/usr/lib/pgl/pglcmd.defaults
}
