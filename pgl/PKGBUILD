# Maintainer: Gilrain <pierre.buard+aur gmail com>

pkgname=pgl
pkgver=2.2.4
pkgrel=2
pkgdesc='PeerGuardian is a privacy oriented firewall application (GUI).'
url='http://sourceforge.net/projects/peerguardian/'
arch=('i686' 'x86_64')
depends=('logrotate' 'wget' 'libnetfilter_queue' 'iptables' 'qt4' 'polkit-qt')
optdepends=('unzip: for zipped blocklists'
	    'p7zip: for 7z blocklists'
	    'net-tools: to whitelist local IP addresses'
	    'networkmanager: to whitelist newly started network interfaces'
	    'smtp-forwarder: to send reports'
	    'tcptraceroute: to diagnose connection problems')
license=('GPL3')
replace=('pgl-nogui')
conflicts=('blockcontrol' 'moblock' 'pgl-cli' 'pgl-git')
backup=('etc/pgl/allow.p2p'
	'etc/pgl/blocklists.list'
	'etc/pgl/pglcmd.conf'
	'etc/logrotate.d/pglcmd'
	'etc/logrotate.d/pgld')
install=install
changelog=changelog
source=("http://downloads.sourceforge.net/project/peerguardian/PeerGuardian%20Linux/${pkgver}/${pkgname}-${pkgver}.tar.gz"
	"http://sourceforge.net/p/peerguardian/patches/3/attachment/systemd-timer.diff")
sha1sums=('cabf47f43a02f7d4ee70dbac30ce73ccec6a6923'
          '0dd32e5d23bc6a1dacd320df0ada501feb91f0bd')

prepare() {
    cd "${srcdir}/pgl-${pkgver}"
    patch -p0 < "${srcdir}/systemd-timer.diff"
}

build() {
    # makes sure qt4 is used
    export QMAKE=qmake-qt4
    export MOC=moc-qt4
    export UIC=uic-qt4
    export RCC=rcc-qt4

    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
		--with-piddir=/run --with-systemd --disable-cron --without-initddir
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR=${pkgdir} install

    # integrates daemon log in systemd journal
    sed -i '/^LOG_SYSLOG/s/0/1/g' ${pkgdir}/usr/lib/pgl/pglcmd.defaults
}
