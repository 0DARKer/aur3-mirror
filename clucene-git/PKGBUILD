# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
#
# This PKGBUILD derives from official clucene PKGBUILD so below is a credits list for that one.
# Contributor: Alois Nespor <alois.nespor@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=clucene-git
pkgver=20110310
pkgrel=1
pkgdesc="CLucene is a C++ port of Lucene: A high-performance, full-featured text search engine."
arch=('i686' 'x86_64')
url="http://clucene.sourceforge.net"
license=('APACHE' 'LGPL')
depends=('gcc-libs' 'zlib')
makedepends=('git' 'cmake>=2.6')
provides=('clucene=0.9.23')
conflicts=('clucene')
options=('!strip')

### This section can and should be adjusted to suit your needs
# How many jobs should 'make' use (for multicore CPUs)?
# Value is 2 by default because it doesn't hurt and can help.
_jobcount="2"
# What type of build do you want (release/debug etc)?
# Default is debugfull to help with providing backtrace.
# If you want to build a release type you can also remove a bang (!) sign from 'options' array above.
_buildtype="debugfull"
###

_gitroot="git://clucene.git.sourceforge.net/gitroot/clucene/clucene"
_gitname="clucene"

build() {
  cd ${srcdir}
  msg "Connecting to GIT server..."

  if [ -d $_gitname ]; then
    cd $_gitname && git pull origin
    msg "Local files are updated."
  else
    git clone --depth 1 $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf ${srcdir}/${_gitname}-build
  cp -r ${srcdir}/$_gitname ${srcdir}/${_gitname}-build
  cd ${srcdir}/${_gitname}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=$_buildtype
  make -j $_jobcount
}

package() {
  cd ${srcdir}/${_gitname}-build
  make DESTDIR=${pkgdir} install
}
