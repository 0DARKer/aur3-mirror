# Contributor: grimi <grimi at poczta dot fm>

pkgname=omnibook-git
pkgver=20110911
pkgrel=8
pkgdesc="Kernel module for HP OmniBook,Pavilion,Toshiba and Compal ACL00 laptops"
arch=('i686' 'x86_64')
url="http://omnibook.sourceforge.net"
license=('GPL')
depends=('linux')
makedepends=('git' 'linux-headers')
conflicts=('omnibook')
provides=('omnibook')
source=("$pkgname::git://omnibook.git.sourceforge.net/gitroot/omnibook/omnibook" "use-new-procfs-api.patch")
md5sums=('SKIP'
         '945b1abaa1989f21bfae751cb7d3bc76')
install=$pkgname.install



pkgver() {
   cd "$srcdir/$pkgname"
   git log -1 --format="%cd" --date="short"|sed 's|-||g'
}

prepare() {
  cd "$srcdir/$pkgname"
  patch -Np1 -i ../use-new-procfs-api.patch
}

build() {
  cd "$srcdir/$pkgname"
  make
  gzip omnibook.ko
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm644 omnibook.ko.gz \
     "$pkgdir"/usr/lib/modules/extramodules-$(uname -r|sed 's/\(.*\)\..*-.*-/\1-/')/omnibook.ko.gz
}


