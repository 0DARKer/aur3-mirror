# Maintainer: Army <uli armbruster google>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=mkvtoolnix-gtk
_pkgname=mkvtoolnix
pkgver=4.8.0
pkgrel=2
pkgdesc="gtk2 user interface for mkvtoolnix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('mkvtoolnix' 'wxgtk' 'curl')
makedepends=('boost' 'ruby')
install=mkvtoolnix-gtk.install
source=("http://www.bunkus.org/videotools/${_pkgname}/sources/${_pkgname}-${pkgver}.tar.bz2")
md5sums=('47a730706f3da2bcf4ba62bba3a8f260')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	
	export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
	export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
	
	./configure --prefix=/usr --with-boost-libdir=/usr/lib --enable-gui
	./drake apps:mmg
}

package () {
	# install binary file
	install -Dm755 "${srcdir}/${_pkgname}-${pkgver}/src/mmg/mmg" "${pkgdir}/usr/bin/mmg"
	# install all manpages
	install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/doc/man/mmg.1" "${pkgdir}/usr/share/man/man1/mmg.1"
	install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/doc/man/ja/mmg.1" "${pkgdir}/usr/share/man/ja/man1/mmg.1"
	install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/doc/man/nl/mmg.1" "${pkgdir}/usr/share/man/nl/man1/mmg.1"
	install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/doc/man/zh_CN/mmg.1" "${pkgdir}/usr/share/man/zh_CN/man1/mmg.1"
}
