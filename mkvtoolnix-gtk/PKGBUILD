# Maintainer: Army <uli armbruster google>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=mkvtoolnix-gtk
_pkgname=mkvtoolnix
pkgver=4.8.0
pkgrel=1
pkgdesc="gtk2 user interface for mkvtoolnix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('mkvtoolnix' 'wxgtk' 'curl')
makedepends=('boost' 'ruby')
install=mkvtoolnix-gtk.install
source=("http://www.bunkus.org/videotools/${_pkgname}/sources/${_pkgname}-${pkgver}.tar.bz2")
md5sums=('47a730706f3da2bcf4ba62bba3a8f260')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	
	export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
	export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
	
	./configure --prefix=/usr --with-boost-libdir=/usr/lib --enable-gui
	./drake
}

package () {
	install -Dm755 "${srcdir}/${_pkgname}-${pkgver}/src/mmg/mmg" "${pkgdir}/usr/bin/mmg"
}
