# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Tuan Nguyen 
# Contributor: Farid <farid at archlinux-br dot org>

pkgname=pdfshuffler
pkgver=0.6svn75
pkgrel=1
pkgdesc="Merge or split pdf documents and rotate, crop and rearrange their pages"
arch=('any')
url="http://sourceforge.net/projects/pdfshuffler/"
license=('GPL3')
depends=('python2-poppler' 'python-pypdf' 'ghostscript')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz")
md5sums=('34c7a193157369251fa8e78f81893ddc')

build() {
  cd "$srcdir/trunk"

  # ui path fix
  sed -i 's|applications/||' setup.py

  python2 setup.py build
}

package() {
  cd "$srcdir/trunk"
  python2 setup.py install --prefix=/usr \
                           --root="$pkgdir/"

  # python2 fix
  find "$pkgdir" -name "*.py" -exec \
    sed -i "s/python/&2/" {} \;
}

# vim:set ts=2 sw=2 et:
