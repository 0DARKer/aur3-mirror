# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

pkgname=juffed-git
pkgver=0.10.1.g054bf0c
pkgrel=1
pkgdesc="Free text editor based on Qt. This package also includes juffed plugins."
arch=('i686' 'x86_64')
url="http://juffed.com/"
license=('GPL2')
depends=('qt4' 'qscintilla')
makedepends=('git' 'cmake')
conflicts=('juffed' 'juffed-plugins')
provides=('juffed' 'juffed-plugins')
source=("git://github.com/Mezomish/juffed.git")
sha256sums=('SKIP')
_gitname="juffed"

pkgver() {
	cd "$srcdir/$_gitname"
	echo $(git describe --always | sed 's/juffed-//g' | sed 's/-/./g')
}

build() {
	cd "$srcdir/$_gitname"
	cmake -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir/" install
}
