# Maintainer: felix <at yandex dot com, local part is isaev, preceded by m dot p dot>
pkgbase=unifont
pkgname="$pkgbase" # ugly hack for AUR
true && pkgname=(ttf-unifont pcf-unifont bdf-unifont unifont-utils)
pkgver=6.3.20131020
pkgrel=2
epoch=
pkgdesc="A free bitmap font with wide Unicode support (split package)"
arch=(i686 x86_64)
url="http://unifoundry.com/unicode-utilities.html"
license=('GPL' 'font exception')
groups=()
depends=()
makedepends=('fontforge' 'perl' 'xorg-bdftopcf')
checkdepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=
source=(
	http://unifoundry.com/pub/unifont-6.3.20131020/unifont-6.3.20131020.tar.gz
	freetype-fix.patch
	fontconfig-avoid-eye-strain.conf
)
noextract=()
sha256sums=('fd8bc3d612e3353babe1b37c602f843b3bea624b3a67918c712b71692afcc305'
            '60b4dc680a555031de474163ced3ec546e175b5f7c48a041cc3add44f2e63aef'
            '1f2fde3d685e0661cdc8eed1efa852ba844b2da42dc18c7e7559057695740121')

prepare() {
	cd "$srcdir/unifont-$pkgver"
	patch -p1 < ../freetype-fix.patch
}

build() {
	cd "$srcdir/unifont-$pkgver"
	make -j1
}

package() {
	msg2 "Please edit the PKGBUILD."
	exit 1
}

package_ttf-unifont() {
	true && pkgdesc="A free bitmap font with wide Unicode support (TrueType version)"
	true && arch=(any)

	cd "$srcdir/unifont-$pkgver/font"
	make install PREFIX="$pkgdir/usr" DESTDIR="$pkgdir" PCFDEST=/tmp
	rm -f /tmp/unifont.pcf /tmp/unifont.pcf.gz

	install -D -m0644 "$srcdir/fontconfig-avoid-eye-strain.conf" \
		"$pkgdir/etc/fonts/conf.avail/20-unifont-noaa.conf"
}

package_pcf-unifont() {
	true && pkgdesc="A free bitmap font with wide Unicode support (PCF version)"
	true && arch=(any)

	cd "$srcdir/unifont-$pkgver/font"
	make install PREFIX="$pkgdir/usr" DESTDIR="$pkgdir" TTFDEST=/tmp
	rm -f /tmp/unifont.ttf
}

package_bdf-unifont() {
	true && pkgdesc="A free bitmap font with wide Unicode support (BDF version)"
	true && arch=(any)

	cd "$srcdir/unifont-$pkgver/font"
	install -D -m0644 "compiled/unifont-$pkgver.bdf" "$pkgdir/usr/share/fonts/unifont.bdf"
}

package_unifont-utils() {
	true && pkgdesc="A free bitmap font with wide Unicode support - utilities"
	true && depends=(perl)
	true && license=(GPL)

	cd "$srcdir/unifont-$pkgver/src"
	make install PREFIX="$pkgdir/usr"
	cd "$srcdir/unifont-$pkgver/man"
	make install PREFIX="$pkgdir/usr" COMPRESS=1
}

# vim:set ts=2 sw=2 et:
