# Maintainer: Thomas Dziedzic < gostrc at gmail >

pkgname=mock
pkgver=1.1.9
pkgrel=3
pkgdesc='A simple chroot build environment manager for building RPMs.'
url='https://fedorahosted.org/mock/'
arch=('i686' 'x86_64')
license=('GPL')
install=mock.install
depends=('python2' 'usermode' 'yum-utils')
makedepends=('git')

mygitroot='git://git.fedorahosted.org/git/mock.git'
mygitname='mock'

build() {
  if [ -d ${mygitname} ] ; then
    cd ${mygitname}
    git pull origin
  else
    git clone ${mygitroot}
    cd ${mygitname}
  fi

  git checkout mock-${pkgver}

  export PYTHON=python2

  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd ${mygitname}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/bin
  ln -s /usr/bin/consolehelper ${pkgdir}/usr/bin/mock

  mkdir -p ${pkgdir}/var/{lib,cache}/mock
  chown -R root:mock ${pkgdir}/var/{lib,cache}/mock
  chmod -R 02755 ${pkgdir}/var/{lib,cache}/mock

  # fix incorrect shebang
  sed -i 's_#!/usr/bin/python_&2_' ${pkgdir}/usr/sbin/mock
}
