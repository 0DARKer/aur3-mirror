# Maintainer: David Roheim < david dot roheim at gmail dot com >
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=mock
pkgver=1.1.41
pkgrel=4
pkgdesc='A simple chroot build environment manager for building RPMs.'
url='http://fedoraproject.org/wiki/Projects/Mock'
arch=('i686' 'x86_64')
license=('GPL')
install=mock.install
depends=('python2' 'usermode' 'yum-utils' 'python2-decoratortools')

source=("https://git.fedorahosted.org/cgit/mock.git/snapshot/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6e8cb04db282493c4f445324464c51f3b17137ea4efd0b4afe181040cc2fcb08')

build() {
	cd ${pkgname}-${pkgver}
	./autogen.sh
	sed -i '/for ac_prog in/ s/python //' configure
	./configure --prefix=/usr
	make
}

#check() {
#	cd ${pkgname}-${pkgver}
#	make check
#}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install

	#install -d ${pkgdir}/usr/bin
	ln -s /usr/bin/consolehelper ${pkgdir}/usr/bin/mock

	mkdir -p ${pkgdir}/var/{lib,cache}/mock
	chown -R root:mock ${pkgdir}/var/{lib,cache}/mock
	chmod -R 02755 ${pkgdir}/var/{lib,cache}/mock

	# fix incorrect shebang
	for i in ${pkgdir}/usr/bin/mockchain ${pkgdir}/usr/sbin/mock; do
		sed -i 's_#!/usr/bin/python_&2_' $i
	done
}
