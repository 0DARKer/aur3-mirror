# Maintainer: David Roheim < david dot roheim at gmail dot com >
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=mock
pkgver=1.2.8
pkgrel=1
pkgdesc='A simple chroot build environment manager for building RPMs'
url='http://fedoraproject.org/wiki/Projects/Mock'
arch=('any')
license=('GPL2')
depends=('python2' 'usermode' 'yum-utils' 'python2-decoratortools')
optdepends=('createrepo_c: for mockchain command')
install="${pkgname}.install"
options=('!strip' 'libtool' 'staticlibs')

source=("https://git.fedorahosted.org/cgit/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.xz")
sha256sums=('9c9f34114809d92466a74ddfea40ce0ad48dbc4f9c6004f25e35f9124a857d60')

build() {
    cd "${pkgname}-${pkgver}"
    ./autogen.sh
    ./configure --prefix=/usr      \
                --sysconfdir=/etc  \
                --libdir=/usr/lib  \
                --sbindir=/usr/bin \
                PYTHON=python2

    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' py/${pkgname}{,chain}.py
    sed -i "s|@VERSION@|${pkgver}|" docs/${pkgname}{,chain}.1
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    mv ${pkgdir}/usr/bin/${pkgname}{,.py}
    sed -i "s|/usr/sbin/${pkgname}|/usr/bin/${pkgname}.py|" ${pkgdir}/etc/security/console.apps/${pkgname}
    ln -s /usr/bin/consolehelper ${pkgdir}/usr/bin/${pkgname}
}
