# Maintainer: David Roheim < david dot roheim at gmail dot com >
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=mock
pkgver=1.2.9
pkgrel=1
pkgdesc='A simple chroot build environment manager for building RPMs'
url='http://fedoraproject.org/wiki/Projects/Mock'
arch=('any')
license=('GPL2')
depends=('python' 'rpm-org' 'usermode' 'yum-utils')
optdepends=('createrepo_c: for mockchain command'
            'lvm2: for lvm_root plugin'
            'pigz: for parallel compression of chroot cache')
install="${pkgname}.install"
options=('!strip' 'libtool' 'staticlibs')

source=("https://git.fedorahosted.org/cgit/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c9177f5867c32a47004530b2a16261917f17db5c32dc1742dc840bcd9aa015c4')

build() {
    cd "${pkgname}-${pkgver}"
    ./autogen.sh
    ./configure --prefix=/usr      \
                --sysconfdir=/etc  \
                --libdir=/usr/lib  \
                --sbindir=/usr/bin

    sed -i "s|@VERSION@|${pkgver}|" docs/${pkgname}{,chain}.1
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    mv ${pkgdir}/usr/bin/${pkgname}{,.py}
    sed -i "s|/usr/sbin/${pkgname}|/usr/bin/${pkgname}.py|" ${pkgdir}/etc/security/console.apps/${pkgname}
    ln -s /usr/bin/consolehelper ${pkgdir}/usr/bin/${pkgname}
}
