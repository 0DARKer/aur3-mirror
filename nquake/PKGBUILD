# Maintainer: brent s. <bts[at]square-r00t[dot]net>

# TODO: convert deps to 32-bit libs. ugh

pkgname=nquake
pkgver=2.3
pkgrel=3
pkgdesc="The easiest, quickest, and most popular QuakeWorld client."
url="http://nquake.sourceforge.net/"
license=('GPL')
depends=('unzip' 'wget')
conflicts=('fuhquake' 'ezquake' 'ezquake-git')
provides=('ezquake' 'quake' 'fuhquake')
install=nquake.install
arch=('any')
_alt_pkgver=$(echo ${pkgver} | sed -e 's/\.//g')
source=("http://downloads.sourceforge.net/project/nquake/nQuake (Linux)/v${pkgver}/nquake${_alt_pkgver}_installer-linux.tar.gz"
	"nquake.launcher")
sha256sums=('71c0014a8f3babf8994bce3e414bd28cbeaa86b1bad9841585a3a93aa9230d36'
	    'b5099392cc02ac0b4a7f073542b7e240b78842c36ac8d0921bbb85a1221e5d45')

build() {

	cd "${srcdir}/nquake${_alt_pkgver}_installer-linux"
	# shut the hell up, please.
	sed -i -e 's/echo\ "\*\ Could\ not\ find\ pak1.pak"/false/g' install_nquake.sh
	sed -i -e 's/echo "\*\ Found\ at\ location\ $pak"/true/g' install_nquake.sh
	sed -i -e "/^[[:space:]]*echo[[:space:]]*$/d ;\
	/^[[:space:]]*echo\ -n/d ;\
	/^[[:space:]]*echo\ \"/d" install_nquake.sh
	# and definitely do not install it in ~/nquake.
	sed -i -e 's|defaultdir="~/nquake"|defaultdir="../dist"|g' install_nquake.sh
	# who the fuck uses uname -i anymore?
	sed -i -e 's|uname\ -i|uname -m|g' install_nquake.sh
	# don't prompt us, and fix some paths.
	sed -i -e 's|\(defaultsearchdir="\)~/"|\1/opt/quake"|g' install_nquake.sh
	sed -i -e 's/^read\ -p\ ".*\ search$/search="y"/g' install_nquake.sh
	sed -i -e 's/^\([[:space:]]*\)read -p ".*\ path/\1path=""/g' install_nquake.sh
	sed -i -e 's|^read\ -p\ ".*\ directory$|directory="../dist"|g' install_nquake.sh
	sed -i -e 's/^read\ -p\ ".*\ clanarena$/clanarena="y"/g' install_nquake.sh
	sed -i -e 's/^read\ -p\ ".*\ fortress$/fortress="y"/g' install_nquake.sh
	sed -i -e 's/^read\ -p\ ".*\ textures$/textures="y"/g' install_nquake.sh
	sed -i -e 's/^read\ -p\ ".*\ mirror$/mirror=""/g' install_nquake.sh
	
	./install_nquake.sh
	
}
package() {

    # Create Destination Directories
    install -d "${pkgdir}/opt/quake"

    # Make id1 Directory for pak0.pak and pak1.pak files
    install -d "${pkgdir}/opt/quake/id1/"

    # Make save game Directory with user group permissions
    install -d -m775 -g users "${pkgdir}/opt/quake/qw/save/"

    # copy over the data
    cp -a ${srcdir}/dist/* ${pkgdir}/opt/quake/.

    # Install Launcher
    install -D -m755 "${srcdir}/nquake.launcher" \
        "${pkgdir}/usr/bin/nquake"

}

