# Maintainer: Thomas Taylor <thomas.taylor@gmx.net>
# This package just gets the source of AOLserver and puts it in /opt/source. The
# reason for this rather strange package is, that we need to install AOLserver
# first (with package aolserver-core), before we can install aolserver modules,
# but the modules however also require the source of AOLserver, which seems
# impossible to handle in a single package.

pkgname=aolserver-source
pkgver=4.5.2
pkgrel=1
pkgdesc="High-performance multithreaded web server source as needed for core and modules"
arch=('any')
url="http://aolserver.sourceforge.net/"
license=(MPL)
groups=(aolserver)
source=("http://downloads.sourceforge.net/project/aolserver/AOLserver/$pkgver/aolserver-$pkgver-rc0.tar.gz")
md5sums=('0895dd7f13d79ddb40d873f3f6823976')
sha256sums=('98986e678fe294ee957a791bd14ca9915b9a45cfdeab0790f82c27406412aab5')

prepare() {
  #the following is a fix for tcl 8.6.x
  sed -i "1i#define USE_INTERP_RESULT" "$srcdir/aolserver-$pkgver/include/nsthread.h"
}

build() {
  cd "$srcdir/aolserver-$pkgver"
  ./configure --prefix=/opt/aolserver-$pkgver --with-tcl=/usr/lib/ --enable-threads --enable-shared TCLSH=$(which tclsh) LDFLAGS="${LDFLAGS/--as-needed/--no-as-needed}"
}

package() {
  mkdir -p "$pkgdir/opt/source/"
  cp -a "$srcdir/aolserver-$pkgver" "$pkgdir/opt/source/."
  chmod -R a+rw "$pkgdir/opt/source/aolserver-$pkgver"
}

# vim:set ts=2 sw=2 et:
