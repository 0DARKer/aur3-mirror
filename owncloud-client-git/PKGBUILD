# Maintainer: Danilo Kuehn <dk at nogo-software dot de>
# Git: https://github.com/nogo/archlinux-pkgbuild

_client=mirall

pkgname=owncloud-client-git
pkgver=4940.e34122a
pkgrel=1
pkgdesc="ownCloud client based on mirall"
arch=('i686' 'x86_64')
url="http://owncloud.org/"
license=('GPL2')
depends=('sqlite3' 'neon' 'qt4')
optdepends=('qtkeychain' 'smbclient' 'libssh')
makedepends=('cmake')
provides=('mirall' 'mirall-git' 'owncloud-client')
conflicts=('mirall-git' 'owncloud-client' 'owncloud-client-ngs')
install=${pkgname}.install
options=(!strip)
backup=('etc/owncloud-client-git/sync-exclude.lst')
source=("${_client}::git+https://github.com/owncloud/mirall.git")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_client}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ${srcdir}/${_client}

  cmake -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
        -DCMAKE_BUILD_TYPE=Release \
        ../${_client} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname}
  make
}

package() {
  cd ${srcdir}/${_client}
  make DESTDIR=${pkgdir} install
}
