# Maintainer: David Phillips <dbphillipsnz at _remove this part if you want_ gmail dot com>

pkgname=abduco
pkgver=0.2
pkgrel=1
pkgdesc="Tool for session {at,de}tach support which allows a process to run independently from its controlling terminal"
arch=('i686' 'x86_64')
url="http://www.brain-dump.org/projects/${pkgname}/"
license=('GPL')
source=("http://www.brain-dump.org/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('9e6bda1779c4eff43c0f8a44857dd3beb25948c0f0bf2ff6e12bc951655e5f5ed3304794d7966b2e1cbb2c9f3917c153a1393889791a5293bd13111e15c3db11')

build () {
	make all --directory="${srcdir}/${pkgname}-${pkgver}"
}

package () {
	make install PREFIX="${pkgdir}/usr" --directory="${srcdir}/${pkgname}-${pkgver}"
	install -Dm 644 "${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
