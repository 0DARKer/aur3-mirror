# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Aaron Hurt <ahurt at anbcs dot com>

pkgname=avant-window-navigator-bzr
_realname=avant-window-navigator
pkgver=828
pkgrel=1
pkgdesc="Fully customisable dock-like window navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn"
license=('GPL2' 'LGPL2.1')
depends=('gconf' 'dbus-python' 'desktop-file-utils' 'hicolor-icon-theme' \
         'libgtop' 'libdesktop-agnostic-bzr' 'libwnck' 'python-feedparser' \
         'pyxdg' 'xdg-utils')
makedepends=('bzr' 'gnome-common' 'gtk-doc' 'intltool' 'vala')
provides=(${_realname})
conflicts=(${_realname})
options=('!libtool')
install=${pkgname}.install

_bzrtrunk="lp:awn"
_bzrmod="awn"

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  export PYTHON="/usr/bin/python2"
  ./autogen.sh --prefix=/usr --sysconfdir=/usr/share

  sed -i 's|${prefix}/etc|${prefix}/share|' Makefile

  str='\(.*"theme_tooltip_outline_color".*\)'
  sed -i "s|${str}|#\1|" awn-settings/awnSettings.py

  # python2 fix
  sed -i 's_with ("python"_with ("python2"_' applet-activation/main.c
  for file in $(find . -name '*.py' -print); do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' ${file}
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' ${file}
  done

  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/awn.schemas" \
    "${pkgdir}"/usr/share/gconf/schemas/*.schemas

  rm -f "${pkgdir}"/usr/share/gconf/schemas/*.schemas
  mv "${pkgdir}"/usr/share/gconf/{,schemas/}awn.schemas
}

