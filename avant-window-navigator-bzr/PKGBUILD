# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=avant-window-navigator-bzr
_pkgname=avant-window-navigator
pkgver=830
pkgrel=3
pkgdesc="Fully customisable dock-like window navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn"
license=('GPL2' 'LGPL2.1')
depends=('gconf' 'dbus-python' 'desktop-file-utils' 'hicolor-icon-theme' \
         'libgtop' 'libdesktop-agnostic' 'libwnck' 'pyxdg' 'xdg-utils')
makedepends=('bzr' 'gtk-doc' 'intltool' 'vala-012')
optdepends=('dockmanager: helpers support')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool')
install="$pkgname".install

_bzrtrunk="https://code.launchpad.net/~awn-core/awn/trunk"
_bzrmod="awn"

build() {
  cd "$srcdir"

  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr --no-plugins pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr --no-plugins branch "$_bzrtrunk" "$_bzrmod" -q -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  # python2 fix
  sed -i 's_with ("python"_with ("python2"_' applet-activation/main.c
  sed -i 's_^#!.*python$_&2_' awn-settings/awnSettings{.py.in,Helper.py}

  export PYTHON="/usr/bin/python2"
  export VALAC="/opt/vala-0.12/bin/valac"

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/usr/share \
              --disable-static --disable-pymod-checks
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

  install -d -m 755 "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir"/usr/share/gconf/awn.schemas \
    "$pkgdir"/usr/share/gconf/schemas/*.schemas

  rm -f "$pkgdir"/usr/share/gconf/schemas/*.schemas
  mv "$pkgdir"/usr/share/gconf/{,schemas/}awn.schemas
}

# vim:set ts=2 sw=2 et:
