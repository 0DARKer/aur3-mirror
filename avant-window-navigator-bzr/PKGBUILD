# Maintainer: paolo <paolostivanin@gmail.com>

pkgname=avant-window-navigator-bzr
pkgver=760
pkgrel=3
pkgdesc="Development branch of Avant Window Navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn"
license=('GPL')
depends=('libwnck' 'gconf' 'xproto' 'dbus-glib' 'libglade' 'libxdamage' \
         'libxcomposite' 'libxrender' 'python2' 'vala' 'hicolor-icon-theme' \
         'pyxdg' 'python-feedparser' 'xdg-utils' 'libdesktop-agnostic-gnome-bzr' \
         'libgtop' 'bzr' 'dbus-python')
makedepends=('intltool' 'gnome-doc-utils' 'bzr' 'gtk-doc' 'gnome-common' 'desktop-file-utils')
provides=('avant-window-navigator=0.4.1')
conflicts=('avant-window-navigator')
options=('!libtool')
install=avant-window-navigator.install

_bzrmod=awn
_bzrtrunk=lp:${_bzrmod}

build() {
   cd ${srcdir}

  msg "Connecting to the server...."

  if [ ! -d ./${_bzrmod} ]; then
    bzr co ${_bzrtrunk} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  cd ${srcdir}/awn

  PYTHON=/usr/bin/python2 ./autogen.sh --prefix=/usr --sysconfdir=/usr/share

  sed -i 's|${prefix}/etc|${prefix}/share|' Makefile

  str='\(.*"theme_tooltip_outline_color".*\)'
  sed -i "s|$str|#\1|" awn-settings/awnSettings.py

  # python2 fix
  sed -i 's_with ("python"_with ("python2"_' applet-activation/main.c
  for file in $(find . -name '*.py' -print); do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done

  make
}

package() {
  cd ${srcdir}/awn

  make DESTDIR=${pkgdir} install
}
