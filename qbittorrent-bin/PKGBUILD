# Maintainer: Ner0

pkgname=qbittorrent-bin
pkgver=2.9.9
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt4 using the good libtorrent library (Ubuntu prebuilt)"
url="http://www.qbittorrent.org/"
arch=('i686' 'x86_64')
_arch='amd64'
[ $CARCH = 'i686' ] && _arch='i386'
license=('GPL')
depends=('qt' 'libtorrent-rasterbar' 'boost-libs' 'openssl>=1.0.0')
optdepends=('python: needed for search'
            'geoip: improves peer country resolution')
conflicts=('qbittorrent')
provides=('qbittorrent')
install=$pkgname.install
source=("https://launchpad.net/~hydr0g3n/+archive/ppa/+files/qbittorrent_${pkgver}-0ubuntu1~precise_${_arch}.deb")
md5sums=('4afd5f5373bde8839b56c82a742d0d26')
[ $CARCH = 'i686' ] && md5sums=('df67595fabfb303e107e47441c63ae94')

package() {
  bsdtar -xf data.tar.gz -C "$pkgdir/"

  cd "$pkgdir/usr/bin"

  _boost=$(strings ./qbittorrent | grep libboost | head -n 1 | sed 's/.*\.so.//')
  _my_boost=$(pacman -Ql boost-libs | grep libboost_system.so.[0-9] | sed 's/.*\.so.//')
  _raster=$(strings ./qbittorrent | grep libtorrent-rasterbar.so | sed 's/.*\.so.//')
  _my_raster=$(pacman -Ql libtorrent-rasterbar | grep .so.[0-9] | head -n1 | sed 's/.*\.so.//')

  if [[ $_boost != $_my_boost ]]; then
    sed -i "s/$_boost/$_my_boost/g" ./qbittorrent
  fi

  if [[ $_raster != $_my_raster ]]; then
    sed -i "s/$_raster/$_my_raster/g" ./qbittorrent
  fi
}
