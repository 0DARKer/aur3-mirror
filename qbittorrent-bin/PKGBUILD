# Maintainer: Ner0

pkgname=qbittorrent-bin
pkgver=3.0.6
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt4 using the good libtorrent library (Ubuntu prebuilt)"
url="http://www.qbittorrent.org/"
arch=('i686' 'x86_64')
_arch='amd64'
[[ "$CARCH" = 'i686' ]] && _arch='i386'
license=('GPL')
depends=('qt' 'libtorrent-rasterbar' 'boost-libs' 'openssl>=1.0.0')
optdepends=('python: needed for search'
            'geoip: improves peer country resolution')
conflicts=('qbittorrent' 'qbittorrent-dev' 'qbittorrent-git' 'qbittorrent-git-nogui' 'qbittorrent-nogui')
provides=("qbittorrent=$pkgver")
install=$pkgname.install
source=("https://launchpad.net/~hydr0g3n/+archive/ppa/+files/qbittorrent_${pkgver}-0ubuntu1~precise_${_arch}.deb")
md5sums=('f9e7683e2b5789ac299ab9e491ac3f8b')
[[ "$CARCH" = 'i686' ]] && md5sums=('268fa68166b8d3693a88c2945e598626')

package() {
  tar -xf data.tar.gz -C "$pkgdir/"

  cd "$pkgdir/usr/bin"

  _boost=$(strings ./qbittorrent | grep libboost | head -n 1 | sed 's/.*\.so.//')
  _my_boost=$(pacman -Ql boost-libs | grep libboost_system.so.'[0-9]' | sed 's/.*\.so.//')
  _raster=$(strings ./qbittorrent | grep libtorrent-rasterbar.so | sed 's/.*\.so.//')
  _my_raster=$(pacman -Ql libtorrent-rasterbar | grep .so.'[0-9]' | head -n1 | sed 's/.*\.so.//')

  if [[ "$_boost" != "$_my_boost" ]]; then
    sed -i "s/$_boost/$_my_boost/g" ./qbittorrent
  fi

  if [[ "$_raster" != "$_my_raster" ]]; then
    sed -i "s/$_raster/$_my_raster/g" ./qbittorrent
  fi
}
