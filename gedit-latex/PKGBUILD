# Maintainer: Aleksei Lissitsin <aleksei.lissitsin@gmail.com>
pkgname=gedit-latex
pkgver=3.4.1
pkgrel=2
pkgdesc="Gedit plugin that provides features to ease the edition of LaTeX documents"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gedit/LaTeXPlugin"
license=('GPL')
groups=()
depends=(gedit rubber texlive-core python2-gobject)
makedepends=(cairo gettext atk intltool libpeas gtk3 glib2)
provides=()
conflicts=(gedit-latex-plugin)
replaces=(gedit-latex-plugin)
install=gschemas.install
source=(http://ftp.gnome.org/pub/gnome/sources/gedit-latex/3.4/$pkgname-$pkgver.tar.xz dialogs.patch)
md5sums=('d221f1ca2ae25f61bb0c15f547781fdc' '8ffc7d7d499e2a83bfd3486afcf1a8ab')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # fixes taken from the fedora package
  sed -i -e 's|_CONFIG_FILENAME = "/etc/texmf/texmf.cnf"|_CONFIG_FILENAME = "/usr/share/texmf/web2c/texmf.cnf"|' latex/latex/environment.py
  sed -i -e 's|_DEFAULT_TEXMF_DIR = "/usr/share/texmf-texlive"|_DEFAULT_TEXMF_DIR = "/usr/share/texmf"|' latex/latex/environment.py

  # fixes some path errors on loading
  sed -i -e 's|cnf_file\[key\]|cnf_file\[key\].replace("$TEXMFROOT", "/usr/share")|' latex/latex/environment.py

  # allows "new latex document" templates to be also loaded from /usr/share/gedit/plugins/latex/templates
  # note that the default search path is usually ~/.local/gedit/latex/templates 
  patch latex/latex/dialogs.py ../../dialogs.patch
    
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

