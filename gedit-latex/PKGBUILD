# Maintainer: Aleksei Lissitsin <aleksei.lissitsin@gmail.com>
pkgname=gedit-latex
pkgver=3.4.1.git300413
pkgrel=2
pkgdesc="Gedit plugin that provides features to ease the edition of LaTeX documents"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gedit/LaTeXPlugin"
license=('GPL')
groups=()
depends=(gedit rubber texlive-core python-gobject python-dbus)
makedepends=(cairo gettext atk intltool libpeas gtk3 glib2)
provides=()
conflicts=(gedit-latex-plugin)
replaces=(gedit-latex-plugin)
install=gschemas.install
source=(https://git.gnome.org/browse/gedit-latex/snapshot/gedit-latex-273a455755453fb298eed37f1c4f608e849b7ced.tar.gz)
md5sums=('253c75b41eed8fdbf2e61abc55b30d4c')

build() {
  cd "$srcdir"
  
  ln -sf gedit-latex-273a455755453fb298eed37f1c4f608e849b7ced "$pkgname-$pkgver"

  cd "$pkgname-$pkgver"
  
  # fixes taken from the fedora package
  sed -i -e 's|_CONFIG_FILENAME = "/etc/texmf/texmf.cnf"|_CONFIG_FILENAME = "/usr/share/texmf/web2c/texmf.cnf"|' latex/latex/environment.py
  sed -i -e 's|_DEFAULT_TEXMF_DIR = "/usr/share/texmf-texlive"|_DEFAULT_TEXMF_DIR = "/usr/share/texmf"|' latex/latex/environment.py

  # fixes some path errors on loading
  sed -i -e 's|cnf_file\[key\]|cnf_file\[key\].replace("$TEXMFROOT", "/usr/share")|' latex/latex/environment.py

  #patch -p1 < $srcdir/cmp.patch

  ./autogen.sh

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

