# Maintainer: Aleksei Lissitsin <aleksei.lissitsin@gmail.com>
pkgname=gedit-latex
pkgver=3.4.1
pkgrel=3
pkgdesc="Gedit plugin that provides features to ease the edition of LaTeX documents"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gedit/LaTeXPlugin"
license=('GPL')
groups=()
depends=(gedit rubber texlive-core python2-gobject python2-dbus)
makedepends=(cairo gettext atk intltool libpeas gtk3 glib2)
provides=()
conflicts=(gedit-latex-plugin)
replaces=(gedit-latex-plugin)
install=gschemas.install
source=(http://ftp.gnome.org/pub/gnome/sources/gedit-latex/3.4/$pkgname-$pkgver.tar.xz dialogs.patch completion.patch)
md5sums=('d221f1ca2ae25f61bb0c15f547781fdc' 'bf54383ad44f05bce50f69e9824a4ea6' '5598bd052dc06e42e87a422f608e6b08')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # fixes taken from the fedora package
  sed -i -e 's|_CONFIG_FILENAME = "/etc/texmf/texmf.cnf"|_CONFIG_FILENAME = "/usr/share/texmf/web2c/texmf.cnf"|' latex/latex/environment.py
  sed -i -e 's|_DEFAULT_TEXMF_DIR = "/usr/share/texmf-texlive"|_DEFAULT_TEXMF_DIR = "/usr/share/texmf"|' latex/latex/environment.py

  # fixes some path errors on loading
  sed -i -e 's|cnf_file\[key\]|cnf_file\[key\].replace("$TEXMFROOT", "/usr/share")|' latex/latex/environment.py

  # allows "new latex document" templates to be also loaded from /usr/share/gedit/plugins/latex/templates
  # note that the default search path is usually ~/.local/gedit/latex/templates 
  patch -p1 < $srcdir/dialogs.patch

  # Use snippets format for the autocompletion of builtin commands
  # Fixed upstream https://bugzilla.gnome.org/show_bug.cgi?id=681966
  patch -p1 < $srcdir/completion.patch
    
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

