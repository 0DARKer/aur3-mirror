# Maintainer: Aleksei Lissitsin <aleksei.lissitsin@gmail.com>
pkgname=gedit-latex
pkgver=3.4.1.git260413
pkgrel=1
pkgdesc="Gedit plugin that provides features to ease the edition of LaTeX documents"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gedit/LaTeXPlugin"
license=('GPL')
groups=()
depends=(gedit rubber texlive-core python-gobject python-dbus)
makedepends=(cairo gettext atk intltool libpeas gtk3 glib2)
provides=()
conflicts=(gedit-latex-plugin)
replaces=(gedit-latex-plugin)
install=gschemas.install
source=(https://git.gnome.org/browse/gedit-latex/snapshot/gedit-latex-13d71e074e171a884e2352c8ebc22c351933e526.tar.gz cmp.patch fixurl.patch)
md5sums=('2f8401990663825eed3b92f96a033e75' 'b590f352792f1d99e925dcf92cde5864' '1f0b3459a4f3c86ab37257168a4d457b')

build() {
  cd "$srcdir"
  
  ln -sf gedit-latex-13d71e074e171a884e2352c8ebc22c351933e526 "$pkgname-$pkgver"

  cd "$pkgname-$pkgver"
  
  # fixes taken from the fedora package
  sed -i -e 's|_CONFIG_FILENAME = "/etc/texmf/texmf.cnf"|_CONFIG_FILENAME = "/usr/share/texmf/web2c/texmf.cnf"|' latex/latex/environment.py
  sed -i -e 's|_DEFAULT_TEXMF_DIR = "/usr/share/texmf-texlive"|_DEFAULT_TEXMF_DIR = "/usr/share/texmf"|' latex/latex/environment.py

  # fixes some path errors on loading
  sed -i -e 's|cnf_file\[key\]|cnf_file\[key\].replace("$TEXMFROOT", "/usr/share")|' latex/latex/environment.py

  patch -p1 < $srcdir/cmp.patch

  patch -p1 < $srcdir/fixurl.patch
    
  ./autogen.sh

  # PYTHON=/usr/bin/python2 

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

