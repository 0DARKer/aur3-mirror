# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-libffi
pkgver=3.0.13
pkgrel=1
pkgdesc="A portable, high level programming interface to various calling conventions (mingw-w64)"
arch=('any')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!libtool' '!strip')
license=('MIT')
url="http://sourceware.org/libffi"
source=("ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz")
md5sums=('45f3b6dbc9ee7c7dfbbbc5feba571529')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  alias wine=true # if wine is installed, configure tries to use it to execute .exe files, this way it fails

  for _arch in ${_architectures}; do
    cd "${srcdir}/libffi-${pkgver}"
    export CFLAGS="-O2 -mms-bitfields"
    export PKG_CONFIG_PATH="/usr/${_arch}/lib/pkgconfig/"
    unset LDFLAGS
    mkdir -p build-${_arch} && cd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/libffi-${pkgver}/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    rm -rf "$pkgdir/usr/${_arch}/share"
    ${_arch}-strip -x -g $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a
  done
}

# vim:set ts=2 sw=2 et:
