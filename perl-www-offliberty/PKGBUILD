# Mantainer: 3ED <krzysztof1987 at gmail dot com>

pkgname=perl-www-offliberty
_lastauthor=M/MG/MGV
_pkgname=WWW-Offliberty
pkgver=0.001
pkgrel=1
pkgdesc="a simple interface to the offliberty.com download service"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-html-tree' 'perl-libwww')
makedepends=('perl-extutils-makemaker')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz
        01-api-compat-fix.patch)
sha256sums=('d9536ac9eb1d98f65c2239c8496cb1b401cca7b5de2b17d26fd4d59c1e6b4ec8'
            '91635c133ded5fa31b721cd7df3fd085830f7d4e375aea08b02a2268b3a98913')

prepare() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  patch -p1 -i "$srcdir/01-api-compat-fix.patch"
}
build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Makefile.PL
  make
}
check() {
  cd ${_pkgname}-${pkgver}
  make test
}
package() {
  cd ${_pkgname}-${pkgver}
  make install
}

