# Maintainer: hauzer <alokinpwn at gmail dot com>
# Contributer: Ghost1227 <ghost1227 at archlinux dot us>
pkgname=stendhal
pkgver=0.93
pkgrel=2
pkgdesc="A fully fledged free open source multiplayer online adventure game (MORPG) developed using the Arianne game system."
arch=('any')
url="http://stendhalgame.org/"
license=('GPL')
depends=('java-runtime')
makedepends=('unzip')
source=("http://sourceforge.net/projects/arianne/files/stendhal/${pkgver}/${pkgname}-FULL-${pkgver}.zip")
noextract=("${pkgname}-FULL-${pkgver}.zip")
md5sums=('3be93062c45a7a1e9bdb587636035589')

build() {
  mkdir -p "${srcdir}/${pkgname}"
  unzip -q "${srcdir}/${pkgname}-FULL-${pkgver}.zip" -d "${srcdir}/${pkgname}"
}

package() {
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -r "${srcdir}/${pkgname}" "${pkgdir}/opt"

  # Make a sh launcher in /usr/bin
  mkdir -p "${pkgdir}/usr/bin"
cat > "${pkgdir}/usr/bin/${pkgname}" <<EOF
#!/bin/bash
"\$JAVA_HOME/bin/java" -jar '/opt/${pkgname}/${pkgname}-${pkgver}.jar'
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  # Make a desktop launcher
  mkdir -p "${pkgdir}/usr/share/applications"
cat > "${pkgdir}/usr/share/applications/${pkgname}.desktop" <<EOF
[Desktop Entry]
Type=Application
Version=1.0
Name=Stendhal
Comment=${pkgdesc}
Exec=${pkgname}
Terminal=false
Categories=Game;Java;
EOF
  chmod 644 "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
