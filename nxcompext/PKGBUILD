# Contributor: Gerhard Brauer <gerbra@archlinux.de>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Markus Opitz <mastero23 at gmail dot com>
# Maintainer: Milan Knížek <knizek@volny.cz>
pkgname=nxcompext
pkgver=3.5.0.9
pkgrel=3
pkgdesc="NX X11 protocol compression extensions library"
arch=('i686' 'x86_64')
url="http://www.x2go.org/"
license=('GPL2')
depends=('libjpeg' 'libpng' 'gcc-libs' 'nxcomp>=3.5.0')
conflicts=("${pkgname}-x2go-git")
groups=('x2go' 'alts')
install=${pkgname}.install
source=('nxcomp_Png.cpp.patch'
        "http://code.x2go.org/releases/source/nx-libs/nx-libs_${pkgver}-full.tar.gz")
md5sums=('4271a8a665b13a16d5999f743e147dd6'
         'be272307cf477f756aef702a6d4ed2ae')

build() {
  cd "$srcdir/nx-libs_${pkgver}"
  sed --in-place=orig \
    's!test -d nxproxy \&\& cd nxproxy \&\& autoconf \&\& ./configure \&\& make!# removed line here!' \
    Makefile
  patch -p1 < ../nxcomp_Png.cpp.patch
  make || return 1
}

package() {
  cd "$srcdir/nx-libs_${pkgver}/${pkgname}"

  install -m 755 -d "$pkgdir/usr/lib/nx/"
  install -m 644 libXcompext.so.3.5.0 "$pkgdir/usr/lib/nx/"
  ln -s libXcompext.so.3.5.0 "$pkgdir/usr/lib/nx/libXcompext.so.3"
  ln -s libXcompext.so.3.5.0 "$pkgdir/usr/lib/nx/libXcompext.so"
  install -m 644 libXcompext.a "$pkgdir/usr/lib/nx/"

  install -m 755 -d "$pkgdir/usr/include/nx/"
  install -m 644 NX*.h "$pkgdir/usr/include/nx/"

  install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 "../doc/changelog" "${pkgdir}/usr/share/doc/${pkgname}/changelog.DEBIAN"
  install -m 644 "../doc/README.NX-redistributed" "${pkgdir}/usr/share/doc/${pkgname}/README.NX-redistributed"
}

