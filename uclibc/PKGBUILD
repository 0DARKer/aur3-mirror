# Maintainer: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: res <andres87p@gmail.com>

pkgname=uclibc
_pkgname=uClibc
pkgver=0.9.31
pkgrel=3
pkgdesc='C library for developing embedded Linux systems'
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.uclibc.org/"
source=(http://www.uclibc.org/downloads/$_pkgname-$pkgver.tar.xz
        config config.x86_64)
md5sums=('5a222b2112bc2b76e384a11c0304da15'
         'ff30eeef715ed0eec44a5bcdddf9c34a'
         'ab8fc1eea0ac1af0f109b390a2e0568c')
_makemenucfg="n"

build() {
    cd $_pkgname-$pkgver

    msg "Copying default config for $CARCH"
    if [ "$CARCH" = "x86_64" ]; then
        cat ../config.x86_64 >./.config
    else
        cat ../config >./.config
    fi
    
    if [ "$_makemenucfg" = "y" ]; then
        msg "Running make menuconfig"
        make menuconfig
    else
        msg "Using default config"
        make oldconfig
    fi

    make
    make PREFIX=$pkgdir install
}
