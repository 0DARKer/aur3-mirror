# Maintainer: Keshav P R <skodabenz at rocketmail dot com>

pkgname=lxappearance2-git
pkgver=20100810
pkgrel=1
pkgdesc="GTK+ theme switcher for LXDE (manages icons and fonts) - Version 2 - GIT"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://lxde.org/"
groups=()
depends=('gtk2' 'intltool')
makedepends=('git' 'pkgconfig' 'gettext')
conflicts=('lxappearance')
provides=('lxappearance')
source=()
md5sums=()

_gitroot="git://lxde.git.sourceforge.net/gitroot/lxde/lxappearance" # LXAppearance2 merged with LXAppearance
_gitname="lxappearance"

build() {
	
    if [ -d ${srcdir}/${_gitname} ]
    then
        msg "Updating git repo"
        echo
        cd ${srcdir}/${_gitname} && git fetch && git checkout origin/master
    else
        msg "Cloning the git repo"
        cd ${srcdir}
        echo
        git clone ${_gitroot} ${_gitname}
        cd ${srcdir}/${_gitname} && git checkout origin/master
    fi
    echo

    if [ -d ${srcdir}/${_gitname}_1 ]
    then
        rm -rf ${srcdir}/${_gitname}_1/
    fi

    cp -r ${srcdir}/${_gitname} ${srcdir}/${_gitname}_1
    cd ${srcdir}/${_gitname}_1/
    
    ./autogen.sh 
    echo   
    ./configure --sysconfdir=/etc --prefix=/usr || return 1
    echo
    make || return 1
    echo
    
}

package() {
	
	cd ${srcdir}/${_gitname}_1/
	make DESTDIR="${pkgdir}" install || return 1
	echo

}
