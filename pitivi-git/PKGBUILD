# Maintainer: Kerrick Staley <mail@kerrickstaley.com>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Gabor Nyekhelyi (n0gabor) <n0gabor@vipmail.hu>

pkgname=pitivi-git
pkgver=20121220
pkgrel=1
pkgdesc='PiTiVi allows users to easily edit audio/video projects based on the GStreamer framework (Git version)'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gstreamer' 'gst-plugins-base' 'gobject-introspection' 'python2' 'goocanvas' 'gst-python2-git' 'gst-editing-services-git' 'gnonlin-git')
makedepends=('automake' 'libtool' 'intltool' 'itstool' 'pygobject2-devel' 'gtk-doc' 'gnome-doc-utils')
provides=('pitivi')
conflicts=('pitivi')
url='http://www.pitivi.org/'

_gitroot='git://git.gnome.org/pitivi'
_gitname='pitivi'

build() {
    cd "$srcdir"

    msg 'Connecting to GIT server...'
    if [[ -d "$_gitname" ]]; then
        (cd "$_gitname"; git pull origin) || return 1
    else
        git clone --depth=1 "$_gitroot" "$_gitname" || return 1
    fi
    cd "$_gitname"
    if [[ ! -f common/gst-autogen.sh ]]; then
        msg '+ Setting up common submodule'
        git submodule init
    fi
    git submodule update || return 1
    msg 'GIT checkout done or server timeout'
    
    msg 'Starting make...'
    ./autogen.sh --prefix=/usr || return 1
    make || return 1
}

package() {
    cd "$srcdir/$_gitname"
    make DESTDIR="$pkgdir" install || return 1
    install -D -m644 data/pitivi.desktop "$pkgdir/usr/share/applications/pitivi.desktop"
    sed -i '1 s/python/python2/' "$pkgdir/usr/bin/pitivi"
}
