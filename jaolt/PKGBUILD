# Maintainer: Max Roder <maxroder AT web DOT de>
pkgname=jaolt
pkgver=0.5.7.1
pkgrel=3
pkgdesc="java auction management tool for ebay, auvito and hood"
arch=('any')
conflicts=('jaolt-svn')
url="http://code.google.com/p/jaolt/"
license=('LGPL' 'CDDL'  'APACHE' 'CCPL:by' 'custom:H2' 'custom:jdom' 'custom:jgoodies' 'custom:ebay-sdk')
depends=('java-runtime' 'sh' 'x-server')
source=(http://jaolt.googlecode.com/files/${pkgname}_0.5.7.1-noarch-807.tar.gz
		jaolt.sh
		ebay-sdk-license.txt)
md5sums=('beb005bb758b2dc8c05710f2441f86c5'
	 'c50ddad7404b6f36a03aa9fa4491dede'
         '7726867071abce13d4c88d626f28e918')
package() {
	cd ${srcdir}/${pkgname} 

	# Create directories
	install -d ${pkgdir}/usr/share/java/jaolt/
	install -d ${pkgdir}/usr/share/applications/
	install -d ${pkgdir}/usr/share/pixmaps/

	# Delete unnecessary files
	rm -rf build.xml INSTALLING jaolt lgpl.txt other.txt sun.txt changelog.txt data/auctionplatforms

	# license stuff
	install -d ${pkgdir}/usr/share/licenses/jaolt
	mv h2.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.h2
	mv jdom.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jdom
	mv jgoodies.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jgoodies
	cp license-instructions.txt ${pkgdir}/usr/share/licenses/jaolt/readme
	install -m 644 ${srcdir}/ebay-sdk-license.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.ebay-sdk

	# Copy files
	mv jaolt.desktop ${pkgdir}/usr/share/applications/
        mv jaolt.xpm ${pkgdir}/usr/share/pixmaps/
	cp -r * ${pkgdir}/usr/share/java/jaolt/

	# Install wrapper script
	install -Dm 755 ${srcdir}/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
}
