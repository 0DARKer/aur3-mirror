# Maintainer: Max Roder <maxroder@web.de>

pkgname='jaolt'
pkgver='0.6.9.3'
_revision='1181'
pkgrel='1'
pkgdesc='Java auction management tool for ebay, auvito and hood'
arch=('any')
url='http://code.google.com/p/jaolt/'
license=('LGPL' 'CDDL'  'APACHE' 'BSD' 'CCPL:by' 'custom:H2' 'custom:jdom' 'custom:jgoodies')
depends=('java-environment' 'bash')
conflicts=('jaolt-svn')
source=("http://jaolt.googlecode.com/files/${pkgname}_${pkgver}-noarch-${_revision}.tar.gz")
sha256sums=('4e5d15ae3e9a5b64d80b5f4a73bbc1d9fb06ca7f81893ef6bc20e827c7639e80')

package() {
	cd ${srcdir}/${pkgname}

	# Patch loader
	sed -i 's/jaolt\/launcher.jar/java\/jaolt\/launcher.jar/g' jaolt
	install -Dm755 jaolt ${pkgdir}/usr/bin/jaolt

	# Copy license files
	install -d ${pkgdir}/usr/share/licenses/jaolt
	mv license-instructions.txt ${pkgdir}/usr/share/licenses/jaolt/readme
	mv jgoodies-uiflite.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jgoodies-uiflite
	mv h2.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.h2
	mv jdom.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jdom
	mv jgoodies-forms.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jgoodies-forms
	mv jgoodies-looks.txt ${pkgdir}/usr/share/licenses/jaolt/LICENSE.jgoodies-looks

	# Desktop stuff
	install -d ${pkgdir}/usr/share/java/jaolt/ ${pkgdir}/usr/share/applications/ ${pkgdir}/usr/share/pixmaps/
	mv jaolt.desktop ${pkgdir}/usr/share/applications/
	mv jaolt.xpm ${pkgdir}/usr/share/pixmaps/
	
	# Remove unnecessary files, copy anything else
	rm -rf build.xml lgpl.txt INSTALLING jaolt
	cp -r * ${pkgdir}/usr/share/java/jaolt/
}

# vim:set ts=2 sw=2 et:
