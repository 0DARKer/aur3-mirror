# Maintainer: Antonio Rojas
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kde-baseapps-git
pkgname=(dolphin-frameworks-git konqueror-frameworks-git kdepasswd-frameworks-git kdialog-frameworks-git kfind-frameworks-git 
keditbookmarks-frameworks-git libkonq-frameworks-git)
pkgver=r19392.3ab1913
pkgrel=1
pkgdesc="Collection of applications used for file and Internet browsing."
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/applications/kde-baseapps'
license=('LGPL')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'kdesu' 'kdelibs4support' 'kactivities-frameworks' 'baloo-widgets-git')
source=('git://anongit.kde.org/kde-baseapps#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kde-baseapps
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kde-baseapps \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package_dolphin-frameworks-git() {
	pkgdesc='File Manager'
	depends=('baloo-widgets-git' 'libkonq-frameworks-git' 'knewstuff' 'kio-extras')
	url="http://kde.org/applications/system/dolphin/"
	conflicts=('kdebase-dolphin')
	provides=('kdebase-dolphin')
	cd $srcdir/build/dolphin
	make DESTDIR=$pkgdir install
}

package_konqueror-frameworks-git() {
	pkgdesc='KDE File Manager & Web Browser'
	depends=('keditbookmarks-frameworks-git' 'dolphin-frameworks-git' 'kded')
	url="http://kde.org/applications/system/konqueror/"
	conflicts=('kdebase-konqueror')
	provides=('kdebase-konqueror')
	cd $srcdir/build/konqueror
	make DESTDIR=$pkgdir install
}

package_kdepasswd-frameworks-git() {
	pkgdesc='Change Password'
	depends=('libkonq-frameworks-git' 'kdesu')
        conflicts=('kdebase-kdepasswd')
        provides=('kdebase-kdepasswd')
	cd $srcdir/build/kdepasswd
	make DESTDIR=$pkgdir install
}

package_kdialog-frameworks-git() {
	pkgdesc='A utility for displaying dialog boxes from shell scripts'
	depends=('kdelibs4support')
        conflicts=('kdebase-kdialog')
        provides=('kdebase-kdialog')
	cd $srcdir/build/kdialog
	make DESTDIR=$pkgdir install
}

package_kfind-frameworks-git() {
	pkgdesc='Find Files/Folders'
	depends=('libkonq-frameworks-git')
        conflicts=('kdebase-kfind')
        provides=('kdebase-kfind')
	url="http://kde.org/applications/utilities/kfind/"
	cd $srcdir/build/kfind
	make DESTDIR=$pkgdir install
}

package_keditbookmarks-frameworks-git() {
        pkgdesc='Bookmark organizer and editor'
        depends=('libkonq-frameworks-git')
        conflicts=('kdebase-keditbookmarks')
        provides=('kdebase-keditbookmarks')
        url="http://www.kde.org/"
        cd $srcdir/build/keditbookmarks
        make DESTDIR=$pkgdir install
}

package_libkonq-frameworks-git() {
	pkgdesc='KDE libraries for the basic desktop applications'
	depends=('kdelibs4support')
	conflicts=('kdebase-lib')
	cd $srcdir/build/lib
	make DESTDIR=$pkgdir install
}
