# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-baseapps-git
pkgver=r19002.534a5cf
pkgrel=1
pkgdesc="Collection of applications used for file and Internet browsing."
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/applications/kde-baseapps'
license=('LGPL')
depends=('kdesu-git' 'kde4support-git')
makedepends=('extra-cmake-modules' 'git')
source=('git://anongit.kde.org/kde-baseapps#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kde-baseapps
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
  # Ugly fix for .SOVERSION
  sed -e 's|${KDE_NON_GENERIC_LIB_VERSION}|"4.97.0"|g' -e 's|${KDE_NON_GENERIC_LIB_SOVERSION}|"5"|g' -i kde-baseapps/lib/konq/CMakeLists.txt
}

build() {
  cd build
  cmake ../kde-baseapps \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
