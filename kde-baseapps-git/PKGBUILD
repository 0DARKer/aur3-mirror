# Maintainer: Antonio Rojas
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kde-baseapps-git
pkgname=(dolphin-frameworks-git kdepasswd-frameworks-git kdialog-frameworks-git kfind-frameworks-git libkonq-frameworks-git)
pkgver=r19257.525b80c
pkgrel=1
pkgdesc="Collection of applications used for file and Internet browsing."
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/applications/kde-baseapps'
license=('LGPL')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'kdesu' 'kdelibs4support' 'kactivities-frameworks' 'baloo-widgets-frameworks-git')
source=('git://anongit.kde.org/kde-baseapps#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kde-baseapps
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kde-baseapps \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins
  make
}

package_dolphin-frameworks-git() {
	pkgdesc='File Manager'
	depends=('baloo-widgets-frameworks-git' 'libkonq-frameworks-git' 'knewstuff' 'kio-extras')
	url="http://kde.org/applications/system/dolphin/"
	cd $srcdir/build/dolphin
	make DESTDIR=$pkgdir install
}

package_kdepasswd-frameworks-git() {
	pkgdesc='Change Password'
	depends=('libkonq-frameworks-git' 'kdesu')
	cd $srcdir/build/kdepasswd
	make DESTDIR=$pkgdir install
}

package_kdialog-frameworks-git() {
	pkgdesc='A utility for displaying dialog boxes from shell scripts'
	depends=('kdelibs4support')
	cd $srcdir/build/kdialog
	make DESTDIR=$pkgdir install
}

package_kfind-frameworks-git() {
	pkgdesc='Find Files/Folders'
	depends=('libkonq-frameworks-git')
	url="http://kde.org/applications/utilities/kfind/"
	cd $srcdir/build/kfind
	make DESTDIR=$pkgdir install
}

package_libkonq-frameworks-git() {
	pkgdesc='KDE libraries for the basic desktop applications'
	depends=('kdelibs4support')
	cd $srcdir/build/lib
	make DESTDIR=$pkgdir install
}
