# Maintainer: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: shining <shiningxc.at.gmail.com>
# Contributor: cyberdune <cyberdune@gmail.com>
# Patches taken from the Debian packaging project

pkgname=octave-mkl
pkgver=3.8.0
pkgrel=2
pkgdesc="A high-level language, primarily intended for numerical computations. BLAS, LAPACK and FFT from Intel's MKL."
arch=('i686' 'x86_64')
url="http://www.octave.org"
license=('GPL')
provides=('octave=3.8.0')
conflicts=('octave')
depends=('intel-mkl' 'fftw>=3.2.2' 'arpack' 'qhull' 'qrupdate' 'glpk'
         'hdf5-openmpi' 'graphicsmagick' 'gl2ps' 'fltk' 'qscintilla'
         'java-environment' 'glu' 'curl')
makedepends=('gcc-fortran' 'texlive-core' 'suitesparse')
optdepends=('texinfo: for help-support in octave'
            'gnuplot: alternative plotting')
options=('!emptydirs')
install=octave-mkl.install

source=(ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.bz2{,.sig}
        add_info_dir_categories.patch)
md5sums=('442fe5205ba6322f7a69145b79fca85e'
         'SKIP'
         'cbde759a77465ae874789df3b602e08f')
sha512sums=('2e8b81b418e235cddd6678d7cc83b8441c64b36f98854ab704c1b34d5ff711d3f6cce7ffe33f5f70a7bc231b2219b39d16033e6b9d04174cbab68f21c5a8abec'
            'SKIP'
            '6f6831268a59e05cd407b4e2e1e0f78d4aee91fe2e49d42427bfe976fd4168434e97901e986785393c0d0f8e977e24061397d9b7e8cb15d059aa07c718dfaf82')

if [ "$CARCH" == "x86_64" ]; then
    _intel_arch=intel64
    _intel_lib=mkl_gf_lp64
elif [ "$CARCH" == "i686" ]; then
    _intel_arch=ia32
    _intel_lib=mkl_gf
fi
_orgpkgname=octave

prepare() {
  # Apply selected patches, conveniently provided by Debian thanks to their diligent packagers
  # Refer to the patch files for details
  cd "${srcdir}/${_orgpkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/add_info_dir_categories.patch"

  autoreconf -vfi
}

build() {
  # Set environment for building octave with Intel MKL
  source /opt/intel/mkl/bin/mklvars.sh ${_intel_arch}
  _mkllibs=" -fopenmp -Wl,--no-as-needed -I${MKLROOT}/include -I${MKLROOT}/include/fftw -L${MKLROOT}/lib/intel64 -l${_intel_lib} -lmkl_core -lmkl_gnu_thread -ldl -lpthread -lm"
  export LANG=C

  cd "${srcdir}/${_orgpkgname}-${pkgver}"

  ./configure  MOC=moc-qt4 UIC=uic-qt4 \
  --prefix=/usr --libexecdir=/usr/lib \
  --enable-shared --disable-static \
  --with-blas="${_mkllibs}" \
  --with-lapack="${_mkllibs}" \
  --with-fftw3="${_mkllibs}" \
  --with-fftw3f="${_mkllibs}"

  # Put Intel's basic math library in front of the GLIBC libm
  find ./ -name Makefile -exec sed -i "s/\(^\| \)-lm\( \|$\)/\1-limf -lm\2/g" {} \;

  make
}

check() {
  cd "${srcdir}/${_orgpkgname}-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/${_orgpkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # add octave library path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${_orgpkgname}/${pkgver}" > "${pkgdir}/etc/ld.so.conf.d/${_orgpkgname}.conf"
}
