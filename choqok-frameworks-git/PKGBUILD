# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=choqok-frameworks-git
pkgver=r1750.c55446f
pkgrel=1
pkgdesc='A Twitter/identi.ca/laconica client for KDE'
arch=(i686 x86_64)
url='http://kde.org/applications/graphics/gwenview/'
license=(GPL)
depends=(kdelibs4support knotifyconfig qca-qt5)
makedepends=(extra-cmake-modules git kdoctools python)
conflicts=(choqok)
provides=(choqok)
source=("git://anongit.kde.org/choqok.git#branch=frameworks")
install=$pkgname.install
sha256sums=('SKIP')

pkgver() {
  cd choqok
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../choqok \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
