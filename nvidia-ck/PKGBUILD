# Maintainer: graysky <graysky AT archlnux.us>
# Contributor: A.J. Korf <jacobkorf at gmail dot com>
# Contrubutor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-ck
pkgver=285.05.09
_extramodules=extramodules-3.1-ck
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=5
_pkgdesc="nVidia drivers for linux-ck."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-ck>=3.1' 'linux-ck<3.2' "nvidia-utils=${pkgver}")
makedepends=('linux-ck-headers>=3.1' 'linux-ck-headers<3.2')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-ck-stable' 'nvidia-beta-ck')
#groups=('ck-generic')
#replaces=()
license=('custom')
options=(!strip)
install=nvidia-ck.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('2d469a90abef50320f548cfa8085e3a0')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('2f4a0e78f5560c07220b7ed6fc1e27aa')
fi

build() {
	cd "${srcdir}"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
		"${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/etc/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist_ck.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-ck.install"
	gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
