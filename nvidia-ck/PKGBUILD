# Maintainer: graysky <graysky AT archlnux.us>
# Contributor: A.J. Korf <jacobkorf at gmail dot com>
# Contrubutor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-ck
pkgver=260.19.44
_kernver='2.6.37-ck'
pkgrel=1
_pkgdesc="NVIDIA drivers for kernel26-ck."
pkgdesc="${_pkgdesc}"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-ck>=2.6.37' 'kernel26-ck<2.6.38' "nvidia-utils=${pkgver}")
makedepends=('kernel26-ck-headers>=2.6.37' 'kernel26-ck-headers<2.6.38')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-ck-stable' 'nvidia-beta-ck')
#groups=('ck-generic')
license=('custom')
install=nvidia.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('c99b0710a43db3aafb4f27408194e910')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('3dfce350616ad70d626b531a15c8604f')
fi

build() {
	cd $srcdir
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
 	make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
	install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
		$pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
        install -d -m755 $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist_ck.conf
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install
	gzip -9 $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
}
pkgdesc="NVIDIA drivers for kernel26-ck."
