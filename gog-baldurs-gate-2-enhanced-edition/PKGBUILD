# Maintainer: Patrice Peterson <runiq at archlinux dot us>
pkgname=gog-baldurs-gate-2-enhanced-edition
_pkgname=baldurs-gate-2-enhanced-edition
_original="Baldurs Gate 2 Enhanced Edition"
pkgver=1.0.0.1
pkgrel=1
pkgdesc="Your story begins anew in the exotic southern kingdom of Amn, amidst the opulence of the sinister capital city of Athkatla. GOG Version. Linux native."
arch=('i686' 'x86_64')
url="http://www.gog.com/game/baldurs_gate_2_enhanced_edition"
license=('custom')
depends_i686=('glibc' 'alsa-lib' 'libgl')
depends_x86_64=('lib32-glibc' 'lib32-alsa-lib' 'lib32-libgl')
optdepends_i686=('libpulse: pulseaudio support')
optdepends_x86_64=('lib32-libpulse: pulseaudio support')
#DLAGENTS+=('gog::/usr/bin/echo "Could not find \"$(echo %u | cut -c 7-)\". Manually download it to \"$(pwd)\", or set up a gog:// DLAGENT."; exit 1')
#source=("gog://gog_baldurs_gate_2_enhanced_edition_${pkgver}.tar.gz"
source=("gog_baldurs_gate_2_enhanced_edition_${pkgver}.tar.gz"
        "${pkgname}")
PKGEXT=".pkg.tar"
sha256sums=('0f0efa5c6cec16d006794fd8f00b98e0e60f3403b19861ac5d415a5f2e31ea91'
            'f7ceec715932198a3e2f054d99230173bd4d0873207f5a9704f8c5b4d9792290')

package() {
  mkdir -p "${pkgdir}"/opt/gog/${_pkgname}
  cp -r "${srcdir}"/"${_original}"/* "${pkgdir}"/opt/gog/${_pkgname}
  install -Dm644 "${srcdir}"/"${_original}"/support/${pkgname}-primary.desktop "${pkgdir}"/usr/share/applications/${pkgname}-primary.desktop
  install -Dm644 "${srcdir}"/"${_original}"/support/${pkgname}.png "${pkgdir}"/usr/share/pixmaps/${pkgname}.png
  install -Dm644 "${srcdir}"/"${_original}"/docs/End\ User\ License\ Agreement.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -Dm755 "${srcdir}"/$pkgname "${pkgdir}"/usr/bin/${pkgname}
}
