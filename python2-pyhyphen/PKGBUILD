# Maintainer: Veli-Jussi Raitila <vjr@iki.fi>

pkgname=python2-pyhyphen
_pkgname=PyHyphen
pkgver=2.0.1
pkgrel=1
pkgdesc="Hyphenation library for Python"
arch=('x86_64' 'i686')
url="http://pyhyphen.googlecode.com/"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('python2')
install=pyhyphen.install

source=(http://pypi.python.org/packages/source/P/PyHyphen/${_pkgname}-${pkgver}.zip)
md5sums=('47441de611fbfb372722f11d682c5ab0')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  sed -i \
    -e "s|\$path|/usr/lib/python2.7/site-packages/hyphen|" \
    -e "s|\$repo|http://cgit.freedesktop.org/libreoffice/dictionaries/plain/|" \
    2.x/config.py

  sed -i \
    -e "s|'install' in sys.argv|False|" setup.py

  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python2 setup.py install --root="${pkgdir}" --optimize=1
}
