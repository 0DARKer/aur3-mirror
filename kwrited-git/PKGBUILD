# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kwrited-git
pkgver=r264.7aad308
pkgrel=1
pkgdesc='Kwrited'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('kpty-git' 'kdelibs4support-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
conflicts=('kwrited' 'kde-workspace<0')
provides=('kwrited')
source=('git://anongit.kde.org/kwrited.git')
md5sums=('SKIP')

pkgver() {
  cd kwrited
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kwrited/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_AS_EXECUTABLE=OFF \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
