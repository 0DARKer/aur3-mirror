# Contributor: Dave Reisner <d@falconindy.com>

pkgname=util-linux-git
pkgver=20120908
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux - the GIT version"
url="http://userweb.kernel.org/~kzak/util-linux/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('zlib' 'udev' 'ncurses' 'shadow')
makedepends=('git')
provides=('util-linux=2.23' 'eject')
conflicts=('util-linux' 'eject')
replaces=('eject')
options=('!libtool')
source=('pam-su'
        'pam-login'
        'pam-chfn-chsh'
        'uuidd.tmpfiles')
install='util-linux.install'
md5sums=('fa85e5cce5d723275b14365ba71a8aad'
         '4368b3f98abd8a32662e094c54e7f9b1'
         'a31374fef2cba0ca34dfc7078e2969e4'
         'a39554bfd65cccfd8254bb46922f4a67')

_gitroot="git://git.kernel.org/pub/scm/utils/util-linux/util-linux.git"
_gitname="util-linux"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname"{,-build}
  cd "$srcdir/$_gitname-build"

  msg "Configuring..."
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --localstatedir=/run \
      --disable-wall \
      --disable-kill \
      --enable-write \
      --enable-raw \
      --enable-fs-paths-extra="/usr/sbin:/usr/bin" \
      --enable-chfn-chsh \
      --enable-vipw \
      --enable-newgrp \
      --enable-runuser \
      --enable-socket-activation

  msg "Starting make..."
  make
}

package() {
  make -C "$_gitname-build" DESTDIR="$pkgdir" install

  # enable setuid bit on various progs
  chmod 4755 "$pkgdir"/usr/bin/{ch{sh,fn},newgrp}

  # PAM
  install -dm755 "$pkgdir/etc/pam.d"
  install -m644 "$srcdir/pam-chfn-chsh" "$pkgdir/etc/pam.d/chfn"
  install -m644 "$srcdir/pam-chfn-chsh" "$pkgdir/etc/pam.d/chsh"
  install -m644 "$srcdir/pam-login" "$pkgdir/etc/pam.d/login"
  install -m644 "$srcdir/pam-su" "$pkgdir/etc/pam.d/su"
  install -m644 "$srcdir/pam-su" "$pkgdir/etc/pam.d/su-l"

  # tmpfiles
  install -Dm644 "$srcdir/uuidd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/uuidd.conf"
}

