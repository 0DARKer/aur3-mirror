# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based On: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

_patchlevel=0
_basever=0.145

pkgname=sdlmame-wout-gconf
_pkgname=sdlmame
pkgver="${_basever}.u${_patchlevel}"
pkgrel=1
pkgdesc="A port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support. WITHOUT GCONF and GTK"
url="http://mamedev.org/"
license=('custom:MAME License')
arch=('i686' 'x86_64')
provides=('sdlmame')
conflicts=('sdlmame')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gcc-libs' 'fontconfig')
makedepends=('mesa')
[ "$CARCH" = 'i686' ] && makedepends=('mesa' 'nasm')
optdepends=('ttf-liberation: recommended UI font')
DLAGENTS=('http::/usr/bin/wget -U "Mozilla/5.0 (X11; U; Linux x86_64; en-US; rv:1.9.1.2) Gecko/20090804 Shiretoko/3.5.2" -c -t 3 --waitretry=3 -O %o %u')
install="${_pkgname}".install

for i in `seq 1 "${_patchlevel}"`; do
  _patches=""${_patches}" sdlmame-"${_basever/./}"u"${i}"_diff.zip::http://mamedev.org/updates/"${_basever/./}"u"${i}"_diff.zip"
done

source=("mame"${_basever/./}"s.zip::http://mamedev.org/downloader.php?file=releases/mame"${_basever/./}"s.zip"
        "sdlmame.sh"
        "extras.tar.gz"
        sdlmame-wout-gconf-v2.patch
        ${_patches})
md5sums=('9ddcda5b26f42873c7c4d4d5dd6fccd5'
         '3119ccfa1e970eba4467df31208adaf0'
         '420b61240bf5ae11615ba7c6100ee00d'
         'e17515824b113725c573d91710337f7a')

build() {
  cd "${srcdir}"

  bsdtar -xf mame.zip

  find . -type f -not -name '*.png' -exec perl -pi -e 's/\r\n?/\n/g' '{}' \;

  for i in `seq 1 "${_patchlevel}"`; do
    msg "Applying patch: mame"${_basever/./}"u"${i}".diff"
    patch --silent -p0 -E < "${_basever/./}"u"$i".diff
    msg2 "Done"
  done

  msg "Patch disable GCONF and GTK dependencies"
  patch --silent -p0 -E < ../sdlmame-wout-gconf-v2.patch
  msg2 "Done"

  # Modify the make options based on the user's architecture
   if [ "${CARCH}" == "x86_64" ]; then
    echo "Compiling for AMD64..."
    make ${MAKEFLAGS} TARGET=mame AMD64=1 PTR64=1 SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS" NO_DEBUGGER=1
    make ${MAKEFLAGS} tools AMD64=1 PTR64=1 SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS" NO_DEBUGGER=1
  elif [ "${CARCH}" == "i686" ]; then
    make ${MAKEFLAGS} TARGET=mame I686=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS" NO_DEBUGGER=1
    make ${MAKEFLAGS} tools I686=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS" NO_DEBUGGER=1
  else
    echo "Compiling for i386..."
    make ${MAKEFLAGS} TARGET=mame PM=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS" NO_DEBUGGER=1
    make ${MAKEFLAGS} tools PM=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS" NO_DEBUGGER=1
  fi
}

package() {
  cd "${srcdir}"

  # Install the sdlmame script
  install -Dm755 "${_pkgname}".sh "${pkgdir}"/usr/bin/"${_pkgname}"

  # Install the applications and the UI font in /usr/share
  install -Dm755 mame "${pkgdir}"/usr/share/"${_pkgname}"/"${_pkgname}"
  
  install -Dm755 chdman "${pkgdir}"/usr/share/"${_pkgname}"/chdman
  install -Dm755 jedutil "${pkgdir}"/usr/share/"${_pkgname}"/jedutil
  install -Dm755 regrep "${pkgdir}"/usr/share/"${_pkgname}"/regrep
  install -Dm755 romcmp "${pkgdir}"/usr/share/"${_pkgname}"/romcmp
  install -Dm755 testkeys "${pkgdir}"/usr/share/"${_pkgname}"/testkeys
  install -Dm755 src2html "${pkgdir}"/usr/share/"${_pkgname}"/src2html
  install -Dm755 srcclean "${pkgdir}"/usr/share/"${_pkgname}"/srcclean
  install -Dm755 ldverify "${pkgdir}"/usr/share/"${_pkgname}"/ldverify
  install -Dm755 ldresample "${pkgdir}"/usr/share/"${_pkgname}"/ldresample

  # Install the extra bits
  install -d "${pkgdir}"/usr/share/"${_pkgname}"/{artwork,ctrlr,keymaps,shader}
  install -d "${pkgdir}"/usr/share/man/man1
  install -Dm644 src/osd/sdl/shader/glsl*.*h "${pkgdir}"/usr/share/"${_pkgname}"/shader/
  install -Dm644 src/osd/sdl/man/* "${pkgdir}"/usr/share/man/man1/

  install -Dm644 "${srcdir}"/artwork/* "${pkgdir}"/usr/share/"${_pkgname}"/artwork/
  install -Dm644 "${srcdir}"/ctrlr/* "${pkgdir}"/usr/share/"${_pkgname}"/ctrlr/
  install -Dm644 src/osd/sdl/keymaps/* "${pkgdir}"/usr/share/"${_pkgname}"/keymaps/

  # Include the license
  install -Dm644 docs/license.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/license.txt
}
