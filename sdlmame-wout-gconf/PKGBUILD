# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based On: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

_patchlevel=2
_basever=0.146

pkgname=sdlmame-wout-gconf
_pkgname=sdlmame
pkgver="${_basever}.u${_patchlevel}"
pkgrel=1
pkgdesc="A port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support. WITHOUT GCONF and GTK"
url="http://mamedev.org/"
license=('custom:MAME License')
arch=('i686' 'x86_64')
provides=('sdlmame')
conflicts=('sdlmame')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gcc-libs' 'fontconfig')
makedepends=('mesa' 'nasm')
DLAGENTS=('http::/usr/bin/wget -U "Mozilla/5.0 (X11; U; Linux x86_64; en-US; rv:1.9.1.2) Gecko/20090804 Shiretoko/3.5.2" -c -t 3 --waitretry=3 -O %o %u')
install="${_pkgname}".install

for i in `seq 1 "${_patchlevel}"`; do
  _patches=""${_patches}" sdlmame-"${_basever/./}"u"${i}"_diff.zip::http://mamedev.org/updates/"${_basever/./}"u"${i}"_diff.zip"
done

source=("mame"${_basever/./}"s.zip::http://mamedev.org/downloader.php?file=releases/mame"${_basever/./}"s.zip"
        "sdlmame.sh"
        "extras.tar.gz"
        ${_patches}
        sdlmame-wout-gconf-v2.patch)
md5sums=('763fdb5b339dda00feafa9e635bd58fd'
         'e11a7d26704a9e2cff223b9b867d6a05'
         '420b61240bf5ae11615ba7c6100ee00d'
         '9a3eecc61c104539c01499834f4cfa27'
         '0ef1c3d8b7798d0be186d802b8610c27'
         'e17515824b113725c573d91710337f7a')

build() {
  cd "${srcdir}"
  if [ $NOEXTRACT -eq 0 ]; then
    bsdtar -xf mame.zip
    find . -type f -not -name \*.png | xargs perl -pi -e 's/\r\n?/\n/g'
    for i in `seq 1 ${_patchlevel}`; do
      msg "Patch#$i"
      patch -p0 -E <${_basever/./}u$i.diff
    done
  fi

  msg "Patch disable GCONF and GTK dependencies"
  patch --silent -p0 -E < ../sdlmame-wout-gconf-v2.patch
  msg2 "Done"

  make NOWERROR=1 NO_DEBUGGER=1
  make tools NOWERROR=1 NO_DEBUGGER=1
}

package() {
  cd "${srcdir}"

  # Install the sdlmame script
  install -Dm755 "${_pkgname}".sh "${pkgdir}"/usr/bin/"${_pkgname}"

  # Install the applications and the UI font in /usr/share
  install -Dm755 mame $pkgdir/usr/share/${_pkgname}/${_pkgname} || install -Dm755 mame64 $pkgdir/usr/share/${_pkgname}/${_pkgname}
  
  install -m755 chdman "${pkgdir}"/usr/share/"${_pkgname}"/chdman
  install -m755 jedutil "${pkgdir}"/usr/share/"${_pkgname}"/jedutil
  install -m755 regrep "${pkgdir}"/usr/share/"${_pkgname}"/regrep
  install -m755 romcmp "${pkgdir}"/usr/share/"${_pkgname}"/romcmp
  install -m755 testkeys "${pkgdir}"/usr/share/"${_pkgname}"/testkeys
  install -m755 src2html "${pkgdir}"/usr/share/"${_pkgname}"/src2html
  install -m755 srcclean "${pkgdir}"/usr/share/"${_pkgname}"/srcclean
  install -m755 ldverify "${pkgdir}"/usr/share/"${_pkgname}"/ldverify
  install -m755 ldresample "${pkgdir}"/usr/share/"${_pkgname}"/ldresample

  # Install the extra bits
  install -d "${pkgdir}"/usr/share/"${_pkgname}"/{artwork,ctrlr,keymaps,shader}
  install -d "${pkgdir}"/usr/share/man/man{1,6}
  install -m644 src/osd/sdl/shader/glsl*.*h "${pkgdir}"/usr/share/"${_pkgname}"/shader/
  install -m644 src/osd/sdl/man/*.1* $pkgdir/usr/share/man/man1/
  install -m644 src/osd/sdl/man/*.6* $pkgdir/usr/share/man/man6/

  install -m644 "${srcdir}"/artwork/* "${pkgdir}"/usr/share/"${_pkgname}"/artwork/
  install -m644 "${srcdir}"/ctrlr/* "${pkgdir}"/usr/share/"${_pkgname}"/ctrlr/
  install -m644 src/osd/sdl/keymaps/* "${pkgdir}"/usr/share/"${_pkgname}"/keymaps/

  # Include the license
  install -Dm644 docs/license.txt "${pkgdir}"/usr/share/licenses/"${_pkgname}"/license.txt

  # FS#28203
  sed -i 's|KEYCODE_2_PAD|KEYCODE_2PAD|' "${pkgdir}"/usr/share/sdlmame/ctrlr/*.cfg
  sed -i 's|KEYCODE_4_PAD|KEYCODE_4PAD|' "${pkgdir}"/usr/share/sdlmame/ctrlr/*.cfg
  sed -i 's|KEYCODE_6_PAD|KEYCODE_6PAD|' "${pkgdir}"/usr/share/sdlmame/ctrlr/*.cfg
  sed -i 's|KEYCODE_8_PAD|KEYCODE_8PAD|' "${pkgdir}"/usr/share/sdlmame/ctrlr/*.cfg

  find $pkgdir -type f -exec strip {} \;
}
