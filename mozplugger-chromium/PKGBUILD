# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Giuseppe Borzi <gborzi@ieee.org>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=mozplugger-chromium
pkgver=2.1.0
pkgrel=2
pkgdesc='Mozilla multimedia plugin for Chromium'
arch=('x86_64' 'i686')
url='http://mozplugger.mozdev.org/'
license=('GPL')
depends=('libx11')
backup=('etc/mozpluggerrc')
conflicts=('mozplugger')
# wget can handle the iffy server (with 2 retries)
DLAGENTS=('http::/usr/bin/wget -c -t 3 --waitretry=3 -O %o %u -U LEDERHOSEN')
source=("http://mozplugger.mozdev.org/files/mozplugger-$pkgver.tar.gz")
sha256sums=('a6b7ee2bce82cc609ef0a637cdd2129d2155bb141f7136a734edc1fd0c2ec9c6')

build() {
  cd "mozplugger-$pkgver"

  ./configure --prefix="$pkgdir/usr" --exec-prefix="$pkgdir/usr/bin" \
    --enable-always-xembed --sysconfdir="/etc"
  make && gzip mozplugger.7
}

package() {
  cd "mozplugger-$pkgver"

  install -Dm644 mozplugger.7.gz "$pkgdir/usr/share/man/man7/mozplugger.7.gz"
  install -Dm644 mozpluggerrc "$pkgdir/etc/mozpluggerrc"
  install -Dm644 mozplugger.so "$pkgdir/usr/lib/mozilla/plugins/mozplugger.so"
  for f in controller helper linker update; do
    install -Dm755 "mozplugger-$f" "$pkgdir/usr/bin/mozplugger-$f"
  done
}

# vim:set ts=2 sw=2 et:
