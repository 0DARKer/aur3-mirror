# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Giuseppe Borzi <gborzi@ieee.org>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Todd Musall <tmusall@comcast.net>
pkgname=mozplugger-chromium
pkgver=1.14.4
pkgrel=1
pkgdesc="Mozilla multimedia plugin for Chromium"
arch=('x86_64' 'i686')
url="http://mozplugger.mozdev.org/"
license=('GPL')
depends=('libx11')
backup=('etc/mozpluggerrc')
conflicts=('mozplugger')
source=("http://mozplugger.mozdev.org/files/mozplugger-$pkgver.tar.gz")
sha256sums=('8343c4b107a18b05ef0bb789ab72e0276c979395fb56148fb531fa5c2319745a')

build() {
  cd "mozplugger-$pkgver"

  msg2 "Configuring..."
  ./configure --prefix="$pkgdir/usr" --exec-prefix="$pkgdir/usr/bin" \
    --enable-always-xembed --sysconfdir="/etc"
  msg2 "Building..."
  make
  gzip mozplugger.7
}

package() {
  cd "mozplugger-$pkgver"

  msg2 "Packaging..."
  install -Dm644 mozplugger.7.gz "$pkgdir/usr/share/man/man7/mozplugger.7.gz"
  install -Dm644 mozpluggerrc "$pkgdir/etc/mozpluggerrc"
  install -Dm644 mozplugger.so "$pkgdir/usr/lib/mozilla/plugins/mozplugger.so"
  install -Dm755 mozplugger-controller "$pkgdir/usr/bin/mozplugger-controller"
  install -Dm755 mozplugger-helper "$pkgdir/usr/bin/mozplugger-helper"
  install -Dm755 mozplugger-linker "$pkgdir/usr/bin/mozplugger-linker"
}

# vim:set ts=2 sw=2 et:
