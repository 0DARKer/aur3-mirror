# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Giuseppe Borzi <gborzi@ieee.org>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=mozplugger-chromium
pkgver=2.1.4
pkgrel=1
pkgdesc='Mozilla multimedia plugin for Chromium'
arch=('x86_64' 'i686')
url='http://mozplugger.mozdev.org/'
license=('GPL')
depends=('libx11')
backup=('etc/mozpluggerrc')
conflicts=('mozplugger')
source=("http://mozplugger.mozdev.org/files/mozplugger-$pkgver.tar.gz")
sha256sums=('0aed537ea826bfdd28a5b14e0c45a772df836a939152e13d52e10a1ceb2ce062')

prepare() {
  cd "mozplugger-$pkgver"

  gzip mozplugger.7
}

build() {
  cd "mozplugger-$pkgver"

  ./configure \
    --prefix="$pkgdir/usr" \
    --exec-prefix="$pkgdir/usr/bin" \
    --sysconfdir="/etc" \
    --enable-always-xembed
  make
}

package() {
  cd "mozplugger-$pkgver"

  install -Dm644 mozplugger.7.gz "$pkgdir/usr/share/man/man7/mozplugger.7.gz"
  install -Dm644 mozpluggerrc "$pkgdir/etc/mozpluggerrc"
  install -Dm644 mozplugger.so "$pkgdir/usr/lib/mozilla/plugins/mozplugger.so"
  for f in controller helper linker update; do
    install -Dm755 "mozplugger-$f" "$pkgdir/usr/bin/mozplugger-$f"
  done
  mkdir -p "$pkgdir/usr/lib/firefox/plugins"
  ln -s /usr/lib/mozilla/plugins/mozplugger.so \
    "$pkgdir/usr/lib/firefox/plugins/mozplugger.so"
}

# vim:set ts=2 sw=2 et:
