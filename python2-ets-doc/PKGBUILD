# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python2-ets-doc
pkgver=4.2.0
_githubtag=f895bee
pkgrel=1
pkgdesc="Documentation for ETS"
arch=('any')
url="https://github.com/enthought/ets"
license=('BSD')
depends=('python2-apptools-doc' 'python2-blockcanvas-doc' 'python2-chaco-doc'
         'python2-codetools-doc' 'python2-enable-doc' 'python2-envisage-doc'
         'python2-etsdevtools-doc' 'python2-mayavi-doc' 'python2-pyface-doc'
         'python2-scimath-doc' 'python2-traits-doc' 'python2-traitsui-doc')
makedepends=('python2-sphinx')
conflicts=('python2-ets-doc-git')
options=(!emptydirs)

source=("$pkgname.tar.gz::https://github.com/enthought/ets/tarball/${pkgver}")
md5sums=('4636d086a1bf1acfabc0556fa2e16b46')

build() {
  cd "$srcdir/enthought-ets-${_githubtag}/docs"
  make SPHINXBUILD=sphinx-build2 html
}

package() {
  cd "$srcdir/enthought-ets-${_githubtag}/docs"
  install -d "${pkgdir}"/usr/share/doc/enthought/ets
  cp -r build/html/* "${pkgdir}"/usr/share/doc/enthought/ets

  install -D "../LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

