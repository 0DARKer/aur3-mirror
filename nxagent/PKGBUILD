# Contributor: Markus Opitz <mastero23 at gmail dot com>
# Maintainer: Milan Knížek <knizek@volny.cz>

pkgname=nxagent
pkgver=3.5.0.9
pkgrel=2
pkgdesc="X proxy server for x2go and NX"
arch=(i686 x86_64)
url="http://www.x2go.org/"
license=('GPL2')
depends=("nxcomp>=${pkgver}" "nxcompshad>=${pkgver}" "nxcompext>=${pkgver}" 'gcc-libs')
conflicts=('x2goagent-git')
groups=('x2go' 'alts')
source=('nxagent.keyboard'
        'rgb'
        'nxcomp_Png.cpp.patch'
        "http://code.x2go.org/releases/source/nx-libs/nx-libs_${pkgver}-full.tar.gz")
md5sums=('31a82daa53bd20a317bcfabf16fdf755'
         '09ee098b83d94c7c046d6b55ebe84ae1'
         '4271a8a665b13a16d5999f743e147dd6'
         'be272307cf477f756aef702a6d4ed2ae')

build() {
  cd "$srcdir/nx-libs_${pkgver}"
  sed --in-place=orig \
    's!test -d nxproxy \&\& cd nxproxy \&\& autoconf \&\& ./configure \&\& make!# removed line here!' \
    Makefile
  patch -p1 < ../nxcomp_Png.cpp.patch
  make || return 1
}

package() {
  cd "$srcdir/nx-libs_${pkgver}"

  install -m 755 -D "nx-X11/programs/Xserver/nxagent" "${pkgdir}/usr/lib/nx/nxagent"

  install -m 755 -D "bin/nxagent" "${pkgdir}/usr/bin/nxagent"

  install -m 755 -d "${pkgdir}/usr/share/nx"
  install -m 644 "${srcdir}/rgb" "${pkgdir}/usr/share/nx/rgb"

  install -m 755 -d "${pkgdir}/etc/nxagent"
  install -m 644 "${srcdir}/nxagent.keyboard" "${pkgdir}/etc/nxagent/nxagent.keyboard"

  install -m 755 -d "${pkgdir}/usr/share/pixmaps"
  install -m 644 "nx-X11/programs/Xserver/hw/nxagent/nxagent.xpm" "${pkgdir}/usr/share/pixmaps/nxagent.xpm"

  install -m 755 -d "${pkgdir}/usr/share/doc/nxagent"
  install -m 644 "nx-X11/programs/Xserver/hw/nxagent/CHANGELOG" "${pkgdir}/usr/share/doc/nxagent/changelog.DEBIAN"
}

