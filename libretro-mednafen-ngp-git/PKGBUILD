# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-mednafen-ngp-git
pkgver=540.21fb7e5
pkgrel=1
pkgdesc="libretro implementation of Mednafen/Beetle Neopop. (Neo Geo Pocket/Neo Geo Pocket Color)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/beetle-ngp-libretro"
license=('GPL')
makedepends=('git')

_gitname=beetle-ngp-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/mednafen_ngp_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm644 "${_gitname}/mednafen_ngp_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-mednafen-ngp.so"
  install -Dm644 "mednafen_ngp_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-mednafen-ngp.info"
}
