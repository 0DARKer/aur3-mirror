# Maintainer: ZekeSulastin <zekesulastin@gmail.com>
# Contributor: Mr_Robotic_Evil <mr.robotic.evil@googlemail.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>

# This PKGBUILD only generates the engine binary.
# The retail Freespace 2 data is required to play the game.
# See http://www.hard-light.net/forums/index.php?topic=79545.0
# or http://www.hard-light.net/wiki/index.php/Fs2_open_on_Linux/Extracting_data_from_CD
# This data should be present and writable in ~/.fs2_open

pkgname=fs2_open
pkgver=3.6.12
_pkgver=3_6_12 # Yeah, it's hacky, but it makes the AUR page etc. look better
pkgrel=3
pkgdesc="An enhancement of the Freespace 2 engine"
url="http://scp.indiegames.us"
arch=('i686' 'x86_64')
license=('custom:freespace2')
depends=('libpng' 'libtheora' 'libvorbis' 'mesa' 'openal' 'sdl')
makedepends=('lua')
conflicts=('fs2_open-svn')
install=fs2_open.install
source=(http://sectorgame.com/f2s/uploads/files/fs2_open_3_6_12_src.tgz
		'pngutils.patch'
		'osapi_unix.patch')
md5sums=('afcbcc42de56db0e5942e4551e5865e9'
		'16b3571e54a604c24c726eb610e3a0f9'
		'783d5ab68a0ce4d26ee415e8fefbc762')

build()
{
	cd $srcdir/${pkgname}_${_pkgver}_src
  
	# Fixes pngutils for libpng 1.5
	patch -Np0 -i $srcdir/pngutils.patch

	# Changes default video settings for better mod compatability
	patch -Np0 -i $srcdir/osapi_unix.patch

	LDFLAGS+=' -ldl ' ./autogen.sh --enable-speech --enable-inferno
	make
}

package()
{
	cd $srcdir/${pkgname}_${_pkgver}_src

	install -D -m644 COPYING $pkgdir/usr/share/licenses/fs2_open/LICENSE
	install -D -m755 code/fs2_open_INF_r $pkgdir/usr/bin/fs2_open_r
	# Because one would expect to see fs2_open as the command but I don't
	# want to break preexisting scripts:

	ln -sf /usr/bin/fs2_open_r $pkgdir/usr/bin/fs2_open
}
