# Contributor: <grawity@gmail.com>
pkgname=netkit-bsd-finger-ipv6
pkgver=0.17+deb15
pkgrel=1
pkgdesc="bsd-finger ported to Linux, with IPv6 and other Debian patches"
arch=('i686' 'x86_64')
url="ftp://ftp.linux.org.uk/pub/linux/Networking/netkit/"
license=('BSD')
depends=('glibc')
provides=('netkit-bsd-finger')
conflicts=('netkit-bsd-finger')
backup=('etc/xinetd.d/finger')
source=("http://ftp.de.debian.org/debian/pool/main/b/bsd-finger/bsd-finger_${pkgver%+*}.orig.tar.gz"
        "http://ftp.de.debian.org/debian/pool/main/b/bsd-finger/bsd-finger_${pkgver/+deb/-}.debian.tar.bz2"
        "finger.xinetd"
        "fingerd.socket"
        "fingerd@.service"
        "LICENSE"
        "fxputc-utf8.patch")
sha1sums=('cc0ab33494c618cf6f75d3e91c744bd36628cfe5'
          '3b41418e94c06711a5fdb7b0702539c211f91b8b'
          '578fe7d6ea7b6435e127739d44ac34eddd9137b9'
          '5298c78c989b01975659ad6545b5f51a3ac6b6e5'
          'cf4dbc06beee5ea4f5a5d3e029199dec7a4472bc'
          '14beef9b763e7c82e5b585a3f5f8b9eade3d011a'
          '52c389d41bc855b1d65b466e66a596bff7d8813a')

prepare() {
  cd "bsd-finger-${pkgver%+*}"

  while read -r file; do
     patch -p1 < "../debian/patches/$file"
  done < "../debian/patches/series"

  patch -p1 < "../fxputc-utf8.patch"

  sed -i 's@include <sys/time.h>@include <time.h>@' finger/lprint.c
  sed -i 's@include <sys/time.h>@include <time.h>@' finger/sprint.c
}

build() {
  cd "bsd-finger-${pkgver%+*}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "bsd-finger-${pkgver%+*}"

  install -d "$pkgdir"/usr/{bin,share/man/{man1,man8}}
  make INSTALLROOT="$pkgdir" SBINDIR=/usr/bin MANDIR=/usr/share/man install

  cd "$srcdir"
  install -Dm0644 LICENSE          "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 fingerd.socket   "$pkgdir/usr/lib/systemd/system/fingerd.socket"
  install -Dm0644 fingerd@.service "$pkgdir/usr/lib/systemd/system/fingerd@.service"
  install -Dm0644 finger.xinetd    "$pkgdir/etc/xinetd.d/finger"
}

# vim: ts=2:sw=2:et:ft=sh
