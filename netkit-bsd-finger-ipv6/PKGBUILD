# Contributor: <grawity@gmail.com>
pkgname=netkit-bsd-finger-ipv6
pkgver=0.17
pkgrel=8
pkgdesc="bsd-finger ported to Linux, with IPv6 and other Debian patches"
arch=('i686' 'x86_64')
url="ftp://ftp.uk.linux.org/pub/linux/Networking/netkit"
license=('BSD')
depends=('glibc')
provides=('netkit-bsd-finger')
conflicts=('netkit-bsd-finger')
backup=('etc/xinetd.d/finger')
source=(
	"ftp://ftp.uk.linux.org/pub/linux/Networking/netkit/bsd-finger-$pkgver.tar.gz"
	"http://ftp.de.debian.org/debian/pool/main/b/bsd-finger/bsd-finger_0.17-14.debian.tar.bz2"
	"finger.xinetd"
	"systemd-fingerd.socket"
	"systemd-fingerd@.service"
	"LICENSE")
md5sums=('52bf281aac8814bf56cdc92f7661ee75'
         '21bf0e0d52d7e186cdc8edceafab99f0'
         '5929b923276d38139aeb3305116980ed'
         'ac34ebab3a412eca1bf5a93b88249b8c'
         '823f0ea1c43deeb34e786fdf63db8368'
         '5fc568418a0379fc3a358ed1c2523b44')

build() {
	cd "$srcdir/bsd-finger-$pkgver"

	while read -r file; do
		 patch -p1 < "../debian/patches/$file"
	done < "../debian/patches/series"

	sed -i 's@include <sys/time.h>@include <time.h>@' finger/lprint.c
	sed -i 's@include <sys/time.h>@include <time.h>@' finger/sprint.c

	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/bsd-finger-$pkgver"
	install -d "$pkgdir"/usr/{bin,sbin} "$pkgdir"/usr/share/man/{man1,man8}
	make INSTALLROOT="$pkgdir" MANDIR=/usr/share/man install

	install -D -m644 "$srcdir/finger.xinetd" "$pkgdir/etc/xinetd.d/finger"
	install -D -m644 "$srcdir/systemd-fingerd.socket" \
		"$pkgdir/lib/systemd/system/fingerd.socket"
	install -D -m644 "$srcdir/systemd-fingerd@.service" \
		"$pkgdir/lib/systemd/system/fingerd@.service"

	install -D -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
