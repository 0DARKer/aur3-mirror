# Contributor: <grawity@gmail.com>
pkgname=netkit-bsd-finger-ipv6
pkgver=0.17
pkgrel=11
pkgdesc="bsd-finger ported to Linux, with IPv6 and other Debian patches"
arch=('i686' 'x86_64')
url="ftp://ftp.uk.linux.org/pub/linux/Networking/netkit"
license=('BSD')
depends=('glibc')
provides=('netkit-bsd-finger')
conflicts=('netkit-bsd-finger')
backup=('etc/xinetd.d/finger')
source=("http://ftp.de.debian.org/debian/pool/main/b/bsd-finger/bsd-finger_0.17.orig.tar.gz"
        "http://ftp.de.debian.org/debian/pool/main/b/bsd-finger/bsd-finger_0.17-14.debian.tar.bz2"
        "finger.xinetd"
        "fingerd.socket"
        "fingerd@.service"
        "LICENSE")
sha1sums=('cc0ab33494c618cf6f75d3e91c744bd36628cfe5'
          '50b248bee925e80b9ce49c320709c79255bcc471'
          '578fe7d6ea7b6435e127739d44ac34eddd9137b9'
          '5298c78c989b01975659ad6545b5f51a3ac6b6e5'
          'cf4dbc06beee5ea4f5a5d3e029199dec7a4472bc'
          '14beef9b763e7c82e5b585a3f5f8b9eade3d011a')

build() {
  cd "$srcdir/bsd-finger-$pkgver"

  while read -r file; do
     patch -p1 < "../debian/patches/$file"
  done < "../debian/patches/series"

  sed -i 's@include <sys/time.h>@include <time.h>@' finger/lprint.c
  sed -i 's@include <sys/time.h>@include <time.h>@' finger/sprint.c

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/bsd-finger-$pkgver"

  install -d "$pkgdir"/usr/{bin,share/man/{man1,man8}}
  make INSTALLROOT="$pkgdir" SBINDIR=/usr/bin MANDIR=/usr/share/man install

  cd "$srcdir"
  install -Dm0644 LICENSE          "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 fingerd.socket   "$pkgdir/usr/lib/systemd/system/fingerd.socket"
  install -Dm0644 fingerd@.service "$pkgdir/usr/lib/systemd/system/fingerd@.service"
  install -Dm0644 finger.xinetd    "$pkgdir/etc/xinetd.d/finger"
}

# vim: ts=2:sw=2:et:ft=sh
