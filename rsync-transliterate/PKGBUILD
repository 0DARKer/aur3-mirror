# Contributor: John Karahalis <John.Karahalis@rit.edu>
# 
# Based on rsync r77348 by:
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
#
# transliterate.diff:
# Matt McCutchen

basepkgname=rsync
pkgname=${basepkgname}-transliterate
pkgver=3.0.7
pkgrel=2
pkgdesc="A file transfer program to keep remote files in sync, with filename transliteration support"
arch=('i686' 'x86_64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=('acl')
provides=("rsync=$pkgver")
conflicts=('rsync')
backup=('etc/rsyncd.conf' 'etc/xinetd.d/rsync')
source=(http://rsync.samba.org/ftp/rsync/${basepkgname}-${pkgver}.tar.gz \
        rsyncd.conf rsyncd rsync.xinetd transliterate.diff::"http://gitweb.samba.org/?p=rsync-patches.git;a=blob_plain;f=transliterate.diff;h=60363dbd8ce28a0adb38f270c9132d95a93deef9;hb=abe6c1c4ac97fe50bb70cca2415131c74446f102")
md5sums=('b53525900817cf1ba7ad3a516ab5bfe9'
         '4395c0591638349b1a7aeaaa4da1f03a'
         '3d8f90ac8467ff6af28754584a61fd11'
         'ea3e9277dc908bc51f9eddc0f6b935c1'
         '8467e3ee7d57ef64c82c129134531b2c')
sha1sums=('63426a1bc71991d93159cd522521fbacdafb7a61'
          '48be09294134dfed888818872fe552a59c29147a'
          '357af5648eb4ecf84cc6b1de1cd79d21eca3a518'
          'fdb99785bc87ee13d77aa90dc1804f3f75dd7fc1'
          '0e828d4ff64c70acd9fe797e3f36925c16a3f830')

build() {
	cd ${srcdir}/${basepkgname}-${pkgver}
	./prepare-source 

	# Apply the transliteration patch.
	patch -p1 < ${srcdir}/transliterate.diff

	./configure --prefix=/usr --with-included-popt \
              --enable-acl-support --enable-xattr-support 
	make 
}

package() {
	cd ${srcdir}/${basepkgname}-${pkgver}
	make DESTDIR=${pkgdir} install 
	install -Dm755 ../rsyncd ${pkgdir}/etc/rc.d/rsyncd 
	install -Dm644 ../rsyncd.conf ${pkgdir}/etc/rsyncd.conf 
	install -Dm644 ../rsync.xinetd ${pkgdir}/etc/xinetd.d/rsync 
}

