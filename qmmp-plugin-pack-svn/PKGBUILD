# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Andrew Panchenko <panchenkoac at gmail>

pkgname=qmmp-plugin-pack-svn
pkgver=0.9.0.svn.r4651
pkgrel=1
pkgdesc="Qmmp Plugin Pack. (SVN Version)"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com"
license=('GPL')
depends=("qmmp-svn>=${pkgver}")
makedepends=('subversion' 'cmake' 'yasm' 'mpg123')
optdepends=('mpg123: alternative Mpeg-1-2-3 support')
provides=('qmmp-plugin-pack')
conflicts=('qmmp-plugin-pack')
source=('svn+http://qmmp.googlecode.com/svn/trunk/qmmp-plugin-pack')
_svnname='qmmp-plugin-pack'
md5sums=('SKIP')

pkgver() {
  cd "${_svnname}"
  echo "$(cat qmmp-plugin-pack.pri | grep QMMP_PLUGIN_PACK_VERSION | cut -d ' ' -f3).svn.r$(svnversion)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake "../${_svnname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DIR=lib \
    -DUSE_FFAP=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
