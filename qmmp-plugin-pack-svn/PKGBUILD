# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Andrew Panchenko <panchenkoac at gmail>

pkgname=qmmp-plugin-pack-svn
pkgver=0.8.0.svn.r3972
pkgrel=1
pkgdesc="Qmmp Plugin Pack. (SVN Version)"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com"
license=('GPL')
depends=("qmmp-svn>=$pkgver" 'taglib')
makedepends=('subversion' 'cmake' 'yasm')
provides=('qmmp-plugin-pack')
conflicts=('qmmp-plugin-pack')
source=('svn+http://qmmp.googlecode.com/svn/trunk/qmmp-plugin-pack')
_svnname='qmmp-plugin-pack'
md5sums=('SKIP')

pkgver() {
  cd "${_svnname}"
  echo "$(cat qmmp-plugin-pack.pri | grep QMMP_PLUGIN_PACK_VERSION | cut -d ' ' -f3).svn.r$(svnversion)"
}

build() {
  cd "${_svnname}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DLIB_DIR=lib -DCMAKE_BUILD_TYPE=RELEASE -DUSE_FFAP:BOOL=TRUE
  make
}

package() {
  cd "${_svnname}"
  make DESTDIR="${pkgdir}" install
}
