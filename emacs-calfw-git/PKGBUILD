# Maintainer: XeCycle <xecycle@gmail.com>

pkgname=emacs-calfw-git
pkgver=20111209
pkgrel=1
pkgdesc="A calendar framework for Emacs"
url="https://github.com/kiwanami/emacs-calfw"
arch=('any')
license=('GPL')
depends=('emacs')
makedepends=('perl-text-markdown') # remove if don't want html doc
install=emacs-calfw-git.install
source=()
md5sums=()

_gitroot=git://github.com/kiwanami/emacs-calfw.git
_gitname=emacs-calfw

build() {
  cd $srcdir
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  for file in calfw{,-cal,-ical,-org}.el; do
    emacs -Q --batch -l calfw.el -f batch-byte-compile $file
  done

  # html doc
  Markdown.pl readme.md > readme.html
}

package() {
  cd $srcdir/$_gitname-build
  el_dir=/usr/share/emacs/site-lisp/emacs-calfw
  doc_dir=/usr/share/doc/emacs-calfw
  install -m 755 -d $pkgdir{$el,$doc}_dir
  install -m 644 -t $pkgdir$el_dir *.el{,c}
  install -m 644 -t $pkgdir$doc_dir readme.*
}
