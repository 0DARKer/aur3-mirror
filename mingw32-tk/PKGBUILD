# Maintainer: Andreas Krinke <andreas dot krinke at gmx dot de>

pkgname=mingw32-tk
pkgver=8.5.10
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl (mingw32)"
arch=('any')
url="http://tcl.sourceforge.net/"
license=('custom')
makedepends=('mingw32-gcc')
depends=('mingw32-runtime' "mingw32-tcl=${pkgver}")
options=('!strip' '!buildflags')
source=("http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz")
md5sums=('adfdaca46cc95fec8557df8ecdb0715c')

build() {
  cd "${srcdir}/tk${pkgver}/win"

  # mingw32 have problems with --hash-style=gnu (default value)
  unset LDFLAGS

  ./configure --host=i486-mingw32 --prefix=/usr/i486-mingw32 --with-tcl=/usr/i486-mingw32/lib --enable-threads
  
  make
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf wish85.exe ${pkgdir}/usr/i486-mingw32/bin/wish.exe
  
  # install private headers (FS#14388)
  cd "${srcdir}/tk${pkgver}"
  for dir in compat generic generic/ttk win; do
    install -dm755 ${pkgdir}/usr/i486-mingw32/include/tk-private/$dir
    install -m644 -t ${pkgdir}/usr/i486-mingw32/include/tk-private/$dir $dir/*.h
  done

  # install license
  install -Dm644 license.terms ${pkgdir}/usr/i486-mingw32/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces / fixes #3602
  sed -i \
  -e "s,^TK_BUILD_LIB_SPEC='-L.*/win,TK_BUILD_LIB_SPEC='-L/usr/i486-mingw32/lib," \
  -e "s,^TK_SRC_DIR='.*',TK_SRC_DIR='/usr/i486-mingw32/include'," \
  -e "s,^TK_BUILD_STUB_LIB_SPEC='-L.*/win,TK_BUILD_STUB_LIB_SPEC='-L/usr/i486-mingw32/lib," \
  -e "s,^TK_BUILD_STUB_LIB_PATH='.*/win,TK_BUILD_STUB_LIB_PATH='/usr/i486-mingw32/lib," \
  "${pkgdir}/usr/i486-mingw32/lib/tkConfig.sh"
}
