# Maintainer: Devin J. Pohly <djpohly+arch@gmail.com>
pkgname=teletype-svn
pkgver=461
pkgrel=1
pkgdesc="Serial emulator based on tinyserial"
arch=('i686' 'x86_64')
url="http://www.clifford.at/tools.html"
license=('custom')
depends=('readline')
makedepends=('subversion')
source=(termcap.patch)
md5sums=('0b19304edfdbf138ac24162e77182687')

_svntrunk=http://svn.clifford.at/tools/trunk
_svnmod=teletype

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD HERE
  #
  patch -Np0 -i "$srcdir/termcap.patch"
  make teletype
}

package() {
  cd "$srcdir/$_svnmod-build"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  sed '/-----/,/-----/d;/\*\//q' teletype.c > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -D teletype "$pkgdir/usr/bin/teletype"
}

# vim:set ts=2 sw=2 et:
