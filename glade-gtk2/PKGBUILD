# Maintainer: Fabien COUTANT
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade-gtk2
pkgver=3.8.1
pkgrel=1
pkgdesc="User interface builder for GTK+ and GNOME. -- latest version for GTK2"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gtk2>=2.24' 'libxml2' 'desktop-file-utils' 'hicolor-icon-theme')
#makedepends=('intltool' 'python2' 'gtk-doc' 'gobject-introspection' 'python2-gobject2')
makedepends=('intltool' 'python2' 'gobject-introspection' 'python2-gobject2')
optdepends=('python2: Python widgets support')
provides=( glade )
conflicts=( glade )
options=('!libtool')
url="http://glade.gnome.org/"
install=glade.install
source=( http://ftp.gnome.org/pub/GNOME/sources/glade3/${pkgver%.*}/glade3-$pkgver.tar.bz2 )
sha256sums=('8844b66c3236afbc3cf768c1856d85f1da00a4c0dd6009bca58fa832e894c2cc')

build() {
  cd "${srcdir}/glade3-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --disable-dependency-tracking
  make V=1
}

package() {
  cd "${srcdir}/glade3-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "$pkgdir"/usr/share/glade3/catalogs/*.in
}
