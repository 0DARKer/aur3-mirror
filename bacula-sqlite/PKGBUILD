# Contributor: Calogero Lo Leggio <kalos@autistici.org>

pkgname=bacula-sqlite
pkgver=2.2.8
pkgrel=3
pkgdesc="Bacula, the Network Backup Tool for Linux, Unix, Mac and Windows. [sqlite3 DB]"
arch=(i686 x86_64)
url="http://www.bacula.org"
license=('GPL')
depends=('sqlite3')
makedepends=('sqlite3')
conflicts=('bacula' 'bacula-postgresql')
backup=(etc/bacula/bacula-{dir,fd,sd}.conf etc/bacula/bconsole.conf)
install="bacula.install"
source=("http://downloads.sourceforge.net/sourceforge/bacula/bacula-$pkgver.tar.gz"
        "bacula-dir" "bacula-fd" "bacula-sd")
md5sums=('4547d0bf9f976d5d9ea5f21a7bd078ee'
         '384cdc720ba2c46dc942caced4f6a0d7'
         '2b4895573289e6f71582e75fb1812a52'
         '9bf6e300e81b1f23111dba1c85bc28b8')

build() {
  cd "$startdir/src/bacula-$pkgver"

  ./configure --prefix=/usr \
    --enable-build-dird --enable-build-stored \
    --with-sqlite3 --with-tcp-wrappers --with-openssl \
    --with-dir-user=bacula-dir --with-dir-group=bacula-dir \
    --with-sd-user=bacula-sd --with-sd-group=bacula-sd \
    --with-fd-user=bacula-fd --with-fd-group=bacula-fd \
    --sysconfdir=/etc/bacula --with-scriptdir=/etc/bacula/scripts \
    --with-archivedir=/usr/var/bacula/archive

  make || return 1
  make DESTDIR="$startdir/pkg" install || return 1

  # install the init script
  install -D -m755 $startdir/src/bacula-dir $startdir/pkg/etc/rc.d/bacula-dir
  install -D -m755 $startdir/src/bacula-fd $startdir/pkg/etc/rc.d/bacula-fd
  install -D -m755 $startdir/src/bacula-sd $startdir/pkg/etc/rc.d/bacula-sd
}
