# Maintainer: cars10t <carsten.1 gmx.net>
# Contributor: grimi <grimi at poczta dot fm>

pkgname=xrick
pkgver=021212
pkgrel=9
pkgdesc='A clone of Rick Dangerous'
arch=('i686' 'x86_64')
url='http://www.bigorno.net/xrick'
install="${pkgname}.install"
license=('custom: spirit of GPL')
depends=('sdl' 'zlib')
makedepends=('imagemagick')
source=("http://www.bigorno.net/xrick/${pkgname}-${pkgver}.zip"
	"${pkgname}.desktop")
md5sums=('6a56d23679f51c602b285642c6451fb6'
         '4b7d95acef1c6c14527d7d3a7f702ebc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix data path
  sed -i 's|"data.zip"|"/usr/share/xrick/data.zip"|' src/xrick.c

  # add our build flags
  sed -i "s|cflags)|cflags) ${CXXFLAGS}|" Makefile
  
  make

  # create icon
  convert src/xrickST.ico xrick.png

  # fix data path in manual page
  gunzip xrick.6.gz
  sed -i 's|where xrick is|/usr/share/xrick.|' xrick.6
  sed -i 's|run from.||' xrick.6
  gzip xrick.6
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # install binary, icon, data, manual and desktop files
  install -d "${pkgdir}"/usr/{bin,man/man6,share/{xrick,applications,pixmaps}}
  install -m755 xrick "${pkgdir}/usr/bin/xrick"
  install -m644 {data.zip,KeyCodes} "${pkgdir}/usr/share/xrick/"
  install -m644 xrick.6.gz "${pkgdir}/usr/man/man6/"
  install -m644 "${srcdir}/xrick.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 xrick.png "${pkgdir}/usr/share/pixmaps/"
}
