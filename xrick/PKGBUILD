# contributor: grimi <grimi at poczta dot fm>

pkgname=xrick
pkgver=021212
pkgrel=8
pkgdesc='xrick is a clone of Rick Dangerous'
arch=('i686' 'x86_64')
url="http://www.bigorno.net/xrick"
license=('custom:spirit of GPL')
depends=('sdl' 'zlib')
makedepends=('imagemagick' 'unzip')
source=("http://www.bigorno.net/xrick/$pkgname-$pkgver.zip" \
        "xrick.sh" "xrick.desktop")
md5sums=('6a56d23679f51c602b285642c6451fb6'
         'ea14a1ed277c361a77152272d49fcff6'
         '6398136341dbc102f4c16574e9bc7460')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make
  convert src/xrickST.ico xrick.png
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  install -d "$pkgdir"/usr/{bin,man/man6,share/{xrick,applications,pixmaps}}
  install -m755 ../xrick.sh "$pkgdir"/usr/bin/xrick
  install -m755 xrick "$pkgdir"/usr/share/xrick/
  install -m644 {data.zip,KeyCodes} "$pkgdir"/usr/share/xrick/
  install -m644 xrick.6.gz "$pkgdir"/usr/man/man6/
  install -m644 ../xrick.desktop "$pkgdir"/usr/share/applications/
  install -m644 xrick.png "$pkgdir"/usr/share/pixmaps/
}

