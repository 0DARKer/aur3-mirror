# Maintainer: Kazuo Teramoto <kaz.rag at gmail dot com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: bender02 at gmx dot com

pkgname=asymptote-svn
pkgver=5383
pkgrel=1
pkgdesc="A vector graphics language (like metapost)"
arch=('i686' 'x86_64')
url="http://asymptote.sourceforge.net/"
license=("GPL3")
depends=('texlive-core' 'gc' 'freeglut' 'gsl' 'fftw')
makedepends=('ghostscript' 'imagemagick' 'subversion')
optdepends=('python2:           for the xasy GUI'
            'python-imaging:    for the xasy GUI'
            'tix:               for the xasy GUI')
conflicts=('asymptote')
provides=('asymptote')
install=texlive.install
source=('asy_filetype.vim')
md5sums=('f96e7d51b13df93399c8c97e347dac6c')
sha256sums=('7ff198b759d3e6f3eb81bbc1d2b7ae81d2e1d6f87f3d621916211bd54b0eeabb')

_svntrunk=http://asymptote.svn.sourceforge.net/svnroot/asymptote/trunk/asymptote
_svnmod=asymptote

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ./autogen.sh
  ./configure --enable-gc=/usr \
              --prefix=/usr \
              --with-latex=/usr/share/texmf/tex/latex \
              --with-context=/usr/share/texmf/tex/context
  make all
}

check() {
  cd "$srcdir/$_svnmod-build"
  make check-all
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="${pkgdir}" install-all
  sed -i -e 's@env python@env python2@' ${pkgdir}/usr/share/asymptote/GUI/*.py
  # this dir contains png files that are already embedded in the pdf documentation:
  rm -rf ${pkgdir}/usr/share/info/asymptote

  # move vim files to correct place
  install -dm755 ${pkgdir}/usr/share/vim/vimfiles/syntax/
  mv ${pkgdir}/usr/share/asymptote/asy.vim \
    ${pkgdir}/usr/share/vim/vimfiles/syntax/asy.vim

  install -Dm644 ${srcdir}/asy_filetype.vim \
    ${pkgdir}/usr/share/vim/vimfiles/ftdetect/asy.vim
}
