# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=xfce4-taskmanager
pkgname=$_pkgname-git
pkgver=20121121
pkgrel=1
pkgdesc="Small taskmanager for the Xfce desktop"
arch=(i686 x86_64)
url=http://goodies.xfce.org/projects/applications/xfce4-taskmanager
license=(GPL2)
groups=(xfce4-git)
depends=(libwnck libgksu)
makedepends=(git xfce4-dev-tools)
conflicts=($_pkgname{,-svn})
provides=($_pkgname=1.0.1)

_gitroot=git://git.xfce.org/apps/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh  \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/xfce4 \
        --localstatedir=/var \
        --disable-static
    echo -e "install:\n\t/bin/true" >> doc/Makefile
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install

    cd doc/
    for i in index.*; do
        install -Dm644 $i "$pkgdir"/usr/share/doc/$pkgname/$i
    done
}
