# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: Raul Castillo <raulmuaddib@gmail.com>

_pkgname=xfce4-taskmanager
pkgname=$_pkgname-git
pkgver=20120915
pkgrel=1
pkgdesc="Small taskmanager for the Xfce desktop"
arch=(i686 x86_64)
url=http://www.xfce.org/
license=(GPL2)
groups=(xfce4-git)
depends=(libwnck)
makedepends=(git xfce4-dev-tools)
conflicts=($_pkgname $_pkgname-svn)
provides=($_pkgname)

_gitroot=http://git.xfce.org/apps/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"

    msg "Connecting to GIT server..."
    if [[ -d $_gitname/.git ]] ; then
        pushd $_gitname && git pull origin
        msg "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh  --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/xfce4 --localstatedir=/var --disable-static
    echo -e "install:\n\t/bin/true" >> doc/Makefile
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install

    cd doc/
    for i in index.*; do
        install -Dm644 $i "$pkgdir"/usr/share/doc/$pkgname/$i
    done
}
