# Maintainer : RaphaÃ«l Doursenaud <rdoursenaud@gpcsolutions.fr>
# Previous Maintainer : kuri <sysegv at gmail dot com>
# Contributor : Kevin C <kiven at kiven dot fr>
# Contributor: Alex 'AdUser' Z
pkgname=fusioninventory-agent
_pkgname="FusionInventory-Agent"
pkgver=2.3.10
_pkgdir=1504
pkgrel=1
pkgdesc="FusionInventory Agent is an application for keeping track of the hardware and software"
arch=(any)
url="http://fusioninventory.org"
_watch="https://github.com/fusinv/fusioninventory-agent/releases"
license=('GPL')
depends=(
  'perl>=5.8'
  'perl-file-which'
# Provides LWP
  'perl-libwww>=5.8'
  'perl-net-ip'
  'perl-socket-getaddrinfo'
  'perl-text-template'
  'perl-universal-require'
  'perl-xml-treepp>=0.26'
)
makedepends=(
# Provided by perl
  'perl-extutils-makemaker>=6.98'
  'perl-http-proxy'
  'perl-http-server-simple'
# Provides IO::Capture::Stderr
  'perl-io-captureoutput'
  'perl-io-socket-ssl'
  'perl-ipc-run'
  'perl-json'
  'perl-lwp-protocol-https'
  'perl-net-snmp'
  'perl-poe-component-client-ping'
  'perl-test-compile'
  'perl-test-deep'
  'perl-test-exception'
  'perl-test-mockmodule'
  'perl-test-mockobject'
  'perl-test-nowarnings'
# FIXME: not in AUR
#  'perl-http-server-simple-authen'
#  'perl-test-more>=0.93'
#  'perl-test-http-server-simple'
)

optdepends=(
  'perl-archive-extract'
  'perl-compress-zlib'
  'perl-crypt-des'
# Provided by perl
#  'perl-digest-md5'
#  'perl-digest-sha'
  'perl-file-copy-recursive'
  'perl-http-daemon'
  'perl-io-socket-ssl'
  'perl-json'
  'perl-lwp-protocol-https'
# Doesn't build ATM
#  'perl-net-cups'
# let's use patched version
  'perl-net-cups-cups16'
# FIXME: not in AUR
#  'perl-net-nbname'
  'perl-net-snmp'
# provided by perl-net-write
#  'perl-net-write-layer2'
  'perl-net-write'
  'perl-poe-component-client-ping'
  'perl-parse-edid'
  'perl-proc-daemon'
  'perl-proc-pid-file'
#  Provided by perl-uri
#  'perl-uri-escape'
  'perl-uri'
  'dmidecode'
  'pciutils'
  'hdparm'
)
source=("http://forge.fusioninventory.org/attachments/download/$_pkgdir/$_pkgname-$pkgver.tar.gz"
  'fusioninventory-agent.service'
  'fusioninventory-agent.config')
md5sums=('e2f71321216dd7a0603f6c4ebf037574'
   'cd0d59b266a41977f51d9e99ecca8cd5'
   '7cce12647a737aadcdd79dee4575aff3')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  perl Makefile.PL \
    PREFIX="/usr" \
    SYSCONFDIR="/etc/fusioninventory" \
    LOCALSTATEDIR="/var/lib/fusioninventory-agent"

  make
}

# FIXME :
# needs at least HTTP::Server::Simple:Authen
# and certainly Test::More and Test::HTTP::Server::Simple
#check() {
#  cd "$srcdir/$_pkgname-$pkgver"
#
#  make test
#}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make install DESTDIR="$pkgdir/"
  install -D -m644 ${srcdir}/fusioninventory-agent.service \
    ${pkgdir}/usr/lib/systemd/system/fusioninventory-agent.service
  mv ${pkgdir}/etc/fusioninventory/agent.cfg \
     ${pkgdir}/etc/fusioninventory/agent.cfg.default
  install -D -m644 ${srcdir}/fusioninventory-agent.config \
    ${pkgdir}/etc/conf.d/fusioninventory-agent
  mkdir -p "$pkgdir/var/lib/fusioninventory-agent"
}
