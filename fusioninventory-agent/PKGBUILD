# Maintainer : kuri <sysegv at gmail dot com>
# Contributor: Alex 'AdUser' Z
pkgname=fusioninventory-agent
_pkgname="FusionInventory-Agent"
pkgver=2.2.5
pkgrel=2
pkgdesc="FusionInventory Agent is an application for keeping track of the hardware and software"
arch=('i686' 'x86_64')
url="http://fusioninventory.org"
license=('GPL')
depends=('perl' 'dmidecode'
         'perl-compress-zlib' 'perl-file-which' 'perl-ipc-run'
         'perl-net-ssleay' 'perl-libwww' 'perl-xml-simple'
         'perl-xml-treepp' 'perl-yaml' 'perl-http-daemon'
         'perl-text-template' 'perl-proc-pid-file'
         'perl-universal-require' 'perl-proc-processtable'
         'perl-proc-daemon' 'perl-json' 'perl-compress-zlib'
         'perl-net-ip' 'perl-http-proxy' 'perl-http-server-simple'
         'perl-io-capture' 'perl-io-socket' 'perl-io-socket-ssl')
makedepends=('perl-test-mockmodule' 'perl-test-exception'
             'perl-extutils-makemaker')
optdepends=('hdparm' 'pciutils')
source=("http://forge.fusioninventory.org/attachments/download/706/$_pkgname-$pkgver.tar.gz"
        'fusioninventory-agent.service'
        'fusioninventory-agent')
md5sums=('73e8fd45a9f17e9ced4b94574b6c7a55'
         '1e10fc92d1d49547eb9509229f9f131e'
         '48d1b22c1bb2fcdf8a2cd9aa9846bdee')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  perl Makefile.PL \
  PREFIX="/usr" \
  SYSCONFDIR="/etc/fusioninventory" \
  LOCALSTATEDIR="/var/lib/fusioninventory-agent"

  make -s
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -s DESTDIR="$pkgdir/" install
  install -D -m644 ${srcdir}/fusioninventory-agent.service \
          ${pkgdir}/usr/lib/systemd/system/fusioninventory-agent.service
  mv ${pkgdir}/etc/fusioninventory/agent.cfg \
     ${pkgdir}/etc/fusioninventory/agent.cfg.default
  install -D -m644 ${srcdir}/fusioninventory-agent \
          ${pkgdir}/etc/conf.d/fusioninventory-agent
  mkdir -p "$pkgdir/var/lib/fusioninventory-agent"

}
