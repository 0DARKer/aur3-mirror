## Maintainer : shahid <helllamer@gmail.com>
## Long PKGBUILD description, todo, wiki:
## * https://bitbucket.org/helllamer/archlinux-pkgbuild/wiki/pkgbuild/bitcoin-git

## Wanna take part on development my PKGBUILDs? Feel welcome on my repo:
## * https://bitbucket.org/helllamer/archlinux-pkgbuild/

name=bitcoin
pkgname=$name-git
pkgver=20110619
pkgrel=1
pkgdesc="Bitcoin is a peer-to-peer network based digital currency."
arch=('i686' 'x86_64')
url="http://www.bitcoin.org/"
depends=('wxgtk-2.9' 'expat' 'gcc-libs' 'boost-libs>=1.46' 'miniupnpc' 'openssl')
makedepends=('git' 'boost' 'gcc' 'make' 'findutils')
conflicts=('bitcoin-bin' 'bitcoin')
provides=('bitcoin')
license=('MIT')
source=("${name}.desktop"
	"makefile.archlinux")
md5sums=('e7520d64cad73a1d8a6f853c4e2ee43f'
	 'bd1f749691e256c53416d2595336a557')

_gitroot='git://github.com/bitcoin/bitcoin.git'
_gitname=$name

## dirs for sources and for build
src=$srcdir/$_gitname
build=$src-build
bsrc=$build/src

build() {
	cd $srcdir
	msg "Connecting to the git server..."

	if [[ -d $src ]] ; then
		cd $_gitname
		git remote set-url origin $_gitroot
		git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot
	fi
	msg "Git checkout done"

	msg "Starting make..."
	rm -rf $srcdir/$_gitname-build
	git clone $srcdir/$_gitname $srcdir/$_gitname-build
	
	## sources ready. Preparing makefile
	local amakefile=makefile.archlinux
	cp $srcdir/$amakefile $bsrc

	cd $bsrc

	# check, if host can compile with SSE (TODO)

	# single-threaded build due to OOM issues reported
	make -j1 -f $amakefile bitcoin bitcoind
}

package() {
	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/share/pixmaps
	mkdir -p $pkgdir/usr/share/applications

	# get compiled binaries
	install -D -m755 $bsrc/bitcoin  $pkgdir/usr/bin/
	install -D -m755 $bsrc/bitcoind $pkgdir/usr/bin/

	# add icon and .desktop file to pkg
	install -D -m644 $bsrc/xpm/bitcoin48.xpm $pkgdir/usr/share/pixmaps/
	install -D -m644 $srcdir/$name.desktop $pkgdir/usr/share/applications/ 
}
