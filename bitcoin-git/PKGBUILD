## Maintainer : shahid <helllamer@gmail.com>
## Long PKGBUILD description, todo, wiki:
## * https://bitbucket.org/helllamer/archlinux-pkgbuild/wiki/pkgbuild/bitcoin-git

## Wanna take part on development my PKGBUILDs? Feel welcome on my repo:
## * https://bitbucket.org/helllamer/archlinux-pkgbuild/

name=bitcoin
pkgname=$name-git
pkgver=20111122
pkgrel=1
pkgdesc="Bitcoin is a peer-to-peer network based digital currency."
arch=('i686' 'x86_64')
url="http://www.bitcoin.org/"
depends=('qt>=4.6' 'libpng>=1.4' 'expat' 'gcc-libs' 'boost-libs>=1.46' 'miniupnpc>=1.5' 'openssl')
makedepends=('git' 'boost' 'gcc' 'make' 'automoc4')
conflicts=('bitcoin-bin' 'bitcoin' 'bitcoind')
provides=('bitcoin')
license=('MIT')
source=("${name}.desktop")
md5sums=('77139ce8a40f46bd7d7ad0a743672113')

_gitroot='git://github.com/bitcoin/bitcoin.git'
_gitname=$name

## dirs for sources and for build
src=$srcdir/$_gitname
build=$src-build
src_qt=$build
src_d=$build/src
makefile_unix=makefile.unix

build() {
	cd $srcdir
	msg "Connecting to the git server..."

	if [[ -d $src ]] ; then
		cd $_gitname
		git remote set-url origin $_gitroot
		git pull origin
		msg "The local files are updated."
	else
		git clone --depth 1 $_gitroot
	fi
	msg "Git checkout done"
	rm -rf $build
	cp -r $src $build

	msg "Starting make..."
	cd $src_d
	
	msg "Patching $makefile_unix..."
	sed -i $makefile_unix \
		-e 's/\(-DNOPCH\)/\1 -DBOOST_FILESYSTEM_VERSION=2/' \
		-e 's/-Bstatic/--as-needed ${LDFLAGS}/g' \
		-e 's/\(USE_UPNP:=\)0/\11/' \
		-e 's/$(DEBUGFLAGS)//g' \
		-e 's/CXXFLAGS/CPPFLAGS/g' \
		-e 's/-O[0-3]/${CXXFLAGS}/g'

	# make bitcoind
	make $MAKEFLAGS -f $makefile_unix bitcoind

	# and make qt gui
	cd $src_qt
	qmake
	make $MAKEFLAGS
}

package() {
	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/usr/share/pixmaps
	mkdir -p $pkgdir/usr/share/applications

	# get compiled binaries
	install -D -m755 $src_qt/bitcoin-qt $pkgdir/usr/bin/
	install -D -m755 $src_d/bitcoind $pkgdir/usr/bin/
	
	# add icon and .desktop file to pkg
	install -D -m644 $src_qt/src/qt/res/icons/bitcoin.png $pkgdir/usr/share/pixmaps/
	install -D -m644 $srcdir/$name.desktop $pkgdir/usr/share/applications/ 
}
