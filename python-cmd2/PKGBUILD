# Maintainer: Carl George < arch at cgtx.us >

_module="cmd2"
pkgname=("python-${_module}" "python2-${_module}")
pkgver="0.6.8"
pkgrel="1"
pkgdesc="Extra features for standard library's cmd module"
arch=("any")
url="https://bitbucket.org/catherinedevlin/cmd2"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('ac780d8c31fc107bf6b4edcbcea711de4ff776d59d89bb167f8819d2d83764a8')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-cmd2() {
    depends=("python-pyparsing>=2.0.1")
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-cmd2() {
    depends=("python2-pyparsing>=2.0.1")
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
