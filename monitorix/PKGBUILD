# Contributor: graysky <graysky AT archlinux dot us>
# Contributor: Jordi Sanfeliu <jordi AT fibranet dot cat>

pkgname=monitorix
pkgver=2.4.0
pkgrel=2
pkgdesc='A lightweight system monitoring tool that uses rrd databases.'
arch=('any')
license=('GPL')
url="http://www.$pkgname.org"
depends=('rrdtool' 'perl-libwww' 'perl-mime-lite' 'perl-cgi' 'perl-mailtools')
optdepends=('monitorix-sync-daemon: offload your databases to tmpfs to save i/o to your disk'
'lm_sensors: enable support for system temp monitoring'
'smartmontools: enable support for hdd bad sector monitoring.'
'nvidia: enable support for nVidia card temp and usage monitoring.'
'hddtemp: enable support for hdd temp monitoring'
'terminus-font: if your graphs do not contain letters and numbers you may need this font package')
backup=etc/$pkgname.conf
source=("http://www.$pkgname.org/$pkgname-$pkgver.tar.gz"
"rc.$pkgname")
sha256sums=('5a807444323bd3cc5f947ef77db3b774d15abbfd62edee77fdfc0d2bb5313c21'
            'b9471a8fbe808a5fac091f257585d82cd67b701a3c4ee55b7a4f9e9c8579854f')
install=readme.install
_basedir="srv/http/monitorix"
_libdir="var/lib/$pkgname"

package() {
 cd "$srcdir"/$pkgname-$pkgver
 install -D $pkgname "$pkgdir"/usr/bin/$pkgname
 mkdir -p "$pkgdir"/$_basedir/cgi-bin
 install -D "$pkgname".cgi "$pkgdir"/$_basedir/cgi-bin/$pkgname.cgi
 mkdir "$pkgdir"/etc

 # use Arch defaults
 sed -e '/^our $BASE_DIR/ s,\/usr\/share,\/srv\/http,' -e '/^our $BASE_CGI/ s,-cgi,\/cgi-bin,' < $pkgname.conf > "$pkgdir"/etc/$pkgname.conf

 install -dm777 "$pkgdir"/$_basedir/imgs
 mkdir -p "$pkgdir"/usr/share/doc
 install -D $pkgname.logrotate "$pkgdir"/etc/logrotate.d/$pkgname
 cp Changes COPYING $pkgname-{alert.sh,apache.conf} README README.{FreeBSD,nginx} "$pkgdir"/usr/share/doc
 cp {logo_bot,logo_top,${pkgname}ico}.png "$pkgdir"/$_basedir
 gzip -9 man/man8/$pkgname.8
 install -Dm644 man/man8/$pkgname.8.gz "$pkgdir"/usr/share/man/man8/$pkgname.8.gz
 gzip -9 man/man5/$pkgname.conf.5
 install -Dm644 man/man5/$pkgname.conf.5.gz "$pkgdir"/usr/share/man/man5/$pkgname.conf.5.gz
 mkdir -p "$pkgdir"/$_libdir
 cp -r reports "$pkgdir"/$_libdir
 install -D "$srcdir"/rc.$pkgname "$pkgdir"/etc/rc.d/$pkgname
}
