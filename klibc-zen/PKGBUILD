# Contributor: Patrick Bartels <p4ddy.b@gmail.com>

_kernver="2.6.24-zen"

pkgname="klibc-zen"
pkgver="1.5"
pkgrel="3"
pkgdesc="A minimal libc made for early-userspace compiled against kernel26zen"
url="http://www.kernel.org/pub/linux/libs/klibc/"
license=("BSD")
arch=("i686" "x86_64")
makedepends=("kernel26zen-uvesafb" "bison")
conflicts=("klibc")
provides=("klibc" "klibc-uvesafb")
options=("!ccache" "!makeflags")
source=(http://www.kernel.org/pub/linux/libs/klibc/Stable/klibc-$pkgver.tar.gz
        multiple_raid_assembly_fix.patch
        klibc-compile-shared-by-default.patch
        klibc-module-init-tools.patch)
md5sums=('d55ce89c0656a7d6896ec0b2af07b5dc'
         '8d257d50a4554d57b8f461c6a87a2877'
         'c263a7c3fd290fcc84a4e230d456d022'
         '4d78311ebce63f2fbb711f8518e71839')

warning "kernel26zen-uvesafb is provided by kernel26zen-git"
warning "if you have enabled uvesafb support"

build() {
	cd $startdir/src/klibc-$pkgver

	# INI_DEBUG causes ipconfig to fail within kinit
	sed -i "/#define INI_DEBUG/d" usr/kinit/kinit.h
	ln -sf /usr/src/linux-${_kernver} linux

	patch -Np1 < ../multiple_raid_assembly_fix.patch
	patch -Np1 < ../klibc-module-init-tools.patch
	patch -Np1 < ../klibc-compile-shared-by-default.patch

	make EXTRA_KLIBCFLAGS="" || return 1
	make INSTALLROOT=$startdir/pkg install || return 1

	mv $startdir/pkg/usr/lib/klibc/bin/sh.shared \
		$startdir/pkg/usr/lib/klibc/bin/sh

	rm $startdir/pkg/usr/lib/klibc/bin/mknod
	rm $startdir/pkg/usr/lib/klibc/bin/kill
}
