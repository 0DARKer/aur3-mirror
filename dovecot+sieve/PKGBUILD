# Contributor: Paul Mattal <paul@mattal.com>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: GARETTE Emmanuel <gnunux at laposte dot net>
# Contributor: Ingmar Steen <iksteen@gmail.com>
# Upstream maintainer of dovecot package: Andreas Radke <andyrtr@archlinux.org>
# AUR maintainer of dovecot+sieve package: Ingmar Steen <iksteen@gmail.com>
#
# This package is a combination of dovecot and the dovecot-sieve plugin that
# comes with the dovecot-sieve tools.
#
# NOTE: Version numbers are hard-coded in the source variable to help AUR.

pkgname=dovecot+sieve
pkgver=1.2.6+0.1.13
_pkgver_dovecot=${pkgver%+*}
_pkgver_sieve=${pkgver#*+}
pkgrel=2
pkgdesc="An IMAP and POP3 server written with security primarily in mind"
arch=('i686' 'x86_64')
url="http://dovecot.org/"
license=("LGPL")
depends=("pam>=1.0.4" "libmysqlclient>=5.1.38" "postgresql-libs>=8.4.1-1" "sqlite3>=3.6.18" "db>=4.8"
	 "heimdal>=1.2.1-6" 'libcap>=2.17' 'bzip2')
optdepends=('libldap: ldap plugin')
makedepends=('libldap>=2.4.18-1')
provides=('dovecot' 'dovecot-sieve' 'imap-server' 'pop3-server')
conflicts=('dovecot' 'dovecot-sieve')
options=('!libtool')
install=dovecot.install
source=(http://dovecot.org/releases/1.2/dovecot-1.2.6.tar.gz
        http://www.rename-it.nl/dovecot/1.2/dovecot-1.2-sieve-0.1.13.tar.gz
        dovecot.sh)
md5sums=('807666f1a112e7379c4dbb57c9ce4899'
         '6151ab50e24abc0384358b1618c8048f'
         'd020d43eab4ded6fb379dadc570a9490')

build() {
  cd "${srcdir}/dovecot-$_pkgver_dovecot" || return 1

  # configure with openssl, mysql, and postgresql support
  ./configure --prefix=/usr --sysconfdir=/etc/dovecot --localstatedir=/var \
  	--libexecdir=/usr/lib  --with-moduledir=/usr/lib/dovecot/modules \
	--with-db --with-mysql --with-pgsql --with-sqlite \
	--with-ssl=openssl --with-ssldir=/etc/dovecot/ssl \
	--with-gssapi --with-ldap=plugin --enable-header-install --with-docs || return 1

  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # install the launch script
  install -D -m755 ${srcdir}/dovecot.sh ${pkgdir}/etc/rc.d/dovecot || return 1

  # fix paths in the config example file where we have a different layout
  sed -i 's|/usr/lib/dovecot|/usr/lib/dovecot/modules|g;
	  s|libexec|lib|g;
	  s|lib/sendmail|sbin/sendmail|g' $pkgdir/etc/dovecot/dovecot-example.conf || return 1
  
  # build and install sieve plugin
  cd "$srcdir/dovecot-1.2-sieve-$_pkgver_sieve" || return 1

  ./configure --prefix=/usr --with-dovecot=../dovecot-$_pkgver_dovecot || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1
}
