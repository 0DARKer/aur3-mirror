# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdnssd-git
pkgver=r31.3a3511d
pkgrel=1
pkgdesc='KDNSSD Framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdnssd'
license=('LGPL')
depends=('qt5-base' 'avahi' 'nss-mdns' 'kconfig-git')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
conflicts=('kdnssd' 'kdnssd-framework' 'kdnssd-framework-git')
provides=('kdnssd' 'kdnssd-framework' 'kdnssd-framework-git')
source=('git://anongit.kde.org/kdnssd.git')
md5sums=('SKIP')

pkgver() {
  cd kdnssd
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdnssd \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
