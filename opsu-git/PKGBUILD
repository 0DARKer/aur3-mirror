# Maintainer: Lucki <Lucki at holarse-linuxgaming dot de>

pkgname=opsu-git
_pkgname=opsu
_pkgurl=git://github.com/itdelatrisu/opsu.git
pkgver=0.5.0.r14.gf6eec5c
pkgrel=1
pkgdesc="An open source osu!-client written in Java."
arch=('any')
url="https://osu-game.com/"
license=('GPL3')
depends=('java-environment' 'bash')
makedepends=('java-web-start' 'maven' 'gendesk')
provides=('opsu')
conflicts=('opsu')
source=( ${_pkgname}::${_pkgurl}
         pom.patch
         opsu.sh )
sha512sums=('SKIP'
            'de80feb590445c36cac8afb8ccddd0951d12dda2a469631d8aa81c1e2456e44a43721bbe394ddfaea69acb3d0a1783d5806d678629fa3e4f73e57d5d7176b6b4'
            '6b2f044af6d5ccb04d77a7a1c70be785d713ac381b83203d6a02f1a02a96f2277ea42f8fa94f591046dacdd70a9c33578bf9199e82c9148b6f29d4dcc2fb1077')

prepare(){
  gendesk -n -f --pkgname ${_pkgname} --pkgdesc "$pkgdesc" --name "opsu!" --exec "java -jar /opt/opsu/opsu.jar" --categories "Game"
}

pkgver() {
  cd ${srcdir}/${_pkgname}
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${srcdir}

  # patch pom.xml for OpenJDK - uncomment for OracleJDK
  patch ${_pkgname}/pom.xml pom.patch

  cd ${_pkgname}
  mvn install -Djar
}

package() {
  # cut pkgver
  _pkgver=${pkgver%.*}
  _pkgver=${_pkgver%.*}

  install -Dm644 ${srcdir}/${_pkgname}/target/${_pkgname}-${_pkgver}-runnable.jar ${pkgdir}/opt/${_pkgname}/${_pkgname}.jar
  install -Dm644 ${srcdir}/${_pkgname}/res/logo.png ${pkgdir}/usr/share/pixmaps/${_pkgname}.png
  install -Dm644 ${_pkgname}.desktop ${pkgdir}/usr/share/applications/${_pkgname}.desktop
  install -Dm755 ${_pkgname}.sh ${pkgdir}/usr/bin/${_pkgname}
}
