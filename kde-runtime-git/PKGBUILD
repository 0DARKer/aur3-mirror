# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-runtime-git
pkgver=r20481.ed63e00
pkgrel=1
pkgdesc='Plugins and applications necessary for the running of KDE applications'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('LGPL')
depends=('libssh' 'openexr' 'exiv2' 'smbclient' 'khtml-git' 'libcanberra' 'kross-git'
         'kcmutils-git' 'kde4support-git' 'kemoticons-git' 'kdesu-git' 'plasma-framework-git'
         'knewstuff-git' 'knotifyconfig-git' 'kinit-git' 'kidletime-git' 'kitemmodels-git')
makedepends=('extra-cmake-modules' 'git' 'openslp' 'networkmanager' 'kdoctools-git' 'kf5umbrella-git')
provides=('kde-runtime')
conflicts=('kde-runtime')
source=('git://anongit.kde.org/kde-runtime.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kde-runtime
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
  cmake ../kde-runtime \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
