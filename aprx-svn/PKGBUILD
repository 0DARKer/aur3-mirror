# Maintainer: WB2FKO <mph at sportscliche dot com>
pkgname=aprx-svn
pkgver=438
pkgrel=1
pkgdesc="Lightweight APRS digipeater and i-Gate interface"
arch=('i686')
url="http://wiki.ham.fi/Aprx.en"
license=('custom')
depends=('glibc')
makedepends=('subversion')

_svntrunk="http://repo.ham.fi/svn/aprx/trunk/"
_svnmod="aprx"

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

# For a small memory system without writeable /tmp add --with-embedded   
  ./configure --prefix=/usr 
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  make DESTDIR="$pkgdir/" install
}
