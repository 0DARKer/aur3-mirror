# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=rho
pkgver=1.0.16
pkgrel=1
pkgdesc="A small, very configurable console-based text editor."
arch=(i686 x86_64)
url="http://rho.tuxfamily.org/"
license=('custom:CC BY-NC-SA')
depends=('ncurses')
install="$pkgname.install"
source=("$url$pkgname-latest.tar.bz2")
md5sums=('453ad781dc32b28a21b73aa4cd1fdf5a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # force DESTDIR in Makefile
  sed -i "s|\(/usr\)/local|$pkgdir\1|g" Makefile

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # prepare dirs
  install -d "$pkgdir"/usr/{bin,share/{licenses,doc,}/$pkgname}

  make install

  # docs
  mv "$pkgdir/usr/share/$pkgname/"{README,*.{html,txt}} \
    "$pkgdir/usr/share/doc/$pkgname"

  # license
  mv "$pkgdir/usr/share/$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
