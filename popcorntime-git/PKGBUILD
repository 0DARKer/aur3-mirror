# Maintainer: Eric Engestrom <aur [at] engestrom [dot] ch>

pkgname=popcorntime-git
_gitname=popcorn-app
pkgver=r438.872afb8
pkgrel=1
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('x86_64')
url="http://getpopcornti.me/"
license=('custom')
makedepends=('git')
provides=("popcorn")
options=('!strip')
source=("git://github.com/popcorn-time/${_gitname}"
        "TOS.txt")
md5sums=('SKIP'
         '1e6df124be80e43f3f819b832560432e')

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}/dist/linux"
  sed -i '$ d' build.sh
  sh build.sh
}

package() {
  cd "${srcdir}/${_gitname}/dist/linux/PopcornTime"

  # Program
  local _OPT="/opt/popcorn-time"
  mkdir -p "${pkgdir}${_OPT}"
  install -m755 popcorn-app.run "${pkgdir}${_OPT}/"
  install -m744 nw.pak "${pkgdir}${_OPT}/"
  install -m755 libffmpegsumo.so "${pkgdir}${_OPT}/"

  # Link to program
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "${_OPT}/popcorn-app.run" "${pkgdir}/usr/bin/${provides[0]}"

  # License
  install -Dm644 "${srcdir}/TOS.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
