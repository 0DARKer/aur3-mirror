# Maintainer: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Attila Bukor <r1pp3rj4ck [at] w4it [dot] eu>

pkgname=popcorntime-git
pkgver=r734.98cc32c
pkgrel=1
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('i686' 'x86_64')
url="http://popcorn.cdnjd.com/"
license=('GPL3')
depends=('alsa-lib' 'gconf' 'gtk2' 'nss' 'libudev.so.0')
makedepends=('git' 'nodejs' 'nodejs-grunt-cli' 'ruby-compass')
conflicts=('popcorntime' 'popcorntime-beta')
provides=("Popcorn-Time")
options=('!strip')

[ "$CARCH" = "i686" ] && _platform=linux32
[ "$CARCH" = "x86_64" ] && _platform=linux64
_gitname=popcorn-app
_DEST="/usr/share/popcorn-time"

source=("git+https://github.com/popcorn-team/${_gitname}"
        "popcorntime.png"
        "popcorntime.desktop")
md5sums=('SKIP'
         'c8a03cc6b0f7af7404d1686b53923bd8'
         '9608f35f4cc083a0c36b0f4ef275ab10')

pkgver() {
  cd "${_gitname}"
  printf -v _tmpver "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  msg2 "$_tmpver"
  echo $_tmpver
}

build() {
  cd "${srcdir}/${_gitname}"

  # Get dependencies
  msg2 "Getting dependencies"
  npm install

  # Build
  msg2 "Building"
  grunt build
}

package() {
  cd "${srcdir}/${_gitname}/build/releases/Popcorn-Time/${_platform}/Popcorn-Time"

  # Program
  msg2 "Installing program to ${_DEST}"
  install -dm755 "${pkgdir}${_DEST}"
  install -m755 "Popcorn-Time" "${pkgdir}${_DEST}"
  install -m744 "nw.pak" "${pkgdir}${_DEST}"
  install -m755 "libffmpegsumo.so" "${pkgdir}${_DEST}"

  # Link to program
  msg2 "Symlink /usr/bin/${provides[0]} -> ${_DEST}/Popcorn-Time"
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "${_DEST}/Popcorn-Time" "${pkgdir}/usr/bin/${provides[0]}"

  # Desktop file
  install -Dm644 "${srcdir}/popcorntime.desktop" "${pkgdir}/usr/share/applications/popcorntime.desktop"

  # Icon
  install -Dm644 "${srcdir}/popcorntime.png" "${pkgdir}/usr/share/pixmaps/popcorntime.png"
}
