# Maintainer: Eric Engestrom <aur [at] engestrom [dot] ch>

pkgname=popcorntime-git
pkgver=r701.ca94792
pkgrel=1
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('i686' 'x86_64')
url="http://getpopcornti.me/"
license=('GPLv3')
depends=('alsa-lib' 'gconf' 'gtk2' 'nss' 'libudev.so.0')
makedepends=('git' 'nodejs' 'nodejs-grunt-cli' 'ruby-compass')
conflicts=('popcorntime' 'popcorntime-beta')
provides=("Popcorn-Time")
options=('!strip')
_gitname=popcorn-app
source=("git+https://github.com/isra17/${_gitname}")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  printf -v _tmpver "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  msg2 "$_tmpver"
  echo $_tmpver
}

[ "$CARCH" = "i686" ]   && _platform=linux32
[ "$CARCH" = "x86_64" ] && _platform=linux64

prepare() {
  if [ "$CARCH" = "i686" ]
  then
    warning "The $CARCH version of this program is not officially supported yet"
    #warning "See https://github.com/isra17/popcorn-app/issues/"
    echo -n "Press Ctrl+C to abort, anything else to try and continue... "
    read -n1
    echo
  fi
}

build() {
  cd "${srcdir}/${_gitname}"

  # Get dependencies
  msg2 "Getting dependencies"
  npm install

  # Build
  msg2 "Building"
  grunt nodewkbuild --platforms=${_platform}
}

package() {
  cd "${srcdir}/${_gitname}/build/releases/Popcorn-Time/${_platform}/Popcorn-Time"

  # Program
  local _DEST="/opt/popcorn-time"
  msg2 "Installing program to ${_DEST}"
  mkdir -p "${pkgdir}${_DEST}"
  install -m755 "Popcorn-Time" "${pkgdir}${_DEST}"
  install -m744 "nw.pak" "${pkgdir}${_DEST}"
  install -m755 "libffmpegsumo.so" "${pkgdir}${_DEST}"

  # Link to program
  msg2 "Symlink /usr/bin/${provides[0]} -> ${_DEST}/Popcorn-Time"
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "${_DEST}/Popcorn-Time" "${pkgdir}/usr/bin/${provides[0]}"
}
