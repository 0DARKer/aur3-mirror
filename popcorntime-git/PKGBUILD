# Maintainer: Eric Engestrom <aur [at] engestrom [dot] ch>

pkgname=popcorntime-git
_gitname=popcorn-app
pkgver=r523.3f3ca57
pkgrel=1
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('x86_64')
url="http://getpopcornti.me/"
license=('custom' 'MIT' 'GPL' 'LGPL')
optdepends=('libudev.so.0: Some people reported error msgs regarding this file')
makedepends=('git' 'coreutils' 'sed' 'nodejs' 'nodejs-grunt-cli' 'ruby-compass')
provides=("Popcorn-Time")
options=('!strip')
source=("git+https://github.com/popcorn-time/${_gitname}"
        "TOS.txt")
md5sums=('SKIP'
         '1e6df124be80e43f3f819b832560432e')

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"

  # Only build what we want
  [ "$CARCH" = "i686" ]   || sed "s/linux32:\s*true/linux32: false/" -i Gruntfile.js
  [ "$CARCH" = "x86_64" ] || sed "s/linux64:\s*true/linux64: false/" -i Gruntfile.js
  sed "s/mac:\s*true/mac: false/" -i Gruntfile.js
  sed "s/win:\s*true/win: false/" -i Gruntfile.js

  # Get dependencies
  npm install

  # Build
  grunt nodewkbuild
}

package() {
  cd "${srcdir}/${_gitname}/build/releases/Popcorn-Time"

  if [[ "$CARCH" == "i686" ]]
  then
    cd "linux32"
  elif [[ "$CARCH" == "x86_64" ]]
  then
    cd "linux64"
  else
    error "Incompatible architecture: $CARCH"
    return 1
  fi

  cd "Popcorn-Time"

  # Program
  local _OPT="/opt/popcorn-time"
  mkdir -p "${pkgdir}${_OPT}"
  install -m755 "Popcorn-Time" "${pkgdir}${_OPT}"
  install -m744 "nw.pak" "${pkgdir}${_OPT}"
  install -m755 "libffmpegsumo.so" "${pkgdir}${_OPT}"

  # Link to program
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "${_OPT}/Popcorn-Time" "${pkgdir}/usr/bin/${provides[0]}"

  # License
  install -Dm644 "${srcdir}/TOS.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
