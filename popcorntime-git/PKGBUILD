# Maintainer: Eric Engestrom <aur [at] engestrom [dot] ch>

pkgname=popcorntime-git
pkgver=r623.ecd85d7
pkgrel=1
pkgdesc="Stream movies from torrents. Skip the downloads. Launch, click, watch."
arch=('i686' 'x86_64')
url="http://getpopcornti.me/"
license=('custom' 'MIT' 'GPL' 'LGPL')
depends=('alsa-lib' 'gconf' 'gtk2' 'nss' 'libudev.so.0')
makedepends=('git' 'nodejs' 'nodejs-grunt-cli' 'ruby-compass')
provides=("Popcorn-Time")
options=('!strip')
_gitname=popcorn-app
source=("git+https://github.com/popcorn-time/${_gitname}"
        "TOS.txt")
md5sums=('SKIP'
         '1e6df124be80e43f3f819b832560432e')

pkgver() {
  cd "${_gitname}"
  printf -v _tmpver "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  msg2 "$_tmpver"
  echo $_tmpver
}

prepare() {
  if [ "$CARCH" = "i686" ]
  then
    warning "The $CARCH version of this program is not officially supported yet"
    warning "See https://github.com/popcorn-time/popcorn-app/issues/615"
    echo -n "Press Ctrl+C to abort, anything else to try and continue... "
    read -n1
    echo
  fi
}

build() {
  cd "${srcdir}/${_gitname}"

  # Disable everything
  sed "s/mac:\s*true/mac: false/" -i Gruntfile.js
  sed "s/win:\s*true/win: false/" -i Gruntfile.js
  sed "s/linux32:\s*true/linux32: false/" -i Gruntfile.js
  sed "s/linux64:\s*true/linux64: false/" -i Gruntfile.js

  # Only build what we want
  [ "$CARCH" = "i686" ]   && sed "s/linux32:\s*false/linux32: true/" -i Gruntfile.js
  [ "$CARCH" = "x86_64" ] && sed "s/linux64:\s*false/linux64: true/" -i Gruntfile.js

  # Get dependencies
  msg2 "Getting dependencies"
  npm install

  # Build
  msg2 "Building"
  grunt nodewkbuild
}

package() {
  cd "${srcdir}/${_gitname}/build/releases/Popcorn-Time"

  if [[ "$CARCH" == "i686" ]]
  then
    cd "linux32"
  elif [[ "$CARCH" == "x86_64" ]]
  then
    cd "linux64"
  else
    error "Incompatible architecture: $CARCH"
    return 1
  fi

  cd "Popcorn-Time"

  # Program
  local _OPT="/opt/popcorn-time"
  msg2 "Installing program to ${_OPT}"
  mkdir -p "${pkgdir}${_OPT}"
  install -m755 "Popcorn-Time" "${pkgdir}${_OPT}"
  install -m744 "nw.pak" "${pkgdir}${_OPT}"
  install -m755 "libffmpegsumo.so" "${pkgdir}${_OPT}"

  # Link to program
  msg2 "Symlink /usr/bin/${provides[0]} -> ${_OPT}/Popcorn-Time"
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "${_OPT}/Popcorn-Time" "${pkgdir}/usr/bin/${provides[0]}"

  # License
  install -Dm644 "${srcdir}/TOS.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
