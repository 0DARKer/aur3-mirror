# Maintainer: L <alaviss0@gmail.com>

pkgname=gnome-shell-theme-elegance-colors-git
_pkgname=elegance-colors
pkgver=git
pkgrel=1
pkgdesc="A chameleon theme for Gnome Shell which can change colors according to the current GTK theme, current wallpaper (uses imagemagick to get color) or a user defined color."
arch=('i686' 'x86_64')
url="https://github.com/satya164/elegance-colors"
license=('GPLv3')
depends=('gtk3' 'gnome-shell>=3.6' 'glib2' 'imagemagick')
makedepends=('vala')
conflicts=('gnome-shell-themes-elegance-colors')
provides=('gnome-shell-themes-elegance-colors')
install="${_pkgname}.install"
source=('git://github.com/satya164/elegance-colors.git')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --tags --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
