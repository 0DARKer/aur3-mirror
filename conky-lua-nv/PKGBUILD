# Maintainer: Jon Wilson <bugs@pandorica.net>
# Contributor: Andrwe Lord Weber <lord-weber-andrwe [at] andrwe [dot] org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>

pkgname=conky-lua-nv
_pkgname=conky
pkgver=1.9.0
pkgrel=1
pkgdesc="An advanced system monitor for X based on torsmo with lua and nvidia enabled"
arch=('i686' 'x86_64')
url="http://conky.sourceforge.net/"
license=('custom')
replaces=('torsmo' 'conky')
conflicts=('conky')
provides=('conky')
depends=('alsa-lib' 'libxml2' 'curl' 'cairo' 'wireless_tools' 'libxft' 'glib2' 'libxdamage' 'imlib2' 'toluapp' 'nvidia' 'libxnvctrl')
makedepends=('pkgconfig')
backup=(etc/conky/conky.conf etc/conky/conky_no_x11.conf)
source=(http://downloads.sourceforge.net/project/${_pkgname}/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.bz2)
md5sums=('d3de615f43aadc98d555e05ad9e902c2')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  
  sed -i '/#include <curl\/types.h>/d' ${srcdir}/${_pkgname}-${pkgver}/src/ccurl_thread.c
  LUA51_LIBS='-llua -lm' ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-wlan \
	      --enable-rss \
	      --enable-ibm \
	      --enable-imlib2 \
	      --enable-lua \
	      --enable-lua-cairo \
	      --enable-lua-imlib2 \
              --enable-nvidia
  make || return 1
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE
}
