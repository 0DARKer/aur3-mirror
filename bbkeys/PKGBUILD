# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: andrewy <andrew@andrewyates.net>
#
pkgname=bbkeys
pkgver=0.9.1
pkgrel=4
pkgdesc="Blackbox tool for setting keyboard shortcuts for manipulating windows and virtual desktop"
arch=('i686' 'x86_64')
url="http://bbkeys.sourceforge.net/"
license=('custom')
groups=('blackbox')
depends=('libxext' 'libxft')
changelog="bbkeys.changelog"
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	"bbkeys.desktop")
md5sums=('6bb7fdf50b7c2df4f9a12f4e16d76f66'
         '99b42809855752bf7182ec8a31bb34a0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr || return 1
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1

  install -m644 -D "${srcdir}/bbkeys-${pkgver}/LICENSE" \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -m644 -D "${srcdir}/bbkeys.desktop" \
		   "${pkgdir}/usr/share/applications/bbkeys.desktop"
  install -m644 -D "${srcdir}/bbkeys.desktop" \
		   "${pkgdir}/etc/xdg/autostart/bbkeys.desktop"
}
