# Maintainer : <dreamspepser at yahoo dot it>

pkgname=distrho-lv2-git
pkgver=20120519
pkgrel=1
pkgdesc="Distrho LV2 Audio Plugins (TAL), using the JUCE Toolkit"
arch=('i686' 'x86_64')
url="http://distrho.sourceforge.net/"
license=('GPL' 'GPL3' 'LGPL')
depends=('alsa-lib' 'fftw' 'libxext' 'freetype2')
makedepends=('premake3' 'jack' 'libxinerama'
             'libxrender' 'libxcursor' 'qt')
optdepends=('jack: jack audio support'
            'distrho-extra-git: HybridReverb2 plugin')
provides=('distrho-lv2' 'distrho-plugins')
conflicts=('distrho-lv2' 'distrho-plugins-lv2-git')
replaces=('distrho-plugins-lv2-git')

_gitroot=git://distrho.git.sf.net/gitroot/distrho/distrho
_gitname=distrho

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # generate build script
  scripts/premake-update.sh linux

  # --as-needed incompatible
  export LDFLAGS="${LDFLAGS//,--as-needed}"

  make lv2

  # generate lv2 ttl
  scripts/generate-ttl.sh
}

package() {
  cd "$srcdir/$_gitname-build"

  # lv2 plugins
  install -d "$pkgdir/usr/lib/lv2"
  cp -a bin/lv2/*.lv2 \
    "$pkgdir/usr/lib/lv2"
}

# vim:set ts=2 sw=2 et:
