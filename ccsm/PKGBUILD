#$ $Id$
# Maintainer: OS Hazard <oshazard+aur@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: nesl247 <nesl247@gmail.com>

pkgname=ccsm
pkgver=0.8.4
pkgrel=4
pkgdesc="Compizconfig Settings Manager in Python2"
arch=('any')
url="http://opencompositing.org"
license=('GPL')
depends=('compizconfig-python>=0.8.4' 'pygtk')
makedepends=('intltool')
optdepends=('compiz-pure: option 1' 'compiz-core: option 2')
groups=('compiz-fusion' 'compiz-fusion-gtk' 'compiz-fusion-kde')
conflicts=('ccsm-git')
provides=('ccsm')
source=(http://releases.compiz.org/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
install=ccsm.install
md5sums=('6bad7a02a91cc8fc9c4c48f83e91c08c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # blank buttons fix
  # Source: https://sites.google.com/site/debininja/tutorials/ccsm-no-icons-text-fix
  sed -i 's/ret = super (PrettyButton, self)/ret = gtk\.Button/g' ccm/Utils.py 

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 ./setup.py install --prefix=/usr --root="${pkgdir}"
}
