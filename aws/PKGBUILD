# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=aws
pkgver=1.77
pkgrel=2
pkgdesc="A simple Perl interface to Amazon's EC2/S3/SQS/SDB/ELB services"
arch=(i686 x86_64)
url=http://timkay.com/$pkgname/
license=(custom)
install=$pkgname.install
source=($pkgname-$pkgver::https://github.com/timkay/$pkgname/raw/master/$pkgname aws{rc,secret})
sha256sums=('9874c31143fcb6aa47d4614c97a6211759534299e50de7feed8bd3364a96b6e3'
    '56e97b20a82310b9a00fbcbfad00552689d84d393b3a3d563112d32e16ac0735'
    '91cdafd4793fdf65eba5aaa63b84d6772b6abfdcfb047aac317b6c5fe16ede4e')
sha512sums=('4fe19da16225a3eb7e88d5f9880c126f70de861b50bbc019d734b7686303c0a2546847734ab2eebec7371c69d7f4c7f37e934177bf068be36c1020e23c7a5e0c'
    '8f61aa32e63e008645cae14dcba6b05add68a389eef2010338c594e8713e82eb501bed1cdfd26f5c5c8954184a5eeba4c7167d6044ba876219f8b5550b8309f6'
    'b6bf86b2f4315924fcca269931575f78e749ab30881bf1ac97f541eec6d08c9b3ffde0c7cdd451ef08e0fd94a83714124ba7b78d7d2b8d8f52dd647d359d8ba0')

package() {
    cd "$srcdir"
    sed -n '3,17 s/#[ ]*//p' $pkgname-$pkgver > COPYING
    install -Dm755 $pkgname-$pkgver "$pkgdir"/usr/bin/$pkgname
    install -Dm644 awsrc "$pkgdir"/usr/share/$pkgname/awsrc
    install -Dm644 awssecret "$pkgdir"/usr/share/$pkgname/awssecret
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING

    cd "$pkgdir"/usr/bin/
    ./aws --link=short
}
