# Maintainer: Pyro Devil <p dot devil at gmail dot com>
pkgname=exfat-dkms-git  
_pkgname=exfat
pkgver=37.3782f98
pkgrel=1 
pkgdesc="Native kernel module for extended fat support"
url="https://github.com/rxrz/exfat-nofuse"
arch=('any')
license=('Samsung Proprietary')
depends=('linux>=3.8' 'dkms')
makedepends=()
options=('!strip')
conflicts=()
replaces=()
backup=()
install="${pkgname}.install"
source=("${pkgname}::git+https://github.com/rxrz/exfat-nofuse.git"
        "dkms.conf")
md5sums=('SKIP'
	 'f92ac8b903ecd0a9968dbd46a7f8e20f')

pkgver() {
  cd "${pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}


#
# Put back gpl symbol for private & testing purpose only
#
prepare() {
  cd "${pkgname}"
  sed 's|MODULE_LICENSE("Samsung Proprietary")|MODULE_LICENSE("GPL")|' \
   -i exfat_api.c exfat_super.c
   
  sed -i "s/PACKAGE_VERSION=\"[-._ 0-9a-zA-Z]*\"/PACKAGE_VERSION=\"${pkgver}\"/g" "${srcdir}/dkms.conf"
}

package() {
  cd "${srcdir}"/"${pkgname}"

  installDir="$pkgdir/usr/src/${_pkgname}-${pkgver}"
	
  install -dm755 "${installDir}"
  install -m644 "${srcdir}/dkms.conf" "${installDir}"

  for d in `find . -type d`
    do
      install -dm755  "$installDir/$d"
    done
	
  for f in `find . -type f`
    do
      install -m644 "${srcdir}/${pkgname}/$f" "${installDir}/$f"
    done
}
