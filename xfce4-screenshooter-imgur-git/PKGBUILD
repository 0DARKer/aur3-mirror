# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-screenshooter-imgur-git
pkgver=1.8.1.r122.g1291d1c
pkgrel=1
pkgdesc="Plugin that makes screenshots for the Xfce panel with Imgur support."
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-screenshooter"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libsoup' 'hicolor-icon-theme' 'curl' 'json-glib')
makedepends=('xfce4-dev-tools' 'intltool' 'git')
install=$pkgname.install
source=("${pkgname%-*-*}"::"git://git.xfce.org/apps/xfce4-screenshooter#branch=master"
		"${pkgname%-*-*}.patch")

provides=('xfce4-screenshooter-plugin')
conflicts=('xfce4-screenshooter-plugin')
replaces=('xfce4-screenshooter-plugin')

sha256sums=('SKIP'
			'SKIP')
  
pkgver() {
  cd "${pkgname%-*-*}"
  git describe --long | sed -r 's/^xfce4.screenshooter.//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/${pkgname%-*-*}"
  patch -p1 < ../"${pkgname%-*-*}".patch
}


build() {
#  cd "$srcdir/$pkgname-$pkgver"
  cd "$srcdir/${pkgname%-*-*}"

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
#  cd "$srcdir/$pkgname-$pkgver"
  cd "$srcdir/${pkgname%-*-*}"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
