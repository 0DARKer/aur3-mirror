# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=otter-browser-git
pkgver=0.1.01.8.gafc55e4
pkgrel=1
pkgdesc='Browser aiming to recreate classic Opera (12.x) UI using Qt5'
arch=('i686' 'x86_64')
url='http://otter-browser.org/'
license=('GPL3')
depends=('qt5-webkit')
makedepends=('git' 'cmake')
source=(git://github.com/Emdek/otter.git)
md5sums=('SKIP')

pkgver() {
  cd otter
  git describe --tags | sed 's/^v//;s/-/./g'
}

build() {
  mkdir build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr ../otter
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
