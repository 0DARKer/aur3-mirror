# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=otter-browser-git
pkgver=0.2.01.r8.g43250b2
pkgrel=1
pkgdesc="Otter Browser, project aiming to recreate classic Opera (12.x) UI using Qt5 - git checkout"
arch=('x86_64' 'i686')
url="http://otter-browser.org/"
license=('GPL3')
provides=('otter-browser')
conflicts=('otter-browser')
depends=('qt5-script' 'qt5-webkit' 'libxcb')
makedepends=('cmake')
makedepends=('git')
source=('git+https://github.com/Emdek/otter.git')

pkgver() {
	cd otter/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	mkdir otter/build/
	cd otter/build/

	cmake -DCMAKE_INSTALL_PREFIX="/usr" ../
	make
}

package() {
	cd otter/

	install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/otter-browser/COPYING
	install -D -m0644 otter-browser.desktop ${pkgdir}/usr/share/applications/otter-browser.desktop

	cd build/

	make DESTDIR=${pkgdir} install

}

sha256sums=('SKIP')
