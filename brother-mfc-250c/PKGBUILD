# Maintainer: libernux <dutchman55@gmx.com>
pkgname="brother-mfc-250c"
pkgver="1.1.2"
pkgrel=2
pkgdesc="LPR and CUPS driver for the Brother MFC-250c"
url="http://solutions.brother.com/linux/en_us/"
arch=("i686" "x86_64")
license=('custom:brother commercial license')
depends=("cups")
install="$pkgname.install"
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/mfc250clpr-$pkgver-$pkgrel.i386.rpm"
	"http://www.brother.com/pub/bsc/linux/dlf/mfc250ccupswrapper-$pkgver-$pkgrel.i386.rpm"
	'cupswrapper-license.txt'
	'lpr-license.txt'
)
md5sums=(
	'154cec370113400e1485900a91c0924f'
	'45e8867bd4f2b6cf7661ce5a9a7ad7a3'
	'97ad0cffd216059e9d1d3121899d8646'
	'5e87a3dc0f3e3438c088eda0f3565f0d'
)

build() {
	cd "$srcdir/usr/local/Brother/Printer/mfc250c/cupswrapper"
#  generate the ppd
	echo '#! /bin/sh' > genppd
	echo 'ppd_file_name=brmfc250c.ppd' >> genppd
	echo 'touch $ppd_file_name' >> genppd
	sed -n '/^printer_model/,/^device_model/p' cupswrappermfc250c >> genppd
	sed -n '/^cat <<ENDOFPPDFILE1/,/^ENDOFPPDFILE1/p' cupswrappermfc250c >>genppd
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genppd
	chmod +x genppd
	./genppd
#  generate the wrapper
	echo '#! /bin/sh' > genwrapper
	echo 'brotherlpdwrapper=brlpdwrappermfc250c'  >> genwrapper
	echo 'touch $brotherlpdwrapper' >>genwrapper
	sed -n '/^printer_model/p' cupswrappermfc250c >>genwrapper
	sed -n '/^device_model/p' cupswrappermfc250c >>genwrapper
	sed -n '/cat <<!ENDOFWFILTER!/,/!ENDOFWFILTER!/p' cupswrappermfc250c >>genwrapper
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genwrapper
	chmod +x genwrapper
	./genwrapper
#  generate other
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/bin/brprintconf_mfc250c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc250c/lpd/filtermfc250c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc250c/lpd/brmfc250cfilter"
	install -d "$srcdir/usr/share"
	mv "$srcdir/usr/local/Brother/" "$srcdir/usr/share/brother"
	rm "$srcdir/usr/share/brother/Printer/mfc250c/cupswrapper/cupswrappermfc250c"
	rm "$srcdir/usr/share/brother/Printer/mfc250c/cupswrapper/genppd"
	rm "$srcdir/usr/share/brother/Printer/mfc250c/cupswrapper/genwrapper"
	rm "$srcdir/usr/share/brother/Printer/mfc250c/inf/setupPrintcapij"
	rm -rf "$srcdir/usr/local"
}

package() {
	cd "$pkgdir"
	cp -R "$srcdir/usr" "$pkgdir"
	install -m 644 -D "$srcdir/usr/share/brother/Printer/mfc250c/cupswrapper/brmfc250c.ppd" "$pkgdir/usr/share/cups/model/brmfc250c.ppd"
	install -m 755 -D "$srcdir/usr/share/brother/Printer/mfc250c/cupswrapper/brlpdwrappermfc250c" "$pkgdir/usr/lib/cups/filter/brlpdwrappermfc250c"
	install -m 644 -D "$startdir"/cupswrapper-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/cupswrapper-licence.txt"
	install -m 644 -D "$startdir"/lpr-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/lpr-licence.txt"
}