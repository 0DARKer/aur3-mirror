# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=supermin
pkgver=4.1.5
pkgrel=1
pkgdesc="Tool for creating supermin appliances"
arch=('i686' 'x86_64')
url="http://people.redhat.com/~rjones/supermin/"
license=('GPL')
makedepends=('ocaml' 'ocaml-findlib' 'prelink')
# Does not provide febootstrap because supermin is not compatible with it
provides=()
conflicts=('febootstrap<=3.21' 'supermin-git')
source=("http://libguestfs.org/download/supermin/supermin-${pkgver}.tar.gz")
sha512sums=('fb874900a76ac091d4cdbfabf986f4f36557eee562ed9ce5bb055b5fa241c18440c10259f50971f972c67dcf7508dd85fb343a809ca6a1709ece329ff7ecaae1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #make -k check
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # From Fedora's spec file:
  #   supermin-helper is marked as requiring an executable stack.  This
  #   happens because we use objcopy to create one of the component object
  #   files from a data file.  The program does not in fact require an
  #   executable stack.  The easiest way to fix this is to clear the flag
  #   here.
  execstack -c "${pkgdir}/usr/bin/supermin-helper"
}

# vim:set ts=2 sw=2 et:
