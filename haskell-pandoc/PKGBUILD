# Maintainer: Joop Kiefte <ikojba@gmail.com>
# TODO: prepare scripts for new AUR_git+haskell+cpan
# TODO: deps from new lines here, way too unreadable now
# TODO: find out which ones are optional (and what is their purpose)
_hkgname=pandoc
pkgname=haskell-pandoc
pkgver=1.12.4.2
pkgrel=1
pkgdesc="Conversion between markup formats"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('alex' 'happy')
depends=('ghc' 'haskell-http' 'haskell-base64-bytestring' 'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-bytestring' 'haskell-citeproc-hs' 'haskell-containers' 'haskell-data-default' 'haskell-directory' 'haskell-extensible-exceptions' 'haskell-filepath' 'haskell-highlighting-kate' 'haskell-json' 'haskell-mtl' 'haskell-network' 'haskell-old-locale' 'haskell-old-time'  'haskell-pandoc-types' 'haskell-parsec' 'haskell-process' 'haskell-random' 'haskell-syb' 'haskell-tagsoup' 'haskell-temporary' 'haskell-texmath' 'haskell-text' 'haskell-time' 'haskell-xml' 'haskell-zip-archive' 'haskell-zlib')
options=('strip' 'staticlibs')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('a6f20fdeb9a548069ad45513f399b083')
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
}
