# Maintainer: Bersam Karbasion <bersam.k at gmail dot com>
# Contributor: pancho <pancho at pancho dot name>
# Contributor: taifunbrowser <bjmnbraun at gmail dot com>
# Contributor: junglepeanut <jungle1peanut1 at hotmail dot com>

pkgname=tpp
pkgver=1.3.1
pkgrel=3
pkgdesc="A text presentation program in ncurses, LaTeX, or text."
arch=(any)
url="http://www.ngolde.de/tpp.html"
license=('GPL')
depends=('ncurses' 'ruby-ncurses' 'ruby>=1.8')
makedepends=('recode')
source=("http://www.ngolde.de/download/$pkgname-$pkgver.tar.gz"
	    'fix-for-ruby1.9.patch')
sha512sums=('f3e0282c01fb5e524a0aac15ce6070c72a1cc98fd2b1614660f8850e4cc1f1af2ba21fba753e854d8797354f76192ee0d29df41d5c6cc4a623a4ae917e55b455'
            '6ba4db72447f40f5a438d6aa05d642e9ce6af4a15ad59a33679f0364535b43405dbc298f5ba8c6313236022a0024fbb4ec2cc10efa2c2b018fb0593f38e646cc')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -p0 < "$srcdir/fix-for-ruby1.9.patch"
	recode latin1..UTF-8 "$srcdir"/$pkgname-$pkgver/examples/*.tpp
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	mkdir -p "$pkgdir"/usr/{bin,share/man/man1}
	make prefix="$pkgdir"/usr install
}
