# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Maintainer: Valere Monseur <valere_monseur@hotmail.com>

pkgname=gtkam
pkgver=0.1.17
pkgrel=1
pkgdesc="A graphical GTK front-end to gPhoto2"
url="http://gphoto.sourceforge.net/proj/gtkam/"
arch=('i686' 'x86_64')
depends=('libgphoto2>=2.4.6' 'libexif-gtk')
makedepends=('intltool' 'gimp')
license=('GPL')
source=(http://downloads.sourceforge.net/sourceforge/gphoto/$pkgname-$pkgver.tar.gz)
md5sums=('6600bb0819e45bfe666882d468029679')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --without-bonobo --without-gnome \
    --disable-scrollkeeper

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  sed -i -e 's/^_//' "${pkgdir}/usr/share/applications/gtkam.desktop"
}
