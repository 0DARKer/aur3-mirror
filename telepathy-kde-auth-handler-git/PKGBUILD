# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-auth-handler-git
pkgver=r220.8c9080f
pkgrel=1
pkgdesc='Provide UI/KWallet Integration For Passwords and SSL Errors on Account Connect'
arch=('i686' 'x86_64')
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
depends=('qca-qt5' 'telepathy-kde-common-internals-git')
makedepends=('extra-cmake-modules' 'git')
conflicts=('telepathy-kde-auth-handler')
replaces=('telepathy-kde-auth-handler-frameworks-git')
source=("git://anongit.kde.org/ktp-auth-handler.git")
sha256sums=('SKIP')

pkgver() {
  cd ktp-auth-handler
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  sed 's|find_package (Qca REQUIRED)|find_package (Qca-qt5 REQUIRED)|g' -i "ktp-auth-handler/CMakeLists.txt"
}

build() {
  cd build
  cmake ../ktp-auth-handler \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DQca-qt5_DIR=/usr/lib/cmake/Qca
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
