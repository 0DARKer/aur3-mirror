# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbase=lame
pkgname=lib32-$_pkgbase
pkgver=3.99.3
pkgrel=1
pkgdesc="A high quality MPEG Audio Layer III (MP3) encoder (32 bit)"
arch=('x86_64')
url="http://lame.sourceforge.net/"
depends=('lib32-glibc' "$_pkgbase") # 'ncurses')
makedepends=('gcc-multilib' 'nasm') 
license=('LGPL')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz)
md5sums=('5ad31e33e70455eb3a7b79a5dd934fce')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-nasm \
              --enable-shared \
	      --libdir=/usr/lib32
	      
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
