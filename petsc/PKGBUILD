# Maintainer: Myles English <myles at rockhead dot biz>
pkgname=petsc
pkgver=3.1_p7
_config=linux-gnu-cxx-debug
pkgrel=3
pkgdesc="Portable, extensible toolkit for scientific computation"
arch=('any')
url="http://www.mcs.anl.gov/petsc/petsc-as"
license=('MIT compatible')
depends=('python2' 'gcc' 'mpich2' 'boost')
#conflicts=('umfpack')
install=(petsc.install)
source=(http://ftp.mcs.anl.gov/pub/petsc/release-snapshots/${pkgname}-${pkgver/_/-}.tar.gz)
md5sums=('1a1d79342def260c3641516efcd782f5')

_build_dir=${srcdir}/${pkgname}-${pkgver/_/-}
_install_dir=/usr/petsc/${_config}

build() {
    cd ${_build_dir}

    unset PETSC_ARCH
    export PETSC_DIR=${_build_dir}

    find ${srcdir} -name "*" -type f -exec \
	sed -i 's#\(/usr/bin/env \|/usr/bin/\)python[2-3]*#\1python2#' {} \;

    python2 ./config/configure.py --prefix=${pkgdir}${_install_dir} \
	--with-mpi-dir=/opt/mpich2 \
	--with-mpi-shared=1 \
	--with-sieve \
	--download-umfpack=1 \
	--with-clanguage=cxx \
	--with-boost-include=/usr/include/boost \
	--with-boost-lib=[/usr/lib/libboost_date_time.so,/usr/lib/libboost_filesystem.so,/usr/lib/libboost_graph.so,/usr/lib/libboost_iostreams.so,/usr/lib/libboost_math_c99.so,/usr/lib/libboost_math_c99f.so,/usr/lib/libboost_math_c99l.so,/usr/lib/libboost_math_tr1.so,/usr/lib/libboost_math_tr1f.so,/usr/lib/libboost_math_tr1l.so,/usr/lib/libboost_prg_exec_monitor.so,/usr/lib/libboost_program_options.so,/usr/lib/libboost_python.so,/usr/lib/libboost_random.so,/usr/lib/libboost_regex.so,/usr/lib/libboost_serialization.so,/usr/lib/libboost_signals.so,/usr/lib/libboost_system.so,/usr/lib/libboost_unit_test_framework.so,/usr/lib/libboost_wave.so,/usr/lib/libboost_wserialization.so] \
	--with-umfpack=1 --with-umfpack-dir=/usr/lib \
	--with-shared=1
    make
    sed -i 's/.\/config\/install.py/python2 .\/config\/install.py/' makefile
}

package() {
    cd ${_build_dir}

    make PETSC_DIR=${_build_dir} PETSC_ARCH=${_config} install

    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/variables"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/petscvariables"
    sed -i 's#'"${pkgdir}"'##g' "${pkgdir}${_install_dir}/conf/rules"

    export PETSC_DIR=${_install_dir}
    
    # documentation
    mkdir -p ${pkgdir}/usr/share/doc/$pkgname/
    cp -r ${_build_dir}/docs ${pkgdir}/usr/share/doc/$pkgname/

    # tutorials
    mkdir -p ${pkgdir}/usr/share/doc/$pkgname/tutorials
    cp -r ${_build_dir}/tutorials ${pkgdir}/usr/share/doc/$pkgname/tutorials

    # install licenCe (even though there is no such word as licenSes)
    mkdir -p ${pkgdir}/usr/share/licenses/petsc
    cp ${_build_dir}/docs/copyright.html ${pkgdir}/usr/share/licenses/$pkgname/

    mkdir -p ${pkgdir}/etc/profile.d
    echo "export PETSC_DIR=/usr/lib/petsc/${_config}" > ${pkgdir}/etc/profile.d/petsc.sh
    chmod +x ${pkgdir}/etc/profile.d/petsc.sh
}
