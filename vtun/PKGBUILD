# Maintainer: Sascha Weaver <wzyboies@gmail.com>

pkgname=vtun
pkgver=3.0.3
pkgrel=4
pkgdesc="The easiest way to create Virtual Tunnels over TCP/IP networks with traffic shaping, compression and encryption."
arch=('i686' 'x86_64')
url="http://vtun.sourceforge.net/"
license=('GPL')
depends=('openssl' 'zlib' 'lzo2')
backup=('etc/vtun/vtund.conf')
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('5fa789d08b556f97492b89515a89c2322c4b0a8fa95bd1035f5ed19061b3654a6a36a9911792096ac872ae9ae5451848cab87d0343dc0ffc064affea1f7d0d54')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/vtun \
    --localstatedir=/var \
    --mandir=/usr/share/man

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 vtund.conf "${pkgdir}/usr/share/doc/vtun/examples/vtund.conf"
  sed -n '/--- Client config ---/,$p' vtund.conf > vtund-client.conf
  install -Dm600 vtund-client.conf "${pkgdir}/etc/vtun/vtund.conf"
  install -Dm644 Credits "${pkgdir}/usr/share/doc/vtun/Credits"
  install -Dm644 README.Setup "${pkgdir}/usr/share/doc/vtun/README.Setup"
  install -Dm644 README.Shaper "${pkgdir}/usr/share/doc/vtun/README.Shaper"
  install -Dm644 FAQ "${pkgdir}/usr/share/doc/vtun/FAQ"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/vtun/ChangeLog"
}
# vim: ts=2 sw=2 et ft=sh
