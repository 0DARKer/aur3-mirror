# Contributor: Sascha Weaver <wzyboies@gmail.com>
# Contributor: Natrio <natrio@list.ru>


pkgname=vtun
pkgver=3.0.3
pkgrel=7
pkgdesc="The easiest way to create Virtual Tunnels over TCP/IP networks with traffic shaping, compression and encryption."
arch=('i686' 'x86_64' 'armv7h')
url="http://vtun.sourceforge.net/"
license=('GPL')
depends=('openssl' 'zlib' 'lzo2')
backup=('etc/vtund.conf')
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz android-dev.patch)
sha512sums=('5fa789d08b556f97492b89515a89c2322c4b0a8fa95bd1035f5ed19061b3654a6a36a9911792096ac872ae9ae5451848cab87d0343dc0ffc064affea1f7d0d54' 'cb26a79085851f166b1a9e7dbb6259a3a79a37616e2d5d874d3055b8d063daf96480fae80c78ab661d119d2763f7a4a0f5f234127f0aa28c91f1cc37a8af02c2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  grep -v setproctitle configure.in > tmp
  mv tmp configure.in
  autoconf
  [ "$CARCH" = "armv7h" ] && patch -i "${srcdir}/android-dev.patch" linux/tun_dev.c
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr/bin DESTDIR="${pkgdir}" install

  install -Dm644 vtund.conf "${pkgdir}/usr/share/doc/vtun/examples/vtund.conf"
  sed -n '/--- Client config ---/,$p' vtund.conf > vtund-client.conf
  install -Dm600 vtund-client.conf "${pkgdir}/etc/vtund.conf"
  install -Dm644 Credits "${pkgdir}/usr/share/doc/vtun/Credits"
  install -Dm644 README.Setup "${pkgdir}/usr/share/doc/vtun/README.Setup"
  install -Dm644 README.Shaper "${pkgdir}/usr/share/doc/vtun/README.Shaper"
  install -Dm644 FAQ "${pkgdir}/usr/share/doc/vtun/FAQ"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/vtun/ChangeLog"
  cd "${pkgdir}"
  mv var/run .
  mv var/lock run/
}
# vim: ts=2 sw=2 et ft=sh
