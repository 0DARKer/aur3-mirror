# Contributor: Sascha Weaver <wzyboies at gmail dot com>
# Maintainer: Natrio <natrio list ru>

pkgname=vtun
pkgver=3.0.3.2013
_pkgver=3.0.3
pkgrel=1
pkgdesc="The easiest way to create Virtual Tunnels over TCP/IP networks with traffic shaping, compression and encryption."
arch=('i686' 'x86_64' 'armv7h' 'armv6h')
url="http://vtun.sourceforge.net/"
license=('GPL')
depends=('openssl' 'zlib' 'lzo2')
backup=('etc/vtund.conf')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${_pkgver}/${pkgname}-${_pkgver}.tar.gz" android-dev.patch cvs-2013.patch)
sha512sums=('5fa789d08b556f97492b89515a89c2322c4b0a8fa95bd1035f5ed19061b3654a6a36a9911792096ac872ae9ae5451848cab87d0343dc0ffc064affea1f7d0d54' 'SKIP' 'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  patch -p1 -i "${srcdir}/cvs-2013.patch"
  [[ "$CARCH" =~ ^arm ]] && patch -p1 -i "${srcdir}/android-dev.patch"
  grep -v setproctitle configure.in > tmp
  mv tmp configure.in
  autoreconf -f
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  make PREFIX=/usr/bin DESTDIR="${pkgdir}" install

  install -Dm644 vtund.conf "${pkgdir}/usr/share/doc/vtun/examples/vtund.conf"
  sed -n '/--- Client config ---/,$p' vtund.conf > vtund-client.conf
  install -Dm600 vtund-client.conf "${pkgdir}/etc/vtund.conf"
  install -Dm644 Credits "${pkgdir}/usr/share/doc/vtun/Credits"
  install -Dm644 README.Setup "${pkgdir}/usr/share/doc/vtun/README.Setup"
  install -Dm644 README.Shaper "${pkgdir}/usr/share/doc/vtun/README.Shaper"
  install -Dm644 FAQ "${pkgdir}/usr/share/doc/vtun/FAQ"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/vtun/ChangeLog"
  cd "${pkgdir}"
  rm -r var/run var/lock
}
# vim: ts=2 sw=2 et ft=sh
