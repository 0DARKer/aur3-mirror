# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban

pkgname=jre
pkgver=7u1
pkgrel=1
pkgdesc="Java 7 Runtime Environment"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=('custom')
depends=('desktop-file-utils' 'libxtst' 'shared-mime-info' 'xdg-utils')
provides=('java-runtime=7')
conflicts=('java-runtime')
install=jre.install
source=("http://download.oracle.com/otn-pub/java/jdk/$pkgver-b08/jre-$pkgver-linux-i586.tar.gz"
        'java-policy-settings.desktop'
        'jre.sh'
        'jre.csh'
        'javaws-launcher')
md5sums=('d9b73cc5ccaa4f0b36cd6b8b62d07142'
         '6614b04176b9b7dfe26f22e9ce846801'
         '7cd3dc10e7a37468cad4053a067dcd01'
         'cc90df2df6fe80fab885a80036d420a1'
         '45c15a6b4767288f2f745598455ea2bf')
_arch=i386
[ "$CARCH" == 'x86_64' ] && _arch=amd64 && source[0]="http://download.oracle.com/otn-pub/java/jdk/$pkgver-b08/jre-$pkgver-linux-x64.tar.gz" && md5sums[0]='07bd73571b7028b73fc8ed19bc85226d'

package() {
  cd $(ls -1d jre1.7.0_*/ | tail -1)
  mkdir -p "$pkgdir"/{opt/java/jre/bin,usr/{share,lib/mozilla/plugins},/etc/.java/.systemPrefs}
  mv lib/desktop/{applications,icons,mime} "$pkgdir"/usr/share/
  rm -r plugin/desktop
  mv * "$pkgdir"/opt/java/jre

  cd ..
  ln -s /opt/java/jre/lib/$_arch/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/libnpjp2.so
  cp javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -D java-policy-settings.desktop "$pkgdir"/usr/share/applications/java-policy-settings.desktop
  install -D jre.sh "$pkgdir"/etc/profile.d/jre.sh
  cp jre.csh "$pkgdir"/etc/profile.d/
  install -D "$pkgdir"/{opt/java,usr/share/licenses}/jre/COPYRIGHT
  cp "$pkgdir"/opt/java/jre/{LICENSE,THIRDPARTYLICENSEREADME.txt} "$pkgdir"/usr/share/licenses/jre/
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "$pkgdir"/usr/share/applications/sun-javaws.desktop
}