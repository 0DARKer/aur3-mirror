# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz,
#               Thomas Dziedzic, Dan Serban, jjacky, EasySly

pkgname=jre
_major=8
_minor=20
_build=b26
pkgver=${_major}u$_minor
pkgrel=1
pkgdesc="Java Runtime Environment"
arch=('i686' 'x86_64')
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound'
            'ttf-dejavu: fonts')
provides=("java-runtime=$_major" "java-runtime-headless=$_major" "java-web-start=$_major")
conflicts=("${provides[@]}" "${provides[@]/$_major/7}")
backup=('opt/java/jre/lib/security/java.policy')
install=$pkgname.install
# Archs
_arch=x64
_arch2=amd64
if [ "$CARCH" = 'i686' ]; then
  _arch=i586
  _arch2=i386
fi
source=("http://download.oracle.com/otn-pub/java/jdk/$pkgver-$_build/$pkgname-$pkgver-linux-$_arch.tar.gz"
        'java-policy-settings.desktop'
        'javaws-launcher'
        'jre.csh'
        'jre.sh')
md5sums=(`curl -sL ${url/i*}/javase8-binaries-checksum-2133161.html | sed -nr "s|.*>${source##*/}</td> *<td>(<*[^<]*).*|\1|p"`
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '224bcf5e1f5d14bfa493ed243a5a5287'  # javaws-launcher
         '92dec202858f2bf7729c6805b1bd3ae4'  # jre.csh
         '85ba1d2b39d5e6efad89ef98d0f19909') # jre.sh
## Alternative mirrors, if your local one is throttled:
#source[0]="http://ftp.wsisiz.edu.pl/pub/pc/pozyteczne%20oprogramowanie/java/$pkgname-$pkgver-linux-$_arch.tar.gz"
#source[0]="http://uni-smr.ac.ru/archive/dev/java/JRE/$_major/JRE-$_major.$_minor/$pkgname-$pkgver-linux-$_arch.tar.gz"

DLAGENTS=('http::/usr/bin/curl -LC - -b "oraclelicense=a" -O')

package() {
  msg2 "Creating required dirs"
  cd jre1.$_major.0_$_minor
  mkdir -p "$pkgdir"/{opt/java/jre,usr/{lib/mozilla/plugins,share/licenses/jre},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Removing redundancies"
  rm -r plugin/ man/ja

  msg2 "Moving stuff in place"
  mv lib/desktop/* man "$pkgdir"/usr/share/
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/jre/
  mv * "$pkgdir"/opt/java/jre/

  msg2 "Symlinking plugin"
  ln -s /opt/java/jre/lib/$_arch2/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing additions"
  cd "$srcdir"
  install -m755 jre.{,c}sh "$pkgdir"/etc/profile.d/
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -m644 java-policy-settings.desktop "$pkgdir"/usr/share/applications/

  msg2 "Tweaking javaws .desktop"
  sed -e 's/Exec=javaws/&-launcher %f/' \
      -e '/NoDisplay=true/d' \
      -i "$pkgdir"/usr/share/applications/sun-javaws.desktop

  # Copy/paste from system clipboard to unsigned Java applets has been disabled since 6u24:
  # - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java
  # - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html
  msg2 "Enabling copy+paste to unsigned applets"
  _line=$(awk '/permission/{a=NR}; END{print a}' "$pkgdir"/opt/java/jre/lib/security/java.policy)
  sed "$_line a\\\\n \
        // (AUR) Allow unsigned applets to read system clipboard, see:\n \
        // - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java\n \
        // - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html\n \
        permission java.awt.AWTPermission \"accessClipboard\";" \
     -i "$pkgdir"/opt/java/jre/lib/security/java.policy
}