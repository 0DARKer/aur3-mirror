# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban, jjacky

pkgname=jre
_major=7
_minor=4
_pkg=${_major}u$_minor
pkgver=$_major.$_minor
pkgrel=1
pkgdesc="Java 7 Runtime Environment"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound support'
            'ttf-dejavu: fonts')
provides=('java-runtime=7' 'java-runtime-headless=7')
conflicts=('java-runtime' 'java-runtime-headless')
backup=('etc/profile.d/jre.sh'
        'etc/profile.d/jre.csh')
install=jre.install
_arch=i386; [ "$CARCH" = x86_64 ] && _arch=amd64
source=("http://uni-smr.ac.ru/archive/dev/java/JRE/oracle/7/jre-$_pkg-linux-i586.tar.gz"
        'java-policy-settings.desktop'
        'jre.sh'
        'jre.csh'
        'javaws-launcher')
md5sums=('8e6db43d9a7cac724be2cb9d1329e702'  # jre-$_pkg-linux-i586.tar.gz"
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '85ba1d2b39d5e6efad89ef98d0f19909'  # jre.sh
         'c106d744a5ca16a7bad35b3332818114'  # jre.csh
         '45c15a6b4767288f2f745598455ea2bf') # javaws-launcher
[ "$CARCH" == 'x86_64' ] && source[0]="http://uni-smr.ac.ru/archive/dev/java/JRE/oracle/7/jre-$_pkg-linux-x64.tar.gz" && md5sums[0]='49da2f0c288f96ed255ed75b796a5455'

package() {
  msg2 "Extracting sources..."
  bsdtar -xf jre-$_pkg-linux-*.tar.gz

  msg2 "Creating required dirs"
  cd $(ls -1d jre1.7.0_*/ | tail -1)
  mkdir -p "$pkgdir"/{opt/java/jre,usr/{share/{,licenses/jre},lib/mozilla/plugins},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Re-ordering folders a bit"
  mv lib/desktop/{applications,icons,mime} "$pkgdir"/usr/share/

  msg2 "Removing empty and redundant dirs"
  rm -r plugin
  rmdir lib/{applet,desktop}

  msg2 "Moving stuff in place"
  mv * "$pkgdir"/opt/java/jre/

  msg2 "Symlinking the plugin"
  cd "$srcdir"
  ln -s /opt/java/jre/lib/$_arch/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing scripts, .desktop file and licenses"
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -Dm644 java-policy-settings.desktop "$pkgdir"/usr/share/applications/java-policy-settings.desktop
  install -m755 jre.{c,}sh "$pkgdir"/etc/profile.d/
  cp "$pkgdir"/opt/java/jre/{COPYRIGHT,LICENSE,THIRDPARTYLICENSEREADME.txt} "$pkgdir"/usr/share/licenses/jre/

  msg2 "Tweaking the javaws .desktop file"
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "$pkgdir"/usr/share/applications/sun-javaws.desktop
}