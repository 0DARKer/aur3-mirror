# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban

pkgname=jre
pkgver=7u2
pkgrel=1
pkgdesc="Java 7 Runtime Environment"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=('custom')
depends=('desktop-file-utils' 'libxtst' 'shared-mime-info' 'xdg-utils')
provides=('java-runtime=7')
conflicts=('java-runtime')
install=jre.install
source=("http://download.oracle.com/otn-pub/java/jdk/$pkgver-b13/jre-$pkgver-linux-i586.tar.gz"
        'java-policy-settings.desktop'
        'jre.sh'
        'jre.csh'
        'javaws-launcher')
md5sums=('78923ef097586c36a6242c54cb20abd7'
         '6614b04176b9b7dfe26f22e9ce846801'
         '7cd3dc10e7a37468cad4053a067dcd01'
         'cc90df2df6fe80fab885a80036d420a1'
         '45c15a6b4767288f2f745598455ea2bf')
_arch=i386
[ "$CARCH" == 'x86_64' ] && _arch=amd64 && source[0]="http://download.oracle.com/otn-pub/java/jdk/$pkgver-b13/jre-$pkgver-linux-x64.tar.gz" && md5sums[0]='c6d0aa62337148787795870a12c17974'

package() {
  # Create required dirs
  cd $(ls -1d jre1.7.0_*/ | tail -1)
  mkdir -p "$pkgdir"/{opt/java/jre,usr/{share,lib/mozilla/plugins},/etc/.java/.systemPrefs}

  # Re-order folders a bit
  mv lib/desktop/{applications,icons,mime} "$pkgdir"/usr/share/

  # Remove empty and redundant dirs from 'lib'
  rm -r plugin
  rmdir lib/{applet,desktop}

  # Move stuff in place
  mv * "$pkgdir"/opt/java/jre

  # Symlink the plugin
  cd "$srcdir"
  ln -s /opt/java/jre/lib/$_arch/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/libnpjp2.so

  # Install scripts, .desktop file and licenses
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -Dm644 java-policy-settings.desktop "$pkgdir"/usr/share/applications/java-policy-settings.desktop
  install -D jre.sh "$pkgdir"/etc/profile.d/jre.sh
  install -m755 jre.csh "$pkgdir"/etc/profile.d/
  install -Dm644 "$pkgdir"/{opt/java,usr/share/licenses}/jre/COPYRIGHT
  cp "$pkgdir"/opt/java/jre/{LICENSE,THIRDPARTYLICENSEREADME.txt} "$pkgdir"/usr/share/licenses/jre/

  # Tweak the javaws .desktop file
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "$pkgdir"/usr/share/applications/sun-javaws.desktop
}