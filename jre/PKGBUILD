# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: Army <gmail.com: uli[dot]armbruster>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Guillaume ALAUX <Guillaume at alaux dot net>
# Contributor: Charles Ghislain <charlyghislain at gmail dot com>

pkgname=jre
pkgver=7
pkgrel=3
arch=('i686' 'x86_64')
  [ "${CARCH}" = 'i686' ]   && _arch='i586'
  [ "${CARCH}" = 'x86_64' ] && _arch='x64'
url='http://jdk7.java.net/'
license=('custom')
source=("http://download.oracle.com/otn-pub/java/jdk/7/jdk-7-linux-${_arch}.tar.gz"
        'java-policy-settings.desktop'
        'jre.profile'
        'jre.profile.csh'
        'javaws-launcher')
[ "${CARCH}" = 'i686' ] && md5sums=('f97244a104f03731e5ff69f0dd5a9927')
[ "${CARCH}" = 'x86_64' ] && md5sums=('b3c1ef5faea7b180469c129a49762b64')
md5sums+=('6614b04176b9b7dfe26f22e9ce846801'
         '7cd3dc10e7a37468cad4053a067dcd01'
         'cc90df2df6fe80fab885a80036d420a1'
         '45c15a6b4767288f2f745598455ea2bf')

package() {
  pkgdesc="Oracle's Java Runtime Environment"
  depends=('libxtst' 'desktop-file-utils' 'xdg-utils' 'shared-mime-info')
  provides=('java-runtime=7')
  conflicts=('java-runtime')
  install='jre.install'

  # main files
  mkdir -p ${pkgdir}/opt/java
  cp -r jdk1.7.0/jre ${pkgdir}/opt/java

  # move icons to appropriate location
  install -d ${pkgdir}/usr/share
  mv ${pkgdir}/opt/java/jre/lib/desktop/icons ${pkgdir}/usr/share

  # install .desktop entries
  mv ${pkgdir}/opt/java/jre/lib/desktop/applications ${pkgdir}/usr/share
  # desktop entries
  install -d ${pkgdir}/usr/share/applications
  install -m644 \
    ${srcdir}/java-policy-settings.desktop \
    ${pkgdir}/usr/share/applications

  # move mime files
  mv ${pkgdir}/opt/java/jre/lib/desktop/mime ${pkgdir}/usr/share

  # get rid of already installed files
  rm -r ${pkgdir}/opt/java/jre/plugin/desktop
  rm -r ${pkgdir}/opt/java/jre/lib/desktop

  # TODO: find out the purpose of ./jre/lib/images/icons

  # profiles
  install -D ${srcdir}/jre.profile \
    ${pkgdir}/etc/profile.d/jre.sh
  install -D ${srcdir}/jre.profile.csh \
    ${pkgdir}/etc/profile.d/jre.csh

  # java browser plugin
  mkdir -p ${pkgdir}/usr/lib/mozilla/plugins
  if [ "$CARCH" = "i686" ]; then
    ln -s /opt/java/jre/lib/i386/libnpjp2.so \
      ${pkgdir}/usr/lib/mozilla/plugins
  else
    ln -s /opt/java/jre/lib/amd64/libnpjp2.so \
      ${pkgdir}/usr/lib/mozilla/plugins
  fi

  # licenses
  install -d ${pkgdir}/usr/share/licenses/jre
  install -m644 \
    ${pkgdir}/opt/java/jre/{COPYRIGHT,LICENSE,THIRDPARTYLICENSEREADME.txt} \
    ${pkgdir}/usr/share/licenses/jre

  # Clicking a .jnlp file does not launch that file but launch javaws -viewer instead FS#22509
  install ${srcdir}/javaws-launcher \
    ${pkgdir}/opt/java/jre/bin
  sed \
    -e 's/Exec=javaws/&-launcher %f/' \
    -e '/NoDisplay=true/d' \
    -i ${pkgdir}/usr/share/applications/sun-javaws.desktop

  # Fix system prefs folder FS#18872
  install -d ${pkgdir}/etc/.java/.systemPrefs
}
