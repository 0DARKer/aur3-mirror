# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban, jjacky, EasySly

pkgname=jre
_major=7
_minor=9
_build=b05
_pkg=${_major}u$_minor
pkgver=$_major.$_minor
pkgrel=1
pkgdesc="Java $_major Runtime Environment"
arch=('i686' 'x86_64')
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound support'
            'ttf-dejavu: fonts')
provides=("java-runtime=$_major" "java-runtime-headless=$_major" "java-web-start=$_major")
conflicts=("${provides[@]}")
backup=('etc/profile.d/jre.sh'
        'etc/profile.d/jre.csh')
options=('!emptydirs')
install=jre.install
_arch=i386; [ "$CARCH" = 'x86_64' ] && _arch=amd64
source=("http://download.oracle.com/otn-pub/java/jdk/$_pkg-$_build/jre-$_pkg-linux-i586.tar.gz"
        'java-policy-settings.desktop'
        'javaws-launcher'
        'jre.csh'
        'jre.sh')
md5sums=('56178ed00dab2ebd8268caf5575743f4'  # jre-$_pkg-linux-i586.tar.gz
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '45c15a6b4767288f2f745598455ea2bf'  # javaws-launcher
         '92dec202858f2bf7729c6805b1bd3ae4'  # jre.csh
         '85ba1d2b39d5e6efad89ef98d0f19909') # jre.sh
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -O --header "Cookie: gpw_e24=h"')
[ "$CARCH" = 'x86_64' ] && source[0]="http://download.oracle.com/otn-pub/java/jdk/$_pkg-$_build/jre-$_pkg-linux-x64.tar.gz" && md5sums[0]='8e17fa7b2152ab11f915c6936542cc12'

PKGEXT=".pkg.tar"

package() {
  msg2 "Creating required dirs"
  cd jre1.$_major.0_0$_minor
  mkdir -p "$pkgdir"/{opt/java/jre,usr/{lib/mozilla/plugins,share/licenses/jre},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Removing the redundancies"
  rm -r plugin/ man/ja

  msg2 "Moving stuff in place"
  mv lib/desktop/{applications,icons,mime} man "$pkgdir"/usr/share/
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/jre/
  mv * "$pkgdir"/opt/java/jre/

  msg2 "Symlinking the plugin"
  ln -s /opt/java/jre/lib/$_arch/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing the scripts, confs and .desktops of our own"
  cd "$srcdir"
  install -m755 jre.{,c}sh "$pkgdir"/etc/profile.d/
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -m644 java-policy-settings.desktop "$pkgdir"/usr/share/applications/

  msg2 "Tweaking the javaws .desktop file"
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "$pkgdir"/usr/share/applications/sun-javaws.desktop
}