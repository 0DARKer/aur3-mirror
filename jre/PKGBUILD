# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz,
#               Thomas Dziedzic, Dan Serban, jjacky, EasySly

pkgname=jre
_major=8
_minor=0
_build=b132
pkgver=${_major}u${_minor}
pkgrel=1
pkgdesc="Java Runtime Environment"
arch=('i686' 'x86_64')
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound'
            'ttf-dejavu: fonts')
provides=("java-runtime=${_major}" "java-runtime-headless=${_major}" "java-web-start=${_major}")
conflicts=("${provides[@]}" "${provides[@]/${_major}/7}")
backup=('etc/profile.d/jre.csh'
        'etc/profile.d/jre.sh')
install=${pkgname}.install
# Workaround for the AUR Web interface Source parser
if [ "${CARCH}" = 'x86_64' ]; then
    _arch=x64
    _arch2=amd64
else
    _arch=i586
    _arch2=i386
fi
source=("http://download.oracle.com/otn-pub/java/jdk/${_major}-${_build}/jre-${_major}-linux-${_arch}.tar.gz"
        'java-policy-settings.desktop'
        'javaws-launcher'
        'jre.csh'
        'jre.sh')
md5sums=('045a0309585e546fa2da2316309c09ea'  # jre-${_major}-linux-i586.tar.gz
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '45c15a6b4767288f2f745598455ea2bf'  # javaws-launcher
         '92dec202858f2bf7729c6805b1bd3ae4'  # jre.csh
         '85ba1d2b39d5e6efad89ef98d0f19909') # jre.sh
[ "$CARCH" = 'x86_64' ] && md5sums[0]='1e024eb9b0f7f61722e10fc08c873543'  # jre-${_major}-linux-x64.tar.gz
## Alternative mirrors, if your local one is throttled:
#source[0]="http://ftp.wsisiz.edu.pl/pub/pc/pozyteczne%20oprogramowanie/java/jre-${_major}-linux-${_arch}.tar.gz"
#source[0]="http://uni-smr.ac.ru/archive/dev/java/JRE/${_major}/JRE-${_major}.${_minor}/jre-${_major}-linux-${_arch}.tar.gz"

DLAGENTS=('http::/usr/bin/curl -LC - -b "oraclelicense=a" -O')

package() {
  msg2 "Creating required dirs"
  cd jre1.${_major}.0
  mkdir -p "${pkgdir}"/{opt/java/jre,usr/{lib/mozilla/plugins,share/licenses/jre},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Removing redundancies"
  rm -r plugin/ man/ja

  msg2 "Moving stuff in place"
  mv lib/desktop/* man "${pkgdir}"/usr/share/
  mv COPYRIGHT LICENSE *.txt "${pkgdir}"/usr/share/licenses/jre/
  mv * "${pkgdir}"/opt/java/jre/

  msg2 "Symlinking plugin"
  ln -s /opt/java/jre/lib/${_arch2}/libnpjp2.so "${pkgdir}"/usr/lib/mozilla/plugins/

  msg2 "Installing additions"
  cd "${srcdir}"
  install -m755 jre.{,c}sh "${pkgdir}"/etc/profile.d/
  install -m755 javaws-launcher "${pkgdir}"/opt/java/jre/bin/
  install -m644 java-policy-settings.desktop "${pkgdir}"/usr/share/applications/

  msg2 "Tweaking javaws .desktop"
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "${pkgdir}"/usr/share/applications/sun-javaws.desktop
}