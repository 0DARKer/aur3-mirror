# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban

pkgname=jre
pkgver=7u3
_build=b04
pkgrel=1
pkgdesc="Java 7 Runtime Environment"
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
arch=('i686' 'x86_64')
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: basic sound support'
            'ttf-dejavu: fonts')
provides=('java-runtime=7' 'java-runtime-headless=7')
conflicts=('java-runtime' 'java-runtime-headless')
backup=(etc/profile.d/jre.sh
        etc/profile.d/jre.csh)
install=jre.install
_arch=i386; [ "$CARCH" == 'x86_64' ] && _arch=amd64
source=("http://download.oracle.com/otn-pub/java/jdk/$pkgver-$_build/jre-$pkgver-linux-i586.tar.gz"
        'java-policy-settings.desktop'
        'jre.sh'
        'jre.csh'
        'javaws-launcher')
md5sums=('cfce10a05f8d152d39aef892f2cd4011'
         '6614b04176b9b7dfe26f22e9ce846801'
         '85ba1d2b39d5e6efad89ef98d0f19909'
         'c106d744a5ca16a7bad35b3332818114'
         '45c15a6b4767288f2f745598455ea2bf')
[ "$CARCH" == 'x86_64' ] && source[0]="http://download.oracle.com/otn-pub/java/jdk/$pkgver-$_build/jre-$pkgver-linux-x64.tar.gz" && md5sums[0]='3d3e206cea84129f1daa8e62bf656a28'

package() {
  # Create required dirs
  cd $(ls -1d jre1.7.0_*/ | tail -1)
  mkdir -p "$pkgdir"/{opt/java/jre,usr/{share/{,licenses/jre},lib/mozilla/plugins},etc/{.java/.systemPrefs,profile.d}}

  # Re-order folders a bit
  mv lib/desktop/{applications,icons,mime} "$pkgdir"/usr/share/

  # Remove empty and redundant dirs
  rm -r plugin
  rmdir lib/{applet,desktop}

  # Move stuff in place
  mv * "$pkgdir"/opt/java/jre

  # Symlink the plugin
  cd "$srcdir"
  ln -s /opt/java/jre/lib/$_arch/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/libnpjp2.so

  # Install scripts, .desktop file and licenses
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -Dm644 java-policy-settings.desktop "$pkgdir"/usr/share/applications/java-policy-settings.desktop
  install -m755 jre.{c,}sh "$pkgdir"/etc/profile.d/
  cp "$pkgdir"/opt/java/jre/{COPYRIGHT,LICENSE,THIRDPARTYLICENSEREADME.txt} "$pkgdir"/usr/share/licenses/jre/

  # Tweak the javaws .desktop file
  sed -e 's/Exec=javaws/&-launcher %f/' -e '/NoDisplay=true/d' -i "$pkgdir"/usr/share/applications/sun-javaws.desktop
}