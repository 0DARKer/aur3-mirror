# Maintainer: Det
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz,
#               Thomas Dziedzic, Dan Serban, jjacky, EasySly

pkgname=jre
_major=8
_minor=20
_build=b26
pkgver=${_major}u$_minor
pkgrel=2
pkgdesc="Java Runtime Environment"
arch=('i686' 'x86_64')
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'java-common' 'libxrender' 'libxtst' 'shared-mime-info'
         'xdg-utils')
optdepends=('alsa-lib: for basic sound support'
            'gtk2: for Gtk+ look and feel (desktop)'
            'ttf-font: fonts')
provides=("java-runtime=$_major" "java-runtime-headless=$_major" "java-web-start=$_major"
          "java-runtime-jre=$_major" "java-runtime-headless-jre=$_major" "java-web-start-jre=$_major")

# Variables
_arch=x64
_arch2=amd64
if [[ "$CARCH" = 'i686' ]]; then
  _arch=i586
  _arch2=i386
fi
DLAGENTS=('http::/usr/bin/curl -LC - -b "oraclelicense=a" -O')
_jname=${pkgname}$_major
_jvmdir=/usr/lib/jvm/java-$_major-$pkgname/jre

backup=("etc/java-$_jname/$_arch2/jvm.cfg"
        "etc/java-$_jname/$_arch2/server/Xusage.txt"
        "etc/java-$_jname/images/cursors/cursors.properties"
        "etc/java-$_jname/management/jmxremote.access"
        "etc/java-$_jname/management/jmxremote.password.template"
        "etc/java-$_jname/management/management.properties"
        "etc/java-$_jname/management/snmp.acl.template"
        "etc/java-$_jname/security/java.policy"
        "etc/java-$_jname/security/java.security"
        "etc/java-$_jname/security/javaws.policy"
        "etc/java-$_jname/calendars.properties"
        "etc/java-$_jname/content-types.properties"
        "etc/java-$_jname/flavormap.properties"
        "etc/java-$_jname/fontconfig.properties.src"
        "etc/java-$_jname/hijrah-config-umalqura.properties"
        "etc/java-$_jname/javafx.properties"
        "etc/java-$_jname/jvm.hprof.txt"
        "etc/java-$_jname/logging.properties"
        "etc/java-$_jname/net.properties"
        "etc/java-$_jname/psfont.properties.ja"
        "etc/java-$_jname/psfontj2d.properties"
        "etc/java-$_jname/sound.properties")
install=$pkgname.install
source=("http://download.oracle.com/otn-pub/java/jdk/$pkgver-$_build/$pkgname-$pkgver-linux-$_arch.tar.gz"
        "policytool-$_jname.desktop")
md5sums=(`curl -sL ${url/i*}/javase8-binaries-checksum-2133161.html | sed -nr "s|.*>${source##*/}</td> *<td>(<*[^<]*).*|\1|p"`
         '762729fa0faba8ff2b5a29c249c95079') # policytool-$_jname.desktop
## Alternative mirror, if your local one is throttled:
#source[0]="http://ftp.wsisiz.edu.pl/pub/pc/pozyteczne%20oprogramowanie/java/$pkgname-$pkgver-linux-$_arch.gz"

package() {
  msg2 "Creating required dirs"
  cd ${pkgname}1.$_major.0_$_minor
  install -d "$pkgdir"/{usr/{lib/{jvm/java-$_major-$pkgname/jre/bin,mozilla/plugins},share/licenses/java$_major-$pkgname},etc/.java/.systemPrefs}

  msg2 "Preparing"
  # Link NPAPI plugin
  ln -sf $_jvmdir/lib/$_arch2/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/libnpjp2-$_jname.so

  # Suffix .desktops, icons and MIME packages
  for i in $(find lib/desktop/ -type f); do
    rename -- . -$_jname. $i
  done

  # Suffix man pages
  rename -- .1 -$_jname.1 man/{,ja_JP.UTF-8/}man1/*

  # Fix .desktop paths
  sed -e "s,Exec=,&$_jvmdir/bin/," \
      -e "s/\.png/-$_jname/" \
      -i lib/desktop/applications/*

  msg2 "Removing redundancies"
  rm -r lib/fontconfig.*.{bfc,properties.src} man/ja plugin/

  msg2 "Moving stuff in place"
  # .desktops + icons
  mv lib/desktop/* "$pkgdir"/usr/share/
  install -m644 "$srcdir"/*.desktop "$pkgdir"/usr/share/applications/

  # Move/link configs: /usr/lib/jvm/java-$_jname/jre/lib -> /etc
  for new_etc in ${backup[@]}; do
    old_usr=lib/${new_etc#*$_jname/}
    install -Dm644 $old_usr "$pkgdir"/$new_etc
    ln -sf /$new_etc $old_usr
  done

  # Man pages
  mv man/ja_JP.UTF-8/ man/ja/
  mv man/ "$pkgdir"/usr/share/

  # Licenses
  mv COPYRIGHT LICENSE README *.txt "$pkgdir"/usr/share/licenses/java$_major-$pkgname/
  ln -sf /usr/share/licenses/java$_major-$pkgname/ "$pkgdir"/usr/share/licenses/$pkgname

  # Do the move
  mv * "$pkgdir"/$_jvmdir

  msg2 "Enabling copy+paste to unsigned applets"
  # Copy/paste from system clipboard to unsigned Java applets has been disabled since 6u24:
  # - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java
  # - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html
  _line=$(awk '/permission/{a=NR}; END{print a}' "$pkgdir"/etc/java-$_jname/security/java.policy)
  sed "$_line a\\\\n \
        // (AUR) Allow unsigned applets to read system clipboard, see:\n \
        // - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java\n \
        // - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html\n \
        permission java.awt.AWTPermission \"accessClipboard\";" \
     -i "$pkgdir"/etc/java-$_jname/security/java.policy
}

# FIXME: Use Arch's certs?
# Link JKS keystore from ca-certificates-java
#rm -f "$pkgdir"/$_jvmdir/lib/security/cacerts
#ln -sf /etc/ssl/certs/java/cacerts "$pkgdir"/$_jvmdir/lib/security/cacerts