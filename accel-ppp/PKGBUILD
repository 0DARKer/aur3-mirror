# Contributor: Brian F.G. <bidulock@openss7.org>
# Maintainer: Alexandr Boiko <brdcom@ya.ru>
pkgname=accel-ppp
pkgver=1.7.4
pkgrel=3
pkgdesc="Kernel-mode PPTP/PPPoE/L2TP server"
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/trac/accel-ppp/"
license=('GPL')
depends=('openssl>=0.9.8' 'pcre' 'libnl')
makedepends=('cmake>=2.6' 'libnl1' 'net-snmp>=5.x')
conflicts=('accel-ppp-git' 'accel-ppp-lts-git')
backup=('etc/accel-ppp.conf' 'etc/snmp/accel-ppp.conf')
install='accel-ppp.install'
options=('docs')
source=(http://sourceforge.net/projects/${pkgname}/files/$pkgname-$pkgver.tar.bz2
	accel-ppp-default
	accel-pppd.service
	accel-ppp.tmpfiles)
md5sums=('28116f1b343ceb672ba1c00393903d5f'
         'bb3e20c8808000f4efe52230c13a13f3'
         'e5dec17bd405052fc647a805db5dd449'
         '5be7d42b434b74d7e692b19a9e3c4297')

build() {
	cd ${srcdir}
	sed -i 's|RUNTIME DESTINATION sbin|RUNTIME DESTINATION bin|' \
		"${pkgname}-${pkgver}/accel-pppd/CMakeLists.txt"
	if [ -d "${pkgname}-build" ]; then
		rm -fr ${pkgname}-build
		mkdir ${pkgname}-build
	else
		mkdir ${pkgname}-build
	fi
	cd ${pkgname}-build
	cmake \
		-DCMAKE_SYSTEM_NAME=Linux \
		-DBUILD_DRIVER=FALSE \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DLOG_FILE=TRUE \
		-DLOG_PGSQL=FALSE \
		-DCPACK_TYPE=Archlinux \
		-DSHAPER=TRUE \
		-DRADIUS=TRUE \
		-DNETSNMP=TRUE \
		-DCPACK_PACKAGE_VERSION="${pkgver}" \
		../${pkgname}-${pkgver}
	make || return 1
}
package() {
	cd ${srcdir}/${pkgname}-build
	make DESTDIR=${pkgdir} install || return 1

	install -dm0755 ${pkgdir}/etc/snmp/
	touch ${pkgdir}/etc/snmp/${pkgname}.conf
	install -Dm0644 ${srcdir}/${pkgname}-default ${pkgdir}/etc/conf.d/accel-pppd
	install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/README ${pkgdir}/usr/share/doc/${pkgname}/README
	install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/accel-pppd/${pkgname}.conf ${pkgdir}/etc/${pkgname}.conf
	install -Dm0644 ${srcdir}/accel-pppd.service ${pkgdir}/usr/lib/systemd/system/accel-pppd.service
	install -Dm0644 ${srcdir}/${pkgname}.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf
	install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/accel-pppd/extra/net-snmp/ACCEL-PPP-MIB.txt ${pkgdir}/usr/share/snmp/mibs/ACCEL-PPP-MIB.txt
}
