# Contributor: Brian F.G. <bidulock@openss7.org>
# Maintainer: Alexandr Boiko <brdcom@ya.ru>
pkgname=accel-ppp
pkgver=1.9.0
pkgrel=1
pkgdesc="High performance PPTP/L2TP/PPPoE/IPoE server"
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/trac/accel-ppp/"
license=('GPL')
depends=('openssl>=0.9.8' 'pcre' 'libnl')
makedepends=('cmake>=2.6' 'libnl1' 'net-snmp>=5.x' 'lua51')
conflicts=('accel-ppp-git')
install='accel-ppp.install'
options=('docs')
backup=('etc/accel-ppp.conf' 'etc/accel-ppp.lua' 'etc/snmp/accel-ppp.conf')
source=(http://sourceforge.net/projects/${pkgname}/files/$pkgname-$pkgver.tar.bz2
	accel-ppp-default
	accel-ppp.lua
	accel-ppp.tmpfiles
	accel-pppd.service
	ipoe.conf)

build() {
	cd ${srcdir}
	sed -i 's|RUNTIME DESTINATION sbin|RUNTIME DESTINATION bin|' \
		"${pkgname}-${pkgver}/accel-pppd/CMakeLists.txt"
	if [ -d "${pkgname}-build" ]; then
		rm -fr ${pkgname}-build
		mkdir ${pkgname}-build
	else
		mkdir ${pkgname}-build
	fi
	cd ${pkgname}-build
	cmake \
		-DCMAKE_SYSTEM_NAME=Linux \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DLOG_FILE=TRUE \
		-DLOG_PGSQL=FALSE \
		-DSHAPER=TRUE \
		-DRADIUS=TRUE \
		-DNETSNMP=TRUE \
		-DLUA=TRUE \
		-DLUA_INCLUDE_DIR="/usr/include/lua5.1" \
		-DBUILD_IPOE_DRIVER=TRUE \
		-DKDIR="/usr/lib/modules/`uname -r`/build" \
		../${pkgname}-${pkgver}
	make || return 1
}
package() {
	cd ${srcdir}/${pkgname}-build
	make DESTDIR=${pkgdir} install || return 1

	install -dm0755 ${pkgdir}/etc/snmp/
	touch ${pkgdir}/etc/snmp/${pkgname}.conf
	install -Dm0644 ${srcdir}/${pkgname}.lua ${pkgdir}/etc/${pkgname}.lua
	install -Dm0644 ${srcdir}/${pkgname}-default ${pkgdir}/etc/conf.d/accel-pppd
	install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/README ${pkgdir}/usr/share/doc/${pkgname}/README
	install -Dm0644 ${srcdir}/accel-pppd.service ${pkgdir}/usr/lib/systemd/system/accel-pppd.service
	install -Dm0644 ${srcdir}/${pkgname}.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf
	install -Dm0644 ${srcdir}/ipoe.conf ${pkgdir}/usr/lib/modules-load.d/ipoe.conf
	install -Dm0644 ${pkgdir}/etc/${pkgname}.conf.dist ${pkgdir}/etc/${pkgname}.conf
	install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/accel-pppd/extra/net-snmp/ACCEL-PPP-MIB.txt ${pkgdir}/usr/share/snmp/mibs/ACCEL-PPP-MIB.txt
	install -Dm0644 ${srcdir}/${pkgname}-build/drivers/ipoe/driver/ipoe.ko $pkgdir/usr/lib/modules/`uname -r`/extramodules/ipoe.ko
}
md5sums=('9b02c5c42ad939ea393811f97e844f54'
         'bb3e20c8808000f4efe52230c13a13f3'
         '23e32b0d758be2adf0f7b6987857afc2'
         '5be7d42b434b74d7e692b19a9e3c4297'
         'e5dec17bd405052fc647a805db5dd449'
         'ecf99787915602dcedef531d5f8c42c9')
