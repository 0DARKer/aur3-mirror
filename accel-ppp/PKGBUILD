# Contributor: Alexandr Boiko <brdcom@ya.ru>
pkgname=accel-ppp
pkgver=1.7.3
pkgrel=3
pkgdesc="Kernel-mode PPTP/PPPoE/L2TP server"
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/trac/accel-ppp/"
license=('GPL')
depends=('openssl>=0.9.8' 'pcre')
makedepends=('cmake>=2.6' 'libnl1')
optdepends=('net-snmp>=5.x: snmp module support')
conflicts=(accel-ppp-git)
install='accel-ppp.install'
options=('docs')
source=(http://sourceforge.net/projects/${pkgname}/files/$pkgname-$pkgver.tar.bz2
	accel-ppp-default
	accel-pppd
	accel-pppd.service
	accel-ppp.tmpfiles)
md5sums=('bf03f20d9df9a0694e5cd0be9b57d1b6'
         '8bbf73edcbfb2d860551f7645cf5b5ce'
         '84817c7ee9c03373177f0641ef6171af'
         '780144b0e67a408adb0d9832fac0afa1'
         'a31e2819f157290ee3bc7358444ef26f')

build() {
	cd ${srcdir}
	mkdir ${pkgname}-build
	cd ${pkgname}-build
	cmake \
		-DBUILD_DRIVER=FALSE \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DLOG_PGSQL=FALSE \
		-DCPACK_TYPE=Archlinux \
		-DSHAPER=TRUE \
		-DRADIUS=TRUE \
		-DNETSNMP=TRUE \
		-DCPACK_PACKAGE_VERSION="1.7.3" \
		../${pkgname}-${pkgver}
	make || return 1
}
package() {
	cd ${srcdir}/${pkgname}-build
	make DESTDIR=${pkgdir} install || return 1

	install -dm0755 ${pkgdir}/etc/snmp/
	touch ${pkgdir}/etc/snmp/${pkgname}.conf

	install -Dm0644 ${srcdir}/${pkgname}-default ${pkgdir}/etc/conf.d/accel-pppd
	install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/README ${pkgdir}/usr/share/doc/${pkgname}/README
	install -D -m0755 ${srcdir}/accel-pppd ${pkgdir}/etc/rc.d/accel-pppd
	install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/accel-pppd/${pkgname}.conf ${pkgdir}/etc/${pkgname}.conf
	install -Dm0644 ${srcdir}/accel-pppd.service ${pkgdir}/usr/lib/systemd/system/accel-pppd.service
	install -Dm0644 ${srcdir}/${pkgname}.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf
}
