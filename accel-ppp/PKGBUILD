# Contributor: Brian F.G. <bidulock@openss7.org>
# Maintainer: Alexandr Boiko <brdcom@ya.ru>
pkgname=accel-ppp
pkgver=1.7.4
pkgrel=1
pkgdesc="Kernel-mode PPTP/PPPoE/L2TP server"
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/trac/accel-ppp/"
license=('GPL')
depends=('openssl>=0.9.8' 'pcre')
makedepends=('cmake>=2.6' 'linux-headers')
optdepends=('net-snmp>=5.x: snmp module support')
conflicts=(accel-ppp-git)
backup=('etc/accel-ppp.conf')
install='accel-ppp.install'
options=('docs')
source=(http://sourceforge.net/projects/${pkgname}/files/$pkgname-$pkgver.tar.bz2
	accel-ppp-default
	accel-pppd.service
	accel-ppp.tmpfiles)
md5sums=('28116f1b343ceb672ba1c00393903d5f'
         'e6922a52102c5d8562310bdfd17ba9ad'
         'e5dec17bd405052fc647a805db5dd449'
         '5be7d42b434b74d7e692b19a9e3c4297')

build() {
	cd ${srcdir}
	sed -i 's|RUNTIME DESTINATION sbin|RUNTIME DESTINATION bin|' \
		"${pkgname}-${pkgver}/accel-pppd/CMakeLists.txt"
	mkdir ${pkgname}-build
	cd ${pkgname}-build
	cmake \
		-DBUILD_DRIVER=FALSE \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DLOG_PGSQL=FALSE \
		-DCPACK_TYPE=Archlinux \
		-DRADIUS=TRUE \
		-DCPACK_PACKAGE_VERSION="1.7.3" \
		../${pkgname}-${pkgver}
	make || return 1
}
package() {
	cd ${srcdir}/${pkgname}-build
	make DESTDIR=${pkgdir} install || return 1

	install -dm0755 ${pkgdir}/etc/snmp/
	touch ${pkgdir}/etc/snmp/${pkgname}.conf

	install -Dm0644 ${srcdir}/${pkgname}-default ${pkgdir}/etc/conf.d/accel-pppd
	install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/README ${pkgdir}/usr/share/doc/${pkgname}/README
	install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/accel-pppd/${pkgname}.conf ${pkgdir}/etc/${pkgname}.conf
	install -Dm0644 ${srcdir}/accel-pppd.service ${pkgdir}/usr/lib/systemd/system/accel-pppd.service
	install -Dm0644 ${srcdir}/${pkgname}.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf
}
