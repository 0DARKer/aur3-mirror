# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Yann Lopez <arch at lonewulf dot net>
pkgname=babeld
pkgver=1.5.1
pkgrel=1
pkgdesc="A loop-avoiding distance-vector routing protocol"
arch=('i686' 'x86_64')
url="http://www.pps.univ-paris-diderot.fr/~jch/software/babel/"
license=('MIT')
depends=('glibc')
optdepends=('ahcpd: for autoconfiguration of addresses and other informations')
backup=('etc/babeld.conf')
source=("http://www.pps.univ-paris-diderot.fr/~jch/software/files/$pkgname-$pkgver.tar.gz"
  "http://www.pps.univ-paris-diderot.fr/~jch/software/files/$pkgname-$pkgver.tar.gz.asc"
  "babeld.conf"
  "babeld.service")
md5sums=('20e3284d5ad291d7ba2ad91d5b47de10'
         'SKIP'
         'e51d49295075e6137679f6c70ed45f97'
         'd6407e493f79aad1887394639ef5d50e')
sha1sums=('6ff3a7685e62034df83b143a36a4960b2e4d89b9'
          'SKIP'
          '531cb04a793534a080885be0417db343239b8d51'
          '864840cfafeeb3f29c0f91421bc210a8c1f4b245')
sha256sums=('73aac784d57a02932cdf1774099927b85bc746fbd4807098c1fed62a85b509b2'
            'SKIP'
            '732ad2444013f3579bbfac2fd71934418f0c5298b0ca7365a44df2dbc7f858d5'
            'fde7554e0d8a200d99b0766bd0ce23cbd7d14593c43d96de158e2fa2c379c1c5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="/usr" TARGET="$pkgdir" install

  install -Dm644 ../babeld.conf "${pkgdir}"/etc/babeld.conf
  install -Dm644 ../babeld.service "${pkgdir}"/usr/lib/systemd/system/babeld.service
}
