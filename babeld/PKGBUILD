# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Yann Lopez <arch at lonewulf dot net>
pkgname=babeld
pkgver=1.4.2
pkgrel=3
pkgdesc="A loop-avoiding distance-vector routing protocol"
arch=('i686' 'x86_64')
url="http://www.pps.univ-paris-diderot.fr/~jch/software/babel/"
license=('custom')
depends=('glibc')
backup=('etc/babeld.conf')
source=("http://www.pps.univ-paris-diderot.fr/~jch/software/files/$pkgname-$pkgver.tar.gz"
        "babeld.conf"
        "babeld.service")
md5sums=('a48e8129f221f66640ae1ccf73190991'
         'c49169935cb756d3c4380184997a451e'
         'd6407e493f79aad1887394639ef5d50e')
optdepends=('ahcpd: for autoconfiguration of addresses and other informations')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="/usr" TARGET="$pkgdir" install

  install -Dm644 ../babeld.conf "${pkgdir}"/etc/babeld.conf
  install -Dm644 ../babeld.service "${pkgdir}"/usr/lib/systemd/system/babeld.service

  # Install custom license
  install -Dm644 LICENCE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENCE
}
