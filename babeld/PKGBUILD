# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Yann Lopez <arch at lonewulf dot net>
pkgname=babeld
pkgver=1.5.0
pkgrel=1
pkgdesc="A loop-avoiding distance-vector routing protocol"
arch=('i686' 'x86_64')
url="http://www.pps.univ-paris-diderot.fr/~jch/software/babel/"
license=('MIT')
depends=('glibc')
optdepends=('ahcpd: for autoconfiguration of addresses and other informations')
backup=('etc/babeld.conf')
source=("http://www.pps.univ-paris-diderot.fr/~jch/software/files/$pkgname-$pkgver.tar.gz"
  "http://www.pps.univ-paris-diderot.fr/~jch/software/files/$pkgname-$pkgver.tar.gz.asc"
  "babeld.conf"
  "babeld.service")
md5sums=('ac884beb644792bdb79f0042755820ee'
         'SKIP'
         'e51d49295075e6137679f6c70ed45f97'
         'd6407e493f79aad1887394639ef5d50e')
sha1sums=('9a1c19a8934a4437fa48e3c410c3a262d2e11c9f'
          'SKIP'
          '531cb04a793534a080885be0417db343239b8d51'
          '864840cfafeeb3f29c0f91421bc210a8c1f4b245')
sha256sums=('faa473b3bf58b568b8bcc24028bc0deb4bb7596eb6bb007c388b1cadf20ff552'
            'SKIP'
            '732ad2444013f3579bbfac2fd71934418f0c5298b0ca7365a44df2dbc7f858d5'
            'fde7554e0d8a200d99b0766bd0ce23cbd7d14593c43d96de158e2fa2c379c1c5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="/usr" TARGET="$pkgdir" install

  install -Dm644 ../babeld.conf "${pkgdir}"/etc/babeld.conf
  install -Dm644 ../babeld.service "${pkgdir}"/usr/lib/systemd/system/babeld.service
}
