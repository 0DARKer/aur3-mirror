# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Yann Lopez <arch at lonewulf dot net>
pkgname=babeld
pkgver=1.4.3
pkgrel=2
pkgdesc="A loop-avoiding distance-vector routing protocol"
arch=('i686' 'x86_64')
url="http://www.pps.univ-paris-diderot.fr/~jch/software/babel/"
license=('MIT')
depends=('glibc')
optdepends=('ahcpd: for autoconfiguration of addresses and other informations')
backup=('etc/babeld.conf')
source=("http://www.pps.univ-paris-diderot.fr/~jch/software/files/$pkgname-$pkgver.tar.gz"
  "http://www.pps.univ-paris-diderot.fr/~jch/software/files/$pkgname-$pkgver.tar.gz.asc"
  "babeld.conf"
  "babeld.service")
md5sums=('959f7037b0a2f31f29975e90dbda3a6b'
         'SKIP'
         '277dda176eb683f8f05b371ea96e7fdb'
         'd6407e493f79aad1887394639ef5d50e')
sha1sums=('ddb124ec68510403e3c1e1d228064e656917371a'
          'SKIP'
          '5d6f8247b61ba7e53b5cc75dc6ffaf36c428182e'
          '864840cfafeeb3f29c0f91421bc210a8c1f4b245')
sha256sums=('d0b80fa9714591b13bb695af32b5116e21286687df5f2e814cf4f57ecf030ba3'
            'SKIP'
            '973b0ee83a4aaf7dd5ef1a70315ec3557bfa474214409ab66a3b83d888702e69'
            'fde7554e0d8a200d99b0766bd0ce23cbd7d14593c43d96de158e2fa2c379c1c5')


build() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="/usr" TARGET="$pkgdir" install

  install -Dm644 ../babeld.conf "${pkgdir}"/etc/babeld.conf
  install -Dm644 ../babeld.service "${pkgdir}"/usr/lib/systemd/system/babeld.service
}
