# Maintainer: Max Roder <maxroder@web.de>

pkgname=openkore-svn
pkgver=7853
pkgrel=1
pkgdesc="A custom Ragnarok online client."
arch=('any')
url="http://openkore.com"
license=('GPL')
depends=('perl' 'perl-time-hires' 'perl-io-compress')
makedepends=('subversion')
provides=('openkore')
install='openkore-svn.install'

_svntrunk="https://openkore.svn.sourceforge.net/svnroot/openkore/openkore/trunk/"
_svnmod="openkore"

_svnfields="https://openkore.svn.sourceforge.net/svnroot/openkore/fieldpack/trunk/fields/"
_svntables="https://openkore.svn.sourceforge.net/svnroot/openkore/tablepack/trunk/tables/"
_svncontrol="https://openkore.svn.sourceforge.net/svnroot/openkore/confpack/trunk/control/"

build() {
	cd ${srcdir}

	msg "Getting main program..."

	if [ -d ${_svnmod}/.svn ]; then
		(cd ${_svnmod} && svn up -r $pkgver)
	else
		svn co ${_svntrunk} --config-dir ./ -r $pkgver ${_svnmod}
	fi

	msg "SVN checkout done or server timeout"

	mkdir -p ${srcdir}/fields ${srcdir}/tables ${srcdir}/control

	msg "Getting fields..."
	cd ${srcdir}/fields/
	if [ -d .svn ]; then
		(svn up)
	else
		svn co ${_svnfields} --config-dir ./ .
	fi

	msg "Getting tables..."
	cd ${srcdir}/tables/
	if [ -d .svn ]; then
		(svn up)
	else
		svn co ${_svntables} --config-dir ./ .
	fi

	msg "Getting config files..."
	cd ${srcdir}/control/
	if [ -d .svn ]; then
		(svn up)
	else
		svn co ${_svncontrol} --config-dir ./ .
	fi

	cd ${srcdir}

	msg "Starting build..."

	svn export ${_svnmod} ${_svnmod}-build
	cd ${_svnmod}-build

	# Patch Makefile
	sed -i 's/python/python2/g' Makefile

	# Patch densehashtable.h to make compilation work
	sed '/#include "sparseconfig.h"/ a #include <stddef.h>' src/auto/XSTools/utils/densehashtable.h > densehashtable.h.new
	mv densehashtable.h.new src/auto/XSTools/utils/densehashtable.h

	# Build
	make
}

package() {
	cd ${srcdir}/${_svnmod}-build

	# Put everything together
	cp -a ${srcdir}/fields ${srcdir}/${_svnmod}-build/
	cp -a ${srcdir}/tables ${srcdir}/${_svnmod}-build/
	cp -a ${srcdir}/control ${srcdir}/${_svnmod}-build/

	# Remove svn directories
	rm -rf $(find "." -type d -name ".svn")

	# Remove all .exe files
	rm -rf $(find "." -name "*.exe")

	install -d ${pkgdir}/opt/openkore/ ${pkgdir}/usr/bin/

	cp -a * ${pkgdir}/opt/openkore/
	
	# Install wrapper script
	echo "#!/bin/sh" >> ${pkgdir}/usr/bin/openkore
	echo "cd /opt/openkore/; perl openkore.pl" >> ${pkgdir}/usr/bin/openkore
	chmod +x ${pkgdir}/usr/bin/openkore

	# Clean up
	cd .. && rm -rf ${srcdir}/${_svnmod}-build
}
