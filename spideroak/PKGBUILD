# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Limao Luo <luolimao@gmail.com>
# Maintainer: ava1ar (mail(dot)avatar(at)gmail(dot)com)

pkgname=spideroak
_PkgName=SpiderOak
pkgver=4.8.3
pkgrel=1
pkgdesc="Secure file backup, sync and sharing client"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
depends=('qt' 'krb5' 'python2')
provides=(${pkgname})
conflicts=(${pkgname}-beta)
options=('!strip')
install=${pkgname}.install
_arch="$CARCH"
[ "$CARCH" = 'i686' ] && _arch='i386'
source=("${pkgname}_${pkgver}_${_arch}.rpm"::"https://spideroak.com/directdownload?platform=fedora&arch=$_arch"
        "terms.txt")
sha1sums=('416ff1795d928d04dafd172e91d5d7ca28805fa3' 'f287fdbad966ac9ae4951a1932e9be7e82112728')
[ "$CARCH" = 'i686' ] && sha1sums[0]='5b8aac5655b2861f558b174a2ad8847d655495db'

package() {
    # install config files
    install -Dm644 ${srcdir}/etc/dbus-1/system.d/${_PkgName}.dbus.conf ${pkgdir}/etc/dbus-1/system.d/${pkgname}.dbus.conf

    # install app in /opt
    install -dm755 ${pkgdir}/opt/
    cp -r ${srcdir}/opt/* ${pkgdir}/opt/

    # install start script file
    install -Dm755 ${srcdir}/usr/bin/SpiderOak ${pkgdir}/usr/bin/SpiderOak

    # install desktop and pixmap files
    install -Dm644 ${srcdir}/usr/share/applications/${_PkgName}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
    install -Dm644 ${srcdir}/usr/share/pixmaps/${_PkgName}.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png

    # fix desktop file
    sed -i \
	-e "/Encoding=UTF-8/d" \
	-e "/^Name=/s: Backup::" \
	-e "/^Comment=/s:${_PkgName} ::" \
	-e "/^Categories=/s:${_PkgName};::" \
	-e "/^Icon=/s:=.*$:=${pkgname}:" \
	-e "/^Exec=/s:=.*$:=${_PkgName}:" \
	${pkgdir}/usr/share/applications/${pkgname}.desktop

    # install custom license file
    install -Dm644 terms.txt ${pkgdir}/usr/share/licenses/${pkgname}/terms.txt
}
