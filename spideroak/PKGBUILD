# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=spideroak
pkgver=4.3.9928
pkgrel=1
pkgdesc="Secure file backup, sync and sharing client"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
conflicts=($pkgname-beta)
options=('!strip')
install="$pkgname".install

[[ "$CARCH" = 'i686' ]] && _arch='i386' || _arch='x86_64'
source=("${pkgname}_${pkgver}_${_arch}.deb"::"https://$pkgname.com/directdownload?platform=ubuntulucid&arch=$_arch"
        "terms.txt")
sha256sums=('a4288b0c92fd4565bc1f179a38540c0828bd6cd638e178d2c2dea1552de5f832'
            '4819c8d923ab19e552e877b87adf1d45aca6adcb5dffcb238d7819501e6e6737')
[[ "$CARCH" = 'i686' ]] && sha256sums[0]='7fb9dfb05c0ca68eccd035152aa4316036c9b1119ef7f6406788eb65c10f30cb'

package() {
  cd "$srcdir"

  # unpack bundled files
  tar zxf data.tar.gz

  # install config files
  install -d -m 755 "$pkgdir"/etc/{dbus-1/system.d,sysctl.d}
  install -m 644 etc/dbus-1/system.d/"$pkgname".dbus.conf \
    "$pkgdir/etc/dbus-1/system.d/$pkgname.dbus.conf"
  install -m 644 etc/sysctl.d/30-"$pkgname".conf \
    "$pkgdir/etc/sysctl.d/30-$pkgname.conf"

  # clean up a bit
  rm -f usr/lib/SpiderOak/lib{gcc_s,stdc++,z}.so.*
  rm -f usr/lib/SpiderOak/*/*/*.exe

  # install app in /opt
  install -d -m 755 "$pkgdir"/opt/SpiderOak
  cp -r usr/lib/SpiderOak/* "$pkgdir"/opt/SpiderOak/

  # change /usr to /opt in start script file
  sed -i 's:/usr/lib:/opt:g' usr/bin/SpiderOak

  # install start script file
  install -d -m 755 "$pkgdir"/usr/bin
  install -m 755 usr/bin/SpiderOak "$pkgdir"/usr/bin/SpiderOak

  # fix desktop file
  sed -i \
    -e "/Encoding=UTF-8/d" \
    -e "/^Name=/s: Backup::" \
    -e "/^Comment=/s:SpiderOak ::" \
    -e "/^Categories=/s:SpiderOak;::" \
    -e "/^Icon=/s:=.*$:=$pkgname:" \
    -e "/^Exec=/s:=.*$:=SpiderOak:" \
    usr/share/applications/"$pkgname".desktop

  # install desktop and pixmap files
  install -d -m 755 "$pkgdir"/usr/share/{applications,pixmaps}
  install -m 644 usr/share/applications/"$pkgname".desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -m 644 usr/share/pixmaps/"$pkgname".png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # install custom license file
  install -d -m 755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m 644 terms.txt "$pkgdir/usr/share/licenses/$pkgname/terms.txt"
}

# vim:set ts=2 sw=2 et:
