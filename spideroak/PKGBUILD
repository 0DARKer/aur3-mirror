# Maintainer: wilson <bugs@pandorica.net>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=spideroak
pkgver=4.6.9945
pkgrel=1
pkgdesc="Secure file backup, sync and sharing client"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
conflicts=($pkgname-beta)
options=('!strip')
install="$pkgname".install

source=("${pkgname}_${pkgver}_${CARCH}.deb"::"https://${pkgname}.com/directdownload?platform=ubuntulucid&arch=${CARCH}"
        "terms.txt")

sha1sums=('f674792992ba2abad24345dbea365c3ab1ae7c44'
          'f287fdbad966ac9ae4951a1932e9be7e82112728')

[[ "$CARCH" = 'i686' ]] && sha1sums[0]='87f27acbc618588746d0b255887e501f15ecdf05'

build() {
  cd "$srcdir"

  # unpack bundled files
  tar zxf data.tar.gz

  # clean up a bit
  rm -f usr/lib/SpiderOak/lib{gcc_s,stdc++,z}.so.*
  rm -f usr/lib/SpiderOak/*/*/*.exe

  # change /usr to /opt in start script file
  sed -i 's:/usr/lib:/opt:g' usr/bin/SpiderOak

  # fix desktop file
  sed -i \
    -e "/Encoding=UTF-8/d" \
    -e "/^Name=/s: Backup::" \
    -e "/^Comment=/s:SpiderOak ::" \
    -e "/^Categories=/s:SpiderOak;::" \
    -e "/^Icon=/s:=.*$:=$pkgname:" \
    -e "/^Exec=/s:=.*$:=SpiderOak:" \
    usr/share/applications/"$pkgname".desktop
}

package() {
  # install config files
  install -d -m 755 "$pkgdir"/etc/{dbus-1/system.d,sysctl.d}
  install -m 644 etc/dbus-1/system.d/"$pkgname".dbus.conf \
    "$pkgdir/etc/dbus-1/system.d/$pkgname.dbus.conf"
  install -m 644 etc/sysctl.d/30-"$pkgname".conf \
    "$pkgdir/etc/sysctl.d/30-$pkgname.conf"

  # install app in /opt
  install -d -m 755 "$pkgdir"/opt/SpiderOak
  cp -r usr/lib/SpiderOak/* "$pkgdir"/opt/SpiderOak/

  # install start script file
  install -d -m 755 "$pkgdir"/usr/bin
  install -m 755 usr/bin/SpiderOak "$pkgdir"/usr/bin/SpiderOak

  # install desktop and pixmap files
  install -d -m 755 "$pkgdir"/usr/share/{applications,pixmaps}
  install -m 644 usr/share/applications/"$pkgname".desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -m 644 usr/share/pixmaps/"$pkgname".png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # install custom license file
  install -d -m 755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m 644 terms.txt "$pkgdir/usr/share/licenses/$pkgname/terms.txt"
}

# vim:set ts=2 sw=2 et:
