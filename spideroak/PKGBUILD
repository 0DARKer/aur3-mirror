# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=spideroak
pkgver=4.2.9913
pkgrel=1
pkgdesc="Secure file backup, sync and sharing client"
arch=('i686' 'x86_64')
url="https://spideroak.com/"
license=('custom')
conflicts=(${pkgname}-beta)
options=('!strip')
install=${pkgname}.install

_arch='x86_64'
[[ "${CARCH}" = 'i686' ]] && _arch='i386'
source=("${pkgname}_${pkgver}_${_arch}.deb"::"https://spideroak.com/directdownload?platform=ubuntulucid&arch=${_arch}"
        "terms.txt")
md5sums=('92f06d35e1b580f04be3edbafb143758'
         '9338d3464e8ce876d7222099c22a205b')
[[ "${CARCH}" = 'i686' ]] && md5sums[0]='7dd8a42b42576e5bea0a5154ea6cdde0'

package() {
  cd "${srcdir}"

  # unpack bundled files
  bsdtar xf data.tar.gz

  # install config files
  install -d -m 755 "${pkgdir}/etc/"{dbus-1/system.d,sysctl.d}
  install -m 644 "etc/dbus-1/system.d/${pkgname}.dbus.conf" \
    "${pkgdir}/etc/dbus-1/system.d/${pkgname}.dbus.conf"
  install -m 644 "etc/sysctl.d/30-${pkgname}.conf" \
    "${pkgdir}/etc/sysctl.d/30-${pkgname}.conf"

  # clean up a bit
  rm -f usr/lib/SpiderOak/lib{gcc_s,stdc++,z}.so.*
  rm -f usr/lib/SpiderOak/*/*/*.exe

  # install app in /opt
  install -d -m 755 "${pkgdir}/opt/SpiderOak"
  cp -r usr/lib/SpiderOak/* "${pkgdir}/opt/SpiderOak/"

  # change /usr to /opt in start script file
  sed -i 's:/usr/lib:/opt:g' usr/bin/SpiderOak

  # install start script file
  install -d -m 755 "${pkgdir}/usr/bin"
  install -m 755 usr/bin/SpiderOak "${pkgdir}/usr/bin/SpiderOak"

  # fix desktop file (according to the desktop entry spec)
  sed -i \
    -e "/Encoding/d" \
    -e "/^Name=/s: Backup::" \
    -e "/^Comment=/s:SpiderOak ::" \
    -e "/^Categories=/s:SpiderOak;::" \
    -e "/^Icon=/s:=.*:=${pkgname}:" \
    -e "/^Exec=/s:=.*:=SpiderOak:" \
    "usr/share/applications/${pkgname}.desktop"

  # install desktop and pixmap files
  install -d -m 755 "${pkgdir}/usr/share/"{applications,pixmaps}
  install -m 644 "usr/share/applications/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -m 644 "usr/share/pixmaps/${pkgname}.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # install custom license file
  install -d -m 755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 terms.txt "${pkgdir}/usr/share/licenses/${pkgname}/terms.txt"
}

# vim:set ts=2 sw=2 et:
