#Maintainer: M0Rf30

pkgname=openbazaar
pkgver=0.3.0
pkgrel=11
pkgdesc="Decentralized marketplace for instantly trading with anyone using Bitcoin"
arch=(any)
url="http://openbazaar.org"
license=('MIT')
depends=(gnupg1 python2-bitcoin python2-gnupg-hg python2-ipy python2-miniupnpc python2-obelisk python2-pillow python2-psutil python2-pycountry 
         python2-pyelliptic python2-pysqlcipher python2-pystun python2-pyzmq python2-qrcode python2-requests python2-rfc3986 
         python2-tornado curl jquery)
optdepends=('pybitmessage: Send and receive messages to anyone on the bitmessage network')
source=(https://github.com/OpenBazaar/OpenBazaar/archive/v${pkgver}.tar.gz
	$pkgname.service
	$pkgname.sh
	$pkgname-stop.sh
)
install=$pkgname.install
options=('!strip')
conflicts=($pkgname-git)

package(){
  cd $srcdir/OpenBazaar-$pkgver
 
  #Install systemd service 
  install -Dm644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service

  #Install $pkgname scripts
  install -Dm755 $srcdir/$pkgname.sh $pkgdir/usr/bin/$pkgname
  install -Dm755 $srcdir/$pkgname-stop.sh $pkgdir/usr/bin/$pkgname-stop

  #Create folder for user $pkgname
  cd installers/ubuntu
  cat build | head -n -2 > create_folders
  python2 create_folders
  cp -r output/usr/share $pkgdir/usr/
  install -dm755 $pkgdir/var/lib/
  mv $pkgdir/usr/share/$pkgname $pkgdir/var/lib/

  #jquery symlink
  ln -s -r /usr/share/jquery/jquery.min.js $pkgdir/var/lib/$pkgname/html/vendors/
  
  #Python2 bytecode generation
  cd $pkgdir/var/lib/$pkgname/ && python2 -m compileall .
}

md5sums=('19d926e20907cadd7960e5a85703ffd2'
         '3aa1c8dc7384d62ffdb404e973afc1f3'
         '0a2e9715837e3b8284fdadfe6306859c'
         '97eb92b231bf82e033926e5d350eb3be')
