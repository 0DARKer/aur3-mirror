#Maintainer: M0Rf30

pkgname=openbazaar
pkgver=0.3.0
pkgrel=1
pkgdesc="A decentralized marketplace proof of concept. It is based off of the POC code by the darkmarket team and protected by the GPL"
arch=(i686 x86_64)
url="http://openbazaar.org"
license=('MIT')
depends=(python2-backports.ssl_match_hostname python2-bitcoin python2-certifi python2-coverage python2-ecdsa python2-flake8 
python2-gnupg python2-ipy python2-miniupnpc python2-mock python2-nose python2-obelisk python2-pillow python2-pycountry
python2-pyelliptic python2-pysqlcipher python2-pyzmq python2-qrcode python2-requests python2-six python2-tornado 
python2-websocket-client python2-wsgiref python2-zope-interface wget)
optdepends=('pybitmessage: Send and receive messages to anyone on the bitmessage network')
source=(https://github.com/OpenBazaar/OpenBazaar/archive/v${pkgver}.tar.gz
	openbazaar.service
	openbazaar.sh
	openbazaar-stop.sh)
install=openbazaar.install
options=('!strip')
conflicts=(openbazaar-git)

package(){
  cd $srcdir/OpenBazaar-$pkgver
  
  #Install systemd service 
  install -Dm644 $srcdir/openbazaar.service $pkgdir/usr/lib/systemd/system/openbazaar.service

  #Install openbazaar scripts
  install -Dm755 $srcdir/openbazaar.sh $pkgdir/usr/bin/openbazaar
  install -Dm755 $srcdir/openbazaar-stop.sh $pkgdir/usr/bin/openbazaar-stop

  #Create folder for user openbazaar
  cd installers/ubuntu
  cat build | head -n -2 > create_folders
  python2 create_folders
  cp -r output/usr/share $pkgdir/usr/
  install -dm755 $pkgdir/var/lib/
  mv $pkgdir/usr/share/openbazaar $pkgdir/var/lib/
  
  #Python2 bytecode generation
  cd $pkgdir/var/lib/openbazaar/ && python2 -m compileall .
  
  chown 421:421 -R $pkgdir/var/lib/{openbazaar,openbazaar/*}
}

md5sums=('19d926e20907cadd7960e5a85703ffd2'
         '3aa1c8dc7384d62ffdb404e973afc1f3'
         '2f9439b68986e5ab56975bffe78e1242'
         'd01e2ececf94d36acfe8e823b41644cd')
