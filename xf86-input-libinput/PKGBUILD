# Maintainer: Paul Neumann <paul104x@yahoo.de>
# Adopted from xf86-input-evdev PKGBUILD

pkgname=xf86-input-libinput
pkgver=0.4.0
pkgrel=1
pkgdesc="X.org libinput input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libinput>=0.7.0')
makedepends=('xorg-server-devel')
conflicts=('xorg-server<1.16.0' 'X-ABI-XINPUT_VERSION<21' 'X-ABI-XINPUT_VERSION>=22')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('0465057818654427a53907d9df040310dd9bc76a846749c83f44c453e5244ec3'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -m755 -d "${pkgdir}/usr/share/X11/xorg.conf.d"
  install -m644 conf/99-libinput.conf "${pkgdir}/usr/share/X11/xorg.conf.d/"
}
