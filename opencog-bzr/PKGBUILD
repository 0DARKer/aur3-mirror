# Maintainer: Thomas Dziedzic < gostrc at gmail >

pkgname=opencog-bzr
pkgver=4623
pkgrel=1
pkgdesc='OpenCog aims to provide research scientists and software developers with a common platform to build and share artificial intelligence programs.'
arch=('i686' 'x86_64')
url='http://www.opencog.org/'
license=('custom:AGPLv3')
depends=('boost' 'unixodbc' 'xmlrpc-c' 'guile' 'gsl' 'xerces-c-2' 'sdl_gfx' 'doxygen')
makedepends=('bzr' 'cmake' 'expat' 'luabind' 'cxxtest' 'chrpath')

_bzrtrunk='lp:opencog'
_bzrmod='opencog'

options=('!makeflags')

build() {
  msg "Connecting to the server...."
  if [ ! -d ./${_bzrmod} ]; then
    bzr co ${_bzrtrunk} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi
  msg "BZR checkout done or server timeout"

  rm -fr build
  cp -r ${_bzrmod} build
  cd build

  unset LDFLAGS

  export DESTDIR=${pkgdir}

  # configure
  # use -DCMAKE_BUILD_TYPE=Release to optimize binaries or remove it for debug
  # use -Wno-dev to suppress developer specific warnings
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    .

  make -j1
}

package() {
  cd build

  export DESTDIR=${pkgdir}

  make install

  install -D -m644 ${srcdir}/opencog/LICENSE ${pkgdir}/usr/share/licenses/opencog-bzr/LICENSE

  # fix invalid rpath
  chrpath -r /usr/lib/opencog ${pkgdir}/usr/lib/opencog/libserver.so
}
