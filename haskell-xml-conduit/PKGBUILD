# Contributor: Lex Black <autumn-wind at web dot de>

pkgname=haskell-xml-conduit
_hkgname=xml-conduit
_licensefile=LICENSE
pkgver=1.0.2
pkgrel=2
pkgdesc="This package provides parsing and rendering functions for XML."
url="http://hackage.haskell.org/package/xml-conduit-1.0.2"
license=('BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-attoparsec'
         'haskell-attoparsec-conduit'
         'haskell-blaze-builder'
         'haskell-blaze-builder-conduit'
         'haskell-blaze-html'
         'haskell-blaze-markup'
         'haskell-bytestring'
         'haskell-conduit'
         'haskell-containers'
         'haskell-data-default'
         'haskell-failure'
         'haskell-monad-control'
         'haskell-resourcet'
         'haskell-system-filepath'
         'haskell-text'
         'haskell-transformers'
         'haskell-xml-types')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('11cc6c1addd19db41b15589ce92cf7adaf31fd1031b7f41d9153f4b50f38cfa8')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}
}
