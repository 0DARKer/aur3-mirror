# Maintainer: Anton Kalmykov <anton dot kalmykov at gmail dot com>

# NOTES:
# 1. You should place Steinberg VST 2.4 SDK archive to the PKGBUILD directory.
# Currently, Steinberg website allows download only VST3 SDK. If you unable to
# get 2.4 version, you could use the latest SDK, but PKGBUILD should be
# modified. Replace vst_sdk2_4_rev2.zip to the latest SDK version filename, then
# and modify VSTSDK_PATH cmake argument.
#
# 2. For disable of building the utility application, add -DBUILD_UTILITY=OFF
# to cmake arguments and then you can even remove qt4 from depends.

_basename=airwave
pkgname=${_basename}-git
pkgver=r36.84e96bd
pkgrel=1
pkgdesc="A WINE-based VST bridge, that allows to use Windows VST 2.4 plugins with Linux VST hosts."
arch=('i686' 'x86_64')
license=('MIT')
url=('https://github.com/phantom-code/airwave')
depends=('wine>=1.7' 'qt4')
makedepends=('git' 'cmake>=2.8')

if [[ $CARCH == 'x86_64' ]]; then
	makedepends+=('gcc-multilib')
fi

provides=('airwave')
conflicts=('airwave')

source=('git+https://github.com/phantom-code/airwave.git#branch=develop')

md5sums=('SKIP')

pkgver()
{
	cd "${_basename}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare()
{
	if [ ! -f "${startdir}/vst_sdk2_4_rev2.zip" ]; then
		echo "Unable to find vst_sdk2_4_rev2.zip file. You should get the Steinberg"
		echo "VST 2.4 SDK archive and place it to the PKGBUILD directory."
	    echo "Go to the http://www.steinberg.net/en/company/developers.html for"
	    echo "more information."
	    return 1
	fi

	unzip "${startdir}/vst_sdk2_4_rev2.zip" -d "${srcdir}"
}

build()
{
	cd "${_basename}"

	if [[ -d build ]]; then
		rm -rf build
	fi

	mkdir build && cd build

	cmake .. -DCMAKE_INSTALL_PREFIX="/usr"       \
		     -DCMAKE_BUILD_TYPE="Release"        \
		     -DVSTSDK_PATH="${srcdir}/vstsdk2.4"
	make
}

package()
{
	cd "${_basename}/build"
	make install DESTDIR="${pkgdir}/"

	# Install the documentation
	install -D -m644 "${srcdir}/${_basename}/README" \
					 "${pkgdir}/usr/share/doc/${_basename}/README"

	# Install the license
	install -D -m644 "${srcdir}/${_basename}/LICENSE" \
					 "${pkgdir}/usr/share/licenses/${_basename}/LICENSE"
}
