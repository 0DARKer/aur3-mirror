# Maintainer: dryes <joswiseman@gmail>
pkgname='aften-git'
pkgver=20110116
pkgrel=1
pkgdesc='Audio encoder which generates compressed audio streams based on ATSC A/52 specification.'
arch=('i686' 'x86_64')
url='http://aften.sourceforge.net/'
license=('GPL')
provides=('aften')
conflicts=('aften')
makedepends=('cmake' 'git')

_gitroot='git://aften.git.sourceforge.net/gitroot/aften/aften'
_gitname='aften'

build() {
  cd "${srcdir}"
  msg 'Connecting to GIT server....'

  if [ -d "${_gitname}" ] ; then
    cd "${_gitname}" && git pull origin
    msg 'The local files are updated.'
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  [ -d "${srcdir}/${_gitname}-build" ] && rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  mkdir "${srcdir}/${_gitname}-build/build"
  cd "${srcdir}/${_gitname}-build/build"
  
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DSHARED=true
  make

}

package() {
  cd "${srcdir}/${_gitname}-build/build"
  make DESTDIR="${pkgdir}/" install
}
