# Maintainer: Spike29 <leguen.yannick@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_pkg='wiLink'
pkgname='wilink'
pkgver='2.4.1'
pkgrel='6'
pkgdesc='Instant messaging client and SIP softphone for Wifirst customers'
arch=('i686' 'x86_64')
url='https://github.com/jlaine/wilink'
license=('GPL3')
depends=('qt4>=4.7.4' 'libmad' 'libvorbis' 'libxss' 'qxmpp' 'qdjango' 'libnotify' 'libgnome-keyring' 'libgphoto2')
makedepends=('cmake>=2.8.2')
install="$pkgname.install"
source=("https://download.wifirst.net/public/${_pkg}-${pkgver}.tar.bz2"
	"link-X11.patch")
sha1sums=('e53ba3e779a5367d38319c4c8e70eb96d0cf61ac'
	  '109f8a24996e42e015b4b7d479735e69b801a60c')
 
build() {
	cd "$scrdir"

	# defining appropriate /usr/lib/qt/ path for ArchLinux system
	sed -e 's/qt4/qt/g' -i "$_pkg-$pkgver/CMakeLists.txt"
	sed -e 's/qt4/qt/g' -i "$_pkg-$pkgver/src/qnetio/CMakeLists.txt"
	
	# patching src/app/CMakeLists to solve an issue with an unlinked X11 library
	patch -p0 -i link-X11.patch
	
	# building
	cd "$_pkg-$pkgver/"
	[ -d build ] || mkdir build && cd "build/"
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	# installing
	cd "$srcdir/$_pkg-$pkgver/build/"
	make DESTDIR="$pkgdir/" install

 	# setting up higher resolution icon
	rm "$pkgdir/usr/share/pixmaps/$_pkg.xpm"
	install -Dm644 ../src/data/images/128x128/$_pkg.png \
    	"$pkgdir/usr/share/pixmaps/$_pkg.png"
}
