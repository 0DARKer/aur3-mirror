# Maintainer: Spike29 <leguen.yannick@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_pkg=wiLink
pkgname=wilink
pkgver=2.4.0
pkgrel=1
pkgdesc='A Wifirst application for Instant communications'
arch=('i686' 'x86_64')
url='http://opensource.wifirst.net/projects/wilink/'
license=('GPL3')
depends=('qt>=4.7.4' 'libmad' 'libvorbis' 'libxss' 'qxmpp' 'qdjango' 'libnotify' 'libgnome-keyring' 'libgphoto2')
makedepends=('cmake>=2.8.2')
install="$pkgname.install"
source=("https://download.wifirst.net/public/${_pkg}-${pkgver}.tar.bz2"
	"CMakeLists.patch")
md5sums=('a305a67556b4301b6634693a058d450c'
	 '71479755f1d26379552beb52754cd1f5')
 
build() {
	cd "$scrdir"

	# defining appropriate /usr/lib/qt/ path for ArchLinux system
	sed -e 's/qt4/qt/g' -i "$_pkg-$pkgver/CMakeLists.txt"
	sed -e 's/qt4/qt/g' -i "$_pkg-$pkgver/src/qnetio/CMakeLists.txt"
	
	# patching src/app/CMakeLists to solve an issue with an unlinked X11 library
	patch -p0 -i CMakeLists.patch
	
	# building
	cd "$_pkg-$pkgver/"
	mkdir build && cd "build/"
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	# installing
	cd "$srcdir/$_pkg-$pkgver/build/"
	make DESTDIR="$pkgdir/" install

 	# higher resolution icon
	rm "$pkgdir/usr/share/pixmaps/$_pkg.xpm"
	install -Dm644 ../src/data/images/128x128/$_pkg.png \
    	"$pkgdir/usr/share/pixmaps/$_pkg.png"
}
