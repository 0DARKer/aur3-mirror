# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-data
pkgver=0.2.54.6
pkgrel=1
pkgdesc="Milena is a Polish speech synthesizer for Linux (data)"
arch=('any')
url="http://milena.polip.com/"
license=('GPL')
source=(http://tts.polip.com/files/${pkgname}-${pkgver}.tar.gz
        10-makefile_prefix.patch)
sha256sums=('f739eb819a116af7414a01fc7c54930f9b8594ed4d84733d7af7ec875012049e'
            'c81dbf98db63baadd5bd7536160627664ddeef679d4f10d107ae6a18f99efc51')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  for (( i=0; i < ${#source[@]}; i++ )); do
    test "${source[i]}" = "${source[i]%.patch}" \
      || patch -Np1 -i "${srcdir}/${source[i]##*/}"
  done

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" prefix="/usr" install
}

# vim:set ts=2 sw=2 et:
