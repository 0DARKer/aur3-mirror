# Maintainer: rtfreedman  <rob<d0t>til<d0t>freedman< T>googlemail<d0t>com>
#
pkgname=exmplayer
_pkgname=ExMplayer
pkgver=3.2.0
pkgrel=2
url='http://exmplayer.sourceforge.net/'
pkgdesc='MPlayer GUI with thumbnail seeking, 3D Video support, ...'
license=('GPL')
#
arch=('i686' 'x86_64')
depends=('mplayer')
#
provides=('exmplayer')
conflicts=('exmplayer-git')

install="${pkgname}.install"

source=("https://launchpad.net/~exmplayer-dev/+archive/exmplayer/+files/exmplayer_${pkgver}.tar.gz"
		'qtlocalpeer.patch')
		
md5sums=('16afb8697173c6c6bb22650431f2b5c2'
         '4c19c035d06183681aa785e6a3bf56a4')

prepare() {
  cd "${_pkgname}"
  patch -p0 -i "$srcdir/qtlocalpeer.patch"
# sed -i 's/QMAKE=qmake/QMAKE=qmake-qt4/' Makefile
}

build() {
  cd "${_pkgname}/src"
  qmake-qt4
  make 
}

package() {
  cd "${_pkgname}"
  install -Dm755 src/exmplayer "$pkgdir"/usr/bin/exmplayer
  #
  install -dm755 "$pkgdir"/usr/share/applications
  install -m644 exmplayer.desktop exmplayer_enqueue.desktop "$pkgdir"/usr/share/applications
  install -Dm644 debian/exmplayer.png "$pkgdir"/usr/share/pixmaps/exmplayer.png
  #
  install -dm755 "$pkgdir"/etc/exmplayer
  install -m644 linux_build/{sc_default.xml,fmts} "$pkgdir"/etc/exmplayer
  #
  install -dm755 "$pkgdir"/usr/share/exmplayer
  # use native installed ffmpeg 
  ln -s /usr/bin/ffmpeg  "$pkgdir"/usr/share/exmplayer/ffmpeg
  # or bundled ffmpeg
  #install -m755 linux_build/ffmpeg "$pkgdir"/usr/share/exmplayer
}
