# Maintainer: pancho horrillo <pancho at pancho dot name>
# Contributor: bender02 at archlinux dot us
# Contributor: Troels Kofoed Jacobsen <tkjacobsen at gmail dot com>

pkgname=pam_ssh
pkgver=2.0
pkgrel=2
pkgdesc='PAM module providing single sign-on behavior for SSH.'
arch=('i686' 'x86_64')
url='http://pam-ssh.sourceforge.net/'
license=('custom')
depends=('pam' 'openssl' 'openssh')
options=('!libtool')
install=pam_ssh.install
source=("http://downloads.sf.net/sourceforge/pam-ssh/$pkgname-$pkgver.tar.xz" "$pkgname.install")
sha512sums=('ace6e89f2f00893cb7893cbed227d77a71bc393fcb8c35d7b97eb3075f898f506c55d34ce0c947dbe55a36176e89c49eb17ee2c689e1e800ed6b4becaa55f355'
            '954f203926e1c559d6a1471cf8b547373673e7ae4bf00b936a3014f38f1f92a035ba2843e3340d7237e5bed7550d9a3dee99f57cd1d01e1697fc12b6ae6ae58b')

build () {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --with-pam-dir=/usr/lib/security
	make
}

package () {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -m 644 -D COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
