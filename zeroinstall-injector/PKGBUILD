# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>

pkgname=zeroinstall-injector
pkgver=0.52
pkgrel=1
pkgdesc="A decentralised loosly-coupled secure installation system"
arch=('any')
url="http://zero-install.sourceforge.net/"
license=('GPL2' 'LGPL')
depends=('pygtk' 'gnupg' 'hicolor-icon-theme')
optdepends=('xdg-utils: GNOME and KDE desktop integration')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/zero-install/$pkgname-$pkgver.tar.bz2")
md5sums=('781002a02e8516e61d369c40c53f6b46')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fixes
  export PYTHON="python2"
  sed -i "s_\(env python\).*_\12_" `grep -Erl "env python" .`

  # Man dir fix
  sed -i "s_man/_share/&_" setup.py

  python2 setup.py install --prefix=/usr --root="$pkgdir/"
}
