# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>

pkgname=zeroinstall-injector
pkgver=1.5
pkgrel=1
pkgdesc="A decentralised loosly-coupled secure installation system"
arch=('any')
url="http://zero-install.sourceforge.net/"
license=('GPL2' 'LGPL')
depends=('pygtk' 'gnupg' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('xdg-utils: GNOME and KDE desktop integration')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/zero-install/$pkgname-$pkgver.tar.bz2")
md5sums=('a1e0291183520875704386ddbc22cc5d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # man path fix
  sed -i "s_man/_share/&_" setup.py

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir/"

  # python2 fix
  sed -i "s/env python/&2/" `grep -rl "env python" "$pkgdir"`
}