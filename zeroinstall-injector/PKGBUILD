# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>

pkgname=zeroinstall-injector
pkgver=1.7
pkgrel=1
pkgdesc="A decentralised loosly-coupled secure installation system"
arch=('any')
url="http://zero-install.sourceforge.net/"
license=('GPL2' 'LGPL')
depends=('pygtk' 'dbus-python' 'gnupg' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('xdg-utils: desktop integration'
            'packagekit: packagekit integration')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/zero-install/$pkgname-$pkgver.tar.bz2")
md5sums=('23e2adf699c52f8cdfbb7ad07d6c5568')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # man path fix
  sed -i "s|man/|share/&|" setup.py

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir/"

  # python2 fix
  sed -i "s/env python/&2/" `grep -rl "env python" "$pkgdir"`
}