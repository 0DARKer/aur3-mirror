# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>

pkgname=zeroinstall-injector
pkgver=2.1
pkgrel=1
pkgdesc="A decentralised loosly-coupled secure installation system"
arch=('any')
url="http://zero-install.sourceforge.net/"
license=('GPL2' 'LGPL')
depends=('pygtk' 'dbus-python' 'gnupg' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('xdg-utils: desktop integration'
            'packagekit: packagekit integration'
            'bash-completion: bash completion support')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/zero-install/0install/$pkgver/0install-$pkgver.tar.bz2")
md5sums=('d502e4828165cd259f007bfd96b6a56d')

build() {
  cd "$srcdir/0install-$pkgver"

  # man path fix
  sed -i "s|man/|share/&|" setup.py

  python2 setup.py build
}

package() {
  cd "$srcdir/0install-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir/"

  # python2 fix
  sed -i "s/env python/&2/" `grep -rl "env python" "$pkgdir"`
}
