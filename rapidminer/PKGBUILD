# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>

pkgname=rapidminer
pkgver=5.1.001
pkgrel=2
pkgdesc='An open-source data mining solution.'
arch=('i686' 'x86_64')
url='http://rapid-i.com/'
license=('custom')
depends=(java-runtime)
optdepends=(gnuplot)
source=("http://downloads.sourceforge.net/project/rapidminer/1.%20RapidMiner/${pkgver:0:3}/rapidminer-${pkgver}.zip"
        "rapidminer.desktop")
md5sums=('d504653f359637ff2584e0e4142e69b2'
         'd0dbcb8ad72c285efec9740c1cda65e0')

package() {
  cd ${pkgname}

  # Determine JAVA_HOME
  [ -f /etc/profile.d/openjdk6.sh ] && source /etc/profile.d/openjdk6.sh
  [ -z "$JAVA_HOME" ] && [ -f "/etc/profile.d/jre.sh" ] && source /etc/profile.d/jre.sh
  [ -z "$JAVA_HOME" ] && ( echo "Could not determine JAVA_HOME."; return 1 )

  # Modify scripts such that the /opt/rapidminer prefix is recognized.
  # Also set the JAVA_HOME
  sed -i'' -e "s|^#RAPIDMINER_HOME=\\\${HOME}\$|RAPIDMINER_HOME=/opt/rapidminer\nJAVA_HOME=${JAVA_HOME}|" scripts/rapidminer
  sed -i'' -e "s|^#RAPIDMINER_HOME=\\\${HOME}\$|RAPIDMINER_HOME=/opt/rapidminer\nJAVA_HOME=${JAVA_HOME}|" scripts/RapidMinerGUI

  # Install files into prefix
  find {lib,resources} -type f -exec install -m 644 -D {} ${pkgdir}/opt/rapidminer/{} \;
  install -m 755 -D scripts/rapidminer ${pkgdir}/opt/rapidminer/bin/rapidminer
  install -m 755 -D scripts/RapidMinerGUI ${pkgdir}/opt/rapidminer/bin/RapidMinerGUI

  # custom licenses
  pushd licenses
  find . -type f -exec install -m 644 -D {} ${pkgdir}/usr/share/licenses/${pkgname}/{} \;
  popd

  # install links to bins
  install -d ${pkgdir}/usr/bin
  ln -s /opt/rapidminer/bin/RapidMinerGUI \
    ${pkgdir}/usr/bin/RapidMinerGUI
  ln -s /opt/rapidminer/bin/rapidminer \
    ${pkgdir}/usr/bin/rapidminer

  # desktop file
  install -m 644 -D "${srcdir}/rapidminer.desktop" "${pkgdir}/usr/share/applications/rapidminer.desktop"
}


