# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

pkgname=minisat-git
_pkgbasename=minisat
pkgver=0
pkgrel=1
epoch=1
pkgdesc="A minimalistic, open-source SAT solver"
arch=('i686' 'x86_64')
url="http://minisat.se/"
license=('MIT')
depends=()
makedepends=('git')
provides=('minisat')
conflicts=('minisat')
source=(git+https://github.com/niklasso/minisat.git)
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgbasename}"

  git describe --tags | sed -E 's/^releases\///;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgbasename}"

  make config prefix="/usr"
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}"

  make DESTDIR="${pkgdir}" install
}

# vim: set ts=2 sw=2 ft=sh noet:
