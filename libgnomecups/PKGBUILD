# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Paulius Palevicius <paulius@birzai.com>
pkgname=libgnomecups
pkgver=0.2.3
pkgrel=10
pkgdesc="GNOME CUPS library"
arch=('x86_64' 'i686')
license=('LGPL' 'GPL')
url="http://www.gnome.org/"
depends=('libcups')
makedepends=('perlxml')
options=(!libtool)
source=("http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.2/$pkgname-$pkgver.tar.bz2"
        "libgnomecups_0.2.3-ignore-ipp-not-found.patch")
md5sums=('dc4920c15c9f886f73ea74fbff0ae48b'
         '973a1b9d93013ce431400a14b78f5d94')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # This avoids generating huge 'IPP request failed with status 1030' lines
  patch -Np1 -i "$srcdir/libgnomecups_0.2.3-ignore-ipp-not-found.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
