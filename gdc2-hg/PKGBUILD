# Contributor: Jerome Berger <jeberger@free.fr>
# Maintainer: Jesus Alvarez <jeezusjr@gmail.com>

pkgname=gdc2-hg
pkgver=788
_gccver=4.6.2
pkgrel=1
pkgdesc="GDC, Digital Mars D Programing Language (DMD) frontend for GCC (D2 version)"
arch=(i686 x86_64)
url="http://bitbucket.org/goshawk/gdc/wiki/Home"
license="GPL"
provides=('gdc')
depends=("gcc>=$_gccver")
makedepends=('mercurial')
conflicts=('gdc' 'gdc1-hg')
options=('!libtool' '!emptydirs')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${_gccver}/gcc-core-${_gccver}.tar.bz2)
md5sums=('780f614ab18c7a9066dec6387d7490b2')

_hgroot=https://bitbucket.org/goshawk
_hgrepo=gdc

build() {
   cd $srcdir

   rm -rf $srcdir/gcc-$_gccver-build $srcdir/gcc-build
   mv $srcdir/gcc-$_gccver $srcdir/gcc-$_gccver-build
   ln -s $srcdir/gdc/d $srcdir/gcc-$_gccver-build/gcc

   cd gcc-$_gccver-build
   ./gcc/d/setup-gcc.sh -v2

   export MAKEFLAGS="-j1"

   # Don't install libiberty
   sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

   # Don't run fixincludes
   sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

   mkdir ../gcc-build
   cd ../gcc-build

   ../gcc-$_gccver-build/configure --prefix=/usr \
      --libdir=/usr/lib \
      --libexecdir=/usr/lib \
      --mandir=/usr/share/man \
      --infodir=/usr/share/info \
      --with-bugurl=https://bitbucket.org/goshawk/gdc/issues \
      --enable-languages=d \
      --disable-bootstrap \
      --disable-shared \
      --disable-nls \
      --with-arch=native \
      --enable-threads=posix \
      --with-system-zlib \
      --enable-__cxa_atexit \
      --enable-clocale=gnu \
      --enable-linker-build-id \
      --disable-multilib \
      --disable-libgomp \
      --disable-libmudflap \
      --enable-checking=release

   make
}

package() {
   cd $srcdir/gcc-build

   # Easier to just do a full install and remove the excess later
   make -j1 DESTDIR=${pkgdir} install

   # Delete all the stuff we don't need
   rm -rf $pkgdir/usr/bin/{gcc,gcov,cpp,$CHOST-gcc,$CHOST-gcc-$_gccver}
   rm -rf $pkgdir/usr/lib/gcc/$CHOST/$_gccver/{crt*,cc1,collect2,include*,install*}
   rm -rf $pkgdir/usr/lib/gcc/$CHOST/$_gccver/{*.a,*.so,*.so.*,lto-wrapper,lto1,plugin*}
   rm -rf $pkgdir/usr/share/info
   rm -rf $pkgdir/usr/share/man/man1/{cpp*,gc*}
   rm -rf $pkgdir/usr/share/man/man7/{fsf*,gfdl*,gpl*}

   # install -dm755 ${pkgdir}/lib
   # ln -sf /usr/bin/gdc ${pkgdir}/lib/gdc
   # install -Dm 755 gcc/gcd $pkgdir/usr/bin/gcd
}
