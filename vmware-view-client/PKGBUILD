# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=vmware-view-client
pkgver=1.7.0.0
pkgrel=5
pkgdesc="VMware View Client connects to virtual machines managed by VMware View (proprietary version with PCoIP support)"
conflicts=('vmware-view-open-client' 'vmware-view-open-client-beta')
arch=('i686' 'x86_64')
url="https://my.vmware.com/web/vmware/info/slug/desktop_end_user_computing/vmware_view/5_0"
license=('custom')
source=("http://archive.canonical.com/pool/partner/v/vmware-view-client/vmware-view-client_${pkgver}.orig.tar.gz"
	'tmpfiles.conf'
	'vmware-view-usbd'
	'vmware-view-usbd.service'
	'vmware-view-client.desktop')
depends=()
optdepends=('rdesktop: for RDP remote desktop connections')
install=vmware-view-client.install

if [[ "${CARCH}" == 'x86_64' ]]; then
	_libprefix='lib32-'
	optdepends+=('lib32-systemd: for USB redirect support')
else
	_libprefix=''
fi
# no spaces here
depends+=(${_libprefix}{'openssl098','libpng12','gtk2','libxml2','libxtst'})
optdepends+=(${_libprefix}{'pcsclite: for smartcard support','libpulse: for audio support'})

package() {
	cd "${srcdir}/vmware-view-client-${pkgver}"

	for FILE in $(find -type f); do
		install -D ${FILE} ${pkgdir}/${FILE}
	done

	# set up vmware-view-usbd
	chmod +s ${pkgdir}/usr/lib/vmware/vmware-{usbarbitrator,view-usbd}
	install -D -m755 ${srcdir}/vmware-view-usbd ${pkgdir}/usr/lib/systemd/scripts/vmware-view-usbd
	install -D -m644 ${srcdir}/vmware-view-usbd.service ${pkgdir}/usr/lib/systemd/system/vmware-view-usbd.service
	if [[ "${CARCH}" == 'x86_64' ]]; then
		ln -s ../../lib32/libudev.so.1 ${pkgdir}/usr/lib/vmware/libudev.so.0
	else
		ln -s ../libudev.so.1 ${pkgdir}/usr/lib/vmware/libudev.so.0
	fi

	# this needs to be writeable for USB redirection
	install -d ${pkgdir}/var/run/vmware
	install -D -m644 ${srcdir}/tmpfiles.conf ${pkgdir}/usr/lib/tmpfiles.d/vmware-view-client.conf

	# desktop file for menu entries
	install -D -m644 ${srcdir}/vmware-view-client.desktop ${pkgdir}/usr/share/applications/vmware-view-client.desktop
}

sha256sums=('6d148d3fec05a015f702269b14812b5d56e3e65212eeb2e2174b3c5ec178e2d4'
            'ec763930dd50d6e77a31c40c939909752cfb124cafb0a4ca4f76860375a14d75'
            'cc03137151aae050f7cdb965d02488089a9e0ef7e03c21a13ea44036a3762b08'
            '021ce563b9a672fc5027fd83821561b2e34ea25c51c1d9cb9e84d905d45003e9'
            'af6893b96e07e9e8023d64eaa4cece90c73890d744e4d91d80cfdcd3120b60bd')
