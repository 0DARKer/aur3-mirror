# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>

pkgname=odb
pkgver=2.3.0
pkgrel=3
pkgdesc="C++ Object-Relational Mapping compiler"
arch=('i686' 'x86_64')
depends=('libcutl')
url="http://www.codesynthesis.com/products/odb/"
options=('!libtool')
license=('GPL')
source=("http://www.codesynthesis.com/download/odb/2.3/odb-${pkgver}.tar.bz2" odb-2.3.0-gcc-4.9.0.patch)
md5sums=('ff561f7eabb50362582c92316efe29fd'
         'f4090f26a9985e356366771eb5696f16')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 < ../odb-2.3.0-gcc-4.9.0.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  CXXFLAGS="${CXXFLAGS} -fno-devirtualize"

  ./configure --prefix=/usr --libexecdir=/usr/lib
  make ECHO=echo
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make ECHO=echo DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
