# Maintainer: Alexander Rødseth <rodseth@gmail.com>
pkgname=golangide
pkgver=x6
pkgrel=1
pkgdesc="Simple IDE for Go to edit code and build projects"
arch=('x86_64' 'i686')
url="http://code.google.com/p/golangide/"
depends=('go' 'qt')
license=('LGPL')
options=(!strip)
if [ "$CARCH" == "x86_64" ]
then
  source=("http://$pkgname.googlecode.com/files/liteide-linux64-$pkgver-1.tar.bz2"
          "golangide.desktop"
          "golangide.png")
  md5sums=('397168976caea46de78e4818b46fa618'
           '61cc285d9aa94bacc8c3bebc58f6565d'
           '7f459be003d754a3a2c34be91b8c0b1f')
else
  source=("http://$pkgname.googlecode.com/files/liteide-linux32-$pkgver-1.tar.bz2"
          "golangide.desktop"
          "golangide.png")
  md5sums=('6e8ad4f4f5e95206efd9e79c4f90e6af'
           '61cc285d9aa94bacc8c3bebc58f6565d'
           '7f459be003d754a3a2c34be91b8c0b1f')
fi

package() {
  cd "$srcdir/liteide"

  msg2 "Creating directories..."
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/$pkgname"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"

  msg2 "Packaging executables..."
  install -Dm755 bin/goastview "$pkgdir/usr/bin/goastview"
  install -Dm755 bin/godocview "$pkgdir/usr/bin/godocview"
  install -Dm755 bin/goexec "$pkgdir/usr/bin/goexec"
  install -Dm755 bin/gopromake "$pkgdir/usr/bin/gopromake"
  install -Dm755 bin/liteide "$pkgdir/usr/bin/$pkgname"

  msg2 "Packaging resources..."
  cp -r data/* "$pkgdir/usr/share/$pkgname"
  cp -r plugins "$pkgdir/usr/share/$pkgname"

  # golangide looks for the plugins in /usr/plugins (!)
  ln -s "/usr/share/$pkgname/plugins" "$pkgdir/usr/plugins"

  msg2 "Packaging docs..."
  cp -r data/doc/*.{html,css,txt} "$pkgdir/usr/share/doc/$pkgname"

  msg2 "Packaging license..."
  install -Dm644 LICENSE.LGPL \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.LGPL"

  msg2 "Packaging license exception..."
  install -Dm644 LGPL_EXCEPTION.TXT \
    "$pkgdir/usr/share/licenses/$pkgname/LGPL_EXCEPTION.TXT"

  cd ..

  msg2 "Packaging menu entry and icon..."
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  msg2 "Cleaning up..."
  rm -rf "$pkgdir/usr/share/$pkgname/doc"
}

# vim:set ts=2 sw=2 et:
