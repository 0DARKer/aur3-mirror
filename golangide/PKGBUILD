# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: spambanane <happy.house@gmx.de>
# Contributor: Matteo <matteo.dek@gmail.com>

pkgname=golangide
pkgver=12.1.1
pkgrel=1
pkgdesc="Simple IDE for Go to edit code and build projects"
arch=('x86_64' 'i686')
url="http://code.google.com/p/golangide/"
depends=('go')
makedepends=('gendesk')
license=('LGPL')
options=('!strip')
_hgrev=575
_name=('Golang IDE')
_genericname=('Integrated development environment')

source=("golangide.png" "railscast.xml")
sha256sums=('47c52b22326034bd3d6a7b11b05a53c8b3838c08e145171cf5cad2ca00260697'
            '57d13f8fe7032f1caae9e1201de6ba9e439d0ec5bad1156f929aabbbc5a4bfb0')
if [ "$CARCH" == "x86_64" ]
then
  source+=("http://$pkgname.googlecode.com/files/liteidex${pkgver}.linux-amd64.hg${_hgrev}.tar.bz2")
  sha256sums+=('7c356e94832f4362787811cd8bf28e5b94dece4633d570897f79b447eb86f699')
else
  source+=("http://$pkgname.googlecode.com/files/liteidex${pkgver}.linux-386.hg${_hgrev}.tar.bz2")
  sha256sums+=('3ef5c45ebc305fca9fcc36245ab7e29276025f576e029cc59229a31463a4902b')
fi

build() {
  cd "$srcdir"
  gendesk -n

  cd "liteide"
  msg2 "Fixing insecure RPATH..."
  find . -name "*.so" -type f -exec sed -i 's|/home/win|/usr/lib/|g' {} \;
  find . -name liteide -type f -exec sed -i 's|/home/win|/usr/lib/|g' {} \;
}

package() {
  cd "$srcdir/liteide"

  msg2 "Creating directories..."
  mkdir -p "$pkgdir/usr/lib/liteide"
  mkdir -p "$pkgdir/usr/share/liteide"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"

  msg2 "Packaging executables..."
  for binary in goastview godocview goexec gopromake; do
    install -Dm755 "bin/$binary" "$pkgdir/usr/bin/$binary"
  done
  install -Dm755 bin/liteide "$pkgdir/usr/bin/golangide"

  msg2 "Packaging resources..."
  cp -r share/liteide/* "$pkgdir/usr/share/liteide"

  msg2 "Packaging libraries and plugins..."
  cp -r lib/liteide/* "$pkgdir/usr/lib/liteide"

  msg2 "Packaging license and license exception..."
  install -Dm644 LICENSE.LGPL \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.LGPL"
  install -Dm644 LGPL_EXCEPTION.TXT \
    "$pkgdir/usr/share/licenses/$pkgname/LGPL_EXCEPTION.TXT"

  cd ..

  msg2 "Packaging bonus color theme..."
  install -Dm644 railscast.xml \
    "$pkgdir/usr/share/liteide/liteeditor/color/railscast.xml"

  msg2 "Packaging menu entry and icon..."
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  msg2 "Cleaning up..."
  rm -rf "$pkgdir/usr/share/$pkgname/doc"
}

# vim:set ts=2 sw=2 et:
