# Maintainer: Alexander Rødseth <rodseth@gmail.com>
pkgname=golangide
pkgver=0.2.1
pkgrel=1
pkgdesc="Simple IDE for Go to edit code and build projects"
arch=('x86_64' 'i686')
url="http://code.google.com/p/golangide/"
depends=('go')
makedepends=("setconf")
license=('BSD')
options=(!strip)
if [ "$CARCH" = "x86_64" ]
then
  source=("http://$pkgname.googlecode.com/files/liteide-linux-x86_64-bin-$pkgver.tar.bz2"
          "golangide.desktop"
          "golangide.png")
  md5sums=('b61aa723639532f65ab6dbb7e5b2ab9b'
           '61cc285d9aa94bacc8c3bebc58f6565d'
           '7f459be003d754a3a2c34be91b8c0b1f')
else
  source=("http://$pkgname.googlecode.com/files/liteide-linux-x86_32-bin-$pkgver.tar.bz2"
          "golangide.desktop"
          "golangide.png")
  md5sums=('77708806aa023766bb56646e58a3152c'
           '61cc285d9aa94bacc8c3bebc58f6565d'
           '7f459be003d754a3a2c34be91b8c0b1f')
fi

build() {
  cd "$srcdir/liteide"

  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/$pkgname"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"

  msg2 "Configuring..."
  setconf bin/liteide.sh appname liteide
  setconf bin/liteide.sh dirname "/usr/share/$pkgname"

  msg2 "Packaging executables..."
  install -Dm755 bin/goastview "$pkgdir/usr/share/$pkgname"
  install -Dm755 bin/gopromake "$pkgdir/usr/share/$pkgname"
  install -Dm755 bin/liteide "$pkgdir/usr/share/$pkgname"
  install -Dm755 bin/liteide.sh "$pkgdir/usr/bin/golangide"

  msg2 "Packaging plugins and templates..."
  cp -r bin/plugins "$pkgdir/usr/share/$pkgname"
  cp -r bin/template "$pkgdir/usr/share/$pkgname"

  msg2 "Packaging docs and examples..."
  cp -r doc "$pkgdir/usr/share/doc/$pkgname"
  cp -r examples "$pkgdir/usr/share/doc/$pkgname"

  cd ..

  msg2 "Packaging menu entry and icon..."
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop" || return 1
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png" || return 1

  # The package does not come with a license file
  #msg2 "Packaging license..."
  #install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
