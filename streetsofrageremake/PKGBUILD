# Maintainer: P. Neidhardt <ambrevar at gmail dot com>

pkgname=streetsofrageremake
pkgver=5.0a
pkgrel=1
pkgdesc="A remake of the classic Streets of Rage 1,2 and 3 by SEGA."
arch=('i686' 'x86_64')
url="http://sorr.forumotion.net/t148-sor-v5-for-linux-debian-download-links"
license=('custom')
depends=('alsa' 'libpng12' 'sdl_mixer' 'smpeg' 'zlib')
if [ "$CARCH" = "i686" ]; then
    depends=('alsa-lib' 'libpng12' 'sdl_mixer' 'smpeg' 'zlib')
else
    depends=('lib32-alsa-lib' 'lib32-libpng12' 'lib32-sdl_mixer' 'lib32-smpeg' 'lib32-zlib')
fi
install="sorr.install"
source=("http://archive.ubuntugames.org/all/sorr_5.0.orig.tar.gz"
    'Readme.linux'
    'sorr-wrapper')
options=(!strip)
sha1sums=('5c719296ab1cbc222897fba00453a18e495ee8c4'
          'd2481dc94eedb19f56277124889c7130ee73d9e7'
          '1e37a1d8332fc7aee941236368fb2c6a8c12bd72')

build() {
    cd "${srcdir}/"

    ## We replace the broken launcher with a wrapper that will start the game
    ## from $HOME/.sorr so that savegames and mods can be loaded from there.
    cp -f sorr-wrapper sorr
}

package() {
    cd "${srcdir}/"

    install -d "${pkgdir}/opt/sorr"

    ## Data
    install -m755 "bgdc" "bgdi" "sorr" "${pkgdir}/opt/sorr"
    install -m644 "Manual.html" "Readme.txt" "SorMaker.dat" "SorR.dat" "${pkgdir}/opt/sorr"
    install -d "${pkgdir}/opt/sorr/libs"
    for i in bennugd/* data/*; do
        install -m644 "$i" "${pkgdir}/opt/sorr/libs/"
    done
    cp -r manual mod palettes "${pkgdir}/opt/sorr/"

    ## For some strange reasons, all files are marked as executable. Let's clean
    ## this up.
    (cd "${pkgdir}/opt/sorr/" && find manual mod palettes -type f -exec chmod a-x {} \;)

    ## Remove unneeded Windows file.
    rm "${pkgdir}/opt/sorr/manual/img/Thumbs.db"

    ## Launcher
    install -d "${pkgdir}/usr/bin"
    ln -s "/opt/sorr/sorr" "${pkgdir}/usr/bin/sorr"

    ## .desktop
    install -d "${pkgdir}/usr/share/applications/"
    install -m644 "sorr.desktop" "${pkgdir}/usr/share/applications/"

    ## Icon
    install -m644 "sorr.png" "${pkgdir}/opt/sorr/"
    install -d "${pkgdir}/usr/share/icons/hicolor/48x48/apps"
    ln -s "/opt/sorr/sorr.png" "${pkgdir}/usr/share/icons/hicolor/48x48/apps/"

    ## Extra files
    install -m644 "Readme.linux" "${pkgdir}/opt/sorr"
}
