# Maintainer: Chad Versace <chad.versace@linux.intel.com>

pkgname='waffle'
pkgver='1.2.3'
pkgrel=1
pkgdesc='a library for choosing window system and OpenGL API at runtime'
arch=('i686' 'x86_64')
url='http://people.freedesktop.org/~chadversary/waffle'
license=('BSD')
depends=(
  'libegl'
  'libgbm'
  'libgl' # for GLX
  'libgles'
  'libx11'
  'libxcb'
  'wayland'
  )
makedepends=(
  'cmake'
  'xcb-proto'

  # For manpages
  'docbook-xsl'
  'libxslt'
  )
source=("http://people.freedesktop.org/~chadversary/waffle/files/release/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('602104a2deaa843679bd25f5532b3422aab17ff2597915b6ed781daf9676131a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_BUILD_TYPE=Release \
    -Dwaffle_has_gbm=1 \
    -Dwaffle_has_glx=1 \
    -Dwaffle_has_wayland=1 \
    -Dwaffle_has_x11_egl=1 \
    -Dwaffle_build_manpages=1 \
    -Dwaffle_build_examples=0 \
    .
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$pkgdir/usr/share/doc/waffle-1/LICENSE.txt" \
                "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
