# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
pkgname=libosl-svn
pkgver=4545
pkgrel=2
pkgdesc="Library for Shogi (Japanese chess) playing programs"
arch=('i686' 'x86_64')
url="http://gps.tanaka.ecc.u-tokyo.ac.jp/gpsshogi/index.php?OpenShogiLib"
license=('custom')
groups=('lib')
depends=('boost' 'boost-libs')
makedepends=('boost')
#checkdepends=('cppunit')
install=libosl.install
options=('staticlibs')
source=("osl-$pkgver.tar.gz::http://gps.tanaka.ecc.u-tokyo.ac.jp/cgi-bin/viewvc.cgi/trunk/osl/?view=tar"
#'gpsshogi-data.tar.gz::http://gps.tanaka.ecc.u-tokyo.ac.jp/cgi-bin/viewvc.cgi/trunk/gpsshogi/data/?root=gpsshogi&view=tar'
'osl-public-data.tar.gz::http://gps.tanaka.ecc.u-tokyo.ac.jp/cgi-bin/viewvc.cgi/data/?view=tar'
'makefile.local'
'Makefile.patch'
'libosl.profile'
)
md5sums=('SKIP'
         'SKIP'
         '3a14d44485fea1a1d9271fa3c478c830'
         'd2b7eb13601f96d02a32f19019033fa3'
         '42c4a9517f368adf9000a1560f2eb786')

pkgver() {
 curl -s 'http://gps.tanaka.ecc.u-tokyo.ac.jp/cgi-bin/viewvc.cgi/trunk/osl/' |\
  sed -n -e 's/^.*Revision \([0-9]*\).*$/\1/p'
}

prepare() {
  cd "$srcdir"/osl
  cp "$srcdir"/makefile.local .
  patch --verbose -i $srcdir/Makefile.patch
  cat <<'.' >> $srcdir/osl/core/osl/Makefile

dynlib: $(OBJS)
	$(CXX) -shared -Wl,-soname=libosl_core.so.1 $(OBJS) -o libosl_core.so.1.0
.
  cat <<'.' >> $srcdir/osl/std/osl/Makefile

dynlib: $(OBJS)
	$(CXX) -shared -Wl,-soname=libosl_std.so.1 $(OBJS) -o libosl_std.so.1.0
.
  cat <<'.' >> $srcdir/osl/full/osl/Makefile

dynlib: $(OBJS)
	$(CXX) -shared -Wl,-soname=libosl_full.so.1 $(OBJS) -o libosl_full.so.1.0
.
}

build() {
  cd "$srcdir"/osl
  make OSL_PUBLIC_RELEASE=t OSL_HOME_FLAGS=-DOSL_HOME=\\\"/usr/share/libosl\\\"
}

#check() {
#  cd "$srcdir/osl"
#  make -k check
#}

package() {
  _dest=osl
  install -Dm755 $srcdir/libosl.profile ${pkgdir}/etc/profile.d/libosl.sh
  install -Dm644 $srcdir/osl/LICENSE $pkgdir/usr/share/licenses/$_dest/LICENSE
  install -Dm644 $srcdir/osl/makefile.local $pkgdir/usr/share/$_dest/makefile.local
  install -Dm644 $srcdir/osl/makefile.conf $pkgdir/usr/share/$_dest/makefile.conf
  mkdir -m755 -p "$pkgdir"/usr/{lib,include}/"$_dest"
  tar xfz "$srcdir"/osl-public-data.tar.gz -C "$pkgdir"/usr/share/$_dest/
  mv "$pkgdir"/usr/share/$_dest/data "$pkgdir"/usr/share/$_dest/public-data
#  tar xfz "$srcdir"/gpsshogi-data.tar.gz -C "$pkgdir"/usr/share/$_dest/
  cd "$srcdir"/osl
  for i in std core full; do
    install -m755 "$srcdir/osl/$i/osl/libosl_$i.so.1.0" "$pkgdir"/usr/lib/
    cd "$pkgdir/usr/lib"
    ln -sf libosl_$i.so.1.0 libosl_$i.so.1
    ln -sf libosl_$i.so.1 libosl_$i.so
  done
  cd "$srcdir"/osl
  find core/osl std/osl full/osl -type f -name '*.h' | tar cT - | tar xf - -C "$pkgdir/usr/include/$_dest"
  cd $pkgdir/usr/lib/
}
