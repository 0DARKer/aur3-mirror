#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=powerpill
pkgver=2013.3.18
pkgrel=1
pkgdesc='Pacman wrapper for parallel and segmented downloads.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/powerpill"
depends=(python3 pyalpm 'pm2ml>2012.12.12' reflector aria2)
optdepends=('rsync: Rsync download support')
backup=(etc/powerpill/powerpill.json)
source=(
  http://xyne.archlinux.ca/projects/powerpill/src/powerpill-2013.3.18.tar.xz
  http://xyne.archlinux.ca/projects/powerpill/src/powerpill-2013.3.18.tar.xz.sig
)
sha512sums=(
  ac4f567e44a58802cf804088816d7159a8ed6fa28dd7ad2439c25f4f0e463767fce642386931ef195815a3fc4437c4784b3f10484da74e1f19819c70c94be40b
  1b0a52851027bd213dc4a1489cf108041d242fa26133749103055480176068f0f818d7c55cfc6344adf2b8a8d408c3bdfa62cbb188bf7777c6c02a93fb830819
)
md5sums=(
  19fe028672efe613ca5634c1a91a00d3
  2c4639e829cd5cf35cafa65d33fa0738
)
install=powerpill.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "powerpill.json" "$pkgdir/etc/powerpill/powerpill.json"
  install -Dm644 "man/powerpill.json.1.gz" "$pkgdir/usr/share/man/man1/powerpill.json.1.gz"
  install -Dm644 "powerpill-bash-completion.sh" "$pkgdir/usr/share/bash-completion/completions/powerpill"
}

# vim: set ts=2 sw=2 et:
