#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=powerpill
pkgver=2013.5.9
pkgrel=1
pkgdesc='Pacman wrapper for parallel and segmented downloads.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/powerpill"
depends=(python3 pyalpm 'pm2ml>2012.12.12' reflector aria2)
optdepends=('rsync: Rsync download support')
backup=(etc/powerpill/powerpill.json)
source=(
  http://xyne.archlinux.ca/projects/powerpill/src/powerpill-2013.5.9.tar.xz
  http://xyne.archlinux.ca/projects/powerpill/src/powerpill-2013.5.9.tar.xz.sig
)
sha512sums=(
  e812956fb5010472bf6cf9a896fabe5f8a7b437e1780420593e1ce24792b44dbf4f85354f757c7e70f0dc5007ef71ea044e27fb9e7e634200333f05435eee45d
  f2108bc758afe17dc377ef1e3069e415c9c84dcb7ff60cd51cdf5062f2455ffe059b51dc3f0e2e365d7ae41864ed93d2c9a7f1b110f3e6956dd61d0777a0917f
)
md5sums=(
  c69c8c04920db779829dc399735ffdb4
  aed66dbf8e85087c3511a85ec3f77bd9
)
install=powerpill.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "powerpill.json" "$pkgdir/etc/powerpill/powerpill.json"
  install -Dm644 "man/powerpill.json.1.gz" "$pkgdir/usr/share/man/man1/powerpill.json.1.gz"
  install -Dm644 "powerpill-bash-completion.sh" "$pkgdir/usr/share/bash-completion/completions/powerpill"
}

# vim: set ts=2 sw=2 et:
