#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=powerpill
pkgver=2013.3
pkgrel=2
pkgdesc='Pacman wrapper for parallel and segmented downloads.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/powerpill"
depends=(python3 pyalpm 'pm2ml>2012.12.12' reflector aria2)
optdepends=('rsync: Rsync download support')
backup=(etc/powerpill/powerpill.json)
source=(
  http://xyne.archlinux.ca/projects/powerpill/src/powerpill-2013.3.tar.xz
  http://xyne.archlinux.ca/projects/powerpill/src/powerpill-2013.3.tar.xz.sig
)
sha512sums=(
  330d85a1c97a3a65fdb7bc2c53953525ec706a5194988bf4b91b8dbbf2f37693f511a8e44ad21abc69912c61678eac5cbe000f1ed526d46dbf06de4038f1d506
  78de18aca592ba677950e3a0c37df15760c318b214076cec4c138749da56fee8767cbf9f71ce2d4d7f0fc969c4e84b103545a2a50fdc07a23446a4d93d1bf06b
)
md5sums=(
  6edc23b18c986daa54754953608537f2
  a4b0dd2e0489c0a516be9f68abb4535c
)
install=powerpill.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "powerpill.json" "$pkgdir/etc/powerpill/powerpill.json"
  install -Dm644 "man/powerpill.json.1.gz" "$pkgdir/usr/share/man/man1/powerpill.json.1.gz"
  install -Dm644 "powerpill-bash-completion.sh" "$pkgdir/usr/share/bash-completion/completions/powerpill"
}

# vim: set ts=2 sw=2 et:
