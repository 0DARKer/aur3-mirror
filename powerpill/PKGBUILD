#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=powerpill
pkgver=2013.5
pkgrel=1
pkgdesc='Pacman wrapper for parallel and segmented downloads.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/powerpill"
depends=(python3 pyalpm 'pm2ml>2012.12.12' reflector aria2)
optdepends=('rsync: Rsync download support')
backup=(etc/powerpill/powerpill.json)
source=(
  http://xyne.archlinux.ca/projects/powerpill/src/powerpill-2013.5.tar.xz
  http://xyne.archlinux.ca/projects/powerpill/src/powerpill-2013.5.tar.xz.sig
)
sha512sums=(
  c11d2f8e155c91f28a1dbc6c2b53e17953eacc31c1ff48ed236c60cf4f769f8e5b574328e0853861d1f9cc09e40fb788c184b6c3a57eff5c97f4f72165055da9
  81496c02cea08b8f457b1c8584a7c01b604ebfd59cf07db4d3c778c09ee53927bd07e8a9bca253418a5e4134a41483a15c7b14be2b2ea2228622fdc812bffa1a
)
md5sums=(
  648faea2a48c46b54a128f14826fc4db
  975abf507e13b2d3dcb5844fb84fa8d2
)
install=powerpill.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "powerpill.json" "$pkgdir/etc/powerpill/powerpill.json"
  install -Dm644 "man/powerpill.json.1.gz" "$pkgdir/usr/share/man/man1/powerpill.json.1.gz"
  install -Dm644 "powerpill-bash-completion.sh" "$pkgdir/usr/share/bash-completion/completions/powerpill"
}

# vim: set ts=2 sw=2 et:
