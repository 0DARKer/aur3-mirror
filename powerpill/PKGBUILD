#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=powerpill
pkgver=2012.12.16
pkgrel=1
pkgdesc='Pacman wrapper for parallel and segmented downloads.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/powerpill"
depends=(python3 pyalpm 'pm2ml>2012.12.12' reflector aria2)
optdepends=('rsync: Rsync download support')
backup=(etc/powerpill/powerpill.json)
source=(
  http://xyne.archlinux.ca/projects/powerpill/archives/powerpill-2012.12.16.tar.xz
  http://xyne.archlinux.ca/projects/powerpill/archives/powerpill-2012.12.16.tar.xz.sig
)
sha512sums=(
  499af051863d5cfe97a3017f98946a31f46fe0a38d8f32f0a51811efa6123b34b31b13308cc0bb3c1b1636d6219c2a0c872c0264be1feb28b869c142e68e9716
  cd09f7ec4f64c31beff617a73acce0be4f9bed2314bfc64788470e10adadb23ebb890b0079726779f20bf61e9f4d2debe4fcc1ddb39a4164b7486f6dd96a9d01
)
md5sums=(
  c787c09cc599c33989007bae39056431
  ccb887bb289318c0fc3d932c5c72063f
)
install=powerpill.install

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "powerpill.json" "$pkgdir/etc/powerpill/powerpill.json"
  install -Dm644 "man/powerpill.json.1.gz" "$pkgdir/usr/share/man/man1/powerpill.json.1.gz"
  install -Dm644 "powerpill-bash-completion.sh" "$pkgdir/usr/share/bash-completion/completions/powerpill"
}

# vim: set ts=2 sw=2 et:
