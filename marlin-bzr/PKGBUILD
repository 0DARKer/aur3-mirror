# Contributors: Flamelab, crazyelf5

pkgname=marlin-bzr
pkgver=762
pkgrel=1
pkgdesc="A sleek and fast GTK3 file manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/marlin"
license=('GPL2' 'GPL3')
depends=('varka-bzr' 'glib2' 'pango' 'gtk3' 'libgee' 'sqlite3' 'dbus-glib'
         'libnotify' 'gnome-icon-theme' 'dconf' 'cairo' 'atk' 'tumbler' 'gettext')
makedepends=('bzr' 'vala' 'cmake' 'desktop-file-utils' 'gtk-update-icon-cache')
optdepends=('extended-actions-bzr: Plugins integration [AUR]'
            'egtk-bzr: Elementary GTK Theme [AUR]'
            'marlin-dropbox-plugin-bzr: Dropbox integration [AUR]')
install=$pkgname.install

_bzrtrunk=lp:marlin
_bzrmod=marlin

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver
    msg "The local files are updated."
    cd "$srcdir"
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build
  [ -d build ] && rm -rf build
  mkdir build
  cd build
  CFLAGS="$CFLAGS -lm"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
