# Maintainer: Ner0
# Contributor: Flamelab

pkgname=marlin-bzr
pkgver=886
pkgrel=1
pkgdesc="A sleek and fast GTK3 file manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/marlin"
license=('GPL2' 'GPL3')
depends=('varka-bzr>=14' 'glib2' 'pango' 'gtk3' 'libgee' 'sqlite3' 'dbus-glib' 'gtk-update-icon-cache'
         'libnotify' 'gnome-icon-theme' 'dconf' 'cairo' 'atk' 'desktop-file-utils')
makedepends=('bzr' 'vala' 'cmake' 'gettext')
optdepends=('tumbler: For generating thumbnails'
            'extended-actions-bzr: Plugins integration [AUR]'
            'marlin-dropbox-plugin-bzr: Dropbox integration [AUR]')
install=marlin.install
source=('marlin::bzr+http://bazaar.launchpad.net/~marlin-devs/marlin/marlin-0.5/')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/marlin"
  bzr revno
}

prepare() {
  cd "$srcdir/marlin"
  sed -i 's/2.35.4/2.34.3/' CMakeLists.txt
}

build() {
  cd "$srcdir/marlin"
  rm -rf build/
  mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DICON_UPDATE=OFF
  make
}

package() {
  cd "$srcdir/marlin/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
