# Contributors: Flamelab, Ner0
# Maintainer: Ner0

pkgname=marlin-bzr
pkgver=819
pkgrel=2
pkgdesc="A sleek and fast GTK3 file manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/marlin"
license=('GPL2' 'GPL3')
depends=('varka-bzr' 'glib2' 'pango' 'gtk3' 'libgee' 'sqlite3' 'dbus-glib' 'gtk-update-icon-cache'
         'libnotify' 'gnome-icon-theme' 'dconf' 'cairo' 'atk' 'desktop-file-utils')
makedepends=('bzr' 'vala' 'cmake' 'gettext')
optdepends=('tumbler: For generating thumbnails'
            'contractor-bzr: Plugins integration [AUR]'
            'marlin-dropbox-plugin-bzr: Dropbox integration [AUR]')
install=$pkgname.install
source=('use-contractor.patch'
        'fix-trash.patch'
        'fix-view-switcher.patch'
        'fix-random-crashes.patch'
        'move-view-switcher.patch')
md5sums=('e175fd657a93e1dc4b3acebf1825b7b3'
         '5679cc363789aa05604a0dd5641ec00a'
         '57779b32a0450fd7af7ec9f8119bbd36'
         'd2c8a4a52823cfa1e295ce999e2dce50'
         '020486627408c99efc54371b5ee6a52d')

_bzrtrunk=lp:~victored/marlin/properties-window-notebook
_bzrmod=marlin

if [ -f /usr/lib/libunity.so.[0-9] ]; then
  _UNITY=ON
else
  _UNITY=OFF
fi

build() {
  msg "Connecting to Bazaar server...."

  if [ -d "$_bzrmod" ]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r "$pkgver" && cd ..
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r "$pkgver"
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build

  patch -Np0 -i "$srcdir/use-contractor.patch"
  patch -Np0 -i "$srcdir/fix-trash.patch"
  patch -Np0 -i "$srcdir/fix-view-switcher.patch"
  patch -Np0 -i "$srcdir/fix-random-crashes.patch"
  patch -Np0 -i "$srcdir/move-view-switcher.patch"

  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_EXE_LINKER_FLAGS="$CFLAGS -lm" \
           -DGSETTINGS_COMPILE=OFF -DGSETTINGS_LOCALINSTALL=OFF -DWITH_UNITY=${_UNITY}
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
