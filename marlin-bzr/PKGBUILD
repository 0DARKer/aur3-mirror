# Contributors: Flamelab, crazyelf5

pkgname=marlin-bzr
pkgver=666
pkgrel=1
pkgdesc="A sleek and fast GTK3 file manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/marlin"
license=('GPL2' 'GPL3')
depends=('varka-bzr' 'glib2' 'pango' 'gtk3' 'libgee' 'sqlite3' 'dbus-glib'
         'libnotify' 'gnome-icon-theme' 'dconf' 'cairo' 'atk' 'tumbler')
makedepends=('bzr' 'vala' 'cmake' 'desktop-file-utils' 'gtk-update-icon-cache')
optdepends=('egtk-bzr: Elementary GTK Theme [AUR]'
            'marlin-dropbox-plugin-bzr: Dropbox Integration [AUR]')
install=${pkgname}.install

_bzrtrunk=lp:${pkgname%-*}
_bzrmod=${pkgname%-*}

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ ! -d ${_bzrmod} ]; then
    bzr co ${_bzrtrunk} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_bzrmod}-build
  cp -r ${_bzrmod} ${_bzrmod}-build
  cd ${_bzrmod}-build
  mkdir build; cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build/build
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  rm -rf "${srcdir}"/${_bzrmod}-build
}
