# Maintainer: Michele Mocciola <mickele>
# Contributor: Brice MÃ©alier <mealier_brice@yahoo.fr>

pkgname=med
pkgver=2.3.6
pkgrel=4
pkgdesc="MED stands for Modelisation et Echanges de Donnees, i.e. Data Modelization and Exchanges - MED is code-aster exchange module linked to hdf5"
url="http://www.code-aster.org/outils/med/"
license="LGPL"
depends=('hdf5' 'tk' 'gcc-libs')
makedepends=('make' 'gcc' 'gcc-fortran' 'coreutils')
provides=()
conflicts=('med_fichier')
replaces=('med_fichier')
backup=()
arch=('i686' 'x86_64')
install=${pkgname}.install
source=("https://launchpad.net/debian/+archive/primary/+files/med-fichier_2.3.6.orig.tar.gz")

build() {
  if [ "$CARCH" = "x86_64" ]; then
    export FFLAGS="-fopenmp -fPIC -fdefault-double-8 -fdefault-integer-8 -fdefault-real-8 -ffixed-line-length-0 ${CFLAGS}"
    export FCFLAGS="-fopenmp -fPIC -fdefault-double-8 -fdefault-integer-8 -fdefault-real-8 -ffixed-line-length-0 ${CFLAGS}"
    export CPPFLAGS="-DHAVE_F77INT64 ${CPPFLAGS}"
  else # i686
    export FFLAGS="-fopenmp -fPIC -ffixed-line-length-0 ${CFLAGS}"
    export FCFLAGS="-fopenmp -fPIC -ffixed-line-length-0 ${CFLAGS}"
  fi

  cd ${srcdir}/${pkgname}-${pkgver}_SRC || return 1

  ./configure --with-f90=gfortran --prefix=/usr || return 1
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}_SRC || return 1

  make DESTDIR=${pkgdir} install || return 1

  mv ${pkgdir}/usr/share/doc ${pkgdir}/usr/share/doc-tmp || return 1
  mkdir -p ${pkgdir}/usr/share/doc/ || return 1
  mv ${pkgdir}/usr/share/doc-tmp ${pkgdir}/usr/share/doc/${pkgname} || return 1

  cd ${pkgdir} || return 1
  for _FILE in `grep -Rl "\-L${srcdir}/${pkgname}-${pkgver}_SRC/src" *`
  do
    sed -e "s| \-L${srcdir}/${pkgname}-${pkgver}_SRC/src||" \
    	-i ${_FILE}
  done
}

md5sums=('f6ad3aedc88aa4165c96f82dc5f09b47')
