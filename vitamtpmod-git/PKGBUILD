# Maintainer: codestation <codestation404@gmail.com>

pkgname=vitamtpmod-git
_pkgname=VitaMTP
pkgver=2.5.3.r0.g9d5c8cb
pkgrel=1
pkgdesc="Library to interact with Vita's USB MTP protocol (fork)"
arch=("i686" "x86_64")
url="https://github.com/codestation/VitaMTP"
license=('GPL')
depends=('libusb' 'libxml2')
conflicts=('vitamtp' 'vitamtp-git')
source=('git://github.com/codestation/VitaMTP.git')
options=('!libtool')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="${pkgdir}" install
  install -Dm644 debian/vitamtp3.udev "$pkgdir/usr/lib/udev/rules.d/80-psvita.rules"
}
