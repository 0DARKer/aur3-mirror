# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: codestation <codestation404@gmail.com>

pkgname=vitamtpmod-git
pkgver=v2.5.4.0.gd6062d2
pkgrel=1
pkgdesc="Library to interact with Vita's USB MTP protocol (fork) (GIT version"
arch=("i686" "x86_64")
url="https://github.com/codestation/VitaMTP"
license=('GPL')
depends=('libusb' 'libxml2')
provides=('vitamtpmod')
conflicts=('vitamtpmod')
source=('vitamtpmod::git+https://github.com/codestation/VitaMTP.git')
options=('!libtool')
md5sums=('SKIP')
_gitname=vitamtpmod

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags |tr - .)"
}

build() {
  cd "${_gitname}"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 debian/vitamtp4.udev "$pkgdir/usr/lib/udev/rules.d/80-psvita.rules"
}
