# Maintainer: Carl George < arch at cgtx dot us >

_name="oslo.serialization"
_module="${_name/./-}"

pkgname=("python-${_module}" "python2-${_module}")
pkgver="1.5.0"
pkgrel="2"
pkgdesc="Oslo Serialization library"
arch=("any")
url="https://github.com/openstack/${_name}"
license=("Apache")
makedepends=("python-pbr>=0.6"
             "python-pip"
             "python-setuptools"
             "python2-pbr>=0.6"
             "python2-pip"
             "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_name:0:1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('3ca368c96777cb54a656b6429914717d17f05a7bda13ce6065c0930dce0d4f78')

prepare() {
    cd "${srcdir}/${_name}-${pkgver}"
    sed -i '/pbr/d' requirements.txt
}

package_python-oslo-serialization() {
    depends=("python>=3.3"
             "python-babel>=1.3"
             "python-six>=1.9.0"
             "python-msgpack>=0.4.0"
             "python-iso8601>=0.1.9"
             "python-oslo-utils>=1.4.0"
             "python-pytz>=2013.6")
    cd "${srcdir}/${_name}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-oslo-serialization() {
    depends=("python2>=2.6"
             "python2-babel>=1.3"
             "python2-six>=1.9.0"
             "python2-msgpack>=0.4.0"
             "python2-iso8601>=0.1.9"
             "python2-oslo-utils>=1.4.0"
             "python2-pytz>=2013.6")
    cd "${srcdir}/${_name}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1
}
