# Maintainer: Carl George < arch at cgtx dot us >

_module="oslo.serialization"
pkgname=("python-${_module/./-}" "python2-${_module/./-}")
pkgver="1.2.0"
pkgrel="1"
pkgdesc="oslo.serialization library"
arch=("any")
url="https://github.com/openstack/${_module}"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('073f25ef4b7138e28ecb75bab413582c4dc1a54ab63b540a1702edee7c0a0557')

prepare() {
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

#check() {
#	cd "${srcdir}/${_module}-${pkgver}"
#	python setup.py test
#	cd "${srcdir}/${_module}-${pkgver}-python2"
#	python2 setup.py test
#}

package_python-oslo-serialization() {
	conflicts=("python-keystoneclient-git")
	depends=(
		"python>=3.3"
		"python-pbr>0.7"
		"python-babel>=1.3"
		"python-six>=1.7.0"
		"python-iso8601>=0.1.9"
		"python-oslo-utils>=1.0.0"
	)
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-oslo-serialization() {
	conflicts=("python2-keystoneclient-git")
	depends=(
		"python2>=2.6"
		"python2-pbr>0.7"
		"python2-babel>=1.3"
		"python2-six>=1.7.0"
		"python2-iso8601>=0.1.9"
		"python2-oslo-utils>=1.0.0"
	)
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
