# Maintainer: Carl George < arch at cgtx dot us >

_module="oslo.serialization"
pkgname=("python-${_module/./-}" "python2-${_module/./-}")
pkgver="1.4.0"
pkgrel="1"
pkgdesc="oslo.serialization library"
arch=("any")
url="https://github.com/openstack/${_module}"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools" "python-pbr" "python2-pbr")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('082dca272f0a941e1cdca1dc02849c5ddac65fa7d19604096be54cd41e7b4317')

prepare() {
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

#check() {
#	cd "${srcdir}/${_module}-${pkgver}"
#	python setup.py test
#	cd "${srcdir}/${_module}-${pkgver}-python2"
#	python2 setup.py test
#}

package_python-oslo-serialization() {
	conflicts=("python-keystoneclient-git")
	depends=(
		"python>=3.3"
		"python-pbr>0.7"
		"python-babel>=1.3"
		"python-six>=1.9.0"
		"python-msgpack>=0.4.0"
		"python-iso8601>=0.1.9"
		"python-oslo-utils>=1.2.0"
		"python-pytz>=2013.6"
	)
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-oslo-serialization() {
	conflicts=("python2-keystoneclient-git")
	depends=(
		"python2>=2.6"
		"python2-pbr>0.7"
		"python2-babel>=1.3"
		"python2-six>=1.9.0"
		"python2-msgpack>=0.4.0"
		"python2-iso8601>=0.1.9"
		"python2-oslo-utils>=1.2.0"
		"python2-pytz>=2013.6"
	)
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
