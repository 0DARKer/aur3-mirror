# Maintainer: Devin Cofer <ranguvar@archlinux.us>
# Contributor: Wantilles
# Contributor: xduugu
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=cairo-xcb-tee
pkgver=1.10.2
pkgrel=1
pkgdesc="Cairo vector graphics library, xcb and tee enabled"
arch=('i686' 'x86_64')
license=('LGPL' 'MPL')
url="http://cairographics.org/"

makedepends=('pkgconfig')
depends=('libpng>=1.4.0' 'libxrender' 'fontconfig>=2.8.0' 'pixman>=0.18.4'
         'glib2>=2.24.0' 'xcb-util>=0.3.6')
provides=("cairo=$pkgver" "cairo-xcb=$pkgver" "cairo-tee=$pkgver")
conflicts=('cairo' 'cairo-xcb' 'cairo-tee')

source=("http://cairographics.org/releases/cairo-$pkgver.tar.gz"
        'cairo-1.10.0-buggy_gradients.patch')
sha256sums=('32018c7998358eebc2ad578ff8d8559d34fc80252095f110a572ed23d989fc41'
            '79085147567e6e080d5c1d7d2246f7d4be1efcc8e4b4fe44b6db1ebeae93c420')


options=('!libtool')
build() {
	cd "$srcdir"/cairo-$pkgver

	patch -Np1 -i "$srcdir"/cairo-1.10.0-buggy_gradients.patch

	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	            --enable-xcb --enable-tee --disable-static
	make
}
package() {
	cd "$srcdir"/cairo-$pkgver

	make DESTDIR="$pkgdir" install
}
