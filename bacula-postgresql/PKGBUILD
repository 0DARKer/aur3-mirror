# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Calogero Lo Leggio <kalos@autistici.org>
# Contributor: Matias Hernandez <msdark@archlinux.cl>

pkgname=bacula-postgresql
_pkgname=bacula
pkgver=5.2.6
pkgrel=1
pkgdesc="An advanced backup tool with network and tape changer support (PostgreSQL backend)"
arch=("i686" "x86_64")
url="http://www.${_pkgname}.org"
license=("GPL")
depends=("postgresql-libs" "lzo2")
makedepends=("qt" "wxgtk" "gtk2" "lzo2")
optdepends=("qt: for bat"
            "wxgtk: for bwx console"
            "gtk2: for tray monitor"
			"python: dvd-handler script")
options=(!buildflags !libtool)
conflicts=("${_pkgname}-sqlite" "${_pkgname}" "${_pkgname}-client")
backup=("etc/${_pkgname}/bconsole.conf"
		"etc/${_pkgname}/${_pkgname}-dir.conf"
		"etc/${_pkgname}/${_pkgname}-fd.conf"
		"etc/${_pkgname}/${_pkgname}-sd.conf")
install="${_pkgname}.install"
source=(http://downloads.sourceforge.net/project/bacula/bacula/${pkgver}/${_pkgname}-${pkgver}.tar.gz
        ${_pkgname}-sd.rc.d
        ${_pkgname}-fd.rc.d
        ${_pkgname}-dir.rc.d)
md5sums=('914483b92539598678542bd2ddb28043'
         'e7be4c9fb3c7ce334f9ee24d71652d3e'
         'a9a070a862e9cf39ae4bc374409c721d'
         '55619ae3094c698541a3bfedc87bdb48')

build() {
	cd ${srcdir}/${_pkgname}-${pkgver}

	# Build
	./configure --prefix=/usr \
		--enable-build-dird --enable-build-stored --enable-smartalloc \
		--enable-bat --enable-tray-monitor --enable-bwx-console \
		--with-postgresql --with-tcp-wrappers=no \
		--with-dir-user=${_pkgname} --with-dir-group=${_pkgname} \
		--with-sd-user=${_pkgname} --with-sd-group=${_pkgname} \
		--sysconfdir=/etc/${_pkgname} --with-scriptdir=/etc/${_pkgname}/scripts \
		--with-working-dir=/var/cache/${_pkgname}/working \
		--with-subsys-dir=/var/cache/${_pkgname}/working \
		--with-archivedir=/var/cache/${_pkgname}/archive || return 1
		#--with-readline --with-python --with-openssl --with-tcp-wrappers \

	make || return 1
}

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install || return 1

	# Permissions
	chmod a+x ${pkgdir}/etc/${_pkgname}/scripts/{update_${_pkgname}_tables,delete_catalog_backup,update_postgresql_tables,make_catalog_backup,bconsole}

	# Daemons
	mkdir -p ${pkgdir}/etc/rc.d/
    install -Dm755 ${srcdir}/bacula-dir.rc.d ${pkgdir}/etc/rc.d/bacula-dir
    install -Dm755 ${srcdir}/bacula-sd.rc.d ${pkgdir}/etc/rc.d/bacula-sd
    install -Dm755 ${srcdir}/bacula-fd.rc.d ${pkgdir}/etc/rc.d/bacula-fd

	# Logs
	install -D -m644 ${srcdir}/${_pkgname}-${pkgver}/scripts/logrotate ${pkgdir}/etc/logrotate.d/${_pkgname}
	sed -i "s|/var/cache/${_pkgname}/working/log|/var/log/${_pkgname}.log|g" ${pkgdir}/etc/{${_pkgname}/${_pkgname}-dir.conf,logrotate.d/${_pkgname}}
}

