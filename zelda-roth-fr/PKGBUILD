# Maintainer: FoolEcho <foolecho at gmail dot com>
pkgname=zelda-roth-fr
pkgver=6.11
pkgrel=1
pkgdesc="Zelda: Return of the Hylian (french version)"
url="http://www.zeldaroth.fr/"
arch=('i686' 'x86_64')
depends=('sdl_gfx' 'sdl_mixer' 'sdl_image' 'xdg-utils' 'hicolor-icon-theme')
license=('Unknown')
makedepends=(imagemagick)
source=(
  "http://www.zeldaroth.fr/fichier/ROTH/linux/ZeldaROTH-src-linux.zip" 
  zelda-roth.desktop
  zelda-roth.install)
md5sums=('3e3f9a3c944491d1cb7683a06437ae38'
         'e668913f4420520de8775192639997f5'
         '9869fb57d07de3b687ca68608dd9d7ec')

_name=zelda-roth

build () {
	cd $srcdir/ZeldaROTH-src-linux/src
	
	# Fix the Game so that it reads save files from $HOME/.zelda-roth
	sed -i -e s~\"data/save/~string\(getenv\(\"HOME\"\)\)+\"/.$_name/~g Joueur.cpp
	
	#Make the game treat /usr/share/$pkgname as its data directory
	for i in *.cpp
	do
		sed -i -e s~data/~/usr/share/$pkgname/~g $i
	done
	
	# Create the directory $HOME/.zelda-roth if it doesnt exist
	sed -i -e s~'(NULL));'~'&\n\tif (system(\"stat $HOME/.'$_name' \&> /dev/null")) system (\"mkdir $HOME/.'$_name'\");'~ main.cpp
	make
}

package () {
	
	install -Dm644 $_name.desktop $pkgdir/usr/share/applications/$_name.desktop
	cd $srcdir/ZeldaROTH-src-linux/src
	
	install -Dm755 ZeldaROTH $pkgdir/usr/bin/$_name
	
	cd data
	for i in {map,music,sound}/*
	do
		install -Dm644 $i $pkgdir/usr/share/$pkgname/$i
	done
	
	cd images
	for i in */*
        do
                install -Dm644 $i $pkgdir/usr/share/$pkgname/images/$i
        done
	mkdir -p $pkgdir/usr/share/icons/hicolor/256x256/apps
	convert logos/fond.png -resize 256x256\! $pkgdir/usr/share/icons/hicolor/256x256/apps/$_name.png
}
