# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani at gmail dot com>
# Contributor: Amadiro <jwringstad@gmail.com>

pkgname=krita
_pkgname=calligra
pkgver=2.9.2
pkgrel=2
pkgdesc="Digital painting and illustration suite (stand-alone)."
arch=('i686' 'x86_64')
url="http://www.krita.org/"
license=('GPL2')
depends=('eigen' 'fftw' 'glew' 'gsl' 'kdebase-runtime' 'libkdcraw' 'opencolorio' 'boost-libs' 'curl' 'poppler-qt4')
makedepends=('automoc4' 'boost' 'cmake' 'lcms2' 'openjpeg' 'vc')
provides=('calligra-krita')
conflicts=('calligra-krita' 'calligra-plugins' 'calligra-libs' 'calligra-extras' 'calligra-filters')
install=krita.install
source=("http://download.kde.org/stable/${_pkgname}-${pkgver}/${_pkgname}-${pkgver}.tar.xz")
sha256sums=('5d8f8909c393cdf85132fc5bdc0dc93c855c5fc7f52f41748c17c2dae452942b')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  mkdir ${pkgname}-build
}

# Remove comment markers only if building with vc on Haswell
build() {
    cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}-build"
  cmake ../ \
    -DCMAKE_BUILD_TYPE=Release \
    -DPRODUCTSET=KRITA \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_Soprano=OFF #\
#    -DTARGET_ARCHITECTURE=none \
#    -DCMAKE_CXX_FLAGS="-fabi-version=0 -march=native"
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}-build"
  make DESTDIR="$pkgdir" install
}
