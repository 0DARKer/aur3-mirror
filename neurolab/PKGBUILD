# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=neurolab
pkgver=1.2.0
pkgrel=1
pkgdesc="Neurocognitive Linguistics Lab is an approach to linguistics which uses relational networks to model what the brain actually does when it handles language."
arch=(i686 x86_64)
url="https://bitbucket.org/kulibali/neurocogling/wiki/Home"
license=('BSD')
depends=('qt')
install="$pkgname.install"
source=("https://bitbucket.org/kulibali/neurocogling/get/$pkgver.tar.bz2"
        "$pkgname.svg::http://upload.wikimedia.org/wikipedia/commons/2/23/Brain_Surface_Gyri.SVG"
        "$pkgname.desktop"
        "$pkgname.patch")
md5sums=('7199d54846657abe2f09c8276b989e84'
         '17493f4c891a607a8da1360c7ffc7c3d'
         '8057430307dc932460a525ef1d606824'
         '3873e82be3d48a7808590627a7673880')

build() {
  cd `find $srcdir -maxdepth 1 -mindepth 1 -type d`/src

  # patches in order to compile on x86_64
  patch -Np3 -i $srcdir/$pkgname.patch

  qmake neurolab_all.pro
  make
}

package() {
  cd `find $srcdir -maxdepth 1 -mindepth 1 -type d`/src

  # bin
  install -Dm755 release/$pkgname "$pkgdir/usr/bin/$pkgname"

  # libs docs and examples
  install -d "$pkgdir"/usr/{lib,share/$pkgname/{examples,docs}}

  cp -a release/{plugins/*,*.so.*} "$pkgdir/usr/lib"
  install -Dm644 ../doc/manual.pdf "$pkgdir/usr/share/$pkgname/docs"
  install -Dm644 ../samples/* "$pkgdir/usr/share/$pkgname/examples"

  # desktop and pixmaps
  install -Dm644 "$srcdir/$pkgname.desktop" \
                 "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/$pkgname.svg" \
                 "$pkgdir/usr/share/pixmaps/$pkgname.svg"

  # license
  install -Dm644 ../LICENSE.txt \
                 "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
