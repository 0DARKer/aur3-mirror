# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=neurolab
pkgver=1.2.2
pkgrel=1
pkgdesc="Neurocognitive Linguistics Lab is an approach to linguistics which uses relational networks to model what the brain actually does when it handles language."
arch=(i686 x86_64)
url="https://bitbucket.org/kulibali/neurocogling/wiki/Home"
license=('BSD')
depends=('qt')
install="$pkgname.install"
source=("https://bitbucket.org/kulibali/neurocogling/get/$pkgver.tar.bz2"
        "$pkgname.png::http://upload.wikimedia.org/wikipedia/commons/thumb/2/23/Brain_Surface_Gyri.SVG/64px-Brain_Surface_Gyri.SVG.png"
        "$pkgname.desktop" "$pkgname.patch")
md5sums=('43304a81589c4b1f4d3777aa148b90de'
         '069237839c0b9371293127257e99c698'
         '8057430307dc932460a525ef1d606824'
         '3873e82be3d48a7808590627a7673880')

build() {
  cd "$srcdir/kulibali-"*/src

  # build fixes
  sed -i "s/\(reinterpret_cast.*IdType\)\(.*\);/(\*\1 \*\2);/" \
    `grep -rl "reinterpret_cast.*IdType" .`
  sed -i "/IdType/s/\(reinterpret_cast.*<\)\(NeuroItem\)/\1const \2/" \
    neurogui/subnetwork/subnetworkitem.cpp

  # add missing glu header
  sed -i "44a#include <GL/glu.h>" griditems/gridviewer.h

  qmake neurolab_all.pro
  make
}

package() {
  cd "$srcdir/kulibali-"*/src

  # bin
  install -Dm755 release/$pkgname "$pkgdir/usr/bin/$pkgname"

  # libs docs and examples
  install -d "$pkgdir"/usr/{lib,share/$pkgname/{examples,docs}}

  cp -a release/{plugins/*,*.so.*} "$pkgdir/usr/lib"
  install -Dm644 ../doc/manual.pdf "$pkgdir/usr/share/$pkgname/docs"
  install -Dm644 ../samples/* "$pkgdir/usr/share/$pkgname/examples"

  # desktop and pixmaps
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # license
  install -Dm644 ../LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
