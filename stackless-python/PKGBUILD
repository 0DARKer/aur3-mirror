# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=stackless-python
pkgver=3.1.2
pkgrel=1
pkgdesc='Python is a high-level scripting language. Stackless is an experimental implementation that supports continuations, generators, microthreads, and coroutines.'
url='http://www.stackless.com'
arch=('i686' 'x86_64')
depends=('db' 'bzip2' 'gdbm' 'openssl')
makedepends=('tk')
optdepends=('tk: for tkinter')
conflicts=('python3')
provides=('python3')
license=('PSFLv2') # Python Software Foundation License Version 2; GPL-compatible
source=("http://www.stackless.com/binaries/stackless-${pkgver//./}-export.tar.bz2")
md5sums=('c43edb361c8321284cdc19a1b157daba')

build() {
  cd python-${pkgver}-stackless

  ./configure \
    --prefix=/usr \
    --with-threads \
    --enable-shared \
    --enable-stacklessfewerregisters

  make
}

package() {
  cd python-${pkgver}-stackless

  make DESTDIR=${pkgdir} install

  rm -f ${pkgdir}/usr/bin/2to3
}
