# Maintainer: Oliver Sherouse <first dot last at gmail dot com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=banshee-nognome
_pkgname=banshee
pkgver=2.0.1
pkgrel=1
pkgdesc="Media management and playback application (no-GNOME dependencies)"
arch=('i686' 'x86_64')
url="http://banshee.fm/"
license=('MIT')
depends=('boo' 'desktop-file-utils' 'gdata-sharp' 'gkeyfile-sharp'
         'gstreamer0.10-base-plugins' 'gtk-sharp-beans' 'gudev-sharp'
         'hicolor-icon-theme' 'libgpod' 'libmtp' 'libwebkit' 'mono-addins'
         'mono-zeroconf' 'ndesk-dbus-glib' 'taglib-sharp' 'xdg-utils')
makedepends=('intltool')
optdepends=('brasero: cd burning'
            'gvfs: audio devices'
            'gstreamer0.10-bad-plugins: extra media codecs'
            'gstreamer0.10-ffmpeg: extra media codecs'
            'gstreamer0.10-good-plugins: extra media codecs'
            'gstreamer0.10-ugly-plugins: extra media codecs')
provides=(${_pkgname})
conflicts=(${_pkgname})
options=('!libtool')
install=${pkgname}.install
source=(http://download.banshee.fm/${_pkgname}/stable/${pkgver}/${_pkgname}-${pkgver}.tar.bz2)
md5sums=('83d77447936eed84eba2123b341b62ea')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${_pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-docs --disable-static --disable-scrollkeeper \
              --disable-gnome --disable-ipod --disable-hal \
              --with-vendor-build-id=ArchLinux
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
