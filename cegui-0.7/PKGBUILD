# Contributor: Bassam Jabbour <bassam.jabbour at gmail.com>
# Maintainer:  zoulnix <http://goo.gl/HQaP>
pkgname=cegui-0.7
pkgver=0.7.5
pkgrel=2
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines."
arch=('i686' 'x86_64')
url="http://www.cegui.org.uk/"
license=('MIT')
depends=('devil' 'expat' 'freetype2' 'libxml2' 'lua' 'pcre' 'python2' 'silly')
makedepends=('gcc' 'make' 'pkg-config')
options=('!libtool')
conflicts=('cegui')
source=(http://downloads.sourceforge.net/crayzedsgui/CEGUI-${pkgver}.tar.gz)
md5sums=('38c79d1fdfaaa10f481c99a2ac479516')

build() {
  cd ${srcdir}/CEGUI-${pkgver}

  PYTHON=python2 \
  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --localstatedir=/var \
	      --disable-static \
	      --disable-xerces-c \
	      --disable-samples \
	      --enable-devil \
	      --enable-silly \
	      --enable-lua-module \
	      --enable-python-module \
	      --with-default-image-codec=SILLYImageCodec

  make || return 1
}

package() {
  cd ${srcdir}/CEGUI-${pkgver}
  install -d ${pkgdir}/usr/share/licenses/${pkgname} || return 1

  make DESTDIR=${pkgdir} install || return 1

  install -m644 doc/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1
}
