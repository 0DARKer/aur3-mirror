# Maintainer: György Balló <ballogy@freestart.hu>
_pkgname=gwibber
pkgname=$_pkgname-bzr
pkgver=992
pkgrel=1
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku"
arch=('any')
url="http://gwibber.com/"
license=('GPL')
depends=('dbus-python>=0.80.2' 'python2-feedparser>=4.1' 'python2-gconf>=2.18.0' 'python-gnomekeyring' 'gnome-keyring' 'python-wnck' 'python-mako>=0.2.2' 'python-egenix-mx-base>=3.0.0' 'python2-oauth' 'python-imaging>=1.1.6' 'python-pycurl' 'python-simplejson>=1.9.1' 'pywebkitgtk>=1.0.1' 'pyxdg>=0.15')
makedepends=('bzr' 'python-distutils-extra')
optdepends=('python2-gtkspell: spell-checking support'
            'python-notify: notification support')
provides=('gwibber')
conflicts=('gwibber')
options=(!emptydirs)
source=(launchpad-export.tar.gz)
md5sums=('1980931d983a0842e47cd918cda1c4bf')

_bzrtrunk="lp:gwibber"
_bzrmod="gwibber"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  #
  # BUILD HERE
  #

  rename $_pkgname- '' ../po/$_pkgname-*.po
  mv -f -t po ../po/*

  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  python2 setup.py build
}

package() {
  cd "$srcdir/$_bzrmod-build"

  python2 setup.py install --root=$pkgdir/ --optimize=1
}
