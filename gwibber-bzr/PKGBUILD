# Maintainer: György Balló <ballogy@freestart.hu>
_pkgname=gwibber
pkgname=$_pkgname-bzr
pkgver=1056
pkgrel=1
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku"
arch=('i686' 'x86_64')
url="http://gwibber.com/"
license=('GPL')
depends=('libgee' 'libnotify>=0.7' 'dee>=0.5.18-2' 'json-glib' 'desktop-file-utils' 'dbus-python' 'gnome-keyring' 'python-gnomekeyring' 'python-wnck' 'python-egenix-mx-base' 'python2-oauth' 'python-imaging' 'python-pycurl' 'python-simplejson' 'pywebkitgtk' 'pyxdg')
makedepends=('bzr' 'gnome-common' 'vala>=0.9.2' 'intltool>=0.35.0' 'gobject-introspection>=0.10')
optdepends=('python2-gtkspell: spell-checking support'
            'python-notify: notification support')
provides=('gwibber')
conflicts=('gwibber')
options=(!libtool)
install=$_pkgname.install
source=(noindicate.patch)
md5sums=('388c6d63586b0351cb79a01e03f3c761')

_bzrtrunk="lp:gwibber"
_bzrmod="gwibber"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  #
  # BUILD HERE
  #

  patch -Np1 -i "$srcdir/noindicate.patch"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static --disable-schemas-compile --disable-unity
  make
  make DESTDIR="$pkgdir" install
}
