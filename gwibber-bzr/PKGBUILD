# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=gwibber-bzr
_pkgname=gwibber
pkgver=1234
pkgrel=1
pkgdesc="Microblogging client for GNOME, which supports Twitter, Identi.ca, StatusNet, Facebook, Flickr, Digg, FriendFeed and Qaiku"
arch=(i686 x86_64)
url="http://gwibber.com/"
license=(GPL)
depends=(libgee 'dee>=0.5.18-2' json-glib gtkspell3 dbus-python gnome-keyring python-gnomekeyring python-notify python-wnck python-egenix-mx-base python2-oauth python-imaging python-pycurl python-simplejson pywebkitgtk pyxdg xdg-utils)
makedepends=(bzr gnome-common 'vala>=0.14' 'intltool>=0.35.0' 'gobject-introspection>=0.10')
optdepends=('libindicate: Messages Indicator support')
provides=(gwibber)
conflicts=(gwibber)
options=(!libtool)
install=$_pkgname.install
source=(gwibber-dee-1.0.patch)
md5sums=('fc28769316d64efbb0461fe5c855e3a9')

_bzrtrunk="lp:gwibber"
_bzrmod="gwibber"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  #
  # BUILD HERE
  #
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Port to dee 1.0
  patch -Np0 -i "$srcdir/gwibber-dee-1.0.patch"

  # Disable Unity
  sed -i '/Dbusmenu/ d' client/Makefile.am

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
                --disable-static --disable-schemas-compile --disable-unity
  make
  make DESTDIR="$pkgdir" install
}
