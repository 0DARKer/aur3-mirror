# Maintainer: oke3 <Sekereg [at] gmx [dot] com>
# Contributor: Manuel Mazzuola <origin.of@gmail.com>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=banshee-git
pkgver=20120119
pkgrel=1
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee.fm/"
license=('MIT')
depends=('libxxf86vm' 'gstreamer0.10-base-plugins' 'mono-addins' 'notify-sharp-svn' 'dbus-sharp-glib' 'boo' 'libwebkit' 'libsoup-gnome' 'gdata-sharp' 'taglib-sharp' 'gudev-sharp' 'gkeyfile-sharp' 'gconf-sharp' 'libmtp' 'libgpod' 'mono-zeroconf' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'media-player-info' 'gtk-sharp-beans-git')
makedepends=('intltool' 'gnome-doc-utils' 'git')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: CD burning')
provides=('banshee')
conflicts=('banshee' 'banshee-devel')
options=('!libtool')
install=$pkgname.install
source=(banshee-recent-gpod.patch)
sha256sums=('018eeb00d1b4e625d13f1184863e499be5b5dde050a7513e012d5898125073d9')

_gitroot="git://git.gnome.org/banshee"
_gitname="banshee"

build() {
#    export MONO_SHARED_DIR="$srcdir/.wabi"
#    mkdir -p "$MONO_SHARED_DIR"

    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin && git submodule init \
            && git submodule update && cd "$srcdir"
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting build..."

    rm -rf "$_gitname-build"
    git clone "$_gitname" "$_gitname-build"

    cd "$_gitname-build"
    patch -Np1 -i "$srcdir/banshee-recent-gpod.patch"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-docs \
              --disable-static \
              --disable-scrollkeeper \
              --disable-schemas-install \
              --with-vendor-build-id=ArchLinux

    make
}

package() {
#    export MONO_SHARED_DIR="$srcdir/.wabi"
#    mkdir -p "$MONO_SHARED_DIR"

    cd "$srcdir/$_gitname-build"

    make DESTDIR="$pkgdir" install

    install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
