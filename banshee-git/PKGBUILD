# Maintainer: Mauro Fruet <maurofruet@gmail.com>
# Contributor: oke3 <Sekereg [at] gmx [dot] com>
# Contributor: Manuel Mazzuola <origin.of@gmail.com>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=banshee-git
pkgver=20121005
pkgrel=1
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee.fm/"
license=('MIT')
depends=('mono' 'sqlite3' 'gstreamer0.10' 'gtk-sharp-2' 'glib' 'dbus-sharp' 'dbus-sharp-glib' 'mono-addins' 'taglib-sharp' 'libmtp' 'mono-zeroconf' 'boo' 'gio-sharp-git' 'gtk-sharp-beans-git' 'gdata-sharp' 'libwebkit' 'gudev-sharp' 'gkeyfile-sharp' 'libgpod' 'mono-upnp' 'udev' 'media-player-info' 'avahi' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-base-plugins' 'libsoup-gnome' 'gconf-sharp' 'hicolor-icon-theme' 'desktop-file-utils' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils' 'git')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: CD burning')
options=('!libtool')
provides=('banshee')
conflicts=('banshee' 'banshee-devel')
install=$pkgname.install

_gitroot="git://git.gnome.org/banshee"
_gitname="banshee"

build() {
    export MONO_SHARED_DIR="$srcdir/.wabi"
    mkdir -p "$MONO_SHARED_DIR"

    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin && git submodule init \
            && git submodule update && cd "$srcdir"
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting build..."

    rm -rf "$_gitname-build"
    git clone "$_gitname" "$_gitname-build"

    cd "$_gitname-build"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-docs \
              --disable-static \
              --disable-scrollkeeper \
              --disable-schemas-install \
              --with-vendor-build-id=ArchLinux

    make
}

package() {
    export MONO_SHARED_DIR="$srcdir/.wabi"
    mkdir -p "$MONO_SHARED_DIR"

    cd "$srcdir/$_gitname-build"

    make DESTDIR="$pkgdir" install

    install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
