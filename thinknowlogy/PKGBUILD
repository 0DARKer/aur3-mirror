# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=Thinknowlogy
pkgname=thinknowlogy
pkgver=2011r1
pkgrel=1
pkgdesc="An artificial language system (by Design), designed to be intelligent."
arch=(i686 x86_64)
url="http://mafait.org/"
license=('GPL2')
source=("${url}files/download/${_pkg}${pkgver}.zip")
md5sums=('f1b3888f270ebfb48585cd9013cb713b')

build() {
  cd "$srcdir/$_pkg$pkgver"

  # Fix data paths
  sed -i "s|data/|/usr/share/$pkgname/&|g" \
         source/C++/Constants.h data/startup/startup.txt

  g++ source/C++/*.cpp -o $pkgname
}

package() {
  cd "$srcdir/$_pkg$pkgver"

  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"

  # datas
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a data docs "$pkgdir/usr/share/$pkgname"

  # removing non UTF-8 paths
  cd "$pkgdir/usr/share/$pkgname/docs/theory"
  rm -rf N* P* D* F* Es*
}

# vim:set ts=2 sw=2 et:
