# Maintainer: willemw <willemw12@gmail.com>
# Contributor: solid <patrick.schwalm (at) googlemail.com
# Contributor: J. W. Birdsong <@gmail.com>

_pkgname=backintime
pkgbase=$_pkgname-bzr
pkgname=($_pkgname-bzr $_pkgname-gnome-bzr $_pkgname-kde4-bzr)
pkgver=r896
pkgrel=1
url="http://backintime.le-web.org"
license=('GPL')
arch=('any')
makedepends=('bzr')
source=($_pkgname::bzr+https://code.launchpad.net/~bit-team/backintime/trunk)
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd $_pkgname
  sed -i "s|^\(Name=Back In Time\)$|\1 (GNOME)|" gnome/backintime-gnome.desktop
  sed -i "s|^Name=Back In Time (root)$|Name=Back In Time (GNOME, root)|" gnome/backintime-gnome-root.desktop
  sed -i "s|^\(Name=Back In Time\)$|\1 (KDE)|" kde4/backintime-kde4.desktop
  sed -i "s|^Name=Back In Time (root)$|Name=Back In Time (KDE, root)|" kde4/backintime-kde4-root.desktop
}

build() {
  for i in common notify gnome kde4; do
    cd "$srcdir/$_pkgname/$i"
    ./configure --python2 --no-check
    make 
  done
}

package_backintime-bzr() {
  pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault (CLI version)"
  depends=('openssh' 'python2-dbus' 'python2-keyring' 'rsync'
           'python2-notify')
  optdepends=('cron: for scheduled backups'
              'encfs: for encrypted filesystems'
              'sshfs: for remote backups')
  provides=($_pkgname)
  conflicts=($_pkgname)

  cd "$srcdir/$_pkgname/common"
  make DESTDIR="$pkgdir" install 

  cd "$srcdir/$_pkgname/notify"
  make DESTDIR="$pkgdir" install 
}

package_backintime-gnome-bzr() {
  pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault (GNOME version)"
  depends=('backintime-bzr' 'gksu' 'meld' 'python2-libgnome')
  optdepends=('gloobus-preview: for file preview'
              'python2-gnomekeyring: for storing passwords'
              'python2-secretstorage: for storing passwords')
  provides=($_pkgname-gnome)
  conflicts=($_pkgname-gnome)

  cd "$srcdir/$_pkgname/gnome"
  make DESTDIR="$pkgdir" install 
}

package_backintime-kde4-bzr() {
  pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault (KDE4 version)"
  depends=('backintime-bzr' 'kdebindings-python2' 'kdesu' 'kdesdk-kompare' 'python2-pyqt4')
  # Note: 'kdebindings-python2' contains kwallet's python language bindings
  optdepends=('kdeutils-kwallet: for storing passwords'
              'python2-secretstorage: for storing passwords')
  provides=($_pkgname-kde4)
  conflicts=($_pkgname-kde4)

  cd "$srcdir/$_pkgname/kde4"
  make DESTDIR="$pkgdir" install 

  sed -i "s|^Exec=kdesudo|Exec=kdesu|" "$pkgdir/usr/share/applications/kde4/backintime-kde4-root.desktop"
}

