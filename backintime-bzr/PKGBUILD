# Maintainer: willemw <willemw12@gmail.com>
# Contributor: solid <patrick.schwalm (at) googlemail.com
# Contributor: J. W. Birdsong <@gmail.com>

_srcname=backintime
pkgname=(backintime-bzr backintime-gnome-bzr backintime-kde4-bzr)
pkgver=r891
pkgrel=4
url="http://backintime.le-web.org"
license=('GPL')
arch=('any')
makedepends=('python2-dbus' 'python2-keyring'
             'python2-notify'
             'pygtk' 'python2-gnomekeyring' 'python2-gobject2' 'python2-libgnome'
             'kdebindings-python2' 'python2-pyqt4'
             'bzr')
source=($_srcname::bzr+https://code.launchpad.net/~bit-team/backintime/trunk)
md5sums=('SKIP')

pkgver() {
  cd $_srcname
  printf "r%s" "$(bzr revno)"
}

prepare() {
  cd $_srcname

  #sed -i "s|python |python2 |g" common/backintime gnome/backintime-gnome kde4/backintime-kde4
  #sed -i "s|python[ \t]*$|python2|" common/askpass.py

  sed -i "s|^\(Name=Back In Time\)$|\1 (GNOME)|" gnome/backintime-gnome.desktop
  sed -i "s|^Name=Back In Time (root)$|Name=Back In Time (GNOME, root)|" gnome/backintime-gnome-root.desktop
  sed -i "s|^\(Name=Back In Time\)$|\1 (KDE)|" kde4/backintime-kde4.desktop
  sed -i "s|^Name=Back In Time (root)$|Name=Back In Time (KDE, root)|" kde4/backintime-kde4-root.desktop
}

build() {
  for i in common notify gnome kde4; do
    cd "$srcdir/$_srcname/$i"
    ./configure --kdesu --no-check
    make 
  done
}

package_backintime-bzr() {
  pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault (non-GUI CLI version)"
  depends=('cron' 'openssh' 'python2-dbus' 'python2-keyring' 'rsync'
           'python2-notify')
  optdepends=('openssh: support for remote backups')
  provides=('backintime')
  conflicts=('backintime')

  cd "$srcdir/$_srcname/common"
  make DESTDIR="$pkgdir" install 

  cd "$srcdir/$_srcname/notify"
  make DESTDIR="$pkgdir" install 

  sed -i "s|python[ \t]*|python2 |g" "$pkgdir/usr/bin/"*
}

package_backintime-gnome-bzr() {
  pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault (GNOME version)"
  depends=('backintime-bzr' 'meld' 'gksu' 'pygtk' 'python2-gnomekeyring' 'python2-gobject2' 'python2-libgnome')
  optdepends=('gloobus-preview: for gnome file preview'
              'gnome-keyring: default keyring daemon'
              'meld: default backintime-gnome diff tool')
  provides=('backintime-gnome')
  conflicts=('backintime-gnome')

  cd "$srcdir/$_srcname/gnome"
  make DESTDIR="$pkgdir" install 

  sed -i "s|python[ \t]*|python2 |g" "$pkgdir/usr/bin/"*
}

package_backintime-kde4-bzr() {
  pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault (KDE4 version)"
  depends=('backintime-bzr' 'kdebindings-python2' 'kdesu' 'kdesdk-kompare' 'python2-pyqt4')
  provides=('backintime-kde4')
  optdepends=('kdesdk-kompare: default backintime-kde diff tool'
              'kdeutils-kwallet: alternative keyring daemon')

  cd "$srcdir/$_srcname/kde4"
  make DESTDIR="$pkgdir" install 

  sed -i "s|python[ \t]*|python2 |g" "$pkgdir/usr/bin/"*
}

