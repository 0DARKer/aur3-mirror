# Maintainer: willemw <willemw12@gmail.com>
# Contributor: solid <patrick.schwalm (at) googlemail.com
# Contributor: J. W. Birdsong <@gmail.com>

_pkgname=backintime
pkgname=$_pkgname-bzr
pkgver=r891
pkgrel=5
pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault (non-GUI CLI version)"
url="http://backintime.le-web.org"
license=('GPL')
arch=('any')
depends=('openssh' 'python2-dbus' 'python2-keyring' 'rsync'
         'python2-notify')
optdepends=('cron: for scheduled backups' 'openssh: for remote backups')
makedepends=('bzr')
provides=($_pkgname)
conflicts=($_pkgname)
source=($_pkgname::bzr+https://code.launchpad.net/~bit-team/backintime/trunk)
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  printf "r%s" "$(bzr revno)"
}

build() {
  for i in common notify; do
    cd "$srcdir/$_pkgname/$i"
    ./configure --python2 --no-check
    make 
  done
}

package() {
  for i in common notify; do
    cd "$srcdir/$_pkgname/$i"
    make DESTDIR="$pkgdir" install 
  done
}

