# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=texi2html-cvs
pkgver=20091010
pkgrel=1
pkgdesc="Texinfo->HTML conversion script -- CVS snapshot"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/texi2html/"
license=('GPL')
depends=('perl')
makedepends=('cvs' 'texinfo')
conflicts=('texi2html')
provides=('texi2html')
install=$pkgname.install
source=()
md5sums=()

_cvsroot=":pserver:anonymous:@cvs.savannah.nongnu.org:/sources/texi2html"
_cvsmod="texi2html"

build() {
  cd "$srcdir"
  msg "Connecting to cvs.savannah.nongnu.org...."
  if [ -d $_cvsmod/CVS ]; then
    cd $_cvsmod
    cvs -z3 update -d
  else
    cvs -z3 -d $_cvsroot co -D $pkgver -f $_cvsmod
    cd $_cvsmod
  fi
  msg "CVS checkout done or server timeout"

  msg "Creating a fresh build tree..."
  rm -rf "$srcdir/$_cvsmod-build"
  cp -R "$srcdir/$_cvsmod" "$srcdir/$_cvsmod-build"
  cd "$srcdir/$_cvsmod-build"

  msg "Starting make..."
  chmod 755 configure mdate-sh || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "$srcdir/$_cvsmod-build"
  make DESTDIR="$pkgdir/" install || return 1
}

# vim:set ts=2 sw=2 et:
