# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

pkgname=archon
pkgver=1.2
_pkgver_x86_64=2.0.0 # x86_64 has a newer version than armv7h or i686
pkgrel=6
pkgdesc="Execute Android APKs"
arch=('i686' 'x86_64' 'armv7h')
url="https://bitbucket.org/vladikoff/archon"
license=('Apache' 'custom')
depends=('chromium' 'chromeos-apk-git')
makedepends=('git')
options=('!strip')
install=${pkgname}.install

# Determine the tag based on $CARCH
case $CARCH in
    i686)   _tag="v${pkgver}-x86_32" ;;
    x86_64) _tag="v${_pkgver_x86_64}-$CARCH" ;;
    armv7h) _tag="v${pkgver}-ARM" ;;
esac

source=(
    "git+${url}.git#tag=${_tag}"
    "http://tools.android.com/recent/miscellaneousimprovements-1/android_icon_128.png"
    "${pkgname}.desktop"
    "${pkgname}.sh")
sha512sums=(
    'SKIP'
    '16c8aa6c9b27d3727f70d6326c736eccefe630bc3bf7a95ba11ea60acd5078d20fc9fe0991db50a4c4c97225d05cbc1c63267a1ba552aebe66b6f04f9ec70012'
    '7b47e1f0253a375c510205572e1de17fe2de23f214b5a6dfa7467cda3e85be0bcf33bfea86c7b00f7096dddebe6e4bc49db31c04d0aaad474931d8623a9e1b9f'
    '4b429b9c27fcae29c040c79c260ded864db8319f2c7e64b06e3d23e5db68ed37445f4c5328df72d5e380dcc5406c46ff9ff7fc33eb66cce82ba358c899d991b2')

pkgver() {
    # Set the version based on $CARCH
    [[ "$CARCH" = 'x86_64' ]] \
        && echo $_pkgver_x86_64 \
        || echo $pkgver
}

prepare() {
    # Increase the application font size by 1.2 times (the default is a bit too small)
    sed -i 's|window\.devicePixelRatio|1\.2\*window\.devicePixelRatio|' $pkgname/gen_{index,main}.min.js
}

package() {
    # Install the .desktop file, icon and launch script
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 android_icon_128.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
    install -Dm755 $pkgname.sh "$pkgdir"/usr/bin/$pkgname

    # Install the ARChon folder
    install -d "$pkgdir"/opt/$pkgname
    cp -r $pkgname/* "$pkgdir"/opt/$pkgname/

    # Install links to the license files
    install -d "$pkgdir"/usr/share/licenses/$pkgname
    ln -s /opt/$pkgname/NOTICE.{html,txt} "$pkgdir"/usr/share/licenses/$pkgname/
}
