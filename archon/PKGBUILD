# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

pkgname=archon
pkgver=1.2
pkgrel=5
pkgdesc="Execute Android APKs"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://bitbucket.org/vladikoff/archon"
license=('Apache' 'custom')
depends=('chromium' 'chromeos-apk-git')
makedepends=('git')
options=('!strip')
install=${pkgname}.install

# Determine the tag to grab using pkgver and $CARCH
_tag="v${pkgver}-"
case $CARCH in
    *arm*) _tag+=ARM    ;;
    i686)  _tag+=x86_32 ;;
    *)     _tag+=$CARCH ;;
esac

source=(
    "git+${url}.git#tag=${_tag}"
    "http://tools.android.com/recent/miscellaneousimprovements-1/android_icon_128.png"
    "${pkgname}.desktop"
    "${pkgname}.sh")
sha512sums=(
    'SKIP'
    '16c8aa6c9b27d3727f70d6326c736eccefe630bc3bf7a95ba11ea60acd5078d20fc9fe0991db50a4c4c97225d05cbc1c63267a1ba552aebe66b6f04f9ec70012'
    '7b47e1f0253a375c510205572e1de17fe2de23f214b5a6dfa7467cda3e85be0bcf33bfea86c7b00f7096dddebe6e4bc49db31c04d0aaad474931d8623a9e1b9f'
    'd7f15cba740f20064c0c2273401251df4bf64fc6ea7854d441cdd9f8e721d39df612553d04afd2b8651487f12ff1ff817a2ab538262053ddee5f956868786482')

package() {
    install -Dm755 $pkgname.sh "$pkgdir"/usr/bin/$pkgname
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 android_icon_128.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
    install -d "$pkgdir"/usr/share/$pkgname/ "$pkgdir"/usr/share/licenses/$pkgname
    cp -r $pkgname/* "$pkgdir"/usr/share/$pkgname/
    cp "$pkgdir"/usr/share/$pkgname/NOTICE.* "$pkgdir"/usr/share/licenses/$pkgname/
}
