# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

pkgname=archon
pkgver=1.2
pkgrel=3
pkgdesc="Execute Android APKs"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://bitbucket.org/vladikoff/archon"
license=('Apache' 'custom')
depends=('chromium' 'chromeos-apk-git')
makedepends=('git')
install=${pkgname}.install
source=("git+${url}.git"
        "http://tools.android.com/recent/miscellaneousimprovements-1/android_icon_128.png"
        "${pkgname}.desktop"
        "${pkgname}.sh")
sha512sums=('SKIP'
            '16c8aa6c9b27d3727f70d6326c736eccefe630bc3bf7a95ba11ea60acd5078d20fc9fe0991db50a4c4c97225d05cbc1c63267a1ba552aebe66b6f04f9ec70012'
            '7b47e1f0253a375c510205572e1de17fe2de23f214b5a6dfa7467cda3e85be0bcf33bfea86c7b00f7096dddebe6e4bc49db31c04d0aaad474931d8623a9e1b9f'
            '806bc6e3c805218c05cecd9904fb4e2dd337884f7c4901bc891fa1a7c6ee997c8eafe065c658eeaeaf6c60a63ac21f3c3cd5ffc012c32012680811adc86f1a71')

prepare() {
    cd $pkgname
    if [ $(grep arm <<< "$CARCH") ]; then
        git checkout -b tags/v${pkgver}-ARM
    elif [ "$CARCH" = "i686" ]; then
        git checkout -b tags/v${pkgver}-x86_32
    else
        git checkout -b tags/v${pkgver}-$CARCH
    fi
}

package() {
    install -Dm755 ${pkgname}.sh "$pkgdir"/usr/bin/${pkgname}
    install -Dm644 ${pkgname}.desktop "$pkgdir"/usr/share/applications/${pkgname}.desktop
    install -Dm644 android_icon_128.png "$pkgdir"/usr/share/pixmaps/${pkgname}.png
    install -d "$pkgdir"/usr/share/${pkgname} "$pkgdir"/usr/share/licenses/${pkgname}
    cp -r ${pkgname}/* "$pkgdir"/usr/share/${pkgname}/
    cp "$pkgdir"/usr/share/${pkgname}/NOTICE.* "$pkgdir"/usr/share/licenses/${pkgname}/
}
