# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

pkgname=gitlab-git
pkgver=ffb5fb0
pkgrel=1
pkgdesc="Project management and code hosting application"
arch=('any')
url="https://www.gitlab.org"
license=('MIT')
depends=('ruby' 'ruby-bundler' 'python2' 'gitlab-shell' 'postgresql-libs')
# Unconfirmed makedeps: base-devel zlib libyaml openssl gdbm readline ncurses libffi curl openssh redis libxml2 libxslt
makedepends=('sudo' 'libxslt' 'icu')
optdepends=(
    'mariadb: database backend'
    'postgresql: database backend'
    'python2-docutils: reStructuredText markup language support'
    'postifx: mail server in order to receive mail notifications'
)
conflicts=('gitlab')
replaces=('gitlab')
provides=('gitlab')
if [[ -n $(which httpd 2> /dev/null) ]]; then
  backup=('etc/httpd/conf/extra/gitlab.conf'
          'etc/httpd/conf/extra/gitlab-ssl.conf')
fi
if [[ -n $(which nginx 2> /dev/null) ]]; then
  backup=('etc/nginx/sites-available/gitlab-ssl')
fi
backup=(etc/gitlab/gitlab.yml etc/gitlab/database.yml)
# etc/gitlab/reque.yml
source=("git+https://gitlab.com/gitlab-org/gitlab-ce.git"
        "https://github.com/gitlabhq/gitlab-recipes/raw/master/init/systemd/gitlab.target"
        "https://github.com/gitlabhq/gitlab-recipes/raw/master/init/systemd/gitlab-unicorn.service"
        "https://github.com/gitlabhq/gitlab-recipes/raw/master/init/systemd/gitlab-sidekiq.service"
        "https://github.com/gitlabhq/gitlab-recipes/raw/master/web-server/apache/gitlab.conf"
        "https://github.com/gitlabhq/gitlab-recipes/raw/master/web-server/apache/gitlab-ssl.conf"
        "https://github.com/gitlabhq/gitlab-recipes/raw/master/web-server/nginx/gitlab-ssl"
        "https://github.com/gitlabhq/gitlab-recipes/raw/master/web-server/lighttpd/10-gitlab.conf")
install='gitlab.install'
sha512sums=("SKIP"
            "c88b7b500225c9a2bfaf10912fc09e5a3beb7c28890dcc1d49d55dfe92df02cef3f1865a138e7e2802b4e795c9a45cb130819be0a1c115eca9b566b8b9fb4395"
            "964fc8ea658270bdf53e6084ce791be3c575232fbb98890ae4bad5f57fc4ae53e243fb105aa0a35c0a3dd3f39f49bf4cfc0731eea52dfd84df8d2f507b1a3a79"
            "97b713b54f8bf7caf40427f868c8286a5c8cb92c7b191ea1ea451430c9123077ab06d5908875a4be3ad6379f43de34a395e6602123eee79daf0734d06f7e3809"
            "40e53c98119bb264c7cc876553ca032a7bb33a42514073b69e968a8e93c02d4dd0fa9c3aa7d0f02e8e7a6dff238c768f838fed067c1e32084b12ccf5286e5363"
            "0b2ad77ebbec1e5a1dee3a90638b4ecb493d83a40538c91f5322508f6cc31afd7594507993192b8c4703e2e44780fd3fbbc9d6711288a005a8d9e4d3aa5a4695"
            "7fbb34affa6d69fa89f0f64980de515e1f6fc62d5279559956d514168595a19e1ad6b93867b626d8b80606357623ff44df3f8640c85d00da081a0f5b61997d67"
            "6b058dd92a9c7a8f603cce968d12ce41c3f1fe82936a83e749bc8d88fb2481130c8fda07b97979df307566e76dfea8434f77b6a83036dff5bcd9646d3222bf30")
options=('!strip')

pkgver() {
  cd "$SRCDEST/${_pkgname}"
  git describe --always | sed 's|-|.|g'
}

prepare() {
    homedir="/var/lib/gitlab"
    datadir="/usr/share/webapps/gitlab"

    cd $srcdir/gitlab-ce

    # Patching Systemd unit files:
    sed -e "s|WorkingDirectory=/home/git|WorkingDirectory=/usr/share/webapps|" \
        -e "s|User=git|User=gitlab|" \
        -e "s|/home/git|/var/lib|g" \
        -e "s|/tmp/pids|/pids|g" \
        -e "s|-P tmp/|-P /var/lib/gitlab/|" \
        -i ${srcdir}/gitlab-sidekiq.service

    sed -e "s|WorkingDirectory=/home/git|WorkingDirectory=/usr/share/webapps|" \
        -e "s|/home/git/gitlab/config/unicorn.rb|/etc/gitlab/unicorn.rb|" \
        -e "s|User=git|User=gitlab|" \
        -e "s|PIDFile=/home/git/gitlab/tmp/pids/unicorn.pid|PIDFile=${homedir}/pids/unicorn.pid|" \
        -e "/ExecStart/c\ExecStart=/usr/bin/bundle exec rails server" \
        -i ${srcdir}/gitlab-unicorn.service

    # Patching config files:
    sed -e "s|# user: git|user: gitlab|" \
        -e "s|/home/git/repositories|$homedir/repositories|" \
        -e "s|/home/git/gitlab-satellites|$homedir/satellites|" \
        -e "s|/home/git/gitlab-shell|/usr/share/gitlab-shell|" \
        -i config/gitlab.yml.example > config/gitlab.yml
    sed -e "s|/home/git/gitlab/tmp|${homedir}|g" \
        -e "s|/home/git/gitlab|${datadir}|g" \
        -i config/unicorn.rb.example > config/unicorn.rb

    sed -e "s|DocumentRoot /home/git/gitlab/public|DocumentRoot /usr/share/webapps/gitlab/public|" \
        -i ${srcdir}/gitlab.conf ${srcdir}/gitlab-ssl.conf
}

build() {
    cd $srcdir/gitlab-ce
    msg "Fetching bundled gems..."
    # Gems will be installed into vendor/bundle
    bundle install --no-cache --no-prune --deployment --without development test aws
}

package() {
    homedir="/var/lib/gitlab"
    datadir="/usr/share/webapps/gitlab"

    cd $srcdir/gitlab-ce
    install -d "$pkgdir/usr/share/webapps"
	cp -r "${srcdir}/gitlab-ce" "${pkgdir}${datadir}"

    # Creating directories
    install -d \
        "${pkgdir}/etc/gitlab" \
        "${pkgdir}/usr/share/webapps" \
        "${pkgdir}/usr/share/doc/${pkgname}" \
        "${pkgdir}${homedir}/pids" \
        "${pkgdir}${homedir}/www" \
        "${pkgdir}${datadir}/www"
    
    # Install config files
    mv config/gitlab.yml "${pkgdir}/etc/gitlab/gitlab.yml"
    [ -f ${pkgdir}${datadir}/config/gitlab.yml ] && rm ${pkgdir}${datadir}/config/gitlab.yml
    ln -s /etc/gitlab/gitlab.yml ${pkgdir}${datadir}/config/gitlab.yml
    mv config/unicorn.rb "${pkgdir}/etc/gitlab/unicorn.rb"
    [ -f ${pkgdir}${datadir}/config/unicorn.rb ] && rm ${pkgdir}${datadir}/config/unicorn.rb
    ln -s /etc/gitlab/unicorn.rb ${pkgdir}${datadir}/config/unicorn.rb
    cp config/database.yml.mysql $pkgdir/etc/gitlab/database.yml
    [ -f ${pkgdir}${datadir}/config/database.yml ] && rm ${pkgdir}${datadir}/config/database.yml
    ln -s /etc/gitlab/database.yml ${pkgdir}${datadir}/config/database.yml

    # Install license and help files
    mv README.md MAINTENANCE.md CONTRIBUTING.md CHANGELOG config/*.{example,mysql,postgresql} "$pkgdir/usr/share/doc/$pkgname"
    install -D "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm $pkgdir/usr/share/webapps/gitlab/LICENSE

    # Install systemd service files
    install -Dm0755 "$srcdir/gitlab.target" "$pkgdir/usr/lib/systemd/system/gitlab.target"
    install -Dm0755 "$srcdir/gitlab-unicorn.service" "$pkgdir/usr/lib/systemd/system/gitlab-unicorn.service"
    install -Dm0755 "$srcdir/gitlab-sidekiq.service" "$pkgdir/usr/lib/systemd/system/gitlab-sidekiq.service"

    # Install apache, nginx and lighttpd template files (if they are installed)
    if [[ -n $(which httpd 2> /dev/null) ]]; then
        install -d  $pkgdir/etc/httpd/conf/extra 
        install -m 644 $srcdir/gitlab.conf  $pkgdir/etc/httpd/conf/extra/ 
        install -m 644 $srcdir/gitlab-ssl.conf  $pkgdir/etc/httpd/conf/extra/ 
    fi
    if [[ -n $(which nginx 2> /dev/null) ]]; then
        install -d  $pkgdir/etc/nginx/sites-enabled
        install -m 644 $srcdir/gitlab-ssl  $pkgdir/etc/nginx/sites-enabled/
    fi
    cp "${srcdir}/10-gitlab.conf" "$pkgdir/usr/share/doc/${pkgname}/"
}

# vim:set ts=4 sw=4 et:
