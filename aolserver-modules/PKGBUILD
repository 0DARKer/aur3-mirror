# Maintainer: Thomas Taylor <thomas.taylor@gmx.net>
# This package adds the necessary modules for aolserver and also installs a
# costum aolserver aware version of the tcl threads library.

pkgname=aolserver-modules
pkgver=4.5.2
pkgrel=1
pkgdesc="High-performance multithreaded web server modules"
arch=('i686' 'x86_64')
url="http://aolserver.sourceforge.net/"
license=(MPL)
makedepends=('aolserver-source')
depends=('aolserver-core' 'xotcl')
optdepends=('postgresql: for postgresql as underlying database')

source=("http://downloads.sourceforge.net/project/tcl/Thread%20Extension/2.7.0/thread2.7.0.tar.gz"
#"http://downloads.sourceforge.net/project/tcl/Thread%20Extension/2.7.1/thread2.7.1.tar.gz"
        "http://downloads.sourceforge.net/project/$pkgname/nssha1/nssha1-0.1/nssha1-0.1.tar.gz"
        "http://downloads.sourceforge.net/project/$pkgname/nscache/nscache-1.5/nscache-1.5.tar.gz"
        "http://downloads.sourceforge.net/project/$pkgname/nspostgres/4.1/nspostgres-4.1-src.tar.gz")
md5sums=('f7782ae163e1447b21d78024d7e49f80'
         '48fac53014a190239a86dd57af351627'
         'daa88717c214ff703d826e22e7447245'
         '5cc6c571b95108cb80e56469696ecec8')
sha256sums=('c10ac6873fdf1e62f55fccb633f4a595fc9924f7a5390a507605943ad7093870'
            '095fc942aa7014438f075528b824cb623e54615948603e1add1c14e9456f6095'
            '64a143a8e8043bdc58721c7f66c67a5c2c4d471406784558fd26fa171469a7a7'
            'c8eb43b8fddd3889e570b8aea90933e3a205dfbde8993cf793ddc7db2255c6b9')

#prepare() {
  #the following is a fix for tcl 8.6.x
#  sed -i "1i#define USE_INTERP_RESULT" "$srcdir/thread2.7.1/generic/tclThread.h"
#}

build() { 
  #threads
  cd "$srcdir/thread2.7.0/unix"
  [[ $CARCH == "x86_64" ]] && BIT="--enable-64bit"
  ../configure --enable-threads --prefix="/opt/aolserver-$pkgver" --exec-prefix="/opt/aolserver-$pkgver" --with-naviserver="/opt/aolserver-$pkgver" $BIT LDFLAGS="${LDFLAGS/--as-needed/--no-as-needed}" 
  make LDFLAGS="${LDFLAGS/--as-needed/--no-as-needed}"

  #modules
  for mod in nssha1-0.1 nscache-1.5; do
    cd "$srcdir/$mod"
    sed -i "s:../aolserver:/opt/source/aolserver-$pkgver:" Makefile*
    sed -i "s:--as-needed:--no-as-needed:g" Makefile*
    make LDFLAGS="${LDFLAGS/--as-needed/--no-as-needed}"
  done

  #extra stuff for postgres
  for mod in nspostgres-4.1; do
    cd "$srcdir/$mod"
    sed -i "s:../aolserver:/opt/source/aolserver-$pkgver:" Makefile*
    sed -i "s:--as-needed:--no-as-needed:g" Makefile*
    make POSTGRES=LSB ACS=1 LDFLAGS="${LDFLAGS/--as-needed/--no-as-needed}"
  done
}

check() {
  cd thread2.7.0/unix
  make test
}

package() {
  #threads
  cd "$srcdir/thread2.7.0/unix"
  make install DESTDIR="$pkgdir"

  #modules
  for mod in nssha1-0.1 nscache-1.5; do
    cd "$srcdir/$mod"
    cp -a *.so "$pkgdir/opt/aolserver-$pkgver/bin/."
    mv $pkgdir/opt/aolserver-$pkgver/bin/lib*.so "$pkgdir/opt/aolserver-$pkgver/lib/."
#    make install AOLSERVER="$pkgdir/opt/aolserver-$pkgver"
  done

  #extra stuff for postgres
  for mod in nspostgres-4.1; do
    cd "$srcdir/$mod"
    cp -a *.so "$pkgdir/opt/aolserver-$pkgver/bin/."
    mv $pkgdir/opt/aolserver-$pkgver/bin/lib*.so "$pkgdir/opt/aolserver-$pkgver/lib/."
#    make install AOLSERVER="$pkgdir/opt/aolserver-$pkgver" POSTGRES=LSB ACS=1
  done
  
}

# vim:set ts=2 sw=2 et:
