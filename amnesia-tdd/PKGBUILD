# Maintainer: Gerardo Marset <gammer1994@gmail.com>

pkgname=amnesia-tdd
pkgver=1.2.1
pkgrel=1
pkgdesc="A game about immersion, discovery and living through a nightmare."
arch=('i686' 'x86_64')
url="http://www.amnesiagame.com/"
license=('custom')
makedepends=('xz')
depends=('libxft')
_sh=amnesia_tdd_$pkgver.sh
source=($_sh $pkgname.desktop)
_dksum=36c807300fe38661f396c532435c9661  
if [ "$pkgver" == "1.2.1" ] ; then
    md5sums=('83a2ea3c97b2da37638914071eb64257'
             $_dksum)
elif [ "$pkgver" == "1.2" ] ; then
    md5sums=('5151ae5b89f6c8a8ddc10dfb19d8feb3'
             $_dksum)
fi

build() {
  # Extract installer.
  if [ ! -d $srcdir/$pkgname-$pkgver ]; then
    mkdir -p $srcdir/$pkgname-$pkgver
  fi
  msg "Extracting archive..."
  sh $_sh --tar xf -C $srcdir/$pkgname-$pkgver 
  cd $srcdir/$pkgname-$pkgver

  case $CARCH in
    i686) _arch=x86 ;;
    x86_64) _arch=x86_64 ; _sfix=64 ;;
  esac
  lzcat subarch | tar xvf -
  tar --lzma -xf instarchive_all
  tar --lzma -xf instarchive_all_$_arch

  # Create pkgdir folders.
  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/share/{applications,games/$pkgname,icons}
  mkdir -p $pkgdir/usr/share/licenses/$pkgname

  # Install files.
  msg "Copying files..."

  install -m644 config/license $pkgdir/usr/share/licenses/$pkgname/

  cd $srcdir/$pkgname-$pkgver/Amnesia/

  # Move all libraries.
  mv libs$_sfix/all/* libs$_sfix/
  rmdir libs$_sfix/all

  # Copy the game folder.
  cp -r * $pkgdir/usr/share/games/$pkgname

  # Make executable links.
  ln -s /usr/share/games/$pkgname/Amnesia.bin$_sfix $pkgdir/usr/bin/$pkgname
  ln -s /usr/share/games/$pkgname/Launcher.bin$_sfix $pkgdir/usr/bin/$pkgname-launcher

  # Install icons and .desktop files.
  install -m644 Amnesia.png $pkgdir/usr/share/icons/$pkgname.png
  install -m644 $startdir/$pkgname.desktop $pkgdir/usr/share/applications/
}
