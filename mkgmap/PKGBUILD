# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Arnaud Durand-Favreau <biginoz at FREE point FR>

pkgname=mkgmap
pkgver=r2651
pkgrel=1
pkgdesc='Utility for making maps for Garmin GPS devices'
url='http://www.mkgmap.org.uk/index.html'
license=('GPL')
arch=('any')
depends=('java-environment' 'sh')
source=("http://www.mkgmap.org.uk/snapshots/${pkgname}-${pkgver}.tar.gz")
md5sums=('1b9809c27d8a2015ab3ec2f70d153a81')
sha256sums=('5d991615d29b54baeb804e00d5f4f924cfd2aa277dae0467db05ac2f189dbaac')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Install mkgmap.jar, it's dependencies, and it's man page.
  install -D -m644 \
    ${pkgname}.jar \
    ${pkgdir}/usr/share/java/${pkgname}/${pkgname}.jar
  mkdir ${pkgdir}/usr/share/java/${pkgname}/lib
  install -m644 \
    -t ${pkgdir}/usr/share/java/${pkgname}/lib/ \
    lib/*
  install -D -m644 doc/${pkgname}.1 ${pkgdir}/usr/share/man/man1/${pkgname}.1

  # Create an executable file.
  install -d ${pkgdir}/usr/bin
  echo '#!/bin/sh' > ${pkgdir}/usr/bin/${pkgname}
  echo CLASSPATH="/usr/share/java/${pkgname}/${pkgname}.jar" >> ${pkgdir}/usr/bin/$pkgname
  echo java -jar \"\${CLASSPATH}\" \"\$@\" >> ${pkgdir}/usr/bin/${pkgname}
  chmod 755 ${pkgdir}/usr/bin/${pkgname}
}

# vim:set ts=2 sw=2 et:
