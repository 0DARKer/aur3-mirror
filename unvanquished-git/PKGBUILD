# Maintainer: Viech <viech unvanquished net>
# Contributor: Gereon Schomber
# Contributor: Martin F. Schumann

# type of target to check out, 'branch' or 'tag'
_type='branch'

# either a branch or tag name, depending on _type
_checkout='master'

pkgname=unvanquished-git
pkgver=0.24.0.2.deb.48.g246b0d5
pkgrel=1

# set this to share more text with the non-git version
_gitdir="${pkgname}"

pkgdesc='A team-based, fast-paced, fps/rts hybrid game which pits aliens against humans. Bleeding edge version.'
arch=('x86_64' 'i686')
url='http://www.unvanquished.net'
license=('GPL3')

makedepends=('git' 'cmake')
depends=('unvanquished-data'
         'zlib' 'gmp' 'nettle' 'geoip' 'curl' 'sdl2' 'glew' 'libpng'
         'libjpeg-turbo' 'libwebp>=0.2.0' 'freetype2' 'openal' 'libogg'
         'libvorbis' 'speex' 'libtheora' 'opusfile' 'ncurses' 'xdg-utils'
         'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme')
provides=('unvanquished')
conflicts=('unvanquished')
options=('emptydirs')
backup=('etc/unvanquished/config/server.cfg' 'etc/unvanquished/config/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=("${_gitdir}::git+https://github.com/Unvanquished/Unvanquished.git#${_type}=${_checkout}"
        'ChangeLog' 'unvanquished.install')

pkgver() {
	cd "${srcdir}/${_gitdir}"

	local ver="$(git describe --long)"
	printf "%s" "${ver//-/.}"
}

build() {
	cd "${srcdir}/${_gitdir}"

	cmake -D BUILD_GAME_QVM=OFF -D BUILD_TTY_CLIENT=ON .
	make
}

package() {
	# create installation directories
	cd "${pkgdir}"

	install -dm755 etc/conf.d \
	               etc/unvanquished/config \
	               usr/bin \
	               usr/lib/systemd/system \
	               usr/lib/unvanquished \
	               usr/share/applications \
	               usr/share/icons/hicolor/128x128/apps \
	               usr/share/licenses/unvanquished \
	               usr/share/unvanquished/pkg \
	               var/lib/unvanquished-server/config

	# install content
	cd "${srcdir}/${_gitdir}"

	install -m 755 daemon* *.so            "${pkgdir}/usr/lib/unvanquished/"
	install -m 644 debian/unvanquished.png "${pkgdir}/usr/share/icons/hicolor/128x128/apps/"
	install -m 644 COPYING.txt             "${pkgdir}/usr/share/licenses/unvanquished/"
	install -m 644 debian/configfiles/*    "${pkgdir}/etc/unvanquished/config/"

	# install starters
	cd "${srcdir}/${_gitdir}/archlinux"

	install -m 755 unvanquished.sh         "${pkgdir}/usr/bin/unvanquished"
	install -m 755 unvanquished-tty.sh     "${pkgdir}/usr/bin/unvanquished-tty"
	install -m 644 unvanquished.conf       "${pkgdir}/etc/conf.d/"
	install -m 644 unvanquished.service    "${pkgdir}/usr/lib/systemd/system/"
	install -m 644 unvanquished.desktop    "${pkgdir}/usr/share/applications/"

	# setup server home directory
	cd "${pkgdir}/var/lib/unvanquished-server/config"

	ln -s ../../../../etc/unvanquished/config/* .
}

md5sums=('SKIP'
         'c2c9c5a49e9180fefa23adceb843ec63'
         '2520ce17b524d3d7b37f7ed019856509')
