# Maintainer: Cravix < dr dot neemous at gmail dot org >
# Contributor: Ronald van Haren <ronald.archlinux.org>
# todo: provide a function to manually select modules or module sets using external script.

### --- Notice --- ###
# 1. Those I tagged useless is in ./REST. read it if you want.
# 2. Extra modules are most buggy, so use the least.

### --- Dependency --- ###
# mpdule: libmpd

### --- Category --- ###

# seems no function here?
dontknowhowtouse=(winlist-ng)

# tbh deskshow and winselector are useless for me, but you may need it. exebuf is a nice mod used for running cmd, but weirdly display.
gadget=(alarm deskshow exebuf mail news places winselector)

# redirect stdout to an edje block
cmdredir=(eektool execwatch)

# itask & taskbar are almost the same, but engage (previous itask-ng) is better, huh?
taskbarlike=(engage itask taskbar)

# mpdule works for mpd. empris works for any player with mpris plugin
musicplay=(empris mpdule)

monitor=(cpu diskio mem net)

# nice but really buggy, take any you need
weathers=(forecasts weather)

# eenvader.fractal: a wallpapaer;
# comp-scale: scale windows in a line (?); moon: display the phase of moon; 
# photo: read from dirs you set and display images slideshow; slideshow: like photo, but for background
eyecandies=(comp-scale eenvader.fractal flame moon rain penguins photo slideshow snow)

# everything-shotgun requires PROTO/shotgun, but nobody package it yet.
# Anyway it won't prompt error even without shotgun installed.
everything=(everything-aspell everything-mpris everything-pidgin 
   everything-places everything-shotgun everything-tracker 
   everything-wallpaper everything-websearch)

# That's only what I select. Change it if you want.
defset=(engage empris forecasts photo slideshow "${gadget[@]}" "${cmdredir[@]}" "${dontknowhowtouse[@]}" "${everything[@]}")


pkgname=e-modules-extra-svn
pkgver=79518
pkgrel=2
pkgdesc="Extra gadgets for e17"
arch=('i686' 'x86_64')
groups=('e17-extra-svn')
url="http://www.enlightenment.org"
license=('GPLv2' 'LGPLv2' 'MIT')
depends=('enlightenment17' 'efreet' 'e_dbus')
makedepends=('subversion')
conflicts=('e-modules-extra')
provides=('e-modules-extra')
options=('!libtool')
source=(REST)
md5sums=('e2a3e5658d44dd983db671153fef2a38')

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/E-MODULES-EXTRA"
_svnmod="E-MODULES-EXTRA"

build() {
   cd $srcdir
   _svnmod="E-MODULES-EXTRA"

  msg "Connecting to $_svntrunk SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  for i in ${defset[@]}; do
    cd $i
    ./autogen.sh --prefix=/usr
    make
    cd ..
  done
} 

package() {
  _svnmod="E-MODULES-EXTRA"
  
  cd $srcdir/$_svnmod-build

  for i in ${defset[@]}; do

  cd $i
  make DESTDIR=$pkgdir install

# install license files
  if [ -e $srcdir/$_svnmod-build/$i/COPYING ]; then
    install -Dm644 $srcdir/$_svnmod-build/$i/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/$i/COPYING
  fi
 
  if [ -e $srcdir/$_svnmod-build/$i/COPYING-PLAIN ]; then
    install -Dm644 $srcdir/$_svnmod-build/$i/COPYING-PLAIN \
        $pkgdir/usr/share/licenses/$pkgname/$i/COPYING-PLAIN
  fi
 
  cd ..
 done
 rm -r $srcdir/$_svnmod-build

}
