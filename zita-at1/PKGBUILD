# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=zita-at1
pkgver=0.2.3
pkgrel=2
pkgdesc="A 'autotuner' Jack application."
arch=('i686' 'x86_64')
url="http://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL')
depends=('jack' 'fftw' 'cairo' 'clxclient>=3.6.1'
         'zita-resampler>=1.1.0' 'clthreads>=2.4.0')
source=("${url}downloads/$pkgname-$pkgver.tar.bz2")
md5sums=('d901f378da09407b550c72b2607cc97f')

build() {
  cd "$srcdir/$pkgname-$pkgver/source"

  # zita-resampler 1.1.0 patch
  sed -i 's/zita-\(resampler\)/&\/\1/' retuner.h

  make PREFIX=/usr
}

package(){
  cd "$srcdir/$pkgname-$pkgver/source"

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # data
  install -d "$pkgdir/usr/share/$pkgname"
  install -Dm644 ../share/* \
    "$pkgdir/usr/share/$pkgname"

  # doc
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 ../doc/* \
    "$pkgdir/usr/share/doc/$pkgname"
}
