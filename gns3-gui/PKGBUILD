# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>

pkgname=gns3-gui
pkgver=1.3.0
pkgrel=3
pkgdesc='GNS3 network simulator. Graphical user interface package.'
arch=('any')
url='https://github.com/GNS3/gns3-gui'
license=('GPL3')
makedepends=('python-setuptools')
depends=('python-apache-libcloud' 'python-paramiko' 'python-requests' 'python-raven' 'python-sip' 'gns3-converter' 'python-pyqt4') 
optdepends=('gns3-server: GNS3 backend. Manages emulators such as Dynamips, VirtualBox or Qemu/KVM.'
            'dynamips: Cisco router emulator.'
            'xterm: Default terminal emulator for CLI management of virtual instances.'
            'wireshark-gtk: Live packet capture.')
conflicts=('gns3' 'gns3-git')
source=("https://github.com/GNS3/${pkgname}/archive/v${pkgver}.tar.gz"
        'https://github.com/GNS3/gns3-gui/commit/41d8e2a0aefac481dc232a865a39afd69d1802af.patch'
        'https://github.com/GNS3/gns3-gui/commit/021ad2a5c21fd53797420c5853d3866f0d2b49e7.patch'
        'gns3.desktop')
md5sums=('f12ea1badd5488f0bef367b8c1c12d10'
         'e477310f995bfb4a509ea91e80c781f7'
         'a104a563b8dd573796257be9a3dd2746'
         'ac6ba60be0a1cb7fc965d1a105e431d5')

prepare() {
 cd ${srcdir}/${pkgname}-${pkgver}
 patch -Np1 -i ../41d8e2a0aefac481dc232a865a39afd69d1802af.patch
 tail -n17 ../021ad2a5c21fd53797420c5853d3866f0d2b49e7.patch | patch -Np1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 ../gns3.desktop "${pkgdir}/usr/share/applications/gns3.desktop"
  install -Dm644 ./resources/images/gns3_icon_256x256.png "${pkgdir}/usr/share/pixmaps/gns3.png"
  install -Dm644 ./LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
