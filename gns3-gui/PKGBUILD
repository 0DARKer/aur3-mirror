# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>

pkgname=gns3-gui
pkgver=1.2
pkgrel=2
pkgdesc='GNS3 network simulator. Graphical user interface package.'
arch=('any')
url='https://github.com/GNS3/gns3-gui'
license=('GPL3')
makedepends=('python-setuptools')
depends=('python-pyqt4' 'python-apache-libcloud' 'python-ws4py' 'python-requests' 'python-paramiko' 'gns3-converter' 'dynamips')
optdepends=('gns3-server: GNS3 backend. Manages emulators such as Dynamips, VirtualBox or Qemu/KVM.'
            'xterm: Default terminal emulator for CLI management of virtual instances.'
            'wireshark-gtk: Live packet capture.')
conflicts=('gns3' 'gns3-git')
source=("https://github.com/GNS3/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('8fa979ac4f71d0812f66c81db2820b26')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
