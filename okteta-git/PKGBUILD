# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=okteta-git
pkgver=r3287.f7b6892
pkgrel=1
pkgdesc="KDE hex editor for viewing and editing the raw data of files.. (GIT version)"
url="https://www.kde.org/applications/utilities/okteta"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kcmutils' 'knewstuff' 'kparts' 'qca-qt5' 'desktop-file-utils')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('kdesdk-okteta' 'okteta')
replaces=('kdesdk-okteta' 'okteta')
source=("git://anongit.kde.org/okteta.git"
        '0001-Use-co-installable-qca-qt5-version.patch')
sha1sums=('SKIP'
          '9f618cc26f417bd72ff7f38fd11f2df997550992')
install="okteta-git.install"

pkgver() {
  cd okteta
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
  sed 's|${DATA_INSTALL_DIR}|${KXMLGUI_INSTALL_DIR}|g' -i okteta/parts/kpart/CMakeLists.txt
  cd okteta
  patch -p1 -i ../0001-Use-co-installable-qca-qt5-version.patch
}

build() {
  cd build
  cmake ../okteta \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
