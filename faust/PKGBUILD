# Maintainer: rtfreedman (rob<d0t>til<d0t>freedman<aT>gmail<d0t>com
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=faust
pkgver=0.9.65
pkgrel=1
pkgdesc="A compiled language for real-time audio signal processing."
arch=('i686' 'x86_64')
url="http://faust.grame.fr/"
license=('GPL')
depends=('ruby')
optdepends=('oscpack: osc support')
provides=('faust')
conflicts=('faust-git' 'faust2-git')
source=("http://sourceforge.net/projects/faudiostream/files/faust-$pkgver.zip")
md5sums=('9e1af0cd0a588aa035abacc8cd7e1f58')


build() {
  cd "$pkgname-$pkgver"

  make PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install
  
  # remove mac os specifics
  rm "$pkgdir"/usr/bin/faust2{max6,msp,vst}
  rm "$pkgdir"/usr/lib/faust/vst*
  rm "$pkgdir"/usr/lib/faust/max*
  
  # cleanup
  grep -l 'usr/local/' "$pkgdir"/usr/bin/faust2* |\
    xargs sed -i 's|usr/local/|usr/|g'

  # docs
  install -m755 -d "$pkgdir/usr/share/doc/$pkgname"
  install -m644 -t "$pkgdir/usr/share/doc/$pkgname" \
	README WHATSNEW documentation/faust-quick-reference.pdf

  ## syntax highlighting files
  cd syntax-highlighting

  # kate
  install -Dm644 $pkgname.xml \
    "$pkgdir/usr/share/apps/katepart/syntax/$pkgname.xml"

  # gedit
  install -Dm644 $pkgname.lang \
    "$pkgdir/usr/share/gtksourceview-2.0/language-specs/$pkgname.lang"
  install -Dm644 $pkgname.lang \
    "$pkgdir/usr/share/gtksourceview-3.0/language-specs/$pkgname.lang"

  # highlight 
  install -Dm644 dsp.lang \
    "$pkgdir/usr/share/highlight/langDefs/dsp.lang"

  # vim
  install -Dm644 $pkgname.vim \
    "$pkgdir/usr/share/vim/vimfiles/syntax/$pkgname.vim"
}
