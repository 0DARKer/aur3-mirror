# Contributor: Charles Leifer
# Maintainer: Sol Bekic <s0lll0s at blinkenshell dot org>

_gitname=themer
_gitbranch=master
pkgname="python-${_gitname}-git"
pkgdesc='Themer is a colorscheme generator and manager for your desktop.'
pkgver=0.4
pkgrel=1
url="https://github.com/s0lll0s/${_gitname}"
license=('MIT')
arch=('any')
depends=('python>=3.2' 'python-pillow' 'python-yaml' 'python-jinja')
optdepends=('i3: default-supported windowmanager' 'fish')
makedepends=('git' 'python-setuptools')
conflicts=()
install="${_gitname}.install"
source=("${_gitname}::git://github.com/s0lll0s/${_gitname}.git#branch=${_gitbranch}"
        "${install}")
sha256sums=('SKIP'
            '3fc9aa9b92cbb7dbbc0b30323faf3744c30abb7d6d365331a5b8a623355987a5')

pkgver() {
  cd "${_gitname}"
  echo "$(git rev-list --count ${_gitbranch}).$(git rev-parse --short ${_gitbranch})"
}

package() {
  cd "${_gitname}"
  python setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 -d "${pkgdir}/usr/share/${_gitname}"
  cp -dr --no-preserve=ownership "data/default" "${pkgdir}/usr/share/${_gitname}"
  chmod -R 755 "${pkgdir}/usr/share/${_gitname}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  if [ -d "${pkgdir}/usr/share/fish/completions" ]; then
    cp -Dm644 "data/fish/themer.fish" "${pkgdir}/usr/share/fish/completions/"
  fi
}
