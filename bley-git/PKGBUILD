# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Contributor:  Thomas Haider <t.haider@deprecate.de>

pkgname=bley-git
_pkgname="${pkgname%-*}"
pkgver=190.3c73407
pkgrel=1
pkgdesc="Intelligent greylisting daemon for Postfix"
url="http://bley.mx"
license=("BSD")
arch=("any")
backup=("etc/bley/bley.conf")
depends=("python2" "python2-pyspf" "twisted")
optdepends=("python-psycopg2: required for PostgreSQL support"
            "mysql-python: required for MySQL support"
            "python2-matplotlib: required for bleygraph")
makedepends=("git" "python2-distribute")
provides=("${_pkgname}")
conflicts=("${_pkgname}")
install="${_pkgname}.install"
source=("git://github.com/evgeni/bley.git"
        "debug_mode.patch")
md5sums=("SKIP"
         "78045d25d19844e33426ddee2df543b0")
sha1sums=("SKIP"
          "fcd86ba3c24395afa4d65726f28819027d35bb40")



pkgver() {
  cd "${_pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "${srcdir}/${_pkgname}"
  git apply "${srcdir}/debug_mode.patch"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}"
  python2 setup.py install --root="${pkgdir}"

  chmod 750 "${pkgdir}/etc/bley"
  chmod 640 "${pkgdir}/etc/bley/bley.conf"

  sed -i 's/log_file = bley.log/log_file = syslog/' "${pkgdir}/etc/bley/bley.conf"
  install -Dm644 "bley.service" "${pkgdir}/usr/lib/systemd/system/bley.service"
}
