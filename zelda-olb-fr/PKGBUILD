# Maintainer: FoolEcho <foolecho at gmail dot com>
pkgname=zelda-olb-fr
pkgver=3.6
pkgrel=1
pkgdesc="Zelda: Oni Link Begins is the sequel of Zelda: Return of the Hylian (french version)"
url="http://www.zeldaroth.fr/"
arch=('i686' 'x86_64')
depends=('sdl_gfx' 'sdl_mixer' 'sdl_image' 'xdg-utils' 'hicolor-icon-theme')
license=('Unknown')
makedepends=(imagemagick)
source=(
  "http://www.zeldaroth.fr/fichier/OLB/linux/ZeldaOLB-src-linux.zip" 
  zelda-olb.desktop
  zelda-olb.install)
md5sums=('93354eb9da873c50349f0158c3ddf90e'
         'aa9e6527ebbdb04aacc7531a1c700865'
         '9869fb57d07de3b687ca68608dd9d7ec')

_name=zelda-olb

build () {
	cd $srcdir/ZeldaOLB-src-linux/src
	
	# Fix the Game so that it reads save files from $HOME/.zelda-olb
	sed -i -e s~\"data/save/~string\(getenv\(\"HOME\"\)\)+\"/.$_name/~g Joueur.cpp
	
	#Make the game treat /usr/share/$pkgname as its data directory
	for i in *.cpp
	do
		sed -i -e s~data/~/usr/share/$pkgname/~g $i
	done
	
	# Create the directory $HOME/.zelda-olb if it doesnt exist
	sed -i -e s~'(NULL));'~'&\n\tif (system(\"stat $HOME/.'$_name' \&> /dev/null")) system (\"mkdir $HOME/.'$_name'\");'~ main.cpp
	make
}

package () {
	
	install -Dm644 $_name.desktop $pkgdir/usr/share/applications/$_name.desktop
	cd $srcdir/ZeldaOLB-src-linux/src
	
	install -Dm755 ZeldaOLB $pkgdir/usr/bin/$_name
	
	cd data
	for i in {map,music,sound}/*
	do
		install -Dm644 $i $pkgdir/usr/share/$pkgname/$i
	done
	
	cd images
	for i in */*
        do
                install -Dm644 $i $pkgdir/usr/share/$pkgname/images/$i
        done
	mkdir -p $pkgdir/usr/share/icons/hicolor/256x256/apps
	convert logos/fond.png -resize 256x256\! $pkgdir/usr/share/icons/hicolor/256x256/apps/$_name.png
}
