# Contributor: sp42b <sp42b|a_t|gmx.net>
# Contributor: AndyRTR <andreas.radke@freenet.de>

pkgname=dvbcut
pkgver=0.6.1
pkgrel=1
pkgdesc="Qt3 application for cutting parts out of DVB streams"
arch=('i686' 'x86_64')
url="http://dvbcut.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'qt3' 'libao' 'libmad' 'a52dec')
optdepends=('mplayer: for video playback inside of DVBCUT')
install=${pkgname}.install
source=(http://www.mr511.de/dvbcut/${pkgname}-${pkgver}.tar.gz)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --with-qt3=/opt/qt
  make -j1 || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm755 bin/dvbcut "${pkgdir}/usr/bin/dvbcut"
  install -Dm644 dvbcut.1 "${pkgdir}/usr/share/man/man1/dvbcut.1"
  install -Dm644 dvbcut.desktop "${pkgdir}/usr/share/applications/dvbcut.desktop"
  install -Dm644 dvbcut.svg "${pkgdir}/usr/share/dvbcut/icons/dvbcut.svg"
}

md5sums=('499587705d4597cbbf37388dc5e94a0d')
