# Contributor: bender02 at archlinux dot us
pkgname=pam-script
pkgver=1.1.4
pkgrel=2
pkgdesc="PAM module which allows you to execute scripts during authorization, password changes and sessions."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/pam-script/"
license=('GPL2')
depends=('pam')
source=(http://downloads.sf.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/etc \
        --sysconfdir=/etc/pam-script \
        --datarootdir=/usr/share \
        --libdir=/usr/lib/security
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install-examples

  # the default symlinks don't work:
  rm $pkgdir/etc/pam-script/pam_script_*
}

md5sums=('b9a65c98c042e26ec2f84f2bd5bb1abc')
