pkgname=mingw-w64-curl
pkgver=7.27.0
pkgrel=1
pkgdesc="An URL retrival utility and library. (mingw-w64)"
arch=(any)
url="http://curl.haxx.se"
license=("MIT")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-openssl mingw-w64-zlib)
options=(!libtool !strip !buildflags)
source=("http://curl.haxx.se/download/curl-$pkgver.tar.bz2")
md5sums=('11fcbfc0de9c12512e607e7d72d500a0')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --without-ca-bundle \
      --without-gnutls \
      --without-random
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}