# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=iulib
pkgver=0.4.4
pkgrel=2
pkgdesc="A library of image understanding-related algorithms"
arch=('i686' 'x86_64')
url="http://code.google.com/p/iulib/"
license=('APACHE')
depends=('sdl_gfx' 'sdl_image')
makedepends=('scons' 'imagemagick' 'mercurial')
source=(libtiff_check.diff)
md5sums=('240c7d1697f6a1f5df903d07b7812d3d')

build() {
  cd "$srcdir"
  msg "Connecting to $pkgname Mercurial server......."
  if [ -d $pkgname ]; then
    cd "$srcdir"/$pkgname
    hg pull -u -r ocropus-$pkgver
    cd "$srcdir"
  else
    hg clone https://iulib.googlecode.com/hg/  $pkgname -r ocropus-$pkgver
  fi
  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir"/$pkgname-build ]; then
    rm -rf "$srcdir"/$pkgname-build
  fi
  cp -r "$srcdir"/$pkgname "$srcdir"/$pkgname-build
  cd "$srcdir"/$pkgname-build
  
  # fix the bug in libtiff detection code
  patch -Np1 < "$srcdir/libtiff_check.diff"

  JOBS=`echo $MAKEFLAGS | sed 's|.*\(-j[1-9][0-9]*\).*|\1|'`
  scons $JOBS prefix=/usr
}

package() {
  cd "$srcdir"/$pkgname-build
  scons prefix="$pkgdir"/usr install
}

# vim:set ts=2 sw=2 et:
