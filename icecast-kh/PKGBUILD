# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=icecast-kh
pkgver=2.3.3_kh2
pkgrel=1
pkgdesc='KH branche extends the offical release of Icecast with new features and bug fixes.'
arch=('i686' 'x86_64')
url='http://karlheyes.github.com'
license=('GPL2')
conflict=('icecast' 'icecast-svn')
provides=("icecast=${pkgver/_*}")
depends=('openssl' 'libxml2' 'libxslt' 'libvorbis' 'libogg' 'libtheora' 'speex' 'curl')
optdepends=('ices-kh: client for icecast server')
backup=('etc/icecast.xml' 'etc/logrotate.d/icecast')
source=("https://github.com/downloads/karlheyes/$pkgname/${pkgname%-*}-${pkgver//_/-}.tar.gz"
        'logrotate'
        'rc.d')
md5sums=('ee5d857286a1c42a741c90ddc5d56469'
         '59c6552bcb1dd9fb542af8670dfabd3c'
         '8bec8be276a9204bf26fe51a5855e626')

build() {
  cd ${pkgname/-*}-${pkgver//_/-}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd ${pkgname/-*}-${pkgver//_/-}
  make DESTDIR="$pkgdir" install

  # rc.d
  install -D -m 755 "${srcdir}/rc.d" "${pkgdir}/etc/rc.d/icecast"

  # logrorate
  install -D -m 644 "${srcdir}/logrotate" "${pkgdir}/etc/logrotate.d/icecast"

  # log directory
  install -d -o root -g log -m 750 "${pkgdir}/var/log/icecast"
}

# vim:set ts=2 sw=2 ft=sh et:
