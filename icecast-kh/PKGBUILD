# Maintainer: Leonard de Ruijter <leonard@aur.archlinux.org>

pkgname=icecast-kh
pkgver=2.3.3_kh6
pkgrel=2
pkgdesc='The KH branche extends the official release of Icecast with features that may be (if found to be working out well) merged into the next official release.'
arch=('i686' 'x86_64')
url='http://karlheyes.github.com'
license=('GPL2')
conflict=('icecast' 'icecast2' 'icecast-svn')
provides=("icecast=${pkgver/_*}")
depends=('openssl' 'libxml2' 'libxslt' 'libvorbis' 'libogg' 'libtheora' 'speex' 'curl')
optdepends=('ices-kh: client for icecast server')
backup=('etc/icecast.xml' 'etc/logrotate.d/icecast')
source=("https://github.com/karlheyes/$pkgname/archive/${pkgname%-*}-${pkgver//_/-}.tar.gz"
        'icecast.logrotate'
        'start-by-nobody.patch'
        'icecast.service')
md5sums=('7acfb2b581976b48f9ac26e9f9045258'
         '59c6552bcb1dd9fb542af8670dfabd3c'
         'd8e929d2214123a1954da4383bf16583'
         '1468e59f76de194579b615889e20198f')

build() {
  cd "${srcdir}/${pkgname}-${pkgname/-*}-${pkgver//_/-}"
  patch -Np1 -i "${srcdir}/start-by-nobody.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgname/-*}-${pkgver//_/-}"
  make DESTDIR="$pkgdir" install

  # install logrotate config (taken from Fedora)
  install -Dm644 "${srcdir}/icecast.logrotate" "${pkgdir}/etc/logrotate.d/icecast"
  # create log directory
  install -d -g99 -o99 "${pkgdir}/var/log/icecast"

  # install systemd unit
  install -Dm0644 "${srcdir}/icecast.service" "${pkgdir}/usr/lib/systemd/system/icecast.service"
}

