# Maintainer: Luca Bennati <lucak3 AT gmail DOT com>

pkgname=perl-regexp-grammars
pkgver=1.016
pkgrel=1
pkgdesc="Perl/CPAN Module Regexp::Grammars - Add grammatical parsing features to Perl regexes"
arch=('any')
url="http://search.cpan.org/~dconway/Regexp-Grammars/"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/D/DC/DCONWAY/Regexp-Grammars-${pkgver}.tar.gz")
md5sums=('363d76e9501cefa55a913f999a731df9')

build() {
  cd "Regexp-Grammars-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "Regexp-Grammars-${pkgver}"
  make test
}

package() {
  cd "Regexp-Grammars-${pkgver}"
  make install DESTDIR="${pkgdir}"
}

# vim:set ts=2 sw=2 ft=sh et:
