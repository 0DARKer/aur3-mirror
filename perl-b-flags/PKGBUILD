# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-b-flags
_lastauthor=R/RU/RURBAN
_pkgname=B-Flags
pkgver=0.05
pkgrel=2
pkgdesc="Friendlier flags for B"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-extutils-makemaker')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('760aff6ff42d91bef8e61145319be78b561c7853319795109dc72ab3d657fd56')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  if [ -f "Build.PL" ]; then
    perl Build.PL
    perl Build
  elif [ -f "Makefile.PL" ]; then
    perl Makefile.PL
    make
  else
    return 1
  fi
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build test
  elif [ -f "Makefile.PL" ]; then
    make test
  fi
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build install
  elif [ -f "Makefile.PL" ]; then
    make install
  fi

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}

