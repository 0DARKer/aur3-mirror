# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_pkgbase=libgssglue
pkgname=lib32-${_pkgbase}
pkgver=0.3
pkgrel=1
pkgdesc="Exports a gssapi interface which calls other random gssapi libraries (lib32)"
arch=('x86_64')
url="http://www.citi.umich.edu/projects/nfsv4/linux/"
license=('BSD')
depends=("${_pkgbase}>=${pkgver}" 'lib32-glibc')
makedepends=('gcc-multilib')
options=('!libtool')
#source=(http://www.citi.umich.edu/projects/nfsv4/linux/$_pkgbase/$_pkgbase-$pkgver.tar.gz)
source=(http://ftp.halifax.rwth-aachen.de/gentoo/distfiles/$_pkgbase-$pkgver.tar.gz)
md5sums=('48aaad11b7371ba3270faa322202c038')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "$srcdir"/$_pkgbase-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "$srcdir"/$_pkgbase-$pkgver
  make DESTDIR="$pkgdir/" install
  rm -rf "${pkgdir}"/usr/include
}
