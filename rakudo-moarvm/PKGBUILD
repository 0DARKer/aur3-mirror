# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
_basename=rakudo
pkgname=$_basename-moarvm
pkgver=2014.03.01
pkgrel=1
pkgdesc="Perl6 compiler for MoarVM"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp-moarvm>=2014.03')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$_basename/$_basename-$pkgver.tar.gz)
sha512sums=('d701edba17b18ff04f05ea0afb7e5329fa7a184a728a2ad766bd85f7f4bbfb8e564f1d20a1fbebe9038e6c1e5a6f58d4fe401019ccb0f16b945ff00e2606eb5e')

build() {
  cd "$_basename-$pkgver"
  perl Configure.pl --prefix=/usr --backends=moar
  make
}

package() {
  cd "$_basename-$pkgver"

  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/bin/perl6
}
