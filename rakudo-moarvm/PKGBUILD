# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
_basename=rakudo
pkgname=$_basename-moarvm
pkgver=2015.01
pkgrel=1
pkgdesc="Perl6 compiler for MoarVM"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=("nqp-moarvm>=$pkgver")
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$_basename/$_basename-$pkgver.tar.gz)
sha512sums=('2e3f8a113a8f3d622f5bf9a3526a45cb7ff82ead645df31679097b77a3f808a886a6f78158c3b62a0962cfaf44048fd913d3c62ada08ef655abf06fdeb0ab367')

build() {
  cd "$_basename-$pkgver"
  perl Configure.pl --prefix=/usr --backends=moar
  make
}

package() {
  cd "$_basename-$pkgver"

  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/bin/perl6
}
