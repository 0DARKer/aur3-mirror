# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
_basename=rakudo
pkgname=$_basename-moarvm
pkgver=2014.04
pkgrel=1
pkgdesc="Perl6 compiler for MoarVM"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=("nqp-moarvm>=$pkgver")
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$_basename/$_basename-$pkgver.tar.gz)
sha512sums=('1298310727bd4007a76ca2d5970ead88e30faaac4735cae9da51968c253f89c2d8afac5f51e49c8ac1886c7c30bd26cafb8cd24459241b477823fca05221b6d9')

build() {
  cd "$_basename-$pkgver"
  perl Configure.pl --prefix=/usr --backends=moar
  make
}

package() {
  cd "$_basename-$pkgver"

  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/bin/perl6
}
