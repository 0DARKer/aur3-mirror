# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
_basename=rakudo
pkgname=$_basename-moarvm
pkgver=2014.08
pkgrel=1
pkgdesc="Perl6 compiler for MoarVM"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=("nqp-moarvm>=$pkgver")
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$_basename/$_basename-$pkgver.tar.gz)
sha512sums=('1133a4a3efe1c3bba318d9d93675815f4ad020b1bfb8c6579412a742f41b1453117660a6940f0bc4cf60c213e5f7045516780f41256c974441116d3987e576eb')

build() {
  cd "$_basename-$pkgver"
  perl Configure.pl --prefix=/usr --backends=moar
  make
}

package() {
  cd "$_basename-$pkgver"

  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/bin/perl6
}
