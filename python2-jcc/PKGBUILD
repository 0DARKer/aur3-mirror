# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Tomoya Kose <mitsuset@gmail.com>

pkgname=python2-jcc
pkgver=2.15
pkgrel=2
pkgdesc="a C++ code generator for calling Java from C++/Python"
arch=(i686 x86_64)
url=http://pypi.python.org/pypi/JCC
license=(Apache)
depends=(java-environment python2)
makedepends=(python2-distribute)
source=(http://pypi.python.org/packages/source/J/JCC/JCC-$pkgver.tar.gz)
sha256sums=('dd83c7d538dfa13427d012bb6eb05cd30580f2fc2d3d304935279343935e3068')
sha512sums=('44a0260f0633d0bbf8fb0521580dd6ec1adc910a8f72954546589bde745f17b6701765cb6f1fda9045d37ac068722269f9d42746c654286ab9a9f4b495d6688c')

prepare() {
    cd JCC-$pkgver/jcc/
    mv __main__.py save
    echo '#!/usr/bin/env python2' > __main__.py
    cat save >> __main__.py
    rm save
}

build() { 
    export JCC_JDK=$JAVA_HOME
    cd JCC-$pkgver/
    python2 setup.py build
}

package() {
    cd JCC-$pkgver/
    python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
    cd "$pkgdir"/usr/
    install -d bin/
    local _bin=lib/python2.7/site-packages/jcc/__main__.py
    chmod +x $_bin
    ln -s /usr/$_bin bin/jcc
}
