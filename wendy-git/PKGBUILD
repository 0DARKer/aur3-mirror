# Maintainer: Miles Sandlar <miles at userbound.com>
pkgname=wendy-git
_pkgname=${pkgname%-git}
pkgver=22.bdb6607
pkgrel=2
pkgdesc='Watch a directory and execute a command on file creation/deletion/modification'
arch=('i686' 'x86_64')
url='http://git.z3bra.org/cgit.cgi/wendy'
license=('WTFPL')
depends=('glibc')
makedepends=('git')
provides=('wendy')
source=('git://z3bra.org/wendy')
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  echo "$(git rev-list --count HEAD).$(git log -1 --pretty=format:%h)"
}

build() {
  cd "$_pkgname"
  make
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

