# Contributer: N30N <archlinux@alunamation.com>

pkgname="css-validator-cvs"
pkgver=20090303
pkgrel=1
pkgdesc="Command line version of the W3C's CSS validator"
url="http://jigsaw.w3.org/css-validator/documentation.html"
license="Apache"
arch=("i686" "x86_64")
depends=("java-runtime")
makedepends=("cvs" "apache-ant" "tomcat")
source=("css-validator.sh" \
	"http://jigsaw.w3.org/Distrib/jigsaw_2.2.6.tar.gz" \
	"http://www.apache.org/dist/commons/collections/binaries/commons-collections-3.2.1-bin.tar.gz" \
	"http://www.apache.org/dist/commons/lang/binaries/commons-lang-2.4-bin.tar.gz" \
	"http://www.apache.org/dist/velocity/engine/1.6.1/velocity-1.6.1.tar.gz" \
	"http://www.apache.org/dist/xerces/j/Xerces-J-bin.2.9.1.tar.gz" \
)
md5sums=("4062f65be015b900ada6d6793c02ff88" \
	"c848e06d7851ca0d243fd8bdef4c7765" \
	"6f5b078f01dd0c3910cc04898bf39924" \
	"5ff5d890e46021a2dbd77caba80f90f2" \
	"c5ede09ac179a2913ec42a9dce61c29e" \
	"32718d4cbe009f93d551ab8d3ef4b2aa" \
)
noextract=("jigsaw_2.2.6.tar.gz" \
	"commons-collections-3.2.1-bin.tar.gz" \
	"commons-lang-2.4-bin.tar.gz" \
	"velocity-1.6.1.tar.gz" \
	"Xerces-J-bin.2.9.1.tar.gz" \
)
provides=("css-validator")
conflicts=()

_cvsroot=":pserver:anonymous:anonymous@dev.w3.org:/sources/public"
_cvsmod="2002/css-validator"

build() {
	cd ${srcdir}
	msg "Connecting to W3C's CVS server......."
	if [ -d ${_cvsmod}/CVS ]; then
		(cd ${_cvsmod} && cvs -z3 update -d && ant clean) || return 1
	else
		cvs -z3 -d ${_cvsroot} co -D ${pkgver} -f ${_cvsmod} || return 1
	fi

	cd ${_cvsmod}
	mkdir tmp
	for file in ${srcdir}/*.tar.gz; do
		cp ${file} tmp
	done

	export CLASSPATH=/opt/tomcat/common/lib/servlet-api.jar
	msg "Starting build process."
	ant || return 1

	install -D -m755 ${srcdir}/css-validator.sh ${pkgdir}/usr/bin/css-validator
	install -D -m644 css-validator.jar \
		${pkgdir}/usr/share/java/css-validator/css-validator.jar
	for file in lib/*.jar; do
		install -D -m644 ${file} ${pkgdir}/usr/share/java/css-validator/${file}
	done
}
