# Maintainer: Nicolas Pouillard http://nicolaspouillard.fr

pkgname=ekeyd
pkgver=1.1.4
pkgrel=6
pkgdesc="Tools to work with the Entropy Key"
arch=('i686' 'x86_64')
source=(http://www.entropykey.co.uk/res/download/$pkgname-$pkgver.tar.gz)
url="http://www.entropykey.co.uk/"
depends=(lua luasocket)
optdepends=('perl: for the munin plugin')
license=('MIT')
sha256sums=('147ca4ff964ab7b344a08d3cd6f9b1bf03ba18776a1941d603681d8102cce0c8')

_make_flags=('LUA_INC=' 'LUA_V=' 'BUILD_ULUSBD=yes' 'BUILD_EGDLINUX=yes')

# From which distribution do we take inspiration:
_base_distro=centos6

build() {
  cd $srcdir/$pkgname-$pkgver
  # In place patching:

  # There is a mismatching parenthesis/curly brace.
  sed -i -e 's/(DESTDIR}/{DESTDIR}/g' Makefile

  # ENTROPYKEY_KEY_SERIAL is not defined anywhere, ENTROPY_KEY_SERIAL was
  # certainly meant.
  sed -i -e 's/ENTROPYKEY_KEY_SERIAL/ENTROPY_KEY_SERIAL/g' udev/entropykey.sh

  # This makes the rule rejected and I don't know why, if you do please tell me.
  sed -i -e 's/BUS=="usb", //g' udev/*/*.rules

  make "${_make_flags[@]}"
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make "${_make_flags[@]}" DESTDIR=$pkgdir/ install
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
  install -D -m644 udev/$_base_distro/60-entropykey.rules ${pkgdir}/lib/udev/rules.d/60-entropykey.rules
  install -D -m755 udev/entropykey.sh ${pkgdir}/lib/udev/entropykey.sh
}
