# Contributor: Juan Diego Tascon

pkgname=artoolkit
pkgver=2.72.1
pkgrel=7
pkgdesc="Augmented Reality ToolKit"
arch=('i686' 'x86_64')
url="http://www.hitl.washington.edu/artoolkit/"
license=('GPL')
makedepends=('gstreamer0.10' 'glut')
source=("http://downloads.sourceforge.net/project/artoolkit/artoolkit/${pkgver}/ARToolKit-${pkgver}.tgz")
md5sums=('4328b512711529309bad4bd1d791952c')

build() {
	# configure
	cd ${srcdir}/ARToolKit
	echo -ne "5\ny\nn\n" >> answers
	./Configure < answers || return 1

	tmpfile=$(mktemp)
	for makefile in lib/SRC/*/Makefile
	do
		cp "$makefile" "$tmpfile"
		echo "Patching $makefile"
		sed '/^CFLAG/ s/=/= -fPIC/' "$tmpfile" > "$makefile"
	done
	rm "$tmpfile"
	
	# install lib
	make -C lib/SRC || return 1
	
	mkdir -p ${pkgdir}/usr/{lib,include}
	
	rm -rf include/AR/config.h.in
	cp -rf include/AR ${pkgdir}/usr/include
	
	# create shared objects (libxxx.so)
	pushd lib
	for liba in *.a
	do
		libso=$(basename $liba .a).so
		ld -shared -s -whole-archive $liba -o $libso
		cp $liba $libso ${pkgdir}/usr/lib/
	done
	popd
	
	# install share
	mkdir -p ${pkgdir}/usr/share/artoolkit/
	cp -rf doc patterns examples ${pkgdir}/usr/share/artoolkit/
	
	#install bin
	make -C util || return 1
	make -C examples || return 1
	cp -rf bin ${pkgdir}/usr/share/artoolkit
}
