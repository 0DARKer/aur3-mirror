pkgname=lzham
pkgver=1.0.1
_pkgver=1_0_stable1
pkgrel=2
pkgdesc="Lossless data compression codec with LZMA-like ratios but 1.5x-8x faster decompression speed"
license=('MIT')
arch=('i686' 'x86_64')
url="https://github.com/richgel999/lzham_codec"
depends=('gcc-libs')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/richgel999/lzham_codec/archive/v$_pkgver.tar.gz")
md5sums=('68ec3db42d2263d7e79d0581293f946d')

build() {
  cd "$srcdir/lzham_codec-$_pkgver"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON -DCMAKE_SKIP_RPATH=ON ..
  make
}

package() {
  cd "$srcdir/lzham_codec-$_pkgver/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/include/zlib.h
}
