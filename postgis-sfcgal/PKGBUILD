# Maintainer: SaultDon <sault.don gmail>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>
pkgname=postgis-sfcgal
_pkgname=${pkgname%-sfcgal}
pkgver=2.1.3
pkgrel=1
pkgdesc="Adds support for geographic objects to PostgreSQL and includes SFCGAL support for experimental 3D functions"
arch=('i686' 'x86_64')
url="http://postgis.net/"
license=('GPL')
depends=('postgresql' 'gdal' 'geos' 'json-c' 'cgal' 'sfcgal' 'libxml2' 'libxslt')
changelog=$pkgname.changelog
provides=("$_pkgname=$pkgver")
conflicts=($_pkgname)
source=("http://download.osgeo.org/postgis/source/${_pkgname}-${pkgver}.tar.gz"
        "postgis-2.1.3-libjson-c.patch::https://bugs.archlinux.org/task/40528?getfile=11818")
sha256sums=('c17812aa4bb86ed561dfc65cb42ab45176b94e0620de183a4bbd773d6d876ec1'
            '68fd36730baf96341744c03582b0e8af309328ba79bb59fa981a515299cc1bda')

prepare() {
  cd ${srcdir}/${_pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}/postgis-2.1.3-libjson-c.patch"
}

build() {
  cd ${_pkgname}-${pkgver}

  ./configure --prefix=/usr --with-gui
  make
}

package() {
  cd ${_pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
