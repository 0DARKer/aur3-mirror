# Maintainer: Spider.007 <aur@spider007.net>

pkgname=graylog2-web-interface
pkgver=0.20.0
pkgrel=3
pkgdesc="Web interface for Graylog2, an open source syslog implementation"
arch=(any)
url="http://graylog2.org"
license=('GPL')
depends=(java-runtime)
source=(
	https://github.com/Graylog2/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tgz
	graylog2-web-interface.service
	graylog2-web-interface.conf
)
backup=(etc/webapps/$pkgname/$pkgname.conf)
options=(!strip)
sha256sums=('78c5747180f59c2b7742f1d943e36fbbfaace23d120737b15a44b24cf4618a87'
			'd9855b18bdddb877bd16aabde54221fea4c93d0414d14a3aa75f1800e60d010c'
			'aa27f060410ffd47a675e95a1caaf37689a2b8201fb252d7c1d3b5096384be0c')

package() {
	cd "$srcdir/$pkgname-$pkgver"

	install -Dm644 "$srcdir/graylog2-web-interface.conf" "$pkgdir/etc/conf.d/graylog2-web-interface.conf"
	install -Dm644 "$srcdir/graylog2-web-interface.service" "$pkgdir/usr/lib/systemd/system/graylog2-web-interface.service"

	install -dm755 -o nobody "$pkgdir/etc/webapps/$pkgname/logs"
	cp conf/* "$pkgdir/etc/webapps/$pkgname/"

	install -dm755 "$pkgdir/usr/share/webapps/$pkgname"
	cp -R {bin,lib,share}/ "$pkgdir/usr/share/webapps/$pkgname/"
	ln -s /etc/webapps/$pkgname/ $pkgdir/usr/share/webapps/$pkgname/conf
}
