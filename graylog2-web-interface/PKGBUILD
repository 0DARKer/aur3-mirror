# Maintainer: Spider.007 <aur@spider007.net>
# Don't forget updating the version @ *.service
pkgname=graylog2-web-interface
pkgver=0.92.0
pkgrel=1
pkgdesc="Web interface for Graylog2, an open source syslog implementation"
arch=(any)
url="http://graylog2.org"
license=('GPL')
depends=(java-runtime)
source=(
	http://packages.graylog2.org/releases/$pkgname/$pkgname-$pkgver-rc.1.tgz
	graylog2-web-interface.service
	graylog2-web-interface.conf
)
backup=(etc/webapps/$pkgname/$pkgname.conf)
options=(!strip)
sha256sums=('88010c4b179265a79d53488817a1ba5ae5a56f65770daf8847893b497b4c7e6b'
            '6312da486636cc624754288d469c9ded226e2cba23333026e34faf8d5e598340'
            'aa27f060410ffd47a675e95a1caaf37689a2b8201fb252d7c1d3b5096384be0c')

package() {
	cd "$srcdir/$pkgname-$pkgver-rc.1"

	# verify classpath in service file
	lib=.$(grep ExecStart= $srcdir/graylog2-web-interface.service |cut -d' ' -f7|cut -d: -f1|cut -d\} -f2-)
	[[ ! -f $lib ]] && { echo "Please update classpath '$lib' in .service file" ; exit 2 ; }

	install -Dm644 "$srcdir/graylog2-web-interface.conf" "$pkgdir/etc/conf.d/graylog2-web-interface.conf"
	install -Dm644 "$srcdir/graylog2-web-interface.service" "$pkgdir/usr/lib/systemd/system/graylog2-web-interface.service"

	install -dm755 -o nobody "$pkgdir/etc/webapps/$pkgname/logs"
	cp conf/* "$pkgdir/etc/webapps/$pkgname/"

	install -dm755 "$pkgdir/usr/share/webapps/$pkgname"
	cp -R lib/ "$pkgdir/usr/share/webapps/$pkgname/"
	ln -s /etc/webapps/$pkgname/ $pkgdir/usr/share/webapps/$pkgname/conf
}
