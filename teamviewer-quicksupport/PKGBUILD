# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=teamviewer-quicksupport
pkgver=9.0.23951
_pkgver_major=${pkgver%%.*}
pkgrel=1
pkgdesc="Teamviewer Quicksupport - All-In-One Software for Remote Support and Online Meetings"
arch=('i686' 'x86_64')
url="http://www.teamviewer.com/"
depends=('bash')
if [[ "${CARCH}" == 'i686' ]]; then
	depends+=('alsa-lib' 'gcc-libs' 'libxdamage' 'libxtst' 'zlib' 'freetype2')
elif [[ $CARCH == 'x86_64' ]]; then
	depends+=('lib32-gcc-libs' 'lib32-alsa-lib' 'lib32-libxtst' 'lib32-libxdamage' 'lib32-zlib' 'lib32-freetype2')
fi
conflicts=('teamviewer')
license=('custom')
source=("teamviewer_qs-${pkgver}.tar.gz::http://download.teamviewer.com/download/version_9x/teamviewer_qs.tar.gz")

prepare() {
	cd teamviewer${_pkgver_major}qs/

	# remove some trash
	rm -rf tv_bin/RTlib/
	rm -f tv_bin/xdg-utils/xdg-email
	rmdir config
	rmdir logfiles
}

build() {
	cd teamviewer${_pkgver_major}qs/

	# duh?!
	sed -i '/UpdateBinaries/s/^/#/' tv_bin/script/tvw_main

	# Yes, this is QuickSupport... But we want to use user's home directory.
	sed -i '/function isInstalledTV/,/^}$/c function isInstalledTV() { return 0; }' tv_bin/script/tvw_aux
}

package() {
	cd teamviewer${_pkgver_major}qs/

	install -d -m0755 "${pkgdir}"/opt/teamviewer${_pkgver_major}/
	cp -a --no-preserve=ownership * "${pkgdir}"/opt/teamviewer${_pkgver_major}/

	install -d -m0755 ${pkgdir}/usr/{bin,share/applications,share/licenses/${pkgname}}
	ln -s /opt/teamviewer${_pkgver_major}/tv_bin/script/teamviewer ${pkgdir}/usr/bin/teamviewer
	ln -s /opt/teamviewer${_pkgver_major}/tv_bin/script/teamviewer ${pkgdir}/usr/bin/quicksupport
	ln -s /opt/teamviewer${_pkgver_major}/tv_bin/desktop/teamviewer-teamviewer${_pkgver_major}.desktop "${pkgdir}"/usr/share/applications/teamviewer.desktop
	ln -s /opt/teamviewer${_pkgver_major}/doc/License.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

	install -d -m1777 "${pkgdir}"/var/lib/teamviewer
	ln -s /var/lib/teamviewer "${pkgdir}"/opt/teamviewer${_pkgver_major}/config
	install -d -m1777 "${pkgdir}"/var/log/teamviewer
	ln -s /var/log/teamviewer "${pkgdir}"/opt/teamviewer${_pkgver_major}/logfiles
}

sha256sums=('b7ca3adfa1d0183bd69ff85dcaa188a63389ff6ab7afcbed97e22daa3bf2e8cd')
