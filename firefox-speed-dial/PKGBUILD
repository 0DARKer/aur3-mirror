# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=firefox-speed-dial
pkgver=0.9.6.1
_firefoxver=6.0
pkgrel=2
pkgdesc="Firefox extension that simplifies the access to your most visited websites"
arch=('any')
url="http://speeddial.uworks.net/"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('firefox')
source=(http://speeddial.uworks.net/speed_dial-$pkgver-fx.xpi)
md5sums=('c25bd71792650cf5d2ddc42fa4953247')

package() {
  cd "$srcdir"

  emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)

  install -d "$pkgdir"/usr/lib/firefox-$_firefoxver/extensions/$emid
  cp -a * "$pkgdir"/usr/lib/firefox-$_firefoxver/extensions/$emid

  rm -f "$pkgdir"/usr/lib/firefox-$_firefoxver/extensions/$emid/*.xpi

  # Fix maximum supported version
  sed -i 's|<em:maxVersion>5.0.*</em:maxVersion>|<em:maxVersion>6.*</em:maxVersion>|' \
    "$pkgdir"/usr/lib/firefox-$_firefoxver/extensions/$emid/install.rdf
}
