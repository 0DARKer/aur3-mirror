# Maintainer: josephgbr <rafael.f.f1 at gmail.com> 

_pkgbasename=pam 
pkgname=lib32-$_pkgbasename 
pkgver=1.1.5 
pkgrel=1 
pkgdesc="PAM (Pluggable Authentication Modules) library (32 bit)" 
arch=('x86_64') 
license=('GPL2') 
url="http://www.kernel.org/pub/linux/libs/pam/" 
depends=('lib32-glibc' 'lib32-db' 'lib32-cracklib' 'lib32-libtirpc' ${_pkgbasename}) 
makedepends=('lib32-flex' 'docbook-xml' 'docbook-xsl' 'w3m' 'gcc-multilib') 
options=('!libtool' '!emptydirs') 
source=(https://fedorahosted.org/releases/l/i/linux-pam/Linux-PAM-$pkgver.tar.bz2 
        ftp://ftp.suse.com/pub/people/kukuk/pam/pam_unix2/pam_unix2-2.6.tar.bz2) 
md5sums=('927ee5585bdec5256c75117e9348aa47' 
        'e2788389a6c59224110a45fcff30e02b') 

build() {
  export CC='gcc -m32'
  
  cd "$srcdir/Linux-PAM-$pkgver"
  ./configure --sysconfdir=/etc DESTDIR="$pkgdir" --libdir=/lib32 
  make 

  cd "$srcdir/pam_unix2-2.6"
  ./configure --libdir=/usr/lib32
  sed -i \
  	-e 's|pam_syslog (pam_handle_t|pam_syslog (const pam_handle_t|g' \
  	src/public.h src/support.c
  ./configure --libdir=/usr/lib32
  make 
} 

package() { 
  cd "$srcdir/Linux-PAM-$pkgver"
  make INSTALL=/bin/install DESTDIR=$pkgdir install 
  
  cd "$srcdir/pam_unix2-2.6"
  #make DESTDIR=$pkgdir install 
  install src/pam_unix2.so "$pkgdir/lib32/security/pam_unix2.so" 

  # fix some missing symlinks from old pam for compatibility 
  cd "$pkgdir/lib32/security"
  ln -s pam_unix.so pam_unix_acct.so 
  ln -s pam_unix.so pam_unix_auth.so 
  ln -s pam_unix.so pam_unix_passwd.so 
  ln -s pam_unix.so pam_unix_session.so 
  
  # cleanup for lib32 package 
  rm -rf "${pkgdir}"/{etc,sbin,usr/{include,share}} 
}
