# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Partly based on original PKGBUILD by Mikko Seppala <t-r-a-y@mbnet.fi>
# Contributor: Manuel Gaul <inkaine@hotmail.com>

_pkgsourcename=catalyst-utils-test
pkgname=lib32-$_pkgsourcename
pkgver=10.10
pkgrel=222
pkgdesc="AMD/ATI catalyst-test driver utilities and libraries. (32-bit)"
url="http://www.ati.amd.com"
arch=(x86_64)
license=('custom')
groups=('lib32')
depends=('lib32-libxext' 'lib32-libdrm' "catalyst-test>=${pkgver}" 'unrar')
conflicts=('lib32-libgl' 'lib32-nvidia-utils' 'lib32-catalyst-utils')
replaces=('lib32-ati-fglrx-utils' 'lib32-fglrx-utils' 'lib32-libgl' 'lib32-catalyst-utils')
provides=('lib32-libgl' "lib32-catalyst-utils=${pkgver}")
source=(http://www.ati-forum.de/files/Downloads/Driver/ati-driver-installer-8.783_RC1-x86.x86_64.rar lib32-catalyst.sh)
md5sums=('7c3e11ab4ebec6132a578ffe8f2915f9' 'af7fb8ee4fc96fd54c5b483e33dc71c4')

build() {
  # Extract Catalyst driver installer
    unrar e ati-driver-installer-8.783_RC1-x86.x86_64.rar
    /bin/sh ati-driver-installer-8.783_RC1-x86.x86_64.run --extract archive_files
}

package() {
  cd ${srcdir}
  install -D -m755 lib32-catalyst.sh ${pkgdir}/etc/profile.d/lib32-catalyst.sh || return 1

  # Install lib32 libraries
  cd "${srcdir}/archive_files/arch/x86/usr" || return 1
  install -dm755 "${pkgdir}/usr/lib32" || return 1
  install -dm755 "${pkgdir}/usr/lib32/xorg/modules/dri" || return 1
  install -m755 lib/*.so* "${pkgdir}/usr/lib32/" || return 1
  install -m755 X11R6/lib/*.so* "${pkgdir}/usr/lib32/" || return 1
  install -m755 X11R6/lib/modules/dri/*.so "${pkgdir}/usr/lib32/xorg/modules/dri/" || return 1

  cd "$pkgdir/usr/lib32/" || return 1
  ln -sf libfglrx_dm.so.1.0 libfglrx_dm.so.1 || return 1
  # ln -sf libfglrx_pp.so.1.0 libfglrx_pp.so.1 || return 1
  # ln -sf libfglrx_tvout.so.1.0 libfglrx_tvout.so.1 || return 1
  ln -sf libfglrx_gamma.so.1.0 libfglrx_gamma.so.1 || return 1
  ln -sf libAMDXvBA.so.1.0 libAMDXvBA.so.1 || return 1
  ln -sf libXvBAW.so.1.0 libXvBAW.so.1 || return 1
  ln -sf libGL.so.1.2 libGL.so.1 || return 1
  ln -sf libGL.so.1.2 libGL.so || return 1
  ln -sf libatiuki.so.1.0 libatiuki.so.1 || return 1
  ln -sf libatiuki.so.1.0 libatiuki.so || return 1
}
