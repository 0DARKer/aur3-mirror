# Maintainer: Spike29 <leguen.yannick@gmail.com>
# Contributor: Petr Malat <petris@penguin.cz> 

pkgname='tong'
pkgver='1.2'
pkgrel='4'
pkgdesc="Tetris meets Pong, with more twists than a contortionist club's secret handshake"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/tong/"
license=('GPL3')
depends=('sdl_image' 'sdl_mixer')
install="$pkgname.install"
source=("http://www.nongnu.org/tong/$pkgname-$pkgver.tar.gz"
	"$pkgname.desktop")
md5sums=('1456c03d24d5c3d0de039ecd7dac0264'
	'90695390ee1c4fd907a00beedb1e651d')

build() {
	cd $srcdir/$pkgname
	make GAME_DATA_DIR=/usr/share/$pkgname
}

package() {
	#create destination folders
	
	mkdir -p $pkgdir/usr/share/{$pkgname,applications,pixmaps}
	mkdir -p $pkgdir/usr/bin
	
	#move source files and binary in appropriate folders
	
	cd $srcdir/$pkgname
	cp -r media $pkgdir/usr/share/$pkgname
	rm -r $pkgdir/usr/share/$pkgname/media/{gp2x,wii} #remove unneeded folders
	
	install -m644 CREDITS $pkgdir/usr/share/$pkgname/
	install -m644 README $pkgdir/usr/share/$pkgname/
	install -Dm644 media/icon.png $pkgdir/usr/share/pixmaps/$pkgname.png
	install -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/
	install -m755 $pkgname $pkgdir/usr/bin/
}
