# Maintainer: Michele Mocciola <mickele>

pkgname=salome-kernel
pkgver=5.1.4
pkgrel=2
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation. KERNEL Module."
url="http://www.salome-platform.org"
depends=('python' 'python-numpy' 'boost' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'hdf5' 'python-numpy' 'graphviz' 'libxml2' 'cppunit' 'sed')
makedepends=('gcc' 'gcc-fortran' 'automake' 'autoconf' 'libtool' 'make' 'doxygen' 'docutils' 'coreutils')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
source=("http://files.opencascade.com/Salome/Salome${pkgver}/src${pkgver}.tar.gz" "${pkgname}.profile")
options=('libtool')

_source=src${pkgver}/KERNEL_SRC_${pkgver}
_installdir=/opt/salome/kernel
_pythonver=2.6

build() {

  cd ${srcdir}/${_source} || return 1 

  # Builds and install
  ./clean_configure || return 1
  ./build_configure || return 1

  ./configure --prefix=${_installdir} \
	      --with-python-site=${_installdir}/lib/python-${_pythonver}/site-packages/salome \
	      --with-python-site-exec=${_installdir}/lib/python-${_pythonver}/site-packages/salome \
	      --disable-debug \
	      --enable-production || return 1

  make || return 1
}

package(){
  cd ${srcdir}/${_source} || return 1 

  make DESTDIR="${pkgdir}" install || return 1

  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/profile.d/${pkgname}.sh" || return 1
}

md5sums=('ec363b2ff2dbdb889c973e9e19b2970f'
         'a2c626a103d977cc03108af2030a04e8')
