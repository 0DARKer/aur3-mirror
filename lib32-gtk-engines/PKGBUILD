_pkgbasename=gtk-engines
pkgname=lib32-$_pkgbasename
pkgver=2.20.2
pkgrel=2
pkgdesc="Theme engines for GTK+ 2 (32 bit, libraries only)"
arch=('x86_64')
url="http://live.gnome.org/GnomeArt"
license=('LGPL')
depends=('lib32-gtk2>=2.22.0' "$_pkgbasename")
makedepends=('pkgconfig' 'gcc-multilib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.20/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('15b680abca6c773ecb85253521fa100dd3b8549befeecc7595b10209d62d66b5')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 \
  			--enable-animation CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/share
}
