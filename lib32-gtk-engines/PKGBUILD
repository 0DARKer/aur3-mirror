# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gtk-engines
pkgname=lib32-${_pkgbase}
pkgver=2.20.2
pkgrel=3
pkgdesc="Theme engines for GTK+ 2 (32 bit)"
arch=('x86_64')
url="http://live.gnome.org/GnomeArt"
license=('LGPL')
depends=('lib32-gtk2' "${_pkgbase}")
makedepends=('intltool' 'gcc-multilib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/${pkgver%.*}/${_pkgbase}-${pkgver}.tar.bz2)
sha256sums=('15b680abca6c773ecb85253521fa100dd3b8549befeecc7595b10209d62d66b5')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --enable-animation --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/share"
}
