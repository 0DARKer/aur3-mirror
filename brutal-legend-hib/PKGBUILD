# Maintainer:  Ainola <opp310@alh.rqh> (ROT13)

pkgname=brutal-legend-hib
pkgver=2013_06_15
pkgrel=1
pkgdesc="Br√ºtal Legend is an action-adventure game with real-time strategy game elements created by Double Fine Productions (Humble Bundle version)."
arch=('i686' 'x86_64')
url="http://www.brutallegend.com"
license=('custom: commercial')
makedepends=('unzip')
if [ "$CARCH" = "x86_64" ]; then
    depends=('lib32-glu')
else
    depends=('glu')
fi
source=("hib://BrutalLegend-Linux-${pkgver//_/-}-setup.bin"
        "${pkgname}.desktop")
noextract=("BrutalLegend-Linux-${pkgver//_/-}-setup.bin")
sha256sums=("133cc6f565966503d347722a9bd13dd27f86c6ff75f5586cbc4e6ef6bbb8640a"
            "d032332fc97efafb3befa639e9568d38881ba3c8d43e58a0af1ba351ae5aad0f")

# Prevent compressing final package
PKGEXT='.pkg.tar'

# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download the file manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf"; echo "Read this PKGBUILD for more info."; exit 1')


package() {
    # Unzip will produce an error code because it is unable to unzip the Mojo Installer.
    # Therefore, a conditional into a no-op command will keep the PKGBUILD from failing
    # Of course, if you have any real problems unzipping the PKGBUILD will not abort.
    unzip -d "${srcdir}" "BrutalLegend-Linux-${pkgver//_/-}-setup.bin" || :
    mkdir -p "${pkgdir}/opt/${pkgname}"
    mv "${srcdir}/data/"* "${pkgdir}/opt/${pkgname}"

    # Launcher
    mkdir -p "$pkgdir/usr/bin"
    ln -s "/opt/${pkgname}/Buddha.bin.x86"  "${pkgdir}/usr/bin/${pkgname}"

    # Install Desktop Integration
    mkdir -p "$pkgdir"/usr/share/{pixmaps,applications}
    ln -s "/opt/${pkgname}/Buddha.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
