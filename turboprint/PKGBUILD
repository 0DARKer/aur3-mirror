# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=turboprint
pkgver=2.18
_realpkgver=${pkgver}-2
pkgrel=2
pkgdesc="High-quality printer driver system for Linux"
arch=('i686' 'x86_64')
url="http://www.turboprint.info/"
license=('custom')
depends=('cups' 'ghostscript')
optdepends=('gimp: For GIMP plugin'
            'kdelibs: For KDE applet'
            'gnome-panel-bonobo: For GNOME applet'
            'libgnomeui: For GNOME applet'
            'firefox: For browsing help')
install="${pkgname}.install"
[ "${CARCH}" = "i686" ]   && _arch='i586'
[ "${CARCH}" = "x86_64" ] && _arch='x86_64'
source=("http://www.turboprint.info/tp2/${pkgname}-${_realpkgver}.${_arch}.tgz"
        "turboprint.desktop"
        "turboprint-monitor.desktop"
        "tprintdaemon")
md5sums=(''
         'e3c504b2c1b7deb01d03fde710b117d7'
         '60a1754b2abd30e75e51acd9576cda43'
         '317d0851171e2a2ced76cea0631f5684')
[ "${CARCH}" = "i686" ]   && md5sums[0]='c30c8b2e1f8c6fe3e2270e70083d3968'
[ "${CARCH}" = "x86_64" ] && md5sums[0]='f0f6307e73df075e2f2ad09c83e61d6c'

build() {
  [ -f "${srcdir}/${pkgname}-${_realpkgver}.${_arch}.tar" ] && bsdtar -xf "${srcdir}/${pkgname}-${_realpkgver}.${_arch}.tar"
  cd "${srcdir}/${pkgname}-${_realpkgver}"
  sed -i 's#^TPDAEMON_USER=lp$#TPDAEMON_USER=root#' system.cfg
  if ! grep TP_CUPS=1 system.cfg > /dev/null; then
    echo TP_CUPS=1 >> system.cfg
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${_realpkgver}"
  RPM_BUILD_ROOT="${pkgdir}" lib/install-static --rpm

  # Install icons
  install -D -m644 img/tpapplet.png "${pkgdir}/usr/share/pixmaps/tpapplet.png"
  install -D -m644 img/tpicon.png "${pkgdir}/usr/share/pixmaps/tpicon.png"
  install -D -m644 img/tpmonitor.png "${pkgdir}/usr/share/pixmaps/tpmonitor.png"

  # Install desktop entries
  install -D -m644 "${srcdir}/turboprint.desktop" "${pkgdir}/usr/share/applications/turboprint.desktop"
  install -D -m644 "${srcdir}/turboprint-monitor.desktop" "${pkgdir}/usr/share/applications/turboprint-monitor.desktop"

  # Install license
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  (cd "${pkgdir}/usr/share/licenses/${pkgname}" && ln -sf "../../${pkgname}/locale/license_en.txt" "LICENSE")

  # Install daemon
  install -D -m755 "${srcdir}/tprintdaemon" "${pkgdir}/etc/rc.d/tprintdaemon"

  # Install CUPS components
  install -D -m755 "${pkgdir}/usr/share/turboprint/lib/pstoturboprint" "${pkgdir}/usr/lib/cups/filter/pstoturboprint"
  install -D -m755 "${pkgdir}/usr/share/turboprint/lib/commandtoturboprint" "${pkgdir}/usr/lib/cups/filter/commandtoturboprint"
  install -D -m755 "${pkgdir}/usr/share/turboprint/lib/tpu" "${pkgdir}/usr/lib/cups/backend/commandtoturboprint"

  # Install GIMP plugin
  install -D -m755 "${pkgdir}/usr/share/turboprint/lib/gimpplugin/gpturboprint" "${pkgdir}/usr/lib/gimp/2.0/plug-ins/gpturboprint"
  install -D -m644 "${pkgdir}/usr/share/turboprint/lib/gimpplugin/de.gmo" "${pkgdir}/usr/local/share/locale/de/LC_MESSAGES/gpturboprint.mo"
  install -D -m644 "${pkgdir}/usr/share/turboprint/lib/gimpplugin/papers.xml" "${pkgdir}/usr/local/share/gpturboprint/1.1.10/xml/papers.xml"
  install -D -m644 "${pkgdir}/usr/share/turboprint/lib/gimpplugin/printers.xml" "${pkgdir}/usr/local/share/gpturboprint/1.1.10/xml/printers.xml"

  # Install KDE applet
  install -D -m644 "${pkgdir}/usr/share/turboprint/lib/kde42applet/plasma-applet-turboprint.desktop" "${pkgdir}/usr/share/kde4/services/plasma-applet-turboprint.desktop"
  install -D -m755 "${pkgdir}//usr/share/turboprint/lib/kde42applet/plasma_applet_turboprint.so" "${pkgdir}/usr/lib/kde4/plasma_applet_turboprint.so"

  # Install GNOME applet
  install -D -m644 "${pkgdir}/usr/share/turboprint/lib/gnomeapplet/tpmonapplet.server" "${pkgdir}/usr/lib/bonobo/servers/tpmonapplet.server"
  install -D -m755 "${pkgdir}/usr/share/turboprint/lib/gnomeapplet/tpgnomeapplet" "${pkgdir}/usr/bin/tpgnomeapplet"
}

# vim:set ts=2 sw=2 et:
