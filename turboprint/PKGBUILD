# Contributor: Jonathan Liu <net147@gmail.com>
pkgname=turboprint
pkgver=2.17
_realpkgver=${pkgver}-2
pkgrel=4
pkgdesc="High-quality printer driver system for Linux"
arch=('i686' 'x86_64')
url="http://www.turboprint.info/"
license=('custom')
depends=('cups' 'ghostscript>=4.03' 'gtk2' 'libpng12')
optdepends=('gimp: For GIMP plugin'
            'kdelibs: For KDE 4 applet'
            'kdelibs3: For KDE 3 applet'
            'gnome-panel: For GNOME applet'
            'libgnomeui: For GNOME applet'
            'firefox: For browsing help')
install="${pkgname}.install"
[ "${CARCH}" = "i686" ]   && _arch='i586'
[ "${CARCH}" = "x86_64" ] && _arch='x86_64'
source=("http://www.turboprint.info/tp2/${pkgname}-${_realpkgver}.${_arch}.tgz"
        "turboprint.desktop"
        "turboprint-monitor.desktop"
        "tprintdaemon")
md5sums=(''
         'e3c504b2c1b7deb01d03fde710b117d7'
         '60a1754b2abd30e75e51acd9576cda43'
         '317d0851171e2a2ced76cea0631f5684')
[ "${CARCH}" = "i686" ]   && md5sums[0]='ea38fb987f44a2d29c6c8419da8a30a8'
[ "${CARCH}" = "x86_64" ] && md5sums[0]='47a7968c06b7bed7bc9f6efc5468ba01'

build() {
  [ -f "${srcdir}/${pkgname}-${_realpkgver}.${_arch}.tar" ] && bsdtar -xf "${srcdir}/${pkgname}-${_realpkgver}.${_arch}.tar"
  cd "${srcdir}/${pkgname}-${_realpkgver}"
  sed -i 's#^TPDAEMON_USER=lp$#TPDAEMON_USER=root#' system.cfg
  if ! grep TP_CUPS=1 system.cfg > /dev/null; then
    echo TP_CUPS=1 >> system.cfg
  fi
}

package() {
  cd "${srcdir}/${pkgname}-${_realpkgver}"
  RPM_BUILD_ROOT="${pkgdir}" lib/install-static --rpm
  install -D -m644 img/tpapplet.png "${pkgdir}/usr/share/pixmaps/tpapplet.png"
  install -D -m644 img/tpicon.png "${pkgdir}/usr/share/pixmaps/tpicon.png"
  install -D -m644 img/tpmonitor.png "${pkgdir}/usr/share/pixmaps/tpmonitor.png"
  install -D -m644 "${srcdir}/turboprint.desktop" "${pkgdir}/usr/share/applications/turboprint.desktop"
  install -D -m644 "${srcdir}/turboprint-monitor.desktop" "${pkgdir}/usr/share/applications/turboprint-monitor.desktop"
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  (cd "${pkgdir}/usr/share/licenses/${pkgname}" && ln -sf "../../${pkgname}/locale/license_en.txt" "LICENSE")
  install -D -m755 "${srcdir}/tprintdaemon" "${pkgdir}/etc/rc.d/tprintdaemon"
}

# vim:set ts=2 sw=2 et:
