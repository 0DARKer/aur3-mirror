# $Id: PKGBUILD 146960 2012-01-19 17:53:37Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-suhosin
pkgver=0.9.36
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='An advanced protection system for PHP installations'
url='http://www.hardened-php.net/suhosin/'
license=('PHP')
source=("http://download.suhosin.org/suhosin-${pkgver}.tgz"
        "http://download.suhosin.org/suhosin-${pkgver}.tgz.sig")
depends=('php')
checkdepends=('php-cgi')
backup=('etc/php/conf.d/suhosin.ini')
md5sums=('416ba40186bd18a4640d5cf93bb0a1cb'
         'SKIP')

build() {
	cd ${srcdir}/suhosin-${pkgver}
	phpize
	./configure --prefix=/usr --enable-suhosin
	make
}

check() {
	cd ${srcdir}/suhosin-${pkgver}
	export NO_INTERACTION=1
	make test
	echo
}

package() {
	cd ${srcdir}/suhosin-${pkgver}
	make INSTALL_ROOT=${pkgdir} install
	# disable by default
	sed -i 's|extension = suhosin.so|;extension=suhosin.so|g' suhosin.ini
	install -D -m644 suhosin.ini ${pkgdir}/etc/php/conf.d/suhosin.ini
}
