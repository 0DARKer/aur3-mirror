# Maintainer: hollunder <murks at tuxfamily dot org>

pkgname=wxlua-svn
pkgver=241
pkgrel=1
pkgdesc="WxLua - SVN version."
arch=('i686' 'x86_64')
url="http://wxlua.sourceforge.net"
license=('custom:wxWindows')
depends=('lua' 'desktop-file-utils' 'wxgtk')
makedepends=('subversion' 'cmake')
provides=('wxlua' 'wxstedit')
conflicts=('wxlua' 'wxstedit')
install="wxlua.install"
source="wxlua::svn+http://svn.code.sf.net/p/wxlua/svn/trunk"
md5sums=('SKIP')

build() {
  cd "$srcdir/wxlua/wxLua/"

  # wxstedit doc folder fix
  sed -i 's|doc/|share/&|' modules/wxstedit/CMakeLists.txt

  # right shebang for wxluafreeze.lua
  sed -i 's| \..*/|/usr/bin/env |;1s||$|' apps/wxluafreeze/wxluafreeze.lua
  
  #
  # BUILD HERE
  #
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config \
           -DwxLua_LUA_INCLUDE_DIR=/usr/include \
           -DwxLua_LUA_LIBRARY=/usr/lib/liblua.so.5.2 \
           -DwxLua_LUA_LIBRARY_USE_BUILTIN=0 \
           -DwxLua_LUA_LIBRARY_VERSION=5.2
  make

}

package() {
  cd "$srcdir/wxlua/wxLua/build"
  make DESTDIR="$pkgdir/" install

  # mv lua module
  install -d "$pkgdir/usr/lib/lua/5.2"
  mv "$pkgdir/usr/lib/libwx.so" "$pkgdir/usr/lib/lua/5.2/wx.so"

  # desktop file
  install -Dm644 ../distrib/autopackage/wxlua.desktop \
    "$pkgdir/usr/share/applications/wxlua.desktop"

  # mime file
  install -Dm644 ../distrib/autopackage/wxlua.xml \
    "$pkgdir/usr/share/mime/packages/wxlua.xml"

  # license
  install -Dm 644 ../docs/licence.txt \
    "$pkgdir/usr/share/licenses/wxlua/LICENSE"
}

# vim:set ts=2 sw=2 et:
