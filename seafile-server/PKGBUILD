
# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

# Contributor: Aaron Lindsay <aaron@aclindsay.com>

pkgname=seafile-server
pkgver=2.0.4
pkgrel=1
pkgdesc="Next-generation open source cloud storage with advanced features on privacy protection and teamwork. (Server components, without seahub)"
arch=('i686' 'x86_64' 'armv6h')
url="https://github.com/haiwen/seafile/"
license=('GPL3')
depends=('python2-mako' 'python2-webpy' 'python2-simplejson' 'python2-imaging' 'python2-chardet' 'python2-pip' 'python2-virtualenv' 'gunicorn' 'python2-djblets' 'libevhtp' 'seafile-shared>=2.0.7')
makedepends=('vala' 'intltool')
optdepends=('python2: for seahub upgrade scripts')
provides=()
conflicts=('django' 'django-rest-framework', 'seafile')
options=('!libtool' '!emptydirs')
install=seafile-server.install
source=("https://github.com/haiwen/seafile/archive/v${pkgver}-server.tar.gz"
        "seafile-admin.patch"
        "seafile-server.install"
        "seafile-server@.service")
md5sums=('7b98499552d7cec48efb5e56ff7c614e'
         '32dbcdd5f2273643be49c5c65389f0b2'
         'a25c00bee5502689ed5d7b0e2379401b'
         '0cc44804bafa6ec7bf0d84271b951152')
sha256sums=('95fbf330f5f0f39c3d764dcfbe9ee49e7479931afb25b0d7c10b7136a5772cf4'
            '0d9578b85c00f3087a19f227b29c3fda13e83d093d024c68c1df5575d30d56dc'
            '44889af3e5dd861caa1bb0b0c63ad739856d9ac22b327b2c2b3d5e5929cf0896'
            '943ed1bb7d4908b452ce8b7abff26e56f3f24582f6a2cf6e0dc4bf2bffd0a6d8')

prepare ()
{
	cd "${srcdir}/seafile-${pkgver}-server"
	patch -p1 -i ${srcdir}/seafile-admin.patch
}

build ()
{
	cd "${srcdir}/seafile-${pkgver}-server"
	./autogen.sh
	./configure --enable-server --enable-httpserver --disable-client --disable-python --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package ()
{
	# Install library and header files
	cd "${srcdir}/seafile-${pkgver}-server"
	make DESTDIR="${pkgdir}/" install

	# Remove files already installed by seafile-shared
	# to maintain compatibility
	rm -rf "${pkgdir}/usr/lib"
	rm -rf "${pkgdir}/usr/include"

	# Install upgrade scripts
	mkdir -p "${pkgdir}/usr/share/$pkgname/scripts"
	cp -r -p "${srcdir}/seafile-${pkgver}-server/scripts/upgrade" "${pkgdir}/usr/share/$pkgname/scripts/upgrade"
	rm -rf "${pkgdir}/usr/share/$pkgname/scripts/upgrade/win32"
	# Fix upgrade script's  python 2 requirement
	sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' "${pkgdir}/usr/share/seafile-server/scripts/upgrade/db_update_1.3_1.4.py"

	# Workaround for strange behaviour in the Makefile, which
	# installs python bindings in the package root directory,
	# even though they were disabled
	rm -rf "${pkgdir}/seaserv"
	rm -rf "${pkgdir}/seafile"

	# Install license
	install -D -m644 ${srcdir}/seafile-${pkgver}-server/LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE.txt

	# Install systemd service
	install -D -m644 ${srcdir}/seafile-server@.service ${pkgdir}/usr/lib/systemd/system/seafile-server@.service
}
