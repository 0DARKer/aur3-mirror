# Maintainer: Ner0

pkgname=nemo-fm
pkgver=1.0.2
pkgrel=1
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('dconf' 'desktop-file-utils' 'exempi' 'gnome-desktop' 'gnome-icon-theme' 'hicolor-icon-theme' 'libnotify' 'libtracker-sparql')
makedepends=('gtk-doc' 'gobject-introspection' 'intltool')
conflicts=('nemo-git')
options=('!emptydirs' '!libtool')
install=nemo.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/nemo/tarball/$pkgver")
md5sums=('0da7b6dd5d42e0273b500e9ca60ca4f5')

build() {
  cd linuxmint-nemo-*

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-schemas-compile
  make
}

package() {
  cd linuxmint-nemo-*
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
