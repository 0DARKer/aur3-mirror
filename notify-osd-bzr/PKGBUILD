# Contributor: Artyom Smirnov <smirnoffjr@gmail.com>
# Maintainer: Andrea Fagiani <andfagiani {at} gmail {dot} com>

pkgname=notify-osd-bzr
pkgver=433
pkgrel=2
pkgdesc="On-screen notification display agent, with semi-transparent click-through bubbles."
url="https://edge.launchpad.net/notify-osd"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gconf' 'cairo' 'libwnck')
makedepends=('bzr' 'gnome-common' 'libnotify')
conflicts=('notification-daemon' 'notify-osd')
provides=('notification-daemon' 'notify-osd')
install=$pkgname.install
source=(3sec.patch dynamic.patch)
md5sums=('6e9b159bf29219d08c7c73cb7f5e93db'
         '5d866067d9416cfe083c4e86a90e9f2e')

_bzrtrunk=lp:notify-osd
_bzrmod=notify-osd

build() {
  cd $srcdir

  msg "Connecting to notify-osd bzr server...."

  if [ -d $_bzrmod ] ; then
    cd $_bzrmod && bzr pull -r $pkgver
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "bzr checkout done or server timeout"
  msg "Starting make..."

  rm -rf $srcdir/$_bzrmod-build
  cp -r $srcdir/$_bzrmod $srcdir/$_bzrmod-build
  cd $srcdir/$_bzrmod-build

  patch -Np1 -i $srcdir/3sec.patch
  patch -Np1 -i $srcdir/dynamic.patch

  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/notify-osd
  # do not build examples causing redundant build dependencies
  sed -i "s|\(^SUBDIRS.*\)examples\(.*\)$|\1\2|" Makefile
  make
  make install DESTDIR=$pkgdir
}
