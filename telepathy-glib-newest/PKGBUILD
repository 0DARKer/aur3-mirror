# $Id: PKGBUILD 126144 2011-06-01 19:05:33Z ibiru $
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Flamelab <panosfilip@gmail.com>

pkgname=telepathy-glib-newest
_realpkgname=telepathy-glib
pkgver=0.15.1
pkgrel=2
pkgdesc="GLib bindings for the Telepathy D-Bus protocol"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL2.1')
options=('!libtool' '!emptydirs')
provides=("telepathy-glib=${pkgver}")
conflicts=("telepathy-glib")
depends=('dbus-glib')
makedepends=('libxslt' 'vala' 'gobject-introspection')
source=("http://telepathy.freedesktop.org/releases/${_realpkgname}/${_realpkgname}-${pkgver}.tar.gz")

build() {
    cd "${srcdir}/${_realpkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
        --libexecdir=/usr/lib/telepathy \
        --enable-vala-bindings \
        --enable-static=no
    make
}

package() {
    cd "${srcdir}/${_realpkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
md5sums=('5e7ff2219734cc0ba08d53fc0ef7105d')
