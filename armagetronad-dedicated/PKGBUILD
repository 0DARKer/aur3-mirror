# Maintainer: vehk <vehk@vehk.de>

pkgname=armagetronad-dedicated
pkgver=0.2.8.3.2
pkgrel=1
pkgdesc='A Tron Clone in 3D - dedicated server version'
arch=('i686' 'x86_64')
url='http://armagetronad.net/'
license=('GPL')
depends=('libxml2' 'python2')
optdepends=('zthread: Threading for requests to remote authentication')
install=armagetronad.install
source=("$pkgname-${pkgver}.tar.gz::http://sourceforge.net/projects/armagetronad/files/stable/$pkgver/armagetronad-${pkgver}.src.tar.gz/download"
        'armagetronad.service' 'armagetronad.install')

md5sums=('f7796de4ef50bd33553e0a3f93fd67e6'
         '30790fadff70b60ef4b47045044cd49f'
         '475e6c0cb010bd6f6d27489c293bd552')
build() {
    cd $srcdir/armagetronad-${pkgver}

    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' language/update.py
    sed -i 's|#!/usr/bin/env PYTHON|#!/usr/bin/python2|' batch/make/sortresources.py

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --mandir=/usr/share/man \
        --enable-dedicated \
        --disable-initscripts \
        --disable-useradd \
        --enable-armathentication \
        --enable-krawall

    sed -i 's|python|python2|' Makefile
    sed -i 's|python|python2|' resource/Makefile
    make
}

package(){
    cd $srcdir/armagetronad-${pkgver}
    make DESTDIR=$pkgdir install

    rm -rf $pkgdir/usr/share/applnk
    rm -rf $pkgdir/usr/share/games/armagetronad-dedicated/desktop
    rm $pkgdir/usr/bin/armagetronad-dedicated-uninstall

    install -D -m644 ${srcdir}/armagetronad.service ${pkgdir}/usr/lib/systemd/system/armagetronad.service
}
