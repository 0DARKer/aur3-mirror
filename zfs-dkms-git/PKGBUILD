#
# Maintainer: isiachi <isiachi@rhyeworld.it>
#

pkgname="zfs-dkms-git"
pkgver=0.6.2_r258_g4fd762f
pkgrel=1
license=('CDDL')

pkgdesc="Kernel modules for the Zettabyte File System."
depends=("spl-dkms-git" "zfs-utils-git" "dkms")
makedepends=("git")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("git+https://github.com/zfsonlinux/zfs.git#commit=4fd762f")
md5sums=('SKIP')
replaces=("zfs")
provides=("zfs" "zfs-git")
conflicts=("zfs" "zfs-lts")

_dirver="`echo ${pkgver} | sed "s|^\(.*\.[0-9]\).*|\1|g"`"

pkgver() {
        cd "${srcdir}/zfs"
        git describe --always | sed "s|^[a-z]*-\([0-9.]*\)\(-\)|\1\2r|;s|-|_|g"
}

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_dirver} -f dkms.conf -n zfs
}

package() {
    install -d ${pkgdir}/usr/src
    cp -a ${srcdir}/zfs ${pkgdir}/usr/src/zfs-${_dirver}
    rm -rf ${pkgdir}/usr/src/zfs-${_dirver}/.git
    rm -f ${pkgdir}/usr/src/zfs-${_dirver}/.gitignore
}