# Maintainer: Ignacio Losiggio <naquo@geneu.co.cc>
pkgname=audio-recorder-bzr
pkgver=56
pkgrel=1
pkgdesc="Audio recorder for GNOME and Unity Desktops."
arch=(i686 x86_64)
url="https://launchpad.net/audio-recorder"
license=('GPL')
depends=('gstreamer0.10-base' 'gstreamer0.10-good-plugins' 'gstreamer0.10-ugly-plugins' 'gstreamer0.10-bad-plugins')
#remove gstreamer0.10-ugly-plugins to disable mp3 support
#remove gstreamer0.10-bad-plugins to disable aac support
makedepends=('bzr' 'automake' 'autoconf' 'gettext' 'intltool' 'pkg-config' 'gtk2' 'glib2' 'gconf' 'gstreamer0.10' 'gstreamer0.10-base' 'gnome-media-pulse' 'libpulse' 'dbus-core' 'dbus-glib')
#add libappindicator(-bzr) for indicator applet support
optdepends=()
source=()
md5sums=()
conflicts=('audio-recorder')
provides=('audio-recorder')
_bzrmod="audio-recorder"
_bzrtrunk="lp:${_bzrmod}"
build() {

  cd "$srcdir"

  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  aclocal
  autoconf
  automake -a
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {

cd "$srcdir/$_bzrmod-build"
	make DESTDIR="$pkgdir/" install

}
