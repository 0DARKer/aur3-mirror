# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=audio-recorder-bzr
pkgver=329
pkgrel=1
pkgdesc="Audio recorder for GNOME and Unity Desktops"
arch=('i686' 'x86_64')
url="https://launchpad.net/audio-recorder"
license=('GPL')
# Add libappindicator3 for indicator applet support
depends=('dbus-glib' 'gconf' 'gst-plugins-bad' 'gst-plugins-base' 'gst-plugins-good' 'gstreamer' 'gtk3' 'libgnome-media-profiles' 'libpulse')
makedepends=('bzr' 'intltool')
conflicts=('audio-recorder')
provides=('audio-recorder')
source=("audio-recorder::bzr+http://bazaar.launchpad.net/~osmoma/audio-recorder/trunk/")
sha512sums=('SKIP')

pkgver() {
  cd audio-recorder
  bzr revno
}

build() {
  cd "${srcdir}/audio-recorder"

  autoreconf -vfi
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/audio-recorder"
  make DESTDIR="${pkgdir}/" install
}
