# Maintainer: Kelvin Ng (qpalz) <kelvin9302104 at gmail dot com>

pkgname=nvidia-uksm-ck
pkgver=319.23
_extramodules=extramodules-3.9-uksm-ck
pkgrel=1
_pkgdesc="NVIDIA drivers for linux-uksm-ck."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-uksm-ck>=3.9' 'linux-uksm-ck<3.10' "nvidia-utils=${pkgver}")
makedepends=('linux-uksm-ck-headers>=3.9' 'linux-uksm-ck-headers<3.10')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-uksm-ck-stable' 'nvidia-beta-ck' 'nvidia-uksm')
#groups=('ck-generic')
#replaces=()
license=('custom')
install=nvidia-uksm-ck.install
options=(!strip)


if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('cbdca80e11b643aa46bded82abe43754')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('74edd76b4bb9229f7d9c00a79f1e3860')
fi

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	cd "${srcdir}"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -Dm644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -dm755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-uksm-ck.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-uksm-ck.install"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
