# Maintainer: Kelvin Ng (qpalz) <kelvin9302104 at gmail dot com>

pkgname=nvidia-uksm-ck
pkgver=313.18
_extramodules=extramodules-3.7-uksm-ck
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=2
_pkgdesc="NVIDIA drivers for linux-uksm-ck."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-uksm-ck>=3.7' 'linux-uksm-ck<3.8' "nvidia-utils=${pkgver}")
makedepends=('linux-uksm-ck-headers>=3.7' 'linux-uksm-ck-headers<3.8')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-ck-stable' 'nvidia-beta-ck' 'nvidia-uksm')
license=('custom')
options=(!strip)
install=nvidia-uksm-ck.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
		'unfuck-3.7.6.patch')
	md5sums=('780c37c28a6e06e9571cafe348b7da64'
		 '1b173db94b390214e5ec1714f4749068')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
		'unfuck-3.7.6.patch')
	md5sums=('fa17a260793a38b4b8ae367db2e03b39'
		 '1b173db94b390214e5ec1714f4749068')
fi

build() {
	cd "${srcdir}"
	sh "${_pkg}.run" --extract-only
	cd "${_pkg}/kernel"
	patch -Np1 -i "${srcdir}/unfuck-3.7.6.patch"
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-uksm-ck.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-uksm-ck.install"
	gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
