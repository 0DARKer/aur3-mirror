# Maintainer: Lawliet <yanzilme@gmail.com>

pkgname=nvidia-uksm-ck
pkgver=295.59
_extramodules=extramodules-3.4-uksm-ck
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=1
_pkgdesc="NVIDIA drivers for linux-uksm-ck."
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
#depends=('linux-uksm-ck>=3.4' 'linux-uksm-ck<3.5' "nvidia-utils=${pkgver}")
depends=('linux-uksm-ck>=3.4' 'linux-uksm-ck<3.5')
makedepends=('linux-uksm-ck-headers>=3.4' 'linux-uksm-ck-headers<3.5')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-ck-stable' 'nvidia-beta-ck' 'nvidia-uksm' 'nvidia-ck')
#groups=('ck-generic')
#replaces=()
license=('custom')
options=(!strip)
install=nvidia-uksm-ck.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('bef732dfcf5cb079c06c1e8672d8d5dd')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('a921cfdb0162b9bb788c4486b1e6d725')
fi

build() {
	cd "${srcdir}"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-uksm-ck.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-uksm-ck.install"
	gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
