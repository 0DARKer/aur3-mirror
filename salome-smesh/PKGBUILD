# Maintainer: Michele Mocciola <mickele>

pkgname=salome-smesh
pkgver=7.4.0
pkgrel=1
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation. SMESH Module."
url="http://www.salome-platform.org"
depends=('salome-kernel=7.4.0' 'salome-gui=7.4.0' 'salome-geom=7.4.0' 'salome-med=7.4.0' 'python2' 'qt4' 'boost-libs' 'opencascade>=6.7.1' 'qwt' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'hdf5' 'med-salome' 'paraview-salome' 'graphviz')
makedepends=('boost' 'swig' 'python2-sphinx' 'optipng')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
source=("http://files.salome-platform.org/Salome/Salome${pkgver}/src${pkgver}.tar.gz" "salome-smesh.profile" "broken-png-list")

_source=SMESH_SRC_${pkgver}
_installdir=/opt/salome/smesh
_paraviewrootdir=/usr
_paraviewver=4.1

prepare() {
  cd "$srcdir/$_source"
  
  # python -> python2
  for _FILE in `grep -Rl "/usr/bin/env python" * `
  do
	sed -e "s|/usr/bin/env python|/usr/bin/env python2|" -i ${_FILE}
  done
}

build() {
  source /etc/salome/profile.d/salome-kernel.sh
  source /etc/salome/profile.d/salome-gui.sh
  source /etc/salome/profile.d/salome-geom.sh
  source /etc/salome/profile.d/salome-med.sh

  rm -rf "${srcdir}/${_source}/build"
  mkdir -p "${srcdir}/${_source}/build"
  cd "${srcdir}/${_source}/build"

  cmake .. \
     -DCMAKE_INSTALL_PREFIX=$_installdir \
     -DPYTHON_EXECUTABLE=/usr/bin/python2 \
     -DSPHINX_EXECUTABLE=/usr/bin/sphinx-build2 \
     -DVTK_DIR="${_paraviewrootdir}/lib/cmake/paraview-${_paraviewver}"
  make
}

package() {
  cd "${srcdir}/${_source}/build"

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"
  cat "${srcdir}/broken-png-list" | xargs -n 1 -P 3 optipng -quiet -force -fix

  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/salome/profile.d/${pkgname}.sh"
}
md5sums=('18055915a14e0921563072f830614e7f'
         '61bfd1bf90ba52db63481c8ba736ee80'
         'e7a1509a2708eacf944480e911509805')
