# Maintainer: TDY <tdy@gmx.com>

pkgname=git-cola
pkgver=1.4.3.5
pkgrel=1
pkgdesc="A powerful GUI for Git"
arch=('any')
url="http://cola.tuxfamily.org/"
license=('GPL')
depends=('git' 'python2-pyqt>=4.3')
makedepends=('asciidoc' 'docbook-xsl' 'python-sphinx' 'rsync' 'xmlto')
optdepends=('pyinotify>=0.7.1: for inotify support'
            'tk: to use the built-in ssh-askpass handler')
options=('!makeflags')
source=(http://cola.tuxfamily.org/releases/cola-$pkgver.tar.gz)
md5sums=('18ced4f4300d85c9cd0183b429b6b092')

build() {
  cd "$srcdir/${pkgname/*-}-$pkgver"
  sed -i 's,= python,&2,' Makefile
  sed -i '1s,python,&2,' bin/$pkgname
  make all doc html
}

package() {
  cd "$srcdir/${pkgname/*-}-$pkgver"
  make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html}
}
