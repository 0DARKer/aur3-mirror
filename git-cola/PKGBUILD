# Maintainer: TDY <tdy@gmx.com>

pkgname=git-cola
pkgver=1.4.3
pkgrel=1
pkgdesc="A powerful GUI for Git"
arch=('i686' 'x86_64')
url="http://cola.tuxfamily.org/"
license=('GPL')
depends=('git' 'python2-qt>=4.3')
makedepends=('asciidoc' 'docbook-xsl' 'python-sphinx' 'rsync' 'xmlto')
optdepends=('pyinotify>=0.7.1: inotify support')
options=('!makeflags')
source=(http://cola.tuxfamily.org/releases/cola-$pkgver.tar.gz)
md5sums=('64d9c3e45a746c8775a0f33f65b91155')

build() {
  cd "$srcdir/${pkgname/*-}-$pkgver"
  sed -i 's/?= python/&2/' Makefile
  sed -i '1 s,#!/usr/bin/env python,&2,' bin/$pkgname
  make all doc html
}

package() {
  cd "$srcdir/${pkgname/*-}-$pkgver"
  make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html}
}
