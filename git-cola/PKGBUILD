# Maintainer: Paul Weingardt <paulweingardt@web.de>
# Contributor: TDY <tdy@gmx.com>
pkgname=git-cola
pkgver=1.9.4
pkgrel=1
pkgdesc="A powerful GUI for Git"
arch=('any')
url="http://git-cola.github.com/"
license=('GPL')
depends=('git' 'python2-pyqt4>=4.4')
makedepends=('asciidoc' 'docbook-xsl' 'rsync' 'xmlto' 'python2-sphinx>=1.1.3')
optdepends=('python2-pyinotify>=0.7.1: for inotify support'
'tk: to use the built-in ssh-askpass handler')
options=('!makeflags')
source=("https://github.com/git-cola/git-cola/archive/v$pkgver.zip")
sha256sums=('30864602fd1ebfd84e287ad0840a7b4c0dfa1f9d8e6496777c9bf5638dc3aa6c')

build() {
	cd "$srcdir/${pkgname}-$pkgver"
	sed -i 's,= python,&2,' Makefile
	sed -i 's,sphinx-build,&2,' share/doc/git-cola/Makefile
	sed -i 's,env python,&2,' setup.py

	make all doc html
}

package() {
	cd "$srcdir/${pkgname}-$pkgver"
	make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html}
}
