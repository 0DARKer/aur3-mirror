# Maintainer: Paul Weingardt <paulweingardt@web.de>
# Contributor: TDY <tdy@gmx.com>
pkgname=git-cola
pkgver=1.8.3
pkgrel=1
pkgdesc="A powerful GUI for Git"
arch=('any')
url="http://git-cola.github.com/"
license=('GPL')
depends=('git' 'python2-pyqt>=4.3')
makedepends=('asciidoc' 'docbook-xsl' 'rsync' 'xmlto' 'python2-sphinx>=1.1.3')
optdepends=('python2-pyinotify>=0.7.1: for inotify support'
'tk: to use the built-in ssh-askpass handler')
options=('!makeflags')
source=('http://git-cola.github.io/releases/git-cola-1.8.3.tar.gz')
sha256sums=('4bc2788f94b62019343b2d2e3fda550283c2b20f2184e2ffddaec74309fffe49')

build() {
	cd "$srcdir/${pkgname}-$pkgver"
	sed -i 's,= python,&2,' Makefile
	sed -i 's,sphinx-build,&2,' share/doc/git-cola/Makefile
	sed -i 's,env python,&2,' setup.py

	make all doc html
}

package() {
	cd "$srcdir/${pkgname}-$pkgver"
	make prefix=/usr DESTDIR="$pkgdir" install{,-doc,-html}
}
