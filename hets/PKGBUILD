# Maintainer: AndrÃ© van Delden (andre.van.delden X uni-bremen.de)

pkgname=hets
pkgver=2013.11.21
pkgrel=1
pkgdesc="A parsing, static analysis and proof management tool incorporating \
various provers and different specification languages, thus providing a tool \
for heterogeneous specifications. Logic translations are first-class citizens."
url="http://www.informatik.uni-bremen.de/agbkb/forschung/formal_methods/CoFI/hets/index_e.htm"
arch=('x86_64')
license='custom:hets-license'
depends=('spass'
         'eprover'
         'minisat'
         'udrawgraph>=3.1.1'
         'gtk2'
         'libglade<3.0'
         'tcl'
         'tk>=8.4'
         'ncurses')
makedepends=('subversion')
optdepends=('e-krhyper' 'isabelle' 'pellet')
provides=('hets')
source=('http://www.informatik.uni-bremen.de/agbkb/forschung/formal_methods/CoFI/hets/linux64/versions/hets-2013-11-21.bz2'
        'http://www.informatik.uni-bremen.de/cofi/lib.tgz'
        'http://www.informatik.uni-bremen.de/agbkb/forschung/formal_methods/CoFI/hets/LICENSE.txt'
        'hets')
sha512sums=('547fb5f491598d9e77d8ad069b3f5d676f1e0afe81b45fff87dff7b315fee06e4e81a005e3b1bf9d962f1382f88e9c5022080b6fae36fac4cf0be5563700474a'
            '5482fc69d666065fd53180cea98c0b2758519e9abc938a8dd12acd8a803191719d5b7ca682931f77e92e21cfacd21af2009beebd23f2ab38522ffd792cd1e6e1'
            '5b322abf6a5a82894113c0dfe549725b140006197a920dfdf1a3ed615730e1de0a947cceea5ff9357cdd42334f8f53a1ab66072fca9ea966be85340a56d4649c'
            'd29a951ac2d6df346074c0be61626f38c0ccd5abe870faf945f50b3e420ff30b24c0115bf0d0e0a7b2af98803233ac3f50165169d341670db83fba17845136cf')

package() {
  cd ${srcdir}

  # install the libraries
  install -d ${pkgdir}/usr/share/lib${pkgname}
  # Maybe we should care for the permissions after the copy:
  cp -r Hets-lib/* ${pkgdir}/usr/share/lib${pkgname}

  # install the binary and starter script
  install -Dm755 ${pkgname}-2013-11-21 ${pkgdir}/usr/bin/${pkgname}.bin
  install -Dm755 hets ${pkgdir}/usr/bin/hets

  # install license
  install -Dm644 ${srcdir}/LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # The libtinfo.so functionality is built into the libncurses.so shared library.
  if ! test -e /usr/lib/libtinfo.so.5; then
    install -d ${pkgdir}/usr/lib
    if test -e /usr/lib/libtinfo.so; then
      echo -e "Will symlink\n\
  '/usr/lib/libtinfo.so' --> '/usr/lib/libtinfo.so.5'\n\
after installation."
      ln -s /usr/lib/libtinfo.so ${pkgdir}/usr/lib/libtinfo.so.5
    elif test -e /usr/lib/libncurses.so.5; then
      echo -e "Will symlink\n\
  '/usr/lib/libncurses.so.5' --> '/usr/lib/libtinfo.so.5'\n\
  '/usr/lib/libtinfo.so.5'   --> '/usr/lib/libtinfo.so'\n\
after installation."
      ln -s /usr/lib/libncurses.so.5 ${pkgdir}/usr/lib/libtinfo.so.5
      ln -s /usr/lib/libtinfo.so.5   ${pkgdir}/usr/lib/libtinfo.so
    else
      echo "Cannot find either of '/usr/lib/libtinfo.so.5', '/usr/lib/libtinfo.so' or '/usr/lib/libncurses.so.5'."
      return 1
    fi
  fi

  # Hets expects udrawgraph to be named uDrawGraph:
  if ! test -e /usr/bin/uDrawGraph; then
    if test -e /usr/bin/udrawgraph; then
      echo -e "Will symlink\n\
  '/usr/bin/uDrawGraph' --> '/usr/bin/udrawgraph'\n\
after installation."
      ln -s /usr/bin/udrawgraph ${pkgdir}/usr/bin/uDrawGraph
    else
      echo "Cannot find either of '/usr/bin/udrawgraph' or '/usr/bin/uDrawGraph'."
      return 1
    fi
  fi
  # Hets expects eprover to be named eproof:
  if ! test -e /usr/bin/eproof; then
    if test -e /usr/bin/eprover; then
      echo -e "Will symlink\n\
  '/usr/bin/eprover' --> '/usr/bin/eproof'\n\
after installation."
      ln -s /usr/bin/eprover ${pkgdir}/usr/bin/eproof
    else
      echo "Cannot find either of '/usr/bin/eprover' or '/usr/bin/eproof'."
      return 1
    fi
  fi
}
# vim:syntax=sh
