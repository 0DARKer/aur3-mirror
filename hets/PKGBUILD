# Maintainer: Andr√© van Delden <andre.van.deldenX@Xuni-bremen.de>

pkgname=hets
pkgver=2013.11.21
pkgrel=1

pkgdesc="A parsing, static analysis and proof management tool incorporating various provers and different specification languages, thus providing a tool for heterogeneous specifications. Logic translations are first-class citizens."

url="http://www.informatik.uni-bremen.de/agbkb/forschung/formal_methods/CoFI/hets/index_e.htm"
arch=('x86_64')
license='custom:hets-license'
depends=('spass'
         'eprover'
         'minisat'
         'udrawgraph>=3.1.1'
         'gtk2'
         'libglade<3.0'
         'tcl'
         'tk>=8.4'
         'ncurses')
makedepends=('subversion')
optdepends=('e-krhyper' 'isabelle' 'pellet')
source=('http://www.informatik.uni-bremen.de/agbkb/forschung/formal_methods/CoFI/hets/linux64/versions/hets-2013-11-21.bz2'
        'http://www.informatik.uni-bremen.de/cofi/lib.tgz'
        'http://www.informatik.uni-bremen.de/agbkb/forschung/formal_methods/CoFI/hets/LICENSE.txt'
        'hets')
sha512sums=('6280c314de9b02300f893a23e5c87c2ccdacfe86c313d324a2c2dbb31f468c0c5c75cfb5daf5663626f535baaf7789ca929358d9d2ba885d460b4c32d05be27d'
            '2a04cd24f9b5216334c90ddadc3e45feb3ccf6b56b01038b21bb3b3abfba9a8d31d7bd816a87423bd3d1b814e0b9309ce9d34fcbc77ad42db1cccfd65faf280b'
            '5b322abf6a5a82894113c0dfe549725b140006197a920dfdf1a3ed615730e1de0a947cceea5ff9357cdd42334f8f53a1ab66072fca9ea966be85340a56d4649c'
            'd29a951ac2d6df346074c0be61626f38c0ccd5abe870faf945f50b3e420ff30b24c0115bf0d0e0a7b2af98803233ac3f50165169d341670db83fba17845136cf')

package() {
  cd ${srcdir}

  # install the libraries
  install -d ${pkgdir}/usr/share/lib${pkgname}
  # Maybe we should care for the permissions after the copy:
  cp -r Hets-lib/* ${pkgdir}/usr/share/lib${pkgname}

  # install the binary and starter script
  install -Dm755 ${pkgname}-2013-11-21 ${pkgdir}/usr/bin/${pkgname}.bin
  install -Dm755 hets ${pkgdir}/usr/bin/hets

  # install license
  install -Dm644 ${srcdir}/LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # The libtinfo.so functionality is built into the libncurses.so shared library.
  if ! test -e /usr/lib/libtinfo.so.5; then
    install -d ${pkgdir}/usr/lib
    if test -e /usr/lib/libtinfo.so; then
      ln -s /usr/lib/libtinfo.so ${pkgdir}/usr/lib/libtinfo.so.5
    elif test -e /usr/lib/libncurses.so.5; then
      ln -s /usr/lib/libncurses.so.5 ${pkgdir}/usr/lib/libtinfo.so.5
      ln -s /usr/lib/libtinfo.so.5   ${pkgdir}/usr/lib/libtinfo.so
    else
      echo "Cannot find either of '/usr/lib/libtinfo.so.5', '/usr/lib/libtinfo.so' or '/usr/lib/libncurses.so.5'."
      return 1
    fi
  fi

  # Hets expects udrawgraph to be named uDrawGraph:
  if ! test -e /usr/bin/uDrawGraph; then
    if test -e /usr/bin/udrawgraph; then
      ln -s /usr/bin/udrawgraph ${pkgdir}/usr/bin/uDrawGraph
    else
      echo "Cannot find either of '/usr/bin/udrawgraph' or '/usr/bin/uDrawGraph'."
      return 1
    fi
  fi
  # Hets expects eprover to be named eproof:
  if ! test -e /usr/bin/eproof; then
    if test -e /usr/bin/eprover; then
      ln -s /usr/bin/eprover ${pkgdir}/usr/bin/eproof
    else
      echo "Cannot find either of '/usr/bin/eprover' or '/usr/bin/eproof'."
      return 1
    fi
  fi
}
# vim:syntax=sh
