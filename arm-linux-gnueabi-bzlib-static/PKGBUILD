# Contributor: Jaroslaw Rosiek (jaroslaw.rosiek@interia.pl)
_target=arm-linux-gnueabi
_sysroot=/usr/${_target}
pkgname=${_target}-bzlib-static
pkgver=1.0.6
pkgrel=2
arch=(any)
pkgdesc="Bzlib (libbzip2) static library (${_target})"
makedepends=("${_target}-gcc" "${_target}-binutils")
url="http://www.bzip.org/"
license=(custom)
source=(http://www.bzip.org/$pkgver/bzip2-$pkgver.tar.gz)
options=(!strip)

build()
{
  cd $srcdir/bzip2-$pkgver

  make CC=${_target}-gcc AR=${_target}-ar RANLIB=${_target}-ranlib \
   "CFLAGS=-Wall -Winline -O2 -D_FILE_OFFSET_BITS=64" \
   libbz2.a
}

package()
{
  cd $srcdir/bzip2-$pkgver

  mkdir -p $pkgdir/${_sysroot}/{lib,include}
  install -m 644 -o 0 -g 0 libbz2.a $pkgdir/${_sysroot}/lib/
  install -m 644 -o 0 -g 0 bzlib.h $pkgdir/${_sysroot}/include/
}

md5sums=('00b516f4704d4a7cb50a1d97e6e8e15b')
