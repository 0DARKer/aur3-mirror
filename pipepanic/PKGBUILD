# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Michal Karas <largon@largon.net>

pkgname=pipepanic
pkgver=0.1.3
pkgrel=3
pkgdesc="Pipe connecting game using libSDL."
arch=('i686' 'x86_64')
depends=('sdl')
url="http://www.users.waitrose.com/~thunor/pipepanic/"
license=('GPL2')
source=("http://www.users.waitrose.com/~thunor/pipepanic/dload/$pkgname-$pkgver-source.tar.gz"
        'pipepanic')
sha256sums=('4b02249c92228b03f4cc3c1d999cacf3fe52c16df53c6bf76fc6c1e2caa74318'
            '94fec3e383999480604aa1723f32876c1b8ea2f3c77425448d9daf2ae3005399')

build() {
  cd "$srcdir/$pkgname-$pkgver-source/"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver-source"

  install -dm755 "$pkgdir/usr/share/games/pipepanic"
  install -m644 -t "$pkgdir/usr/share/games/pipepanic/" {*.png,*.bmp,*.xcf}
  install -m755 pipepanic "$pkgdir/usr/share/games/pipepanic/pipepanic"
  install -Dm755 ../pipepanic "$pkgdir/usr/bin/pipepanic"
}

