#Maintainer: Christian Stankowic <info at stankowic hypen development dot net>
#Contributor: Gary Wright <wriggary at gmail dot com>
pkgname=monkey
pkgver=1.2.2
pkgrel=1
pkgdesc="A very small and fast open source web server for Linux"
arch=('i686' 'x86_64')
url="http://www.monkey-project.com/"
license=('GPL2')
depends=('gcc-libs')
optdepends=('php')
source=("http://www.monkey-project.com/releases/1.2/$pkgname-$pkgver.tar.gz"
    monkey
    monkey.service)
install=monkey.install
md5sums=('0eae7644dd5060b0e9c25874f9f1dccc'
	 '1527d1fbddddcfd69ad328639dcd0eed'
	 'ae1f2da03aa2a515a3bed0fb16ab7130')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --bindir=/usr/bin --sysconfdir=/etc/$pkgname --default-user=http --default-port=80 \
  --datadir=/srv/http --logdir=/var/log/$pkgname \
  --plugdir=/usr/lib/$pkgname
  make 
}

package() {
  cd $srcdir/$pkgname-$pkgver

  #msg2 "Fix typo in Makefile"
  #sed -i -e "s/src\/libmonkey.so.1.2/src\/libmonkey.so/" $srcdir/$pkgname-$pkgver/Makefile

  make DESTDIR=$pkgdir install

  msg2 "Change default pid file location from /var/log to /var/run/monkey"
  sed -i -e "s/\/var\/log\/monkey\/monkey.pid/\/var\/run\/monkey.pid/" $pkgdir/etc/monkey/monkey.conf

  install -Dm 755 "$srcdir/monkey" "$pkgdir/etc/rc.d/monkey"
  install -Dm 644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}
