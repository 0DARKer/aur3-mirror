arch='any'
pkgname='python-fusepy-git'
pkgver=2.0.2.1.g27d76e4
pkgrel=1

pkgdesc='Simple ctypes bindings for FUSE'
license=('custom:ISCL')
url='https://github.com/terencehonles/fusepy'

source=('git://github.com/terencehonles/fusepy.git')
md5sums=('SKIP')

depends=('fuse>=2.6' 'python')
makedepends=('python-distribute')
conflicts=('fusepy')
replaces=('fusepy')

pkgver() {
	cd "${srcdir}/fusepy"
	git describe --always | sed 's:^v::; s:-:.:g'
}

package() {
	cd "${srcdir}/fusepy"
	python setup.py install --root="$pkgdir/" --optimize=1

	sed -n '/Copyright/,/ THIS SOFTWARE[.]/p' fuse.py > LICENSE
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}