# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Unknown
# Contributor: Gergely Imreh <imrehg@gmail.com>>
# Contributor: twa022 <twa022 at gmail dot com>

pkgbase=python-fusepy-git
pkgname=('python-fusepy-git' 'python2-fusepy-git')
pkgver=2.0.2.1.g27d76e4
pkgrel=1
pkgdesc='Simple ctypes bindings for FUSE'
url='https://github.com/terencehonles/fusepy'
license=('custom:ISCL')
arch=('any')
makedepends=('python-distribute' 'python2-distribute' 'fuse')
source=("$pkgbase::git://github.com/terencehonles/fusepy.git")
md5sums=('SKIP')


pkgver() {
  cd "$pkgbase"
  git describe --always | sed 's:^v::; s:-:.:g'
}

prepare() {
  cd $pkgbase
  sed -n '/Copyright/,/ THIS SOFTWARE[.]/p' fuse.py > LICENSE
}

package_python-fusepy-git() {
  depends=('python' 'fuse')

  cd "$pkgbase"

  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-fusepy-git() {
  depends=('python2' 'fuse')
  conflicts=('fusepy')
  provides=('fusepy')

  cd "$pkgbase"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
