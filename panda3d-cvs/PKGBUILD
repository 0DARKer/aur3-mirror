# Maintainer: Hubert Grzeskowiak <arch at nemesis13 de>
pkgname=panda3d-cvs
pkgver=20110207
pkgrel=1
pkgdesc="A 3D game engine with Python bindings. SDK package. CVS version"
arch=('x86_64' 'i686')
url="http://www.panda3d.org"
license=('BSD')
provides=('panda3d')
conflicts=('panda3d')
_cvsroot=":pserver:anonymous@panda3d.cvs.sourceforge.net:/cvsroot/panda3d"
_cvsmod="panda3d"
makedepends=('cvs' 'python2')
depends=('python2' 'xorg-server' 'freetype2' 'openssl' 'libpng' 'libjpeg' 'ffmpeg' 'nvidia-cg-toolkit' 'ode' 'openal' 'fmodex' 'libsquish' 'gtk2')

# multithreading tends to be buggy with snapshots
#_threads=2

#-----------------------------------------------------------------------------
#
# You can safely remove some of the above dependencies if you're sure you won't
# need the features.
#
# WARNING: All make-time dependencies are also expected to be installed
#          afterwards at run-time. This is why it's useless to post-install
#          dependencies and why you can break your panda installation by
#          uninstalling or updating those afterwards.
#          
#-----------------------------------------------------------------------------
# DEPENDENCY: FEATURE
#
# python: needed for building script and python bindings
# xorg-server: basic display stuff. required
# openssl: required
# freetype2: font rendering
# libpng, libjpeg, libtiff: image libs
# libsquish: DXT file support
# ffmpeg: general audio and video
# nvidia-cg-toolkit: shader system
# ode: Open Dynamics Engine - physics
# openal: OpenAL audio
# fmodex: FMod audio
# gtk: PStats analysis and debugging tool
#-----------------------------------------------------------------------------


build() {
  cd $srcdir
  if [ -d $_cvsmod/CVS ]; then
    msg "Found a dir, updating"
    cd $_cvsmod
    cvs -q up -AdP || return 1
  else
    msg "Checking out."
    cvs -z3 -d $_cvsroot co -f $_cvsmod || return 1
    cd $_cvsmod
  fi
  msg "CVS checkout done (or server timeout)"
  
  msg "Starting makepanda"
  # makepanda/makepanda.py --everything --threads $_threads || return 1
  python2 makepanda/makepanda.py --everything || return 1
  msg "Success! Creating file structure and installing stuff."
  python2 makepanda/installpanda.py --prefix=/usr --destdir=$pkgdir || return 1
  rm $pkgdir/usr/bin/bin2c
  install -D -m644 $srcdir/$_cvsmod/doc/LICENSE $pkgdir/usr/share/licenses/$_cvsmod/LICENSE
}
