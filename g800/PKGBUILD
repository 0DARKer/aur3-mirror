# Maintainer: kusakata <shohei atmark kusakata period com>

pkgname=g800
pkgver=0.9.9
pkgrel=1
pkgdesc="SHARP PC-G850/G815/E200 Emulator"
arch=('i686' 'x86_64')
url="http://www16.ocn.ne.jp/~ver0/pc/index.html"
license=('BSD')
depends=('sdl')
source=("http://www16.ocn.ne.jp/~ver0/pc/g800src.${pkgver}.tgz")

build() {
	cd "${srcdir}/g800"
	make
}

package() {
	cd "${srcdir}/g800"
	install -Dm755 g800 "${pkgdir}/usr/bin/${pkgname}"
	
	cd "${srcdir}/g800/doc"
	sed -n '/Copyright (c)/,/す。/p' manual.txt > LICENSE
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 manual.txt "${pkgdir}/usr/share/doc/${pkgname}/manual.txt"
}

md5sums=('545e8639a14fdcb0360196ddbeafcb08')
