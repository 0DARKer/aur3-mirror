# Maintainer: Jesse Jaara <gmail.com: jesse.jaara>

pkgname=libass-git
pkgver=0.10.2.56.g5566069
pkgrel=1
pkgdesc="A portable library for SSA/ASS subtitles rendering"
arch=('i686' 'x86_64')
url="http://code.google.com/p/libass/"
license=('BSD')
depends=('enca' 'harfbuzz' 'fribidi' 'freetype2')
makedepends=('git' 'yasm')
provides=('libass=0.10.1')
conflicts=('libass')
options=(!libtool)
source=('libass::git://github.com/libass/libass.git')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/libass"

  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${srcdir}/libass"

  ./autogen.sh --prefix=/usr
  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/libass"

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
