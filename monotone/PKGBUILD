pkgname=monotone
pkgver=1.0
pkgrel=7
pkgdesc="A modern distributed version control system, like hg, darcs, or git"
arch=('i686' 'x86_64')
url="http://www.monotone.ca"
license=('GPL')
depends=('pcre' 'lua51' 'botan' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost')
install=monotone.install
source=("http://www.monotone.ca/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        '00-db-extension-bash-completion.diff'
        '01-format-security.diff'
        '02-file_handle.diff'
        '03-url_escaping.diff'
        '04-botan-1.10-adaption.diff'
        '10-mtn-ignore-syntax-error-test.diff'
        'boost-153.patch')
md5sums=('6a0b5d8560f08d76a950172f9ed0feff'
         'a8ad00b3e703c43a59f40c1d04e5cb2b'
         '06dc088cb7fdaec4e478628d3d6218ba'
         'cd835b5b9815d32e599cb603d48e8433'
         'd6b60f3b0adecc158dc942045d04bcf6'
         '5ae90fe0397f1c3f6556e998d55c2150'
         '69f6e48a6c7872ac3aec826a22dea2f2'
         'e3bedde3f1cf0d34dd823f3fca052c0f')

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -uNp0 -i "${srcdir}/00-db-extension-bash-completion.diff"
  patch -uNp0 -i "${srcdir}/01-format-security.diff"
  patch -uNp0 -i "${srcdir}/02-file_handle.diff"
  patch -uNp0 -i "${srcdir}/03-url_escaping.diff"
  patch -uNp0 -i "${srcdir}/04-botan-1.10-adaption.diff"
  patch -uNp0 -i "${srcdir}/10-mtn-ignore-syntax-error-test.diff"
  patch -uNp0 -i "${srcdir}/boost-153.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  DISABLE_NETWORK_TESTS=1 \
    lua_LIBS="-llua5.1 -lm" \
    lua_CFLAGS="-I/usr/include/lua5.1" \
    ./configure --prefix=/usr --sysconfdir=/etc --disable-dependency-tracking --disable-nls
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 contrib/monotone.zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_mtn"
}

# vim:set ts=2 sw=2 et:
