# $Id: PKGBUILD 119532 2011-04-11 21:19:22Z angvp $
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Dan McGee <dpmcgee@gmail.com>
pkgname=monotone
pkgver=1.0
_botan_pkgver=1.8.12
pkgrel=2
pkgdesc="A modern distributed version control system, like hg, darcs, or git"
arch=('i686' 'x86_64')
url="http://www.monotone.ca"
license=('GPL')
depends=('pcre' 'lua' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost')
install=${pkgname}.install
source=(http://www.monotone.ca/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        http://files.randombit.net/botan/Botan-${_botan_pkgver}.tgz
        rcs_file.cc.patch)

_build_botan() {
  cd ${srcdir}/Botan-${_botan_pkgver}
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' configure.py
  ./configure.py
  make
}

build() {
  _build_botan
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 <${srcdir}/rcs_file.cc.patch
  botan_LIBS="${srcdir}/Botan-${_botan_pkgver}/libbotan.a -lrt" botan_CFLAGS="-I${srcdir}/Botan-${_botan_pkgver}/build/include" ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  MAKEFLAGS="-j1" make DESTDIR="${pkgdir}" install
  install -Dm644 contrib/monotone.zsh_completion \
    "${pkgdir}/usr/share/zsh/site-functions/monotone"
}

md5sums=('6a0b5d8560f08d76a950172f9ed0feff'
         '3602af0851e542c58a736d25f45fce45'
         'b85f9bf6001dddd09cf218d3af061a6e')
sha1sums=('aac556bb26d92910b74b65450a0be6c5045e2052'
          'b74072995641ad0ed9b55f0710dd17edcf46ad85'
          '66f0bf0f62dd30bfab3b9754efe99c0d063f1bc4')
sha256sums=('5c530bc4652b2c08b5291659f0c130618a14780f075f981e947952dcaefc31dc'
            '23bf62d297bfa37e9b23b73ef1f8ddd809c0a0ac0b84bc2aa776667e021ebe40'
            'cfe78dd325a518c6e2a42efa1d1d0a0fad909e05eb5027305b9b87135b36969e')
sha384sums=('6265e57f52b99b14dd4aa8e30899a1914d967386a12196764f02a5e8e7242ff9a4c46ebb3aff7d1101e7f1ba76431c08'
            '8da8c311885a85a08dc57a8621810bff68de52695af9c3444c88397fa6d1bdccd9a305096774efa4a08b9dbc2018d72c'
            '25468473cdf6b4d16b4da323857fe4235814b29c10571e890645530e6f08963d89edbee9f29539bbf321d3c2d632d0ac')
sha512sums=('85f5a015ee3f7a924e16b7f20974cadd18bd6aaf2815eca0da42689373fc654c661051ce6981ec473f9eac7f9796cfed3431f0cfe16d1cbe0b0b625302ef3e77'
            '1119cfe93c7edc389c27e624f03de66ec5aed4f7bf233cdbdb12e3b2aa255e68060e683bdce511a9ab9c24d7944ab7bdcddf0b24b45fe70a5d47e7f1a9e7aaeb'
            'e9517f7a4ca1ad3a21e1568af21367284e962f195c9249a3de123eb06fe62b2dcc606016950bca35e3382ae36db734e6ff8cd99a93e08c13475a774090360e7d')
