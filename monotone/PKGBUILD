# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Dan McGee <dpmcgee@gmail.com>

pkgname=monotone
pkgver=1.0
pkgrel=3
pkgdesc="A modern distributed version control system, like hg, darcs, or git"
arch=('i686' 'x86_64')
url="http://www.monotone.ca"
license=('GPL')
depends=('pcre' 'lua51' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost' 'botan')
install=monotone.install
source=("http://www.monotone.ca/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        '00-db-extension-bash-completion.diff'
        '01-format-security.diff'
        '02-file_handle.diff'
        '03-url_escaping.diff'
        '04-botan-1.10-adaption.diff'
        '10-mtn-ignore-syntax-error-test.diff')

sha256sums=('5c530bc4652b2c08b5291659f0c130618a14780f075f981e947952dcaefc31dc'
            'ad312960e5e87a85fe0def92e17bdd2230bc7601dc542e5c93b9217c9f10a380'
            'cbb666ff2444150a9a346f8ccd09b715e877135c177501be8de0b8399f71e44e'
            'a6c2a391d4f0c336b8fbb5a68bc88309b29c300f6eacebfab65ba805b8fd3f9c'
            '28703787def2d6514d8ea8fd542e7c29b2bb520ec3cf160ba93e9f3487e9d4c6'
            '17e12c1b7a9eba83b12b725175a0fcb56e0ce8b04d0edacc68422f00d6be58d8'
            'df78fd0196dad56da174bcb3d9df8a169028406f52ef76b613f190637a4012fb')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    export DISABLE_NETWORK_TESTS=1

    patch -uNp0 -i "${srcdir}/00-db-extension-bash-completion.diff"
    patch -uNp0 -i "${srcdir}/01-format-security.diff"
    patch -uNp0 -i "${srcdir}/02-file_handle.diff"
    patch -uNp0 -i "${srcdir}/03-url_escaping.diff"
    patch -uNp0 -i "${srcdir}/04-botan-1.10-adaption.diff"
    patch -uNp0 -i "${srcdir}/10-mtn-ignore-syntax-error-test.diff"

    lua_LIBS="-llua5.1 -lm" \
    lua_CFLAGS="-I/usr/include/lua5.1" \
    ./configure --prefix=/usr --sysconfdir=/etc --disable-nls
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" install
    install -Dm644 contrib/monotone.zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_mtn"
}
