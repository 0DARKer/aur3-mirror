# $Id: PKGBUILD 119532 2011-04-11 21:19:22Z angvp $
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Dan McGee <dpmcgee@gmail.com>
pkgname=monotone
pkgver=1.0
_botan_pkgver=1.8.12
pkgrel=3
pkgdesc="A modern distributed version control system, like hg, darcs, or git"
arch=('i686' 'x86_64')
url="http://www.monotone.ca"
license=('GPL')
depends=('pcre' 'lua51' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost')
install=${pkgname}.install
source=(http://www.monotone.ca/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        http://files.randombit.net/botan/Botan-${_botan_pkgver}.tgz
        rcs_file.cc.patch)

_build_botan() {
  cd "${srcdir}/Botan-${_botan_pkgver}"
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' configure.py
  ./configure.py
  make
}

build() {
  _build_botan
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 <${srcdir}/rcs_file.cc.patch
  export botan_LIBS="${srcdir}/Botan-${_botan_pkgver}/libbotan.a -lrt"
  export botan_CFLAGS="-I${srcdir}/Botan-${_botan_pkgver}/build/include"
  export lua_LIBS=$(pkg-config --libs lua5.1)
  export lua_CFLAGS=$(pkg-config --cflags lua5.1)
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  MAKEFLAGS="-j1" make DESTDIR="${pkgdir}" install
  install -Dm644 contrib/monotone.zsh_completion \
    "${pkgdir}/usr/share/zsh/site-functions/monotone"
}

sha1sums=('aac556bb26d92910b74b65450a0be6c5045e2052'
          'b74072995641ad0ed9b55f0710dd17edcf46ad85'
          '66f0bf0f62dd30bfab3b9754efe99c0d063f1bc4')
