pkgname=monotone
pkgver=1.1
pkgrel=2
pkgdesc="A modern distributed version control system, like hg, darcs, or git"
arch=('i686' 'x86_64')
url="http://www.monotone.ca"
license=('GPL')
depends=('pcre' 'lua51' 'botan-1.10' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost')
install=monotone.install
source=("http://www.monotone.ca/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('df3f40ca22120aa142ac9becba9e1bf7')

build() {
  cd "${pkgname}-${pkgver}"

  DISABLE_NETWORK_TESTS=1 \
    lua_LIBS="-llua5.1 -lm" \
    lua_CFLAGS="-I/usr/include/lua5.1" \
    botan_CFLAGS="-I/usr/include/botan-1.10" \
    botan_LIBS=-lbotan-1.10 \
    ./configure --prefix=/usr --sysconfdir=/etc --disable-dependency-tracking --disable-nls
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 contrib/monotone.zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_mtn"
}

# vim:set ts=2 sw=2 et:
