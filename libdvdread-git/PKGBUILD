# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libdvdread-git
pkgver=20131118.9626911
pkgrel=3
pkgdesc="Library to access DVD disks (GIT version)"
arch=('i686' 'x86_64')
url="http://dvdnav.mplayerhq.hu/"
license=('GPL2')
depends=('libdvdcss-git')
makedepends=('git')
provides=('libdvdread')
conflicts=('libdvdread')
source=("git://git.videolan.org/libdvdread.git")
md5sums=('SKIP')
_gitname="libdvdread"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
  #echo "$(git describe --always | tr - .)"
}

prepare() {
  cd "${_gitname}"

  sed -e 's|DVDREAD_LT_AGE=1|DVDREAD_LT_AGE=2|g' \
      -e 's|DVDREAD_LT_REVISION=2|DVDREAD_LT_REVISION=1|g' -i configure.ac

  autoreconf -i
}

build() {
  cd "${_gitname}"

  ./configure --prefix=/usr --enable-static=no --with-libdvdcss
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
