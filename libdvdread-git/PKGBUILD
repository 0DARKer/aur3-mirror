# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libdvdread-git
pkgver=4.9.9.2.gd65889e
pkgrel=1
pkgdesc="Library to access DVD disks. (GIT version)"
arch=('i686' 'x86_64')
url="http://dvdnav.mplayerhq.hu"
license=('GPL2')
depends=('libdvdcss-git')
makedepends=('git')
provides=('libdvdread')
conflicts=('libdvdread')
source=("git://git.videolan.org/libdvdread.git")
md5sums=('SKIP')
_gitname="libdvdread"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  autoreconf -i
  ./configure --prefix=/usr --enable-static=no --with-libdvdcss
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
