# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libdvdread-git
pkgver=20130318.949b77e
pkgrel=1
pkgdesc="Library to access DVD disks (GIT version)"
arch=('i686' 'x86_64')
url="http://dvdnav.mplayerhq.hu/"
license=('GPL2')
depends=('libdvdcss' 'bash')
makedepends=('git')
provides=('libdvdread')
conflicts=('libdvdread')
options=('!libtool')
source=("git://git.videolan.org/libdvdread.git")
md5sums=('SKIP')
_gitname="libdvdread"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

build() {
  cd "${srcdir}/${_gitname}"

  autoreconf -if
  ./configure --prefix=/usr --enable-static=no --enable-libdvdcss
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}
