# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: JIN Xiao-Yong <jinxiaoyong@gmail.com>
# Contributor: Andre Fettouhi <A.Fettouhi@gmail.com>

_pkgbasename=freetype2-infinality
pkgname=lib32-$_pkgbasename
pkgver=2.4.8
pkgrel=4
_pkgver=20111125
_pkgrel=1
pkgdesc="TrueType font rendering library (32-bit) with infinality patch"
arch=(x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('lib32-zlib' 'lib32-bzip2' $_pkgbasename)
makedepends=(gcc-multilib)
conflicts=('lib32-freetype2')
provides=("lib32-freetype2=$pkgver")
options=('!libtool')
source=(
	"http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2"
	"http://www.infinality.net/fedora/linux/zips/freetype-infinality-${pkgver}-${_pkgver}_${_pkgrel}.tar.bz2"
	"freetype-2.2.1-enable-valid.patch"
)
noextract=($(basename $source[1]))
md5sums=(
	'dbf2caca1d3afd410a29217a9809d397'
	'f7fbff87d1ca9b3661650393a80f5d8e'
	'214119610444c9b02766ccee5e220680'
)

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	rm -rf "${srcdir}/freetype-${pkgver}-build"
	cp -a "${srcdir}/freetype-${pkgver}" "${srcdir}/freetype-${pkgver}-build"
	cd "${srcdir}/freetype-${pkgver}-build"
	cat "$srcdir/freetype-"*.patch | patch -Np1
	./configure --prefix=/usr --libdir=/usr/lib32
	make
}

package() {
	cd "${srcdir}/freetype-${pkgver}-build"
	make DESTDIR="${pkgdir}" install
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}
