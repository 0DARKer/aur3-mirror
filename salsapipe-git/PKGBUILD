# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=salsapipe
pkgname=${_pkgname}-git
pkgver=20130901.r22.9ce1fd2
pkgrel=2
pkgdesc="Encrypted network tunneling using salsa20 from libnettle and GPG from libgpgme"
url="https://github.com/0xcaca0/${_pkgname}"
license=('GPL3')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
groups=('archassault' 'archassault-networking')
depends=('gpgme' 'nettle')
makedepends=('git')

source=("git://github.com/0xcaca0/${_pkgname}.git#branch=master"
        "salsamsg.1")
sha512sums=('SKIP'
            '41a3a24380dac6a37667a9ed13572dcca61c77fe5ba0228278c7fc12a87ff7e066e047efd51e58ae6a864a82d77cf6b017b3497a05675af3dc80a90d9b873c4f')

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd $_pkgname
    make
}

package() {
    install -Dm755 ${_pkgname}/salsamsg "${pkgdir}/usr/bin/salsamsg"
    install -Dm644 salsamsg.1 "${pkgdir}/usr/share/man/man1/salsamsg.1"
}
