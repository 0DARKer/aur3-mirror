# Maintainer: Techlive Zheng <techlivezheng@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gedit-code-assistance-git
pkgver=190.a2cf772
pkgrel=1
pkgdesc="gedit code assistance plugin"
arch=('i686' 'x86_64')
url="http://git.gnome.org/browse/gedit-code-assistance"
license=('GPL')
depends=('gedit' 'clang' 'glib2' 'libgee')
makedepends=('git' 'vala')
provides=('gedit-code-assistance')
conflicts=('gedit-code-assistance')
replaces=('gedit-code-assistance')
source=('git+git://git.gnome.org/gedit-code-assistance')
sha512sums=('SKIP')

pkgver() {
  cd gedit-code-assistance
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "${srcdir}/gedit-code-assistance"
  sed -i 's/AM_PROG_CC_STDC/AC_PROG_CC/g' configure.ac
}

build() {
  cd "${srcdir}/gedit-code-assistance"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/gedit-code-assistance"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
