#Maintainer: Loren Copeland <thisisquitealongname at gm--l> 

pkgname=jack2-git
_realname=${pkgname%-git}
pkgver=20120920
pkgrel=1
pkgdesc="Git version of the popular multiprocessor-aware low-latency audio server."
arch=('i686' 'x86_64')
url="http://www.grame.fr/~letz/jackdmp.html"
license=('GPL2')
depends=('python2' 'libsamplerate' 'glibc') 
makedepends=('git')
optdepends=('celt-0.7' 'dbus-core' 'libffado' 'libfreebob' 'python2')
provides=('jack2' 'jack' 'jack-audio-connection-kit')
conflicts=('jack2' 'jackmp' 'tschack' 'tschack-git' 'jack' 'jackd' 'jack-audio-connection-kit')
replaces=('jack2-svn')

_gitname=jack2-git
_gitroot=https://github.com/jackaudio/jack2

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."

  if [ -d ${srcdir}/$_gitname ] ; then
    cd $_gitname && git pull origin master
    msg "Local repository updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout."
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

   LDFLAGS="$LDFLAGS,--no-as-needed -lm" python2 ./waf configure \
    --prefix=/usr \
    --alsa

  python2 ./waf build
}

package() {
  cd "${srcdir}/${_gitname}-build"

  python2 ./waf --destdir="${pkgdir}" install
}
