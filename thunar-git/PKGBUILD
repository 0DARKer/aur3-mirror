# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from thunar package)
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

_pkgname=thunar
pkgname=$_pkgname-git
pkgver=20121111
pkgrel=1
pkgdesc="New modern file manager for Xfce - migration to GIO."
arch=(i686 x86_64)
license=(GPL2)
url=http://$_pkgname.xfce.org
groups=(xfce4-git)
depends=(dbus desktop-file-utils exo-git gtk2 hicolor-icon-theme libexif libpng)
makedepends=(git xfce4-dev-tools)
provides=($_pkgname)
conflicts=($_pkgname{,-devel})
options=(!libtool)
install=$_pkgname.install
source=(thunar-1.4.0-show-loop-devices-hack.patch)
sha256sums=('185c44db9bdf578d22cb69b81dce7fcc3e2ee6031b1547de6453ff0752dbc97e')
sha512sums=('d2cbfe3145167a573330443517588e9eab5127293c63c073ff7e97768729acc633f73316022f6a2d0811ae693b81ecf7949c3fb5f3657513c99bfc1455b332d7')

_gitroot=git://git.xfce.org/xfce/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    # Fix: Loop devices not appearing in Thunar side pane
    # https://bugs.archlinux.org/task/29894
    # https://bugzilla.xfce.org/show_bug.cgi?id=8947
    patch -Np1 -i ../thunar-1.4.0-show-loop-devices-hack.patch

    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-static \
        --enable-gio-unix \
        --enable-dbus \
        --enable-startup-notification \
        --enable-gudev \
        --enable-notifications \
        --enable-exif \
        --enable-pcre \
        --enable-gtk-doc \
        --disable-debug
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
    sed -i ':x-directory/gnome-default-handler;:d' "$pkgdir"/usr/share/applications/Thunar-folder-handler.desktop
}
