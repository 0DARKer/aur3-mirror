# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

_pkgbasename=db
pkgname=lib32-$_pkgbasename
pkgver=5.3.15
pkgrel=1
pkgdesc="The Berkeley DB embedded database system (32-bit)"
arch=('x86_64')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
depends=('lib32-gcc-libs' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://download.oracle.com/berkeley-db/db-$pkgver.tar.gz)
sha1sums=('7683b632a01ff3543c379a120809a942ec457738')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}/build_unix"
  ../dist/configure --prefix=/usr --enable-compat185 \
    --enable-shared --enable-static --enable-cxx --enable-dbm \
    --libdir=/usr/lib32
  make LIBSO_LIBS=-lpthread
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}/build_unix"
  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}"/usr/{docs,include,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s ${_pkgbasename} "$pkgdir/usr/share/licenses/$pkgname"
}
