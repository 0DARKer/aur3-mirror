# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Diego <cdprincipe@gmail.com>

_pkgname=numix-themes
pkgname=numix-themes-archblue
pkgver=2.2.2
pkgrel=1
pkgdesc="A flat and light theme with a modern look with Arch Linux blue accents (GNOME, Openbox, Unity, Xfce)"
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('gtk-engine-murrine')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/shimmerproject/Numix/archive/v${pkgver}.tar.gz")
sha256sums=('b31e6fededfe21102debf5396eab54ce9de222d6977647ffe33b94c5fca854f7')

prepare() {
    cd Numix-${pkgver}

    # Replace the default Numix Pink/Red with Arch Linux Blue.
    for FILE in gtk-2.0/gtkrc \
            gtk-3.0/gtk-dark.css \
            gtk-3.0/gtk-widgets-assets.css \
            gtk-3.0/gtk-widgets.css \
            gtk-3.0/gtk.css \
            gtk-3.0/settings.ini \
            gtk-3.0/apps/gnome-applications.css \
            gtk-3.0/apps/granite-widgets.css \
            gtk-3.0/apps/lightdm-gtk-greeter.css \
            gtk-3.0/apps/nemo.css \
            gtk-3.0/apps/synaptic.css \
            gtk-3.0/apps/unity.css \
            gtk-3.0/apps/xfce.css \
            metacity-1/metacity-theme-2.xml \
            metacity-1/metacity-theme-3.xml \
            openbox-3/themerc \
            xfce-notify-4.0/gtkrc \
            xfwm4/themerc
    do
        sed -i 's/#fc6f5d/#1793d1/' "${FILE}"
        sed -i 's/#d64937/#1793d1/' "${FILE}"
    done
    sed -i 's/=Numix/=Numix-ArchBlue/' index.theme
}

package() {
    cd Numix-${pkgver}

    install -dm 755 "$pkgdir"/usr/share/themes/Numix-ArchBlue
    rm -rf .git .gitignore CREDITS LICENSE README.md
    cp -dr --no-preserve='ownership' * "$pkgdir"/usr/share/themes/Numix-ArchBlue
}
