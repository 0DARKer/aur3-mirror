# Maintainer: Antonio Rojas < nqn1976 @ gmail.com >

pkgname=milou-frameworks-git
pkgver=r302.34b1c41
pkgrel=1
pkgdesc="A dedicated search application built on top of Baloo"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/base/milou/'
license=('LGPL')
depends=('baloo-frameworks-git' 'kdelibs4support-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git' 'kf5umbrella-git')
source=('git://anongit.kde.org/milou#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd milou
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../milou \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
