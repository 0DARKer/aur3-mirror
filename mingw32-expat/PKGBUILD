# Contributor: Andr√© Klitzing <aklitzing () online () de>
_pkgname=expat
_target=i486-mingw32
pkgname=mingw32-expat
pkgver=2.0.1
pkgrel=6
pkgdesc="The Expat XML Parser (mingw32)"
arch=(any)
url="http://expat.sourceforge.net/"
options=(!strip !buildflags) # no !libtool because mingw32-cairo will complain and fail
license=('MIT')
depends=(mingw32-runtime)
makedepends=('mingw32-gcc' 'mingw32-binutils')
source=("http://switch.dl.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz")
md5sums=('ee8b492592568805593f81f8cdf2a04c')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  unset LDFLAGS
  mkdir build && cd build
  ../configure \
  --prefix=/usr/${_target} \
  --host=${_target}
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/build"
  make DESTDIR=$pkgdir install
  i486-mingw32-strip "$pkgdir/usr/i486-mingw32/bin/xmlwf"
  i486-mingw32-strip -x $pkgdir/usr/i486-mingw32/bin/*.dll
  i486-mingw32-strip -g $pkgdir/usr/i486-mingw32/lib/*.a
  rm -rf "$pkgdir/usr/i486-mingw32/man"
}

