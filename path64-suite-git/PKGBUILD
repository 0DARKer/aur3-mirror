# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=path64-suite-git
pkgver=20111017
pkgrel=1
pkgdesc="Open source version of the EKOPath 4 Compiler Suite"
arch=('x86_64')
url="https://github.com/pathscale/path64-suite"
#url="http://www.pathscale.com/ekopath-compiler-suite"
license=('GPL')
depends=('gcc-libs')
makedepends=('git' 'cmake')
provides=('path64-git')
conflicts=('path64-git')
options=(!buildflags)
source=()
md5sums=()

_gitroot="git://github.com/pathscale/path64-suite.git"
_gitname="path64-suite"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin

    # update the modules
    ./update
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
    # clone all modules
    cd $_gitname
    ./initial-clone
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  # build debug
  [ -e "$srcdir/$_gitname-build-debug" ] || mkdir "$srcdir/$_gitname-build-debug"
  cd "$srcdir/$_gitname-build-debug"
  cmake -DCMAKE_BUILD_TYPE=Debug ../$_gitname
  make

  # build release
  export PATH=$srcdir/$_gitname-build-debug/Xcompiler/bin:$PATH
  [ -e "$srcdir/$_gitname-build" ] || mkdir "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_C_COMPILER=pathcc -DCMAKE_CXX_COMPILER=pathCC -DCMAKE_INSTALL_PREFIX=/opt/path64 ../$_gitname
  make
}

# test takes very long time, because it bootstraps entire path64 compiler
#check() {
#  export PATH="$srcdir/$_gitname/build/Xcompiler/bin:$PATH"
#  cd "$srcdir/$_gitname/buildrelease"
#  make -k test
#}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
  sed -i 's|#!/usr/bin/python$|#!/usr/bin/python2|' "$pkgdir/opt/path64/bin/explain"
} 
