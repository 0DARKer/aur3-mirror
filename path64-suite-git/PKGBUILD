# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=path64-suite-git
pkgver=20110713
pkgrel=1
pkgdesc="Open source version of the EKOPath 4 Compiler Suite"
arch=('x86_64')
url="https://github.com/pathscale/path64-suite"
#url="http://www.pathscale.com/ekopath-compiler-suite"
license=('GPL')
depends=('gcc-libs')
makedepends=('git' 'cmake')
provides=('path64-git')
conflicts=('path64-git')
options=(!buildflags)
source=()
md5sums=()

_gitroot="git://github.com/pathscale/path64-suite.git"
_gitname="path64-suite"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin

    # update modules
    cd compiler
    _modules=(compiler compiler-rt assembler libcxxrt libdwarf-bsd libunwind pathdb stdcxx)
    for _module in "${_modules[@]}"; do
        cd $_module
        git pull origin
        cd ../
    done

    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname

    mkdir $_gitname/compiler
    cd $_gitname/compiler

    # clone all modules
    git clone git://github.com/path64/compiler.git
    git clone git://github.com/pathscale/compiler-rt.git
    git clone git://github.com/path64/assembler.git
    git clone git://github.com/pathscale/libcxxrt.git
    git clone git://github.com/pathscale/libdwarf-bsd.git
    git clone git://github.com/pathscale/libunwind.git
    git clone git://github.com/path64/debugger.git pathdb
    git clone git://github.com/pathscale/stdcxx.git
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  # build debug
  cd "$srcdir/$_gitname"
  ./configure
  cd build
  make
  
  # build release
  msg "building release"
  export PATH="$srcdir/$_gitname/build/Xcompiler/bin:$PATH"
  cd "$srcdir/$_gitname"
  ./configure-release --prefix=/opt/path64
  cd buildrelease/
  make
}

# test takes very long time, because it bootstraps entire path64 compiler
#check() {
#  export PATH="$srcdir/$_gitname/build/Xcompiler/bin:$PATH"
#  cd "$srcdir/$_gitname/buildrelease"
#  make -k test
#}

package() {
  export PATH="$srcdir/$_gitname/build/Xcompiler/bin:$PATH"
  cd "$srcdir/$_gitname/buildrelease"
  make DESTDIR="$pkgdir/" install
  sed -i 's|#!/usr/bin/python$|#!/usr/bin/python2|' "$pkgdir/opt/path64/bin/explain"
} 
