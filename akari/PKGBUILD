# Maintainer: Jamie Nguyen <jamie AT tomoyolinux.co.uk>

pkgname=akari
pkgver=1.0.10
_timestamp=20110215
_basever=2.6.37
_kernpkgver=${_basever}.1-1
_kernver=${_basever}-ARCH
_ccstoolsver=1.8.0.20110214
pkgrel=3
pkgdesc='TOMOYO Linux 1.8.x Linux Kernel Module'
arch=('i686' 'x86_64')
url='http://akari.sourceforge.jp/'
license=('GPL')
depends=('kernel26>=2.6.37' 'kernel26<2.6.38')
optdepends=("ccs-tools>=${_ccstoolsver}")
install=akari.install

if [ "$CARCH" = 'i686' ]; then
	source=("ftp://ftp.archlinux.org/core/os/i686/kernel26-headers-${_kernpkgver}-i686.pkg.tar.xz"
	        "http://sourceforge.jp/frs/redir.php?f=/akari/49272/${pkgname}-${pkgver}-${_timestamp}.tar.gz")
    sha256sums=('ec14820b4232ee36331bdc8b279d87cc744c3746e3b3ca41824654c9d1075fbc'
                '833751c6e28bf2906a514e3a0ebca3f7f7b0feb58efe5ae72b022de6c03d605b')
	noextract=("${pkgname}-${pkgver}-${_timestamp}.tar.gz")
elif [ "$CARCH" = 'x86_64' ]; then
	source=("ftp://ftp.archlinux.org/core/os/x86_64/kernel26-headers-${_kernpkgver}-x86_64.pkg.tar.xz"
	        "http://sourceforge.jp/frs/redir.php?f=/akari/49272/${pkgname}-${pkgver}-${_timestamp}.tar.gz")
    sha256sums=('6fe3894d72b2e3d4cdaef0a9464081114824e95cd6581e79c41ccd6316cc991e'
                '833751c6e28bf2906a514e3a0ebca3f7f7b0feb58efe5ae72b022de6c03d605b')
	noextract=("${pkgname}-${pkgver}-${_timestamp}.tar.gz")
fi

build() {
  cd "${srcdir}/usr/src/linux-${_basever}-ARCH/"

  # extract akari into the kernel source
  tar -xf "${srcdir}/${pkgname}-${pkgver}-${_timestamp}.tar.gz"

  make SUBDIRS=akari SYSSRC=/lib/modules/${_kernver}/build modules || return 1
}

package() {
  cd "${srcdir}/usr/src/linux-${_basever}-ARCH/"

  install -D -m644 "${srcdir}/usr/src/linux-${_kernver}/akari/akari.ko" \
	  "${pkgdir}/lib/modules/${_kernver}/kernel/extra/akari.ko"

  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "${startdir}/akari.install"
}
