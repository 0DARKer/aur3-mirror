# Maintainer: Jamie Nguyen <jamie tomoyolinux co uk>

pkgname=akari
#pkgname=akari-ck
_extraver=-ARCH
#_extraver=-ck
_kernelname=kernel26
#_kernelname=kernel26-ck
pkgver=1.0.14
_timestamp=20110511
_basever=2.6.38
_kernver=${_basever}${_extraver}
_ccstoolsver=1.8.0.20110511
pkgrel=1
pkgdesc='TOMOYO Linux 1.8.x Module for kernel26'
arch=('i686' 'x86_64')
url='http://akari.sourceforge.jp/'
license=('GPL')
depends=("${_kernelname}>=2.6.38" "${_kernelname}<2.6.39")
makedepends=("${_kernelname}-headers>=2.6.38" "${_kernelname}-headers<2.6.39")
optdepends=("ccs-tools>=${_ccstoolsver}")
conflicts=("ccs-tools<${_ccstoolsver}")
install=akari.install
source=("http://sourceforge.jp/frs/redir.php?f=/akari/49272/akari-${pkgver}-${_timestamp}.tar.gz")
noextract=("akari-${pkgver}-${_timestamp}.tar.gz")

build() {
  msg "Copying headers to source directory..."
  cp -a "/usr/src/linux-${_kernver}/" "${srcdir}"

  cd "${srcdir}/linux-${_kernver}/"

  msg "Extracting akari into kernel headers..."
  tar -xf "${srcdir}/akari-${pkgver}-${_timestamp}.tar.gz"

  msg "Building akari module..."
  make SUBDIRS=akari SYSSRC=/lib/modules/${_kernver}/build modules || return 1
}

package() {
  install -D -m644 "${srcdir}/linux-${_kernver}/akari/akari.ko" \
      "${pkgdir}/lib/modules/${_kernver}/kernel/extra/akari.ko"

  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "${startdir}/akari.install"
}

sha256sums=('b7214abbd438ea295139076eeab372a01349a28e5bae0128b2fbb0cf9063af02')
