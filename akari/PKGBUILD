# Maintainer: Jamie Nguyen <jamie AT tomoyolinux.co.uk>

pkgname=akari
#pkgname=akari-ck
#pkgname=akari-pf
_extraver=-ARCH
#_extraver=-ck
#_extraver=-pf
_kernelname=kernel26
#_kernelname=kernel26-ck
#_kernelname=kernel26-pf
pkgver=1.0.10
_timestamp=20110215
_basever=2.6.37
_kernver=${_basever}${_extraver}
_ccstoolsver=1.8.0.20110214
pkgrel=11
pkgdesc='TOMOYO Linux 1.8.x Module for kernel26'
arch=('i686' 'x86_64')
url='http://akari.sourceforge.jp/'
license=('GPL')
depends=("${_kernelname}>=2.6.37" "${_kernelname}<2.6.38")
makedepends=("${_kernelname}-headers>=2.6.37" "${_kernelname}-headers<2.6.38")
optdepends=("ccs-tools>=${_ccstoolsver}")
conflicts=("ccs-tools<${_ccstoolsver}")
install=akari.install
source=("http://sourceforge.jp/frs/redir.php?f=/akari/49272/akari-${pkgver}-${_timestamp}.tar.gz")
noextract=("akari-${pkgver}-${_timestamp}.tar.gz")

build() {
  msg "Copying headers to source directory..."
  cp -a "/usr/src/linux-${_kernver}/" "${srcdir}"

  cd "${srcdir}/linux-${_kernver}/"

  msg "Extracting akari into kernel headers..."
  tar -xf "${srcdir}/akari-${pkgver}-${_timestamp}.tar.gz"

  msg "Building akari module..."
  make SUBDIRS=akari SYSSRC=/lib/modules/${_kernver}/build modules || return 1
}

package() {
  install -D -m644 "${srcdir}/linux-${_kernver}/akari/akari.ko" \
      "${pkgdir}/lib/modules/${_kernver}/kernel/extra/akari.ko"

  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "${startdir}/akari.install"
}

sha256sums=('833751c6e28bf2906a514e3a0ebca3f7f7b0feb58efe5ae72b022de6c03d605b')
