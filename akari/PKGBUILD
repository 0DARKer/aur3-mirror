# Maintainer: Jamie Nguyen <jamie tomoyolinux co uk>

pkgname=akari
pkgver=1.0.15.20110626
_pkgver=1.0.15-20110626
_kernver=2.6.39-ARCH
_ccstoolsver=1.8.2.20110626
pkgrel=1
pkgdesc='TOMOYO Linux 1.8.x Module for kernel26'
arch=('i686' 'x86_64')
url='http://akari.sourceforge.jp/'
license=('GPL')
depends=("kernel26>=2.6.39" "kernel26<3.0.0")
makedepends=("kernel26-headers>=2.6.39" "kernel26-headers<3.0.0")
optdepends=("ccs-tools>=${_ccstoolsver}")
conflicts=("ccs-tools<${_ccstoolsver}")
install=akari.install
source=("http://sourceforge.jp/frs/redir.php?f=/akari/49272/akari-${_pkgver}.tar.gz")
noextract=("akari-${_pkgver}.tar.gz")

build() {
  msg "Copying headers to source directory..."
  cp -a "/usr/src/linux-${_kernver}/" "${srcdir}"

  cd "${srcdir}/linux-${_kernver}/"

  msg "Extracting akari into kernel headers..."
  tar -xf "${srcdir}/akari-${_pkgver}.tar.gz"

  msg "Building akari module..."
  make SUBDIRS=akari SYSSRC=/lib/modules/${_kernver}/build modules || return 1
}

package() {
  install -D -m644 "${srcdir}/linux-${_kernver}/akari/akari.ko" \
    "${pkgdir}/lib/modules/${_kernver}/kernel/extra/akari.ko"

  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "${startdir}/akari.install"
}

sha256sums=('fec65d50951193479bf5f302e5b3be6a8b2d4f4f7d121b504d4d4373c0ec739d')

# vim: set ts=2 sw=2 et:
