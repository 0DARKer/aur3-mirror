# Maintainer: Jamie Nguyen <jamie tomoyolinux co uk>

pkgname=akari
_basever=1.0.21
_timestamp=20111025
pkgver=${_basever}.${_timestamp}
_kernver=3.0-ARCH
_ccstoolsver=1.8.3.20111025
pkgrel=1
pkgdesc='TOMOYO Linux 1.8.x Module for the Linux kernel'
arch=('i686' 'x86_64')
url='http://akari.sourceforge.jp/'
license=('GPL')
depends=("linux>=3.0" "linux<3.1")
makedepends=("linux-headers>=3.0" "linux-headers<3.1")
optdepends=("ccs-tools>=${_ccstoolsver}")
conflicts=("ccs-tools<${_ccstoolsver}")
install=akari.install
source=("http://sourceforge.jp/frs/redir.php?f=/akari/49272/akari-${_basever}-${_timestamp}.tar.gz")
noextract=("akari-${_basever}-${_timestamp}.tar.gz")

build() {
  msg "Copying headers to source directory..."
  cp -a "/usr/src/linux-${_kernver}/" "${srcdir}"

  cd "${srcdir}/linux-${_kernver}/"

  msg "Extracting akari into kernel headers..."
  tar -xf "${srcdir}/akari-${_basever}-${_timestamp}.tar.gz"

  msg "Building akari module..."
  make SUBDIRS=akari SYSSRC=/lib/modules/${_kernver}/build modules || return 1
}

package() {
  install -D -m644 "${srcdir}/linux-${_kernver}/akari/akari.ko" \
    "${pkgdir}/lib/modules/${_kernver}/kernel/extra/akari.ko"

  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "${startdir}/akari.install"
}

sha256sums=('e3f382104fc048c22c6bd47f7f9d277fe7cb0fdf79dc6c473f717613dadbddfa')
