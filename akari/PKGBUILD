# Maintainer: jorge_barroso <jorge.barroso.11@gmail.com>

#!/bin/bash
pkgname=akari
_basever=1.0.29
_timestamp=20130117
pkgver=${_basever}.${_timestamp}
_kernver=$(uname -r)
_ccstoolsver=1.8.3.20120805
pkgrel=1
pkgdesc='TOMOYO Linux 1.8.x Module for the Linux kernel'
arch=('any')
url='http://akari.sourceforge.jp/'
license=('GPL')
depends=("linux")
makedepends=("linux-headers")
optdepends=("ccs-tools>=${_ccstoolsver}")
conflicts=("ccs-tools<${_ccstoolsver}")
install=akari.install
source=("http://sourceforge.jp/frs/redir.php?f=/akari/49272/akari-${_basever}-${_timestamp}.tar.gz")
sha512sums=('d8dcead34b92b591aadc0d1fcc049c412c194d9306a4384342f6d28b52db669a4f79f96345f36c0f971bc9d3c550c367bd649118e8620e810edc4df13959d598')
noextract=("akari-${_basever}-${_timestamp}.tar.gz")

build() {
  msg "Copying headers to source directory..."
  cp -a "/usr/src/linux-${_kernver}/" "${srcdir}"

  cd "${srcdir}/linux-${_kernver}/"

  msg "Extracting akari into kernel headers..."
  tar -xf "${srcdir}/akari-${_basever}-${_timestamp}.tar.gz"

  msg "Building akari module..."
  make SUBDIRS=akari SYSSRC=/lib/modules/${_kernver}/build modules || return 1
}

package() {
  install -D -m644 "${srcdir}/linux-${_kernver}/akari/akari.ko" \
    "${pkgdir}/lib/modules/${_kernver}/kernel/extra/akari.ko"

  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "${startdir}/akari.install"
}
