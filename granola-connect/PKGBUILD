# Maintainer: Paul Cox <pauldcox@gmail.com>
# Contributor: Matthew Bauer

pkgname=granola-connect
pkgver=5.0.3
pkgrel=1
pkgdesc="MiserWare Granola daemon to upload energy stats"
arch=('i686' 'x86_64')
  if [ $CARCH = x86_64 ]; then
    _arch=$CARCH
  else
    _arch=i386
  fi
url="http://grano.la/"
license=('custom')
depends=('granola>=5.0.0' 'openssl>=1.0.0')
backup=('etc/granola-connect.conf')
options=(!strip)
install=$pkgname.install

source=("https://download.miserware.com/linux/tar/$_arch/$pkgname-$pkgver-$_arch.tar.gz"
        'granola-connect.confd'
        'granola-connect.rcd.sh')

[ $_arch = x86_64 ] && sha1sums=('b6ac9f71ed2f541c233af19c89085cd47ac1bcad')
[ $_arch = i386 ] && sha1sums=('9f55c59bad2b316d13ad7a47b7254c0b247c67a8')
sha1sums+=('ae55b014982a8085aa30ed8aaa8252a66c732012'
           'b7d14c3f906ff63f25a1028b0af60ee1fe0d9e7a')

package() {
  cd "$srcdir/$pkgname-$pkgver-$_arch"

  cp -r opt usr "$pkgdir"
  install -Dm600 etc/granola-connect.conf "$pkgdir/etc/granola-connect.conf"

  # rc.d files
  install -Dm644 "$srcdir/granola-connect.confd" "$pkgdir/etc/conf.d/granola-connect.conf"
  install -Dm755 "$srcdir/granola-connect.rcd.sh" "$pkgdir/etc/rc.d/granola-connect"

  # put license in proper directory
  rm -fr "$pkgdir/usr/share/doc"
  install -Dm644 usr/share/doc/$pkgname-$pkgver/LICENSE.txt \
          "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  # nasty, dirty hack to make openssl work like on Fedora because that is
  # what granola-connect looks for
  install -d "$pkgdir/usr/lib"
  ln -s libcrypto.so.1.0.0 "$pkgdir/usr/lib/libcrypto.so.10"
  ln -s libssl.so.1.0.0 "$pkgdir/usr/lib/libssl.so.10"
}

# vim:set ts=2 sw=2 et:
