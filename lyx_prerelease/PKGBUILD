# Maintainer: Sam Madhani <shemz -at- gmx -dot- com>

pkgname=lyx_prerelease
pkgver=2.1.0
pkgrel=1
_pkgrel=beta2
pkgdesc="A document processor with Tex/Latex backend."
arch=(i686 x86_64)
url='http://www.lyx.org'
license=('GPL')

depends=('qt4' 'texlive-core' 'python2' 'perl' 'hunspell' 'imagemagick' 'libmythes' 'boost' 'texlive-latex3')
optdepends=('texlive-latexextra: for more Latex packages' 'mythes-en: thesaurus data files')
makedepend=('unzip')
provides=('lyx')
conflicts=('lyx' 'lyx-svn')
replaces=('lyx' 'lyx-svn')

source=(ftp://ftp.lyx.org/pub/lyx/devel/lyx-2.1/lyx-${pkgver}${_pkgrel}/lyx-${pkgver}${_pkgrel}.tar.xz
	lyx.desktop)
md5sums=('c388664143e109d9a2318b3c5bfb655b'
	 'b2a093f9dbbdeaa3dc085113c0f187c2')

install=lyx.install

build() {
  cd ${srcdir}/lyx-${pkgver}${_pkgrel}

  find . -type f -exec sed -i 's|#!.*python|#!/usr/bin/env python2|' {} +
  sed -i 's|"python|"python2|' lib/configure.py src/support/os.cpp
  
  ./configure PYTHON=/usr/bin/python2 --without-aspell \
    --without-enchant --prefix=/usr --with-frontend=qt4 \
    --enable-build-type=pre --enable-warnings \
    --without-included-boost --without-included-mythes

  make || return 1
}

package() {
  cd ${srcdir}/lyx-${pkgver}${_pkgrel} 
  
  make DESTDIR=${pkgdir} install-strip || return 1

  # desktop entry
  install -Dm 644 ${srcdir}/lyx.desktop \
	${pkgdir}/usr/share/applications/lyx.desktop || return 1
}