# $Id$

pkgname=emacs-git-modes-git
pkgver=0+489+g32d4fd6
pkgrel=1
pkgdesc="GNU Emacs modes for Git-related files"
arch=('any')
url="https://github.com/magit/git-modes"
license=('GPL3')
depends=('emacs' 'git')
provides=('emacs-git-modes')
conflicts=('emacs-git-modes')
source=('git+https://github.com/magit/git-modes.git')
sha256sums=('SKIP')

pkgver() {
  cd 'git-modes'
  echo "0+$(git rev-list --count HEAD)+g$(git rev-parse --short HEAD)"
}

build() {
  cd 'git-modes'
  make
}

package() {
  cd 'git-modes'

  install -d "$pkgdir/usr/share/emacs/site-lisp/git-modes"
  install -m644 *.el *.elc "$pkgdir/usr/share/emacs/site-lisp/git-modes"
}
