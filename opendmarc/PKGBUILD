# Maintainer: Arthur Titeica <arthur.titeica@gmail.com>

pkgname=opendmarc
pkgver=1.1.1
pkgrel=1
pkgdesc="Free open source software implementation of the DMARC specification"
arch=('i686' 'x86_64')
url="http://www.trusteddomain.org/opendmarc/"
license=('custom')
depends=('libidn' 'perl')
makedepends=('libmilter')
optdepends=('python: run opendmarc scripts'
            'perl-switch: run opendmarc scripts'
            'perl-dbi: run opendmarc scripts'
            'perl-dbd-mysql: run opendmarc scripts'
            'perl-http-message: run opendmarc scripts')
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz{,.asc}
        opendmarc.service
        opendmarc.tmpfiles)
md5sums=('6e5383a745c438be76f63528e51bd5c5'
         '321e399c954b1be289aff9f80bd13c2f'
         'd9036cda7242c6fc353a7989cad7b3da'
         'b6bdb4ea8b6237637f2eb92072b14924')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --sysconfdir=/etc/$pkgname

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # sample config
  install -D -m644 "$srcdir"/$pkgname-$pkgver/$pkgname/opendmarc.conf.sample "$pkgdir"/etc/$pkgname/opendmarc.conf.sample
  # License
  install -D -m644 "$srcdir"/$pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm "$pkgdir"/usr/share/doc/$pkgname/LICENSE
  # systemd service
  install -D -m644 "$srcdir/$pkgname.service" "${pkgdir}/usr/lib/systemd/system/$pkgname.service"
  # tmpfile
  install -Dm644 "$srcdir/$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

}

# vim:set ts=2 sw=2 et:
