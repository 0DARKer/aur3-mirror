# Maintainer: Arthur Titeica <arthur.titeica@gmail.com>

pkgname=opendmarc
pkgver=1.0.1
pkgrel=4
pkgdesc="Free open source software implementation of the DMARC specification"
arch=('i686' 'x86_64')
url="http://www.trusteddomain.org/opendmarc/"
license=('custom')
groups=()
depends=('libidn' 'perl')
makedepends=('libmilter')
checkdepends=()
optdepends=('python: run opendmarc scripts'
            'perl-switch: run opendmarc scripts'
            'perl-dbi: run opendmarc scripts'
            'perl-dbd-mysql: run opendmarc scripts'
            'perl-http-message: run opendmarc scripts')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz
        opendmarc.service
        opendmarc.tmpfiles)
noextract=()
md5sums=('fffb77c2e620cf535b77f7f130163f4a'
         '440cc090fda273554b3faf9892486ccf'
         'b6bdb4ea8b6237637f2eb92072b14924')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --sysconfdir=/etc/$pkgname

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # sample config
  install -D -m644 "$srcdir"/$pkgname-$pkgver/$pkgname/opendmarc.conf.sample "$pkgdir"/etc/$pkgname/opendmarc.conf.sample
  # License
  install -D -m644 "$srcdir"/$pkgname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm "$pkgdir"/usr/share/doc/$pkgname/LICENSE
  # systemd service
  install -D -m644 "$srcdir/$pkgname.service" "${pkgdir}/usr/lib/systemd/system/$pkgname.service"
  # tmpfile
  install -Dm644 "$srcdir/$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

}

# vim:set ts=2 sw=2 et:
