# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Arch Haskell Team <>

pkgname=haskell-snap-core
_hkgname=snap-core
_licensefile=LICENSE
pkgver=0.9.2.2
pkgrel=1
pkgdesc="Snap: A Haskell Web Framework (Core)"
url="http://snapframework.com/"
license=('BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-attoparsec'
         'haskell-attoparsec-enumerator'
         'haskell-blaze-builder'
         'haskell-blaze-builder-enumerator'
         'haskell-bytestring'
	 'haskell-bytestring-mmap'
         'haskell-case-insensitive'
         'haskell-containers'
         'haskell-deepseq'
         'haskell-directory'
         'haskell-enumerator'
         'haskell-filepath'
         'haskell-hunit'
         'haskell-monadcatchio-transformers'
         'haskell-mtl'
         'haskell-old-locale'
         'haskell-random'
         'haskell-regex-posix'
         'haskell-text'
         'haskell-time'
         'haskell-unix-compat'
	 'haskell-unordered-containers'
         'haskell-vector'
         'haskell-zlib-enum')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('8ed60802fc131998027a026616d3e7f5850031def269effa4152c52b60846a6b')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}
}

