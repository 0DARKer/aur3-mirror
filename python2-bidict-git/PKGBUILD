# Maintainer:  jyantis <yantis@yantis.net>

pkgname=python2-bidict-git
pkgver=r5.6c84d3c
_gitname=bidict
pkgrel=1
pkgdesc='Provides a bidirectional mapping data structure and related functionality to naturally work with one-to-one relations in Python 2'
arch=('any')
url='https://bidict.readthedocs.org/en/latest'
license=('ISC')
depends=('python2')
source=('git+https://github.com/jab/bidict.git'
        'http://opensource.org/licenses/isc-license')
sha256sums=('SKIP'
            'SKIP')
pkgver() {
  cd "$_gitname"
  set -o pipefail
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${_gitname}

    # patch any #!/usr/bin/python to #!/usr/bin/python2
    for file in $(find . -name '*.py' -print); do
      sed -r -i 's_^#!.*/usr/bin/python(\s|$)_#!/usr/bin/python2_' $file
      sed -r -i 's_^#!.*/usr/bin/env(\s)*python(\s|$)_#!/usr/bin/env python2_' $file
    done

    python2 setup.py build
}

package() {
    install -D -m644 isc-license "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.html"

    cd ${_gitname}
    python2 setup.py install --root="${pkgdir}" --optimize=1
}

# vim:set ts=2 sw=2 et:
