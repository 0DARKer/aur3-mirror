# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=pybitmessage-git
_gitname='PyBitmessage'
pkgver=e694c6f
pkgrel=1
pkgdesc="Bitcoin inspired P2P encrypted messenger"
arch=('any')
depends=('python2' 'python2-pyqt' 'qt4')
makedepends=('git')
url="https://github.com/Bitmessage/PyBitmessage"
license=('MIT')
source=(git://github.com/Bitmessage/PyBitmessage.git
        pybitmessage.sh)
sha256sums=('SKIP'
            'c20eeeffd4931d8bcd50de5e6e1a974323ebbd3f5ad1d34483c6adeb3b5f3ae1')
provides=('pybitmessage')
conflicts=('pybitmessage')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $_gitname
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd "$srcdir"
  install -D -m755 pybitmessage.sh "${pkgdir}/usr/bin/pybitmessage"

  cd $_gitname/src
  mkdir -p "$pkgdir"/usr/share/pybitmessage
  cp -R ./* "$pkgdir"/usr/share/pybitmessage
}
