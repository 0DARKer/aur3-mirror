pkgname=zfs-utils
pkgver=0.6.0_rc10
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Native ZFS for Linux"
url="http://zfsonlinux.org/"
source=(http://github.com/downloads/zfsonlinux/zfs/zfs-${pkgver//_/-}.tar.gz
        zfs.initcpio.hook
        zfs.initcpio.install)
sha1sums=('f2be241e95faefc3a0593860dd071e981aa1a045'
          '09b18970836571d15448b58ad5d5f3e43fe4711d'
          'baf5fc940140bb630c784e446fbeb0136f971904')
license=('CDDL')

build() {
  cd "$srcdir/zfs-${pkgver//_/-}"
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc --with-config=user
  make
}

package() {
  cd "$srcdir/zfs-${pkgver//_/-}"
  make DESTDIR="$pkgdir" install

  install -D -m644 "$srcdir"/zfs.initcpio.hook "$pkgdir"/usr/lib/initcpio/hooks/zfs
  install -D -m644 "$srcdir"/zfs.initcpio.install "$pkgdir"/usr/lib/initcpio/install/zfs
}
