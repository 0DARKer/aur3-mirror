# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgname=('zfs-utils')
pkgver=0.6.0_rc12
pkgrel=4
pkgdesc="Native ZFS for Linux Utilities"
depends=('spl=0.6.0_rc12' 'linux=3.6.7')
makedepends=('linux-headers=3.6.7')
arch=('x86_64')
url='http://zfsonlinux.org/'
source=(http://github.com/downloads/zfsonlinux/zfs/zfs-0.6.0-rc12.tar.gz zfs-utils.initcpio.install zfs-utils.initcpio.hook zfs-utils.service)
groups=('archzfs')
md5sums=('6524f2c20fe9ad33a97879981e5c9a45' '512292ada08b21df52dd73ec7c45ed62' '7e046cbb04c19e5628bccb0a2b6ef2d6' 'ed409ba1607a9b48e73d02b5f72b3f79')
license=('CDDL')

build() {
  cd ${srcdir}/zfs-0.6.0-rc12
  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/sbin \
              --libdir=/usr/lib \
              --datadir=/usr/share \
              --includedir=/usr/include \
              --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-0.6.0-rc12 \
              --with-config=user
  make
}

package() {
  cd ${srcdir}/zfs-0.6.0-rc12
  make DESTDIR=${pkgdir} install

  # move module tree /lib -> /usr/lib
  cp -r ${pkgdir}/{lib,usr}
  rm -r ${pkgdir}/lib

  install -D -m644 ${srcdir}/zfs-utils.initcpio.hook ${pkgdir}/usr/lib/initcpio/hooks/zfs
  install -D -m644 ${srcdir}/zfs-utils.initcpio.install ${pkgdir}/usr/lib/initcpio/install/zfs
  install -D -m644 ${srcdir}/zfs-utils.service ${pkgdir}/usr/lib/systemd/system/zfs.service
}