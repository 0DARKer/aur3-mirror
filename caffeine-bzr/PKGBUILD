# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Fork Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: LookTJ/Taylor Lookabaugh <jesus.christ.i.love@gmail.com>
# Contributor: alessandro_ufms

_pkgname=caffeine
pkgname=$_pkgname-bzr
pkgver=464
pkgrel=1
pkgdesc="A system applet that allows you to inhibit the screensaver and the sleep power saving mode in an easy way"
arch=(any)
url="https://launchpad.net/caffeine"
license=(GPL)
depends=(dbus-python gtk2 python2-gobject python-notify python-xlib dconf pyxdg kaa-metadata hicolor-icon-theme)
makedepends=(bzr)
options=(!libtool !emptydirs)
install=$_pkgname.install
source=($_pkgname.diff)
sha256sums=('201be147516beb1f225674cbfcb501d625512af7325ff66553cd480913a5506d')
sha512sums=('7da99040796528f8654518d498e9749a826469aad7d14734f4de2a395032677fa0fdc590371e883edf96294cf8fe062533daddfcb13abfa0d89d6e43f05ee795')

_bzrtrunk="lp:caffeine"
_bzrmod=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to Bazaar server...."
    if [[ -d $_bzrmod/.bzr ]]; then
        pushd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver
        msg "The local files are updated."
        popd
    else
        bzr branch $_bzrtrunk $_bzrmod -q -r $pkgver
    fi
    msg "Bazaar checkout done or server timeout"

    # export without .bzr directory
    rm -rf $_bzrmod-build
    bzr export $_bzrmod-build -q -d $_bzrmod

    # Thanks to Andr√© Ericson for the patch!
    # https://bugs.launchpad.net/caffeine/+bug/892663
    patch -Np0 -i $_pkgname.diff

    # use python2
    find $_bzrmod-build/ -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python:&2:' '{}' \;
}

package() {
    cd "$srcdir"/$_bzrmod-build/
    python2 setup.py install --root="$pkgdir"
}

