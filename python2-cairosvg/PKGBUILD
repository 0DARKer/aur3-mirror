# Maintainer: Dennis Fink <the_metalgamer@hackerspace.lu>
# Submitter: Simon Sapin <simon dot sapin at exyr dot org>

pkgname='python2-cairosvg'
pkgver=0.5
pkgrel=1
pkgdesc="A Simple SVG Converter for Cairo"
arch=('any')
url="http://cairosvg.org/"
license=('LGPL3')
depends=('python2' 'python2-cairo')
optdepends=('python2-lxml: To use lxml to parse SVG files'
'python2-tinycss: Apply CSS not included in the style attribute of the tags'
'python2-cssselect: Apply CSS not included in the style attribute of the tags'
'python2-pystacia: To handle embedded raster images other than PNG')
source=("http://pypi.python.org/packages/source/C/CairoSVG/CairoSVG-${pkgver}.tar.gz")
md5sums=('6c092cce2b2ade47054aea6657173cbf')

build() {
  cd "$srcdir/CairoSVG-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/CairoSVG-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  cd $pkgdir/usr/bin
  mv cairosvg cairosvg-python2
}
