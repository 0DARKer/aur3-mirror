# Maintainer: twa022 <twa022 at gmail dot com>
# Based on PKGBUILD for gnome-control-center 3.6.3-1

_pkgname=cinnamon-control-center
pkgname=${_pkgname}-git
pkgver=1.7.2.2.g6d4914f
pkgrel=1
pkgdesc="The Control Center for Cinnamon"
arch=('i686' 'x86_64')
depends=('cinnamon' 'cheese' 'libgtop' 'network-manager-applet')
optdepends=('mesa-demos: provides glxinfo for graphics information'
            'gnome-color-manager: for color management tasks')
makedepends=('intltool' 'gnome-common' 'libgnomekbd' 'git')
url="https://github.com/linuxmint/cinnamon-control-center"
install=cinnamon-control-center.install
license=('GPL')
options=('!libtool' '!emptydirs')
source=("git://github.com/linuxmint/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${_pkgname}"
  echo $(git describe --always | sed 's/-/./g')
}

prepare() {
  cd "$srcdir/${_pkgname}"

  #patch -Np1 -i ../pulse3.0.patch
  #patch -uNp2 -r- -i ../keyboard_fix.patch
  autoreconf -fi
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-systemd \
      --disable-update-mimedb

  #https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
}

build() {
  cd "$srcdir/${_pkgname}"
  make
}

package() {
  cd "$srcdir/${_pkgname}"

  make DESTDIR="$pkgdir" install

  # Remove unused stuff
  make -C panels/datetime DESTDIR="$pkgdir" uninstall
  make -C panels/printers DESTDIR="$pkgdir" uninstall
  make -C panels/sound DESTDIR="$pkgdir" uninstall
  make -C panels/sound/data DESTDIR="$pkgdir" install
  make -C shell DESTDIR="$pkgdir" uninstall
  make -C shell DESTDIR="$pkgdir" install-libLTLIBRARIES

  # Remove Region panel, because it's not compatible with gnome-settings-daemon 3.6
  make -C panels/region DESTDIR="$pkgdir" uninstall

  # Remove broken desktop files
  rm -R "$pkgdir/usr/share/applications"
  rm -R "$pkgdir/etc"

  # Fix a warning
  chown 102:0 "$pkgdir/usr/share/polkit-1/rules.d"
  chmod 700 "$pkgdir/usr/share/polkit-1/rules.d"
}
