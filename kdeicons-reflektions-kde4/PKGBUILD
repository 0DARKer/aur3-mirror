# Maintainer: Artem A. Klevtsov unikum.pm@gmail.com

## Options
# change to "yes" that activate
install_colors=no
install_systray=no

pkgname=kdeicons-reflektions-kde4
_pkgname=Reflektions_KDE4
pkgver=1.3
pkgrel=1
pkgdesc="This is a version of the KDE4 port of the "Reflektions" icons."
arch=('any')
url="http://kde-look.org/content/show.php/Reflektions_KDE4?content=110944"
license=('CCPL')
makedepends=('imagemagick' 'unrar')
source=("http://fc09.deviantart.net/fs70/f/2011/308/1/e/reflektions_kde4_v1_3_by_linuxfever-d287xmx.rar")
md5sums=('ca8e37d1c74e5cf62d37a14c625fb94d')

SIZES="8x8 16x16 22x22 32x32 48x48 64x64"
DIRS="actions apps categories devices emblems emotes mimetypes places status"

build() {
  cd ${srcdir}
  if [ -d Reflektions_KDE4 ]; then
    msg2 "Clean previous build directory..."
    rm -rf Reflektions_KDE4
  fi

  msg2 "Extract files..."
  unrar x -inul reflektions_kde4_v*.rar

  cd ${srcdir}/${_pkgname}
  
# create the dirs
  msg2 "Create directories..."
  for size in ${SIZES}; do
    for dir in ${DIRS}; do
      mkdir -pm 755 ${size}/${dir}
    done
  done

  msg2 "Convert icons..."
  for dir in ${DIRS}; do
    cd ${srcdir}/${_pkgname}/128x128/${dir}
      for icon in *; do
	for size in ${SIZES}; do
	  convert "${icon}" -resize ${size} ../../${size}/${dir}/"${icon}"
	done
      done
  done
}

package() {
  cd ${srcdir}/${_pkgname}
  install -dm 755 ${pkgdir}/usr/share/icons/${_pkgname}
  install -Dm 644 index.theme ${pkgdir}/usr/share/icons/${_pkgname}/
  cp -r 128x128 ${pkgdir}/usr/share/icons/${_pkgname}

  for size in ${SIZES}; do
    cp -r ${size} ${pkgdir}/usr/share/icons/${_pkgname}
  done

  for size in ${SIZES}; do
    cp -r ${size}_or/* ${pkgdir}/usr/share/icons/${_pkgname}/${size}
  done

## Install color-scheme
  if [ ${install_colors} = yes ]; then
    install -Dm 644 Theme/Reflektions.colors ${pkgdir}/usr/share/apps/color-schemes/Reflektions.colors
  fi
##Install systray icons
  if [ ${install_systray} = yes ]; then
    install -dm 755 ${pkgdir}/usr/share/apps/desktoptheme/default/{icons,widgets}
    install -Dm 644 Systray/icons/* ${pkgdir}/usr/share/apps/desktoptheme/default/icons/
    install -Dm 644 Systray/widgets/* ${pkgdir}/usr/share/apps/desktoptheme/default/widgets/
  fi

  find ${pkgdir} -type d -exec chmod 755 {} \;
  find ${pkgdir} -type f -exec chmod 644 {} \;
}
