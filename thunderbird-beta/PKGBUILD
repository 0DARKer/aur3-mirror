# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Here you choose whether you want a PGO (Profile-Guided Optimization) build. See: http://en.wikipedia.org/wiki/Profile-guided_optimization
_pgo="0"  # "1" to enable

pkgname=thunderbird-beta
pkgver=6.0
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell>=1.3' 'fontconfig' 'freetype2' 'cairo' 'desktop-file-utils' 'libjpeg' 'libpng' 'nspr' 'libmng' 'libevent')
makedepends=('zip' 'libgnomeui' 'python2' 'libidl2' 'yasm' 'libnotify' 'gconf' 'yasm' 'mesa')
optdepends=('libcanberra: for sound support')
options=(!buildflags)
provides=("thunderbird=$pkgver")
install=$pkgname.install
# In case releases.mozilla.org is not responding or the file(s) aren't yet uploaded there:
source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2
# source=(http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2
        mozconfig
        $pkgname.desktop
        thunderbird-3.0-lang.patch
        thunderbird-appversion.patch)
md5sums=(`wget ${source/so*}/MD5SUMS -qO - | grep source | cut -d " " -f1`
         'b8fd97ab4bfb2e4538b1e1340d70f915'
         'f8a9b91202e8c85ad7b8f23fb82a4d7b'
         '25b6fe16ac24cd5c852213e5c1adb272'
         'be5f6aea50eeefae234bfc0f30e2f0ec')

build() {
  cd comm-*
  patch -p1 -i ../thunderbird-3.0-lang.patch
  patch -p1 -i ../thunderbird-appversion.patch

  cp ../mozconfig .mozconfig

  export CXXFLAGS="-fpermissive"
  export LDFLAGS="-Wl,-rpath,/opt/thunderbird-$pkgver -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"
  export PYTHON="/usr/bin/python2"

  if [[ "$_pgo" = "1" ]]; then
     sed -i '/^NO_PROFILE_GUIDED_OPTIMIZE = 1$/d' mozilla/memory/jemalloc/Makefile.in
     echo 'LDFLAGS += -lX11 -lXrender' >> mozilla/layout/build/Makefile.in
     LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
     LD_PRELOAD="" DISPLAY=:99 make -j2 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
  else
     make -j2 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  fi
}

package() {
  cd comm-*/obj-*
  make package

  cd mozilla/dist/
  bsdtar -x -f thunderbird-*.tar.bz2
  install -m755 -d "$pkgdir"/{opt,usr/bin}
  cp -r thunderbird/ "$pkgdir/opt/thunderbird-$pkgver/"
  ln -sf /opt/thunderbird-$pkgver/thunderbird "$pkgdir/usr/bin/$pkgname"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -D "$srcdir/comm-*/other-licenses/branding/thunderbird/mailicon${i/x*/}.png" "$pkgdir/usr/share/icons/hicolor/$i/apps/$pkgname.png"
  done

  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  ln -sf /usr/share/myspell/dicts "$pkgdir/opt/thunderbird-$pkgver/dictionaries"

  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "$pkgdir/opt/thunderbird-$pkgver/defaults/pref/channel-prefs.js"
}