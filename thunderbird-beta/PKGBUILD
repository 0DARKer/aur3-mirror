# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Here you choose whether you want a PGO (Profile-Guided Optimization) build
_pgo="0"  # Change to "1" to do so - every other number/mark disables PGO

pkgname=thunderbird-beta
pkgver=3.3a3
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'gcc-libs' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell' 'sqlite3>=3.7.4' 'fontconfig' 'freetype2' 'cairo')
makedepends=('zip' 'libgnomeui' 'python2' 'libidl2' 'wireless_tools' 'yasm' 'autoconf2.13' 'libnotify')
optdepends=('libcanberra: for sound support'
	    'yasm: for WebM support')
provides=("thunderbird=${pkgver}")
# In case releases.mozilla.org is not responding or the file(s) aren't yet uploaded there:
# source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/source/thunderbird-${pkgver}.source.tar.bz2
source=(http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/source/thunderbird-${pkgver}.source.tar.bz2
        mozconfig
        thunderbird-beta.desktop
        thunderbird-3.0-lang.patch
        thunderbird-appversion.patch
        thunderbird-preferences.patch)
md5sums=(`wget ${source/so*}/MD5SUMS -qO - | grep source | cut -d " " -f1`
         'dff0a1bb5523e7842de851b023375679'
         '3fc6e687e61a958c617e645122c26b0f'
         '25b6fe16ac24cd5c852213e5c1adb272'
         '48ffcdb877a69d383b7d354e330f7658'
         '8a9737a545f18e75b0da7a80f63a407a')

build() {
  cd comm-central

  patch -Np1 -i ../thunderbird-3.0-lang.patch
  patch -Np1 -i ../thunderbird-appversion.patch
  patch -Np1 -i ../thunderbird-preferences.patch

  cp ../mozconfig .mozconfig
  export LDFLAGS="-Wl,-rpath,/opt/thunderbird-${pkgver}"
  unset CXXFLAGS CFLAGS

  if [[ "${_pgo}" = "1" ]]; then
     [[ "$CARCH" = "i686" ]] && export CFLAGS="-march=i686 -mtune=generic"
     [[ "$CARCH" = "x86_64" ]] && export CFLAGS="-march=x86-64 -mtune=generic"
     export CXXFLAGS="${CFLAGS}"
     make -f client.mk profiledbuild MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  else
     make -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  fi
}

package() {
  cd comm-central/obj-*

  make package

  cd mozilla/dist/
  bsdtar -x -f thunderbird-3.1.en-US.linux-${CARCH}.tar.bz2
  install -m755 -d "${pkgdir}"{/opt,/usr/{share/{applications,pixmaps},bin}}
  cp -r thunderbird/ "${pkgdir}"/opt/thunderbird-${pkgver}/

  ln -sf /opt/thunderbird-${pkgver}/thunderbird "${pkgdir}"/usr/bin/thunderbird-beta

  install -m644 "${srcdir}"/comm-central/mail/branding/unofficial/mailicon48.png "${pkgdir}"/usr/share/pixmaps/thunderbird-beta.png

  install -m644 "${srcdir}"/thunderbird-beta.desktop "${pkgdir}"/usr/share/applications/
}