# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Here you choose whether you want a PGO (Profile-Guided Optimization) build
_pgo="0"  # Change to "1" to do so - every other number/mark disables PGO

pkgname=thunderbird-beta
pkgver=5.0b1
pkgrel=2
pkgdesc="Standalone Mail/News reader - Bleeding edge version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell>=1.3' 'fontconfig' 'freetype2' 'cairo' 'desktop-file-utils' 'libjpeg' 'libpng' 'nspr' 'libmng')
makedepends=('zip' 'libgnomeui' 'python2' 'libidl2' 'yasm' 'autoconf2.13' 'libnotify')
optdepends=('libcanberra: for sound support'
            'yasm: for WebM support')
provides=("thunderbird=${pkgver}")
install=${pkgname}.install
# In case releases.mozilla.org is not responding or the file(s) aren't yet uploaded there:
# source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/source/thunderbird-${pkgver}.source.tar.bz2
source=(http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/source/thunderbird-${pkgver}.source.tar.bz2
        mozconfig
        ${pkgname}.desktop
        thunderbird-3.0-lang.patch)
md5sums=(`wget ${source/so*}/MD5SUMS -qO - | grep source | cut -d " " -f1`
         'ac1edd41e214742421c7f917948a9ad6'
         '1a6f871700cf22d59a0ba9c0c237df81'
         '25b6fe16ac24cd5c852213e5c1adb272')

build() {
  cd comm-miramar
  patch -Np1 -i ../thunderbird-3.0-lang.patch

  cp ../mozconfig .mozconfig
  export LDFLAGS="-Wl,-rpath,/opt/thunderbird-${pkgver}"
  export CXXFLAGS="-fpermissive"

  if [[ "${_pgo}" = "1" ]]; then
     [[ "$CARCH" = "i686" ]] && export CFLAGS="-march=i686 -mtune=generic"
     [[ "$CARCH" = "x86_64" ]] && export CFLAGS="-march=x86-64 -mtune=generic"
     export CXXFLAGS="${CXXFLAGS} ${CFLAGS}"
     make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  else
     unset CFLAGS
     make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  fi
}

package() {
  cd comm-miramar/obj-*
  make package

  cd mozilla/dist/
  bsdtar -x -f thunderbird-*.tar.bz2
  install -m755 -d "${pkgdir}"/{/opt,/usr/{share/applications,bin}}
  cp -r thunderbird/ "${pkgdir}/opt/thunderbird-${pkgver}/"
  ln -sf /opt/thunderbird-${pkgver}/thunderbird "${pkgdir}/usr/bin/${pkgname}"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -D "${srcdir}/comm-miramar/other-licenses/branding/thunderbird/mailicon${i/x*/}.png" "${pkgdir}/usr/share/icons/hicolor/${i}/apps/${pkgname}.png"
  done

  install -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/"

  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "${pkgdir}/opt/thunderbird-${pkgver}/defaults/pref/channel-prefs.js"
}