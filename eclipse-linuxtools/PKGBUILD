# Contributor: Jesus Jerez <jhuss@archlinux.org.ve>

pkgname=eclipse-linuxtools
pkgver=0.6.1
_pkgbuild=R201008261752
pkgrel=1
pkgdesc="Eclipse Linux Tools Project"
url="http://www.eclipse.org/linuxtools"
arch=('any')
license=('EPL')
depends=('eclipse')
makedepends=('unzip')
changelog=$pkgname.changelog
source=("http://download.eclipse.org/technology/linuxtools/${_pkgbuild}/linuxtools-Update-incubation-${pkgver}.zip")
md5sums=('953dea771920ff12b9084f21169b145f')
sha256sums=('ee7f8568e5768d618ddb6753df09c6060d745d37bf0857faeb5a16f7fcde89d9')

build() {

  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done

}
