# Contributor: Jesus Jerez <jhuss@archlinux.org.ve>
# Maintainer: Ng Oon-Ee <  n g  o o n  e e AT g m a i l DOT c o m>

pkgname=eclipse-linuxtools
pkgver=0.8.0
pkgrel=1
pkgdesc="Eclipse Linux Tools Project"
url="http://www.eclipse.org/linuxtools"
arch=('any')
license=('EPL')
depends=('eclipse')
makedepends=('unzip')
source=("http://download.eclipse.org/technology/linuxtools/linuxtools-${pkgver}-incubation.zip")
md5sums=('6eddf2ab2b903053f90766a6c986eac6')
sha256sums=('9189513bcefc84e651f214752be8a50f94cce61a6d23cf519aaed5efcb9d356c')

build() {

  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}/linuxtools-${pkgver}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/linuxtools-${pkgver}/${_feature} || return 1
      cd ${srcdir}/linuxtools-${pkgver}
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done

}
