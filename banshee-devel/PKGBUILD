# Current Maintainer: Jeremy Newton (Mystro256)
# Old Maintainer: Matthias Sobczyk <matthias.sobczyk@googlemail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=banshee-devel
_pkgname=banshee
pkgver=2.3.1
pkgrel=1
pkgdesc="Music management and playback for GNOME (development release)"
arch=('i686' 'x86_64')
url="http://banshee.fm/"
license=('MIT')
depends=('mono>=2.4.3' 'sqlite3>=3.4' 'gtk-sharp-2>=2.12.10' 'gstreamer0.10-base-plugins' 'glib2>=2.22' 'dbus-sharp>=0.7' 'dbus-sharp-glib>=0.5' 'mono-addins>=0.3.1' 'taglib-sharp>=2.0.3.7' 'libmtp>=0.2.0' 'mono-zeroconf>=0.8.0' 'boo>=0.8.1' 'libwebkit>=1.2.2' 'gdata-sharp>=1.4' 'gio-sharp' 'gtk-sharp-beans>=2.14.0' 'gudev-sharp' 'gkeyfile-sharp' 'desktop-file-utils' 'gconf-sharp' 'gdata-sharp>=1.4' 'hicolor-icon-theme' 'libxxf86vm' 'media-player-info' 'notify-sharp-svn' 'shared-mime-info' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('brasero: CD burning'
            'gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
provides=('banshee')
conflicts=('banshee' 'banshee-1-svn' 'banshee-git')
options=('!libtool')
install=${pkgname}.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgname}/2.3/${_pkgname}-${pkgver}.tar.bz2 banshee-recent-gpod.patch)
sha256sums=('bcb8faaf8b7faba430412396f96bed403a1d537f0c13327d88a9149c299f668a'
            '81f6618e027b92dec02b867b6fae97d8ad84f4b4c523f3ed6dd238c917e5aeba')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir/banshee-recent-gpod.patch"

  #A small fix#
  temp=$(sed "s/Volume.MountInstance.GuessContentTypeSync (false, null)/new string\[\] {Volume.MountInstance.GuessContentTypeSync (false, null)}/" ${srcdir}/${_pkgname}-${pkgver}/src/Backends/Banshee.Gio/Banshee.Hardware.Gio/LowLevel/GioVolumeMetadataSource.cs)
  echo "$temp" > ${srcdir}/${_pkgname}-${pkgver}/src/Backends/Banshee.Gio/Banshee.Hardware.Gio/LowLevel/GioVolumeMetadataSource.cs
  #############

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-docs \
              --disable-static \
              --disable-scrollkeeper \
              --disable-schemas-install \
              --with-vendor-build-id=ArchLinux

  make
}

package() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
