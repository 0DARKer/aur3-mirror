# Maintainer: Matthias Sobczyk <matthias.sobczyk@googlemail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=banshee-devel
_pkgname=banshee
pkgver=2.1.0
pkgrel=3
pkgdesc="Music management and playback for GNOME (development release)"
arch=('i686' 'x86_64')
url="http://banshee.fm/"
license=('MIT')
depends=('boo>=0.8.1' 'dbus-sharp-glib>=0.5' 'desktop-file-utils' 'gconf-sharp' 'gdata-sharp>=1.4' 'gkeyfile-sharp>=0.1' 'gstreamer0.10-base-plugins' 'gtk-sharp-beans>=2.14.0' 'gudev-sharp>=0.1' 'hicolor-icon-theme' 'libgpod>=0.7.95' 'libmtp>=0.2.0' 'libwebkit>=1.2.2' 'libxxf86vm' 'musicbrainz' 'media-player-info' 'mono-addins>=0.3.1' 'mono-zeroconf>=0.8.0' 'notify-sharp-svn' 'shared-mime-info' 'taglib-sharp>=2.0.3.7' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('brasero: CD burning'
            'gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
provides=('banshee')
conflicts=('banshee' 'banshee-1-svn' 'banshee-git')
options=('!libtool')
install=${pkgname}.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgname}/2.1/${_pkgname}-${pkgver}.tar.bz2)
sha256sums=('9bd08cd428c97c2ee5dfc8643769f05a26737b20699a1f48a9ee760287dcdf54')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${_pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-docs \
              --disable-static \
              --disable-scrollkeeper \
              --with-vendor-build-id=ArchLinux
  make
}

package() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/COPYING"
}
