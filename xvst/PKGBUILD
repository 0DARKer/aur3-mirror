# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Laurie Clark-Michalek <bluepeppers@archlinux.us>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Maintainer: Larry Hajali <larryhaja[at]gmail[dot]com>

pkgname=xvst
pkgver=2.4.1
pkgrel=3
pkgdesc='GUI tool for downloading videos from sites like YouTube, GoogleVideo etc.'
url='http://xviservicethief.sourceforge.net'
license=('GPL3')
arch=('i686' 'x86_64')
depends=('qt' 'ffmpeg' 'qtwebkit' 'icu')
makedepends=('sed' 'subversion')
optdepends=('flvstreamer: for downloading flv files.')
install=xvst.install
source=('http://downloads.sourceforge.net/xviservicethief/xVST_2_4_1_src.zip'
	'xvst.desktop'
        'gcc-4.7.patch')
md5sums=('7f8c2e79c3431cc46da306419abc16eb'
         '9d4d4d4841b497fe64705d36e7955174'
         '79a5299880dce26e97fee267f6ed9198')

build()
{
  # Patch for newer gcc.
  patch -p1 < ${srcdir}/gcc-4.7.patch

  # Update the plugins via subversion.
  rm -rf resources/{services,translations/definitions}
  svn co https://xviservicethief.svn.sourceforge.net/svnroot/xviservicethief/trunk/resources/services \
    resources/services
  svn co https://xviservicethief.svn.sourceforge.net/svnroot/xviservicethief/trunk/resources/translations/definitions \
    resources/translations/definitions

  sed -i "s|getApplicationPath()\ +\ \"|\"/usr/share/${pkgname}|g" src/options.cpp
  lrelease resources/translations/*.ts
  rm -f resources/translations/template_for_new_translations.qm

  qmake -set build_mode dynamic_build
  qmake PREFIX=/usr -o Makefile xVideoServiceThief.pro
  make \
    CFLAGS="-pipe ${CFLAGS} -Wall -W -D_REENTRANT \$(DEFINES)" \
    CXXFLAGS="-pipe ${CXXFLAGS} -Wall -W -D_REENTRANT \$(DEFINES)"
}

package()
{
  install -d -m 0755 "$pkgdir"/usr/share/$pkgname/{plugins,languages}
  find resources/services -name "*.js" \
    -exec cp -dpR {} "$pkgdir"/usr/share/$pkgname/plugins \;
  install -m 0644 resources/translations/*.qm \
    "$pkgdir"/usr/share/$pkgname/languages
  install -m 0644 resources/translations/definitions/*.language \
    "$pkgdir"/usr/share/$pkgname/languages
  install -Dm755 "$srcdir"/bin/$pkgname "$pkgdir"/usr/bin/$pkgname

  install -Dm644 "$srcdir"/xvst.desktop \
    "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -Dm644 "$srcdir"/resources/images/InformationLogo.png \
    "$pkgdir"/usr/share/pixmaps/$pkgname.png

  install -d -m 0755 "$pkgdir"/usr/share/doc/$pkgname
  cp -a "how to compile.txt" resources/changelog.txt \
    "$pkgdir"/usr/share/doc/$pkgname
}
