# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Laurie Clark-Michalek <bluepeppers@archlinux.us>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Larry Hajali <larryhaja[at]gmail[dot]com>

pkgname=xvst
pkgver=2.4.1
pkgrel=1
pkgdesc='GUI tool for downloading videos from sites like YouTube, GoogleVideo etc.'
url='http://xviservicethief.sourceforge.net'
license=('GPL3')
arch=('i686' 'x86_64')
depends=('qt>=4.6.0' 'ffmpeg')
makedepends=('sed')
optdepends=('flvstreamer: for downloading flv files.')
install=xvst.install
source=('http://downloads.sourceforge.net/xviservicethief/xVST_2_4_1_src.zip'
	'xvst.desktop')
md5sums=('7f8c2e79c3431cc46da306419abc16eb'
         '9d4d4d4841b497fe64705d36e7955174')

build()
{
  sed -i "s|getApplicationPath()\ +\ \"|\"/usr/share/${pkgname}|g" src/options.cpp || return 1
  lrelease resources/translations/*.ts || return 1
  rm -f resources/translations/template_for_new_translations.qm

  qmake -set build_mode dynamic_build || return 1
  qmake -unix PREFIX=/usr -o Makefile xVideoServiceThief.pro || return 1
  make \
    CFLAGS="-pipe ${CFLAGS} -Wall -W -D_REENTRANT \$(DEFINES)" \
    CXXFLAGS="-pipe ${CXXFLAGS} -Wall -W -D_REENTRANT \$(DEFINES)" || return 1
}

package()
{
  # Install data and exectuable.
  install -d -m 0755 "$pkgdir"/usr/share/$pkgname/{plugins,languages}
  find resources/services -name "*.js" \
    -exec cp -dpR {} "$pkgdir"/usr/share/$pkgname/plugins \; || return 1
  install -m 0644 resources/translations/*.qm \
    "$pkgdir"/usr/share/$pkgname/languages || return 1
  install -m 0644 resources/translations/definitions/*.language \
    "$pkgdir"/usr/share/$pkgname/languages || return 1
  install -Dm755 "$srcdir"/bin/$pkgname "$pkgdir"/usr/bin/$pkgname || return 1

  # Install desktop item.
  install -Dm644 "$srcdir"/xvst.desktop \
    "$pkgdir"/usr/share/applications/$pkgname.desktop || return 1
  install -Dm644 "$srcdir"/resources/images/InformationLogo.png \
    "$pkgdir"/usr/share/pixmaps/$pkgname.png || return 1

  # Install documentation.
  install -d -m 0755 "$pkgdir"/usr/share/doc/$pkgname
  cp -a "how to compile.txt" resources/changelog.txt \
    "$pkgdir"/usr/share/doc/$pkgname || return 1
}
