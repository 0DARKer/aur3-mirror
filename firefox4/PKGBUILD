# $Id: PKGBUILD 90129 2010-09-08 10:38:46Z ibiru $
# Maintainer: Konstantin Shabanov <kes.eclipse@gmail.com>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox4
pkgver=4.0b11
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
conflicts=('firefox' 'firefox-pgo')
provides=('firefox')
depends=('desktop-file-utils' 'startup-notification>=0.8')
makedepends=('zip' 'pkg-config' 'diffutils' 'libgnomeui>=2.24.1' 'python2' 'wireless_tools' 'yasm' 'ccache' 'autoconf2.13' 'cairo-tee')
install=firefox.install
url="http://www.mozilla.com/en-US/firefox/beta/"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
        mozconfig
        firefox.desktop
        firefox-safe.desktop
        system-cairo-fixup.patch
        cairo-tee-check.patch)

md5sums=('a52b8f4bb98f158ef0639db59dd39bce'
         '7ecc978ea6db0c26836cbe116dfb3956'
         'ba96924ece1d77453e462429037a2ce5'
         '6f38a5899034b7786cb1f75ad42032b8'
         'be77398428707ddd5354078b86c4c700'
         '68a68b4945ecc9e5f659bc3cccc06569')

build() {
  cd "${srcdir}/mozilla-central"

  cp "${srcdir}/mozconfig" .mozconfig

  patch -Np1 -i ${srcdir}/system-cairo-fixup.patch
  patch -Np1 -i ${srcdir}/cairo-tee-check.patch

  export LDFLAGS="-Wl,-rpath,/usr/lib/firefox-4.0b11"
  make -f client.mk build


  if [ "$CARCH" = "x86_64" ]; then
    make DESTDIR="${pkgdir}" -C obj-x86_64-unknown-linux-gnu install
  else
    make DESTDIR="${pkgdir}" -C obj-i686-pc-linux-gnu install
  fi 

  install -m755 -d ${pkgdir}/usr/share/{applications,pixmaps}
  install -m644 ${srcdir}/mozilla-central/browser/branding/unofficial/default48.png ${pkgdir}/usr/share/pixmaps/firefox.png
  install -m644 ${srcdir}/firefox.desktop ${pkgdir}/usr/share/applications/
  install -m644 ${srcdir}/firefox-safe.desktop ${pkgdir}/usr/share/applications/
}
