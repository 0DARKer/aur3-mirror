# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: A Rojas < nqn1976 @ gmail.com >

pkgname=partitionmanager-git
pkgver=1.1.0.36.g46b4f27
pkgrel=1
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/kdepartitionmanager/'
license=('GPL2')
depends=('parted' 'kio')
makedepends=('cmake' 'git' 'extra-cmake-modules')
optdepends=('e2fsprogs: ext2/3/4 support'
            'xfsprogs: XFS support'
            'jfsutils: JFS support'
            'reiserfsprogs: Reiser support'
            'ntfsprogs: NTFS support'
            'dosfstools: FAT32 support')
source=('git://anongit.kde.org/partitionmanager')
sha1sums=('SKIP')
install=partitionmanager-git.install
_gitname=partitionmanager

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long | tr -d v |tr - .)"
}

prepare() {
  cd "${_gitname}"
  sed 's|KF5 CONFIG REQUIRED COMPONENTS|KF5 REQUIRED|g' -i CMakeLists.txt
}

build() {
  cd "${_gitname}"
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
          -DLIB_INSTALL_DIR=lib \
          -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
          -DENABLE_UDISKS2=ON
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}