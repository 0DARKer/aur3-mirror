# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: A Rojas < nqn1976 @ gmail.com >

pkgname=partitionmanager-git
pkgver=1.1.0.0.g1bab2bf
pkgrel=1
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/kdepartitionmanager/'
license=('GPL2')
depends=('kdebase-runtime' 'parted')
makedepends=('cmake' 'automoc4' 'git')
conflicts=('partitionmanager')
provides=('partitionmanager')
optdepends=('e2fsprogs: ext2/3/4 support'
            'xfsprogs: XFS support'
            'jfsutils: JFS support'
            'reiserfsprogs: Reiser support'
            'ntfsprogs: NTFS support'
            'dosfstools: FAT32 support')
source=('git://anongit.kde.org/partitionmanager'
        'http://download.kde.org/stable/partitionmanager/1.1.0/src/partitionmanager-1.1.0.tar.xz')
sha1sums=('SKIP'
          '797964522d077ec9daae752bd2d40f1bd284d149')
noextract=('partitionmanager-1.1.0.tar.xz')
install=partitionmanager-git.install
_gitname=partitionmanager

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long | tr -d v |tr - .)"
}

prepare() {
  cd "${_gitname}"
  # add extra PO for localization
  rm -fr po
  rm -fr partitionmanager-1.1.0/po
  bsdtar -xf "${srcdir}/partitionmanager-1.1.0.tar.xz" partitionmanager-1.1.0/po
  mv partitionmanager-1.1.0/po .
  rm -fr partitionmanager-1.1.0

  echo -e 'include(MacroOptionalAddSubdirectory)\nmacro_optional_add_subdirectory( po )' >> CMakeLists.txt

  sed '1icmake_policy(SET CMP0002 OLD)' -i po/CMakeLists.txt
}

build() {
  cd "${_gitname}"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DENABLE_UDISKS2=ON \
          -DQT_QMAKE_EXECUTABLE=qmake-qt4
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}