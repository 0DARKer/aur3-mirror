# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: A Rojas < nqn1976 @ gmail.com >

pkgname=partitionmanager-git
pkgver=v1.2.0.r885.c0307bf
pkgrel=1
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/kdepartitionmanager/'
license=('GPL2')
depends=('parted' 'frameworkintegration' 'kinit' 'ktexteditor')
conflicts=('partitionmanager')
provides=('partitionmanager')
makedepends=('cmake' 'git' 'extra-cmake-modules' 'kdoctools')
optdepends=('e2fsprogs: ext2/3/4 support'
            'xfsprogs: XFS support'
            'jfsutils: JFS support'
            'reiserfsprogs: Reiser support'
            'ntfsprogs: NTFS support'
            'dosfstools: FAT32 support')
source=('git://anongit.kde.org/partitionmanager')
sha1sums=('SKIP')
install=partitionmanager-git.install

pkgver() {
  _ver="$(cat partitionmanager/CMakeLists.txt | grep -e VERSION_MAJOR -e VERSION_MINOR -e VERSION_RELEASE | head -n3 | cut -d '"' -f2)"
  echo "v$(echo ${_ver} | tr ' ' .).r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
  sed 's|${DATA_INSTALL_DIR}|${KXMLGUI_INSTALL_DIR}|g' -i partitionmanager/src/CMakeLists.txt
}

build() {
  cd build
  cmake ../partitionmanager \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}