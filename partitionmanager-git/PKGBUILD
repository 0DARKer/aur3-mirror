# Maintainer: A Rojas < nqn1976 @ gmail.com >

pkgname=partitionmanager-git
_gitname=partitionmanager
pkgver=r781.e8fdee1
pkgrel=2
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/kdepartitionmanager/'
license=('GPL2')
depends=('kdebase-runtime' 'parted')
makedepends=('cmake' 'automoc4' 'git')
conflicts=('partitionmanager' 'partitionmanager-svn')
replaces=('partitionmanager-svn')
provides=('partitionmanager')
source=('git://anongit.kde.org/partitionmanager')
md5sums=('SKIP')
install=${pkgname}.install

pkgver() {
  cd $_gitname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_gitname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_UDISKS2=ON \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}