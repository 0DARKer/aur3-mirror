# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
pkgname=nqp
pkgver=2015.01
pkgrel=1
pkgdesc="High-level environment to create compilers and libraries for virtual machines"
arch=('i686' 'x86_64')
url='https://github.com/perl6/nqp#readme'
license=(PerlArtistic)
depends=('parrot>=6.10.0')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$pkgname/$pkgname-$pkgver.tar.gz dyncall.patch)
sha512sums=('61865a6bf5e6adf07397ea0c985a4b20a61cf30d125849b3f2b7d04848d12c4c3858fe627a82369b6ac9142b99e2ca3fadf775eeb76ba6a9fb45f558a5148022'
            'ef7656ed8914a4efb87285d3e633cbb28b63a02eed0876853dfe84ea61ca5d09d0d6b5fb1be5ea6fe0570b34e426d1df8f60cee32a5e4cb872c291922a04ee83')

prepare() {
    cd "$pkgname-$pkgver"
    msg2 "$(patch -p1 < ../dyncall.patch)"
}

build() {
    cd "$pkgname-$pkgver"

    perl Configure.pl --backends=parrot --with-parrot=/usr/bin/parrot --prefix=/usr
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}
