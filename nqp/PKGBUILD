# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
pkgname=nqp
pkgver=2014.03
pkgrel=1
pkgdesc="High-level environment to create compilers and libraries for virtual machines"
arch=('i686' 'x86_64')
url='https://github.com/perl6/nqp#readme'
license=(PerlArtistic)
depends=('parrot>=6.1.0')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$pkgname/$pkgname-$pkgver.tar.gz dyncall.patch)
sha512sums=('08c19f514cac444af55fe51c64ed1901dc59a8d0f6cae2262e4e04cd8bdf75bfc0f420bd15b11f964bf58f2d065a1457b674089859510500430055c19c4dbb40'
            '73017410166f963ce337a3523c3293418c101a98bd6da16810fc6d4480323e39aa03534dc42459d6ac0d154501b88eae6f7a35e0ae0eb394cdbc4fa7396cfafe')

prepare() {
    cd "$pkgname-$pkgver"
    msg2 "$(patch -p1 < ../dyncall.patch)"
}

build() {
    cd "$pkgname-$pkgver"

    perl Configure.pl --with-parrot=/usr/bin/parrot --prefix=/usr
    make
}
package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}

