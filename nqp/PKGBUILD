# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
pkgname=nqp
pkgver=2013.02.1
pkgrel=1
pkgdesc="Compiler for quickly generating PIR routines from Perl6-like code"
arch=('i686' 'x86_64')
url='https://github.com/perl6/nqp#readme'
license=(PerlArtistic)
depends=('parrot>=5.1.0')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(https://github.com/perl6/$pkgname/archive/${pkgver}.tar.gz)
md5sums=('0e62e117c0dd2836fb23957e2f4bafc9')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    perl Configure.pl --with-parrot=/usr/bin/parrot --prefix=/usr
    perl -pe 's/(?<=libffi-3.0.)11/12/g' -i Makefile
    make
}
package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}

