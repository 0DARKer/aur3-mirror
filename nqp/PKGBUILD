# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
pkgname=nqp
pkgver=2014.04
pkgrel=1
pkgdesc="High-level environment to create compilers and libraries for virtual machines"
arch=('i686' 'x86_64')
url='https://github.com/perl6/nqp#readme'
license=(PerlArtistic)
depends=('parrot>=6.1.0')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$pkgname/$pkgname-$pkgver.tar.gz dyncall.patch)
sha512sums=('079860c83f93f04cb511b09cbc3a021a15df5bc1b43fbf8b978865212344145e41d70b545a12b4685783591a347fdcd1b4a92ee7bd09e4f20701c86a4dc77791'
            '73017410166f963ce337a3523c3293418c101a98bd6da16810fc6d4480323e39aa03534dc42459d6ac0d154501b88eae6f7a35e0ae0eb394cdbc4fa7396cfafe')

prepare() {
    cd "$pkgname-$pkgver"
    msg2 "$(patch -p1 < ../dyncall.patch)"
}

build() {
    cd "$pkgname-$pkgver"

    perl Configure.pl --with-parrot=/usr/bin/parrot --prefix=/usr
    make
}
package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}

