# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
pkgname=nqp
pkgver=2014.01
pkgrel=1
pkgdesc="High-level environment to create compilers and libraries for virtual machines"
arch=('i686' 'x86_64')
url='https://github.com/perl6/nqp#readme'
license=(PerlArtistic)
depends=('parrot>=5.9.0')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('b0fa91f21d097f4e1dc3fdca6af5598cd153813f0648eb18848817d31e3a25d8692cdf9b38653ca3601b647e7bae51c8d2894f715b6135f8ea491fd1e9455664')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    perl Configure.pl --with-parrot=/usr/bin/parrot --prefix=/usr
    make
}
package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}

