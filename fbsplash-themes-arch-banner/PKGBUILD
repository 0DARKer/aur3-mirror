
# Maintainer: Kurt J. Bosch <kjb-temp-2009 at alpenjodel.de>

pkgname=fbsplash-themes-arch-banner
pkgver=1.4.3
pkgrel=1
_sizes_normal="640x480 800x600 1024x768 1152x864 1152x900 1280x960 1280x1024 1400x1050 1600x1200"
_sizes_wide="1280x720 1280x768 1280x800 1280x854 1366x768 1440x900 1680x1050 1920x1080 1920x1200"
pkgdesc="Fbsplash themes with/without customizable services-icons"
arch=('any')
license=("GPL" "CCPL:cc-by-nc-sa" "custom:ArchLinux-trademarks" "custom:Vera-copyright")
url="http://aur.archlinux.org/packages.php?ID=26966"
depends=(
	'fbsplash'
)
makedepends=(
	'archlinux-artwork>=1.6'
	'ttf-dejavu'
	'ttf-liberation'
	'imagemagick' 'librsvg'
)
optdepends=(
	'fbsplash-extras>=2.0.9: needed for icons'
	'tango-icon-theme: Icons for services/daemons'
	'uswsusp-fbsplash: Suspend/resume with Fbsplash'
)
conflicts=( ${pkgname}-{common,normal,wide} )
replaces=(  ${pkgname}-{common,normal,wide} )
backup=( etc/splash/arch-banner-icons/icons.conf )
install=${pkgname}.install
changelog=Changelog
_font=Vera.ttf
_vera_ver=1.10
source=(
	Screenshot-icons-Tango.png
	Screenshot-noicons.png
	make-foregrounds.sh
	make-backgrounds.sh
	make-cfg.sh
	make-meta.sh
	icons.conf
	cache-icons
	svc_event
	http://ftp.gnome.org/pub/GNOME/sources/ttf-bitstream-vera/${_vera_ver}/ttf-bitstream-vera-${_vera_ver}.tar.bz2
)
md5sums=('e81a772fb82c9697b091f9275a3dba0e'
         '662aec6a14b244d010878ae2bd716bbf'
         '4e31ff99a6018d73d810ecb806b96e87'
         '5988de3aacbda553cd4bcd0ea3364423'
         '27911335fcf7d0ad1c816f4333213302'
         '7d999f92d8b20c075b5f2899244cb0b0'
         'b352c171124fe42ba002b3caa7cf072d'
         '66db34379c41c6684b4a689ec9b0cf24'
         'ff70a1f0eb83b63fadc81d31ee765d99'
         'bb22bd5b4675f5dbe17c6963d8c00ed6')

build() {
	cd "${srcdir}"

	# source for banner images
	local _banner_silent=/usr/share/archlinux/logos/archlinux-grad1-light.svg
	local _banner_verbose=/usr/share/archlinux/logos/archlinux-mono-black.svg
	# separator images dimensions
	local _sep_width=640
	local _sep_height=3
	# width of the vertical progress-bar
	local _progress_width=20
	# font size used
	local _font_size=13
	# number and sizes of daemon icons used
	# (change values in cache-icons and comments in icons.conf too !)
	local _icon_count=13
	local _icon_size=32
	local _icon_size_small=16

	# get in font file
	cp -f ttf-bitstream-vera-${_vera_ver}/${_font} .
	# make foreground images and animations
	./make-foregrounds.sh $_banner_silent $_banner_verbose \
		$_sep_width $_sep_height $_progress_width \
		$_icon_size $_icon_size_small
	# make background images
	for _size in $_sizes_normal $_sizes_wide ; do
		./make-backgrounds.sh $_size
	done
	# make <size>.cfg files
	for _size in $_sizes_normal $_sizes_wide; do
		./make-cfg.sh arch-banner-icons $_size $_sep_width $_sep_height \
			$_progress_width $_font $_font_size $_icon_size $_icon_count
		./make-cfg.sh arch-banner-noicons $_size $_sep_width $_sep_height \
			$_progress_width $_font $_font_size
	done
	# make metadata.xml files
	./make-meta.sh arch-banner-icons ${pkgver} " with message log and service icons" &&
	./make-meta.sh arch-banner-noicons ${pkgver} " (message log only version)"
}

package() {
	## install common files
	mkdir -p "${pkgdir}"/etc/splash/arch-banner-noicons/images
	mkdir -p "${pkgdir}"/etc/splash/arch-banner-icons/{images,scripts}
	cd "${pkgdir}"/etc/splash/
	# install the font
	install -m644 "${srcdir}"/$_font arch-banner-noicons/ &&
	ln -s  ../arch-banner-noicons/$_font arch-banner-icons/
	# install foreground images and animations
	local _file
	for _file in banner.png separator.png separator-red.png progress-bkgd.mng
	do
		install -m644 "${srcdir}"/images/$_file arch-banner-noicons/images/ &&
		ln -s  ../../arch-banner-noicons/images/$_file arch-banner-icons/images/
	done
	# install special icon images
	install -m644 "${srcdir}"/images/{cover,cover-a50,dummy}.png arch-banner-icons/images/
	# install the icons config file
	install -m644 "${srcdir}"/icons.conf arch-banner-icons/
	# install metadata.xml
	install -m644 "${srcdir}"/arch-banner-noicons/metadata.xml arch-banner-noicons/metadata.xml &&
	install -m644 "${srcdir}"/arch-banner-icons/metadata.xml arch-banner-icons/metadata.xml
	# install the scripts
	install -m755 "${srcdir}"/{cache-icons,svc_event} arch-banner-icons/scripts/
	# install splash event hook symlinks
	ln -s cache-icons arch-banner-icons/scripts/rc_init-pre
	local _hook
	for _hook in \
		svc_{start,started,start_failed}-pre \
		svc_{stop,stopped,stop_failed}-pre # svc_inactive_{start,stop}-pre
	do
		# initscripts-extras-fbsplash=>2.0.6-4 support *.sh sourcing for speed
		#ln -s svc_event arch-banner-icons/scripts/$_hook || return 1
		ln -s svc_event arch-banner-icons/scripts/${_hook}.sh
	done
	# install symlinks to dummy icon to satisfy fbcondecor_ctl
	PREVLEVEL="" RUNLEVEL="" \
	SPLASH_THEME=arch-banner-icons \
	spl_cachedir="${pkgdir}"/lib/splash/cache \
		"${srcdir}"/cache-icons
	# install licenses
	mkdir -p "${pkgdir}"/usr/share/licenses/${pkgbase}
	cd       "${pkgdir}"/usr/share/licenses/${pkgbase}
	install -m644 /usr/share/licenses/archlinux-artwork/TRADEMARKS ArchLinux-trademarks
	install -m644 "${srcdir}"/ttf-bitstream-vera-${_vera_ver}/COPYRIGHT.TXT Vera-copyright

	## install screen size dependant files
	mkdir -p "${pkgdir}"/etc/splash/arch-banner-{noicons,icons}/images
	cd "${pkgdir}"/etc/splash/
	local _size _file
	for _size in $_sizes_normal $_sizes_wide; do
		# install backgrounds
		for _file in silent-$_size.png verbose-$_size.png; do
			install -m644 "${srcdir}"/images/$_file arch-banner-noicons/images/ &&
			ln -s ../../arch-banner-noicons/images/$_file arch-banner-icons/images/
		done
		# install cfg-files
		install -m644 "${srcdir}"/arch-banner-noicons/$_size.cfg arch-banner-noicons/$_size.cfg &&
		install -m644 "${srcdir}"/arch-banner-icons/$_size.cfg arch-banner-icons/$_size.cfg
	done
}
