# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=syslog-ng-systemd
_pkgname=syslog-ng
pkgver=3.2.2
pkgrel=1
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('base')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glib2' 'eventlog' 'openssl' 'libcap')
makedepends=('flex' 'pkgconfig')
optdepends=('logrotate')
provides=('logger' 'syslog-ng')
conflicts=('syslog-ng')
options=('!libtool')
backup=('etc/syslog-ng/modules.conf' 'etc/syslog-ng/syslog-ng.conf' 'etc/logrotate.d/syslog-ng')
source=(http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/$_pkgname\_$pkgver.tar.gz
        https://api.opensuse.org/public/source/Base:System/syslog-ng/syslog-ng-3.2.2-systemd-integration.bnc656104.diff
        cap_syslog.patch
        syslog-ng.conf
        syslog-ng.logrotate
        syslog-ng.rc)
md5sums=('ed8ebe559d52a63fb61e3e2db566643f'
         '515c4b4028b2d1336e195b23116bfeaf'
         '46e5dcff71f820d497898331a3f608fe'
         '726baa4a0bed583786afaa72bf34e96b'
         '735636090be4582885974542d2a75855'
         '9bd98250cfa49e13fa3fa731b6040549')

build() {
    cd $srcdir/$_pkgname-$pkgver

    # suse's systemd integration patchwork
    patch -Np1 < "$srcdir"/syslog-ng-3.2.2-systemd-integration.bnc656104.diff

    # support for 2.6.38
    patch -Np1 < "$srcdir"/cap_syslog.patch

    ./configure --prefix=/usr \
                --sysconfdir=/etc/syslog-ng \
                --localstatedir=/var/lib/syslog-ng \
                --disable-tcp-wrapper \
                --with-pidfile-dir=/var/run \
                --disable-spoof-source
    make
}

package() {
    cd $srcdir/$_pkgname-$pkgver
    make DESTDIR="$pkgdir" install
    install -dm755 "$pkgdir"/var/lib/syslog-ng
    install -Dm644 "$srcdir"/syslog-ng.conf "$pkgdir"/etc/syslog-ng/syslog-ng.conf
    install -Dm644 "$srcdir"/syslog-ng.logrotate "$pkgdir"/etc/logrotate.d/syslog-ng
    install -Dm755 "$srcdir"/syslog-ng.rc "$pkgdir"/etc/rc.d/syslog-ng

    # remove libexec
    rmdir "$pkgdir"/usr/libexec
}
