# Maintainer: Joshua Hunt <snark1994@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
pkgname=haskell-hdbc
pkgver=2.3.1.2
pkgrel=1
pkgdesc="Haskell Database Connectivity"
url="http://hackage.haskell.org/package/HDBC"
license=('custom:BSD3') #changed from LGPL
arch=('i686' 'x86_64')
makedepends=()
depends=('haskell-base>=3' 'haskell-base<5' 'haskell-bytestring' 'haskell-containers' 'haskell-convertible>=1.0.10.0' 'haskell-mtl' 'haskell-old-locale' 'haskell-old-time' 'haskell-text' 'haskell-time>=1.1.3' 'haskell-time<=1.5' 'haskell-utf8-string')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/HDBC/${pkgver}/HDBC-${pkgver}.tar.gz)
md5sums=('a4609bbd736cc10fd70922e05ada45df')
install=${pkgname}.install
build() {
    cd ${srcdir}/HDBC-${pkgver}
    runhaskell Setup configure -O --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/HDBC-${pkgver}
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" # Added
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/HDBC
    runhaskell Setup copy --destdir=${pkgdir}
}
