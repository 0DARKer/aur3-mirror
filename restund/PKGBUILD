# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=restund
pkgver=0.4.0
pkgrel=1
pkgdesc="A modular and flexible STUN and TURN Server, with IPv4 and IPv6 support."
arch=('i686' 'x86_64')
url="http://www.creytiv.com/"
license=('BSD')
depends=('libre>=0.4.0' 'libmysqlclient')
optdepends=('munin: for monitoring')
backup=("etc/$pkgname.conf")
source=("http://www.creytiv.com/pub/$pkgname-$pkgver.tar.gz" $pkgname{,-args}.sh)
md5sums=('fcfda012e04b5402e898da211dc10b6b'
         '40ac82d7b1a888b74006a088d48899bc'
         '18354f901fb74cb620a8753a0b4d6fd4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix munin plugins path
  sed -i '/munin/s/DATA/LIB/' Makefile

  # restund.conf module path fix
  sed -i 's/\/local//' etc/$pkgname.conf

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 docs/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # restund.conf
  install -Dm644 etc/$pkgname.conf \
    "$pkgdir/etc/$pkgname.conf"

  # daemon
  install -Dm755 ../$pkgname.sh \
    "$pkgdir/etc/rc.d/$pkgname"

  install -Dm644 ../$pkgname-args.sh \
    "$pkgdir/etc/conf.d/$pkgname"
}
