# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=gmpc
pkgname=$_pkgname-git
pkgver=20130326
pkgrel=1
pkgdesc="A gnome based GUI client for MPD"
arch=(i686 x86_64)
url=http://www.gmpclient.org
license=(GPL2)
depends=(glyr-git gtk3 hicolor-icon-theme libmpd-git libsoup libunique3)
makedepends=(git gnome-doc-utils gob2 intltool vala)
provides=($_pkgname=11.8.90)
conflicts=($_pkgname)
options=(!libtool !makeflags)
install=$pkgname.install

_gitroot=git://git.musicpd.org/master/$_pkgname.git
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone --depth 1 $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh --prefix=/usr
    make
}

package() {
    make -C $_gitname-build DESTDIR="$pkgdir" install
}
