# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=gmpc
pkgname=$_pkgname-git
pkgver=20121230
pkgrel=1
pkgdesc="A gnome based GUI client for MPD"
arch=(i686 x86_64)
url=http://www.gmpclient.org
license=(GPL2)
depends=(glyr-git hicolor-icon-theme lib{mpd-git,soup,unique} sqlite)
makedepends=(git gnome-doc-utils gob2 intltool vala)
provides=($_pkgname=11.8.90)
conflicts=($_pkgname)
options=(!libtool !makeflags)
install=$pkgname.install
source=($pkgname-treeview-undefined.patch)
sha256sums=('97004814e796b504d0a7aeed5ba715933bd3716981718dbf06c8b509d4765ef0')
sha512sums=('9e360dab2768c0a75374bbe08d920ba7d5a3260a41196c711999b4faba155962e33dbfe485bf133a02ca9bb7b5fb1f1270fa63bc3e812f5fdd3f705064be9656')

_gitroot=git://repo.or.cz/$_pkgname.git
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone --depth 1 $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    patch -Np1 -i ../$pkgname-treeview-undefined.patch
    msg "Building..."
    ./autogen.sh --prefix=/usr
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
