# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=gmpc
pkgname=$_pkgname-git
pkgver=11.8.16.368.g66786d7
pkgrel=1
pkgdesc="A gnome based GUI client for MPD"
arch=(i686 x86_64)
url=http://www.gmpclient.org
license=(GPL2)
depends=(glyr-git gtk3 hicolor-icon-theme libmpd-git libsoup libunique sqlite)
makedepends=(git gnome-doc-utils gob2 intltool vala)
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
options=(!libtool !makeflags)
install=$pkgname.install
source=($pkgname::git://repo.or.cz/$_pkgname.git)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $pkgname/
    git describe | sed 's/^release-//;s/-/./g'
}

build() {
    cd $pkgname/
    ./autogen.sh --prefix=/usr
    make
}

package() {
    make -C $pkgname DESTDIR="$pkgdir" install
}
