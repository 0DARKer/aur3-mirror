# Maintainer: James Reed <supplantr at archlinux dot info>

_pkgname=notificathor
pkgname=$_pkgname-git
pkgver=20130603.38
pkgrel=1
pkgdesc='Themeable On Screen Displays for X'
arch=('i686' 'x86_64')
url='https://github.com/TheWebster/NotificaThor'
license=('BSD')
depends=('cairo')
makedepends=('git' 'libxcb' 'cairo')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git://github.com/TheWebster/NotificaThor.git")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-list --count master)"
}

build() {
    cd "$srcdir/$_pkgname"
    make
}

package() {
    cd "$srcdir/$_pkgname"
    install -Dm644 LICENCE.md "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
    mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/man" "$pkgdir/etc"
    make prefix="$pkgdir" install
}
