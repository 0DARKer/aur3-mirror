# This is a modified version of dbus-core that includes the patch from here:
# http://lists.freedesktop.org/archives/dbus/2008-January/009100.html
# This fixes some multi-threading locking issues that particularly affect
# KDE software.  It is a behaviour change for bindings, though, so will only
# be included in dbus 1.4.  Qt and Glib bindings should be fine, and there
# are no bindings that are known to have problems (see comments on
# https://bugs.freedesktop.org/show_bug.cgi?id=17754).
#
pkgname=dbus-core-mt
pkgver=1.2.24
pkgrel=1
pkgdesc="Freedesktop.org message bus system, patched to fix multithreading issues"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('expat>=2.0.1' 'coreutils' 'filesystem')
conflicts=('dbus<1.2.3-2' 'dbus-core')
provides=('dbus-core=1.2.24')
options=(!libtool)
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz
        multithread-locking-fix.patch dbus)
md5sums=('565346cecd9cfecf1463540c6086cc2c'
         '057f5b70a5d59ddd68d0598ba50e90b1'
         '08f93dd19cffd1b45ab05c1fd4efb560')

build() {
  cd "${srcdir}/dbus-${pkgver}"
  patch -Np1 -i "${srcdir}/multithread-locking-fix.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/var/run/dbus.pid \
      --enable-inotify --disable-dnotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts --without-x || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm -f "${pkgdir}/usr/bin/dbus-launch"
  rm -f "${pkgdir}/usr/share/man/man1/dbus-launch.1"

  chown 81:81 "${pkgdir}/var/run/dbus" || return 1

  install -m755 -d "${pkgdir}/etc/rc.d" || return 1
  install -m755 "${srcdir}/dbus" "${pkgdir}/etc/rc.d/" || return 1

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|' "${pkgdir}/etc/dbus-1/system.conf" || return 1

  #install .keep files so pacman doesn't delete empty dirs
  touch "${pkgdir}/usr/share/dbus-1/services/.keep" || return 1
  touch "${pkgdir}/usr/share/dbus-1/system-services/.keep" || return 1
  touch "${pkgdir}/etc/dbus-1/session.d/.keep" || return 1
  touch "${pkgdir}/etc/dbus-1/system.d/.keep" || return 1

  rmdir "${pkgdir}/usr/lib/dbus-1.0/dbus-1" || return 1

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus-core"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/dbus-core/" || return 1
}
