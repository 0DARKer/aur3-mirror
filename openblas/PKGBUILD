# Maintainer: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
pkgname=openblas
_pkgname=xianyi-OpenBLAS
pkgver=0.2.0
pkgrel=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD "
arch=('i686' 'x86_64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl')
provides=('blas=3.4.0' 'cblas')
conflicts=('blas' 'cblas')
options=(!makeflags)
source=(${_pkgname}-v${pkgver}-0-g47860cf.tar.gz::http://github.com/xianyi/OpenBLAS/tarball/v${pkgver})
md5sums=('132a52e919932b69edfa36c3dcba477d')

build() {
  cd "$srcdir/$_pkgname-23a7062"

  unset CFLAGS
  unset CXXFLAGS
  make USE_OPENMP=1 NO_LAPACK=1
}

package() {
  cd "$srcdir/$_pkgname-23a7062"

  make PREFIX="$pkgdir/usr" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/lib/"
  ln -sf libopenblas_*-r$pkgver.a libopenblas.a
  ln -sf libopenblas_*-r$pkgver.so libopenblas.so
  ln -sf libopenblas_*-r$pkgver.so libopenblas.so.0
  ln -sf libopenblas.so libblas.so.3
  ln -sf libopenblas.a libblas.a
  ln -sf libblas.so.3 libblas.so
  ln -sf libopenblas.so libcblas.so.3
  ln -sf libopenblas.a libcblas.a
  ln -sf libcblas.so.3 libcblas.so
}

# vim:set ts=2 sw=2 et:
