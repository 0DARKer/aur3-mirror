# Maintainer: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
pkgname=openblas
_pkgname=xianyi-OpenBLAS
pkgver=0.2.7
pkgrel=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD "
arch=('i686' 'x86_64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl' 'gcc-fortran')
provides=('blas=3.4.2')
conflicts=('blas')
options=(!makeflags)
source=(${_pkgname}-v${pkgver}.tar.gz::http://github.com/xianyi/OpenBLAS/tarball/v${pkgver})
md5sums=('e35b1341f8d33e498013648cf2588964')

build() {
  cd "$srcdir/$_pkgname-b8ff9e6"

  unset CFLAGS
  unset CXXFLAGS
  NPROC=`grep "physical id" /proc/cpuinfo|sort|uniq|wc -l`
  NCORE4PROC=`grep "cores" /proc/cpuinfo|sort|tail -n 1|sed -e 's/cpu cores.*: //'`
  let NCORE=NPROC*NCORE4PROC
  make USE_OPENMP=1 NO_LAPACK=1 NUM_THREADS=$NCORE LIBPREFIX=libblas \
    MAJOR_VERSION=3 NO_CBLAS=1 NO_AFFINITY=1
}

package() {
  cd "$srcdir/$_pkgname-b8ff9e6"

  NPROC=`grep "physical id" /proc/cpuinfo|sort|uniq|wc -l`
  NCORE4PROC=` grep "cores" /proc/cpuinfo|sort|tail -n 1|sed -e 's/cpu cores.*: //'`
  let NCORE=NPROC*NCORE4PROC
  make PREFIX="$pkgdir/usr" NUM_THREADS=$NCORE LIBPREFIX=libblas \
    MAJOR_VERSION=3 install
  rm -f "$pkgdir/usr/include/cblas.h" "$pkgdir"/usr/include/lapacke*
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/lib/"
  ln -sf libblas_*-r$pkgver.a libblas.a
  ln -sf libblas_*-r$pkgver.so libblas.so
  ln -sf libblas_*-r$pkgver.so libblas.so.3
}

# vim:set ts=2 sw=2 et:
