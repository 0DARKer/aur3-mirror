# Maintainer: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
pkgname=openblas
_pkgname=xianyi-OpenBLAS
pkgver=0.1.1
pkgrel=1
pkgdesc="An optimized BLAS library based on GotoBLAS2 1.13 BSD "
arch=('i686' 'x86_64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl')
provides=('blas=3.4.0' 'cblas')
conflicts=('blas' 'cblas')
options=(!makeflags)
source=(${_pkgname}-v${pkgver}-0-g5b7f443.tar.gz::http://github.com/xianyi/OpenBLAS/tarball/v0.1.1)
md5sums=('d76bebc1638391f1ef8959e98a2a666a')

build() {
  cd "$srcdir/$_pkgname-e6e87a2"

  unset CFLAGS
  unset CXXFLAGS
  make USE_OPENMP=1 NO_LAPACK=1
}

package() {
  cd "$srcdir/$_pkgname-e6e87a2"

  make PREFIX="$pkgdir/usr" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/lib/"
  ln -sf libopenblas_*-r$pkgver.a libopenblas.a
  ln -sf libopenblas_*-r$pkgver.so libopenblas.so
  ln -sf libopenblas_*-r$pkgver.so libopenblas.so.0
  ln -sf libopenblas.so libblas.so.3
  ln -sf libopenblas.a libblas.a
  ln -sf libblas.so.3 libblas.so
  ln -sf libopenblas.so libcblas.so.3
  ln -sf libopenblas.a libcblas.a
  ln -sf libcblas.so.3 libcblas.so
}

# vim:set ts=2 sw=2 et:
