# Mantainer: Diego Principe <cdprincipeat gmaildot com>
# Contributor: Baurzhan Muftakhidinov <baurthefirst@gmail.com>

pkgname=xfce4-session-git
pkgver=1608.9179f8a
pkgrel=1
pkgdesc="A session manager for Xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4-git')
depends=('libxfce4ui' 'libwnck' 'libsm' 'xorg-iceauth' 'upower' 'xorg-xinit' 'hicolor-icon-theme')
makedepends=('git' 'xfce4-dev-tools-git')
#makedepends=('git' 'xfce4-dev-tools')
replaces=('xfce4-utils' 'xfce4-session' 'xfce4-session-svn')
conflicts=('xfce4-session')
provides=('xfce4-session')
options=('!libtool')
install=xfce4-session-git.install
source=(git://git.xfce.org/xfce/xfce4-session)
md5sums=('SKIP')

pkgver(){
  cd $srcdir/xfce4-session
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $srcdir/xfce4-session
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd $srcdir/xfce4-session
  make DESTDIR=$pkgdir install
}

