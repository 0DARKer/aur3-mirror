# Maintainer: MaÃ«l Kerbiriou <mael.kerbiriouATfreeDOTfr>
# Based on openimageio-git's PKGBUILD

pkgname="openshadinglanguage-git"
pkgver=20121128
pkgrel=1
pkgdesc="A language for programmable shading in renderers and other applications."
url="http://code.google.com/p/openshadinglanguage/"
license=("BSD")
arch=("i686" "x86_64")
provides=("openshadinglanguage")
conflicts=("openshadinglanguage")
depends=("boost-libs" "llvm" "openexr" "openimageio-git")
optdepends=()
makedepends=("git" "cmake" "boost")

_gitroot="git://github.com/imageworks/OpenShadingLanguage.git"
_gitname="OpenShadingLanguage"

build() {
	cd "${srcdir}"
	if [ -d "${_gitname}" ]; then
		msg "Updating local GIT repository..."
		cd "${_gitname}"
		git clean -dfx
		git reset --hard
		git pull origin
	else
		msg "Cloning GIT repository..."
		git clone "$_gitroot"
		cd "${_gitname}"
	fi
	msg2 "Done."
	
	# Remove the unnecessary 'Insecure RPATH' of ${dist_dir}/lib.
	sed '/set (CMAKE_INSTALL_RPATH "${CMAKE_INSTALL_PREFIX}\/lib")/d' -i "src/CMakeLists.txt"

	msg "Starting make..."
	make dist_dir="dist"
}

package() {
	cd "${srcdir}/${_gitname}/dist"
	install -d "${pkgdir}/usr/bin" \
		"${pkgdir}/usr/lib"
	install -m755 bin/* "${pkgdir}/usr/bin"
	install -m755 lib/* "${pkgdir}/usr/lib"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	find include \( -type f -o -type l \) -exec \
		install -Dm644 "{}" "${pkgdir}/usr/{}" \;
	cd doc
	find * \( -type f -o -type l \) -exec \
		install -Dm644 "{}" "${pkgdir}/usr/share/doc/${pkgname}/{}" \;
}

# vim: set noet ff=unix:
