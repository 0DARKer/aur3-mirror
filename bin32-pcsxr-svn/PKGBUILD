# Contributor: Kazuo Teramoto <kaz.rag at gmail dot com>
# Contributor: Eugene Yudin aka Infy <Eugene dot Yudin at gmail dot com>

pkgname=bin32-pcsxr-svn
pkgver=83636
pkgrel=1
pkgdesc="A Sony PlayStation emulator based on the PCSX-df Project (can use 32-bit plugins)"
arch=('x86_64')
url="http://pcsxr.codeplex.com/"
license=('GPL')
depends=('lib32-libcdio' 'lib32-gtk3' 'lib32-libxv' 'lib32-sdl')
makedepends=('subversion' 'gcc-multilib' 'intltool' 'lib32-mesa')
provides=('pcsx' "pcsx-svn=${pkgver}")
conflicts=('pcsx' 'pcsx-df' 'pcsxr' 'pcsxr-alsa' 'pcsxr-svn')
options=(!libtool)

_svntrunk=https://pcsxr.svn.codeplex.com/svn/pcsxr
_svnmod=pcsxr

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r ${pkgver})
  else
    svn co $_svntrunk --config-dir ./ -r ${pkgver} $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh --prefix=/usr --enable-libcdio --enable-opengl \
      --enable-dynarec=x86
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}
