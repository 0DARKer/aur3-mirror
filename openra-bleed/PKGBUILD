# $Id: PKGBUILD 78667 2012-10-22 14:23:25Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Matthew Bowra-Dean <matthew@ijw.co.nz>
__gitroot="git://github.com/OpenRA/OpenRA.git"
__gitname="OpenRA"
__gitcommit="bleed"

pkgname=openra-bleed
pkgver=20130302
pkgrel=1
pkgdesc="An open-source implementation of the Red Alert engine using .NET/mono and OpenGL. Git bleed version."
arch=('any')
url="http://open-ra.org"
license=('GPL3')
install=openra.install
depends=('mono' 'openal' 'libgl' 'freetype2' 'sdl' 'hicolor-icon-theme' 'ttf-dejavu')
makedepends=('git' 'unzip')
optdepends=('nvidia-cg-toolkit: for using the alternative Cg renderer')
options=(!strip)
source=("openra.install")
sha256sums=("5371e118b99ec55fc45a16e09ee6e6eb9b33b07a07323fe2fe839bbd88416e6c")

build() {
  _git_setup
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/applications"
  sed "s/{VERSION}/$pkgver/g" packaging/linux/openra.desktop > "$pkgdir/usr/share/applications/openra.desktop"

  sed "/Version/s/{DEV_VERSION}/release-${pkgver}/" -i mods/{ra,cnc}/mod.yaml  

  mkdir -p "$pkgdir/usr/share/icons/"
  cp -r packaging/linux/hicolor "$pkgdir/usr/share/icons/"

  make prefix=/usr DESTDIR="$pkgdir" install

  find "$pkgdir" -iname \*.mdb -delete
}

_git_setup() {

    cd "$SRCDEST"
    
    if [[ ! -d "$__gitname" ]]; then
        msg2 "Cloning git repository"
        git clone --mirror "$__gitroot" "$__gitname"
    else
        msg2 "Updating git repository"
        cd "$__gitname"
        git fetch
    fi

    cd "$srcdir"
    if [[ -d "$pkgname-$pkgver" ]]; then
        msg2 "Removing previous build tree"
        rm -fr "$pkgname-$pkgver"
    fi

    msg2 "Creating fresh build tree"
    git clone --depth=1 -b "$__gitcommit" "file://$SRCDEST/$__gitname" "$pkgname-$pkgver"
}

