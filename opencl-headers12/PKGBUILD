# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sylvain HENRY <hsyl20@yahoo.fr>

pkgname=opencl-headers12
pkgver=1.2
pkgrel=1
pkgdesc='OpenCL 1.2 (Open Computing Language) header files'
arch=('any')
url='http://www.khronos.org/registry/cl/'
license=('custom')
optdepends=('libcl: OpenCL library')
provides=('opencl-headers')
conflicts=('opencl-headers')
source=("http://www.khronos.org/registry/cl/api/$pkgver/cl_ext.h"
          "http://www.khronos.org/registry/cl/api/$pkgver/cl_gl_ext.h"
          "http://www.khronos.org/registry/cl/api/$pkgver/cl_gl.h"
          "http://www.khronos.org/registry/cl/api/$pkgver/cl.h"
          "http://www.khronos.org/registry/cl/api/$pkgver/cl.hpp"
          "http://www.khronos.org/registry/cl/api/$pkgver/cl_platform.h"
          "http://www.khronos.org/registry/cl/api/$pkgver/opencl.h"
          "LICENSE.txt")
md5sums=('1e9627453c1760b9d918f63848b4fbea'
         '8b58ea9e244c8817e66b62b5d533f1f0'
         'b0a7709b747b0b4a0c6629206ba5b55b'
         '33105ffe2bb8996aaed19f7ac6ff5182'
         '2130e2cade4a7c8d9f7623f2b1a77ca2'
         '63d7245fdcd2b77436872ca3a183caea'
         '8bc589e602246900e4435694d5bc4379'
         '8e59f0e0f64326e826007b49700fba76')

package() {
   cd "$srcdir"

   install -dm755 "$pkgdir"/usr/include/CL

   for h in $(ls -1 *.h *.hpp); do
      install -m 644 $h "$pkgdir"/usr/include/CL/
   done

   install -D -m644 "$srcdir"/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
