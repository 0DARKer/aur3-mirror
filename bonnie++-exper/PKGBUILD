# Maintainer:  Devin Cofer <ranguvar@archlinux.us>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <jeepster@gmx.co.uk>

pkgname=bonnie++-exper
_pkgname=bonnie++
pkgver=1.96
pkgrel=1
pkgdesc="Experimental version of a popular hard drive benchmark suite"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.coker.com.au/bonnie++/"

depends=('perl')
conflicts=("$_pkgname")
provides=("$_pkgname=$pkgver")

source=(http://www.coker.com.au/bonnie++/experimental/$_pkgname-$pkgver.tgz)
sha256sums=('9c9d87bf09ac5a42a915ceeb9d8bb8358c0ca4642d86b85ed48ca5f15ee3456c')


build() {
	cd "$srcdir/$_pkgname-$pkgver"
	./configure --prefix=/usr || return 1
	sed -i "s:CFLAGS=-O2  -DNDEBUG \$(WFLAGS) \$(MORECFLAGS):CFLAGS=${CFLAGS}:" \
	       Makefile || return 1
	make || return 1
}
package() {
	cd "$srcdir/$_pkgname-$pkgver"

	make prefix="$pkgdir/usr" install || return 1
}
