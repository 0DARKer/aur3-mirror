# Contributor: Florian LÃ©ger <florian6 dot leger at laposte dot net>
# Maintainer: Alois Nespor <info at aloisnespor dot info>

pkgname=firefox-oxygen-kde
pkgver=4.0_b2
pkgrel=1
pkgdesc="Complete add-on/theme bringing the Oxygen style for Firefox."
arch=(any)
depends=('firefox')
url="http://oxygenkde.altervista.org/index.html"
license=('GPL')
source=("http://oxygenkde.altervista.org/download/OxygenKDE_${pkgver//./_}.xpi")


package() {
  local f
  local emid
  local destdir

  cd "${srcdir}"

  for f in "oxykdeopt" "oxykdetheme"; do
    mkdir -p "${f}"
    bsdtar -xf "${f}.xpi" -C "${f}"
    emid="$(sed -n -e '/<\?em:id>\?/!d; s/.*\([\"{].*[}\"]\).*/\1/; s/\"//g; p; q' "${f}/install.rdf")"
    #sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>18.*</em:maxVersion>#' "${f}/install.rdf"
    dstdir="${pkgdir}/usr/lib/firefox/extensions/${emid}"
    install -d "${dstdir}"
    pushd "${f}"
    find . -type f -exec install -Dm644 '{}' "${dstdir}/{}" \;
    popd
  done
}
sha256sums=('45a4ca5ff8b3ba4c5798d9fcebfe9f4465e80227682cced5b94cad6680798943')
