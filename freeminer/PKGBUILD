# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=freeminer
pkgver=0.4.11.5
pkgrel=1
pkgdesc="An open source sandbox game inspired by Minecraft."
arch=(
	"$CARCH"
)
url="http://freeminer.org/"
license=(
	"GPL3"
)

depends=(
	"irrlicht"
	"leveldb"
	"libvorbis"
	"openal"
	"sqlite"
	"curl"
	"luajit"
	"msgpack"
	"gtk-update-icon-cache"
)
optdepends=()
makedepends=(
	"cmake"
)
provides=(
	"${pkgname}=${pkgver}"
)
conflicts=(
	"${pkgname}-git"
)
replaces=(
	"${pkgname}-git"
)

install=${pkgname}.install

source=(
	"https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.zip"
	"${pkgname}.install"
)
sha384sums=(
	"6835dff76b302ccae4c9148b1419f19aadcdc325efcb67f36203704ed570666df5d60c87fbb0d77ad27604b87181ff7e"
	"5a0e3c9aa4ef5028f424000e3dd089e0db7921349fe71b840cb9372c6a95742cd6c315264eb726020cec55bc7aaf57f4"
)
sha512sums=(
	"d747856d2945f37a30f80173ff03f4944cbd4702510c0e4693e7e58e059c9bba6a17bc7063db7b1a3a2164fca3a7bc9710ba76c8a211eb7ec72b731f974ac10a"
	"dbb54802291438a256147765325be970379ee761f20b85ec31d878d145dcccd209ae7e521ae1ee5981adff0c7f62a50b582c0d47db59d491db07c9df842e635c"
)

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ ${njobs} -ge 8 ]]; then
		njobs=$(( $njobs - 2 ))
	fi
	
	# Building package
	cd ${srcdir}/${pkgname}-${pkgver}
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DRUN_IN_PLACE=0
	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}