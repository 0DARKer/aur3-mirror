# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=freeminer
pkgver=0.4.11.5
pkgrel=2
pkgdesc="An open source sandbox game inspired by Minecraft."
arch=(
	"$CARCH"
)
url="http://freeminer.org/"
license=(
	"GPL3"
	"CCPL:cc-by-sa-3.0"
)

depends=(
	"irrlicht"
	"leveldb"
	"libvorbis"
	"openal"
	"sqlite"
	"curl"
	"luajit"
	"msgpack"
	"xdg-utils"
	"hicolor-icon-theme"
)
optdepends=()
makedepends=(
	"cmake"
)
provides=(
	"${pkgname}=${pkgver}"
)
conflicts=(
	"${pkgname}-git"
)
replaces=(
	"${pkgname}-git"
)

install=${pkgname}.install

source=(
	"https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.zip"
	"${pkgname}.install"
)
sha384sums=(
	"6835dff76b302ccae4c9148b1419f19aadcdc325efcb67f36203704ed570666df5d60c87fbb0d77ad27604b87181ff7e"
	"5665e644e68a0b05d5c9b425f8d37feb554819a9d4de5b716c7f67f924e2bc23a381988e8461c04ddeca42a6a2a3a52b"
)
sha512sums=(
	"d747856d2945f37a30f80173ff03f4944cbd4702510c0e4693e7e58e059c9bba6a17bc7063db7b1a3a2164fca3a7bc9710ba76c8a211eb7ec72b731f974ac10a"
	"d590345e9b87e4350b7420eebf8f69e4ad65a53415257573b569ed1c85568a40b4f65fc8df5925f7c03af75d340c52a10a4d1389e5d868816aef9711102ebef1"
)

prepare() {
	# Create build directory
	mkdir -p ${srcdir}/build
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ ${njobs} -ge 8 ]]; then
		njobs=$(( ${njobs} - 2 ))
	fi
	
	# Building package
	cd ${srcdir}/build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DRUN_IN_PLACE=0
	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install
}