# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=freeminer
pkgver=0.4.10.4
pkgrel=2
pkgdesc="An open source sandbox game inspired by Minecraft."
arch=(
	"$CARCH"
)
url="http://freeminer.org/"
license=(
	"GPL3"
)

depends=(
	"irrlicht"
	"leveldb"
	"libvorbis"
	"openal"
	"sqlite"
	"curl"
	"luajit"
	"gtk-update-icon-cache"
)
optdepends=()
makedepends=(
	"cmake"
)
provides=(
	"${pkgname}=${pkgver}"
)
conflicts=(
	"${pkgname}"
	"${pkgname}-git"
)
replaces=(
	"${pkgname}"
	"${pkgname}-git"
)

install=${pkgname}.install

source=(
	"https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.zip"
	"${pkgname}.install"
)
md5sums=(
	"f4e07858f80693d24045bc41dfec9ef8"
	"fddfcb7efb5aad784ef04db97bba69e8"
)
sha512sums=(
	"0f20c0230eac3a07858bd1849eab301a2cb7ca86a92a3380e820b52c48a445bb2807d96d37ab5e8a7e43ae77b22ce256fa28646e1c7f646f06f92c81601ac249"
	"dbb54802291438a256147765325be970379ee761f20b85ec31d878d145dcccd209ae7e521ae1ee5981adff0c7f62a50b582c0d47db59d491db07c9df842e635c"
)

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ $(nproc) -ge 8 ]]; then
		njobs=$(( $njobs/2 ))
	fi
	
	# Building package
	cd ${srcdir}/${pkgname}-${pkgver}
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DRUN_IN_PLACE=0
	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}