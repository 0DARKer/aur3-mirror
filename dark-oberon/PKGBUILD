# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=dark-oberon
pkgver=1.0.2rc2
pkgrel=2
pkgdesc="Open source real-time strategy game similar to Warcraft II"
arch=('i686' 'x86_64')
url="http://dark-oberon.sourceforge.net/"
license=('GPL2' 'CCPL:by-nc-nd' 'CCPL:by-nc-sa')
depends=('sdl_mixer' 'mesa' 'glfw')
makedepends=('wget')
source=("${pkgname}.xpm"
        "${pkgname}.desktop")
md5sums=('0db2ff866f8a08126a525b5ffe2ce20f'
         '0dc0ebe83b5c696be8990b8d313ab2d1')

build() {
  cd "${srcdir}"

  # Download the tarball with wget, because it always has a different md5
  wget "http://${pkgname}.cvs.sourceforge.net/viewvc/${pkgname}/${pkgname}.tar.gz"
  tar xfz "${pkgname}.tar.gz"
  cd "${pkgname}/src"

  # Fix the path for data files
  sed -i "s#DATA_DIR=''#DATA_DIR='/usr/share/dark-oberon/'#" create_makefile.sh

  # Compile
  chmod a+x create_makefile.sh
  ./create_makefile.sh || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}"

  # Install binary
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install data files
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  cp -rf dat maps races schemes "${pkgdir}/usr/share/${pkgname}"

  # Install docs
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 ChangeLog Licence README "${pkgdir}/usr/share/doc/${pkgname}"

  # Install pixmap and desktop file
  install -Dm644 "../${pkgname}.xpm" "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
