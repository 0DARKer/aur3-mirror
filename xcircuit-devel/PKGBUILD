# Maintainer: Rikard Falkeborn <rikard.falkeborn@gmail.com>
# Contributor: Alex Tercete <alextercete at gmail.com>

pkgname=xcircuit-devel
pkgver=3.8.52
pkgrel=1
pkgdesc="A program for drawing publishable-quality electrical circuit schematic diagrams (development version)"
arch=('i686' 'x86_64')
url="http://opencircuitdesign.com/xcircuit/"
depends=('tk' 'libxpm' 'zlib')
makedepends=('chrpath')
license=('GPL2')
conflicts=('xcircuit')
source=("http://opencircuitdesign.com/xcircuit/archive/xcircuit-${pkgver}.tgz")
md5sums=('26356e1d1423faf33f5555443c59da82')

build() {
  cd "${srcdir}/xcircuit-${pkgver}"
  ./configure --prefix=/usr --with-tcl=/usr/lib --with-tk=/usr/lib --mandir=/usr/share/man
  make
}

package() {
  cd $srcdir/xcircuit-$pkgver
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/share/man/man1"
  mv "$pkgdir/usr/lib/xcircuit-3.8/man/"* "$pkgdir/usr/share/man/man1/"
  rm -rf "$pkgdir/usr/lib/xcircuit-3.8/man/"
  chrpath -d "$pkgdir/usr/lib/xcircuit-3.8/xcircexec"
}
