# Maintainer: Det <nimetonmaili at gmail a-dot com>

pkgname=lastpass
pkgver=3.1.0  # Version of the universal installer: https://lastpass.com/misc_download2.php
_chromver=3.1.1-1  # The actual extensions' versions
_ffver=3.1.0-1
_opver=3.1.1-1
pkgrel=1
pkgdesc="The Universal LastPass installer for Firefox, Chrome, and Opera"
arch=('i686' 'x86_64')
url="https://lastpass.com"
license=('custom')
optdepends=('chromium: for Chromium'
            'chromium-dev: for Chromium (Dev Channel) (AUR)'
            'firefox: for Mozilla Firefox'
            'firefox-beta-bin: for Mozilla Firefox (Beta) (AUR)'
            'firefox-nightly: for Mozilla Firefox (Nightly) (AUR)'
            'google-chrome: for Google Chrome (AUR)'
            'google-chrome-beta: for Google Chrome (Beta Channel) (AUR)'
            'google-chrome-dev: for Google Chrome (Dev Channel) (AUR)'
            'opera: for Opera'
            'opera-next: for Opera Next (AUR)')
conflicts=('lastpass-plugin-opera')
install=${pkgname}.install
source=("lplinux_${pkgver}.tar.bz2::${url}/lplinux.tar.bz2"
        'com.lastpass.nplastpass.json'
        'lastpass_policy.json'
        # Chrome
        #"lpchrome_linux_${_chromver}.crx::${url}/lpchrome_linux.crx"
        "http://fs1.d-h.st/download/00101/5st/lpchrome_linux_${_chromver}.crx"
        # Firefox
        "lp_linux_${_ffver}.xpi::${url}/lp_linux.xpi"
        'profiles.ini'
        # Opera
        "lastpass-${_opver}.oex::${url}/lpopera.oex"
        'prefs.dat'
        'widgets.dat')
noextract=("lpchrome_linux_${_chromver}.crx" # Chrome
           "lp_linux_${_ffver}.xpi"          # Firefox
           "lastpass-${_opver}.oex")         # Opera
md5sums=('eedb4dcf4f44ba80c4531514aec4d38f'
         '3b41376936fa483b3818e4b41175ce4f'
         '4b8db7768ed5135b2ab8ece44f1176d6'
         '36e4f4b75cb53261ced84fffee026f4d'
         'd033f0068fbdb78c7b4049513fbb31bc'
         'd0f555a644484baccf649f7969794ece'
         'ea2c31e680ac317b66cd8d011c56d44e'
         'c373651995c0f8ef332439f2ed3b0670'
         '1a6927e679ac378ea0583662d41f6c46')

# Custom functions for makepkg
_note() {
    printf "${RED}==>${ALL_OFF}${RED} NOTE: ${1}${ALL_OFF}\n"
}

_read_msg2() {
    read -p "${BLUE}  ->${YELLOW} ${1} ${ALL_OFF}${BOLD}${2}${ALL_OFF}"
}

prepare() {
    # Since we're installing to $HOME in .install, we need checks such as this to
    # see whether some clever guy is building with "--asroot"
    if [ "${USER}" = 'root' ]; then
        _note "Makepkg is running as root. The extensions will not be available to a regular user."
        _read_msg2 "Continue?" "(Y/n)"

        if [[ ${REPLY} != [yY] ]]; then
            error "Aborted by user! Exiting..."
            exit 1
        fi
    fi

    # Write extension versions and user vars to .install
    sed -e "s/_ffver= *[^ ]*/_ffver=${_ffver}/" \
        -e "s/_opver= *[^ ]*/_opver=${_opver}/" \
        -e "s/_user= *[^ ]*/_user=${USER}/" \
        -e "s,_HOME= *[^ ]*,_HOME=/home/${USER}/," \
        -i "${startdir}"/${pkgname}.install

    # The Firefox profile(s) might not exist
    if [ -f ~/.mozilla/firefox/profiles.ini ]; then
        _ffprofile=$(sed -n 's/^Path=//p' ~/.mozilla/firefox/profiles.ini | grep default || true)
        if [ ${_ffprofile} ]; then
            sed "s,_mozilla= *[^ ]*,_mozilla=/home/${USER}/.mozilla/firefox/${_ffprofile}/," \
                -i "${startdir}"/${pkgname}.install
        else  # Default naming
            sed "s,_mozilla= *[^ ]*,_mozilla=/home/${USER}/.mozilla/firefox/lastpass.default/," \
                -i "${startdir}"/${pkgname}.install
        fi
    else  # Leave empty
        sed "s/_mozilla= *[^ ]*/_mozilla=-/" -i "${startdir}"/${pkgname}.install
    fi
}

_chrome_package() {
    # 64-bit?
    if [ "${CARCH}" = 'x86_64' ]; then
        _64=64
    fi

    # Install to single place for linking
    install -Dm644 lpchrome_linux_${_chromver}.crx "${pkgdir}"/usr/share/lastpass/lpchrome_linux_${_chromver}.crx
    install -Dm644 nplastpass${_64} "${pkgdir}"/etc/opt/chrome/native-messaging-hosts/nplastpass${_64}

    # 64-bit?
    sed "s,/nplastpass,/nplastpass${_64}," -i com.lastpass.nplastpass.json

    # JSONs
    for i in opt/chrome chromium chromium-dev; do
        # Messaging host
        install -Dm644 com.lastpass.nplastpass.json "${pkgdir}"/etc/${i}/native-messaging-hosts/com.lastpass.nplastpass.json

        # Allow silent installation since Chrome v21: http://www.chromium.org/administrators/policy-list-3#ExtensionInstallSources
        install -Dm644 lastpass_policy.json "${pkgdir}"/etc/${i}/policies/managed/lastpass_policy.json
    done
}

_firefox_package() {
    # Extension + profiles.ini go to $HOME, so do this in .install
    for i in lp_linux_${_ffver}.xpi profiles.ini; do
        install -Dm644 ${i} "${pkgdir}"/usr/share/lastpass/${i}
    done

    ## Binary plugin
    #for i in opera opera-next; do
    #    install -Dm644 libnplastpass${_64}.so "${pkgdir}"/usr/lib/mozilla/plugins/libnplastpass${_64}.so
    #done
}

_opera_package() {
    # Same for Operas
    install -m644 lastpass-${_opver}.oex {pref,widget}s.dat "${pkgdir}"/usr/share/lastpass/

    ## Binary plugin
    #for i in opera opera-next; do
    #    install -d "${pkgdir}"/usr/lib/${i}/plugins/
    #    ln -s /usr/lib/mozilla/plugins/libnplastpass${_64}.so "${pkgdir}"/usr/lib/${i}/plugins/libnplastpass${_64}.so
    #done
}

package() {
    msg2 "Installing for Google Chromes/Chromiums"
    _chrome_package

    msg2 "Installing for Mozilla Firefoxes"
    _firefox_package

    msg2 "Installing for Operas"
    _opera_package
}
