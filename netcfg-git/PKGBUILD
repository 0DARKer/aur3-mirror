# Maintainer: Army
# Contributor: James Rayner <james@archlinux.org> 

pkgname=netcfg-git
pkgver=20120609
pkgrel=1
pkgdesc="Network configuration and profile scripts"
url="http://projects.archlinux.org/netcfg.git/"
license=("BSD")
arch=('any')
backup=('etc/iftab' 'etc/conf.d/netcfg')
depends=('dhcpcd' 'iproute2')
optdepends=('dialog: for the menu based profile and wifi selectors'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through rc.d/net-auto-wired'
            'wpa_actiond: for automatic wireless connections through rc.d/net-auto-wireless'
            'wireless_tools: for interface renaming through net-rename'
            'ifenslave: for bond connections'
            'bridge-utils: for bridge connections'
           )
makedepends=('git' 'asciidoc')
conflicts=(netcfg)
provides=(netcfg)

_gitroot="git://projects.archlinux.org/netcfg.git"
_gitname="netcfg"

build() {
	cd ${srcdir}
	msg "Connecting to ${_gitroot} GIT server...."
	if [[ -d ${srcdir}/${_gitname} ]] ; then
		cd ${_gitname}
		git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot}
	fi
	msg "GIT checkout done"

	rm -rf "${srcdir}/${_gitname}-build"
	cp -a "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
}

package() {
	cd "${srcdir}/${_gitname}-build"
	make DESTDIR=${pkgdir} install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	# Shell Completion
	install -D -m644 contrib/bash-completion "$pkgdir/etc/bash_completion.d/netcfg"
	install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netcfg"
}
