# Maintainer: James Rayner <james@archlinux.org> 

pkgname=netcfg-git
pkgver=20110403
pkgrel=1
pkgdesc="Network configuration and profile scripts"
url="http://archlinux.org"
license=("BSD")
arch=('any')
backup=('etc/iftab')
depends=('wireless_tools' 'coreutils' 'wpa_supplicant' 'net-tools' 'dhcpcd' 'iproute2')
optdepends=("dialog: Required for menu based profile selector"
            "wpa_actiond: Required for automatic wireless connection with rc.d/net-auto-wireless"
            "ifplugd: Required for automatic wired connection with rc.d/net-auto-wired")
conflicts=(netcfg)
provides=(netcfg)

_gitname="netcfg"
_gitroot="git://projects.archlinux.org/${_gitname}.git"

build() {
    cd ${srcdir}
    msg "Connecting to ${_gitroot} GIT server...."
    if [[ -d ${srcdir}/${_gitname} ]] ; then
        cd ${_gitname}
        git pull origin
        msg "The local files are updated."
    else
        git clone ${_gitroot}
    fi
    msg "GIT checkout done"
}

package() {
    cd ${srcdir}/${_gitname}
    sed -i 's|/man/|/share/man/|' Makefile
    make DESTDIR=${pkgdir} install
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/netcfg/LICENSE
    ln -s /usr/bin/netcfg2 ${pkgdir}/usr/bin/netcfg 
}



# vim:set ts=4 sw=4 et:


