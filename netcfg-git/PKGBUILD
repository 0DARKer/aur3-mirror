# Maintainer: Army
# Contributor: James Rayner <james@archlinux.org> 

pkgname=netcfg-git
pkgver=20120305
pkgrel=1
pkgdesc="Network configuration and profile scripts"
url="https://github.com/joukewitteveen/netcfg"
license=("BSD")
arch=('any')
backup=('etc/iftab')
depends=('dhcpcd' 'iproute2')
optdepends=(
"dialog: for interactive menu"
"dhclient: for DHCPv6"
"ifenslave: for bonding support"
"bridge-utils: for bridge support"
"wpa_supplicant: for wireless support"
"wireless_tools: for net_rename"
"wpa_actiond: for automatic wireless connection"
"ifplugd: for automatic wired connection"
)
makedepends=(asciidoc)
conflicts=(netcfg)
provides=(netcfg)

_gitroot="git://github.com/joukewitteveen/netcfg.git"
_gitname="netcfg"

build() {
	cd ${srcdir}
	msg "Connecting to ${_gitroot} GIT server...."
	if [[ -d ${srcdir}/${_gitname} ]] ; then
		cd ${_gitname}
		git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot}
	fi
	msg "GIT checkout done"

	rm -rf "${srcdir}/${_gitname}-build"
	cp -a "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
}

package() {
	cd "${srcdir}/${_gitname}-build"
	make DESTDIR=${pkgdir} install
	install -D -m644 "${srcdir}/${_gitname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
