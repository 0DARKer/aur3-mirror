# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=gnotero
pkgver=0.42
pkgrel=1
pkgdesc="A Gtk application which provides access to your Zotero references"
arch=(any)
url="http://www.cogsci.nl/software/gnotero"
license=('GPL')
depends=('python2' 'pygtk' 'pdftk' 'desktop-file-utils')
optdepends=(
  'gnote: for integration with gnote'
  'python-levenshtein: for integration with gnote'
)
source=(http://www.cogsci.nl/files/software/${pkgname}/${pkgname}_${pkgver}-1.tar.gz)
md5sums=('2516ecd8851ef70747fcbb902cdf46a0')
sha1sums=('69b306506e03ea5ab0bdf633ad0d0af5b54e441d')
install=gnotero.install

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root=$pkgdir

  # Patch to use python2
  find $pkgdir -type f \( -name '*.py' -or -executable \) -exec \
    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    \{\} +
}
