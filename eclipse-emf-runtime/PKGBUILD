# Contributor: Jesus Jerez <jerezmoreno@gmail.com>

pkgname=eclipse-emf-runtime
pkgver=2.6.1
_pkgbuild=R201009141218
pkgrel=1
pkgdesc="EMF and XSD runtime only for the Eclipse platform"
url="http://www.eclipse.org/modeling/emf/"
arch=('any')
license=('EPL')
depends=('eclipse')
makedepends=('unzip')
conflicts=('eclipse-emf')
provides=('eclipse-emf')
changelog=$pkgname.changelog
source=("http://www.mirrorservice.org/sites/download.eclipse.org/eclipseMirror/modeling/emf/emf/downloads/drops/2.6.x/${_pkgbuild}/emf-runtime-${pkgver}.zip"
        "http://www.mirrorservice.org/sites/download.eclipse.org/eclipseMirror/modeling/emf/emf/downloads/drops/2.6.x/${_pkgbuild}/xsd-runtime-${pkgver}.zip")
md5sums=('3a0e91bb38795f6b8d707bc1107c8d8b'
         '79dd1d68019a4ef772d327a8b67109f2')
sha256sums=('49978cee5bae3b9bf7bd235a3a437c945beae5349ca1cf06e5d65e955497812d'
            'b9bfea2eb8a960b6aeafd5c8fb3490f484587b9649ed5fb612dd85b0e7cbf346')

build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}/eclipse

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
