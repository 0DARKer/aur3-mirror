# Maintainer: Rubens Brand√£o <construidor[at]gmail[dot]com>
pkgname=mosix
pkgver=4.2.0
pkgrel=1
pkgdesc="MOSIX can manage Linux clusters and multi-cluster private Clouds."
url="http://www.mosix.cs.huji.ac.il"
arch=('x86_64')
license=('custom:Mosix')
depends=('glibc')
optdepends=('lzop')
makedepends=()
conflicts=('mosix-tools' 'linux-mosix' 'linux-mosix-headers')
replaces=()
backup=()
source=(
		"http://www.mosix.cs.huji.ac.il/mos4/MOSIX-${pkgver}.tbz"
		'mosix.service'
		)
md5sums=(
		'36271230c7e34839daa1922b06fd888b'
		'5aeaeff2a00855c14ee1de1a69b8593b'
		)



package() {	
  cd "${srcdir}/${pkgname}-${pkgver}"
	
  install -D -m 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  mkdir -p "${pkgdir}/usr/share/man/"
  cp -ra man/* "${pkgdir}/usr/share/man/"
  
  for file in mos_autoconf mos_checkconf mos_in_job mosconf* mosd mosixd mospostald mosremoted mossetpe 
  do
	install -D -m 755 "${file}" "${pkgdir}/usr/sbin/${file}"
  done
  
  for file in mosmon mosnative mospipe mostestload mosenv mosctl
  do
	install -D -m 755 "${file}" "${pkgdir}/usr/bin/${file}"
  done
  
  for file in moskillall mosmigrate mosps mosrun mosbestnode mostimeof
  do
	install -D -m 755 "${file}" "${pkgdir}/usr/bin/${file}"
	chmod u+s "${pkgdir}/usr/bin/${file}"
  done
  
  install -D -m 644 "${srcdir}/mosix.service" "${pkgdir}/usr/lib/systemd/system/mosix.service"

  mkdir -p -m 755 "${pkgdir}/etc/mosix/partners"
  mkdir -p -m 755 "${pkgdir}/var/mosix"
  ln -s "/var/mosix" "${pkgdir}/etc/mosix/var"
  

}

# vim:set ts=2 sw=2 et:
