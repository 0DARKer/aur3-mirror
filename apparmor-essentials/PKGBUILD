pkgname=apparmor-essentials
_pkgname=apparmor
pkgver=2.9.0
_pkgsver=${pkgver%.*}
pkgrel=1
pkgdesc="AppArmor Linux application security framework - mandatory access control for programs"
arch=('i686' 'x86_64')
url="https://launchpad.net/apparmor"
license=('GPL2' 'LGPL2.1')
depends=('python>=3.3' 'pam' 'perl-locale-gettext' 'perl-term-readkey' 'perl-file-tail' 'perl-rpc-xml')
provides=('libapparmor.so' 'apparmor' 'apparmor-parser' 'apparmor-libapparmor' 'apparmor-utils' 'apparmor-pam' 'apparmor-vim')
conflicts=('apparmor' 'apparmor-parser' 'apparmor-libapparmor' 'apparmor-utils' 'apparmor-pam' 'apparmor-vim')
makedepends=('swig' 'linux-api-headers' 'libcap' 'bzr')
options=('!libtool' '!staticlibs' '!emptydirs')
source=("https://launchpad.net/$_pkgname/$_pkgsver/$pkgver/+download/$_pkgname-$pkgver.tar.gz"{,.asc}
        'secdir.patch'
        'build-process-fix-hardcoded-paths.patch'
        'lightweight-docs.patch'
        'no-static-ldflags.patch')
sha256sums=('782df74c8a7a8a5302b4ad0d00184a7e623ef0631c1b8a16a1d92a968e4b4b6b'
            'SKIP'
            '0da696003544a710bc26ffb769b5333fe01434181f07faa1a428b9a7e104470c'
            '793ef6b088393a241b85950e5a59276f88f7deee1079963ff67706b46d2fa108'
            '6b3ac4a689d854f2053b98550f98b75774bec10cb09de31af11f3ace99fc6152'
            '269e313c32a9240ff7d27900edfb521c143ea37fd236823a95d2b291f14932f9')
backup=('etc/apparmor/'{easyprof,logprof,notify,parser,subdomain}.conf
        'etc/apparmor/severity.db')

export PYTHON=/usr/bin/python3
export MAKEFLAGS+=" V=1"

prepare() {
  cd "$_pkgname-$pkgver"
  patch -Np2 -i "$srcdir/secdir.patch"
  patch -Np1 -i "$srcdir/build-process-fix-hardcoded-paths.patch"
  patch -Np1 -i "$srcdir/lightweight-docs.patch"
  patch -Np1 -i "$srcdir/no-static-ldflags.patch"
}

build() {
  cd "$_pkgname-$pkgver/libraries/libapparmor"

  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc --sbindir=/usr/bin \
    --enable-shared --with-pic \
    --with-perl --with-python

  make
  make -C ../../utils
  make -C ../../parser
  make -C ../../changehat/pam_apparmor
}

check() {
  cd "$_pkgname-$pkgver"
  make -k -C "$srcdir/$_pkgname/parser" -j1 check || true
}

package() {
  export MAKEFLAGS+=" DESTDIR=${pkgdir}"

  cd "$_pkgname-$pkgver"
  make -C libraries/libapparmor install
  make -C utils VIM_INSTALL_PATH="$pkgdir/usr/share/vim/vimfiles/syntax" install
  make -C parser install
  make -C changehat/pam_apparmor install
}

# vim:set ts=2 sw=2 et:
