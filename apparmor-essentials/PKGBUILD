pkgname=apparmor-essentials
_pkgname=apparmor
pkgver=2.8.0
_pkgver=$pkgver
_pkgsver=${pkgver%.*}
pkgrel=2
pkgdesc="AppArmor Linux application security framework - mandatory access control for programs"
arch=('i686' 'x86_64')
url="https://launchpad.net/apparmor"
license=('GPL2' 'LGPL2.1')
depends=('python2' 'gcc-libs' 'pam>=1.1.5-3' 'perl-locale-gettext' 'perl-term-readkey' 'perl-file-tail' 'perl-rpc-xml')
# Cover the following AUR packages, we are only (?) missing the Ruby bindings:
provides=('apparmor' 'apparmor-parser' 'apparmor-libapparmor' 'apparmor-utils' 'apparmor-pam' 'apparmor-vim')
conflicts=('apparmor' 'apparmor-parser' 'apparmor-libapparmor' 'apparmor-utils' 'apparmor-pam' 'apparmor-vim')
makedepends=('swig' 'chrpath' 'linux-api-headers>=3.1.0' 'libcap')
options=('!libtool' '!emptydirs')
        #https://launchpad.net/apparmor/2.8/2.8.0/+download/apparmor-2.8.0.tar.gz
source=("https://launchpad.net/$_pkgname/$_pkgsver/$_pkgver/+download/$_pkgname-$_pkgver.tar.gz"{,.asc}
        'dlopen.c'
        'libs-cloexec.patch')
backup=('etc/apparmor/subdomain.conf'
        'etc/apparmor/severity.db'
        'etc/apparmor/parser.conf'
        'etc/apparmor/logprof.conf'
        'etc/apparmor/notify.conf')
md5sums=('eaf90c52992df3d205a753b2933595fe'
        '3a106a426730a7d80224c7b6ac298ace'
        '103147af240a0a537182d20a456f1099'
        'a80f997a6b9713f0680050aded732d3e')
sha256sums=('03e2e91fac17694635d25d7482e46db69320cd844590740073cf5fdfdd5379c6'
        'e127b1f825382f7698710885a46b02ef4b0cf81da0d7c646e874f17486ed0980'
        'd1758f84a69173a852e598fa55e69df0d73b8b62c6993b0ba04aa21d539213e6'
        'd5a07ef08387b57f9311581a98bca69fc4d7574ef3e50f1a5ac55b6c69b38deb')

build() {

  #LDFLAGS+=" -Wl,--fatal-warnings,--no-undefined"

  _perl_bindir='/usr/bin/core_perl'
  export PYTHON='/usr/bin/python2'
  export MAKEFLAGS+=" POD2MAN=${_perl_bindir}/pod2man POD2HTML=${_perl_bindir}/pod2html PROVE=${_perl_bindir}/prove"

  cd "$srcdir/$_pkgname-$_pkgver" && patch -Np1 -i "$srcdir/libs-cloexec.patch"

  cd "$srcdir/$_pkgname-$_pkgver/libraries/libapparmor"

  # We need perl and python anyway, may as well build the bindings...

  # Force py2
  mkdir "$srcdir/python2-path"
  ln -s /usr/bin/python2 "$srcdir/python2-path/python"
  export PATH="$srcdir/python2-path:$PATH"

  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc \
    --disable-static --enable-shared --with-pic \
    --with-perl --with-python

  sed -i "s|pdflatex|true|g" ../../parser/Makefile # texlive-bin is huge.
  sed -i "s|python|python2|g" ../../utils/vim/Makefile ../../utils/aa-easyprof

  make
  make -C ../../utils
  make -C ../../parser \
    AARE_LDFLAGS="$LDFLAGS -lstdc++" AAREOBJECTS=libapparmor_re/libapparmor_re.a # Fix static linking.
  make -C ../../changehat/pam_apparmor LIBS="$LDFLAGS -lpam -lapparmor"

}

check() {
  # Check manually for unexpected failures:
  make -k -C "$srcdir/$_pkgname-$_pkgver/parser" -j1 check || true
}

package() {

  export MAKEFLAGS+=" DESTDIR=${pkgdir}"

  cd "$srcdir/$_pkgname-$_pkgver/libraries/libapparmor"

  make install
  make -C ../../utils install
  make -C ../../parser \
    AARE_LDFLAGS="$LDFLAGS -Wl,--as-needed -lstdc++" AAREOBJECTS=libapparmor_re/libapparmor_re.a install
  make -C ../../changehat/pam_apparmor SECDIR="$pkgdir/usr/lib/security" install

  mkdir -m 0755 -p "$pkgdir/usr/share/vim/vimfiles/syntax" && ln -sf '/usr/share/apparmor/apparmor.vim' "$pkgdir/usr/share/vim/vimfiles/syntax/apparmor.vim"
  cp 'swig/perl/LibAppArmor.pm' "$pkgdir/usr/lib/perl5/vendor_perl"
  sed -i '1s/python$/python2/' "$pkgdir/usr/sbin/aa-status"
  chrpath -d "$pkgdir/usr/lib/perl5/vendor_perl/auto/LibAppArmor/LibAppArmor.so"
  rm -rv "$pkgdir/lib/apparmor" # Nothing useful in there.

  ${CC:-gcc} ${CFLAGS} "$srcdir/dlopen.c" -o dlopen -ldl

  for module in "$pkgdir"/usr/lib/security/*.so ; do
    if ! env LD_PRELOAD='' LD_LIBRARY_PATH="$pkgdir/usr/lib" ./dlopen ${module} ; then
      error "Sanity check failed: ${module} cannot be loaded, exiting."
      exit 1
    fi
  done

}

# vim:set ts=2 sw=2 et:
