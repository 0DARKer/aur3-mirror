# Maintainer.: Sietse van der Molen <sietse [at] vdmolen.eu>
# Maintainer.: Lucky <aur.archlinux.org [at] lucky.take0ver [dot] net>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=anomos-git
_pkgname="${pkgname%-*}"
pkgver=1.0.0rc1
pkgrel=1
pkgdesc="Anonymous, Encrypted BitTorrent"
url="http://anomos.info"
license=("GPL3")
arch=("i686" "x86_64")
depends=("python2" "openssl" "python2-m2crypto")
makedepends=("git")
source=("anomos.desktop"
        "anomos.png"
        "git://anomos.info/git/anomos.git")
md5sums=("e1ed7f40279d2397e61a33125cee852f"
         "fb8e695aeab268834c9d7790be3be8a5"
         "SKIP")

_gitname="${_pkgname}"

build() {
  cd "${_gitname}"
  python2 setup.py build
}

pkgver() {
  cd "${_gitname}"
  git describe --always | sed 's|-|.|g' | sed 's|anomos\.||g'
}

package() {
  cd "${srcdir}/anomos"
  python2 setup.py install --prefix="${pkgdir}/usr"

  install -Dm644 "${srcdir}/anomos.png" "${pkgdir}/usr/share/pixmaps/anomos.png"
  install -Dm644 "${srcdir}/anomos.desktop" "${pkgdir}/usr/share/applications/anomos.desktop"

  #remove the .py from the end of the binaries
  cd "${pkgdir}/usr/bin"
  rename .py '' *.py
}
