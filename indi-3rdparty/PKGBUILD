# Maintainer: Marco Gulino <marco.gulino@gmail.com>

pkgname=indi-3rdparty
pkgver=0.9.8
pkgrel=0
pkgdesc="The INDI 3rd party drivers collection"
url="http://www.indilib.org"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libnova' 'cfitsio' 'boost-libs' 'libindi' 'qsiapi' 'dcraw')
makedepends=('pkgconfig' 'cmake' 'boost')
#options=('!libtool')
source=("http://www.indilib.org/jdownloads/Source/libindi_3rdparty_${pkgver}.tar.gz")
md5sums=('0acdb38f7af0dbe7b57e7f32e13d2702')                                                                                                                                                                                                                          

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../libindi_3rdparty_${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_SBIG=Off \
    -DWITH_FLI=Off \
    -DWITH_APOGEE=Off \
    -DWITH_FISHCAMP=Off \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
  # cleanup: conflicting files already installed by qsiapi
  rm -f ${pkgdir}/usr/bin/qsiapitest
  rm -f ${pkgdir}/usr/include/QSIError.h
  rm -f ${pkgdir}/usr/include/qsiapi.h
  rm -f ${pkgdir}/usr/lib/libqsiapi.so
}
