# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname="mesa-demos"
pkgname="glxinfo"
pkgver=8.1.0
pkgrel=3
arch=("i686" "x86_64")
pkgdesc="Display info about a GLX extension and OpenGL renderer. Standalone binary from the mesa-demos project."
url="http://www.mesa3d.org/"
license=("MIT")
depends=("libgl")
makedepends=("glew")
conflicts=("$_pkgname")
source=("ftp://ftp.freedesktop.org/pub/mesa/demos/$pkgver/$_pkgname-$pkgver.tar.bz2")
sha256sums=("9703fa0646b32a1e68d2abf5628f936f77bf97c69ffcaac90de380820a87a828")

build() {
	cd "$srcdir/$_pkgname-$pkgver"
	mkdir -p build
	cd build
	../configure --prefix=/usr
	make -C src/xdemos bin_PROGRAMS="$pkgname"
}

package() {
	cd "$srcdir/$_pkgname-$pkgver"
	cd build
	make DESTDIR="$pkgdir" -C src/xdemos bin_PROGRAMS="$pkgname" install
}
