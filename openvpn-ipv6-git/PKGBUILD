# Maintainer: crazyb0y < bjin1990 at gmail dot com>

pkgname=openvpn-ipv6-git
pkgver=20100526
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network) with IPv6 patch"
arch=(i686 x86_64)
url="http://github.com/jjo/openvpn-ipv6/"
depends=('openssl' 'lzo2')
license=('custom')
backup=(usr/share/openvpn/easy-rsa/vars
        usr/share/openvpn/easy-rsa/openssl.cnf
        etc/conf.d/openvpn-tapdev)
source=(openvpn.rc
        openvpn-tapdev.rc
        openvpn-tapdev.conf)
md5sums=('a3809b9727f0c2af2d0770f5c7442db2'
         'd2c48e970088d679dd3c2afd914ff731'
         '722f483c9e3ce2ec66d3301aaf7cf3d5')
makedepends=('git' 'autoconf' 'automake' 'make' 'gcc')
provides=('openvpn')
conflicts=('openvpn')


_gitroot="git://github.com/jjo/openvpn-ipv6.git"
_gitname="openvpn-ipv6"


build() {
  cd $srcdir
  msg "Connecting to GIT server...."

  if [ -d "$srcdir/$_gitname" ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  # Build and install openvpn
  cd $srcdir/$_gitname
  autoreconf -i -v
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\"" ./configure --prefix=/usr --enable-password-save --mandir=/usr/share/man
  make || return 1
  make DESTDIR=$pkgdir install || return 1
  install -d -m755 $pkgdir/etc/openvpn
  # Install examples
  install -d -m755 $pkgdir/usr/share/openvpn
  cp -r sample-config-files $pkgdir/usr/share/openvpn/examples || return 1
  find $pkgdir/usr/share/openvpn -type f -exec chmod 644 {} \;
  find $pkgdir/usr/share/openvpn -type d -exec chmod 755 {} \;
  # Install license
  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING || return 1
  # Build and install plugins
  for plug in auth-pam down-root; do
    cd $srcdir/$_gitname/plugin/$plug
    make || return 1 
    install -D -m755 openvpn-$plug.so $pkgdir/usr/lib/openvpn/openvpn-$plug.so || return 1
  done
  # Install easy-rsa
  cd $srcdir/$_gitname
  make -C easy-rsa/2.0 install DESTDIR=$pkgdir PREFIX=usr/share/openvpn/easy-rsa || return 1
  # Install rc scripts
  install -D -m755 $srcdir/openvpn.rc $pkgdir/etc/rc.d/openvpn || return 1
  install -D -m755 $srcdir/openvpn-tapdev.rc $pkgdir/etc/rc.d/openvpn-tapdev || return 1
  install -D -m644 $srcdir/openvpn-tapdev.conf $pkgdir/etc/conf.d/openvpn-tapdev || return 1
}
