_target=arm-linux-gnueabi
_sysroot=/usr/${_target}

pkgname=${_target}-glib2
pkgver=2.38.0
pkgrel=2
arch=(any)
pkgdesc="Common C routines used by GTK+ and other libs (${_target})"
depends=(${_target}-gettext ${_target}-zlib ${_target}-libffi)
makedepends=(${_target}-gcc python2 automake)
license=(LGPL)
options=('!libtool' '!strip' '!buildflags')
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz
        'revert-warn-glib-compile-schemas.patch')
md5sums=('c50d2805a76763e9b4cc4385d4ea215d'
	'cde8e1a9d18918be0d90af67a981f184')

prepare() {
  cd "$srcdir/glib-$pkgver"
touch arm_cache.conf  
cat >> ./arm_cache.conf << EOF

am_cv_func_working_getline=no
glib_cv_stack_grows=no
ac_cv_func_posix_getpwuid_r=yes
glib_cv_uscore=yes
# glib fix
ac_cv_android_arm=yes
ac_cv_func_posix_getgrgid_r=no

# lucasvr
gt_cv_have_gettext=yes
gt_cv_func_ngettext_libc=yes
gt_cv_func_dgettext_libc=yes
gt_cv_func_dgettext_libintl=yes
ac_cv_func_bind_textdomain_codeset=yes

EOF
  
  patch -Rp1 -i ../revert-warn-glib-compile-schemas.patch
}

build()
{
  cd "$srcdir/glib-$pkgver"

  export PKG_CONFIG_PATH="${_sysroot}/lib/pkgconfig:${_sysroot}/share/pkgconfig"
  unset LDFLAGS
  
  ./autogen.sh
  
  PYTHON="/usr/bin/python2" \
  ./configure \
    --prefix=${_sysroot} \
    --host=${_target} \
    --enable-static \
    --enable-shared \
    --cache-file=./arm_cache.conf \
    --disable-java --disable-openmp --without-libiconv-prefix --without-libintl-prefix --without-libglib-2.0-prefix --without-libcroco-0.6-prefix --with-included-libxml --without-libncurses-prefix --without-libtermcap-prefix --without-libcurses-prefix --without-libexpat-prefix --without-emacs
  
  make
}

package() {
  cd "$srcdir/glib-$pkgver"
  make DESTDIR="$pkgdir" install
}

