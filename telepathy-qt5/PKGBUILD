# Maintainer: Antonio Rojas
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-qt5
pkgver=0.9.4
pkgrel=2
pkgdesc="A library for Qt5-based Telepathy clients"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
license=('LGPL')
depends=('qt5-base' 'telepathy-farstream')
makedepends=('libxslt' 'python2' 'cmake' 'doxygen')
options=('staticlibs')
source=("http://telepathy.freedesktop.org/releases/telepathy-qt/telepathy-qt-$pkgver.tar.gz" 'farstream-0.2.patch')
md5sums=('75b18e151b1c4fab9f149d0eb46a494d'
         '6b2421c59d20d29fece8d5e6445354c7')

prepare() {
  mkdir -p build

  cd telepathy-qt-$pkgver
  patch -p1 -i "$srcdir"/farstream-0.2.patch
}

build() {
  cd build
  cmake ../telepathy-qt-$pkgver \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DPYTHON_EXECUTABLE=/usr/bin/python2 \
      -DQT_QMAKE_EXECUTABLE=qmake-qt5 \
      -DENABLE_EXAMPLES=OFF \
      -DENABLE_TESTS=false \
      -DENABLE_EXPERIMENTAL_SERVICE_SUPPORT=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
