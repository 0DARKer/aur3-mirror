# Maintainer: Stefano Bergamini <bergs at live dot it>
# Contributor: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Quentin Foussette <quentinf7@gmail.com>

pkgname=epson-inkjet-printer-escpr
pkgver=1.4.0
pkgrel=1
pkgdesc="Epson Inkjet Printer Driver (ESC/P-R) for Linux"
arch=('i686' 'x86_64')
url="http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX"
license=('GPL2')
depends=('cups' 'gutenprint')
options=('!libtool')
source=(http://download2.ebz.epson.net/dsc/f/03/00/02/71/40/628a09f1d54c1e3910fd8ee1015505420a0a592a/$pkgname-$pkgver-1lsb3.2.src.rpm)
md5sums=('6a808a463c0ec71949ef8913be41e8d2')
 
prepare() {
  tar zxf "$pkgname-$pkgver-1lsb3.2.tar.gz"
}
 
build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --with-cupsfilterdir=/usr/lib/cups/filter \
              --with-cupsppddir=/usr/share/ppd
  make
}
 
package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
