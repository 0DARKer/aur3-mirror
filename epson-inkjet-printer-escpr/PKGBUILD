# Maintainer: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Quentin Foussette <quentinf7@gmail.com>

pkgname=epson-inkjet-printer-escpr
pkgver=1.1.2
pkgrel=1
pkgdesc="A common Linux printer driver for Epson Multifonction Inkjet Printers."
arch=('i686' 'x86_64')
url="https://www.epson.com"
license=('GPL')
depends=('cups')
source=(http://download.ebz.epson.net/dsc/f/01/00/01/71/01/1be0c8c347ae7c444d7e9565de0a52dc93851288/$pkgname-$pkgver-1lsb3.2.src.rpm)
md5sums=('febe993423487982789ac2e2ef8e9f83')

build() {
  cd "$srcdir"
  tar xzvf "$pkgname-$pkgver-1lsb3.2.tar.gz"
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
