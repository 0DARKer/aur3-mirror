# Maintainer: Stefano Bergamini <bergs@live.it>
# Contributor: Quentin Foussette <quentinf7@gmail.com>
 
pkgname=epson-inkjet-printer-escpr
pkgver=1.2.2
pkgrel=2
pkgdesc="Epson Inkjet Printer Driver (ESC/P-R) for Linux"
arch=('i686' 'x86_64')
url="http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX"
license=('GPL2')
depends=('cups' 'gutenprint')
options=('!libtool')
source=(http://download.ebz.epson.net/dsc/op/stable/SRPMS/$pkgname-$pkgver-1lsb3.2.src.rpm)
md5sums=('7d3a58dd09cf38dd3c35c199af029a28')
 
prepare() {
  tar zxf "$pkgname-$pkgver-1lsb3.2.tar.gz"
}
 
build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --with-cupsfilterdir=/usr/lib/cups/filter \
              --with-cupsppddir=/usr/share/ppd
  make
}
 
package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}