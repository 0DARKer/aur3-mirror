# Maintainer: Vi0L0 <vi0l093@gmail.com>
# GREAT Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

# PKGEXT='.pkg.tar.gz'  # imho time to pack this pkg into tar.xz is too long, unfortunatelly yaourt got problems when ext is different from .pkg.tar.xz - V

pkgname=amdapp-codexl
pkgver=1.4
pkgrel=5728
pkgdesc="CodeXL is an OpenCL and OpenGL Debugger and Profiler, with static OpenCL kernel analyzer"
arch=('x86_64')
url="http://developer.amd.com/tools/heterogeneous-computing/codexl/"
license=('custom')
options=('staticlibs' 'libtool' '!strip' '!upx')
groups=('amdapp')
depends=('libpng12' 'libgl' 'libcl')
source=(http://developer.amd.com/download/AMD_CodeXL_Linux_x86_64_$pkgver.$pkgrel.tar.gz
      CodeXL.sh
      codexl.png
      codexl.desktop)
  md5sums=('3d6f40b7accf5ff721e13afdd1e1ef44'
      'aa9e53ea4c6fca125fbedaf6d968a76a'
      '34792207ff65c7b2314a9bd8cbeb4e1f'
      '6448b135b652852a63ab486eb894639e')

#Install path
_ipath='opt/AMDAPP/CodeXL'

package() {
  mkdir -p "${pkgdir}/${_ipath}"
  cd "${srcdir}/AMD_CodeXL_Linux_x86_64_$pkgver.$pkgrel/"
  cp -a *  "${pkgdir}/${_ipath}"
  install -D -m644 "Legal/CodeXLEndUserLicenseAgreement-Linux.htm" "$pkgdir/usr/share/licenses/$pkgname/EULA.htm"

  cd "${srcdir}"
  install -D -m755 "CodeXL.sh" "$pkgdir/usr/bin/CodeXL"
  install -D -m755 "codexl.png" "$pkgdir/usr/share/pixmaps/codexl.png"
  install -D -m755 "codexl.desktop" "$pkgdir/usr/share/applications/codexl.desktop"
}