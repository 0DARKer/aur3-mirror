# Maintainer: Jerome Lebleu <jerome dot lebleu at mailoo dot org>

pkgname=qlcplus-qt5
_pkgname=qlcplus
pkgver=4.8.0
pkgrel=1
pkgdesc="Q Light Controller Plus - The open DMX lighting desk software for controlling professional lighting fixtures."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/qlcplus"
license=('APACHE')
depends=('qt5-script' 'qt5-multimedia' 'liblo' 'libftdi-compat' 'libsndfile' 'libmad' 'shared-mime-info' 'fftw')
optdepends=('qlcplus-plugin-ola: Open Lighting Architecture plugin')
provides=("$_pkgname=$pkgver")
conflicts=('qlcplus' 'qlc' 'qlc-svn')
install=${_pkgname}.install
source=("http://downloads.sourceforge.net/project/qlcplus/${pkgver}/qlcplus_${pkgver}.tar.gz")
md5sums=('adc68f90f9f3744bb00cecf2729bf45a')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # don't build ola plugin
  sed -i "/ola/s/^/#/" plugins/plugins.pro

  qmake-qt5
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/" install
}
