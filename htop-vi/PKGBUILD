# $Id: PKGBUILD 105144 2012-01-21 21:54:48
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor (v1.0 vi-patch): Evan LeCompte <evanlec@gmail.com>
# Maintainer: Evan LeCompte <evanlec@gmail.com>

pkgname=htop-vi
pkgver=1.0.1
pkgrel=3
pkgdesc="Interactive process viewer patched for vi style scroll keys"
arch=('i686' 'x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
provides=('htop')
conflicts=('htop')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/htop/htop-${pkgver}.tar.gz
	htop-vi.patch)
md5sums=('d3b80d905a6bff03f13896870787f901'
	 'bf2718a2550795ef2e3df1b12b6128c4')

build() {
	cd ${srcdir}/htop-${pkgver}
	patch -p3 -i ../../htop-vi.patch
	sed -i 's|ncursesw/curses.h|curses.h|' RichString.h RichString.c configure 
	sed -i 's|python|python2|' scripts/MakeHeader.py
	./configure --prefix=/usr --enable-unicode --enable-openvz \
	    --enable-vserver 
	make 
}

package() {
	cd ${srcdir}/htop-${pkgver}
	make DESTDIR=${pkgdir} install 
}
