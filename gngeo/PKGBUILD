# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=gngeo
pkgver=0.8
pkgrel=1
pkgdesc="A command-line NeoGeo Emulator."
url="http://gngeo.googlecode.com"
license="custom"
arch=('i686' 'x86_64')

if [ $CARCH == i686 ]; then
  depends=('zlib' 'sdl')
  makedepends=('nasm')
  optdepends=('libgl: OpenGL support for open source gfx drivers'
  	      'nvidia-utils: OpenGL support for NVIDIA drivers'
  	      'catalyst: OpenGL support for AMD/ATI Catalyst drivers')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-zlib' 'lib32-sdl')
  makedepends=('nasm' 'gcc-multilib')
  optdepends=('lib32-libgl: OpenGL support for open source gfx drivers'
  	      'lib32-nvidia-utils: OpenGL support for NVIDIA drivers'
  	      'lib32-catalyst-utils: OpenGL support for AMD/ATI Catalyst drivers')
fi

source=(${url}/files/${pkgname}-${pkgver}.tar.gz)
backup=(usr/share/${pkgname}/sample_gngeorc)
install=gngeo.install
md5sums=('21055885694e9f6cdbaa20be069a4258')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  if [ $CARCH == i686 ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man
  elif [ $CARCH == x86_64 ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --libdir=/usr/lib32 CC='gcc -m32'
  fi
  
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 sample_gngeorc "${pkgdir}/usr/share/${pkgname}/sample_gngeorc"
  # Install licenses (GPL and more)
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
