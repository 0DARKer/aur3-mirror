# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=gngeo
pkgver=0.8
pkgrel=3
pkgdesc="A command-line NeoGeo Emulator."
url="http://gngeo.googlecode.com"
license="custom"
arch=('i686' 'x86_64')

if [ $CARCH == i686 ]; then
  depends=('zlib' 'sdl' 'libgl')
  makedepends=('nasm')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-zlib' 'lib32-sdl' 'lib32-libgl')
  makedepends=('nasm' 'gcc-multilib')
fi

backup=("usr/share/${pkgname}/sample_gngeorc")
install=gngeo.install
source=("${url}/files/${pkgname}-${pkgver}.tar.gz"
	'4dec1ccfb85d.patch'
	'fix-sdl-libs-in-64bit.patch')
md5sums=('21055885694e9f6cdbaa20be069a4258'
	 '9c29bb165d61facebafd3819b57a3076'
	 'f783005f76f5bfdf2d243ee40f51ee00')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # Fix "undefined reference to" 'cpu_68k_mkstate' and 'cpu_z80_mkstate'
  patch -p2 -i "${srcdir}/4dec1ccfb85d.patch"
  
  # /usr/games/lib/gngeo/ -> /usr/share/gngeo/
  sed '/\/usr\/games\/lib\/gngeo/s/\/games\/lib/\/share/' -i sample_gngeorc
  
  # if 64-bit, use '/usr/lib32/' directory and '-m32' flag
  _libdir32=
  if [ $CARCH == x86_64 ]; then
     sed '/\/usr\/lib\/libGL.so/s/lib\//lib32\//' -i sample_gngeorc
     _libdir32='--libdir=/usr/lib32'
     export CC='gcc -m32'
     patch -p1 -i "${srcdir}/fix-sdl-libs-in-64bit.patch"
  fi
  
  ./configure --prefix=/usr --mandir=/usr/share/man ${_libdir32}
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 sample_gngeorc "${pkgdir}/usr/share/${pkgname}/sample_gngeorc"
  # Install licenses (GPL and more)
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
