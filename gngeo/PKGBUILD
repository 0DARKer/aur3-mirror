# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=gngeo
pkgver=0.7
pkgrel=1
pkgdesc="A command-line NeoGeo Emulator."
url="http://code.google.com/p/gngeo/"
license="custom"
arch=('i686' 'x86_64')

if [ $CARCH == i686 ]; then
  depends=('zlib' 'sdl')
  makedepends=('nasm')
  optdepends=('libgl: OpenGL support for open source gfx drivers'
  	      'nvidia-utils: OpenGL support for NVIDIA drivers'
  	      'catalyst: OpenGL support for AMD/ATI Catalyst drivers')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-zlib' 'lib32-sdl')
  makedepends=('nasm' 'gcc-multilib')
  optdepends=('lib32-libgl: OpenGL support for open source gfx drivers'
  	      'lib32-nvidia-utils: OpenGL support for NVIDIA drivers'
  	      'lib32-catalyst-utils: OpenGL support for AMD/ATI Catalyst drivers')
fi

source=(http://download.berlios.de/${pkgname}/${pkgname}-${pkgver}.tar.gz)
install=gngeo.install
md5sums=('a91836e6762bbc28ad164ef94b2d00f8')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  if [ $CARCH == i686 ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man
  elif [ $CARCH == x86_64 ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --libdir=/usr/lib32 CC='gcc -m32'
  fi
  
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  make DESTDIR=${pkgdir} install

  # Install the sample configuration file
  install -m644 sample_gngeorc "${pkgdir}/usr/share/${pkgname}"
  
  # Install licenses
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
