# Maintainer : falkTX <falktx@gmail.com>
# Contributor: SpepS <speps at aur dot archlinux dot org>

_name=cadence
pkgname=$_name-git
pkgver=20120512
pkgrel=1
pkgdesc="A set of tools useful for Audio-related tasks."
arch=(i686 x86_64)
url="http://kxstudio.sourceforge.net/cadence"
license=('GPL')
depends=('python2-pyqt' 'python2-pyliblo' 'pyxdg' 'gtk2' 'fluidsynth')
makedepends=('git')
optdepends=('a2jmidid: alsa to jack midi bridge'
            'pulseaudio: pulseaudio to jack bridge'
            'jack_capture: Render support'
            'rdflib: LADSPA-RDF and LV2 support in Carla'
            'ladish: needed for Claudia to run')
provides=("$_name")
conflicts=("$_name")
options=('!emptydirs')
install="$pkgname.install"

_gitroot="git://repo.or.cz/$_name.git"
_gitname="$_name"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
