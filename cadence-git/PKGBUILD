# Maintainer: falkTX <falktx@gmail.com>

_p=cadence
pkgname=$_p-git
pkgver=20111025
pkgrel=1
pkgdesc="A set of tools useful for Audio-related tasks."
arch=(i686 x86_64)
url="http://kxstudio.sourceforge.net/cadence"
license=('GPL')
depends=('python2-pyqt' 'pyliblo' 'dbus-python' 'gtk2' 'liblo' 'jack' 'pyxdg' 'xdg-utils')
makedepends=('git')
optdepends=('a2jmidid: alsa to jack midi bridge'
            'pulseaudio: pulseaudio to jack bridge'
            'jack_capture: Render support'
            'rdflib: LADSPA-RDF and LV2 support in Carla'
            'ladish: needed for Claudia to run')
provides=("$_p")
conflicts=("$_p")
options=('!emptydirs')
install="$pkgname.install"

_gitroot="git://repo.or.cz/$_p.git"
_gitname="$_p"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
