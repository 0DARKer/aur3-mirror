# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor : falkTX <falktx@gmail.com>

_name=cadence
pkgname=$_name-git
pkgver=20120913
pkgrel=1
pkgdesc="A set of tools useful for audio production."
arch=(i686 x86_64)
url="https://github.com/falkTX/Cadence"
license=('GPL')
depends=('pyqt' 'suil' 'python-dbus' 'python-pyliblo' 'gtk2' 'fluidsynth' 'linuxsampler')
makedepends=('git')
optdepends=('a2jmidid: alsa to jack midi bridge'
            'pulseaudio: pulseaudio to jack bridge'
            'jack_capture: Render support'
            'python-rdflib: LADSPA-RDF support in Carla'
            'ladish: needed for Claudia to run')
provides=("$_name")
conflicts=("$_name")
options=('!emptydirs')
install="$pkgname.install"

_gitroot="git://github.com/falkTX/Cadence.git"
_gitname="$_name"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
