# Maintainer: kfgz <kfgz at interia pl>
# Contributor: hauzer <alokinpwn at gmail dot com>

pkgname=pburn
pkgver=4.3.5
pkgrel=1
pkgdesc="CD/DVD recording program used in Puppy Linux."
arch=('any')
url="http://murga-linux.com/puppy/viewtopic.php?t=23881"
license=('GPL')
depends=('cddetect' 'dvd+rw-tools' 'gtkdialog' 'hotplug2stdout' 'usleep')
makedepends=('pet2tgz')
optdepends=('dvdauthor: for burning video-DVD'
            'ffmpeg: for converting audio/video'
            'normalize: for volume settings on Audio-CD'
            'pfilesearch: for file search'
            'vamps: for shrinking video-DVD'
            'vcdimager: for burning Video-CD'
            'vobcopy: for copying video-DVD'
            'nrg2iso: for converting CD or DVD image generated by Nero Burning Rom to ISO format')

source=(http://www.meownplanet.net/zigbert/${pkgname}-${pkgver}.pet)
md5sums=('be644c4e917851c99e71a62cf96f869c')

build() {
  cd "${srcdir}"
  pet2tgz -i "${srcdir}"/${pkgname}-${pkgver}.pet -o "${srcdir}"/${pkgname}-${pkgver}.tar.gz
  tar -xzf "${srcdir}"/${pkgname}-${pkgver}.tar.gz
}

#_md5sum=dd9d73d7364f5cee1bb757d740f38fd7

#build() {
#  cd "${srcdir}"
#  if [ -e ${pkgname}-${pkgver}.pet ]; then
#    if [ ${_md5sum} != `md5sum ${pkgname}-${pkgver}.pet | awk '{print $1}'` ]; then error "Md5sum is incorrect!" && exit; fi
#    pet2tgz -i "${srcdir}"/${pkgname}-${pkgver}.pet -o "${srcdir}"/${pkgname}-${pkgver}.tar.gz
#    tar -xzf "${srcdir}"/${pkgname}-${pkgver}.tar.gz
#  else
#    #wget --user=puppy --password=linux http://www.meownplanet.net/zigbert/${pkgname}-${pkgver}.pet
#    wget http://www.meownplanet.net/zigbert/${pkgname}-${pkgver}.pet
#    #wget http://kfgz.mydevil.net/${pkgname}-${pkgver}.pet
#    if [ ${_md5sum} != `md5sum ${pkgname}-${pkgver}.pet | awk '{print $1}'` ]; then error "Md5sum is incorrect!" && exit; fi
#    pet2tgz -i "${srcdir}"/${pkgname}-${pkgver}.pet -o "${srcdir}"/${pkgname}-${pkgver}.tar.gz
#    tar -xzf "${srcdir}"/${pkgname}-${pkgver}.tar.gz
#  fi
#}


package() {
 cat > ${pkgname} <<EOF
#!/bin/bash
"/usr/share/${pkgname}/${pkgname}" "\$@"
EOF

 cat > ${pkgname}.desktop <<EOF
[Desktop Entry]
Encoding=UTF-8
Name=Pburn CD/DVD/Blu-ray writer
Name[de]=Pburn CD/DVD/Blu-ray Brenner
Name[es]=Pburn-Grabador de CD/DVD/Blu-ray
Name[fr]=Graveur CD/DVD/Blu-ray
Name[hu_HU]=Pburn CD/DVD/Blu-ray író
Name[ja]=Pburn CD/DVD/Blu-ray書き込み
Icon=/usr/share/icons/hicolor/scalable/apps/pburn.svg
Comment=Pburn CD/DVD/Blu-ray writer
Exec=${pkgname}
Terminal=false
Type=Application
Categories=Multimedia;Utility;
GenericName=Pburn CD/DVD/Blu-ray writer
EOF

  install -D -m755 ${pkgname} "${pkgdir}"/usr/bin/${pkgname}
  install -D -m644 ${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
  install -D -m644 "${srcdir}"/${pkgname}-${pkgver}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg \
				   "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg

  mkdir -p "${pkgdir}"/usr/share
  mkdir -p "${pkgdir}"/usr/share/locale/en/LC_MESSAGES
  cp -r "${srcdir}"/${pkgname}-${pkgver}/usr/local/${pkgname} "${pkgdir}"/usr/share/
  cp -r "${srcdir}"/${pkgname}-${pkgver}/usr/share/doc "${pkgdir}"/usr/share/
}
