# Maintainer: hauzer <alokinpwn@gmail.com>
pkgname=pburn
pkgver=3.3.4
pkgrel=3
pkgdesc="CD/DVD recording program used in Puppy Linux."
arch=('any')
url="http://murga-linux.com/puppy/viewtopic.php?t=23881"
license=('GPL')
depends=('coreutils' 'cdrkit' 'dvd+rw-tools' 'pfilesearch' 'cddetect' 'usleep')
makedepends=('pet2tgz')
optdepends=('ffmpeg: for converting audio/video'
            'dvdauthor: for burning video-DVD'
            'vobcopy: for copying video-DVD'
            'vamps: for shrinking video-DVD'
            'vcdimager: for burning Video-CD'
            'normalize: for volume settings on Audio-CD')
source=("${pkgname}-${pkgver}.pet"
        "pburn"
        "pburn.desktop")
md5sums=('6a8ab076fa4c90a69b0819f7d067dac2'
         'c17d0e56578d87a972cfdb11377e6ceb'
         'e585848a368528520a5df59d71ca4a75')

build() {
  cd "${srcdir}"
  pet2tgz -i ../${pkgname}-${pkgver}.pet -o ../${pkgname}-${pkgver}.tar.gz
  tar -xzf ../${pkgname}-${pkgver}.tar.gz
}

package() {
  mkdir -p "${pkgdir}/usr/share"
  cp -r "${srcdir}/${pkgname}-${pkgver}/usr/local/${pkgname}" "${pkgdir}/usr/share/"
  cp -r "${srcdir}/${pkgname}-${pkgver}/usr/share/doc" "${pkgdir}/usr/share"

  install -D -m755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -D -m644 ${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
