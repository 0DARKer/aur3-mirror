# Maintainer: hauzer (alokinpwn at gmail dot com)
pkgname=pburn
pkgver=3.3.4
pkgrel=1
pkgdesc="CD/DVD recording program used in Puppy Linux."
arch=('any')
url="http://murga-linux.com/puppy/viewtopic.php?t=23881"
license=('GPL')
depends=('coreutils' 'gtkdialog' 'cdrkit' 'dvd+rw-tools' 'cddetect' 'usleep')
makedepends=('pet2tgz')
optdepends=('ffmpeg: for converting audio/video'
'Pfilesearch: for file search'
'dvdauthor: for burning video-DVD'
'vobcopy: for copying video-DVD'
'vamps: for shrinking video-DVD'
'vcdimager: for burning Video-CD'
'normalize: for volume settings on Audio-CD')
source=("http://www.mdn.fm/files/300067_bgtvp/${pkgname}-${pkgver}.pet")
noextract=("${pkgname}-${pkgver}.pet")
md5sums=('6a8ab076fa4c90a69b0819f7d067dac2')

build() {
  cd "${srcdir}"
  pet2tgz -i ../${pkgname}-${pkgver}.pet -o ../${pkgname}-${pkgver}.tar.gz
  tar -xzf ../${pkgname}-${pkgver}.tar.gz
}

package() {
  mkdir -p "${pkgdir}/usr/share"
  cp -r "${srcdir}/${pkgname}-${pkgver}/usr/local/${pkgname}" "${pkgdir}/usr/share/"
  cp -r "${srcdir}/${pkgname}-${pkgver}/usr/share/doc" "${pkgdir}/usr/share"

    mkdir -p "${pkgdir}/usr/bin"
cat > "${pkgdir}/usr/bin/${pkgname}" <<EOF
#!/bin/bash
"/usr/share/${pkgname}/${pkgname}" "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/applications"
cat > "${pkgdir}/usr/share/applications/${pkgname}.desktop" <<EOF
[Desktop Entry]
Encoding=UTF-8
Name=Pburn CD/DVD/Blu-ray writer
Name[de]=Pburn CD/DVD/Blu-ray Brenner
Name[es]=Pburn-Grabador de CD/DVD/Blu-ray
Name[fr]=Graveur CD/DVD/Blu-ray
Name[hu_HU]=Pburn CD/DVD/Blu-ray író
Name[ja]=Pburn CD/DVD/Blu-ray書き込み
Icon=/usr/local/pburn/pburn20.png
Comment=Pburn CD/DVD/Blu-ray writer
Exec=${pkgname}
Terminal=false
Type=Application
Categories=AudioVideo;DiscBurning;Core;Utility;
GenericName=Pburn CD/DVD/Blu-ray writer
EOF
  chmod 644 "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
