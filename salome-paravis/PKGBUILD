# Maintainer: Michele Mocciola <mickele>

pkgname=salome-paravis
pkgver=7.4.0
pkgrel=1
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation - PARAVIS Module"
url="http://www.salome-platform.org"
depends=('salome-kernel=7.4.0' 'salome-gui=7.4.0' 'salome-med=7.4.0' 'python2' 'qt4' 'boost-libs' 'qwt' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'paraview-salome' 'opencascade>=6.7.1')
makedepends=('boost' 'swig' 'python2-sphinx')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
source=(http://files.salome-platform.org/Salome/Salome${pkgver}/src${pkgver}.tar.gz salome-paravis.profile)

_source=PARAVIS_SRC_${pkgver}
_installdir=/opt/salome/paravis
_paraviewrootdir=/usr
_paraviewver=4.1

prepare() {
  cd "${srcdir}/${_source}"
  
  # python -> python2
  for _FILE in `grep -Rl "/usr/bin/env python" * `
  do
	sed -e "s|/usr/bin/env python|/usr/bin/env python2|" -i ${_FILE}
  done
}

build() {
  source /etc/salome/profile.d/salome-kernel.sh
  source /etc/salome/profile.d/salome-gui.sh
  source /etc/salome/profile.d/salome-med.sh

  cd "${srcdir}/${_source}"

  rm -rf build
  mkdir -p build
  cd build

  export PYTHONPATH=$PYTHONPATH:"${_paraviewrootdir}/lib/paraview-${_paraviewver}/site-packages/":"${_paraviewrootdir}/lib/paraview-${_paraviewver}/site-packages/vtk"
  export LD_LIBRARY_PATH=$LD_LIBRARY_PATH:"${_paraviewrootdir}/lib/paraview-${_paraviewver}/"

  cmake .. \
     -DCMAKE_INSTALL_PREFIX="${_installdir}" \
     -DMEDFILE_ROOT_DIR=/usr \
     -DPARAVIEW_ROOT_DIR="${_paraviewrootdir}" \
     -DPYTHON_EXECUTABLE=/usr/bin/python2 \
     -DSPHINX_EXECUTABLE=/usr/bin/sphinx-build2 \
     -DSALOME_CMAKE_DEBUG=ON \
     -DCAS_ROOT_DIR=/opt/opencascade \
     -DVTK_DIR="${_paraviewrootdir}/lib/cmake/paraview-${_paraviewver}"

  make
}

package() {
  cd "${srcdir}/${_source}/build"

  make DESTDIR="${pkgdir}" install

  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/salome/profile.d/${pkgname}.sh"
}
md5sums=('18055915a14e0921563072f830614e7f'
         'adf424395b88558d54554b933a90c76a')
