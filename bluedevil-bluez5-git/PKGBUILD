# Contributor: Robert Kornmesser <Robert.Kornmesser@gfz-potsdam.de>

pkgname=bluedevil-bluez5-git
_gitname=bluedevil
pkgver=v1.3.1.16.g2ac0a22
pkgrel=2
pkgdesc='KDE bluetooth framework for bluez5 - Git version'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-workspace' 'libbluedevil-bluez5-git' 'obex-data-server')
makedepends=('cmake' 'automoc4' 'git')
provides=('bluedevil')
conflicts=('bluedevil')
source=('git://anongit.kde.org/bluedevil.git#branch=bluez5')
md5sums=('SKIP')

pkgver() {
	cd $_gitname
	# Use the tag of the last commit
	git describe --always | sed 's|-|.|g'
}

build() {
	cd $_gitname
	cmake ../${_gitname} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd $_gitname
	make DESTDIR="$pkgdir" install
}
