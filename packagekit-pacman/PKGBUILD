# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: Zom <zom@eevul.org>
# Mantainer: Madek <gonzaloseguel@gmail.com>

pkgname=packagekit-pacman
pkgver=0.6.10
pkgrel=2
pkgdesc='A system designed to make installation and updates of packages easier. Pacman backend supplied by PirateJonno'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('glib2>=2.22.0' 'dbus-glib>=0.74' 'polkit>=0.92'
	 'pacman-glib-git' 'python2' 'sqlite3')
makedepends=('intltool' 'gobject-introspection' 'pm-utils' 'gtk-doc')
optdepends=('networkmanager')
options=('!libtool')
backup=('var/lib/PackageKit/transactions.db'
        'etc/PackageKit/pacman.d/pacman.conf'
        'etc/PackageKit/pacman.d/repos.list')
provides=('packagekit')
conflicts=('packagekit')
source=("http://www.packagekit.org/releases/PackageKit-${pkgver}.tar.bz2")
md5sums=('dd1417c09a437ec99cb62af52214856c')

build() {
	cd "${srcdir}/PackageKit-${pkgver}"
	
	sed -i 's/SUBDIRS = test/SUBDIRS =/' backends/Makefile.in
	
	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-qt \
		--disable-managed \
		--disable-tests \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-debuginfo-install \
		--enable-pm-utils \
		--disable-dummy \
		--enable-pacman \
		--enable-alpm \
		--with-default-backend=pacman

	make || return 1
	make DESTDIR=${pkgdir} install || return 1

	# clean up unneeded stuff
	rm -rf "${pkgdir}/usr/share/PackageKit/website"

	# rename bash completion script
	mv "${pkgdir}/etc/bash_completion.d/pk-completion.bash" "${pkgdir}/etc/bash_completion.d/pkcon"

	# make sure log gets removed
	touch "${pkgdir}/var/log/PackageKit"
}
