# !!! NOTE !!!
# Please download AMD-APP-SDK-v2.8.1.0-lnx<your_bits_here>.tgz from
# http://developer.amd.com/tools-and-sdks/heterogeneous-computing/amd-accelerated-parallel-processing-app-sdk/downloads/
# and paste it next to this PKGBUILD

# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Previous Maintainer: Michael Krause <mk-arch@spline.de>
# Contributor: kralyk
# Contributor: pfdm
# Contributor: caust1c

#PKGEXT=".tar.gz"  # time to pack this pkg into tar.xz is long, unfortunatelly yaourt got problems when ext is different than .pkg.tar.xz - V


pkgname=amdapp-sdk
true && pkgname=('amdapp-sdk' 'amdapp-sdk-aparapi' 'amdapp-sdk-opencv')
pkgver=2.8.1.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://developer.amd.com/sdks/AMDAPPSDK/Pages/default.aspx"
license=("custom")
groups=('amdapp')
makedepends=('perl' 'llvm' 'amdapp-aparapi' 'apache-ant')

#Architecture resolution
    if [ "$CARCH" = 'i686' ]; then
      _bits=32
      _arch=x86
	else _bits=64
		_arch=x86_64
     fi

[ "$CARCH" = 'i686' ] && _hash='c6c1d3462c35247f7dfdb23470ae1ed29eea01af0e9288644b85cfbcd54adf4b' \
                        || _hash='6fd45187fcc4d2ca401d8fc348112ac7a8782243cc384909e9ea860e6953236b'

#Sources
source=(
	"http://developer.amd.com/wordpress/media/files/AMD-APP-SDK-v${pkgver}-lnx${_bits}.tgz"
        '01-implicit-linking.patch'
        '02-readlink-include.patch'
	'03-remove-aparapi-samples.patch'
	'amd.icd'
	'amdapp-sdk.sh')

#sha256sums
sha256sums=($_hash
'25781556b6441c26449e5577ea068eda74fb2dc520004897c64482cedb6fac0e'
'8418553d624d2cfd04a573311133f9d20d3bcce426025abfe2f68bef8cec4bc7'
'6be9823780074b4167940a0de0fcb0e83eb118f6896e259e28a6235a0152159a'
'77cb18c5a588e02c73c2406e1057461b6c030b97534154aa3163cbfb9b7e97b7'
'f196e0794ebe073b53967850223203d9c286fcbfe873ee3735fa1161ae0848bb')

_subdir="AMD-APP-SDK-v${pkgver}-RC-lnx${_bits}"
#Install path
_ipath='opt/AMDAPP/SDK'


prepare() {
  cd ${srcdir}
  tar xf ${_subdir}.tgz
  cd ${_subdir}
  patch -p0 < ../../01-implicit-linking.patch
  patch -p0 < ../../02-readlink-include.patch
  . /etc/profile.d/aparapi.sh
}


build()
{
  cd ${srcdir}/${_subdir}
  # FIXME - dependency has to move into Makefile
 make -j1
}


package_amdapp-sdk() {
pkgdesc="AMD Accelerated Parallel Processing (APP) SDK, with OpenCL 1.2 support."
install=amdapp-sdk.install
provides=('opencl' 'amdstream')
depends=('libcl' 'libgl' 'llvm' 'gcc-libs' 'mesa' 'glut' 'glew')
conflicts=('amdstream')
optdepends=(
	    'opencl-headers: for development'
            'catalyst: for OpenCL on AMD GPU'
	    'libxext: to run some samples'
	    'libsm: to run some samples'
	    'libgl: (or nvidia/catalyst-utils) to run some samples'
)

  cd ${srcdir}/${_subdir}

  #Install SDK
  install -m755 -d ${pkgdir}/${_ipath}
  cp -r {glut_notice.txt,docs,include} ${pkgdir}/${_ipath}
  install -m755 -d ${pkgdir}/${_ipath}/{bin,lib,samples/opencl/bin}
  cp -r ./bin/${_arch}/clinfo ${pkgdir}/${_ipath}/bin/clinfo
  cp -r ./lib/${_arch}/* ${pkgdir}/${_ipath}/lib
  find ./samples/opencl/ -mindepth 1 -maxdepth 1 -type d -not -name bin -exec cp -r {} ${pkgdir}/${_ipath}/samples/opencl \;
  cp -r ./samples/opencl/bin/${_arch}/* ${pkgdir}/${_ipath}/samples/opencl/bin

  #Licenses
  install -m755 -d ${pkgdir}/${_ipath}/licenses
  install -m755 -d ${pkgdir}/usr/share/licenses/amdapp-sdk
  install -m644 ./docs/opencl/LICENSES ${pkgdir}/${_ipath}/licenses/LICENSE
  install -m644 ./docs/opencl/LICENSES ${pkgdir}/usr/share/licenses/amdapp-sdk/LICENSE

  #Register ICD
  install -m755 -d ${pkgdir}/etc/OpenCL/vendors
  install -m755 ${srcdir}/amd.icd ${pkgdir}/etc/OpenCL/vendors
  sed -i -e "s|PATH|${_ipath}|" ${pkgdir}/etc/OpenCL/vendors/amd.icd
  sed -i -e "s|BITS|${_bits}|" ${pkgdir}/etc/OpenCL/vendors/amd.icd
  # The OpenCL ICD specifications: http://www.khronos.org/registry/cl/extensions/khr/cl_khr_icd.txt

  #Install includes
  install -m755 -d ${pkgdir}/usr/include/OpenVideo
  install -m644 ./include/OpenVideo/{OVDecode.h,OVDecodeTypes.h} ${pkgdir}/usr/include/OpenVideo

  #Symlink binaries   -- doesn't needed? V
#  mkdir -p "${pkgdir}/usr/bin"
 # ln -s "/${_ipath}/bin/clinfo" "${pkgdir}/usr/bin/clinfo"

  #Env vars
  install -m755 -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/amdapp-sdk.sh ${pkgdir}/etc/profile.d
  sed -i -e "s|PATH|${_ipath}|" ${pkgdir}/etc/profile.d/amdapp-sdk.sh

  #Fix modes
  find ${pkgdir}/${_ipath} -type f -exec chmod 644 {} \;
  chmod 755 ${pkgdir}/${_ipath}/bin/*
  chmod 755 ${pkgdir}/${_ipath}/lib/*
  find ${pkgdir}/${_ipath}/samples/opencl/bin -type f -not -name "*.*" -exec chmod 755 {} \;
}


package_amdapp-sdk-aparapi() {
pkgdesc="AMD Accelerated Parallel Processing (APP) SDK, with OpenCL 1.2 support. AparapiUtil and aparapi samples"
depends=('libcl' 'libgl' 'llvm' 'gcc-libs' 'mesa' 'glut' 'glew' 'amdapp-aparapi' 'apache-ant')
install=amdapp-sdk-aparapi.install

  cd ${srcdir}/${_subdir}
  install -m755 -d ${pkgdir}/${_ipath}/samples/aparapi
  cp -r ./samples/aparapi/* ${pkgdir}/${_ipath}/samples/aparapi
  find ${pkgdir}/${_ipath}/samples/aparapi -name \*.sh -exec chmod 755 {} \;
  install -m755 -d ${pkgdir}/usr/share/licenses/amdapp-sdk-aparapi
  install -m644 ./docs/opencl/LICENSES ${pkgdir}/usr/share/licenses/amdapp-sdk-aparapi/LICENSE
}


package_amdapp-sdk-opencv() {
pkgdesc="AMD Accelerated Parallel Processing (APP) SDK, with OpenCL 1.2 support. OpenCVUtils and opencv samples"
depends=('libcl' 'libgl' 'llvm' 'gcc-libs' 'mesa' 'glut' 'glew' 'opencv')
install=amdapp-sdk-opencv.install

  cd ${srcdir}/${_subdir}
  install -m755 -d ${pkgdir}/${_ipath}/samples/opencv/bin
  find ./samples/opencv/ -mindepth 1 -maxdepth 1 -type d -not -name bin -exec cp -r {} ${pkgdir}/${_ipath}/samples/opencv \;
  cp -r ./samples/opencv/bin/${_arch}/* ${pkgdir}/${_ipath}/samples/opencv/bin
  find ${pkgdir}/${_ipath}/samples/opencv/bin -type f -not -name "*.*" -exec chmod 755 {} \;
  install -m755 -d ${pkgdir}/usr/share/licenses/amdapp-sdk-opencv
  install -m644 ./docs/opencl/LICENSES ${pkgdir}/usr/share/licenses/amdapp-sdk-opencv/LICENSE
}