# Maintainer: Hilton Medeiros <medeiros.hilton@gmail.com>

pkgname=gdb-c++-pretty-svn
pkgver=193573
pkgrel=1
pkgdesc="A GNU debugger extension for C++ STL pretty printers"
arch=('any')
url="http://sourceware.org/gdb/wiki/STLSupport"
license=('GPL3')
depends=('gdb')
makedepends=('subversion')
install=gdb-c++-pretty.install
source=(gdb-c++-pretty.gdb)
md5sums=('2d4403b023395f0beca871600ec5a2d0')

_svntrunk="svn://gcc.gnu.org/svn/gcc/trunk/libstdc++-v3/python"
_svnmod="gdb-c++-pretty"

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
    msg "The local files are updated"
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
}

package() {
  cd "$srcdir"
  install -d "$pkgdir/usr/share/gdb"

  cp -r $_svnmod "$pkgdir/usr/share/gdb/$_svnmod"
  cd "$pkgdir/usr/share/gdb/$_svnmod"
  rm -f ./{Makefile.{am,in},hook.in}
  find . -type d -name ".svn" -exec rm -fr '{}' \; 2> /dev/null || true

  install -m644 "$srcdir/$_svnmod.gdb" "$pkgdir/usr/share/gdb/$_svnmod"
}
