# Contributor: scj <scj archlinux us>
# Maintainer: Eivind Eide <xenofil A-T gmail D-O-T com>
pkgname="apetag"
pkgver="1.12"
pkgrel="3"
pkgdesc="CLI tool for manipulating APEv2 tags"
arch=('i686' 'x86_64')
url="http://muth.org/Robert/Apetag"
license="GPL3"
depends=('python2' 'gcc-libs')
optdepends=('id3v2: mp3 support for tagdir.py'
            'mutagen: mp3 support for tagdir.py'
	    'flac: flac support for tagdir.py')
source="${url}/${pkgname}.${pkgver}.tar.gz"
md5sums=('f47a2fc02ea88018afd05e8a16f11235')

build() {
  cd "${srcdir}/Apetag"
  sed -e '/C++ imports/a #include <cstdlib>' -i switch.C
  sed "1s/python$/python2/" -i cddb.py -i rmid3tag.py -i tagdir.py
  make
}

package() { 
  cd "${srcdir}/Apetag"
  install -Dm755 apetag "${pkgdir}/usr/bin/apetag"
  install -Dm755 cddb.py "${pkgdir}/usr/bin/cddb.py"
  install -Dm755 rmid3tag.py "${pkgdir}/usr/bin/rmid3tag.py"
  install -Dm755 tagdir.py "${pkgdir}/usr/bin/tagdir.py"
  install -Dm644 00copying \
  "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 00readme \
  "${pkgdir}/usr/share/doc/${pkgname}/README"
}

