#Maintainer: Shaumux <shaumya "at" gmail {dot} com>

pkgname=vmware-modules
pkgver=271.1
pkgrel=1
pkgdesc="VMware kernel modules"
arch=('i686' 'x86_64')
url="http://www.vmware.com/"
license=('GPL2')
conflicts=('open-vm-tools-modules')
_MINOR=$(echo ${pkgver} | cut -d . -f 2)
depends=("vmware-player=5.0.${_MINOR}")
makedepends=('linux-headers')
options=('!buildflags')
install=${pkgname}.install
source=('vmblock.tar' 'vmci.tar' 'vmmon.tar' 'vmnet.tar' 'vsock.tar' '271-putname.patch' '271-apic.patch' '60-vmware.rules')
sha256sums=('dba3405520082eab8a068fb69e5c0baebca65461efa91f2210b5d0f54bc6d0d4'
	    'ace8d86e6af6c3d93a893de6b380ff1e21216571ebef2c9cad5cc6997763711a'
	    '767168e17fe9a3bb291df645e99beef29b8ac7320da4ea1791d23c2d024532a1'
	    'b6e971b4a14fdee0cffd649efb8d03d166e37149a0f13de43c3a422d80669cbb'
	    'd7ce3b82ff691899865edc8de4f6a08eab160f5f1b820cea2c8ecf801735bf29'
	    'b8103d3c72c24696e974e49dcdcdf2d5a2b2c2fdd5f6ca050842b840e906ba77'
	    '60e7df881281fedcabe9ea4427b324b5e1142a1a2b6ab5236ac0843bd1051048'
	    '654a11be0b0a6f0f4f8fdc32f8a92542cec637e0f6c62dd7097d3cf679c06f4d')
build() {
	_kernver=$(pacman -Q linux | cut -d . -f 2 | cut -f 1 -d -)
	VMWARE_MODULE_LIST="vmblock vmci vmmon vmnet vsock"
	patch -p1 < ${srcdir}/271-apic.patch
	#patch -p1 < ${srcdir}/271-makefile-dirs.patch
	chmod +w ${srcdir}/vmblock-only/linux/control.c
	[[ ${_kernver} -ge 7 ]] && patch -p1 < ${srcdir}/271-putname.patch
	for mod in ${VMWARE_MODULE_LIST}; do
		cd ${srcdir}/${mod}-only
		[[ ${mod} == 'vsock' ]] && cp ${srcdir}/vmci-only/Module.symvers ./
		make || return 1
	done
}

package() {
	  _kernver=$(pacman -Q linux | cut -d . -f 2 | cut -f 1 -d -)
	  depends=("linux>=3.${_kernver}" "linux<3.$(expr ${_kernver} + 1)")
	  KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-3.${_kernver}-ARCH/version)
	  msg "Kernel = $KERNEL_VERSION"
	  mkdir -p $pkgdir/usr/lib/modules/extramodules-3.${_kernver}-ARCH/
	  cd "$srcdir"
	  find -type f -name '*.ko' \
	       -exec install -t "$pkgdir/usr/lib/modules/extramodules-3.$_kernver-ARCH" {} +
	  gzip "$pkgdir/usr/lib/modules/extramodules-3.$_kernver-ARCH"/*.ko
	  install -D -m0644 "$srcdir"/60-vmware.rules "$pkgdir"/etc/udev/rules.d/60-vmware.rules
}
