# Contributor: Jesus Jerez <jhuss@archlinux.org.ve>

pkgname=eclipse-cvs
pkgver=3.6.0
pkgrel=3
pkgdesc="Eclipse Platform CVS"
url="http://www.eclipse.org/eclipse/platform-cvs/"
arch=('any')
license=('EPL')
depends=('eclipse-platform')
makedepends=('unzip')
changelog=$pkgname.changelog
source=("http://www.mirrorservice.org/sites/download.eclipse.org/eclipseMirror/eclipse/downloads/drops/R-3.6-201006080911/org.eclipse.cvs-3.6.zip")
md5sums=('251bfea8f5ff67891f96119fc13c9a05')

build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
  
}
