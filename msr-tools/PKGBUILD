# Maintainer: Roberto Calabrese <robertocalabrese75@gmail.com>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Michal Bozon <michal.bozon__at__gmail.com>
 
pkgname=msr-tools
pkgver=1.3
pkgrel=5
pkgdesc="Utilities allowing to read and write CPU model-specific registers (MSR)"
arch=('i686' 'x86_64')
url="https://01.org/msr-tools"
license=('GPL')
source=("https://01.org/sites/default/files/downloads/msr-tools/msr-tools-${pkgver}.zip")
md5sums=('67d18527f64fd707c9b7a79f13d19fd5')
 
prepare() {
  cd "${srcdir}/${pkgname}-master"
 
  # fix /usr/sbin --> usr/bin
  sed -i -e '/^sbindir/ s,/sb,/b,' Makefile
}
 
build() {
  cd "${srcdir}/${pkgname}-master"
  make
}
 
package() {
  cd "${srcdir}/${pkgname}-master"
  install -dm755 "$pkgdir/usr/bin"
  install -m755 wrmsr "$pkgdir/usr/bin/wrmsr"
  install -m755 rdmsr "$pkgdir/usr/bin/rdmsr"
  install -m755 cpuid "$pkgdir/usr/bin/cpuid"
}
 
# vim:set ts=2 sw=2 et:

