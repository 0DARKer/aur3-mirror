# Contributor: Marti Raudsepp <marti@juffo.org>
# Maintainer: mutantmonkey <aur@mutantmonkey.in>

pkgname=tcpcrypt-git
_gitname=tcpcrypt
pkgver=254.c8b7efa
pkgrel=1
pkgdesc="Transparent user space implementation for the tcpcrypt TCP extensions"
arch=(i686 x86_64)
url="http://tcpcrypt.org/"
license=('BSD')
depends=('libnetfilter_queue' 'openssl' 'iptables')
makedepends=('git')
provides=('tcpcrypt')
conflicts=('tcpcrypt')
install=tcpcrypt.install
backup=(etc/conf.d/tcpcryptd.conf)
source=('git+https://github.com/sorbo/tcpcrypt.git'
        'tcpcryptd.conf'
        'tcpcryptd.service')
sha256sums=('SKIP'
            'a41bfa7b0c8f527f96c993f68e0a02265ad672fe6c2e10f41bc00b8b1f819305'
            '68a3227f971dce0056bbe8768b570fade4ee8b17aca2e354c35352f2bf0a7028')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd $_gitname
  cd user
  autoreconf -fi
}

build() {
  cd $_gitname/user
  ./configure --prefix=/usr
  make
}

package() {
  cd $_gitname/user
  make DESTDIR=$pkgdir install

  install -Dm644 $srcdir/$_gitname/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 $srcdir/tcpcryptd.conf $pkgdir/etc/conf.d/tcpcryptd.conf
  install -Dm644 $srcdir/tcpcryptd.service $pkgdir/usr/lib/systemd/system/tcpcryptd.service
}

# vim:set ts=2 sw=2 et:
