# From the AUR:
# Maintainer: ShapeShifter499 
# From the official repositories:
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
#

pkgbase=mesa-full-i915g
pkgdesc="experimental Gallium3D driver for certain Intel video cards, built from the latest stable mesa. Packages intel-dri-i915g, mesa-i915g, and mesa-libgl-i915g"
#pkgname=$pkgbase
pkgname=("intel-dri-i915g" "mesa-i915g" "mesa-libgl-i915g")
pkgver=10.2.4
pkgver2=10.2.4
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('python2' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto' 'dri3proto' 'presentproto' 
             'libxshmfence' 'libxxf86vm'  'libxdamage' 'libvdpau' 'wayland' 'elfutils' 'llvm' 'systemd')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=(ftp://ftp.freedesktop.org/pub/mesa/${pkgver2}/MesaLib-${pkgver}.tar.bz2
        LICENSE)

md5sums=('11d3542da1b703618634be840a87b0b2'
         '5c65a0fe315dd347e09b1f2826a1df5a')

build() {
  cd ${srcdir}/?esa-*

  autoreconf -vfi # our automake is far too new for their build system :)
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=i915 \
    --with-dri-drivers=i915 \
    --with-egl-platforms=x11,drm,wayland \
    --with-llvm-shared-libs \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau \
    --enable-dri3
    # --help

  make

  # fake installation
  mkdir $srcdir/fakeinstall
  make DESTDIR=${srcdir}/fakeinstall install
}

package_intel-dri-i915g() {
  #pkgdesc="experimental Gallium3D driver for certain Intel video cards"
  depends=('mesa-libgl-i915g' 'libtxc_dxtn' "mesa-i915g=${pkgver}")
  provides=('intel-dri')
  conflicts=('intel-dri')
  replaces=('intel-dri')
  install -m755 -d ${pkgdir}/usr/lib/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/i915_dri.so ${pkgdir}/usr/lib/xorg/modules/dri/
  
  install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_i915* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/intel-dri/"
}

package_mesa-i915g() {
  #pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libvdpau' 'wayland' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'systemd' 'elfutils' 'llvm-libs')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 
'khrplatform-devel' 'mesa' "mesa=${pkgver}")
  conflicts=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel' 'mesa')
  replaces=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel' 'mesa')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  # rename libgl/EGL/glesv*.so to not conflict with blobs - may break .pc files ?
  mv ${pkgdir}/usr/lib/libGL.so.1.2.0 		${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  mv ${pkgdir}/usr/lib/libEGL.so.1.0.0 		${pkgdir}/usr/lib/mesa-libEGL.so.1.0.0
  mv ${pkgdir}/usr/lib/libGLESv1_CM.so.1.1.0 	${pkgdir}/usr/lib/mesa-libGLESv1_CM.so.1.1.0
  mv ${pkgdir}/usr/lib/libGLESv2.so.2.0.0 	${pkgdir}/usr/lib/mesa-libGLESv2.so.2.0.0

  rm ${pkgdir}/usr/lib/lib{GL,EGL,GLESv1_CM,GLESv2}.so*

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}

package_mesa-libgl-i915g() {
  #pkgdesc="Mesa 3-D graphics library"
  depends=("mesa-i915g=${pkgver}")
  provides=("libgl=${pkgver}" 'mesa-libgl')
  conflicts=('libgl' 'mesa-libgl')
  replaces=('libgl' 'mesa-libgl')
 
  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s mesa-libGL.so.1.2.0 	    ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0         ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0         ${pkgdir}/usr/lib/libGL.so.1.2.0
  ln -s mesa-libEGL.so.1.0.0        ${pkgdir}/usr/lib/libEGL.so
  ln -s mesa-libEGL.so.1.0.0        ${pkgdir}/usr/lib/libEGL.so.1
  ln -s mesa-libEGL.so.1.0.0        ${pkgdir}/usr/lib/libEGL.so.1.0.0
  ln -s mesa-libGLESv1_CM.so.1.1.0  ${pkgdir}/usr/lib/libGLESv1_CM.so
  ln -s mesa-libGLESv1_CM.so.1.1.0  ${pkgdir}/usr/lib/libGLESv1_CM.so.1
  ln -s mesa-libGLESv1_CM.so.1.1.0  ${pkgdir}/usr/lib/libGLESv1_CM.so.1.1.0
  ln -s mesa-libGLESv2.so.2.0.0     ${pkgdir}/usr/lib/libGLESv2.so
  ln -s mesa-libGLESv2.so.2.0.0     ${pkgdir}/usr/lib/libGLESv2.so.2
  ln -s mesa-libGLESv2.so.2.0.0     ${pkgdir}/usr/lib/libGLESv2.so.2.0.0

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libgl/"
}
