#  Maintainer: Loui Chang <louipc dot ist at gmail company>

pkgname=brlcad
pkgver=7.20.2
pkgrel=1
pkgdesc="An extensive 3D solid modeling system."
url="http://brlcad.org"
license=('LGPL' 'BSD' 'custom:BDL')
arch=('i686' 'x86_64')
install=$pkgname.install
makedepends=('bc' 'libxslt')
depends=('boost' 'flex' 'libgl' 'libpng' 'mesa' 'tk' 'zlib')

# Maybe we could build brlcad with these externally some time.
#	'itcl:        libs included, but package can build with external libs'
#	'itk:         libs included, but package can build with external libs'
#	'opennurbs:   libs included, but package can build with external libs'
#	'tkhtml3-cvs: for man page browsing in mged. CVS repo dead'
#	'urt:         Utah Raster Toolkit'
optdepends=(
    'fop:   for building pdf documentation'
    'tkpng: for archer'
)

# tkhtml3-cvs is needed for manpage browsing in mged.
# Older versions won't work properly

options=('!libtool')

source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2)

_pkgprefix=/opt/$pkgname

build() {
    cd $srcdir/$pkgname-$pkgver

    # Uncomment the next line if building from SVN fetched sources.
    #./autogen.sh
    ./configure --prefix=${_pkgprefix} \
    --with-opengl=/usr/lib \
    --with-tcl=/usr/lib \
    --disable-strict \
    --disable-documentation \
#    --disable-step \
#    --disable-tkhtml3 \
#    --disable-tkimg \
#    --disable-opennurbs

    make
}

package() {
    cd $srcdir/$pkgname-$pkgver

    make DESTDIR=$pkgdir install

    mkdir -p $pkgdir/etc/profile.d
    echo "export PATH=\$PATH:$_pkgprefix/bin" > $pkgdir/etc/profile.d/$pkgname.sh
    chmod 755 $pkgdir/etc/profile.d/$pkgname.sh

    mkdir -p $pkgdir/usr/share/licenses/$pkgname
    cp $srcdir/$pkgname-$pkgver/doc/legal/bdl.txt \
        $pkgdir/usr/share/licenses/$pkgname
    cp $srcdir/$pkgname-$pkgver/doc/legal/bsd.txt \
        $pkgdir/usr/share/licenses/$pkgname
    mkdir -p $pkgdir/etc/ld.so.conf.d
    echo ${_pkgprefix}/lib > $pkgdir/etc/ld.so.conf.d/$pkgname.conf
}

md5sums=('67f22029ced78a429c9511072797612c')
