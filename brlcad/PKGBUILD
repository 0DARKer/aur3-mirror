#  Maintainer: Loui Chang <louipc dot ist at gmail company>

pkgname=brlcad
pkgver=7.20.6
pkgrel=1
pkgdesc="An extensive 3D solid modeling system."
url="http://brlcad.org"
license=('LGPL' 'BSD' 'custom:BDL')
arch=('i686' 'x86_64')
install=$pkgname.install
makedepends=('bc' 'cmake' 'libxslt')
depends=('boost' 'flex' 'libgl' 'libpng' 'mesa' 'tk' 'zlib')

# Maybe we could build brlcad with these externally some time.
#	'itcl:        libs included, but package can build with external libs'
#	'itk:         libs included, but package can build with external libs'
#	'opennurbs:   libs included, but package can build with external libs'
#	'tkhtml3-cvs: for man page browsing in mged. CVS repo dead'
#	'urt:         Utah Raster Toolkit'
optdepends=(
    'fop:   for building pdf documentation'
    'tkpng: for archer'
)

# tkhtml3-cvs is needed for manpage browsing in mged.
# Older versions won't work properly

options=('!libtool')

source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
http://downloads.sourceforge.net/sourceforge/$pkgname/rtedge_parsing_fix-1.patch
http://downloads.sourceforge.net/sourceforge/$pkgname/rtwizard_update-2.patch
http://downloads.sourceforge.net/sourceforge/$pkgname/nurbs_raytracing_improvement-3.patch
http://downloads.sourceforge.net/sourceforge/$pkgname/red_command_fix-4.patch)


_pkgprefix=/opt/$pkgname

build() {
    # Default flags minus --as-needed
    LDFLAGS="-Wl,-O1,--sort-common,-z,relro"

    cd $srcdir/$pkgname-$pkgver

    patch -p0 < $srcdir/rtedge_parsing_fix-1.patch
    patch -p0 < $srcdir/rtwizard_update-2.patch
    patch -p0 < $srcdir/nurbs_raytracing_improvement-3.patch
    patch -p0 < $srcdir/red_command_fix-4.patch

    mkdir $srcdir/$pkgname-build
    cd $srcdir/$pkgname-build

    cmake $srcdir/$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=${_pkgprefix} \
    -DBRLCAD-ENABLE_OPTIMIZED=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DBRLCAD-ENABLE_STRICT=OFF

    make
}

package() {
    cd $srcdir/$pkgname-build

    make DESTDIR=$pkgdir install

    mkdir -p $pkgdir/etc/profile.d
    echo "export PATH=\$PATH:$_pkgprefix/bin" > $pkgdir/etc/profile.d/$pkgname.sh
    chmod 755 $pkgdir/etc/profile.d/$pkgname.sh

    mkdir -p $pkgdir/usr/share/licenses/$pkgname
    cp $srcdir/$pkgname-$pkgver/doc/legal/bdl.txt \
        $pkgdir/usr/share/licenses/$pkgname
    cp $srcdir/$pkgname-$pkgver/doc/legal/bsd.txt \
        $pkgdir/usr/share/licenses/$pkgname
    mkdir -p $pkgdir/etc/ld.so.conf.d
    echo ${_pkgprefix}/lib > $pkgdir/etc/ld.so.conf.d/$pkgname.conf
}

md5sums=('2249e9f5f556e48ed66a1ba6e407ed60'
         '93b2c4229daaa45e1907effd55187b3b'
         '21cf450bcc82baca0b25d14fc6e06ee9'
         'c04bb09e50afe15e34a64b2cb025c5a5'
         '924eeed606bf4ac4615d28310ec4f99d')
