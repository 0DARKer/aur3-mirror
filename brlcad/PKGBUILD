pkgname=brlcad
pkgver=7.24.2
pkgrel=0
pkgdesc='An extensive 3D solid modeling system.'
url='http://brlcad.org'
license=('LGPL' 'BSD' 'custom:BDL')
arch=('i686' 'x86_64')
depends=('libpng' 'zlib' 'mesa' 'libgl')
makedepends=('cmake')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2" "build.patch")
sha1sums=('339b5445aebcf858f06ad1247332abae67997a25' '5e045ad7dbdd267afa14ef56d4fdd4f952a99f17')


_pkgprefix="/opt/brlcad"


prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch --strip=0 "--input=${srcdir}/build.patch"
}


build() {
    mkdir "${srcdir}/${pkgname}-build"
    cd "${srcdir}/${pkgname}-build"
    cmake -Wno-dev "${srcdir}/${pkgname}-${pkgver}" "-DCMAKE_INSTALL_PREFIX=${_pkgprefix}" -DCMAKE_BUILD_TYPE=Release -DBRLCAD_BUNDLED_LIBS=ON -DBRLCAD_PNG=SYSTEM -DBRLCAD_ZLIB=SYSTEM -DBRLCAD_ENABLE_STRICT=OFF
    make
}


package() {
    cd "${srcdir}/${pkgname}-build"
    make "DESTDIR=${pkgdir}" install
    install -D -m644 share/doc/legal/bsd.txt "${pkgdir}/usr/share/licenses/${pkgname}/bsd.txt"
    install -D -m644 share/doc/legal/bdl.txt "${pkgdir}/usr/share/licenses/${pkgname}/bdl.txt"

    mkdir -p "${pkgdir}/etc/profile.d"
    echo "export PATH=\"\$PATH:${_pkgprefix}/bin\"" >"${pkgdir}/etc/profile.d/${pkgname}.sh"
    chmod 755 "${pkgdir}/etc/profile.d/${pkgname}.sh"
}
