# Maintainer: Joe Stringer <joestringernz@gmail.com>
pkgname=libnbee
pkgver=0.3.90
pkgrel=1
pkgdesc="NetBee is a new library intended for several types of packet processing, such as packet sniffing and filtering, packet decoding, and traffic classification."
url="http://www.nbee.org"
arch=('x86_64' 'i686')
license=('LGPL2 or any later version')
depends=('xerces-c' 'pcre' 'libpcap')
optdepends=('sqlite3')
makedepends=('cmake')
source=(http://www.nbee.org/download/nbeesrc-12-06-01.zip)
md5sums=('3c2b6de2c9d0451c801666ebf58cfe2a');

build() {
  cd "${srcdir}/netbee/src"
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr || return 1
  make || return 1
}

package() {
  cd "${srcdir}/netbee"

  # There's no make install for this project :(
  #make DESTDIR="${pkgdir}" install

  install -dm755 $pkgdir/usr/lib
  install -Dpm755 bin/*.so $pkgdir/usr/lib/

  install -dm755 $pkgdir/usr/include
  install -Dpm755 include/*.h $pkgdir/usr/include/

  install -dm755 $pkgdir/usr/share/licenses/$pkgname/
  install -Dpm644 lgpl-2.1.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 $pkgdir/usr/share/doc/$pkgname/
  install -Dpm644 readme-compile.txt readme-compile.htm readme-devpack.txt readme-release.txt $pkgdir/usr/share/doc/$pkgname/
}

# vim:set ts=2 sw=2 et:
