# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>

pkgname=keysync
pkgver=0.2
pkgrel=1
pkgdesc='convert OTR keystores into other keystore formats for moving keys into a new IM app'
arch=('any')
url="https://guardianproject.info/apps/keysync/"
makedepends=('desktop-file-utils' 'python2-setuptools')
depends=('python2' 'python2-beautifulsoup3' 'python2-psutil' 'python2-python-potr'
         'python2-pyasn1' 'python2-crypto' 'python2-pyjavaproperties'
         'python2-pyparsing' 'python2-pgpdump' 'hicolor-icon-theme')
optdepends=('python2-qrcode: for GUI'
            'tk: for GUI'
            'python2-pymtp: for MTP on GUI')
license=('GPL3')
install=keysync.install
sha256sums=('8cc4325010bb50b008ff6701dcb31252f8ed54639bfee20600b01393f535693e'
            'b25194c9b67d5c6c7761535c59ad768ece4a65a0566f15de275ea72da0bb8991')
source=("$pkgname-$pkgver.tar.gz::https://github.com/guardianproject/$pkgname/archive/$pkgver.tar.gz"
        keysync-gui-icons-path.patch)

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir/keysync-gui-icons-path.patch"
}

package() {
  cd "$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
