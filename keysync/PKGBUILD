# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>

pkgname=keysync
pkgver=0.2
pkgrel=1
pkgdesc='convert OTR keystores into other keystore formats for moving keys into a new IM app'
arch=('any')
url="https://guardianproject.info/apps/keysync/"
makedepends=('desktop-file-utils')
depends=('python2' 'python2-beautifulsoup3' 'python2-psutil' 'python2-python-potr'
         'python2-pyasn1' 'python2-crypto' 'python2-pyjavaproperties'
         'python2-pyparsing' 'python2-pgpdump' 'hicolor-icon-theme')
optdepends=('python2-qrcode: for GUI'
            'tk: for GUI'
            'pymtp: for MTP on GUI')
license=('GPL3')
install=keysync.install
sha256sums=('243704234358c23e1e1512ae1e2f1dda6530d32e59592980c5f62f76667e582f'
            'SKIP'
            'b25194c9b67d5c6c7761535c59ad768ece4a65a0566f15de275ea72da0bb8991')
source=(https://guardianproject.info/releases/$pkgname-${pkgver}.tar.gz{,.asc}
        keysync-gui-icons-path.patch)

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir/keysync-gui-icons-path.patch"
}

package() {
  cd "$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
