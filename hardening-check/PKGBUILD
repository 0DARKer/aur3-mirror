pkgname=hardening-check
_pkgname=hardening-wrapper
pkgver=2.5
_pkgver=2.5ubuntu2
pkgrel=1
pkgdesc="Check binaries for security hardening features"
arch=('any')
url="http://wiki.debian.org/Hardening"
license=('GPL2')
depends=('bash' 'perl')
source=("http://archive.ubuntu.com/ubuntu/pool/main/h/$_pkgname/hardening-wrapper_$_pkgver"{.tar.gz,.dsc})
md5sums=('43e959ff0910f7da1828ee60dfbb35d6'
'b9a5b66eb9760f92b41054858190eb55')
sha256sums=('4dfa9574b251c52e26464d9e7168e6890ca1f67b6aef6400fc98a19873babb65'
'a665c62e36f469f493df65f695f2914346c68d00f826c88054f1d7d842351df5')

build() {
  cd "$srcdir/$_pkgname-$_pkgver"
  make DEB_HOST_ARCH_OS=amd64 DEB_HOST_ARCH=amd64 # Irrelevant, we do not ship the wrappers.
}

package() {
  cd "$srcdir/$_pkgname-$_pkgver/build-tree"
  install -Dm0755 'hardening-check' "$pkgdir/usr/bin/hardening-check"
  install -Dm0755 '../hardening-check.sh' "$pkgdir/usr/bin/hardening-check.sh"
  install -Dm0755 'hardening-check.1' "$pkgdir/usr/share/man/man1/hardening-check.1" \
    && chmod -c 0444 "$pkgdir/usr/share/man/man1/hardening-check.1"
}

# vim:set ts=2 sw=2 et:
