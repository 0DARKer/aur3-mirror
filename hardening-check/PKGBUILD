pkgname=hardening-check
_pkgname=hardening-wrapper
pkgver=2.3
pkgrel=1
pkgdesc="Check binaries for security hardening features"
arch=('any')
url="http://wiki.debian.org/Hardening"
license=('GPL2')
depends=('bash' 'perl')
source=("http://archive.ubuntu.com/ubuntu/pool/main/h/$_pkgname/hardening-wrapper_$pkgver"{.tar.gz,.dsc})
md5sums=('a4330dfb5f41ba00f61cf68dbfcbc26d'
'322078300d45df03f61b6073bb54d65f')
sha256sums=('8bd0ea0010465e42f1a9fb51d52f09b91792c8fdfb2ad299ec8cc25ce9597d20'
'28b1310834fe14ea6b45e1b6cb78d2e7f1c550ced04f12a0674e45a551266102')

build() {
  cd "$srcdir/$_pkgname"
  make DEB_HOST_ARCH_OS=amd64 DEB_HOST_ARCH=amd64 # Irrelevant, we do not ship the wrappers.
}

package() {
  cd "$srcdir/$_pkgname/build-tree"
  install -Dm0755 'hardening-check' "$pkgdir/usr/bin/hardening-check"
  install -Dm0755 '../hardening-check.sh' "$pkgdir/usr/bin/hardening-check.sh"
  install -Dm0755 'hardening-check.1' "$pkgdir/usr/share/man/man1/hardening-check.1" \
    && chmod -c 0444 "$pkgdir/usr/share/man/man1/hardening-check.1"
}

# vim:set ts=2 sw=2 et:
