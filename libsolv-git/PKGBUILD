# Maintainer: Spyhawk

pkgname=libsolv-git
pkgver=0.6.6.2.ga4a4a37
pkgrel=1
pkgdesc="A new approach to package dependency solving"
arch=('i686' 'x86_64')
url="https://github.com/openSUSE/libsolv"
license=('BSD')
makedepends=('git' 'make' 'cmake' 'expat' 'swig'
             'perl' 'python' 'ruby')
optdepends=('perl: for perl bindings'
            'python: for python bindings'
            'ruby: for ruby bindings')
provides=('libsolv')
conflicts=('libsolv')
source=('git://github.com/openSUSE/libsolv.git')
md5sums=('SKIP')
_gitname="libsolv"

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git describe --always | sed -r 's/-/./g')
}

build() {
  cd "$srcdir/$_gitname"
    #-D ARCHLINUX=1 \
  cmake -D CMAKE_INSTALL_PREFIX=/usr \
    -D ENABLE_ARCHREPO=1 \
    -D ENABLE_RPMDB=1 \
    -D ENABLE_HELIXREPO=1 \
    -D ENABLE_PERL=1 \
    -D ENABLE_PYTHON=1 \
    -D ENABLE_RUBY=1 \
    .
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install

  # cmake fix (see GH#56)
  mkdir -p $pkgdir/usr/lib/cmake/LibSolv
  mv $pkgdir/usr/share/cmake/Modules/FindLibSolv.cmake $pkgdir/usr/lib/cmake/LibSolv/LibSolvConfig.cmake 
}
