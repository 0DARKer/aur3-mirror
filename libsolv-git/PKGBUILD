# Maintainer: Spyhawk

pkgname=libsolv-git
pkgver=0.6.1
pkgrel=1
pkgdesc="A new approach to package dependency solving"
arch=('i686' 'x86_64')
url="https://github.com/openSUSE/libsolv"
license=('BSD')
makedepends=('git' 'make' 'cmake' 'expat' 'swig')
optdepends=('python: for python bindings'
            'perl: for perl bindings'
            'ruby: for ruby bindings')
provides=('libsolv')
conflicts=('libsolv')
source=("git://github.com/openSUSE/libsolv.git")
md5sums=('SKIP')
_gitname="libsolv"

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git describe --always | sed -r 's/^BASE.SuSE.Code.//;s/.Branch//;s/_/./;s/([^-]*-g)/\1/;s/-/./g')
}

build() {
  cd "$srcdir/$_gitname"
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D ENABLE_ARCHREPO=1 \
    -D ENABLE_PYTHON=1 \
    -D ENABLE_PERL=1 \
    -D ENABLE_RUBY=1 \
    .
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
}
