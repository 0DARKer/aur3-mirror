# Maintainer: Ilya Oshchepkov 
pkgname=glab
pkgver=2.0.0
pkgrel=2
pkgdesc="Interactive educational multipurpose package to process and analyse GNSS data"
arch=(i686 x86_64)
url="http://gage.upc.edu/drupal6/gLAB"
license=('Apache')
depends=('python2' 'python2-matplotlib' 'wxpython2.8' 'tk')
source=($pkgname.tgz::http://gage14.upc.es/gLAB/gLAB.tgz)
noextract=($pkgname.tgz)
md5sums=('9aab34114cb19cb4c853dd80aa74289e')

build() {
    tar -xf $pkgname.tgz
    mv "$srcdir"/gLAB "$srcdir/$pkgname" 
	cd "$srcdir/$pkgname"
	make -f makefile_linux
}

package() {
	cd "$srcdir/$pkgname"

    install -dm755 "$pkgdir"/usr/bin/
    mv gLAB_linux  "$pkgdir"/usr/bin/glab-dpc

    # fix python2
    sed -i 's/\#!\/usr\/bin\/python/\#!\/usr\/bin\/env python2/g' gLAB_GUI.py
    sed -i 's/\#!\/usr\/bin\/python/\#!\/usr\/bin\/env python2/g' graph.py

    # fix wxpython version
    sed -i '/import wx, wx.html/i\import wxversion\nwxversion.select("2.8")' gLAB_GUI.py

    # change the names for DAT and DPC
    sed -i 's/graph.py/glab-dat/g' gLAB_GUI.py
    sed -i 's/gLAB_linux/glab-dpc/g' gLAB_GUI.py


    mv gLAB_GUI.py "$pkgdir"/usr/bin/glab
    mv graph.py "$pkgdir"/usr/bin/glab-dat

    install -dm755 "$pkgdir"/usr/share/gLAB/

    # move all images
    mv -t "$pkgdir"/usr/share/gLAB/ *.gif *.ico *.png
    # move all txt and pdf files
    mv -t "$pkgdir"/usr/share/gLAB/ *.txt *.pdf
    # move test and src folders
    mv -t "$pkgdir"/usr/share/gLAB/ test/ src/

}
