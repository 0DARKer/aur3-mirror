# Maintainer: David Jurenka <arch.box NO-SP@M imx.jurenka DOT cz>
pkgname=vaiofand
pkgver=0.9.2
pkgrel=1
pkgdesc="Fan controller for Sony Vaio laptops"
arch=(i686 x86_64)
url="http://vaio-utils.org/fan/"
license=('custom:ISC')
depends=(bash)
makedepends=('kernel26-headers>=2.6.22')
backup=(etc/vaiofand.conf)
install=vaiofand.install
source=(http://vaio-utils.org/download/$pkgname-$pkgver.tar.gz)
md5sums=(e9462b2dab93c1f2ee46994300aa57aa)
sha256sums=(4826018c6a8e9fa16e51e287b327d79aa3eecb4bd79e386dcfdc9c2e21281600)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix=/usr DESTDIR="$pkgdir/" copy
  # rc.d script
  install -d "${pkgdir}/etc/rc.d"
  install "init-scripts/arch/${pkgname}" "${pkgdir}/etc/rc.d"
  # license
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"
}
