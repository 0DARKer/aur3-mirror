# Maintainer: David Jurenka <arch.box NO-SP@M imx.jurenka DOT cz>
pkgname=vaiofand
pkgver=0.9.1
pkgrel=2
pkgdesc="Fan controller for Sony Vaio laptops"
arch=(i686 x86_64)
url="http://vaio-utils.org/fan/"
license=('custom:ISC')
depends=(bash)
makedepends=('kernel26-headers>=2.6.22')
backup=(etc/vaiofand.conf)
install=vaiofand.install
source=(http://vaio-utils.org/download/$pkgname-$pkgver.tar.gz)
md5sums=(1ebdd986603d1e3bede7c67aee9d3e59)
sha256sums=(a82722b99b10a68210b413a3bab33d5c50597d8c19b28acfaaf274a690bd8011)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix=/usr DESTDIR="$pkgdir/" copy
  # rc.d script
  install -d "${pkgdir}/etc/rc.d"
  install "init-scripts/arch/${pkgname}" "${pkgdir}/etc/rc.d"
  # license
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"
}
