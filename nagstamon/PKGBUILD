_pkgname=Nagstamon
pkgname=nagstamon
pkgver=1.0
pkgrel=3
pkgdesc="Nagios status monitor for the desktop"
depends=('python2' 'pygtk' 'gnome-python-extras' 'python2-lxml' 'python2-rsvg' 'python2-distribute' 'python2-xdg')
arch=('any')
url="http://nagstamon.ifw-dresden.de/"
license=('GPL')
source=("http://sourceforge.net/projects/nagstamon/files/nagstamon/Nagstamon%20$pkgver/$_pkgname-$pkgver.tar.gz"
        "nagstamon.desktop"
        "nagstamon-noti.patch"
        "xdg-directories.patch")
md5sums=('08c37085457b3bbf4ee32117f78ef5e5'
         '484a012b99f483dc293251e329ba760d'
         'a5a1e72daff5459cd886e8097be33919'
         'b362135dbeb9f81f446673d565601249')

package() {
  cd "$srcdir/$_pkgname"

  patch -Np0 -i "$srcdir/nagstamon-noti.patch"
  patch -Np0 -i "$srcdir/xdg-directories.patch"

  python2 setup.py install --prefix=/usr --root="$pkgdir"

  install -Dm644 "$srcdir/nagstamon.desktop" "$pkgdir/usr/share/applications/nagstamon.desktop"
  install -Dm644 "$srcdir/nagstamon.desktop" "$pkgdir/etc/xdg/autostart/nagstamon.desktop"

  mv $pkgdir/usr/bin/nagstamon.py $pkgdir/usr/bin/nagstamon
}
