# Maintainer: Remy Marquis <remy.marquis@gmail.com>

pkgname=pacaur-git
pkgver=4.0.5
pkgrel=1
pkgdesc="A fast workflow AUR helper using cower as backend"
arch=('any')
url="https://github.com/Spyhawk/pacaur"
license=('GPL')
depends=('cower-git' 'expac-git' 'sudo' 'pacman>=4.1')
makedepends=('git' 'perl')
provides=('pacaur')
conflicts=('pacaur')
backup=('etc/xdg/pacaur/config')
source=("git://github.com/Spyhawk/pacaur.git#branch=master")
md5sums=('SKIP')
_gitname="pacaur"

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git describe --always | sed 's/-/./g')
}

build() {
  cd "$srcdir/$_gitname"
  pod2man --utf8 --section=8 --center="Pacaur Manual" --name="PACAUR" --release="$pkgname $pkgver" README.pod > pacaur.8
}

package() {
  mkdir -p $pkgdir/etc/xdg/pacaur
  install -D -m644 $_gitname/config $pkgdir/etc/xdg/pacaur/config
  mkdir -p $pkgdir/usr/bin
  install -D -m755 $_gitname/pacaur $pkgdir/usr/bin/pacaur
  mkdir -p $pkgdir/usr/share/bash-completion/completions
  install -D -m755 $_gitname/bash.completion $pkgdir/usr/share/bash-completion/completions/pacaur
  mkdir -p $pkgdir/usr/share/man/man8
  install -m 644 $_gitname/pacaur.8 $pkgdir/usr/share/man/man8/pacaur.8 
  mkdir -p $pkgdir/usr/share/locale/{de,fr,ja}/LC_MESSAGES/
  msgfmt $_gitname/po/de.po -o $pkgdir/usr/share/locale/de/LC_MESSAGES/pacaur.mo
  msgfmt $_gitname/po/fr.po -o $pkgdir/usr/share/locale/fr/LC_MESSAGES/pacaur.mo
  msgfmt $_gitname/po/ja.po -o $pkgdir/usr/share/locale/ja/LC_MESSAGES/pacaur.mo

  # use git version number
  cd $srcdir/$_gitname
  sed -i "s/version=\"[0-9].[0-9].[0-9]\"/version=\"$pkgver\"/g" $pkgdir/usr/bin/pacaur
}
