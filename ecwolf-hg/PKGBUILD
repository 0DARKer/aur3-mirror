pkgname=ecwolf-hg
pkgver=685
pkgrel=1
pkgdesc='Advanced source port of Wolfenstein 3D and Spear of Destiny based on Wolf4SDL'
arch=(i686 x86_64)
url="http://maniacsvault.net/ecwolf"
license=(GPL)
depends=(sdl_mixer libjpeg-turbo gtk2)
makedepends=(mercurial cmake)
install=ecwolf.install
source=(hg+https://bitbucket.org/Blzut3/ecwolf
	pcspk.patch
	revert-signon.patch
	https://bitbucket.org/Blzut3/ecwolf/raw/79363bbf182cf5a3fc340e8e4cc6847697bb23b4/wadsrc/static/graphics/{wlf,sod}sgnon.png)
md5sums=('SKIP'
	 '683793d3775b213d4cbbb816675a8250'
	 '3a7f2cb1781e2806a7fe4dfd3bfd27e3'
	 '059ddce23a588547b999df58971dc0a6'
	 '3ccf00c1b87e7ca594b9406b2a71a87d')

pkgver() {
  cd "$srcdir/ecwolf"
  hg identify -n
}

prepare() {
  cd "$srcdir/ecwolf"

  msg2 "Converting line endings to Unix format"
  #find "$srcdir" ! -type d -print |xargs file |grep text |cut -d: -f1 |xargs perl -pi -e 's/\r\n?/\n/g'
  find src -name '*.cpp' -o -name '*.h' |xargs perl -pi -e 's/\r\n?/\n/g'

  msg2 "Patching"
  # Use global datadir
  sed -e 's/progdir(".")/progdir("\/usr\/share\/games\/wolf3d")/' \
	-i src/wl_main.cpp

  # PC speaker sound effects
  patch -p1 -i "$srcdir/pcspk.patch"

  # Bring back the old school, though non-working, system info screen
  # Reverts commits 6f00214 and f717e89 from 2012-08-13
  patch -p1 -i "$srcdir/revert-signon.patch"
  cp "$srcdir"/{wlf,sod}sgnon.png wadsrc/static/graphics/
}

build() {
  cd "$srcdir/ecwolf"
  cmake . -DGPL=ON
  make
}

package() {
  cd "$srcdir/ecwolf"
  install -Dm755 ecwolf "$pkgdir/usr/bin/ecwolf"
  install -Dm644 ecwolf.pk3 "$pkgdir/usr/share/games/wolf3d/ecwolf.pk3"
}

