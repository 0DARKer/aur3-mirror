# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=slapi-nis
pkgver=0.44
pkgrel=1
pkgdesc="NIS Server and Schema Compatibility plugins for Directory Server"
arch=('i686' 'x86_64')
url="http://slapi-nis.fedorahosted.org/"
license=('GPL')
depends=('389-ds-base' 'libtirpc' 'nspr' 'nss' 'openldap' 'tcp_wrappers')
options=('!libtool')
source=("https://fedorahosted.org/releases/s/l/slapi-nis/slapi-nis-${pkgver}.tar.gz")
sha512sums=('d4d3d6e6dd35cbd8e975fcc97dd0e722a104e428e07d8dc40036c5de7d0ee272388801bf2a8934a846b329693429fdbd518c5a9af85c3dd5f2ceaa6e7963687c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --disable-static \
    --with-tcp-wrappers \
    --with-ldap=openldap

  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # 389 needs to be running for this to work
  #make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
