# Maintainer: Gilles Hamel <hamelg at laposte dot net>
pkgname=grafana
_pkgver=2.0.0-beta1
pkgver=2.0.0beta1
pkgrel=2
pkgdesc="A general purpose dashboard and graph composer. It requires graphite, influxdb or opentsdb"
url="http://grafana.org"
arch=('x86_64' 'i686')
license=('APACHE')
depends=(phantomjs)
makedepends=(go nodejs-grunt-cli)
install=${pkgname}.install
source=("https://github.com/${pkgname}/${pkgname}/archive/v${_pkgver}.tar.gz" "gha.patch" "grafana.service")
md5sums=(
	'51e16bcf6553c069f6e3bfeb09958c76'
	'28012b3cd817375e57899ad6be9d1122'
	'1e24873c6e107cde899a91e51eac4890'
	)
prepare () {
  cd "${pkgname}-${_pkgver}"	
  patch -p1 -i "${srcdir}"/gha.patch
}
build() {
  export GOPATH="${srcdir}"/${pkgname}-${_pkgver}
  mkdir $GOPATH/{bin,src}
  PATH=$PATH:$GOPATH/bin
  cd "${srcdir}/${pkgname}-${_pkgver}"
  go run build.go setup
  godep restore
  mkdir -p "${srcdir}"/${pkgname}-${_pkgver}/src/github.com/grafana/grafana/
  ln -s "${srcdir}"/${pkgname}-${_pkgver}/pkg "${srcdir}"/${pkgname}-${_pkgver}/src/github.com/grafana/grafana/
  # build less to css for the frontend 
  npm install
  grunt
  # build the backend
  go run build.go build package
}
package() {
  install -Dm644 "${srcdir}"/grafana.service "$pkgdir/usr/lib/systemd/system/grafana.service"
  cd "${srcdir}/${pkgname}-${_pkgver}"
  install -Dsm755 bin/grafana "$pkgdir/usr/bin/grafana"
  install -Dm644 tmp/conf/defaults.ini "$pkgdir/etc/grafana/grafana.ini"
  mkdir -p "$pkgdir/usr/share/grafana/"
  cp -r tmp/public tmp/vendor "$pkgdir/usr/share/grafana/"
  rm "$pkgdir/usr/share/grafana/vendor/phantomjs/phantomjs"
}

# vim:set ts=2 sw=2 et: