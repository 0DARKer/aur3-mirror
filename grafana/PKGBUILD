# Maintainer: Gilles Hamel <hamelg at laposte dot net>
pkgname=grafana
pkgver=2.0.1
pkgrel=1
pkgdesc="A general purpose dashboard and graph composer. It supports graphite, influxdb or opentsdb"
url="http://grafana.org"
arch=('x86_64' 'i686')
license=('APACHE')
depends=(phantomjs)
makedepends=(go nodejs-grunt-cli)
install=${pkgname}.install
backup=("etc/${pkgname}/${pkgname}.ini")
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz" "gha.patch" "grafana.service")
md5sums=(
	'82687d67d9696130b85ac331efa2bbf2'
	'89dbb1d978b0c7fe7ac887ebfa70f384'
	'27c24a5f8113cc93c83d67d05281a64b'
	)
prepare () {
  cd "${pkgname}-${pkgver}"	
  patch -p1 -i "${srcdir}"/gha.patch
}
build() {
  export GOPATH="${srcdir}"/${pkgname}-${pkgver}
  mkdir $GOPATH/{bin,src}
  PATH=$PATH:$GOPATH/bin
  cd "${srcdir}/${pkgname}-${pkgver}"
  go run build.go setup
  godep restore
  mkdir -p "${srcdir}"/${pkgname}-${pkgver}/src/github.com/grafana/grafana/
  ln -s "${srcdir}"/${pkgname}-${pkgver}/pkg "${srcdir}"/${pkgname}-${pkgver}/src/github.com/grafana/grafana/
  # build less to css for the frontend 
  npm install
  grunt
  # build the backend
  go run build.go build package
}
package() {
  install -Dm644 "${srcdir}"/grafana.service "$pkgdir/usr/lib/systemd/system/grafana.service"
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dsm755 bin/grafana-server "$pkgdir/usr/bin/grafana-server"
  install -Dm644 tmp/conf/sample.ini "$pkgdir/etc/${pkgname}/${pkgname}.ini"
  install -Dm644 tmp/conf/defaults.ini "$pkgdir/usr/share/grafana/conf/defaults.ini"
  cp -r tmp/public tmp/vendor "$pkgdir/usr/share/grafana/"
  rm "$pkgdir/usr/share/grafana/vendor/phantomjs/phantomjs"
}

# vim:set ts=2 sw=2 et: