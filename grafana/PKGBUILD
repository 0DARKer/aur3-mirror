# Maintainer: Gilles Hamel <hamelg at laposte dot net>
pkgname=grafana
_pkgver=2.0.0-beta3
pkgver=2.0.0beta3
pkgrel=2
pkgdesc="A general purpose dashboard and graph composer. It supports graphite, influxdb or opentsdb"
url="http://grafana.org"
arch=('x86_64' 'i686')
license=('APACHE')
depends=(phantomjs)
makedepends=(go nodejs-grunt-cli)
install=${pkgname}.install
source=("https://github.com/${pkgname}/${pkgname}/archive/v${_pkgver}.tar.gz" "gha.patch" "grafana.service")
md5sums=(
	'6d77fb19d07e125c4a0e4cbdae21b6cd'
	'89dbb1d978b0c7fe7ac887ebfa70f384'
	'27c24a5f8113cc93c83d67d05281a64b'
	)
prepare () {
  cd "${pkgname}-${_pkgver}"	
  patch -p1 -i "${srcdir}"/gha.patch
}
build() {
  export GOPATH="${srcdir}"/${pkgname}-${_pkgver}
  mkdir $GOPATH/{bin,src}
  PATH=$PATH:$GOPATH/bin
  cd "${srcdir}/${pkgname}-${_pkgver}"
  go run build.go setup
  godep restore
  mkdir -p "${srcdir}"/${pkgname}-${_pkgver}/src/github.com/grafana/grafana/
  ln -s "${srcdir}"/${pkgname}-${_pkgver}/pkg "${srcdir}"/${pkgname}-${_pkgver}/src/github.com/grafana/grafana/
  # build less to css for the frontend 
  npm install
  grunt
  # build the backend
  go run build.go build package
}
package() {
  install -Dm644 "${srcdir}"/grafana.service "$pkgdir/usr/lib/systemd/system/grafana.service"
  cd "${srcdir}/${pkgname}-${_pkgver}"
  install -Dsm755 bin/grafana-server "$pkgdir/usr/bin/grafana-server"
  install -Dm644 tmp/conf/sample.ini "$pkgdir/etc/grafana/grafana.ini"
  install -Dm644 tmp/conf/defaults.ini "$pkgdir/usr/share/grafana/conf/defaults.ini"
  cp -r tmp/public tmp/vendor "$pkgdir/usr/share/grafana/"
  rm "$pkgdir/usr/share/grafana/vendor/phantomjs/phantomjs"
}

# vim:set ts=2 sw=2 et: