# Maintainer: Gilles Hamel <hamelg at laposte dot net>
pkgname=grafana
pkgver=2.0.2
pkgrel=2
pkgdesc="A general purpose dashboard and graph composer. It supports graphite, influxdb or opentsdb"
url="http://grafana.org"
arch=('x86_64' 'i686')
license=('APACHE')
depends=(phantomjs)
makedepends=(go nodejs-grunt-cli)
install=${pkgname}.install
backup=("etc/${pkgname}/${pkgname}.ini")
source=("https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz" "gha.patch" "grafana.service" "0001-Replaced-slug-dependency-with-one-that-did-not-use-g.patch")
md5sums=(
	'ac0111eef2e1d3329edfcfe2c6346255'
	'3d34c2212cf9d18cc1041ff96b3330d8'
	'05508c9c21f4c93ad86944a52d37e925'
	'b9a46a357bdd157fa5811d92f6de01ae'
	)
prepare () {
  cd "${pkgname}-${pkgver}"	
  patch -p1 -i "${srcdir}"/gha.patch
  patch -p1 -i "${srcdir}"/0001-Replaced-slug-dependency-with-one-that-did-not-use-g.patch
}
build() {
  export GOPATH="${srcdir}"/${pkgname}-${pkgver}
  mkdir $GOPATH/{bin,src}
  PATH=$PATH:$GOPATH/bin
  cd "${srcdir}/${pkgname}-${pkgver}"
  go run build.go setup
  godep restore
  mkdir -p "${srcdir}"/${pkgname}-${pkgver}/src/github.com/grafana/grafana/
  ln -s "${srcdir}"/${pkgname}-${pkgver}/pkg "${srcdir}"/${pkgname}-${pkgver}/src/github.com/grafana/grafana/
  # build less to css for the frontend 
  npm install
  grunt
  # build the backend
  go run build.go build package
}
package() {
  install -Dm644 "${srcdir}"/grafana.service "$pkgdir/usr/lib/systemd/system/grafana.service"
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dsm755 bin/grafana-server "$pkgdir/usr/bin/grafana-server"
  install -Dm644 tmp/conf/sample.ini "$pkgdir/etc/${pkgname}/${pkgname}.ini"
  install -Dm644 tmp/conf/defaults.ini "$pkgdir/usr/share/grafana/conf/defaults.ini"
  cp -r tmp/public tmp/vendor "$pkgdir/usr/share/grafana/"
  rm "$pkgdir/usr/share/grafana/vendor/phantomjs/phantomjs"
}

# vim:set ts=2 sw=2 et: