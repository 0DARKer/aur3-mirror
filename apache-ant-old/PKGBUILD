# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: giniu <gginiu@gmail.com>
pkgname=apache-ant-old
pkgver=1.7.1
pkgrel=1
pkgdesc="A java-based build tool, version 1.7 for apps not compatible with 1.8 changes."
arch=('i686' 'x86_64')
license=('APACHE')
url="http://ant.apache.org/"
depends=('java-runtime')
provides=('apache-ant')
conflicts=('apache-ant')
optdepends=('junit: to jave junit on the classpath in javac tasks')
source=(http://archive.apache.org/dist/ant/binaries/apache-ant-${pkgver}-bin.tar.bz2
        ${pkgname}.sh)
md5sums=('9330447f3763b87570dd1118c49a8efd'
         'f5c7c713fc8cd250cdeed0c496bb05f4')

build() {
  # install profile.d script
  install -dm755 ${pkgdir}/etc/profile.d || return 1
  install -m755 ${srcdir}/${pkgname}.sh ${pkgdir}/etc/profile.d/ || return 1

  # Get the ANT_HOME env var
  source ${srcdir}/${pkgname}.sh || return 1

  cd ${srcdir}/apache-ant-${pkgver}
  install -dm755 ${pkgdir}/${ANT_HOME}/{bin,lib} || return 1

  install -m644 ./lib/*.jar ${pkgdir}/${ANT_HOME}/lib || return 1
  cp -Rp ./etc ${pkgdir}/${ANT_HOME} || return 1

  # Do not copy Windows .bat/.cmd files
  find ./bin -type f -a ! -name \*.bat -a ! -name \*.cmd \
    -exec install -m755 {} ${pkgdir}/${ANT_HOME}/bin \; || return 1

  # symlink to junit so it's on the javac build path for ant
  # matches behavior on ubuntu 9 and makes sense for compatibility
  # http://bugs.archlinux.org/task/15229
  cd ${pkgdir}/${ANT_HOME}/lib || return 1
  ln -s ../../junit.jar . || return 1
  cd - || return 1

  # The license says the NOTICE file should be redistributed for derivative
  # works, so lets supply it.
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname} || return 1
  install -m644 LICENSE NOTICE ${pkgdir}/usr/share/licenses/${pkgname} || return 1
}

