# Maintainer: Audric Schiltknecht <storm+arch@chemicalstorm.org>
pkgname=opensand-core
pkgver=0.9.0
pkgrel=1
pkgdesc="The OpenSAND emulation testbed core"
arch=('i686')
url="http://www.opensand.org"
license=('GPL3')
groups=('opensand')
depends=('opensand-env-plane' 'opensand-margouilla' 'opensand-conf')
install='opensand-core.install'
source=("https://launchpad.net/opensand/0.9.x/0.9.0/+download/opensand-${pkgver}.tar.gz"
        "https://bugs.launchpad.net/opensand/+bug/1045240/+attachment/3291124/+files/fix_bug_1045240_core.patch"
        "fix_libdl.patch")
md5sums=('16ccb9ce24259f7ca943f05b5b322bc1'
         'dd2275929b9f3f7c8efa820531214eb3'
         '5e0c21e57814144ac8c6983877fae2c0')

build() {
  cd "$srcdir/opensand-$pkgver"
  tar xf ${pkgname}-${pkgver}.tar.gz
  cd "${pkgname}-${pkgver}"
  patch -p1 < $srcdir/fix_libdl.patch
  patch -p1 < $srcdir/fix_bug_1045240_core.patch
  aclocal
  libtoolize --force
  autoconf
  autoheader
  automake
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "$srcdir/opensand-$pkgver/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/opensand-$pkgver/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Copy configuration
  install -Dm644 conf/core_sat.conf "${pkgdir}/usr/share/opensand/sat/core.conf"
  install -Dm644 conf/core_st.conf "${pkgdir}/usr/share/opensand/st/core.conf"
  install -Dm644 conf/core_gw.conf "${pkgdir}/usr/share/opensand/gw/core.conf"

  install -Dm644 conf/core_global.conf "${pkgdir}/usr/share/opensand/core_global.conf"
  install -Dm644 conf/topology.conf "${pkgdir}/usr/share/opensand/topology.conf"

  install -Dm644 conf/xsd/core_sat.xsd "${pkgdir}/usr/share/opensand/sat/core.xsd"
  install -Dm644 conf/xsd/core_st.xsd "${pkgdir}/usr/share/opensand/st/core.xsd"
  install -Dm644 conf/xsd/core_gw.xsd "${pkgdir}/usr/share/opensand/gw/core.xsd"

  install -Dm644 conf/xsd/core_global.xsd "${pkgdir}/usr/share/opensand/core_global.xsd"
  install -Dm644 conf/xsd/topology.xsd "${pkgdir}/usr/share/opensand/topology.xsd"

  install -Dm644 conf/deploy.ini "${pkgdir}/usr/share/opensand/deploy.ini"

  # Copy MODCOD and DRA simulation files
  mkdir -p "${pkgdir}/usr/share/opensand/modcod_dra/collective"
  mkdir -p "${pkgdir}/usr/share/opensand/modcod_dra/individual"
  mkdir -p "${pkgdir}/usr/share/opensand/modcod_dra/clear_sky"

  install -Dm644 conf/modcod_dra/collective/*.txt "${pkgdir}/usr/share/opensand/modcod_dra/collective/"
  install -Dm644 conf/modcod_dra/individual/*.txt "${pkgdir}/usr/share/opensand/modcod_dra/individual/"
  install -Dm644 conf/modcod_dra/clear_sky/*.txt "${pkgdir}/usr/share/opensand/modcod_dra/clear_sky/"

  # Copy default configuration
  install -Dm644 conf/modcod_dra/clear_sky/def_modcods.txt "${pkgdir}/etc/opensand/modcod_dra/modcod_def.txt"
  install -Dm644 conf/modcod_dra/clear_sky/cst_modcods.txt "${pkgdir}/etc/opensand/modcod_dra/modcod_simu.txt"
  install -Dm644 conf/modcod_dra/clear_sky/def_dra_scheme.txt "${pkgdir}/etc/opensand/modcod_dra/dra_def.txt"
  install -Dm644 conf/modcod_dra/clear_sky/cst_dra_scheme.txt "${pkgdir}/etc/opensand/modcod_dra/dra_simu.txt"

  # Copy DAMA request simulation file
  install -Dm644 conf/dama.input "${pkgdir}/usr/share/opensand/request_simu/dama.input"

  # Copy default configuration
  install -Dm644 conf/dama.input "${pkgdir}/etc/opensand/simulation/dama.input"
}

# vim:set ts=2 sw=2 et:
