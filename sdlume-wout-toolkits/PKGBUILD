# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based on: Anton Shestakov <engored*ya.ru>

pkgname=sdlume-wout-toolkits
pkgver="0.159.u0"
_pkgver="$(echo ${pkgver} | tr -d . | cut -d "u" -f1)"
pkgrel=1
pkgdesc="Universal Machine Emulator combines the features of MAME and MESS into a single multi-purpose emulator. Without toolkits (gtk and qt)"
url='http://mamedev.emulab.it/haze/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
conflicts=('sdlume')
depends=('sdl2_ttf' 'flac' 'portmidi' 'libjpeg-turbo' 'libxinerama' 'sqlite')
makedepends=('nasm' 'mesa' 'glu' 'wget' 'python2')
optdepends=('sdlmamefamily-tools: Tools for manage MAME/MESS/UME roms')
source=("https://github.com/mamedev/mame/archive/mame${_pkgver}.tar.gz"
        "sdlume.sh")
sha1sums=('c68b620b04c6da4fe6776a13cdc0aed776a1bed0'
          'e0521878c21a9d8369cab1e4b22fc0300b84b082')
install="sdlume.install"

prepare() {
  cd "mame-mame${_pkgver}"

  find . -type f -not -name "*.png" -exec perl -pi -e 's|\r\n?|\n|g' "{}" \;
}

build() {
  cd "mame-mame${_pkgver}"

  [ "${CARCH}" = "x86_64" ] && _ptr64=1 && _sse2=1
  [ "${CARCH}" = "i686" ] && _ptr64=0 && _sse2=0

  make PYTHON=python2 PTR64="${_ptr64}" SSE2="${_sse2}" OPTIMIZE=2 NOWERROR=1 USE_OPENGL=1 BUILD_EXPAT=0 BUILD_ZLIB=1 BUILD_FLAC=0 BUILD_JPEGLIB=0 BUILD_MIDILIB=0 BUILD_SQLITE3=0 NO_USE_QTDEBUG=1 FULLNAME=sdlume TARGET=ume
}

package() {
  cd "mame-mame${_pkgver}"
  # Installing the wrapper script
  install -Dm755 ../sdlume.sh "${pkgdir}/usr/bin/sdlume"

  # Installing binaries
  install -Dm755 sdlume "${pkgdir}/usr/share/sdlume/sdlume"

  # Installing extra bits
  install -d "${pkgdir}/usr/share/sdlume/"{artwork,hash,shader,keymaps}
  install -m644 artwork/* "${pkgdir}/usr/share/sdlume/artwork/"
  install -m644 hash/* "${pkgdir}/usr/share/sdlume/hash/"
  install -m644 src/osd/sdl/shader/glsl*.*h "${pkgdir}/usr/share/sdlume/shader/"
  install -m644 src/osd/sdl/keymaps/* "${pkgdir}/usr/share/sdlume/keymaps/"

  # The license
  install -Dm644 docs/license.txt "${pkgdir}/usr/share/licenses/custom/${pkgname}/license.txt"

  # documentation
  cd docs
  for i in $(find . -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/doc/${pkgname}/${i}"; done
}
