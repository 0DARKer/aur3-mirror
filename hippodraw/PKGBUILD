# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Sebastien Binet (binet@cern.ch)

pkgname=hippodraw
pkgver=1.21.3
pkgrel=2
pkgdesc="A highly interactive data analysis environment written in C++/Qt"
arch=('i686' 'x86_64')
url="http://www.slac.stanford.edu/grp/ek/hippodraw/"
license=('GPL')
depends=('qt' 'boost' 'minuit2' 'cfitsio' 'python-numarray' 'python-pyfits')
options=('!libtool')
source=(ftp://ftp.slac.stanford.edu/users/pfkeb/$pkgname/${pkgname^^[hd]}-$pkgver.tar.gz
        $pkgname.patch)
md5sums=('d4b427b7469af5728951eab8c502074d'
         '9d46527e305b3d30a6db73c9ea2821ee')

build() {
  cd "$srcdir"/${pkgname^^[hd]}-$pkgver

  # some fixes
  sed -i "s_Minuit2Base_Minuit2_" configure
  sed -i "s_/usr/include/cfitsio_/usr/include_" configure
  patch -Np0 -i ../$pkgname.patch || return 1

  # configure
  LDFLAGS="-fopenmp" ./configure \
    --prefix=/usr \
    --disable-help \
    --with-qt4-dir=/usr \
    --with-qt4-include=/usr/include \
    --with-qt4-lib=/usr/lib || return 1

  # make and install
  make || return 1
  make prefix="$pkgdir"/usr install || return 1
}
