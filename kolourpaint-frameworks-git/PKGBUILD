#maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kolourpaint-frameworks-git
pkgver=r1957.84f6261
pkgrel=1
pkgdesc="Paint Program (GIT version)"
url="http://kde.org/applications/graphics/kolourpaint/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs4support' 'qimageblitz-qt5-svn' 'libkexiv2' )
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('kdegraphics-kolourpaint')
source=("git://anongit.kde.org/kolourpaint.git#branch=frameworks")
sha1sums=('SKIP')
install="kolourpaint-frameworks-git.install"
_gitname=kolourpaint

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  rm -fr build
  mkdir -p build
  cd build
  
  cmake "../${_gitname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  
  make DESTDIR="${pkgdir}" install
}
