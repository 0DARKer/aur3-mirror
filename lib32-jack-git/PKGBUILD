# Maintainer: jhernberg@alchemy.lu>
# Contributor: Ray Rashif <schiv@archlinux.org>

_pkgbasename=jack-git
pkgname=lib32-$_pkgbasename
pkgver=20130224
pkgrel=1
pkgdesc="A low-latency audio server (32-bit client libs)"
arch=('x86_64')
url="http://jackaudio.org"
license=('GPL' 'LGPL')
depends=('lib32-glibc' "$_pkgbasename=$pkgver")
makedepends=('git' 'gcc-multilib')
provides=('lib32-jack')
conflicts=('lib32-jack')
replaces=('lib32-jack')
options=('!libtool')
md5sums=()

_gitroot=git://github.com/jackaudio/jack1.git
_gitname=jack1-git

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname"
    git submodule init
    git submodule update
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  git submodule init
  git submodule update
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac
  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib32
  make -C libjack
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR=${pkgdir} install-pkgconfigDATA
  make -C libjack DESTDIR=${pkgdir} install
}
# vim:set ts=2 sw=2 et:
