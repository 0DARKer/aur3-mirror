# Contributor: Ananda Samaddar ananda@samaddar.co.uk
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: atommix aka Aleks Lifey <Aleks.Lifey@gmail.com>
# Contributor: Keshav P R <(skodabenz) (aatt) (rocketmail) (ddoott) (ccoomm)>

_pkgname=jitsi
pkgname=$_pkgname-stable
pkgver=1.0.beta1.3820
pkgrel=1
pkgdesc="An audio/video/chat communicator that supports protocols such as SIP and XMPP/Jabber"
arch=('i686' 'x86_64')
url="http://jitsi.org"
license=('LGPL')
depends=('java-runtime=6')
makedepends=('apache-ant' 'java-environment=6')
provides=("$_pkgname=$pkver")
conflicts=("$_pkgname")
options=(!strip !emptydirs zipman !libtool docs)
source=("http://download.jitsi.org/jitsi/src/jitsi-src-1.0-beta1-nightly.build.${pkgver##*.}.zip"
        $_pkgname.{desktop,sh})
md5sums=('a5675da30e2e5fd801d2457ddddba0f9'
         '5923eeca35823f8ef1e416a9a6fc0fac'
         '55fe144bbf19283e7ec5d7bd3228cf5b')
sha256sums=('74d845b993cdd53856415a179ac72bbe9c6f5db37482874eab7be34490e7c0c2'
            '770132b617d94ed468e9592b991ceac10eb3e03e7198b8f5f2f05918f7db4302'
            '98027ddaa1ebfe948fa49f6e57ab0d2c866d5e7c37873e30638b86223a2a15e9')

# uncomment to get the latest stable release; you have to adjust/skip checksums
#source[0]=http://download.jitsi.org/jitsi/src/$(curl -Ss 'http://download.jitsi.org/jitsi/src/' | grep -om1 'jitsi-src[^"<]\+.zip' | head -1)
#pkgver=$(sed -r 's/[^0-9]*([0-9.]+)(-(beta[0-9]+))?.*(.build.([0-9]+))\..*/\1.\3.\5/' <<<${source[0]})

build() {
	cd "$srcdir/$_pkgname"

	# append the build revision to the jitsi version
	sed -i "s/0\.build\.by\.SVN/build.${pkgver##*.}/" src/net/java/sip/communicator/impl/version/NightlyBuildID.java

	. /etc/profile.d/apache-ant.sh
	ant rebuild
}

package() {
	cd "$srcdir/$_pkgname"

	find lib/ lib/bundle/ -maxdepth 1 -type f \
		-exec install -Dm644 {} "$pkgdir/usr/lib/$_pkgname/"{} \;
	find lib/os-specific/linux/ -maxdepth 1 -type f \
		-execdir install -Dm644 {} "$pkgdir/usr/lib/$_pkgname/lib/"{} \;

	find lib/native/linux$(sed 's/_/-/g' <<<${CARCH/#*(i?86|x86)/})/ -maxdepth 1 -type f \
		-execdir install -Dm644 {} "$pkgdir/usr/lib/$_pkgname/lib/native/"{} \;

	find sc-bundles/{,os-specific/linux/} -maxdepth 1 -type f \
		-execdir install -Dm644 {} "$pkgdir/usr/lib/$_pkgname/sc-bundles/"{} \;

	install -Dm755 "$srcdir/$_pkgname.sh"      "$pkgdir/usr/bin/$_pkgname"
	install -Dm644 "$srcdir/$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"

	local _file
	for _file in resources/install/debian/*.{svg,xpm}; do
		install -Dm644 "$_file" "$pkgdir/usr/share/pixmaps/$_pkgname${_file/*sip-communicator/}"
	done
}
