# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>

pkgname=csound
pkgver=6.02
pkgrel=1
pkgdesc="A programming language for sound rendering and signal processing."
arch=(i686 x86_64)
url="http://csound.sourceforge.net/"
license=('LGPL')
depends=('fltk' 'fluidsynth' 'liblo' 'portaudio' 'portmidi' 'tk' 'curl' 'stk')
makedepends=('cmake' 'gmm' 'swig' 'java-environment' 'dssi' 'boost' 'pd' 'luajit' 'eigen')
optdepends=('qutecsound: qt frontend'
            'cecilia: tcl/tk frontend'
            'cecilia4: wxpython frontend'
            'vim: vim frontend'
            'java-environment: java wrapper'
            'luajit: lua wrapper'
            'python2: python wrapper')
provides=('csound6')
conflicts=('csound6')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname${pkgver%.*}/Csound$pkgver/Csound$pkgver.tar.gz" csound.sh)
md5sums=('c99a890eb771a7626768ad7cec579958'
         '2e3a1f546dcb2c125ae5b7247eb81d6d')

prepare() {
  cd Csound$pkgver

  # install modules to proper paths
  sed -i '/^set.*MODULE_INSTALL_DIR/d' CMakeLists.txt
  sed -i '/pdname/{n;s/LIBRARY/PD_MODULE/2}' frontends/CMakeLists.txt
}

build() {
  cd Csound$pkgver
  [ -d bld ] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DPYTHON_MODULE_INSTALL_DIR=/usr/lib/python2.7/site-packages \
           -DJAVA_MODULE_INSTALL_DIR=/usr/lib/csound/java \
           -DLUA_MODULE_INSTALL_DIR=/usr/lib/lua/5.1 \
           -DPD_MODULE_INSTALL_DIR=/usr/lib/pd/extra
  make
}

package() {
  cd Csound$pkgver/bld
  make DESTDIR="$pkgdir/" install

  # export vars in profile.d
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/etc/profile.d/$pkgname.sh"
}

# vim:set ts=2 sw=2 et:
