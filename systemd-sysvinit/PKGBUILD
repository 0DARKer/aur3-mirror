# Contributor: udvzsolt < udvzsolt gmail com >
# Based on package `sysvinit'

pkgname=systemd-sysvinit
_pkgname=sysvinit
pkgver=2.88
pkgrel=2
pkgdesc="Some tools from sysvinit (last, lastb, wall, pidof)"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/sysvinit"
license=('GPL')
groups=('base')
depends=('shadow' 'util-linux' 'coreutils' 'glibc' 'awk')
conflicts="sysvinit"
source=(http://download.savannah.gnu.org/releases/sysvinit/${_pkgname}-${pkgver}dsf.tar.bz2
pidof.8
)
md5sums=('6eda8a97b86e0a6f59dabbf25202aa6f'
         '05acb14189436ca7a54232fa8bbfb2d5')
sha1sums=('f2ca149df1314a91f3007cccd7a0aa47d990de26'
          'f6a7bf24eabf5e3a5125c55da62a1da5d3e8d3ba')

_progs="last wall"
build() {
  cd "${srcdir}/${_pkgname}-${pkgver}dsf/src"
  make ${_progs}
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}dsf"
  install -d $pkgdir/usr/bin/
  cd src
  install -m755 ${_progs} $pkgdir/usr/bin

  cd ../man
  install -d $pkgdir/usr/share/man/man{1,8}
  for manpage in ${_progs} lastb; do
      install -m644 $manpage.* $pkgdir/usr/share/man/man1
  done

  cd $pkgdir/usr/bin
  ln -s last lastb

  ln -s killall pidof
  install ${srcdir}/pidof.8 $pkgdir/usr/share/man/man8
}
