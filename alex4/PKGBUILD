# Maintainer: megadriver <megadriver at gmx dot com>

pkgname=alex4
pkgver=1.1
pkgrel=3
pkgdesc="Help Alex rescue his girlfriend before the humans make a pair of shoes out of her!"
url="http://allegator.sourceforge.net"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dumb')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/allegator/Alex4/source%20and%20data/alex4src_data.zip"
         unix-port.patch allegro-4.2.patch $pkgname.png $pkgname.desktop)
md5sums=('c81b8e3813300a0cfbe828bcdff60fda'
         '8dcafad79e34fc9adb78a49b6933e10c'
         'f9ccd450934d346deec450e91ecfc42e'
         '1c21affded614148ad06deb1bb59086c'
         '6ba86e2da472b4fe33cf206b7f4636c5')

build() {
    cd $srcdir/alex4src
    patch -p1 < ../unix-port.patch
    patch -p1 < ../allegro-4.2.patch
    cd src
    make PREFIX=/usr 
}

package() {
    cd $srcdir/alex4src/src
    make PREFIX=$pkgdir/usr install
    install -m644 ../$pkgname.ini $pkgdir/usr/share/$pkgname
    install -D -m644 $srcdir/$pkgname.png $pkgdir/usr/share/icons/$pkgname.png
    install -D -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
