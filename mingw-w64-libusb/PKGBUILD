# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-libusb
pkgver=1.0.18rc1
_libusbver=1.0.18-rc1
pkgrel=1
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
pkgdesc="Library that provides generic access to USB devices (mingw-w64)"
arch=(any)
url="http://libusb.info"
license=('LGPL')
replaces=('mingw-w64-libusbx')
options=('staticlibs' '!makeflags' '!strip' '!buildflags')
source=(http://downloads.sourceforge.net/project/libusb/libusb-1.0/libusb-${_libusbver%-*}/libusb-${_libusbver}.tar.bz2)
md5sums=('6195d43451e07e2a71e956008c2527b9')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  cd "$srcdir/libusb-${_libusbver}"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --enable-shared
    make
    popd
  done
}

package () {
  for _arch in ${_architectures}; do
    cd "$srcdir/libusb-${_libusbver}/build-${_arch}"
    make install DESTDIR="$pkgdir"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a    
  done
}
