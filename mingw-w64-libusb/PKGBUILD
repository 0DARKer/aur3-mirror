# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-libusb
pkgver=1.0.18
pkgrel=1
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
pkgdesc="Library that provides generic access to USB devices (mingw-w64)"
arch=(any)
url="http://libusb.info"
license=('LGPL')
conflicts=('mingw-w64-libusbx')
replaces=('mingw-w64-libusbx')
options=('staticlibs' '!makeflags' '!strip' '!buildflags')
source=("http://downloads.sourceforge.net/project/libusb/libusb-1.0/libusb-${pkgver}/libusb-${pkgver}.tar.bz2")
md5sums=('4a6d049923efc6496f1d383054dac1a6')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  cd "libusb-${pkgver}"
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --enable-shared
    make
    popd
  done
}

package () {
  cd "libusb-${pkgver}"
  for _arch in ${_architectures}; do
    pushd "build-${_arch}"
    make install DESTDIR="${pkgdir}"
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
    popd
  done
}
