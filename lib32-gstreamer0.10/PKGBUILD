# Maintainer : josephgbr <rafael.f.f1 at gmail.com>

_pkgbasename=gstreamer0.10
pkgname=lib32-$_pkgbasename
pkgver=0.10.35
pkgrel=1
pkgdesc="GStreamer Multimedia Framework (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('lib32-libxml2' 'lib32-glib2' 'gstreamer0.10')
makedepends=('intltool' 'gcc-multilib')
options=('!libtool')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.bz2)
sha256sums=('817bfea2cd46e2487b97e2ed9218f0299b32a3de1e5e80b4c7868d17e9089786')
 
build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cd "${srcdir}/gstreamer-${pkgver}"
  
  ./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--libdir=/usr/lib32 \
	--libexecdir=/usr/lib32 \
	--enable-introspection=no \
	--disable-{gtk-doc,static} \
	--with-package-name="GStreamer (Archlinux)" \
	--with-package-origin="http://www.archlinux.org/"
  make
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}

