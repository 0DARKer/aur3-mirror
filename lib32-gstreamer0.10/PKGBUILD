_pkgbasename=gstreamer0.10
pkgname=lib32-$_pkgbasename
pkgver=0.10.34
pkgrel=1
pkgdesc="GStreamer Multimedia Framework (32 bit, library only)"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('lib32-libxml2>=2.7.8' 'lib32-glib2>=2.24.1' 'gstreamer0.10>=$pkgver')
makedepends=('intltool' 'gcc-multilib')
options=('!libtool')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.bz2)
sha256sums=('85348f70dc4b70ad1beb05c9a59a64175c5058f4ee5273f89230a3c1d11b26a3')
 
build() {
  cd "${srcdir}/gstreamer-${pkgver}"
 
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
                        --libexecdir=/usr/lib32 --libdir=/usr/lib32 \
                        --enable-introspection=no CC='gcc -m32' CXX='g++ -m32'
  make
}
 
package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}

