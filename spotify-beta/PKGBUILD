# Maintainer: Gadget3000 <gadget3000@msn.com>
# Contributor: Eothred <yngve.levinsen@gmail.com>
# Contributor: xintron <carlsson.marcus@gmail.com>

pkgname=spotify-beta
pkgver=0.8.0.1031
_anotherpkgver=.ga1569aa.552-1
pkgrel=1
pkgdesc="A proprietary peer-to-peer music streaming service"
makedepends=('tar')
arch=('x86_64' 'i686')
license=('custom:"Copyright (c) 2006-2010 Spotify Ltd"')
url="http://www.spotify.com"
changelog='spotify.changelog'
conflicts=('spotify')
install=spotify.install

#Spotify changelog is installed to /usr/share/doc/spotify-client/

if [ "${CARCH}" = "x86_64" ]; then
  md5sums=('93d6dccd2064684eb4c3ac42bfe0a986'
           'a6352bd4af8682ff7c55a2beacbcecb2'
           'b30c062c7cfc15567f29629e0f9fabdb'
	   'ef25ddc5b6bf8fe1a0d64cbd79e1f7b4')
  _carch=_amd64
elif [ "${CARCH}" = "i686" ]; then
  md5sums=('c96d9af6d5fa046b994c9d9d2d1751e1'
           'a6352bd4af8682ff7c55a2beacbcecb2'
           'b30c062c7cfc15567f29629e0f9fabdb'
	   'ef25ddc5b6bf8fe1a0d64cbd79e1f7b4')
  _carch=_i386
fi

depends=("alsa-lib>=1.0.14" "glibc>=2.6" "qt>=4.5.0" "libxss" "openssl" "qtwebkit" "gtk2" "gconf" "libpng12" "networkmanager")
source=("http://download.spotify.com/preview/spotify-client_${pkgver}${_anotherpkgver}${_carch}.deb"
        'spotify.desktop'
        'spotify.png'
	'spotify.protocol')

build() {
  cd ${srcdir}
  ar x spotify-client_${pkgver}${_anotherpkgver}${_carch}.deb > /dev/null || return 1
}

package() {
  cd ${srcdir}
  tar -xzf data.tar.gz -C ${pkgdir} || return 1

#Copy license
  install -d ${pkgdir}/usr/share/licenses/spotify-beta
  install -D -m644 ${pkgdir}/usr/share/doc/spotify-client/copyright \
	  ${pkgdir}/usr/share/licenses/spotify-beta/ || return 1

#Copy icon
  install -d ${pkgdir}/usr/share/pixmaps/
  install -D -m644 ${srcdir}/spotify.png ${pkgdir}/usr/share/pixmaps/ || return 1

#Copy desktop file
  install -d ${pkgdir}/usr/share/applications/
  install -D -m644 ${srcdir}/spotify.desktop ${pkgdir}/usr/share/applications/ || return 1

#Copy protocol file if KDE is installed
if [ -f /usr/bin/startkde ]; then
  echo "Installing with KDE support"
  install -d ${pkgdir}/usr/share/kde4/services/
  install -D -m644 ${srcdir}/spotify.protocol ${pkgdir}/usr/share/kde4/services/ || return 1
fi

#Link ssl 0.9.8 libraries to most up to date openssl
#as long as the libraries don't already exist as a standard file (ie and isn't a symlink)
if [ ! -f /usr/lib/libssl.so.0.9.8 ] || [ -L /usr/lib/libssl.so.0.9.8 ]; then 
if [ ! -f /usr/lib/libcrypto.so.0.9.8 ] || [ -L /usr/lib/libcrypto.so.0.9.8 ]; then
  install -d ${pkgdir}/usr/lib
  ln -s /usr/lib/libssl.so ${pkgdir}/usr/lib/libssl.so.0.9.8
  ln -s /usr/lib/libcrypto.so ${pkgdir}/usr/lib/libcrypto.so.0.9.8
fi
fi
}

