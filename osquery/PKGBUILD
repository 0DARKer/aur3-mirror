# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=osquery
pkgver=1.0.3
pkgrel=1
pkgdesc='Tool makes low-level operating system analytics and monitoring both performant and intuitive'
arch=(i686 x86_64)
url='https://github.com/facebook/osquery/'
license=(BSD3)
depends=(rocksdb google-glog python2-jinja thrift procps-ng)
makedepends=(cmake doxygen boost gflags)
source=(https://github.com/facebook/osquery/archive/$pkgver.zip
        gflags_compile.patch
        fix_lib_path.patch
        https://github.com/osquery/third-party/archive/master.zip)
sha1sums=('a245aaa356eb4fc6fdbdd4c487c08fe227fd191f'
          '2c024cdc01fb7c63b9f1af240fe4a3fa91f2f786'
          '85faffb875627033f4bca846c48f7afa1a7d96fd'
          '12cedba20e67c8e79463a5040c636c5cd6403122')
# https://github.com/facebook/osquery/issues/334

prepare() {
  cd osquery-$pkgver
  patch -p1 < ../gflags_compile.patch
#  patch -p1 < ../fix_lib_path.patch

  rm -r third-party
  ln -sf ../third-party-master third-party

  sed -e 's|python|python2|g' -i osquery/CMakeLists.txt
}

build() {
  cd osquery-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_SHARED_LIBS=ON
  make VERBOSE=1
}

check() {
  cd osquery-$pkgver
  make test
}

package () {
  cd osquery-$pkgver
  make DESTDIR="$pkgdir" install
}
