# Maintainer: paolo <paolostivanin@gmail.com>

pkgname=awn-extras-bzr
pkgver=1463
pkgrel=1
pkgdesc="Avant-window-navigator applets with minimal dependencies"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL')
depends=('avant-window-navigator-bzr' 'gnome-menus' 'libgtop' 'libnotify' 
	 'libsexy' 'vte' 'gstreamer0.10-python')
makedepends=('intltool' 'libdesktop-agnostic' 'vala' 'bzr')
optdepends=('python-notify: needed for some applets')
provides=('awn-extras-applets=0.4.1')
conflicts=('awn-extras-applets' 'vala-git')
install=awn-extras-applets.install
options=('!libtool')

_bzrmod=awn-extras
_bzrtrunk=lp:${_bzrmod}

build() {
  msg "Connecting to the server..."

  if [ ! -d ./${_bzrmod} ]; then
    bzr co ${_bzrtrunk} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_bzrmod}-build"
  cp -r "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
  cd "${srcdir}/${_bzrmod}-build"

  gnome_flag="--without-gnome"

  if ls /usr/include/gnome-menus &>/dev/null; then
    gnome_flag="--with-gnome"
  fi

  webkit_flag="--without-webkit"

  if ls /usr/include/webkit-* &>/dev/null; then
    webkit_flag="--with-webkit"
  fi

  if ls /usr/include/libindicator-* &>/dev/null; then
    indicator_flag="--with-indicator"
  fi

  PYTHON=/usr/bin/python2 ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --datadir=/usr/share \
    --disable-static \
    --enable-sound=auto \
    ${webkit_flag} \
    ${gnome_flag} \
    ${indicator_flag} \
    --disable-pymod-checks
    make
}

package() {
        cd "$srcdir/${_bzrmod}-build"
        make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

	# yes... theses guys can not even get a "#!" right
	sed -i -e "s|#[ ]*[!]*[ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
	       -e "s|#[ ]*![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
	  $(find $pkgdir/usr/share/avant-window-navigator/applets/ -name '*.py') \
	  $pkgdir/usr/lib/python2.7/site-packages/awn/extras/awnmediaplayers.py

        mkdir -p ${pkgdir}/usr/share/gconf/schemas
        gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/awn-extras.schemas \
            ${pkgdir}/etc/gconf/schemas/*.schemas
        rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
        rmdir --ignore-fail-on-non-empty -p ${pkgdir}/usr/share/locale
        rmdir --ignore-fail-on-non-empty -p ${pkgdir}/etc/gconf/schemas
}
