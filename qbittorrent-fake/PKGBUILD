# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Maintainer: B.Vasilyev <bobartmail@gmail.com>

pkgname=qbittorrent-fake
pkgver=2.8.2
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt4 using the good libtorrent library with fake of user-agent to uTorrent"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt' 'libtorrent-rasterbar' 'xdg-utils')
makedepends=('boost' 'geoip')
optdepends=('python2: needed for search'
            'geoip: improves peer country resolution')
provides=('qbittorrent')
conflicts=('qbittorrent')
replaces=('qbittorrent')
install='qbittorrent.install'
source=("http://downloads.sourceforge.net/sourceforge/qbittorrent/qbittorrent-${pkgver}.tar.gz"
        'python2.patch' 'rutracker.patch')
md5sums=('58008b6b0233c7997067a61cf07af50d'
         'db0d79fee8ce3470ad3741d36b02a94c' 
         'e39a29a050e2c4fdea82c5b27bb1f749')

build() {
  cd qbittorrent-${pkgver}
  
  patch -Np1 -i ${srcdir}/rutracker.patch

  sed -i \
    -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find ./ -name '*.py')

  patch -Np1 -i ${srcdir}/python2.patch
  
  ./configure \
    --prefix=/usr

  make
}

package() {
  cd qbittorrent-${pkgver}

  make INSTALL_ROOT=${pkgdir} install
}
