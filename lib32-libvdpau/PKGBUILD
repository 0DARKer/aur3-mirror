# Maintainer : josephgbr <rafael.f.f1@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: TryA <tryagainprod {at} gmail.com>

_pkgbase=libvdpau
pkgname=lib32-libvdpau
pkgver=0.4.1
pkgrel=4
pkgdesc="Nvidia VDPAU library (32-bit)"
arch=('x86_64')
url=http://cgit.freedesktop.org/~aplattner/libvdpau
license=('custom')
depends=('lib32-gcc-libs' ${_pkgbase})
makedepends=('gcc-multilib' 'lib32-libx11')
options=('!libtool')
source=(http://people.freedesktop.org/~aplattner/vdpau/${_pkgbase}-${pkgver}.tar.gz)
sha256sums=('061b6f86c64912f79851bfefcac4f77b401e9a939dbbb7b1ec795bc7d5aaf59b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  # disable libxext, just like 'libvdpau' pkg
  sed '/pkg_cv_XEXT_/s/xext//' -i configure
  ./configure --prefix=/usr --libdir=/usr/lib32 
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/include

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbase "$pkgdir/usr/share/licenses/$pkgname"
}
