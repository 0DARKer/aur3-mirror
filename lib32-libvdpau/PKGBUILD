# Maintainer: TryA <tryagainprod {at} gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libvdpau
pkgname=lib32-libvdpau
pkgver=0.4.1
pkgrel=1
pkgdesc="Nvidia VDPAU library (32-bit)"
arch=('x86_64')
url=http://cgit.freedesktop.org/~aplattner/libvdpau
depends=('gcc-libs-multilib>=4.4.2' 'lib32-libxext')
conflicts=('lib32-nvidia-utils<190.42-2')
options=('!libtool' 'force')
license=('custom')
source=(http://people.freedesktop.org/~aplattner/vdpau/${_pkgbasename}-${pkgver}.tar.gz)
makedepends=('gcc-multilib' 'lib32-libx11')
sha256sums=('061b6f86c64912f79851bfefcac4f77b401e9a939dbbb7b1ec795bc7d5aaf59b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -rf "${pkgdir}/usr/include"
}
