# Maintainer: Sam S. <smls75@gmail.com>

pkgname=aquaria-data-hib
pkgver=1.1.3+h20100504
pkgrel=1
pkgdesc="A 2D fantasy action-adventure game (Humble Bundle version; game data only)"
url='http://www.bit-blot.com/'
arch=('any')
license=('custom: "commercial"')
provides=('aquaria-data=1.1.3')
PKGEXT='.pkg.tar.gz'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

source=('hib://aquaria-lnx-humble-bundle.mojo.run')
md5sums=('8b24ddeeb553e028bbd501102f891cc2')

package() {
  cd $pkgdir

  # Install game files
  mkdir -p usr/share/aquaria
  cp -rT "$srcdir/data" usr/share/aquaria

  # Remove binaries & libraries, and redundant files
  rm usr/share/aquaria/{aquaria,xdg-open,lib*}
  rm usr/share/aquaria/README-linux.txt

  # Install documentation
  mkdir -p usr/share/doc/aquaria
  mv -T "$srcdir/data/docs" usr/share/doc/aquaria

  # Install icon
  install -Dm644 "$srcdir/data/aquaria.png" usr/share/pixmaps/aquaria.png
}
