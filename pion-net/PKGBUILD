# Maintainer: David Watzke <david@watzke.cz>

pkgname=pion-net
pkgver=3.0.27
pkgrel=1
pkgdesc="a C++ development library for implementing lightweight HTTP interfaces"
url="http://www.pion.org/projects/pion-network-library"
arch=('i686' 'x86_64')
license=('custom:Boost')
depends=('boost-libs' 'boost')
source=("http://www.pion.org/files/$pkgname-$pkgver.tar.bz2")
md5sums=('454d0601e56e4f6169d94d79e1eab98c')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	# override configure bug (forces debug even when nobody asked for it)
	sed -i "s/ -ggdb//g;s/ -DNDEBUG//g" configure

	./configure --prefix=/usr --disable-doxygen-doc --with-ostream-logging

	find -name Makefile -print0 | xargs -0 \
	sed -i -r '/^C(XX)?FLAGS/ s/$/ -DBOOST_FILESYSTEM_VERSION=2/' Makefile

	# override Makefile bug
	sed -i "/docs:/ s/doxygen-doc//" Makefile

	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	# install license
	install -D -m0755 COPYING "$pkgdir/usr/share/licenses/$pkgname/Boost"
}
