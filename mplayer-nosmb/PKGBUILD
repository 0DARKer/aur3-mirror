# Maintainer: Hilton Medeiros <medeiros.hilton at gmail dot com>
# Contributor : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=mplayer-nosmb
_pkgname=mplayer
pkgver=35014
pkgrel=1
pkgdesc="A movie player for linux (without samba support)."
arch=('i686' 'x86_64')
depends=('libxxf86dga' 'libxxf86vm' 'libmad' 'cdparanoia' 'libxinerama' 'sdl' 'lame' 'libtheora' 'xvidcore' 'libmng' 'libxss'
         'libgl' 'aalib' 'jack' 'libcaca' 'x264' 'faac' 'lirc-utils' 'ttf-dejavu' 'libxvmc' 'libvdpau>=0.4'
         'opencore-amr' 'libdca' 'a52dec' 'nas' 'libass' 'openjpeg' 'schroedinger' 'libvpx')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'mesa' 'live-media>=2010.01.13' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
conflicts=('mplayer')
provides=("mplayer=$pkgver")
source=(ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.xz)
md5sums=('d0785560e5e8eb02e089728ef4412eae')
         

build() {
  # Custom CFLAGS break the mplayer build
  unset CFLAGS LDFLAGS

  cd ${srcdir}/${_pkgname}

  ./configure --prefix=/usr \
      --enable-runtime-cpudetection \
      --disable-gui \
      --disable-arts \
      --disable-liblzo \
      --disable-speex \
      --disable-openal \
      --disable-fribidi \
      --disable-libdv \
      --disable-musepack \
      --disable-esd \
      --disable-mga \
      --disable-smb \
      --enable-xvmc \
      --language=all \
      --confdir=/etc/mplayer

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make
}

package() {
  unset CFLAGS LDFLAGS
  cd ${srcdir}/${_pkgname}

  make -j1 DESTDIR=${pkgdir} install

  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
  install -dm755 ${pkgdir}/usr/share/mplayer/

  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
  rm -rf ${pkgdir}/usr/share/mplayer/font
}
