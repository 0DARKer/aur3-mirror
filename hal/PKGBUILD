# Maintainer: Rohit Manokaran <rohitmanokaran@yahoo.com>
# Contributor: Pawel "l0ner" Soltys <pwslts@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
# Contributor: Calvin Morisson <mutantturkey@gmail.com>

pkgname=hal
pkgver=0.5.14
pkgrel=19
pkgdesc="Hardware Abstraction Layer"
arch=(i686 x86_64)
license=('GPL' 'custom')
url="http://www.freedesktop.org/wiki/Software/hal"
depends=('dbus-glib>=0.82' 'libusb-compat' 'systemd>=189' 'filesystem>=2012' 'hal-info>=0.20090716' 'dmidecode' 'pciutils>=3.0.2' 'usbutils>=006' 'util-linux>=2.24' 'v4l-utils')
optdepends=('acpid>=2.0' 'pm-utils>=1.2.5')
makedepends=('gperf')
options=('!libtool' '!makeflags')
install=hal.install
source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
         hald
         hal.patch
         udev-update.patch
         hal-glib-2.3-compile-fix.patch
         hal-libudev-events.patch
         badvok-compile-fix.patch)
#        patches/hal-0.5.9-hide-diagnostic.patch
#        patches/hal-remove-dell-killswitch.patch
#        patches/hal-KVM-evdev.patch
#        patches/hal-HDAPS-blacklist.patch
#        patches/hal-xen-unignore-axes.patch
#        patches/hal-use-at-console.patch
#        patches/fix-libusb-detection.patch
#        patches/dbus-error-init.patch
#        patches/path-max.patch
#        patches/handle-input-touchpad.patch
#        patches/macbook-fix-ioperm.patch
#        patches/hal-ignore-internal-dm-devices.patch

md5sums=('e9163df591a6f38f59fdbfe33e73bf20'
         '4cdfc673ad65ddb51919f5a757f62145'
         '185dd5d6ffc703ec8395b4eb3a1ae498'
         '68209ba62b2a19537bfa459fdef70ed3'
         'c126a66766118f83a171511832d1b619'
         'd115a2f1793121b08dd58527c1530d0b'
         '7acfdd590c98272930ddfd37f1c7f5a3'
        )

build() {

  cd $srcdir
  patch -Np1 -d ${srcdir} < hal.patch

  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/hal-libudev-events.patch"
  patch -Np1 -i "${srcdir}/hal-glib-2.3-compile-fix.patch"
  patch -Np1 -i "${srcdir}/udev-update.patch"
  patch -Np1 -i "${srcdir}/badvok-compile-fix.patch"

  # fix trialing space
  sed -i 's/failed; [\] /failed; \\/' policy/Makefile.am

  # fix subdir-objects mess in automake 1.14
  sed -i 's/AM_INIT_AUTOMAKE[(]\[gnu 1.9\][)]/AM_INIT_AUTOMAKE([subdir-objects])/' configure.in

  libtoolize --force
  aclocal
  autoupdate   # required to fix obsolete macros
  autoconf
  automake --add-missing --warnings=all

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/hal --with-udev-prefix=/etc \
      --enable-static=no --disable-acpi-ibm \
      --disable-docbook-docs --disable-console-kit \
      --disable-policy-kit --disable-acl-management \
      --enable-umount-helper --disable-smbios \
      --with-hal-user=hal --with-hal-group=hal \
      --with-pid-file=/var/run/hald.pid \
      --sbindir=/usr/bin --disable-gtk-doc
  make

}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
#   install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 -d "${pkgdir}/media"
#   install -m755 "${srcdir}/hald" "${pkgdir}/etc/rc.d/hal"

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # These empty directories are required by hal to successfuly create its fdi-cache
  install -m755 -d "${pkgdir}/etc/hal/fdi/"{policy,preprobe,information}

  # udev-sockets support has been removed from udev. hal-libudev-events.patch fixes hal to get events from libudev
  rm -r "${pkgdir}/etc/udev"

  # move umount.hal from non-standard /sbin to /usr/bin
  if test -e "${pkgdir}/sbin/umount.hal"; then
    install -m755 -d "${pkgdir}/usr/bin"
    mv "${pkgdir}/sbin/umount.hal" "${pkgdir}/usr/bin/umount.hal"
    rmdir "${pkgdir}/sbin"
  fi

  # create systemd service files
  install -m755 -d "${pkgdir}/usr/lib/systemd/system"
  cat >${pkgdir}/usr/lib/systemd/system/hal.service <<EOF
[Unit]
Description=Hardware Abstraction Layer
Requires=dbus.service
Wants=acpid.service
After=dbus.service syslog.target acpid.service

[Service]
Type=dbus
BusName=org.freedesktop.Hal
ExecStart=/usr/sbin/hald --daemon=no --use-syslog

[Install]
WantedBy=multi-user.target
Alias=dbus-org.freedesktop.Hal.service
EOF

  # These dirs dont appear to be used. If they are needed we need to add systemd tmpfile to create them
  rm -r "${pkgdir}/var/run/hald"

  # Change ownership from hal:hal to root [namcap error]
  chown root:root "${pkgdir}/var/cache/hald"
}


