# Maintainer: Antonio Rojas 

pkgname=kmix-git
pkgver=r1947.db9c8fa
pkgrel=1
pkgdesc='KDE volume control program'
arch=('i686' 'x86_64')
url='http://kde.org/applications/multimedia/kmix/'
license=('GPL')
depends=('kdelibs4support' 'libcanberra')
makedepends=('extra-cmake-modules' 'git')
conflicts=('kdemultimedia-kmix')
provides=('kdemultimedia-kmix')
source=("git://anongit.kde.org/kmix.git")
sha256sums=('SKIP')

pkgver() {
  cd kmix
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../kmix \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKMIX_KF5_BUILD=ON \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
