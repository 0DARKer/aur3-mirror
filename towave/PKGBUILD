# Maintainer: Devin J. Pohly <djpohly+arch@gmail.com>
pkgname=towave
pkgver=3
pkgrel=1
pkgdesc="Generates wave files from chiptunes"
arch=('i686' 'x86_64')
url="http://icesoldier.me/towave.html"
license=('GPL3')
depends=('libgme')
source=("http://icesoldier.me/filedump/$pkgname-rel$pkgver-src.tar.gz")
md5sums=('534dac1520bb66b6df549cde0de6e0d6')

prepare() {
	cd "$srcdir"
	sed -i -e 's,"gme/gme\.h",<gme/gme.h>,;s,\bgme_t\b,Music_Emu,g' towave.cpp
	echo 'towave: wave_writer.o -lgme' > Makefile
}

build() {
	cd "$srcdir"
	make
}

package() {
	cd "$srcdir"
	install -D towave "$pkgdir/usr/bin/towave"
}
