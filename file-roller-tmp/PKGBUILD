# $Id$
# Maintainer: Kresimir Pripuzic <kpripuzic@gmail.com>

pkgname=file-roller-tmp
pkgver=3.16.2
pkgrel=2
pkgdesc="This version of file-roller puts temporary files in /tmp instead of ~/.cache and therefore reduces the amount of writes to SSDs."
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'hicolor-icon-theme' 'dconf' 'libarchive' 'file' 'json-glib' 'libnotify' 'p7zip'
         'zip' 'unzip')
makedepends=('intltool' 'pkg-config' 'libnautilus-extension' 'itstool' 'docbook-xsl')
optdepends=('unrar: better RAR archive support'
            'unace: ACE archive support'
            'lrzip: lrzip archive support'
            'cdrkit: ISO image support')
groups=('gnome-extra')
provides=('file-roller')
conflicts=('file-roller')
options=('!emptydirs')
install=file-roller.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname:0:11}/${pkgver:0:4}/${pkgname:0:11}-$pkgver.tar.xz
        rar1.patch rar2.patch tmp-as-temp.patch)
sha256sums=('60897ff24d4822395a227b62811c3b8768b701844961e93303d6d6d9463d7e3f'
            '020a2debda885afc5748ebc3620ec9c9320f21d9caad631c8d79216b38f96d56'
            'd92da92d64c8c35c19c17b19a7bdfe6d0a00fae28207d1681dec707e8e8dc9f9'
            '9556742b38c06f131f7d56eb72522e38402a85ec99904be73ccae228f5b9572f')

prepare() {
  cd ${pkgname:0:11}-$pkgver
  patch -Np1 -i ../rar1.patch
  patch -Np1 -i ../rar2.patch
  patch -Np1 -i ../tmp-as-temp.patch
}

build() {
  cd ${pkgname:0:11}-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/file-roller \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd ${pkgname:0:11}-$pkgver
  make DESTDIR="$pkgdir" install
}
