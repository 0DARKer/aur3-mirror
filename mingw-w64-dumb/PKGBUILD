pkgname=mingw-w64-dumb
pkgver=0.9.3
pkgrel=2
pkgdesc="IT, XM, S3M and MOD player library (mingw-w64)"
arch=(any)
url="http://dumb.sourceforge.net"
license=("custom:dumb" "GPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt)
options=(!strip !buildflags)
source=("http://downloads.sourceforge.net/sourceforge/dumb/dumb-$pkgver.tar.gz"
"CVE-2006-3668.patch"
"config.txt")
md5sums=('f48da5b990aa8aa822d3b6a951baf5c2'
         'e1378e2eb30346073ee188608ac2c9e7'
         '112b5cabd3256fafe0141f29e58b33c6')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/dumb-$pkgver"
  patch -p1 < "$srcdir/CVE-2006-3668.patch"
  cp "$srcdir/config.txt" "$srcdir/dumb-$pkgver/make/"
  
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cp -r "${srcdir}/dumb-${pkgver}/"* "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    sed -i "s,PKGDIR/usr/ARCH,$pkgdir/usr/${_arch}," "make/config.txt"
    make CC=${_arch}-gcc AR=${_arch}-ar RANLIB=${_arch}-ranlib
    ${_arch}-gcc -shared obj/unix/debug/*.o -o libdumbd.dll -Xlinker --out-implib -Xlinker libdumbd.dll.a
    ${_arch}-gcc -shared obj/unix/release/*.o -o libdumb.dll -Xlinker --out-implib -Xlinker libdumb.dll.a
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    mkdir -p "$pkgdir/usr/${_arch}/"{bin,include,lib}
    make DESTDIR=$pkgdir install
    rm "$pkgdir/usr/${_arch}/bin/dumb"{2wav,out}
    install -m644 "libdumb.dll" "$pkgdir/usr/${_arch}/bin/"
    install -m644 "libdumbd.dll" "$pkgdir/usr/${_arch}/bin/"
    install -m644 "libdumb.dll.a" "$pkgdir/usr/${_arch}/lib/"
    install -m644 "libdumbd.dll.a" "$pkgdir/usr/${_arch}/lib/"
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name 'libdumb.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name 'libdumb.dll.a' -o -name 'libdumb.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}