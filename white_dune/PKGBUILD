# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=white_dune
pkgver=0.31beta170
pkgrel=1
pkgdesc="Low level VRML97 tool with simple NURBS/SuperFormula 3D Modeller"
arch=('i686' 'x86_64')
url="http://vrml.cip.ica.uni-stuttgart.de/dune/"
license=('GPL')
depends=('libjpeg' 'libpng' 'mesa' 'lesstif' 'libgl' 'libxmu')
source=(http://129.69.35.12/dune/$pkgname-$pkgver.tar.gz)
md5sums=('a5e6891423d162f8163c8a707ac5823d')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  install -d -m755 "$pkgdir"/usr/bin
  install -m755 bin/* "$pkgdir"/usr/bin/
  install -d -m755 "$pkgdir"/usr/share/{applications,pixmaps,man/man1}
  install -m644 man/* "$pkgdir"/usr/share/man/man1
  install -m644 "$srcdir"/$pkgname-$pkgver/desktop/kde/*.desktop \
          "$pkgdir"/usr/share/applications/
  install -m644 "$srcdir"/$pkgname-$pkgver/desktop/kde/*.png \
            "$pkgdir"/usr/share/pixmaps/
}
