# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Magnus Therning <magnus@therning.org>

pkgname=sagasu
pkgver=2.0.11
pkgrel=1
pkgdesc="GNOME tool to find strings in multiple files"
arch=('i686' 'x86_64')
url="http://perso.b2b2c.ca/sarrazip/dev/sagasu.html"
license=('GPL')
depends=('libgnomeui')
source=(http://sarrazip.com/dev/${pkgname}-${pkgver}.tar.gz)
sha1sums=('ec27538da41fad030b87d299c437ffdca8c7963f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's|glib/gmem.h|glib.h|' -e 's|glib/gconvert.h|glib.h|' src/util.h
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
