pkgname=mingw-w64-glproto
pkgver=1.4.10
pkgrel=2
pkgdesc="X11 OpenGL extension wire protocol (mingw-w64)"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
provides=('glproto' 'glproto=1.4.10')
depends=('mingw-w64-crt')
source=('http://xorg.freedesktop.org/archive/individual/proto/glproto-1.4.10.tar.gz')
md5sums=('ac89626ed6bced045d0515217bfffd4f')
_architectures='i686-w64-mingw32 x86_64-w64-mingw32'

build() {
  cd "${srcdir}"/glproto-${pkgver}
  for _arch in $_architectures; do
    mkdir -p build-${_arch}; pushd build-${_arch}
    ../configure --host="${_arch}" --prefix=/usr/${_arch}
    make
    popd
  done
}

package() {
  for _arch in $_architectures; do
    cd "${srcdir}"/glproto-${pkgver}/build-${_arch}
    make DESTDIR="${pkgdir}" install
    install -m755 -d "${pkgdir}"/usr/${_arch}/share/licenses/${pkgname}
    install -m644 "${srcdir}"/glproto-${pkgver}/COPYING \
                  "${pkgdir}"/usr/${_arch}/share/licenses/${pkgname}/
  done
}
