pkgname=simplescreenrecorder
_gitname=ssr
pkgver=0.0.7
pkgrel=2
pkgdesc="A feature-rich screen recorder that supports X11 and OpenGL."
arch=("i686" "x86_64")
url="http://www.maartenbaert.be/simplescreenrecorder/"
license=("GPL3")
source=('git+https://github.com/MaartenBaert/ssr.git#tag=0.0.7')
md5sums=('SKIP')
depends=("qt4" "ffmpeg" "alsa-lib" "libgl" "glu" "libx11" "libxext" "libxfixes")
if test "$CARCH" == x86_64; then
	optdepends=("lib32-simplescreenrecorder: OpenGL recording of 32-bit applications")
fi
makedepends=("git")
options=("!libtool")
install=simplescreenrecorder.install

build() {
	cd "${_gitname}"
	
	export CPPFLAGS="$CPPFLAGS -DNDEBUG -DQT_NO_DEBUG -DTEST_USE_FFMPEG_VERSIONS=1"
	
	./configure --prefix=/usr
	make
}

package() {
	cd "${_gitname}"
	make DESTDIR="${pkgdir}" install
}
