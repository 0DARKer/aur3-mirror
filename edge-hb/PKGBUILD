# Maintainer: Carl Reinke <mindless2112 gmail com>

pkgname=edge-hb
pkgver=1327984913
pkgrel=1
pkgdesc="[Humble Bundle] a casual puzzle-platformer that challenges you to roll your cube through over 100 levels"
url="http://www.mobigame.net/"
license=('custom: "commercial"')
source=("EDGE-HIB-5.sh"::"file://EDGE-HIB-5.sh"
        'edge.desktop')
md5sums=('11d8415cc69952986ada14dc75453343'
         '1d36149439a55daa4eae8fecd26cbe8b')
arch=('i686' 'x86_64')
depends=()
options=('!strip')
#PKGEXT='.pkg.tar'

package()
{
    chmod a+x $srcdir/EDGE-HIB-5.sh
    $srcdir/EDGE-HIB-5.sh --tar xf
    
    lzma=$srcdir/bin/linux/${CARCH/i686/x86}/libc.so.6/lzma-decode
    
    $lzma $srcdir/subarch - | tar x
    
    mkdir $pkgdir/opt
    
    $lzma instarchive_all - | tar x -C $pkgdir/opt/
    $lzma instarchive_all_${CARCH/i686/x86} - | tar x -C $pkgdir/opt/
    
    chgrp -R games $pkgdir/opt/EDGE/
    chmod -R g-w $pkgdir/opt/EDGE/
    chmod -R a+x $pkgdir/opt/EDGE/EDGE.bin.*
    
    echo -e '#!/bin/sh\n/opt/EDGE/EDGE.bin.'"${CARCH/i686/x86}"' $*' > $srcdir/edge.launcher
    install -Dm755 $srcdir/edge.launcher     $pkgdir/usr/bin/edge
    
    install -Dm644 $pkgdir/opt/EDGE/EDGE.png $pkgdir/usr/share/pixmaps/edge.png
    install -Dm644 $srcdir/edge.desktop      $pkgdir/usr/share/applications/edge.desktop
}
