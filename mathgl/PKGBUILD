# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nick Parastatidis <nparasta@auth.gr>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: oslik <oslik@mail.ru>

pkgname=mathgl
pkgver=1.11
pkgrel=2
pkgdesc='A library for making high-quality scientific graphics, fast data plotting and handling of large data arrays'
arch=('i686' 'x86_64')
url='http://mathgl.sourceforge.net'
license=('GPL')
depends=('qt' 'fltk' 'gsl' 'libgl' 'freeglut' 'libjpeg' 'libtiff' 'python-numpy' 'swig')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}%20${pkgver}/${pkgname}-${pkgver}.tgz"
        "http://mathgl.sourceforge.net/${pkgname}_en.pdf")
md5sums=('1a21d7d819e1739a894121b58236efb8'
         '0767e2d1dc6356dbf4fadb94b4f854f4')
# bsdtar is having problems with this file
noextract=("${pkgname}-${pkgver}.tgz")

build() {
  tar xf ${pkgname}-${pkgver}.tgz

  cd ${pkgname}-${pkgver}

  sed -i 's/python-config/python2-config/' configure

  env CPPFLAGS="-I/usr/lib/python2.7/site-packages/numpy/core/include" \
  ./configure \
    --enable-glut \
    --enable-jpeg \
    --enable-qt \
    --enable-python \
    --prefix=/usr \
    PYTHON=/usr/bin/python2

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/${pkgname}_en.pdf \
    ${pkgdir}/usr/share/doc/${pkgname}/${pkgname}_en.pdf 
}
