# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nick Parastatidis <nparasta@auth.gr>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: oslik <oslik@mail.ru>

pkgname=mathgl
pkgver=1.11.2
pkgrel=2
pkgdesc='A library for making high-quality scientific graphics, fast data plotting and handling of large data arrays.'
arch=('i686' 'x86_64')
url='http://mathgl.sourceforge.net'
license=('GPL')
depends=('qt' 'fltk' 'wxgtk' 'giflib' 'libharu' 'gsl' 'freeglut' 'u3d' 'python2-numpy')
makedepends=('libtiff' 'swig' 'texi2html' 'u3d' 'octave')
options=('!libtool')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname%20${pkgver%%.1}/$pkgname-$pkgver.tar.gz"
        "http://mathgl.sourceforge.net/${pkgname}_en.pdf")
md5sums=('acd33e68911d9506f60d769dce23f95e'
         '0767e2d1dc6356dbf4fadb94b4f854f4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # build against numpy
  export CPPFLAGS="-I/usr/lib/python2.7/site-packages/numpy/core/include"

  # python2 fixes
  export PYTHON="python2"
  sed -i "s|\(python\).*|\12|" texinfo/filter.py
  sed -i "s|\(python\)\(-config\)|\12\2|g" configure*

  # fltk 1.3 fix
  sed -i "s|Fl/|FL/|" include/mgl/mgl_fltk.h

  ./configure --prefix=/usr \
              --enable-static=no \
              --enable-double \
              --enable-pthread \
              --enable-u3d \
              --enable-gsl \
              --enable-glut \
              --enable-jpeg \
              --enable-gif \
              --enable-pdf \
              --enable-fltk \
              --enable-wx \
              --enable-qt \
              --enable-python \
              --enable-octave \
              --enable-docs

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # additional pdf doc
  install -D -m644 "$srcdir/${pkgname}_en.pdf" \
    "$pkgdir/usr/share/doc/$pkgname/${pkgname}_en.pdf"
}
