# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nick Parastatidis <nparasta@auth.gr>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: oslik <oslik@mail.ru>

pkgname=mathgl
pkgver=2.3
pkgrel=1
pkgdesc="A library for scientific data visualization"
arch=('i686' 'x86_64')
url='http://mathgl.sourceforge.net/doc_en/Main.html'
license=('GPL3')
depends=('qt5-base' 'fltk' 'giflib' 'libharu' 'gsl' 'freeglut' 'hdf5')
makedepends=('cmake' 'swig' 'texi2html' 'octave' 'python2-numpy')
provides=('udav')
conflicts=('udav')
options=('!libtool' '!emptydirs')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname%20$pkgver/$pkgname-$pkgver.tar.gz"
        "http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname%20$pkgver/$pkgname-$pkgver.eng.pdf")
md5sums=('06ce798a1dc82bacc154ff9f63b8f359'
         'a53ab16d565825dd8c37ecb89b714bb8')


prepare() {
  cd $pkgname

  # do not build pdf files
  sed -i "/add_custom_command.*pdf/,/)/d;/pdf/d;/info\*/s/$/)/" texinfo/CMakeLists.txt
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # build against numpy
  export CPPFLAGS+=" -I/usr/lib/python2.7/site-packages/numpy/core/include"

  [[ -d bld ]] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DPYTHON_EXECUTABLE=/usr/bin/python2 \
           -Denable-all=OFF \
           -Denable-doc=ON \
           -Denable-double=ON \
           -Denable-fltk=ON \
           -Denable-gif=ON \
           -Denable-glut=ON \
           -Denable-gsl=ON \
           -Denable-hdf4=OFF \
           -Denable-hdf5=ON \
           -Denable-hdf5_18=ON \
           -Denable-jpeg=ON \
           -Denable-langall=OFF \
           -Denable-lgpl=OFF \
           -Denable-ltdl=ON \
           -Denable-octave=ON \
           -Denable-opengl=ON \
           -Denable-pdf=ON \
           -Denable-pthread=ON \
           -Denable-python=ON \
           -Denable-qt5=ON \
           -Denable-wx=OFF
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/bld"

  make DESTDIR="$pkgdir/" install

  # mv info files in default path
  install -d "$pkgdir/usr/share/info"
  mv "$pkgdir/usr/share/doc/$pkgname/${pkgname}_en.info"* \
    "$pkgdir/usr/share/info"

  # additional pdf doc
  install -D -m644 "$srcdir/$pkgname-$pkgver.eng.pdf" \
    "$pkgdir/usr/share/doc/$pkgname/$pkgname-$pkgver.eng.pdf"
}
