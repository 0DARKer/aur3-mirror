# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nick Parastatidis <nparasta@auth.gr>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: oslik <oslik@mail.ru>

pkgname=mathgl
pkgver=2.0
pkgrel=1
pkgdesc="A library for scientific data visualization"
arch=('i686' 'x86_64')
url='http://mathgl.sourceforge.net'
license=('GPL3')
depends=('qt' 'fltk' 'giflib' 'libharu' 'gsl' 'freeglut' 'hdf5')
makedepends=('cmake' 'swig' 'texi2html' 'octave' 'python2-numpy')
provides=('udav')
conflicts=('udav')
options=('!libtool' '!emptydirs')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname%20$pkgver/$pkgname-$pkgver.tar.gz"
        "http://downloads.sourceforge.net/project/$pkgname/documentation/$pkgname%20refman%20$pkgver/$pkgname-$pkgver.eng.pdf")
md5sums=('857447e97af1a909e53a7fa72f243157'
         '69af91f4872c06163cdf266a1850875b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # build against numpy
  export CPPFLAGS+=" -I/usr/lib/python2.7/site-packages/numpy/core/include"

  # do not build pdf files
  sed -i "/add_custom_command.*pdf/,/)/d;/pdf/d;/info\*/s/$/)/" texinfo/CMakeLists.txt

  # fltk 1.3 fix
  sed -i "s|Fl/|FL/|" include/mgl2/fltk.h

  [[ -d bld ]] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DPYTHON_EXECUTABLE=/usr/bin/python2 \
           -Denable-all=OFF \
           -Denable-doc=ON \
           -Denable-double=ON \
           -Denable-fltk=ON \
           -Denable-gif=ON \
           -Denable-glut=ON \
           -Denable-gsl=ON \
           -Denable-hdf4=OFF \
           -Denable-hdf5=ON \
           -Denable-hdf5_18=ON \
           -Denable-jpeg=ON \
           -Denable-langall=OFF \
           -Denable-lgpl=OFF \
           -Denable-ltdl=ON \
           -Denable-octave=ON \
           -Denable-opengl=ON \
           -Denable-pdf=ON \
           -Denable-pthread=ON \
           -Denable-python=ON \
           -Denable-qt=ON \
           -Denable-wx=OFF
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/bld"

  make DESTDIR="$pkgdir/" install

  # mv info files in default path
  install -d "$pkgdir/usr/share/info"
  mv "$pkgdir/usr/share/doc/$pkgname/${pkgname}_en.info"* \
    "$pkgdir/usr/share/info"

  # additional pdf doc
  install -D -m644 "$srcdir/$pkgname-$pkgver.eng.pdf" \
    "$pkgdir/usr/share/doc/$pkgname/$pkgname-$pkgver.eng.pdf"
}
