# $Id$
# Foked by: Cedric Bellegarde <gnumdk@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor : sebikul <sebikul@gmail.com>
# Contributor : Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary-unity
pkgrealname=geary
pkgver=0.8.0
pkgrel=2
pkgdesc='A lightweight email client for the GNOME/Unity desktop'
arch=('i686' 'x86_64')
conflicts=('geary' 'geary-devel' 'geary-git')
url='http://www.yorba.org/projects/geary/'
license=('GPL3')
depends=('gnome-keyring' 'gmime' 'libunity' 'libcanberra' 'libgee' 'libnotify' 'webkitgtk')
makedepends=('cmake' 'gnome-doc-utils' 'gobject-introspection' 'intltool' 'vala')
install="${pkgname}.install"
source=("https://download.gnome.org/sources/${pkgrealname}/${pkgver%.?}/${pkgrealname}-${pkgver}.tar.xz" 'geary_toolbar.patch')
sha256sums=('4d4c168180d1c5193ba2784902f75b90b9c13d674204da4014e70d5bee14bd48' 'b320f1736f431ef8f1ddd4a19d563227f5f18927d822cbf636bba4a11cc8b720')

build() {
  cd ${pkgrealname}-${pkgver}
  patch -p1 < ../geary_toolbar.patch
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' \
           -D{DESKTOP_UPDATE,GSETTINGS_COMPILE{,_IN_PLACE},ICON_UPDATE}='FALSE'
  make
}

package() {
  cd ${pkgrealname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
