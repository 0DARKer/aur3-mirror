# Maintainer: Rafael Ferreira <rafael.f.f1@gmail.com>

_name=gnome-2048
pkgname=$_name-git
pkgver=0.1.0.r15.0432683
pkgrel=1
pkgdesc="Obtain the 2048 tile"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/2048"
license=('GPL')
depends=('clutter-gtk' 'libgee')
makedepends=('vala' 'gnome-common' 'intltool' 'git')
options=(!libtool)
install=$_name.install
provides=($_name)
conflicts=($_name)
source=(git://git.gnome.org/$_name)
md5sums=('SKIP')

prepare() {
  cd "$srcdir/$_name"
    # solve a "Oops" in version 0.1.0.r15.0432683
  sed -i '/scoreboard.ui/d' src/gnome-2048.gresource.xml
}

pkgver() {
  cd "$srcdir/$_name"
  v=`grep AC_INIT configure.ac | cut -d[ -f3 | cut -d] -f1`
  r=`git rev-list --count HEAD`
  h=`git rev-parse --short HEAD`
  printf "$v.r$r.$h"
}

build() {
  cd "$srcdir/$_name"
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$_name"
  make DESTDIR=${pkgdir} install
}
