# Maintainer: Rafael Ferreira <rafael.f.f1@gmail.com>

_name=gnome-2048
pkgname=$_name-git
pkgver=0.1.0.r20.ed6f8b3
pkgrel=1
pkgdesc="Obtain the 2048 tile"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/2048"
license=('GPL')
depends=('clutter-gtk' 'libgee')
makedepends=('vala' 'gnome-common' 'intltool' 'git')
options=(!libtool)
install=$_name.install
provides=($_name)
conflicts=($_name)
source=(git+https://git.gnome.org/browse/$_name)
md5sums=('SKIP')

pkgver() {
  cd $_name
  v=`grep AC_INIT configure.ac | cut -d[ -f3 | cut -d] -f1`
  r=`git rev-list --count HEAD`
  h=`git rev-parse --short HEAD`
  printf "$v.r$r.$h"
}

build() {
  cd $_name
  ./autogen.sh --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd $_name
  make DESTDIR="$pkgdir" install
}
