# Maintainer: Limao Luo <luolimao@gmail.com>

_pkgname=pari
pkgname=$_pkgname-git
pkgver=20120915
pkgrel=1
pkgdesc="Pari/GP: Computer algebra system designed for fast computations in number theory (nightly snapshot)"
arch=(i686 x86_64)
url=http://pari.math.u-bordeaux.fr
license=(GPL)
depends=(libx11)
makedepends=(git texlive-core)
provides=($_pkgname=2.6.0)
conflicts=($_pkgname pari-nightly-snapshot)
replaces=(pari-nightly-snapshot)
backup=(etc/gprc)

_gitroot=$url/git/pari.git
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]] ; then
        pushd $_gitname && git pull origin master
        msg "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -rL $_gitname/ $_gitname-build/
    cd $_gitname-build/

    sed 's:\$addlib64: :' -i config/get_libpth

    msg "Building..."
    ./Configure --prefix=/usr --with-gmp --with-readline
    make all
}

check() {
    cd "$srcdir"/$_gitname-build/
    make bench
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install

    ln -s gp.1.gz "$pkgdir"/usr/share/man/man1/$_pkgname.1.gz
    install -Dm644 misc/gprc.dft "$pkgdir"/etc/gprc
}
