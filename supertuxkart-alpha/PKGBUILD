# Maintainer: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: rabyte <rabyte__gmail>
# Contributor to alpha version: Wiz <wiz@haxx.es>

pkgname=supertuxkart-alpha
pkgver=0.7alpha2
pkgrel=1
pkgdesc="A new and improved version of TuxKart, a kart racing game featuring Tux and his friends"
arch=('i686' 'x86_64')
url="http://supertuxkart.sourceforge.net/"
license=('GPL2')
depends=('sdl>=1.2' 'libvorbis' 'freealut' 'libgl' 'freeglut' 'irrlicht')
makedepends=('plib>=1.8.4')
conflicts=('supertuxkart')
source=(http://downloads.sourceforge.net/project/supertuxkart/SuperTuxKart-alpha/supertuxkart-$pkgver.tar.bz2)
md5sums=('b8b8402c4d5ff13342a4b38c3597f718')

build() {
  cd ${srcdir}/supertuxkart-${pkgver}

  ./configure --prefix=/usr --datadir=/usr/share || return 1

  # Fix compilation failure: http://bugs.gentoo.org/283490
  sed -i 's/-lGL/-lGL -lGLU/' config.status

  # those configure flags really mean nothing...
  sed -i "s#/usr/local#/usr#" src/file_manager.cpp
  sed -i "s#/games##" $(grep -Rl "/games" *)

  make || return 1
  make DESTDIR=${pkgdir} install

  # fix executable location...
  install -dm755 ${pkgdir}/usr/bin
  mv ${pkgdir}/usr/supertuxkart ${pkgdir}/usr/bin/supertuxkart
  sed -i "s#usr/supertuxkart#usr/bin/supertuxkart#" \
    ${pkgdir}/usr/share/applications/supertuxkart.desktop
}

# vim:set ts=2 sw=2 et:
