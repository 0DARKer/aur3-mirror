#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2013.3
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.3.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.3.tar.xz.sig
)
sha512sums=(
  bdede8a961d06c9a145fb97430e5b02074146c53c991dfc49bc9bb0484b4deef420d228652465df660bd3a506070fd31dbc2dc42a052bfe99f2b03bf63acd7ec
  d57b394742827d299d3c411036a2ea11cc3c8465aa2d2b5e519cd264161c46b76b992cd59430eef5a0dfe1137cdb38ad9f2a366cdd2608f55fdcddd0a7895a9d
)
md5sums=(
  5a472d4d27fec1ff1ae92262f16a9814
  9eb6d40c7549e486c2be87efabae22e2
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
