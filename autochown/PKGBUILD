#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2013.4.25
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.4.25.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.4.25.tar.xz.sig
)
sha512sums=(
  98cdb8a41e30a494b89d80f5ce09c40bb4e57b92629b61f960322e3537653711d1b44e01d178dcf21e1a3ada2f71b2453783de523c2a118080b2c12dab0cf6cf
  a49dda23808c6f38332c725862d79ed73d0d4f7627f42096bfe09486f6937584f87865b82a31a3ab98b76475aa0da17dc01b71440117061cb6143cebeebcf8f8
)
md5sums=(
  47307a94f96a2e8a68daa74d4a4d8590
  1f3a4e95b1986f51b296be3414a1e658
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
