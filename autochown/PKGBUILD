#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2013.2.13.2
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.2.13.2.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.2.13.2.tar.xz.sig
)
sha512sums=(
  c8be7c2ad569c1bce1853d04f29dfa2bbb4d5534622655e42420530b941d8429164b142d8a8d5c6f25b2a4ee378bb77df09bc8732af193092cbb348a0192597a
  d9b8c6935c342d617bf05d54cfe1cdb91e2f791aa3d87844e9d91c2159b4bd04cef23275919d433c6add1680855d3f9e37273c49809193f87102a5dccb09541b
)
md5sums=(
  3c411d1911cbde54ccb7028fb304acbc
  ee7ca2622ee5d2d17e0a6e27cc81a2cb
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
