#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2012.12.24
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2012.12.24.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2012.12.24.tar.xz.sig
)
sha512sums=(
  bbfce08b110b91871020ebe9b79dacbe0bf0873a4cf24355fba12790d383b6eda081e9075aea2dd482c384878e1ea44ec587a1091626ca8a413c8a69a6385e4d
  7345dfbddf53b823caf517ccf7a77cc2f71439a8b29cf18a292ef54485ed782753fac8be836cb651021a410ec1c4ff32efd7f14df0224299581544a17bb7b08b
)
md5sums=(
  0795df5d5e59ae25cbbc91d81d989501
  79fa328e99a4cabf0b4ff9b31c1e6745
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
