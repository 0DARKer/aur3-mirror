#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2013
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.tar.xz.sig
)
sha512sums=(
  d25f3105838002a012fc988216796122b8645f71667b7e76337f6c747b8e5affbaad9bc0a8be9c45778a16d90dd191c7d10c5a934de34079f63eefa6f10e9252
  af5fe4c3f719e97fc5b98ca1ff959811a4de384c17317723a316e86d77e039d61107fad7e428bbde6247b2ee1dbc8eb61c27d77394ce3011b179d933094a0550
)
md5sums=(
  2cd57caf9088238ab5d135dfcf34702b
  ba6c823b9789dc8e52ccd38b15850838
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
