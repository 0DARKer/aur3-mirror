#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2014
pkgrel=2
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
backup=(etc/autochownd.conf)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2014.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2014.tar.xz.sig
)
sha512sums=(
  d21d66aa083fe73aa7b85b19d0f18142a27a115fa991346004f5df478dcc8b90f83222a9681599a11e6762ac0665fd5c178754d645d2f5e5b0181fd4d7046667
  7c180466b7ad7ceee2968ed24bd8caa2c32386dcbb4a81c59680926aa8acfd262223b0db78418a07c6005be66a864948fa4b8075eea59770b404846233ab3e30
)
md5sums=(
  37c57475aa4e00b3be74216b566509a2
  a048c49ac328603f8338fd55ada3a70a
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
