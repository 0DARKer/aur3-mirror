#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2013.2
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.2.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.2.tar.xz.sig
)
sha512sums=(
  b026f210ffc40ab74c27292e77d5088ed96d912017c51cf45c5b23fec057d55f13bec876c80d9069efd79e081a964d17dbaba784b07b2dbdf4dbaf63d522e9f2
  8fedec0a6f5faba46fde7f4b6a2734d63e068238874d2a85b418692665d20217733309f68f1aa50dd3ebf5eb00d545fffe80e182cdb9520d23239936b17abfcd
)
md5sums=(
  9bb1a240038149f94abfa82ad2e1fd89
  5b890b583cc8fd81e4ddca0bd7f096b3
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
