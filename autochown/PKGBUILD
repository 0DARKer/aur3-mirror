#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2012.11
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.11.tar.xz
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.11.tar.xz.sig
)
sha512sums=(
  dfe39acb98877ca26adbc98aca14d45bd145eaa4ce09ac90c4ebba6582ac8f74dd3ce1430b00ee6522430bc7ca17df06b5d689ec0a51d04c222b9b66574c7707
  c8f899ecfda39659704084b5a1e5f04ce515562845567b3eac9f3355e7108e9b06f49d2faf1515dbffb06836e2d05e635b3d32c9d4788ce601e5ec98ca488590
)
md5sums=(
  4ed7fa57ddb64f49394bce85d080f0ca
  19a9242bc767e4467c753c6a06343af0
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
