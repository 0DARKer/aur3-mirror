#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2012.9.20
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.9.20.tar.xz
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.9.20.tar.xz.sig
)
sha512sums=(
  60b7cbeedb2d03c446419873cbda08e3ec3bbc23f1ea9766fec0e1f611055dfdaca9fe4d2a1213ef4a4457c0997f0a20a2b8f03c70c7b815c0248c3036d99412
  745751571802e1c3c58682367444e8658645f30349fd6094cdac4749ee034be03e7658da19e67712c7bff40d93b8597f987058b04c5975c938b391f818014c4c
)
md5sums=(
  5e590ddf4d29f3649d55492def80233c
  8e9627340ddb01d42af3c26f13d32e56
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -D -m755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d";
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
