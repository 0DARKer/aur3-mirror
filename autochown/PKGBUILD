#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2012.12.24.2
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2012.12.24.2.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2012.12.24.2.tar.xz.sig
)
sha512sums=(
  c504fd9c231d4f897277a6e1458c1aa13b210a3df16a6e0f932ec0594e7a38d508f4c61eccd9243e1c24f8afa1817a36ff5bbe9b9be10266ffc179ef28458ae1
  aa325c1402727d6ab4cd89e256cc62d9c5756ab6af3ea149e6be2d358b9126d5944c9ab2c135508a8b130f01a023d1c896f34d9fc04d0133c37b7b188ad1bd1d
)
md5sums=(
  235a6e311059d8fe481eecc7dcea52d6
  876db648d1915bcae8cf3782d6eb421a
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
