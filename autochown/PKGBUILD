#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2012.9.17
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.9.17.tar.xz
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.9.17.tar.xz.sig
)
sha512sums=(
  fd5bdf0febc2c624f52b6c9920407ce4f13e9254473c7bc2c94a788d2777445cc1751f2a659ebe7a0902362d91525c309737a3c6cbfcccf3435f7a64ccb7711e
  d7ae9edbf221b22485eb521967768c18887dc6f734180c0d7eaab9e1675f6a765e1e4b5d6af85d9f5fa2f37c8bfd2c968258ac4af909498032f99eeaf67c4d18
)
md5sums=(
  b341a5e26ff0b909e6e4aadeb2b97479
  db598ccc8ec2dc7e01a80938e80c44e1
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -D -m755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d";
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
