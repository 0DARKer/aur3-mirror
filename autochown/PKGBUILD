#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2012.10.8.1
pkgrel=2
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.10.8.1.tar.xz
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.10.8.1.tar.xz.sig
)
sha512sums=(
  ee2cdfc069574910087ed49269177c8d3aeb14577d3f23b5b9621c675d2dc4b3b85c526bf796066c8471dde7fffa2b7b3b07a07b21d30741b5a368a4360ee3af
  14ea24c43a82d3726eab675061ed91522620c077afa2a66035c2bdd2f2bbe7c0a2c551d2867023a307a74ff130a6e924051f1ded991c78db15cc8e1592220a9a
)
md5sums=(
  3faa1efb32ec08a4518f1b87016aa900
  59dadfc8db0d6ada4e0cf3bb200e009e
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -D -m755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d";
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
