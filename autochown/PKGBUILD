#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2013.2.13.1
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.2.13.1.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.2.13.1.tar.xz.sig
)
sha512sums=(
  05e0171c65a59e519b166b51f4919fca22242689461433df473149f5ace56cb3aba151c48e1c1fc082ce68bd77d40618345874846656ea51ece4ef053ae80ca8
  7c53cb56fb92462aae71c3b4d440c7db7f24ee06c1c6fd06c271c04b38f34b487a2757117ac6902cefa1086869604e7911d8c037c3249f5ad230b5f446ee5a05
)
md5sums=(
  30e3d60c4ca7b4840b90a3fa005e06b8
  7bdeddebd790c1f1c7896fc0e801e473
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
