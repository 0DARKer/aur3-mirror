#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2013.11
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.11.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.11.tar.xz.sig
)
sha512sums=(
  eb4f25c93334c154059d01d77dcf0eecfea232eb35565eefba16fafe5c7c118a06fcc4701c122295a0edfd0cf19408120d2cef506cd5fced27d158cbc900182e
  6c1b29ef411d66fbbaa10ac8d857eff377a7a6e5d9dc9b1201bd247af19a1a65876aa6276988a2ecf37031b1b5147f6a3430cb3d605e34b1a54be1db09acacef
)
md5sums=(
  3a7a82fe9c477548e5c123c37318663e
  f5b8a292100695e52a7f9232cacdf4c7
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
