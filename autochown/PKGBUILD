#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2013.7
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.7.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.7.tar.xz.sig
)
sha512sums=(
  04bcccf9d0069063be8a3b4c49cf0a65b78f2a1f01a7095d6efa12c60662eeeeb7f4e22e916df8334af1ddc9a730c1f202925cd9c7e1400d9a8145d612466b26
  eb4e8dc7a6251cd606c4e408d9ca1edda8f82ca6594a94ba1ff01c3cb82a7504c8d1d78ecb12344d0bfad986f3b14f1f9d293b1c932afc7fd6e9081011011dfd
)
md5sums=(
  5cc4e12a160222d099db325671bc20e2
  807072c73161aa4f7ea7d1aca32c04a4
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
