#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2013.2.13
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.2.13.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2013.2.13.tar.xz.sig
)
sha512sums=(
  14bf6d650d86d17bff8a060954f6bd8ec1fbc1166169b16ae61c5b918cd46de360277c7c924193c50f3bb4e848dd2ca25fea20371ebc6e1a2496b748745dd832
  af656ce89db0d81bb26c2d12e6db2e99cd8f1263b536ffceade5151898c920b76134aab3845559407baf7db368ad1030ee44aa14c4a2c22966863b2bd0ed3a9a
)
md5sums=(
  9b8c02461e19fdaa66bb6cc841f85490
  8c9561eeef5f745fa1dec8b1322e37d3
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
