#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2014.1
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
backup=(etc/autochownd.conf)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2014.1.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2014.1.tar.xz.sig
)
sha512sums=(
  e6c1d6518ee755406419a0d0ba34e39ec6c24a08926b7791aff91a640a99fe9b85aafadcd011470bc35b6ed012414babf384c5057c98f81219ee6e21d0549930
  d52f472bdad67cc4f64a0be9492a4d2376bbf01bc104513a25dd94cf08d0b43ef1bf57db45a5e9f33be54d1a9edcb2770460ff3d8a0295d3d8cfa2db66951ab9
)
md5sums=(
  569dad722ced529c95e44e095dcd7e0c
  89d29d4424557b3523a5c4aa744a1fdd
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
