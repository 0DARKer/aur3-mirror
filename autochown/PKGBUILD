#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2012
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake)
source=(
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.tar.xz
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.tar.xz.sig
)
sha512sums=(
  63acc2ff4842061ade94c741415c83584986f8d53044086f69e5d171872b4cf7571bc0e18278aad8228942c0652bbb40d7863ac5c4aec6e83435191e05068dcd
  996907c97355820fe7f346623cdcd23925389f66e295992f8e0f37c3fc1bb4f3b9fd99e6a63a7c17ea99c13568da453d77b4f70e446905b480c98cbd71f22c7c
)
md5sums=(
  6efb1fca2fe6048697ebb3ab1f3a9d1f
  1b2d2ee5e8f610e6bb7356034f35bb95
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -D -m755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d";
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
