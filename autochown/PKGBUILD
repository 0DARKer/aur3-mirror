#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2012.11.27.1
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.11.27.1.tar.xz
  http://xyne.archlinux.ca/projects/autochown/archives/autochown-2012.11.27.1.tar.xz.sig
)
sha512sums=(
  90d8db524304805cbcfe73ec578e670bd7b2e33ea3061427badf9cc19b41e1bdaad01952df49a06ee3e9ce839de1574d4b926e16a87a5f9efdcc4208ba680e92
  a2c34519fa0ba0703b228733bddebf77da25de0c1fd3ede781b065796e6841d1b8412a4f5fb647d2a61207f71129979ee71d8f92fdb9d8b288c964c81bc0ee29
)
md5sums=(
  3e8e4db5410d54690f17bc5f0e0cac9a
  bfe55734cab0a853e19a9c827e416d58
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
