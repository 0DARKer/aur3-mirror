#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=autochown
pkgver=2012.12.24.1
pkgrel=1
pkgdesc='Monitor multiple directories using glob patterns and automatically adjust file ownership and permissions.'
arch=(i686 x86_64)
license=(GPL)
url="http://xyne.archlinux.ca/projects/autochown"
makedepends=(cmake rabbit_tree)
source=(
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2012.12.24.1.tar.xz
  http://xyne.archlinux.ca/projects/autochown/src/autochown-2012.12.24.1.tar.xz.sig
)
sha512sums=(
  337c1d3a1a703788b86b8785e10ad0aaaa23628678d67d110b711a9315b78f4cd3fb2fab123650ca2a76dde7027903d9852c3fa4cdc6bdeecab67d806e52a6bb
  abe2fe30fb8339185b0ab69cffbd58b566bd596c9d8a751953fe085f3e15884315fdc3cdba7cfec62b3c2a83e6798f48c84579ee392b41fae4f5d72dab050e90
)
md5sums=(
  f23dd418a28c1a4b9feefa73ad629480
  a56c8461f89fa18914517aae47e0423c
)

build ()
{
  mkdir -p "$srcdir/build"
  rm -fr "$srcdir/build/"*
  cd -- "$srcdir/build"
  cmake ../"$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package ()
{
  cd -- "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}d" "$pkgdir/etc/rc.d/${pkgname}d"
  install -Dm644 "$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "${pkgname}d.conf" "${pkgdir}/etc/${pkgname}d.conf"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"

  cd -- "$srcdir/build"
  make install DESTDIR="$pkgdir"
}


# vim: set ts=2 sw=2 et:
