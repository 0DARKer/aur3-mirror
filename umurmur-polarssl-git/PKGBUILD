# Maintainer: kleim <kleim8 at yahoo dot fr>

pkgname=umurmur-polarssl-git
_gitname=umurmur
pkgver=20130205
pkgrel=1
pkgdesc="A minimalistic Mumble server (PolarSSL version)"
arch=('i686' 'x86_64')
url="https://github.com/fatbob313/umurmur"
license=('GPL')
depends=('libconfig' 'protobuf-c' 'polarssl')
makedepends=('git')
conflicts=('umurmur')
provides=('umurmur')
source=(git://github.com/fatbob313/umurmur.git
        umurmur.conf.d
        umurmur.service
        umurmur.1)
md5sums=('SKIP'
         '8b793353ad5133a9030652c6f9e807c2'
         '167e00413c4a63076a601fa3aa64fef2'
         'fa4ce7a44e03641b638d5b098942e969')

pkgver() {
    cd $_gitname
    git log -1 --format="%cd" --date=short | tr -d '-'
}

build() {
    cd $_gitname
    ./autogen.sh
    ./configure --prefix=/usr --mandir=/usr/share/man --with-ssl=polarssl
    make
}

package() {
    cd $_gitname
    make PREFIX=/usr DESTDIR=${pkgdir} install
     
    install -Dm644 umurmur.conf.example ${pkgdir}/etc/umurmur/umurmur.conf
    install -Dm644 ${srcdir}/umurmur.conf.d ${pkgdir}/etc/conf.d/umurmur
    install -Dm755 ${srcdir}/umurmur.service ${pkgdir}/usr/lib/systemd/system/umurmur.service
    install -Dm644 ${srcdir}/umurmur.1 ${pkgdir}/usr/share/man/man1/umurmur.1
} 

