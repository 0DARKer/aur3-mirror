# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=squaw
pkgver=0.7
pkgrel=1
pkgdesc='Simplistic Qt-based user agent for the Web'
url='http://www.normalesup.org/~bisson/src/'
license=('ISC')
arch=('i686' 'x86_64')
depends=('qtwebkit')
source=("${url}/${pkgname}-${pkgver}.cpp")
sha1sums=('03bf30ee5a34034458a8dcbf8541a18bdcb37fe2')

build() {
	cd "${srcdir}"
	moc -o squaw.moc squaw-${pkgver}.cpp
	c++ -lQtWebKit -lQtGui -lQtNetwork -lQtCore \
		${CXXFLAGS} ${LDFLAGS} \
		-o ${pkgname} ${pkgname}-${pkgver}.cpp
}

package() {
	cd "${srcdir}"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	head -n 15 "${pkgname}-${pkgver}.cpp" > "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
