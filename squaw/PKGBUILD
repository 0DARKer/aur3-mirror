# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=squaw
pkgver=1.0
pkgrel=1
pkgdesc='Simplistic Qt-based user agent for the Web'
url='http://www.normalesup.org/~bisson/src/'
license=('ISC')
arch=('i686' 'x86_64' 'armv7h')
depends=('qtwebkit')
source=("${url}/${pkgname}-${pkgver}.cpp")
sha1sums=('4ab06e1c657e0b251fc94b43bc7927e852cf3724')

build() {
	cd "${srcdir}"
	moc -o squaw.moc squaw-${pkgver}.cpp
	c++ -lQtWebKit -lQtGui -lQtNetwork -lQtCore \
		${CXXFLAGS} ${LDFLAGS} \
		-o ${pkgname} ${pkgname}-${pkgver}.cpp
}

package() {
	cd "${srcdir}"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -Dm755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	head -n 15 "${pkgname}-${pkgver}.cpp" > "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
