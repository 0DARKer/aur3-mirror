# Maintainer: DonVla <donvla@users.sourceforge.net>
# Contributor: BlackEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Ulf Winkelvos <ulf [at] winkelvos [dot] de>
# Contributor: Ralf Barth <archlinux dot org at haggy dot org>
# Contributor: B - Thanks for your hints :)
#
# Original credits go to Edgar Hucek <gimli at dark-green dot com>
# for his xbmc-vdpau-vdr PKGBUILD at https://archvdr.svn.sourceforge.net/svnroot/archvdr/trunk/archvdr/xbmc-vdpau-vdr/PKGBUILD

pkgname=xbmc-git
_commit=cc49b8581491436578d89fa5e4a85b71728e5d63
# 10.1-Dharma release commit
#_commit=e9e909922ed87c4f0bc9
pkgver=20111127
pkgrel=1
pkgdesc="XBMC Media Center - git development version"
provides=('xbmc')
conflicts=('xbmc' 'xbmc-pulse' 'xbmc-svn')
replaces=('xbmc-svn')
arch=('i686' 'x86_64')
url="https://github.com/xbmc/xbmc"
license=('GPL' 'LGPL')
depends=('bzip2' 'faac' 'faad2' 'fribidi' 'glew' 'jasper' 'libass' 'libcdio' 'libgl' 'libmad' 'libmicrohttpd' 'libplist'
         'libmodplug' 'libmpeg2' 'libmysqlclient' 'libsamplerate' 'libxrandr' 'libxinerama' 'libxtst' 'lsb-release' 'lzo2' 
         'python2' 'sdl_image>=1.2.10' 'sdl_mixer' 'smbclient' 'unzip' 'wavpack' 'mesa-demos' 'xorg-xdpyinfo' 'yajl')
makedepends=('boost' 'cmake' 'git' 'gperf' 'nasm' 
             # 'crystalhd-snapshot'
             'zip')
optdepends=('avahi: to use zerconf features (remote, etc...)' 
            'gdb: for meaningful backtraces in case of trouble - STRONGLY RECOMMENDED' 
            'libcrystalhd: Broadcom CrystalHD kernel module'
            'libssh: support for sshfs'
            'libvdpau: accelerated video playback for nvidia cards' 
            'libva: accelerated video playback for nvidia, ati/amd and some intel cards'
            'lirc: remote controller support' 
            'pulseaudio: pulseaudio support'
            'udisks: automount external drives' 
            'upower: used to trigger suspend functionality' 
            'unrar: access compressed files without unpacking them')
options=('makeflags')
install="${pkgname}.install"
source=("FEH.sh" "xbmc-10.0-python-2.7.patch" "xbmc-10.1-gcc-4.6.patch") 
_gitroot="git://github.com/xbmc/xbmc.git"
_gitname="xbmc"
_prefix=/usr
md5sums=('5c09caddbf02b07c09a05bbaf7894ee6'
         '62d46c8b44e4e5313a10922209cb14f8'
         'd88b854d6f356a74d7658cd1f74a4695')
sha256sums=('7128cc43ab8d467b8f18b33d053826f84cbad4855cdb56fde294157299298edb'
            '8f6b2a0a962ad250fc317542d0fa180585d3c9e630ca98f99299b6f750555085'
            'cef1154bc3352b215bfece1a749d901d43ec7aa0c6d050921ae8745c553e84c9')

build() {
    cd "${srcdir}"
    msg "Connecting to GIT server...."

    if [ -d "${_gitname}" ] ; then
        cd "${_gitname}" && git checkout master && git pull origin 
        [[ "${_commit}" ]] && git checkout "${_commit}"
        msg "The local files are updated."
        msg "Running make distclean"
        make distclean || :
    else
        git clone "${_gitroot}" "${_gitname}"
        cd "${_gitname}"
        [[ "${_commit}" ]] && git checkout "${_commit}"
    fi

    # Configure XBMC
    cd "${srcdir}/${_gitname}"

    # fix lsb_release dependency
    sed -i -e 's:/usr/bin/lsb_release -d:cat /etc/arch-release:' xbmc/utils/SystemInfo.cpp
#    patch -p0 < "${srcdir}/xbmc-10.0-python-2.7.patch"
#    patch -p0 < "${srcdir}/xbmc-10.1-gcc-4.6.patch"

    # remove "--as-needed" from LDFLAGS
    unset LDFLAGS; LDFLAGS="-Wl,--hash-style=gnu"

    msg "Bootstrapping XBMC"
    ./bootstrap

    msg "Configuring XBMC" 
    export PYTHON_VERSION=2  # external python v2
    ./configure --prefix="${_prefix}" \
                --enable-external-libraries \
                --disable-external-ffmpeg \
                --disable-hal \
                --enable-debug 

    # Now build
    msg "Running make" 
    make ${MAKEFLAGS}
}

package() {

    cd "${srcdir}/${_gitname}"
    msg "Running make install" 
    make prefix="${pkgdir}${_prefix}" install

# Replace FEH.py with FEH.sh (and thus remove external python dependency)
    install -D -m 0755 "${srcdir}/FEH.sh" "${pkgdir}${_prefix}/share/xbmc/FEH.sh"
    sed -i -e 's/^python \(.*\)FEH.py \(.*\)$/\1FEH.sh \2/' "${pkgdir}${_prefix}/bin/xbmc"

# lsb_release fix
    sed -i -e 's/which lsb_release &> \/dev\/null/\[ -f \/etc\/arch-release ]/g' "${pkgdir}${_prefix}/bin/xbmc"
    sed -i -e "s/lsb_release -a 2> \/dev\/null | sed -e 's\/\^\/    \/'/cat \/etc\/arch-release/g" "${pkgdir}${_prefix}/bin/xbmc"

# .desktop files
    install -D -m 0644 "${srcdir}/${_gitname}/tools/Linux/xbmc.desktop" "${pkgdir}${_prefix}/share/applications/xbmc.desktop"
    install -D -m 0644 "${srcdir}/${_gitname}/tools/Linux/xbmc-48x48.png" "${pkgdir}${_prefix}/share/pixmaps/xbmc.png"

# Tools
    install -D -m 0755 "${srcdir}/${_gitname}/xbmc-xrandr" "${pkgdir}${_prefix}/share/xbmc/xbmc-xrandr"
    install -D -m 0755 "${srcdir}/${_gitname}/tools/TexturePacker/TexturePacker" "${pkgdir}${_prefix}/share/xbmc/"

# Licenses
    install -d -m 0755 "${pkgdir}${_prefix}/share/licenses/${pkgname}" 
    for licensef in LICENSE.GPL copying.txt; do 
        mv "${pkgdir}${_prefix}/share/doc/xbmc/${licensef}" "${pkgdir}${_prefix}/share/licenses/${pkgname}" 
    done 

# Docs 
    install -d -m 0755 "${pkgdir}${_prefix}/share/doc/${pkgname}" 
    for docsf in keymapping.txt README.linux; do 
        mv "${pkgdir}${_prefix}/share/doc/xbmc/${docsf}" "${pkgdir}${_prefix}/share/doc/${pkgname}" 
     done

# cleanup some stuff
    msg "Cleanup unneeded files"
    rm -rf "${pkgdir}/usr/share/xsessions"
    rm -f "${pkgdir}/usr/share/xbmc/FEH.py"
    rm -f "${pkgdir}/usr/share/icons/hicolor/icon-theme.cache"

# strip
    msg "Stripping binaries"
    find "${pkgdir}" -type f -exec strip {} \; >/dev/null 2>/dev/null
}
