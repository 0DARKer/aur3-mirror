# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Jochen Immendörfer <jochen dot immendoerfer at gmail dot com>

pkgname=fmit
pkgver=0.99.2
pkgrel=2
pkgdesc="Free Music Instrument Tuner"
arch=('i686' 'x86_64')
url="http://home.gna.org/fmit/index.html"
license=("GPL")
depends=('jack' 'glut' 'qt4' 'fftw' 'freeglut')
makedepends=('cmake' 'mesa')
install="$pkgname.install"
source=("http://download.gna.org/fmit/$pkgname-$pkgver-Source.tar.gz")
md5sums=('726e9a16ab92cb512cdc46edac420c26')

build() {
  cd "$srcdir/$pkgname-$pkgver-Source"
  [ -d build ] || mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver-Source/build"
  make DESTDIR="$pkgdir/" install
}
