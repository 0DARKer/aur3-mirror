# Contributor: Anton S <fluffylime at gmail dot com>
pkgname=redsocks-git
pkgver=20101013
pkgrel=1
pkgdesc='Transparent redirector of any TCP connection to proxy using your firewall'
arch=('i686' 'x86_64')
url='http://darkk.net.ru/redsocks/'
license=('GPL3')
depends=('libevent')
makedepends=('git')
conflicts=('redsocks')
provides=('redsocks')
source=()
md5sums=()

_gitroot='http://github.com/darkk/redsocks.git'
_gitname=redsocks

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."
  if [[ -d "${_gitname}" ]]; then
      (cd "${_gitname}" && git pull origin)
      msg "The local files are updated."
  else
      git clone "${_gitroot}" "${_gitname}"
  fi
  msg "GIT checkout done or server timeout"

  cd "${_gitname}"
  make
}

package() {
  install -m755 -D "${srcdir}/${_gitname}/redsocks" "${pkgdir}/usr/bin/redsocks"
  install -m755 -d "${pkgdir}/usr/share/doc/redsocks"
  install -m644 -t "${pkgdir}/usr/share/doc/redsocks" ${srcdir}/${_gitname}/{README,redsocks.conf.example}
}
