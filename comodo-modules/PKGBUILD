# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>
pkgname=comodo-modules
pkgver=3.19.2_1
_kernel=${pkgver/_/-}-ARCH
pkgrel=1
pkgdesc="Modules for Comodo Antivirus for providing realtime protection"
arch=('i686' 'x86_64')
url="http://www.bondoffamily-net.com/~kinta-chan"
license=('GPL3')
depends=('linux>=3.19' 'linux<4.0' 'comodo')
makedepends=('linux-headers>=3.19' 'linux-headers<4.0')
install=comodo-modules.install
_date=20150307
source=("driver-${_date}.tar::${url}/techknow/DownLoad/redirfs/beta/driver.tar")
md5sums=('bdf595fdb2493def6e6e128f96b83916')

prepare() {
	cd "$srcdir/driver"

  # make sure we build for the installed kernel, not the booted one
  sed -i "s:\`uname -r\`:${_kernel}:g" Makefile
}

build() {
	cd "$srcdir/driver"
	ROOT_PATH="$srcdir/driver" make all
}

package() {
	cd "$srcdir/driver"
	ROOT_PATH="$srcdir/driver" make INSTALL_MOD_PATH="$pkgdir/usr" install

  # fix .install file
  sed -i "s:KERNEL=.*:KERNEL=${_kernel}:" "${startdir}/comodo-modules.install"
}
