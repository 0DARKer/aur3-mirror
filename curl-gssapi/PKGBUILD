# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=curl-gssapi
pkgver=7.28.0
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'krb5' 'libssh2' 'openldap' 'openssl' 'zlib')
provides=("curl=${pkgver}")
conflicts=('curl')
options=('!libtool')
source=("http://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc}
        curlbuild.h)

ptrsize=$(cpp <<<'__SIZEOF_POINTER__' | sed '/^#/d')
case ${ptrsize} in
  8) _curlbuild=curlbuild-64.h ;;
  4) _curlbuild=curlbuild-32.h ;;
  *) error "unknown pointer size for architecture: %s bytes" "${ptrsize}"
    exit 1
    ;;
esac

build() {
  cd "${srcdir}/curl-${pkgver}"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-dependency-tracking \
    --enable-ldap \
    --enable-ldaps \
    --enable-ipv6 \
    --enable-manual \
    --enable-versioned-symbols \
    --enable-threaded-resolver \
    --without-libidn \
    --with-random=/dev/urandom \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
    --with-gssapi

  make
}

package() {
  cd "${srcdir}/curl-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # devel
  install -Dm644 docs/libcurl/libcurl.m4 \
    "${pkgdir}/usr/share/aclocal/libcurl.m4"
  mv "${pkgdir}/usr/include/curl/curlbuild.h" \
    "${pkgdir}/usr/include/curl/${_curlbuild}"
  install -m644 "${srcdir}/curlbuild.h" "${pkgdir}/usr/include/curl/curlbuild.h"
}

# vim:set ts=2 sw=2 et:
sha512sums=('be036cf76360f0c3618b115603e2b2db63b8fcb209bff395b363da2b3d67aff5abf1e48e2a4f49d1809e89d7b8795397efa6a4e3e73d556e60df2adb257ad533'
            'd68c3a0491d1cb32a8a5faef34007d4aef88c1e141325276ebd2d0d543a088421b4c9775928bdde16c57ecfc027356df3aa3d25424142577820503f56f1ffe94'
            '4e71366c3faaab76a6e438fcc484fb1ce16b2ae84eefa176879cf164e8fb0b04e4a355384e66a1137e9641e95041ee573553b43da98ec92820696d7256beb4f5')
