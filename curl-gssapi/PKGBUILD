# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=curl-gssapi
pkgver=7.28.1
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'krb5' 'libssh2' 'openldap' 'openssl' 'zlib')
provides=("curl=${pkgver}")
conflicts=('curl')
options=('!libtool')
source=("http://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc}
        curlbuild.h)
sha512sums=('613085ae48fbc2b81a06c160aac57238375a4c76b7699e857af892beeadc2e7913802aa14ffedc63963f0abab8c8475b2603676bc7f8595a0211f69c63d0ef48'
            '986e1681147a5c8bad8fd2fb5096b82c78744627ac4b50e644462fd9dc30bc4699233a97e47d3c8e7254d23cbca32d5ffcedace2f0487497dd553a9c93054ca7'
            '4e71366c3faaab76a6e438fcc484fb1ce16b2ae84eefa176879cf164e8fb0b04e4a355384e66a1137e9641e95041ee573553b43da98ec92820696d7256beb4f5')

ptrsize=$(cpp <<<'__SIZEOF_POINTER__' | sed '/^#/d')
case ${ptrsize} in
  8) _curlbuild=curlbuild-64.h ;;
  4) _curlbuild=curlbuild-32.h ;;
  *) error "unknown pointer size for architecture: %s bytes" "${ptrsize}"
    exit 1
    ;;
esac

build() {
  cd "${srcdir}/curl-${pkgver}"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-dependency-tracking \
    --enable-ldap \
    --enable-ldaps \
    --enable-ipv6 \
    --enable-manual \
    --enable-versioned-symbols \
    --enable-threaded-resolver \
    --without-libidn \
    --with-random=/dev/urandom \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
    --with-gssapi

  make
}

package() {
  cd "${srcdir}/curl-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # devel
  install -Dm644 docs/libcurl/libcurl.m4 \
    "${pkgdir}/usr/share/aclocal/libcurl.m4"
  mv "${pkgdir}/usr/include/curl/curlbuild.h" \
    "${pkgdir}/usr/include/curl/${_curlbuild}"
  install -m644 "${srcdir}/curlbuild.h" "${pkgdir}/usr/include/curl/curlbuild.h"
}

# vim:set ts=2 sw=2 et:
