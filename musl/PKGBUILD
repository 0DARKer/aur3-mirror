# Maintainer: Serge Ziryukin <ftrvxmtrx@gmail.com>

pkgname=musl
pkgver=0.6.0
pkgrel=1
pkgdesc="An implementation of the C/POSIX standard library intended for use on Linux-based systems"
arch=('i686' 'x86_64')
url="http://www.etalabs.net/musl/"
license=('LGPL')
makedepends=('make')
source=(http://www.etalabs.net/musl/releases/$pkgname-$pkgver.tar.gz)
md5sums=('370abc0df0d40e75b99b3100fceeeeac')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if [ "$CARCH" = "x86_64" ]; then
    # native x86_64 is not supported yet
    sed "s/#CFLAGS += -m32/CFLAGS += -m32/" -i config.mak
    sed "s/gcc /gcc -m32 /g;s/ -std/ -m32 -std/" -i tools/gen-musl-gcc.sh
  fi

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
