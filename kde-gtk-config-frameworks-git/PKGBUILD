# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kde-gtk-config-frameworks-git
pkgver=r323.e033b6b
pkgrel=1
pkgdesc='GTK2 and GTK3 Configurator for KDE'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/base/kde-gtk-config'
license=('LGPL')
depends=('kcmutils-git' 'knewstuff-git')
makedepends=('extra-cmake-modules-git' 'git' 'gtk2' 'gtk3')
conflicts=('kde-gtk-config-frameworks')
provides=('kde-gtk-config-frameworks')
source=('git://anongit.kde.org/kde-gtk-config.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kde-gtk-config
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kde-gtk-config/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
