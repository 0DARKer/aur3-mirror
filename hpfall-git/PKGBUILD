# Maintainer: Srijan Choudhary <srijan4@gmail.com>
pkgname=hpfall-git
_gitname=hpfall
pkgver=5.be24a8b
pkgrel=1
pkgdesc="Disk protection for HP machines."
arch=('i686' 'x86_64')
url="https://github.com/srijan/hpfall"
license=('GPL')
makedepends=('git')
provides=('hpfall')
backup=('etc/conf.d/hpfall')
source=('git+https://github.com/srijan/hpfall.git'
        'hpfall.conf'
        'hpfall.service')
md5sums=('SKIP'
         '0297b31746ac94f86f5de30a5ee27239'
         '36dda3a9f214ac1cc5454db70003c428')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm0755 "${_gitname}/hpfall" "${pkgdir}/usr/bin/hpfall"
  install -Dm0644 "${srcdir}/hpfall.conf" "${pkgdir}/etc/conf.d/hpfall"
  install -Dm0644 "${srcdir}/hpfall.service" "${pkgdir}/usr/lib/systemd/system/hpfall.service"
}

# vim:set ts=2 sw=2 et:
