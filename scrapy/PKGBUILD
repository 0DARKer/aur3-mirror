# Maintainer: PyroDevil <p dot devil at gmail dot com>
# Contributor: Anibal Pacheco <apacheco.uy@gmail.com>
pkgname="scrapy"
pkgver=0.16
pkgrel=2
pkgdesc="A fast high-level scraping and web crawling framework."
arch=('i686' 'x86_64')
license=('BSD')
url="http://scrapy.org"
depends=('twisted>=2.5' 'libxml2>=2.6.28' 'python2-w3lib')
makedepends=('git')
optdepends=('python2-pyopenssl: only if you want to crawl secure (HTTPS) pages')
provides=('scrapy')
conflicts=('scrapy-hg' 'scrapy-git')
options=(!emptydirs)
source=("git://github.com/scrapy/scrapy.git#branch=${pkgver}")
md5sums=('SKIP')

package() {
  cd "${srcdir}/scrapy"
  python2 setup.py install --root="${pkgdir}"
  
  # copying license information
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # copying readme information
  install -D -m644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
  install -D -m644 INSTALL "${pkgdir}/usr/share/doc/${pkgname}/INSTALL"
}
