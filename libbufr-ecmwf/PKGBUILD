# Maintainer: Rich Lindsley <richli.ff at gmail dot com>
pkgname=libbufr-ecmwf
pkgver="000389"
pkgrel=4
pkgdesc="ECMWF BUFR library"
arch=('i686' 'x86_64')
url="http://www.ecmwf.int/products/data/software/bufr.html"
license=('LGPL3')
depends=('gcc-fortran')
source=(http://www.ecmwf.int/products/data/software/download/software_files/bufrdc_$pkgver.tar.gz)
md5sums=('da3d8417cb0454df5cf738d67728766e')
sha1sums=('782a961a0cfb7b3ee28989a034732b9b5898ee6e')
sha256sums=('a06945b283da9399b9b8d8465ce8dd17fe92bdb2342fd17b05dc62bd6492d48f')

build() {
  cd "$srcdir/bufrdc_$pkgver"

  # gcc/fortran: y; ifort: i
  # NB: ifort could be enabled here instead of gfortran
  _response="y\n"

  # 64-bit reals on x86_64, else 32-bit reals
  if [[ "$CARCH" == "x86_64" ]]; then
    _response="${_response}y\n"
  else
    _response="${_response}n\n"
  fi

  # The installation directory (/usr/local/lib) appears to be 
  # hard-coded during compilation
  _response="${_response}/usr/local/lib\n"

  # Patch a file since Arch hasn't undergone the /usr move yet
  sed -i -e "s|^#!/usr/bin/sh$|#!/bin/sh|" tables_tools/check_tables.sh
  # Patch a file since they forgot to link to libm?
  sed -i -e "s|bufr_decode.o -L|bufr_decode.o -lm -L|" examples/Makefile.in

  echo -e $_response | ./build_library
}

package() {
  cd "$srcdir/bufrdc_$pkgver"
  # Path installation to get right dest dir for packaging purposes
  sed -i -e "s|^INSTALL_DIR=|INSTALL_DIR=$pkgdir|" install
  ./install
  
  # Fix permisions
  chmod -R 755 $pkgdir
}

# vim:set ts=2 sw=2 et:
