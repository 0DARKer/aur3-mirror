# Maintainer: Vinycius Maia <suportevg@uol.com.br>

pkgname=nvidia-beta-zen
pkgver=304.37
_extramodules=extramodules-3.5-zen
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=1
pkgdesc="NVIDIA drivers for linux-zen."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-zen>=3.5' 'linux-zen<3.6' "nvidia-utils=${pkgver}")
conflicts=('nvidia-173xx-lqx')
license=('custom')
options=(!strip)
install=nvidia-zen.install
source=(nvidia-zen.install)
md5sums=('553e58f304e2668a3de89cbeb7b978ab')
__mainserver="ftp://download.nvidia.com"
__usserver="http://us.download.nvidia.com"
__currentserver=${__usserver}

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source+=("${__currentserver}/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums+=('ee3e97109201ab954c96860e28ada611')	
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source+=("${__currentserver}/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums+=('65fce562c0df3def06012db9ff6bafa1')
fi

build() {
	rm -rf "${_pkg}"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	make SYSSRC="/usr/lib/modules/${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-zen.conf"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
