# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=gstreamer0.10-good
pkgname=lib32-$_pkgname
pkgver=0.10.31
pkgrel=2
pkgdesc="GStreamer Multimedia Framework Good plugin libraries"
arch=(x86_64)
url=http://gstreamer.freedesktop.org/
license=('LGPL')
depends=("${_pkgbasename}" lib32-gstreamer0.10-base lib32-bzip2)
makedepends=(gcc-multilib intltool libavc1394 libiec61883 aalib libshout libdv lib32-flac
    lib32-gconf wavpack taglib lib32-libsoup-gnome lib32-v4l-utils lib32-libcaca lib32-gdk-pixbuf2 lib32-libdv
    lib32-libpulse lib32-jack)
options=(!emptydirs !libtool)
source=($url/src/gst-plugins-good/gst-plugins-good-$pkgver.tar.xz
    v4l2-buffer-input.patch)
sha256sums=('77a8436a7c0a15f876bad29616835046890df2bcaf72da02151bd91e3d292b64'
            '6845c110cb5953d884a7777ac50a31efa75127b7e504209aa895896814ede032')
sha512sums=('d153dcd5bb846450e9d76fe247f2b2e627e760feaad1bfd4c503a39af2128fad3630788463ea0d50539209236c864df0b2172ab9dac52f49cadff69ac7346809'
            '2388fda7459c4475e236f594c9abe8b2401a9c83d63f8df52a80faf0b2be801a09aab27134b27b5cc5e59053a99e5c1e4deea3dd9e881946749ba8169172b8fe')

prepare() {
    cd gst-plugins-good-$pkgver/
    sed -i -e 's:AC_PATH_XTRA::g' -e 's:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:g' configure.ac
    patch -Np1 -i ../v4l2-buffer-input.patch
}

build() {
    export CC="gcc -m32 -march=i686"
    export CXX="g++ -m32 -march=i686"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd gst-plugins-good-$pkgver/
    autoreconf -fi
    ./configure --prefix=/usr \
        --libdir=/usr/lib32/ \
        --build=i686-unknown-linux-gnu \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --enable-experimental \
        --disable-schemas-install \
        --disable-hal \
        --disable-esd \
        --with-package-name="GStreamer Good Plugins (Archlinux)" \
        --with-package-origin="http://www.archlinux.org/"
    make
    sed 's:gst sys ext:gst:' -i Makefile
}

package() {
    cd gst-plugins-good-$pkgver/
    make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
    rm -rf "$pkgdir"/etc/gconf/
}
