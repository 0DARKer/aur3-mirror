pkgname=mingw-w64-ftgl
pkgver=2.1.3rc5
pkgrel=1
pkgdesc="OpenGL library to use arbitrary fonts (mingw-w64)"
arch=(any)
url="http://ftgl.wiki.sourceforge.net"
license=("MIT")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt mingw-w64-freetype)
options=(!libtool !strip !buildflags)
source=(ftgl.tar.gz::"http://sourceforge.net/projects/ftgl/files/latest/download?source=files"
"configure.patch"
"m4.gl.m4.patch")
md5sums=('fcf4d0567b7de9875d4e99a9f7423633'
         '07ab51b38eb41b1097e7b6c9304e9023'
         '8590beb2dea7e28e7b7214ed562b2472')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  patch "${srcdir}/ftgl-2.1.3~rc5/m4/gl.m4" m4.gl.m4.patch
  patch "${srcdir}/ftgl-2.1.3~rc5/configure" configure.patch
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-2.1.3~rc5/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --without-x
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}