# Maintainer: Felipe Morales <hel.sheep@gmail.com>
pkgname=devede-git
pkgver=20120414
pkgrel=1
pkgdesc="A program to create VideoDVDs and CDs"
arch=('any')
url="http://www.rastersoft.com/programas/devede.html"
license=('GPL3')
depends=('mplayer' 'dvdauthor' 'vcdimager' 'cdrkit' 'pygtk' 'ttf-dejavu' 'mencoder' 'dbus-python')
makedepends=('git')
conflicts=('devede')
provides=('devede')

_gitroot="git://github.com/devede/Devede.git"
_gitname="devede"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Changing script headers to python2..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  for file in *.py; do
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
}

package() {
	cd "$srcdir/$_gitname-build"

	./install.sh --DESTDIR=${pkgdir} \
		--prefix=/usr \
		--unistall=no \
		--targeted=yes

	install -D -m644 devede.desktop \
		${pkgdir}/usr/share/applications/devede.desktop

} 
