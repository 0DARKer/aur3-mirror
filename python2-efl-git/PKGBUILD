# Maintainer: Doug Newgard <scimmia22 at outlook dot com>

pkgname=python2-efl-git
_pkgname=python-efl
pkgver=1.8.0.r509.1c1f102
pkgrel=1
pkgdesc="Python2 bindings for the Enlightenment Foundataion Libraries"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('LGPL3' 'GPL3')
depends=('elementary>=1.7.99' 'python2-dbus')
makedepends=('git' 'cython2')
provides=("${pkgname%-*}=$pkgver" 'python2-ecore' 'python2-edje' 'python2-elementary' 'python2-emotion' 'python2-evas' 'python2-e_dbus')
conflicts=("${pkgname%-*}" 'python2-ecore' 'python2-edje' 'python2-elementary' 'python2-emotion' 'python2-evas' 'python2-e_dbus')
source=("git://git.enlightenment.org/bindings/python/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"

  echo $(python2 setup.py -V).r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_pkgname"

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname"
  
  python2 setup.py install --root="$pkgdir"

# install text files
  install -Dm644 AUTHORS "$pkgdir/usr/share/doc/${pkgname%-*}/AUTHORS"
  install -Dm644 README "$pkgdir/usr/share/doc/${pkgname%-*}/README"
  install -Dm644 changes.html "$pkgdir/usr/share/doc/${pkgname%-*}/changes.html"
}
