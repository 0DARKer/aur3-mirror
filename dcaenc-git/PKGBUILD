# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
pkgname=dcaenc-git
pkgver=20111204
pkgrel=1
pkgdesc="A Free Software (LGPL-licensed) encoder for the DTS Coherent Acoustics audio format."
arch=('x86_64' 'i686')
url="http://aepatrakov.narod.ru/dcaenc/"
license=('LGPL')
depends=('alsa-lib')
makedepends=('git' 'autoconf')
provides=('dcaenc')
conflicts=('dcaenc')
options=('!libtool')

_gitroot="git://gitorious.org/dtsenc/dtsenc.git"
_gitname="dtsenc"

build() {
  cd "${srcdir}"

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi  

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  autoreconf -vfi
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${_gitname}-build"
  make -k check
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
