# Maintainer: Michael Lass <bevan@bi-co.net>

pkgname=mumudvb
pkgver=1.7
pkgrel=1
pkgdesc="Redistribute a stream from DVB on a network in multicast or in HTTP unicast"
arch=(i686 x86_64)
url="http://mumudvb.braice.net/mumudvbwiki/index.php/Main_Page"
license=('GPL')
depends=('glibc')
options=(docs emptydirs)
source=("http://gitweb.braice.net/gitweb?p=${pkgname};a=snapshot;h=${pkgver};sf=tgz")
md5sums=('5d705722835eeb2f6de30dd043600d1d')

build() {
  cd "${srcdir}/${pkgname}"

# Needed for development snapshots
# autoreconf -i -f

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install

  # docs
  install -d -m 0775 ${pkgdir}/usr/share/doc/mumudvb/configuration_examples
  install -d -m 0775 ${pkgdir}/usr/share/doc/mumudvb/html
  install -D -m 0644 doc/mumudvb.8 ${pkgdir}/usr/share/man/man8/mumudvb.8
  install -m 0644 doc/configuration_examples/* ${pkgdir}/usr/share/doc/mumudvb/configuration_examples
  install -m 0644 doc/html/* ${pkgdir}/usr/share/doc/mumudvb/html
  install -m 0644 doc/README* ${pkgdir}/usr/share/doc/mumudvb
  install -m 0644 doc/QUICKSTART.txt ${pkgdir}/usr/share/doc/mumudvb
  install -m 0644 doc/WEBSERVICES.txt ${pkgdir}/usr/share/doc/mumudvb

  # user has to be in video group to get access to mumudvb socket
  install -d -m 0775 -g video ${pkgdir}/var/run/mumudvb
}

# vim:set ts=2 sw=2 et:
