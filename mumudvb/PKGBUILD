# Maintainer: Michael Lass <bevan@bi-co.net>

pkgname=mumudvb
pkgver=1.7.1
pkgrel=2
pkgdesc="Redistribute a stream from DVB on a network in multicast or in HTTP unicast"
arch=(i686 x86_64)
url="http://mumudvb.braice.net/mumudvbwiki/index.php/Main_Page"
license=('GPL')
depends=('glibc')
options=(docs emptydirs)
source=("https://launchpad.net/debian/+archive/primary/+files/${pkgname}_${pkgver}.orig.tar.gz"
        "${pkgname}.install"
        "mumudvb.conf")
md5sums=('fb8a5c432cfb120122eb1a75d73adcc8'
         '02a2095488e9b3b0beb02bfa9753fd1d'
         '1a1001de517d772aec3ced80fd815454')
install="${pkgname}.install"

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Needed for development snapshots
# autoreconf -i -f

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # systemd tmpfile configuration
  install -D -m 0644 ${srcdir}/mumudvb.conf ${pkgdir}/etc/tmpfiles.d/mumudvb.conf

  # docs
  install -d -m 0775 ${pkgdir}/usr/share/doc/mumudvb/configuration_examples
  install -d -m 0775 ${pkgdir}/usr/share/doc/mumudvb/html
  install -D -m 0644 doc/mumudvb.8 ${pkgdir}/usr/share/man/man8/mumudvb.8
  install -m 0644 doc/configuration_examples/* ${pkgdir}/usr/share/doc/mumudvb/configuration_examples
  install -m 0644 doc/html/* ${pkgdir}/usr/share/doc/mumudvb/html
  install -m 0644 doc/README* ${pkgdir}/usr/share/doc/mumudvb
  install -m 0644 doc/QUICKSTART.txt ${pkgdir}/usr/share/doc/mumudvb
  install -m 0644 doc/WEBSERVICES.txt ${pkgdir}/usr/share/doc/mumudvb
}

# vim:set ts=2 sw=2 et:
