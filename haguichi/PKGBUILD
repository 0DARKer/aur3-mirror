# Maintainer: Andreas Wagner <andreas.wagner@lowfatcomputing.org>
# Contributor: scrawl <scrawl@baseoftrash.de>

pkgname=haguichi
pkgver=1.0.26
_series=1.0
pkgrel=2
pkgdesc="Hamachi GUI"
arch=('any')
url="http://www.haguichi.net"
license=('GPL2')
depends=('logmein-hamachi' 'mono' 'gconf-sharp' 'notify-sharp' 'dbus-sharp-glib')
source=("http://launchpad.net/$pkgname/$_series/$pkgver/+download/$pkgname-$pkgver-clr4.0.tar.gz"
        'use_dbussharp_2.patch')
sha512sums=('6272910b23f2ef857489e73e6f65b2a28e6a81f4cd5b6139ae674b465fafd22408f0d12a8cc132b91f966bfd51dfe97a1b93d746d7747d2cd81d0d4f175de687'
            '8b6b37f45f384addca5e14626446854304ad7641fd697fdfe14c33c98c5a40be4758155457dee8a8d78c6099147e03384cabed48308d0e0e8fee801f3964a558')

prepare() {
	cd $srcdir/$pkgname-$pkgver
	patch -Np1 -i $srcdir/use_dbussharp_2.patch
}

build() {
	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr
}

check() {
	cd $srcdir/$pkgname-$pkgver
	make -k check
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
}

