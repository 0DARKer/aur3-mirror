# Maintainer: Reventlov <contact@volcanis.me>
# Your system will break if you install this. For breaking purposes only

pkgname=libressl
pkgver=2.0.5
pkgrel=1
pkgdesc='FREE version of the SSL/TLS protocol forked from OpenSSL - Portable edition'
arch=('i686' 'x86_64')
# Uncomment this if you know what you are doing - libressl is not yet a replacement of openssl
#provides=('openssl=2.0.1')
#conflicts=('openssl')
url='http://www.libressl.org/'
license=('custom:Openssl')
source=("http://ftp.openbsd.org/pub/OpenBSD/LibreSSL/libressl-$pkgver.tar.gz" "libressl-dummy-rand-egd.patch")
sha256sums=('3f5463b5deb93efd2ac4e23a20612a1ccf3299a569d03880bb204c90647dfc9a'
            'a23c08187c7288cee2d8b584555f1e7ae9dba6315cd62d784c20e3cdf3d7a80f')


build() {
    cd "$srcdir/$pkgname-$pkgver"
    # Dummy RAND_egd() function - Can help to compile some stuff - See https://blog.hboeck.de/archives/851-LibreSSL-on-Gentoo.html
    patch -p1 < $srcdir/libressl-dummy-rand-egd.patch || exit 1
    ./configure  --prefix=/usr
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    # Ugly renaming of the libressl manual pages using the scheme of openssl because libressl doesn't provide the MANSUFFIX var yet - Look at the openssl PKGBUILD
    rename ".1" ".1ssl" "$pkgdir"/usr/share/man/man1/*
    rename ".3" ".3ssl" "$pkgdir"/usr/share/man/man3/*
    install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
