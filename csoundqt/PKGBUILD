# Maintainer: tfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com

pkgname=csoundqt
pkgver=0.8.3
pkgrel=1
pkgdesc="CsoundQt (formerly qutecsound) is a frontend for Csound featuring a highlighting editor with autocomplete, interactive widgets and integrated help"
arch=('i686' 'x86_64')
url="http://qutecsound.sourceforge.net/"
license=('GPLv3') # GPLv3 or at your option LGPLv2
depends=('csound' 'qt4' )
provides=('qutecsound')
conflicts=('qutecsound')
install="csoundqt.install"
source=("http://sourceforge.net/projects/qutecsound/files/CsoundQt/${pkgver}/csoundqt-${pkgver}-src.tar.gz" csoundqt.desktop)
md5sums=('e236b35b2bbd4767174f223debe2ee47'
         '121a44312396a1db6eba1a3542e7076b')

#prepare() { }

build() {
# tarball doesn't have a root dir, extracting to $srcdir
  qmake-qt4 qcs.pro
  make
}

package() {
# tarball doesn't have a root dir, extracting to $srcdir
  install -d "$pkgdir"/usr/{bin,share/{applications,csoundqt,doc/csoundqt}}

  install -Dm755 bin/CsoundQt-d-cs6 "$pkgdir"/usr/bin/csoundqt
# desktop file and icon
  install -Dm644 "$srcdir"/csoundqt.desktop "$pkgdir"/usr/share/applications
  install -Dm644 images/qtcs.png "$pkgdir"/usr/share/pixmaps/csoundqt.png
  install -Dm644 images/* -t "$pkgdir"/usr/share/csoundqt/images
# docs
  install -Dm644 doc/*.pdf  -t "$pkgdir"/usr/share/doc/csoundqt  
# examples
  find examples -type f -print0 | xargs -0 -n1 -i{} install -Dm644 '{}' "$pkgdir"/usr/share/csoundqt/'{}'
}
