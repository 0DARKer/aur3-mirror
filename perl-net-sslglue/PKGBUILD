# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-net-sslglue
pkgver=1.04
pkgrel=1
pkgdesc="Adds/extends SSL support for common Perl modules"
arch=(any)
license=('PerlArtistic')
url="http://search.cpan.org/dist/Net-SSLGlue/"
depends=('perl-io-socket-ssl>=1.19')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/SU/SULLR/Net-SSLGlue-${pkgver}.tar.gz)
md5sums=('8a6116f625eb5cb58791359f89800234')
sha512sums=('8eab85189f95d1be397396f5e0bdde49c0b9ae14fe60138c1e187fa4dcc653543e45e0f6edf01a0730f68b7814a18aff08254a185df885e217e51a16624dd9bd')

prepare_env() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Net-SSLGlue-${pkgver}"
}

build() {
  prepare_env
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_env
  make test
}

package() {
  prepare_env
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
