# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-net-sslglue
pkgver=1.03
pkgrel=1
pkgdesc="Adds/extends SSL support for common Perl modules"
arch=(any)
license=('PerlArtistic')
url="http://search.cpan.org/dist/Net-SSLGlue/"
depends=('perl-io-socket-ssl>=1.19')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/SU/SULLR/Net-SSLGlue-${pkgver}.tar.gz)
md5sums=('16c73d1419881904483377232a75fc93')
sha512sums=('6c8b0b60227289f02cedfcc09d020550fcbf315bc0ba67cd244ff5f3926145451e8f39fcd15c43183751a5cfe50f70efbda718813043aaa1b1a86b2fffbf60c0')

prepare_env() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Net-SSLGlue-${pkgver}"
}

build() {
  prepare_env
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_env
  make test
}

package() {
  prepare_env
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
