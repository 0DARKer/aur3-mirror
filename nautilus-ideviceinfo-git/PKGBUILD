# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=nautilus-ideviceinfo-git
pkgver=20110518
pkgrel=1
pkgdesc="Get extra information about your iDevice"
arch=('i686' 'x86_64')
url="http://www.libimobiledevice.org/"
license=('GPL')
depends=('libimobiledevice' 'nautilus')
optdepends=('mobile-broadband-provider-info: to show mobile carrier'
            'libgpod>=0.7.90: to enable detailed audio/video information')
makedepends=('git' 'intltool')
options=(!libtool)
source=(rb-segmented-bar-gtk3-fix.patch)
md5sums=('8b8eb9570d4006aaa9bfc641ab60136c')

_gitroot="git://git.gnome.org/nautilus-ideviceinfo"
_gitname="nautilus-ideviceinfo"

build() {
  cd ${srcdir}
  msg "Connecting to GIT server...."
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${srcdir}/${_gitname}-build
  git clone ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
  cd ${srcdir}/${_gitname}-build

  export providerinfo_CFLAGS="-DMOBILE_BROADBAND_PROVIDER_INFO=\\\"`pkg-config --variable=database mobile-broadband-provider-info`\\\""
  ./autogen.sh --prefix=/usr
  unset providerinfo_CFLAGS
  patch -Np0 -i ../rb-segmented-bar-gtk3-fix.patch
  make
}

package() {
  cd ${srcdir}/${_gitname}-build
  make DESTDIR=${pkgdir}/ install
}
