# Maintainer: Justin Davis <aurperl@juster.us>

pkgname=perl-extutils-makemaker
pkgver=6.59
pkgrel=4
pkgdesc="Create a module Makefile"
arch=(any)
license=(PerlArtistic GPL)
depends=('perl>=5.006' 'perl-cpan-meta>=2.112150'
         'perl-extutils-install>=1.52' 'perl-extutils-command>=1.16'
         'perl-extutils-manifest>=1.58')
url=http://search.cpan.org/dist/ExtUtils-MakeMaker
source=('http://search.cpan.org/CPAN/authors/id/M/MS/MSTROUT/ExtUtils-MakeMaker-6.59.tar.gz')
md5sums=(c1967a3b79b8853a976e09d263bffbf3)
sha512sums=(099925279f589e075c091b4d2830738c1fe4ae4fb102479eff82668cecabd5a1f60dda5ffee8da6b3064cf41097599edd3683360343344603d7bffe508ff13da)
_distdir="${srcdir}/ExtUtils-MakeMaker-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    sed -e '/my::bundles/d' Makefile.PL >Makefile.PL.new
    mv Makefile.PL.new Makefile.PL
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
