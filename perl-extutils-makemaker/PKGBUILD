# Packager: Justin Davis <jrcd83@gmail.com>

pkgname=perl-extutils-makemaker
pkgver=6.62
pkgrel=1
pkgdesc="Create a module Makefile"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.006' 'perl-pathtools' 'perl-podlators'
         'perl-cpan-meta>=2.112150' 'perl-extutils-install>=1.52'
         'perl-extutils-command>=1.16' 'perl-extutils-manifest>=1.58')
makedepends=('perl-data-dumper')
url=https://metacpan.org/release/ExtUtils-MakeMaker
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSCHWERN/ExtUtils-MakeMaker-${pkgver}.tar.gz")
md5sums=(2ae291030c52999b5672b2a502eab195)
sha512sums=(999595e9f6fe07eb6420ecc9c595085e355ce8ea48b06f2f39538c74e7d69f5431e5a9e12218f465c749beab539fa3f37439777e2a2232afa304ad3e720e2a16)
_distdir="${srcdir}/ExtUtils-MakeMaker-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    sed -e '/my::bundles/d' Makefile.PL >Makefile.PL.new
    mv Makefile.PL.new Makefile.PL
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
