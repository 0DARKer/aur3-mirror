# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=awn-extras-applets-bzr
_pkgname=awn-extras-applets
pkgver=1539
pkgrel=1
pkgdesc="A collection of applets for avant-window-navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL2' 'LGPL2.1')
depends=('avant-window-navigator-bzr' 'gnome-menus' \
         'libnotify' 'libsexy' 'libwebkit' 'vte')
makedepends=('bzr' 'intltool' 'vala-012')
optdepends=('fortune-mod: animal-farm applet'
            'gnome-applets: cpufreq applet'
            'gstreamer0.10-python: volume control applet'
            'libgweather: weather applet'
            'python-gdata: calendar applet'
            'python-gtop: bandwidth-monitor applet'
            'python-notify: comics, battery applets'
            'python-rsvg: hardware-sensors, cairo-clock, comics applets'
            'python-simplejson: feeds applet'
            'python-vobject: calendar applet'
            'python-wnck: slickswitcher applet'
            'python-xklavier: dialect applet'
            'python2-dateutil: cairo-clock applet'
            'python2-feedparser: comics, feeds applets'
            'python2-pyinotify: thinkhdaps applet'
            'tomboy: tomboy applet')
provides=('$_pkgname')
conflicts=('$_pkgname')
options=('!libtool')
install="$pkgname".install

_bzrtrunk="https://code.launchpad.net/~awn-extras/awn-extras/trunk"
_bzrmod="awn-extras"

build() {
  cd "$srcdir"

  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr --no-plugins pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr --no-plugins branch "$_bzrtrunk" "$_bzrmod" -q -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  # python2 fix
  find . -type f -name *.py | xargs sed -i 's_^#.*python$_#!/usr/bin/python2_'
  sed -i 's/async(\"python \"/async(\"python2 \"/' \
    applets/maintained/shiny-switcher/shinyswitcherapplet.c

  export PYTHON="/usr/bin/python2"
  export VALAC="/opt/vala-0.12/bin/valac"
  export CFLAGS="$CFLAGS -I/usr/include/gio-unix-2.0/"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
               --datadir=/usr/share --disable-static \
               --disable-pymod-checks \
               --with-webkit
               #--without-gnome   # <-- remove gnome-menus from deps
               #--with-indicator  # <-- add libindicator as dep
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

  install -d -m 755 "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir"/usr/share/gconf/schemas/awn-extras.schemas \
    "$pkgdir"/etc/gconf/schemas/*.schemas

  rm -rf "$pkgdir"/etc
  rm -rf "$pkgdir"/usr/share/locale
}

# vim:set ts=2 sw=2 et:
