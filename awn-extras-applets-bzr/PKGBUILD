# Maintainer: paolo <paolostivanin@gmail.com>

pkgname=awn-extras-applets-bzr
pkgver=1372
pkgrel=3
pkgdesc="Avant-window-navigator applets with minimal dependencies"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL')
depends=('avant-window-navigator-bzr' 'gnome-menus' 'libgtop' 'libnotify' 
	 'libsexy' 'vte' 'gstreamer0.10-python')
makedepends=('intltool' 'libdesktop-agnostic' 'vala-devel' 'bzr')
optdepends=('python-notify: needed for some applets')
provides=('awn-extras-applets=0.4.1')
conflicts=('awn-extras-applets' 'vala-git')
install=awn-extras-applets.install
options=('!libtool')

_bzrmod=awn-extras
_bzrtrunk=lp:${_bzrmod}

build() {
  msg "Connecting to the server..."

  if [ ! -d ./${_bzrmod} ]; then
    bzr co ${_bzrtrunk} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_bzrmod}-build"
  cp -r "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
  cd "$srcdir/${_bzrmod}-build"

  gnome_flag="--without-gnome"

  if ls /usr/include/gnome-menus &>/dev/null; then
    gnome_flag="--with-gnome"
  fi

  webkit_flag="--without-webkit"

  if ls /usr/include/webkit-* &>/dev/null; then
    webkit_flag="--with-webkit"
  fi

  if ls /usr/include/libindicator-* &>/dev/null; then
    indicator_flag="--with-indicator"
  fi

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --datadir=/usr/share \
    --disable-static \
    --enable-sound=auto \
    ${webkit_flag} \
    ${gnome_flag} \
    ${indicator_flag} \
    --disable-pymod-checks || return 1
    make || return 1
    make install DESTDIR=$pkgdir
}
