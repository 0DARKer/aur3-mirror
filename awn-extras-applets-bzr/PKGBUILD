# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Maintainer: twa022 <twa022 at gmail dot com>

_pkgname=awn-extras-applets
pkgname=${_pkgname}-bzr
pkgver=1542
pkgrel=2
pkgdesc="A collection of applets for avant-window-navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL2' 'LGPL2.1')
depends=('avant-window-navigator-bzr' 'gnome-menus' 'gnome-menus2' 
         'python2-notify' 'webkitgtk2' 'vte')
makedepends=('bzr' 'intltool' 'vala-012')
optdepends=('fortune-mod: animal-farm applet'
            'gnome-applets: cpufreq applet'
            'gstreamer0.10-python: volume control applet'
            'libgweather: weather applet'
            'python2-gdata: calendar applet'
            'python2-gtop: bandwidth-monitor applet'
            'python2-notify: comics, battery applets'
            'python2-rsvg: hardware-sensors, cairo-clock, comics applets'
            'python2-simplejson: feeds applet'
            'python2-vobject: calendar applet'
            'python2-wnck: slickswitcher applet'
            'python-xklavier: dialect applet'
            'python2-dateutil: cairo-clock applet'
            'python2-feedparser: comics, feeds applets'
            'python2-pyinotify: thinkhdaps applet'
            'tomboy: tomboy applet')
provides=($_pkgname)
conflicts=($_pkgname)
options=('!libtool')
install="$pkgname".install
source=(fix-weather-applet.patch)

_bzrtrunk="https://code.launchpad.net/~awn-extras/awn-extras/trunk"
_bzrmod="awn-extras"

build() {
  cd "$srcdir"

  msg "Connecting to Bazaar server...."

  if [[ -d "$_bzrmod" ]]; then
    cd "$_bzrmod" && bzr --no-plugins pull "$_bzrtrunk" -r "$pkgver"
    msg "The local files are updated."
  else
    bzr --no-plugins branch "$_bzrtrunk" "$_bzrmod" -q -r "$pkgver"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"

  patch -uNp2 -r-  -i "$srcdir/fix-weather-applet.patch"

  # python2 fix
  # Python2 fix
  find . -type f -name *.py | xargs sed -i 's@^#.*python$@#!/usr/bin/python2@'
  sed -i 's/async(\"python \"/async(\"python2 \"/' applets/maintained/shiny-switcher/shinyswitcherapplet.c

  export PYTHON="/usr/bin/python2"
  export VALAC="/opt/vala-0.12/bin/valac"
  export CFLAGS="$CFLAGS -I/usr/include/gio-unix-2.0/"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
               --datadir=/usr/share --disable-static \
               --disable-pymod-checks \
               --with-webkit
               #--without-gnome   # <-- remove gnome-menus from deps
               #--with-indicator  # <-- add libindicator as dep
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

  install -d -m 755 "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir"/usr/share/gconf/schemas/awn-extras.schemas \
    "$pkgdir"/etc/gconf/schemas/*.schemas

  rm -rf "$pkgdir"/etc

  # Remove bandwidth-monitor applet, because it's not compatible with latest netstat
  rm -R "$pkgdir"/usr/share/avant-window-navigator/applets/bandwidth-monitor*

  # Remove pandora applet, because gtkmozembed is not available
  rm -R "$pkgdir"/usr/share/avant-window-navigator/applets/pandora*
}

sha256sums=('d820d826cbb9c412cff9eb455ef8c3ba0d96b4247f097933db812e87a72f7e4f')
