# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=awn-extras-applets-bzr
_pkgname=awn-extras-applets
pkgver=1537
pkgrel=2
pkgdesc="A collection of applets for avant-window-navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL2' 'LGPL2.1')
depends=('avant-window-navigator-bzr' 'gnome-menus2' \
         'libnotify' 'libsexy' 'libwebkit' 'vte')
makedepends=('bzr' 'intltool' 'libdesktop-agnostic' 'vala-012')
optdepends=('fortune-mod: animal-farm applet'
            'gnome-applets: cpufreq applet'
            'gstreamer0.10-python: volume control applet'
            'libgweather: weather applet'
            'python-gdata: calendar applet'
            'python-gtop: bandwidth-monitor applet'
            'python-notify: comics, battery applets'
            'python-rsvg: hardware-sensors, cairo-clock, comics applets'
            'python-simplejson: feeds applet'
            'python-vobject: calendar applet'
            'python-wnck: slickswitcher applet'
            'python2-dateutil: cairo-clock applet'
            'python2-feedparser: comics, feeds applets'
            'python2-pyinotify: thinkhdaps applet')
provides=(${_pkgname})
conflicts=(${_pkgname})
install=${pkgname}.install
options=('!emptydirs' '!libtool')

_bzrtrunk="lp:awn-extras"
_bzrmod="awn-extras"

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  export PYTHON="/usr/bin/python2"
  export VALAC="/opt/vala-0.12/bin/valac"
  export CFLAGS="${CFLAGS} -I/usr/include/gio-unix-2.0/"

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --datadir=/usr/share \
               --disable-static \
               --disable-pymod-checks \
               --with-webkit #--without-gnome
  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  # yes... theses guys can not even get a "#!" right
  sed -i -e "s|#[ ]*[!]*[ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|#[ ]*![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    $(find "${pkgdir}"/usr/share/avant-window-navigator/applets/ -name '*.py') \
    "${pkgdir}"/usr/lib/python2.7/site-packages/awn/extras/awnmediaplayers.py

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/awn-extras.schemas \
    "${pkgdir}"/etc/gconf/schemas/*.schemas

  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -rf "${pkgdir}"/usr/share/locale
}

# vim:set ts=2 sw=2 et:
