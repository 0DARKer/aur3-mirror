# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=awn-extras-applets-bzr
_realname=awn-extras-applets
pkgver=1513
pkgrel=1
pkgdesc="A collection of applets for avant-window-navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL2' 'LGPL2.1')
depends=('avant-window-navigator-bzr' 'gstreamer0.10-python' \
         'hicolor-icon-theme' 'libnotify' 'libsexy' 'vte')
makedepends=('bzr' 'intltool' 'libdesktop-agnostic-bzr' 'vala-010')
optdepends=('python-notify: needed for some applets'
            'python-vobject: needed for some applets')
provides=(${_realname})
conflicts=(${_realname})
install=${pkgname}.install
options=('!emptydirs' '!libtool')

_bzrtrunk="lp:awn-extras"
_bzrmod="awn-extras"

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  gnome_flag="--without-gnome"
  if ls /usr/include/gnome-menus &>/dev/null; then
    gnome_flag="--with-gnome"
  fi

  webkit_flag="--without-webkit"
  if ls /usr/include/webkit-* &>/dev/null; then
    webkit_flag="--with-webkit"
  fi

  if ls /usr/include/libindicator-* &>/dev/null; then
    indicator_flag="--with-indicator"
  fi

  export PYTHON="/usr/bin/python2"
  export VALAC="/usr/bin/valac-0.10"
  CFLAGS="${CFLAGS} -I/usr/include/gio-unix-2.0/"

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --datadir=/usr/share \
               --disable-static \
               --disable-pymod-checks \
                 ${gnome_flag} \
                 ${webkit_flag} \
                 ${indicator_flag}
  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  # yes... theses guys can not even get a "#!" right
  sed -i -e "s|#[ ]*[!]*[ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|#[ ]*![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    $(find "${pkgdir}"/usr/share/avant-window-navigator/applets/ -name '*.py') \
    "${pkgdir}"/usr/lib/python2.7/site-packages/awn/extras/awnmediaplayers.py

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/awn-extras.schemas \
                      "${pkgdir}"/etc/gconf/schemas/*.schemas

  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -rf "${pkgdir}"/usr/share/locale
}

