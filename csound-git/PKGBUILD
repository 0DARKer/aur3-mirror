# Contributor: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: Jon Austin (IRC: slypheed) <jon.i.austin (gmail)>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Bernardo 'smoge' Barros <bernardobarros@gmail.com>
# Maintainer:  Patrick McCarty <pnorcks at gmail dot com>

pkgname=csound-git
pkgver=5_19_01.69.gca66844
pkgrel=1
pkgdesc="A programming language designed and optimized for sound rendering and signal processing"
url="http://www.csounds.com/"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('alsa-lib'
         'fltk'
         'fluidsynth'
         'liblo'
         'luajit'
         'portaudio'
         'portmidi'
         'python2'
         'tk')
makedepends=('boost'
             'eigen3'
             'dssi'
             'git'
             'gmm'
             'ladspa'
             'pd'
             'scons'
             'swig')
optdepends=('cecilia: tcl/tk frontend'
            'java-environment: java wrapper'
            'vim: vim frontend'
            'wiiuse: Wiimote support')
provides=('csound' 'csound5')
conflicts=('csound' 'csound5' 'csound6-git')
source=(git://git.code.sf.net/p/csound/csound5-git
        http://ccrma.stanford.edu/software/stk/release/stk-4.4.4.tar.gz
        csound.sh
        custom.py
        luajit-include-path.patch)
sha256sums=('SKIP'
            '1276986481704f148933d4a2f8eaebb84827124acbd0243e5c8a004fa7c70710'
            'fa8566ed327c223d7d7e384dcceb87ab3bc439df817771b29fa4c1455e11ab68'
            'bf3ad9efefa186ab4a6097b2e9c3350f63da1b01860d83dee1d74f1fd44b5003'
            '48a876ab98ad6499c1e9ce24232a78eda8f93c51e585c8e8144efd38785110e3')

_csopts="Lib64=0
         NewParserDebug=0
         Word64=1
         bufferoverflowu=0
         buildBeats=1
         buildCsound5GUI=1
         buildCsoundAC=1
         buildCsoundVST=0
         buildDSSI=1
         buildImageOpcodes=1
         buildInterfaces=1
         buildJavaWrapper=1
         buildLuaOpcodes=1
         buildLuaWrapper=1
         buildMultiCore=1
         buildNewParser=1
         buildPDClass=1
         buildPythonOpcodes=1
         buildPythonWrapper=1
         buildRelease=1
         buildStkOpcodes=1
         buildTclcsound=1
         buildUtilities=1
         buildVirtual=1
         buildWinsound=0
         buildcatalog=1
         buildvst4cs=0
         dynamicCsoundLibrary=1
         gcc3opt=0
         gcc4opt=native
         generatePdf=0
         generateTags=0
         includeP5Glove=0
         includeSerial=1
         includeWii=1 install=0
         instdir=$pkgdir/
         noDebug=1
         noFLTKThreads=1
         prefix=/usr
         pythonVersion=2.7
         tclversion=8.6
         useALSA=1
         useAltivec=0
         useCoreAudio=0
         useDouble=1
         useFLTK=1
         useGettext=1
         useGprof=0
         useJack=1
         useLrint=0
         useOSC=1
         useOpenMP=0
         usePortAudio=1
         usePortMIDI=1
         useUDP=1
         withICL=0
         withMSVC=0
         withSunStudio=0"
            
pkgver() {
  cd csound5-git/
  git describe --tags | sed -e 's|csound\(.*\)|\1|' -e 's|-|.|g'
}

prepare() {
  cd csound5-git/

  # fix pd install path, remove uninstaller and ldconfig call
  sed -e "s|/usr/local/lib|$pkgdir/usr/lib|" \
      -e "/create/,/^print ''/d" \
      -e "/getuid/,/ldconfig/d" \
      -i install.py

  # add some optimization
  sed -e "s_mtune_march_g" \
      -i SConstruct

  # various include path fixes for Arch
  patch -p1 -i "$srcdir/luajit-include-path.patch"

  # place stk sources for the stk opcodes
  cp -a "$srcdir"/stk-4.4.4/{include,rawwaves,src} Opcodes/stk
}

build() {
  cd csound5-git/

  # place custom.py
  cp ../custom.py .

  scons $_csopts
}

package() {
  cd csound5-git/

  python2 install.py --vimdir="/usr/share/vim/vimfiles" \
                     --instdir="$pkgdir" \
                     --prefix="/usr"

  # link libraries
  ln -s /usr/lib/libcsound64.so "$pkgdir/usr/lib/libcsound.so"
  ln -s /usr/lib/libcsound64.so "$pkgdir/usr/lib/libcsound.so.5.2"

  # lua bindings
  install -d "$pkgdir/usr/lib/lua/5.1"
  install -Dm 755 luaC{snd,soundAC}.so "$pkgdir/usr/lib/lua/5.1"
  ln -s /usr/lib/lua/5.1/luaCsnd.so "$pkgdir/usr/lib/luaCsnd.so"

  # python CsoundAC bindings
  install -D _CsoundAC.so CsoundAC.py "$pkgdir/usr/lib/python2.7/site-packages"

  # examples
  install -d "$pkgdir/usr/share/csound"
  cp -a examples "$pkgdir/usr/share/csound"
  cp -a samples "$pkgdir/usr/share/csound"

  # export vars in profile.d
  install -Dm755 ../csound.sh "$pkgdir/etc/profile.d/csound.sh"

  # python2 fix
  sed -i "s/^\#\!.*python/&2/" `grep -rl "\#\!.*python" "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
