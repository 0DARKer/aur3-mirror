# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: josephgbr <rafael.f.f1 at gmail dot com>

pkgname=lib32-glew1.5
pkgver=1.5.8
pkgrel=1
pkgdesc="Legacy version of the OpenGL Extension Wrangler Library (32 bit)"
arch=(x86_64)
url=http://glew.sourceforge.net
license=(BSD MIT GPL)
depends=(glew1.5 lib32-libxi lib32-libxmu lib32-mesa)
makedepends=(gcc-multilib)
source=(http://downloads.sourceforge.net/glew/glew-$pkgver.tgz)
sha256sums=('33fdccbbd16fff6f9672e813d020e731b3afaa4e864f68dd2c8c08f236ef4584')
sha512sums=('dfa6b456dc4ebf01382de9b83b1d56c3d564a998285db23af9b302ce69b1c5b7780de8a7656b411fdcce4038187d63f5901be91262affaf962483482d4a3347a')

build() {
    cd "$srcdir"/glew-$pkgver/
    sed -e 's|CC = cc|CC = gcc -m32|g' \
        -e 's|LD = cc|LD = gcc -m32|g' \
        -e 's|lib64|lib32|g' -i config/Makefile.linux
    make
}

package() {
    cd "$srcdir"/glew-$pkgver/

    # We want only these two libraries
    strip -x lib/libGLEW.so.$pkgver
    install -Dm644 lib/libGLEW.so.$pkgver "$pkgdir"/usr/lib32/libGLEW.so.$pkgver
    ln -sf libGLEW.so.$pkgver "$pkgdir"/usr/lib32/libGLEW.so.1.5
    chmod 755 "$pkgdir"/usr/lib32/libGLEW.so.$pkgver

    # License
    mkdir -p "$pkgdir"/usr/share/licenses
    ln -s glew1.5 "$pkgdir"/usr/share/licenses/$pkgname
}
