# Maintainer: Szymon Jakubczak <s.jakubczak@gmail.com>
pkgname=gnuradio-svn
pkgver=11624
pkgrel=2
pkgdesc="free software development toolkit that provides the signal processing runtime and processing blocks to implement software radios using readily-available, low-cost external RF hardware and commodity processors"
arch=('i686' 'x86_64')
license=('GPL')
depends=('fftw' 'python-numpy' 'boost' 'cppunit' 'swig' 'gsl')
makedepends=('subversion')
optdepends=('wxpython: gr-wxgui' 'python-cheetah' 'python-lxml' 'qwtplot3d: gr-qtgui' 'sdcc: firmware')
provides=('gnuradio')
url="http://gnuradio.org"
source=('config-python.patch')
md5sums=('2df6cad8bad13e939e3a983b55efa80b')
install=gnuradio.install

_svntrunk=http://gnuradio.org/svn/gnuradio/trunk
_svnmod=gnuradio

build() {
  cd ${srcdir}

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cd $_svnmod

  # patch the config scripts to use C instead of Fortran
  # for Python.h usability testing
  patch -Np0 < $startdir/config-python.patch

  ./bootstrap
  ./configure --prefix=/usr
  make || return 1
  make check || return 1
  make DESTDIR=${pkgdir} install || return 1
}


