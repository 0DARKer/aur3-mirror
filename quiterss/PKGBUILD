# Contributer: giacomogiorgianni@gmail.com 

pkgname=quiterss
_pkgname=QuiteRSS
pkgver=0.15.4
pkgrel=2
pkgdesc='Fast and light RSS/Atom feed reader written on Qt/ะก++'
arch=('i686' 'x86_64')
url='http://quiterss.org/'
license=('GPL3')
depends=('qtwebkit' 'phonon-qt4')
makedepends=('cmake' 'gettext' 'automoc4')
source=("http://quiterss.org/files/$pkgver/$_pkgname-$pkgver-src.tar.bz2")
sha512sums=('f0d0d2cadff46378ec0560523a0208e35b077376b1f5de32f425724e5c620336a8275196c469286203fa63a539b5c35c4872a5c5f57bb25d48f2043ce0bdaba1')
install=$pkgname.install

build() {
  cd "$srcdir/$_pkgname-$pkgver-src"
  qmake-qt4 "$srcdir/$_pkgname-$pkgver-src/$_pkgname.pro" \
    PREFIX=/usr \
    CONFIG+=LINUX_INTEGRATED \
    INSTALL_ROOT_PATH="$pkgdir"

  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver-src"
  make INSTALL_ROOT="$pkgdir" install
}

