# Contributer Jonathan Wiersma <arch aur at jonw dot org>

pkgname=helixplayer-nightly-bin
pkgver=11.1.1.2474
_pkgdate=20100201
_pkgdlhash=022d0e07dcfaab300202
pkgrel=1
pkgdesc="Open source media player based on the Helix DNA Client media engine"
arch=('i686' 'x86_64')
url="http://forms.helixcommunity.org/helix/builds/"
license=("GPL" "custom:RPSL" "custom:RCSL")
depends=('gtk2' 'libxv' 'libstdc++5' 'alsa-lib')
provides=("helixplayer=$pkgver")
conflicts=("helixplayer" "helixplayer-nightly" "realplayer" "realplayer-nightly" "realplayer-nightly-bin")
source=("http://software-dl.real.com/$_pkgdlhash/helix/$_pkgdate/player_all-hxplay_gtk_current-$_pkgdate-linux-2.2-libc6-gcc32-i586/hxplay-$pkgver-linux-2.2-libc6-gcc32-i586.tar.bz2")
md5sums=('0614d64debf356ee242d1d2ac56edaf2')
if [ "$CARCH" == "x86_64" ]; then
	source=("http://software-dl.real.com/$_pkgdlhash/helix/$_pkgdate/player_all-hxplay_gtk_current-$_pkgdate-linux-2.6-glibc23-amd64/hxplay-$pkgver-linux-2.6-glibc23-amd64.tar.bz2")
md5sums=('aad54244b44b8487f25338bbd9879fca')
fi


build() {
	cd $srcdir || return 1
	
	# Set-up Directories
	install -d $pkgdir/usr/{bin,share} || return 1
	install -d $pkgdir/opt/hxplay/bin || return 1
	install -d $pkgdir/usr/lib/mozilla/plugins || return 1
	install -d $pkgdir/usr/share/licenses/${pkgname} || return 1
	
	# Install shared libs
	for _file in {codecs,common,plugins}/*.so; do
		install -Dm755 "$_file" "$pkgdir/opt/hxplay/$_file" || return 1
	done || return 1
	
	# Install selected files from share directory
	find share/{default,distcode,hxplay*,superbuffer,*.css,*.html} \
		-type f | while read _file; do
			install -Dm644 "$_file" "$pkgdir/opt/hxplay/$_file" || return 1
	done || return 1
	
	for _file in share/locale/*/{LICENSE,README}; do
		install -Dm644 "$_file" "$pkgdir/opt/hxplay/$_file" || return 1
	done || return 1
	
	# Install executables
	install -m755 hxplay hxplay.bin $pkgdir/opt/hxplay/ || return 1
	install -m755 Bin/setup $pkgdir/opt/hxplay/bin/ || return 1
	ln -s /opt/hxplay/hxplay $pkgdir/usr/bin/hxplay || return 1
	
	# Install Icons
	for _res in 16 192 32 48; do
		install -Dm644 $srcdir/share/icons/hxplay_${_res}x${_res}.png \
			 $pkgdir/usr/share/icons/hicolor/${_res}x${_res}/apps/hxplay.png || return 1
	done || return 1
	
	cd $srcdir/share/icons || return 1
	for _file in *; do
		install -Dm644 "$_file" \
			"$pkgdir/usr/share/icons/hicolor/$(echo ${_file#*_} \
				| sed -e 's:\..*::g')mimetypes/${_file%_*}.${_file#*.}" || return 1
	done || return 1
	
	# Install Locales
	cd $srcdir/share/locale || return 1
	for _locale in *; do
		install -d $pkgdir/usr/share/locale/$_locale/LC_MESSAGES || return 1
		install -m644 $_locale/*.mo \
			$pkgdir/usr/share/locale/$_locale/LC_MESSAGES/ || return 1
	done
	
	# Install desktop extras
	cd $srcdir || return 1
	install -Dm644 share/hxplay.png \
		$pkgdir/usr/share/pixmaps/hxplay.png || return 1
	install -Dm644 share/hxplay.applications \
		$pkgdir/usr/share/application-registry/hxplay.applications || return 1
	install -Dm644 share/hxplay.desktop \
		$pkgdir/usr/share/applications/hxplay.desktop || return 1
	
	install -d $pkgdir/usr/share/mime-info || return 1
	install -m644 share/hxplay.keys share/hxplay.mime \
		$pkgdir/usr/share/mime-info/ || return 1

	# Install mozilla plugins
	install -m755 mozilla/*.{so,xpt} \
		$pkgdir/usr/lib/mozilla/plugins/ || return 1

	# Install Licence and extras
	install -m644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/ || return 1
	install -m644 LICENSE README $pkgdir/opt/hxplay/ || return 1
}
