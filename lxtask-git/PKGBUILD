# Maintainer: Raimar Buehmann (raimar)

pkgname=lxtask-git
#pkgver=20120117
pkgver=0.1.4.r365.20140222
pkgrel=1
pkgdesc="Task manager and system monitor of LXDE"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('lxde-git')
depends=('gtk2')
makedepends=('autoconf' 'automake' 'gcc' 'libtool' 'make' 'pkgconfig' 'git')
provides=('lxtask')
conflicts=('lxtask')
replaces=('lxtask' 'lxtask-git=20120117-1')
url="http://lxde.org/"
# startlxde is useless without lxsession 
optdepends=(lxsession-lite)
source=($pkgname::git+git://lxde.git.sourceforge.net/gitroot/lxde/lxtask
	fix-sort-tasks-by-memory.patch)
sha256sums=('SKIP' 'b34ac51062f064293feefc3b50fcb2074380889e00e7dae1d2bb14b241800525')

_gitroot=$pkgname

pkgver() {
	# remove ".r*.*" from package version
	_pkgverTriple=$(echo $pkgver | sed "s/\.r.*//g")
	cd $_gitroot
	# get number of last git commit
	_commitCount=$(git rev-list --count HEAD)
	# get time of last git commit
	_commitTime=$(git show -s --format="%ci" | grep -o "....-..-.." | sed "s/-//g")
	# add ".r*.*" from package version
	echo "$_pkgverTriple.r$_commitCount.$_commitTime"
}

prepare() {
	cd $_gitroot
	patch -Np1 -i ../fix-sort-tasks-by-memory.patch
}

build() {
	cd $_gitroot
	msg "Starting make..."
	./autogen.sh
	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd $_gitroot
	make DESTDIR="$pkgdir" install
}
