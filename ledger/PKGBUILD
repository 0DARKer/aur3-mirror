# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>
# Contributor: TDY <tdy@archlinux.info>
# Contributor: Adam Ehlers Nyholm Thomsen <adament at gmail dot com>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=ledger
pkgver=3.1
pkgrel=2
pkgdesc='A double-entry accounting system with a command-line reporting interface.'
arch=('i686' 'x86_64')
url='http://www.ledger-cli.org'
license=('BSD')
depends=('boost>=1.49' 'mpfr>=2.4.0' 'pcre' 'expat' 'libedit')
makedepends=('cmake' 'git' 'patch' 'python' 'texinfo' 'texlive-plainextra' 'icu>=55')
options=('!libtool')
install="${pkgname}.install"
source=("https://github.com/ledger/${pkgname}/archive/v${pkgver}.tar.gz"
        'ledger_git.diff')
sha256sums=('eeb5d260729834923fc94822bcc54ca3080c434f81466a3f5dc4274b357ce694'
            'ef87d8ff27334f7342be3a1d1c2716861525dd57d2bec70df89a19d8514a654c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  git clone --depth=1 https://github.com/ledger/utfcpp.git lib/utfcpp # submodule excluded
  # https://groups.google.com/forum/#!topic/ledger-cli/BMYK_bupv5Y
  patch -Np1 -i ../ledger_git.diff
  ./acprep --prefix=/usr opt update
  cmake ./ -DCMAKE_INSTALL_LIBDIR=/usr/lib -DBUILD_EMACSLISP:BOOL=ON -DBUILD_DOCS:BOOL=ON
  make doc
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 doc/LICENSE "${pkgdir}/usr/share/licenses/ledger/LICENSE"
}

# vim:set ts=2 sw=2 et:
