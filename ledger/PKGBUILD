# Maintainer: korjjj <korjjj+aur@gmail.com>
# Contributor: TDY <tdy@archlinux.info>
# Contributor: Adam Ehlers Nyholm Thomsen <adament at gmail dot com>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=ledger
pkgver=3.0.3
pkgrel=4
pkgdesc="A double-entry accounting system with a command-line reporting interface"
arch=('i686' 'x86_64')
url="http://ledger-cli.org/"
license=('BSD')
depends=('boost>=1.35' 'expat>=2.0.1' 'gmp>=4.2.2' 'pcre>7.7')
makedepends=('cmake' 'git' 'python' 'texinfo' 'texlive-plainextra')
options=('!libtool')
install=${pkgname}.install
source=("https://github.com/ledger/${pkgname}/archive/v${pkgver}.tar.gz"
        "https://github.com/ledger/${pkgname}/commit/c1598b7de27c5ba5fafc8a5efb1db18fe54d3070.patch")
sha256sums=('b4d506dec9180af5e44cbf591edae542919e0961f2611f292680fc4658c29498'
            'b3e229d4911f811466032a50f52528512a6dd21eb50e40e3679762242d425e94')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  git clone https://github.com/ledger/utfcpp.git lib/utfcpp # submodule excluded
  patch -p1 < ${srcdir}/c1598b7de27c5ba5fafc8a5efb1db18fe54d3070.patch # boost 1.56 patch for 3.0.3
  ./acprep --prefix=/usr opt update
  cmake -DBUILD_EMACSLISP:BOOL=ON -DBUILD_DOCS:BOOL=ON .
  make doc
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 doc/LICENSE "${pkgdir}/usr/share/licenses/ledger/LICENSE"
}

# vim:set ts=2 sw=2 et:
