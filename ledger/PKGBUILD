# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=ledger
pkgver=2.6.3
pkgrel=2
pkgdesc="A double-entry accounting system with a command-line reporting interface"
arch=('i686' 'x86_64')
url="http://github.com/jwiegley/ledger/wiki/"
license=('BSD')
depends=('expat>=2.0.1' 'gmp>=4.2.2' 'pcre>7.7')
options=('!libtool')
install=$pkgname.install
source=(https://github.com/downloads/jwiegley/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('6d5d8396b1cdde5f605854c7d21d1460')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  autoreconf -vis
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/ledger/LICENSE"
}
