# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=ledger
pkgver=2.6.3
pkgrel=4
pkgdesc="A double-entry accounting system with a command-line reporting interface"
arch=('i686' 'x86_64')
url="http://ledger-cli.org/"
license=('BSD')
depends=('expat>=2.0.1' 'gmp>=4.2.2' 'pcre>7.7')
options=('!libtool')
install=$pkgname.install
source=(https://github.com/jwiegley/$pkgname/archive/v$pkgver.tar.gz
        http://ledger-cli.org/2.6/ledger.pdf)
sha256sums=('d5c244343f054c413b129f14e7020b731f43afb8bdf92c6bdb702a17a2e2aa3a'
            'a1fc66f620480e1a6a50818afa9d2bdba33be2960c2845a38ede2ff0a82eae8a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  autoreconf -vis
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/ledger/LICENSE"
  install -Dm644 ../$pkgname.pdf "$pkgdir/usr/share/doc/$pkgname/$pkgname.pdf"
}

# vim:set ts=2 sw=2 et:
