# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=ledger
pkgver=2.6.3
pkgrel=3
pkgdesc="A double-entry accounting system with a command-line reporting interface"
arch=('i686' 'x86_64')
url="http://github.com/jwiegley/ledger/wiki/"
license=('BSD')
depends=('expat>=2.0.1' 'gmp>=4.2.2' 'pcre>7.7')
options=('!libtool')
install=$pkgname.install
source=(https://github.com/jwiegley/$pkgname/archive/v$pkgver.tar.gz)
md5sums=('c710d4c4e2a2f037bcd8a3991a0158c4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  autoreconf -vis
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/ledger/LICENSE"
}
