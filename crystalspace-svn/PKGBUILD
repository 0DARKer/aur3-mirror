# Maintainer: Harley Laue <losinggeneration@gmail.com>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=crystalspace-svn
pkgver=38214
pkgrel=1
pkgdesc="A free and portable 3D Game Development Kit written in C++"
arch=('i686' 'x86_64')
url="http://www.crystalspace3d.org/"
license=('LGPL2')
depends=('speex' 'libgl' 'wxgtk' 'openal' 'alsa-lib' 'python' \
         'cegui' 'libvorbis' 'nvidia-cg-toolkit' 'cal3d'\
         'bullet' 'assimp')
makedepends=('subversion' 'ftjam' 'swig')
provides=('crystalspace')
conflicts=('crystalspace' 'crystalspace-1.4branch-svn')
source=()
md5sums=()

_svntrunk=http://crystal.svn.sourceforge.net/svnroot/crystal/CS/trunk
_svnmod=crystalspace

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
      (cd "$_svnmod" && svn up -r "$pkgver")
  else
      svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -ru "$srcdir/$_svnmod/" "$srcdir/$_svnmod-build/"
  cd "$srcdir/$_svnmod-build"

  # Ode's plugin does not compile & java isn't found with jdk7
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --without-java --with-python --without-ode --disable-make-emulation || return 1
  jam
}

package() {
  cd "$srcdir/$_svnmod-build"
  jam DESTDIR=$pkgdir/ install
}

# vim:set ts=2 sw=2 et:
