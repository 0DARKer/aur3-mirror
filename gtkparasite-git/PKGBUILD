# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Hagen Schink <troja84@googlemail.com>

pkgname=gtkparasite-git
_pkgname=gtkparasite
pkgver=0.2.0.71.0a0c90b
pkgrel=1
pkgdesc="Gtk+ debugging and development tool"
url="http://chipx86.github.com/gtkparasite/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('pygtk')
makedepends=('git')
options=('!libtool')
source=("git://github.com/chipx86/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"

  echo $(awk -F , '/^AC_INIT/ {print $2}' configure.ac | tr -d '[ ]').$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
# Replace old AutoMake macro with new one
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' "$srcdir/$_pkgname/configure.ac"
}

build() {
  cd "$srcdir/$_pkgname"

  PYTHON=python2 \
  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
} 
