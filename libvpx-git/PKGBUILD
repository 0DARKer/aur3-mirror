# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libvpx-git
pkgver=1.3.0.480.gcdc933c
pkgrel=1
pkgdesc='The VP8 and VP9 Codec SDK'
arch=('i686' 'x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('glibc')
makedepends=('git' 'yasm')
provides=('libvpx')
conflicts=('libvpx')
source=(git+http://git.chromium.org/webm/libvpx.git)
md5sums=('SKIP')

pkgver() {
  cd libvpx
  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd libvpx

    ./configure --prefix=/usr \
	--enable-vp8 \
	--enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs

  make
}

package() {
  cd libvpx
  make DIST_DIR="$pkgdir/usr" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
