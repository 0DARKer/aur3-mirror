# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: h31 <h31mail@yandex.com>
# Contributor: julroy67 <julroy67 [AT] gmail.com>

pkgname=libvpx-git
pkgver=20101120
pkgrel=1
pkgdesc="The VP8 Codec SDK (git version)"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
makedepends=('git' 'yasm')
provides=('libvpx=0.9.5')
conflicts=('libvpx')

_gitroot='git://review.webmproject.org/libvpx.git'
_gitname='libvpx'

build() {
  msg "Connecting to the GIT server...."
  cd "$srcdir"

  if [[ -d "$srcdir/$_gitname" ]] ; then
      cd "$_gitname"
      git pull origin
      msg "The local files are updated."
  else
      git clone $_gitroot
  fi

  msg "GIT checkout done"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build/"
  cp -r "$srcdir/$_gitname/" "$srcdir/$_gitname-build/"

  cd "$srcdir/$_gitname-build/"

  ./configure --enable-vp8 \
	      --enable-shared \
	      --enable-postproc \
	      --enable-psnr \
	      --enable-pic \
	      --disable-install-docs \
	      --disable-install-srcs
  make
}

package() {
  cd "$srcdir/$_gitname-build/"

  make DIST_DIR="$pkgdir/usr" install
}
