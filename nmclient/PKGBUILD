pkgname=nmclient
pkgver=2.2.2
pkgrel=1
pkgdesc="Novell Messenger client for linux"
url="http://gwclient.provo.novell.com/"
license=(Novell-GW-8)
install=$pkgname.install
arch=('i686' 'x86_64')
case ${CARCH} in
    i686 )        
        depends=('bash' 'jre' 'glib2' 'libxext' 'gtk2' 'hicolor-icon-theme' 'desktop-file-utils')
        makedepends=('unzip' 'gcc' 'rpmextract')
        ;;
    x86_64 )
        depends=('bash' 'bin32-jre' 'lib32-glib2' 'lib32-libxext' 'lib32-gtk2' 'hicolor-icon-theme' 'desktop-file-utils')
        makedepends=('unzip' 'gcc-multilib' 'rpmextract')
        ;;
esac
source=(https://gwclient.innerweb.novell.com/client/messenger/nvlmsgrlinuxrpm.zip)
md5sums=('b10d0015e76edb50eb169193bc919a5a')

package() {
      # extract blob package
    cd "$srcdir"
    unzip -o nvlmsgrlinuxrpm.zip
    cd "$pkgdir"
    rpmextract.sh "$srcdir"/nvlmsgrlinux.rpm
    chmod +rx opt usr
    
      # use distribution's JavaRE package
    rm -rf opt/novell/messenger/client/{jre,java}
    sed -i 's%^JAVA_BIN=.*%JAVA_BIN=/opt/bin32-jre/bin/java%' "$pkgdir"/opt/novell/messenger/client/run-messenger
    case ${CARCH} in
        i686 )
            sed -i '/LD_LIBRARY_PATH/s/:.*/:\/opt\/java\/jre\/lib\/i386:\/opt\/java\/jre\/lib\/i386\/client/' "$pkgdir"/opt/novell/messenger/client/run-messenger
            ;;
        x86_64 )
            sed -i '/LD_LIBRARY_PATH/s/:.*/:\/opt\/bin32-jre\/jre\/lib\/i386:\/opt\/bin32-jre\/lib\/i386\/client/' "$pkgdir"/opt/novell/messenger/client/run-messenger
            ;;
    esac
     # prepare system files
    rm -rf "$pkgdir"/usr
    install -d "$pkgdir"/usr/bin \
               "$pkgdir"/usr/share/applications/ \
               "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
               
    ln -s /opt/novell/messenger/client/run-messenger \
           "$pkgdir"/usr/bin/nmclient
    ln -s /opt/novell/messenger/client/nmclient.desktop \
           "$pkgdir"/usr/share/applications/
    ln -s /opt/novell/messenger/client/nmclient.png \
           "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
    
     # fix Exec and Icon paths in the desktop file
    sed -i -e 's/Exec=.*/Exec=nmclient/;s/Icon=.*/Icon=nmclient.png/' \
           "$pkgdir"/opt/novell/messenger/client/nmclient.desktop
}
