# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=nmclient
pkgver=2.2.2
pkgrel=3
pkgdesc="Novell Messenger client for linux"
url="http://gwclient.provo.novell.com/"
license=(Novell-GW-8)
install=$pkgname.install
arch=('i686' 'x86_64')
if [ "${CARCH}"  == "i686" ]; then
    depends=('bash' 'java-runtime' 'glib2' 'libxext' 'gtk2' 'hicolor-icon-theme' 'desktop-file-utils')
    makedepends=('unzip' 'gcc' 'rpmextract')
elif [ "${CARCH}"  == "x86_64" ]; then
    depends=('bash' 'java-runtime-32' 'lib32-glib2' 'lib32-libxext' 'lib32-gtk2' 'hicolor-icon-theme' 'desktop-file-utils')
    makedepends=('unzip' 'gcc-multilib' 'rpmextract')
fi
source=(https://gwclient.innerweb.novell.com/client/messenger/nvlmsgrlinuxrpm.zip)
md5sums=('b10d0015e76edb50eb169193bc919a5a')

package() {
      # extract blob package
    cd "$srcdir"
    unzip -o nvlmsgrlinuxrpm.zip
    cd "$pkgdir"
    rpmextract.sh "$srcdir"/nvlmsgrlinux.rpm
    chmod +rx opt usr

      # use distribution's JavaRE package; set correctly LD path and binary
    rm -rf opt/novell/messenger/client/{jre,java}
    if [[ "${CARCH}"  == "i686" ]]; then
        sed -i "$pkgdir"/opt/novell/messenger/client/run-messenger \
          -e 's#LD_LIBRARY_PATH=.*#LD_LIBRARY_PATH=/opt/java/jre:$CLIENT_PATH \\#' \
          -e 's#JAVA_BIN=.*#JAVA_BIN=/opt/java/jre/bin/java#'
    elif [[ "${CARCH}"  == "x86_64" ]]; then
        sed -i "$pkgdir"/opt/novell/messenger/client/run-messenger \
         -e 's#LD_LIBRARY_PATH=.*#LD_LIBRARY_PATH=/opt/java32/jre:$CLIENT_PATH \\#' \
         -e 's#JAVA_BIN=.*#JAVA_BIN=/opt/java32/jre/bin/java#'
    fi
    
     # prepare system files
    rm -rf "$pkgdir"/usr
    install -d "$pkgdir"/usr/bin \
               "$pkgdir"/usr/share/applications/ \
               "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
               
    ln -s /opt/novell/messenger/client/run-messenger \
           "$pkgdir"/usr/bin/nmclient
    ln -s /opt/novell/messenger/client/nmclient.desktop \
           "$pkgdir"/usr/share/applications/
    ln -s /opt/novell/messenger/client/nmclient.png \
           "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
    
     # fix Exec and Icon paths in the desktop file
    sed -i -e 's/Exec=.*/Exec=nmclient/;s/Icon=.*/Icon=nmclient.png/' \
           "$pkgdir"/opt/novell/messenger/client/nmclient.desktop
}
