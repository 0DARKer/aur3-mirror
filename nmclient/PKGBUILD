# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=nmclient
pkgver=2.2.2
pkgrel=4
pkgdesc="Novell Messenger client $_version for linux"
url="http://gwclient.provo.novell.com/"
license=(custom)
install=$pkgname.install

arch=('i686' 'x86_64')
if [ "${CARCH}"  == "i686" ]; then
  depends=('bash' 'java-runtime' 'glib2' 'libxext' 'gtk2'
           'hicolor-icon-theme' 'desktop-file-utils')
  makedepends=('unzip' 'gcc' 'rpmextract')
elif [ "${CARCH}"  == "x86_64" ]; then
  depends=('bash' 'java-runtime-32' 'lib32-glib2' 'lib32-libxext' 'lib32-gtk2'
           'hicolor-icon-theme' 'desktop-file-utils')
  makedepends=('unzip' 'gcc-multilib' 'rpmextract')
fi
source=(https://gwclient.innerweb.novell.com/client/messenger/nvlmsgrlinuxrpm.zip)
md5sums=('b10d0015e76edb50eb169193bc919a5a')

build() {
    # just extract blob package
  rm -rf "$pkgname-$pkgver"
  mkdir "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  rpmextract.sh "$srcdir"/nvlmsgrlinux.rpm
  chmod +rx opt usr
}

package() {
  cd "$pkgname-$pkgver"

     # prepare directories  
  install -d "$pkgdir"/usr/bin \
         "$pkgdir"/usr/share/nmclient/ \
         "$pkgdir"/usr/share/applications/ \
         "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
  
    # install data and doc files, excluding embedded JRE
  for file in `ls opt/novell/messenger/client/ | egrep -v 'jre'`; do
    cp -R opt/novell/messenger/client/$file "$pkgdir"/usr/share/nmclient/
  done
  
    # install  executable script, and fix java path and LD_LIBRARY_PATH
  mv "$pkgdir"/usr/share/nmclient/run-messenger "$pkgdir"/usr/bin/nmclient
  
  if [ "$CARCH"  == "i686" ]; then
    JAVA_BIN=/usr/bin/java
  elif [ "$CARCH"  == "x86_64" ]; then
    JAVA_BIN=/opt/java32/jre/bin/java
  fi
  
  sed -e "s#LD_LIBRARY_PATH=.*#LD_LIBRARY_PATH=\$JAVA_BIN:\$CLIENT_PATH \\\\#" \
    -e "s#^JAVA_BIN=.*#JAVA_BIN=$JAVA_BIN#" \
    -e "s#^CLIENT_PATH=.*#CLIENT_PATH=/usr/share/nmclient/#" \
    -i "$pkgdir"/usr/bin/nmclient
  
    # set Icons and desktop file, and fix their paths
  mv "$pkgdir"/usr/share/nmclient/nmclient.desktop \
       "$pkgdir"/usr/share/applications/
  mv "$pkgdir"/usr/share/nmclient/nmclient.png \
       "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
  
  sed -e 's/Exec=.*/Exec=nmclient/;s/Icon=.*/Icon=nmclient.png/' \
    -i "$pkgdir"/usr/share/applications/nmclient.desktop
}
