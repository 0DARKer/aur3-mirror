# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Judd <jvinet@zeroflux.org>

_pkgbase=libevent
pkgname=lib32-${_pkgbase}
pkgver=2.0.17
pkgrel=1
pkgdesc="An event notification library (32 bit)"
arch=('x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('GPL2')
depends=('lib32-openssl' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=(https://github.com/downloads/libevent/libevent/${_pkgbase}-${pkgver}-stable.tar.gz)
sha1sums=('cea3af2d4bd688784f270ac2ecae8ea6aaaa463f')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKGC_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${srcdir}/${_pkgbase}-${pkgver}-stable"
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}-stable"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include}
}
