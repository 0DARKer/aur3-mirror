# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=buzztard
pkgver=0.6.0
pkgrel=1
pkgdesc="A modular, free, open source music studio that is conceptually based on Buzz."
arch=('i686' 'x86_64')
url="http://www.buzztard.org"
license=('LGPL')
depends=('gst-buzztard>=0.6.0' 'libgsf' 'libgnomecanvas' 'gstreamer0.10-base' 'gconf'
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'gtk-doc')
options=('!libtool' '!emptydirs')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/sourceforge/buzztard/$pkgname-$pkgver.tar.gz")
md5sums=('3c5bd98c9332963b63fc048d54d2ea29')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --enable-static=no \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Deleting unneeded files
  rm -f "$pkgdir/usr/share/applications/mimeinfo.cache"
  find "$pkgdir/usr/share/mime" -maxdepth 1 -type f -exec rm {} \;
}
