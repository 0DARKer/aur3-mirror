# Maintainer Yichao Yu <yyc1992@gmail.com>
# Contributor kfgz <kfgz at interia pl>
# Package based on glib2

pkgname=glib2-git
pkgver=20120729
pkgrel=1
_realver=2.33.7
pkgdesc="Common C routines used by GTK+ 2.4 and other libs"
url="http://www.gtk.org/"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('pcre>=8.13')
makedepends=('pkgconfig' 'gettext' 'gtk-doc' 'git' 'python2')
conflicts=('glib2')
provides=("glib2=${_realver}")
options=('!libtool' '!docs')
source=(glib2.sh
        glib2.csh)
sha256sums=('9456872cdedcc639fb679448d74b85b0facf81033e27157d2861b991823b5a2a'
            '8d5626ffa361304ad3696493c0ef041d0ab10c857f6ef32116b3e2878ecf89e3')

_gitroot="git://git.gnome.org/glib"
_gitname="glib"

build() {
  msg "Connecting to gnome.org GIT server...."

  if [ -d ${srcdir}/${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Creating build directory"
  if [ -d ${srcdir}/${_gitname}-build ]; then rm -rf ${srcdir}/${_gitname}-build; fi
  cp -R ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build

  msg "Starting make..."
  cd ${srcdir}/${_gitname}-build
  ./autogen.sh --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam

  #brutal python2 patch
  find ${srcdir}/${_gitname}-build -name Makefile -exec sed -i "s|^.*/usr/bin/python.*$|PYTHON = /usr/bin/python2|g" {} \;
  make
}

package() {
  cd ${srcdir}/${_gitname}-build
  make completiondir=/usr/share/bash-completion/completions \
    DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/glib2.sh" "${pkgdir}/etc/profile.d/"
  install -m755 "${srcdir}/glib2.csh" "${pkgdir}/etc/profile.d/"

  for _i in "$pkgdir/usr/share/bash-completion/completions/"*; do
      chmod -x "$_i"
  done
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" \
    "$pkgdir"/usr/bin/gdbus-codegen
}
