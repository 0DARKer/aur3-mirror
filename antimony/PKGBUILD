# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=antimony
pkgver=0.7.9
pkgrel=1
pkgdesc="Graph-based 3D CSG CAD modeller"
url="http://www.mattkeeter.com/projects/antimony/3/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('qt5-base' 'boost-libs' 'python' 'libpng')
makedepends=('boost')
source=("https://github.com/mkeeter/antimony/archive/$pkgver.tar.gz")
md5sums=('f77a5648d287905343e70f56a3703f8e')

# todo, replace bundled eigen with official

build() {
    cd "$srcdir/$pkgname-$pkgver"

    sed -i 's/lboost_python-py34/lboost_python3/' qt/shared.pri
    sed -i 's|/usr/local/bin/sb/|/usr/share/antimony/sb/|' qt/antimony.pro 
    sed -i 's|/usr/share/antimony/sb/fab|/usr/lib/python3.4/site-packages/fab|' qt/antimony.pro
    sed -i 's|return path.join("/");|return "/usr/share/antimony/sb/nodes";|' src/app/app.cpp

    mkdir -p build
    cd build
    qmake-qt5 PREFIX="/usr" ../qt/antimony.pro
    sed -i 's|/local/bin|/bin|g' Makefile
    make
    make  # binary isn't built on first pass?
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    return 0
    mkdir -p tests
    cd tests
    qmake-qt5 PREFIX="/usr" ../qt/antimony-tests.pro
    make
    # failure involving QNetworkAccessManager
}

package() {
    cd "$srcdir/$pkgname-$pkgver/build"
    make INSTALL_ROOT="$pkgdir" install
    #mkdir -p "$pkgdir/usr/lib/python3.4/site-packages/"
    #cp -r "$pkgdir/usr/share/antimony/sb/fab" "$pkgdir/usr/lib/python3.4/site-packages/"
}
