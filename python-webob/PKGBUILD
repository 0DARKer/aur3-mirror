# Maintainer: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
pkgname='python-webob'
pkgver='1.2'
pkgrel='1'
pkgdesc="WebOb provides objects for HTTP requests and responses. (Python 3)"
arch=('any')
url="http://pythonpaste.org/webob/"
license=('MIT')
depends=('python')
makedepends=('python-distribute')
optdepends=('python-webob-docs: documentation')
changelog='Changelog'
source=("http://pypi.python.org/packages/source/W/WebOb/WebOb-${pkgver}.zip")
md5sums=('e5d9699f31f296384f67cbfd90e84a9e')

build() {
  cd "$srcdir/WebOb-${pkgver}"
  python setup.py build
}

package() {
  cd "$srcdir/WebOb-${pkgver}"
  python setup.py install --root="$pkgdir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

check() {
  cd "$srcdir/WebOb-${pkgver}"
  python setup.py test
}
