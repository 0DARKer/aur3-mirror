# Maintainer: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
pkgname='python-webob'
pkgver='1.2.3'
pkgrel='1'
pkgdesc="WebOb provides objects for HTTP requests and responses. (Python 3)"
arch=('any')
url="http://pythonpaste.org/webob/"
license=('MIT')
depends=('python')
makedepends=('python-distribute')
optdepends=('python-webob-docs: documentation')
changelog='Changelog'
source=("http://pypi.python.org/packages/source/W/WebOb/WebOb-${pkgver}.tar.gz"
        "test_headers2.patch")
md5sums=('11825b7074ba7043e157805e4e6e0f55'
         '973366db2c25c4131b7edf47df5df8de')

build() {
  cd "$srcdir/WebOb-${pkgver}"
  # A test assumes that dict keys are always in the same order. Change
  # to sets.
  patch -p1 < "$startdir/test_headers2.patch"
  python setup.py build
}

package() {
  cd "$srcdir/WebOb-${pkgver}"
  python setup.py install --root="$pkgdir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

check() {
  cd "$srcdir/WebOb-${pkgver}"
  python setup.py test
}
