# Maintainer: Seishinryohosha <seishinryohosha @ googlemail . com>
# Contributor: Seishinryohosha <seishinryohosha @ googlemail . com>
pkgname=greenfoot
pkgver=2.4.1
pkgrel=1
pkgdesc="It allows easy development of two-dimensional graphical applications, such as simulations and interactive games."
arch=('any')
url="http://www.greenfoot.org"
license=('GPL2' 'BSD')
depends=('sh' 'java-runtime' 'java-environment')
makedepends=('libarchive')
source=(http://www.greenfoot.org/download/files/Greenfoot-generic-${pkgver//.}.jar
        greenfoot
        greenfoot.desktop)
md5sums=('b1c6331f75089ce47a81b045c438034d'
         'ec3799362efcfdd8b14aee66002ee440'
         '23769127e8e72bd721e4b1634d3df2f9')

package() {
  cd "$srcdir"
  bsdtar -x -f "greenfoot-dist.jar"
  mkdir -p "$pkgdir"/usr/share/{java,greenfoot}
  cp -r lib "$pkgdir/usr/share/java/greenfoot/"
  cp -r scenarios "$pkgdir/usr/share/greenfoot/"
  cp -r doc "$pkgdir/usr/share/greenfoot/"
  install -Dm644 greenfoot.desktop "$pkgdir/usr/share/applications/greenfoot.desktop"
  install -Dm755 greenfoot "$pkgdir/usr/bin/greenfoot"
}
