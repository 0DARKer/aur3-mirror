# Maintainer: XZS <d.f.fischer at web dot de>
pkgname=thunderbird-extension-gnome-integration
pkgver=0.4.3
pkgrel=1
pkgdesc='Provides better integration with Gnome using standard notifications and other features'
url='http://davidmartinez.net/proyectos/gnomeintegration'
arch=('any')
license=('GPLv3')
depends=('thunderbird' 'libnotify')
source=("https://addons.mozilla.org/thunderbird/downloads/file/135268/gnome_integration-${pkgver}-tb-linux.xpi")
md5sums=('b6832dd3d47a0d19734923f3c5e21a33')

build() {
  cd "$srcdir"
  [[ -d $pkgname-extract ]] || mkdir $pkgname-extract
  unzip -q -d $pkgname-extract gnome_integration-${pkgver}-tb-linux.xpi
}

package() {
  cd "$srcdir/$pkgname-extract"
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  local dstdir=$pkgdir/usr/lib/thunderbird/extensions/$emid
  install -d $dstdir
  mv * $dstdir
}
