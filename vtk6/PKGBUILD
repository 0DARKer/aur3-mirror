# Maintainer: eolianoe <eolianoe@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Christofer Bertonha <christoferbertonha at gmail dot com>
# Contributor: leepesjee <lpeschier at xs4all dot nl>
# Contributor: Olivier Medoc
# Contributor: ignotus
# Contributor: Fabian Moser
# Contributor: djscholl
# Contributor: Christian BÃ¼hler <christian.buehler@ipoplan.de>

# Based on vtk PKGBUILD in community and on the debian package
# Qt5 support status: http://vtk.org/Bug/view.php?id=12945

pkgname=vtk6
pkgver=6.1.0
pkgrel=3
pkgdesc='A software system for 3D computer graphics, image processing, and visualization.'
arch=('i686' 'x86_64')
url='http://www.vtk.org/'
license=('BSD')
conflicts=('vtk')
provides=('vtk')
depends=('libjpeg-turbo' 'boost' 'ffmpeg' 'qt4' 'lesstif' 'mariadb' 'hdf5' 'libgl')
makedepends=('cmake' 'java-environment' 'doxygen' 'gnuplot' 'python2' 'tk')
optdepends=('python2: python bindings'
            'java-runtime: java bindings'
            'tk: tcl bindings'
            'gnuplot: plotting tools'
            'graphviz: drawing tools')
source=("http://www.vtk.org/files/release/${pkgver:0:3}/VTK-${pkgver}.tar.gz"
        "http://www.vtk.org/files/release/${pkgver:0:3}/VTKData-${pkgver}.tar.gz"
        "http://www.vtk.org/files/release/${pkgver:0:3}/VTKLargeData-${pkgver}.tar.gz")
md5sums=('25e4dfb3bad778722dcaec80cd5dab7d'
         'f82c8fe151279c0422ab8a6cfe991c94'
         '5bc47885923ef30e3ac77fcdf6d4e599')


prepare(){
  # Fix for Mesa 10.3.0
  sed -i 's/\/\/#define\ GLX_GLXEXT_LEGACY/#define\ GLX_GLXEXT_LEGACY/g' ${srcdir}/VTK-${pkgver}/Rendering/OpenGL/vtkXOpenGLRenderWindow.cxx
}

build() {
  cd "${srcdir}"

  rm -rf build
  mkdir -p build
  cd build
  
  # flags to enable using system libs
  local cmake_system_flags=""
  for lib in HDF5 EXPAT FREETYPE JPEG PNG TIFF ZLIB LIBXML2 OGGTHEORA; do
    cmake_system_flags+="-DVTK_USE_SYSTEM_${lib}:BOOL=ON "
  done

  # flags to use python2 instead of python which is 3.x.x on archlinux
  local cmake_system_python_flags="-DPYTHON_EXECUTABLE:PATH=/usr/bin/python2 -DPYTHON_INCLUDE_DIR:PATH=/usr/include/python2.7 -DPYTHON_LIBRARY:PATH=/usr/lib/libpython2.7.so"

  # export JAVA_HOME to help FindJava.cmake and FindJNI.cmake
  export JAVA_HOME=/usr/lib/jvm/default

  cmake  \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DVTK_DATA_ROOT:FILEPATH="${srcdir}/VTKData$pkgver" \
    -DBUILD_DOCUMENTATION:BOOL=ON \
    -DDOCUMENTATION_HTML_HELP:BOOL=ON \
    -DDOCUMENTATION_HTML_TARZ:BOOL=ON \
    -DBUILD_EXAMPLES:BOOL=ON \
    -DVTK_USE_CHEMISTRY:BOOL=ON \
    -DVTK_USE_HYBRID:BOOL=ON \
    -DVTK_USE_PARALLEL:BOOL=ON \
    -DVTK_USE_PATENTED:BOOL=ON \
    -DVTK_USE_BOOST:BOOL=ON \
    -DVTK_USE_INFOVIS:BOOL=ON \
    -DVTK_USE_GL2PS:BOOL=ON \
    -DVTK_USE_MYSQL:BOOL=ON \
    -DVTK_USE_FFMPEG_ENCODER:BOOL=ON \
    -DVTK_USE_TEXT_ANALYSIS:BOOL=ON \
    -DVTK_WRAP_JAVA:BOOL=ON \
    -DVTK_WRAP_PYTHON:BOOL=ON \
    -DVTK_WRAP_TCL:BOOL=ON \
    -DVTK_USE_QT:BOOL=ON \
    -DVTK_Group_Imaging=ON \
    -DVTK_Group_MPI=ON \
    -DVTK_Group_Qt=ON \
    -DVTK_Group_Tk=ON \
    -DVTK_Group_Views=ON \
    -DVTK_Group_Rendering:BOOL=ON \
    -DVTK_Group_StandAlone:BOOL=ON \
    -DVTK_Group_Tk:BOOL=ON \
    -DVTK_Group_Views:BOOL=ON \
    -DVTK_Group_Web:BOOL=ON \
    -DVTK_QT_VERSION:STRING=4 \
    -DVTK_INSTALL_QT_PLUGIN_DIR:STRING="/usr/lib/qt4/plugins" \
    -DVTK_USE_GUISUPPORT:BOOL=ON \
    -DCMAKE_CXX_FLAGS="-D__STDC_CONSTANT_MACROS" \
    ${cmake_system_flags} \
    ${cmake_system_python_flags} \
    "${srcdir}/VTK-$pkgver"

  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install

  # Move the vtk.jar to the arch-specific location
  install -dv "${pkgdir}/usr/share/java/vtk"
  mv -v "${pkgdir}/usr/lib/vtk.jar" \
    "${pkgdir}/usr/share/java/vtk"
  rm -rf "${pkgdir}/usr/lib/vtk.jar"

  # Install license
  install -dv "${pkgdir}/usr/share/licenses/vtk"
  install -m644 "${srcdir}/VTK-$pkgver/Copyright.txt" \
    "${pkgdir}/usr/share/licenses/vtk"
}



