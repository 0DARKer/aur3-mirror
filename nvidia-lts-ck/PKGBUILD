# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributer: Eric Belanger <eric@archlinux.org>

pkgname=nvidia-lts-ck
pkgver=270.30
_kernver='2.6.32-lts-ck'
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26-lts-ck"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-lts-ck>=2.6.32' 'kernel26-lts-ck<2.6.33' "nvidia-utils=$pkgver")
makedepends=('kernel26-lts-ck-headers>=2.6.32' 'kernel26-lts-ck-headers<2.6.33')
provides=('nvidia')
license=('custom')
options=(!strip)
install=nvidia-lts-ck.install
if [ "$CARCH" = "i686" ]; then
  _arch='x86'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
  source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('ff319212b9ac52d645df31027c31ee45')
elif [ "$CARCH" = "x86_64" ]; then
  _arch='x86_64'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
  source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('2fa08295aa0d0ea21646bdc9630c5116')
fi

build() {
  cd "${srcdir}"
  sh ${_pkg}.run --extract-only
  cd ${_pkg}/kernel
  make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
    "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
  install -d -m755 "${pkgdir}/etc/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-lts_ck.conf"
  install -D -m644 "${srcdir}/${_pkg}/LICENSE" "${pkgdir}/usr/share/licenses/nvidia-lts-ck/LICENSE"
  gzip -9 $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
}
