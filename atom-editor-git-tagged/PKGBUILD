# Maintainer: Matthew Wardrop <mister.wardrop@gmail.com>
# Upstream URL: https://github.com/atom/atom
# Based on atom-editor-git by John Reese <john@noswap.com>

pkgname=atom-editor-git-tagged
pkgver=20140814
pkgrel=1
pkgdesc="Chrome-based text editor from Github (builds latest tagged version from github; always up to date)"
arch=('x86_64' 'i686')
url="https://github.com/atom/atom"
license=('MIT')
provides=('atom-editor')
conflicts=('atom-editor')
depends=('alsa-lib' 'gconf' 'gtk2' 'libatomic_ops' 'libgcrypt' 'libgnome-keyring' 'libnotify' 'libxtst' 'nodejs' 'nss' 'python2')
makedepends=('git')

_gitroot="git://github.com/atom/atom"
_gitname="atom"

source=("$_gitroot" 'atom-python.patch')
sha256sums=('SKIP' '76882a35b17444aa475e77c81e360418d97fb3864a0508f2dc214bcf9a3b7e7a')

pkgver() {
  cd "$srcdir/$_gitname"
  git log --tags --simplify-by-decoration --pretty="format:%ai %d" -1 | sed -r 's/^.*tag\: (v[0-9\.]+).*$/\1/'
}

package() {
  cd "$srcdir/$_gitname"

  export PYTHON=python2
  export INSTALL_PREFIX="$pkgdir/usr"
  
  git checkout $pkgver

  script/build --install-dir=$pkgdir/usr
  script/grunt install

  patch "$pkgdir/usr/bin/atom" < "$srcdir/atom-python.patch"

  sed -e "s/<%= description %>/$pkgdesc/" \
    -e "s|<%= installDir %>/share/atom/atom|/usr/bin/atom|"\
    -e "s|<%= iconName %>|atom|"\
    resources/linux/Atom.desktop.in > resources/linux/Atom.desktop
  install -Dm644 resources/linux/Atom.desktop "$pkgdir/usr/share/applications/Atom.desktop"

  install -Dm644 resources/atom.png "$pkgdir/usr/share/pixmaps/atom.png"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
