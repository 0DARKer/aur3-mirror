# Maintainer: Raziel23 <venom23 at runbox dot com>

pkgname=vcmi-git
pkgver=r4027.ec7e587
pkgrel=1
pkgdesc="Heroes of Might and Magic 3 game engine"
arch=('i686' 'x86_64')
url="http://forum.vcmi.eu/portal.php"
license=('GPL')
depends=('boost-libs' 'ffmpeg' 'sdl2_image' 'sdl2_mixer' 'sdl2_ttf' 'qt5-base' 'libxkbcommon-x11'
         'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme')
makedepends=('boost' 'cmake' 'git')
optdepends=('innoextract: required by vcmibuilder'
            'unshield: required by vcmibuilder'
            'unzip: required by vcmibuilder'
            'wget: required by vcmibuilder')
provides=('vcmi')
conflicts=('vcmi' 'vcmi-svn')
replaces=('vcmi-svn')
install='vcmi-git.install'
source=("$pkgname::git+https://github.com/vcmi/vcmi.git#branch=develop")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir"

  if [[ -d "$pkgname-build" ]]; then
    msg 'Removing the last build...'
    rm -rf "$pkgname-build"
  fi

  mkdir "$pkgname-build"
}

build() {
  cd "$srcdir/$pkgname-build"

  cmake "../$pkgname" \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_SKIP_RPATH='OFF' \
    -DCMAKE_BUILD_TYPE='Debug'
  MAKEFLAGS='-j1' make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
