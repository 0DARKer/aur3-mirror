# Contributor:grimi <grimi at poczta dot fm>
# Contributor:Sebastian Wolf <fatmike303 at gmail dot com>
# Contributor: Lobotomius <lobotomius at gmail dot com>
# Last modified: 06/06/2013 17:28:23 +0100 PKGBUILD

pkgname=vice-svn
pkgver=27413
pkgrel=2
pkgdesc="A Versatile Commodore Emulator, development version, GTK3 and FFMPEG enabled"
arch=('i686' 'x86_64')
license=('GPL')
url="http://vice-emu.sourceforge.net"
depends=('vte3' 'giflib' 'ffmpeg')
makedepends=('xorg-bdftopcf' 'xorg-mkfontdir' 'subversion')
provides=("vice=$pkgver")
conflicts=('vice' 'vice-gnomeui' 'vice-gtkglext' 'vice-sdl')
source=('svn+https://vice-emu.svn.sourceforge.net/svnroot/vice-emu/trunk' 'filechooser.c.patch' 'vice.texi.patch')
md5sums=('SKIP'
         '2a0f30763044e6e686518dc5f06ab1d0'
         '85bd35e9b07bc83d5d046dd4a1de7032')

pkgver() {
	cd trunk; svnversion | tr -d [A-z]
}

build() {
	cd "$srcdir/trunk/vice"
	patch -p1 src/arch/unix/x11/gnome/uifilechooser.c < "$srcdir/filechooser.c.patch"
	patch -p1 doc/vice.texi < "$srcdir/vice.texi.patch"

	./autogen.sh
	./configure \
		--enable-arch=unix \
		--prefix=/usr \
		--enable-fullscreen \
		--without-oss \
		--without-arts \
		--with-png \
		--without-x \
		--enable-ffmpeg \
		--enable-gnomeui3

	make
}

package() {
	cd "$srcdir/trunk/vice"
	make DESTDIR="$pkgdir" install
}

