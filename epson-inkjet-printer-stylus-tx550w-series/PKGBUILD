# Contributor: Andre Klitzing <andre () incubo () de>

pkgname=epson-inkjet-printer-stylus-tx550w-series
_pkgname_filter=epson-inkjet-printer-filter
pkgver=1.0.0
pkgrel=2
pkgdesc="Linux printer driver for Epson (SX510W, SX515W, TX550W, see: avasys url)"
arch=('i686' 'x86_64')
url="http://avasys.jp/eng/linux_driver/download/lsb/epson-inkjet/escp/"
license=('GPL' 'LGPL' 'custom:AVASYS Public Licence')
depends=('cups')
makedepends=('rpmextract' 'libtool')
if [ "$CARCH" = "x86_64" ]; then
   source=(http://linux.avasys.jp/drivers/lsb/epson-inkjet/stable/SRPMS/$pkgname-$pkgver-1lsb3.2.src.rpm
           http://linux.avasys.jp/drivers/lsb/epson-inkjet/stable/RPMS/x86_64/$pkgname-$pkgver-1lsb3.2.x86_64.rpm)
   md5sums=('4d38415c92bc7d90efd27ac3632a3b8d'
            'c3fd26b20e285431ed252c58744b9aa0')
else
   source=(http://linux.avasys.jp/drivers/lsb/epson-inkjet/stable/SRPMS/$pkgname-$pkgver-1lsb3.2.src.rpm)
   md5sums=('4d38415c92bc7d90efd27ac3632a3b8d')
fi

build() {
  cd "$startdir/src"
  rpmextract.sh $pkgname-$pkgver-1lsb3.2.src.rpm || return 1
  tar xzf $pkgname-$pkgver.tar.gz || return 1
  tar xzf $_pkgname_filter-$pkgver.tar.gz || return 1
  if [ "$CARCH" = "x86_64" ]; then
     rpmextract.sh $pkgname-$pkgver-1lsb3.2.x86_64.rpm || return 1
  fi

  cd $_pkgname_filter-$pkgver
  aclocal
  libtoolize
  chmod +x configure
  # if you have runtime problems: add "--enable-debug" and look into /tmp/epson-inkjet-printer-filter.txt
  ./configure LDFLAGS="$LDFLAGS -Wl,--no-as-needed" --prefix=/opt/$pkgname || return 1
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "$pkgdir/opt/$pkgname/"
  if [ "$CARCH" = "x86_64" ]; then
     cp -r $srcdir/opt/$pkgname/lib64/ "$pkgdir/opt/$pkgname/"
  else
     cp -r lib "$pkgdir/opt/$pkgname/"
  fi
  cp -r resource "$pkgdir/opt/$pkgname/"
  cp -r watermark "$pkgdir/opt/$pkgname/"
  install -d "$pkgdir/usr/share/cups/model/$pkgname"
  install -m 644 ppds/* "$pkgdir/usr/share/cups/model/$pkgname"

  cd "$srcdir/$_pkgname_filter-$pkgver"
  install -d "$pkgdir/opt/$pkgname/cups/lib/filter/"
  install -m 755 src/epson_inkjet_printer_filter "$pkgdir/opt/$pkgname/cups/lib/filter/epson_inkjet_printer_filter"
}
