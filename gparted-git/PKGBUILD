# Maintainer: Keshav P R <skodabenz at rocketmail dot com>

pkgname=gparted-git
pkgver=20110611
pkgrel=1
pkgdesc="GNOME Partition Manager, frontend to GNU Parted - GIT Version"
arch=('i686' 'x86_64')
url="http://gparted.sourceforge.net"
license=('GPL')
depends=('parted' 'gtkmm' 'libsigc++' 'gksu')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.4' 'gnome-common')
optdepends=('e2fsprogs: for ext2/ext3 partitions'
			'dosfstools: for fat16/32 partitions'
			'jfsutils: for jfs partitions'
			'ntfsprogs: for ntfs partitions'
			'reiserfsprogs: for reiserfs partitions'
			'hfsprogs: for hfs+ partitions'
			'util-linux-ng: for swap partitions'
			'xfsprogs: for xfs partitions'
			'gksu: to run gparted directly from menu')
install=gparted-git.install
conflicts=('gparted')
provides=('gparted')
source=()
md5sums=()

_gitroot="git://git.gnome.org/gparted"
_gitname="gparted"

update_git() {
	
	cd "${srcdir}"
	
	msg "Connecting to GIT server...."
	
	if [ -d "${srcdir}/${_gitname}/" ]
	then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout master
		git merge remotes/origin/master
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
	fi
	
	msg "GIT checkout done or server timeout"
	
}

build() {
	
	update_git
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	cd "${srcdir}/${_gitname}_build/"
	
	mkdir -p "${srcdir}/${_gitname}_build/BUILD"
	cd "${srcdir}/${_gitname}_build/BUILD"
	
	CFLAGS= ../autogen.sh --prefix=/usr --enable-libparted-dmraid
	echo
	
	msg "Starting make..."
	make
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build/BUILD"
	
	make DESTDIR="${pkgdir}" install
	echo
	
	sed -i -e "s:Exec= /usr/sbin:Exec=gksu /usr/sbin:g" "${pkgdir}/usr/share/applications/gparted.desktop"
	
}
