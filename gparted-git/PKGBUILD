# Maintainer : Keshav P R <(skodabenz) (aatt) (rocketmail) (ddoott) (ccoomm)>

_pkgname="gparted"
pkgname="${_pkgname}-git"

pkgver=20111001
pkgrel=1
pkgdesc="GNOME Partition Manager, frontend to GNU Parted - GIT Version"
arch=('i686' 'x86_64')
url="http://gparted.sourceforge.net"
license=('GPL')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.4' 'gnome-common')
depends=('util-linux' 'parted' 'gtkmm' 'libsigc++' 'gksu' 'hicolor-icon-theme')
optdepends=('e2fsprogs: for ext2/ext3 partitions'
			'dosfstools: for fat16/32 partitions'
			'jfsutils: for jfs partitions'
			'ntfsprogs: for ntfs partitions'
			'reiserfsprogs: for reiserfs partitions'
			'hfsprogs: for hfs+ partitions'
			'xfsprogs: for xfs partitions')
install="${pkgname}.install"
conflicts=("${_pkgname}")
provides=("${_pkgname}")
source=()
md5sums=()

_gitroot="git://git.gnome.org/${_pkgname}"
_gitname="${_pkgname}"

_update_git() {
	
	cd "${srcdir}"
	
	msg "Connecting to GIT server...."
	
	if [ -d "${srcdir}/${_gitname}/" ]
	then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout master
		git merge remotes/origin/master
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
		msg "GIT checkout done or server timeout"
	fi
	
	echo
	
}

build() {
	
	_update_git
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build/"
	
	mkdir -p "${srcdir}/${_gitname}_build/BUILD"
	cd "${srcdir}/${_gitname}_build/BUILD"
	
	CFLAGS="" LDFLAGS="" ../autogen.sh --prefix=/usr --enable-libparted-dmraid
	echo
	
	msg "Starting make..."
	CFLAGS="" LDFLAGS="" make
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build/BUILD"
	
	make DESTDIR="${pkgdir}/" install
	echo
	
	sed -e "s:Exec= /usr/sbin:Exec=gksudo /usr/sbin:g" -i "${pkgdir}/usr/share/applications/gparted.desktop"
	
}
