# Maintainer : Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

_pkgname="gparted"
pkgname="${_pkgname}-git"

pkgver=20120304
pkgrel=1
pkgdesc="GNOME Partition Manager, frontend to GNU Parted - GIT Version"
arch=('i686' 'x86_64')
url="http://gparted.sourceforge.net"
license=('GPL')

makedepends=('intltool' 'pkg-config' 'gnome-doc-utils>=0.20.4' 'gnome-common')
depends=('util-linux' 'parted' 'gtkmm' 'libsigc++' 'hicolor-icon-theme')
optdepends=('e2fsprogs: for ext2/ext3 partitions'
            'dosfstools: for fat16/32 partitions'
            'jfsutils: for jfs partitions'
            'ntfsprogs: for ntfs partitions'
            'reiserfsprogs: for reiserfs partitions'
            'hfsprogs: for hfs+ partitions'
            'xfsprogs: for xfs partitions'
            'polkit: to run gparted directly from menu'
            'gpart: for recovering corrupt partition tables'
            'mtools: for manipulating FAT fs image files')

install="${pkgname}.install"
conflicts=("${_pkgname}")
provides=("${_pkgname}")

source=('org.archlinux.pkexec.gparted.policy'
        'gparted-pkexec.in')

sha256sums=('5e0d15ae1c182dfc21c5a6a4cd5ebc010311c6c46d8bc3d67d20d55253840fe2'
            '868ad1960ee496ddf2f854a0609ee062536fa222d2f987a7f96cc4d9d6317ac3')

_gitroot="git://git.gnome.org/${_pkgname}"
_gitname="${_pkgname}"
_gitbranch="master"

_update_git() {
	
	cd "${srcdir}/"
	
	msg "Connecting to GIT server...."
	
	if [[ -d "${srcdir}/${_gitname}/" ]]; then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout "${_gitbranch}"
		git merge "remotes/origin/${_gitbranch}"
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
		cd "${srcdir}/${_gitname}/"
		git checkout "${_gitbranch}"
		msg "GIT checkout done or server timeout"
	fi
	
	echo
	
}

build() {
	
	_update_git
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build/"
	
	git reset --hard
	echo
	
	mkdir -p "${srcdir}/${_gitname}_build/BUILD"
	cd "${srcdir}/${_gitname}_build/BUILD"
	
	CFLAGS="" LDFLAGS="" "${srcdir}/${_gitname}_build/autogen.sh" --prefix="/usr" --enable-libparted-dmraid
	echo
	
	msg "Starting make..."
	CFLAGS="" LDFLAGS="" make
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build/BUILD"
	
	make DESTDIR="${pkgdir}/" install
	echo
	
	## Install policy file
	install -D -m0644 "${srcdir}/org.archlinux.pkexec.gparted.policy" "${pkgdir}/usr/share/polkit-1/actions/org.archlinux.pkexec.gparted.policy"
	
	## Install launcher script
	install -d "${pkgdir}/usr/bin/"
	install -D -m0755 "${srcdir}/gparted-pkexec.in" "${pkgdir}/usr/bin/gparted-pkexec"
	
	## Modify desktop file
	sed -e "s/Exec=.*/Exec=gparted-pkexec/" -i "${pkgdir}/usr/share/applications/gparted.desktop"
	
}
