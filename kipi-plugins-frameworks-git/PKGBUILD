# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kipi-plugins-frameworks-git
pkgver=r10275.3691d7b
pkgrel=1
pkgdesc="A collection of plugins extending the KDE graphics and image applications as digiKam, Gwenview, and KPhotoalbum. KF5 Frameworks branch (GIT version)"
url='http://www.digikam.org/sharedlibs'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkdcraw-frameworks-git' 'libkexiv2-frameworks-git' 'libkipi-frameworks-git' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'kparts')
conflicts=('kipi-plugins')
provides=('kipi-plugins')
source=("git://anongit.kde.org/kipi-plugins#branch=frameworks")
sha1sums=('SKIP')
install="kipi-plugins-frameworks-git.install"

pkgver() {
  cd kipi-plugins
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kipi-plugins \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR=${pkgdir} install
}

