# Maintainer: Joshua Stiefer <facedelajunk@gmail.com>

pkgname=beret-git
pkgver=20120505
pkgrel=1
pkgdesc="A 2D puzzle platformer about a telekinetic scientist"
arch=('i686' 'x86_64')
url="http://kiwisauce.com/beret/"
license=('LGPL')
depends=('sdl' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
makedepends=('git')
provides=(beret)
source=("git://gitorious.org/beret/beret.git"
        "beret-game")
md5sums=('SKIP'
         '28e61b1f662efa8fb2f684636b167172')

pkgver() {
  cd "$srcdir/beret"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

prepare() {
  cd "$srcdir/beret"
  sed -i "1,5 s#SDL#SDL/SDL#" *.c
  sed -i "s/-lSDL_mixer/-lSDL_mixer -lm/" Makefile
}

build() {
  cd "$srcdir/beret"
  make
}

package() {
  cd "$srcdir/beret"
  install -D "$srcdir/beret-game" "$pkgdir/usr/bin/beret"
  install -d "$pkgdir/usr/share/beret"
  cp -r {beret,images,music,rooms,sfx,AveriaSans-Regular.ttf} "$pkgdir/usr/share/beret/"
}

# vim:set ts=2 sw=2 et:
