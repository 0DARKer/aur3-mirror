# Maintainer : P. A. LÃ³pez-Valencia <palopezv AT gmail DOT com>

pkgname=gxine-hg
pkgver=2276.a014a1dc37ad
pkgrel=1
pkgdesc="GTK+ frontend for xine. Development version."
arch=('i686' 'x86_64')
url="http://www.xine-project.org/"
license=('GPL')
depends=('xine-lib' 'gtk2' 'js185' 'lirc-utils' 
	 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('autoconf')
makedepends=('libxaw' 'autoconf')
optdepends=('libxaw: Mozilla browser plugin')
options=('!libtool')
install="$pkgname.install"
source=("$pkgname::hg+http://anonscm.debian.org/hg/xine-lib/gxine/")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd "$srcdir/$pkgname"

  ./autogen.sh

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-integration-wizard \
              --with-browser-plugin \
              --with-logo-format=image \
              --with-dbus \
              --with-gudev \
              --with-pic \
              --enable-watchdog \
	      --disable-rpath \
	      --disable-ffjs \
	      --disable-seajs \
              --disable-deprecated \
              --disable-own-playlist-parsers \
              VENDOR_PKG_VERSION="$pkgver-$pkgrel; Arch Linux"

  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir/" install

  # mozilla plugin
  install -d "$pkgdir/usr/lib/mozilla/plugins"
  ln -s /usr/lib/gxine/gxineplugin.so \
    "$pkgdir/usr/lib/mozilla/plugins"
}
