# Maintainer: garion < garion @ mailoo.org >

pkgname=mkvalidator
pkgver=0.4.2
pkgrel=5
pkgdesc="Validator for MKV files"
arch=('armv6h' 'i686' 'x86_64')
url="http://www.matroska.org/downloads/$pkgname.html"
license=('BSD')
depends=('libebml' 'libmatroska')
options=(!makeflags)
source=(http://sourceforge.net/projects/matroska/files/$pkgname/$pkgname-$pkgver.tar.bz2/download?use_mirror=autoselect)
md5sums=('3e40952681941f40e943b328343f6e78')

case $CARCH in
    "armv6h")
        _gcc_linux="gcc_linux_arm" ;;
    "i686")
        _gcc_linux="gcc_linux" ;;
    "x86_64")
        _gcc_linux="gcc_linux_x64" ;;
esac

build(){
    cd $srcdir/$pkgname-$pkgver

    case $CARCH in
        "armv6h")
            #Patch for ARM support by DarkElf
            find ./corec/tools/coremake/ -type f -exec sed -i "s/arm-none-linux-gnueabi-//g" {} \; ;;
    esac

    ./configure
    make -C $pkgname
}

package(){
    cd $srcdir/$pkgname-$pkgver
    install -d $pkgdir/usr/bin/
    install -m755 release/$_gcc_linux/$pkgname   $pkgdir/usr/bin/
}
