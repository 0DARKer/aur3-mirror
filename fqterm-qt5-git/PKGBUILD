# Contributor: Iru Dog<mytbk920423 at gmail.com>
pkgname=fqterm-qt5-git
pkgver=0.9.8.3.r1.46.gad79104
pkgrel=1
pkgdesc='FQTerm, a Telnet-based BBS client, ported to Qt5'
arch=('i686' 'x86_64')
url='https://github.com/mytbk/fqterm'
license=('GPL2')
depends=('qt5-script' 'qt5-x11extras' 'qt5-multimedia' 'qt5-tools')
makedepends=('git' 'cmake')
provides=('fqterm')
conflicts=('fqterm')
source=('git+https://github.com/mytbk/fqterm.git#branch=Qt5')
md5sums=('SKIP')

pkgver(){
	cd "${srcdir}/fqterm"
	git describe --always | tr '-' '.'
}

build() {
  mkdir -p "${srcdir}/build"
  cd "${srcdir}/build"

  sed -i "s#0.9.8#0.9.8 ($(pacman -Q qt5-base), Arch Linux ${CARCH})#g" \
      ../fqterm/res/userconf/fqterm.cfg.orig
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_C_FLAGS="${CFLAGS}" \
      -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
      ../fqterm
  make
}

package(){
	cd "${srcdir}/build"
  	make DESTDIR="$pkgdir/" install
}

