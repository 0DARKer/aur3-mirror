# Maintainer: Phil Schaf <flying-sheep@web.de>

pkgname=kdevelop-python
pkgver=1.3.81b
pkgrel=1
pkgdesc='Python plugin for KDevelop'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/devtools/plugins/kdev-python"
license=('GPL')
groups=('kde' 'kdevelop-plugins')
depends=('kdevelop')
makedepends=('kdevelop-pg-qt' 'cmake' 'automoc4')
replaces=('kdevelop-extra-plugins-python-git')
source=("http://download.kde.org/unstable/kdevelop/kdev-python/${pkgver%b}/src/kdev-python-v$pkgver.tar.bz2")
sha1sums=('6c299a0acfff23984c6b6a131b5bbc54e0d5a6c6')

build() {
	cd "$srcdir/kdev-python-v$pkgver"
	sed -i 's|#!.*python$|&2|' python-src/Parser/asdl_c.py
	
	mkdir -p "$srcdir/build"
	cd "$srcdir/build"
	
	cmake "../kdev-python-v$pkgver" \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_BUILD_TYPE=RELWITHDEBINFO \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	
	make parser
	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR="$pkgdir" install
}
