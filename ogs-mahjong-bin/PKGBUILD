# Maintainer: hickop <hickop@gmail.com>

pkgname=ogs-mahjong-bin
pkgver=1.0.0
pkgrel=1
pkgdesc="Simple mahjong solitaire with 3D and relaxing soundtrack. (binary)"
arch=('i686' 'x86_64')
url="http://www.opengamestudio.org"
license=('GPL')
depends=('freeglut' 'freeimage' 'libvorbis' 'openal' 'xdg-utils')
_arch=32
[ "$CARCH" == 'x86_64' ] && _arch=64
source=("http://ogstudio.googlecode.com/files/ogs-mahjong-$pkgver-linux$_arch.tar.lzma"
        'ogs-mahjong'
        'ogs-mahjong.desktop')
md5sums=('887640fcf9d44593b25f4245295b8d91'
         '756556e00ca27a413f1f439ef7884d50'
         '69854635ca4338f09c8ab6cd5fd24489')
[ "$_arch" == '64' ] && md5sums[0]='5fafdc42eb6cd21cc52091ca762f62ac'

package() {
    install -d $pkgdir/opt/ogs-mahjong/
    cp -r $srcdir/ogs-mahjong-$pkgver-linux$_arch/* $pkgdir/opt/ogs-mahjong/

    # Link required crypto libraries.
    ln -s /usr/lib/libcrypto.so $pkgdir/opt/ogs-mahjong/lib/libcrypto.so.0.9.8
    ln -s /usr/lib/libpcre.so $pkgdir/opt/ogs-mahjong/lib/libpcre.so.3
    ln -s /usr/lib/libssl.so $pkgdir/opt/ogs-mahjong/lib/libssl.so.0.9.8

    # Remove starting script.
    rm $pkgdir/opt/ogs-mahjong/mj

    chmod 755 $pkgdir/opt/ogs-mahjong/lib/mj-bin

    # Install executable, desktop file and icon.
    install -Dm755 $srcdir/ogs-mahjong $pkgdir/usr/bin/ogs-mahjong
    install -Dm644 $srcdir/ogs-mahjong.desktop $pkgdir/usr/share/applications/ogs-mahjong.desktop
    install -Dm644 $srcdir/ogs-mahjong-$pkgver-linux$_arch/res/GLX_icon.png $pkgdir/usr/share/pixmaps/ogs-mahjong.png
}
