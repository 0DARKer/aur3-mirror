# vim:set ts=2 sw=2 et:

pkgname='devilspie2'
pkgver='0.26'
pkgrel=3
pkgdesc='A window matching utility, allowing the user to perform scripted actions on windows as they are created.'
arch=('i686' 'x86_64')
url="http://www.gusnan.se/${pkgname}/"
license=('GPL3')
depends=('lua>=5.1' 'gtk3' 'libwnck3')
options=('!purge')
source=("${url}download/${pkgname}_${pkgver}-src.tar.gz"
        'lua-5.2.patch')
md5sums=('10eb2efcfdea83dbbe202ace7baf375a'
         '33e762e5eda7e106557589fcbeada529')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < ../lua-5.2.patch
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" PREFIX=/usr/ install
  # Install man page
  install -o root -g root -m 644 -D "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  # Install documentation
  install -o root -g root -m 644 -D "README" "${pkgdir}/usr/share/doc/${pkgname}/README"
}

