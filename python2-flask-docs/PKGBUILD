# Maintainer: Hilton Medeiros <medeiros.hilton@gmail.com>

pkgname=python2-flask-docs
_pkgname=Flask
pkgver=0.9
pkgrel=1
pkgdesc="A set of HTML documentation for the Flask microframework"
arch=('any')
url="http://flask.pocoo.org/"
license=('BSD')
options=('docs' '!strip')
makedepends=('python2-sphinx' 'python2-flask' 'python2-distribute')
source=("http://pypi.python.org/packages/source/F/$_pkgname/$_pkgname-$pkgver.tar.gz")
md5sums=('4a89ef2b3ab0f151f781182bd0cc8933')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  sphinx-build2 docs html
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  install -d "$pkgdir/usr/share/doc/"
  cp -rf html "$pkgdir/usr/share/doc/python2-flask"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
