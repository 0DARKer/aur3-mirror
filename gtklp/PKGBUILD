# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=gtklp
pkgver=1.2.8
pkgrel=5
pkgdesc="Tools to use and administrate CUPS"
arch=('i686' 'x86_64')
url="http://gtklp.sourceforge.net/"
license=('GPL')
depends=('gtk2')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.src.tar.gz"
        "$pkgname.desktop"
        "${pkgname}q.desktop")
md5sums=('65c7089140b5f8e2a0b282b3083b78ae'
         '673bbc897e557f90777c6fad3a5b1071'
         'c4b73e5d3fe725bfd09dda0efbca95f0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # libcups 1.5.0 compatibility fix
  sed -i "30i#include <cups/ppd.h>" $pkgname/$pkgname.h

  ./configure --prefix=/usr
  make FORTE_LDFLAGS="$LDFLAGS -lX11"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # desktop file
  install -d "$pkgdir/usr/share/applications"
  install -Dm644 ../*.desktop "$pkgdir/usr/share/applications"
}
