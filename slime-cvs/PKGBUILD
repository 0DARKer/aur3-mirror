# $Id: PKGBUILD 59343 2011-11-23 20:07:48Z andrea $
# Maintainer:
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Johannes Weiner <hannes@saeurebad.de>
# Contributor: Daniel Leidisch <spam@leidisch.net>

pkgname=slime-cvs
pkgver=2011_11_23
pkgrel=1
arch=('any') 
pkgdesc="The Superior Lisp Interaction Mode for Emacs"
url="http://common-lisp.net/project/slime/" 
depends=('emacs')
makedepends=('cvs' 'awk' 'texlive-core') 
optdepends=('awk: for recreating the documentation' 
  'texlive-core: for recreating the documentation') 
license=('custom') 
conflicts=('slime')
provides=('slime') 
install='slime-cvs.install'
source=('licenses') 
md5sums=('1cdfb69afc10f0d0b690884591678081')

_cvsroot="common-lisp.net:/project/slime/cvsroot"
_cvsmod="slime"

build() {
  cd "$srcdir"
  msg "Connecting to $_cvsmod.sourceforge.net CVS server...."

  if [[ -d "$_cvsmod/CVS" ]]; then
    cd "$_cvsmod"
    cvs -z3 update -d
  else
    cvs -z3 -d "$_cvsroot" co -D "$pkgver" -f "$_cvsmod"
    cd "$_cvsmod"
  fi

  msg "CVS checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_cvsmod-build"
  cp -r "$srcdir/$_cvsmod" "$srcdir/$_cvsmod-build"
  cd "$srcdir/$_cvsmod-build"

  #
  # BUILD HERE
  #
  cd doc
  make
}

package() {
  cd "$srcdir/$_cvsmod-build"
  install -d $pkgdir/usr/share/emacs/site-lisp/slime
  cp -r "$srcdir/$_cvsmod-build"/* \
    $pkgdir/usr/share/emacs/site-lisp/slime

  install -d $pkgdir/usr/share/common-lisp/systems
  ln -s /usr/share/emacs/site-lisp/slime/swank.asd \
    $pkgdir/usr/share/common-lisp/systems/

  find $pkgdir -iname "*cvs*" | xargs rm -rf
  
  install -D -m644 $srcdir/licenses \
    $pkgdir/usr/share/licenses/$pkgname/licenses 
 
  cd "$srcdir/$_cvsmod-build"/doc
  make infodir=$pkgdir/usr/share/info install

  rm $pkgdir/usr/share/info/dir
  install -Dm644 slime-small.pdf $pkgdir/usr/share/doc/slime/slime.pdf 
  install -Dm644 slime-refcard.pdf \
    $pkgdir/usr/share/doc/slime/slime-refcard.pdf 
}
