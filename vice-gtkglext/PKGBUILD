# Contributor: grimi <grimi at poczta dot fm>

pkgname=vice-gtkglext
_pkgname=vice
pkgver=2.4
pkgrel=3
pkgdesc="A Versatile Commodore Emulator , with enabled gtkglext"
arch=('i686' 'x86_64')
license=('GPL')
url="http://vice-emu.sourceforge.net"
depends=('alsa-lib' 'gtkglext' 'giflib' 'libjpeg' 'libpng' 'libxrandr' 'vte' 'lame' 'libpcap' 'libnet')
makedepends=('pkg-config')
provides=("vice=$pkgver")
conflicts=('vice' 'vice-gnomeui' 'vice-sdl' 'vice-ethernet')
replaces=('vice')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/project/vice-emu/releases/${_pkgname}-${pkgver}.tar.gz
        vice-no-fc-cache.patch vice-notexi-notxt.patch vice-x11video.patch)
md5sums=('b017647a0c159bbe43cdb81762d1c577'
         'c7ba8ff930bee0e41565f4a1d9594922'
         '877f93db1550ea81386aae7c3b578442'
         '99931efcb779734c9df069e94f0b38a5')


prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 -i ../vice-no-fc-cache.patch
  patch -Np1 -i ../vice-notexi-notxt.patch
  patch -Np1 -i ../vice-x11video.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --enable-gnomeui \
     --enable-ethernet --disable-ffmpeg
  make
}


package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" VICEDIR=/usr/lib/vice install
}

