# Contributor: grimi <grimi at poczta dot fm>

pkgname=vice-gtkglext
_pkgname=vice
pkgver=2.2
pkgrel=2
pkgdesc="A Versatile Commodore Emulator , with enabled gtkglext"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.viceteam.org/"
depends=('alsa-lib' 'gtkglext' 'gcc-libs' 'giflib' 'lame' 'libjpeg' 'libpng' 'libxxf86vm' 'libxrandr')
makedepends=('xorg-font-utils' 'pkgconfig')
provides=("vice=$pkgver")
conflicts=('vice' 'vice-gnomeui')
replaces=('vice')
options=('!makeflags')
source=(http://www.zimmers.net/anonftp/pub/cbm/crossplatform/emulators/VICE/${_pkgname}-$pkgver.tar.gz)
md5sums=('6737f540806205384e9129026898b0a1')


build() {
  cd $srcdir/${_pkgname}-$pkgver
  sed 's:png_check_sig:png_sig_cmp:g' -i configure.in  configure || return 1  #fix lack of png icons
  ./configure --prefix=/usr --enable-fullscreen --enable-gnomeui --without-esd
  make
}

package() {
  cd $srcdir/${_pkgname}-$pkgver
  make DESTDIR=$pkgdir install
}

