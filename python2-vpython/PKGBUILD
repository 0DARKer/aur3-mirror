#Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
#Contributor: Juergen Hoetzel <juergen@archlinux.org>
#Contributor: Comete <la_comete@tiscali.fr>

pkgname=python2-vpython
pkgver=6.05
pkgrel=1
pkgdesc="Easy-to-use 3D graphics module for Python."
arch=('i686' 'x86_64')
url="http://www.vpython.org"
depends=('gtkglextmm' 'python2-numpy' 'boost-libs' 'wxpython' 'tk' 'python2-fonttools' 'python2-polygon' 'python2-ttfquery')
makedepends=('boost' 'python2-setuptools')
conflicts=('python-vpython')
replaces=('python-vpython')
license=('custom')
options=(!libtool)

source=("http://sourceforge.net/projects/vpythonwx/files/${pkgver}-release/vpython-wx-src.${pkgver}.tgz"
        boostmt.patch)
md5sums=('0120bb8e28717666051f3985c5572db2'
         'f3c2fe9d8d99d98873dff2b4ab0296c5')

build() {
  cd "${srcdir}"/vpython-wx-${pkgver}

  patch -p1 < "${srcdir}"/boostmt.patch
  python2 setup.py build
}

package() {
  cd "${srcdir}"/vpython-wx-${pkgver}

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  sed -i "s|#![ ]*/usr/bin/env python[^2]|#!/usr/bin/env python2|" ${pkgdir}/usr/lib/python2.7/site-packages/vidle/PyShell.py

  install -D license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

