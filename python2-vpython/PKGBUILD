#Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
#Contributor: Juergen Hoetzel <juergen@archlinux.org>
#Contributor: Comete <la_comete@tiscali.fr>

pkgname=python2-vpython
pkgver=6.10
pkgrel=1
pkgdesc="Easy-to-use 3D graphics module for Python."
arch=('i686' 'x86_64')
url="http://www.vpython.org"
depends=('gtkglextmm' 'python2-numpy' 'boost-libs' 'wxpython' 'tk' 'python2-fonttools' 'python2-polygon' 'python2-ttfquery')
makedepends=('boost' 'python2-setuptools')
conflicts=('python-vpython')
replaces=('python-vpython')
license=('custom')
options=(!libtool)

source=("http://sourceforge.net/projects/vpythonwx/files/${pkgver}-release/vpython-wx-src.${pkgver}.tgz")
md5sums=('4690fa39fbf1a61fbeb9a6e124525435')
noextract=('vpython-wx-src.${pkgver}.tgz')

prepare() {
  cd "${srcdir}"

  rm -rf vpython-wx-${pkgver}
  mkdir vpython-wx-${pkgver}
  cd vpython-wx-${pkgver}
  tar -xzf "${srcdir}"/vpython-wx-src.${pkgver}.tgz

  sed -i "s|#![ ]*/usr/bin/env python[^2]|#!/usr/bin/env python2|" site-packages/vidle2/PyShell.py
}

build() {
  cd "${srcdir}"/vpython-wx-${pkgver}

  python2 setup.py build
}

package() {
  cd "${srcdir}"/vpython-wx-${pkgver}

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

