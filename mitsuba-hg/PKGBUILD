# Maintainer: N30N <archlinux@alunamation.com>
# Contributer: Alex Combas <blenderwell@gmail.com>

pkgname="mitsuba-hg"
pkgver=1231
pkgrel=2
pkgdesc="A physically based renderer."
url="http://mitsuba-renderer.org/"
license=("GPL3")
arch=("i686" "x86_64")
depends=("xerces-c" "glew" "openexr" "qt" "collada-dom" "libxxf86vm")
makedepends=("mercurial" "scons" "boost" "python")
optdepends=("python: for the bindings")
provides=("mitsuba")
source=("mitsuba.install" "rmdeps.patch")
sha1sums=("253e9e45e414bbf0ca2b26aad3d1b5f90fdaa3c9" \
	"3f692f722d68989703d05d4c34239f5cbad5bbe9")
install="mitsuba.install"

_hgroot="https://mitsuba-renderer.org/hg"
_hgrepo="mitsuba"

build() {
	[ -d build ] && rm -rf build
	cp -r ${_hgrepo} build
	cd build

	cp build/config-linux.py config.py
	cat >> config.py <<-EOF
		CXXFLAGS.remove("-O3")
		CXXFLAGS.remove("-march=nocona")
		CXXFLAGS.extend("${CXXFLAGS}".split())
	EOF

	patch -p0 -i "${srcdir}/rmdeps.patch"
	# scons --jobs=$[${MAKEFLAGS/-j/} - 1]
	scons

	msg "Starting to package..."
	install -d \
		"${pkgdir}/usr/bin" \
		"${pkgdir}/usr/lib" \
		"${pkgdir}/usr/share/mitsuba/plugins"

	install -Dm644 data/linux/mitsuba.desktop \
		${pkgdir}/usr/share/applications/mitsuba.desktop
	install -Dm644 src/mtsgui/resources/mitsuba48.png \
		"${pkgdir}/usr/share/pixmaps/mitsuba48.png"

	find include -type f -exec \
		install -Dm644 "{}" "${pkgdir}/usr/{}" \;

	cd dist
	install -m755 mitsuba mtsgui mtssrv mtsutil "${pkgdir}/usr/bin"
	install -m755 *.so "${pkgdir}/usr/lib"
	install -m755 plugins/* "${pkgdir}/usr/share/mitsuba/plugins"
	install -Dm755 python/mitsuba.so \
		"${pkgdir}/usr/lib/python3.2/lib-dynload/mitsuba.so"

	find data -type f -exec \
		install -Dm644 "{}" "${pkgdir}/usr/share/mitsuba/{}" \;
}

# vim: set noet ff=unix:
