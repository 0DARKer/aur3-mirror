# Contributer: N30N <archlinux@alunamation.com>
# Contributer: Alex Combas <blenderwell@gmail.com>
pkgname="mitsuba-hg"
pkgver=514
pkgrel=1
pkgdesc="A physically based renderer."
url="http://mitsuba-renderer.org/"
license=("GPL3")
arch=("i686" "x86_64")
depends=("xerces-c" "glewmx" "openexr" "boost" "libpng" "libjpeg" "qt" "collada-dom")
makedepends=("mercurial" "scons")
optdepends=('blender-collada-svn: a collada eneabled version of Blender, required for mtsblend addon')
provides=("mitsuba")

_hgroot="https://mitsuba-renderer.org/hg"
_hgrepo="mitsuba"

build() {
	if [ -d build ]; then
		rm -rf build
	fi
	cp -r ${_hgrepo} build
	cd build

	cp config/config-linux.py config.py
	cat >> config.py <<-EOF
		CXXFLAGS.remove("-O3")
		CXXFLAGS.remove("-march=nocona")
		CXXFLAGS.extend("${CXXFLAGS}".split())
		GLLIB.remove("GLEWmx")
		GLLIB.append("GLEW")
		COLLADAINCLUDE = ["/usr/include/colladadom", "/usr/include/colladadom/1.4"]
	EOF

	scons --jobs=$[${MAKEFLAGS/-j/} - 1]
}

package() {
	install -d \
		${pkgdir}/usr/bin \
		${pkgdir}/usr/lib \
		${pkgdir}/usr/share/mitsuba/plugins \
		${pkgdir}/usr/share/mitsuba/schema \
		${pkgdir}/usr/share/applications \
		${pkgdir}/usr/share/pixmaps \
		${pkgdir}/usr/include/mitsuba/{core,hw,render}

	cd build
	install -m755 mitsuba mtsgui mtsimport mtssrv mtsutil ${pkgdir}/usr/bin
	install -m755 src/libcore/libmitsuba-core.so \
		src/libhw/libmitsuba-hw.so \
		src/librender/libmitsuba-render.so \
		${pkgdir}/usr/lib
	install -m755 plugins/* ${pkgdir}/usr/share/mitsuba/plugins
	install -m644 schema/scene.xsd ${pkgdir}/usr/share/mitsuba/schema
	install -m644 data/linux/mitsuba.desktop ${pkgdir}/usr/share/applications
	install -m644 src/qtgui/resources/mitsuba48.png ${pkgdir}/usr/share/pixmaps
	install -m644 include/mitsuba/*.h ${pkgdir}/usr/include/mitsuba
	install -m644 include/mitsuba/core/* ${pkgdir}/usr/include/mitsuba/core
	install -m644 include/mitsuba/hw/* ${pkgdir}/usr/include/mitsuba/hw
	install -m644 include/mitsuba/render/* ${pkgdir}/usr/include/mitsuba/render

	install -m755 -D data/blender/mitsuba/__init__.py ${pkgdir}/usr/share/blender/2.55/scripts/addons/mitsuba/__init__.py
	cp -a data/blender/mitsuba/* ${pkgdir}/usr/share/blender/2.55/scripts/addons/mitsuba
	
}

# vim: set noet ff=unix:
