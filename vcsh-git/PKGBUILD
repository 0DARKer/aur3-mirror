# Maintainer: Chet Gray <chetgray@gmail.com>
# Contributor: joni <kljohann@gmail.com>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>
_pkgname=vcsh
pkgname=$_pkgname-git
pkgver=2013.04.23_11.42_0871cff
pkgrel=1
pkgdesc='manage config files in HOME via fake bare git repositories'
arch=('any')
url='https://github.com/RichiH/vcsh'
license=('GPL2')
depends=('git')
makedepends=('pacman>=4.1' 'git' 'ruby-ronn')
optdepends=('mr: helps manage a large number of repositories')
provides=('vcsh')
conflicts=('vcsh')
source=("$_pkgname::git://github.com/RichiH/vcsh.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(date -u -d "$(git log -n1 --pretty='format:%ci')" '+%Y.%m.%d_%H.%M')_$(git log -n1 --pretty='format:%h')
}

build() {
  cd "$srcdir/$_pkgname"
  make
}

check() {
  cd "$srcdir/$_pkgname"
  make -k test
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
