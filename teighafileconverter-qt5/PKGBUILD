# Maintainer: josephgbr <rafael.f.f1@gmail.com>

# NOTE about libgl dependency (same note applies to its 'lib32-' version)
# if you have and use 'mesa',     install 'mesa-libgl'
# if you have and use 'nvidia',   install 'nvidia-libgl'
# if you have and use 'catalyst', install 'catalyst-libgl' (or catalyst-total)
# p.s.: there is no 'libgl' pkg, instead it is provided by the above pkgs

pkgname=teighafileconverter-qt5
_altname=teighafileconverter
pkgver=4.00.1.0
pkgrel=1
pkgdesc="Converts files between the .dwg and .dxf file formats, QT5 version"
arch=('i686' 'x86_64')
url="http://www.opendesign.com/guestfiles"
license=('custom')
conflicts=('teighafileconverter-qt4')
depends=('qt5-base' 'libxfixes' 'libgl' 'bash' 'hicolor-icon-theme')
install=${pkgname}.install
source_i686=(http://www.opendesign.com/files/guestdownloads/TeighaFileConverter/TeighaFileConverter_QT5_lnxX86_4.7dll.deb)
md5sums_i686=('5a7e3f3d50d6ace899462d834daa1416')  
source_x86_64=(http://www.opendesign.com/files/guestdownloads/TeighaFileConverter/TeighaFileConverter_QT5_lnxX64_4.7dll.deb)
md5sums_x86_64=('9d8e4474de26fc98708430e759d367cd')


build() {
  [ ! -d ${_altname}-${pkgver} ] && mkdir ${_altname}-${pkgver}
  tar xf data.tar.gz -C ${_altname}-${pkgver}
}

package() {
  cd ${_altname}-${pkgver}

  for file in $(ls usr/bin/TeighaFileConverter_${pkgver}/); do
    install -Dm755 usr/bin/TeighaFileConverter_${pkgver}/${file} \
        "${pkgdir}/usr/lib/teighafileconverter/${file}"
  done
  
  # binary
  install -Dm755 usr/bin/TeighaFileConverter \
    "${pkgdir}/usr/bin/teighafileconverter"
  sed -e "s#usr/bin/TeighaFileConverter_${pkgver}#usr/lib/teighafileconverter#" \
    -i "${pkgdir}/usr/bin/${_altname}"
  
  # desktop item
  install -Dm644 usr/share/applications/TeighaFileConverter_${pkgver}.desktop \
    "${pkgdir}/usr/share/applications/teighafileconverter.desktop"
  sed -e "s#/TeighaFileConverter#/teighafileconverter#" \
    -i "${pkgdir}/usr/share/applications/teighafileconverter.desktop"
  
  # hicolor icons
  for icon in $(find usr/share/icons -type f); do
    install -Dm644 ${icon} \
        "${pkgdir}/${icon}"
  done
  
  # copyright
  install -Dm644 usr/share/doc/TeighaFileConverter/copyright \
    "${pkgdir}/usr/share/licenses/${_altname}/COPYRIGHT"
  
}
