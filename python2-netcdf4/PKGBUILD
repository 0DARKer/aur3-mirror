# Maintainer: richli <rich at dranek dot com>
# Contributor: Foucault <foucault.online@_GMAIL_DOT_COM_>
# Contributor: rememberthemer <rememberthemer@_GMAIL_DOT_COM_>

pkgname=python2-netcdf4
pkgver=1.1.6
pkgrel=1
pkgdesc="Python/numpy interface to the netCDF version 4 library."
arch=('x86_64' 'i686')
url="https://unidata.github.io/netcdf4-python/"
license=('MIT')

depends=('python2-numpy' 'netcdf' 'hdf5' 'curl' 'zlib')
makedepends=('python2-setuptools' 'cython2')
optdepends=('python-netcdf4: python 3 version')
source=(https://github.com/Unidata/netcdf4-python/archive/v${pkgver}rel.tar.gz)
md5sums=('d54669d962c95a15b6d789adf65afd2c')

build() {
    cd "$srcdir"/netcdf4-python-${pkgver}rel
    # USE_NCCONFIG=1 python2 setup.py clean --all
    USE_NCCONFIG=1 python2 setup.py build
}

check() {
    cd "$srcdir"/netcdf4-python-${pkgver}rel
    cd "test"
    PYTHONPATH="../build/lib.linux-${CARCH}-2.7" python2 ./run_all.py
}

package() {
    cd "$srcdir"/netcdf4-python-${pkgver}rel

    # Note that this installs the python2 package as well as some scripts in /usr/bin
    USE_NCCONFIG=1 python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize 2 --skip-build

    # We don't need to install the test suite if we already use it in check()
    # mkdir -p "$pkgdir"/usr/share/doc/${pkgname}/test
    # install -m644 test/* -t "$pkgdir"/usr/share/doc/$pkgname/test

    # Install documentation and examples
    mkdir -p "$pkgdir"/usr/share/doc/${pkgname}/{docs,examples}
    install -m644 docs/* -t "$pkgdir"/usr/share/doc/$pkgname/docs
    install -m644 examples/* -t "$pkgdir"/usr/share/doc/$pkgname/examples

    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: set et sw=4 ts=4 sts=4:
