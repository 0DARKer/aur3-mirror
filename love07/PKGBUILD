# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Linus Sj√∂gren <thelinx@unreliablepollution.net>
# Contributor: Eric Forgeot < http://anamnese.online.fr >, dreeze

pkgname=love07
_realpkgver=0.7.0
pkgver=1
pkgrel=1
pkgdesc="An open-source 2D game engine which uses the versatile Lua scripting language to create dynamic gaming experiences"
arch=('i686' 'x86_64')
url="http://love2d.org/"
license=('zlib')
depends=('luajit' 'physfs' 'freetype2' 'devil' 'mpg123' 'openal' 'libvorbis' 'libmodplug' 'sdl' 'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils')
install=$pkgname.install
conflicts=('love')
provides=('love')
source=("https://bitbucket.org/rude/love/downloads/love-${_realpkgver}-linux-src.tar.gz"
        "https://bitbucket.org/rude/love/raw/$_realpkgver/license.txt"
        "https://bitbucket.org/rude/love/raw/$_realpkgver/platform/unix/app.svg"
        "https://bitbucket.org/rude/love/raw/$_realpkgver/platform/unix/game.svg"
        "https://bitbucket.org/rude/love/raw/$_realpkgver/platform/unix/love.desktop"
        "https://bitbucket.org/rude/love/raw/$_realpkgver/platform/unix/love.xml")
md5sums=('a012b0e699794a0e200cd58e783f835b'
         '8eae9186869e16dc0de815af99e5b997'
         'a1e19f91420cc519a683af360f5b1120'
         '16f2ecc899c9ffc8b7b7c807f8967861'
         '8d28b93962a262f72afab5501d13a307'
         'b4f00fb1cb80057a0a371a994100d418')

build() {
  cd $srcdir/love-$_realpkgver
  ./configure --prefix=/usr --with-luajit
  make
}

package() {
  cd $srcdir/love-$_realpkgver

  make DESTDIR=$pkgdir install

  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  mkdir -p $pkgdir/usr/share/icons/hicolor/scalable/mimetypes
  mkdir -p $pkgdir/usr/share/icons/hicolor/scalable/apps
  mkdir -p $pkgdir/usr/share/applications
  mkdir -p $pkgdir/usr/share/mime/packages

  cd ..
  install -Dm644 license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 app.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/love.svg
  install -Dm644 game.svg $pkgdir/usr/share/icons/hicolor/scalable/mimetypes/gnome-mime-application-x-love-game.svg
  install -Dm644 love.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 love.xml $pkgdir/usr/share/mime/packages/love.xml
}