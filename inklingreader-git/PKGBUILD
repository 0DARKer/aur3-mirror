# Maintainer: Simon Schuster $(echo "aur . remove stuff like this . schuster.re" | sed 's/ . remove stuff like this . /@/g')

pkgname=inklingreader-git
_pkgname=inklingreader
pkgver=0.8.r31.g2183ea7
pkgrel=1
pkgdesc="A GNU/Linux-friendly version of the Wacom Inkling SketchManager."
arch=('i686' 'x86_64')
url="https://github.com/roelj/inklingreader"
license=('GPL3')
depends=('librsvg' 'libusb' 'gtk3')
provides=("${_pkgname%-git}")
conflicts=("${_pkgname%-git}")
# gcc, automake, autoconf, make are already part of base devel
makedepends=('git')
source=("git+https://github.com/roelj/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/${pkgname%-git}"

	# Git tags, unannotated, starting with "v"
	printf "%s" "$(git describe --long --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"

	autoreconf -i || exit 1
	./configure --prefix=/usr || exit 1
	make || exit 1
}

package() {
	cd "$srcdir/${pkgname%-git}"

    make DESTDIR="$pkgdir/" install || exit 1
}
