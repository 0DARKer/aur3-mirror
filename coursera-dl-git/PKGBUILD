# Contributor: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=coursera-dl-git
_gitname=coursera
pkgver=360.1c14524
pkgrel=1
pkgdesc="Script for downloading Coursera.org videos and naming them"
arch=('any')
url="https://github.com/jplehmann/coursera/"
license=('GPL')
depends=('python2-beautifulsoup3' 'python2-distribute' 'python2-requests')
makedepends=('git')
source=("git://github.com/jplehmann/$_gitname.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  echo "$(git shortlog | grep -c '\s\+').$(git describe --always)"
}

package() {
  cd "$srcdir/$_gitname"
  sed -i "1s|python|&2|;s|\(from \)\(credentials import\)|\1$_gitname.\2|" $_gitname-dl
  install -dm755 $_gitname "$pkgdir/usr/lib/python2.7/site-packages/$_gitname"
  install -cm644 $_gitname/*.py "$pkgdir/usr/lib/python2.7/site-packages/$_gitname/"
  install -Dm755 $_gitname-dl "$pkgdir/usr/bin/$_gitname-dl"
}

# vim:set ts=2 sw=2 et:
