# Contributor: E Nikulenkov <enikulenkov -- gmail>
# Contributor: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=coursera-dl-git
_gitname=coursera
pkgver=431.7b5916b
pkgrel=1
pkgdesc="Script for downloading Coursera.org videos and naming them"
arch=('any')
url="https://github.com/jplehmann/coursera/"
license=('GPL')
depends=('python2-beautifulsoup3' 'python2-distribute' 'python2-requests')
makedepends=('git')
source=("git://github.com/jplehmann/$_gitname.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  echo "$(git shortlog | grep -c '\s\+').$(git describe --always)"
}

prepare() {
  cd "$srcdir/$_gitname"
  sed -i "s/^ROOT =.*$/ROOT = os.path.join(os.path.expanduser(\"~\"), '.coursera_dl')/" $_gitname/define.py
}

package() {
  cd "$srcdir"
  echo "python2 /usr/lib/python2.7/site-packages/$_gitname/coursera_dl.py \$@" >> "$srcdir/${pkgname%-*}"
  install -Dm755 ${pkgname%-*} "$pkgdir/usr/bin/${pkgname%-*}"

  cd "$srcdir/$_gitname"
  install -dm755 $_gitname "$pkgdir/usr/lib/python2.7/site-packages/$_gitname"
  install -cm644 $_gitname/*.py "$pkgdir/usr/lib/python2.7/site-packages/$_gitname/"
}

# vim:set ts=2 sw=2 et:
