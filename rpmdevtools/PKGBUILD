# Maintainer: mihai303

pkgname=rpmdevtools
pkgver=8.5
pkgrel=1
pkgdesc="RPM Development Tools"
arch=('any')
url="https://fedorahosted.org/releases/r/p/${pkgname}/"
source=(${url}/${pkgname}-${pkgver}.tar.xz)
license=('GPL2')
depends=('python2' 'rpm-org')
makedepends=('help2man')
sha256sums=('e6391c9feb19c68468d590936a8178fdfb23b190f9f038586e8be5062d26f054')

prepare() {
    cd ${pkgname}-${pkgver}

    sed -i 's|/usr/bin/python|/usr/bin/python2|' rpmdev-*
}

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc

    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR=${pkgdir} install
}
