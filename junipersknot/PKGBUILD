# Maintainer: AlexanderR <alexander r at gmx com>

pkgname=junipersknot
pkgver=1.0
pkgrel=1
pkgdesc="Juniper's Knot is game about boy and fiend."
arch=('any')
url="http://junipersknot.dischan.org/"
license=('custom')
depends=('renpy' 'ttf-liberation')
makedepends=('p7zip' 'icoutils')
options=(!strip !zipman)
source=("junipersknot.7z::https://dischan.org/store/download.php?fileID=15"
	junipersknot.desktop
	python2.patch)
md5sums=('203c49639e33d052f919c9eb21753380'
         'a53c66726f0a4b82e6d5661495f3ce17'
         'd00783636657a37728c48b8ce295cc93')

build() {
  cat <<"EOF" > junipersknot
#!/bin/sh
exec env RENPY_BASE=${RENPY_BASE:-/usr/share/renpy/} python2 -OO "/usr/share/junipersknot/Juniper's Knot.py" /usr/share/junipersknot "$@"
EOF

  7z x -y ${pkgname}.7z
  wrestool -x --output=. -t14 Juniper\'s\ Knot.exe

  patch -Np1 -i "$srcdir/python2.patch"
  rm game/Liberation*.ttf
}

package() {
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -m644 README.txt SOUNDCREDITS.txt "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm755 "$srcdir"/${pkgname} "$pkgdir/usr/bin/${pkgname}"
  install -Dm644 "Juniper's Knot.py" "$pkgdir/usr/share/$pkgname/Juniper's Knot.py"
  install -Dm644 DISCHANLICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/DISCHANLICENSE.txt"
  install -Dm644 Juniper\'s\ Knot.exe_14_1_0.ico "$pkgdir/usr/share/pixmaps/${pkgname}.ico"
  install -Dm644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/${pkgname}.desktop"
  cp -r game/* "$pkgdir/usr/share/$pkgname"
}

# vim:set ts=2 sw=2 et:
