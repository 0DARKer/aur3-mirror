# Maintainer: Thomas Dziedzic <gostrc at gmail>
# Based off of extra/dmenu

pkgname=dmenu-file-completion
pkgver=4.2.1
pkgrel=1
pkgdesc='Dynamic X menu with tab file completion patch.'
url='http://www.suckless.org/programs/dmenu.html'
arch=('i686' 'x86_64')
license=('MIT')
depends=('sh' 'libxinerama')
makedepends=('xineramaproto')
provides=('dmenu')
conflicts=('dmenu')
source=("http://dl.suckless.org/tools/dmenu-${pkgver}.tar.gz"
        'dmenu.patch'
        'filecompletion.diff')
md5sums=('5c95f974fa0c723f46838d0d5fbf5aca'
         'd74a6665a19cb0403720f1f3abcd280d'
         '10550346687098106bd26cddf7a3ad2a')

build()
{
  cd dmenu-${pkgver}

  patch -Np1 -i ${srcdir}/dmenu.patch
  patch -Np1 -i ${srcdir}/filecompletion.diff

  make
}

package() {
  cd dmenu-${pkgver}

  make DESTDIR=${pkgdir} install

  #install license
  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/dmenu/LICENSE
}
