# Maintainer: Thomas Dziedzic <gostrc at gmail>
# Based off of extra/dmenu

pkgname=dmenu-file-completion
pkgver=4.3.1
pkgrel=1
pkgdesc='Dynamic X menu with tab file completion patch.'
url='http://www.suckless.org/programs/dmenu.html'
arch=('i686' 'x86_64')
license=('custom:MIT')
depends=('sh' 'libxinerama')
makedepends=('xineramaproto')
provides=('dmenu')
conflicts=('dmenu')
source=("http://dl.suckless.org/tools/dmenu-${pkgver}.tar.gz"
        'dmenu.patch'
        'filecompletion.diff')
md5sums=('6b619f6901bf4765442b4e410b1e5410'
         'd74a6665a19cb0403720f1f3abcd280d'
         '0d857574c2c6d551d428a7d0a06b9c11')

build()
{
  cd dmenu-${pkgver}

  patch -Np1 -i ${srcdir}/dmenu.patch
  patch -Np1 -i ${srcdir}/filecompletion.diff

  make
}

package() {
  cd dmenu-${pkgver}

  make DESTDIR=${pkgdir} install

  #install license
  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/dmenu-file-completion/LICENSE
}
