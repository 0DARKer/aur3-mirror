# Maintainer: Thomas Dziedzic <gostrc at gmail>
# Based off of community/dmenu

pkgname=dmenu-file-completion
pkgver=4.4
pkgrel=1
pkgdesc='Dynamic X menu with tab file completion patch.'
url='http://www.suckless.org/programs/dmenu.html'
arch=('i686' 'x86_64')
license=('custom:MIT')
depends=('sh' 'libxinerama')
makedepends=('xineramaproto')
provides=('dmenu')
conflicts=('dmenu')
source=("http://dl.suckless.org/tools/dmenu-${pkgver}.tar.gz"
        'filecompletion.diff')
md5sums=('ae1902fc37716f739836fddce6123ebc'
         '0d857574c2c6d551d428a7d0a06b9c11')

build()
{
  cd dmenu-${pkgver}

  patch -Np1 -i ${srcdir}/filecompletion.diff

  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11
}

package() {
  cd dmenu-${pkgver}

  make \
    PREFIX=/usr \
    DESTDIR=${pkgdir} \
    install

  #install license
  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/dmenu-file-completion/LICENSE
}
