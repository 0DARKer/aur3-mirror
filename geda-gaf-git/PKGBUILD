# Maintainer: veox

pkgname=geda-gaf-git
_pkgname=geda-gaf
pkgver=20121222
pkgrel=1
pkgdesc="gEDA/gaf suite - Contains gschem, gnetlist, gsymcheck, gattrib, utilities and documentation from the gEDA project (latest git revision)"
arch=('i686' 'x86_64')
url="http://www.geda-project.org/"
license=('GPL')
depends=('gtk2' 'guile' 'libstroke' 'shared-mime-info' 'hicolor-icon-theme')
makedepends=('git' 'autoconf' 'automake' 'pkgconfig' 'perlxml' 'flex' 'gawk')
optdepends=('python2: for two of the commands (garchive, tragesym)'
  'gawk: for sw2asc')
provides=('geda-gaf')
conflicts=('geda-gaf')
install=geda-gaf.install
#source=('')

_gitroot="git://git.geda-project.org/geda-gaf.git"
_gitname="geda-gaf"

build ()
{
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    # has to be cloned entirely, otherwise errors during build
    # (version symbols are retrieved from git information)
    # if you know how to fix this, please do
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  cp -a "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

  cd "${srcdir}/${_gitname}-build"
  ./autogen.sh
  ./configure --prefix=/usr --disable-update-xdg-database

  make
}

package ()
{
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install 
  sed -i 's+/usr/bin/env python+/usr/bin/env python2+' \
    "$pkgdir/usr/bin/garchive"
  sed -i 's+/usr/bin/python+/usr/bin/python2+' \
    "$pkgdir/usr/bin/tragesym"
  sed -i 's+/usr/bin/python+/usr/bin/python2+' \
    "$pkgdir/usr/share/doc/${_pkgname}/examples/lightning_detector/bom"
}
