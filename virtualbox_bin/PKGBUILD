# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: xduugu <xduuguΘgmx·com>
# Contributor: Peter 'piie' Feuerer <peterΘpiie·net>
# Contributor: Sascha Pfau <MrPeacockΘgmail·com>
# Contributor: iggy <iggy.mfΘgmail·com>

pkgname=virtualbox_bin
pkgver=4.0.6
_build=71344
pkgrel=2
pkgdesc='Oracle VM VirtualBox Binary Edition'
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL2')
depends=('libidl2' 'libxcursor' 'libxinerama' 'libxslt' 'curl' 'gcc' 'make' 'kernel26-headers' 'python2')
options=('!strip')
optdepends=('virtualbox-ext-oracle: for Oracle extensions'
            'qt: for GUI support'
            'sdl: for VBoxSDL and GUI support'
            'mesa: for OpenGL support'
            'libgl: for shared OpenGL support'
            'libxt: for shared clipboard support'
            'alsa-lib: for ALSA support'
            'pulseaudio: for PulseAudio support')
provides=("virtualbox=${pkgver}")
conflicts=('virtualbox' 'virtualbox-ose' 'virtualbox-modules')
backup=('etc/vbox/vbox.cfg' 'etc/conf.d/vboxdrv' 'etc/conf.d/vboxweb')
install='install'
_arch='x86'
[ "${CARCH}" = 'x86_64' ] && _arch='amd64'
source=(
  "VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run::http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run"
  '10-vboxdrv.rules'
  'vboxdrv.rc'
  'vboxdrv.conf'
  'vboxweb.rc'
  'vboxweb.conf'
)
md5sums=('133cfbb8dc11da780b24f1ef5ef06697'
         '98859bfca9ef2ebf2ea43eb9123316fc'
         '55f7acbeb37c0ed2b4fe0452b84b4eb0'
         '2d5a0c5275bf660da9f30f75820b6078'
         '9d5f1eeea43f23d4696e3568671a4c4d'
         '3ac185709bfe688bb753c46e170d0546')
[[ "${CARCH}" = 'i686' ]] && md5sums[0]='45414b45e5b43e95068b1f3fec71de76'

build() {
  # Check and unpack the run package via sh(1)
  sh "VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run" --check
  echo yes | sh "VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run" --target "${srcdir}" \
    --nox11 --noexec &> /dev/null

  # Unpack bundled files
  install -d "${pkgdir}/opt/VirtualBox"
  cd "${pkgdir}/opt/VirtualBox"
  tar -xjf "${srcdir}/VirtualBox.tar.bz2"

  # Hardened build: Mark binaries suid root, create symlinks for working around
  #                 unsupported $ORIGIN/.. in VBoxC.so and make sure the
  #                 directory is only writable by the user (paranoid).
  chmod 4511 VirtualBox VBox{SDL,Headless,NetDHCP}
  for _lib in VBox{VMM,REM,RT,DDU,XPCOM}.so; do
    ln -sf "/opt/VirtualBox/${_lib}" "components/${_lib}"
  done
  chmod go-w .

  # VBoxNetAdpCtl needs to be suid root in any case
  chmod 4511 VBoxNetAdpCtl

  # Replace VirtualBox built-in Qt by system Qt libraries (disabled as of
  # 2010-03-26, 3.1.6-1)
  #for _lib in libQt{Core,Gui,Network,OpenGL}; do
  #  rm "${_lib}VBox.so.4"
  #  ln -s "/usr/lib/${_lib}.so.4" "${_lib}VBox.so.4"
  #done

  # Patch "vboxshell.py" to use Python 2.x instead of Python 3
  sed -i 's#/usr/bin/python#\02#' "${pkgdir}/opt/VirtualBox/vboxshell.py"

  # Install the SDK
  cd "${pkgdir}/opt/VirtualBox/sdk/installer"
  VBOX_INSTALL_PATH="/opt/VirtualBox" python2 vboxapisetup.py install --root "${pkgdir}"
  rm -Rf build
  cd "${pkgdir}/opt/VirtualBox"

  # Update Arch initscripts way of life in VBox.sh
  sed -i -e 's,sudo /etc/init.d/vboxdrv setup,/etc/rc.d/vboxdrv setup,g' \
    "${pkgdir}/opt/VirtualBox/VBox.sh"
  sed -i -e 's,sudo /etc/init.d/vboxdrv restart,/etc/rc.d/vboxdrv restart,g' \
    "${pkgdir}/opt/VirtualBox/VBox.sh"

  # Install vboxdrv initscript
  install -Dm0755 "${srcdir}/vboxdrv.rc" "${pkgdir}/etc/rc.d/vboxdrv"
  install -Dm0644 "${srcdir}/vboxdrv.conf" "${pkgdir}/etc/conf.d/vboxdrv"
  
  # Install vboxweb initscript
  install -Dm0755 "${srcdir}/vboxweb.rc" "${pkgdir}/etc/rc.d/vboxweb"
  install -Dm0644 "${srcdir}/vboxweb.conf" "${pkgdir}/etc/conf.d/vboxweb"

  # Install udev rules
  install -Dm0644 "${srcdir}/10-vboxdrv.rules" "${pkgdir}/lib/udev/rules.d/10-vboxdrv.rules"

  # Symlink the launchers
  install -dm0755 "${pkgdir}/usr/bin"
  for _bin in VirtualBox VBox{Headless,Manage,SDL,SVC,Tunctl,NetAdpCtl} rdesktop-vrdp; do
    ln -s "/opt/VirtualBox/${_bin}" "${pkgdir}/usr/bin/${_bin}"
  done

  # Symlink the desktop icon and ".desktop" files
  install -dm0755 "${pkgdir}/usr/"{share/applications,share/pixmaps}
  ln -s "/opt/VirtualBox/VBox.png" "${pkgdir}/usr/share/pixmaps/VBox.png"
  ln -s "/opt/VirtualBox/virtualbox.desktop" "${pkgdir}/usr/share/applications/VirtualBox.desktop"

  # Symlink mime info
  install -dm0755 "${pkgdir}/usr/share/mime/packages"
  ln -s "/opt/VirtualBox/virtualbox.xml" "${pkgdir}/usr/share/mime/packages/virtualbox.xml"

  # Symlink doc
  install -dm0755 "${pkgdir}/usr/share/doc/${pkgname}"
  ln -s "/opt/VirtualBox/VirtualBox.chm" "${pkgdir}/usr/share/doc/$pkgname/VirtualBox.chm"

  # Setup default configuration
  install -dm 0755 "${pkgdir}/etc/vbox"
  echo 'INSTALL_DIR="/opt/VirtualBox"' > "${pkgdir}/etc/vbox/vbox.cfg"
  chmod 0644 "${pkgdir}/etc/vbox/vbox.cfg"
  
  # Create the directory below if it doesn't exist
  install -dm0755 "${pkgdir}/var/run/VirtualBox"
}

# vim:set ts=2 sw=2 ft=sh et:
