# Maintainer: Nicolas Pouillard (http://nicolaspouillard.fr)
# Contributor: Peter Simons <simons@cryp.to>
pkgname=mr
pkgver=1.06
pkgrel=1
pkgdesc='Checkout, update, or perform other actions on a set of VC repositories.'
arch=('any')
url='http://kitenet.net/~joey/code/mr/'
license=('GPL2')
depends=('perl' ' perl-libwww' 'perl-html-parser' 'perl-uri')
makedepends=('git' 'rsync')
optdepends=('git: support for git repositories'
	    'cvs: support for cvs repositories'
	    'svn: support for subversion repositories'
	    'mercurial: support for mercurial repositories'
	    'bzr: support for bzr repositories'
	    'darcs: support for darcs repositories')

build()
{
  local _gitroot='git://git.kitenet.net/mr'

  cd "${srcdir}"
  if [ -d mr ] ; then
    cd mr
    git checkout master
    git pull origin
  else
    git clone --depth=1 "${_gitroot}"
  fi
  cd "${srcdir}/mr"
  git checkout "${pkgver}"
  rsync -aH --del "${srcdir}/mr/" "${srcdir}/build/"
  cd "${srcdir}/build"
  make
}

check()
{
  cd "${srcdir}/build"
  make test
}

package()
{
  cd "${srcdir}/build"
  install -m755 -D mr "${pkgdir}/usr/bin/mr"
  install -m755 -D webcheckout "${pkgdir}/usr/bin/webcheckout"
  install -m644 -D mr.1 "${pkgdir}/usr/share/man/man1/mr.1"
  install -m644 -D webcheckout.1 "${pkgdir}/usr/share/man/man1/webcheckout.1"
  install -m644 -D README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
