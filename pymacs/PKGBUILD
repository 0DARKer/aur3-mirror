# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>
# Contributor: Richard Murri <admin@richardmurri.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pymacs
pkgver=0.24_beta2_0
pkgrel=2
_gitrel=g016b0bc
pkgdesc='Interface between Emacs Lisp and Python.'
arch=('any')
url='http://pymacs.progiciels-bpi.ca/'
license=('GPL2')
depends=('python2' 'emacs')
source=($pkgname-$pkgver.tar.gz::https://github.com/pinard/Pymacs/tarball/v0.24-beta2)
md5sums=('d126837a14f2e26e2cac34fd70ab01e6')
install=$pkgname.install

build() {
  cd ${srcdir}/pinard-P${pkgname:1}-${_gitrel:1}
  make PREFIX=/usr PYTHON=python2
  emacs -batch -f batch-byte-compile pymacs.el
}
package() {
  cd ${srcdir}/pinard-P${pkgname:1}-${_gitrel:1}
  python2 ./setup.py install --prefix=${pkgdir}/usr
  install -d ${pkgdir}/usr/share/emacs/site-lisp
  install -m 644 pymacs.{el,elc} ${pkgdir}/usr/share/emacs/site-lisp
}
