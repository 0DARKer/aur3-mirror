# Maintainer: Aaron DeVore <aaron.devore@gmail.com>

pkgname=python-selenium
pkgver=2.42.0
pkgrel=1
pkgdesc="Python binding for Selenium Remote Control [Python 3]"
arch=(i686 x86_64)
url="http://pypi.python.org/pypi/selenium"
license=('Apache')
changelog='Changelog'
depends=('python')
makedepends=('python-setuptools')
optdepends=('lib32-glibc: Firefox x86 webdriver support for x86_64')
source=("http://pypi.python.org/packages/source/s/selenium/selenium-${pkgver}.tar.gz"
0001-Make-exceptions-Python-3-compatible.patch)
md5sums=('455bb6da2fd758ac035f541d575546a6'
         '818bf3eb451099dacba50b6963a8a0be')

prepare() {
  cd "$srcdir/selenium-$pkgver"
  patch -p1 < "$srcdir/0001-Make-exceptions-Python-3-compatible.patch"
}

build() {
  cd "$srcdir/selenium-$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/selenium-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

# check() takes too many dependencies to run

