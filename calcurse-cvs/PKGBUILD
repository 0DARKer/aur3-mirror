# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>

pkgname=calcurse-cvs
pkgver=20100917
pkgrel=1
pkgdesc="A text-based personal organizer (CVS version)."
arch=('i686' 'x86_64')
url="http://culot.org/calcurse/"
license=('GPL2')
depends=('ncurses')
makedepends=('cvs')
provides=('calcurse')
conflicts=('calcurse')

_cvsroot=":pserver:anonymous@culot.org:/cvs"
_cvsmod="calcurse"

build() {
  cd "$srcdir"
  msg "Connecting to $_cvsmod.sourceforge.net CVS server...."
  if [ -d $_cvsmod/CVS ]; then
    cd $_cvsmod
    cvs -z3 update -d
  else
    cvs -z3 -d $_cvsroot co -D $pkgver -f $_cvsmod
    cd $_cvsmod
  fi

  msg "CVS checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_cvsmod-build"
  cp -r "$srcdir/$_cvsmod" "$srcdir/$_cvsmod-build"
  cd "$srcdir/$_cvsmod-build"

  ./autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$_cvsmod-build"
  make DESTDIR=${pkgdir} install 
}
