# Maintainer: Richard "Nothing4You" Schwab <mail NOSPAM w.tf-w.tf>

# Found typos, bugs or other problems? Feel free to create a pull-request:
# https://github.com/Nothing4You/PKGBUILDs

pkgname=nzbget-svn
pkgver=0.r1014
pkgrel=3
epoch=1
pkgdesc="Download from Usenet using .nzb files"
arch=('i686' 'x86_64')
url="http://nzbget.sourceforge.net/"
depends=('libpar2' 'libxml2' 'ncurses' 'libsigc++' 'libgcrypt' 'openssl')
makedepends=('svn' 'autoconf' 'automake')
conflicts=('nzbget')
provides=('nzbget')
license=('GPL')
source=('nzbget::svn://svn.code.sf.net/p/nzbget/code/'
        'nzbget.service')
sha512sums=('SKIP'
            '5feab7e2b5232407713c3d90acfff8c5a1abaae00674cb76265737e4acb80396bd26adc77852cb39684464d3a43117e7ce8e5c18e71842f011080a1fbe030fe1')

pkgver() {
  cd "$srcdir/${pkgname%-*}"
  local ver="$(svnversion)"
  printf "0.r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd "$srcdir/${pkgname%-*}/trunk"

  ./configure --prefix=/usr --sbindir=/usr/bin --enable-parcheck --with-tlslib=OpenSSL
  make "-j$(nproc)" CXXFLAGS="-lsigc-2.0"
}

package() {
  cd "$srcdir/${pkgname%-*}/trunk"

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/share/nzbget"
  install -m644 -t "$pkgdir/usr/share/nzbget" AUTHORS ChangeLog README

  install -d "$pkgdir/usr/lib/systemd/system"
  install -m644 -t "$pkgdir/usr/lib/systemd/system" "$srcdir/nzbget.service"
}
