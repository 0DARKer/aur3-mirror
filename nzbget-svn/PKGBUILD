# Maintainer: Richard "Nothing4You" Schwab <mail NOSPAM w.tf-w.tf>

# Found typos, bugs or other problems? Feel free to create a pull-request:
# https://github.com/Nothing4You/PKGBUILDs

pkgname=nzbget-svn
pkgver=0.r912
pkgrel=1
epoch=1
pkgdesc="Download from Usenet using .nzb files"
arch=('i686' 'x86_64')
url="http://nzbget.sourceforge.net/"
depends=('gnutls' 'libpar2' 'libxml2' 'ncurses' 'libsigc++' 'libgcrypt')
makedepends=('svn' 'autoconf' 'automake')
conflicts=('nzbget')
provides=('nzbget')
license=('GPL')
source=(nzbget::svn://svn.code.sf.net/p/nzbget/code/)
sha512sums=(SKIP)

pkgver() {
  cd "$srcdir/${pkgname%-*}"
  local ver="$(svnversion)"
  printf "0.r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd "$srcdir/${pkgname%-*}/trunk"

  ./configure --prefix=/usr --sbindir=/usr/bin --enable-parcheck
  make CXXFLAGS="-lsigc-2.0"
}

package() {
  cd "$srcdir/${pkgname%-*}/trunk"

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/share/nzbget"
  install -m644 -t "$pkgdir/usr/share/nzbget" AUTHORS ChangeLog README
}
