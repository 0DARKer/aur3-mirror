# Maintainer: Richard Schwab <mail NOSPAM w.tf-w.tf>

pkgname=nzbget-svn
pkgver=697
pkgrel=1
pkgdesc="Download from Usenet using .nzb files"
arch=('x86_64' 'i686')
url="http://nzbget.sourceforge.net/"
license=('GPL')
depends=('gnutls' 'libpar2' 'libxml2' 'ncurses' 'libsigc++' 'libgcrypt')
makedepends=('svn' 'autoconf' 'automake')
provides=('nzbget')
conflicts=('nzbget')
source=('nzbget::svn+https://nzbget.svn.sourceforge.net/svnroot/nzbget/trunk')
md5sums=(SKIP)

pkgver() {
  cd nzbget
  svnversion | tr -d [A-z]
}

build() {
  cd nzbget

  ./configure --prefix=/usr --sbindir=/usr/bin --enable-parcheck
  make CXXFLAGS="-lsigc-2.0"
}

package() {
  cd nzbget

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/$pkgname
  install -m644 -t ${pkgdir}/usr/share/${pkgname} AUTHORS ChangeLog README
}
