# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=fxmoviemanager
pkgver=4.7
pkgrel=1
pkgdesc="A filemanager written to help managing Movie files and Video clips."
arch=(i686 x86_64)
url="http://stagione.interfree.it/"
license=('GPL')
depends=('ffmpegthumbnailer' 'fox-devel')
install="$pkgname.install"
source=(${url}fxmm_${pkgver}.tar.bz2)
md5sums=('99e89ea075c823e9639a68644eeda8c5')

build() {
  cd "${srcdir}/fxmm_${pkgver}"

  # fox-config fix
  sed -i "s|fox-config-1.7|fox-config|g" configure*

  # fix icons links
  find desktop_icons -type l -delete
  for s in 16 32 48 64; do
    ln -s fxar.png desktop_icons/${s}/fxar${s}.png
    ln -s fxmm.png desktop_icons/${s}/fxmm${s}.png
    ln -s fxsv.png desktop_icons/${s}/fxsv${s}.png
  done

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/fxmm_${pkgver}"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
