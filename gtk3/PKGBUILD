# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on the gtk3 package trunk

pkgname=gtk3
pkgver=3.0.2
pkgrel=1
pkgdesc="The GTK+ Toolkit (v3)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk2' 'gnutls' 'heimdal' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info')
makedepends=('gobject-introspection-dev>=0.10.1' 'glib2-newest>=2.28.0')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/gtkrc)
license=('LGPL')
source=(http://ftp.gnome.org/pub/GNOME/sources/gtk+/3.0/gtk+-${pkgver}.tar.bz2)
sha256sums=(`wget ${source/.t*}.sha256sum -qO - | grep tar.bz2 | cut -d " " -f1`)

build() {
    cd gtk+-${pkgver}
    CXX=/bin/false ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --enable-gtk2-dependency --disable-schemas-compile
    make
}

package() {
    cd gtk+-${pkgver}
    make DESTDIR="${pkgdir}" install

    echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-3.0/gtkrc"
}