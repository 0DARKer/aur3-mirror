# Maintainer: Estevao Valadao <estevao@archlinux-br.org>
# Contributor: nil
# Contributor: Guten <ywzhaifei@gmail.com>
# Contributor: Lee.MaRS <leemars@gmail.com>

pkgname=google-appengine
pkgver=1.5.1
pkgrel=2
arch=('any')
pkgdesc="Google App Engine SDK for Python"
url="http://code.google.com/appengine/"
license=('Apache')
depends=('python2' 'python2-pyopenssl')
options=('!strip')
source=(http://googleappengine.googlecode.com/files/google_appengine_${pkgver}.zip
        profile.google_appengine)

build() {
  cd "$srcdir"
  mkdir "$pkgdir/opt/"

  # Fix file mode
  find google_appengine -type f -exec chmod 644 '{}' \;
  # Make some .py executable
  chmod 755 google_appengine/*.py

  cp -R google_appengine/ "$pkgdir/opt/$pkgname"
  grep -r -l python "$pkgdir/opt" | xargs sed -i '/#!\/usr\/bin\/env python/s|python|python2|'
  
  # Creating docs directory for text files (BUGS, LICENSE, etc) and moving files into it
  mkdir "$pkgdir/opt/$pkgname/docs"
  mv "$pkgdir/opt/$pkgname/BUGS" "$pkgdir/opt/$pkgname/LICENSE" "$pkgdir/opt/$pkgname/README" \
  "$pkgdir/opt/$pkgname/RELEASE_NOTES" "$pkgdir/opt/$pkgname/VERSION" "$pkgdir/opt/$pkgname/docs"

  install -D -m755 profile.google_appengine "$pkgdir/etc/profile.d/google_appengine.sh"
}

sha1sums=('2b2900ec5f2d9d51282645015b4f1bef07b1cc07'
          '858130a6e6f2b09c5bd7923e301fa5c9a74e80ee')

