# Maintainer: Estevao Valadao <estevao@archlinux-br.org>
# Contributor: pisuka <tekmon@gmail.com>
# Contributor: Guten <ywzhaifei@gmail.com>
# Contributor: Lee.MaRS <leemars@gmail.com>

pkgname=google-appengine
pkgver=1.5.1
pkgrel=5
arch=('any')
pkgdesc="Google App Engine SDK for Python"
url="http://code.google.com/appengine/"
license=('Apache')
depends=('python2' 'python2-pyopenssl')
options=('!strip')
install=google-appengine.install
source=(http://googleappengine.googlecode.com/files/google_appengine_${pkgver}.zip
        profile.google_appengine)
sha1sums=('2b2900ec5f2d9d51282645015b4f1bef07b1cc07'
          'f1e0089cf49f3aba5acdb35c85fca3f3e2aa3ca3')

build() {
  cd "$srcdir"
  mkdir "$pkgdir/opt/"

  # Fix file mode
  find google_appengine -type f -exec chmod 644 '{}' \;
  # Make some .py executable (all of them?)
  chmod 755 google_appengine/*.py

  cp -R google_appengine/ "$pkgdir/opt/$pkgname"
  grep -r -l python "$pkgdir/opt" | xargs sed -i '/#!\/usr\/bin\/env python/s|python|python2|'

  # Creating directory /opt/google_appengine to create symlinks and to maintain compatibility with django-nonrel
  mkdir -p "$pkgdir/opt/google_appengine/"
  find "$pkgdir/opt/$pkgname" -type f -executable -printf "/opt/$pkgname/%P\n" | xargs ln -st "$pkgdir/opt/google_appengine/"
  find "$pkgdir/opt/$pkgname" -maxdepth 1 -type d -printf "/opt/$pkgname/%P\n" | xargs ln -st "$pkgdir/opt/google_appengine/"

  install -D -m755 profile.google_appengine "$pkgdir/etc/profile.d/google_appengine.sh"
}
