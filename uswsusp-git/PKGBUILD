# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: cantabile <cantabile dot desu at gmail dot com>

pkgname=uswsusp-git
pkgver=20120504
pkgrel=1
pkgdesc="Userspace software suspend - latest code from git"
arch=('i686' 'x86_64')
url='http://suspend.sourceforge.net/'
license=('GPL')
depends=('libgcrypt' 'libx86' 'lzo2')
makedepends=('git')
provides=('uswsusp')
conflicts=('uswsusp')
backup=('etc/suspend.conf')
install=uswsusp.install
source=('uresume-hook'
        'uresume-install'
        'no-deprecated-switch.patch')

_gitroot="git://git.kernel.org/pub/scm/linux/kernel/git/rafael/suspend-utils.git"
_gitname="suspend-utils"

build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."

	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "${srcdir}/${_gitname}-build"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
	cd "${srcdir}/${_gitname}-build"

	#
	# BUILD HERE
	#

	patch -Np1 < "${srcdir}/no-deprecated-switch.patch"

	./autogen.sh
	./configure \
		--prefix=/usr \
		--enable-compress \
		--enable-encrypt \
		--enable-threads \
		--disable-resume-static \
		--disable-static \
		--sysconfdir=/etc
	make
}

package() {
	cd "${srcdir}/${_gitname}-build"
	mkdir "${pkgdir}/etc"
	make DESTDIR="${pkgdir}/" install

	install -D -m644 "${srcdir}/uresume-hook" "${pkgdir}/usr/lib/initcpio/hooks/uresume"
	install -D -m644 "${srcdir}/uresume-install" "${pkgdir}/usr/lib/initcpio/install/uresume"
} 

sha256sums=('6560073a87140a7b9f3eb565b3092113a44aa2437bb4fabaff44d672f16d6a75'
            '43bca6fdeb305042d8a376f1eb10d1de50b32739598d97bc5fdd5676abe61ac4'
            '35819ee80e64c58c406aba14607df9f7a4e3950522295c4f95a815fa25776a02')
