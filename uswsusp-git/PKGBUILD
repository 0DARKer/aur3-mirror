# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: cantabile <cantabile dot desu at gmail dot com>

pkgname=uswsusp-git
pkgver=20120524
pkgrel=1
pkgdesc="Userspace software suspend - latest code from git"
arch=('i686' 'x86_64')
url='http://suspend.sourceforge.net/'
license=('GPL')
depends=('libgcrypt' 'libx86' 'lzo2')
makedepends=('git')
provides=('uswsusp')
conflicts=('uswsusp')
backup=('etc/suspend.conf')
install=uswsusp.install
source=('uresume-hook'
        'uresume-install'
        'no-deprecated-switch.patch')

_gitroot="git://git.kernel.org/pub/scm/linux/kernel/git/rafael/suspend-utils.git"
_gitname="suspend-utils"

build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."

	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "${srcdir}/${_gitname}-build"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
	cd "${srcdir}/${_gitname}-build"

	#
	# BUILD HERE
	#

	patch -Np1 < "${srcdir}/no-deprecated-switch.patch"

	./autogen.sh
	./configure \
		--prefix=/usr \
		--enable-compress \
		--enable-encrypt \
		--enable-threads \
		--disable-resume-static \
		--disable-static \
		--sysconfdir=/etc
	make
}

package() {
	cd "${srcdir}/${_gitname}-build"
	mkdir "${pkgdir}/etc"
	make DESTDIR="${pkgdir}/" install

	install -D -m644 "${srcdir}/uresume-hook" "${pkgdir}/usr/lib/initcpio/hooks/uresume"
	install -D -m644 "${srcdir}/uresume-install" "${pkgdir}/usr/lib/initcpio/install/uresume"
} 

sha256sums=('2662d879ca7a2339233c8bdce123f0d686397093759ca54f162a3577603587b0'
            '9fe3e38301014b4c8597aec2041ad3c6d85e6935004cbd3ac93659d391db3157'
            '35819ee80e64c58c406aba14607df9f7a4e3950522295c4f95a815fa25776a02')
