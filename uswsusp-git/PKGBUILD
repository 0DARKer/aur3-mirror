# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: cantabile <cantabile dot desu at gmail dot com>

pkgname=uswsusp-git
pkgver=2013.04.05
pkgrel=1
pkgdesc="Userspace software suspend - latest code from git"
arch=('i686' 'x86_64')
url='http://suspend.sourceforge.net/'
license=('GPL')
depends=('libgcrypt' 'libx86' 'lzo2')
makedepends=('git')
provides=('uswsusp')
conflicts=('uswsusp')
backup=('etc/suspend.conf')
install=uswsusp.install
source=('git://git.kernel.org/pub/scm/linux/kernel/git/rafael/suspend-utils.git'
	'uresume-hook'
        'uresume-install'
        'no-deprecated-switch.patch')

pkgver() {
	cd suspend-utils/
	#git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
	date +%F | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd suspend-utils/

	patch -Np1 < "${srcdir}/no-deprecated-switch.patch"

	sed -i "s/^AM_CONFIG_HEADER/AC_CONFIG_HEADERS/" configure.ac
	mkdir m4

	./autogen.sh
	./configure \
		--prefix=/usr \
		--enable-compress \
		--enable-encrypt \
		--enable-threads \
		--disable-resume-static \
		--disable-static \
		--sysconfdir=/etc
	make
}

package() {
	cd suspend-utils/

	mkdir "${pkgdir}/etc"
	make DESTDIR="${pkgdir}/" install

	install -D -m644 "${srcdir}/uresume-hook" "${pkgdir}/usr/lib/initcpio/hooks/uresume"
	install -D -m644 "${srcdir}/uresume-install" "${pkgdir}/usr/lib/initcpio/install/uresume"
} 

sha256sums=('SKIP'
            '2662d879ca7a2339233c8bdce123f0d686397093759ca54f162a3577603587b0'
            '9fe3e38301014b4c8597aec2041ad3c6d85e6935004cbd3ac93659d391db3157'
            '35819ee80e64c58c406aba14607df9f7a4e3950522295c4f95a815fa25776a02')
