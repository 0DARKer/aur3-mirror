# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=mc2xml
pkgver=1.1
pkgrel=1
pkgdesc="Media Center TV Listings to XMLTV.xml for mythtv"
arch=('i686' 'x86_64')
url="http://mc2xml.my3gb.com"
license=('unknown')
depends=()
makedepends=('curl' 'gawk' 'sed' 'grep' 'vim')
options=('!strip')
source=(${pkgname}.md5)
md5sums=('d2843f5cb97d33f40315e72411c2b93e')
# The following are what the source and md5sums lines would look like
# if the download URL was constant
#source=(${pkgname}::http://mc2xml.yourfreehosting.net?h=ospocz6)
#md5sums=('4be0cafe399173b5033e3b8ef1ec3b4d')

build() {
  # This is rather unorthodox but the page uses javascript to construct
  # URLs using some key value that is generated when the page is generated
  # It is either based on the time or has some timeout value.
  # This command grabs the page data, pulls out the URL generating line,
  # parses out the hex value to be 'unescaped' and converts it to ASCII
  if [ ! -e "${srcdir}/${pkgname}" ]; then
    __download_url=`curl -s ${url}          | \
                    awk '/&nbsp;Linux/,0'   | \
                    grep -m 1 "unescape"    | \
                    cut -d ';' -f 1         | \
                    sed -e 's/[^0-9A-F]//g' | \
                    xxd -r -p`
    msg2 "Downloading ${__download_url}..."
    curl -s -o "${srcdir}/${pkgname}" ${__download_url}
  fi

  # manually check the md5sum
  md5sum --quiet -c ${pkgname}.md5 || (error "Integrity checks failed." && false)
}

package() {
  install -D -m 755 "${srcdir}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
