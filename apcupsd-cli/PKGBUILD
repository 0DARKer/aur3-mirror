# $Id: PKGBUILD 8842 2010-01-21 22:55:25Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Todd Musall <tmusall@comcast.net>

pkgname=apcupsd-cli
_orig=apcupsd
pkgver=3.14.12
pkgrel=2
pkgdesc="Apcupsd version without GUI and webinterface"
arch=(i686 x86_64)
url="http://www.apcupsd.org"
license=('GPL')
depends=('gcc-libs' systemd-sysvcompat)
makedepends=('pkgconfig')
conflicts=('apcupsd')
backup=(etc/apcupsd/apcupsd.conf
        etc/apcupsd/changeme
        etc/apcupsd/commfailure
        etc/apcupsd/commok
        etc/apcupsd/offbattery
        etc/apcupsd/onbattery
        etc/apcupsd/apccontrol)
source=(http://downloads.sourceforge.net/apcupsd/$_orig-$pkgver.tar.gz
        service)
md5sums=('08bf201acea6b4dd69cf2fb0eecfd55e'
         'ccb06b7c2f680c75c2fa3e5c9aa7a234')

build() {
  cd "$srcdir/$_orig-$pkgver"
  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin --libexecdir=/usr/lib/$_orig \
	  --enable-usb --enable-net \
	  --with-upstype=usb --with-upscable=usb \
	  --with-serial-dev=/dev/usb/hid/hiddev[0-9] \
	  --enable-pthreads

  make
}

package() {
  cd "$srcdir/$_orig-$pkgver"

  make DESTDIR="$pkgdir" install

  install -D -m755 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/apcupsd.service"

  rm -rf "$pkgdir/usr/share/hal"
  chmod 755 $pkgdir/usr/bin/*
}
