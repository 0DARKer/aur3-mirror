# Maintainer: Faheem Pervez <trippin1 bei g-to-the-m-to-the-a-to-the-i-to-the-l>
pkgname=subtitleedit-bin
pkgver=3.4.1
pkgrel=2
pkgdesc="A primarily Windows-oriented, Mono-based subtitle (SRT etc.) editor"
arch=('any')
url="http://www.nikse.dk/subtitleedit/"
license=('GPL3')
depends=('mono' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('icoutils')
optdepends=('mplayer2: visually sync subtitles with videos'
	    'hunspell: spell checking')
options=('!strip')
install="$pkgname.install"
source=("https://github.com/SubtitleEdit/subtitleedit/releases/download/${pkgver}/SE${pkgver//./}.zip"
	'subtitleedit.desktop'
	'subtitleedit-launcher')
_seditzip="${source[0]##*/}"
noextract=("${_seditzip}")
backup=('opt/subtitleedit/Settings.xml') #User etc. dictionary files are added below
sha1sums=('b05cdfd223f4f7d957ea14d6bc7c4a03e7dfe932'
	  '78ae949ff9ac35f3274e352d7b95f692b94ac4d6'
	  'fc92954aec931d1b6239190544cf1ace51e49912')

prepare() {
	mkdir "$srcdir/$pkgname-$pkgver" "$srcdir/ico"
	cd "$srcdir/$pkgname-$pkgver"

	bsdtar -xf "$srcdir/$_seditzip"
	wrestool -x --output="$srcdir/ico" --type=14 SubtitleEdit.exe
	for i in "$srcdir/ico/"SubtitleEdit.exe*.ico; do icotool -x --output="$srcdir/ico" "${i}"; done
}

package() {
	# Trying to resist turning on extglob etc. here...
	for i in "$srcdir/ico/"SubtitleEdit.exe*x32.png; do
	  local _icon_base="${i##*_}"
	  install -D -m 644 "$i" "$pkgdir/usr/share/icons/hicolor/${_icon_base%x32*}/apps/subtitleedit.png"
	done
	install -D -m 644 -t "$pkgdir/usr/share/applications/" "$srcdir/subtitleedit.desktop"
	install -D -m 755 "$srcdir/subtitleedit-launcher" "$pkgdir/usr/bin/subtitleedit"

	install -d -m 755 "$pkgdir/opt/subtitleedit"
	cp -a -T "$srcdir/$pkgname-$pkgver/" "$pkgdir/opt/subtitleedit/"
	rm -f "$pkgdir/opt/subtitleedit/gpl.txt" # cat /usr/share/licenses/common/GPL3/license.txt
	for i in 64 86; do rm -f "$pkgdir/opt/subtitleedit/Hunspellx$i.dll"; done
	for i in msvcp90.dll msvcr90.dll tesseract.exe; do rm -f "$pkgdir/opt/subtitleedit/Tesseract/$i"; done

	#SE stores all of its settings and modifications to its dictionaries in the folder where it is installed
	#Hence the bad permission hacks to ensure users can write to these files without chmodding 777 the entire SE folder
	
	#Admittedly, the repositories should be the first place to find dictionaries, but users may use SE's built-in download dictionaries feature:
	chmod 777 "$pkgdir/opt/subtitleedit/Dictionaries"
	#Words can be added by the user in SE's settings, so account for that here
	for i in "${pkgdir}/opt/subtitleedit/Dictionaries/"*.xml; do backup+=("${i##*${pkgdir}/}"); done
	for i in "${backup[@]}"; do
	  #Create empty settings file to register it as belonging to this package but don't change times on files that come with SE, like the user dicts
	  test -f "$pkgdir/$i" || touch "$pkgdir/$i"
	  chmod 666 "$pkgdir/$i"
	done
}
