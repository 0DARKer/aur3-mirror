# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani at gmail dot com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgbase=gwenview
pkgname=gwenview-light
pkgver=14.12.2
pkgrel=1
pkgdesc="A fast and easy to use image viewer for KDE, without Baloo support"
url='http://kde.org/applications/graphics/gwenview/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kded' 'kactivities-frameworks' 'hicolor-icon-theme' 'kdelibs4support' 'exiv2')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'python')
optdepends=('qt5-imageformats: Plugins for additional image formats (TIFF, WEBP, RAW…)'
            'kimageformats: Plugins for additional image formats (EPS, EXR, PSD, XCF…)')
replaces=('kdegraphics-gwenview')
conflicts=('kdegraphics-gwenview' 'gwenview')
install=$pkgname.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('9a5168ed2ad47ed77e178c501c006a2f71403cf2')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
