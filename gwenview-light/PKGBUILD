# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani at gmail dot com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgbase=gwenview
pkgname=gwenview-light
pkgver=14.12.1
pkgrel=4
pkgdesc="A fast and easy to use image viewer for KDE, without Baloo support"
url='http://kde.org/applications/graphics/gwenview/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kded' 'kactivities-frameworks' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'python')
replaces=('kdegraphics-gwenview')
conflicts=('kdegraphics-gwenview' 'gwenview')
install=$pkgname.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('eb4415328bea6309ac0a51fe1852b715ed263883')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
