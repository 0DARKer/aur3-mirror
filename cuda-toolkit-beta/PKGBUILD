# Maintainer: Thomas Jost <schnouki@schnouki.net>
# Contributor: Carson Reynolds <carson@k2.t.u-tokyo.ac.jp>
#
## WARNING: this is a package for a beta version of the CUDA SDK, which is only
# available to NVIDIA registered developers who signed a NDA. As a consequence,
# this package does NOT include source URLs: you must download the corresponding
# files yourself from https://nvdeveloper.nvidia.com/, put them in the same
# folder as this PKGBUILD, and run makepkg. This package will NOT be available
# in my custom repository neither, for the very same reason.

pkgname=cuda-toolkit-beta
pkgver=4.1.15
_shortver=4.1
pkgrel=1
_fedver=14
pkgdesc="NVIDIA's CUDA architecture can be programmed in the only C language environment that unlocks the processing power of GPUs to solve the most complex compute-intensive challenges."
arch=('i686' 'x86_64')

url="http://www.nvidia.com/object/cuda_home.html"
license=('custom')

depends=('gcc45' 'ncurses' 'nvidia>=285.05' 'python2')
optdepends=('libxtst: for the NVIDIA Visual Profiler'
            'libxrender: for the NVIDIA Visual Profiler'
            'libxt: for the NVIDIA Visual Profiler'
            'lib32-nvidia-utils')
mkdepends=('perl')
options=(!strip)

provides=("opencl-headers" "cuda-toolkit=${pkgver}" "thrust")
conflicts=("opencl-headers" "cuda-toolkit" "thrust")
replaces=("thrust")

if [ "$CARCH" = "i686" ]; then
  _bits=32
  md5sums=('981332f3bfdffdd15ad0c1a3a56985fa')
  sha256sums=('4ea012de61f0b69619acbd15fa70bc0de4df48c210e578ec481bfc776861ddf5')
else
  _bits=64
  md5sums=('7f870c87a6234f6c5463d211c75f9971')
  sha256sums=('f480ecae9930f648099ea8b358a1dd5966a6457a88140708c155acd72ebcf78e')
fi

source=(cudatoolkit_${pkgver}_linux_${_bits}_fedora${_fedver}.run)
install=cuda-toolkit.install

# This is ugly, but saves a lot of time: don't compress the package with xz.
PKGEXT=".pkg.tar"

build() {
  cd "$srcdir"
  
  msg2 "Uncompressing the CUDA toolkit..."
  sh cudatoolkit_${pkgver}_linux_${_bits}_fedora${_fedver}.run --noexec --keep --target toolkit &>/dev/null

  msg2 "Patching the install script..."
  # don't uninstall first
  sed -i 's/my $yesno = ""/my $yesno = "no"/' toolkit/install-linux.pl

  #Inspection hook
  #echo "Inspect srcdir"
  #read 
}

package() {
  cd "$srcdir/toolkit"

  msg2 "Installing the CUDA toolkit..."
  install -dm755 "$pkgdir/usr/cuda"
  ./install-linux.pl --prefix="$pkgdir/usr/cuda" >/dev/null

  msg2 "Fixing paths..."
  sed -i "s#$pkgdir##g" "$pkgdir/usr/cuda/bin/nvvp"

  msg2 "Creating required symlinks..."
  install -dm755 "$pkgdir/usr/cuda/gcc-4.5"
  ln -s ../../bin/gcc-4.5 "$pkgdir/usr/cuda/gcc-4.5/gcc"
  ln -s ../../bin/g++-4.5 "$pkgdir/usr/cuda/gcc-4.5/g++"

  # Hack: cuda-gdb needs libtinfo.so.5, which is apparently now in ncurses
  ln -s ../../lib/libncurses.so.5 "$pkgdir/usr/cuda/lib/libtinfo.so.5"

  msg2 "Patching nvcc.profile for gcc-4.5..."
  # Thanks to Valentine Sinitsyn for the idea!
  echo '' >> "$pkgdir/usr/cuda/bin/nvcc.profile"
  echo '# Arch-specific paths' >> "$pkgdir/usr/cuda/bin/nvcc.profile"
  echo 'compiler-bindir  = /usr/cuda/gcc-4.5' >> "$pkgdir/usr/cuda/bin/nvcc.profile"

  msg2 "Adding config files in /etc/ld.so.conf.d and /etc/profile.d..."
  # ldconfig
  install -dm755 "$pkgdir/etc/ld.so.conf.d"
  (
    [[ -d "$pkgdir/usr/cuda/lib" ]] && echo "/usr/cuda/lib"
    [[ -d "$pkgdir/usr/cuda/lib64" ]] && echo "/usr/cuda/lib64"
  ) > "$pkgdir/etc/ld.so.conf.d/cuda-toolkit.conf"

  # profile
  install -dm755 "$pkgdir/etc/profile.d"
  echo 'export PATH="${PATH}:/usr/cuda/bin"' > "$pkgdir/etc/profile.d/cuda-toolkit.sh"

  msg2 "Installing license information..."
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s ../../../cuda/doc/EULA.txt "$pkgdir/usr/share/licenses/$pkgname/EULA.txt"

  #Inspection hook
  #echo "Inspect pkgdir"
  #read 
}

# Local Variables:
# pkgbuild-update-sums-on-save: nil
# End:
