# Maintainer: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Anish Bhatt <anish@archlinux.us>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Fabio Scotoni <fabio@esse.ch>
# Contributor: Smith Dhumbumroong <zodmaner@gmail.com>

pkgname=re-alpine
pkgver=2.02
pkgrel=2
pkgdesc="The continuation of the Alpine email client from University of Washington."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/re-alpine/"
license=('APACHE')
depends=('libldap' 'heimdal>=1.2.1' 'gettext')
optdepends=('aspell: spell-checking support'
	    'hunspell: spell-checking support')
provides=('pine' 'alpine')
conflicts=('pine' 'alpine')
replaces=('pine')
options=('!makeflags')
source=(http://downloads.sourceforge.net/project/re-alpine/$pkgname-$pkgver.tar.bz2 
        maildir.patch)
md5sums=('5e75826b15f05674856be8618bdefdfb'
         '8d7a0e8d940e84e978f50266405c7361')
build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 < ../maildir.patch
    # fix compile problem on i686 machines (credit goes to orivej)
    LIBS=-lpam \
    ./configure --prefix=/usr --without-passfile --without-tcl \
        --disable-shared --with-system-pinerc=/etc/alpine.d/pine.conf \
        --with-system-fixed-pinerc=/etc/alpine.d/pine.conf.fixed || return 1
    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}