# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Mikhail Borisov <borisov.mikhail@gmail.com>

pkgname=coreutils-acp
pkgver=8.8_0.3

_pkgname=coreutils
_pkgver=${pkgver%_*}
_patchver=${pkgver#*_}
pkgrel=3

pkgdesc="The basic file, shell and text manipulation utilities of the GNU operating system"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('base')
depends=('glibc' 'shadow' 'pam' 'acl' 'gmp>=5.0' 'libcap')
replaces=('mktemp')
conflicts=('coreutils')
provides=('coreutils')
backup=('etc/pam.d/su')
install=${_pkgname}.install
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/$_pkgname/$_pkgname-$_pkgver.tar.xz
        coreutils-uname.patch
        coreutils-pam.patch
        su.pam
        advcpmv-$pkgver.patch)

build() {
	cd ${srcdir}/${_pkgname}-${_pkgver}

	# added su wheel group pam patch (from fedora cvs)
	patch -Np1 -i ${srcdir}/coreutils-pam.patch

	# linux specific uname improvement (from gentoo portage)
	patch -Np1 -i ${srcdir}/coreutils-uname.patch

	patch -Np1 -i ${srcdir}/advcpmv-${pkgver}.patch

	autoreconf -v
	./configure --prefix=/usr \
	            --enable-install-program=su \
	            --enable-no-install-program=groups,hostname,kill,uptime \
	            --enable-pam ac_cv_func_openat=no
	make
	make check
}

package() {
	cd ${srcdir}/${_pkgname}-${_pkgver}
	make DESTDIR=${pkgdir} install

	cd ${pkgdir}/usr/bin
	install -dm755 ${pkgdir}/{bin,usr/sbin}

	# binaries required by FHS
	_fhs="cat chgrp chmod chown cp date dd df echo false ln ls mkdir mknod mv pwd rm rmdir stty su sync true uname"
	mv ${_fhs} ${pkgdir}/bin
	ls -lha ${pkgdir}/bin/su
	chmod -v 4555 ${pkgdir}/bin/su

	# binaries required by various Arch scripts
	_bin="cut dir dircolors du install mkfifo readlink shred sleep touch tr vdir"
	mv ${_bin} ${pkgdir}/bin
	ln -sf /bin/sleep ${pkgdir}/usr/bin/sleep

	mv chroot ${pkgdir}/usr/sbin
	install -Dm644 ${srcdir}/su.pam ${pkgdir}/etc/pam.d/su
}

md5sums=('a78848e3d7ba52e65b564ffea875ef20'
         'c4fcca138b6abf6d443d48a6f0cd8833'
         'aad79a2aa6d566c375d7bdd1b0767278'
         'fa85e5cce5d723275b14365ba71a8aad'
         '616899c15986ccc9452735cfbf74d8d2')
