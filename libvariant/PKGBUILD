# Maintainer: Ed Branch <brance carp at mail dot utexas dot edu nofish>
pkgbase=libvariant
pkgname=libvariant
pkgver=1.0.0
pkgrel=2
pkgdesc="A C++ dynamically typed composable object library with de/serializers"
arch=('i686' 'x86_64')
url="https://bitbucket.org/gallen/libvariant"
license=('GPL')
depends=('libxml2' 'libyaml' 'curl')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.bz2::https://bitbucket.org/gallen/${pkgname}/get/${pkgver}.tar.bz2")
sha256sums=('708930d2487f0227bdeaf543bc997f4850ece1e493c541128e011099816f3cbd')

_srcdir="${pkgname}-${pkgver}"

prepare() {
  local extractdir="$(dirname $(tar -f ${pkgname}-${pkgver}.tar.bz2 --list | head -n1))"
  mv "${extractdir}" "${_srcdir}"
  mkdir -p "${_srcdir}/build"
}

build() {
  cd "${_srcdir}/build"
  cmake -DBUILD_SHARED_LIBS=ON -DCMAKE_INSTALL_PREFIX:PATH=/usr \
      -DCMAKE_BUILD_TYPE=release -DLIBVARIANT_BUILD_EXAMPLES=off ..
  make
}

package() {
  cd "${_srcdir}/build"
  make DESTDIR="$pkgdir/" install
}

check() {
  cd "${_srcdir}/build"
  make test
}
