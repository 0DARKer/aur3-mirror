# Contributor: Flamelab <panosfilip@gmail.com>

pkgname=kde-qt-git
pkgver=20091019
_realname=qt
pkgrel=1
pkgdesc="The Qt Gui Toolkit (patched for KDE)"
url="http://gitorious.org/+kde-developers/qt/kde-qt"

arch=(i686 x86_64)
license=(GPL3 LGPL)

conflicts=($_realname)

depends=('libpng' 'mesa' 'fontconfig' 'libtiff>=3.8.2-5' 'libmng>=1.0.10-2' 'sqlite3' 'xdg-utils' 'hicolor-icon-theme'
         'libxrandr' 'glib2' 'libxi' 'dbus' 'libxcursor' 'libxinerama' 'libxrender' 'gstreamer0.10-base-plugins'
         'ca-certificates')
optdepends=("postgresql-libs" "libmysqlclient" "unixodbc")
makedepends=('inputproto' 'postgresql-libs' 'mysql' 'unixodbc' 'cups' 'libxfixes' 'gtk2' 'git')
options=(!libtool)

source=(assistant.desktop
        designer.desktop
        linguist.desktop
        qtconfig.desktop)

_gitroot="git://gitorious.org/+kde-developers/qt/kde-qt.git"
_gitname="kde-qt"

#_realver=$(echo `/bin/grep '^# *define *QT_VERSION_STR' ${srcdir}/${_gitname}-build/src/corelib/global/qglobal.h` | sed 's,^# *define *QT_VERSION_STR *"*\([^ ]*\)"$,\1,')
_realver=4.6.3
provides=("$_realname=$_realver")


build() 

{ 
  cd ${srcdir}/

  msg "Connecting to the Freedesktop.org GIT server...."
  if [[ -d ${srcdir}/${_gitname} ]] ; then
      cd ${_gitname}
      git pull origin
      msg "The local files are updated..."
  else
      git clone ${_gitroot}
  fi
  
  msg "GIT checkout done."

  msg "Starting make for: ${pkgname}"
  
  if [[ -d ${srcdir}/${_gitname}-build ]]; then
    msg "Cleaning the previous build directory..." 
    rm -rf ${srcdir}/${_gitname}-build
  fi

  git clone ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
  
  cd ${srcdir}/${_gitname}-build
  unset QMAKESPEC
  export QT4DIR=${srcdir}/$pkgname
  export PATH=${QT4DIR}/bin:${PATH}
  export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}

  sed -i "s|-O2|$CXXFLAGS|" mkspecs/common/g++.conf
  sed -i "/^QMAKE_RPATH/s| -Wl,-rpath,||g" mkspecs/common/g++.conf

 ./configure -confirm-license -opensource \
		-prefix /usr \
		-sysconfdir /etc \
		-plugindir /usr/lib/qt/plugins \
		-translationdir /usr/share/qt/translations \
		-datadir /usr/share/qt \
		-docdir /usr/share/doc/qt \
		-examplesdir /usr/share/doc/qt/examples \
		-demosdir /usr/share/doc/qt/demos \
		-largefile \
		-plugin-sql-{psql,mysql,sqlite,odbc} \
		-system-sqlite \
		-xmlpatterns \
		-phonon \
		-svg \
		-webkit \
		-scripttools \
		-system-zlib \
		-system-libtiff \
		-system-libpng \
		-system-libmng \
		-system-libjpeg \
		-openssl-linked \
		-nomake demos \
		-nomake examples \
		-nomake docs \
		-no-rpath \
		-silent \
		-optimized-qmake \
		-dbus \
		-reduce-relocations \
		-no-separate-debug-info \
		-gtkstyle \
		-opengl \
		-glib  || return 1

  make || return 1

}

package()
{
  cd ${srcdir}/${_gitname}-build
  make INSTALL_ROOT=${pkgdir} install || return 1

  install -D -m644 tools/assistant/tools/assistant/images/assistant.png ${pkgdir}/usr/share/pixmaps/assistant.png
  install -D -m644 tools/linguist/linguist/images/appicon.png ${pkgdir}/usr/share/pixmaps/linguist.png
  install -D -m644 tools/designer/src/designer/images/designer.png ${pkgdir}/usr/share/pixmaps/designer.png
  install -D -m644 src/gui/dialogs/images/qtlogo-64.png ${pkgdir}/usr/share/pixmaps/qtlogo.png
  install -d ${pkgdir}/usr/share/applications
  install -m644 ${srcdir}/{linguist,designer,assistant,qtconfig}.desktop ${pkgdir}/usr/share/applications/

  find ${pkgdir}/usr/lib -type f -name '*prl' -print -exec sed -i -e "/^QMAKE_PRL_BUILD_DIR/d" {} \;
  sed -i -e "s|-L${srcdir}/${_gitname}-build/lib||g" ${pkgdir}/usr/lib/pkgconfig/*.pc
  sed -i -e "s|${srcdir}/${_gitname}-build/bin/moc|/usr/bin/moc|g" ${pkgdir}/usr/lib/pkgconfig/*.pc
  sed -i -e "s|${srcdir}/${_gitname}-build/bin/uic|/usr/bin/uic|g" ${pkgdir}/usr/lib/pkgconfig/*.pc

}

md5sums=('b352b4b70faba2571af3fce5d119580a'
         '491a96682faa03407f768a53cca71db5'
         'cda7ed7e132689991dc2968a0043b4b0'
         '717669b728a0a795217d2f52969e454c')
