# Maintainer: Carl George < arch at cgtx dot us >

_name="testtools"
_module="${_name}"

pkgname=("python-${_module}" "python2-${_module}")
pkgver="1.7.1"
pkgrel="2"
pkgdesc="Extensions to the Python standard library unit testing framework"
arch=("any")
url="https://github.com/testing-cabal/testtools"
license=("MIT")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_name:0:1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('e9999eecf002a9fdc499ab82c13cc7b40226ed4b5fc4ed2a2c1cfb08fe23cfea')

package_python-testtools() {
    depends=("python-extras" "python-mimeparse")
    cd "${srcdir}/${_name}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-testtools() {
    depends=("python2-extras" "python2-mimeparse")
    cd "${srcdir}/${_name}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1
}
