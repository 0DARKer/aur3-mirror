# Maintainer: Gavin Lloyd <gavinhungry@gmail.com>
# Contributor: xav <xav at ethertricks dot net>
# Contributor: julian <pidanc_j at epita dot fr>

pkgname=trousers
pkgver=0.3.7
pkgrel=3
pkgdesc="Open-source TCG Software Stack implementation for use with a TPM"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/trousers"
license=('CPL')
depends=('openssl')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz
        tss-user.patch
        no-werror.patch
        ${pkgname}.install
        tcsd)
md5sums=('0b4bc5daed265a56409a9c7af77c7ca6'
         '9a82bc0d3a08753c1aac5bfa3a4294a9'
         '326e92ef91883b3fe7f261c11e1c0e69'
         '3808cb22913851d0e93349cbac7b2900'
         '9fa52c5a41ddc58e7826488424db310a')

build() {
  cd ${srcdir}
  patch -p 0 < tss-user.patch
  patch -p 0 < no-werror.patch

  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib --with-openssl=/usr
  ln -sf `which libtool` libtool
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm755 ${srcdir}/tcsd ${pkgdir}/etc/rc.d/tcsd
}
