# Maintainer: cantabile <cantabile dot desu at gmail dot com>
# Contributor: reMiND <detanator@gmail.com>

pkgname=rhash
pkgver=1.2.10
pkgrel=1
pkgdesc='Utility for verifying hash sums (SFV, CRC, etc). Supports lots of algorithms.'
arch=('i686' 'x86_64')
url='http://rhash.anz.ru/?l=en'
license=('custom')
depends=('glibc')
backup=('etc/rhashrc')
options=('!makeflags')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-src.tar.gz)

build() {
   cd "$srcdir/$pkgname-$pkgver"
   make
   make lib-static
   make lib-shared
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   make PREFIX=/usr/ DESTDIR="$pkgdir" install
   make PREFIX=/usr/ DESTDIR="$pkgdir" install-lib-shared
   make PREFIX=/usr/ DESTDIR="$pkgdir" install-lib-static

   # Install license
   install -D -m644 "$srcdir/$pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
md5sums=('86ba1fa3d28f788aa68c0108c74c4c0d')
