# Maintainer: Beej Jorgensen <beej@beej.us>
# Contributor: Beej Jorgensen <beej@beej.us>

# The actual real sources are hosted somewhere under
# ftp://ftp.greyhelm.com/pub/Games/Moria/source/
# but this site is consistently down.  So the beej.us mirror
# is used instead

pkgname=umoria
pkgver=5.5.2
pkgrel=3
pkgdesc="Rogue-like dungeon crawler"
arch=('i686' 'x86_64')
url="http://www-math.bgsu.edu/~grabine/moria.html"
license=('custom')
depends=('ncurses')
makedepends=('gzip')
backup=("usr/share/${pkgname}/files/scores")
source=(
  'license.txt'
  "${pkgname}-aur.patch"
  "http://beej.us/moria/files/source/um${pkgver}.tar.Z"
)
install=${pkgname}.install
md5sums=('36baf52bdb15df8e4bab603dedb6f021'
         '768b281e30aeb390184abfa76c1075f0'
         '04a2852864302c702e82992023d97184')

build() {
  cd "$srcdir/$pkgname"

  patch -Np1 < ../${pkgname}-aur.patch

  cd source
  cp ../unix/* .
  make
  cd ..

  # binary
  install -D -m0755 source/moria ${pkgdir}/usr/bin/moria

  # support files
  install -d -m0755 ${pkgdir}/usr/share/${pkgname}/files
  install -m0644 files/* ${pkgdir}/usr/share/${pkgname}/files

  # set permissions on scores file (must be in games group to use)
  chgrp games ${pkgdir}/usr/share/${pkgname}/files/scores
  chmod 664 ${pkgdir}/usr/share/${pkgname}/files/scores

  # documentation
  install -d -m0755 ${pkgdir}/usr/share/doc/${pkgname}/doc
  install -m0644 doc/* ${pkgdir}/usr/share/doc/${pkgname}/doc
  install -m0644 CHANGES ${pkgdir}/usr/share/doc/${pkgname}
  install -m0644 OChanges ${pkgdir}/usr/share/doc/${pkgname}
  install -m0644 OOChanges ${pkgdir}/usr/share/doc/${pkgname}
  install -m0644 ERRORS ${pkgdir}/usr/share/doc/${pkgname}
  install -m0644 INSTALL ${pkgdir}/usr/share/doc/${pkgname}
  install -m0644 README ${pkgdir}/usr/share/doc/${pkgname}

  # man page
  install -d -m0755 ${pkgdir}/usr/share/man/man6
  gzip -c doc/moria.6 > ${pkgdir}/usr/share/man/man6/moria.6.gz

  # custom license
  install -d -m0755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m0644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

# vim:set ts=2 sw=2 et:
