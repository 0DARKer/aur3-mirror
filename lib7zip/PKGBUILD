# Maintainer: speps <speps at aur dot archlinux dot org>

_pkg=p7zip
_ver=9.20.1
pkgname=lib7zip
pkgver=1.6.3
pkgrel=1
pkgdesc="A library using 7z.dll/7z.so(from 7-Zip) to handle different archive types."
arch=(i686 x86_64)
url="http://code.google.com/p/lib7zip/"
license=('MPL')
makedepends=('libtool')
options=('!libtool')
source=("http://lib7zip.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        "http://downloads.sourceforge.net/project/${_pkg}/${_pkg}/${_ver}/${_pkg}_${_ver}_src_all.tar.bz2")
md5sums=('6413c398a8def15ac9dda392b41353b7'
         'bd6caaea567dc0d995c990c5cc883c89')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # load 7z.so from /usr/lib/p7zip
  sed -i "/Load7ZLibrary/s/7z/\/usr\/lib\/p7zip\/&/" Lib7Zip/7zipLibrary.cpp

  # point to p7zip source
  export P7ZIP_SOURCE_DIR="$srcdir/${_pkg}_${_ver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # static lib
  install -Dm644 Lib7Zip/$pkgname.a \
    "$pkgdir/usr/lib/$pkgname.a"

  # header
  install -Dm644 Lib7Zip/$pkgname.h \
     "$pkgdir/usr/include/$pkgname.h"
}

# vim:set ts=2 sw=2 et:
