# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=p7zip
_ver=9.20.1
pkgname=lib7zip
pkgver=1.4.1
pkgrel=1
pkgdesc="A library using 7z.dll/7z.so(from 7-Zip) to handle different archive types."
arch=(i686 x86_64)
url="http://code.google.com/p/lib7zip/"
license=('GPL')
source=("http://lib7zip.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        "http://downloads.sourceforge.net/project/${_pkg}/${_pkg}/${_ver}/${_pkg}_${_ver}_src_all.tar.bz2")
md5sums=('79ef69fc5c8ddce040387377b6dbbdcc'
         'bd6caaea567dc0d995c990c5cc883c89')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export P7ZIP_SOURCE_DIR="$srcdir/${_pkg}_${_ver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # static lib
  install -Dm644 Lib7Zip/$pkgname.a \
    "$pkgdir/usr/lib/$pkgname.a"

  # header
  install -Dm644 Lib7Zip/$pkgname.h \
     "$pkgdir/usr/include/$pkgname.h"
}

# vim:set ts=2 sw=2 et:
