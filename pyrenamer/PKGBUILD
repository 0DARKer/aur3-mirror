# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Lucas Salies Brum <lucas at archlinux dot com dot br>
# Maintainer: max.bra <max.bra at alice dot it>

pkgname=pyrenamer
pkgver=0.6.0
pkgrel=5
pkgdesc="An application for the mass renaming of files"
arch=('any')
url="http://www.infinicode.org/code/pyrenamer/"
license=('GPL')
depends=('gnome-python' 'gconf')
makedepends=('pkgconfig>=0.9' 'perlxml')
optdepends=('hachoir-metadata: rename multimedia files metadata'
            'python-eyed3: rename MP3 files metadata')
install=$pkgname.install
source=(http://www.infinicode.org/code/pyrenamer/$pkgname-$pkgver.tar.gz)
md5sums=('a7159f803cd919ab76f17789189c577b')

build() {
    cd $pkgname-$pkgver

    sed -i 's|python python2 python2.5|python2 python2.5|' "$srcdir"/$pkgname-$pkgver/configure
    sed -i 's|PYTHON=python|PYTHON=python2|' "${srcdir}"/$pkgname-$pkgver/py-compile
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "$srcdir"/$pkgname-$pkgver/src/$pkgname

    ./configure --prefix=/usr --sysconfdir=/etc \
        --with-gconf-schema-file-dir=/usr/share/gconf/schemas
    make || return 1
}

package() {
    cd $pkgname-$pkgver

    make DESTDIR="$pkgdir" install || return 1
}
