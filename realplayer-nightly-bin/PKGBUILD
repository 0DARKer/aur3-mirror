# Contributer Jonathan Wiersma <arch aur at jonw dot org>

pkgname=realplayer-nightly-bin
pkgver=11.1.1.2536
_pkgdate=20100222
_pkgdlhash=22a2854850cdaa908f22
pkgrel=1
pkgdesc="Real Media Player produced by RealNetworks"
arch=('i686' 'x86_64')
url="http://forms.helixcommunity.org/helix/builds/"
license=("custom")
depends=('gtk2' 'libxv' 'libstdc++5' 'alsa-lib')
provides=("realplayer=$pkgver")
conflicts=("realplayer" "realplayer-nightly" "helixplayer" "helixplayer-nightly" "helixplayer-nightly-bin")
options=(!strip)
source=("http://software-dl.real.com/$_pkgdlhash/helix/$_pkgdate/player_all-realplay_gtk_current-$_pkgdate-linux-2.2-libc6-gcc32-i586/realplay-$pkgver-linux-2.2-libc6-gcc32-i586.tar.bz2")
md5sums=('e17beb7043c960755bf0a9b1ef89d0fa')
if [ "$CARCH" == "x86_64" ]; then
	source=("http://software-dl.real.com/$_pkgdlhash/helix/$_pkgdate/player_all-realplay_gtk_current-$_pkgdate-linux-2.6-glibc23-amd64/realplay-$pkgver-linux-2.6-glibc23-amd64.tar.bz2")
md5sums=('596436f1fd01ca577d4b9949bded5b84')
fi

build() {
	cd $srcdir || return 1
	
	# Set-up Directories
	install -d $pkgdir/usr/{bin,share} || return 1
	install -d $pkgdir/opt/realplayer/bin || return 1
	install -d $pkgdir/usr/lib/mozilla/plugins || return 1
	install -d $pkgdir/usr/share/licenses/${pkgname} || return 1
	
	# Install shared libs
	for _file in {codecs,common,plugins}/*.so; do
		install -Dm755 "$_file" "$pkgdir/opt/realplayer/$_file" || return 1
	done || return 1
	
	# Install selected files from share directory
	find share/{default,distcode,mimelnk,realplay*,superbuffer,*.css,*.html} \
		-type f | while read _file; do
			install -Dm644 "$_file" "$pkgdir/opt/realplayer/$_file" || return 1
	done || return 1
	
	for _file in share/locale/*/{LICENSE,README}; do
		install -Dm644 "$_file" "$pkgdir/opt/realplayer/$_file" || return 1
	done || return 1
	
	# Install executables
	install -m755 realplay realplay.bin $pkgdir/opt/realplayer/ || return 1
	install -m755 Bin/setup $pkgdir/opt/realplayer/bin/ || return 1
	ln -s /opt/realplayer/realplay $pkgdir/usr/bin/realplay || return 1
	
	# Install Icons
	for _res in 16 192 32 48; do
		install -Dm644 $srcdir/share/icons/realplay_${_res}x${_res}.png \
			 $pkgdir/usr/share/icons/hicolor/${_res}x${_res}/apps/realplay.png || return 1
	done || return 1
	
	cd $srcdir/share/icons || return 1
	for _file in *; do
		install -Dm644 "$_file" \
			"$pkgdir/usr/share/icons/hicolor/$(echo ${_file#*_} \
				| sed -e 's:\..*::g')mimetypes/${_file%_*}.${_file#*.}" || return 1
	done || return 1
	
	# Install Locales
	cd $srcdir/share/locale || return 1
	for _locale in *; do
		install -d $pkgdir/usr/share/locale/$_locale/LC_MESSAGES || return 1
		install -m644 $_locale/*.mo \
			$pkgdir/usr/share/locale/$_locale/LC_MESSAGES/ || return 1
	done
	
	# Install desktop extras
	cd $srcdir || return 1
	install -Dm644 share/realplay.png \
		$pkgdir/usr/share/pixmaps/realplay.png || return 1
	install -Dm644 share/realplay.applications \
		$pkgdir/usr/share/application-registry/realplay.applications || return 1
	install -Dm644 share/realplay.desktop \
		$pkgdir/usr/share/applications/realplay.desktop || return 1
	
	install -d $pkgdir/usr/share/mime-info || return 1
	install -m644 share/realplay.keys share/realplay.mime \
		$pkgdir/usr/share/mime-info/ || return 1

	# Install mozilla plugins
	install -m755 mozilla/*.{so,xpt} \
		$pkgdir/usr/lib/mozilla/plugins/ || return 1

	# Install Licence and extras
	install -m644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/ || return 1
	install -m644 LICENSE README $pkgdir/opt/realplayer/ || return 1
}
