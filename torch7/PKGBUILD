# $Id: PKGBUILD 78820 2012-10-25 06:47:28Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=torch7
pkgver=7.0.1_0
pkgrel=1
pkgdesc="State of the art machine learning library"
arch=(i686 x86_64)
url="https://github.com/andresy/torch"
license=('GPL')
depends=(gcc-libs qtwebkit lapack)
source=("$pkgname-$pkgver.tar.gz::https://github.com/andresy/torch/archive/${pkgver/_/-}.tar.gz")
md5sums=('ad86237b9741ea1cbc6b8b2997a69344')

build() {
  cd "$srcdir/torch-${pkgver/_/-}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir/torch-${pkgver/_/-}"
  make install DESTDIR=$pkgdir
  rm -f $pkgdir/usr/{bin/luac,include/lua*}
}
