# Maintainer: Mael Kerbiriou <mael.kerbiriouATfreeDOTfr>

pkgname=wld-git
pkgver=r134.8884f07
pkgrel=1
pkgdesc='A primitive drawing library targeted at Wayland'
arch=('i686' 'x86_64')
license=('MIT')
url='https://github.com/michaelforney/wld'
depends=('wayland' 'libdrm' 'fontconfig' 'pixman')
makedepends=('git')
provides=('wld')
conflicts=('wld')
source=('git+https://github.com/michaelforney/wld.git')
md5sums=('SKIP')

_gitname="wld"

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"
  head wld.h -n22 > COPYING
  make
  
}

package() {
  cd "$srcdir/$_gitname"
  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
}

# vim: set noet ff=unix: