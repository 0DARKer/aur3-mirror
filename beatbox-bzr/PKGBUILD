# Submitter: schuay <jakob.gruber@gmail.com>
# Maintainer: crazyelf5

pkgname=beatbox-bzr
pkgver=latest
pkgrel=1
pkgdesc="A music player written for the Elementary project"
arch=('i686' 'x86_64')
url="https://launchpad.net/beat-box"
license=('GPL3')
depends=('glib2' 'gtk3' 'libgee' 'gstreamer0.10' 'gstreamer0.10-base' 'taglib' 'hicolor-icon-theme'
         'libxml2' 'gconf' 'libnotify' 'libsoup' 'json-glib' 'granite-bzr' 'sqlheavy-git')
makedepends=('bzr' 'vala' 'cmake' 'desktop-file-utils' 'gtk-update-icon-cache' 'shared-mime-info')
optdepends=('libzeitgeist' 'libdbusmenu' 'libindicate')
install=${pkgname}.install

_bzrtrunk=lp:beat-box
_bzrmod=beat-box

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ ! -d ${_bzrmod} ]; then
    bzr co ${_bzrtrunk} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_bzrmod}-build
  cp -r ${_bzrmod} ${_bzrmod}-build
  cd ${_bzrmod}-build

  sed -i 's|sqlheavy-0.1|sqlheavy-0.2|' \
	data/Makefile \
	images/Makefile \
	src/CMakeLists.txt
	
  sed -i 's|lsqlheavy0.1|lsqlheavy0.2|' \
	data/Makefile \
	images/Makefile

  mkdir build; cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${_bzrmod}-build/build
  make DESTDIR="${pkgdir}" install
  rm -rf "${srcdir}"/${_bzrmod}-build
}
