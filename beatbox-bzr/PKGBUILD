# Submitter: schuay <jakob.gruber@gmail.com>
# Maintainer: crazyelf5

pkgname=beatbox-bzr
pkgver=386
pkgrel=1
pkgdesc="A music player written for the Elementary project"
arch=('i686' 'x86_64')
url="https://launchpad.net/beat-box"
license=('GPL3')
depends=('glib2' 'gtk3' 'libgee' 'gstreamer0.10' 'gstreamer0.10-base'
         'taglib' 'hicolor-icon-theme' 'libxml2' 'gconf' 'libnotify' 'gettext'
         'libsoup' 'json-glib' 'granite-bzr' 'sqlheavy-git' 'dconf' 'libgpod')
makedepends=('bzr' 'vala' 'cmake' 'desktop-file-utils' 'gtk-update-icon-cache')
optdepends=('libzeitgeist' 'libdbusmenu' 'libindicate')
install=${pkgname}.install

_bzrtrunk=lp:beat-box
_bzrmod=beat-box

build() {
  msg "Connecting to Bazaar server...."

  if [ -d ${_bzrmod} ]; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
    cd "${srcdir}"
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d ${_bzrmod}-build ] && rm -rf ${_bzrmod}-build
  cp -r ${_bzrmod} ${_bzrmod}-build
  cd ${_bzrmod}-build

  sed -i 's|sqlheavy-0.1|sqlheavy-0.2|' \
	data/Makefile \
	images/Makefile \
	src/CMakeLists.txt
	
  sed -i 's|lsqlheavy0.1|lsqlheavy0.2|' \
	data/Makefile \
	images/Makefile

  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build/build"
  make DESTDIR="${pkgdir}" install
}
