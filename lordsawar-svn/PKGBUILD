# Maintainer: Dmitrij D. Czarkoff <czarkoff@gmail.com>
# Contributor: zoulnix <http://goo.gl/HQaP>

pkgname=lordsawar-svn
pkgver=2553
pkgrel=1
pkgdesc="A free, turn-based strategy game in spirit of Warlords II"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/lordsawar/"
license=('GPL3')
depends=('gtkmm' 'sdl_mixer')
makedepends=('pkgconfig' 'gettext' 'intltool' 'libtar' 'boost' 'subversion')
conflicts=('lordsawar')
changelog="ChangeLog"
install="lordsawar-svn.install"
source=("desktop-icon.patch")
md5sums=('b5c9f967d9cfed4f09fc23c1e7c9d9ed')

_svnmod="lordsawar"
_svntrunk="svn://svn.sv.gnu.org/${_svnmod}/${_svnmod}/trunk"

build() {
  cd "$srcdir"

  msg "Getting sources..."
  if [ -d ${_svnmod}/.svn ]
  then
    cd ${_svnmod}
    svn up || return 1
    cd ..
  else
    svn co ${_svntrunk} --config-dir ./ ${_svnmod} || return 1
  fi

  msg "SVN checkout done or server timeout"
  msg "Copying source tree to new folder"
  cp -r ${_svnmod} ${pkgname}
  cd ${pkgname}
  patch -Np1 -i ../desktop-icon.patch
  msg "Starting make..."

# Workaround to make gettextize non-interactive
  PATH="./:$PATH"
  sed -e 's:read dummy < /dev/tty::' `which gettextize` > gettextize
  chmod +x gettextize
  ./autogen.sh	# this is what You have to keep
  msg "We pressed Return for You, now running \"aclocal -I m4\" and \"autoconf\"."
  rm gettextize
  aclocal -I m4
  autoconf
# End  of workaround

# You may want to append to "./configure":
# don't compile editor
#    --disable-editor \
# don't compile play-by-mail
#    --disable-pbm \
# don't compile game list server
#    --disable-gls \
# don't compile game host server
#    --disable-ghs \
# zip save files (experimental)
#    --enable-zipping \
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-sdltest || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}/" install || return 1

  msg "Removing build dir..."
  cd "${srcdir}"
  rm -r ${pkgname}
}
