# Maintainer: Dmitrij D. Czarkoff <czarkoff@gmail.com>
# Contributor: zoulnix <http://goo.gl/HQaP>

pkgname=lordsawar-svn
pkgver=2591
pkgrel=1
pkgdesc="A free, turn-based strategy game in spirit of Warlords II"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/lordsawar/"
license=('GPL3')
depends=('gtkmm' 'sdl_mixer' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'gettext' 'intltool' 'libtar' 'boost' 'subversion')
conflicts=('lordsawar')
changelog="ChangeLog"
install="lordsawar-svn.install"

_svnmod="lordsawar"
_svntrunk="svn://svn.sv.gnu.org/${_svnmod}/${_svnmod}/trunk"

build() {
  cd "$srcdir"

  msg "Checking if previous sources are saved"
  if [ -f $SRCDEST/${pkgname}.tar.gz ]
  then
    msg "Unpacking previous sources"
    tar xzf $SRCDEST/${pkgname}.tar.gz
  fi

  msg "Getting latest revision from SVN..."
  if [ -d ${pkgname}/.svn ]
  then
    cd ${pkgname}
    svn up || return 1
    cd ..
  else
    svn co ${_svntrunk} --config-dir ./ ${pkgname} || return 1
  fi
  msg "SVN checkout done"

  msg "Backing up source tree"
  tar czf $SRCDEST/${pkgname}.tar.gz ${pkgname}

  cd ${pkgname}
  msg "Starting make..."

# Workaround to make gettextize non-interactive
  PATH="./:$PATH"
  sed -e 's:read dummy < /dev/tty::' `which gettextize` > gettextize
  chmod +x gettextize
  ./autogen.sh	# this is what You have to keep
  msg "We pressed Return for You, now running \"aclocal -I m4\" and \"autoconf\"."
  rm gettextize
  aclocal -I m4
  autoconf
# End  of workaround

# You may want to append to "./configure":
# don't compile editor
#    --disable-editor \
# don't compile play-by-mail
#    --disable-pbm \
# don't compile game list server
#    --disable-gls \
# don't compile game host server
#    --disable-ghs \
# zip save files (experimental)
#    --enable-zipping \
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-sdltest || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}/" install || return 1

  msg "Removing build dir..."
  cd "${srcdir}"
  rm -r ${pkgname}
}
