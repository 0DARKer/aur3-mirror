
pkgname=libxcm-git
pkgver=0.5.3.2.g7d7f7f9
pkgrel=1
pkgdesc="A library containing the a reference implementation of the net-color specs. (GIT version)"
arch=('i686' 'x86_64')
url="http://www.oyranos.org/kolormanager/"
license=('BSD')
depends=('libxfixes' 'libxmu')
makedepends=('git')
conflicts=('libxcm')
provides=('libxcm')
source=("libxcm::git://www.oyranos.org/git/xcolor")
md5sums=('SKIP')
_gitname="libxcm"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | sed 's|libXcm-||g' | tr - .)"
}

build() {
  cd "${_gitname}"
  LDFLAGS+=" -lm" ./configure --prefix=/usr
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

