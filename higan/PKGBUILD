# Maintainer : Alucryd <alucryd at gmail dot com>

pkgbase=higan
pkgname=higan
pkgver=092
pkgrel=7
arch=('i686' 'x86_64')
url="http://code.google.com/p/higan/"
license=('GPL3')
source=("http://higan.googlecode.com/files/higan_v${pkgver}-source.tar.xz" 'higan' 'higan.desktop')
sha1sums=('a205005f118f6e138065af6e0d14ed990b8f1ce1'
          '931baedc3bbdd343d2decda72c13b7d0efdcba3c'
          '55f4de0a65d6428840d013f9b003d88158c131c7')

# UI (only one can be set to true)
_gtk=true
_qt=false
# Profile (all can be set to true)
_accuracy=false
_balanced=true
_performance=false

true && pkgname=('higan' 'higan-common')

if [ "$_gtk" == true ] ; then
	pkgname+=('libananke-gtk')
	[ "$_accuracy" == true ] && pkgname+=('higan-gtk-accuracy')
	[ "$_balanced" == true ] && pkgname+=('higan-gtk-balanced')
	[ "$_performance" == true ] && pkgname+=('higan-gtk-performance')
fi

if [ "$_qt" == true ] ; then
	pkgname+=('libananke-qt')
	[ "$_accuracy" == true ] && pkgname+=('higan-qt-accuracy')
	[ "$_balanced" == true ] && pkgname+=('higan-qt-balanced')
	[ "$_performance" == true ] && pkgname+=('higan-qt-performance')
fi

buildlib() {
	make compiler=gcc platform=x phoenix=$1
	mv libananke.so libananke-$1.so
	rm ananke.o
}

installlib() {
	cd "${srcdir}/higan_v${pkgver}-source/ananke"
	install -dm 755 "${pkgdir}/usr/lib"
	install -Dm 644 libananke-$1.so "${pkgdir}/usr/lib/libananke.so.1"
	ln -s /usr/lib/libananke.so.1 "${pkgdir}/usr/lib/libananke.so"
}

buildui() {
	msg "Building $3 profile"
	make compiler=gcc platform=x target=$1 phoenix=$2 profile=$3
	mv out/higan "out/higan-$2-$3"
	make clean
}

installui() {
	cd "${srcdir}/higan_v${pkgver}-source/higan"
	install -dm 755 "${pkgdir}/usr/bin"
	install -m 755 "out/higan-$2-$3" "${pkgdir}/usr/bin/higan-$3"
}

build() {
# Warn user about the default settings
	msg "The script defaults to a balanced build using a GTK GUI. Please edit to suit your needs."

	cd "${srcdir}/higan_v${pkgver}-source/ananke"

# QT 4.8.0 fix
	if [ "$1" == qt ] ; then
		moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp
	fi

# Compile libananke GTK
	if [ "$_gtk" == true ] ; then
		msg "Building libananke GTK"
		buildlib gtk
	fi

# Compile libananke QT
	if [ "$_qt" == true ] ; then
		msg "Building libananke QT"
		buildlib qt
	fi

	cd "${srcdir}/higan_v${pkgver}-source/higan"

# QT 4.8.0 fix
	if [ "$2" == qt ] ; then
		moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp
	fi

# Compile higan GTK GUI
	if [ "$_gtk" == true ] ; then
		msg "Building GTK GUI"
		if [ "$_accuracy" == true ] ; then
			buildui ethos gtk accuracy
		fi
		if [ "$_balanced" == true ] ; then
			buildui ethos gtk balanced
		fi
		if [ "$_performance" == true ] ; then
			buildui ethos gtk performance
		fi
	fi

# Compile higan QT GUI
	if [ "$_qt" == true ] ; then
		msg "Building QT GUI"
		if [ "$_accuracy" == true ] ; then
			buildui ethos qt accuracy
		fi
		if [ "$_balanced" == true ] ; then
			buildui ethos qt balanced
		fi
		if [ "$_performance" == true ] ; then
			buildui ethos qt performance
		fi
	fi
}

package_higan-common() {
	pkgdesc="Nintendo multi-system emulator - Common files"
	arch=('any')
	depends=('xdialog')

	cd "${srcdir}/higan_v${pkgver}-source/higan"

	mkdir -p "${pkgdir}/usr/bin"
	cp "${srcdir}/higan" "${pkgdir}/usr/bin/"

	mkdir -p "${pkgdir}/usr/share/applications"
	cp "${srcdir}/higan.desktop" "${pkgdir}/usr/share/applications"

	mkdir -p "${pkgdir}/usr/share/higan"
	cp -R profile/* "${pkgdir}/usr/share/higan/"
	cp data/cheats.bml "${pkgdir}/usr/share/higan/"

	mkdir "${pkgdir}/usr/share/pixmaps"
	cp data/higan.png "${pkgdir}/usr/share/pixmaps/"

	cd "${srcdir}/higan_v${pkgver}-source/shaders"
	mkdir "${pkgdir}/usr/share/higan/Video Shaders"
	cp *.shader "${pkgdir}/usr/share/higan/Video Shaders/"

# Fix permissions
	find "${pkgdir}" -type d -print0 | xargs -0 chmod 755
	find "${pkgdir}" -type f -print0 | xargs -0 chmod 644
	chmod -R 755 "${pkgdir}/usr/bin"
}

package_libananke-gtk() {
	pkgdesc="ROM library for the higan emulator - GTK GUI"
	depends=('gtk2')
	conflicts=('libananke-qt')

	installlib gtk
}

package_libananke-qt() {
	pkgdesc="ROM library for the higan emulator - QT GUI"
	depends=('qt>=4.7.0')
	conflicts=('libananke-gtk')

	installlib qt
}

package_higan-gtk-accuracy() {
	pkgdesc="Nintendo multi-system emulator - GTK GUI - Accuracy profile"
	depends=("higan-common>=${pkgver}" 'libananke-gtk' 'libpulse' 'libao' 'libxv' 'libgl' 'openal' 'sdl')
	provides=("higan-ui=${pkgver}-${pkgrel}")
	conflicts=('higan-qt-accuracy')

	installui ethos gtk accuracy
}

package_higan-gtk-balanced() {
	pkgdesc="Nintendo multi-system emulator - GTK GUI - Balanced profile"
	depends=("higan-common>=${pkgver}" 'libananke-gtk' 'libpulse' 'libao' 'libxv' 'libgl' 'openal' 'sdl')
	provides=("higan-ui=${pkgver}-${pkgrel}")
	conflicts=('higan-qt-balanced')

	installui ethos gtk balanced
}

package_higan-gtk-performance() {
	pkgdesc="Nintendo multi-system emulator - GTK GUI - Performance profile"
	depends=("higan-common>=${pkgver}" 'libananke-gtk' 'libpulse' 'libao' 'libxv' 'libgl' 'openal' 'sdl')
	provides=("higan-ui=${pkgver}-${pkgrel}")
	conflicts=('higan-qt-performance')

	installui ethos gtk performance
}

package_higan-qt-accuracy() {
	pkgdesc="Nintendo multi-system emulator - QT GUI - Accuracy profile"
	depends=("higan-common>=${pkgver}" 'libananke-qt' 'libpulse' 'libao' 'libxv' 'libgl' 'openal' 'sdl')
	provides=("higan-ui=${pkgver}-${pkgrel}")
	conflicts=('higan-gtk-accuracy')

	installui ethos qt accuracy
}

package_higan-qt-balanced() {
	pkgdesc="Nintendo multi-system emulator - QT GUI - Balanced profile"
	depends=("higan-common>=${pkgver}" 'libananke-qt' 'libpulse' 'libao' 'libxv' 'libgl' 'openal' 'sdl')
	provides=("higan-ui=${pkgver}-${pkgrel}")
	conflicts=('higan-gtk-balanced')

	installui ethos qt balanced
}

package_higan-qt-performance() {
	pkgdesc="Nintendo multi-system emulator - QT GUI - Performance profile"
	depends=("higan-common>=${pkgver}" 'libananke-qt' 'libpulse' 'libao' 'libxv' 'libgl' 'openal' 'sdl')
	provides=("higan-ui=${pkgver}-${pkgrel}")
	conflicts=('higan-gtk-performance')

	installui ethos qt performance
}

package_higan() {
	pkgdesc="Nintendo multi-system emulator - Dummy package for AUR helpers"
	arch=('any')
	depends=("higan-ui>=${pkgver}-${pkgrel}")
	provides=('bsnes')
	install=${pkgname}.install
	optdepends=('purify: ROM purifier (currently not compatible)'
	            'beat: Delta patcher')
}

pkgdesc="Nintendo multi-system emulator"
depends=()
