# Maintainer: smx
pkgname=arp-scan-git
pkgver=1.9.r29.gf20c8d2
pkgrel=2
pkgdesc="A tool that uses ARP to discover and fingerprint IP hosts on the local network"
arch=('x86_64' 'i686')
license=('GPL3')
url="https://github.com/royhills/arp-scan"
source=(git+'https://github.com/royhills/arp-scan.git')
md5sums=('SKIP')
makedepends=('automake' 'autoconf' 'libpcap')
depends=('libpcap')
conflicts=('arp-scan')
options=('!strip' '!emptydirs')

_srcname=arp-scan

pkgver() {
  cd "${srcdir}/${_srcname}"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_srcname}"
  if [ -e "Makefile" ]; then
     make distclean
  fi
  autoreconf --install
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${_srcname}"
  make check
}

package() {
  cd "${srcdir}/${_srcname}"
  make DESTDIR="$pkgdir" install
}
