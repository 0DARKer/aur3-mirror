# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth-git
pkgver=20130714.5f8a1dc
pkgrel=1
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth (GIT version)"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPLv2.1')
depends=('ffmpeg' 'python' 'qt4')
makedepends=('cython' 'git' 'waf' 'yasm' 'python-sphinx')
provides=('vapoursynth')
conflicts=('vapoursynth')
source=('git://github.com/vapoursynth/vapoursynth.git')
md5sums=('SKIP')
_gitname='vapoursynth'

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  mkdir doc/_static
}

build() {
  cd "${_gitname}"

  msg2 "Build Vapoursynth"
  waf configure --prefix="/usr" --docs="true" --translate --filters="true" --mode="release" --examples="true" --pipe="true" --script="true" --core="true"
  waf build ${MAKEFLAGS}
  python setup.py build
}

# check() {
#   cd "${_gitname}"
#   waf test
# }

package() {
  cd "${_gitname}"
  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 test/test.py "${pkgdir}/usr/share/vapoursynth/test/test.py"
  install -Dm644 test/filter_test.py "${pkgdir}/usr/share/vapoursynth/test/filter_test.py"
}
