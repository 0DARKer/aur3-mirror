# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth-git
pkgver=r23.0.gc6047f3
pkgrel=2
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth (GIT version)"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL2.1' 'custom:OFL')
depends=('ffmpeg' 'tesseract' 'python')
makedepends=('cython' 'git' 'yasm' 'python-sphinx')
provides=('vapoursynth')
conflicts=('vapoursynth')
source=('git+http://github.com/vapoursynth/vapoursynth.git'
        "https://waf.googlecode.com/files/"waf-1.7.15{,.sig})
sha1sums=('SKIP'
          'c5c2ed76b72a81ee0154265cbb55d6c7cdce434f'
          'SKIP')
_gitname='vapoursynth'
_python_ver="$(python -c "import sys; print('%d.%d' % sys.version_info[:2])")"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - . | tr R r)"
}

prepare() {
  cd "${_gitname}"
#  ./bootstrap.py
  cp ../waf-1.7.15 waf
  chmod +x waf
  mkdir doc/_static
  rm -fr _test
}

build() {
  cd "${_gitname}"
  ./waf --prefix="/usr" --docs="true" --filters="true" --mode="release" --examples="true" --pipe="true" --script="true" --core="true" configure build
  python setup.py build
}

check() {
  cd "${_gitname}"
  LD_LIBRARY_PATH="${srcdir}/${_gitname}/build" PYTHONPATH="${srcdir}/${_gitname}/build/lib.linux-${CARCH}-${_python_ver}" ./waf test
}

package() {
  cd "${_gitname}"
  ./waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 ofl.txt "${pkgdir}/usr/share/licenses/${pkgname}/ofl.txt"
}
