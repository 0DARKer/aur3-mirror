# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth-git
pkgver=r22.31.g8434551
pkgrel=1
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth (GIT version)"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL2.1' 'custom:OFL')
depends=('ffmpeg' 'tesseract' 'python')
makedepends=('cython' 'git' 'yasm' 'python-sphinx')
provides=('vapoursynth')
conflicts=('vapoursynth')
source=('git+http://github.com/vapoursynth/vapoursynth.git')
md5sums=('SKIP')
_gitname='vapoursynth'

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - . | tr R r)"
}

prepare() {
  cd "${_gitname}"
  ./bootstrap.py
  mkdir doc/_static
  rm -fr _test
}

build() {
  cd "${_gitname}"
  ./waf configure --prefix="/usr" --docs="true" --filters="true" --mode="release" --examples="true" --pipe="true" --script="true" --core="true"
  ./waf build
  python setup.py build
}

check() {
  cd "${_gitname}"
  LD_LIBRARY_PATH="${srcdir}/${_gitname}/build" PYTHONPATH="${srcdir}/${_gitname}/build/lib.linux-${CARCH}-3.3" ./waf test
}

package() {
  cd "${_gitname}"
  ./waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 ofl.txt "${pkgdir}/usr/share/licenses/${pkgname}/ofl.txt"
}
