# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth-git
pkgver=r24.13.gf56246e
pkgrel=1
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth (GIT version)"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL2.1' 'custom:OFL')
depends=('ffmpeg' 'tesseract' 'python' 'imagemagick-no-hdri')
makedepends=('cython' 'git' 'yasm' 'python-sphinx')
provides=('vapoursynth')
conflicts=('vapoursynth')
source=('git+http://github.com/vapoursynth/vapoursynth.git'
        'vapoursynth.xml')
sha1sums=('SKIP'
          '71e04f8a7c177b44e4ce93e5eecbb4242a3c2215')
_gitname='vapoursynth'
install=vapoursynth-git.install
# _python_ver="$(python -c "import sys; print('%d.%d' % sys.version_info[:2])")"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - . | tr R r)"
}

prepare() {
  cd "${_gitname}"
  ./bootstrap.py
  mkdir doc/_static
  rm -fr _test
}

build() {
  cd "${_gitname}"
  ./waf --prefix="/usr" --enable-examples configure build --enable-imwri
  python setup.py build
}

# Disable unittest https://github.com/vapoursynth/vapoursynth/issues/100
# check() {
#   cd "${_gitname}"
#   LD_LIBRARY_PATH="${srcdir}/${_gitname}/build" PYTHONPATH="${srcdir}/${_gitname}/build/lib.linux-${CARCH}-${_python_ver}" ./waf test
# }

package() {
  cd "${_gitname}"
  install -Dm644 ../vapoursynth.xml "${pkgdir}/usr/share/mime/packages/vapoursynth.xml"
  ./waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 ofl.txt "${pkgdir}/usr/share/licenses/${pkgname}/ofl.txt"
}
