# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth-git
pkgver=r25.34.gdf98144
pkgrel=1
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth (GIT version)"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL2.1' 'custom:OFL')
depends=('ffmpeg' 'tesseract' 'python' 'imagemagick-no-hdri')
makedepends=('cython' 'git' 'yasm' 'python-sphinx')
provides=('vapoursynth')
conflicts=('vapoursynth')
source=('git+http://github.com/vapoursynth/vapoursynth.git'
        'vapoursynth.xml')
sha1sums=('SKIP'
          '71e04f8a7c177b44e4ce93e5eecbb4242a3c2215')
_gitname='vapoursynth'
install=vapoursynth-git.install

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - . | tr R r)"
}

prepare() {
  cd "${_gitname}"
  ./autogen.sh
  mkdir -p doc/_static
}

build() {
  cd "${_gitname}"
  ./configure --prefix="/usr" --enable-imwri
  make
  make -C doc man
}

package() {
  make -C "${_gitname}" DESTDIR="${pkgdir}" install
  install -Dm644 "${_gitname}/doc/_build/man/vapoursynth.1" "${pkgdir}/usr/share/man/man1/vapoursynth.1"
  install -Dm644 "${_gitname}/doc/_build/man/vspipe.1" "${pkgdir}/usr/share/man/man1/vspipe.1"
  install -Dm644 vapoursynth.xml "${pkgdir}/usr/share/mime/packages/vapoursynth.xml"
  install -Dm644 "${_gitname}/ofl.txt" "${pkgdir}/usr/share/licenses/${pkgname}/ofl.txt"
}
