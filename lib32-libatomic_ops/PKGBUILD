# Maintainer:  Jesse   Jaara      <gmail.com: jesse.jaara>
# Contributor: Jan     de Groot   <jgc@archlinux.org>
# Contributor: Adam    Walters    <abwalters@abwalters.us>
# Contributor: William Rea        <sillywilly@gmail.com>
# Contributor: RÃ©my    Oudompheng <remy@archlinux.org>

pkgname=lib32-libatomic_ops
pkgver=7.2.e
pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('i686' 'x86_64')
url="https://github.com/ivmai/libatomic_ops"
depends=('lib32-glibc')
license=('GPL2' 'MIT')
source=(http://www.hpl.hp.com/research/linux/atomic_ops/download/libatomic_ops-7.2e.tar.gz)
md5sums=('7035692fec4db2659b06485040829e43')

prepare(){
  cd libatomic_ops-7.2

  sed -i 's#AM_CONFIG_HEADER#AC_CONFIG_HEADERS#' configure.ac
  sed -i 's#AC_PROG_RANLIB#AC_LIBTOOL_DLOPEN\nAC_PROG_LIBTOOL#' configure.ac
  sed -i 's#b_L#b_LTL#;s#\.a#.la#g;s#_a_#_la_#' src/Makefile.am
  sed -i 's#\.a#.so#g;s#\.\./src/#../src/.libs/#g' tests/Makefile.am
  sed -i 's#pkgdata#doc#' doc/Makefile.am
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd libatomic_ops-7.2
  autoreconf -fi
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

check() {
  cd libatomic_ops-7.2
  LD_LIBRARY_PATH="${srcdir}/libatomic_ops-7.2/src/.libs" make check
}

package() {
  cd libatomic_ops-7.2
  make DESTDIR="${pkgdir}" install
  install -D -m644 doc/LICENSING.txt "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"

  rm -r "${pkgdir}/usr/share/doc"
  rm -r "${pkgdir}/usr/include"
}
