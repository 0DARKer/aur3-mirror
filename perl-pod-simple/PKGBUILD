# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

_perlmod=Pod-Simple
pkgname=perl-pod-simple
pkgver=3.23
pkgrel=1
pkgdesc="Pod::Simple - the framework for parsing Pod"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl>=5.10.0')
source=("http://cpan.perl.org/modules/by-module/Pod/$_perlmod-$pkgver.tar.gz")
sha512sums=('0b64f35a2ea2fda131be935518d6b8c74c90b9c9d179b44dcab8317b800b7ff35c602967163e13f0ea932dba985065e7c79651901e548188079641ca3149348c')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
