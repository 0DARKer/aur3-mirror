# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-pod-simple
_pkgname=Pod-Simple
pkgver=3.20
pkgrel=1
pkgdesc="Pod::Simple - the framework for parsing Pod"
arch=('any')
url="http://search.cpan.org/dist/$_pkgname"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
source=("http://search.cpan.org/CPAN/authors/id/D/DW/DWHEELER/$_pkgname-$pkgver.tar.gz")
sha512sums=('f8c06d88038315f9e6dca5c45057d57bc795b93ea1df4b46865c6666eef50c04e90a831998ec2e764138cd67b17019680af195a59fc403c86dbac84227e058c6')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/$_pkgname-$pkgver"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
