# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-pod-simple
_pkgname=Pod-Simple
pkgver=3.22
pkgrel=1
pkgdesc="Pod::Simple - the framework for parsing Pod"
arch=('any')
url="http://search.cpan.org/dist/$_pkgname"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl>=5.10.0')
source=("http://search.cpan.org/CPAN/authors/id/D/DW/DWHEELER/$_pkgname-$pkgver.tar.gz")
sha512sums=('f7804310b343e483f397fc8005cab4f6d7fa080dbe7ff21726943022c124a518f1d5db9b1c7a9de860a3e3d2631109b895920b589f0433eff150966eb7f07bb8')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
