# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-pod-simple
_pkgname=Pod-Simple
pkgver=3.19
pkgrel=3
pkgdesc="Pod::Simple - the framework for parsing Pod"
arch=('any')
url="http://search.cpan.org/dist/$_pkgname"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
source=("http://search.cpan.org/CPAN/authors/id/D/DW/DWHEELER/$_pkgname-$pkgver.tar.gz")
sha512sums=('dbe1c2b9456c322b45d48b8e405586a7fe11def4f19c3ed0d3bfc95303029dd4a51ebd350480736d5c09d8d8d665d35089d8511bd80922e7eadd3bca5c58f62f')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
