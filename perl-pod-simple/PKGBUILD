# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

_perlmod=Pod-Simple
_modnamespace=Pod
pkgname=perl-pod-simple
pkgver=3.26
pkgrel=1
pkgdesc="Pod::Simple - the framework for parsing Pod"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl>=5.10.0')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('8deee5e226064029bc16cc5e25373962914d1ff0724e11b7baaa55aeaa18bb00b3a0bae435df6950f430970d7b7f1aaef27380e786bc8f9e3370c0daae636ffa')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	make test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	make install DESTDIR="$pkgdir/"
}
