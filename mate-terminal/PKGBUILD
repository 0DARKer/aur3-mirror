# Maintainer: Perberos <perberos@gmail.com>
pkgname=mate-terminal
pkgver=20120309
pkgrel=1
pkgdesc="The MATE Terminal Emulator"
arch=('i686' 'x86_64')
license=('GPL')
depends=('mate-conf' 'vte' 'libmate' 'libsm')
makedepends=('git' 'pkgconfig' 'mate-doc-utils' 'intltool')
options=('!emptydirs' '!libtool')
install=mate-terminal.install
url="http://matsusoft.com.ar/projects"
groups=('mate-extras')
source=()
sha256sums=()

_gitroot=git://github.com/mate-desktop/mate-terminal.git
_gitname=mate-terminal

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"

	./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static \
		--localstatedir=/var --disable-scrollkeeper --with-gnu-ld || return 1
	make || return 1
}

package() {
	cd "$srcdir/$_gitname-build"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
