# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-git
pkgver=r786.5937a76
pkgrel=1
pkgdesc='KIO'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kio'
license=('LGPL')
depends=('kbookmarks-git' 'kjobwidgets-git' 'kwallet-git' 'solid-git' 'libxslt')
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git')
groups=('kf5')
conflicts=(kio)
provides=(kio)
install=kio.install
source=('git://anongit.kde.org/kio.git')
md5sums=('SKIP')

pkgver() {
  cd kio
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kio \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
