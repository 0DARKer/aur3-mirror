# Maintainer: garion < garion @ mailoo.org >
# Contributor: PyroPeter < googlemail.com @ abi1789 >

pkgname=hplip-plugin
pkgver=3.12.11
pkgrel=1
pkgdesc="Binary plugin for HPs hplip printer driver library"
arch=('i686' 'x86_64')
url="http://hplipopensource.com/"
license=('custom:proprietary')
depends=(hplip)
source=("http://www.openprinting.org/download/printdriver/auxfiles/HP/plugins/hplip-$pkgver-plugin.run")
md5sums=('71ca43371bec133edebd29affcb8ecae')
optdepends=("pyqt3: needed for hp-setup's qt3 gui"
            "python2-pyqt: needed for hp-setup's qt4 gui")

build(){
    cd $srcdir
    # Untargziping the makeself selfextracting archive
    sh hplip-$pkgver-plugin.run --tar xvf
}

package() {
    if [ $CARCH = "i686" ]; then
        _arch='x86_32'
    elif [ $CARCH = "x86_64" ]; then
        _arch='x86_64'
    fi

    # Create folders
    install -d $pkgdir/usr/lib/udev/rules.d
    install -d $pkgdir/usr/share/hplip/data/firmware
    install -d $pkgdir/usr/share/hplip/fax/plugins
    install -d $pkgdir/usr/share/hplip/prnt/plugins
    install -d $pkgdir/usr/share/hplip/scan/plugins
    install -d $pkgdir/usr/share/licenses/hplip-plugin

    # Copy files
    cd $srcdir
    install -m644 86-hpmud-hp_laserjet_*.rules $pkgdir/usr/lib/udev/rules.d/
    install -m755 hp_laserjet_*.fw.gz          $pkgdir/usr/share/hplip/data/firmware/
    install -m755 fax_marvell-"$_arch".so      $pkgdir/usr/share/hplip/fax/plugins/
    install -m755 lj-"$_arch".so               $pkgdir/usr/share/hplip/prnt/plugins/
    install -m755 bb_*-"$_arch".so             $pkgdir/usr/share/hplip/scan/plugins/
    install -m644 license.txt                  $pkgdir/usr/share/licenses/hplip-plugin/

    # Create symlinks
    for f in $(find $pkgdir/usr/share/hplip -type f -name "*.so"); do
        cd $pkgdir/usr/share/hplip
        cd $(dirname $f)
        link_name="$(basename $f | cut -d- -f1).so"
        ln -s $(basename $f) $link_name
    done
}
