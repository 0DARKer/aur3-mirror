# Contributor: Graziano Giuliani <giuliani@lamma.rete.toscana.it>
# Contributor: Frank Sch√§ffer <frank.schaeffer@o2online.de>
# Contributor: rememberthermer <rememberthermer_AT_GMAIL_DOT_COM>
# Maintainer:  Chris Finlay <christopher.finlay@gmail.com>
pkgname=nco
pkgver=4.2.5
pkgrel=1
pkgdesc="command-line operators to analyze and mnipulate netCDF files"
url="http://nco.sourceforge.net/"
license=('GPL3' 'FDL1.3')
depends=('netcdf' 'udunits' 'gsl' )
makedepends=('antlr2')
arch=('i686' 'x86_64')
options=('!libtool' '!makeflags')
source=(http://nco.sourceforge.net/src/${pkgname}-${pkgver}.tar.gz)
install=$pkgname.install

build() {
    cd $pkgname-$pkgver
    NETCDF_INC="$(nc-config --includedir)" \
    NETCDF4_ROOT="$(nc-config --prefix)" \
        ./configure --prefix=/usr --enable-netcdf4
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
    if [ -n "$(echo ${OPTIONS[@]} | grep ' docs ')" ]; then  
        make DESTDIR="$pkgdir" install-html
    fi
}
md5sums=('8bb89217ac1dadb62679d1b08ea19025')
