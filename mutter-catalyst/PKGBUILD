# $Id$
# Maintainer: Christian Assfalg <ch.assfalg_at_gmx_dot_de>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

pkgname=mutter-catalyst
pkgver=3.16.1.1
pkgrel=1
pkgdesc="A window manager for GNOME with patch for catalyst compatibility"
arch=(i686 x86_64)
license=('GPL')
depends=('clutter' 'dconf' 'gobject-introspection-runtime' 'gsettings-desktop-schemas' 'libcanberra' 'startup-notification' 'zenity' 'libsm' 'gnome-desktop' 'upower' 'libxkbcommon-x11' 'gnome-settings-daemon')
makedepends=('intltool' 'libxkbcommon-x11' 'gobject-introspection')
conflicts=('mutter-wayland' 'mutter')
replaces=('mutter-wayland')
provides=('mutter=3.16.1.1')
url="http://www.gnome.org"
groups=('gnome')
options=('!emptydirs')
install=mutter.install
source=(http://ftp.gnome.org/pub/gnome/sources/mutter/${pkgver:0:4}/mutter-$pkgver.tar.xz
        'catalyst-comment.patch' )
sha256sums=('795084f6b642ab45e9f73b82b185949a110f2149bc0bce9f70ff30c8ab4c051c'
            'fe058744aadcf87cb92a9268beddbb1a501631e02b335a58c593b36c8b47d7d9')

build() {
  cd "mutter-$pkgver"

  #https://bugzilla.gnome.org/show_bug.cgi?id=741581
  echo "Commenting out call to function with XRRChangeOutputProperty to fix issue with catalyst"
  patch -p1 < ../catalyst-comment.patch
  echo "Patch applied"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/mutter \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile --enable-compile-warnings=minimum

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "mutter-$pkgver"
  make DESTDIR="$pkgdir" install
}
