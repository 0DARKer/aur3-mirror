# Contributor: Vojtech Horky <vojta . horky at-symbol seznam . cz>
# based on vmware-server-console and bin32-vmware-server PKGBUILDs

pkgname=bin32-vmware-server-console
pkgver=1.0.10
pkgrel=2
pkgdesc="Remote console for VMware Server"
arch=('x86_64')
url="http://www.vmware.com/products/server/"
license=('custom')
depends=('lib32-glibmm' 'lib32-libgnomecanvas' 'lib32-libxdamage' 'lib32-librsvg' 'lib32-libxt' 'lib32-libxtst' 'lib32-libstdc++5' 'perl')
options=('!strip' '!libtool')
install=bin32-vmware-server-console.install
source=(http://download3.vmware.com/software/vmserver/VMware-mui-${pkgver}-203137.tar.gz
        vmware-server-console.desktop
		wrapper-gtk24.patch)
md5sums=('0f01e9bdeee3fa2aa84f87f66b69dc83'
         'd5f9319812fffe7bd832a64e1f333231'
         'c1b8e3eab95e6b1640e49422b38c3b3a')

build() {
  # unpack the server console package
  cd "$srcdir/vmware-mui-distrib/console-distrib"
  tar xzf VMware-server-console-${pkgver}-203137.tar.gz || return 1
  
  msg "Patching because of 32bit libraries..."
  cd "$srcdir/vmware-mui-distrib/console-distrib/vmware-server-console-distrib/lib/lib"
  patch -Np1 -i "$srcdir/wrapper-gtk24.patch" || return 1
  
  cd "$srcdir/vmware-mui-distrib/console-distrib/vmware-server-console-distrib"

  # Install binary files
  mkdir -p "$pkgdir/usr/bin" || return 1
  install -m755 bin/* "$pkgdir/usr/bin" || return 1

  # Install libs
  mkdir -p "$pkgdir/usr/lib/vmware-server-console" || return 1
  cp -a lib/* "$pkgdir/usr/lib/vmware-server-console" || return 1
  chmod -R u+w "$pkgdir/usr/lib/vmware-server-console" || return 1
  

  # Install configuration files
  install -D -m755 etc/installer.sh "$pkgdir/etc/vmware-server-console/installer.sh" || return 1

  # Install man page
  install -D -m644 man/man1/vmware-server-console.1.gz \
    "$pkgdir/usr/share/man/man1/vmware-server-console.1.gz" || return 1

  # Install license
  mkdir -p "$pkgdir/usr/share/licenses/vmware-server-console" || return 1
  install -m644 doc/{EULA,open_source_licenses.txt} \
    "$pkgdir/usr/share/licenses/vmware-server-console" || return 1

  # Install desktop file
  install -D -m644 doc/icon48x48.png \
    "$pkgdir/usr/share/pixmaps/vmware-server-console.png" || return 1
  install -D -m644 "$srcdir/vmware-server-console.desktop" \
    "$pkgdir/usr/share/applications/vmware-server-console.desktop" || return 1

  msg "Another hack because of 32bit libraries..."
  # Creating some symbolic links to shipped libs
  # (needed because current version of these libs in repository is newer than the required dependency)
  mkdir -p "$pkgdir/opt/lib32/usr/lib/" || return 1
  for libfile in libcrypto.so.0.9.7 libexpat.so.0 libsexymm.so.1 libsexy.so.1 libssl.so.0.9.7; do
    ln -s "/usr/lib/vmware-server-console/lib/$libfile/$libfile" "$pkgdir/opt/lib32/usr/lib/$libfile"
  done
}

