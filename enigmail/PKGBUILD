# Maintainer: Alexander Fehr <pizzapunk gmail com>
# Contributor: Thomas Jost <schnouki schnouki net>
# Contributor: Hinrich Harms <arch hinrich de>

pkgname=enigmail
pkgver=1.4.2
pkgrel=1
_tbver=12.0.1
pkgdesc="Thunderbird extension that enables sending and receiving signed and encrypted e-mail messages"
arch=('i686' 'x86_64')
url="http://enigmail.mozdev.org/"
license=('MPL' 'GPL')
depends=('thunderbird' 'gnupg')
makedepends=('zip' 'unzip' 'python2')
source=(http://www.mozilla-enigmail.org/download/source/enigmail-$pkgver.tar.gz
        http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/$_tbver/source/thunderbird-$_tbver.source.tar.bz2
        http://www.linuxfromscratch.org/patches/blfs/svn/thunderbird-12.0_gcc-4.7-1.patch
        mozconfig)
md5sums=('ed608e1cd4cd20b96f7f5afdbf081141'
         '64cacde4cb2b1e8736f1c3a0ea6a02db'
         '36a7dff23f4b8ee6bf0b92dc0cb15bb7'
         'b3991092bd3d4692ec91374cb5e6c361')

build() {
  # Compile the required parts of Thunderbird
  cd "$srcdir"/comm-release
  patch -Np1 -i "$srcdir"/thunderbird-12.0_gcc-4.7-1.patch
  cp "$srcdir"/mozconfig .mozconfig
  export PYTHON=/usr/bin/python2
  make -f client.mk build

  # Compile Enigmail
  mv "$srcdir"/enigmail "$srcdir"/comm-release/mailnews/extensions
  cd "$srcdir"/comm-release/mailnews/extensions/enigmail
  ./makemake -r -o "$srcdir"/comm-release/obj-enigmail
  cd "$srcdir"/comm-release/obj-enigmail/mailnews/extensions/enigmail
  make

  # Create the Enigmail XPI
  make xpi
}

package() {
  cd "$srcdir"/comm-release

  emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' mailnews/extensions/enigmail/package/install.rdf)

  install -d -m755 "$pkgdir"/usr/lib/thunderbird/extensions/$emid
  cd "$pkgdir"/usr/lib/thunderbird/extensions/$emid

  unzip "$srcdir"/comm-release/obj-enigmail/mozilla/dist/bin/enigmail-*.xpi
}
