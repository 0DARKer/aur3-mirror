# Maintainer: Mijo Medvedec <mijo dot medvedec at gmail dot com>

pkgname=qmapshack
pkgver=0.12.0
pkgrel=1
pkgdesc="Application for handling offline and online maps as well as GIS data like waypoints and tracks"
arch=('i686' 'x86_64')
url="https://bitbucket.org/maproom/qmapshack/wiki/Home"
license=('GPL3')
depends=('qt5-base' 'qt5-tools' 'qt5-script' 'qt5-webkit' 'gdal' 'proj')
makedepends=('cmake')
source=("https://bitbucket.org/maproom/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
md5sums=('186d009f69e700c262ae2e144c8078a1')
sha256sums=('c72a0c37f434d0864dbda008b9b04d218d02a694bc1c9953801b18ac66f9e9a8')

build() {
	cd "$pkgname-$pkgver"
	mkdir build && cd build
	
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
	      -DCMAKE_BUILD_TYPE=Release \
	      -DQT_QMAKE_EXECUTABLE=qmake-qt5 \
	..

	make
}

package() {
	cd "$pkgname-$pkgver/build"
	make DESTDIR="$pkgdir/" install
}
