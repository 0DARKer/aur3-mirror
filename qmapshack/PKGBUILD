# Maintainer: Mijo Medvedec <mijo dot medvedec at gmail dot com>

pkgname=qmapshack
pkgver=1.0.1
pkgrel=1
pkgdesc="Application for handling offline and online maps as well as GIS data like waypoints and tracks"
arch=('i686' 'x86_64')
url="https://bitbucket.org/maproom/qmapshack/wiki/Home"
license=('GPL3')
depends=('qt5-base' 'qt5-tools' 'qt5-script' 'qt5-webkit' 'gdal' 'proj')
makedepends=('cmake')
source=("https://bitbucket.org/maproom/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
md5sums=('6cff04de8a701b48ef251c9aa4796288')
sha256sums=('a4c3d2752b0a373c951f7a2bfd3e1404cea4fecf26c2ef28bce787333f1003c1')

build() {
	cd "$pkgname-$pkgver"
	mkdir build && cd build
	
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
	      -DCMAKE_BUILD_TYPE=Release \
	      -DQT_QMAKE_EXECUTABLE=qmake-qt5 \
	..

	make
}

package() {
	cd "$pkgname-$pkgver/build"
	make DESTDIR="$pkgdir/" install
}
