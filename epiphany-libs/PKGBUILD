# Maintainer: Jonathan Scott Tinkham <sctincman@gmail.com>

pkgname=epiphany-libs
pkgver=5.13.09.10
pkgrel=1
pkgdesc='Adapteva Epiphany runtime libraries and utilities'
arch=(i686 x86_64)
url='http://nwww.adapteva.com/'
license=(GPL)
depends=(epiphany-elf-gcc)
provides=(epiphany-libs)
conflicts=(epiphany-libs)
makedepends=()
options=(staticlibs !strip !buildflags)
source=("https://github.com/adapteva/$pkgname/archive/esdk.$pkgver.tar.gz"
	"arch-release.patch"
	"epiphany_hdf.sh")
md5sums=('c9b07b80b00eb7e9793c4306fbdcf40e'
         'dca74cf404bcea9e21a5e2419c49f437'
	 'e16b733e950b5a760fe2543781dc0b1c')

prepare() {
    cd $srcdir/$pkgname-esdk.$pkgver

    patch -Np1 < ../arch-release.patch
}

build() {
    cd $srcdir/$pkgname-esdk.$pkgver

    ./build-libs.sh 1 2 3 5 6
}

package() {
    cd $srcdir/$pkgname-esdk.$pkgver

    # Install bsps, link "current" to E16 parallella
    # This is what is selected by parallella-firmware
    # Hopefully, there'll be a way to select the current plat
    install -d -m 0755 $pkgdir/usr/share/$pkgname
    cp -r bsps $pkgdir/usr/share/$pkgname
    ln -sTf parallella_E16G3_1GB $pkgdir/usr/share/$pkgname/bsps/current
    chmod -R a+rwX $pkgdir/usr/share/$pkgname/bsps
    chmod -R og-w $pkgdir/usr/share/$pkgname/bsps

    # Install e-xml
    install -d -m 0755 $pkgdir/usr/lib
    install -m 0755 src/e-xml/Release/libe-xml.so $pkgdir/usr/lib

    # Install e-hal
    install -d -m 0755 $pkgdir/usr/include
    install -m 0644 \
	src/e-hal/src/epiphany-hal.h \
	src/e-hal/src/epiphany-hal-data.h \
	src/e-hal/src/epiphany-hal-data-local.h \
	src/e-hal/src/epiphany-hal-api.h \
	$pkgdir/usr/include
    ln -s epiphany-hal.h $pkgdir/usr/include/e-hal.h
    ln -s epiphany-hal.h $pkgdir/usr/include/e_hal.h
    ln -s /usr/share/$pkgname/bsps/current/libe-hal.so $pkgdir/usr/lib/libe-hal.so

    # Install e-loader
    install -m 0644 src/e-loader/src/e-loader.h $pkgdir/usr/include
    ln -s e-loader.h $pkgdir/usr/include/e_loader.h
    install -m 0755 src/e-loader/Release/libe-loader.so $pkgdir/usr/lib

    # Install e-server
    install -d -m 0755 $pkgdir/usr/bin
    #install -m 0755 src/e-server/Release/e-server $pkgdir/usr/bin/e-server.e
    #install -m 0755 src/e-server/e-server.sh $pkgdir/usr/bin/e-server

    # Install utilities
    cd src/e-utils
    install -m 0755 e-reset/e-reset $pkgdir/usr/bin/e-reset.e
    install -m 0755 e-reset/e-reset.sh $pkgdir/usr/bin/e-reset
    install -m 0755 e-loader/Debug/e-loader $pkgdir/usr/bin/e-loader.e
    install -m 0755 e-loader/e-loader.sh $pkgdir/usr/bin/e-loader
    install -m 0755 e-read/Debug/e-read $pkgdir/usr/bin/e-read.e
    install -m 0755 e-read/e-read.sh $pkgdir/usr/bin/e-read
    install -m 0755 e-write/Debug/e-write $pkgdir/usr/bin/e-write.e
    install -m 0755 e-write/e-write.sh $pkgdir/usr/bin/e-write
    install -m 0755 e-hw-rev/e-hw-rev $pkgdir/usr/bin/e-hw-rev.e
    install -m 0755 e-hw-rev/e-hw-rev.sh $pkgdir/usr/bin/e-hw-rev
    install -m 0755 e-objcopy $pkgdir/usr/bin
    cd ../..

    # Install e-lib
    install -d -m 0755 $pkgdir/usr/lib/esdk
    install -d -m 0755 $pkgdir/usr/include/esdk
    cd src/e-lib
    install -m 0755 Release/libe-lib.a $pkgdir/usr/lib/esdk
    ln -s libe-lib.a $pkgdir/usr/lib/esdk/libelib.a
    install -m 0644 \
	include/e_common.h \
	include/e_coreid.h \
	include/e_ctimers.h \
	include/e_dma.h \
	include/e_ic.h \
	include/e_lib.h \
	include/e_mem.h \
	include/e_mutex.h \
	include/e_regs.h \
	include/e_types.h \
	$pkgdir/usr/include/esdk
    ln -s e_lib.h $pkgdir/usr/include/esdk/e-lib.h
    cd ../..

    #add EPIPHANY_HDF to profile for all users
    install -d -m 0755 $pkgdir/etc/profile.d
    install -m 0755 $srcdir/epiphany_hdf.sh $pkgdir/etc/profile.d

}

