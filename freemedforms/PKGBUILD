# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=freemedforms
pkgver=0.7.6
pkgrel=1
pkgdesc="Electronic Medical Record Manager"
arch=(i686 x86_64)
url="http://www.freemedforms.com/en/"
license=('GPL3')
depends=('qt')
install="$pkgname.install"
source=("http://freemedforms.googlecode.com/files/${pkgname}fullsources-$pkgver.tgz")
md5sums=('e4ba2e15eaa5e2e27d088e61077065c7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake $pkgname.pro -r -config release \
    "CONFIG+=LINUX_INTEGRATED" \
    "INSTALL_ROOT_PATH=$pkgdir/usr/" \
    "LOWERED_APPNAME=$pkgname"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make install

  # fix folders exec bit
  chmod a+x "$pkgdir"/usr/share/{$pkgname/forms,doc/$pkgname-project/$pkgname/{en,fr}}
}

# vim:set ts=2 sw=2 et:
