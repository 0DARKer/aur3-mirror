# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=freemedforms
pkgver=0.6.0
pkgrel=1
pkgdesc="Electronic Medical Record Manager"
arch=(i686 x86_64)
url="http://www.freemedforms.com/en/"
license=('GPL')
depends=('qt')
install="$pkgname.install"
source=("http://freemedforms.googlecode.com/files/${pkgname}fullsources-$pkgver.tgz")
md5sums=('7ded8fcc5cd86efa6ae0d578c843bac7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake $pkgname.pro -r -config release \
    "CONFIG+=LINUX_INTEGRATED" \
    "INSTALL_ROOT_PATH=$pkgdir/usr/" \
    "LOWERED_APPNAME=$pkgname"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make install

  # Fix doc directories exec bit
  chmod a+x "$pkgdir"/usr/share/$pkgname/{forms,doc/$pkgname/{en,fr}}

  # man
  install -Dm644 buildspecs/freemedforms_debian/$pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1"
}

# vim:set ts=2 sw=2 et:
