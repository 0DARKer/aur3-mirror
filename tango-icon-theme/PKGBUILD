# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>

pkgname=tango-icon-theme
pkgver=0.8.90
pkgrel=7
pkgdesc="An icon theme that follows the Tango visual guidelines"
arch=('any')
url="http://tango.freedesktop.org"
license=('custom:public domain' 'custom:TRADEMARKS')
makedepends=('imagemagick' 'icon-naming-utils' 'intltool' 'librsvg')
options=(!strip !zipman)
source=(${url}/releases/${pkgname}-${pkgver}.tar.bz2
        archlinux-icon.svg
        TRADEMARKS
        rsvg.patch)
md5sums=('b7b9b16480afb781a4c13f8bceb8688b'
         '34c9f7d5774e93f4a6cbf1ec818fb763'
         '05066419eb8239652a0467131485db4d'
         '46f86ad43d793cd5389a7bd9c58a57ae')

build() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "${srcdir}/rsvg.patch"
  autoreconf -fi
  ./configure --prefix=/usr --enable-png-creation
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 ../TRADEMARKS "${pkgdir}/usr/share/licenses/${pkgname}/TRADEMARKS"

  # Replace default logo with Arch Linux's
  # Create icon for category "Education"
  cd "${pkgdir}/usr/share/icons/Tango"
  for size in 16 22 24 32 48 64 72 96 128; do
    rsvg-convert -w ${size} -h ${size} "${srcdir}/archlinux-icon.svg" \
     -o "${size}x${size}/places/start-here.png"
    ln -s "../status/dialog-information.png" \
      "${size}x${size}/categories/applications-science.png"
  done
  install -D -m644 "${srcdir}/archlinux-icon.svg" \
    "scalable/places/start-here.svg"
  ln -s "../status/dialog-information.svg" \
    "scalable/categories/applications-science.svg"
}