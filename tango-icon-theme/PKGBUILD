# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>

pkgname=tango-icon-theme
pkgver=0.8.90
pkgrel=5
pkgdesc="An icon theme that follows the Tango visual guidelines"
arch=('any')
url="http://tango.freedesktop.org"
license=('custom:public domain' 'custom:TRADEMARKS')
makedepends=('imagemagick' 'icon-naming-utils' 'intltool' 'librsvg')
options=(!strip)
source=(http://ftp.debian.org/debian/pool/main/t/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz
        archlinux-icon-crystal-256.svg
        TRADEMARKS)
md5sums=('0795895d2f20eddcbd2bffe94ed431a6'
         '34c9f7d5774e93f4a6cbf1ec818fb763'
         '05066419eb8239652a0467131485db4d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-png-creation
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Replace default logo with Arch Linux's
  for size in 16 22 24 32 48 64 72 96 128; do
    rsvg -w $size -h $size "$srcdir/archlinux-icon-crystal-256.svg" \
      "$pkgdir/usr/share/icons/Tango/${size}x${size}/places/start-here.png"
  done
  install -D -m644 "$srcdir/archlinux-icon-crystal-256.svg" \
    "$pkgdir/usr/share/icons/Tango/scalable/places/start-here.svg"

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 $srcdir/TRADEMARKS "${pkgdir}/usr/share/licenses/$pkgname/TRADEMARKS"
}
