# Maintainer: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: sqwishy <sqwishybon@gmail.com>
# Contributor: Duologic

pkgname=links-g-directfb
_pkgname=links
pkgver=2.3
pkgrel=1
pkgdesc="A text WWW browser, similar to Lynx, with framebuffer and graphics enabled (also with directfb)"
arch=('i686' 'x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'zlib' 'openssl' 'gpm' 'libtiff' 'libpng' 'directfb')
conflicts=('links')
provides=("links=${pkgver}")
source=("http://links.twibright.com/download/${_pkgname}-${pkgver}.tar.bz2")
md5sums=('1dc17862e766cec7c544166186e7167b')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  (cd intl; ./gen-intl; ./synclang)
  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --enable-javascript \
      --enable-graphics \
      --with-ssl \
      --with-fb \
      --with-directfb
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
