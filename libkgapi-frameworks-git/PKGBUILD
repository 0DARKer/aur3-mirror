# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libkgapi-frameworks-git
pkgver=r652.2f7ca42
pkgrel=1
pkgdesc="A KDE-based library for accessing various Google services via their public API. KF5 Frameworks branch (GIT version)"
url='http://www.dvratil.cz/category/akonadi-google/'
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdepimlibs-git')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('libkgapi')
source=("git://anongit.kde.org/libkgapi#branch=frameworks")
sha1sums=('SKIP')
_gitname=libkgapi

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  rm -fr build
  mkdir -p build
}

build() {
  cd build

  cmake "../${_gitname}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  make -C build DESTDIR=${pkgdir} install
}

