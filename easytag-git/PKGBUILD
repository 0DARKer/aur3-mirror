# Maintainer: rtfreedman (rob<d0t>til<d0t>freedman<aT>gmail<d0t>com

pkgname=easytag-git
_gitname=easytag
pkgver=393.4c3e8b9
pkgrel=2
pkgdesc="Utility for viewing and editing tags for most audio formats - git version"
arch=('i686' 'x86_64')
license=('GPL')
url='http://projects.gnome.org/easytag/'
depends=('id3lib' 'taglib' 'libid3tag' 'gtk2' 'libvorbis' 'flac' 'speex' 'wavpack' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('git')
provides=('easytag')
conflicts=('easytag')
install="$pkgname.install"
source=("git://git.gnome.org/easytag" 
        'easytag-2.1.8-fix_mime_directory_association.patch'
        "$pkgname.install")
md5sums=('SKIP'
         'b756626f1f71a6a6da318f107d11b142'
         'a2583875aa894ac884a27531480fb28a')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
#  git describe --always | sed -e 's|-|.|g'
#  git describe --always | sed 's/^easytag.//;s/-/./g'
}

prepare() {
  cd "$_gitname"
  # Patch from mainline package 
  # Bad Easytag! Bad! You don't know your place!
  patch -p0 -i "${srcdir}/easytag-2.1.8-fix_mime_directory_association.patch"
}

build() {
  cd "$_gitname"
  ./autogen.sh
  ./configure --prefix=/usr 
  make
}

check() {
  cd "$_gitname"
  make -k check
}
package() {
  cd "$_gitname"
  make DESTDIR="${pkgdir}" install
}
