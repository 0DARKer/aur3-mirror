## Maintainer: rtfreedman (rob<d0t>til<d0t>freedman<aT>gmail<d0t>com
## 
## enable/disable gnome help files; adds makedepends+='yelp-tools'
_build_gnome_help="no" # yes|no

##
## list of locales to build see also: po/LINGUAS
## bg ca cs da de el es fr he hu it ja lt nl pl pt_BR ro ru sl sr sr@latin sv tr uk zh_CN zh_TW
##
## eg. export LINGUAS="bg ru"
##
#export LINGUAS="ru" # uncomment to enable

pkgname=easytag-git
_gitname=easytag
pkgver=967.4bfe75d
pkgrel=1
pkgdesc="Utility for viewing and editing tags for most audio formats - git version"
arch=('i686' 'x86_64')
license=('GPL')
url='http://projects.gnome.org/easytag/'
depends=('id3lib' 'taglib' 'libid3tag' 'gtk2' 'libvorbis' 'flac' 'speex' 'opus' 'wavpack' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('git' 'intltool' 'appdata-tools')
## depends on buiding gnome help files build or not
if [ "$_build_gnome_help" = 'yes' ]; then
  echo -e "\n>>>\n>>> Build with GNOME help files\n>>>\n"
  makedepends+=('yelp-tools')
fi

provides=('easytag')
conflicts=('easytag')
install="$pkgname.install"
source=("git://git.gnome.org/easytag" "$pkgname.install")
md5sums=('SKIP' 'a2583875aa894ac884a27531480fb28a')

pkgver() {
  cd "$_gitname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
#  git describe --always | sed -e 's|-|.|g'
#  git describe --always | sed 's/^easytag.//;s/-/./g'
}

prepare() {
  cd "$_gitname"
  ## Patch from mainline package 
  sed -i 's/MimeType=inode\/directory;/MimeType=/' data/easytag.desktop.in
  ## disable-gnome-help-files
  if [ "$_build_gnome_help" != 'yes' ]; then
    echo -e "\n>>>\n>>> Build without GNOME help files\n>>>\n"
	sed -i 's@= help po@= po@' Makefile.am
	sed -e'/help\/Makefile/d' -e 's/AC_MSG_ERROR(\[yelp-tools/AC_MSG_WARN([yelp-tools/' -i configure.ac
	rm -fr help/
  fi
  ## add git rev to version
  sed "s/(\[EasyTAG\], \[\([0-9]\).\([0-9]\).\([0-9]\)\], /(\[EasyTAG\], [\1.\2.\3-$pkgver], /" -i configure.ac
  ./autogen.sh
}

build() {
  cd "$_gitname"
  ./configure --prefix=/usr # --disable-nls
  make
}

check() {
  cd "$_gitname"
  make -k check
}
package() {
  cd "$_gitname"
  make DESTDIR="${pkgdir}" install
  ## disable-nls
#  sed -i "/]=/d" "${pkgdir}"/usr/share/applications/easytag.desktop
#  rm -fr "${pkgdir}"/usr/share/icons/hicolor/{128x128,256x256}
}
