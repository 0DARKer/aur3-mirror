# Contributor: Anton Leontiev <bunder /at/ t-25.ru>
pkgname=kactus2
pkgver=2.0
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='Graphical IP-XACT tool for embedded products design'
url='http://funbase.cs.tut.fi/index.php/Kactus2'
license=('GPL')
depends=('qt4')
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz
	kactus2.install kactus2.desktop)
options=('!makeflags')

md5sums=('b7675ae817ab02289ca31231363da879'
         'c972ae56aa72d78c93eaca6eabf83514'
         'e820e1ec6f61bcc49120719163557240')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	qmake Kactus2_Solution.pro
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	install -D -m755 executable/Kactus2 $pkgdir/usr/bin/kactus2
	
	mkdir -p $pkgdir/usr/lib
	ln -s /usr/bin/kactus2 $pkgdir/usr/lib/libKactus2.so

	install -D -m644 releaseFiles/readme.txt $pkgdir/usr/share/doc/kactus2/README
	install -m644 releaseFiles/release_notes.txt $pkgdir/usr/share/doc/kactus2/RELEASE-NOTES

	install -D -m644 releaseFiles/DefaultSettingsLinux.ini $pkgdir/etc/xdg/TUT/Kactus2.ini
	
	mkdir -p $pkgdir/usr/share/kactus2/library
	cp -r releaseFiles/Library/TUT $pkgdir/usr/share/kactus2/library/

	install -D -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
	install -D -m644 graphics/appicon.png $pkgdir/usr/share/pixmaps/kactus2.png
}
