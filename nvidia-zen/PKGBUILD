# Maintainer: t3ddy  <t3ddy1988 "at" gmail {dot} com>
# Creator: FallenWizard <fallenwiz@gmail.com>

pkgname=nvidia-zen
pkgver=275.19
_kernver=(`uname -r`)
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26zen"
arch=('i686' 'x86_64')
provides=('nvidia')
url="http://www.nvidia.com/"
depends=('kernel26-zen' "nvidia-utils>=${pkgver}")
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('9e74b1913ff5eb6786600d47b64ba57d')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('45a6074db3efe961d025afa7a89fa036')
fi

build() {
	cd $srcdir
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
	install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install

	# gzip -9 all modules to safe 100MB of space
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}