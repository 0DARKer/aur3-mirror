pkgname=telepathy-kde-mission-control
_pkgname=telepathy-mission-control
pkgver=5.14.0
pkgrel=2
pkgdesc="A Telepathy component providing abstraction of some of the details of connection managers. for KDE (without Gnome Dependencies)"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/Mission Control"
license=('LGPL2.1')
depends=('telepathy-glib' 'dconf' 'upower' 'networkmanager')
makedepends=('libxslt' 'python2')
conflicts=('telepathy-mission-control')
replaces=('telepathy-mission-control')
provides=('telepathy-mission-control')
install="${pkgname}.install"
options=('!libtool')
source=("http://telepathy.freedesktop.org/releases/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('483fa8860891815a540ecf1bb9155122')

build() {
  cd "${srcdir}"

  # fix python"
  rm -rf "${srcdir}/python"
  mkdir "${srcdir}/python"
  ln -s /usr/bin/python2 "${srcdir}/python/python"
  export PATH="${srcdir}/python:$PATH"

  rm -fr "${_pkgname}-build"
  cp -R "${_pkgname}-${pkgver}" "${_pkgname}-build"
  cd "${_pkgname}-build"

  ./configure --prefix=/usr --libexecdir=/usr/lib/telepathy --disable-schemas-compile --disable-gnome-keyring
  make
}

package() {
  cd "${_pkgname}-build"
  make DESTDIR="${pkgdir}" install
}