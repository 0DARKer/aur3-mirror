# Maintainer: cars10 <carsten.1 gmx net>
# Contributor: Evangelos Foutras <foutrelis gmail com>
# Contributer: Matthew Bauer <mjbauer95 gmail com>

pkgname=smw-svn
pkgver=6
pkgrel=3
pkgdesc="Super Mario War multiplayer game"
arch=('i686' 'x86_64')
url="http://smw.supersanctuary.net/"
license=('GPL2')
depends=('sdl_mixer' 'sdl_image' 'desktop-file-utils')
makedepends=('dos2unix' 'subversion')
provides=('smw' 'supermariowar')
conflicts=('smw')
install="smw-svn.install"
source=(smw.desktop
	smw.png
	gcc.patch
	libpng.patch)
md5sums=('609f98c3bb5ef4961ae5eb12ea36d569'
	'b7f5ef181e41eb0339be746ea03ff628'
	'586cf917af0a81912d8c5c0fcfddb64b'
	'ee4c6fd9e7f6f9a4dd699cdcd028b72f')

_svntrunk=https://supermariowar.googlecode.com/svn/trunk/
_svnmod=smw

build() {
  cd ${srcdir}

  msg2 "Connecting to GoogleCode SVN server...."
  if [ -d ${_svnmod}/.svn ]; then
    ( cd ${_svnmod} && svn up -r $pkgver )
  else
    svn co ${_svntrunk} --config-dir ./ -r $pkgver ${_svnmod}
  fi
  msg2 "SVN checkout done or server timeout"
  msg "Starting the build..."

  # Copy source for the build
  [ -d ${srcdir}/${_svnmod}-build ] && rm -rf ${srcdir}/${_svnmod}-build
  cp -r ${srcdir}/${_svnmod} ${srcdir}/${_svnmod}-build

  cd ${srcdir}/${_svnmod}-build

  # Fix line breaks and permission
  dos2unix configure
  chmod +x configure

  # Fixes for gcc warnings and libpng >1.5
  patch --forward -p0 -i ../gcc.patch
  patch --forward -p0 -i ../libpng.patch

  ./configure

  # Change data directory
  sed -i "s|usr/share/games/smw|usr/share/smw|" configuration

  # Build
  make
}

package() {
  cd ${srcdir}/${_svnmod}-build

  # Not using "make install" because of different data directory

  # Install binaries
  msg2 "Installing binaries"
  mkdir -p "${pkgdir}/usr/bin"
  install -D smw "${pkgdir}/usr/bin"
  install -D leveledit "${pkgdir}/usr/bin/smw-leveledit"
  install -D worldedit "${pkgdir}/usr/bin/smw-worldedit"

  # Install data folders
  msg2 "Installing data folders"
  mkdir -p "${pkgdir}/usr/share/smw/"
  for _datadir in gfx maps music sfx tours worlds; do
    cp -r ${_datadir} "${pkgdir}/usr/share/smw/"
  done

  # Set sane permissions
  find "${pkgdir}/usr/share/smw" -type d -exec chmod 755 {} \;
  find "${pkgdir}/usr/share/smw" -type f -exec chmod 644 {} \;

  # Install application shortcut and icon
  install -D -m644 "${srcdir}/smw.desktop" "${pkgdir}/usr/share/applications/smw.desktop"
  install -D -m644 "${srcdir}/smw.png" "${pkgdir}/usr/share/pixmaps/smw.png"
}
