# Maintainer: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# 2010-03-11  Modified by catweazle for poppler 0.12.4
# 2010-06-23  Modified by catweazle for poppler 0.14.0
# 2010-10-16  Modified by rankin for poppler 0.14.4
# 2010-10-16  Modified by rankin for poppler 0.14.5

pkgname=poppler-qt3
pkgver=0.16.2
pkgrel=1
pkgdesc="Poppler Qt3 bindings"
arch=(i686 x86_64)
license=('GPL')
depends=('qt3>=3.3.8' "poppler>=${pkgver}")
makedepends=('pkgconfig')
options=('!libtool')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/poppler-${pkgver}.tar.gz)
md5sums=('0e68e4a894a4234d5468560bcd79baa9')

build() {
  . /etc/profile.d/qt3.sh

  cd "${srcdir}/poppler-${pkgver}"

  libtoolize --force --copy
  AT_M4DIR="m4" autoreconf -i
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
              --enable-zlib \
              --enable-libjpeg \
              --enable-cairo-output \
              --enable-poppler-qt \
              --disable-poppler-glib \
              --disable-gtk-test \
              --disable-poppler-qt4
  sed -i -e 's|^LDFLAGS =|LDFLAGS = -L/opt/qt/lib -lqt-mt|' qt/Makefile
  pushd poppler
  make libpoppler-cairo.la
  popd
  make
}

package() { 
  cd "${srcdir}/poppler-${pkgver}"
 make DESTDIR="${pkgdir}" install
}

