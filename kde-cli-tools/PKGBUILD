# Contributor: Antonio Rojas

pkgname=kde-cli-tools
pkgver=4.96.0
pkgrel=2
pkgdesc="Tools based on KDE Frameworks 5 to better interact with the system"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kde-cli-tools'
license=('LGPL')
depends=('kdesu' 'kdelibs4support' 'kcmutils')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz" "fix-doc-conflicts.patch::http://quickgit.kde.org/?p=kde-cli-tools.git&a=commitdiff&h=60d6c723a36c68516677f1fd4e2760128736102f&o=plain")
md5sums=('b3a0d990daaa45af14a28a4f976761fd'
         'fa74118d4d25e304c7707483becbc068')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ${srcdir}/fix-doc-conflicts.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
