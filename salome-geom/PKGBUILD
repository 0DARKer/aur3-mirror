# Maintainer: Michele Mocciola <mickele>

pkgname=salome-geom
pkgver=7.4.1
pkgrel=1
pkgdesc="Generic platform for Pre and Post-Processing for numerical simulation - GEOM Module"
url="http://www.salome-platform.org"
depends=('salome-gui>=7.4.0' 'salome-gui<7.5.0')
makedepends=('boost' 'optipng' 'swig2')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
source=("${pkgname}.profile" "broken-png-list")

_source=geom
_installdir=/opt/salome/geom
_paraviewrootdir=/usr
_paraviewver=4.1

prepare(){
  msg "Connecting to git server..."

  if [[ -d ${_source} ]] ; then
    rm -rf ${_source}
  fi

  git clone git://git.salome-platform.org/modules/${_source}.git
  cd ${_source}
  git checkout V7_4_1

  msg "GIT checkout done or server timeout"

  # python -> python2
  for _FILE in `grep -Rl "/usr/bin/env python" * `
  do
	sed -e "s|/usr/bin/env python|/usr/bin/env python2|" -i ${_FILE}
  done
}

build() {
  source /etc/salome/profile.d/salome-kernel.sh
  source /etc/salome/profile.d/salome-gui.sh

  rm -rf "$srcdir/$_source/build"
  mkdir -p "$srcdir/$_source/build"
  cd "$srcdir/$_source/build"

  cmake .. \
     -DCMAKE_INSTALL_PREFIX=${_installdir} \
     -DCAS_ROOT_DIR=/opt/opencascade \
     -DVTK_DIR="${_paraviewrootdir}/lib/cmake/paraview-${_paraviewver}" \
     -DPYTHON_EXECUTABLE=/usr/bin/python2 \
     -DLIBXML2_ROOT_DIR=/usr \
     -DLibXml2_DIR=/usr/lib/cmake/libxml2 \
     -DLIBXML2_INCLUDE_DIR=/usr/include/libxml2 \
     -DSWIG_EXECUTABLE=/usr/bin/swig-2
     
  make
}

package() {
  cd "$srcdir/$_source/build"

  make DESTDIR="$pkgdir/" install
  
  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/salome/profile.d/${pkgname}.sh"

  cd "${pkgdir}"
  cat "${srcdir}/broken-png-list" | xargs -n 1 -P 3 optipng -quiet -force -fix
}
md5sums=('5198b9409a98baffc88ae6bf2c3477ce'
         '9b0c81a641be4aa249ebbf29292196db')
