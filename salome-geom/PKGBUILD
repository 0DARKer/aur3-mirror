# Maintainer: Michele Mocciola <mickele>

pkgname=salome-geom
pkgver=7.4.0
pkgrel=1
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation. GEOM Module."
url="http://www.salome-platform.org"
depends=('salome-kernel=7.4.0' 'salome-gui=7.4.0' 'python2' 'qt4' 'boost-libs' 'opencascade>=6.7.1' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'hdf5' 'paraview-salome' 'graphviz')
makedepends=('boost' 'optipng')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
source=("http://files.salome-platform.org/Salome/Salome${pkgver}/src${pkgver}.tar.gz" "${pkgname}.profile" "broken-png-list")

_source=GEOM_SRC_${pkgver}
_installdir=/opt/salome/geom
_paraviewrootdir=/usr
_paraviewver=4.1

prepare(){
  cd "$srcdir/$_source"

  # python -> python2
  for _FILE in `grep -Rl "/usr/bin/env python" * `
  do
	sed -e "s|/usr/bin/env python|/usr/bin/env python2|" -i ${_FILE}
  done
}

build() {
  source /etc/salome/profile.d/salome-kernel.sh
  source /etc/salome/profile.d/salome-gui.sh

  rm -rf "$srcdir/$_source/build"
  mkdir -p "$srcdir/$_source/build"
  cd "$srcdir/$_source/build"

  # flags to use python2 instead of python which is 3.x.x on archlinux
  local cmake_system_python_flags="-DPYTHON_EXECUTABLE=/usr/bin/python2"

  cmake .. \
     -DCMAKE_INSTALL_PREFIX=${_installdir} \
     -DQWT_ROOT=/usr \
     -DCAS_ROOT_DIR=/opt/opencascade \
     -DSPHINX_EXECUTABLE=/usr/bin/sphinx-build2 \
     -DVTK_DIR="${_paraviewrootdir}/lib/cmake/paraview-${_paraviewver}" \
     ${cmake_system_python_flags}
  make
}

package() {
  cd "$srcdir/$_source/build"

  make DESTDIR="$pkgdir/" install
  
  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/salome/profile.d/${pkgname}.sh"

  cd "${pkgdir}"
  cat "${srcdir}/broken-png-list" | xargs -n 1 -P 3 optipng -quiet -force -fix
}
md5sums=('18055915a14e0921563072f830614e7f'
         '5198b9409a98baffc88ae6bf2c3477ce'
         '9b0c81a641be4aa249ebbf29292196db')
