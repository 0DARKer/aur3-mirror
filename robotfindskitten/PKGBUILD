# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: muflax <muflax@gmail.com>
pkgname=robotfindskitten
pkgver=1.7320508.406
pkgrel=2
pkgdesc="Yet another zen simulation"
arch=('x86_64' 'i686')
url="http://www.robotfindskitten.org"
license=('GPL2')
depends=("ncurses")
source=("http://robotfindskitten.org/download/POSIX/${pkgname}-${pkgver}.tar.gz")
md5sums=('6b9cf314ffee0de52ed85ac5ba11d66b')

build() {
  cd $srcdir/${pkgname}-${pkgver}     
  
  msg2 "Configuring..."
  ./configure --prefix=/usr
  msg2 "Patching..."
  sed -i src/Makefile -e \
    's:execgamesdir = $(prefix)/games:execgamesdir = $(prefix)/bin:'
  msg2 "Compiling..."
  make
  make -C doc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make prefix="$pkgdir/usr" install
  msg2 "Packaging documentation..."
  make -C doc prefix="$pkgdir/usr" install
  msg2 "Packaging license..."
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  msg2 "Cleaning up..."
  rm -r "$pkgdir/usr/share/info"
}
# vim:set ts=2 sw=2 et:
