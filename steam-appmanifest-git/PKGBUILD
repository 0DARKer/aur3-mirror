# Maintainer: Joost Bremmer <toost dot b at gmail dot com >
pkgname=steam-appmanifest-git
pkgver=r7.858cead
pkgrel=1
pkgdesc="a short python script that tricks Steam for Linux into downloading non-Linux apps."
arch=(any)
url="https://github.com/dotfloat/steam-appmanifest"
license=('MIT')
depends=('python' 
         'python-gobject')

makedepends=('git')

source=(${pkgname}::'git+https://github.com/dotfloat/steam-appmanifest.git'
        "${pkgname}.patch")

sha256sums=('SKIP'
            '501f13e8899fea97cc578aa28dc098ce24e4ca088c6b49f6d6b9bd5b5713db94')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


prepare() {
  cd "${pkgname}"
  git apply --check -v "${srcdir}/${pkgname}.patch"
  git am "${srcdir}/${pkgname}.patch"

}


package() {
  cd "${srcdir}/${pkgname}"
  install -Dvm755 "steam-appmanifest.py" "${pkgdir}/usr/bin/steam-appmanifest"
}

# vim:set ts=2 sw=2 et:
