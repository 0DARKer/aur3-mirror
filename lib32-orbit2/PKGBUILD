_pkgbasename=orbit2
pkgname=lib32-${_pkgbasename}
pkgver=2.14.19
pkgrel=2
pkgdesc="Thin/fast CORBA ORB (32 bit, library only)"
arch=('x86_64')
license=('LGPL' 'GPL')
depends=('lib32-libidl2>=0.8.11' ${_pkgbasename})
makedepends=('pkgconfig' 'gcc-multilib')
options=('!libtool' '!makeflags')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/ORBit2/2.14/ORBit2-${pkgver}.tar.bz2)
sha256sums=('55c900a905482992730f575f3eef34d50bda717c197c97c08fa5a6eafd857550')

build() {
  cd "${srcdir}/ORBit2-${pkgver}"
  
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
    	--disable-static --enable-shared CC='gcc -m32' \
    	--with-idl-compiler=/usr/bin/orbit-idl-2

  make
}

package() {
  cd "${srcdir}/ORBit2-${pkgver}"  
  make DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share,lib32/libname-server-2.a}
  
}
