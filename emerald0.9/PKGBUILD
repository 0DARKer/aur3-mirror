# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Iven Hsu <ivenvd at gmail dot com>
# Contributor: Vinycius Maia <suportevg@uol.com.br>

pkgname=emerald0.9
pkgver=0.9.5
pkgrel=11
pkgdesc="Emerald window decorator (0.9 version)"
arch=('i686' 'x86_64')
url="http://cgit.compiz.org/fusion/decorators/emerald"
license=('GPL')
depends=('compiz' 'libwnck' 'xdg-utils' \
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=(
  'emerald-themes: extra themes for Emerald'
)
options=(!libtool)
conflicts=('emerald' 'emerald-git')
provides=('emerald=0.9.5')
install=emerald.install
source=('http://cgit.compiz.org/fusion/decorators/emerald/snapshot/emerald-0.9.5.tar.bz2')
sha256sums=('7982b8b7aaf3056d644c411963bdf0b980d93e4eabe8788bc773558fb5f9cda5')

build() {
  cd "$srcdir"/emerald-0.9.5
  LIBS+="-lm -ldl" ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir"/emerald-0.9.5
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

