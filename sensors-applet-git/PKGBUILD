# Maintainer: Thomas Hopkins <hopkinsth@gmail.com>
# Contributor:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Michael Barker

pkgname=sensors-applet-git
pkgver=20110123
pkgrel=1
pkgdesc="The latest git pull of an applet for the GNOME Panel to display readings from hardware sensors."
arch=('i686' 'x86_64')
url="http://sensors-applet.sourceforge.net/"
license=('GPL')
depends=('gnome-panel-bonobo' 'lm_sensors' 'hicolor-icon-theme')
makedepends=('git' 'intltool' 'gnome-doc-utils>=0.18.1' 'libxnvctrl' 'libgnomeui')
conflicts=('sensors-applet')
optdepends=('hddtemp: for HDD support'
	    'libxnvctrl: for Nvidia GPU support')
install=sensors-applet.install
_gitroot=git://sensors-applet.git.sourceforge.net/gitroot/sensors-applet/sensors-applet
_gitname=sensors-applet
options=('!libtool')

build() {

  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh --prefix=/usr \
    --libexecdir=/usr/lib/sensors-applet \
    --localstatedir=/var \
    --disable-scrollkeeper \
    --with-nvidia \
    --enable-libnotify
    
  make
  make DESTDIR=$pkgdir install
}
