# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Lich ( lich0x2b@gmail.com ) 

pkgname=barewm-git
pkgver=20130311
pkgrel=1
pkgdesc="Bare WM - A minimal fullscreen window manager for Linux"
url="http://github.com/lich0x2b/barewm"
license=('GPL')
arch=('i686' 'x86_64')
depends=('xorg-server')
makedepends=('git')
source=('git+https://github.com/lechatleon/barewm')
md5sums=('SKIP')
_gitname="barewm"

pkgver() {
  cd "$_gitname"
  git show -s --format="%ci" HEAD | sed -e 's/-//g' -e 's/ .*//'
}

build(){
  cd "$srcdir/$_gitname"
  unset CFLAGS
  sed -i '9s+Mod1Mask+Mod4Mask+' conf.h
  sed -i '18s+urxvt+st+' conf.h
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}
