# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Lich ( lich0x2b@gmail.com ) 

pkgname=barewm-git
pkgver=20091016
pkgrel=1
pkgdesc="Bare WM - A minimal fullscreen window manager for Linux"
url="http://github.com/lich0x2b/barewm"
license=('GPL')
arch=('i686' 'x86_64')
depends=('xorg-server')
makedepends=('git')
source=('git+https://github.com/lich0x2b/barewm')
md5sums=('SKIP')

_gitname="barewm"

pkgver() {
  cd "$_gitname"
  git show -s --format="%ci" HEAD | sed -e 's/-//g' -e 's/ .*//'
}

build(){
  cd "$srcdir/$_gitname"
  unset CFLAGS
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
  install -Dm644 README "$pkgdir/usr/share/barewm/README"
}
