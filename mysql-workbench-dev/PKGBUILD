# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Alexandre Boily <alexandreboily@gmail.com>
# Contributor: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

#pkgname=mysql-workbench
pkgname=mysql-workbench-dev
pkgver=6.2.2
pkgrel=3
pkgdesc='A cross-platform, visual database design tool developed by MySQL'
arch=('i686' 'x86_64')
url='https://www.mysql.com/products/workbench/'
license=('GPL2')
depends=('python2' 'libzip' 'libmariadbclient' 'lua51' 'gtkmm' 'ctemplate'
	 'libgnome-keyring' 'libgl' 'python2-paramiko' 'python2-pexpect' 'gdal'
	 'pcre' 'tinyxml' 'libxml2' 'mysql-python' 'python2-pysqlite' 'cairo'
         'python2-cairo' 'hicolor-icon-theme' 'desktop-file-utils' 'libiodbc'
	 'mysql-connector-c++' 'vsqlite++' 'unixodbc' 'freetype2' 'libantlr3c')
optdepends=('gnome-keyring: store SSH/MySQL passwords in GNOME password manager'
	'python2-pyodbc: database migration')
makedepends=('cmake' 'boost' 'curl' 'mesa' 'imagemagick' 'swig')
if [ "${pkgname}" == "mysql-workbench-dev" ]; then
	pkgdesc='A cross-platform, visual database design tool developed by MySQL - development version'
	provides=('mysql-workbench')
	conflicts=('mysql-workbench')
fi
install=mysql-workbench.install
source=("http://cdn.mysql.com/Downloads/MySQLGUITools/mysql-workbench-community-${pkgver}-src.tar.gz"{,.asc}
	'mysql-workbench-ctemplate.patch'
	'mysql-workbench-swig3.patch'
	'mysql-workbench-mariadb-10.patch'
	'mysql-workbench-no-check-for-updates.patch'
	'arch_linux_profile.xml')

prepare() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src/"

	# Disable 'Help' -> 'Check for Updates'
	patch -Np1 < "${srcdir}"/mysql-workbench-no-check-for-updates.patch

	# fix deprecated calls to tpl->ReloadIfChanged
	# http://bugs.mysql.com/72585
	patch -Np1 < "${srcdir}"/mysql-workbench-ctemplate.patch

	# fix compilation with Mariadb
	patch -Np1 < "${srcdir}"/mysql-workbench-mariadb-10.patch

	# fix bool with swig 3.x
	patch -Np1 < "${srcdir}"/mysql-workbench-swig3.patch

	# fix GDAL
	# provided by cmake
	rm build/cmake/Modules/FindGDAL.cmake
	# fix patch
	sed -i '/#include/s|gdal/||' backend/wbpublic/grtui/geom_draw_box.h backend/wbpublic/grt/spatial_handler.h
}

build() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src/"

	cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .

	# fix path to lua include files
	# is this a bug by cmake? (/usr/share/cmake-2.8/Modules/FindLua51.cmake)
	sed -i '/^LUA_INCLUDE_DIR:PATH=/c LUA_INCLUDE_DIR:PATH=/usr/include/lua5.1' CMakeCache.txt

	# fix detection of Python
	sed -i 's|python3.3m|python2.7|' CMakeCache.txt

	make
}

package() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src"

	make DESTDIR="${pkgdir}" install

	# generate icons
	for SIZE in 16 24 32 48 64 96 128 160 192 256 384 512; do
		if [ ! -s images/icons/MySQLWorkbench-${SIZE}.png ]; then
			convert -scale ${SIZE} \
				images/icons/MySQLWorkbench-512.png \
				images/icons/MySQLWorkbench-${SIZE}.png
		fi
		install -D -m0644 images/icons/MySQLWorkbench-${SIZE}.png \
			"${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/mysql-workbench.png"
	done

	install -D -m 0644 "${srcdir}"/arch_linux_profile.xml "${pkgdir}"/usr/share/mysql-workbench/mysql.profiles/Arch_Linux_\(MariaDB_5.5_Package\).xml
}

sha256sums=('e9c65c28752f7e68bf649f26f6d3396186e16f63135eda9507bdaaae4a79349f'
            'SKIP'
            'bbf6f3e56847cc012634e262295f34eaaa43ef32d04e1ec291315dabd6613042'
            '2d8e41bba048b711a4c67a48f26b7b1f7c3f6d7208740db189585507ab9d0c29'
            '6fc7d88a4e43ba3ccf0fac8075e4e5ca253626c736416b0ffa1ed44ac9ee5d92'
            '996482e15d88af097f5e7a578885338cad5b8724f8abb4341749d5538801f5c0'
            '28724c4b4cec29ce19aada08279df1b086381cd788fef7ae07c1860f7d17af7e')
