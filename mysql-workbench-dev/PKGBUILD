# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Alexandre Boily <alexandreboily@gmail.com>
# Contributor: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mysql-workbench-dev
pkgver=6.2.2
pkgrel=8
pkgdesc='A cross-platform, visual database design tool developed by MySQL - development version'
arch=('i686' 'x86_64')
url='https://www.mysql.com/products/workbench/'
license=('GPL2')
depends=('python2' 'libzip' 'libmariadbclient' 'lua51' 'gtkmm' 'ctemplate'
	 'libgnome-keyring' 'libgl' 'python2-paramiko' 'python2-pexpect' 'gdal'
	 'pcre' 'tinyxml' 'libxml2' 'mysql-python' 'python2-pysqlite' 'cairo'
         'python2-cairo' 'hicolor-icon-theme' 'desktop-file-utils' 'libiodbc'
	 'mysql-connector-c++' 'vsqlite++' 'unixodbc' 'freetype2' 'libantlr3c')
optdepends=('gnome-keyring: store SSH/MySQL passwords in GNOME password manager'
	'python2-pyodbc: database migration')
makedepends=('cmake' 'boost' 'curl' 'mesa' 'imagemagick' 'swig' 'binutils')
provides=('mysql-workbench')
conflicts=('mysql-workbench')
install=mysql-workbench.install
source=("http://cdn.mysql.com/Downloads/MySQLGUITools/mysql-workbench-community-${pkgver}-src.tar.gz"{,.asc}
	'mysql-workbench-ctemplate.patch'
	'mysql-workbench-boolean.patch'
	'mysql-workbench-python-bool.patch'
	'mysql-workbench-cmake-definitions.patch'
	'mysql-workbench-paramiko.patch'
	'mysql-workbench-mariadb-10.patch'
	'mysql-workbench-no-check-for-updates.patch'
	'arch_linux_profile.xml')

prepare() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src/"

	# Disable 'Help' -> 'Check for Updates'
	patch -Np1 < "${srcdir}"/mysql-workbench-no-check-for-updates.patch

	# fix deprecated calls to tpl->ReloadIfChanged
	# http://bugs.mysql.com/72585
	patch -Np1 < "${srcdir}"/mysql-workbench-ctemplate.patch

	# fix compilation with Mariadb
	# This will be fixed in Mariadb 10.0.14! Yeah!
	# For now use nm to check whether or not libmysqlclient do have mysql_options4().
	# TODO: Remove this and the make dependency to binutils once this get fixed.
	if ! nm -D /usr/lib/libmysqlclient.so | grep -q mysql_options4; then
		patch -Np1 < "${srcdir}"/mysql-workbench-mariadb-10.patch
	fi

	# fix bool with python and swig 3.x
	patch -Np1 < "${srcdir}"/mysql-workbench-boolean.patch
	patch -Np1 < "${srcdir}"/mysql-workbench-python-bool.patch

	# fix cmake definitions with mysql-connector-c++ 1.1.4
	patch -Np1 < "${srcdir}"/mysql-workbench-cmake-definitions.patch

	# fix window_size with paramiko 1.15
	patch -Np1 < "${srcdir}"/mysql-workbench-paramiko.patch

	# fix GDAL
	# provided by cmake
	rm build/cmake/Modules/FindGDAL.cmake
	# fix patch
	sed -i '/#include/s|gdal/||' backend/wbpublic/grtui/geom_draw_box.h backend/wbpublic/grt/spatial_handler.h
}

build() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src/"

	cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .

	make
}

package() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src"

	make DESTDIR="${pkgdir}" install

	# generate icons
	for SIZE in 16 24 32 48 64 96 128 160 192 256 384 512; do
		if [ ! -s images/icons/MySQLWorkbench-${SIZE}.png ]; then
			convert -scale ${SIZE} \
				images/icons/MySQLWorkbench-512.png \
				images/icons/MySQLWorkbench-${SIZE}.png
		fi
		install -D -m0644 images/icons/MySQLWorkbench-${SIZE}.png \
			"${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/mysql-workbench.png"
	done

	install -D -m 0644 "${srcdir}"/arch_linux_profile.xml "${pkgdir}"/usr/share/mysql-workbench/mysql.profiles/Arch_Linux_\(MariaDB_5.5_Package\).xml
}

sha256sums=('e9c65c28752f7e68bf649f26f6d3396186e16f63135eda9507bdaaae4a79349f'
            'SKIP'
            'bbf6f3e56847cc012634e262295f34eaaa43ef32d04e1ec291315dabd6613042'
            'bca8b9f01206a84caa8f419d278352106217fe8e0452ccaf11bf56c8e090e291'
            'b32b4b5679e3f23e1325095f29cc6cf395d44e77c790b8651ade77b491dd3a24'
            '142d499416ea4a4743a4c3166014fc2fc60c34374e587715908634b5f107a700'
            'ec232dd055888c1091f64631c6d0c0c038c20b9e7a25be8b29dfb5f25aacb5e4'
            'ce933b7d64935e3deec34f4d5945bb291c58f76942c36ee67ec1c41d3dbb2e48'
            '996482e15d88af097f5e7a578885338cad5b8724f8abb4341749d5538801f5c0'
            '28724c4b4cec29ce19aada08279df1b086381cd788fef7ae07c1860f7d17af7e')
