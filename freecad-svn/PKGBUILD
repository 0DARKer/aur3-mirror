# Maintainer: Giuseppe Borzi <gborzi@ieee.org>
# Contributor : Omar Lakhdar <omar_lakhdar@hotmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Joaquim Coimbra <joaca_rj@yahoo.com.br>
# Contributor: Michele Mocciola <mickele>
# Contributor : FoolEcho <Archlinux.fr>
# Thanks to Werner Mayer for his support

pkgname=freecad-svn
pkgver=4314
pkgrel=1
pkgdesc='A general purpose 3D CAD modeler'
arch=('i686' 'x86_64')
url='http://sourceforge.net/apps/mediawiki/free-cad/'
license=('GPL')
depends=('boost-libs=1.46.0' 'curl' 'opencascade' 'pivy-hg' 'python-qt' 'xerces-c')
makedepends=('boost=1.46.0' 'eigen' 'gcc-fortran' 'subversion' 'swig')
provides=('freecad')
options=(!libtool)
source=("${pkgname}.desktop")
md5sums=('6caf38205272083c9f44b80f2cac06a9')

_svnmod='freecad'
_svntrunk='https://free-cad.svn.sourceforge.net/svnroot/free-cad/trunk'

build() {
  msg 'Getting sources...'
  if [ -d ${_svnmod} ]; then
    cd ${_svnmod}
    svn up -r ${pkgver}
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
    cd ${_svnmod}
  fi
  msg 'SVN checkout done or server timeout'

  cd ${srcdir}
  cd ${_svnmod}


  ./autogen.sh

  sed -i -e 's/-lboost\(.*\)-mt/-lboost\1/' configure
  ./configure \
    --prefix=/usr \
    --with-qt4-dir=/usr/lib/qt/ \
    --with-qt4-bin=/usr/bin/ \
    --with-qt4-include=/usr/include/ \
    --with-qt4-lib=/usr/lib/qt/ \
    PYTHON=/usr/bin/python2 \
   --with-python-include=/usr/include/python2.7/ \
    --with-python-lib=/usr/lib/python2.7/ \
    --with-boost-include=/usr/include/boost \
    --with-boost-lib=/usr/lib \
    --with-occ-lib=/opt/opencascade/lib \
    --with-occ-include=/opt/opencascade/inc  \

  # prepare for compilation
  # 1) fix Driver_Document.h problem
  sed -i -e 's#AM_CPPFLAGS = -I$(OCC_INC) -I$(srcdir)/$(smeshdir)inc/#& -I./inc/#' src/3rdParty/salomesmesh/Makefile
  # 2) fix boost filesystem version
  sed -i -e '/#include <vector>/a#define BOOST_FILESYSTEM_VERSION 2' \
     src/App/PropertyStandard.h

  make
}

package() {
  cd ${_svnmod}

  make DESTDIR=${pkgdir} install

  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${pkgdir}/usr/share/freecad.xpm" "${pkgdir}/usr/share/pixmaps/freecad.xpm"
  rm -f "${pkgdir}/usr/share/freecad.xpm"
}
