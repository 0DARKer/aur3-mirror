# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Joaquim Coimbra <joaca_rj@yahoo.com.br>
# Contributor: Michele Mocciola <mickele>
# Thanks to Werner Mayer for his support

pkgname=freecad-svn
pkgver=3732
pkgrel=1
pkgdesc='A general purpose 3D CAD modeler, aimed directly at mechanical engineering and product design but also architecture or other engineering specialties.'
arch=('i686' 'x86_64')
url='http://sourceforge.net/apps/mediawiki/free-cad/'
license=('GPL')
depends=('xerces-c' 'gts' 'opencascade' 'soqt' 'qt' 'opencv' 'boost' 'python' 'curl' 'eigen' 'swig' 'pyqt' 'pivy-hg' 'gcc-fortran')
makedepends=('subversion' 'desktop-file-utils')
provides=('freecad')
options=('!makeflags')
source=("${pkgname}.desktop")
md5sums=('68651e7273e692b52ae78da474e97ba9')

_svnmod='freecad'
_svntrunk='https://free-cad.svn.sourceforge.net/svnroot/free-cad/trunk'

build() {
  msg 'Getting sources...'
  if [ -d ${_svnmod} ]; then
    cd ${_svnmod}
    svn up -r ${pkgver}
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
    cd ${_svnmod}
  fi
  msg 'SVN checkout done or server timeout'

  ./autogen.sh

  ./configure \
    --prefix=/opt/freecad-svn \
    --with-qt4-dir=/usr/lib/qt/ \
    --with-qt4-bin=/usr/bin/ \
    --with-qt4-include=/usr/include/ \
    --with-qt4-lib=/usr/lib/qt/ \
    --with-xercesc-include=/usr/include/xercesc/ \
    --with-xercesc-lib=/usr/lib/ \
    PYTHON=/usr/bin/python2 \
    --with-python-include=/usr/include/python2.7/ \
    --with-python-lib=/usr/lib/python2.7/ 

  make
}

package() {
  cd ${_svnmod}

  make DESTDIR=${pkgdir} install

  desktop-file-install --dir=${pkgdir}/usr/share/applications ${srcdir}/${pkgname}.desktop
}
