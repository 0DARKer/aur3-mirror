# Maintainer: mickele
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Joaquim Coimbra <joaca_rj@yahoo.com.br>
# Thanks to Werner Mayer for his support

pkgname=freecad-svn
pkgver=4258
pkgrel=1
pkgdesc='A general purpose 3D CAD modeler, aimed directly at mechanical engineering and product design but also architecture or other engineering specialties.'
arch=('i686' 'x86_64')
url='http://sourceforge.net/apps/mediawiki/free-cad/'
license=('GPL')
depends=('xerces-c' 'gts' 'opencascade' 'soqt' 'qt' 'opencv' 'boost' 'python' 'curl' 'eigen' 'swig' 'python2-qt' 'pivy-hg' 'gcc-fortran')
makedepends=('subversion' 'desktop-file-utils')
provides=('freecad')
options=('!makeflags')
source=("${pkgname}.desktop")

_svnmod='freecad'
_svntrunk='https://free-cad.svn.sourceforge.net/svnroot/free-cad/trunk'

build() {
  msg 'Getting sources...'
  if [ -d ${_svnmod} ]; then
    cd ${_svnmod}
    svn up -r ${pkgver}
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
    cd ${_svnmod}
  fi
  msg 'SVN checkout done or server timeout'

  for _FILE in `grep -R -l "\-mt\"" *`
  do
     sed -e 's|\-mt"|"|' -i $_FILE
  done

  ./autogen.sh

  ./configure \
    CPPFLAGS="$CPPFLAGS -DBOOST_FILESYSTEM_VERSION=2" \
    --prefix=/opt/freecad-svn \
    --with-qt4-dir=/usr/lib/qt/ \
    --with-qt4-bin=/usr/bin/ \
    --with-qt4-include=/usr/include/ \
    --with-qt4-lib=/usr/lib/qt/ \
    --with-xercesc-include=/usr/include/xercesc/ \
    --with-xercesc-lib=/usr/lib/ \
    PYTHON=/usr/bin/python2 \
    --with-python-include=/usr/include/python2.7/ \
    --with-python-lib=/usr/lib/python2.7/ \
    --with-occ-include=/opt/opencascade/include \
    --with-occ-lib=/opt/opencascade/lib \
    --disable-dependency-tracking \
    --disable-debug

  make
}

package() {
  cd ${_svnmod}

  make DESTDIR=${pkgdir} install

  # Symlink to /usr/bin
  mkdir -p ${pkgdir}/usr/bin/
  ln -s /opt/freecad/bin/FreeCAD ${pkgdir}/usr/bin/freecad-svn
  ln -s /opt/freecad/bin/FreeCADCmd ${pkgdir}/usr/bin/freecadcmd-svn

  # Install pixmaps and desktop shortcut
  install -D -m 755 src/Gui/Icons/freecad.xpm "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm" || return 1
  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/${pkgname}.desktop"
}
md5sums=('68651e7273e692b52ae78da474e97ba9')
