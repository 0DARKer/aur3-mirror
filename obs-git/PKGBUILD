# Maintainer: Weegee <weegee at devzero dot info>

pkgname=obs-git
pkgver=0.2.3.g1d809ba
pkgrel=1
pkgdesc="Free, open source software for live streaming and recording"
arch=('i686' 'x86_64')
url="http://obsproject.com/"
license=('GPL2')
depends=('qt5-base' 'qt5-x11extras' 'ffmpeg')
makedepends=('git')
source=('git+https://github.com/jp9000/obs-studio.git')
md5sums=('SKIP')

_gitname=obs-studio

pkgver() {
  cd "$_gitname"
  rev="$(git log -1 --pretty=format:%h )"
  echo "0.2.3.g$rev"
}

build() {
  cd "$_gitname"

  cmake ./ -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd "$_gitname"
  make DESTDIR="$pkgdir" install
}

