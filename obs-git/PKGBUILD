# Maintainer: Weegee <weegee at devzero dot info>

pkgname=obs-git
pkgver=20140424.dc6a17b
pkgrel=1
pkgdesc="Free, open source software for live streaming and recording"
arch=('i686' 'x86_64')
url="http://obsproject.com/"
license=('GPL2')
depends=('qt5-base' 'qt5-x11extras' 'ffmpeg')
makedepends=('git')
source=('git+https://github.com/jp9000/obs-studio.git')
md5sums=('SKIP')

_gitname=obs-studio

pkgver() {
  cd "$_gitname"
  majorver="$(git log -1 --format='%cd' --date=short | tr -d -- '-')"
  microver="$(git log -1 --pretty=format:%h )"
  echo $majorver.$microver
}

build() {
  cd "$_gitname"

  cmake ./ -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd "$_gitname"
  make DESTDIR="$pkgdir" install
}

