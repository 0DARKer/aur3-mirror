# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Modified: andrewthomas <atswartz@gmail.com>

pkgname=syslog-ng-caps
_realname=syslog-ng
pkgver=3.2.2
pkgrel=2
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities with libcaps patch for 2.6.38"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('base')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glib2' 'eventlog' 'tcp_wrappers' 'openssl' 'libcap')
makedepends=('flex' 'pkgconfig')
optdepends=('logrotate')
provides=('logger' 'syslog-ng=$pkgver' )
replaces=('syslog-ng')
conflicts=('syslog-ng')
options=('!libtool')
backup=('etc/modules.conf' 'etc/syslog-ng.conf' 'etc/logrotate.d/syslog-ng')
source=(http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/${_realname}_$pkgver.tar.gz
        cap_syslog.patch
        syslog-ng.conf
        syslog-ng.logrotate
        syslog-ng.rc)
md5sums=('ed8ebe559d52a63fb61e3e2db566643f'
         '46e5dcff71f820d497898331a3f608fe'
         '08662e1ebb848f25e336c2588dc81261'
         '735636090be4582885974542d2a75855'
         '9bd98250cfa49e13fa3fa731b6040549')

build() {
    cd $srcdir/${_realname}-$pkgver
   
    msg 'Applying patches ...'
    patch -Np1 -i "${srcdir}/cap_syslog.patch"

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var/lib/syslog-ng \
                --enable-tcp-wrapper \
                --with-pidfile-dir=/var/run \
                --disable-spoof-source
    make
}

package() {
    cd $srcdir/${_realname}-$pkgver
    make DESTDIR=$pkgdir install
    install -dm755 $pkgdir/var/lib/syslog-ng
    install -Dm644 $srcdir/syslog-ng.conf $pkgdir/etc/syslog-ng.conf
    install -Dm644 $srcdir/syslog-ng.logrotate $pkgdir/etc/logrotate.d/syslog-ng
    install -Dm755 $srcdir/syslog-ng.rc $pkgdir/etc/rc.d/syslog-ng

    # remove libexec
    rmdir $pkgdir/usr/libexec
}
