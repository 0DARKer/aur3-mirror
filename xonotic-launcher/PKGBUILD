# Maintainer: Spike29 <leguen.yannick@gmail.com>

pkgname='xonotic-launcher'
pkgver='0.20'
pkgrel='3'
pkgdesc='A Xonotic windowed launcher with an overview on servers, IRC pickup channel and configuration'
arch=('i686' 'x86_64')
url='http://forums.xonotic.org/showthread.php?tid=3655'
license=('GPL3')
depends=('qtwebkit')
optdepends=('xonotic')
install="$pkgname.install"
source=('http://ubuntuone.com/3VcCK6kBvs2YRziSt3aJ0d'
		"$pkgname.desktop")
md5sums=('9b172a6511ea560ac51c98d8ece3bee5'
		 '38390633876371dc6f665557ece8867d')
 
build() {
	cd "$srcdir/Xonotic Launcher-$pkgver/src/"
	[ -d build ] || mkdir build && cd build
	qmake-qt4 PREFIX=/usr ..
	make
}

package() {
	install -Dm0755 "$srcdir/Xonotic Launcher-$pkgver/src/build/XonoticLauncher" \
	"$pkgdir/usr/bin/$pkgname"
	install -Dm0644 "$srcdir/$pkgname.desktop" \
	"$pkgdir/usr/share/applications/$pkgname.desktop"
	install -Dm0644 "$srcdir/Xonotic Launcher-$pkgver/src/COPYING" \
	"$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

