# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=nautilus-elementary-bzr
_realname=nautilus
pkgver=162
pkgrel=6
pkgdesc="Nautilus patched for simplicity"
arch=('i686' 'x86_64')
url="https://launchpad.net/nautilus-elementary"
license=('GPL')
depends=('clutter-gtk' 'desktop-file-utils' 'exempi>=2.1.1' \
         'gnome-desktop>=2.32.1' 'gvfs>=1.6.6' 'hicolor-icon-theme' \
         'libexif>=0.6.19' 'libunique>=1.1.6' 'shared-mime-info' 'vte>=0.26.0')
makedepends=('bzr' 'gnome-common' 'gobject-introspection' 'gtk-doc' 'intltool')
optdepends=('gloobus-preview-bzr: for preview support'
            'ne-terminal-config: for embedded terminal')
provides=(${_realname}=2.32.2.1)
conflicts=(${_realname} ${_realname}-elementary)
replaces=(${_realname})
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=('no_zeitgeist.patch'
        'nautilus_2322.patch')
md5sums=('f826b1b7609ad3c9810361d5e4f73f8f'
         '14487274cfd4ee947ffda3a6103d6444')

_bzrtrunk=lp:${_realname}-elementary
_bzrmod=${_realname}-elementary

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  # remove zeitgeist
  patch -Np1 -i ${srcdir}/no_zeitgeist.patch

  # update to nautilus 2.32.2
  patch -Np1 -i ${srcdir}/nautilus_2322.patch

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
              --libexecdir=/usr/lib/nautilus \
              --disable-update-mimedb \
              --disable-packagekit \
              --disable-appindicator
  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}"/usr/share/gconf/schemas
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/"${_realname}".schemas \
    --domain "${_realname}" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}

