# Maintainer: Fabien Dubosson <fabien dot dubosson at gmail dot com>
pkgname=knime-sdk
_pkgname=eclipse_knime
pkgver=2.7.4
pkgrel=1
pkgdesc="Software Development Kit for Knime"
arch=('any')
url="http://www.knime.org/"
license=('custom')
depends=('java-environment' 'cairo')
optdepends=(
    'bash: Required for some nodes'
    'python: Required for some nodes'
    )
provides=("${pkgname}")
conflicts=("${pkgname}")
options=('!emptydirs')
md5sums=(
    '452de8c32d134ba0053a05c77791735b'
    'a277038bcf71106fd6f0df8549caae17'
    '9e93e4def16f04f7808bddb48da3c009'
    )

_arch=x86
if [ "$CARCH" = "x86_64" ]; then
	md5sums[0]='83e4b042905ae81e6b4cc4f53b5af3f1'
	_arch="$CARCH"
fi

source=(
    "http://www.knime.org/knime_downloads/linux/${_pkgname}_${pkgver}.linux.gtk.${_arch}.tar.gz"
    'knime.sh'
    'LICENSE'
    )

package() {
	cd "${srcdir}/${_pkgname}_${pkgver}"

	# Install files
	rm -r "jre/"
	rm "libcairo-swt.so"
	install -m755 -d "${pkgdir}/usr/share/${pkgname}/"
	cp -av * "${pkgdir}/usr/share/${pkgname}/"
	ln -s "/usr/lib/libcairo.so" "${pkgdir}/usr/share/${pkgname}/libcairo-swt.so"

	# Install executable
	install -D -m755 "${srcdir}/knime.sh" "${pkgdir}/usr/bin/${pkgname}"

	# Install license
	install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
