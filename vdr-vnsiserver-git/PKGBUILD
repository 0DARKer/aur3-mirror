# Contributor: David Spicer <azleifel at gmail dot com>

pkgname=vdr-vnsiserver-git
_pluginname=vnsiserver3
pkgver=20130329
pkgrel=1
pkgdesc="A VDR plugin to handle XBMC clients"
arch=('i686' 'x86_64')
url="http://xbmc.org/"
license=('GPL2')
depends=('vdr>=1.7.42')
makedepends=('git')
provides=('vdr-vnsiserver' 'vdr-plugin-vnsiserver')
conflicts=('vdr-vnsiserver' 'vdr-plugin-vnsiserver')
backup=('var/lib/vdr/plugins/vnsiserver3/allowed_hosts.conf')
install=${pkgname}.install

_gitroot=https://github.com/opdenkamp/xbmc-pvr-addons.git
_gitname=xbmc-pvr-addons.git

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  # Clone source to fresh build directory
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "${srcdir}/$_gitname-build/addons/pvr.vdr.vnsi/vdr-plugin-vnsiserver"

  make
}

package() {
  cd "${srcdir}/$_gitname-build/addons/pvr.vdr.vnsi/vdr-plugin-vnsiserver"

  # VDR directory environment
  _vdrCONFDIR="/var/lib/vdr"
  _vdrINCDIR="/usr/include"
  _vdrLIBDIR="/usr/lib/vdr"
  _vdrLOCDIR="/usr/share/locale"

  make DESTDIR="${pkgdir}" install

  # Install configuration file
  install -d -m755 "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"
  install -m644 vnsiserver/allowed_hosts.conf "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"
  
  # Install documents
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  for _file in HISTORY README; do
    install -m644 ${_file} "${pkgdir}/usr/share/doc/${pkgname}/${_file}"
  done
}
