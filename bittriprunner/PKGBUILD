# Maintainer: Sam S. <smls75@gmail.com>
# Original submitter: Yann Kaiser <kaiser.yann@gmail.com>

pkgname=bittriprunner
pkgver=1.0_20120927
_hibver=1.0-4
_buildver=1348702546
pkgrel=1
pkgdesc="BIT.TRIP RUNNER, a rhythm-based action platformer"
url="http://bittripgame.com/bittrip-runner.html"
arch=('i686' 'x86_64')
license=('custom')
depends=('sdl' 'libgl' 'openal' 'libvorbis' 'libogg' 'zlib')
source=('bittriprunner.desktop')
md5sums=('4b909d84b34d6d7076f9712f8e122ece')

case $CARCH in
  i686)
    _archive="bit.trip.runner-linux-${_hibver}_i386-${_buildver}.tar.gz"
    _archive_md5='900019b5994e1b8b2e08fcf07d9c1c08' ;;
  x86_64)
    _archive="bit.trip.runner_${_hibver}_amd64-${_buildver}.tar.gz"
    _archive_md5='026712ac29972467f0b167a3eeaa3933' ;;
esac
_archive_subdir="bit.trip.runner-1.0";
_installname="bit.trip.runner"

build() {
  # Get game archive
  _get_local_source "${_archive}" --md5 "${_archive_md5}" || {
    error "Unable to find the game archive. Please download it from your Humble
           Bundle page, and place it into one of the above locations."
    exit 1; }

  msg "Starting setup..."
  cd $srcdir

  # Unpack game archive
  [[ -d ${_archive_subdir} ]] && rm -r "${_archive_subdir}"
  tar -xf "${_archive}"

  # Install game files
  mkdir -p "${pkgdir}/opt/${_installname}"
  cp -r "${_archive_subdir}/bit.trip.runner"/* "${pkgdir}/opt/${_installname}/"

  # Install launcher script
  echo -e "#!/bin/sh\ncd /opt/${_installname} && ./bit.trip.runner" \
        > "launcher.sh"
  install -Dm755 "launcher.sh" "${pkgdir}/usr/bin/${_installname}"

  # Install icon
  install -Dm644 "${_archive_subdir}/bit.trip.runner/RUNNER.png" \
                 "${pkgdir}/usr/share/pixmaps/${_installname}.png"

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${_installname}.desktop"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${_installname}"
  cp -r "${_archive_subdir}/README"* "${pkgdir}/usr/share/doc/${_installname}"
}


###### HELPER FUNCTIONS ######

# Locates a file or folder provided by the user, and symlinks it into $srcdir
_get_local_source() {
  msg "Looking for '$1'..."; rm -f "$srcdir/$1"
  declare -A _search=(['build dir']="$startdir"
                      ['$LOCAL_PACKAGE_SOURCES']="$LOCAL_PACKAGE_SOURCES")
  for _key in "${!_search[@]}"; do local _dir="${_search["$_key"]}"
    echo -n "    - in $_key [${_dir:-<undefined>}] ... ";
    if [[ -z "$_dir" || ! -e "$_dir/$1" ]]; then
      echo "NOT FOUND"
    elif [[ -n $2 && "$(${2#--}sum "$_dir/$1"|awk '{print $1}')" != $3 ]]; then
      echo "CHECKSUM FAILED";
    else
      echo "FOUND"; ln -sfT "$(readlink -f "$_dir/$1")" "$srcdir/$1"; break; fi
  done
  if [ ! -e "$srcdir/$1" ]; then return 1; fi
}
