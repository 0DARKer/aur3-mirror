# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=blink1
pkgver=1.93
pkgrel=1
pkgdesc='software for blink(1) USB RGB LED'
arch=('i686' 'x86_64')
url='https://github.com/todbot/blink1'
license=('GPL')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/todbot/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('d917613e3c5b69e15c4a00b8168d70dbd7910b40d2b48a117c5f4a3101a42530')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}/commandline/

	sed -i 's/-static//g' Makefile
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}/commandline/

	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}/commandline/

	# binary
	install -D -m0755 blink1-tool ${pkgdir}/usr/bin/blink1-tool

	cd ${srcdir}/${pkgname}-${pkgver}/linux/

	# udev rule
	install -D -m0644 51-blink1.rules ${pkgdir}/usr/lib/udev/rules.d/51-blink1.rules
}

