# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=blink1
pkgver=1.94
pkgrel=1
pkgdesc='software for blink(1) USB RGB LED'
arch=('i686' 'x86_64')
url='https://github.com/todbot/blink1'
license=('GPL')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/todbot/${pkgname}/archive/v${pkgver}.tar.gz")

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}/commandline/

	sed -i 's/-static//g' Makefile

	cd ${srcdir}/${pkgname}-${pkgver}/linux/

	sed -i 's/, GROUP="plugdev"//' 51-blink1.rules
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}/commandline/

	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}/commandline/

	# binary
	install -D -m0755 blink1-tool ${pkgdir}/usr/bin/blink1-tool

	cd ${srcdir}/${pkgname}-${pkgver}/linux/

	# udev rule
	install -D -m0644 51-blink1.rules ${pkgdir}/usr/lib/udev/rules.d/51-blink1.rules
}

sha256sums=('85030c78b40e3adbf0c924d7dfc5e8a0b90b4a0b7789269567a541e4703c0500')
