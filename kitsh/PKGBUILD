# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=kitsh
pkgver=0.4
pkgrel=1
pkgdesc="A tasteless WebKit shell."
arch=(i686 x86_64)
url="http://code.google.com/p/kitsh/"
license=('GPL3')
depends=('libwebkit')
install="$pkgname.install"
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop")
md5sums=('b54b3826c372c0032833d3376b8e09e7'
         '6e8b53b2c4a99099d09ca51670b550b5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sh $pkgname.c
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # bin
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"

  # data
  install -Dm644 $pkgname.jsh \
    "$pkgdir/usr/share/$pkgname/$pkgname.jsh"
  install -Dm644 $pkgname.png readme.txt \
    "$pkgdir/usr/share/$pkgname"

  # tests
  cp -a tests "$pkgdir/usr/share/$pkgname"

  # desktop file and icon
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 $pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
