# Maintainer: Ng Oon-Ee <ngoonee.talk@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
pkgname=syncevolution
pkgver=1.1.1
pkgrel=4
pkgdesc="Provides full two-way synchornization for Evolution via SyncML"
arch=('i686' 'x86_64')
url="http://syncevolution.org/"
license=('GPL')
depends=('evolution' 'python2' 'openobex' 'libnotify')
optdepends=('twisted: support for running local http server')
makedepends=('autoconf' 'boost' 'docutils' 'intltool' 'gnome-bluetooth')
source=(http://downloads.syncevolution.org/syncevolution/sources/$pkgname-$pkgver.tar.gz
        0001-syncevo-dbus-server-deal-with-libnotify-0.5.x-compil.patch
        gcc-4.6.patch)
md5sums=('41aab6f5a4a14b3af1db6c28e92f4174'
         'e2318d671dd1868015e4a5af0adda1c2'
         '7645bb37a393188090831c6e4ce361fa')

build() {
  cd ${srcdir}/$pkgname-*
  patch -p1 < ${srcdir}/0001-syncevo-dbus-server-deal-with-libnotify-0.5.x-compil.patch
  patch -Nup1 -i ${srcdir}/gcc-4.6.patch

  ./configure --prefix=/usr \
              --enable-core \
              --enable-bluetooth \
              --enable-gui \
              --enable-dbus-service \
              --enable-gnome-bluetooth-panel-plugin
  make 
}

package(){
  cd ${srcdir}/$pkgname-*
  make DESTDIR="${pkgdir}" install
  sed -i 's/\/usr\/bin\/python/\/usr\/bin\/python2/' ${pkgdir}/usr/bin/syncevo-phone-config
  sed -i 's/\/usr\/bin\/python/\/usr\/bin\/python2/' ${pkgdir}/usr/bin/syncevo-http-server
  #install -D -m644 ${pkgdir}/usr/etc/xdg/autostart/syncevo-dbus-server.desktop ${pkgdir}/etc/xdg/autostart/syncevo-dbus-server.desktop
  rm -R ${pkgdir}/usr/etc

}
