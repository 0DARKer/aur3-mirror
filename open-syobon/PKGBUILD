# Maintainer : AlexanderR <alexander r at gmx com>
# Contributor : Zhang Li <richselian at gmail.com>
pkgname=open-syobon
pkgver=1.0.1
pkgrel=1
pkgdesc='A cross platform, open source version of the unforgiving Japanese parody of Super Mario Bros.'
arch=('i686' 'x86_64')
url='http://zapek.com/?p=189' # http://sourceforge.net/projects/opensyobon/
license=('custom')
depends=('sdl_image' 'sdl_gfx' 'sdl_mixer' 'sdl_ttf' 'gcc-libs')
changelog=ChangeLog
source=("http://download.zapek.com/software/syobon/syobon_${pkgver}_src.tar.gz"
        'open-syobon.sh')
md5sums=('67d15adae1026537fe9ec8cec0ef2595'
         '9672196d6f2dc9df2b19695ed98849fe')

build() {
  cd "$srcdir/syobon"

  make
}

package() {
  cd "$srcdir"

  install -D -m 0755 ${pkgname}.sh "$pkgdir/usr/bin/$pkgname"

  cd syobon

  install -dm 0755 "$pkgdir/usr/lib/$pkgname/"{BGM,res,SE}
  install -D -m 0644 BGM/* -t "$pkgdir/usr/lib/$pkgname/BGM"
  install -D -m 0644 res/* -t "$pkgdir/usr/lib/$pkgname/res"
  install -D -m 0644 SE/* -t "$pkgdir/usr/lib/$pkgname/SE"
  #install -D -m 0644 icon.ico -t "$pkgdir/usr/lib/$pkgname"
  install -D -m 0755 syobon -t "$pkgdir/usr/lib/$pkgname"
}

# vim:set ts=2 sw=2 et:
