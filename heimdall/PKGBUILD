# Contributor: Todd Musall <tmusall@gmail.com>
# Maintainer: Victor Noel
pkgname=('heimdall')
pkgver=1.1.1
pkgrel=1
pkgdesc="Heimdall is a cross-platform open-source utility to flash firmware (aka ROMs) onto Samsung Galaxy S devices."
arch=('i686' 'x86_64')
url="http://www.glassechidna.com.au/products/heimdall/"
license=('MIT')
depends=('libusb' 'qt')
optdepends=('udev-android-rules: Udev rules to connect Android devices to you linux box')
makedepends=('gcc')
source=(https://github.com/downloads/Benjamin-Dobell/Heimdall/$pkgname-$pkgver.tar.gz)
md5sums=('1ebf7203939574c63ef26fe83169469f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Build heimdall command line tool
  cd heimdall/
  
  ./configure --prefix=/usr
  make

  # Build heimdall GUI front end
  cd ../heimdall-frontend/

  qmake OUTPUTDIR="$pkgdir/usr/bin" heimdall-frontend.pro
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install heimdall command line tool
  cd heimdall/

  # Prevent make install from trying to reload udev
  # We'll do this the Arch way at package install time
  mv Makefile Makefile.orig
  sed -e 's/sudo service udev reload/echo sudo service udev reload/' <Makefile.orig >Makefile

  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/lib/udev/"
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/"

  # Install heimdall GUI front end
  cd ../heimdall-frontend
  make DESTDIR="$pkgdir/" install
}
