pkgname=libkipi-git
pkgver=r937.baf426f
pkgrel=1
pkgdesc='An interface to use kipi-plugins from a KDE application'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('git' 'automoc4' 'cmake')
conflicts=('libkipi')
provides=('libkipi')
groups=('digikam-git')
source=('git://anongit.kde.org/libkipi')
md5sums=('SKIP')

pkgver() {
  cd libkipi
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake  ../libkipi -DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
