# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Bill Fraser <wfraser@codewise.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

_pkgbasename=libpcap
pkgname=lib32-libpcap
pkgver=1.5.3
pkgrel=1
pkgdesc="A system-independent interface for user-level packet capture"
arch=('x86_64')
url="http://www.tcpdump.org/"
license=('BSD')
depends=('lib32-glibc' 'lib32-libnl' 'lib32-libusb' 'sh' "${_pkgbasename}")
makedepends=('flex')
source=("http://www.tcpdump.org/release/libpcap-${pkgver}.tar.gz")

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd ${srcdir}/${_pkgbasename}-${pkgver}
	./configure --prefix=/usr --libdir=/usr/lib32 --enable-ipv6
	make
}

package() {
	cd ${srcdir}/${_pkgbasename}-${pkgver}

	make DESTDIR=${pkgdir} install

	rm -rf ${pkgdir}/usr/lib32/libpcap.a
	rm -rf ${pkgdir}/usr/{include,share,bin}

	mkdir -p "${pkgdir}/usr/share/licenses"
	ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}

sha256sums=('9ae92159c1060f15e6a90f2c4ad227268b6aaa382c316fa49a31c496b9979e93')
