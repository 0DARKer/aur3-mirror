# Maintainer: Westminboy <westminboy@gmail.com>

pkgname=sylpheed-lite
_pkgname=sylpheed
pkgver=3.3.0
pkgrel=1
pkgdesc="Lightweight in lightweight and user-friendly with nice icons e-mail client "
arch=('i686' 'x86_64')
url="http://sylpheed.sraoss.jp/en/"
license=('GPL')
depends=('gpgme' 'desktop-file-utils')
makedepends=('openssl')
options=('!libtool')
conflicts=('sylpheed')
install="$pkgname.install"
source=(http://sylpheed.sraoss.jp/$_pkgname/v3.3/$_pkgname-$pkgver.tar.bz2 
        https://dl.dropboxusercontent.com/u/40318807/Sylpheed3.0_icon-set.tar.gz)
sha256sums=('4d8f791aaf0f347439512aec72cd8262f90e020352cb2f7b63727ac5bc07353b'
            'f2bc4ffff465eb7302624cf2ba4bb95430d896e2a7149318b1c5571ab75a65d4')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  cp -rf $srcdir/src/icons/ $srcdir/$_pkgname-$pkgver/src/
  ./configure --prefix=/usr \
    --disable-compface \
    --disable-gtkspell \
    --disable-ipv6 \
    --disable-updatecheck \
    --disable-updatecheckplugin
  make

  # Build Attachment-Tool Plug-in
  cd plugin/attachment_tool && make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Install Attachment-Tool Plug-in
  cd plugin/attachment_tool
  make DESTDIR="$pkgdir/" install-plugin
}
