# Maintainer: Val√®re Monseur (dobedo) <valere dot monseur@ymail dot com>

pkgname=awf-git
pkgver=20111205
pkgrel=1
pkgdesc="A theme preview application for gtk2 and gtk3"
arch=(i686 x86_64)
url="https://github.com/dobedo/awf"
license=('GPL')
depends=('gtk2' 'gtk3')
makedepends=('git')

_gitroot="git://github.com/dobedo/awf.git"
_gitname="awf"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh
  mkdir build-gtk{2,3}

  cd "$srcdir/$_gitname-build/build-gtk2"
  ../configure --prefix=/usr
  make

  cd "$srcdir/$_gitname-build/build-gtk3"
  ../configure --prefix=/usr --with-gtk3
  make
}

package() {
  cd "$srcdir/$_gitname-build/build-gtk2"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/$_gitname-build/build-gtk3"
  make DESTDIR="$pkgdir" install

  install -D -m644 "$srcdir/$_gitname-build/COPYING" "$pkgdir/usr/share/licenses/$_gitname/COPYING"
} 
