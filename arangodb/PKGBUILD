# Maintainer: Bambang Purnomosidi D. P. <bpurnomo at akakom dot ac dot id>
# Contributor: Jan Steemann <j.steemann at triagens dot de>
pkgname=arangodb
pkgver=1.0
pkgrel=1
pkgdesc="The universal NOSQL database"
arch=('i686' 'x86_64')
url="http://www.arangodb.org/repositories/Source"
license=('Apache')
depends=('openssl' 'bash')
makedepends=('python2')
source=(http://www.arangodb.org/repositories/Source/ArangoDB-$pkgver.tar.gz)
md5sums=(a9263629e6516523969dc53dca9b3fd1) #generate with 'makepkg -g'

build() {
  cd "$srcdir/ArangoDB-$pkgver"
  ln -s -f /usr/bin/python2 python
  export PATH="`pwd`:$PATH"
  ./configure --bindir=/usr/bin --sbindir=/usr/sbin --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share
  make CFLAGS="-Wno-c++0x-compat" CXXFLAGS="-Wno-c++0x-compat" 
}

check() {
  cd "$srcdir/ArangoDB-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/ArangoDB-$pkgver"
  make DESTDIR="$pkgdir/" install
  rmdir $pkgdir/var/arangodb
  mkdir $pkgdir/var/log
  mkdir $pkgdir/var/log/arangodb
}

# vim:set ts=2 sw=2 et:
