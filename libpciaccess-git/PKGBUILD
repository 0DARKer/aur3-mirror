# Maintainer: Det <nimetonmaili at gmail a dot com>
# Contributor: Matt Parnell /ilikenwf <parwok@gmail.com>
# Contributor: Jonathan <eyeswide@gmail.com>
# Based on [extra]'s libpciaccess

pkgname=libpciaccess-git
pkgver=20120119
pkgrel=1
pkgdesc="X11 PCI access library - Git version"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/xorg/lib/libpciaccess/"
license=('custom')
makedepends=('xorg-util-macros' 'git')
options=('!libtool')
provides=('libpciaccess=99')
conflicts=('libpciaccess')

_gitroot=git://anongit.freedesktop.org/xorg/lib/${pkgname/-*}
_gitname=${pkgname/-*}

build() {
  msg "Connecting to anongit.freedesktop.org GIT server...."

  if [ -d $_gitname ] ; then
     cd $_gitname && git pull origin && cd ..
     msg "The local files are updated."
  else
     git clone --depth 1 $_gitroot
  fi

  msg "GIT checkout done or server timeout"

  msg "Starting make..."
  cd $_gitname
  ./autogen.sh --prefix=/usr
  make
}

package(){
  cd $_gitname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname/-*}/COPYING"
}