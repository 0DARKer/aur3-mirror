# Maintainer: J0k3r <moebius282 at gmail dot com>

pkgname=darkplaces_rm-git
pkgver=8931
pkgrel=1
pkgdesc="DarkplacesRM is a fork of the DarkPlaces engine compatible with the RocketMinsta project"
url="http://rocketminsta.net/"
license=('GPL2')
arch=('x86_64' 'i686')
depends=('sdl' 'libjpeg')
# additional depends of the glx version
depends+=('alsa-lib' 'libxpm' 'libxxf86vm')
optdepends=('libircclient: for irc support')
# changelog="${pkgname}.changelog"
_local_repo="${pkgname}"
source=("${_local_repo}::git+https://github.com/nexAkari/DarkPlacesRM.git")
sha256sums=("SKIP")

function pkgver
{
    cd "${srcdir}/${_local_repo}/"

    git rev-list --count HEAD
}

function prepare
{
	cd "${srcdir}/${_local_repo}/"

	sed -e 's|darkplaces-|darkplacesRM-|g' -i makefile.inc
}

function build
{
	cd "${srcdir}/${_local_repo}/"

	make sv-release sdl-release cl-release
}

function package
{
	cd "${srcdir}/${_local_repo}/"

	install -d "${pkgdir}/usr/bin/"
	install -t "${pkgdir}/usr/bin/" darkplacesRM-dedicated darkplacesRM-sdl darkplacesRM-glx
}
