# Maintainer: Philip Abernethy<chais.z3r0@gmail.com>
pkgname="shadowrun-returns"
pkgver=1397581033
pkgrel=2
pkgdesc="a science fantasy turn based tactical role-playing game"
arch=('i686' 'x86_64')
license=('custom:commercial')
url="http://harebrained-schemes.com/shadowrun/"

DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

source=("hib://${pkgname}-linux_${pkgver}.tar.gz")
sha512sums=("5062446f9466ee4864c07e9a9aa1d1bac0a9f06f37b8db85dcb9dc7cd60635fd858fe976d1652937db599ff5ad98e30eaf71906b4e739ff42cf7712448a887c2")

PKGEXT='.pkg.tar'

package() {
	mkdir -p ${pkgdir}/{opt,usr/{bin,share/applications}}
	mv "${srcdir}/Shadowrun Returns" ${pkgdir}/opt/${pkgname}
	ln -s /tmp/${pkgname}/output.log ${pkgdir}/opt/${pkgname}/output.log
	ln -s /tmp/${pkgname}/DumpBox ${pkgdir}/opt/${pkgname}/DumpBox
	echo "#!/bin/bash
if [ ! -d /tmp/${pkgname} ]; then
	mkdir -pm 777 /tmp/${pkgname}/DumpBox
	touch /tmp/${pkgname}/output.log
	chmod 666 /tmp/${pkgname}/output.log
fi
/opt/${pkgname}/Shadowrun.sh" > ${pkgdir}/usr/bin/${pkgname}
	chmod a+x ${pkgdir}/usr/bin/${pkgname}
	echo "[Desktop Entry]
Type=Application
Name=Shadowrun Returns
GenericName=Shadowrun Returns
Comment=${pkgdesc}
Icon=/opt/${pkgname}/Shadowrun_Data/Resources/UnityPlayer.png
Exec=/usr/bin/${pkgname}
Categories=Game;
Path=/opt/${pkgname}" > ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
