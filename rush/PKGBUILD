# Maintainer: Brandon Invergo <brandon@invergo.net>
# Contributor: B3nny <benny@b3nny.net>
pkgname=rush
pkgver=1.7
pkgrel=3
pkgdesc="GNU Restricted User Shell"
arch=('i686' 'x86_64')
url="http://puszcza.gnu.org.ua/software/rush/"
license=('GPL3')
backup=(etc/rush.rc)
install='rush.install'
source=("http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"
    'rush-1.7-glib-2.16-gets.patch')
noextract=()
md5sums=('02fae838fe8f5dc5b4e3a2e4da0182b8'
         'dcd87b8bf9738796621030930384f97d')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -p1 < "$srcdir/rush-1.7-glib-2.16-gets.patch"
    sed -i 's/^\( *[a-z#].*\)/#\1/g' etc/rush.rc
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}

