# Maintainer: Weirch Sodora <sodora at gmail dot com>

pkgname=perl-net-mac-vendor
_cpanname=Net-MAC-Vendor
_module=Net::MAC::Vendor
pkgver=1.21
pkgrel=1
pkgdesc="$_module - look up the vendor for a MAC"
arch=('any')
url="https://metacpan.org/release/$_cpanname"
license=('GPL' 'PerlArtistic')
depends=('perl-libwww')
options=('!emptydirs')
source=("http://cpan.metacpan.org/authors/id/B/BD/BDFOY/${_cpanname}-${pkgver}.tar.gz")
md5sums=('cdf98cbd6d8994f0a0a46aa408cb5ece')

build() {
  cd "$srcdir/$_cpanname-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_cpanname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_cpanname-$pkgver"
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -o -name '*.pod' -delete
}

