# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=geogebra-beta-maxima
_pkgname=geogebra
pkgver=3.9.244.0
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics, algebra and spreadsheet - with Maxima as CAS"
arch=('any')
url='http://www.geogebra.org/'
license=('custom')
# Application and source code under GPL / Language files under CCPL:by-sa
depends=('java-runtime' 'shared-mime-info' 'hicolor-icon-theme' 'maxima')
provides=('geogebra')
optdepends=('geogebra-thumbnail-kde')
conflicts=('geogebra')
install=('geogebra-beta.install')
source=(http://geogebra.googlecode.com/files/GeoGebra-Linux-Installer-${pkgver}.tar.gz
	geogebra.patch)
noextract=('*.jar')

package() {
  cd $srcdir/${_pkgname}-$pkgver

  # Installing application
  install -d -m755 $pkgdir/usr/share/java/${_pkgname}/unsigned
  install -D -m644 *.jar $pkgdir/usr/share/java/${_pkgname}
  install -D -m644 unsigned/*.jar $pkgdir/usr/share/java/${_pkgname}/unsigned
  patch -p0 < ${srcdir}/geogebra.patch
  install -d -m755 ${pkgdir}/usr/bin
  install -D -m755 ${_pkgname} $pkgdir/usr/bin/${_pkgname}

  # Copying icons
  cp -R icons $pkgdir/usr/share/
  
  # Creating menu item
  install -D -m644 ${_pkgname}.desktop $pkgdir/usr/share/applications/${_pkgname}.desktop

  # Installing mime-type
  install -D -m644 ${_pkgname}.xml $pkgdir/usr/share/mime/packages/${_pkgname}.xml

  # Installing license
  install -D -m644 _LICENSE.txt ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE
}

md5sums=('2e604fb8f0f4e16cefb492cca8c24dfc'
         '03614b78741d523b7212e90e5ebac641')
