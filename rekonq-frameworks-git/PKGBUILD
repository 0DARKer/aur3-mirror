# Maintainer: Antonio Rojas 

pkgname=rekonq-frameworks-git
pkgver=r3705.94520b6
pkgrel=1
pkgdesc='A WebKit based web browser for KDE'
arch=('i686' 'x86_64')
url='http://rekonq.kde.org/'
license=('GPL')
depends=('kcmutils' 'kdewebkit' 'kinit')
makedepends=('extra-cmake-modules' 'git')
source=("git://anongit.kde.org/rekonq.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
  cd rekonq
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  sed -i 's|KF5 CONFIG REQUIRED|KF5 REQUIRED|' rekonq/CMakeLists.txt
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
  export CMAKE_PREFIX_PATH="/opt/kf5"
 
  cd build
  cmake ../rekonq \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
