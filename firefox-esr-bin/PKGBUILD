# Maintainer: T. Jameson Little <t.jameson.little at gmail dot com>
# Previous Maintainer: Stephen Michael <ihateseptictanks at gmail dot com>
# Previous Maintainer: Simon Tunnat <simon+aur@tunn.at>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=firefox-esr-bin
_pkgname=${pkgname/-bin/}
pkgver=24.7.0
pkgrel=1
pkgdesc='Standalone web browser from mozilla.org - Extended Support Release'
url='http://www.mozilla.org/en-US/firefox/organizations/'
arch=('i686' 'x86_64')
depends=('gtk2' 'dbus-glib' 'desktop-file-utils' 'libxt' 'mime-types' 'nss' 'shared-mime-info' 'gconf')
optdepends=('libgnome: fixes file association/default browser issues on GNOME desktops.'
            'gstreamer0.10-base-plugins: vorbis decoding, ogg demuxing'
            'gstreamer0.10-good-plugins: webm and mp4 demuxing'
            'gstreamer0.10-bad-plugins: aac, vp8 and opus decoding'
            'gstreamer0.10-ugly-plugins: h.264 and mp3 decoding'
            'gstreamer0.10-ffmpeg: more decoders')
provides=('firefox=24')

license=('MPL' 'GPL' 'LGPL')
install=$_pkgname.install
source=(http://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}esr/linux-$CARCH/en-US/firefox-${pkgver}esr.tar.bz2
        $_pkgname.desktop 
        $_pkgname-safe.desktop)
sha512sums=('b328a0ce2c050443b3fa2bd46982a56db6bd2d27ad0574eed94204b489568387d5f3123e88777d14803df1b3d78aaeffd9512b38ffbf3498523eb2d87d417edd'
            'efe674b915079bcf0664a7ad094258189407b8d7bbbb393e1d5b5915af395446a0c8f74518e63402ed5b008cf7d27740fded3ab93d11b7640cf7ac5cf2d5b3c6 '
            'fafc9dac5de84253db1d1372d7dc2789182e01a556b3f55d4794f4ffa3a9fefc7d28df0351edce8ba3ab0203a7731e098f092011bc7a30ce98cba8c0a340b9b4 ')

if [ "$CARCH" == "i686" ]; then
    sha512sums[0]='db2367a6323b9b15d87bd4003c612303b466abaa666add0c804ba6ed885965ad5621f402c24fb4d1ed349799df2e954e4f6ef996b63e0eb8f0743fd25e3c0227'
fi


package() {
	cd $srcdir
	
	install -d $pkgdir/{usr/{bin,share/{applications,pixmaps}},opt}
	cp -r firefox/ $pkgdir/opt/$_pkgname

	ln -s /opt/$_pkgname/firefox $pkgdir/usr/bin/$_pkgname
	install -m644 $srcdir/{$_pkgname.desktop,$_pkgname-safe.desktop} $pkgdir/usr/share/applications/
	install -m644 $srcdir/firefox/browser/icons/mozicon128.png $pkgdir/usr/share/pixmaps/$_pkgname.png
}
