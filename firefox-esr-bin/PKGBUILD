# Maintainer: T. Jameson Little <t.jameson.little at gmail dot com>
# Previous Maintainer: Stephen Michael <ihateseptictanks at gmail dot com>
# Previous Maintainer: Simon Tunnat <simon+aur@tunn.at>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=firefox-esr-bin
_pkgname=${pkgname/-bin/}
pkgver=31.1.1
pkgrel=1
pkgdesc='Standalone web browser from mozilla.org - Extended Support Release'
url='http://www.mozilla.org/en-US/firefox/organizations/'
arch=('i686' 'x86_64')
depends=('gtk2' 'dbus-glib' 'desktop-file-utils' 'libxt' 'mime-types' 'nss' 'shared-mime-info' 'gconf')
optdepends=('libgnome: fixes file association/default browser issues on GNOME desktops.')
provides=('firefox=31')

license=('MPL' 'GPL' 'LGPL')
install=$_pkgname.install
source=(http://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}esr/linux-$CARCH/en-US/firefox-${pkgver}esr.tar.bz2
        $_pkgname.desktop 
        $_pkgname-safe.desktop)
sha512sums=('6fee70491e2421da499c569abf3a78b8ba872f2b777d420d615b918d24e596d7b916ab43e413ce8dcc8c457e57de8ce2265e9c8f216154d675204869719feb41'
            'efe674b915079bcf0664a7ad094258189407b8d7bbbb393e1d5b5915af395446a0c8f74518e63402ed5b008cf7d27740fded3ab93d11b7640cf7ac5cf2d5b3c6'
            'fafc9dac5de84253db1d1372d7dc2789182e01a556b3f55d4794f4ffa3a9fefc7d28df0351edce8ba3ab0203a7731e098f092011bc7a30ce98cba8c0a340b9b4')
[ "$CARCH" == "i686" ] && sha512sums[0]='4a502864f8ee4c6e5e62e239fe57c5cba29f093122d1e0f8144f8d202b2b480ebcfbd1c78d0c378ed36e0577de079c844d3d9371e1a2eb539abbb1f2cc9da0cb'


package() {
    cd $srcdir
    
    install -d $pkgdir/{usr/{bin,share/{applications,pixmaps}},opt}
    cp -r firefox/ $pkgdir/opt/$_pkgname

    ln -s /opt/$_pkgname/firefox $pkgdir/usr/bin/$_pkgname
    install -m644 $srcdir/{$_pkgname.desktop,$_pkgname-safe.desktop} $pkgdir/usr/share/applications/
    install -m644 $srcdir/firefox/browser/icons/mozicon128.png $pkgdir/usr/share/pixmaps/$_pkgname.png
}
