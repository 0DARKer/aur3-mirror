# Maintainer: Ng Oon-Ee <ngoonee.talk@gmail.com>
pkgname=gnome-themes-standard-git
pkgver=20110412
pkgrel=1
pkgdesc="Default themes for the GNOME desktop pulled from git, specifically contains the Adwaita dark theme."
arch=('i686' 'x86_64')
url="http://abattis.org/cantarell/"
license=('GPL2')
depends=('cantarell-fonts' 'gtk3' 'gtk-engines')
makedepends=('intltool' 'gnome-common')
replaces=('gnome-themes' 'gnome-themes-standard')
conflicts=('gnome-themes' 'gnome-themes-standard')
provides=('gnome-themes-standard')
source=('adwaita-dark-gtk2')
md5sums=('4f801784d786dbd8102359891b3e40a5')

_DEFAULTDARKADWAITA=0 # set this to one to make Adwaita default to dark

_gitroot="git://git.gnome.org/gnome-themes-standard"
_gitname="gnome-themes-standard"

build() {
  msg "Connecting to gnome GIT server...."
  cd $srcdir
 
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done"
  msg "Starting make..."

  # Remove old -build
  rm -rf $srcdir/$_gitname-build

  cp -r $srcdir/$_gitname $srcdir/$_gitname-build
  cd $srcdir/$_gitname-build

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$_gitname-build
  make DESTDIR="${pkgdir}" install

  if [ "$_DEFAULTDARKADWAITA" = "1" ]; then
    cd $pkgdir/usr/share/themes/Adwaita/gtk-3.0
    mv gtk.css gtk.css.save
    mv gtk-widgets-assets.css gtk-widgets-assets.css.save
    ln -s gtk-dark.css gtk.css
    ln -s gtk-widgets-assets-dark.css gtk-widgets-assets.css

    cd $pkgdir/usr/share/themes/Adwaita/gtk-2.0
    sed -i "/gtk-color-scheme.*/ {
              r $srcdir/adwaita-dark-gtk2
              d}" gtkrc
  fi

}

# vim:set ts=2 sw=2 et:
