# Maintainer: codestation <codestation404@gmail.com>

pkgname=qcma-git
_pkgname=qcma
pkgver=0.3.2.r3.ga85d49e
pkgrel=1
pkgdesc="Content Manager Assistant for the PS Vita"
arch=("i686" "x86_64")
url="https://github.com/codestation/qcma"
license=('GPL')
depends=('qt5-base' 'vitamtpmod-git>=2.5.3' 'ffmpeg')
makedepends=('git' 'qt5-tools')

conflicts=('qcma-testing-git' 'qcma-kde-git')
source=('git://github.com/codestation/qcma.git')
install=qcma.install
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --tags --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

build() {
  cd "$srcdir/$_pkgname"

  lrelease-qt5 resources/translations/*.ts
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make INSTALL_ROOT="${pkgdir}" install
}
