# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: codestation <codestation404@gmail.com>

pkgname=qcma-git
pkgver=v0.3.6.0.g953afb0
pkgrel=1
pkgdesc="Content Manager Assistant for the PS Vita (GIT version)"
arch=("i686" "x86_64")
url="https://github.com/codestation/qcma"
license=('GPL')
depends=('qt4' 'vitamtpmod' 'ffmpeg' 'libnotify')
makedepends=('git')
conflicts=('qcma')
provides=('qcma')
source=('git+https://github.com/codestation/qcma.git')
install=qcma.install
md5sums=('SKIP')
_gitname=qcma
#options=('!makeflags')

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"

  lrelease-qt4 resources/translations/*.ts
  qmake-qt4 qcma.pro PREFIX=/usr
  make
}

package() {
  cd "${_gitname}"
  make INSTALL_ROOT="${pkgdir}" install
}
