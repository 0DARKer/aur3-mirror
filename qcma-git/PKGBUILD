# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: codestation <codestation404@gmail.com>

pkgbase=qcma-git
pkgname=('qcma-git' 'qcma-kdenotifier-git')
pkgver=v0.3.6.2.ga4662a1
pkgrel=1
pkgdesc="Content Manager Assistant for the PS Vita. (GIT version)"
arch=("i686" "x86_64")
url="https://github.com/codestation/qcma"
license=('GPL')
makedepends=('git' 'qt4' 'vitamtpmod' 'ffmpeg' 'libnotify' 'knotifications')
conflicts=('qcma' 'qcma-kdenotifier')
provides=('qcma' 'qcma-kdenotifier')
source=('git+https://github.com/codestation/qcma.git')
install=qcma.install
md5sums=('SKIP')

pkgver() {
  cd qcma
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  mkdir -p build-qcma
  mkdir -p build-qcma-kdenotifier

  # workaround for qtchooser defaulting to Qt5
  sed 's/system(qdbuscpp2xml /system(qdbuscpp2xml-qt4 /' -i qcma/qcma_gui.pro -i qcma/qcma_cli.pro
}

build() {
  export QT_SELECT=4
  pushd build-qcma &> /dev/null
  lrelease-qt4 ../qcma/resources/translations/*.ts
  qmake-qt4 ../qcma/qcma.pro PREFIX=/usr
  make
  popd &> /dev/null
  pushd build-qcma-kdenotifier &> /dev/null
  qmake-qt4 ../qcma/qcma_kdenotifier.pro PREFIX="/usr"
  make
  popd &> /dev/null
}

package_qcma-git() {
  pkgdesc="Content Manager Assistant for the PS Vita. (GIT Version)"
  depends=('ffmpeg' 'qt4' 'libnotify' 'vitamtpmod')
  conflicts=('qcma')
  provides=('qcma')
  install=qcma.install
  make -C build-qcma INSTALL_ROOT="${pkgdir}" install
}

package_qcma-kdenotifier-git() {
  pkgdesc="Content Manager Assistant for the PS Vita (KDE notifier) (GIT Version)"
  depends=('qcma-git' 'kdelibs')
  conflicts=('qcma-kdenotifier')
  provides=('qcma-kdenotifier')
  make -C build-qcma-kdenotifier INSTALL_ROOT="${pkgdir}" install
}