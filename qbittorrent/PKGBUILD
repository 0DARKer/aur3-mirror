# Maintainer: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=qbittorrent
pkgver=3.0.6
pkgrel=2
pkgdesc="A bittorrent client written in C++ / Qt4 using the good libtorrent library"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt' 'libtorrent-rasterbar' 'boost-libs' 'xdg-utils' 'geoip')
makedepends=('boost')
optdepends=('python: needed for search')
install="qbittorrent.install"
source=("http://downloads.sourceforge.net/sourceforge/qbittorrent/${pkgname}-${pkgver}.tar.xz"
"http://geolite.maxmind.com/download/geoip/database/GeoLiteCountry/GeoIP.dat.gz"
)


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp ${srcdir}/GeoIP.dat ${srcdir}/${pkgname}-${pkgver}/src/geoip/ 

  ./configure --prefix=/usr --with-geoip-database-embedded

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make INSTALL_ROOT=${pkgdir} install
}

md5sums=('97b1c06c6868c4222f469a6a0b88b2ff'
         'ab0f52a35128d1aced906ac4cbfbed9c')
