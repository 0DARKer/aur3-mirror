# Maintainer: Philippe Mongeau <ph.mongeau@gmail.com>

pkgname=ympd-git
_pkgname=ympd
pkgver=1.0.0.r9.g392cf1e
pkgrel=1
pkgdesc="A standalone MPD Web GUI"
arch=('i686' 'x86_64')
url="http://www.ympd.org"
license=('BSD')
depends=('libwebsockets-git' 'libmpdclient')
makedepends=('cmake' 'git')
optdepends=('openssl: SSL support in libwebsockets webserver')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=(
  "git+https://github.com/notandy/ympd.git"
)
sha1sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --long --tags | sed -E 's/^v//; s/([^-]*-g)/r\1/; s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  mkdir build && cd build
  cmake .. -DWITH_STATIC_WEBSOCKETS=0 -DCMAKE_INSTALL_PREFIX:PATH=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}/build"
  make DESTDIR="$pkgdir/" install
  install -Dm644  "${srcdir}/${_pkgname}/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644  "${srcdir}/${_pkgname}/contrib/ympd.service" "$pkgdir/usr/lib/systemd/system/ympd.service"
}

# vim:set ts=2 sw=2 et:
