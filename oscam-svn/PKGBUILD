# Maintainer:  Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Markus Opitz <mastero23 at gmail dot com>
pkgname=oscam-svn
pkgrel=1
pkgver=8603
pkgdesc="The Open Source Conditional Access Module daemon"
arch=('i686' 'x86_64')
url="http://oscam.to/"
license=('GPL3')
depends=('bash' 'openssl' 'libusbx' 'pcsclite')
makedepends=('subversion')
provides=('oscam')
conflicts=('oscam')
options=('emptydirs')
install=oscam.install
source=('oscam::svn+http://www.streamboard.tv/svn/oscam/trunk'
        'oscam.service')
md5sums=('SKIP'
         'c0e65229e114c6a61aba7f0ce57f785d')
_svnmod=oscam

pkgver() {  
  cd "$SRCDEST/$_svnmod"
  svnversion
}

prepare() {
  rm -rf "$srcdir/$_svnmod"
  cp -r "$SRCDEST/$_svnmod" "$srcdir"
}

build() {
  cd "$srcdir/$_svnmod"
  make allyesconfig
  make OSCAM_BIN=oscam CONF_DIR=/etc/oscam USE_LIBUSB=1 USE_PCSC=1
}

package() {
  install -D -m 755 "$srcdir/$_svnmod/oscam" "$pkgdir/usr/bin/oscam"
  install -D -m 644 "$srcdir/oscam.service" "$pkgdir/usr/lib/systemd/system/oscam.service"
  install -m 755 -d "$pkgdir/etc/oscam"
  install -m 755 -d "$pkgdir/usr/share/doc/oscam"/{example,monitor}
  install -m 644 "$srcdir/$_svnmod/Distribution/doc/txt"/* "${pkgdir}/usr/share/doc/oscam/"
  install -m 644 "$srcdir/$_svnmod/Distribution/doc/example"/* "${pkgdir}/usr/share/doc/oscam/example"
  install -m 644 "$srcdir/$_svnmod/Distribution/monitor"/* "${pkgdir}/usr/share/doc/oscam/monitor"
  install -m 755 -d "$pkgdir/usr/share/man"/{man1,man5}
  install -m 644 "$srcdir/$_svnmod/Distribution/doc/man"/*.1 "${pkgdir}/usr/share/man/man1"
  install -m 644 "$srcdir/$_svnmod/Distribution/doc/man"/*.5 "${pkgdir}/usr/share/man/man5"
}