# Maintainer:  Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Markus Opitz <mastero23 at gmail dot com>

pkgname=oscam-svn
pkgver=7936
pkgrel=1
pkgdesc="The Open Source Conditional Access Module daemon"
arch=('i686' 'x86_64')
url="http://oscam.to/"
license=('GPL3')
depends=('bash' 'openssl' 'libusbx' 'pcsclite')
makedepends=('subversion')
provides=('oscam')
conflicts=('oscam')
options=(emptydirs)
install=oscam.install
source=(oscam.conf.d
        oscam.rc.d
        oscam.service)
md5sums=('79ad19c4e1a9ffbc105698914240e940'
         '148f0d5e027c5e7b0ebe0fa45fd3e42e'
         'c0e65229e114c6a61aba7f0ce57f785d')

_svntrunk=http://streamboard.de.vu/svn/oscam/trunk/
_svnmod=oscam

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/oscam-build"
  cp -r "$srcdir/oscam" "$srcdir/oscam-build"
  cd "$srcdir/oscam-build"

  make allyesconfig
  make  OSCAM_BIN=oscam CONF_DIR=/etc/oscam USE_LIBUSB=1 USE_PCSC=1
}

package() {
  install -D -m 755 "$srcdir/oscam-build/oscam" "$pkgdir/usr/bin/oscam"
  install -D -m 755 "$srcdir/oscam.rc.d" "$pkgdir/etc/rc.d/oscam"
  install -D -m 644 "$srcdir/oscam.conf.d" "$pkgdir/etc/conf.d/oscam"
  install -D -m 644 "$srcdir/oscam.service" "$pkgdir/usr/lib/systemd/system/oscam.service"
  install -m 755 -d "$pkgdir/etc/oscam"

  install -m 755 -d "$pkgdir/usr/share/doc/oscam"/{example,monitor}
  install -m 644 "$srcdir/oscam-build/Distribution/doc/txt"/* "${pkgdir}/usr/share/doc/oscam/"
  install -m 644 "$srcdir/oscam-build/Distribution/doc/example"/* "${pkgdir}/usr/share/doc/oscam/example"
  install -m 644 "$srcdir/oscam-build/Distribution/monitor"/* "${pkgdir}/usr/share/doc/oscam/monitor"
  install -m 755 -d "$pkgdir/usr/share/man"/{man1,man5}
  install -m 644 "$srcdir/oscam-build/Distribution/doc/man"/*.1 "${pkgdir}/usr/share/man/man1"
  install -m 644 "$srcdir/oscam-build/Distribution/doc/man"/*.5 "${pkgdir}/usr/share/man/man5"
}
