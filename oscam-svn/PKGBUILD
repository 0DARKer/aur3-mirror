# Maintainer: Markus Opitz <mastero23 at gmail dot com>

pkgname=oscam-svn
pkgver=7544
pkgrel=1
pkgdesc="An Open Source Conditional Access Module software"
arch=('i686' 'x86_64')
url="http://oscam.to/"
license=('unknown')
depends=('bash' 'openssl')
makedepends=('cmake' 'subversion')
provides=('oscam')
conflicts=('oscam')
options=(emptydirs)
install=oscam.install
source=(oscam.conf.d
        oscam.rc.d
        oscam.service)
md5sums=('79ad19c4e1a9ffbc105698914240e940'
         '148f0d5e027c5e7b0ebe0fa45fd3e42e'
         '5e5c8bc2d88eeb3ee60dade6c2d2ab0c')

_svntrunk=http://streamboard.de.vu/svn/oscam/trunk/
_svnmod=oscam

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DWEBIF=1 \
        -DHAVE_DVBAPI=1 \
        -WITH_SSL=1 \
        "$srcdir/$_svnmod-build"
  make
}

package() {
  cd "$srcdir/$_svnmod-build"

  make DESTDIR="$pkgdir/" install

  install -m 755 -d "$pkgdir/etc/oscam"
  install -D -m 755 "$srcdir/oscam.rc.d" "$pkgdir/etc/rc.d/oscam"
  install -D -m 644 "$srcdir/oscam.conf.d" "$pkgdir/etc/conf.d/oscam"
  install -D -m 644 "$srcdir/oscam.service" "$pkgdir/usr/lib/systemd/system/oscam.service"
}
