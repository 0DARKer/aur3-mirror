# Maintainer: Not me.

pkgname=ipset-git
pkgver=20111123
pkgrel=1
pkgdesc="Manage Linux IP sets"
url="http://ipset.netfilter.org/"
license=('GPL2')
depends=('libmnl')
makedepends=('git')
provides=('ipset')
conflicts=('ipset')
options=('!libtool')
arch=('i686' 'x86_64')

_gitroot="git://git.netfilter.org/ipset.git"
_gitname="ipset.git"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh
  ./configure --prefix=/usr --with-pic --disable-debug --disable-static --enable-shared --with-kmod=no
  # Prevent libtool from defining rpath
  sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
  sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool
  make V=1
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
} 
