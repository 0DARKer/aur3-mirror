# Maintainer: jospehgbr <rafael.f.f1@gmail.com>

_pkgbase=fribidi
pkgname=lib32-${_pkgbase}
pkgver=0.19.2
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://fribidi.org"
depends=('lib32-glibc' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://fribidi.org/download/${_pkgbase}-${pkgver}.tar.gz")
md5sums=('626db17d2d99b43615ad9d12500f568a')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
