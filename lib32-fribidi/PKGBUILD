# Maintainer: jospehgbr <rafael.f.f1@gmail.com>

_pkgbase=fribidi
pkgname=lib32-${_pkgbase}
pkgver=0.19.4
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://fribidi.org"
depends=('lib32-glibc' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://fribidi.org/download/${_pkgbase}-${pkgver}.tar.bz2")
md5sums=('0173f98ff929782d45819fc4a813c175')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
