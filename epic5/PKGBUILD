# Maintainer: Karol "Kenji Takahashi" Wozniak <wozniakk@gmail>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Mateusz Herych <heniekk@gmail.com>

pkgname=epic5
pkgver=1.1.3
pkgrel=1
pkgdesc="IRC client program that was forked from ircII"
arch=('i686' 'x86_64')
url="http://www.epicsol.org/"
license=('custom')
depends=('libarchive' 'ncurses' 'openssl' 'perl' 'tcl')
source=("http://ftp.epicsol.org/pub/epic/EPIC5-PRODUCTION/$pkgname-$pkgver.tar.bz2")
md5sums=('0fa9942fc72b5bae01a180550bd55983')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
                --with-ssl \
                --with-ipv6 \
                --with-perl \
                --with-tcl || return 1
    make || return 1
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make prefix="$pkgdir/usr" \
         libexecdir="$pkgdir/usr/bin" \
         mandir="$pkgdir/usr/share/man" \
         install
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}

# vim:set ts=4 sw=4 et:

