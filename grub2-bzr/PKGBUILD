# This is an example PKGBUILD file. Use this as a start to creating your own,
# and remove these comments. For more information, see 'man PKGBUILD'.
# NOTE: Please fill out the license field for your package! If it is unknown,
# then please put 'unknown'.

# See http://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines
# for more information on packaging from Bazaar sources.

# Maintainer: Your Name <youremail@domain.com>
pkgname=grub2-bzr
pkgver=3006
pkgrel=1
pkgdesc="The GNU GRand Unified Bootloader"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/grub/"
license=('GPL3')
groups=()
depends=('sh' 'xz' 'freetype2')
makedepends=('bzr' 'freetype2' 'bdf-unifont' 'sdl' 'python2' 'autogen' 'help2man')
    # already in core: ('device-mapper' 'gettext' 'ncurses' 'libusb')
optdepends=('gettext:   /sbin/grub-mkconfig'
            'libusb:    /sbin/grub-emu'
            'os-prober: /etc/grub.d/30_os-prober')
provides=('grub')
conflicts=('grub' 'grub2')
replaces=()
backup=('boot/grub/grub.cfg'
        'etc/default/grub'
        'etc/grub.d/40_custom')
options=()
install="grub.install"
source=('20_memtest86+'
        'grub.cfg'
        'grub.default'
        'grub.install'
        'archlinux-script-fixes.patch'
        'menucolors-in-mkconfig.patch'
        'kernel.vbefb.patch'
        'mkfont.freetype.errors.patch')
noextract=()
md5sums=('5a07e04c4ecb8ed145d54fec3043e0d5'
         '743215998a581a54ac77630f0db222ce'
         '50123c37cf145a98aba4ffb0fc4efba1'
         '0fb6ccb29f66423423d973d6dd9cd73a'
         '6ffb0711f3c66998572448141fb7a401'
         'f4deed7bfee7a8d7044c498006080965'
         '285e18868a8aed296aad85bb865f300d'
         'a64a104b6d1274491b64d955e4bbe213')

_bzrtrunk="http://bzr.savannah.gnu.org/r/grub/trunk/grub/"
_bzrmod="grub2"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d "${_bzrmod}" ] ; then
    cd "${_bzrmod}" && bzr --no-plugins pull "${_bzrtrunk}" -r "${pkgver}"
    msg "The local files are updated."
  else
    bzr --no-plugins branch "${_bzrtrunk}" "${_bzrmod}" -q -r "${pkgver}"
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_bzrmod}-build"
  cp -a "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
  cd "${srcdir}/${_bzrmod}-build"

  #
  # Start Grub2 Build
  #

  # some random patches to facilitate automatic creation of grub.cfg
  patch -Np0 -i "${srcdir}/archlinux-script-fixes.patch"
  patch -Np0 -i "${srcdir}/menucolors-in-mkconfig.patch"
  # expose freetype errors from grub-mkconfig, phcoder@freenode.net/#grub
  patch -Np0 -i "${srcdir}/mkfont.freetype.errors.patch"
  # pass extended VBE modes to the kernel, phcoder@freenode.net/#grub
  patch -Np0 -i "${srcdir}/kernel.vbefb.patch"

  # use python2 in autogen.sh
  sed -i 's|python|python2|g' autogen.sh

  # run autogen.sh to create configure files
  ./autogen.sh

  # add the unifont.bdf directory location
  sed -i 's|/usr/share/fonts/unifont|/usr/share/fonts/unifont /usr/share/fonts/misc|g' configure

  CFLAGS= ./configure   --prefix=/usr \
                        --bindir=/bin \
                        --sbindir=/sbin \
                        --mandir=/usr/share/man \
                        --infodir=/usr/share/info \
                        --sysconfdir=/etc \
                        --enable-device-mapper \
                        --enable-grub-mkfont

  CFLAGS= make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"
  make DESTDIR="${pkgdir}" install

  # install /etc/default/grub
  install -Dm644 ${srcdir}/grub.default ${pkgdir}/etc/default/grub

  # install grub.cfg (needed so it doesn't get removed on upgrading because it was previously here)
  install -Dm644 ${srcdir}/grub.cfg ${pkgdir}/boot/grub/grub.cfg

  # install memtest config detection
  install -Dm755 ${srcdir}/20_memtest86+ ${pkgdir}/etc/grub.d/20_memtest86+
}
