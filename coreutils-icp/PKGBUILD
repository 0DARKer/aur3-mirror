# Maintainer: Henrik J. Pedersen <HenrikJuul@gmail.com>
# Contributor: Palle Ravn <Ravnzon@gmail.com>
#
# This package is created from PKGBUILD and .install for coreutils-8.19 by:
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
#
# NOTE:
#
# Checks might take a long time. The 'check' section can be left out without
# breaking the build flow. 

pkgname=coreutils-icp
pkgver=8.20
pkgrel=1
_icpVer=1.0
pkgdesc="GNU Coreutils with icp progressbar patch for copy"
arch=('x86_64' 'i686')
license=('GPL3')
url="http://bitbucket.org/henrikjuul/icp"
depends=('glibc' 'pam' 'acl' 'gmp' 'libcap')
provides=('coreutils')
conflicts=('coreutils')
install=${pkgname}.install
source=(ftp://ftp.gnu.org/gnu/coreutils/coreutils-$pkgver.tar.xz
        http://bitbucket.org/henrikjuul/icp/downloads/coreutils-$pkgver-icp-$_icpVer.patch)
md5sums=('3d69af8f561fce512538a9fe85f147ff'
         'a71be92152385387d49fc3bb27b1130c')

build() {
  patch -p1 < coreutils-$pkgver-icp-$_icpVer.patch
  cd ${srcdir}/coreutils-${pkgver}
  
  echo "Running autoreconf, this might take a while..."
  autoreconf

  ./configure --prefix=/usr --libexecdir=/usr/lib \
              --enable-no-install-program=groups,hostname,kill,uptime \
              --enable-pam
  make
}

check() {
  cd ${srcdir}/coreutils-${pkgver}
  make RUN_EXPENSIVE_TESTS=yes check
}

package() {
  cd ${srcdir}/coreutils-${pkgver}
  make DESTDIR=${pkgdir} install

  cd ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/bin

  # binaries required by FHS
  _fhs=('cat' 'chgrp' 'chmod' 'chown' 'cp' 'date' 'dd' 'df' 'echo' 'false'
        'ln' 'ls' 'mkdir' 'mknod' 'mv' 'pwd' 'rm' 'rmdir' 'stty' 'sync'
        'true' 'uname')
  for i in ${_fhs[@]}; do
    ln -s ../usr/bin/$i ${pkgdir}/bin/$i
  done
}
