# $Id: PKGBUILD 142173 2011-11-05 11:56:53Z pierre $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=wv2
pkgver=0.4.2
pkgrel=3
pkgdesc="MSWord 9,8,7,6 formats parser"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/wvware/"
depends=('libgsf')
makedepends=('cmake')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/wvware/${pkgname}-${pkgver}.tar.bz2"
        'olestream.h-glib.diff')
license=('GPL2')
md5sums=('850ed8e44a84e7bf0610747827018cbc'
         'a35660dcababe7a2fa1c600d314aae6f')

build() {
  cd ${srcdir}

  ## patch olestream.h for glib include changes
  msg "Patching olestream.h for glib include changes..."
  patch -Np0 -i olestream.h-glib.diff

  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
