pkgname=transmission-cli-svn
pkgver=11796
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (CLI tools, daemon and web client)"
url="http://www.transmissionbt.com/"
license="MIT"
arch=('i686' 'x86_64')
depends=('curl>=7.16.3' 'openssl>=0.9.4' 'desktop-file-utils' 'libevent>=2.0.10')
provides=('transmission-cli')
conflicts=('transmission-cli')
makedepends=('subversion' 'intltool')
backup=('etc/conf.d/transmissiond')
install=transmission-cli.install
source=(transmissiond
        transmissiond.conf)
md5sums=('2a87aa815d2702209639719a9179d6fb'
         '7b019a710493ec4f8b54a1f528e1981d')

_svntrunk="svn://svn.transmissionbt.com/Transmission/trunk"
_svnmod="Transmission"

build() {
  cd "$srcdir"
  svn co "$_svntrunk" "$_svnmod"
  msg "SVN checkout done or server timeout"

  [[ -d "$srcdir/$_svnmod-build" ]] && rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  msg "Starting make..."
  ./autogen.sh --prefix=/usr
  make
}
package() {
  cd "$srcdir/$_svnmod-build"

  for dir in daemon cli web utils
  do
      make -C "${dir}" DESTDIR="${pkgdir}" install
  done

  install -D -m755 "${srcdir}/transmissiond" "${pkgdir}/etc/rc.d/transmissiond"
  install -D -m644 "${srcdir}/transmissiond.conf" "${pkgdir}/etc/conf.d/transmissiond"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-cli/COPYING"
}
