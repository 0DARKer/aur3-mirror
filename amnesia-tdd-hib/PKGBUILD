# Maintainer:  ainola <opp310@alh.rqh> (ROT13)
# Contributor: Gerardo Marset <gammer1994@gmail.com>

pkgname=amnesia-tdd-hib
pkgver=1.3.1
pkgrel=1
pkgdesc="Amnesia: The Dark Descent is a first person survival horror game. (Humble Bundle version)"
arch=('i686' 'x86_64')
url="http://www.amnesiagame.com/"
license=('custom: commercial')
makedepends=('unzip')
depends=('sdl2' 'glu' 'openal' 'libtheora' 'libvorbis' 'libxft')
conflicts=('amnesia-tdd')
source=("hib://amnesia_tdd_${pkgver}.sh"
        "${pkgname}-justine.sh"
        "${pkgname}.desktop")
noextract=("amnesia_tdd_${pkgver/_/-}.sh")
sha256sums=("00b4486d64e0ed4c64dd3f1e718adf08a063fede4b5bb215fbda5e6c034d013a"
            "3cf52e2a5eab7154e182a7c3a6d3172f647df1e5c1d06cc9115c8dc95f0478da"
            "bf7c6c5d1c214ca47e8cbb38dc175ca1e258ef1a44a2dd480d4a60ec0c98d7e0")
[ "$CARCH" == "x86_64" ] && _arch="x86_64" || _arch="x86"
# Prevent compressing final package
PKGEXT='.pkg.tar'

# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download the file manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf"; echo "Read this PKGBUILD for more info."; exit 1')


package() {
    # Unzip will produce an error code because it is unable to unzip the Mojo Installer.
    # Therefore, a conditional into a no-op command will keep the PKGBUILD from failing
    # Of course, if you have any real problems unzipping the PKGBUILD will not abort.
    unzip -d "${srcdir}" "amnesia_tdd_${pkgver/_/-}.sh" || :
    mkdir -p "${pkgdir}/opt/${pkgname}"
    mv "${srcdir}/data/noarch/"* "${pkgdir}/opt/${pkgname}"
    mv "${srcdir}/data/${_arch}/"* "${pkgdir}/opt/${pkgname}"

    # Install Binaries/Launchers
    mkdir -p "$pkgdir/usr/bin"
    ln -s "/opt/${pkgname}/Amnesia.bin.${_arch}"  "${pkgdir}/usr/bin/${pkgname}"
    ln -s "/opt/${pkgname}/Launcher.bin.${_arch}" "${pkgdir}/usr/bin/${pkgname}-launcher"
    install -Dm755 "${pkgname}-justine.sh" "${pkgdir}/usr/bin"

    # Install License
    mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
    mv "${srcdir}/data/EULA/"* "${pkgdir}/usr/share/licenses/${pkgname}/"

    # Install Desktop Integration
    mkdir -p "$pkgdir"/usr/share/{pixmaps,applications}
    ln -s "/opt/${pkgname}/Amnesia.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
