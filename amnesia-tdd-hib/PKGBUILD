# Maintainer:  ainola <opp310@alh.rqh> (ROT13)
# Contributor: Gerardo Marset <gammer1994@gmail.com>

pkgname=amnesia-tdd-hib
pkgver=1.3.0_3
pkgrel=1
pkgdesc="Amnesia: The Dark Descent is a first person survival horror game. (Humble Bundle version)"
arch=('i686' 'x86_64')
url="http://www.amnesiagame.com/"
license=('custom: commercial')
makedepends=('unzip')
depends=('sdl2' 'glu' 'openal' 'libtheora' 'libvorbis' 'libxft')
conflicts=('amnesia-tdd')
source=("hib://amnesia_tdd_${pkgver/_/-}.sh"
        "${pkgname}-justine.sh"
        "${pkgname}.desktop")
noextract=("amnesia_tdd_${pkgver/_/-}.sh")
sha256sums=("05d58d091a2450ab1a1f1c867df7fbfcb8f588757f87e0a0ae049dbd50fb7cd7"
            "28c7fb88936f92fb5f4383a27ce1dbfa551a38b4d0f8dcd1767a73de67abb907"
            "44a4e521a55dbeb22dc82327de0f146037d24bf61a9698defb0cec4e4032d2ab")
# Prevent compressing final package
PKGEXT='.pkg.tar'

# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download the file manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf"; echo "Read this PKGBUILD for more info."; exit 1')


package() {
    # Unzip will produce an error code because it is unable to unzip the Mojo Installer.
    # Therefore, a conditional into a no-op command will keep the PKGBUILD from failing
    # Of course, if you have any real problems unzipping the PKGBUILD will not abort.
    unzip -d "${srcdir}" "amnesia_tdd_${pkgver/_/-}.sh" || :
    mkdir -p "${pkgdir}/opt/${pkgname}"
    mv "${srcdir}/data/noarch/"* "${pkgdir}/opt/${pkgname}"
    mv "${srcdir}/data/${CARCH}/"* "${pkgdir}/opt/${pkgname}"

    # Fill in the lines with proper names
    sed -i "s/FOO/Launcher.bin.${CARCH}/" "${pkgname}.desktop"
    sed -i "s/BAR/${pkgname}/" "${pkgname}.desktop"
    sed -i "s/BAZ/${pkgname}/g" "${pkgname}-justine.sh"

    # Install Binaries/Launchers
    mkdir -p "$pkgdir/usr/bin"
    ln -s "/opt/${pkgname}/Amnesia.bin.${CARCH}"  "${pkgdir}/usr/bin/${pkgname}"
    ln -s "/opt/${pkgname}/Launcher.bin.${CARCH}" "${pkgdir}/usr/bin/${pkgname}-launcher"
    install -Dm755 "${pkgname}-justine.sh" "${pkgdir}/usr/bin"

    # Install License
    mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
    mv "${srcdir}/data/EULA/"* "${pkgdir}/usr/share/licenses/${pkgname}/"

    # Install Desktop Integration
    mkdir -p "$pkgdir"/usr/share/{pixmaps,applications}
    ln -s "/opt/${pkgname}/Amnesia.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
