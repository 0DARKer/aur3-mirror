# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Franco Tortoriello <franco.tortoriello@gmail.com>

pkgname=doomsday-git
pkgver=20120117
pkgrel=1
pkgdesc="Portable game engine for classic FPS such as DOOM, Heretic and Hexen, with launcher included"
arch=('i686' 'x86_64')
url="http://dengine.net/"
license=('GPL2')
depends=('libgl' 'libpng' 'sdl_net' 'sdl_mixer' 'curl' 'mesa'  'python2' 'fmodex' 'wxpython')
makedepends=('git' 'qt')
provides=('doomsday')
conflicts=('doomsday')

_gitroot="git://deng.git.sourceforge.net/gitroot/deng/deng"
_gitname="deng"

build() {
  cd "${srcdir}"

  msg "Force usage Python2"
    rm -rf "${srcdir}"/python
    mkdir "${srcdir}"/python
    ln -s /usr/bin/python2 "${srcdir}"/python/python
    export PATH="${srcdir}"/python:$PATH
  msg2 "Done"

  msg "Connecting to GIT server..."

    if [ -d "${srcdir}"/"${_gitname}" ] ; then
      cd "${_gitname}" && git pull
    else
      git clone "${_gitroot}"
    fi

    [ -d "${srcdir}"/"${_gitname}"-build ] && rm -fr "${srcdir}"/"${_gitname}"-build
    cp -R "${srcdir}"/"${_gitname}" "${srcdir}"/"${_gitname}"-build

  msg "GIT checkout done or server timeout"

  msg "Starting build..."
    cd "${srcdir}"/"${_gitname}"-build/doomsday

    # Fix libs path
    sed 's|lib64|lib|g' -i config.pri
    
    # Dirty hack for compiling Fmodex libs
    echo -e "FMOD_DIR = \""${srcdir}"/"${_gitname}"-build/doomsday/plugins/fmod\"" > config_user.pri
    ln -s /usr/include/fmodex/* "${srcdir}"/"${_gitname}"-build/doomsday/plugins/fmod/include

    qmake CONFIG+="deng_fmod release" DENG_BUILD=`python2 ../distrib/build_number.py --print`
    make ${MAKEFLAGS}
  msg2 "Done"
}

package() {
  cd "${srcdir}"/"${_gitname}"-build/doomsday
  make INSTALL_ROOT="${pkgdir}" install
  rm -fr "${pkgdir}"/etc

  # Write launcher script (this should be automatic...)
  echo -e '#!/bin/sh\ncd /usr/share/doomsday/snowberry/\npython2 snowberry.py' > "${pkgdir}"/usr/bin/launch-doomsday
  chmod +x  "${pkgdir}"/usr/bin/launch-doomsday
}