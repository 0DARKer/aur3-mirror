# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=quine-relay-git
pkgver=3be24d0
pkgrel=1
pkgdesc='Uroboros program with 50 programming languages'
arch=('x86_64' 'i686')
url='http://github.com/mame/quine-relay'
license=('MIT')
depends=('algol68g' 'bash' 'boo' 'chicken' 'clisp' 'clojure' 'fpc' 'gawk' 'gcc'
         'gcc-fortran' 'ghc' 'go' 'gprolog' 'groovy' 'llvm' 'make' 'mono'
         'nodejs' 'ocaml' 'octave' 'parrot' 'perl' 'php' 'python' 'r' 'ruby'
         'scala' 'tcc' 'tcl' 'ucblogo' 'vala' 'c-intercal' 'coffee-script'
         'f2c' 'gforth' 'icon' 'iverilog' 'open-cobol' 'pike'
         'regina-rexx-das' 'swi-prolog') # ucblogo and the following are in AUR
source=('quine-relay::git://github.com/mame/quine-relay.git'
        'quine-relay.sh')
sha256sums=('SKIP'
            '0d683b54051d1e831ac68dc5e876755462097844dcfafc451ccfa846a2201029')

pkgver() {
  cd "$srcdir/${pkgname%-git}"

  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/${pkgname%-git}"

  cat README.md | tail -22 > LICENSE
}

package() {
  cd "$srcdir/${pkgname%-git}"

  install -d "$pkgdir/usr/share/quine-relay"
  cp -r * "$pkgdir/usr/share/quine-relay"
  install -Dm755 ../quine-relay.sh "$pkgdir/usr/bin/quine-relay"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
