# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-regexp-common
_pkgname='Regexp-Common'
pkgver=2011121001
pkgrel=3
pkgdesc="Regexp::Common - Provides commonly requested regular expressions"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.4.730')
url="http://search.cpan.org/dist/${_pkgname}"
source=("http://search.cpan.org/CPAN/authors/id/A/AB/ABIGAIL/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('cb6a2efcf6c56868f3a773d185509d1d9ecf8437d99684465729308d45e7edbf9fc253f43f1d5fcf58c57a1b88a562d8fb873b47be3c810b993a055c92547ab2')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
