# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

_perlmod=Regexp-Common
_modnamespace=Regexp
pkgname=perl-regexp-common
pkgver=2013031301
pkgrel=3
pkgdesc="Regexp::Common - Provides commonly requested regular expressions"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('MIT')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('2fad761c169be76771856938a53f28dab232cb44cea4e22f56869754633f9a38f94568096ac0f21a885e6dd11049f401c8957b04929eee6802b8454c4fe4f62a')

build() {
	cd "${srcdir}/${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${srcdir}/${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}/"
}
