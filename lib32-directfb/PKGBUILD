# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor:

pkgname=lib32-directfb
pkgver=1.4.14
pkgrel=1
pkgdesc="A thin library that provides hardware graphics acceleration, input device handling and abstraction, integrated windowing system on top of the Linux Framebuffer Device (32 bit)"
arch=('x86_64')
url="http://www.directfb.org"
license=('LGPL')
depends=('lib32-gcc-libs' 'lib32-libjpeg-turbo' 'lib32-sdl'
	 'lib32-sysfsutils' 'lib32-libpng' 'lib32-freetype2'
	 "directfb>=$pkgver")
makedepends=('gcc-multilib')
options=('!libtool')
changelog=$pkgname.changelog
source=("http://www.directfb.org/downloads/Core/DirectFB-1.4/DirectFB-${pkgver}.tar.gz")
md5sums=('de0745d25a6ac9e337d4d5572df85471')

build() {
  cd "${srcdir}/DirectFB-${pkgver}"
  
  ./configure --prefix=/usr --sysconfdir=/etc --disable-{osx,vnc} \
	      --enable-{static,zlib,x11,sdl,video4linux2,voodoo} \
  	      --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
  
  make
} 

package() {
  cd "${srcdir}/DirectFB-${pkgver}"
  make DESTDIR=${pkgdir} install  
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
