# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: eycreate <eyecreate@gmail.com>

pkgname=lib32-directfb
pkgver=1.5.3
pkgrel=1
pkgdesc="A thin library that provides hardware graphics acceleration, input device handling and abstraction, integrated windowing system on top of the Linux Framebuffer Device (32 bit)"
arch=('x86_64')
url="http://www.directfb.org"
license=('LGPL')
depends=('lib32-gcc-libs' 'lib32-libjpeg-turbo' 'lib32-sdl' 'lib32-sysfsutils'
         'lib32-libpng' 'lib32-freetype2' 'directfb' )
makedepends=('gcc-multilib')
options=('!libtool')
source=(${url}/downloads/Core/DirectFB-${pkgver%.*}/DirectFB-${pkgver}.tar.gz
        DirectFB-1.4.9-libpng-1.5.patch
        directfb-missing_include.patch)
md5sums=('54a9ec931c8e3c82adb924194e65120e'
         '00de1d0c23c8e4b96f5a4d81362de821'
         '6b7ceca416c28a3791a145c15bf6f462')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/DirectFB-${pkgver}"
  patch -p1 -i ../DirectFB-1.4.9-libpng-1.5.patch
  patch -p1 -i ../directfb-missing_include.patch
  CFLAGS+="-fno-inline" ./configure --prefix=/usr --sysconfdir=/etc \
	      --enable-{static,zlib,x11,sdl,video4linux2,voodoo} \
	      --disable-{osx,vnc,x11vdpau,freetype,mngg,mesa} \
	      --libdir=/usr/lib32
  
  make
} 

package() {
  cd "${srcdir}/DirectFB-${pkgver}"
  make DESTDIR="${pkgdir}" install  
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
