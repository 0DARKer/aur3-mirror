# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: eycreate <eyecreate@gmail.com>

pkgname=lib32-directfb
pkgver=1.6.1
pkgrel=1
pkgdesc="A thin library that provides hardware graphics acceleration, input device handling and abstraction, integrated windowing system on top of the Linux Framebuffer Device (32 bit)"
arch=('x86_64')
url="http://www.directfb.org"
license=('LGPL')
depends=('lib32-gcc-libs' 'lib32-libjpeg-turbo' 'lib32-sdl' 
         'lib32-libpng' 'lib32-freetype2' 'directfb' ) #'lib32-sysfsutils'
makedepends=('gcc-multilib')
options=('!libtool')
source=(${url}/downloads/Core/DirectFB-${pkgver%.*}/DirectFB-${pkgver}.tar.gz
        DirectFB-1.6.1-zlib-include.patch)
md5sums=('76d3066e75664aa79204af545f2f3c65'
         '12c6074524f95902cbd0998c7eca9dc8')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/DirectFB-${pkgver}"
  patch -p1 -i ../DirectFB-1.6.1-zlib-include.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
	      --enable-{static,zlib,x11,sdl,video4linux2,voodoo} \
	      --disable-{osx,vnc,x11vdpau,mng,mesa,imlib2} \
	      --libdir=/usr/lib32
  make
} 

package() {
  cd "${srcdir}/DirectFB-${pkgver}"
  make DESTDIR="${pkgdir}" install  
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
