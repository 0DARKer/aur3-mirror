# Maintainer: Rene Schoebel (wesley) <schoebel.r at gmail dot com>

pkgname=jedioutcastlinux-git
_gitname=JediOutcastLinux
pkgver=b69fb9c
pkgrel=1
pkgdesc="Singleplayer Jedi Knight II: Jedi Outcast, based on ioquake3. needs original media files."
arch=('i686' 'x86_64')
url="https://github.com/xLAva/JediOutcastLinux"
license=('GPL2')
depends=('mesa' 'openal')
makedepends=('git' 'cmake')
conflicts=('jedioutcastlinux')
provides=('jedioutcastlinux')
install=$pkgname.install
changelog=$pkgname.changelog
#source=('git+https://github.com/xLAva/JediOutcastLinux.git'
source=('git://github.com/xLAva/JediOutcastLinux.git'
        'jedioutcastlinux.png'
	'jedioutcastlinux.desktop'
	'jedioutcastlinux.sh'
	'jedioutcastlinux-git.install'
	'jedioutcastlinux-git.changelog')
# Because the sources are not static, skip Git checksum:
md5sums=('SKIP'
         'f26b9520d5eb70de380717044d03abd7'
	 '7c0597077c36493f622972bbbe973a27'
	 'b01d738474238fbc179ef55b779885c6'
	 'fd47f617e6fd7aed542bd2fb33b47155'
	 '346d5e5090ceda7cb00730fea1198cc4')

pkgver() {
  cd $_gitname

  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname

  cmake . -DCMAKE_INSTALL_PREFIX=/usr/share/jedioutcastlinux
  make
}

package() {
  cd $_gitname

  #make PREFIX=/usr/share/jedioutcastlinux DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/$_gitname/jk2sp" "$pkgdir/usr/share/jedioutcastlinux/jk2sp"
  install -Dm755 "$srcdir/$_gitname/jk2gamex86.so" "$pkgdir/usr/share/jedioutcastlinux/jk2gamex86.so"

  mkdir -p "$pkgdir/usr/share/jedioutcastlinux/base" # folder for mediafiles

  install -Dm755 "$srcdir/jedioutcastlinux.sh" "$pkgdir/usr/bin/jedioutcastlinux"
  install -Dm755 "$srcdir/jedioutcastlinux.desktop" "$pkgdir/usr/share/applications/jedioutcastlinux.desktop"
  install -Dm644 "$srcdir/jedioutcastlinux.png" "$pkgdir/usr/share/pixmaps/jedioutcastlinux.png"
}
