# Maintainer: Justin Davis <jrcd83@gmail.com>
_distname='gambc-v4_6_0'
pkgname='gambit-c'
pkgver='4.6.0'
pkgrel='3'
pkgdesc="Scheme R5RS interpreter and compiler (into portable C)"
arch=('i686' 'x86_64')
url='http://dynamo.iro.umontreal.ca/~gambit/'
license=('LGPL' 'APACHE')
makedepends=('gcc')
options=()
source=(
  "http://www.iro.umontreal.ca/~gambit/download/gambit/v4.6/source/${_distname}.tgz"
  "gambit-c.install"
)
install='gambit-c.install'
md5sums=('4f0e8b3e90a96f2203cbaf1e1cc1388a'
         '8bccf8ee8f2fbb1673fb7c9c3993f20f')

# Comment the line below if you don't have about 500 megs of RAM and
# compilation fails...
_optimize=1

build() {
  CFGOPTS='--prefix=/usr --docdir=/usr/share/doc/gambit-c
    --libdir=/usr/lib/gambit-c
    --enable-gcc-opts'

  if [ "$_optimize" ] ; then
    CFGOPTS="$CFGOPTS --enable-single-host"
  fi

  cd "$srcdir/$_distname"
  ./configure $CFGOPTS
  make

  return 0
}

package() {
  cd "$srcdir/$_distname"
  make DESTDIR="$pkgdir" install

  # gsc conflicts with the one provided by ghostscript...
  cd "$pkgdir/usr/bin"
  mv gsc gambitc
  rm gsc-script
  ln -s gambitc gsc-script
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
