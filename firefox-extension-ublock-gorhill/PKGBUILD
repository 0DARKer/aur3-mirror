# Maintainer: ThePierrezou <thepierrezou@openmailbox.org>
# Contributor: polyzen <polycitizen@gmail.com>

pkgname=firefox-extension-ublock-gorhill
pkgver=0.9.4.3
pkgrel=1
pkgdesc='Finally, an efficient blocker. Easy on CPU and memory. Gorhill version.'
url=https://github.com/gorhill/uBlock
arch=('any')
license=('GPL3')
depends=('firefox')
conflicts=('firefox-extension-ublock' 'firefox-extension-ublock-git' 'firefox-extension-ublock-gorhill-git')
source=("https://github.com/gorhill/uBlock/releases/download/$pkgver/uBlock0.firefox.xpi")
sha256sums=('51968068eb7478226624446a43d42b193dc5b076063ccf5735a19852cde35a77')

package() {
  local GLOBIGNORE=*.xpi:LICENSE.txt
  local version="$(grep -Pom1 '(?<=<id>).*(?=</id>)' install.rdf)"
  local dstdir="$pkgdir/usr/lib/firefox/browser/extensions/$version"
  install -d "$dstdir"
  cp -dpr --no-preserve=ownership * "$dstdir"
  chmod -R 755 "$dstdir"
}

# vim:set ts=2 sw=2 et:
