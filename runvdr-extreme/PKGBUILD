# Contributor: David Spicer <azleifel at gmail dot com>

pkgname=runvdr-extreme
pkgver=0.4.2
pkgrel=4
pkgdesc="A runvdr script for VDR"
arch=('any')
url="http://www.udo-richter.de/vdr/scripts.html"
license=('GPL')
depends=('bash' 'vdr')
backup=('etc/runvdr.conf')
options=(emptydirs)
source=(http://www.udo-richter.de/vdr/files/${pkgname}-$pkgver.tgz
        ${pkgname}-$pkgver.conf
        ${pkgname}.service)
md5sums=('ca95ce455fc6e66fcaa4b3476ecdcffc'
         '8f3c897d3aa8a868e07df024fb6c3d92'
         '67a81304b4d7dd8177bccf7765789083')

package() {
  cd "${srcdir}/${pkgname}-$pkgver"

  install -d -m755 "${pkgdir}/etc/runvdr/conf.d"
  install -d -m755 "${pkgdir}/usr/bin/"
  install -d -m755 "${pkgdir}/usr/lib/systemd/system/"
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -d -m755 "${pkgdir}/usr/share/${pkgname}"

  install -m644 "${srcdir}/${pkgname}-$pkgver.conf" "${pkgdir}/etc/runvdr.conf"
  install -m644 skeleton.example "${pkgdir}/usr/share/${pkgname}"
  install -m755 runvdr-conf.d "${pkgdir}/usr/bin"
  sed -i "s:^\#\!/bin/bash:\#\!/usr/bin/bash:" runvdr
  install -m755 runvdr "${pkgdir}/usr/bin"

  # Install systemd service file
  install -m644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/"

  # Install documents
  for _file in README README.de HISTORY; do
    install -m644 ${_file} "${pkgdir}/usr/share/doc/${pkgname}/${_file}"
  done
}

