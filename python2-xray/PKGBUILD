# Maintainer: Rich Li <rich@dranek.com>
pkgname='python2-xray'
pkgver=0.3.1
_ver=0.3.1
pkgrel=1
pkgdesc="N-D labeled arrays and datasets in Python"
url="https://pypi.python.org/pypi/xray"
arch=('any')
license=('Apache')
makedepends=('python2-setuptools')
depends=('python2-numpy' 'python2-pandas')
optdepends=('python2-netcdf4: NetCDF4 support'
            'python2-scipy: OPeNDAP support')
checkdepends=('python2-nose')
#source=(https://pypi.python.org/packages/source/x/xray/xray-"$pkgver".tar.gz)
# I'm using the Github source instead of the PyPI source since the Github source includes the data files used for testing
source=(https://github.com/xray/xray/archive/v"$_ver".tar.gz)
md5sums=('b1531b0552b1a4fb717f4643a8f34707')
 
build() {
  cd "$srcdir/xray-$_ver"
  python2 setup.py build
}

check() {
  cd "$srcdir/xray-$_ver"
  nosetests2
}
 
package() {
  cd "$srcdir/xray-$_ver"
  python2 setup.py install --prefix=/usr --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
