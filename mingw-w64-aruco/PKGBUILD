# Maintainer: Alexander Yunin <iflo0[at]myhotspot[dot]ru>
pkgname=mingw-w64-aruco
pkgver=1.2.4
pkgrel=1
pkgdesc="A minimal library for Augmented Reality applications based on OpenCV"
url="http://www.uco.es/investiga/grupos/ava/node/26" 
arch=('any')
license=('BSD')
options=('!buildflags' 'staticlibs' '!strip')
depends=('mingw-w64-opencv>=2.1' 'mingw-w64-freeglut')
makedepends=('mingw-w64-cmake')
source=("http://sourceforge.net/projects/aruco/files/1.2.4/aruco-1.2.4.tgz/download"
        "aruco-1.2.4-mingw.patch")
md5sums=('d66f1adb3eb7e1e7ea4a3d8ff759e07c'
         '3f5e4a5c10bdc3193c07888ebd500c6e')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/aruco-${pkgver}"
  patch -p1 -i ../aruco-1.2.4-mingw.patch
}

build() {
  cd "${srcdir}/aruco-${pkgver}"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      ..
    make
    popd
  done
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ${_arch}-cmake \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=OFF \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/aruco-${pkgver}/build-${_arch}-static"
    make DESTDIR="$pkgdir" install
    cd "${srcdir}/aruco-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -Dm644 ../COPYING "$pkgdir/usr/${_arch}/share/licenses/$pkgname/COPYING"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

# vim:set ts=2 sw=2 et:
