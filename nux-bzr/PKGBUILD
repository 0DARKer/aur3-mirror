# Contributor: Flamelab <panosfilip@gmail.com
# Fixes by: gregorburger

pkgname=nux-bzr
_realname=nux
pkgver=172
pkgrel=1
pkgdesc="An OpenGL toolkit (for Unity)"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('GPL3')
depends=('dbus-sharp-glib' 'gio-sharp-git' 'gnome-desktop-sharp' 'gnome-keyring-sharp' 'gtk2' \
         'hicolor-icon-theme' 'mono-addins' 'xdg-utils' 'glib2' 'gdk-pixbuf2' 'glewmx-fixed')
makedepends=('bzr' 'intltool')
provides=(${_realname})
conflicts=(${_realname})
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("png_fix_for_nux.patch")
md5sums=('0bbc49694ae535f4797757af97d5d6d1')


_bzrtrunk=lp:${_realname}
_bzrmod=${_realname}

build() {

  export MONO_SHARED_DIR=${srcdir}/.wabi
  mkdir -p ${MONO_SHARED_DIR}

  cd ${srcdir}

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_bzrmod}-build
  cp -r ${_bzrmod} ${_bzrmod}-build
  cd ${_bzrmod}-build

  #Fixing the png related issue
  patch -p0 < ${srcdir}/png_fix_for_nux.patch

  sed -i 's/\-Werror//g' configure.ac

  ./autogen.sh
  ./configure --prefix=/opt/unity --enable-examples=no --disable-doxygen-html --disable-doxygen-doc

  make
}

package() {

  cd ${srcdir}/${_bzrmod}-build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  install -dm755 ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${_realname}.schemas \
                        ${_realname} ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

}
