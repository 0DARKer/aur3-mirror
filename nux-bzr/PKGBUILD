# Contributor: Flamelab <panosfilip@gmail.com
# Fixes by: gregorburger

pkgname=nux-bzr
_realname=nux
pkgver=361
pkgrel=1
pkgdesc="An OpenGL toolkit (for Unity)"
arch=('i686' 'x86_64')
url="https://launchpad.net/nux"
license=('GPL3')
depends=('gdk-pixbuf2' 'glew' 'libsigc++' 'pango' 'gnome-common' 'libxxf86vm' 'libsigc++')
makedepends=('bzr' 'intltool' 'libpci')
provides=(${_realname})
conflicts=(${_realname})
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("png_fix_for_nux.patch" "https://launchpadlibrarian.net/68912156/nux-0.9.40-fix-build-with-gcc46.patch")

_bzrtrunk=lp:${_realname}
_bzrmod=${_realname}

build() {

  export MONO_SHARED_DIR=${srcdir}/.wabi
  mkdir -p ${MONO_SHARED_DIR}

  cd ${srcdir}

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_bzrmod}-build
  cp -r ${_bzrmod} ${_bzrmod}-build
  cd ${_bzrmod}-build

  #Fixing the png related issue
  patch -p0 < ${srcdir}/png_fix_for_nux.patch
  patch -Np1 -i ${srcdir}/nux-0.9.40-fix-build-with-gcc46.patch

  sed -i 's/\-Werror//g' configure.ac

  ./autogen.sh
  ./configure --prefix=/opt/unity --enable-examples=no --disable-doxygen-html --disable-doxygen-doc

  make
}

package() {

  cd ${srcdir}/${_bzrmod}-build

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  install -dm755 ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${_realname}.schemas \
                        ${_realname} ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

}

md5sums=('0bbc49694ae535f4797757af97d5d6d1'
         'b4410b11f694673263bae1ebf0509578')
