# Maintainer: Alex Forencich <alex@alexforencich.com>

pkgname=linux-gpib
pkgver=3.2.16
pkgrel=1
pkgdesc="Linux GPIB support"
arch=('i686' 'x86_64')
license=('GPL')
url="http://linux-gpib.sourceforge.net/"
depends=('bash' 'linux>=3.0')
makedepends=('linux-headers' 'automake' 'autoconf' 'libtool')
options=()
optdepends=('gpib-firmware: firmware for USB GPIB cables')
source=(http://downloads.sourceforge.net/project/${pkgname}/linux-gpib%20for%202.6.x%20kernels/${pkgver}/${pkgname}-${pkgver}.tar.gz
    'gpib_build.patch'
    'fix-asm-includes.patch'
    'gpib.rules')
install='linux-gpib.install'
md5sums=('d6788af8da4e18e8d453362798abfb6b'
         '2687559f95d5614bb5651ff6f0ce5aee'
         '1383f6a53a3beb105364dbcedad1bfd1'
         '3d09fb5b7975cd8c22edce80b30ea821')

_kernver=$(uname -r)
_extramodules=/lib/modules/${_kernver}/extramodules/

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   
   patch -Np0 -i "${srcdir}/../gpib_build.patch" || return 1
   patch -Np1 -i "${srcdir}/../fix-asm-includes.patch" || return 1
   
   ./bootstrap
   ./configure \
      --prefix=/usr \
      --with-linux-srcdir=/usr/src/linux-${_kernver}
   make || return 1
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   make INSTALL_MOD_PATH="${pkgdir}" DESTDIR="${pkgdir}" install

   mkdir -p ${pkgdir}/${_extramodules}
   mv ${pkgdir}/lib/modules/${_kernver}/gpib ${pkgdir}/${_extramodules}/

   find ${pkgdir} -depth -type d -empty -exec rmdir {} \;

   mkdir -p ${pkgdir}/etc/udev/rules.d/
   cp ${srcdir}/gpib.rules ${pkgdir}/etc/udev/rules.d/30-gpib.rules
}


