# Maintainer: PyroPeter <googlemail.com@abi1789>

pkgname=arduino
epoch=1
pkgver=1.0
pkgrel=3
pkgdesc="Arduino SDK (includes patched avrdude and librxtx)"
arch=('i686' 'x86_64')
url="http://arduino.cc/en/Main/Software"
options=(!strip)
license=('GPL')
depends=('avr-libc' 'libusb-compat' 'java-runtime')
install="arduino.install"
source=("http://arduino.googlecode.com/files/arduino-$pkgver-linux.tgz"
	"http://arduino.googlecode.com/files/arduino-$pkgver-linux64.tgz"
	'arduino'
	'arduino.png'
	'arduino.desktop'
	'print_progmem.patch')
md5sums=('dc5085c25aab1dcb267e0f0fa4af468e' '14336316af4cc4aef5f879afed9dd7ea' '551304dc658224c867513f6099075f0e' '9e36d33891d5e68d38ec55d1494499a5' 'eebc4d6495864bea99ad057af801afb9' '15a2c878d1418551463eed5ad22dccff')

if   [ "$CARCH" == 'i686' ]; then
  unset source[1]
  unset md5sums[1]
elif [ "$CARCH" == 'x86_64' ]; then
  unset source[0]
  unset md5sums[0]
fi

package() {
  cd "$srcdir/arduino-$pkgver"

  # replace deprecated use of prog_char
  patch -p1 < "$srcdir/print_progmem.patch"

  # arduino excutable should accept arguments
  sed -i 's/^java .* processing.app.Base$/\0 "$*"/' arduino

  # remove the ugly __MACOSX folder
  rm -r ./__MACOSX

  mkdir -p "$pkgdir"/usr/{bin,share/{doc,applications,pixmaps}}

  # copy the whole SDK to /usr/share/arduino/
  cp -r . "$pkgdir/usr/share/arduino"

  # at least support the FHS a little bit:
  install -m755 "$srcdir/arduino" "$pkgdir/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "$pkgdir/usr/share/doc/arduino"

  # desktop icon
  install -m644 "$srcdir/arduino.desktop" "$pkgdir/usr/share/applications/"
  install -m644 "$srcdir/arduino.png" "$pkgdir/usr/share/pixmaps/"
}
