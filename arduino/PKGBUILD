# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: PyroPeter <googlemail.com@abi1789>
# Contributor: darkapex <me@jailuthra.in>
# Contributor: tty0 <vt.tty0[d0t]gmail.com>

pkgname=arduino
epoch=1
pkgver=1.0.6
pkgrel=1
pkgdesc="Arduino prototyping platform SDK"
arch=('i686' 'x86_64')
url="http://arduino.cc/en/Main/Software"
options=(!strip staticlibs)
license=('GPL' 'LGPL')
depends=('libusb-compat' 'java-runtime' 'java-rxtx')
optdepends=('gtk2: Arduino IDE')
conflicts=('arduino-toolchain')
install="arduino.install"
source=("http://arduino.cc/download.php?f=/arduino-${pkgver}-linux32.tgz"
        'arduino-fix-arguments.patch'
        'arduino.png'
        'arduino.desktop')
sha256sums=('f059a572231abafb92099a3a404c0a41502c3413668610676c40029384edc658'
            '631921fce485d14c79bcdc31d5d6f3a95d729d49975390b6b785fe49d238ff65'
            '3aff4239209751f0396cc27e4348f9adbd8c193e410565895811c38d2bda6ae8'
            '3991ce3980bf0bea26ad6c6836038ebece17d443e130aace901c2ac637a7746f')

if [ "$CARCH" == 'x86_64' ]; then
  source[0]="http://arduino.cc/download.php?f=/arduino-${pkgver}-linux64.tgz"
  sha256sums[0]='1ff175c487de335981d3aad64cf212b56cb2c778a6bdd6655045335ffc65e147'
fi

prepare() {
  cd "arduino-${pkgver}"

  patch -Np1 <"${srcdir}/arduino-fix-arguments.patch"
}

package() {
  cd "arduino-${pkgver}"

  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/"{doc,applications,pixmaps}

  # copy the whole SDK to /usr/share/arduino/
  cp -a . "${pkgdir}/usr/share/arduino"

  # use system's RXTX library
  ln -sf /usr/lib/librxtxSerial.so "${pkgdir}/usr/share/arduino/lib/librxtxSerial.so"
  ln -sf /usr/lib/librxtxSerial.so "${pkgdir}/usr/share/arduino/lib/librxtxSerial64.so"
  ln -sf /usr/share/java/rxtx/RXTXcomm.jar "${pkgdir}/usr/share/arduino/lib/RXTXcomm.jar"

  # at least support the FHS a little bit
  ln -s /usr/share/arduino/arduino "${pkgdir}/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "${pkgdir}/usr/share/doc/arduino"

  # desktop icon
  install -m644 "${srcdir}/arduino.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/arduino.png" "${pkgdir}/usr/share/pixmaps/"
}
