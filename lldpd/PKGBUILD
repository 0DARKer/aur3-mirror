# Maintainer: Brian Bidulock <bidulock@openss7.org>

pkgname=lldpd
pkgver=0.7.1
pkgrel=7
pkgdesc="LLDP daemon for GNU/Linux implementing both reception and sending"
arch=('i686' 'x86_64')
url="https://github.com/vincentbernat/lldpd/wiki"
license=('custom:"ISC"')
install=lldpd.install
source=(http://media.luffy.cx/files/$pkgname/$pkgname-$pkgver.tar.gz
	lldpd.service
        lldpd.install
        LICENSE)
md5sums=('cee0e2ae7d4b8bf25ae234d9536052b8'
         'd52722f13225f3a8043f6c080354a52e'
         '0b06475bc2048aa29fb1d8c660446f87'
         '8ae98663bac55afe5d989919d296f28a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # and this was supposed to be "easier"?
  sed -i 's|sbin_PROGRAMS|bin_PROGRAMS|g;s|sbindir|bindir|g' \
	src/daemon/Makefile.am
  sed -i 's|sbin_PROGRAMS|bin_PROGRAMS|g;s|sbindir|bindir|g' \
	src/client/Makefile.am
  sed -i 's|TESTS = \$(top_srcdir)/|TESTS = |g' \
	libevent/test/Makefile.am
  autoreconf -i
  ./configure --prefix=/usr --with-snmp --with-xml
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../lldpd.service $pkgdir/usr/lib/systemd/system/lldpd.service
  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
