# Maintainer: Brian Bidulock <bidulock@openss7.org>

pkgname=lldpd
pkgver=0.7.8
pkgrel=5
pkgdesc="LLDP daemon for GNU/Linux implementing both reception and sending"
arch=('i686' 'x86_64')
url="http://vincentbernat.github.io/lldpd/"
license=('custom:"ISC"')
depends=('libxml2' 'net-snmp' 'libevent' 'libbsd' 'jansson')
options=('!libtool')
install=lldpd.install
backup=('etc/lldpd.conf')
source=("http://media.luffy.cx/files/$pkgname/$pkgname-$pkgver.tar.gz"
	'lldpd.service'
        'lldpd.install'
        'LICENSE')
md5sums=('b96ef3285ee7412cd7ca318863c30e83'
         'b7e8ff9ad84eac551d6975f26b814c49'
         '18d76cccdbbfed66c9c39232dd5f81ae'
         '8ae98663bac55afe5d989919d296f28a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -e '/LLDPD_CTL_SOCKET/s,/var/run,/run,' -i src/ctl.h
  sed -e '/LLDPD_PID_FILE/s,/var/run,/run,' -i src/daemon/lldpd.h
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-snmp \
    --with-xml \
    --with-json \
    --with-privsep-user=lldpd \
    --with-privsep-group=lldpd \
    --with-privsep-chroot=/run/lldpd
  make
  echo "" >>lldpd.conf
  echo "# Place configuration files in this directory: see lldpcli(8)" >README.conf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 lldpd.conf "$pkgdir/etc/lldpd.conf"
  install -Dm644 README.conf "$pkgdir/etc/lldpd.d/README"
  install -Dm644 ../lldpd.service "$pkgdir/usr/lib/systemd/system/lldpd.service"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
