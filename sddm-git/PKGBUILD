# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=sddm
pkgname=$_pkgname-git
pkgver=538.db1f7c1
pkgrel=1
pkgdesc="QML based X11 display manager"
arch=("i686" "x86_64")
url="https://github.com/sddm/sddm"
license=('GPL')
depends=("qt4")
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
backup=("etc/sddm.conf")
source=("git://github.com/$_pkgname/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	echo "$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DQT_QMAKE_EXECUTABLE=qmake-qt4 ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
