# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_pkgname=sddm
pkgname=$_pkgname-git
pkgver=513.c270144
pkgrel=1
pkgdesc="QML based X11 display manager"
arch=('i686' 'x86_64')
url="https://github.com/sddm/sddm"
license=('GPL')
depends=('qt4')
makedepends=('git' 'cmake')
provides=($_pkgname)
conflicts=($_pkgname)
backup=("etc/sddm.conf")
source=("git://github.com/$_pkgname/$_pkgname.git")
sha256sums=('SKIP')
_gitname=$_pkgname


pkgver() {
	cd "$srcdir/$_gitname"
	echo "$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
	cd "$srcdir/$_gitname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DQT_QMAKE_EXECUTABLE=qmake-qt4 ..
	make
}

package() {
	cd "$srcdir/$_gitname"
	cd build
	make DESTDIR="$pkgdir" install
}
