# Maintainer: DavidK <david_king at softhome dot net>
# Contributor: SaThaRiel <sathariel74[at] gmail[dot]com>
pkgname=angband-git
pkgver=v3.5.0.r0.g8752f41
pkgrel=1
pkgdesc="A roguelike dungeon-exploration game based on Tolkien's books"
arch=('i686' 'x86_64')
url="http://rephial.org/"
license=('GPL2' 'custom')
depends=('sdl' 'sdl_image' 'sdl_ttf' 'sdl_mixer' 'ncurses' 'libx11' 'freetype2')
makedepends=('git' 'autogen' 'python-docutils')
conflicts=('angband' 'angband-svn')
source=('git://github.com/angband/angband.git')
md5sums=('SKIP')
_gitname=angband

pkgver() {
  cd "$srcdir"/$_gitname
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir"/$_gitname
  ./autogen.sh
}

build() {
  cd "$srcdir"/$_gitname
  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin/ \
    --sysconfdir=/usr/share/angband \
    --enable-sdl \
    --enable-sdl-mixer
  make
}

package() {
  cd "$srcdir"/$_gitname
  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir"/usr/share/angband/*/delete.me
  install -Dm644 copying.txt "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  install -d "$pkgdir"/usr/bin
}
