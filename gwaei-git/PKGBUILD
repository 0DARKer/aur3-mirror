# Maintainer: Patrice Peterson <runiq at archlinux dot us>

pkgname=gwaei-git
pkgver=20100724
pkgrel=1
pkgdesc="A Japanese-English dictionary program"
arch=('i686' 'x86_64')
url="http://gwaei.sourceforge.net"
license=('GPL3')
depends=('curl>=7.18.0' 'gconf>=2.22.0' 'hicolor-icon-theme'
         'libsexy>=0.1.11' 'libunique>=1.0.0')
makedepends=('git' 'gnome-doc-utils>=0.13.0' 'pkgconfig>=0.9.0')
optdepends=('rsync: Provide incremental dictionary updates'
            'ttf-arphic-ukai'
            'ttf-kanjistrokeorders: Show stroke order diagrams')
provides=(gwaei)
conflicts=(gwaei)

_gitroot="git://gwaei.git.sourceforge.net/gitroot/gwaei/gwaei"
_gitname="gwaei"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # temporary workaround until upstream has updated to gettext 0.18
  gettextize --force
  ./autogen.sh
  ./configure --prefix=/usr --disable-schemas-install \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make || return 1
  make DESTDIR="$pkgdir/" install
} 
