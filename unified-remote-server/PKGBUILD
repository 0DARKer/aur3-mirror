# Maintainer: AudioLinux  <audiolinux AT fastmail DOT fm>
# Contributor: peace4all <markspost at rocketmail dot com>

pkgname=unified-remote-server
pkgrel=1
pkgdesc="Unified Remote Server"
arch=('i686' 'x86_64' 'armv6h')
url="http://www.unifiedremote.com/"
depends=('libxext' 'libx11')
optdepends=('bluez' 'bluez-utils')
license=('freeware')
install=$pkgname.install

if test "$CARCH" == x86_64; then
        pkgver=3.2.2.631
	source=("http://www.unifiedremote.com/static/builds/server/linux-x64/631/urserver-$pkgver.deb" "urserver.service")
	sha256sums=('df2c5aa4b95b78f8e4f734fd21f6edc24bfd87d23b43d6712d7ee21dcd26ac84' 'ca049fc2e6b08276bf22b262577feca3fb4f83bef95e8f95a95d16b8d59fd107' )
fi
if test "$CARCH" == armv6h; then
        pkgver=3.2.2.413
        source=("http://www.unifiedremote.com/static/builds/server/linux-rpi/413/urserver-$pkgver.deb" "urserver.service")
        sha256sums=('e34be0dfba65be1eb08cae4715bef517b7b4d49dcfd1482ffa8e69362ddd118e' 'ca049fc2e6b08276bf22b262577feca3fb4f83bef95e8f95a95d16b8d59fd107')
fi

if test "$CARCH" == i686; then
        pkgver=3.2.2.631
        source=("http://www.unifiedremote.com/static/builds/server/linux-x86/631/urserver-$pkgver.deb" "urserver.service")
        sha256sums=('4a73c3ddaa0350b98b47e6a6e5ab9a70c82607de078e1e00e3d9010c09bb3b2c' 'ca049fc2e6b08276bf22b262577feca3fb4f83bef95e8f95a95d16b8d59fd107')
fi

package() {
	cd ${srcdir}

	# decompress data
	tar zxf data.tar.gz

	# fix and revert desktop file to old behaviour
	sed -i -e '9,24d;26d' $(find . -name 'urserver.desktop')

	# install folders
	mkdir -p ${pkgdir}/{opt,usr}
	cp -r {opt,usr} "${pkgdir}/"

	# clean up permissions
	find "${pkgdir}" -type d | xargs -I {} chmod -R 755 "{}"
	find "${pkgdir}" -type f | xargs -I {} chmod -R 644 "{}"
	chmod 755 "${pkgdir}/opt/urserver/urserver"
	
	# add systemd service
        install -Dm644 "urserver.service" "$pkgdir/usr/lib/systemd/system/urserver.service"
}

