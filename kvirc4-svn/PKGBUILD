# Contributor: John Schoenick <john@pointysoftware.net>
# Contributor: Holger "Skittles" Vogel <holger.vogel [at] gmail [dot] com>

pkgname=kvirc4-svn
pkgver=6295
pkgrel=1
pkgdesc="Qt4 based IRC-Client, compiled with kde4 support - SVN Version"
arch=('i686' 'x86_64')
url="http://www.kvirc.net"
license=('GPL')
depends=('qt' 'glibc' 'openssl' 'zlib' 'kdelibs' 'perl')
makedepends=('cmake' 'subversion' 'automoc4' 'gettext')
conflicts=('kvirc4' 'kvirc')
provides=('kvirc' 'kvirc4')

_svntrunk=https://svn.kvirc.de/svn/trunk/kvirc
_svnmod=kvirc

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"

  msg "Removing old build dir"
  rm -rf "${_svnmod}-build"

  msg "Creating build dir"
  svn export "$_svnmod"{,-build}

  cd "${_svnmod}-build"
  cmake -DCMAKE_INSTALL_PREFIX="$(kde4-config --prefix)"

  make
}

package() {
  cd "$srcdir/${_svnmod}-build"
  make DESTDIR="$pkgdir" install
}
