# $Id: PKGBUILD 170098 2012-11-01 17:28:55Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=lib32-procps-ng
pkgver=3.3.5
pkgrel=1
pkgdesc='Utilities for monitoring your system and its processes (32-bit)'
url="http://gitorious.org/procps"
license=('GPL' 'LGPL')
arch=('x86_64')
depends=('procps-ng' 'lib32-glibc')
options=('!libtool')
source=("procps-ng-${pkgver}.tar.gz::http://gitorious.org/procps/procps/archive-tarball/v${pkgver}")
sha1sums=('f591f2ff01f8866daf8999f45b797d18929b7aec')

build() {
  cd "${srcdir}/procps-procps"

  echo ${pkgver} > .tarball-version

  export CC='gcc -m32'
  
  ./autogen.sh
  ./configure --exec-prefix=/ --prefix=/usr \
    --sysconfdir=/etc --libdir=/usr/lib32 --without-ncurses
  make
}

package() {
  cd "${srcdir}/procps-procps"
  
  make DESTDIR="${pkgdir}" install

  rm -r ${pkgdir}/{bin,sbin}
  rm -r ${pkgdir}/usr/{bin,include,share}
}
