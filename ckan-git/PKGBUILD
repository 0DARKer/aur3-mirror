# See http://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines
# for more information on packaging from GIT sources.

# Maintainer: Andrew Smith <ChucklesTheBeard@gmail.com>
# Contributer: Patrik Plihal <patrik.plihal@gmail.com>

pkgname=ckan-git
pkgver=v1.5.6.r2.g9d76e14
pkgrel=1
pkgdesc="A metadata repository and associated tools for searching, installing, and managing mods for Kerbal Space Program (KSP)"
arch=('any')
url="http://forum.kerbalspaceprogram.com/threads/100067-"
license=('MIT')
provides=('ckan')
depends=('mono')
makedepends=('git' 'nunit2' 'python2')
source=('git://github.com/KSP-CKAN/CKAN.git'
        'git://github.com/KSP-CKAN/CKAN-GUI.git'
        'git://github.com/KSP-CKAN/CKAN-cmdline'
        'git://github.com/KSP-CKAN/CKAN-core'
        'ckan_github_utils.skip_git_dl.diff'
        'core-build.sh-fix_tool_name.diff'
        'GUI-build.sh-fix_tool_name.diff')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         '1e5145ceffd9f4b663455453ddbebd8c'
         '334d570d2875aa4ce45b32c64057e4ed'
         '8063cedbcc89e1293636bfc15273724d')

pkgver() {
  cd "$srcdir/CKAN"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir"
  
  # git dirs are cloned by pacman, don't do that in the build process
  patch -uN ./CKAN/bin/ckan_github_utils.py ./ckan_github_utils.skip_git_dl.diff || return 1
  
  # apply arch-specific patches involving tool naming
  patch -uN ./CKAN-GUI/build.sh ./GUI-build.sh-fix_tool_name.diff || return 1
  patch -uN ./CKAN-core/build.sh ./core-build.sh-fix_tool_name.diff || return 1
  
  # build
  python2 ./CKAN/bin/ckan-build.py
}

package() {
  mkdir -p "$pkgdir/usr/bin/"
  cp $srcdir/ckan.exe "$pkgdir/usr/bin/ckan"
}

# vim:set ts=2 sw=2 et:
