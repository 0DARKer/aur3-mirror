# $Id: PKGBUILD 208592 2014-03-24 15:32:22Z bpiotrowski $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=lib32-libva
pkgver=1.3.1
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('lib32-libgl' 'lib32-libdrm' 'lib32-libxfixes')
makedepends=('mesa' 'gcc-multilib')
optdepends=('lib32-libva-vdpau-driver: vdpau back-end for nvidia'
            'lib32-libva-intel-driver: back-end for intel cards')
source=(http://freedesktop.org/software/vaapi/releases/libva/libva-$pkgver.tar.bz2
        0001-va-release-VADisplayContext-at-the-end-of-vaTerminat.patch)

prepare() {
  cd libva-$pkgver
  patch -p1 -i ../0001-va-release-VADisplayContext-at-the-end-of-vaTerminat.patch
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd ${pkgname#*-}-${pkgver}
  ./configure --prefix=/usr --libdir='/usr/lib32' 
  make
}

package() {
  cd ${pkgname#*-}-${pkgver}
  make DESTDIR="$pkgdir" install

	echo $pkgdir

  rm -rfv "${pkgdir}"/usr/{include,bin}
  install -m644 -D COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
md5sums=('eb4db967f068854444b597071c66b480'
         'e564ae7c20962c821ac2b5cb1693e1a2')
