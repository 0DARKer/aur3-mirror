# Maintainer: Dave Kleinschmidt <dave.f.kleinschmidt at gmail.com>
# Contributor: Frikilinux <frikilinux at gmail.com>

pkgname=gnome-shell-extension-appindicator-git
pkgver=72.ffddc73
pkgrel=1
pkgdesc="Integrates AppIndicators into Gnome Shell"
arch=('any')
url="https://github.com/rgcjonas/gnome-shell-extension-appindicator"
license=('GPL')
depends=('gnome-shell' 'libappindicator' 'libappindicator3')
makedepends=('git')
optdepends=()
source=("git://github.com/rgcjonas/gnome-shell-extension-appindicator.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}"/${pkgname%-*}
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "${srcdir}"/${pkgname%-*}
  make
}

package() {
  ## analogously to the github instructions which say to just symlink
  ## make-ed clone of git repo to local extension.
  UUID="appindicatorsupport@rgcjonas.gmail.com"
  mkdir -p "${pkgdir}"/usr/share/gnome-shell/extensions
  cp -a "${srcdir}/${pkgname%-*}" "${pkgdir}"/usr/share/gnome-shell/extensions/"${UUID}"
}
