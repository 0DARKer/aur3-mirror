# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=freefem++-hg
pkgver=r3202.f8b54b5a5eb0
pkgrel=1
pkgdesc='A PDE oriented language using the finite element method (Mercurial)'
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'glu' 'suitesparse' 'hdf5-cpp-fortran' 'gsl')
makedepends=('mercurial' 'gcc-fortran' 'ed')
provides=('freefem++')
conflicts=('freefem++')
backup=('etc/freefem++.pref')
source=('hg+http://www.freefem.org/ff++/ff++/')
sha256sums=('SKIP')
options=('!makeflags')

pkgver() {
  cd 'ff++'
  printf "r%s.%s" $(hg identify -n) $(hg identify -i)
}

prepare() {
  cd 'ff++'
  ## fix mumps Makefile includes
  ed -v download/mumps/Makefile-mumps-4.10.0.inc <<< $',s/^INCS = /& -I. -I\\/usr\\/include /g\nw'
}

build() {
  cd 'ff++'
  autoconf -i
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-download \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd"
  download/getall -a
  make
}

# check() {
#   cd 'ff++'
#   # Some tests fail. See README_HG
#   make check
# }

package() {
  cd 'ff++'
  make DESTDIR="$pkgdir" install
  install -Dm644 examples++/freefem++.pref $pkgdir/etc/freefem++.pref
  find $pkgdir/usr/lib/ff++/ -name "*.h" -exec chmod o+r {} \;
  # remove unneeded stuff
  rm -f $pkgdir/usr/share/freefem++/INSTALL*
  rm -f $pkgdir/usr/share/freefem++/README_*
  rm -f $pkgdir/usr/share/freefem++/mode-mi-edp.zip
  rm -rf $pkgdir/usr/share/freefem++/download
}
