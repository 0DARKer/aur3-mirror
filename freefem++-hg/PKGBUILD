# NOTE: build fails if suitesparse is installed

_hgname="freefem++"
pkgname="freefem++-hg"
pkgver=2663+e0d9c72558a1
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut')
makedepends=('mercurial' 'gcc-fortran' 'wget')
provides=('freefem++')
conflicts=('freefem++')
options=(!makeflags)
source=("$_hgname::hg+http://www.freefem.org/ff++/ff++"
        "disable-doc.patch")
md5sums=('SKIP'
         '5d3e48e641cb0b067855077c94ee45da')

pkgver() {
  cd "$_hgname"

  echo "$(hg identify -n)+$(hg identify -i)"
}

prepare() {
  cd "$_hgname"

  sed -i "1i#include <unistd.h>" src/Graphics/getprog-unix.hpp
  patch -Np1 -i ../disable-doc.patch
}

build() {
  cd "$_hgname"

  ./configure \
    --prefix=/usr \
    --enable-download \
    --disable-pdf

  make
}

package() {
  cd "$_hgname"

  make DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
