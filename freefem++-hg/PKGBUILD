# $Id$

_hgname="freefem++"
pkgname="freefem++-hg"
pkgver=2839+7ed38d0c562b
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'suitesparse')
makedepends=('ed' 'mercurial' 'gcc-fortran' 'wget')
provides=('freefem++')
conflicts=('freefem++')
#backup=('etc/freefem++.pref')
source=("$_hgname::hg+http://www.freefem.org/ff++/ff++"
        "disable-doc.patch"
        "configure-dirs.patch")
sha256sums=('SKIP'
            'c1a3671d64cecbc4ce80e80b1389e1129654542d87b3e6d82036c10500aef189'
            'fad50a2150e65135dace1dd25fe525032d11649e07793a6be471c2078bfd50ad')

pkgver() {
  cd "$_hgname"

  echo "$(hg identify -n)+$(hg identify -i)"
}

prepare() {
  cd "$_hgname"

  # disable doc
  patch -p1 < "$srcdir/disable-doc.patch"

  # control install with ./configure
  # patch -Np1 < "$srcdir/configure-dirs.patch"

  ## fix mumps Makefile includes
  ed -v download/mumps/Makefile-mumps-4.10.0.inc <<< $',s/^INCS = /& -I. -I\\/usr\\/include /g\nw'

  ## include downloaded headers in ff-c++ jobs
  ed -v examples++-load/Makefile.am <<< $',s/^	 .\\/ff-c++/& -I..\\/download\\/include /g\nw'
}

build() {
  cd "$_hgname"

  autoreconf -i

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-download \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lcholmod -lcolamd"

  make
}

package() {
  cd "$_hgname"

  make DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -f $pkgdir/usr/share/freefem++/3.26/INSTALL*
  rm -f $pkgdir/usr/share/freefem++/3.26/{README_MAC,README_WINDOWS,mode-mi-edp.zip}
  rm -rf "$pkgdir/usr/share/freefem++/3.26/download"
}

# vim: ft=sh syn=sh et
