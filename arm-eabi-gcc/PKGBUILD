# $Id$
# Maintainer: Andreas Messer <andi@bastelmap.de>
# Based on arm-elf-gcc-base by Sergej Pupykin <pupykin.s+arch@gmail.com>

_target=arm-eabi

pkgname=${_target}-gcc
pkgver=4.7.0
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
depends=("${_target}-binutils" 'cloog' 'ppl' "${_target}-newlib")
conflicts=("${_target}-gcc-base")
provides=("${_target}-gcc-base")
replaces=("${_target}-gcc-base")
options=(!libtool !emptydirs zipman docs !strip)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2)
md5sums=('2a0f1d99fda235c29d40b561f81d9a77')

build() {
  cd $srcdir/gcc-$pkgver

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

#  rm -rf build
  mkdir -p build
  cd build

  ../configure --prefix=/usr \
	--target=${_target} \
	--host=$CHOST \
	--build=$CHOST \
	--enable-shared --disable-nls \
        --enable-languages=c,c++ \
        --enable-multilib \
        --enable-interwork \
	--with-local-prefix=/usr/lib/${_target} \
	--with-as=/usr/bin/${_target}-as \
        --with-ld=/usr/bin/${_target}-ld \
	--with-newlib \
	--with-float=soft \
	--with-sysroot=/usr/$CHOST/${_target}

  make all-gcc all-target-libgcc all-target-libstdc++-v3 || return 1
}

package() {
  cd $srcdir/gcc-$pkgver/build

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  make DESTDIR=$pkgdir install-gcc install-target-libgcc install-target-libstdc++-v3 || return 1

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info
  rm -rf $pkgdir/usr/share/gcc-${pkgver}

  cp -r $pkgdir/usr/libexec/* $pkgdir/usr/lib/
  rm -rf $pkgdir/usr/libexec

  # strip it manually
  strip $pkgdir/usr/bin/* 2>/dev/null || true
  find $pkgdir/usr/lib -type f -exec ${_target}-strip --strip-debug --strip-unneeded {} \; 2>/dev/null || true
}
