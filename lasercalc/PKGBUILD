# Contributor: Anton Leontiev <unsector /at/ km.ru>
pkgname=lasercalc
pkgver=0.4
pkgrel=1
pkgdesc="A tool to calculate optical paths and resonators based on Gaussian beam matrix optics"
arch=("i686" "x86_64")
url="https://sourceforge.net/apps/phpwebsite/lasercalc/"
license=("GPL3")
depends=("wxgtk-2.9")
makedepends=("wxgtk-2.9")
install=lasercalc.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/${pkgname}_$pkgver-1.tar.gz)
md5sums=('248c824b437b387b643779e63364bc60')

build() {
  cd "$srcdir/$pkgname"
  autoreconf -i
  sed -ie "s:WXCONFIG=/usr/local/bin/wx-config:WXCONFIG=/usr/bin/wx-config-2.9:" configure
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname"
  make -k check
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
  install -Dm644 src/lasercalc.xpm "$pkgdir/usr/share/pixmaps/lasercalc.xpm"
  install -Dm644 debian/lasercalc.desktop "$pkgdir/usr/share/applications/lasercalc.desktop"
}
