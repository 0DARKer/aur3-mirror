# Maintainer:  Bartlomiej Piotrowski <nospam@bpiotrowski.pl> 
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Christos Kotsaris <christos dot kotsaris at gmail dot com>

pkgname=lib32-fmodex
pkgver=4.40.08
pkgrel=1
pkgdesc="An advanced audio engine"
arch=('x86_64')
url="http://www.fmod.org/"
license=('custom')
depends=('lib32-gcc-libs' 'fmodex')
source=(http://www.fmod.org/index.php/release/version/fmodapi${pkgver//./}linux.tar.gz)
md5sums=('66886f371dfd38e65893621f4b778aaa')

package() {
  cd "${srcdir}"/fmodapi${pkgver//./}linux
  install -dm755 "${pkgdir}"/usr/lib32
  
  make DESTLIBDIR="${pkgdir}"/usr/lib32 \
       DESTHDRDIR="${pkgdir}"/usr/include/fmodex \
       install

  ln -sf "/usr/lib32/libfmodex-${pkgver}.so" "${pkgdir}"/usr/lib32/libfmodex.so
  ln -sf "/usr/lib32/libfmodexL-${pkgver}.so" "${pkgdir}"/usr/lib32/libfmodexL.so
  
  rm -rf "${pkgdir}"/usr/include
}
