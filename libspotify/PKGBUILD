# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=libspotify
pkgver=12.1.45
pkgrel=1
pkgdesc="C API package allowing third-party developers to write applications that utilize the Spotify music streaming service"
arch=(i686 x86_64)
url="https://developer.spotify.com/technologies/libspotify/"
license=('custom')
depends=('glibc')
source=(http://developer.spotify.com/download/libspotify/libspotify-${pkgver}-Linux-${CARCH}-release.tar.gz)

md5sums=('133e7c5b4dbe5f8e6054de93ada0e02b')
sha256sums=('dc9c984c38b90f166c3324ad79de5b20237d4ea1a3f1a807d6ca9bbf971dac16')
if [ "$CARCH" == "x86_64" ]; then
  md5sums=('465d513aa0ac7e9dc237dc1e018645a1')
  sha256sums=('9245ec55a5450668f05851c23e63d999bc5b5a91d20e9cab8e8ca94371277080')
fi

build() {
  cd "$srcdir/$pkgname-$pkgver-Linux-$CARCH-release"

  # Don't do stupid things from a Makefile
  msg2 "Patching Makefile..."
  sed -i 's/ldconfig//' Makefile
}

package() {
  cd "$srcdir/$pkgname-$pkgver-Linux-$CARCH-release"

  make prefix="$pkgdir/usr" install

  # Install documentation
  cp -R share "$pkgdir"/usr/share
  mkdir -p "$pkgdir"/usr/share/man
  mv "$pkgdir"/usr/share/man3 "$pkgdir"/usr/share/man/man3

  # Correct pkgconfig file
  sed -e s:PKG_PREFIX:/usr:g \
          < lib/pkgconfig/libspotify.pc \
          > "$pkgdir"/usr/lib/pkgconfig/libspotify.pc

  # Custom license
  install -Dm644 LICENSE licenses.xhtml "$pkgdir"/usr/share/doc/libspotify
  mkdir -p "$pkgdir"/usr/share/licenses/libspotify
  ln -s ../../doc/libspotify/LICENSE "$pkgdir"/usr/share/licenses/libspotify/LICENSE
}

# Local Variables:
# pkgbuild-update-sums-on-save: nil
# End:
