# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=libspotify
pkgver=10.1.16
pkgrel=1
pkgdesc="C API package allowing third party developers to write applications that utilize the Spotify music streaming service"
arch=(i686 x86_64)
url="http://developer.spotify.com/en/libspotify/overview/"
license=('custom')
depends=('glibc')
source=(http://developer.spotify.com/download/libspotify/libspotify-${pkgver}-Linux-${CARCH}-release.tar.gz)

md5sums=('eaec871b27dc46087681337b6992d787')
sha256sums=('2c8748b65d63f4da5383f24410b10af294290c1d163355b1f4aecae2ed2f245f')
if [ "$CARCH" == "x86_64" ]; then
  md5sums=('263c17a6f74268217fab7300db12b017')
  sha256sums=('07c87bf12b63249c07c07a9f65b12264ab07f4b460c453d03d1b6e3086fbdc46')
fi

build() {
  cd "$srcdir/$pkgname-Linux-$CARCH-release"

  # Don't do stupid things from a Makefile
  msg2 "Patching Makefile..."
  sed -i 's/ldconfig//' Makefile
}

package() {
  cd "$srcdir/$pkgname-Linux-$CARCH-release"

  make prefix="$pkgdir/usr" install

  # Install documentation
  cp -R share $pkgdir/usr/share
  mkdir -p $pkgdir/usr/share/man
  mv $pkgdir/usr/share/man3 $pkgdir/usr/share/man/man3

  # Correct pkgconfig file
  sed -e s:PKG_PREFIX:/usr:g \
          < lib/pkgconfig/libspotify.pc \
          > $pkgdir/usr/lib/pkgconfig/libspotify.pc

  # Custom license
  install -Dm644 LICENSE licenses.xhtml $pkgdir/usr/share/doc/libspotify
  mkdir -p $pkgdir/usr/share/licenses/libspotify
  ln -s ../../doc/libspotify/LICENSE $pkgdir/usr/share/licenses/libspotify/LICENSE
}

# Local Variables:
# pkgbuild-update-sums-on-save: nil
# End:
