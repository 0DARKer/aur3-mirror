# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=libspotify
pkgver=0.0.7
pkgrel=1
pkgdesc="C API package allowing third party developers to write applications that utilize the Spotify music streaming service"
arch=(i686 x86_64)
url="http://developer.spotify.com/en/libspotify/overview/"
license=('custom')
depends=('glibc')
source=(http://developer.spotify.com/download/libspotify/libspotify-${pkgver}-linux6-${CARCH}.tar.gz)

md5sums=('0ccddce7355070532e70ddfd44431686')
sha1sums=('b4545c807fca78e6fa56113df4f88ae5443b74d9')
if [ "$CARCH" == "x86_64" ]; then
  md5sums=('ea3d1f0f1e0b50803c5d84e53d6f29ee')
  sha1sums=('6c82234a77b75ebe68c0032ee40a80e53ca6aa99')
fi

build() {
  msg2 "Nothing to build."
}

package() {
  cd "$srcdir/$pkgname-$pkgver-linux6-$CARCH"

  make prefix="$pkgdir/usr" install

  # Install documentation
  mkdir -p $pkgdir/usr/share/man
  cp -R share/man3 $pkgdir/usr/share/man/man3
  cp -R share/doc $pkgdir/usr/share/doc

  # Correct pkgconfig file
  sed -e s:@pkg_prefix@:/usr:g \
          < lib/pkgconfig/libspotify.pc.in \
          > $pkgdir/usr/lib/pkgconfig/libspotify.pc

  # Custom license
  mkdir -p $pkgdir/usr/share/licenses/libspotify
  ln -s ../../doc/libspotify/LICENSE $pkgdir/usr/share/licenses/libspotify/LICENSE
}

# Local Variables:
# pkgbuild-update-sums-on-save: nil
# End:
