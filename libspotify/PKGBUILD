# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=libspotify
pkgver=0.0.8
pkgrel=1
pkgdesc="C API package allowing third party developers to write applications that utilize the Spotify music streaming service"
arch=(i686 x86_64)
url="http://developer.spotify.com/en/libspotify/overview/"
license=('custom')
depends=('glibc')
source=(http://developer.spotify.com/download/libspotify/libspotify-${pkgver}-linux6-${CARCH}.tar.gz)

md5sums=('39bfebfadbfa50054456c92bd7b538b8')
sha256sums=('c9dbbf386b595a3db9c6b1a86a2d508548b4b3a526bb0148607d94ecfb8b9821')
if [ "$CARCH" == "x86_64" ]; then
  md5sums=('634e1acd9715068c74c486e38ed12c54')
  sha256sums=('fc611765441f2c15d2c20c702a47175b8941b8bae253e82f0b89b3f8b91f07b1')
fi

build() {
  msg2 "Nothing to build."
}

package() {
  cd "$srcdir/$pkgname-$pkgver-linux6-$CARCH"

  make prefix="$pkgdir/usr" install

  # Install documentation
  mkdir -p $pkgdir/usr/share/man
  cp -R share/man3 $pkgdir/usr/share/man/man3
  cp -R share/doc $pkgdir/usr/share/doc

  # Correct pkgconfig file
  sed -e s:@pkg_prefix@:/usr:g \
          < lib/pkgconfig/libspotify.pc.in \
          > $pkgdir/usr/lib/pkgconfig/libspotify.pc

  # Custom license
  mkdir -p $pkgdir/usr/share/licenses/libspotify
  ln -s ../../doc/libspotify/LICENSE $pkgdir/usr/share/licenses/libspotify/LICENSE
}

# Local Variables:
# pkgbuild-update-sums-on-save: nil
# End:
