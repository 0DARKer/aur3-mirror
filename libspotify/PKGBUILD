# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=libspotify
pkgver=11.1.60
pkgrel=1
pkgdesc="C API package allowing third party developers to write applications that utilize the Spotify music streaming service"
arch=(i686 x86_64)
url="http://developer.spotify.com/en/libspotify/overview/"
license=('custom')
depends=('glibc')
source=(http://developer.spotify.com/download/libspotify/libspotify-${pkgver}-Linux-${CARCH}-release.tar.gz)

md5sums=('e07ac443c215cfb6542ec15edfc0a358')
sha256sums=('10cb550e3f572377188c620dde01d3f8c6a5842fde7579b5841006004f914689')
if [ "$CARCH" == "x86_64" ]; then
  md5sums=('14446eccca099e532493425adb0ae222')
  sha256sums=('e1df8cad3158ed8d1804aaa4d8a1ed9f36dba0ea3174c51a77eb282dc873bde7')
fi

build() {
  cd "$srcdir/$pkgname-$pkgver-Linux-$CARCH-release"

  # Don't do stupid things from a Makefile
  msg2 "Patching Makefile..."
  sed -i 's/ldconfig//' Makefile
}

package() {
  cd "$srcdir/$pkgname-$pkgver-Linux-$CARCH-release"

  make prefix="$pkgdir/usr" install

  # Install documentation
  cp -R share "$pkgdir"/usr/share
  mkdir -p "$pkgdir"/usr/share/man
  mv "$pkgdir"/usr/share/man3 "$pkgdir"/usr/share/man/man3

  # Correct pkgconfig file
  sed -e s:PKG_PREFIX:/usr:g \
          < lib/pkgconfig/libspotify.pc \
          > "$pkgdir"/usr/lib/pkgconfig/libspotify.pc

  # Custom license
  install -Dm644 LICENSE licenses.xhtml "$pkgdir"/usr/share/doc/libspotify
  mkdir -p "$pkgdir"/usr/share/licenses/libspotify
  ln -s ../../doc/libspotify/LICENSE "$pkgdir"/usr/share/licenses/libspotify/LICENSE
}

# Local Variables:
# pkgbuild-update-sums-on-save: nil
# End:
