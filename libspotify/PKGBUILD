# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=libspotify
pkgver=9.1.32
pkgrel=1
pkgdesc="C API package allowing third party developers to write applications that utilize the Spotify music streaming service"
arch=(i686 x86_64)
url="http://developer.spotify.com/en/libspotify/overview/"
license=('custom')
depends=('gcc-libs' 'glibc')
source=(http://developer.spotify.com/download/libspotify/libspotify-${pkgver}-Linux-${CARCH}.tar.gz)

md5sums=('b45db177b4ee2b44a681176f0836872f')
sha256sums=('7168cffd9755cba2057fb918038d74ed130ec0415ee52f7ec77309f40a5dd52f')
if [ "$CARCH" == "x86_64" ]; then
  md5sums=('83dda65f381f6e3fe957733a6d074cce')
  sha256sums=('19c52ef49eef2c833014459b561f0db449571a624d2f8de432327a441e5162ff')
fi

build() {
  msg2 "Nothing to build."
}

package() {
  cd "$srcdir/$pkgname-$pkgver-Linux-$CARCH"

  make prefix="$pkgdir/usr" install

  # Install documentation
  cp -R share $pkgdir/usr/share
  mkdir -p $pkgdir/usr/share/man
  cp -R man/man3/man3spotify $pkgdir/usr/share/man/man3

  # Correct pkgconfig file
  sed -e s:PKG_PREFIX:/usr:g \
          < lib/pkgconfig/libspotify.pc \
          > $pkgdir/usr/lib/pkgconfig/libspotify.pc

  # Custom license
  mkdir -p $pkgdir/usr/share/licenses/libspotify
  ln -s ../../doc/libspotify/LICENSE $pkgdir/usr/share/licenses/libspotify/LICENSE
}

# Local Variables:
# pkgbuild-update-sums-on-save: nil
# End:
