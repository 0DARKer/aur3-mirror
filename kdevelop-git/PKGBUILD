# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevelop-git
pkgver=r20612.267d661
pkgrel=1
pkgdesc="A C/C++ development environment for KDE. (GIT Version)"
arch=(i686 x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
groups=('kde')
depends=('qt5-tools' 'kdevplatform-git' 'krunner')
makedepends=(cmake git)
optdepends=('okteta-git: hex editor'
            'konsole-git: embedded terminal'
            'git: git support'
            'gdb: gdb support'
            'qt4-doc: qt4 documentation integration'
            'qt5-doc: qt5 documentation integration')
provides=('kdevelop')
conflicts=('kdevelop')
install='kdevelop-git.install'
source=(git+git://anongit.kde.org/kdevelop)
sha512sums=('SKIP')

pkgver() {
  cd kdevelop
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdevelop \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
