# Maintainer: Army
pkgname=mupdf-git
_pkgname=mupdf
pkgver=0.3089.3c20011
pkgrel=1
pkgdesc="lightweight PDF viewer and toolkit written in portable C"
arch=('i686' 'x86_64')
url="http://mupdf.com"
license=('GPL3')
depends=('libxext' 'jbig2dec' 'desktop-file-utils' 'libjpeg' 'openjpeg' 'freetype2')
makedepends=('git')
provides=('mupdf')
conflicts=('mupdf')
install=mupdf-git.install
source=('git+git://git.ghostscript.com/mupdf.git')
md5sums=('SKIP')

# without tags
pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd "$srcdir/$_pkgname"
  make build=release prefix="${pkgdir}/usr"
}

package() {
  cd "$srcdir/$_pkgname"
  make build=release prefix="${pkgdir}/usr" install

  cd "$srcdir/$_pkgname/debian"
  install -Dm644 mupdf.desktop "${pkgdir}/usr/share/applications/mupdf.desktop"
  install -Dm644 mupdf.xpm "${pkgdir}/usr/share/pixmaps/mupdf.xpm"
  
  # apparently an upstream bug, namcap doesn't like it, so I fix it
  cd "$pkgdir/usr/lib"
  for i in *;do chmod -x "$i";done
}

# vim:set ts=2 sw=2 et:
