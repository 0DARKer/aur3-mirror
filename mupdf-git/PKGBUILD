# Contributor: Army 
# Maintainer: Stefan Husmann <stefan-husmannt-online.de> 

_pkgname=mupdf
pkgname=$_pkgname-git
pkgver=1.5.ios.appstore.18.g25f7f41
pkgrel=1
pkgdesc="lightweight PDF viewer and toolkit written in portable C"
arch=('i686' 'x86_64')
url="http://mupdf.com"
license=('GPL3')
depends=('libxext' 'jbig2dec' 'desktop-file-utils' 'libjpeg' 'openssl')
makedepends=('git')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
install=$pkgname.install
source=("git://git.ghostscript.com/mupdf.git" 'mupdf.desktop')
md5sums=('SKIP'
         '54abfeb678a7dfca238fa8d423f8d2fc')
options=('!makeflags' 'staticlibs')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(git describe --tags | sed 's|-|.|g')
}

prepare() {
  cd "$srcdir/$_pkgname"
  git submodule update --init thirdparty/openjpeg
  git submodule update --init thirdparty/mujs
}

build() {
  CFLAGS+=' -fPIC'
  CXXFLAGS+=' -fPIC'
  cd "$srcdir/$_pkgname"
  make build=release prefix="$pkgdir/usr"
}

package() {
  cd "$srcdir/$_pkgname"
  make build=release prefix="$pkgdir/usr" install
  install -Dm644 build/release/libmujs.a "$pkgdir"/usr/lib
  install -Dm644 $srcdir/mupdf.desktop \
    $pkgdir/usr/share/applications/mupdf.desktop
  find "$pkgdir"/usr/include \
    "$pkgdir"/usr/share \
    "$pkgdir"/usr/lib -type f | xargs chmod -v 0644
}
