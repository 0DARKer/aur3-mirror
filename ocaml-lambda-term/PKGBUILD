# Maintainer: Taylor Venable <taylor@metasyntax.net>

srcname='lambda-term'
pkgname="ocaml-${srcname}"
pkgver=1.0
pkgrel=1
pkgdesc='A cross-platform library for manipulating the terminal'
arch=('i686' 'x86_64')
url='http://forge.ocamlcore.org/projects/lambda-term/'
license=('BSD')
depends=('ocaml>=3.11.0' 'ocaml-lwt' 'ocaml-react' 'ocaml-zed')
makedepends=('ocaml-findlib')
source=('https://forge.ocamlcore.org/frs/download.php/663/lambda-term-1.0.tar.gz')
options=('!strip')
md5sums=('dac406948f298df1417dfb11a90efe59')

build() {
    cd "$srcdir/${srcname}-${pkgver}"
    ./configure

    env DESTDIR="$pkgdir" \
        OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
        make
}


package() {
    mkdir -p "$pkgdir/$(ocamlfind printconf destdir)"
    mkdir -p "$pkgdir/$(ocamlfind printconf destdir)/stublibs"
    cd "$srcdir/${srcname}-${pkgver}"
    env DESTDIR="$pkgdir" \
        OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
        make install
    install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
