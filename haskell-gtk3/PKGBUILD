# Contrubutor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-gtk3
pkgver=0.12.5.7
pkgrel=1
pkgdesc="Binding to the gtk3 library for Gtk2Hs."
url="http://hackage.haskell.org/package/gtk3"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'gtk3' "haskell-pango" 'haskell-mtl')
makedepends=("gtk2hs-buildtools")
provides=('gtk2hs-gtk3')
replaces=('gtk2hs-gtk3')
conflicts=('gtk2hs-gtk3')
options=('strip' 'staticlibs')
install=gtk2hs-gtk3.install
source=(http://hackage.haskell.org/packages/archive/gtk3/$pkgver/gtk3-$pkgver.tar.gz)
md5sums=('768dba70147a7166ce1c7151e5da7064')

build() {
  cd ${srcdir}/gtk3-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/haskell-gtk3 \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  _hmtlver=`pacman -Q haskell-mtl | cut -f2 -d\  | cut -f1 -d-`
  _hpangover=`pacman -Q haskell-pango | cut -f2 -d\  | cut -f1 -d-`
  depends=("ghc" "gtk3" "haskell-pango=${_hpangover}" "haskell-mtl=${_hmtlver}")

  cd ${srcdir}/gtk3-${pkgver}
  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/gtk2hs-gtk3/register.sh
  install -m744 unregister.sh ${pkgdir}/usr/share/haskell/gtk2hs-gtk3/unregister.sh
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/haskell-gtk3/html ${pkgdir}/usr/share/doc/ghc/html/libraries/gtk2hs-gtk3
  runhaskell Setup copy --destdir=${pkgdir}
}
