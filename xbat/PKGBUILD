# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=xbat
pkgver=1.1.1
pkgrel=1
pkgdesc="Vertical scrolling shooter based on the arcade game Xevious"
arch=('i686' 'x86_64')
url="http://www.happypenguin.org/show?XBat"
license=('GPL')
depends=('libx11')
makedepends=('imake')
source=("http://ftp.freebsd.org/pub/FreeBSD/ports/local-distfiles/hrs/xev${pkgver//./}.tar.gz"
        "${pkgname}-install.patch")
md5sums=('87a156ca35a75c94b5617005e2b2ca99'
         '779cccc30bc296a2b1be967fe3acaa39')

build() {
  cd "${srcdir}/Xev${pkgver//./}"

  # Apply the patch to create configure/make files
  # Downloaded from: http://www.identicalsoftware.com/ogs/1999/xbat.html
  patch -p1 -i "../${pkgname}-install.patch"

  # Compile
  chmod 755 configure
  LIBS=-lm ./configure -prefix=/usr || return 1
  make || return 1
}

package() {
  cd "${srcdir}/Xev${pkgver//./}"

  # Install game files
  mkdir -p "${pkgdir}/usr/share/Xbat/Score"
  install -m666 Score/* "${pkgdir}/usr/share/Xbat/Score"
  cp -r Domo Image Map "${pkgdir}/usr/share/Xbat"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install docs
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 README* "${pkgdir}/usr/share/doc/${pkgname}"
}
