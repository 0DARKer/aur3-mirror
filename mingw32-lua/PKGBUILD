# Maintainer: Mikael Eriksson <mikael_miffe_eriksson@yahoo.se3

pkgname=mingw32-lua 
_pkgver=5.1.4
pkgver=$_pkgver.2
pkgrel=1
pkgdesc="A powerful light-weight programming language designed for extending applications." 
arch=('any')
url="http://www.lua.org/" 
depends=() 
makedepends=('mingw32-gcc' 'mingw32-w32api')
license=(MIT)
source=($url/ftp/lua-$_pkgver.tar.gz $url/ftp/patch-lua-$_pkgver-2)
md5sums=('d0870f2de55d59c1c8419f36e8fac150'
         '1239310e0c4a581c7831e596f95cc6cd')

build() { 
  cd $srcdir/lua-$_pkgver/src
  patch -i $srcdir/patch-lua-$_pkgver-2
  make CC=i486-mingw32-gcc \
    AR="\$(CC) -shared -o" \
    RANLIB="i486-mingw32-strip --strip-unneeded" \
    MYCFLAGS=-DLUA_BUILD_AS_DLL \
    MYLIBS="" \
    MYLDFLAGS=-s \
    LUA_A=lua51.dll \
    LUA_T=lua.exe \
    LUAC_T=luac.exe \
    lua.exe
  make CC=i486-mingw32-gcc \
    AR="i486-mingw32-ar rcu" \
    RANLIB=i486-mingw32-ranlib \
    LUAC_T=luac.exe \
    luac.exe
  cd ..
  make TO_BIN="lua.exe luac.exe lua51.dll" INSTALL_DATA="cp -d" INSTALL_TOP=$pkgdir/usr/i486-mingw32 install || return 1
  install -D -m 644 etc/lua.pc $pkgdir/usr/i486-mingw32/lib/pkgconfig/lua.pc
  sed -i 's,^prefix=.*$,prefix=/usr/i486-mingw32,' $pkgdir/usr/i486-mingw32/lib/pkgconfig/lua.pc
}

# vim: ts=2 sw=2 et ft=sh
