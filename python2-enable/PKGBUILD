# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-enable
pkgver=4.4.1
pkgrel=1
pkgdesc="Low-level drawing and interaction packages"
arch=('i686' 'x86_64')
url="https://github.com/enthought/enable"
license=('BSD')
depends=('python2-reportlab' 'python2-traitsui' 'glu')
makedepends=('python2-distribute' 'cython2' 'swig')
conflicts=('python-enthought-enable' 'python2-enable-git' 'python-ets-enable-svn')
options=(!emptydirs)

source=("https://github.com/enthought/enable/archive/${pkgver}.tar.gz")
md5sums=('9315870edffa5d47e9af63a69ced15f5')

prepare() {
  cd "$srcdir"/enable-$pkgver

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  cd "$srcdir"/enable-$pkgver

  python2 setup.py build
}

package() {
  cd "$srcdir"/enable-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

