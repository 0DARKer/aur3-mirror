# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>
# Contributor: Matt Portas <matt.r.portas(at)gmail(dot)com>

pkgname=ghdl
_gccver=4.3.4
pkgver=0.29
pkgrel=3
arch=('i686' 'x86_64')
pkgdesc=" GHDL is a complete VHDL  simulator, using the GCC  technology."
url="http://ghdl.free.fr/"
license=('GPL')
makedepends=('gcc-ada')
source=("http://ghdl.free.fr/$pkgname-$pkgver.tar.bz2"
	"ftp://gcc.gnu.org/pub/gcc/releases/gcc-${_gccver}/gcc-core-${_gccver}.tar.bz2"
  "gcc_pure64.patch")

md5sums=('fd3dc7a71ba521e442bef76f48177767'
         '575b3220bb8e7060939c429fc8608d22'
         '22cec272f9cc2801d3cd348feaca888b')
sha512sums=('6e86013d3798c875b8fbb67b64c9b0cdda89c878985b64a1ea2d0e04e0c3ae714799a4ffe050f3651f7df8078b791d8613733065c799261f28884103c1597210'
            '03282633d5dcfee2b474b6d0ed2bfc708611a2049f4f4abf85c371ae04c2060a97a01380c77ef2e88b4e6f7eff5c368d1031737f4eab9f537dc1ed61430f0cf1'
            '3cc8063a37298681219c21fe8987535183d544dd647e8307f063c543832804f27acce61a51eb727b0e952ac2ea08c54984230bf34684201d6b7025f9954cbbc6')

build() {
  cd $srcdir
  mv $pkgname-$pkgver/vhdl gcc-$_gccver/gcc
  cd gcc-$_gccver

  if [ "${CARCH}" = "x86_64" ]; then
    patch -Np1 -i ${srcdir}/gcc_pure64.patch
  fi

  ./configure \
    --prefix=/usr \
    --infodir=/usr/share/info \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --mandir=/usr/share/man \
    --enable-clocale=gnu \
    --enable-languages=vhdl \
    --enable-threads=posix \
    --enable-__cxa_atexit \
    --disable-bootstrap \
    --disable-libstdcxx-pch \
    --disable-multilib

  make -j1
}

package() {
  cd ${srcdir}/gcc-${_gccver}
  make -j1 DESTDIR=${pkgdir} install

  # Clean up
  rm ${pkgdir}/usr/bin/{cpp,gcc{,bug},gcov,$CHOST-gcc{,-${_gccver}}}
  rm ${pkgdir}/usr/share/man/man1/{cpp,gcc,gcov}.1
  rm ${pkgdir}/usr/share/man/man7/{fsf-funding,gfdl,gpl}.7
  rm ${pkgdir}/usr/share/info/{cpp{,internals},gcc{,install,int},libgomp}.info
  rm ${pkgdir}/usr/lib/lib{iberty.a,gcc_s*,gomp*,mudflap*,ssp*}
  rm -r ${pkgdir}/usr/{include,share/locale}
}
