pkgname=ghdl
pkgver="0.31"
_gccver=4.8.2
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='VHDL simulator'
url='http://ghdl.free.fr/'
license=('GPL')
depends=('gcc-ada' 'zlib')
makedepends=('bison' 'flex' 'gawk' 'gettext' 'texinfo')
options=(!makeflags !emptydirs !libtool staticlibs)
source=("http://sourceforge.net/projects/ghdl-updates/files/Source/ghdl-${pkgver}.tar.gz"
	"ftp://ftp.gnu.org/gnu/gcc/gcc-${_gccver}/gcc-${_gccver}.tar.bz2")

prepare() {
	cd ${srcdir}/translate/gcc
	./dist.sh sources
	mv ghdl-${pkgver}.tar.bz2 ${srcdir}
	cd ${srcdir}
	tar xf ghdl-${pkgver}.tar.bz2
	mv ghdl-${pkgver}/vhdl gcc-${_gccver}/gcc
	rm -rf ghdl-${pkgver}* *.ad* doc libraries ortho psl translate xtools README NEWS COPYING
}

build() {
	mkdir ${srcdir}/gcc-build
	cd ${srcdir}/gcc-build

	export CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
	${srcdir}/gcc-${_gccver}/configure --prefix=/usr \
		--libdir=/usr/lib --libexecdir=/usr/lib \
		--mandir=/usr/share/man --infodir=/usr/share/info \
		--disable-bootstrap \
		--enable-languages=vhdl \
		--enable-shared --enable-threads=posix \
		--with-system-zlib --enable-__cxa_atexit \
		--disable-libunwind-exceptions --enable-clocale=gnu \
		--disable-libstdcxx-pch --disable-libssp \
		--enable-gnu-unique-object --enable-linker-build-id \
		--enable-cloog-backend=isl --disable-cloog-version-check \
		--enable-lto --enable-plugin --disable-install-libiberty \
		--with-linker-hash-style=gnu \
		--disable-multilib --disable-werror \
		--enable-checking=release
	make
}

package() {
	cd ${srcdir}/gcc-build
	make DESTDIR=${pkgdir} install
	rm -r ${pkgdir}/usr/{lib64,share/locale,share/man/man7}
	find ${pkgdir}/usr/lib \
		-maxdepth 1 -mindepth 1 -not -name 'gcc' \
		-exec rm -rf '{}' \;
	find ${pkgdir}/usr/lib/gcc/${CARCH}-unknown-linux-gnu/${_gccver} \
		-maxdepth 1 -mindepth 1 -not -name 'vhdl' -not -name 'ghdl1' \
		-exec rm -rf '{}' \;
	find ${pkgdir}/usr/bin \
		${pkgdir}/usr/share/man/man1 \
		${pkgdir}/usr/share/info \
		-maxdepth 1 -mindepth 1 -not -name 'ghdl*' \
		-exec rm -rf '{}' \;
}
md5sums=('16860d3ec7c371346373f52f50de3ebf'
         'a3d7d63b9cb6b6ea049469a0c4a43c9d')
