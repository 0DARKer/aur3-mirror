# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lightdm-gtk-greeter-bzr
pkgver=22
pkgrel=1
pkgdesc="GTK+ greeter for lightweight display manager - bzr checkout"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=(GPL3 LGPL3)
depends=('lightdm' 'gtk3' 'gnome-themes-standard' 'archlinux-wallpaper')
optdepends=('xorg-xserver-xephr: run lightdm in test mode')
makedepends=('bzr' 'vala' 'gnome-common' 'gtk-doc' 'gnome-doc-utils' 'pkg-config' 'gobject-introspection')
conflicts=('lightdm-gtk-greeter')
provides=('lightdm-gtk-greeter')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')

_bzrtrunk="lp:lightdm-gtk-greeter"
_bzrmod="lightdm-gtk-greeter-trunk"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_bzrmod}-build"
  cp -r "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
  cd "${srcdir}/${_bzrmod}-build"

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm \
    --disable-static
  make

  # add default background image
  sed -i -e '/^#background=/s/^#//' -e '/^background=/s|=.*|=/usr/share/archlinux/wallpaper/archlinux-arrival.jpg|' data/lightdm-gtk-greeter.conf

  # add default theme
  sed -i -e '/^#theme-name=/s/^#//' -e '/^theme-name=/s/=.*/=Adwaita/' data/lightdm-gtk-greeter.conf
}

package() {
  cd "${srcdir}/${_bzrmod}-build"
  make DESTDIR="${pkgdir}" install
}
