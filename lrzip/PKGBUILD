#Maintainer: graysky <graysky AT archlinux DOT us>
#Contributor: kastor@fobos.org.ar
pkgname=lrzip
pkgver=0.605
pkgrel=1
pkgdesc="A compression program which uses the lzma, lzo, and rzip compression algorithms."
url="http://lrzip.kolivas.org/"
license="GPL"
arch=('i686' 'x86_64')
depends=('lzo2' 'bzip2' 'zlib')
[[ $CARCH == i686 ]] && depends+=('nasm')

source=(http://ck.kolivas.org/apps/$pkgname/$pkgname-$pkgver.tar.bz2)

build() {
  # CFLAGS recommended by developer #tks to lrzip-git
  CFLAGS="${CFLAGS/-O2/-O3} -fomit-frame-pointer"
  CXXFLAGS="${CXXFLAGS/-O2/-O3} -fomit-frame-pointer"

  cd $startdir/src/$pkgname-$pkgver

 if [[ $CARCH == i686 ]];then
      ./autogen.sh --prefix=/usr --enable-asm
  else
      ./autogen.sh --prefix=/usr
  fi
 
 #tks to @chenxiaolong
  sed -i -e 's/^\t\$\(LN_S\).*//g' -e 's/^install-exec-hook.*$//g' -e 's/install-exec-hook//g' Makefile
  make
}

package() {
  cd $startdir/src/$pkgname-$pkgver
  make DESTDIR=$startdir/pkg install
  cd $startdir/pkg/usr/bin
  ln -sf lrzip lrunzip
  ln -sf lrztar lrzuntar
}
sha256sums=('88216bfc4a6b7406b87bf97447daa625704f1ee5d35301b12d78d89bfd4f4143')
