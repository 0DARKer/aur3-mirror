#Maintainer: graysky <graysky AT archlinux DOT us>
#Contributor: kastor@fobos.org.ar
pkgname=lrzip
pkgver=0.610
pkgrel=1
pkgdesc="A multi-threaded compression program which uses the rzip/lzma, lzo, and zpaq compression algorithms."
url="http://lrzip.kolivas.org/"
license="GPL"
arch=('i686' 'x86_64')
depends=('lzo2' 'bzip2' 'zlib')
[[ $CARCH == i686 ]] && depends+=('nasm')
source=(http://ck.kolivas.org/apps/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('d6ac97881265d5d5a48998ca57017be3eae904353c7f6e300d4b9803ddf13a5f')

build() {
  # CFLAGS recommended by developer #tks to lrzip-git
  CFLAGS="${CFLAGS/-O2/-O3} -fomit-frame-pointer"
  CXXFLAGS="${CXXFLAGS/-O2/-O3} -fomit-frame-pointer"

  cd "$srcdir/$pkgname-$pkgver"

 if [[ $CARCH == i686 ]];then
      ./autogen.sh --prefix=/usr --enable-asm
  else
      ./autogen.sh --prefix=/usr
  fi

# tks to @chenxiaolong
#  sed -i -e 's/^\t\$\(LN_S\).*//g' -e 's/^install-exec-hook.*$//g' -e 's/install-exec-hook//g' Makefile
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
