# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

_pkgname=gnumeric
pkgname=$_pkgname-devel
pkgver=1.11.4
pkgrel=1
pkgdesc="A GNOME Spreadsheet Program (development release)"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice-devel' 'dconf' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils' 'python2-gobject2' 'psiconv')
optdepends=('python2-gobject2: for python plugin support'
            'psiconv: for Psion 5 file support')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
install=$_pkgname.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz)
sha256sums=('3a239ca5aae25dd8bad173c89ea930491c1fc6556d41de45f31397f455334201')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
