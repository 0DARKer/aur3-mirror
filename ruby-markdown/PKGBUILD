# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
_gemname=markdown
pkgname=ruby-markdown
pkgver=1.1.1
pkgrel=2
pkgdesc="Markdown Engine Wrapper - Use Your Markdown Library of Choice"
arch=(any)
url="https://github.com/rubylibs/markdown"
license=('custom')
depends=('ruby' 'ruby-kramdown' 'ruby-props' 'ruby-textutils')
makedepends=('rubygems')
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem
        'LICENSE')
noextract=(${_gemname}-${pkgver}.gem)
md5sums=('a5b8e57b025f07f52b354a5fdc69e6f8'
         'de8f254bb75f084f97fb23e14230b9db')

package() {
    cd "$srcdir"
    # _gemdir is defined inside package() because if ruby[gems] is not installed on
    # the system, makepkg will exit with an error when sourcing the PKGBUILD.
    local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

    gem install --no-user-install --ignore-dependencies -i "${pkgdir}${_gemdir}" -n "$pkgdir/usr/bin" \
        "${_gemname}-${pkgver}.gem"

    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

