# Maintainer: Okki <okki666@free.fr>
pkgname=dolibarr
pkgver=3.6.0
pkgrel=6
pkgdesc="ERP/CRM for small and medium companies"
arch=('any')
url="http://www.dolibarr.org"
license=('GPL3')
depends=('php')
optdepends=('apache: Web server to run Dolibarr'
            'mysql: Database server'
            'postgreql: Database server')
install=dolibarr.install
source=("http://downloads.sourceforge.net/project/dolibarr/Dolibarr%20ERP-CRM/$pkgver/dolibarr-$pkgver.tgz"
        "httpd-dolibarr.conf")
sha256sums=('9c7dabb9be7738486cd9047d9e9f2461299f7be08f089d76a984549980112e57'
            '8ae345462bbc908c2c10b436178a4679938a0f7e371b41e515900eec8e47cc54')

package() {
  mkdir -p "${pkgdir}/usr/share/webapps/"
  mkdir -p "${pkgdir}/etc/webapps/${pkgname}"

  mv "${srcdir}/${pkgname}-${pkgver}" "${srcdir}/${pkgname}"

  cp -a "${srcdir}/${pkgname}" "${pkgdir}/usr/share/webapps/${pkgname}"
  install -D -m644 "${srcdir}/${pkgname}/htdocs/conf/conf.php.example" "${pkgdir}/etc/webapps/${pkgname}/dolibarr.example.conf"

  # Required for configuration from the web browser
  touch "${pkgdir}/usr/share/webapps/${pkgname}/htdocs/conf/conf.php"

  # Change rights
  find "${pkgdir}/usr/share/webapps/${pkgname}" -type f -exec chmod 0664 {} \;
  find "${pkgdir}/usr/share/webapps/${pkgname}" -type d -exec chmod 0775 {} \;
  chown -R http.http "${pkgdir}/usr/share/webapps/${pkgname}"

  # Application storage 
  mkdir -p "${pkgdir}/var/lib/${pkgname}/documents"
  chown http "${pkgdir}/var/lib/${pkgname}/documents"

  # License
  install -D -m644 "${srcdir}/${pkgname}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Apache configuration
  install -D -m644 httpd-dolibarr.conf "${pkgdir}/etc/webapps/${pkgname}/httpd-dolibarr.conf"
}

