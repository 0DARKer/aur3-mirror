# Maintainer: carstene1ns <carsten.1 gmx net>

pkgname=qwad
pkgver=0.8
pkgrel=1
pkgdesc="A Python-Qt utility to manage Wad files with NUS downloader for Wii"
arch=('any')
url="http://www.nanolx.org/wiihomebrew/qwad"
license=('GPL3')
depends=('python2-crypto' 'python2-pyqt')
source=("http://nanolx.org/downloads/Qwad/${pkgname}_${pkgver}.tar.bz2")
# using sha1sum provided by author
sha1sums=('c42952993b31c09de01a6cc96d5b0aec092ff89d')

prepare() {
  cd "$srcdir/${pkgname}_${pkgver}"

  # QT 4 fix
  sed -i -e "s|-lrelease|-lrelease-qt4|" Makefile
}

build() {
  cd "${srcdir}/${pkgname}_${pkgver}"

  # build translation(s)
  make
}

package() {
  cd "$srcdir/${pkgname}_${pkgver}"

  DESTDIR="$pkgdir" make install

  # python 2 fix
  sed -i -e "s|python|/usr/bin/env python2|" $pkgdir/usr/bin/$pkgname
}
