# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=conky-manager
pkgver=2.3.1
_ubuntu=~123~ubuntu14.10.1
pkgrel=1
pkgdesc="Simple tool for managing conky scripts"
url="http://teejeetech.blogspot.in/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('conky' 'desktop-file-utils' 'gtk3' 'imagemagick' 'json-glib' 'libgee06' 'libsoup' 'p7zip' 'rsync')
makedepends=('vala')
options=(!emptydirs)
install=conky-manager.install
source=(http://ppa.launchpad.net/teejee2008/ppa/ubuntu/pool/main/c/${pkgname}/${pkgname}_${pkgver}${_ubuntu}.tar.xz)
sha512sums=('9191b349be8667d9d67a2f3ee266aaeee68279c32c78bde121ac53a12a89c531b865cf379c2f37075ccce5e8b6768dab5073fa9babd30ca321128d249a248fb0')

build() {
  cd ${pkgname}-${pkgver}${_ubuntu}
  make
}

package() {
  cd ${pkgname}-${pkgver}${_ubuntu}
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/bin/conky-manager-uninstall"
  # fix make install permissions
  find "${pkgdir}/usr/share/${pkgname}" -type f -print0 | xargs -0 chmod 644
  chmod 644 "${pkgdir}/usr/share/applications/conky-manager.desktop"
}

# vim: ts=2 sw=2 et:
