# Maintainer: ksj <podhorsky.ksj@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

_gitname=libfm
pkgname=libfm-gtk3-git
pkgver=20141229
pkgrel=1
pkgdesc='Library for file management'
arch=('i686' 'x86_64')
url="http://www.lxde.org"
license=('GPL2')
depends=('gtk3' 'menu-cache' 'desktop-file-utils' 'libexif' 'dbus-glib')
makedepends=('git' 'intltool' 'gtk-doc')
provides=('libfm' 'libfm-extra')
conflicts=('libfm')
install=libfm.install
source=("git://github.com/lxde/libfm.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {    
    cd "$srcdir/$_gitname"
	./autogen.sh
	./configure --with-gtk=3 --enable-gtk-doc-html=no --disable-actions --prefix=/usr --sysconfdir=/etc --with-gnu-ld

	make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install

  # Temporary fix to FS#32361
  rm -rf "$pkgdir"/usr/include/libfm
  mv "$pkgdir"/usr/include/libfm-1.0/ "$pkgdir"/usr/include/libfm

  # Split libfm-extra
  [[ -d $srcdir/libfm-extra ]] && rm -r "$srcdir/libfm-extra/"
  mkdir "$srcdir"/libfm-extra
  mv "$pkgdir"/usr/include/libfm/fm-{extra,version,xml-file}.h \
     "$pkgdir"/usr/lib/libfm-extra.so* \
     "$pkgdir"/usr/lib/pkgconfig/libfm-extra.pc \
     "$srcdir/libfm-extra/"
}
