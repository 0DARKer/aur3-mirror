# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=xfconf-devel
pkgver=4.8.0
pkgrel=1
pkgdesc="Simple client-server configuration storage and query system (Development Version)"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4-devel')
# http://www.xfce.org/documentation/requirements
# keep dbus and gtk2, see also http://bugs.archlinux.org/task/14536
depends=( "libxfce4util-devel>=4.8.0" "dbus-glib" 'dbus' 'gtk2')
makedepends=('pkgconfig' 'perl-extutils-depends' 'perl-extutils-pkgconfig' 'glib-perl' 'intltool' 'gtk-doc' 'chrpath')
options=('!libtool' '!emptydirs')
conflicts=('xfconf' 'xfconf-git')
source=(http://archive.xfce.org/src/xfce/xfconf/4.8/xfconf-$pkgver.tar.bz2)
md5sums=('0f11ed1ec7789c5c4c3fcc7cdb3c2940')

build() {
  cd ${srcdir}/xfconf-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var \
	--disable-static \
	--enable-gtk-doc \
	--with-perl-options=INSTALLDIRS="vendor" \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/xfconf-${pkgver}
  make DESTDIR=${pkgdir} install
  # remove unneeded dynloader bootstrap file
  rm ${pkgdir}/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.bs 
  
  # fix insecure rpath, http://bugs.archlinux.org/task/19980
  chrpath -d ${pkgdir}/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.so
}