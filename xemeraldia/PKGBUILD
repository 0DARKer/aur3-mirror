# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=xemeraldia
pkgver=0.4.3
pkgrel=1
pkgdesc="A tetris-like falling blocks game"
arch=('i686' 'x86_64')
url="http://www.reloco.com.ar/xemeraldia/"
license=('custom')
depends=('gtk2')
source=("http://www.reloco.com.ar/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.patch")
md5sums=('55cb4d5ea0564e535ba7f8cb48a915da'
         'b221ed800c733889c81ed7b7ecec32ed')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "../${pkgname}.patch"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install game files
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "bitmaps/${pkgname}.xpm" "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # Install translations
  cd po
  make DESTDIR="${pkgdir}" install || return 1
}
