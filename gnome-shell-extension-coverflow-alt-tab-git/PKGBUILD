# Maintainer: XZS <d.f.fischer at web dot de>
# Contributor: Alucryd <alucryd at gmail dot com>

pkgname=gnome-shell-extension-coverflow-alt-tab-git
_extname='CoverflowAltTab@dmo60.de'
pkgver=92
pkgrel=1
pkgdesc="Replacement of Alt-Tab, iterates through windows in a cover-flow manner"
arch=('any')
url="https://github.com/dmo60/CoverflowAltTab"
license=('GPL')
depends=('gnome-shell')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=gschemas.install
source=("${pkgname%-*}::git+https://github.com/dmo60/CoverflowAltTab.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  git rev-list --count HEAD
}

# gnome-shell 3.12 is now officially supported.
# The workaround is kept here to reuse it with future versions, if needed.
#build() {
#  # gnome-shell 3.12 is not officially supported.
#  cd "${pkgname%-*}/$_extname"
#  sed -i '3s/]/, "3.12"]/' metadata.json
#}

package() {
  cd ${pkgname%-*}

  mkdir -p "${pkgdir}"/usr/share/{glib-2.0,gnome-shell/extensions}
  cp -dr --no-preserve=ownership "$_extname" "${pkgdir}"/usr/share/gnome-shell/extensions/
  cd "${pkgdir}/usr/share/gnome-shell/extensions/$_extname"
  rm schemas/gschemas.compiled
  mv schemas "${pkgdir}"/usr/share/glib-2.0/

# Fix permissions
  find "${pkgdir}" -type d -exec chmod 755 {} +
  find "${pkgdir}" -type f -exec chmod 644 {} +
}

# vim: ts=2 sw=2 et:
