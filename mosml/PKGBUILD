# Maintainer : Yi Dai <plmday_at_gmail_dot_com>
# Contributor: Constantin Berhard <cberhard_at_directbox_dot_com>
#            , Moritz Wilhelmy <echo dGhlLnd1bGYuZ2FuZ0Bnb29nbGVtYWlsLmNvbQ== | base64 -d>
#            , Drew Desai <alerante_at_yahoo_dot_com>

pkgname=mosml
pkgver=2.01
pkgrel=6
pkgdesc="Moscow ML is a light-weight implementation of Standard ML (SML)"
url="http://www.itu.dk/~sestoft/mosml.html"
license=('GPL')
arch=('i686' 'x86_64')
install=mosml.install
source=(http://www.itu.dk/people/sestoft/mosml/mos201src.tar.gz
        http://www.itu.dk/people/sestoft/mosml/gc_ctrl.c
        mosml.sh)
md5sums=('74aaaf988201fe92a9dbfbcb1e646f70'
         'ebb714a2d45889f4ecc796a9add40bd6'
         'e226cfe205477a1fb77caa9b8a44d3ee')

build() {
  cd "${srcdir}/${pkgname}/src/"

  if [ "$CARCH" = "x86_64" ]
    then cp "${srcdir}/gc_ctrl.c" "./runtime/"
  fi

  mkdir -p "${pkgdir}/opt/mosml"
  sed -i 's/^CPP=\/lib\/cpp/CPP=\/usr\/bin\/cpp/' Makefile.inc
  make MOSMLHOME="/opt/mosml" world
}

package() {
  cd "${srcdir}/${pkgname}/src"

  make MOSMLHOME="${pkgdir}/opt/mosml" install
  cp -r ../examples "${pkgdir}/opt/mosml/"
  cp -r ../copyrght "${pkgdir}/opt/mosml/"
  cd "${pkgdir}/opt/mosml/lib"
  ln -sf ../bin/camlrunm .
  install -D -m755 "${srcdir}/mosml.sh" "${pkgdir}/etc/profile.d/mosml.sh"
}

