# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=teg
pkgver=0.11.2
pkgrel=2
pkgdesc="Tenes Empanadas Graciela - A risk clone for Gnome 2"
arch=(i686 x86_64)
license=('GPL')
url="http://teg.sourceforge.net/"
depends=('libgnomeui>=2.18.1-2')
makedepends=('intltool' 'pkgconfig')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz" "${pkgname}.desktop")
md5sums=('1ec1ac9bd023b329b1e4a32bb48c9132'
         'bcea50cbf702aab96d4f6d5026265ec5')
sha256sums=('9a0135870c01ff70b63a95f7224556fdf70f21d977c369085ef3c2cf7697ddf9'
            'd55264406ed289d9ef7484bd2baae07563869f761b864fd75b295415c3629fd8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --with-readline
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  install -D -m 0644 ${srcdir}/teg.desktop ${pkgdir}/usr/share/applications/teg.desktop
  install -d  ${pkgdir}/usr/share/gconf/schemas

  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas

  # remove unused stuff
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/usr/share/gnome/apps/Games/teg.desktop
  rm -rf ${pkgdir}/opt/gnome/share/gnome/apps
}
