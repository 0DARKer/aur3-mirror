# Maintainer: birdflesh <antkoul at gmail dot com>

pkgname=trojita
pkgver=0.3.92
pkgrel=3
pkgdesc="A QT IMAP email client"
arch=('i686' 'x86_64')
url="http://trojita.flaska.net"
license=('GPL')
depends=('qtwebkit')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('8938d959789f0e20d724a511b265213c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/lconvert/lconvert-qt4/' src/po2qm.prf
  qmake-qt4 PREFIX=/usr CONFIG+=debug trojita.pro
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install
}
