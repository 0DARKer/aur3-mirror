# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libjpeg-turbo
pkgver=1.1.0
pkgrel=1
pkgdesc="A high-speed version of libjpeg for x86 and x86-64 processors which uses SIMD instructions"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('custom:wxWindows Library Licence')
makedepends=('nasm')
provides=('libjpeg=8.3.0')
conflicts=('libjpeg')
options=('!libtool')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('83e6914a281d649ad289445dc20f9de4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 LICENSE.txt $pkgdir/usr/share/licenses/libjpeg-turbo/LICENSE
}

