# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
pkgname=gnome-boxes-git
pkgver=20111022
pkgrel=1
pkgdesc="A simple GNOME 3 application to access remote or virtual systems"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
groups=()
depends=('vala-git' 'gtk3' 'clutter-gtk' 'spice-gtk3' 'libvirt-glib-git')
makedepends=('git')
install=gnome-boxes.install

_gitroot=git://git.gnome.org/gnome-boxes
_gitname=gnome-boxes

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
