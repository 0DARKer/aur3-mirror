# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
pkgname=gnome-boxes-git
pkgver=20111122
pkgrel=1
pkgdesc="A simple GNOME 3 application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('LGPL2.1')
depends=('gtk3' 'clutter-gtk' 'spice-gtk3-git' 'libvirt-glib-git'
         'gtk-vnc-git' 'udev' 'libosinfo' 'dconf' 'hicolor-icon-theme'
         'desktop-file-utils' 'shared-mime-info')
makedepends=('git' 'vala')
optdepends=('qemu-kvm-spice: to create new VMs')
provides=('gnome-boxes')
conflicts=('gnome-boxes')
install=gnome-boxes.install
source=('gtk-3.2-toolbar-fix.patch')
md5sums=('0dd09f031a13254a79526320d9bc36bb')

_gitroot=git://git.gnome.org/gnome-boxes
_gitname=gnome-boxes

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # Unbreak gnome-boxes with gtk+ 3.2
  # (gnome-boxes would require gtk+ commit 5cf0450c69f7bbdb0d1ca22ec54355e3b3e8c9c2)
  patch -p1 < "$srcdir/gtk-3.2-toolbar-fix.patch"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
