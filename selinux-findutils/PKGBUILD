# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)

pkgname=selinux-findutils
_origname=findutils
pkgver=4.4.2
pkgrel=3
pkgdesc="GNU utilities to locate files with Gentoo SELinux patch"
groups=('selinux' 'selinux-system-utilities')
arch=('i686' 'x86_64')
license=('GPL3')
depends=('glibc' 'sh' 'selinux-usr-libselinux')
conflicts=('findutils')
provides=('findutils=4.4.2')
url="http://www.gnu.org/software/findutils"
source=(ftp://ftp.gnu.org/pub/gnu/findutils/${_origname}-${pkgver}.tar.gz
	http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sys-apps/${_origname}/files/${_origname}-${pkgver}-selinux.diff)
install=findutils.install
md5sums=('351cc4adb07d54877fa15f75fb77d39f'
	 'fd69b760dc57f1e0059d4e7f7b872e12')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  # Apply SELinux patch
  patch -Np1 -i "${srcdir}/${_origname}-${pkgver}-selinux.diff" || return 1
  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/"{libexec,var}

  # remove locate utils (we use mlocate instead)
  rm -f "${pkgdir}/usr/bin/"{locate,updatedb}
  rm -f "${pkgdir}/usr/share/man/man1/"{updatedb.1,locate.1}
  rm -f "${pkgdir}/usr/share/man/man5/locatedb.5"
}
