# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=python2-flask-socketio
_pkgname=Flask-SocketIO
pkgver=0.5.0
pkgrel=1
pkgdesc="Socket.IO integration for Flask applications"
url="https://flask-socketio.readthedocs.org/"
arch=('any')
license=('MIT')
depends=('python2-gevent-websocket' 'python2-gevent-socketio')
makedepends=('python2-setuptools' 'python2-sphinx')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/Flask-SocketIO/archive/v${pkgver}.tar.gz)
sha512sums=('337a0c38e9314765c9e69f72f4a12b9b9893ea652a610a35fb97fd35bed3e299dfd7c265ba4779a24806eac51c3ea145f32fbfc8a73091aa1b6a0faa27e50946')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -ri 's|(sphinx-build)|\12|g' docs/Makefile
}

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build

  cd docs
  make man text
  #gzip -c _build/man/flask-socketio.1 > _build/man/${pkgname}.1.gz
}

check() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py test
}

package () {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
