# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=python2-flask-socketio
_pkgname=Flask-SocketIO
pkgver=0.3.8
pkgrel=2
pkgdesc="Socket.IO integration for Flask applications"
url="https://flask-socketio.readthedocs.org/"
arch=('any')
license=('MIT')
depends=('python2-gevent-websocket' 'python2-gevent-socketio')
makedepends=('python2-setuptools')
source=(https://github.com/miguelgrinberg/Flask-SocketIO/archive/v${pkgver}.tar.gz)
sha512sums=('9c115d108dfcb3bfdf21a7c82a3decdeab8fce4ce931a9d77af07eb91a2a55df4f95909e1783af35f8c99ba9f18926283abfcce8a95f6f9826a9bd9d502859da')

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py test
}

package () {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
