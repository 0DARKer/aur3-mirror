# Maintainer: mlq <mlq@pwmt.org>

pkgname=girara-gtk3-git
pkgver=20110717
pkgrel=1
pkgdesc="user interface library"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/libgirara"
license=('custom')
depends=('gtk3>=3.0.3')
makedepends=('git')

conflicts=('girara-gtk3')
provides=('girara-gtk3')

_gitroot='git://pwmt.org/girara.git'
_gitname='girara'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make GIRARA_GTK_VERSION=3 

  make GIRARA_GTK_VERSION=3 DESTDIR="$pkgdir/" install
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
