# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Dan Serban
# Contributor: Florian Richter <Florian_Richter@gmx.de>

pkgname=yacy
pkgver=1.0
_pkgextraver=20111127_8121
pkgrel=1
pkgdesc="Peer to peer search engine"
arch=('any')
url=http://www.yacy.net/
license=(GPL)
depends=(java-environment sudo)
makedepends=(apache-ant m4)
install=yacy.install
source=(http://www.yacy.net/release/yacy_v${pkgver}_${_pkgextraver}.tar.gz
        yacyInit-ArchLinux.patch)
options=(!emptydirs)
md5sums=('4be6fee592bb82411bde4804fbfa1256'
         '915495273780379b77e5bcdc11377a73')

build()
{
  . /etc/profile.d/apache-ant.sh

  cd "$srcdir/$pkgname"
  patch addon/yacyInit.m4 "$srcdir"/yacyInit-ArchLinux.patch
  ant all
}

package()
{
  cd "$srcdir/$pkgname"
  ant installonlinux -DDESTDIR="$pkgdir/"
  install -d "$pkgdir"/usr/share/java/yacy
  install -t "$pkgdir"/usr/share/java/yacy/ lib/*.jar
  m4 -DArchLinux "$srcdir/$pkgname"/addon/yacyInit.m4 > yacy.init
  install -Dm755 yacy.init "$pkgdir"/etc/rc.d/yacy
  rm -f "$pkgdir"/etc/yacy "$pkgdir"/usr/share/yacy/DATA "$pkgdir"/var/log/yacy
}
