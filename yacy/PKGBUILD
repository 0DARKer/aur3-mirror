# Maintainer: Andreas B. Wagner <AndreasBWagner@pointfree.net>
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Dan Serban
# Contributor: Florian Richter <Florian_Richter@gmx.de>

pkgname=yacy
pkgver=1.68
_pkgextraver=20140209_9000
pkgrel=1
pkgdesc="Peer to peer search engine"
arch=('any')
url=http://www.yacy.net/
license=('GPL')
depends=('java-environment' 'sudo' 'xorg-server' 'libcups')
makedepends=('apache-ant')
install=yacy.install
source=("http://www.yacy.net/release/yacy_v${pkgver}_${_pkgextraver}.tar.gz"
'yacyInit-ArchLinux.patch'
'yacy.sh'
'yacy.service')
options=(!emptydirs)
sha512sums=('d2878b40cd79a7aa1c73b50420df7f7c431ef3275497d6addc5e2b79db71fbefac58175607a11a70d0373a82cf6984ba8ba1763a58e2310967f44727b1ecea3f'
            '7cf58016c099fd63af8611a2618a562fd3a6e8b46507ca3c8b44a1ba4822ad8a7ff27bef6a431f4c6b1fa273aaa42dea41d91201cacbe6d07191fddf8eacec7c'
            'ba15bcb3ee1873bae57881ca4d96f65ee1fa5b40cff0f0c21c542cad6a7983a057ec83faae9452b426d5441aad02f80e3633d1798d7f0d84a680ed2233adb11f'
            '754b6224ef2a640cbfb50a3d46e92c04955dac3e683239872d2a5c9ba92016a1511c6049903322dd8a21e6d2cc51e2f4e9bd3339b4ef4aada68ae20b3acc189b')

build() {
	. /etc/profile.d/jdk.sh 2>/dev/null || . /etc/profile.d/openjdk6.sh
	. /etc/profile.d/apache-ant.sh

	cd "$srcdir/$pkgname"
	#  patch addon/yacyInit.m4 "$srcdir"/yacyInit-ArchLinux.patch
	# Fix build with non-unicode locales
	sed -i -e 's/<javac/& encoding="UTF-8"/' build.xml
	ant all
}

package() {
	cd "$srcdir/$pkgname"
	ant installonlinux -DDESTDIR="$pkgdir/"
	install -d "$pkgdir"/usr/share/java/yacy
	install -t "$pkgdir"/usr/share/java/yacy/ lib/*.jar
	#  m4 -DArchLinux "$srcdir/$pkgname"/addon/yacyInit.m4 > yacy.init
	#  install -Dm755 yacy.init "$pkgdir"/etc/rc.d/yacy

	install -Dm755 "${srcdir}/yacy.sh" "${pkgdir}/opt/yacy/yacy"
	install -Dm644 "${srcdir}/yacy.service" "${pkgdir}/usr/lib/systemd/system/yacy.service"

	rm -f "$pkgdir"/etc/yacy "$pkgdir"/usr/share/yacy/DATA "$pkgdir"/var/log/yacy
}

