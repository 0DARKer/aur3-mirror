# Maintainer: lolilolicon <lolilolicon[at]gmail.com>
# Contributor: oliparcol <oliparcol[at]gmail.com>

pkgname=ruby-redcloth
_gemname=RedCloth
pkgver=4.2.7
pkgrel=3
pkgdesc="Module for using the Textile markup language in Ruby."
provides=(redcloth)
replaces=(redcloth)
conflicts=(redcloth)
arch=(i686 x86_64)
url="http://redcloth.org/"
license=('MIT')
depends=('ruby')
source=(http://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=("$_gemname-$pkgver.gem")
md5sums=('939c0a4763c9612d94f99a6fd2e550e0')

build() {
  cd "$srcdir"

  # Temporary fix for RedCloth 4.2.7
  # Removing -Werror switch preventing compilation on Arch
  gem unpack "$_gemname-$pkgver.gem"
  cd "$_gemname-$pkgver"
  sed -i "s/ -Werror//g" tasks/compile.rake ext/redcloth_scan/extconf.rb
  gem build redcloth.gemspec

  local _gemdir=$(ruby -rubygems -e'puts Gem.default_dir')
  gem install --ignore-dependencies --verbose \
    -i "$pkgdir$_gemdir" -n "$pkgdir"/usr/bin "$_gemname-$pkgver.gem"
}
