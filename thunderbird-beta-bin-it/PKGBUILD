# Maintainer: Paolo Stivanin <admin AT polslinux DOT it>
# Based on [extra]'s thunderbird

pkgname=thunderbird-beta-bin-it
_pkgreal=thunderbird-beta-bin
pkgver=10.0b5
pkgrel=1
pkgdesc="Standalone Mail/News reader - Ultima beta disponibile in lingua italiana (file binari)."
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="http://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss' 'shared-mime-info' 'sqlite3' 'startup-notification')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
conflicts=('thunderbird-beta')
install=$_pkgreal.install
source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/it/thunderbird-$pkgver.tar.bz2
        $_pkgreal.desktop)
[ "$CARCH" = 'x86_64' ] && source[0]="ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-x86_64/it/thunderbird-$pkgver.tar.bz2"
md5sums=(`curl -s ${source/li*}/MD5SUMS | grep it | grep "$CARCH" | grep bz | cut -d " " -f1`
         'f8a9b91202e8c85ad7b8f23fb82a4d7b')

package() {
  install -d "$pkgdir"/{usr/bin,opt}
  cp -a thunderbird "$pkgdir/opt/$_pkgreal"
  ln -s /opt/$_pkgreal/thunderbird "$pkgdir/usr/bin/thunderbird-beta"
  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 thunderbird/chrome/icons/default/default${i/x*/}.png "$pkgdir/usr/share/icons/hicolor/$i/apps/thunderbird-beta.png"
  done
  install -Dm644 $_pkgreal.desktop "$pkgdir/usr/share/applications/$_pkgreal.desktop"
  rm -rf "$pkgdir/opt/$_pkgreal/dictionaries"
  ln -sf /usr/share/hunspell "$pkgdir/opt/$_pkgreal/dictionaries"
}
