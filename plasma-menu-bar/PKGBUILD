#CONTRIBUTOR: Zephyr

pkgname=plasma-menu-bar
pkgver=0.1.14
pkgrel=2
pkgdesc="Plasmoid to display menubar of application windows"
url="https://launchpad.net/plasma-widget-menubar"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kdebase-workspace>=4.6.0' 'appmenu-qt' 'qjson')
makedepends=('cmake' 'automoc4')
optdepends=('appmenu-gtk: Showing GTK+ Apps menus')
source=("http://launchpad.net/plasma-widget-menubar/trunk/${pkgver}/+download/plasma-widget-menubar-${pkgver}.tar.bz2"
        'shadow.patch')
sha1sums=('f41ec456dbe7083d5fd4255f6189b5e4da67121b'
          '4c34ac4d2c22d35504458a486f8d11e98d384f85')

build() {
  cd ${srcdir}/plasma-widget-menubar-${pkgver}

  #This Patch adds an emboss/shadow effect depending on
  #the Plasma theme used. It is provided by the packager
  #and NOT upstream. It should not create any problems
  #and if you do not like it, just comment (#) the
  #following line.
  patch -Np0 -i ${srcdir}/shadow.patch

  cmake -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix`
  make
}

package() {
  cd "${srcdir}/plasma-widget-menubar-${pkgver}"
  make DESTDIR=${pkgdir} install
}
# vim:syntax=sh
