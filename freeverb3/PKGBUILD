# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=freeverb3
pkgver=2.5.21
pkgrel=1
pkgdesc="A sound processing library which includes sampling rate scaling enabled version of freeverb with a lot of extensions."
arch=(i686 x86_64)
url="http://freeverb3.sourceforge.net/"
license=('GPL')
depends=('fftw' 'jack')
makedepends=('bmp' 'xmms')
optdepends=('bmp: beep media player plugin'
            'xmms: xmms plugin')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('cf99010a2ddf87600ebe58f292ad6ef1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove unsecure rpath
  sed -i "s|\(\${wl}\-rpath\)\( \${wl}\$libdir\)|\1-link\2|" configure

  # strip as-needed
  export LDFLAGS="${LDFLAGS/,--as-needed}"

  # add missing typedefs
  sed -e "59atypedef fv3::utils_ UTILS;" \
      -e "63atypedef fv3::utils_f UTILS;" \
      -i xmms/freeverb3.cpp

  ./configure --prefix=/usr \
              --enable-release \
              --enable-double \
              --enable-sample \
              --enable-jack \
              --enable-xmms \
              --enable-bmp \
              --enable-plugdouble \
              --disable-rpath
#              --enable-audacious # disabled broken
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
