# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=freeverb3
pkgver=2.5.15
pkgrel=1
pkgdesc="A sound processing library which includes sampling rate scaling enabled version of freeverb with a lot of extensions."
arch=(i686 x86_64)
url="http://freeverb3.sourceforge.net/"
license=('GPL')
depends=('fftw' 'jack')
makedepends=('bmp' 'xmms')
optdepends=('bmp: beep media player plugin'
            'xmms: xmms plugin')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('e66dcd52acf57b4830b00c480c0e51ed')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # do not work with audacious new api
  #sed -i "s|__AUDACIOUS_PLUGIN_API__|_AUD_PLUGIN_VERSION|g" \
  #  `grep -Erl "__AUDACIOUS_PLUGIN_API__" .`

  # remove unsecure rpath
  sed -i "s|\${wl}\-rpath \${wl}\$libdir||" configure

  ./configure --prefix=/usr \
              --enable-release \
              --enable-double \
              --enable-sample \
              --enable-jack \
              --enable-xmms \
              --enable-bmp \
              --enable-plugdouble \
              --disable-rpath
            #  --enable-audacious \ do not work
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
