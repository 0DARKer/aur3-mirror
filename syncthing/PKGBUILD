# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Sauyon Lee <sauyonl@sauyon.com>
# Contributor: Kevin MacMartin <prurigro@gmail.com>

pkgname=syncthing
pkgver=0.8.12
pkgrel=1
_pkgarch=amd64
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing"
url="http://syncthing.net/"
license=('MIT')
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
source=("https://github.com/calmh/${pkgname}/releases/download/v${pkgver}/${pkgname}-linux-amd64-v${pkgver}.tar.gz"
        "syncthing@.service"
        "syncthing.1")
sha1sums=('76bbc28ddf4ab9c2843978682ddc657ea3d15069'
          '204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')
install=${pkgname}.install

if [ "$CARCH" == "armv6h" ]; then
    _pkgarch="armv6"
    sha1sums[0]='38eb0b38b790fe4419dbe8784d0ae1524a1655f4'
    source[0]="https://github.com/calmh/${pkgname}/releases/download/v${pkgver}/${pkgname}-linux-armv6-v${pkgver}.tar.gz"
elif [ "$CARCH" == "armv7h" ]; then
    _pkgarch="armv7"
    sha1sums[0]='a1cd37c554370090b8506524cd1bc8fe8ca10e83'
    source[0]="https://github.com/calmh/${pkgname}/releases/download/v${pkgver}/${pkgname}-linux-armv7-v${pkgver}.tar.gz"
elif [ "$CARCH" == "i686" ]; then
    _pkgarch="386"
    sha1sums[0]='e4875eab1f3ed1bf6defd5e89cf5bf15d963bf59'
    source[0]="https://github.com/calmh/${pkgname}/releases/download/v${pkgver}/${pkgname}-linux-386-v${pkgver}.tar.gz"
fi

package() {
    cd "${srcdir}/${pkgname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -D -m 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
    install -D -m 644 "${srcdir}/syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "${srcdir}/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
