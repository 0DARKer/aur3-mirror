# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=syncthing
pkgver=0.8.4
pkgrel=1
_pkgarch=amd64
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing"
url="http://syncthing.net/"
license=('MIT')
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
source=("https://github.com/calmh/${pkgname}/releases/download/v${pkgver}/${pkgname}-linux-amd64-v${pkgver}.tar.gz"
        "syncthing@.service")
sha1sums=('3921bddf0d221022b1a37c565a708217780fc514'
          '619d71df8fdbd4b3c4c25112a3614ca0292226d4')
install=${pkgname}.install

if [ "$CARCH" == "armv6h" ]; then
    _pkgarch="armv6"
    sha1sums[0]='d8ad1a929b5754816db7cef5aa6283dc81f1245f'
    source[0]="https://github.com/calmh/${pkgname}/releases/download/v${pkgver}/${pkgname}-linux-armv6-v${pkgver}.tar.gz"
elif [ "$CARCH" == "armv7h" ]; then
    _pkgarch="armv7"
    sha1sums[0]='507e086f21a65d7a5cc66bc6b0f87f7efd9ed208'
    source[0]="https://github.com/calmh/${pkgname}/releases/download/v${pkgver}/${pkgname}-linux-armv7-v${pkgver}.tar.gz"
elif [ "$CARCH" == "i686" ]; then
    _pkgarch="386"
    sha1sums[0]='81dd1042d85fe2bcac5f8831ab1c0dbc81a02246'
    source[0]="https://github.com/calmh/${pkgname}/releases/download/v${pkgver}/${pkgname}-linux-386-v${pkgver}.tar.gz"
fi

package() {
    cd "${srcdir}/${pkgname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -D -m 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
    install -D -m 644 "${srcdir}/syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
}
