# Maintainer: Dennis Fink <the_metalgamer@hackerspace.lu>

pkgname=python-acrylamid-git
pkgver=0.7.47.g78b46a1
pkgrel=1
pkgdesc="static blog generator in python with incremental rendering."
arch=('any')
url="http://posativ.org/acrylamid/"
license=('BSD')
depends=('python-jinja>=2.4' 'python-markdown>=2.0.1' 'python-unidecode')
makedepends=('python-distribute' 'git')
optdepends=('python-pygments: Syntax Highlighting'
'python-docutils: reStructuredText'
'python-textile: Textile'
'python-yaml: exact YAML parser'
'python-twitter-tools: Twitter'
'discount: Discount'
'python-mako: Mako Templating'
)
source=('git://github.com/posativ/acrylamid.git')
provides=('python-acrylamid')
conflicts=('python-acrylamid')
md5sums=('SKIP')

_gitname=acrylamid

pkgver() {
  cd "$srcdir"/$_gitname
  git describe --always | sed 's|-|.|g'
}

build() {

  cd "$srcdir"/$_gitname
  python setup.py build

}

package() {

  cd "$srcdir"/$_gitname

  python setup.py install --root="$pkgdir" --optimize=1

  install -D -m644 "$srcdir"/$_gitname/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

}

# vim:set ts=2 sw=2 et:
