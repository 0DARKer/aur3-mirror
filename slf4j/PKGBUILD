# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua

pkgname=slf4j
pkgver=1.6.4
pkgrel=1
pkgdesc="A successor to the log4j project."
arch=('i686' 'x86_64')
url="http://www.slf4j.org"
license=('MIT')
depends=('java-environment')
optdepends=('log4j')
source=(${url}/dist/${pkgname}-${pkgver}.tar.gz setClasspath.sh migrator.desktop)

package() {
	mkdir -p ${pkgdir}/usr/share/java/${pkgname}

	cd ${srcdir}
	install -D -m644 migrator.desktop ${pkgdir}/usr/share/applications/migrator.desktop || return 1
	install -m644 setClasspath.sh ${pkgdir}/usr/share/java/${pkgname} || return 1
	cd ${pkgname}-${pkgver}
	install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt || return 1

	# jars
	for i in jcl-over jul-to log4j-over; do
		install -m644 ${i}-${pkgname}-${pkgver}.jar ${pkgdir}/usr/share/java/${pkgname} || return 1
	done
	for i in api ext jcl jdk14 log4j12 migrator nop simple; do
		install -m644 ${pkgname}-${i}-${pkgver}.jar ${pkgdir}/usr/share/java/${pkgname} || return 1
	done
	for i in ${pkgdir}/usr/share/java/${pkgname}/*.jar; do
		_filename=${i##*\/}
		ln -s ${_filename} ${pkgdir}/usr/share/java/${pkgname}/${_filename/-${pkgver}/} || return 1
	done
}

md5sums=('fb5f588a2b2bf3d10f5d90dcce1c85e0'
         '30637bebdbd56f4800072e2c931e13d1'
         'f3c2acb8d560c27c0a1ec78e27c05b44')
sha256sums=('67bc98b28b8d84f0104f235eda0756fe91c747e599332ab61593b30ec477ad78'
            '9a1efcd3b99c0ef88e1fbdf361a7832f09a925802a7518629f271ff8e08ae940'
            'c81832da83a0de1de2a7169ffb1432f6e2d3cfb9c3c8a1d50baf8e902457f8cc')
