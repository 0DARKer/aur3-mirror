# Maintainer: Salan54 <salan at fremenil dot com>
# Created: 2013-03-16
pkgname=zulucrypt
pkgver=4.6.2
pkgrel=1
pkgdesc="a cli and gui frontend to cryptsetup."
url="http://code.google.com/p/zulucrypt/"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('cryptsetup' 'qt4' 'libpwquality' 'libgnome-keyring')
optdepends=('kdeutils-kwallet: retrieve volume keys from kde kwallet')
conflicts=('zulucrypt-git')
makedepends=('cmake')
options=('!buildflags')
source=("http://zulucrypt.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('43788cc220f3771435df078f988c93e0')
changelog=${pkgname}.changelog
install=${pkgname}.install

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DNOKDE=true -DCMAKE_BUILD_TYPE=release . ..
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cd build
  make DESTDIR="$pkgdir" install
}
 
# vim:set ts=2 sw=2 et:

