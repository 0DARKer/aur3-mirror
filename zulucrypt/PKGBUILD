# Maintainer: Salan54 <salan at fremenil dot com>
# Created: 2013-03-17
pkgname=zulucrypt
pkgver=4.6.3
pkgrel=1
_altpkgname=zuluCrypt
pkgdesc="a cli and gui frontend to cryptsetup."
url="http://code.google.com/p/zulucrypt/"
arch=('x86_64' 'i686')
license=('GPL')
depends=('cryptsetup' 'qt4' 'libpwquality' 'libgnome-keyring')
optdepends=('kdeutils-kwallet: retrieve volume keys from kde kwallet')
conflicts=('zulucrypt-git')
makedepends=('cmake')
options=('!buildflags')
source=("http://zulucrypt.googlecode.com/files/${_altpkgname}-${pkgver}.tar.bz2")
md5sums=('47924f927e5218aae7ac723ff7bd9b39')
changelog=${pkgname}.changelog
install=${pkgname}.install

build() {
  cd "${srcdir}/${_altpkgname}-${pkgver}"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DNOKDE=true -DCMAKE_BUILD_TYPE=release . ..
  make
}

package() {
  cd "${srcdir}/${_altpkgname}-${pkgver}"
  cd build
  make DESTDIR="$pkgdir" install
}
 
# vim:set ts=2 sw=2 et:

