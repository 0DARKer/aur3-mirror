# $Id: PKGBUILD 65574 2012-02-21 09:02:39Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Allan Henriksen <allan.henriksen@gmail.com>

pkgname=ipsec-tools-wild-char
pkgver=0.8.0
pkgrel=3
pkgdesc="KAME IPSec tools ported to Linux"
arch=('i686' 'x86_64')
url="http://ipsec-tools.sourceforge.net/"
depends=('readline' 'openssl' 'krb5')
makedepends=('linux-headers')
license=('GPL')
options=('!makeflags' '!libtool')
conflicts=('ipsec-tools')
relname=ipsec-tools
source=(http://downloads.sourceforge.net/sourceforge/ipsec-tools/$relname-$pkgver.tar.bz2
	racoon.rc
	ipsec.rc
	wild-char.patch)
md5sums=('b79aae3055a51f8de5c0f1b8ca6cf619'
         '416b8e362d86987b8c55f7153cdafbeb'
         '90d0810267cbd847383ae3101699b192'
		 '3360bedf07793382a04ece37b55b4f68')

build() {
  cd $srcdir/$relname-$pkgver
  patch -Np1 -i ../wild-char.patch
  sed -i 's#-Werror##' configure
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--enable-security-context=no --enable-hybrid --enable-dpd --enable-natt \
	--enable-adminport --enable-gssapi
  make
}

package() {
  cd $srcdir/$relname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm0755 $srcdir/racoon.rc $pkgdir/etc/rc.d/racoon
  install -Dm0755 $srcdir/ipsec.rc $pkgdir/etc/rc.d/ipsec
}
