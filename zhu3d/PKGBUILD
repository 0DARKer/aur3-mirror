# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>
# Contributor: Ray Kohler <ataraxia937@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=zhu3d
pkgver=4.2.2
pkgrel=1
pkgdesc="View, animate, and solve up to three functions in 3D-space in an interactive manner"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php?content=43071"
license=('GPL3')
depends=('libgl' 'qt>=4.4.0')
options=('docs')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz" \
        "${pkgname}.desktop")
md5sums=('a37c23ea8b548f05f342486f711e45ba'
         '48770d40bf1d9dd8f420365f2b0238cf')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  
  # Redefine the locations of the default directories
  sed -e 's|SYSDIR=|SYSDIR=/usr/share/zhu3d/system|g; s|DOCDIR=|DOCDIR=/usr/share/zhu3d/doc|g' \
      -e 's|WORKDIR=|WORKDIR=/usr/share/zhu3d/work|g; s|TEXDIR=|TEXDIR=/usr/share/zhu3d/work/textures|g' \
      -i zhu3d.pri

  /usr/bin/qmake
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  
  # Install data files
  mkdir -p "${pkgdir}/usr/share/zhu3d"
  mv {system,work,doc} "${pkgdir}/usr/share/zhu3d/"

  # Install binary
  install -D -m755 zhu3d "${pkgdir}/usr/bin/zhu3d"

  # Install shortcut
  install -D -m644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Fix file permissions
  find "${pkgdir}/usr/share/${pkgname}" -type f -exec chmod 644 "{}" +
}
