# Maintainer: Boohbah <boohbah at gmail.com>
# Contributor: Zach Latta <zach@zachlatta.com>

pkgname=fleet-git
pkgver=0.1.3.r503.g2c69f66
pkgrel=1
pkgdesc='A distributed init system (git version)'
arch=('i686' 'x86_64')
url='https://github.com/coreos/fleet'
license=('Apache')
makedepends=('git' 'go')
provides=('fleet')
conflicts=('fleet')
backup=('etc/fleet/fleet.conf')
source=('git://github.com/coreos/fleet.git'
        'fleet.service')
md5sums=('SKIP'
         'b0490f8a028951c09d8c0aae31784cad')

pkgver() {
  cd fleet
  git describe --long | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd fleet
  ./build
}

package() {
  cd fleet
  install -Dm644 fleet.conf.sample "$pkgdir/etc/fleet/fleet.conf"
  install -Dm755 bin/fleet "$pkgdir/usr/bin/fleet"
  install -Dm755 bin/fleetctl "$pkgdir/usr/bin/fleetctl"

  # Install unit file from coreos
  install -Dm644 "$srcdir/fleet.service" "$pkgdir/usr/lib/systemd/system/fleet.service"
}
