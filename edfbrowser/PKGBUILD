# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=edfbrowser
pkgver=1.41
pkgrel=1
pkgdesc="A viewer and toolbox for timeseries storage files like EEG, EMG, ECG, BioImpedance, etc."
arch=(i686 x86_64)
url="http://www.teuniz.net/edfbrowser/"
license=('GPL')
depends=('qt')
install="$pkgname.install"
source=("$url${pkgname}_${pkgver/.}_source.tar.gz"
	"$pkgname.desktop")
md5sums=('1edcf9b3b63ebb94041b5413cd71b209'
         '15ee52eb704bf15498326a8ab29e2c91')

build() {
  cd "$srcdir/${pkgname}_${pkgver/.}_source"

  qmake

  # FORTIFY_SOURCE causes internal
  # compiler error in fidlib
  make CFLAGS="${CFLAGS/-D_FORTIFY_SOURCE=2}"
}

package() {
  cd "$srcdir/${pkgname}_${pkgver/.}_source"

  # bin
  install -Dm 755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop
  install -Dm 644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm 644 images/edf.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
