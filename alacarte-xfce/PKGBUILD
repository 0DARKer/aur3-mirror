# Maintainer: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: 3ED <krzysztof1987 at googlemail>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: pressh <pressh@gmail.com>

pkgname=alacarte-xfce
_realname=${pkgname/-xfce/}
pkgver=3.5.4
pkgrel=2
pkgdesc="Menu editor for Xfce (with debian patchset)"
arch=(any)
license=('LGPL')
url="http://www.gnome.org"
depends=('gnome-menus' 'exo' 'hicolor-icon-theme' 'pygtk')
makedepends=('intltool')
conflicts=('alacarte' 'alacarte-git' 'alacarte-xfce-devel')
install=alacarte.install
options=('!libtool')
groups=('xfce4-goodies')
source=(http://ftp.gnome.org/pub/gnome/sources/${_realname}/3.5/${_realname}-${pkgver}.tar.xz
        09-configure-gnome_menus-3.5.3_to_3.4.patch)
md5sums=('760dbe2941866cebb9a1d8e5fbd983a1'
         '3a8d1b1488d77c83f6d65fd4f9d0048c')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i ../09-configure-gnome_menus-3.5.3_to_3.4.patch
  sed -e 's/gnome-desktop-item-edit/exo-desktop-item-edit/g' -i Alacarte/MainWindow.py
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
