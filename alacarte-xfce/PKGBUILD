# Maintainer: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: 3ED <krzysztof1987 at googlemail>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: pressh <pressh@gmail.com>

pkgname=alacarte-xfce
_realname=${pkgname/-xfce/}
pkgver=3.7.3
pkgrel=4
pkgdesc="Menu editor for Xfce (with debian patchset)"
arch=(any)
license=('LGPL')
url="http://www.gnome.org"
depends=('gnome-menus' 'exo' 'hicolor-icon-theme' 'pygtk' 'pyxml')
makedepends=('intltool')
conflicts=('alacarte' 'alacarte-git' 'alacarte-xfce-devel')
install=alacarte.install
options=('!libtool')
groups=('xfce4-goodies')
source=(http://ftp.gnome.org/pub/gnome/sources/alacarte/3.7/alacarte-${pkgver}.tar.xz toprettyxml-and-unicode-fixes.patch alacarte.desktop)
md5sums=('59e1e9041400e57a77e896460555067a' '4bb6b558687f6ffde356758a6c4a7c28' 'b927a3927560037c87dab73ef2d3d38b')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -p1 -i $srcdir/toprettyxml-and-unicode-fixes.patch
  sed -e 's/gnome-desktop-item-edit/exo-desktop-item-edit/g' -i Alacarte/MainWindow.py
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D "../../alacarte.desktop" "${pkgdir}/usr/share/applications/alacarte.desktop"
}
