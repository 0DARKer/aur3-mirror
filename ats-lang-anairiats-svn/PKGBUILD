_pkgname=ats-lang-anairiats
pkgname=ats-lang-anairiats-svn
pkgver=3377
pkgrel=1 
pkgdesc="ATS (Applied Type System) is a programming language which supports theorem proving in combination with practical programming through the use of an advanced type system."
url="http://www.ats-lang.org"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gmp')
makedepends=('subversion')
optdepends=(
  'cairo' 'blas' 'lapack' 'curl' 'clutter'
  'fuse' 'gtkglext' 'libgl' 'glib' 'graphviz'
  'gsl' 'jansson' 'pango' 'libmariadbclient'
  'sdl' 'xorg-server-devel' 'zeromq'
)

conflicts=($_pkgname)
provides=($_pkgname)

_svntrunk='https://svn.code.sf.net/p/ats-lang/code/trunk'
_svnmod=$_pkgname
source=("${_svnmod}::svn+$_svntrunk")
md5sums=('SKIP')

pkgver() {
  cd "$_svnmod"
  svnversion | tr -d "[A-z]"
}

build() {
  cd "$_svnmod"
  aclocal
  automake --add-missing --foreign || true
  autoconf
  touch config.mk config.h
  ./configure --prefix="/usr"
  make all
  make contrib
}

package() {
  cd $_svnmod
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
