# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
pkgname=linux-tools
pkgver=3.0
pkgrel=1
pkgdesc="Tools embedded with Linux kernel $pkgver (e.g: perf)"
license=('GPL2')
arch=('i686' 'x86_64')
url='http://www.kernel.org'
options=(!strip)
depends=('python2' 'perl')
makedepends=('asciidoc' 'xmlto')
optdepends=('libnewt' 'slang')
source=( "http://ftp.kernel.org/pub/linux/kernel/v3.0/linux-$pkgver.tar.bz2" ) 
md5sums=('398e95866794def22b12dfbc15ce89c0')

build() {
  # firewire
  msg2 'nosy-dump'
  cd "${srcdir}/linux-$pkgver/tools/firewire"
  make

  # slabinfo
  msg2 'slabinfo'
  cd "${srcdir}/linux-$pkgver/tools/slub"
  gcc -Wall -O2 -g slabinfo.c -o slabinfo
  
  # usb
  msg2 'testusb'
  cd "${srcdir}/linux-$pkgver/tools/usb"
  make

  # perf
  msg2 'perf'
  cd "${srcdir}/linux-$pkgver/tools/perf"
  make PYTHON=python2 DESTDIR="${pkgdir}/usr" all man
}

package() {
  # firewire
  msg2 'nosy-dump'
  cd "${srcdir}/linux-$pkgver/tools/firewire"
  install -m755 -D nosy-dump ${pkgdir}/usr/bin/nosy-dump

  # slabinfo
  msg2 'slabinfo'
  cd "${srcdir}/linux-$pkgver/tools/slub"
  install -m755 -D slabinfo ${pkgdir}/usr/bin/slabinfo

  # usb
  msg2 'testusb'
  cd "${srcdir}/linux-$pkgver/tools/usb"
  install -m755 -D testusb ${pkgdir}/usr/bin/testusb

  # perf
  msg2 'perf'
  cd "${srcdir}/linux-${pkgver}/tools/perf"
  make PYTHON=python2 DESTDIR="${pkgdir}/usr" install install-man
}
# vim:set ts=2 sw=2 ft=sh et:
