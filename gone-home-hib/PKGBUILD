# Maintainer:  ainola <opp310@alh.rqh> (ROT13)

pkgname=gone-home-hib
pkgver=1.1
pkgrel=1
pkgdesc="Gone home is an interactive exploration simulator by Fullbright."
arch=('i686' 'x86_64')
url="http://www.gonehomegame.com/"
license=('custom: commercial')
depends=('glu' 'libxext' 'libxcursor')
source=("hib://GoneHome_v${pkgver}.tar.gz"
        "${pkgname}.desktop"
        "${pkgname}.install")
sha256sums=("ecd01fcde184f7d1937579e7d5d4a791007887b88f9baa10039747166bb2d097"
            "dea74a9436b2b37a63265c0606195293cb751855e2de473e678ab85600c92053"
            "ea11e510a7c52e485cf1e87eb985691029b62fc694830a0c57f14b4173ac8edd")
install=("${pkgname}.install")
# Prevent compressing final package
PKGEXT='.pkg.tar'

# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download the file manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf"; echo "Read this PKGBUILD for more info."; exit 1')


package() {
    # Launcher and Data
    install -Dm755 "${srcdir}/GoneHome.${CARCH}" "${pkgdir}/opt/${pkgname}/GoneHome.${CARCH}"
    mv "${srcdir}/GoneHome_Data" "${srcdir}/"*.txt "${pkgdir}/opt/${pkgname}/"

    # Fill in the line with proper architecture
    sed -i "s/FOO/GoneHome.${CARCH}/" "${pkgname}.desktop"

    # Install Binaries/Launchers
    mkdir -p "$pkgdir/usr/bin"
    ln -s "/opt/${pkgname}/GoneHome.${CARCH}"  "${pkgdir}/usr/bin/${pkgname}"

    # Desktop Integration
    mkdir -p "$pkgdir/usr/share/pixmaps"
    ln -s "/opt/${pkgname}/GoneHome_Data/Resources/UnityPlayer.png" \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    # Fix wonky permissions
    find "${pkgdir}/opt/${pkgname}" -type d -exec chmod 755 {} \;
    find "${pkgdir}/opt/${pkgname}" -type f -exec chmod 644 {} \;
    chmod 755 "${pkgdir}/opt/${pkgname}/GoneHome.${CARCH}"
}
