# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>

_p=muse
pkgname=${_p}2
pkgver=2.0rc1
pkgrel=1
pkgdesc="A MIDI/Audio sequencer with recording and editing capabilities"
arch=('i686' 'x86_64')
url="http://muse-sequencer.org/"
license=('GPL')
depends=('qt' 'fluidsynth' 'dssi' 'lash')
makedepends=('cmake' 'doxygen' 'graphviz')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/lmuse/$_p-$pkgver.tar.gz")
md5sums=('43948df553bc35748cb87f75576ecceb')

build() {
  cd "$srcdir/$_p-$pkgver"

  # build dir
  [ -d bld ] || mkdir bld && cd bld

  cmake -DCMAKE_BUILD_TYPE=release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_FLUID=1 \
        -DENABLE_DSSI=1 \
        -DENABLE_LASH=1 \
        -DENABLE_OSC=1 ..
  make
}

package() {
  cd "$srcdir/$_p-$pkgver/bld"

  make DESTDIR="$pkgdir" install

  # prevent conflicts with Muse qt3 ..
  cd "$pkgdir/usr/share/applications"
  mv $_p.desktop $pkgname.desktop
  sed -i "s|sE|& 2|" $pkgname.desktop

  # .. and oomidi grepmidi bin
  cd "$pkgdir/usr/bin"
  mv grepmidi $pkgname-grepmidi

  # grepmidi man
  cd "$pkgdir/usr/share/man/man1"
  mv grepmidi.1 $pkgname-grepmidi.1

  # python2 Fixes
  sed -i "s/\#\!.*python/&2/" \
    `grep -rl "\#\!.*python" "$pkgdir"`
}
