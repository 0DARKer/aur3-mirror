# Maintainer: Michael Bitches <noodlylight@gmail.com>
pkgname=fusilli-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Fusilli Window Manager"
arch=('i686' 'x86_64')
url="https://github.com/noodlylight/fusilli"
license=('GPL' 'LGPL' 'MIT')
install="fusilli.install"

depends=('libgl' 'glu' 'librsvg' 'libsm' 'libxslt' 'python2' 'pygtk' 'libwnck' 
         'desktop-file-utils' 'libpng' 'libxrandr' 'libxml2' 
         'startup-notification' 'libxcomposite' 'libxinerama')
makedepends=('intltool' 'git' 'gcc')

source=("$pkgname::git+https://github.com/noodlylight/fusilli.git")
md5sums=(SKIP)

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --long --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$pkgname"
	./autogen.sh \
		--prefix=/usr \
		--enable-glib \
		--enable-dbus-glib \
		--enable-inotify \
		--enable-annotate \
		--enable-librsvg \
		--enable-gtk \
		--disable-metacity \
		--disable-gnome \
		--disable-gnome-keybindings \
		--disable-kde4 \
		--enable-fsm
	make
}

package() {
	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/$pkgname"/COPYING.MIT "$pkgdir"/usr/share/licenses/$pkgname/COPYING.MIT
}
