# Maintainer : Maximilien Noal <noal dot maximilien at gmail dot com>
# Based on nvidia-ck by graysky <graysky AT archlnux.us>

pkgname=nvidia-96xx-ck
pkgver=96.43.23
pkgrel=12
pkgdesc="NVIDIA drivers, 96xx branch, for linux-ck."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686" ] && ARCH=x86 && _nv=0
[ "$CARCH" = "x86_64" ] && ARCH=x86_64 && _nv=2
depends=('linux-ck')
makedepends=('linux-ck-headers')
provides=('nvidia-96xx')
url="http://www.nvidia.com/"
optdepends=("nvidia-96xx-utils>=${pkgver}: driver utilities, 96xx branch"
            'pangox-compat: to run nvidia-settings'
            'xorg-server1.12: latest compatible Xorg server')
conflicts=('nvidia-ck' 'nvidia-173xx-all' 'nvidia-275xx-ck' \
  'nvidia-304xx-ck' 'nvidia-96xx-all' 'nvidia-all' 'nvidia-beta-ck')
license=('custom')
install='nvidia.install'
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg${_nv}.run" \
  "173.14.36-37.patch")
options=(!strip)
md5sums=('ca0bc6ae3b37cb259f3a906b4dc4670b' \
  '944ed806c8d0f9174d5e9e16ae065bf6')
[ "$CARCH" = "x86_64" ] && md5sums[0]='dd8546e5ae7d10da072306e5f13952b8'

_extramodules=$(find /usr/lib/modules/*-ck/ -name modules.alias.bin -not \
  -type d|head -1|sed 's/.*\/lib\/modules\/\(.*\)\/modules.alias.bin/\1/g'\
  | awk -F. '{ print $1 "." $2 }')
_kernver=$(cat /usr/lib/modules/extramodules-${_extramodules}-ck/version)

build() {
  msg "kernver: $_kernver"
  msg "extramodules: $_extramodules"
  msg "Building ${pkgname}..."
  msg "Making module for linux ${_kernver} :"
  msg "1 of 3 - Extracting nvidia drivers..."
  cd $srcdir
  sh NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}.run --extract-only
  cd NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}/usr/src/nv
  msg "2 of 3 - Applying 173.14.36-37.patch..."
  patch -p1 -i $srcdir/173.14.36-37.patch
  msg "3 of 3 - Building module for ${_kernver}..."
  make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
  msg "Packaging ${pkgname}..."
  mkdir -p $pkgdir/lib/modules/${_kernver}/kernel/drivers/video
  install -m644 \
    $srcdir/NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}/usr/src/nv/nvidia.ko \
    $pkgdir/lib/modules/${_kernver}/kernel/drivers/video
  mkdir -p $pkgdir/usr/share/licenses/${pkgname}
  install -m644 $srcdir/NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}/LICENSE \
    $pkgdir/usr/share/licenses/${pkgname}
  if [ ! -f /etc/modprobe.d/nouveau_blacklist.conf ]; then
    mkdir -p $pkgdir/etc/modprobe.d
    echo "blacklist nouveau" >> \
      $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
  fi
}
