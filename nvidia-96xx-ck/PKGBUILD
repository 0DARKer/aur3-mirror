# Maintainer : Maximilien Noal <noal dot maximilien at gmail dot com>
# Based on nvidia-ck by graysky <graysky AT archlnux.us>

pkgname=nvidia-96xx-ck
pkgver=96.43.23
pkgrel=3
pkgdesc="NVIDIA drivers, 96xx branch, for linux-ck."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686" ] && ARCH=x86 && _nv=0
[ "$CARCH" = "x86_64" ] && ARCH=x86_64 && _nv=2
depends=('linux-ck')
makedepends=('linux-ck-headers')
provides=('nvidia-96xx')
url="http://www.nvidia.com/"
optdepends=("nvidia-96xx-utils>=${pkgver}: NVIDIA drivers utilities and libraries, legacy 96xx branch"
            'pangox-compat: to run nvidia-settings'
            'xorg-server1.12: latest compatible Xorg server'
            'xf86-input-evdev1.12: needed by xorg-server1.12')
conflicts=('nvidia-71xx' 'nvidia-96xx' 'nvidia-173xx' 'nvidia-legacy' 'nvidia-beta' 'nvidia')
license=('custom')
install="nvidia.install"
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg${_nv}.run" "linux-3.7-gentoo.patch" "linux-3.8-gentoo.patch")
options=(!strip)
md5sums=('ca0bc6ae3b37cb259f3a906b4dc4670b' '4cddc386cdf2c22c81b12f9b7ffdb04b' '68c4a350e8bfdd4a3f912dc01791718f')
[ "$CARCH" = "x86_64" ] && md5sums[0]='dd8546e5ae7d10da072306e5f13952b8'

_extramodules=extramodules-3.9-ck
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

build() {
  msg "Building ${pkgname}..."
  msg "Making module for linux $_kernver :"
  msg "1 of 5 - Extracting nvidia drivers..."
  cd $srcdir
  sh NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}.run --extract-only
  cd NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}/usr/src/nv
  msg "2 of 5 - Applying linux-3.7-gentoo.patch..."
  patch -p3 -i $srcdir/linux-3.7-gentoo.patch
  msg "3 of 5 - Applying linux-3.8-gentoo.patch..."
  patch -p3 -i $srcdir/linux-3.8-gentoo.patch conftest.sh
  msg "4 of 5 - Building module for ${_kernver}..."
  make SYSSRC=/lib/modules/${_kernver}/build module
  msg "5 of 5 - Making preparations for packaging..."
  mv nvidia.ko $srcdir/nvidia-${_kernver}.ko
  mv -f $srcdir/NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}/LICENSE $srcdir/LICENSE
  rm -rf $srcdir/NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}
}

package() {
  msg "Packaging ${pkgname}..."
  mkdir -p $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/
  install -m644 $srcdir/nvidia-${_kernver}.ko $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
  mkdir -p $pkgdir/usr/share/licenses/${pkgname}/
  install -m644 $srcdir/LICENSE $pkgdir/usr/share/licenses/${pkgname}/
  mkdir -p $pkgdir/etc/modprobe.d
  echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
}
