# Maintainer: 3ED_0 <krzysztof1987 at gmail>
# Contributor: City-busz <AUR>
# Contributor: Maximilien Noal <noal dot maximilien at gmail dot com>
# Contributor: graysky <graysky AT archlnux.us>

pkgname=nvidia-96xx-ck
pkgver=96.43.23
pkgrel=13
pkgdesc="NVIDIA drivers, 96xx branch, for linux-ck."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686" ] && ARCH=x86 && _nv=0
[ "$CARCH" = "x86_64" ] && ARCH=x86_64 && _nv=2
depends=('linux-ck')
makedepends=('linux-ck-headers')
provides=('nvidia-96xx')
url="http://www.nvidia.com/"
optdepends=("nvidia-96xx-utils>=${pkgver}: driver utilities, 96xx branch"
            'pangox-compat: to run nvidia-settings'
            'xorg-server1.12: latest compatible Xorg server')
conflicts=('nvidia-ck' 'nvidia-173xx-all' 'nvidia-275xx-ck' \
  'nvidia-304xx-ck' 'nvidia-96xx-all' 'nvidia-all' 'nvidia-beta-ck')
license=('custom')
install='nvidia.install'
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg${_nv}.run"
  "173.14.36-37.patch"
  "fix-build-with-linux-3.10.patch")
options=(!strip)
md5sums=('ca0bc6ae3b37cb259f3a906b4dc4670b'
         '944ed806c8d0f9174d5e9e16ae065bf6'
         'edda124f64772b87314945eb95abfa24')

[ "$CARCH" = "x86_64" ] && md5sums[0]='dd8546e5ae7d10da072306e5f13952b8'

_extramodules=$(cd /usr/lib/modules/ && echo extramodules-[0-9.]*-ck|cut -d\  -f1)
_kernver=$(cat /usr/lib/modules/${_extramodules}/version)

prepare() {
  test ! -d NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv} \
    || rm -rf NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}

  sh NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}.run --extract-only

  cd NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}/usr/src/nv
  patch -p1 -i $srcdir/173.14.36-37.patch
  patch -p4 -i $srcdir/fix-build-with-linux-3.10.patch

  sed -i -e "s/_extramodules='.*'/_extramodules='${_extramodules}'/" "$startdir/nvidia.install"
}

build() {
  cd "$srcdir/NVIDIA-Linux-${ARCH}-${pkgver}-pkg${_nv}/usr/src/nv"
  make SYSSRC=/usr/lib/modules/${_kernver}/build module
}

package() {
  cd "$srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg${_nv}/usr/src/nv/"
  install -dm755 "$pkgdir/usr/lib/modules/${_extramodules}/"
  install -dm755 "$pkgdir/etc/modprobe.d"
  install -m 644 nvidia.ko "$pkgdir/usr/lib/modules/${_extramodules}/"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"

  cd "$srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg${_nv}"
  install -dm755 "$pkgdir/usr/share/licenses/${pkgname}/"
  install -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/"
}
