# Maintainer: Prurigro

_pkgname=gitsync
pkgname=${_pkgname}-git
pkgver=20131011.r35.296e001
pkgrel=1
pkgdesc="Git repository syncronisation daemon"
url="https://github.com/raybejjani/${_pkgname}"
license=('BSD')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
depends=('glibc')
makedepends=('git' 'go' 'make' 'python2')
source=("git://github.com/raybejjani/${_pkgname}.git"
        "git://github.com/ngmoco/timber.git"
        "hg+https://code.google.com/p/go.net/"
        "${_pkgname}.1.gz")
sha512sums=('SKIP' 'SKIP' 'SKIP' 'ab8d184f5859dfc78aa42d71576a14bb527386ddf3973215f7b91c459feb263772ecb3aae38451c36925d3f48f1ee5fe815cb67a90d675a70b8b8a4cf54c48f1')

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    sed -i 's|/usr/bin/env python|/usr/bin/env python2|g' ${_pkgname}/util/make_code_fs.py

    install -d ${_pkgname}/src/code.google.com/p/
    ln -s "${srcdir}/go.net" "${_pkgname}/src/code.google.com/p/go.net"

    install -d ${_pkgname}/src/github.com/ngmoco/
    ln -s "${srcdir}/timber" "${_pkgname}/src/github.com/ngmoco/timber"
}

build() {
    cd $_pkgname
    make
}

package() {
    install -Dm644 ${_pkgname}.1.gz "${pkgdir}/usr/share/man/man1/${_pkgname}.1.gz"
    install -Dm644 ${_pkgname}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm755 ${_pkgname}/bin/${_pkgname}d "${pkgdir}/usr/bin/${_pkgname}d"
}
