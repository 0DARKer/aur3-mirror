# Maintainer: Christoph Wiechert <wio@psitrax.de>
pkgname=sflphone-kde
pkgver=1.2.3
pkgrel=1
pkgdesc="SIP/IAX2 softphone"
url="http://www.sflphone.org"
arch=('x86_64' 'i686')
license=('GPL3')
depends=('kdelibs' 'libpulse' 'libzrtpcpp' 'libyaml' 'dbus-c++' 'speex' 'libsamplerate' 'kdepimlibs')
makedepends=('autoconf' 'cmake' 'boost' 'automoc4')
conflicts=('sflphone')
source=("https://projects.savoirfairelinux.com/attachments/download/6423/sflphone-${pkgver}.tar.gz")
md5sums=('774eb07bbfc0717ad1e0090623e34616')
install="generateStarter.install"
build() {

## pjsip
  cd ${srcdir}/sflphone-${pkgver}/daemon/libs/pjproject-2.0.1
  ./configure --disable-ilbc-codec
  make dep
  make
  
  
## daemon
  cd "${srcdir}/sflphone-${pkgver}/daemon"
  ./autogen.sh
  ./configure --prefix="/usr" --disable-ilbc
   make
   
## KDE client
  cd "${srcdir}/sflphone-${pkgver}/kde"
  ./config.sh --prefix="/usr"
   cd build
   make  
}

package() {

## daemon
  cd "${srcdir}/sflphone-${pkgver}/daemon"
  make DESTDIR="${pkgdir}" install
  
## KDE client
  cd "${srcdir}/sflphone-${pkgver}/kde/build"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et: