# Maintainer: Christoph Wiechert <wio@psitrax.de>
pkgname=sflphone-kde
pkgver=1.3.0
pkgrel=2
pkgdesc="SIP/IAX2 softphone"
url="http://www.sflphone.org"
arch=('x86_64' 'i686')
license=('GPL3')
depends=('kdelibs' 'libpulse' 'libzrtpcpp' 'libyaml' 'dbus-c++' 'speex' 'libsamplerate' 'kdepimlibs')
makedepends=('autoconf' 'cmake' 'boost' 'automoc4')
conflicts=('sflphone')
source=("https://projects.savoirfairelinux.com/attachments/download/9198/sflphone-${pkgver}.tar.gz")
md5sums=('63602443176da2c572d98d1617233c24')

build() {

## pjsip
  cd ${srcdir}/sflphone-${pkgver}/daemon/libs
  ./compile_pjsip.sh
  
  
## daemon
  cd "${srcdir}/sflphone-${pkgver}/daemon"
  ./autogen.sh
  # zrtp leads to comile error
  ./configure --prefix="/usr" --disable-ilbc --without-zrtp
   make
   
## KDE client
  cd "${srcdir}/sflphone-${pkgver}/kde"
  ./config.sh -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR=lib
  cd build
  make -j3
}

package() {

## daemon
  cd "${srcdir}/sflphone-${pkgver}/daemon"
  make DESTDIR="${pkgdir}" install
  
## KDE client
  cd "${srcdir}/sflphone-${pkgver}/kde/build"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et: