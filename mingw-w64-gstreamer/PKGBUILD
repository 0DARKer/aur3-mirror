pkgname=mingw-w64-gstreamer
pkgver=1.2.3
pkgrel=2
pkgdesc="GStreamer Multimedia Framework (mingw-w64)"
arch=(any)
license=('LGPL')
url="http://gstreamer.freedesktop.org/"

options=('!strip' '!buildflags' 'staticlibs')
depends=(mingw-w64-glib2)
makedepends=(mingw-w64-gcc bison flex)

source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz)

sha256sums=('0f9a9817a384b3448c368c23345e5122435caef9c00f1c40d7b1953827b0d8eb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"


build() {
  cd ${srcdir}/gstreamer-${pkgver}
  
  export CFLAGS="-O2 -pipe -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="$CFLAGS"
  unset LDFLAGS
  
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} --build="$CHOST" \
      --with-package-origin="http://www.archlinux.org/" \
      --enable-static --enable-shared \
      --disable-examples --disable-benchmarks
    make
    popd
  done
}


package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/gstreamer-${pkgver}/build-${_arch}
    make DESTDIR="${pkgdir}" install
    
    rm $pkgdir/usr/$_arch/lib/gstreamer-1.0/*.a
    rm $pkgdir/usr/$_arch/lib/gstreamer-1.0/*.la
    
    find "$pkgdir" -name '*.dll' -exec ${_arch}-strip --strip-unneeded {} \;
    find "$pkgdir" -name '*.dll' -o -name '*.a' -exec ${_arch}-strip -g {} \;
  done
}
