# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>

pkgname=perl-set-intspan
_pkgname=Set-IntSpan
pkgver=1.16
pkgrel=4
pkgdesc="Set::IntSpan - Manages sets of integers, .newsrc style"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/${_pkgname}/"
license=('PerlArtistic')
depends=('perl')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/S/SW/SWMCD/$_pkgname-$pkgver.tar.gz)
sha512sums=('3a0ca7b43d78310360f646ec4bc8c4bce5c921cbdd33b3f29b55bbde69da6f7146486e609bcb4ee995e4075ce41d33ae5a3eef36e8c34539b13423b439e9f62f')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/$_pkgname-$pkgver"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
