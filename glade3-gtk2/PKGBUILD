# Maintainer: Ricky Thomson <punkrockunix at gmail dot com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=glade3-gtk2
_pkgname=glade3
pkgver=3.8.0
pkgrel=2
pkgdesc="User interface builder for GTK+2 applications"
arch=('i686' 'x86_64')
url="http://glade.gnome.org/"
license=('GPL' 'LGPL')
depends=('desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 'libxml2')
makedepends=('intltool' 'python2' 'gtk-doc')
optdepends=('python2: python widgets support')
options=('!libtool')
install=${pkgname}.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.bz2)
sha256sums=('ad3f726ccd34d210dbec9cc2787cab91f7b4c4b9e7518c00bc72b6d52c9858bb')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export PYTHON="/usr/bin/python2"

  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
