# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: aurperl@juster.us

pkgname=perl-log-dispatch
pkgver=2.29
pkgrel=2
pkgdesc="Dispatches messages to one or more outputs"
arch=('any')
license=('PerlArtistic' 'GPL')
options=(!emptydirs)
depends=('perl-params-validate>=0.15' 'perl-module-implementation')
url="https://metacpan.org/release/Log-Dispatch"
source=("http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Log-Dispatch-$pkgver.tar.gz")
md5sums=('781a4b5a9db2b617d2487dadecf65081')

build() (
	export \
    PERL_MM_USE_DEFAULT=1 \
    PERL5LIB= \
		PERL_AUTOINSTALL='--skipdeps' \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
		PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
		MODULEBUILDRC='/dev/null'

	cd "$srcdir/Log-Dispatch-$pkgver"
	perl Makefile.PL
	make
)

check() (
	export \
    PERL_MM_USE_DEFAULT=1 \
    PERL5LIB=

	cd "$srcdir/Log-Dispatch-$pkgver"
	make test
)

package() {
	cd "$srcdir/Log-Dispatch-$pkgver"
	make DESTDIR="$pkgdir" install
}
