# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=crafty
pkgver=23.4
pkgrel=1
pkgdesc="A computer chess program"
arch=('i686' 'x86_64')
url="http://www.cis.uab.edu/hyatt/"
license=('custom')
backup=()
source=(ftp://ftp.cis.uab.edu/pub/hyatt/source/$pkgname-$pkgver.zip
        ftp://ftp.cis.uab.edu/pub/hyatt/book/book.bin
        ftp://ftp.cis.uab.edu/pub/hyatt/book/books.bin
        copyright
        i686.patch
        x86_64.patch)
md5sums=('7e0811dd2d801428d8da48a4e487885a'
         '6d527840579904bf0e0b0a456a580a9b'
         '966a852a2ccd3c32e9167061592928d3'
         '438cec9f32fb79f58822f97cf64e7afb'
         'ea60586b858a15d18032136e77f2232e'
         'e38ee3557a30bd5826c4bf9d745c5fda')

build()
{
    unset LDFLAGS
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i "$srcdir/$CARCH.patch"
    make profile
}

package()
{
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm755 crafty "$pkgdir/usr/bin/crafty"
    install -Dm644 crafty.hlp "$pkgdir/usr/share/crafty/crafty.hlp"

    cd "$srcdir"
    install -m644 book.bin "$pkgdir/usr/share/crafty/"
    install -m644 books.bin "$pkgdir/usr/share/crafty/"
    install -Dm644 copyright "$pkgdir/usr/share/licenses/$pkgname/copyright"
}
