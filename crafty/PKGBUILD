# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=crafty
pkgver=23.8
pkgrel=2
pkgdesc="A computer chess program"
arch=('i686' 'x86_64')
url="http://www.cis.uab.edu/hyatt/"
license=('custom')
backup=()
source=(http://www.cis.uab.edu/hyatt/crafty/source/$pkgname-$pkgver.zip
        http://www.cis.uab.edu/hyatt/crafty/book/book.bin
        http://www.cis.uab.edu/hyatt/crafty/book/books.bin
        copyright
        i686.patch
        x86_64.patch)
md5sums=('906b7c260bb3042f2991a5106ea63731'
         '6d527840579904bf0e0b0a456a580a9b'
         '966a852a2ccd3c32e9167061592928d3'
         '438cec9f32fb79f58822f97cf64e7afb'
         '96640c16e28463bae947e29670480617'
         'f823fcda3fc130393673f7176a18dc8e')

build()
{
    unset LDFLAGS
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i "$srcdir/$CARCH.patch"
    make profile
}

package()
{
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm755 crafty "$pkgdir/usr/bin/crafty"
    install -Dm644 crafty.hlp "$pkgdir/usr/share/crafty/crafty.hlp"

    cd "$srcdir"
    install -m644 book.bin "$pkgdir/usr/share/crafty/"
    install -m644 books.bin "$pkgdir/usr/share/crafty/"
    install -Dm644 copyright "$pkgdir/usr/share/licenses/$pkgname/copyright"
}
