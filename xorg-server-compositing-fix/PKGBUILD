# Maintainer combuster <combuster@gmail.com>
# Based on xorg-server-catalyst-maximize-fix created by Det, Vi0l0 and JGC's [extra] xorg-server

pkgname=xorg-server-compositing-fix
srcname=xorg-server
pkgver=1.9.2.901
pkgrel=1
pkgdesc="X server 1.9.3 RC1 with propper fix for preventing artifacts while using compositing window managers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org"
license=(custom)
depends=(dri2proto fontsproto inputproto kbproto libdrm libgcrypt libpciaccess libdmx libxau libxaw libxdmcp libxfixes libxfont libxi libxrender libxv pixman randrproto renderproto udev videoproto xextproto xf86-input-evdev xineramaproto xkeyboard-config xorg-fonts-misc xorg-xkb-utils xorg-util-macros xproto libgl)
makedepends=(bigreqsproto compositeproto dmxproto libx11 libxkbfile libxmu libxres libxtst mesa recordproto resourceproto scrnsaverproto xcmiscproto xf86dgaproto xf86driproto xtrans)
conflicts=(xorg-server xorg-server-common xorg-server-devel)
provides=(x-server "xorg-server=$pkgver-$pkgrel" "xorg-server-common=$pkgver-$pkgrel" "xorg-server-devel=$pkgver-$pkgrel")
options=(!libtool)
groups=(xorg)
backup=(etc/X11/xorg.conf.d/10-evdev.conf etc/X11/xorg.conf.d/10-quirks.conf)
source=($url/archive/individual/xserver/xorg-server-$pkgver.tar.bz2
        xorg-redhat-die-ugly-pattern-die-die-die.patch
        10-evdev.conf
        10-quirks.conf)
md5sums=('aa2b294277d1733d33d30609321c1b15'
	 '1a336eb22e27cbf443ec5a2ecddfa93c'
         '16c235dd8c8fa3b232c032d22cd69ec8'
         'd4f7dfc6be4ef4e2c6dd7632a9d88abe')

build() {
  cd "$srcdir/${srcname/-d*}-$pkgver"

  # Get rid of the ugly pattern
  patch -Np3 -i "${srcdir}/xorg-redhat-die-ugly-pattern-die-die-die.patch"
  
  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-dri-driver-path=/usr/lib/xorg/modules/dri \
      --with-fontrootdir=/usr/share/fonts
  make

  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile

}

package() {
  cd "$srcdir/${srcname/-d*}-$pkgver"
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/var/log"

  install -m755 -d "$pkgdir/var/lib/xkb"

  # Install sane config files in /etc
  install -m755 -d "$pkgdir/etc/X11/xorg.conf.d"
  install -m644 "$srcdir/10-quirks.conf" "$pkgdir/etc/X11/xorg.conf.d/"
  install -m644 "$srcdir/10-evdev.conf" "$pkgdir/etc/X11/xorg.conf.d/"
  rm -rf "$pkgdir/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "$pkgdir/usr/lib/xorg/modules/extensions/libglx.so" \
     "$pkgdir/usr/lib/xorg/modules/extensions/libglx.xorg"

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/"
} 
