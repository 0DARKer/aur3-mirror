# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Luigi Ranghetti <ggranga@gmail.com>

pkgname=gdal-ecw
pkgver=1.10.0
pkgrel=1
pkgdesc="The GDAL package with support to ecw raster"
arch=('i686' 'x86_64')
url="http://www.gdal.org/"
license=('custom')
depends=('libecwj2' 'curl' 'geos' 'giflib' 'hdf5' 'libgeotiff' 'libjpeg' 'libpng' 'libtiff' 'netcdf'  'python2' 'python2-numpy' 'cfitsio' 'sqlite' 'libmariadbclient' 'postgresql-libs')
makedepends=('perl' 'swig' 'chrpath')
optdepends=('postgresql: postgresql database support'
            'mariadb: mariab database support'
            'perl:  perl binding support'
            'swig:  perl binding support'
            'libpcre: for regular expressions support in SQLite'
            'libxml2: validation of GML files')
provides=('gdal')
options=('!libtool')
source=(http://download.osgeo.org/gdal/${pkgver}/gdal-${pkgver}.tar.gz
        gdal-1.5.1-python-install.patch)
md5sums=('a2062d6ad09250e2ade40064bcd1a384'
         '81afc1c26d29cee84aadb6924fe33861')

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  # bug 23654
  export LDFLAGS="$LDFLAGS -Wl,--as-needed" 

  cd "${srcdir}/gdal-$pkgver"
  patch -Np0 -i "${srcdir}/gdal-1.5.1-python-install.patch"

# python2 fixes
  sed -i 's_python python1.5_python2 python python1.5_' configure
  for file in swig/python/{,osgeo/,samples/,scripts/}*.py; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr --with-netcdf --with-libtiff --with-sqlite3 \
              --with-geotiff --with-mysql --with-python --without-libtool --with-curl \
              --with-hdf5 --with-perl --with-geos --with-png --with-ecw

# workaround for bug #13646
  sed -i 's/PY_HAVE_SETUPTOOLS=1/PY_HAVE_SETUPTOOLS=/g' ./GDALmake.opt
  sed -i 's/EXE_DEP_LIBS/KILL_EXE_DEP_LIBS/' apps/GNUmakefile

  make
}

package () {
  cd "${srcdir}/gdal-$pkgver"
  install -d "${pkgdir}/usr/lib/python2.7/site-packages/"
  make DESTDIR="${pkgdir}" install

# install license
  install -D -m644 LICENSE.TXT "${pkgdir}/usr/share/licenses/gdal/LICENSE"

#FS15477 clean up junks
  rm -f "${pkgdir}"/usr/bin/*.dox

  # Remove RPATH
  chrpath --delete "${pkgdir}"/usr/lib/perl5/${CARCH}-linux-thread-multi/auto/Geo/OSR/OSR.so
  chrpath --delete "${pkgdir}"/usr/lib/perl5/${CARCH}-linux-thread-multi/auto/Geo/OGR/OGR.so
  chrpath --delete "${pkgdir}"/usr/lib/perl5/${CARCH}-linux-thread-multi/auto/Geo/GDAL/GDAL.so
  chrpath --delete "${pkgdir}"/usr/lib/perl5/${CARCH}-linux-thread-multi/auto/Geo/GDAL/Const/Const.so
}
