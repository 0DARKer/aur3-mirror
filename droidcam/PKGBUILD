# Contributor: Marco A Rojas <marquicus at gmail dot com>

pkgname=droidcam
pkgver=3.2.1
pkgrel=1
pkgdesc='A tool for using your android device as a wireless/usb webcam'
arch=('i686' 'x86_64')
url='http://www.dev47apps.com/'
license=('custom')
depends=('kernel26-headers' 'glibc' 'glib2' 'pcre' 'freetype2' 'gdk-pixbuf2' 'gtk2' 'pango')
options=('!strip')
optdepends=('v4l-utils: Userspace tools and conversion library for Video 4 Linux'
             'xf86-video-v4l: X.org v4l video driver')
install='droidcam.install'
_arch='x86'
[ "${CARCH}" = 'x86_64' ] && _arch='x64'
source=("http://www.dev47apps.com/files/droidcam-${_arch}.tar.gz"
        "droidcam.rc"
        "droidcam.desktop"
        "install.patch")
md5sums=('b6605eab3717cd9ac8694b9bedca0d8f'
         '61c412372d5734461b8b83d3e02da302'
         '199d8f3dbc6697f06350b00de99f2274'
         '22bdef9aec7fbb07fc289b696911400e')
[[ "${CARCH}" = 'x86_64' ]] && md5sums[0]='be3984e1fd6bdf33d536cd1affb2f9c2'

build() {
  cd $srcdir
  patch -p0 < ${srcdir}/install.patch
}

package() {

  # Install droidcam binary file
  cd $pkgdir
  mkdir -p usr/bin
  install -m755 ${srcdir}/${pkgname} $pkgdir/usr/bin/${pkgname}

  # Install droidcam initscript
  install -Dm0755 "${srcdir}/${pkgname}.rc" "${pkgdir}/etc/rc.d/${pkgname}"
  
  # Install the desktop icon and ".desktop" files
  install -dm0755 "${pkgdir}/usr/"{share/applications,share/pixmaps,share/$pkgname}
  install -m0644 "${srcdir}/icon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -m0644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install doc
  install -dm0755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -m0644 "${srcdir}/README" "${pkgdir}/usr/share/doc/$pkgname/README"

  # Install module sources
  install -dm0755 "${pkgdir}/usr/src/${pkgname}-${pkgver}"
  install -m0755 "${srcdir}/install" "${pkgdir}/usr/src/${pkgname}-${pkgver}/install"
  cp -a "${srcdir}/webcam" "${pkgdir}/usr/src/${pkgname}-${pkgver}/"

  # Write the build module file 
  echo "# Droidcam installation directory" > "${pkgdir}/usr/share/${pkgname}/build_tmp"
  echo "cd /usr/src/${pkgname}-${pkgver}" >> "${pkgdir}/usr/share/${pkgname}/build_tmp"
  echo "./install" >> "${pkgdir}/usr/share/${pkgname}/build_tmp"
  echo "cd -" >> "${pkgdir}/usr/share/${pkgname}/build_tmp"
  chmod 0755 "${pkgdir}/usr/share/${pkgname}/build_tmp"

}

# End of file
# vim: set ts=2 sw=2 noet:
