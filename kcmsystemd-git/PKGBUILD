# Maintainer: Antonio Rojas <arojas@archlinux.org>

_gitname=kcmsystemd
pkgname=$_gitname-git
pkgver=0.7.0.r23.g1f68e41
pkgrel=1
pkgdesc="Systemd control module for KDE - Git version"
arch=(i686 x86_64)
url='https://github.com/rthomsen/kcmsystemd'
license=(GPL)
depends=(kconfigwidgets boost-libs)
makedepends=(extra-cmake-modules boost python)
provides=(kcmsystemd)
conflicts=(kcmsystemd)
source=("git://github.com/rthomsen/$_gitname.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $_gitname
  mkdir -p build
}

build() {
  cd $_gitname/build
  cmake .. \
   -DCMAKE_BUILD_TYPE=Release \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_INSTALL_LIBDIR=lib \
   -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd $_gitname/build
  make DESTDIR=$pkgdir install
}
