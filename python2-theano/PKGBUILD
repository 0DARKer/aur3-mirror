# Maintainer: Thomas Dziedzic < gostrc at gmail >

pkgname=python2-theano
pkgver=0.4.1
pkgrel=1
pkgdesc='A Python library that allows you to define, optimize, and evaluate mathematical expressions involving multi-dimensional arrays efficiently.'
arch=('any')
url='http://www.deeplearning.net/software/theano/'
license=('BSD')
depends=('python2-numpy')
makedepends=('python2-distribute')
source=("http://pypi.python.org/packages/source/T/Theano/Theano-${pkgver}.tar.gz")
md5sums=('68a66dc7e18ad3bae20e9ed8a89f97f4')

build() {
  cd Theano-${pkgver}

  # fix hardcoded magic bits
  find \
    -name '*\.py' \
    -exec sed -e 's_#!/usr/bin/env python_&2_' -e 's_#!/usr/bin/python_&2_' -i {} \;

  python2 setup.py build
}

package() {
  cd Theano-${pkgver}

  python2 setup.py install \
    --root=${pkgdir} \
    --optimize=1

  install -d ${pkgdir}/usr/share/licenses/python2-theano
  install -m644 LICENSE.txt \
    ${pkgdir}/usr/share/licenses/python2-theano
}
