# Maintainer: Benoit Favre <benoit.favre@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=aseprite
pkgver=1.0.4
pkgrel=1
pkgdesc='Create animated sprites and pixel art'
arch=('x86_64' 'i686')
url='http://www.aseprite.org/'
license=('GPL')
depends=('allegro')
makedepends=('cmake')
source=("git+https://github.com/aseprite/aseprite.git#tag=v${pkgver}"
        "aseprite.desktop"
        "fix-none-macro-collision.patch")
sha256sums=('SKIP'
            'c9e624b9fd095ebb3eec8220a58d4a9422f39d68477bafcc0047d773814ba0aa'
            'f19ba08b8446831820161714b828068f52bdfe7fd98fd84bf31c41904c61d7b1')

build() {
  cd "$pkgname"
  git apply "$srcdir/fix-none-macro-collision.patch"

  cmake -DCMAKE_INSTALL_PREFIX:STRING=/usr .
  make aseprite
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir/" install/fast
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "data/icons/ase48.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
