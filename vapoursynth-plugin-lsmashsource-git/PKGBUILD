# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=lsmashsource
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20130412.69a5c77
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/VFR-maniac/L-SMASH-Works"
license=('GPL')
depends=('vapoursynth' 'l-smash-ffmpeg-git')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
makedepends=('git')
source=('0001-Fix-permissions-on-configure.patch::https://gist.github.com/qyot27/5229879'
        '0002-Support-for-compiling-vslsmashsource-under-Linux.patch::https://gist.github.com/qyot27/5229885'
        "${_plug}::git://github.com/VFR-maniac/L-SMASH-Works.git"
        'git://git.videolan.org/ffmpeg.git'
        'git+https://code.google.com/p/l-smash/')
md5sums=('f36889bb03c93012aa3e1258ee96004c'
         '1c04134667511a341b02acfe064118c9'
         'SKIP'
         'SKIP'
         'SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  patch -Np1 -i "${srcdir}/0001-Fix-permissions-on-configure.patch"
  patch -Np1 -i "${srcdir}/0002-Support-for-compiling-vslsmashsource-under-Linux.patch"

  rm -fr "${srcdir}/build"
}

build() {
  cd "${srcdir}/ffmpeg"
  ./configure --prefix="${srcdir}/build" --enable-gpl --disable-{network,{encod,mux}ers,hwaccels,{in,out}devs,debug,ff{play,probe,server},doc,vdpau} --enable-pic --enable-avresample
  make
  make install

  cd "${srcdir}/l-smash"
  PKG_CONFIG_LIBDIR="${srcdir}/build/lib/pkgconfig" ./configure --prefix="${srcdir}/build" --extra-cflags="-fPIC"
  make
  make install

  cd "${srcdir}/${_gitname}/VapourSynth"
  PKG_CONFIG_LIBDIR="${srcdir}/build/lib/pkgconfig" ./configure --prefix=/usr --extra-cflags="-fPIC"
  make
}

package(){
  cd "${_gitname}/VapourSynth"
  for i in $(find . -name *.so*); do
    install -Dm755 $i "${pkgdir}/usr/lib/vapoursynth/${i}"
    ln -s "/usr/lib/vapoursynth/${i}" "${pkgdir}/usr/lib/vapoursynth/vslsmashsource.so"
  done
}
