# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=lsmashsource
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20130523.f22760e
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (build with static FFmpeg and L-smash from GIT) (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/VFR-maniac/L-SMASH-Works"
license=('GPL')
depends=('vapoursynth')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
makedepends=('git')
source=("${_plug}::git://github.com/VFR-maniac/L-SMASH-Works.git"
        'git://git.videolan.org/ffmpeg.git'
        'git+https://code.google.com/p/l-smash/')
md5sums=('SKIP'
         'SKIP'
         'SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"

  rm -fr "${srcdir}/build"
}

build() {
  cd "${srcdir}/ffmpeg"
  ./configure --prefix="${srcdir}/build" --enable-gpl --disable-{network,{encod,mux}ers,hwaccels,{in,out}devs,debug,programs,doc,vdpau} --enable-pic --enable-avresample --enable-avisynth
  make
  make install

  cd "${srcdir}/l-smash"
  PKG_CONFIG_LIBDIR="${srcdir}/build/lib/pkgconfig" ./configure --prefix="${srcdir}/build" --extra-cflags="-fPIC"
  make lib
  make install-lib

  cd "${srcdir}/${_gitname}/VapourSynth"
  PKG_CONFIG_LIBDIR="${srcdir}/build/lib/pkgconfig" ./configure --prefix=/usr --extra-cflags="-fPIC"
  make
}

package(){
  cd "${_gitname}/VapourSynth"
  for i in $(\ls *.so*); do
    install -Dm755 $i "${pkgdir}/usr/lib/vapoursynth/${i}"
    ln -s "${i}" "${pkgdir}/usr/lib/vapoursynth/vslsmashsource.so"
  done
  install -Dm644 README "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
}
