# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=lsmashsource
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=r775.6b08551
_pkgver="$(echo ${pkgver} | cut -d '.' -f1 | tr -d r)"
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="http://forum.doom9.org/showthread.php?t=167435"
license=('custom:L-SMASH')
depends=('vapoursynth' 'l-smash-git')
makedepends=('git')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/VFR-maniac/L-SMASH-Works.git")
md5sums=('SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "r$(git rev-list HEAD 2> /dev/null | wc -l | sed 's/ //g').$(git describe --always)"
}

prepare() {
  rm "${_gitname}/VapourSynth/VapourSynth.h"
}

build() {
  cd "${_gitname}/VapourSynth"
  ./configure --prefix=/usr --extra-cflags="-fPIC $(pkg-config --cflags vapoursynth)"
  make
}

package(){
  cd "${_gitname}/VapourSynth"
  install -Dm755 "vslsmashsource.so.${_pkgver}" "${pkgdir}/usr/lib/vapoursynth/libvslsmashsource.so.${_pkgver}"
  ln -s "libvslsmashsource.so.${_pkgver}" "${pkgdir}/usr/lib/vapoursynth/libvslsmashsource.so"

  install -Dm644 README "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
