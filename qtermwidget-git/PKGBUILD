# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: kreed <kreed@kreed.org>
# Contributor: vicky91 <vickypaiers@gmail.com>

_pkgname=qtermwidget
pkgname=$_pkgname-git
pkgver=0.4.0.42.g0b430bf
pkgrel=3
pkgdesc="A terminal widget for Qt4"
arch=("i686" "x86_64")
url="https://github.com/qterminal/qtermwidget"
license=("GPL2")
provides=($_pkgname)
conflicts=($_pkgname)
depends=("qt5-base")
makedepends=("git" "cmake")
source=("git://github.com/qterminal/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_DESIGNER_PLUGIN=0 \
		-DUSE_QT5=true ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
