# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: kreed <kreed@kreed.org>
# Contributor: vicky91 <vickypaiers@gmail.com>
pkgname=qtermwidget-git
pkgver=20130301
pkgrel=1
provides=('qtermwidget')
conflicts=('qtermwidget')
pkgdesc="A terminal widget for Qt4"
arch=('i686' 'x86_64')
url="https://github.com/qterminal/qtermwidget"
license=('GPL2')
depends=('qt4')
makedepends=('git' 'cmake')

_gitroot=https://github.com/qterminal/qtermwidget.git
_gitname=qtermwidget

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
	cd "$_gitname" && git pull origin
	msg "The local files are updated."
	else
	git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"

	# -DLIB_SUFFIX=/ prevents libraries from being installed in /usr/lib64
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DLIB_SUFFIX=/ -DQT_QMAKE_EXECUTABLE=qmake-qt4
	make
}

package() {
	cd "$srcdir/$_gitname-build"
	make DESTDIR="$pkgdir/" install
}
