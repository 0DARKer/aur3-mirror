# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: kreed <kreed@kreed.org>
# Contributor: vicky91 <vickypaiers@gmail.com>

pkgname=qtermwidget-git
pkgver=78.57f1455
pkgrel=1
pkgdesc="A terminal widget for Qt4"
arch=('i686' 'x86_64')
url="https://github.com/qterminal/qtermwidget"
license=('GPL2')
provides=('qtermwidget')
conflicts=('qtermwidget')
depends=('qt4')
makedepends=('git' 'cmake')
source=("git://github.com/qterminal/qtermwidget.git")
sha256sums=('SKIP')
_gitname="qtermwidget"

pkgver() {
	cd "$srcdir/$_gitname"
	echo "$(git shortlog | grep -c '\s\+').$(git describe --always)"
}

build() {
	cd "$srcdir/$_gitname"
	# -DLIB_SUFFIX=/ prevents libraries from being installed in /usr/lib64
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DLIB_SUFFIX=/ -DQT_QMAKE_EXECUTABLE=qmake-qt4
	make
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="$pkgdir/" install
}
