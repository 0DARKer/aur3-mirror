# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: kreed <kreed@kreed.org>
# Contributor: vicky91 <vickypaiers@gmail.com>

_pkgname=qtermwidget
pkgname=$_pkgname-git
pkgver=0.4.0.28.g9fc6530
pkgrel=1
pkgdesc="A terminal widget for Qt4"
arch=('i686' 'x86_64')
url="https://github.com/qterminal/qtermwidget"
license=('GPL2')
provides=($_pkgname)
conflicts=($_pkgname)
depends=('qt4')
makedepends=('git' 'cmake')
source=("git://github.com/qterminal/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	# -DLIB_SUFFIX=/ prevents libraries from being installed in /usr/lib64
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Debug -DLIB_SUFFIX=/ ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
