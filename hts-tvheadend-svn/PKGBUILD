# Contributor: azleifel <azleifel at googlemail dot com>

pkgname=hts-tvheadend-svn
_pkgname=hts-tvheadend
pkgver=5556
pkgrel=4
pkgdesc="Combined DVB receiver, Digital Video Recorder and Showtime streaming server for Linux"
arch=('i686' 'x86_64')
url="http://www.lonelycoder.com/hts/"
license=('GPL3')
depends=('avahi')
makedepends=('subversion' 'gzip')
optdepends=('xmltv: For an alternative source of programme listings')
conflicts=('hts-tvheadend')
install=tvheadend.install
source=('rc-tvheadend' 'conf-tvheadend')
backup=('etc/conf.d/tvheadend')
md5sums=('8cd63181f55750e75c1103d30ab3efb7' '67d4c9837e870c1ca1a71b91b15d51c1')
options=('makeflags')

# Note: _svntrunk URL must end with /tvheadend otherwise the whole
# repository will be checked out, showtime and all
_svntrunk=svn://svn.lonelycoder.com/hts/trunk/tvheadend
_svnmod=tvheadend

build() {
  cd ${srcdir}

  msg "Connecting to SVN server...."
  
  if [ -d ${_svnmod}/.svn ]; then
    cd ${_svnmod} && svn up -r $pkgver
  else
    svn co ${_svntrunk} --config-dir ./ -r $pkgver
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  [ -d ${srcdir}/${_svnmod}-build ] && rm -rf ${srcdir}/${_svnmod}-build
  cp -r ${srcdir}/${_svnmod} ${srcdir}/${_svnmod}-build
  cd ${srcdir}/${_svnmod}-build

  #
  # BUILD
  #

  ./configure --prefix=/usr --release
  make || return 1
}

package() {
  cd ${srcdir}/${_svnmod}-build

  # Install tvheadend
  install -D -m755 build.Linux/tvheadend ${pkgdir}/usr/bin/tvheadend || return 1
  
  # Install rc-script
  install -D -m755 ${srcdir}/rc-tvheadend ${pkgdir}/etc/rc.d/tvheadend || return 1
  install -D -m644 ${srcdir}/conf-tvheadend ${pkgdir}/etc/conf.d/tvheadend || return 1
  
  # Install man page and documents
  install -D -m644 man/tvheadend.1 ${pkgdir}/usr/share/man/man1/tvheadend.1 || return 1
  install -D -m644 README ${pkgdir}/usr/share/doc/${_pkgname}/README || return 1
  install -D -m644 debian/changelog ${pkgdir}/usr/share/doc/${_pkgname}/ChangeLog || return 1
  gzip ${pkgdir}/usr/share/doc/${_pkgname}/ChangeLog || return 1
}
