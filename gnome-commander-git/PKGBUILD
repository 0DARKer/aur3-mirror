# Maintainer: Thomas Jost <schnouki@schnouki.net>
# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>

pkgname=gnome-commander-git
pkgver=20110707
pkgrel=1
pkgdesc="A graphical two-pane filemanager for Gnome"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/gcmd/"
license=('GPL')
depends=('libgnomeui' 'exiv2' 'taglib' 'libgsf' 'python2' 'poppler' 'libunique')
makedepends=('perlxml' 'gnome-doc-utils>=0.11.2' 'chmlib' 'gnome-common' 'intltool')
options=(!libtool)
install=gnome-commander.install
provides=(gnome-commander)
conflicts=(gnome-commander)

_gitroot="git://git.gnome.org/gnome-commander"
_gitname="gnome-commander"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD
  #

  # python2 fix
  for file in doc/*/gnome-commander.xml; do
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
  export PYTHON=python2

  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc \
    --localstatedir=/var --disable-scrollkeeper --enable-python

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir"/usr/share/gconf/schemas/${pkgname}.schemas \
    "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
