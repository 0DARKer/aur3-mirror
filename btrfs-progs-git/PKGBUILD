# Maintainer:  WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributer: J.W Birdsong < @ gmail>
# Contributor: Igor Nemilentsev <trezorg dog gmail.com>
# Contributor: Miroslaw "firestarter" Wojtylak mwojtylak@gmail.com
# Based on PKGBUILD from Dan Zwell <dzwell@zwell.net>

pkgname=btrfs-progs-git
_gitname=${pkgname%-git}-unstable
_ver=3.16
pkgver=3.16_108_d34cbe7
pkgrel=1
pkgdesc="Btrfs filesystem utilities"
arch=("i686" "x86_64")
url="http://btrfs.wiki.kernel.org/index.php/Main_Page"
license=('GPL')
depends=('glibc' 'e2fsprogs' 'lzo2' 'zlib')
makedepends=('git' 'asciidoc' 'xmlto')
provides=('btrfs-progs')
conflicts=('btrfs-progs')
_url=https://projects.archlinux.org/svntogit/packages.git/plain/trunk/
source=(btrfs-progs-unstable::git+"http://repo.or.cz/r/btrfs-progs-unstable/devel.git#branch=v${_ver}.x"
        "initcpio-hook-btrfs::${_url}initcpio-hook-btrfs?h=packages/btrfs-progs"
        "initcpio-install-btrfs::${_url}initcpio-install-btrfs?h=packages/btrfs-progs")
md5sums=('SKIP'
         'b09688a915a0ec8f40e2f5aacbabc9ad'
         '7241ba3a4286d08da0d50b7176941112')

pkgver() {
  cd "$_gitname"
  # git --no-pager describe --tags | sed -e 's:^v::' -e 's:-:.:g'
  # tags are unreliable, count number of commits since a certain point
  # using "btrfs-show-super: don't try to print not-superblocks"
  # http://repo.or.cz/w/btrfs-progs-unstable/devel.git?a=commit;h=fd518cfcb1fdb49ec4e5f09c397d16abb36178b8
  _commits=$(git rev-list "fd518cfcb1f^..HEAD" | wc -l)
  _current=$(git rev-parse --short HEAD)
  printf "%s_%s_%s" $_ver $_commits $_current
}

build() {
  cd "$_gitname"
  make CFLAGS="$CFLAGS" all btrfs-select-super
}

package() {
  # install mkinitcpio files
  install -Dm644 initcpio-hook-btrfs "$pkgdir/usr/lib/initcpio/hooks/btrfs"
  install -Dm644 initcpio-install-btrfs "$pkgdir/usr/lib/initcpio/install/btrfs"

  cd "$_gitname"
  # install compiled software + readme
  make prefix="$pkgdir/usr" install
  install -m755 btrfs-select-super "$pkgdir/usr/bin/btrfs-select-super"
  install -Dm644 INSTALL "$pkgdir/usr/share/doc/btrfs/README"
}

# vim:set ts=2 sw=2 et:
