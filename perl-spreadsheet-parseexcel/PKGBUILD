# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
# Contributor: AUR Perl <aurperl@juster.info>

pkgname='perl-spreadsheet-parseexcel'
pkgver='0.65'
pkgrel='1'
pkgdesc='Read information from an Excel file'
_realname='Spreadsheet-ParseExcel'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-crypt-rc4' 'perl-digest-perl-md5' 'perl-io-stringy' 'perl-ole-storage-lite>=0.19')
url='http://search.cpan.org/dist/Spreadsheet-ParseExcel'
source=(http://search.cpan.org/CPAN/authors/id/D/DO/DOUGW/${_realname}-${pkgver}.tar.gz)

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}
check() {
  cd ${_realname}-${pkgver}
  make test
}
package(){
  cd ${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}
}

md5sums=('4b8857e3a391d86501c1b742b459ac9e')
sha512sums=('a57be764a9781d1e219d1aab940a9b834d1ddf90ebb4966eaeba86afea3de03617fbdeda67d9bc2bedd8651cc3feafd46905021150bbcd68cbc7453865877637')
