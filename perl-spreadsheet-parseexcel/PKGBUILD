# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
# Contributor: AUR Perl <aurperl@juster.info>

pkgname='perl-spreadsheet-parseexcel'
pkgver='0.59'
pkgrel='2'
pkgdesc='Read information from an Excel file'
_realname='Spreadsheet-ParseExcel'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-crypt-rc4' 'perl-digest-perl-md5' 'perl-io-stringy' 'perl-ole-storage-lite>=0.19')
url='http://search.cpan.org/dist/Spreadsheet-ParseExcel'
source=(http://search.cpan.org/CPAN/authors/id/J/JM/JMCNAMARA/${_realname}-${pkgver}.tar.gz)
md5sums=('7ed6487d09135a9ee6ae9c290dad3a5e')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}
check() {
  cd ${_realname}-${pkgver}
  make test
}
package(){
  cd ${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}
}

