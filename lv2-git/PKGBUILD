# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=lv2-git
pkgver=1.11.0.commitc88c
pkgrel=1
pkgdesc="A standard for plugins and matching host applications, mainly targeted at audio processing and generation."
arch=('i686' 'x86_64')
url="http://lv2plug.in/"
license=('custom:ISC')
makedepends=('subversion' 'python2' 'libsndfile' 'gtk2')
optdepends=('libsndfile: example extension'
            'gtk2: example extension'
            'python2: scripts')
provides=("lv2=$pkgver" "lv2core" "lv2-svn")
conflicts=('lv2' 'lv2-svn')
replaces=('lv2core')
source=("$pkgname::git+http://lv2plug.in/git/cgit.cgi/lv2.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  echo "$(awk -F "'" '/^VERSION/{print $2}' wscript).commit$(git rev-parse --short=0 HEAD)"
}

build() {
  cd $pkgname
  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd $pkgname
  python2 waf install --destdir="$pkgdir/"

  # python2 shebangs
  sed -i 's/python/&2/' "$pkgdir/usr/bin/lv2specgen.py"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
