# Maintainer: Kyle <kyle@gmx.ca>
# Contributor: Christopher Brannon <chris@the-brannons.com>
pkgname=svox-pico-git
pkgver=20110303
pkgrel=1
pkgdesc="Small footprint text-to-speech engine"
arch=('i686' 'x86_64')
url="http://android.git.kernel.org/?p=platform/external/svox.git;a=summary"
source=(fix-as-needed.patch fix-64bits.patch)
md5sums=('efc54a60c05593f0268431ae665c1771' '837f869b39e3b3e05aed1ec0a865a00b')
license=('APACHE')
groups=()
makedepends=('git')
depends=('popt')
options=('!libtool')

_gitroot="git://git.debian.org/collab-maint/svox.git"
_gitname="svox-pico"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone -b upstream+patches $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build/pico"

  # Build breaks with --as-needed; this patch fixes it.
  # Hopefully, it'll get accepted upstream.
  patch -p2 < "$srcdir/fix-as-needed.patch"
  # This patch fixes buffer overruns that cause pico2wave to segfault on 64-bit systems.
  # It has been downloaded from an issue reported upstream.
  patch -p2 < "$srcdir/fix-64bits.patch"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build/pico"
  make DESTDIR="$pkgdir/" install
} 

