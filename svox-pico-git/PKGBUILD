# Maintainer: Kyle <kyle@gmx.ca>
# Contributor: JoKoT3 <jokot3 at gmail dot com>
# Contributor: Christopher Brannon <chris@the-brannons.com>
pkgname=svox-pico-git
_gitname=svox
pkgver=android.4.2.2_r1.2.2.g3a31433
pkgrel=1
pkgdesc="Small footprint text-to-speech engine"
arch=('i686' 'x86_64')
url="http://android.git.kernel.org/?p=platform/external/svox.git;a=summary"
license=('APACHE')
groups=()
makedepends=('git')
depends=('popt')
options=('!libtool')
source=('git+https://android.googlesource.com/platform/external/svox'
  '0008-64bits.patch'
  'autogen.sh'
  'configure.ac'
  'Makefile.am'
  'pico2wave.c')
md5sums=('SKIP'
  '0983a652612f9398b3cc12fbeb750695'
  '467ae4f4619ded94b940a12d79425844'
  '75f9c9309ac31895e30bbd0a9a2bec63'
  '9e95798e864b4e2ffcbbc7527b6d6434'
  '87aac4be9acbac512bcbc0e7e67e441f')

pkgver() {
  cd $_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname/
  # see patches from debian packager @ http://packages.debian.org/sid/libttspico-utils
   patch -p1 < "$srcdir/0008-64bits.patch"
  # files below are based on the work of debian packager (patch 0001 to 0006) and adapted to works with autotools 1.13+
  cp -pr "$srcdir/autogen.sh" "$srcdir/Makefile.am" "$srcdir/configure.ac" $srcdir/$_gitname/pico
  mkdir -p $srcdir/$_gitname/pico/bin/ && cp -p "$srcdir/pico2wave.c" $srcdir/$_gitname/pico/bin/
  cd $srcdir/$_gitname/pico
  [ -x ./autogen.sh ] || chmod u+x autogen.sh
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$_gitname/pico
  make DESTDIR="$pkgdir/" install
} 

