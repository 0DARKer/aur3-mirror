# $Id: PKGBUILD 103215 2013-12-30 13:50:26Z arodseth $
# Maintainer:  skydrome <skydrome@i2pmail.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>

pkgname=botan-unstable
pkgver=1.11.10
pkgrel=1
pkgdesc='Crypto library written in C++'
license=('BSD')
arch=('x86_64' 'i686')
url='https://botan.randombit.net'
#depends=('boost-libs' 'gmp' 'zlib' 'openssl' 'bzip2' 'sqlite')
makedepends=('python' 'boost')
provides=('botan')
conflicts=('botan')
source=("http://files.randombit.net/botan/Botan-${pkgver}.tbz"
        "http://files.randombit.net/botan/Botan-${pkgver}.tbz.asc")
sha256sums=('b5555fe4bc6da7520f63d01794e1bf0182eb4e3a86891f1062ebd29e095d3c10'
            'SKIP')

prepare() {
    cd "Botan-$pkgver"

    sed -i 'src/build-data/cc/gcc.txt' \
        -e 's|i686        -> "-mtune.*|i686        -> "-march=native"|'
}

build() {
    cd "Botan-$pkgver"

    ./configure.py \
        --prefix=/usr \
        --destdir=$pkgdir/usr \
        --cc="$CC" \
        --os=linux \
        --without-sphinx \
        --disable-modules=proc_walk,unix_procs

    make
}

package() {
    cd "Botan-$pkgver"

    make DESTDIR="$pkgdir/usr" install
    find "$pkgdir/usr/share/doc" -type f |xargs chmod 644
    install -Dm644 doc/license.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
