# $Id: PKGBUILD 103215 2013-12-30 13:50:26Z arodseth $
# Maintainer:  skydrome <skydrome@i2pmail.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>

pkgname=botan-unstable
pkgver=1.11.8
pkgrel=1
pkgdesc='Crypto library written in C++'
license=('BSD')
arch=('x86_64' 'i686')
url='https://botan.randombit.net'
#depends=('boost-libs' 'gmp' 'zlib' 'openssl' 'bzip2' 'sqlite')
makedepends=('python')
provides=('botan')
conflicts=('botan')
source=("http://files.randombit.net/botan/Botan-${pkgver}.tbz"
        "http://files.randombit.net/botan/Botan-${pkgver}.tbz.asc")
sha256sums=('5c3a5485dd8f2b5e690bdc73d5848b1f436a090e7fc521c66920f34d6af68efd'
            'SKIP')

prepare() {
    cd "Botan-$pkgver"

    sed -i 'src/build-data/cc/gcc.txt' \
        -e 's|i686        -> "-mtune.*|i686        -> "-march=native"|'
}

build() {
    cd "Botan-$pkgver"

    ./configure.py \
        --prefix=/usr \
        --cc="$CC" \
        --os=linux \
        --without-sphinx \
        --disable-modules=proc_walk,unix_procs
    make
}

package() {
    cd "Botan-$pkgver"

    make DESTDIR="$pkgdir/usr" install
    find "$pkgdir/usr/share/doc" -type f |xargs chmod 644
    install -Dm644 doc/license.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
