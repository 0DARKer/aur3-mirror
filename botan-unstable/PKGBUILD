# $Id: PKGBUILD 103215 2013-12-30 13:50:26Z arodseth $
# Maintainer:  skydrome <skydrome@i2pmail.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>

pkgname=botan-unstable
pkgver=1.11.7
pkgrel=2
pkgdesc='Crypto library written in C++'
license=('BSD')
arch=('x86_64' 'i686')
url='https://botan.randombit.net'
depends=('boost-libs' 'gmp' 'zlib' 'openssl' 'bzip2' 'sqlite')
makedepends=('python')
provides=('botan')
conflicts=('botan')
source=("http://files.randombit.net/botan/Botan-${pkgver}.tbz"
        "http://files.randombit.net/botan/Botan-${pkgver}.tbz.asc")
sha256sums=('037837a9b49defd21226f0988f2990fc9b1f19e9825392be5684c21ab09a5b27'
            'SKIP')

prepare() {
    cd "Botan-$pkgver"

    # bug reported upstream
    sed -i 'src/build-data/makefile/header.in' \
        -e 's|HEADERIR|HEADERDIR|'

    # Intel atom cpu needs to use -march=native to detect SSE2 correctly
    #sed -i 'src/build-data/cc/gcc.txt' \
    #    -e 's|x86_32    -> "-march.*|x86_32    -> "-march=native"|'
}

build() {
    cd "Botan-$pkgver"

    ./configure.py \
        --prefix=/usr \
        --cc=gcc \
        --os=linux \
        --without-sphinx \
        --disable-modules=proc_walk,unix_procs
    make
}

package() {
    cd "Botan-$pkgver"

    make DESTDIR="$pkgdir/usr" install
    find "$pkgdir/usr/share/doc" -type f |xargs chmod 644
    install -Dm644 doc/license.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
