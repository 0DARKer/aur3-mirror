# Mantainer:   Thomas Kuther <archlinux@kuther.net>
# Contributor: Gianni Vialetto <gianni at rootcube dot net>
# Contributor: Paul N. Maxwell <msg dot maxwel at gmail dot com>
# Contributor: Thomas Mudrunka <harvie@@email..cz>
# Contributor: Max Fierke <max@maxfierke.com>

_bzrname=apparmor
pkgbase=apparmor-stable-bzr
pkgname=apparmor-stable-bzr
true && pkgname=($pkgbase apparmor-parser-stable-bzr apparmor-libapparmor-stable-bzr apparmor-utils-stable-bzr apparmor-profiles-stable-bzr apparmor-pam-stable-bzr apparmor-vim-stable-bzr)
pkgver=2.8.3.bzr.2126
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url='http://wiki.apparmor.net/index.php/Main_Page'
groups=('apparmor')
makedepends=('bzr' 'flex' 'swig' 'perl' 'python' 'perl-locale-gettext' 'perl-rpc-xml' 'audit')

source=("apparmor::bzr+lp:apparmor/2.8"
        "apparmor_load.sh"
        "apparmor_unload.sh"
        "apparmor.service")

md5sums=('SKIP'
         'a657bc9e2670509e489615e63ba718c9'
         'f083aa1926430bc48eb332f1b77fca08'
         'a5c217ad417d08c2d2c2173bb8cd1842')

#Configuration
core_perl_dir='/usr/bin/core_perl'
export MAKEFLAGS+=" POD2MAN=${core_perl_dir}/pod2man"
export MAKEFLAGS+=" POD2HTML=${core_perl_dir}/pod2html"
export MAKEFLAGS+=" PROVE=${core_perl_dir}/prove"

pkgver() {
  cd $_bzrname
  echo $(cat common/Version).bzr.$(bzr revno)
}

prepare() {
  cd "${srcdir}/${_bzrname}/parser"
  # avoid depend on texlive-latex
  sed -i -e 's/pdflatex/true/g' Makefile

  cd "${srcdir}/${_bzrname}/utils"
  # Set Arch paths
  sed -e '/logfiles/ s/syslog /syslog.log /g' \
      -e '/logfiles/ s/messages/messages.log/g' \
      -e '/parser/ s# /sbin/# /usr/bin/#g' \
      -i logprof.conf
  # do not build/install vim file with utils package (causes ref to $srcdir and wrong location)
  sed -i '/vim/d' Makefile

  cd "${srcdir}/${_bzrname}/profiles/apparmor.d"
  # /usr merge vs. profiles
  for i in `find . -name "*sbin*"`; do sed -i -e 's@sbin@bin@g' ${i} && mv ${i} ${i/sbin/bin}; done
  for i in klogd ping syslog-ng syslogd; do 
    sed -e "s@/bin/${i}@/usr/bin/${i}@g" \
        -e "s@bin\.${i}@usr\.bin\.${i}@g" \
        -i bin.${i} && \
    mv bin.${i} usr.bin.${i}
  done
}

build() {
  msg2 "Building: apparmor-libapparmor"
  cd "${srcdir}/${_bzrname}/libraries/libapparmor"
  unset PERL_MM_OPT
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin --with-perl --with-python
  make

  cd "${srcdir}/${_bzrname}"
  msg2 "Building: apparmor-parser"
  make -C parser
	
  msg2 "Building: apparmor-utils"
  make -C utils

  msg2 "Building: apparmor-profiles"
  make -C profiles

  msg2 "Building: apparmor-pam"
  make -C changehat/pam_apparmor

  msg2 "Building: apparmor-vim"
  make -C utils/vim -j1
}

package_apparmor-stable-bzr() {
  pkgdesc='Linux application security framework - mandatory access control for programs (metapackage for AUR)'
  depends=(apparmor-parser-stable-bzr apparmor-libapparmor-stable-bzr apparmor-utils-stable-bzr apparmor-profiles-stable-bzr apparmor-pam-stable-bzr apparmor-vim-stable-bzr)
  optdepends=('linux-apparmor: an arch kernel with AppArmor patches')
  install='apparmor.install'
  conflicts=(apparmor)
  replaces=(apparmor)
  provides=(apparmor)
}

package_apparmor-parser-stable-bzr() {
  pkgdesc='AppArmor parser - loads AA profiles to kernel module'
  depends=('apparmor-libapparmor' 'bash')
  conflicts=(apparmor-parser)
  replaces=(apparmor-parser)
  provides=(apparmor-parser)

  cd "${srcdir}/${_bzrname}"
  make -C parser install DESTDIR=${pkgdir}
  mv ${pkgdir}/lib ${pkgdir}/usr/lib
  mv ${pkgdir}/sbin ${pkgdir}/usr/bin
}

package_apparmor-libapparmor-stable-bzr() {
  pkgdesc='AppArmor library'
  makedepends=('swig' 'perl' 'python')
  depends=('python')
  provides=(apparmor-libapparmor)
  conflicts=(apparmor-libapparmor)
  replaces=(apparmor-libapparmor)

  cd "${srcdir}/${_bzrname}"
  make -C libraries/libapparmor install DESTDIR=${pkgdir}
  install -D -m644 "libraries/libapparmor/swig/perl/LibAppArmor.pm" "${pkgdir}/usr/lib/perl5/vendor_perl/"
}

package_apparmor-utils-stable-bzr() {
  pkgdesc='AppArmor userspace utilities'
  arch=('any')
  depends=('perl' 'perl-locale-gettext' 'perl-term-readkey' 
    'perl-file-tail' 'perl-rpc-xml' 'python' 'bash')
  install='apparmor-utils.install'
  provides=(apparmor-utils)
  conflicts=(apparmor-utils)
  replaces=(apparmor-utils)

  cd "${srcdir}/${_bzrname}"
  make -C utils install DESTDIR=${pkgdir} BINDIR=${pkgdir}/usr/bin
  install -D -m755 "${srcdir}/apparmor_load.sh" "${pkgdir}/usr/bin/apparmor_load.sh"
  install -D -m755 "${srcdir}/apparmor_unload.sh" "${pkgdir}/usr/bin/apparmor_unload.sh"
  install -D -m644 "${srcdir}/apparmor.service" "${pkgdir}/usr/lib/systemd/system/apparmor.service"
}

package_apparmor-profiles-stable-bzr() {
  pkgdesc='AppArmor sample pre-made profiles'
  depends=(apparmor-parser)
  arch=('any')
  conflicts=(apparmor-profiles)
  replaces=(apparmor-profiles)
  provides=(apparmor-profiles)

  # backup /etc/apparmor.d/* so using logprof is safe
  cd "${srcdir}/${_bzrname}/profiles/apparmor.d"
  declare -a _profiles=(`find -type f|sed 's@./@etc/apparmor.d/@'`)
  backup=(`echo ${_profiles[@]}`)

  cd "${srcdir}/${_bzrname}"
  make -C profiles install DESTDIR=${pkgdir}
}

package_apparmor-pam-stable-bzr() {
  pkgdesc='AppArmor PAM library'
  depends=('apparmor-libapparmor' 'pam')
  conflicts=(apparmor-pam)
  replaces=(apparmor-pam)
  provides=(apparmor-pam)

  cd "${srcdir}/${_bzrname}"
  make -C changehat/pam_apparmor install DESTDIR=${pkgdir}/usr
  install -D -m644 changehat/pam_apparmor/README "${pkgdir}/usr/share/doc/apparmor/README.pam_apparmor"
}
package_apparmor-vim-stable-bzr() {
  pkgdesc='AppArmor VIM support'
  arch=('any')
  depends=('vim')
  conflicts=(apparmor-vim)
  replaces=(apparmor-vim)
  provides=(apparmor-vim)

  cd "${srcdir}/${_bzrname}/utils/vim"
  install -D -m644 apparmor.vim \
    "${pkgdir}/usr/share/vim/vimfiles/syntax/apparmor.vim"
}
#AUR hack:
pkgdesc='Linux application security framework - mandatory access control for programs (stable 2.8 BZR branch)'
depends=('perl' 'perl-locale-gettext' 'perl-term-readkey' 'perl-file-tail' 'perl-rpc-xml' 'python' 'bash' 'pam' 'vim')

# vim:set ts=2 sw=2 et:
