# Maintainer: Dennis Fink <the_metalgamer@hackerspace.lu>
# Submitter: Zoran Zeric <zz@zoranzaric.de>

pkgname=python2-acrylamid
pkgver=0.5.0
pkgrel=4
pkgdesc="static blog generator in python with incremental rendering. Python2 version "
arch=('any')
url="http://posativ.org/acrylamid/"
license=('custom:BSD')
depends=('python2>=2.6' 'python2-jinja>=2.4' 'python2-markdown>=2.0.1' 'python2-translitcodec>=0.2')
makedepends=('python2-distribute')
optdepends=('python2-pygments: Syntax Highlighting'
'python2-docutils: reStructuredText'
'python2-smartypants: Typography enhancements'
'python2-asciimathml: AsciiMathML to MathML'
'python2-unidecode: cyrillic/chinese ascii slugs'
'python2-textile: Textile'
'python2-yaml: exact YAML parser'
'python2-twitter-tools: Twitter'
'discount: Discount'
'python2-mako: Mako Templating'
)
conflicts=('python2-acrylamid-git' 'python-acrylamid')
source=("http://pypi.python.org/packages/source/a/acrylamid/acrylamid-$pkgver.tar.gz")
md5sums=('41c45182988f704119290f0fd549055b')

build() {

  cd $srcdir/acrylamid-$pkgver
  python2 setup.py build
}

package() {

  cd $srcdir/acrylamid-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  cp $srcdir/acrylamid-$pkgver/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

}
# vim:set ts=2 sw=2 et:
