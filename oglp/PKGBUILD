# Maintainer: Daniel Kirchner <daniel at ekpyron dot org>
pkgname=oglp
pkgver=r115.0950559
pkgrel=1
pkgdesc="OpenGL wrapper for C++"
license=('GPL')
arch=('any')
url="http://github.com/ekpyron/oglp/"
depends=('glm')
makedepends=('git' 'python')
source=('git+git://github.com/ekpyron/oglp')
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  ( set -o pipefail
    git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  
  cd "$srcdir"/$pkgname
  ./glcorew_gen.py
}

package() {
  cd "${srcdir}/$pkgname"
  install -d -m755 "${pkgdir}/usr/include/oglp"
  install -m644 oglp/*.h "${pkgdir}/usr/include/oglp"
  install -m644 oglp/*.cxx "${pkgdir}/usr/include/oglp"
  install -d -m755 "${pkgdir}/usr/include/oglp/ext"
  install -m644 oglp/ext/*.h "${pkgdir}/usr/include/oglp/ext"
}
