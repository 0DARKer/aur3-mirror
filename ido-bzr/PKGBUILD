# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Michal Gawronski <dinth1906@gmail.com>

pkgname=ido-bzr
_bzrname=ido
pkgver=180
pkgrel=2
pkgdesc="Widgets and other objects used for indicators. (Ayatana Project)"
arch=('i686' 'x86_64')
url="https://launchpad.net/ido"
license=('GPL')
depends=('gtk3-ubuntu')
makedepends=('bzr')
provides=('ido')
conflicts=('ido')
source=("$_bzrname::bzr+http://bazaar.launchpad.net/~indicator-applet-developers/$_bzrname/trunk.14.04/")
md5sums=('SKIP')

pkgver() {
  cd $_bzrname
  bzr revno
}

build() {
  cd "$srcdir"/$_bzrname
  ./autogen.sh --disable-static --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$_bzrname
  make DESTDIR="$pkgdir" install
}
