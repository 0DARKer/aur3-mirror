# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=go-openoffice-git
_pkgname=go-openoffice
_GOver=3.2.0.9 # = post OOo 3.2.0 final bugfix
pkgver=${_GOver}
pkgrel=1
pkgdesc="OpenOffice.org - go-oo.org enhanced version of SUN's office suite"
arch=('i686' 'x86_64')
_ootag=ooo320-m15
license=('LGPL3')
url="http://go-oo.org/"
install=${_pkgname}.install
depends=("curl>=7.19.6" "hunspell>=1.2.8" "python>=2.6.4" 'libwpd' 'redland>=1.0.10' 
         'libxaw' "neon>=0.28.6" "icu>=4.2.1" 'hsqldb-java' 'libxslt' 'libxtst' 'lpsolve'
	  'beanshell' 'saxon' 'vigra' 'hyphen' 'libmspack' 'libldap' 'gtk2' 'lucene'
	 'hicolor-icon-theme' 'shared-mime-info' 'desktop-file-utils') #   'libmythes' 'libgraphite' 
optdepends=('java-runtime:	adds java support'
            'libcups:		adds printing support'
            'gconf:		adds additional gnome support'
            'nss:		adds support for signed files/macros'
            'pstoedit:		translates PostScript and PDF graphics into other vector formats'
	    'poppler:		for the pdfimport extension'
	    'mesa:		for the OGLTrans extension'
	    'mono:		allows UNO automation with Mono'
	    'gstreamer0.10-base:	+ some gstr-plugins to support multimedia content, e.g. in impress'
            'kdelibs:		for kde integration')
makedepends=('automake' 'autoconf' 'wget' 'bison' 'findutils' 'flex' 'gawk' 'gcc-libs' 'libart-lgpl'
	'pam' 'sane' 'perl-archive-zip' 'pkgconfig' 'unzip' 'zip' "xulrunner>=1.9.2-4" 'apache-ant' 'cairo' 
	'gperf' 'libcups' 'pstoedit' 'gconf' 'java-environment' 'unixodbc' 'mesa>=7.5' 'poppler>=0.10.7'
	'gstreamer0.10-base>=0.10.26'  'mono>=2.6.1' 'kdelibs>=4.4.0' 'libjpeg' 'boost' 'git' 'rsync')
backup=(usr/lib/go-openoffice/program/sofficerc)
provides=('openoffice-base')
conflicts=('openoffice-base')
_mirror="http://download.go-oo.org/"
source=(ArchLinux.patch
	buildfix_64bit_system_libjpeg.diff
	system-redland.patch
	localize-ooo.diff)
#options=('!distcc' '!ccache' '!makeflags')
options=('!makeflags')

_gitroot='git://anongit.freedesktop.org/git/ooo-build/ooo-build'

build() {
	msg "Connecting to GIT server..."
	if [[ -d ooo-build ]]; then
		cd ooo-build && git pull origin || return 1
	else
		git clone $_gitroot || return 1
	fi
	msg "GIT checkout done or server timeout"

	cd "$srcdir"
	cp -r ooo-build $_pkgname || return 1

	unset J2REDIR; unset J2SDKDIR; unset JAVA_HOME; unset CLASSPATH
	[ -z "${JAVA_HOME}" ] && . /etc/profile.d/openjdk6.sh
	[ -z "${MOZ_PLUGIN_PATH}" ] && . /etc/profile.d/mozilla-common.sh
	[ -z "${ANT_HOME}" ] && . /etc/profile.d/apache-ant.sh

	cd "$srcdir/$_pkgname" || return 1

	# our ArchLinux distribution patch until we go upstream
	patch -Np0 -i ${srcdir}/ArchLinux.patch

	# buildfix for broken language settings in build
	patch -Np0 -i ${srcdir}/localize-ooo.diff

	# hotfixes not yet upstream
	#cp ${srcdir}/*.diff ${srcdir}/ooo-build-${_GOver}/patches/hotfixes/
	cp ${srcdir}/buildfix_64bit_system_libjpeg.diff ${srcdir}/$_pkgname/patches/hotfixes/

	./autogen.sh --with-distro=ArchLinux
	./download

	# export C(XX)FLAGS
	# http://www.openoffice.org/issues/show_bug.cgi?id=103205
	unset CFLAGS
	unset CXXFLAGS
#	export ARCH_FLAGS="$CFLAGS"

	if [ "$CARCH" = "x86_64" ]; then
	      EXTRAOPTS="--without-stlport"
	 else EXTRAOPTS="--with-stlport"
	fi

	#export CC="ccache gcc"
	#export CXX="ccache g++"
#	autoreconf
	./configure --with-distro=ArchLinux \
		--with-build-version="${_GOver} ArchLinux build-${pkgrel} (${_ootag})"\
		--with-srcdir=${srcdir} \
		--with-max-jobs=${MAKEFLAGS/-j/} \
		--with-installed-ooo-dirname="${_pkgname}" \
		--prefix=/usr --exec-prefix=/usr --sysconfdir=/etc \
		--with-docdir=/usr/share/doc/packages/"${_pkgname}" \
		--mandir=/usr/share/man \
		--with-lang="" \
		--with-dict=ALL\
		--with-binsuffix=no \
		--disable-ldap \
		--enable-cairo\
		--disable-kde\
		--enable-kde4\
		--enable-lockdown\
		--with-system-boost\
		--with-system-cairo\
		--enable-crashdump\
		--without-gpc\
		--enable-opengl \
		--enable-minimizer \
		--enable-presenter-console \
		--enable-pdfimport \
		--enable-wiki-publisher \
		--enable-ogltrans \
		--with-ant-home="/usr/share/java/apache-ant"\
		--with-system-saxon\
		--with-saxon-jar=/usr/share/java/saxon/saxon9he.jar\
		--with-system-lucene\
		--with-lucene-core-jar=/usr/share/java/lucene-core.jar\
		--with-lucene-analyzers-jar=/usr/share/java/lucene-analyzers.jar\
		--with-system-beanshell\
		--with-system-vigra\
		--with-system-altlinuxhyph\
		--with-system-lpsolve\
		--without-system-redland \
		--disable-binfilter \
		--with-gcc-speedup=ccache \
		--disable-strip \
		$EXTRAOPTS || return 1

		#--enable-dbgutil \
#		--with-system-mythes\
#		--with-system-graphite\
#		--with-tag=${_ootag}
#		--enable-report-builder \
#		--with-additional-sections="OOXMLExport"

	unset MAKEFLAGS

	LD_PRELOAD="" make  || return 1
}

package() {
	cd ${srcdir}/$__pkgname
	LD_PRELOAD="" make DESTDIR=${pkgdir} install || return 1

       # install all built dictionaries from source tree
       pushd ${srcdir}/$_pkgname/build/${_ootag}/dictionaries/unxlng?6.pro/bin/
       for i in `ls -1 dict-??.oxt`; do
         install -D -m644 $i ${pkgdir}/usr/lib/"${_pkgname}"/share/extension/install/$i || return 1
       done
       popd

	# install all other built extensions
	pushd ${srcdir}/$_pkgname/build/${_ootag}/solver/320/unxlng?6.pro/bin/
	install -m644 pdfimport/pdfimport.oxt ${pkgdir}/usr/lib/"${_pkgname}"/share/extension/install/pdfimport.oxt || return 1
	install -m644 swext/wiki-publisher.oxt ${pkgdir}/usr/lib/"${_pkgname}"/share/extension/install/wiki-publisher.oxt || return 1
	install -m644 minimizer/presentation-minimizer.oxt ${pkgdir}/usr/lib/"${_pkgname}"/share/extension/install/presentation-minimizer.oxt || return 1
	install -m644 presenter/presenter-screen.oxt ${pkgdir}/usr/lib/"${_pkgname}"/share/extension/install/presenter-screen.oxt || return 1
	popd
	
	# fix unopkg call for mktemp, #15410
	sed -i "s:\/bin\/mktemp:\/usr\/bin\/mktemp:" ${pkgdir}/usr/lib/go-openoffice/program/unopkg || return 1
	
	#fix http://bugs.archlinux.org/task/17656
	find ${pkgdir} -perm 444 -exec ls -lh {} \; 
	find ${pkgdir} -perm 444 -exec chmod 644 {} \;
	find ${pkgdir} -perm 555 -exec ls -lh {} \;
	find ${pkgdir} -perm 555 -exec chmod 755 {} \;
}
md5sums=('a1ff3d64d0ca95c62a15c07e7f19f193'
         'b005c4cf9f8e586539ca98c9cfe9bb77'
         '78ccc03d13a62919ae599e0983b53e31'
         '2ec32f0276dfe9b1e7c3c9d896a92e5b')
