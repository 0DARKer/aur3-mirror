# Maintainer:  Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks-pre
_pkgname=elinks
pkgver=0.12pre5
pkgrel=1
pkgdesc="Text-mode web browser with tabs, MIME support, scripting, and other advanced features (Prerelease version)"
arch=('i686' 'x86_64')
url="http://elinks.or.cz/"
license=('custom')

depends=('bzip2' 'expat>=2.0' 'gpm>=1.20.4' 'openssl' 'zlib' 'lua>=5.1.1' 'libidn' 'spidermonkey>=1.7.0' 'gnutls' 'python' 'perl' 'tre')
provides=("elinks=$pkgver")
conflicts=('elinks' 'elinks-git')

source=("http://elinks.or.cz/download/$_pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop")
sha256sums=('2d19547831098acff0c5f97f9fbc7912240f122961fd49fb8b76738417dd24d2'
            '6ed19da65ebbe59c334e92c8a6e6b8449c328aab2a9f65c1b5dda3a246d635ab')


build() {
	cd "$_pkgname-$pkgver"

	./configure --prefix=/usr --mandir=/usr/share/man --sysconfdir=/etc \
	      --disable-smb --without-x --enable-cgi --enable-leds --disable-256-colors \
	      --enable-html-highlight --disable-nntp --disable-true-color --disable-88-colors \
	      --disable-bittorrent --enable-fastmem --enable-exmode --without-lzma --with-perl --with-python \
	      --with-gnutls || return 1
	make || return 1
}
package() {
	cd "$_pkgname-$pkgver"

	make DESTDIR="$pkgdir" install || return 1
	rm -f "$pkgdir/usr/share/locale/locale.alias" || return 1

	install -Dm644 "$srcdir/$pkgname.desktop" \
	               "$pkgdir/usr/share/applications/$pkgname.desktop" || return 1
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING" || return 1
}
