# Maintainer: David Manouchehri <d@32t.ca>
# Submitter: josephgbr <rafael.f.f1@gmail.com>

pkgname=teighafileconverter
pkgver=3.09.1
pkgrel=2
pkgdesc="Free cross-platform application for the conversion of .dwg and .dxf files to/from different versions"
arch=('i686' 'x86_64')
url="http://www.opendesign.com/guestfiles/TeighaFileConverter"
license=('custom')
install=${pkgname}.install

if [ $CARCH == i686 ]; then
  depends=('qt4' 'bash' 'hicolor-icon-theme' 'libpng12')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-qt4' 'bash' 'hicolor-icon-theme' 'lib32-libpng12')
fi

_file=TeighaFileConverter_lnxX86_4.4dll_${pkgver}_i386.deb
source=(http://www.opendesign.com/files/guestdownloads/TeighaFileConverter/$_file)
sha512sums=('3f2202dead33d7a42ebb4f28cb40208928f3a9a6b532eddfc329cbb2652cda400d75c3e0b2e3b99267b715f5b400d195baed07c32e47a0cdd4ca9e586685e24e')

build() {
  rm -rf && mkdir ${pkgname}-${pkgver}
  tar xf data.tar.gz -C ${pkgname}-${pkgver}
}

package() {
  cd ${pkgname}-${pkgver}
  
  # persistent application storage (libs, executables, etc.)
  if [ $CARCH == i686 ]; then
    appstoragedir=usr/lib/teighafileconverter
  elif [ $CARCH == x86_64 ]; then
    appstoragedir=usr/lib32/teighafileconverter
  fi
  for file in $(ls usr/bin/TeighaFileConverter_${pkgver}/); do
    install -Dm755 usr/bin/TeighaFileConverter_${pkgver}/${file} \
        "${pkgdir}/${appstoragedir}/${file}"
  done
  
  # binary
  install -Dm755 usr/bin/TeighaFileConverter \
    "${pkgdir}/usr/bin/teighafileconverter"
  sed -e "s#usr/bin/TeighaFileConverter_${pkgver}#${appstoragedir}#" \
    -i "${pkgdir}/usr/bin/${pkgname}"
  
  # desktop item
  install -Dm644 usr/share/applications/TeighaFileConverter_${pkgver}.desktop \
    "${pkgdir}/usr/share/applications/teighafileconverter.desktop"
  sed -e "s#/TeighaFileConverter#/teighafileconverter#" \
    -i "${pkgdir}/usr/share/applications/teighafileconverter.desktop"
  
  # hicolor icons
  for icon in $(find usr/share/icons -type f); do
    install -Dm644 ${icon} \
        "${pkgdir}/${icon}"
  done
  
  # copyright
  install -Dm644 usr/share/doc/TeighaFileConverter/copyright \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
}
