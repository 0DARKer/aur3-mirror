# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=teighafileconverter
pkgver=3.5.1
pkgrel=1
pkgdesc="Free cross-platform application for the conversion of .dwg and .dxf files to/from different versions"
arch=('i686' 'x86_64')
url="http://www.opendesign.com/guestfiles/TeighaFileConverter"
license=('custom')

if [ $CARCH == i686 ]; then
  depends=('qt' 'bash')
elif [ $CARCH == x86_64 ]; then
  depends=('lib32-qt' 'bash')
fi

install=${pkgname}.install
source=(http://www.opendesign.com/files/guestdownloads/TeighaFileConverter/${pkgname}_${pkgver}_i386.deb)
md5sums=('bace4aed812794453a61100eb11091be')


build() {
  cd "${srcdir}"
  
  [ ! -d ${pkgname}-${pkgver} ] && mkdir ${pkgname}-${pkgver}
  
  ar xo ${pkgname}_${pkgver}_i386.deb
  tar xf data.tar.gz -C ${pkgname}-${pkgver}
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  # persistent application storage (libs, executables, etc.)
  if [ $CARCH == i686 ]; then
    appstoragedir=usr/lib/teighafileconverter
  elif [ $CARCH == x86_64 ]; then
    appstoragedir=usr/lib32/teighafileconverter
  fi
  for file in $(ls usr/bin/TeighaFileConverter_${pkgver}/); do
    install -Dm755 usr/bin/TeighaFileConverter_${pkgver}/${file} \
        "${pkgdir}/${appstoragedir}/${file}"
  done
  
  # binary
  install -Dm755 usr/bin/TeighaFileConverter \
    "${pkgdir}/usr/bin/teighafileconverter"
  sed -e "s#usr/bin/TeighaFileConverter_${pkgver}#${appstoragedir}#" \
    -i "${pkgdir}/usr/bin/${pkgname}"
  
  # desktop item
  install -Dm644 usr/share/applications/TeighaFileConverter_3.5.1.desktop \
    "${pkgdir}/usr/share/applications/teighafileconverter.desktop"
  sed -e "s#/TeighaFileConverter#/teighafileconverter#" \
    -i "${pkgdir}/usr/share/applications/teighafileconverter.desktop"
  
  # hicolor icons
  for icon in $(find usr/share/icons -type f); do
    install -Dm644 ${icon} \
        "${pkgdir}/${icon}"
  done
  
  # copyright
  install -Dm644 usr/share/doc/TeighaFileConverter/copyright \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
  
}
