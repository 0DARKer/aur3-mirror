# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Flamelab <panosfilip@gmail.com>

pkgname=libkgeomap-frameworks-git
pkgver=r617.fd89338
pkgrel=1
pkgdesc='A wrapper around world map components for browsing and arranging photos on a map. KF5 Frameworks branch. (GIT Version)'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kio' 'marble-qt-git')
makedepends=('git' 'automoc4' 'cmake')
source=('git://anongit.kde.org/libkgeomap#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd libkgeomap
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake  ../libkgeomap \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=NO
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}