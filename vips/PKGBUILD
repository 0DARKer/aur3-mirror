# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: damir <damir@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>
pkgname=vips
pkgver=7.40.3
pkgrel=1
pkgdesc="A free image processing system"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.vips.ecs.soton.ac.uk/index.php"
depends=('libxml2' 'fftw' 'imagemagick' 'orc' 'openexr' 'pango' 'libexif')
# less used depends: 'libwebp' 'cfitsio'
# optional makedepends: 'v4l-utils' 'python2'
# minimal depends: 'libxml2'
options=('!libtool')
source=("http://www.vips.ecs.soton.ac.uk/supported/${pkgver%.*}/$pkgname-$pkgver.tar.gz")
md5sums=('d0d7fa241a4916b155f8180f1eaa2d21')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  # if we are building with python, make sure only python2 is used
  export PYTHON=`pacman -Qq python2` || true
  if [ "$PYTHON" != "" ]; then
    ./configure --prefix=/usr
  else
    ./configure --prefix=/usr --without-python
  fi

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

