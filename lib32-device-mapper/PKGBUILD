# Maintainer: Bill Fraser <wfraser@codewise.org>

_pkgbasename='device-mapper'
pkgname='lib32-device-mapper'
pkgver=2.02.85
_pkgverlvm=${pkgver}
pkgrel=2
arch=('x86_64')
pkgdesc="Device mapper userspace library for multilib"
url="http://sourceware.org/dm/"
depends=('glibc' 'udev')
license=('GPL2' 'LGPL2.1')
groups=('base')
conflicts=('mkinitcpio<0.5.99')
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${_pkgverlvm}.tgz
        Be-quiet-on-removing-cache-on-read-only-fs.diff)
md5sums=('91785ca438e5ce679dd3a386b183d552'
         '6db89da27928d2415a8e26cd0a842712')
sha1sums=('43ba2a3be84b2e897ae6b47b0b0be7e212216be7'
          '6d7dd04be291d7537235704c816a701fd5beacc0')

build() {
  export CC="gcc -m32"
  export LD="ld -m32"
  cd "${srcdir}/LVM2.${_pkgverlvm}"
  sed -i 's|/usr/bin/tr|/bin/tr|' scripts/lvmdump.sh
  patch -p1 -i ../Be-quiet-on-removing-cache-on-read-only-fs.diff
  unset LDFLAGS
  ./configure --prefix=/usr --libdir=/usr/lib32 --enable-dmeventd --enable-cmdlib --enable-pkgconfig
  make device-mapper
}

package() {
  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" install_device-mapper
  rm -r ${pkgdir}/usr/{include,lib,sbin,share}
  ln -s libdevmapper.so.1.02 ${pkgdir}/usr/lib32/libdevmapper.so
  ln -s libdevmapper-event.so.1.02 ${pkgdir}/usr/lib32/libdevmapper-event.so
}
