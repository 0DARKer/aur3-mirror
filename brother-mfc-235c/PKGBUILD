# Maintainer: libernux <dutchman55@gmx.com>
pkgname="brother-mfc-235c"
pkgver="1.0.1"
pkgrel=1
pkgdesc="LPR and CUPS driver for the Brother MFC-235c"
url="http://solutions.brother.com/linux/en_us/"
arch=("i686" "x86_64")
license=('custom:brother commercial license')
depends=("cups")
install="$pkgname.install"
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/mfc235clpr-$pkgver-$pkgrel.i386.rpm"
	"http://www.brother.com/pub/bsc/linux/dlf/mfc235ccupswrapper-$pkgver-$pkgrel.i386.rpm"
	'cupswrapper-license.txt'
	'lpr-license.txt'
)
md5sums=(
	'c48461fed0df3eb2dc429aab566db41d'
	'e1b738524614890bcaa2965e285be533'
	'97ad0cffd216059e9d1d3121899d8646'
	'5e87a3dc0f3e3438c088eda0f3565f0d'
)

build() {
	cd "$srcdir/usr/local/Brother/Printer/mfc235c/cupswrapper"
#  generate the ppd
	echo '#! /bin/sh' > genppd
	echo 'ppd_file_name=brmfc235c.ppd' >> genppd
	echo 'touch $ppd_file_name' >> genppd
	sed -n '/^printer_model/,/^device_model/p' cupswrappermfc235c >> genppd
	sed -n '/^cat <<ENDOFPPDFILE1/,/^ENDOFPPDFILE_END/p' cupswrappermfc235c >>genppd
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genppd
	chmod +x genppd
	./genppd
#  generate the wrapper
	echo '#! /bin/sh' > genwrapper
	echo 'brotherlpdwrapper=brlpdwrappermfc235c'  >> genwrapper
	echo 'touch $brotherlpdwrapper' >>genwrapper
	sed -n '/^printer_model/p' cupswrappermfc235c >>genwrapper
	sed -n '/^device_model/p' cupswrappermfc235c >>genwrapper
	sed -n '/cat <<!ENDOFWFILTER!/,/!ENDOFWFILTER!/p' cupswrappermfc235c >>genwrapper
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genwrapper
	chmod +x genwrapper
	./genwrapper
#  generate other
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/bin/brprintconf_mfc235c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc235c/lpd/filtermfc235c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc235c/lpd/brmfc235cfilter"
	install -d "$srcdir/usr/share"
	mv "$srcdir/usr/local/Brother/" "$srcdir/usr/share/brother"
	rm "$srcdir/usr/share/brother/Printer/mfc235c/cupswrapper/cupswrappermfc235c"
	rm "$srcdir/usr/share/brother/Printer/mfc235c/cupswrapper/genppd"
	rm "$srcdir/usr/share/brother/Printer/mfc235c/cupswrapper/genwrapper"
	rm "$srcdir/usr/share/brother/Printer/mfc235c/inf/setupPrintcapij"
	rm -rf "$srcdir/usr/local"
}

package() {
	cd "$pkgdir"
	cp -R "$srcdir/usr" "$pkgdir"
	install -m 644 -D "$srcdir/usr/share/brother/Printer/mfc235c/cupswrapper/brmfc235c.ppd" "$pkgdir/usr/share/cups/model/brmfc235c.ppd"
	install -m 755 -D "$srcdir/usr/share/brother/Printer/mfc235c/cupswrapper/brlpdwrappermfc235c" "$pkgdir/usr/lib/cups/filter/brlpdwrappermfc235c"
	install -m 644 -D "$startdir"/cupswrapper-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/cupswrapper-licence.txt"
	install -m 644 -D "$startdir"/lpr-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/lpr-licence.txt"
}