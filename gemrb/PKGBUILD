# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=gemrb
pkgver=0.7.1
pkgrel=2
pkgdesc="OSS implementation of Bioware's Infinity Engine which supports eg. Baldur's Gate"
arch=('i686' 'x86_64')
url="http://www.gemrb.org/"
license=('GPL')
depends=('python2' 'openal' 'libpng' 'sdl_mixer')
makedepends=('cmake')
install=gemrb.install
options=(!libtool)
source=(http://downloads.sourceforge.net/sourceforge/gemrb/$pkgname-$pkgver.tar.gz \
        force_python_2.7.diff)
md5sums=('435c73827090139eb91137d3e6999bfa'
         'e3fb4234634619556407be2f1f7ff453')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # force the use of python 2, otherwise it may try to use python 3 which doesn't work
  patch -Np1 < "$srcdir/force_python_2.7.diff" || true

  if [ ! -d "$srcdir/build" ] ; then
    mkdir "$srcdir/build"
  fi
  cd "$srcdir/build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr ../$pkgname-$pkgver
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
}

