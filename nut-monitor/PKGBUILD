# Maintainer: Dan Ziemba <zman0900@gmail.com>

pkgname=nut-monitor
pkgver=2.7.1
pkgrel=1
pkgdesc="GUI to manage devices connected a NUT server"
arch=('any')
url="http://www.networkupstools.org/"
license=('GPL2')
depends=('network-ups-tools>=2.7.1' 'python2')
makedepends=('desktop-file-utils')
source=("http://www.networkupstools.org/source/2.7/nut-$pkgver.tar.gz")
options=('!emptydirs' '!libtool')
md5sums=('e64cf832063ecd6645683fc2ac2bd564')

prepare() {

  cd $srcdir/nut-$pkgver
  sed -i 's|=NUT-Monitor|=nut-monitor|'  scripts/python/app/nut-monitor.desktop
  sed -i 's|=nut-monitor.png|=nut-monitor|'  scripts/python/app/nut-monitor.desktop
  sed -i "s|sys.argv\[0\]|'/usr/share/nut-monitor/nut-monitor'|" scripts/python/app/NUT-Monitor
  sed -i 's|/usr/bin/env python|/usr/bin/env python2.7|' scripts/python/app/NUT-Monitor

}

package() {

  cd $srcdir/nut-$pkgver
  install -d -m 755 $pkgdir/usr/share/nut-monitor/pixmaps
  install -d -m 755 $pkgdir/usr/share/pixmaps
  install -d -m 755 $pkgdir/usr/bin
  
  install -D -m644 scripts/python/module/PyNUT.py $pkgdir/usr/lib/python2.7/site-packages/PyNUT.py
  
  install -m 755 scripts/python/app/NUT-Monitor $pkgdir/usr/share/nut-monitor/nut-monitor
  install -m 644 scripts/python/app/gui-1.3.glade $pkgdir/usr/share/nut-monitor
  install -m 644 scripts/python/app/pixmaps/* $pkgdir/usr/share/nut-monitor/pixmaps/
  install -m 644 scripts/python/app/nut-monitor.png $pkgdir/usr/share/pixmaps/nut-monitor.png
  
  desktop-file-install --dir=$pkgdir/usr/share/applications scripts/python/app/nut-monitor.desktop
  
  ln -s /usr/share/nut-monitor/nut-monitor $pkgdir/usr/bin/nut-monitor

}
