# Maintainer: Maks Verver <maksverver@geocities.com>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>
pkgname=golly
pkgver=2.3
pkgrel=1
pkgdesc="A simulator for Conway's Game of Life and other cellular automata"
arch=('i686' 'x86_64')
url="http://golly.sourceforge.net/"
license=('GPL')
depends=('wxgtk>=2.8' 'wxgtk<2.9')
makedepends=('perl' 'python2')
optdepends=('perl: for Perl scripting support'
            'python2: for Python scripting support')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-src.tar.gz)
md5sums=('20495bafe63f4f3f1807a5c52a280ba5')

build() {
    cd "$srcdir/$pkgname-$pkgver-src"

    ./configure \
        --with-perl-shlib=/usr/lib/perl5/core_perl/CORE/libperl.so \
        --with-python-shlib=libpython2.7.so \
        --prefix=/usr

    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver-src"

    make DESTDIR="$pkgdir" install
}
