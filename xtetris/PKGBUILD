# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=xtetris
pkgver=2.6
pkgrel=1
pkgdesc="Classical tetris game for X11"
arch=('i686' 'x86_64')
url="http://happypenguin.org/show?XTetris"
license=('custom')
depends=('xorg-server' 'libxaw' 'xbitmaps')
makedepends=('imake')
source=("http://www.ibiblio.org/pub/Linux/games/arcade/tetris/${pkgname}-${pkgver}.tar.gz")
md5sums=('9547dd17d0e53e295dd61f794b7acdb2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's_$(LIBDIR)_/var/lib/X11_g' Imakefile
  sed -i 's_$(INCDIR)_/usr/include/X11_g' Imakefile
  sed -i 's_-UCOLOR > Xtetris.ad_-DCOLOR > Xtetris.ad_g' Imakefile
  xmkmf || return 1
  make || return 1
  make depend || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -Dm666 xtetris-scores "${pkgdir}/var/lib/X11/xtetris-scores"
  install -Dm644 ${pkgname}.man "${pkgdir}/usr/share/man/man6/${pkgname}.6"
  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
