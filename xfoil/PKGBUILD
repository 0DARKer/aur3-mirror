# Contributor: pfm <nl081130@yahoo.de>
# Based on the Gentoo ebuild: http://gentoo-portage.com/sci-physics/xfoil

pkgname=xfoil
pkgver=6.97
pkgrel=3
pkgdesc="Program for design and analysis of subsonic airfoils."
arch=('i686' 'x86_64')
url="http://web.mit.edu/drela/Public/web/xfoil/"
license=('GPL')
depends=('gcc-libs' 'libx11')
makedepends=('gcc-fortran' 'sed' 'patch')
source=("http://web.mit.edu/drela/Public/web/xfoil/$pkgname$pkgver.tar.gz" \
        "xfoil-6.96-gfortran.patch" "xfoil-6.97-overflow.patch")
md5sums=('b9ce001ed838be880262c75038d489c5'
         'b66e0eab1473537c7813111fe856b693'
         '6c993590ae3303b7e59968115682e01d')

_FORTRANC=gfortran

build() {

  cd "$srcdir/Xfoil"

  patch -Np0 -i ../xfoil-6.96-gfortran.patch || return 1
  patch -Np0 -i ../xfoil-6.97-overflow.patch || return 1

  sed -i \
    -e '/^FC/d' \
    -e '/^CC/d' \
    -e '/^FFLAGS/d' \
    -e '/^CFLAGS/d' \
    -e 's/^\(FFLOPT .*\)/FFLOPT = $(FFLAGS)/g' \
    {bin,plotlib,orrs/bin}/Makefile plotlib/config.make \
    || return 1
  sed -i \
     -e 's:/var/local/codes/orrs/osmap.dat:/usr/share/xfoil/orrs/osmap.dat:' \
     orrs/src/osmap.f || return 1
  
  export FC="${_FORTRANC}" F77="${_FORTRANC}"

  cd "$srcdir/Xfoil/orrs/bin"
    make FLG="${FFLAGS}" FTNLIB="${LDFLAGS}" OS || return 1
  cd "$srcdir/Xfoil/orrs"
    bin/osgen osmaps_ns.lst || return 1
  cd "$srcdir/Xfoil/plotlib"
    make CFLAGS="${CFLAGS} -DUNDERSCORE" || return 1
  cd "$srcdir/Xfoil/bin"
    for i in xfoil pplot pxplot; do
      make \
        PLTOBJ="../plotlib/libPlt.a" \
        CFLAGS="${CFLAGS} -DUNDERSCORE" \
        FTNLIB="${LDFLAGS}" \
        ${i} || return 1
    done

  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/share/xfoil/{doc,orrs}

  cd "$srcdir/Xfoil/bin"
  install -m755 xfoil $pkgdir/usr/bin
  install -m755 pplot $pkgdir/usr/bin
  install -m755 pxplot $pkgdir/usr/bin

  cd "$srcdir/Xfoil/orrs"
  install -m644 osm*.dat $pkgdir/usr/share/xfoil/orrs

  cd "$srcdir/Xfoil"
  install -m644 xfoil_doc.txt $pkgdir/usr/share/xfoil/doc
}
