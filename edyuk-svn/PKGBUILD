# Maintainer: willemw <willemw12@gmail.com>
# Contributor: Sphax <zecmerquise@gmail.com>

pkgname=edyuk-svn
pkgver=1053
pkgrel=1
pkgdesc="Fully-featured Qt4 IDE"
arch=(i686 x86_64)
url="http://www.edyuk.org"
license="GPL"
depends=('qt4')
makedepends=('gcc')
provides=($pkgname)
conflicts=($pkgname)
#install=$pkgname.install
source=($pkgname::svn+http://edyuk.svn.sf.net/svnroot/edyuk/trunk
        installpri.patch)
md5sums=('SKIP'
         '66ce655bc8ad981f724150b06aea9ae4')

pkgver() {
  cd $pkgname
  svnversion | tr -d [A-z]
}

build() {
  cd $pkgname

  # Fix path to Qt mkspecs directory
  # Fix directory for icons on KDE4
  patch -Np0 -i ../installpri.patch

  qmake-qt4
  make

  # Second run of qmake, needed for lib directory to be installed correctly
  qmake-qt4
}

package() {
  cd $pkgname
  make INSTALL_ROOT="$pkgdir" install

  # All libs files are installed as binary, but only libedyuk.so.1.0.0
  # is supposed to be a binary, the others are supposed to be symlinks
  # Fix this now because of ldconfig reporting an error when installing
  # with pacman
  cd "$pkgdir/usr/lib"
  rm libedyuk.{so,so.1,so.1.1}
  ln -s libedyuk.so.1.1.0 libedyuk.so.1.1
  ln -s libedyuk.so.1.1   libedyuk.so.1
  ln -s libedyuk.so.1     libedyuk.so
}


#TODO Update mime database: usr/share/mime/packages/edyuk.xml

