# Maintainer: <grinko@gmx.at>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=modemmanager-git
pkgver=20130429
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/ModemManager/ModemManager/"
license=('GPL2')
depends=('dbus-glib' 'udev' 'ppp' 'libqmi')
makedepends=('intltool' 'git' 'gtk-doc')
options=('!libtool')
conflicts=('modemmanager')
provides=('modemmanager')
source=()
md5sums=()

_gitroot="git://anongit.freedesktop.org/ModemManager/ModemManager"
_gitname="modemmanager"

build() {
  cd "${srcdir}"

  if [[ -d $_gitname ]]; then
    (cd $_gitname && git pull origin)
  else
    git clone $_gitroot $_gitname
  fi

  [ -d $_gitname-build ] && rm -rf $_gitname-build
  cp -rf $_gitname $_gitname-build
  cd $_gitname-build
  ./autogen.sh
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd ${srcdir}/$_gitname-build
  make DESTDIR="$pkgdir" install
}
