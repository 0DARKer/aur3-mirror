# Maintainer: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: bugix

pkgname=openni-git
pkgver=20110805
pkgrel=1
pkgdesc="The OpenNI Framework provides the interface for physical devices and for middleware components"
arch=('i686' 'x86_64')
url="http://www.openni.org/"
license=('GPL')
depends=('freeglut' 'libusb' 'java-environment')
makedepends=('git')
provides=('openni')
conflicts=('openni')
install="openni.install"
source=()
md5sums=()

_gitroot="git://github.com/OpenNI/OpenNI.git"
_gitname="openni"
_gitbranch="unstable"
#_gitbranch="master"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone -b ${_gitbranch} ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build/Platform/Linux-x86/Build"

  # BUILD
  make -j1 NI_CONF_DIR=/etc || return 1
}

package() {
  install -d -m755 "${pkgdir}/usr/"{lib,bin,include/ni/Linux-x86}
  install -d -m755 "${pkgdir}/var/lib/ni"

  cd "${srcdir}/${_gitname}-build/Include"
  install *.h "${pkgdir}/usr/include/ni"
  install Linux-x86/* "${pkgdir}/usr/include/ni/Linux-x86"

  cd "${srcdir}/${_gitname}-build/Platform/Linux-x86/Build"
  # install CommonMakefile "${pkgdir}/usr/include/ni"

  cd "${srcdir}/${_gitname}-build/Platform/Linux-x86/Bin/Release"
  install niLicense niReg NiViewer "${pkgdir}/usr/bin"

  install libnimCodecs.so libnimMockNodes.so libnimRecorder.so libOpenNI.so "${pkgdir}/usr/lib"
}
