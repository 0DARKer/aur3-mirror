# Maintainer: Harvey <harv|at|gmx.de>
# Contributors: Dan Serban, Dany Martineau, Jordi De Groof, RTFreedman

pkgname="mp3diags"
pkgver="1.1.17.073"
pkgrel=1
pkgdesc="Identify issues with your MP3 files, fix those issues and make other changes"
url="http://mp3diags.sourceforge.net/"
license=("GPL")
arch=("i686" "x86_64")
depends=("boost-libs" "qt")
makedepends=("boost" "sed")
optdepends=('mp3gain: MP3 normalization support')
source=("http://downloads.sourceforge.net/project/mp3diags/unstable/mp3diags-src/MP3Diags-unstable-${pkgver}.tar.gz")
sha1sums=('d3b2bd60f424042010c04e6cc2589ad1dc29793d')

build() {
	cd "${srcdir}/MP3Diags-unstable-${pkgver}"
	# first extinct a gcc 4.7 bug:
	# See: http://sourceforge.net/apps/mantisbt/mp3diags/view.php?id=93
	sed "104 s/clearPtrContainer(m_lpNodes);/pearl::clearPtrContainer(m_lpNodes);/" -i src/SimpleSaxHandler.h	
	./AdjustMt.sh
	qmake
	make
}

package() {
	cd "${srcdir}/MP3Diags-unstable-${pkgver}"
	mkdir -p "${pkgdir}/usr/bin" "${pkgdir}/usr/share/applications"
	install -p -m755 "bin/MP3Diags-unstable" "${pkgdir}/usr/bin/"
	install -p -m644 "desktop/MP3Diags-unstable.desktop" "${pkgdir}/usr/share/applications/"


	for i in "16" "22" "24" "32" "36" "40" "48"; do
		mkdir -p "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps"
		install -p -m644 "desktop/MP3Diags-unstable${i}.png" "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/MP3Diags-unstable.png"
	done
}
