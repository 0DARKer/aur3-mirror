pkgname=mingw-w64-atk
pkgver=2.16.0
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility (mingw-w64)"
arch=(any)
license=("LGPL")
makedepends=(mingw-w64-configure)
depends=(mingw-w64-glib2)
options=(!strip !buildflags staticlibs)
source=("http://ftp.gnome.org/pub/gnome/sources/atk/${pkgver:0:4}/atk-$pkgver.tar.xz")
url="https://developer.gnome.org/atk/"
sha256sums=('095f986060a6a0b22eb15eef84ae9f14a1cf8082488faa6886d94c37438ae562')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
	cd atk-$pkgver
	for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure \
      --disable-glibtest
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/atk-${pkgver}/build-${_arch}"
    make -j1 DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -exec rm {} \;
    find "$pkgdir/usr/${_arch}" -name '*.dll' -exec ${_arch}-strip --strip-unneeded {} \;
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
