pkgname=mingw-w64-gnutls
pkgver=3.1.0
pkgrel=3
pkgdesc="A library which provides a secure layer over a reliable transport layer (mingw-w64)"
arch=(any)
url="http://www.gnu.org/software/gnutls/"
license=("GPL3" "LGPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-libtasn1 mingw-w64-readline mingw-w64-zlib mingw-w64-nettle)
options=(!libtool !strip !buildflags)
source=("ftp://ftp.gnu.org/gnu/gnutls/${pkgname#mingw-w64-}-${pkgver}.tar.xz")
md5sums=('db88d8fc44e44be4a0a14b24aa204bbf')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  sed -i "s|Wincrypt.h|wincrypt.h|g" "$srcdir/${pkgname#mingw-w64-}-$pkgver/lib/system.c"
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-guile \
      --with-zlib
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}