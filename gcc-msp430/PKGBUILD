# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=gcc-msp430
pkgver=4.4.5
pkgrel=4
pkgdesc="GNU toolchain for the TI MSP430 processor"
arch=('i686' 'x86_64')
url="http://mspgcc4.sourceforge.net/"
license=('GPL')
depends=('binutils-msp430')
options=('!strip' '!emptydirs' '!libtool')

_mspgcc4_release=20110312
_mspgcc4_path=mspgcc4-${_mspgcc4_release}
_gcc_version=${pkgver}
_gcc_patch_folder=gcc-4.x
_gmp_version=4.3.2
_mpfr_version=2.4.2
_gnu_mirror="http://ftpmirror.gnu.org"

source=("${_mspgcc4_path}.tar.bz2::http://sourceforge.net/projects/mspgcc4/files/mspgcc4/${_mspgcc4_path}.tar.bz2/download"
        "${_gnu_mirror}/gcc/gcc-${_gcc_version}/gcc-${_gcc_version}.tar.bz2"
        "${_gnu_mirror}/gmp/gmp-${_gmp_version}.tar.bz2"
        "http://www.mpfr.org/mpfr-${_mpfr_version}/mpfr-${_mpfr_version}.tar.bz2")
sha1sums=('8883f7932e395809ca32592c54112132ca140f9c'
          '2b1427a932a620c909d74f1e4821ed90c90fd350'
          'c011e8feaf1bb89158bd55eaabd7ef8fdd101a2c'
          '7ca93006e38ae6e53a995af836173cf10ee7c18c')
md5sums=('333a9b58d7a1db2138fb95015ab06a4f'
         '44b3192c4c584b9be5243d9e8e7e0ed1'
         'dd60683d7057917e34630b4a787932e8'
         '89e59fe665e2b3ad44a6789f40b059a0')

build() {
  msg "Building gcc"

  [[ -d ${srcdir}/gcc-${_gcc_version}/gmp  ]] && rm -fr ${srcdir}/gcc-${_gcc_version}/gmp
  [[ -d ${srcdir}/gcc-${_gcc_version}/mpfr ]] && rm -fr ${srcdir}/gcc-${_gcc_version}/mpfr
  mv ${srcdir}/gmp-${_gmp_version}   ${srcdir}/gcc-${_gcc_version}/gmp
  mv ${srcdir}/mpfr-${_mpfr_version} ${srcdir}/gcc-${_gcc_version}/mpfr
  cp -rf ${srcdir}/${_mspgcc4_path}/ports/${_gcc_patch_folder}/* ${srcdir}/gcc-${_gcc_version}/

  if [[ -e ${srcdir}/${_mspgcc4_path}/gcc-${_gcc_version}.patch ]] ; then
    cd ${srcdir}/gcc-${_gcc_version}
    msg2 "Applying ${srcdir}/${_mspgcc4_path}/gcc-${_gcc_version}.patch"
    patch -N -p1 < ${srcdir}/${_mspgcc4_path}/gcc-${_gcc_version}.patch
  fi

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  cd ${srcdir}/gcc-${_gcc_version}
  rm -fr build
  mkdir build
  cd build
  ../configure --disable-libssp \
               --disable-nls \
               --target=msp430 \
               --enable-languages=c,c++ \
               --infodir=/usr/share/info \
               --libdir=/usr/lib \
               --libexecdir=/usr/lib \
               --mandir=/usr/share/man \
               --prefix=/usr \
               --with-gnu-as \
               --with-gnu-ld \
               --with-as=/usr/bin/msp430-as \
               --with-ld=/usr/bin/msp430-ld \
               --with-pkgversion="MSPGCC4_${_mspgcc4_release}"
  make
}

package() {
  msg "Begin packaging"

  cd ${srcdir}/gcc-${_gcc_version}/build
  make DESTDIR=${pkgdir} install
  cd ${srcdir} && rm -fr ${srcdir}/gcc-${_gcc_version}/build

  rm -f ${pkgdir}/usr/lib/libiberty.a
  rm -rf ${pkgdir}/usr/share/man/man7
  rm -rf ${pkgdir}/usr/share/info

  _docdir=${pkgdir}/usr/share/doc/${pkgname}

  msg "Stripping debugging symbols from binaries"
  local binary
  find ${pkgdir} -type f 2>/dev/null | while read binary ; do
    case "$(file -biz "$binary")" in
      *compressed-encoding*)      # Skip compressed binarys
        ;;
      *application/x-executable*) # Binaries
        /usr/bin/strip "$binary" >/dev/null 2>&1 ;;
    esac
  done
}

# vim:set sts=2 ts=2 sw=2 et:
