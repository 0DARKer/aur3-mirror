# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=gcc-msp430
pkgver=4.5.3
pkgrel=7
pkgdesc="GNU toolchain for the TI MSP430 processor"
arch=('i686' 'x86_64')
url="http://mspgcc4.sourceforge.net/"
license=('GPL')
makedepends=('binutils-msp430')
depends=('elfutils' 'libmpc')
options=(!strip !emptydirs !libtool !buildflags)

_mspgcc_ver=20110716
_gnu_mirror="http://ftpmirror.gnu.org"
_sf_base="http://sourceforge.net/projects/mspgcc/files"
_patches_base="${_sf_base}/Patches/LTS/${_mspgcc_ver}"

_patches=("msp430-gcc-${pkgver}-20110706-sf3370978.patch"
          "msp430-gcc-${pkgver}-20110706-sf3390964.patch"
          "msp430-gcc-${pkgver}-20110706-sf3394176.patch"
          "msp430-gcc-${pkgver}-20110706-sf3396639.patch"
          "msp430-gcc-${pkgver}-20110706-sf3409864.patch"
          "msp430-gcc-${pkgver}-20110706-sf3417263.patch"
          "msp430-gcc-${pkgver}-20110706-sf3431602.patch"
          "msp430-gcc-${pkgver}-20110706-sf3433730.patch")

source=("http://sourceforge.net/projects/mspgcc/files/mspgcc/mspgcc-${_mspgcc_ver}.tar.bz2"
        "${_gnu_mirror}/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2"
        "${_patches[0]}::${_patches_base}/${_patches[0]}/download"
        "${_patches[1]}::${_patches_base}/${_patches[1]}/download"
        "${_patches[2]}::${_patches_base}/${_patches[2]}/download"
        "${_patches[3]}::${_patches_base}/${_patches[3]}/download"
        "${_patches[4]}::${_patches_base}/${_patches[4]}/download"
        "${_patches[5]}::${_patches_base}/${_patches[5]}/download"
        "${_patches[6]}::${_patches_base}/${_patches[6]}/download"
        "${_patches[7]}::${_patches_base}/${_patches[7]}/download")
sha1sums=('db625da1295fa7b64fe687420d908e532e72439c'
          '73c45dfda5eef6b124be53e56828b5925198cc1b'
          '7d1b0adb04bf2c1bf9c3b2dad07ff4f1eaf47aee'
          'b158623b7f10a23e2e6f144b2822e1733bd27b76'
          'ed5eccad295d7bb8e883e13c0c8ff90fb156d642'
          'ce812f349084a887f27e99b5a3277545da52f5ad'
          'd2402a262ba5455324fdab9107bcbd297994fa55'
          '032e6dfd1665aa70ac3cde75233367cb2a68f7f9'
          'd6e8e4c3b1ab64e4068408a36a6d74e868a148de'
          '84a69a42e168c97e99d6b120db4a48fe4fabaaf3')

_builddir="${srcdir}/build"

build() {
  _patch_name="msp430-gcc-${pkgver}-20110706.patch"
  (cd "${srcdir}/gcc-${pkgver}" &&
    patch -p1 < "${srcdir}/mspgcc-${_mspgcc_ver}/${_patch_name}" &&
    for patch in ${_patches[@]} ; do
      msg "Applying ${patch}"
      patch -p1 < "${srcdir}/${patch}"
    done)

  rm -frv ${_builddir}
  mkdir -p ${_builddir} && cd ${_builddir}
  "${srcdir}/gcc-${pkgver}/configure" \
               --prefix=/usr \
               --infodir=/usr/share/info \
               --mandir=/usr/share/man \
               --disable-libssp \
               --disable-nls \
               --target=msp430 \
               --enable-languages=c,c++ \
               --with-gnu-as \
               --with-gnu-ld \
               --with-as=/usr/bin/msp430-as \
               --with-ld=/usr/bin/msp430-ld \
               --with-pkgversion="mspgcc_${_mspgcc_ver}"
  make
}

package() {
  cd ${_builddir}
  make DESTDIR=${pkgdir} install

  rm -f  ${pkgdir}/usr/lib/libiberty.a
  rm -rf ${pkgdir}/usr/share/man/man7
  rm -rf ${pkgdir}/usr/share/info

  msg "Stripping debugging symbols from binaries"
  local binary
  find ${pkgdir} -type f 2>/dev/null | while read binary ; do
    case "$(file -biz "$binary")" in
      *compressed-encoding*)      # Skip compressed binarys
        ;;
      *application/x-executable*) # Binaries
        /usr/bin/strip "$binary" >/dev/null 2>&1 ;;
    esac
  done
}

# vim:set sts=2 ts=2 sw=2 et:
