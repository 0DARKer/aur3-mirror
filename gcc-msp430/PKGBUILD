# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=gcc-msp430
pkgver=4.5.2
pkgrel=1
pkgdesc="GNU toolchain for the TI MSP430 processor"
arch=('i686' 'x86_64')
url="http://mspgcc4.sourceforge.net/"
license=('GPL')
makedepends=('binutils-msp430')
depends=('elfutils' 'libmpc')
options=('!strip' '!emptydirs' '!libtool' !buildflags)

_mspgcc_ver=20110612
_gnu_mirror="http://ftpmirror.gnu.org"

source=("http://sourceforge.net/projects/mspgcc/files/mspgcc/mspgcc-${_mspgcc_ver}.tar.bz2"
        "${_gnu_mirror}/gcc/gcc-${pkgver}/gcc-${pkgver}.tar.bz2")
sha1sums=('b00fff4f9c6311a9b467b4b428efc0c41822b6f6'
          'ad5c440526c98fd17a74eab80c031af6b64d9c90')

_builddir="${srcdir}/build"

build() {
  _patch_name="msp430-gcc-${pkgver}-${_mspgcc_ver}.patch"
  (cd "${srcdir}/gcc-${pkgver}" &&
    patch -p1 < "${srcdir}/mspgcc-${_mspgcc_ver}/${_patch_name}")

  rm -frv ${_builddir}
  mkdir -p ${_builddir} && cd ${_builddir}
  "${srcdir}/gcc-${pkgver}/configure" \
               --disable-libssp \
               --disable-nls \
               --target=msp430 \
               --enable-languages=c,c++ \
               --infodir=/usr/share/info \
               --libdir=/usr/lib \
               --libexecdir=/usr/lib \
               --mandir=/usr/share/man \
               --prefix=/usr \
               --with-gnu-as \
               --with-gnu-ld \
               --with-as=/usr/bin/msp430-as \
               --with-ld=/usr/bin/msp430-ld \
               --with-pkgversion="mspgcc_${_mspgcc_ver}"
  make
}

package() {
  cd ${_builddir}
  make DESTDIR=${pkgdir} install

  rm -f ${pkgdir}/usr/lib/libiberty.a
  rm -rf ${pkgdir}/usr/share/man/man7
  rm -rf ${pkgdir}/usr/share/info

  msg "Stripping debugging symbols from binaries"
  local binary
  find ${pkgdir} -type f 2>/dev/null | while read binary ; do
    case "$(file -biz "$binary")" in
      *compressed-encoding*)      # Skip compressed binarys
        ;;
      *application/x-executable*) # Binaries
        /usr/bin/strip "$binary" >/dev/null 2>&1 ;;
    esac
  done
}

# vim:set sts=2 ts=2 sw=2 et:
