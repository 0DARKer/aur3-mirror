# Maintainer: ifaigios <ifaigios_at_gmail_dot_com>

pkgname=upower-git-systemd
pkgver=20121118
pkgrel=1
pkgdesc="UPower from GIT master branch, with no pm-utils dependency"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('systemd' 'libusb' 'polkit' 'dbus-glib' 'libimobiledevice')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'systemd' 'gtk-doc' 'git')
provides=('upower=0.9.19')
conflicts=('upower')
options=('!libtool')
source=("drop-pm-utils.patch")
md5sums=('ecc6fb6eb004a6d8930f9e37c31afa3b')

_gitroot="git://anongit.freedesktop.org/upower"
_gitname="upower"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -Np1 -i "$srcdir/drop-pm-utils.patch"
  export NOCONFIGURE="true"

  ./autogen.sh

  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$_gitname \
    --disable-static

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
