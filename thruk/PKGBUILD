# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>

_pkgname=Thruk
pkgname=thruk
_pkgver=1.84-3
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Multibackend monitoring webinterface for Naemon, Nagios, Icinga and Shinken"
arch=('any')
url="http://thruk.org"
license=('GPL2')
depends=('perl' 'perl-file-copy-recursive' 'perl-catalyst-action-renderview')
backup=('etc/thruk/cgi.cfg' 'etc/thruk/htpasswd'
  'etc/thruk/log4perl.conf' 'etc/thruk/menu_local.conf'
  'etc/thruk/naglint.conf')
source=(http://thruk.org/files/pkg/v$_pkgver/src/$_pkgname-$_pkgver.tar.gz)
md5sums=('9029e31a480a441b1f1bcff7683fc181')

build() {
  cd "$srcdir"/$_pkgname-$_pkgver

  ./configure --prefix=/usr \
    --exec-prefix=/bin \
    --bindir=/usr/bin \
    --sysconfdir=/etc/$pkgname \
    --datadir=/usr/share/$pkgname \
    --localstatedir=/var \
    --libdir=/var/lib \
    --mandir=/usr/share/man \
    --with-initdir=/etc/thruk \
    --with-logdir=/var/log/thruk \
    --with-checkresultdir=/var/cache/naemon/checkresults \
    --with-tempdir=/var/lib/thruk \
    --with-logrotatedir=/etc/logrotate.d

  make
}

package() {
  cd "$srcdir"/$_pkgname-$_pkgver

  make DESTDIR="$pkgdir"/ install

  install -d "$pkgdir"/usr/lib/perl5/vendor_perl/
  ln -s /usr/share/thruk/lib/Thruk/ "$pkgdir"/usr/lib/perl5/vendor_perl/Thruk
  ln -s /usr/share/thruk/lib/Thruk.pm "$pkgdir"/usr/lib/perl5/vendor_perl/Thruk.pm
  ln -s /usr/share/thruk/lib/Plack/ "$pkgdir"/usr/lib/perl5/vendor_perl/Plack
  ln -s /usr/share/thruk/lib/Monitoring/ "$pkgdir"/usr/lib/perl5/vendor_perl/Monitoring
  ln -s /usr/share/thruk/lib/FCGI/ "$pkgdir"/usr/lib/perl5/vendor_perl/FCGI
  ln -s /usr/share/thruk/lib/Catalyst/ "$pkgdir"/usr/lib/perl5/vendor_perl/Catalyst
  ln -s /var/ "$pkgdir"/usr/share/thruk/var
}
