# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>

_pkgname=Thruk
pkgname=thruk
_pkgver=1.84-3
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="Multibackend monitoring webinterface for Naemon, Nagios, Icinga and Shinken"
arch=('any')
url="http://thruk.org"
license=('GPL2')
depends=('perl')
backup=('etc/thruk/cgi.cfg' 'etc/thruk/htpasswd'
  'etc/thruk/log4perl.conf' 'etc/thruk/menu_local.conf'
  'etc/thruk/naglint.conf')
source=(http://thruk.org/files/pkg/v$_pkgver/src/$_pkgname-$_pkgver.tar.gz)
md5sums=('9029e31a480a441b1f1bcff7683fc181')

build() {
  cd "$srcdir"/$_pkgname-$_pkgver

  ./configure --prefix=/usr \
    --exec-prefix=/bin \
    --bindir=/usr/bin \
    --sysconfdir=/etc/$pkgname \
    --localstatedir=/var \
    --libdir=/var/lib \
    --datadir=/usr/share/$pkgname \
    --mandir=/usr/share/man \
    --with-initdir=/etc/thruk \
    --with-logdir=/var/log/thruk \
    --with-checkresultdir=/var/cache/naemon/checkresults \
    --with-tempdir=/var/lib/thruk \
    --with-logrotatedir=/etc/logrotate.d
#   --with-thruk-libs  # [PREFIX/lib/thruk/perl5]
  make
}

package() {
  cd "$srcdir"/$_pkgname-$_pkgver

  make DESTDIR="$pkgdir"/ install
}
