# Maintainer: Gilrain <pierre.buard+aur gmail com>
#
# BUILD INSTRUCTIONS:
#
# Download the archive from the given url!
#
pkgname=cudaminer
pkgver=20130430
_pkgver=2013-04-30
_srcver=${_pkgver//-/.} #2013.04.30
pkgrel=3
pkgdesc="CUDA accelerated mining application for Litecoin."
url="https://bitcointalk.org/index.php?topic=167229.0"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cuda' 'curl')
optdepends=('stratum-mining-proxy-git: to connect to Stratum mining pools')
makedepends=('unzip')
options=(!buildflags)
source=(${pkgname}-${_pkgver}.zip
	conf
	service)
sha256sums=('2d81b52e1051a4f724e75b0e84e231293809437aa060d21b2fd3b8bfc5b711f2'
	    '37c3793573721ea5a5eadddf95d4bd72d8ec384db5cc082eba6bcb268c4eaa6d'
	    'e669435e00a9a5a339500dd105b94066cf49ad4a6524ee81c23054678819b7c4')
backup=(etc/${pkgname}.conf)

prepare() {
  unzip -q ${srcdir}/${pkgname}-${_pkgver}/${pkgname}-src-${_srcver}.zip -d ${srcdir}/${pkgname}-${_pkgver}/

  cd ${srcdir}/${pkgname}-${_pkgver}/${pkgname}-src-${_srcver}/
  chmod +x configure autotools.sh
}

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}/${pkgname}-src-${_srcver}/
  ./autotools.sh
  ./configure --prefix=/usr --with-cuda=/opt/cuda

  if [ "$CARCH" = "i686" ]; then
    CFLAGS="-machine 32 -mtune=generic -O2 -pipe -fstack-protector --param=ssp-buffer-size=4"
  elif [ "$CARCH" = "x86_64" ]; then
    CFLAGS="-machine 64 -mtune=generic -O2 -pipe -fstack-protector --param=ssp-buffer-size=4"
  fi
  make
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}/${pkgname}-src-${_srcver}/

  install -D -m755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}

  install -d -m755 ${pkgdir}/usr/lib/
  install -m644 *.o ${pkgdir}/usr/lib/

  install -D -m644 README.txt ${pkgdir}/usr/share/doc/${pkgname}/README

  install -Dm644 ${srcdir}/conf ${pkgdir}/etc/${pkgname}.conf
  install -Dm644 ${srcdir}/service ${pkgdir}/usr/lib/systemd/system/${pkgname}.service
}