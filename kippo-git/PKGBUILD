# Maintainer: Christian Rebischke <echo Q2hyaXMuUmViaXNjaGtlQGdtYWlsLmNvbQo= | base64 -d>

pkgname=kippo-git
pkgver=8.913c84c
pkgrel=1
pkgdesc="A medium interaction SSH honeypot designed to log brute force attacks and, most importantly, the entire shell interaction performed by the attacker"
arch=('i686' 'x86_64')
url="https://github.com/desaster/kippo"
license=('BSD')
depends=('python2' 'twisted' 'pycrypto' 'zope-interface' 'python2-pyasn1')
makedepends=('git')
source=('git+http://github.com/desaster/kippo' 'start.sh' 'kippo.service')
md5sums=('SKIP' 'f0a3fe0bd1598bcdc10a1a882cfe96f6' '7e1b470f16e6e466e64b3ec56f25b4a0')
install=kippo.install

pkgver() {
  cd kippo
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  sed -e 's/#!\/usr\/bin\/env python/#!\/usr\/bin\/env python2/' \
    $srcdir/kippo/utils/* -i
}

package() {
  cd "$srcdir/kippo"
  mkdir -p $pkgdir/opt/kippo/
  mv * $pkgdir/opt/kippo/
  install -D -m644 "${srcdir}/kippo.service" \
  "${pkgdir}/lib/systemd/system/kippo.service"
  install -Dm755 $srcdir/start.sh $pkgdir/opt/kippo/start.sh
}


# vim:set ts=2 sts=2 sw=2 et
