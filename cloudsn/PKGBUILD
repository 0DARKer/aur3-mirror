# Maintainer: willemw <willemw12@gmail.com>
# Contributor: Angelo "Encelo" Theodorou <encelo@gmail.com>
# Contributor: Jesús Barbero Rodríguez <chuchiperriman@gmail.com>

_pkgname=cloud-services-notifications
pkgname=cloudsn
pkgver=0.10.2
pkgrel=3
pkgdesc="E-mail (pop3, gmail, imap), rss and tweets notifier"
arch=('any')
url="http://chuchiperriman.github.com/cloud-services-notifications/"
license=('GPL3')
depends=('pygtk' 'python2-notify' 'python2-xdg' 'python2-dbus' 'libindicate-gtk2' 'hicolor-icon-theme'
         'libindicate-gtk3' 'python2-gobject') # See ubuntu package "python-indicate"
optdepends=('python2-feedparser: RSS support'
            'python2-gnomekeyring: store passwords in gnome-keyring'
            'gstreamer0.10-python: play sounds')
install=cloudsn.install
source=(https://github.com/chuchiperriman/$_pkgname/archive/$pkgver.tar.gz)
md5sums=('470a67a55e00bb6785bf717c58c937f8')

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Patch "'NoneType object has no attribute 'set_markup'"
  sed -i "s|\(indmenuItem.get_child().set_markup\)|#\1|" src/cloudsn/ui/indicators/statusicon.py

  python2 setup.py install --root="$pkgdir/" --prefix=/usr
}

