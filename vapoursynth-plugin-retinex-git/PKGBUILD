# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=retinex
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20141015.32a7095
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="http://homeofvapoursynthevolution.github.io/"
license=('GPL')
depends=('vapoursynth')
makedepends=('git')
provides=("vapoursynth--plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/HomeOfVapourSynthEvolution/VapourSynth-Retinex.git")
md5sums=('SKIP')
_gitname="${_plug}"

# pkgver() {
#   cd "${_gitname}"
#   echo "$(git describe --long --tags | tr - .)"
# }

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  sed -e 's|#include "|#include "../include/|g' \
      -i source/*.cpp -i include/*.h
}

build() {
  cd "${_gitname}/source"
  echo "all:
	  g++ -shared ${CXXFLAGS} -std=c++11 -fPIC -o lib${_plug}.so Gaussian.cpp MSR.cpp MSRCP.cpp VSPlugin.cpp $(pkg-config --cflags vapoursynth)"> Makefile
  make
}

package(){
  cd "${_gitname}/source"
  install -Dm755 "lib${_plug}.so" "${pkgdir}/usr/lib/vapoursynth/lib${_plug}.so"
  install -Dm644 ../README.md "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README.md"
}
