# Maintainer: Joshua Hunt <snark1994 at gmail dot com>
# Contributer: Joao Cordeiro <jlcordeiro at gmail dot com>
pkgname=unnethack
pkgver=4.0.0
_pkgverdate=20120401
pkgrel=6
pkgdesc="Variant of NetHack. It features more randomness, more levels, more challenges and more fun than vanilla NetHack."
arch=('i686' 'x86_64')
options=(!makeflags)
url="http://sourceforge.net/apps/trac/unnethack/"
license=('custom')
depends=('ncurses' 'glibc' 'bash')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/$pkgname-$pkgver-$_pkgverdate.tar.gz
        unnethack.sh)
  md5sums=('ad3cb590b54b66c8766974af50bce169'
           'adf530300dee3919437b765246718456')


build() {
  cd "$srcdir/$pkgname-$pkgver-$_pkgverdate"

  ./configure --prefix=/usr/share/unnethack --with-owner=`id -un` --with-group=`id -gn` --enable-wizmode=`id -un` --enable-curses-graphics
  make
}

package(){
  cd "$srcdir/$pkgname-$pkgver-$_pkgverdate"

  make DESTDIR="$pkgdir" install
  install -T -D -m 755 $srcdir/unnethack.sh $pkgdir/usr/bin/unnethack
  install -D -m 644 dat/license $pkgdir/usr/share/licenses/$pkgname/license
  mkdir $pkgdir/var/unnethack/ -p
}

# vim:set ts=2 sw=2 et:
