pkgname=libva-xvba-driver
pkgver=0.8.0
pkgrel=1
pkgdesc='XvBA backend for VA-API'
arch=(i686 x86_64)
url=http://freedesktop.org/wiki/Software/vaapi/
license=(GPL2)
depends=(catalyst-utils)
makedepends=(libva python2 xvba-sdk)
source=(
http://cgit.freedesktop.org/vaapi/xvba-driver/snapshot/xvba-driver-$pkgver.tar.xz
http://patch-tracker.debian.org/patch/series/dl/xvba-video/0.8.0-8/01-glx-assert.diff
https://build.opensuse.org/source/home:maxge73/xvba-video/xvba-video-h264-level51.patch
https://build.opensuse.org/source/home:maxge73/xvba-video/xvba-video-remove-installation.patch
https://raw.github.com/frugalware/frugalware-current/master/source/x11-extra/$pkgname/fixes.patch
)
md5sums=(93d164f40b103d8526aac3710c4f974e
         515507d22d2a1dc1ad05721182a20e37
         bce1de0a8b274049568453a53e8fce6a
         9330d0af491d139748d4c861423edc5f
         6f4096f8b2b8abb837e05827109bacc7)

prepare() {
  cd xvba-driver-$pkgver

  patch -Np1 < ../xvba-video-remove-installation.patch
  patch -Np1 < ../xvba-video-h264-level51.patch
  patch -Np1 < ../01-glx-assert.diff
  patch -Np1 < ../fixes.patch
}

build() {
  cd xvba-driver-$pkgver

  CPPFLAGS+=' -I/usr/include/amd'

  ./autogen.sh --disable-debug --disable-tracer --disable-valgrind --prefix=/usr

  make PYTHON=python2
}

package() {
  cd xvba-driver-$pkgver

  make DESTDIR=$pkgdir install
}
