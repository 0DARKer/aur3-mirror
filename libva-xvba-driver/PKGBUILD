pkgname=libva-xvba-driver
pkgver=0.8.0
pkgrel=3
pkgdesc='XvBA backend for VA-API'
arch=(i686 x86_64)
url=http://freedesktop.org/wiki/Software/vaapi/
license=(GPL2)
depends=(catalyst-utils)
makedepends=(libva python2 xvba-sdk)
conflicts=(xvba-video)
source=(
  http://catalyst.wirephire.com/files/patches/lano1106_xvba_bug950_workaround.patch
  http://cgit.freedesktop.org/vaapi/xvba-driver/snapshot/xvba-driver-$pkgver.tar.xz
  https://build.opensuse.org/source/home:maxge73/xvba-video/xvba-video-$pkgver-glx-fix.patch
  https://build.opensuse.org/source/home:maxge73/xvba-video/xvba-video-h264-level51.patch
  https://raw.github.com/frugalware/frugalware-current/master/source/x11-extra/$pkgname/fixes.patch
)
sha256sums=(e660bb8e71920149c79dfb0c999f7b9c3b600ed19116c6eb6791a98f158943e3
            a3d87c992e2f2b21655f5c1fe36c408c59e36d2d846fc207f56b7f725fab43f9
            66625bcd61e5c295a04ad8dc00e57aa95f984da5eb1e7e4a5e7e072eaf472b4a
            20397f668ae6a27be36ca21665fa3a744d1bccd989bb3250bef01ed5cb566436
            f942b9eb5e216a0b6fca5cbb1e5bc09a5b8084c9fb7ed161fae4cde0ed1605f7)

_confopts=(
  --disable-debug
  --disable-tracer
  --disable-valgrind
  --prefix=/usr
)

prepare() {
  cd xvba-driver-$pkgver

  patch -Np1 < $srcdir/xvba-video-h264-level51.patch
  patch -Np1 < $srcdir/xvba-video-$pkgver-glx-fix.patch
  patch -Np1 < $srcdir/fixes.patch
  patch -Np1 < $srcdir/lano1106_xvba_bug950_workaround.patch
}

build() {
  cd xvba-driver-$pkgver

  ./autogen.sh ${_confopts[@]}

  make PYTHON=python2
}

package() {
  cd xvba-driver-$pkgver

  make DESTDIR=$pkgdir install

  install -d $pkgdir/usr/share/doc/$pkgname
  install -m644 AUTHORS COPYING NEWS README $pkgdir/usr/share/doc/$pkgname
}
