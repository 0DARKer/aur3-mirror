# Maintainer: Michael Gerhaeuser <michael dot gerhaeuser at gmail dot com>

pkgname=ios-webkit-debug-proxy-git
pkgver=1.3.r4.gc58b132
pkgrel=2
pkgdesc="A DevTools proxy (WebKit Remote Debugging) for iOS devices (Safari Remote Web Inspector)"
url="https://github.com/google/ios-webkit-debug-proxy"
arch=('i686' 'x86_64')
license=('BSD')
depends=('usbmuxd-git' 'libusbmuxd-git' 'libplist-git' 'libimobiledevice-git')
makedepends=('git')
provides=('ios-webkit-debug-proxy')
conflicts=('ios-webkit-debug-proxy')

source=("git://github.com/google/ios-webkit-debug-proxy.git"
	'ios7.patch')
sha512sums=('SKIP'
	'd571cce8f49767353cb039d483c0feb17af0d410f2b405037bec6d3d360fde2932caab4a21bbc9b27c4366a60c9c51c3bcf27fde4854110f7345f98b8fc9f7e4')

pkgver() {
	cd ios-webkit-debug-proxy
	git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd ios-webkit-debug-proxy

	patch -p1 -i ${srcdir}/ios7.patch

	./autogen.sh --prefix=/usr
	./configure --prefix=/usr
	make
}

package() {
	cd ios-webkit-debug-proxy
	make DESTDIR="$pkgdir" install

	# copy license
	install -D -m644 LICENSE.md "${pkgdir}/usr/share/licenses/ios-webkit-debug-proxy/LICENSE.md"

	# move header files to /usr/include/ios-webkit-debug-proxy
	mkdir -p ${pkgdir}/usr/include/ios-webkit-debug-proxy
	mv ${pkgdir}/usr/include/*.h $pkgdir/usr/include/ios-webkit-debug-proxy
}
