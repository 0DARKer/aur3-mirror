# Maintainer: Max Roder <maxroder@web.de>

pkgname='jondobrowser'
pkgver='0.5'
pkgrel='1'
_language='en-US'
pkgdesc='Firefox tweaked to work with jondo (anonymous browsing) (english version)'
url='https://anonymous-proxy-servers.net/en/jondobrowser.html'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-runtime')
optdepends=('zenity: simple dialog boxes'
			'kdialog: KDE dialog boxes'
			'notify-send: Gnome dialog boxes'
			'jondo: connection to jondo proxy cascades, gui version'
			'jondoconsole: connection to jondo proxy cascades, cli version')
install="${pkgname}.install"
source=("https://anonymous-proxy-servers.net/en/downloads/JonDoBrowser-${pkgver}-linux-${CARCH}-${_language}.tar.xz"
		"https://anonymous-proxy-servers.net/en/downloads/JonDoBrowser-${pkgver}-linux-${CARCH}-${_language}.tar.xz.asc")
if [[ "$CARCH" == 'x86_64' ]]; then
	sha256sums=('c5d149e3a3c9cce3b554d27091d1953e62135f66b2342102d0c3a972c6a66d08'
				'dc2a5d59b6c29709174ce8687407a76f448531d872a0e0966932b8638135fc36')
else
	sha256sums=('2bd8a75d609d121e10a3a10f25a1b07499cfcddc5172cbdb0bfeaf0419ee743b'
				'c60e15c6db173bd7c4c947453e3fd0915a36d7f632e6940c4a18c9de637e8788')
fi

package() {
	cd "${srcdir}/JonDoBrowser/"

	sed -i "s/usr\/local/usr/g"	misc/applications/${pkgname}.desktop

	install -Dm 644 misc/applications/${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 misc/pixmaps/${pkgname}.png	${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 misc/bin/${pkgname}	${pkgdir}/usr/bin/${pkgname}
	install -Dm 644 CHANGELOG	${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG

	install -d	${pkgdir}/opt/JonDoBrowser
	cp -r App	${pkgdir}/opt/JonDoBrowser/
	cp -r Data	${pkgdir}/opt/JonDoBrowser/
	
}

# vim:set ts=2 sw=2 et:
