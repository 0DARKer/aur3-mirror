# Maintainer: Max Roder <maxroder@web.de>

pkgname='jondobrowser'
pkgver='0.3.1'
pkgrel='1'
_language='de'
pkgdesc='Firefox tweaked to work with jondo (anonymous browsing) (german version)'
url='https://www.anonym-surfen.de/jondobrowser.html'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-runtime' 'jondo')
optdepends=('zenity: simple dialog boxes'
			'kdialog: KDE dialog boxes'
			'notify-send: Gnome dialog boxes')
install="${pkgname}.install"
source=("https://www.anonym-surfen.de/downloads/JonDoBrowser-${pkgver}-linux-${CARCH}-${_language}.tar.bz2"
		"https://www.anonym-surfen.de/downloads/JonDoBrowser-${pkgver}-linux-${CARCH}-${_language}.tar.bz2.asc")
if [[ "$CARCH" == 'x86_64' ]]; then
	sha256sums=('26ee33945f254486d0dbd7271cf99390f5891463e376042858461d5a683c7bf2'
				'db77aa2e9f979c255b49ebae8b3f3d3bf197eb41fdbf5066d7313f5d41fccc55')
else
	sha256sums=('446902cd0c15131d92506f70b1b174ca4eadb04bff34535463fb3536a19910f7'
				'19ab7b328a610b66565fb78c71cfca894457d92fe32ecef74704d7d538c4ac4a')
fi

package() {
	cd "${srcdir}/JonDoBrowser/"

	sed -i "s/usr\/local/usr/g"		misc/applications/${pkgname}.desktop

	install -Dm 644 misc/applications/${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 misc/pixmaps/${pkgname}.png				${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 misc/bin/${pkgname}						${pkgdir}/usr/bin/${pkgname}
	install -Dm 644 CHANGELOG								${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG

	install -d ${pkgdir}/opt/JonDoBrowser
	cp -r App ${pkgdir}/opt/JonDoBrowser/
	cp -r Data ${pkgdir}/opt/JonDoBrowser/
	
}

# vim:set ts=2 sw=2 et:
