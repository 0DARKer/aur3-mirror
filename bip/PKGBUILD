# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=bip
pkgver=0.8.8
pkgrel=1
pkgdesc="Multiuser IRC proxy that supports replaying of logged irc conversations"
arch=('i686' 'x86_64')
url="http://bip.milkypond.org/"
license=('GPL')
depends=('flex' 'openssl' 'perl')
makedepends=('bison')
backup=('etc/bip.conf')
source=(https://projects.duckcorp.org/attachments/download/39/$pkgname-$pkgver.tar.gz)
sha256sums=('7ca3fb96f5ee6b76eb398d7ea45344ea24855344ced11632241a33353bba05d7')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  install -D -m755 ${srcdir}/$pkgname-$pkgver/scripts/bipgenconfig \
    ${pkgdir}/usr/bin/bipgenconfig
  install -D -m644 ${srcdir}/$pkgname-$pkgver/samples/$pkgname.conf \
    ${pkgdir}/usr/share/$pkgname/$pkgname.conf.sample
  install -D -m644 ${srcdir}/$pkgname-$pkgver/samples/$pkgname.vim \
    ${pkgdir}/usr/share/vim/syntax/$pkgname.vim
}
