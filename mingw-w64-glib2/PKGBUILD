# Contributor: Filip Brcic <brcha@gna.org>
# Contributor: ant32 <antreimer@gmail.com>
# Contributor: Renato Silva <br.renatosilva@gmail.com>
pkgname=mingw-w64-glib2
pkgver=2.42.2
pkgrel=1
arch=(any)
pkgdesc="Common C routines used by GTK+ and other libs (mingw-w64)"
depends=(mingw-w64-gettext mingw-w64-zlib mingw-w64-libffi mingw-w64-pcre)
makedepends=(mingw-w64-configure python)
license=("LGPL")
options=(!strip !buildflags staticlibs)
url="http://www.gtk.org/"
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz"
        "0001-gsocket-block-when-errno-says-it-will-block.patch"
        "0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch"
        "0003-g_abort.all.patch"
        "0015-fix-stat.all.patch"
        "0021-use-64bit-stat-for-localfile-size-calc.all.patch"
        "0024-return-actually-written-data-in-printf.all.patch"
        "0027-no_sys_if_nametoindex.patch"
        "glib-send-log-messages-to-correct-stdout-and-stderr.patch"
        "glib-prefer-constructors-over-DllMain.patch")
sha256sums=('a3cc1ebd2bd310a9fdf42ae4293ee713cdf1764bd29f552febf3bf44cadae7db'
            '6f70461738964dfdd0fa7bed9a057dc79a5e75a2a121cc1a30564bf70f4c5a16'
            '1420d8a8cadef2d33d748b31e5ae9c385aee1351d267dabf7a6a68fed6dfe7db'
            '6df7b4e00aa7ded9e2e9e729dd1fb3f78e11c5936b316ebf9cf102c9043756f4'
            '9ba68777f48816cbee154084c10d0d577a021ef406606d5c2d230761cf82b66b'
            'ad13c9aaa3d2ca7b1e3dcfd6d0c8043fa4fe45d8abb3659dbbc1fcbc412c10c7'
            '497d057e79943df233ca304c389f462d3c4bf55ab3267e94dbef892b79fe3274'
            'bf80d21ed1931f839d5920d0c3633b7c84ceb8f6da051cecc942ce90eccb883f'
            '4e636326f39261dc359784853bd61a897f8bb2d438fffc256ae60bb580799f16'
            '7d1096d4a799df63939ded9437bcd39ad13ab71ba213ef7c83abd6ebf4efaf6e')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd glib-$pkgver
  patch -Np1 -i ../0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
  patch -Np1 -i ../0003-g_abort.all.patch
  patch -Np1 -i ../glib-prefer-constructors-over-DllMain.patch
  patch -Np0 -i ../glib-send-log-messages-to-correct-stdout-and-stderr.patch
  patch -Np1 -i ../0015-fix-stat.all.patch
  patch -Np1 -i ../0021-use-64bit-stat-for-localfile-size-calc.all.patch
  patch -Np1 -i ../0024-return-actually-written-data-in-printf.all.patch
  patch -Np1 -i ../0001-gsocket-block-when-errno-says-it-will-block.patch
  patch -Np1 -i ../0027-no_sys_if_nametoindex.patch

  # detection of if_nametoindex fails as part of libiphlpapi.a
  sed -i "s|#undef HAVE_IF_NAMETOINDEX|#define HAVE_IF_NAMETOINDEX 1|g" config.h.in 
  
  NOCONFIGURE=1 ./autogen.sh
}


build() {
  cd glib-$pkgver
  conf="--with-pcre=system --with-threads=win32"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ${_arch}-configure \
      --disable-shared $conf
    make
    popd
    
    mkdir -p build-${_arch}-shared && pushd build-${_arch}-shared
    ${_arch}-configure \
      --disable-static $conf
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/glib-$pkgver/build-${_arch}-shared"
    make DESTDIR="$pkgdir" install
    make -C "$srcdir/glib-$pkgver/build-${_arch}-static" DESTDIR="$pkgdir/static" install
    mv "$pkgdir/static/usr/${_arch}/lib/"*.a "$pkgdir/usr/${_arch}/lib/"
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.def' | xargs -rtl1 rm
		find "$pkgdir/usr/${_arch}" -name '*.dll' -exec ${_arch}-strip --strip-unneeded {} \;
		find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
		rm "$pkgdir/usr/${_arch}/bin/"{glib-{gettextize,mkenums},gdbus-codegen}
		rm -r "$pkgdir/usr/${_arch}/lib/charset.alias"
		rm -r "$pkgdir/static"
		rm -r "$pkgdir/usr/${_arch}/share"
  done
}
