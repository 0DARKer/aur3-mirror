# Maintainer: Filip Brcic <brcha@gna.org>
# Update by: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-glib2
pkgver=2.40.0
pkgrel=1
arch=(any)
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw-w64)"
depends=(mingw-w64-crt mingw-w64-gettext mingw-w64-zlib mingw-w64-libffi)
makedepends=(mingw-w64-gcc python2)
license=('LGPL')
options=(!strip !buildflags staticlibs)
url="http://www.gtk.org/"

source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz"
        "0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch"
        "glib-send-log-messages-to-correct-stdout-and-stderr.patch"
        "glib-prefer-constructors-over-DllMain.patch")

sha256sums=('0d27f195966ecb1995dcce0754129fd66ebe820c7cd29200d264b02af1aa28b5'
             '1420d8a8cadef2d33d748b31e5ae9c385aee1351d267dabf7a6a68fed6dfe7db'
             '4e636326f39261dc359784853bd61a897f8bb2d438fffc256ae60bb580799f16'
             '7d1096d4a799df63939ded9437bcd39ad13ab71ba213ef7c83abd6ebf4efaf6e')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"



prepare() {
  cd glib-$pkgver
  patch -Np1 -i ../0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
  patch -Np0 -i ../glib-send-log-messages-to-correct-stdout-and-stderr.patch
  patch -Np1 -i ../glib-prefer-constructors-over-DllMain.patch
}



build() {
  cd glib-$pkgver
  
  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  unset LDFLAGS
  
  # Can not build both shared and static at the same time on Windows.
  
  for _arch in ${_architectures}; do
    export PKG_CONFIG_LIBDIR="/usr/${_arch}/lib/pkgconfig"
    unset PKG_CONFIG_PATH
    
    rm -rf build-${_arch}-static
    mkdir build-${_arch}-static && pushd build-${_arch}-static
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CHOST} \
      --disable-shared --enable-static
    make
    popd
    
    rm -rf build-${_arch}-shared
    mkdir build-${_arch}-shared && pushd build-${_arch}-shared
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CHOST} \
      --disable-static
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/glib-$pkgver/build-${_arch}-shared"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-all "${pkgdir}/usr/${_arch}/bin/"*.exe
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/bin/"*.dll

    make -C "$srcdir/glib-$pkgver/build-${_arch}-static" DESTDIR="$pkgdir/static" install
    ${_arch}-strip --strip-debug "${pkgdir}/static/usr/${_arch}/lib/"*.a
    mv "$pkgdir/static/usr/${_arch}/lib/"*.a "$pkgdir/usr/${_arch}/lib/"

    rm -rf "$pkgdir/static"

    rm -rf "$pkgdir/usr/${_arch}"/{share/{gtk-doc,man,bash-completion,gdb},lib/charset.alias,etc}
    rm "$pkgdir/usr/${_arch}/lib/"*.def

    rm -f "$pkgdir/usr/${_arch}/bin/gdbus-codegen"
    rm -rf "$pkgdir/usr/${_arch}/lib/gdbus-2.0"
  done
}
