# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-glib2
pkgver=2.30.2
pkgrel=1
arch=(any)
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw-w64)"
depends=(mingw-w64-crt mingw-w64-gettext mingw-w64-zlib mingw-w64-libffi)
makedepends=(mingw32-gcc python2)
license=(LGPL)
options=(!strip)
url="http://www.gtk.org/"
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/2.30/glib-$pkgver.tar.xz")
sha256sums=('f0e91e6333321ddb48fa12b5c66f56c3d5f05325748c66dd2e9016c278ff8e82')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "$srcdir/glib-$pkgver"

  for _arch in ${_architectures}; do
    export CFLAGS="-O2 -pipe -mms-bitfields"
    export CPPFLAGS="-D_REENTRANT"
    export PKG_CONFIG_LIBDIR="/usr/${_arch}/lib/pkgconfig"
    unset PKG_CONFIG_PATH
    unset LDFLAGS
    mkdir build-${_arch} && pushd build-${_arch}
    cat > win32.cache << EOF
glib_cv_long_long_format=I64
glib_cv_stack_grows=no
EOF
    PYTHON="/usr/bin/python2" \
      ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --cache-file=win32.cache
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/glib-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install

    rm -rf $pkgdir/usr/${_arch}/{share/{gtk-doc,man},lib/charset.alias,etc}
    rm $pkgdir/usr/${_arch}/lib/*.def
    ${_arch}-strip $pkgdir/usr/${_arch}/bin/*.exe
    ${_arch}-strip -x $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a
  done
}

