# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-glib2
pkgver=2.34.3
pkgrel=1
arch=(any)
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw-w64)"
depends=(mingw-w64-crt mingw-w64-gettext mingw-w64-zlib mingw-w64-libffi)
makedepends=(mingw-w64-gcc python2)
license=(LGPL)
options=(!strip)
url="http://www.gtk.org/"
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/2.34/glib-$pkgver.tar.xz"
	'0001-Don-t-start-a-DBus-server-when-built-as-static-lib.patch'
	'0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch'
        'glib-build-dbus-proxy-testcase-only-on-unix.patch'
        '0001-Add-Win32-versions-of-some-new-content-type-APIs.patch'
        '0001-Unconditionally-use-g_content_type_get_symbolic_icon.patch')
md5sums=('a4ca31e258273c3761e3de2edd607661'
         '82c31506c6f8d22296a86a6ad7511063'
         'c08c41118f5318d10f326c7c30e1a33a'
         '4b4e57380c27422e44d538a509925833'
         '719259d68c237bb2a6ef753cca83e2d1'
         '1e5277f81c2acadcb5b480714f720dca')


_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "$srcdir/glib-$pkgver"

  patch -Np1 -i $srcdir/0001-Don-t-start-a-DBus-server-when-built-as-static-lib.patch
  patch -Np1 -i $srcdir/0001-Use-CreateFile-on-Win32-to-make-sure-g_unlink-always.patch
  patch -Np1 -i $srcdir/glib-build-dbus-proxy-testcase-only-on-unix.patch
  patch -Np1 -i $srcdir/0001-Add-Win32-versions-of-some-new-content-type-APIs.patch
  patch -Np1 -i $srcdir/0001-Unconditionally-use-g_content_type_get_symbolic_icon.patch

  # Fix obsolete ac macros
  sed -e 's/AM_PROG_CC_STDC/AC_PROG_CC/g' -i configure.ac aclocal.m4

  autoreconf -i --force

  for _arch in ${_architectures}; do
    export CFLAGS="-O2 -pipe -mms-bitfields"
    export CPPFLAGS="-D_REENTRANT"
    export PKG_CONFIG_LIBDIR="/usr/${_arch}/lib/pkgconfig"
    unset PKG_CONFIG_PATH
    unset LDFLAGS
    rm -rf build-${_arch}-static
    mkdir build-${_arch}-static && pushd build-${_arch}-static
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --disable-shared --enable-static
    make
    popd
    rm -rf build-${_arch}-shared
    mkdir build-${_arch}-shared && pushd build-${_arch}-shared
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --disable-static
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/glib-$pkgver/build-${_arch}-shared"
    make DESTDIR="$pkgdir" install
    make -C $srcdir/glib-$pkgver/build-${_arch}-static DESTDIR=$pkgdir/static install
    mv $pkgdir/static/usr/${_arch}/lib/*.a $pkgdir/usr/${_arch}/lib/
    rm -rf $pkgdir/static

    rm -rf $pkgdir/usr/${_arch}/{share/{gtk-doc,man,bash-completion,gdb},lib/charset.alias,etc}
    rm $pkgdir/usr/${_arch}/lib/*.def
    ${_arch}-strip $pkgdir/usr/${_arch}/bin/*.exe
    ${_arch}-strip -x $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a

    rm -f $pkgdir/usr/${_arch}/bin/gdbus-codegen
    rm -rf $pkgdir/usr/${_arch}/lib/gdbus-2.0
  done
}

