# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-glib2
pkgver=2.32.4
pkgrel=1
arch=(any)
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw-w64)"
depends=(mingw-w64-crt mingw-w64-gettext mingw-w64-zlib mingw-w64-libffi)
makedepends=(mingw32-gcc python2)
license=(LGPL)
options=(!strip)
url="http://www.gtk.org/"
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/2.32/glib-$pkgver.tar.xz"
	'0001-Don-t-start-a-DBus-server-when-built-as-static-lib.patch'
	'0001-win32-fix-build-after-bug-674452.patch')
md5sums=('bf84fefd9c1a5b5a7a38736f4ddd674a'
         'b378290c1740a57772749fb7cbe2ac71'
         '15a2b5806952acc24b0c88a0a119db87')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "$srcdir/glib-$pkgver"

  patch -Np1 -i $srcdir/0001-Don-t-start-a-DBus-server-when-built-as-static-lib.patch
  patch -Np1 -i $srcdir/0001-win32-fix-build-after-bug-674452.patch

  for _arch in ${_architectures}; do
    export CFLAGS="-O2 -pipe -mms-bitfields"
    export CPPFLAGS="-D_REENTRANT"
    export PKG_CONFIG_LIBDIR="/usr/${_arch}/lib/pkgconfig"
    unset PKG_CONFIG_PATH
    unset LDFLAGS
    rm -rf build-${_arch}-static
    mkdir build-${_arch}-static && pushd build-${_arch}-static
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --disable-shared --enable-static
    make
    popd
    rm -rf build-${_arch}-shared
    mkdir build-${_arch}-shared && pushd build-${_arch}-shared
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --disable-static
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/glib-$pkgver/build-${_arch}-shared"
    make DESTDIR="$pkgdir" install
    make -C $srcdir/glib-$pkgver/build-${_arch}-static DESTDIR=$pkgdir/static install
    mv $pkgdir/static/usr/${_arch}/lib/*.a $pkgdir/usr/${_arch}/lib/
    rm -rf $pkgdir/static

    rm -rf $pkgdir/usr/${_arch}/{share/{gtk-doc,man},lib/charset.alias,etc}
    rm $pkgdir/usr/${_arch}/lib/*.def
    ${_arch}-strip $pkgdir/usr/${_arch}/bin/*.exe
    ${_arch}-strip -x $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a
  done
}

