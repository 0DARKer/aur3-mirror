# Maintainer: OK <ok100.ok100.ok100@gmail.com>
# Based on kernel-eee PKGBUILD by Dan McGee <dan@archlinux.org>

pkgname=kernel-eee-ck
pkgver=2.6.39
_kernver=2.6.39
_ckver=ck1
pkgrel=1
pkgdesc="The Linux Kernel and modules for the Asus Eee PC 701, with Brain Fuck Scheduler v0.404 and all the goodies in the $_ckver patchset."
arch=('i686')
license=('GPL2')
url="http://www.kernel.org"
depends=('coreutils' 'module-init-tools')
optdepends=('crda: for wireless regulatory domain support'
            'iw: for wireless configuration support')
replaces=('linux-uvc-eee-svn' 'madwifi-eee-svn')
install=kernel-eee-ck.install
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$_kernver.tar.bz2
        ftp://ftp.kernel.org/pub/linux/kernel/v2.6/patch-$pkgver.bz2
        http://www.kernel.org/pub/linux/kernel/people/ck/patches/2.6/$pkgver/$pkgver-$_ckver/patch-$pkgver-$_ckver.bz2
        61-eee-ssd.rules
        kernelconfig)

build() {
  # get into the linux source directory and start some magic
  cd $srcdir/linux-$_kernver

  if [ "$_kernver" != "$pkgver" ]; then
    # add latest kernel stable patch
    patch -Np1 < ../patch-$pkgver || return 1
  fi
  
  # apply ck patch
  patch -p1 < ../patch-$pkgver-$_ckver

  # load configuration and build kernel + modules
  cp ../kernelconfig ./.config
  make silentoldconfig

  # if you want to modify kernel settings, uncomment following line
  #make menuconfig

  make || return 1
}

package() {
  # install our modules
  cd $srcdir/linux-$_kernver
  mkdir -p $pkgdir/{lib/modules,boot}
  make INSTALL_MOD_PATH=$pkgdir modules_install || return 1

  # remove the junk symlinks
  cd $srcdir/linux-$_kernver
  rm $pkgdir/lib/modules/${pkgver}-${_ckver}eee/{build,source}
  # install the kernel
  cp System.map $pkgdir/boot/System.map.eee-ck
  cp arch/x86/boot/bzImage $pkgdir/boot/vmlinuzeee-ck
  # install a helper file for all install scripts
  mkdir -p $pkgdir/usr/share/kernel-eee-ck/
  echo "KERNEL_VERSION='${pkgver}-${_ckver}eee'" > $pkgdir/usr/share/kernel-eee-ck/currver

  # udev rules for SSD drives
  mkdir -p $pkgdir/lib/udev/rules.d/
  install -m644 $srcdir/61-eee-ssd.rules $pkgdir/lib/udev/rules.d/
}

md5sums=('1aab7a741abe08d42e8eccf20de61e05'
         '07c8b7463009b336f54aca3d99e6fa3d'
         '95fcd4f72792cf881f85d7da5919c751'
         '73a20e8bf2bb29ba342f43460c6291cb'
         'be197dc2869c5463e57ca9866db7b351')
