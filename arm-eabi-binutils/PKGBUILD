# Maintainer: Andreas Messer <andi@bastelmap.de>
# Based on arm-elf-binutils 2.22-1
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

_xprefix=/usr
_target=arm-eabi

pkgname=${_target}-binutils
pkgver=2.23.2
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=(i686 x86_64)
url="http://sources.redhat.com/binutils"
license=('GPL')
groups=('devel')
options=(!libtool)
depends=('glibc>=2.17' 'zlib')
checkdepends=('dejagnu' 'bc')
options=('!libtool' '!distcc' '!ccache')
conflicts=('cross-arm-elf-binutils')
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.gz
        binutils-2.23.2-texinfo-5.0.patch)
md5sums=('cda9dcc08c86ff2fd3f27e4adb250f6f'
         '34e439ce23213a91e2af872dfbb5094c')

prepare() {
  cd ${srcdir}/binutils-${pkgver}

  patch -p1 -i ${srcdir}/binutils-2.23.2-texinfo-5.0.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir ${srcdir}/binutils-build
}

build() {
  cd ${srcdir}/binutils-build

  ${srcdir}/binutils-${pkgver}/configure --prefix=${_xprefix} \
	--program-prefix=${_target}- \
  --with-bugurl=https://aur.archlinux.org/packages/arm-eabi-binutils/ \
	--enable-shared \
	--enable-multilib \
	--disable-nls \
	--target=${_target} \
	--with-sysroot=/usr/${CHOST}/${_target} 

  make configure-host
  make tooldir=$pkgdir/${_xprefix}
}

check() {
  cd ${srcdir}/binutils-build
  make LDFLAGS="" -k check || true
}

package() {
  cd $srcdir/binutils-build
  make prefix=$pkgdir/${_xprefix} tooldir=$pkgdir/${_xprefix} install

  cd $srcdir/binutils-${pkgver}
  cp -v include/libiberty.h $pkgdir/${_xprefix}/${CHOST}/${_target}/include

  rm -f $pkgdir/usr/bin/ar
  rm -f $pkgdir/usr/bin/as
  rm -f $pkgdir/usr/bin/ld
  rm -f $pkgdir/usr/bin/nm
  rm -f $pkgdir/usr/bin/objdump
  rm -f $pkgdir/usr/bin/ranlib
  rm -f $pkgdir/usr/bin/strip
  rm -f $pkgdir/usr/bin/objcopy
  rm -f $pkgdir/usr/lib/libiberty.a
  rm -rf $pkgdir/usr/share/info
}
