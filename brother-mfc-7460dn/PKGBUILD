# Maintainer: Vivien Maisonneuve <v dot maisonneuve at gmail dot com>
# Maintainer: libernux <dutchman55@gmx.com>
# Category: system
pkgname=brother-mfc-7460dn
pkgver=2.0.4
pkgrel=2
pkgdesc='LPR and CUPS driver for the Brother MFC-7460DN'
url='http://support.brother.com/g/s/id/linux/en/download_prn.html#MFC-7460DN'
arch=('i686' 'x86_64')
license=('custom:brother commercial license')
depends=('cups')
if test "$CARCH" == x86_64; then
    depends+=('lib32-glibc')
fi
install="$pkgname.install"
source=(
    "http://www.brother.com/pub/bsc/linux/dlf/cupswrapperMFC7460DN-$pkgver-2.i386.rpm"
    'http://www.brother.com/pub/bsc/linux/dlf/mfc7460dnlpr-2.1.0-1.i386.rpm'
    'cupswrapper-license.txt'
    'lpr-license.txt'
)
md5sums=(
    '4329c24062a836c2788854f901c453d3'
    'b255588a630e399e6d5bf8ec25de1d80'
    '97ad0cffd216059e9d1d3121899d8646'
    '5e87a3dc0f3e3438c088eda0f3565f0d'
 )

prepare() {
    cd "$srcdir"
    # do not install in '/usr/local'
    install -dm 755 "usr/share"
    mv "usr/local/Brother" "usr/share/brother"
    rmdir "usr/local"
    for file in $(grep -lr '/usr/local/Brother' ./); do
        sed -i 's|/usr/local/Brother|/usr/share/brother|g' "$file"
    done
    # setup cups directories
    install -dm 755 "usr/share/cups/model"
    install -dm 755 "usr/lib/cups/filter"
    #  go to the cupswrapper directory and find the source file from wich to
    #  generate the ppd and wrapper files
    local wrapper="usr/share/brother/Brother/Printer/MFC7460DN/cupswrapper/cupswrapperMFC7460DN-$pkgver"
    sed -i '/^\/etc\/init.d\/cups/d' "$wrapper"
    sed -i '/^sleep/d' "$wrapper"
    sed -i '/^lpadmin/d' "$wrapper"
    sed -i "s|/usr|$srcdir/usr|g" "$wrapper"
    sed -i "s|/opt|$srcdir/opt|g" "$wrapper"
    sed -i 's|/model/Brother|/model|g' "$wrapper"
    sed -i 's|lpinfo|echo|g' "$wrapper"
    "./$wrapper"
    rm "$wrapper"
    sed -i "s|$srcdir||" 'usr/lib/cups/filter/brlpdwrapperMFC7460DN'
    sed -i "s|$srcdir||" 'usr/lib/cups/filter/brlpdwrapperMFC7460DN'
    # /etc/printcap is managed by cups
    rm 'usr/share/brother/Brother/Printer/MFC7460DN/inf/setupPrintcap2'
}

package() {
    cp -R "$srcdir/usr" "$pkgdir"
    [ ! -e "$srcdir/opt" ]
    install -Dm 644 'cupswrapper-license.txt' "$pkgdir/usr/share/licenses/$pkgname/cupswrapper-licence.txt"
    install -Dm 644 'lpr-license.txt' "$pkgdir/usr/share/licenses/$pkgname/lpr-licence.txt"
}
