# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: bzt <unmacaque at gmail dot com>

pkgname=python2-mmkeys
pkgver=1
pkgrel=1
pkgdesc="Multimedia key support as a PyGTK object"
arch=('i686' 'x86_64')
url="http://sacredchao.net/~piman/software/python.shtml"
license=('GPL2')
depends=('pygtk' 'python2')
source=(http://sacredchao.net/~piman/software/mmkeys-1.tar.gz)
md5sums=('f3a377523d2f3d7129af2c48e4c20716')

build() {
  cd "${srcdir}/mmkeys-${pkgver}"

  # fix python2 path and cflags
  PYTHON2_INCLUDE_PATH=`python2-config --includes | awk '{print $2}' | sed 's!-I!!'`
  sed -i \
    -e "s!/usr/include/python2.3/!${PYTHON2_INCLUDE_PATH}!g" \
    -e "s!\(CFLAGS = .*\)!\1 -fPIC!g" Makefile

  make mmkeys.so
}

package() {
  cd "${srcdir}/mmkeys-${pkgver}"

  # install module
  PYTHON2_LIBS_VERSION=`python2-config --libs | awk '{print $5}' | sed 's!-l!!'`
  install -d -m755 "${pkgdir}/usr/lib/${PYTHON2_LIBS_VERSION}/site-packages"
  install -m644 mmkeys.so \
    "${pkgdir}/usr/lib/${PYTHON2_LIBS_VERSION}/site-packages/mmkeys.so"
}

# vim:set ts=2 sw=2 et:
