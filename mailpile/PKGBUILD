# Maintainer: Maxim Andersson <thesilentboatman@gmail.com>
# Based on the mailpile-git PKGBUILD
# Contributor: cornholio <vigo.the.unholy.carpathian@gmail.com>

pkgname=mailpile
pkgver=0.2.0
pkgrel=1
pkgdesc="A modern, fast web-mail client with user-friendly encryption and privacy features."
arch=('any')
url="http://www.mailpile.is"
license=('AGPL3','Apache')
depends=('python2-jinja>=2.6' 'python2-lxml>=2.3.2' 'python2-pgpdump' 'python2-pillow' 'spambayes>=1.1a6')
install=${pkgname}.install
source=("https://github.com/pagekite/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}.service")
sha256sums=('1cb632c473e4605ac048962b0d71b4e810c63134f8537daa561c4bda4893d4ce'
            'f9ab688794ccb6737b55c997caa29799eaa0c70f39e90ff70211a8217d0b69db')

prepare() {
  cd "${srcdir}/Mailpile-${pkgver}"

  # python2 fixes
  sed -i 's^bin/python^bin/python2^g' mailpile/plugins/vcard_mork.py
  sed -i 's^python ^python2 ^g' Makefile

  # Set absolute paths for static files
  sed -i "s^('static/^('/usr/share/mailpile/static/^g" mailpile/config.py
  sed -i '/os.path.dirname(              # scripts/d' mp
  sed -i 's^__file__))^"/usr/share/mailpile/mailpile")^g' mp
}

build() {
  cd "${srcdir}/Mailpile-${pkgver}"

  # Compile bytecode
  python2 -c 'import compileall; compileall.compile_dir("mailpile", force=1)'
}

package() {
  install -d "${pkgdir}/usr/share/${pkgname}"

  cp -r "${srcdir}/Mailpile-${pkgver}/static" "${pkgdir}/usr/share/${pkgname}/"
  cp -r "${srcdir}/Mailpile-${pkgver}/mailpile" "${pkgdir}/usr/share/${pkgname}/"
  cp -r "${srcdir}/Mailpile-${pkgver}/locale" "${pkgdir}/usr/share/${pkgname}/"

  find "${pkgdir}/usr/share/${pkgname}" -type f -exec chmod 644 '{}' ';'
  find "${pkgdir}/usr/share/${pkgname}" -type d -exec chmod 755 '{}' ';'

  install -D "${srcdir}/Mailpile-${pkgver}/mp" "${pkgdir}/usr/bin/mp"
  install -Dm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}

# vim:set ts=2 sw=2 et:
