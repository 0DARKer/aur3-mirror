# $Id: PKGBUILD 58087 2011-11-06 13:20:34Z lcarlier $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=lib32-talloc
pkgver=2.1.1
pkgrel=1
pkgdesc="A hierarchical pool based memory allocator with destructors (32-bit)"
arch=('x86_64')
license=('GPL3')
url="http://talloc.samba.org/"
source=(http://samba.org/ftp/talloc/talloc-${pkgver}.tar.gz)
depends=('lib32-glibc' 'talloc')
makedepends=('gcc-multilib' 'python2')

prepare() {
   cd "${srcdir}/talloc-${pkgver}"
   sed -i -e 's#python#python2#g' buildtools/bin/waf
}

build() {
  export CC="gcc -m32"

  cd ${srcdir}/talloc-${pkgver}

  ./configure --prefix=/usr \
  --disable-python \
  --sysconfdir=/etc/samba \
  --localstatedir=/var \
  --enable-talloc-compat1 \
  --libdir=/usr/lib32 \
  --bundled-libraries=NONE \
  --builtin-libraries=replace

  make
}

package() {
  cd ${srcdir}/talloc-${pkgver}

  make DESTDIR=$pkgdir/ install

  rm -rf "$pkgdir/usr/include"
  rm -rf "$pkgdir/usr/share"
}

md5sums=('5dffb86414218a91864ed4453ba9be07')
