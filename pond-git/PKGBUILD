# Maintainer: Andres Erbsen <andres@krutt.org>

pkgname=pond-git
pkgrel=1
pkgver=v0.1.119.g637bac7
pkgdesc="Forward-Secure asynchronous messaging"
url="https://pond.imperialviolet.org/"
arch=('x86_64' 'i686')
license=('BSD') # 3-clause
depends=('hunspell-en' 'tor' 'gtkspell3')
optdepends=(
  'trousers: TPM support'
)
makedepends=('go' 'git' 'mercurial' 'gtkspell3' 'trousers')
options=('!strip' '!emptydirs')
_goimportpath="github.com/agl/pond/client"

pkgver() {
  cd "$srcdir/src/github.com/agl/pond/"
  git describe --always | tr - .
}

prepare() {
  source /etc/profile.d/go.sh
  export GOPATH="$srcdir"
  go get -d -u "$_goimportpath"
}

build() {
  source /etc/profile.d/go.sh
  export GOPATH="$srcdir"
  go get "$_goimportpath"
}

package() {
  install -Dm755 "$srcdir/bin/client" "$pkgdir/usr/bin/pond"
}

# vim:set ts=2 sw=2 et:
