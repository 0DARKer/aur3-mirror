# Maintainer: Andres Erbsen <andres@krutt.org>

pkgname=pond-git
pkgrel=1
pkgver=v0.1.166.g16afb67
pkgdesc="Forward-Secure asynchronous messaging"
url="https://pond.imperialviolet.org/"
arch=('x86_64' 'i686')
license=('BSD') # 3-clause
depends=('hunspell-en' 'tor' 'gtkspell3')
optdepends=(
  'trousers: TPM support'
)
makedepends=('go' 'git' 'mercurial' 'gtkspell3' 'trousers')
source=('git+https://github.com/agl/pond.git')
sha256sums=('SKIP')
options=('!strip' '!emptydirs')

pkgver() {
  cd "pond"
  git describe --always | tr - .
}

build() {
  test -x /etc/profile.d/go.sh && source /etc/profile.d/go.sh
  mkdir -p "$srcdir/go/src/github.com/agl/"
  export GOPATH="$srcdir/go"
  rm -rf "$GOPATH/src/github.com/agl/pond" || :
  mv "$srcdir/pond" "$GOPATH/src/github.com/agl/pond"
  cd "$GOPATH/src/github.com/agl/pond/client"
  go get -u ./...
  go install
}

package() {
  install -Dm755 "$srcdir/go/bin/client" "$pkgdir/usr/bin/pond"
}

# vim:set ts=2 sw=2 et:
