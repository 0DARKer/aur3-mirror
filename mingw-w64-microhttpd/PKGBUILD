# Maintainer: Leo von Klenze <devel@leo.von-klenze.de>
pkgname=mingw-w64-microhttpd
pkgver=0.9.24
pkgrel=1
pkgdesc="GNU libmicrohttpd is a small C library that is supposed to make it easy to run an HTTP server as part of another application"
arch=(any)
url="http://www.gnu.org/software/libmicrohttpd/"
license=('LGPL')
makedepends=(mingw-w64-gcc mingw-w64-plibc)
depends=(mingw-w64-crt mingw-w64-plibc)
options=(!libtool !strip !buildflags)
source=(http://www.artfiles.org/gnu.org/libmicrohttpd/libmicrohttpd-0.9.24.tar.gz)
md5sums=(2891c82cc85a92e6944cacf9ae413f7c)

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

_optimal_make_jobs() {
  if [ -r /proc/cpuinfo ]; then
    local core_count=$(grep -Fc processor /proc/cpuinfo)
  else
    local core_count=0
  fi  
  if [ $core_count -gt 1 ]; then
    echo -n $[$core_count-1]
  else
    echo -n 1
  fi  
}

build() {

  cd ${srcdir}/libmicrohttpd-${pkgver}
  # we can't build the examples because iberty is missing
  sed -i 's/examples//g' src/Makefile.in
  sed -i 's/examples//g' src/Makefile.am

  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"

    ${srcdir}/libmicrohttpd-${pkgver}/configure \
      --disable-https \
      --disable-static \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make -j$(_optimal_make_jobs)
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install    
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g 
  done
}

# vim:set ts=2 sw=2 et:
