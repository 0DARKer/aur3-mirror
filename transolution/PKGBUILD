# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=transolution
pkgver=0_4b5
pkgrel=4
pkgdesc="Computer Aided Translation (CAT) suite supporting the XLIFF standard"
arch=('any')
url="http://transolution.python-hosting.com/"
license=('GPL')
depends=('python' 'pygtk' '4suite' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
install=$pkgname.install
source=(http://downloads.sourceforge.net/eviltrans/Transolution-$pkgver.zip \
        $pkgname.desktop)
md5sums=('fa21fe1b58503aea16f833ef239283f8'
         'e3ea7d1689fdf0b3ad8eef2ed727b6b8')

build() {
  cd ${srcdir}/Transolution-$pkgver

  install -d ${pkgdir}/usr/share/$pkgname \
             ${pkgdir}/usr/bin \
             ${pkgdir}/usr/share/$pkgname/filters || return 1

  cp -r * ${pkgdir}/usr/share/$pkgname || return 1
  rm ${pkgdir}/usr/share/$pkgname/*{bat,txt,ico,png} || return 1
  find ${pkgdir}/usr/share/$pkgname/$pkgname -type f -exec chmod 644 "{}" \; || return 1
  install -D -m644 readme.txt ${pkgdir}/usr/share/$pkgname/README || return 1

#links to run files
  for i in *.py
    do ln -s /usr/share/$pkgname/$i ${pkgdir}/usr/bin/$i || return 1
  done

#icons and .desktop file
  install -D -m644 icon_16x16.png \
    ${pkgdir}/usr/share/icons/hicolor/16x16/apps/$pkgname.png || return 1
  install -D -m644 icon_32x32.png \
    ${pkgdir}/usr/share/icons/hicolor/32x32/apps/$pkgname.png || return 1
  install -D -m644 ${srcdir}/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop || return 1

#links to filter files for easy use
  cd ${pkgdir}/usr/share/$pkgname/$pkgname/filters/filter_settings
  for i in *.ini
    do ln -s /usr/share/$pkgname/$pkgname/filters/filter_settings/$i \
      ${pkgdir}/usr/share/$pkgname/filters/$i || return 1
  done
}
