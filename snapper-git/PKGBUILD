# Maintainer: Tom Kuther <gimpel@sonnenkinder.org>

pkgname=snapper-git
_gitname=snapper
pkgver=0.1.4.git.18ae303
pkgrel=1
pkgdesc="A tool for managing BTRFS and LVM snapshots. It can create, diff and restore snapshots and provides timelined auto-snapping."
provides=('snapper')
conflicts=('snapper')
arch=('i686' 'x86_64')
url="http://en.opensuse.org/Portal:Snapper"
license=('GPL2')
depends=('btrfs-progs' 'libxml2' 'dbus')
makedepends=('boost' 'lvm2')
backup=('etc/conf.d/snapper')
source=('snapper::git+https://github.com/openSUSE/snapper.git'
        'btrfs-master.patch')
md5sums=('SKIP'
         '34af4113b6a39030c27ecdb81a3ff457')
install=snapper.install

pkgver() {
  cd $_gitname
  echo $(cat VERSION).git.$(git rev-parse --short master)
}

build() {
  cd $_gitname

  # rename cron scripts
  sed -i -e 's@suse.de-@@g' scripts/Makefile.am
  # fix config location in cron scripts
  sed -i -e 's@/etc/sysconfig/@/etc/conf.d/@g' scripts/snapper-{daily,hourly}

  # boost fixlets
  # don't link against non-existant libbost_thread-mt
  sed -i -e 's@lboost_thread-mt@lboost_thread@g' snapper/Makefile.am
  sed -i -e 's@lboost_system-mt@lboost_system@g' snapper/Makefile.am

  # https://github.com/openSUSE/snapper/issues/19
  # uncomment if you're using btrfs-git from 2013/05/10 or newer
  #patch -p1 < $srcdir/btrfs-master.patch

  aclocal
  libtoolize --force --automake --copy
  autoheader
  automake --add-missing --copy
  autoconf
  ./configure --prefix=/usr \
              --with-conf=/etc/conf.d \
              --disable-zypp \
              --disable-silent-rules \
              --disable-ext4 \
              --enable-xattrs
  make
}

package() {
  cd $_gitname
  make DESTDIR=${pkgdir} install
  mkdir ${pkgdir}/etc/conf.d
  install -D -m 644 data/sysconfig.snapper ${pkgdir}/etc/conf.d/snapper
}

# vim:set ts=2 sw=2 et:
