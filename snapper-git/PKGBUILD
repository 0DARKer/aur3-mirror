# Maintainer: Tom Kuther <gimpel@sonnenkinder.org>

pkgname=snapper-git
_gitname=snapper
pkgver=0.1.3.4263aa3
pkgrel=1
pkgdesc="A tool for managing BTRFS and LVM snapshots. It can create, diff and restore snapshots and provides timelined auto-snapping."
provides=('snapper')
conflicts=('snapper')
arch=('i686' 'x86_64')
url="http://en.opensuse.org/Portal:Snapper"
license=('GPL2')
depends=('btrfs-progs' 'libxml2' 'dbus')
makedepends=('boost' 'lvm2')
backup=('etc/conf.d/snapper')
source=('snapper::git+https://github.com/openSUSE/snapper.git')
md5sums=('SKIP')
install=snapper.install

pkgver() {
  cd $_gitname
  echo $(cat VERSION).$(git rev-parse --short master)
}

build() {
  cd $_gitname

  # rename cron scripts
  sed -i -e 's@suse.de-@@g' scripts/Makefile.am

  # boost fixlets
  # don't link against non-existant libbost_thread-mt
  sed -i -e 's@lboost_thread-mt@lboost_thread@g' snapper/Makefile.am
  # add -lboost_system
  sed -E -i 's@snapperd_LDADD(.*)@snapperd_LDADD\1 \-lboost_system@g' server/Makefile.am
  sed -E -i 's@snapper_LDADD(.*)@snapper_LDADD\1 \-lboost_system@g' client/Makefile.am
  sed -E -i 's@LDADD(.*)@LDADD\1 \-lboost_system@g' examples/Makefile.am
  sed -E -i 's@LDADD(.*)@LDADD\1 \-lboost_system@g' testsuite-cmp/Makefile.am

  aclocal
  libtoolize --force --automake --copy
  autoheader
  automake --add-missing --copy
  autoconf
  ./configure --prefix=/usr \
              --with-conf=/etc/conf.d \
              --disable-zypp \
              --disable-silent-rules \
              --disable-ext4 \
              --enable-xattrs
  make
}

package() {
  cd $_gitname
  make DESTDIR=${pkgdir} install
  mkdir ${pkgdir}/etc/conf.d
  install -D -m 644 data/sysconfig.snapper ${pkgdir}/etc/conf.d/snapper
}

# vim:set ts=2 sw=2 et:
