# Maintainer: Tom Kuther <gimpel@sonnenkinder.org>

pkgname=snapper-git
pkgver=20120201
pkgrel=1
pkgdesc="A tool for managing btrfs snapshots. It can compare snapshots and revert differences between snapshots."
provides=('snapper')
conflicts=('snapper')
arch=('i686' 'x86_64')
url="http://en.opensuse.org/Portal:Snapper"
license=('GPL2')
depends=('libxml2' 'blocxx-svn' 'openssl' 'pcre' 'python')
makedepends=('boost' 'swig')
backup=('etc/conf.d/snapper')
source=()
install=snapper.install

_gitname='snapper'
_gitroot='git://gitorious.org/opensuse/snapper.git'

build() {
	if [ -d ${srcdir}/${_gitname}/.git ] ; then
		( cd ${srcdir}/${_gitname} && git pull )
		msg "The local files are updated."
	else
		( git clone ${_gitroot} ${_gitname} )
	fi
	msg "GIT checkout done or server timeout"

	msg "creating build directory"
	cd ${srcdir}
	[ -d ${_gitname}-build ] && rm -rf ${_gitname}-build
	cp -a ${_gitname} ${_gitname}-build

	msg "Starting make..."
	cd ${_gitname}-build

	# use /etc/conf.d instead of /etc/sysconfig	
	sed -i -e 's@/etc/sysconfig@/etc/conf.d@g' \
		snapper/SnapperDefines.h \
		scripts/* \
		doc/*
	# don't install zypper plugin
	sed -i '/usr\/lib\/zypp/d' scripts/Makefile.am
	# rename cron scripts
	sed -i -e 's@suse.de-@@g' scripts/Makefile.am

	aclocal
	libtoolize --force --automake --copy
	autoheader
	automake --add-missing --copy
	autoconf
	export CPPFLAGS="-I/usr/include/libxml2 -I/usr/include/python3.2mu"
	./configure --prefix=/usr
	make
}

package() {
	cd ${_gitname}-build
	msg2 "Running make install"
	make DESTDIR=${pkgdir} install
	mkdir ${pkgdir}/etc/conf.d
	mv ${pkgdir}/var/adm/fillup-templates/sysconfig.snapper ${pkgdir}/etc/conf.d/snapper
	rm -rf ${pkgdir}/var
}
