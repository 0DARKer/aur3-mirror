# Maintainer: Tom Kuther <gimpel@sonnenkinder.org>

pkgname=snapper-git
pkgver=20120131
pkgrel=1
pkgdesc="A tool for managing btrfs snapshots. It can compare snapshots and revert differences between snapshots."
provides=('snapper')
conflicts=('snapper')
arch=('i686' 'x86_64')
url="http://en.opensuse.org/Portal:Snapper"
license=('GPL2')
depends=('libxml2' 'blocxx' 'boost' 'python2')
makedepends=()
source=()
install=snapper.install

_gitname='snapper'
_gitroot='git://gitorious.org/opensuse/snapper.git'

build() {
	if [ -d ${srcdir}/${_gitname}/.git ] ; then
		( cd ${srcdir}/${_gitname} && git pull )
		msg "The local files are updated."
	else
		( git clone ${_gitroot} ${_gitname} )
	fi
	msg "GIT checkout done or server timeout"

	msg "creating build directory"
	cd ${srcdir}
	[ -d ${_gitname}-build ] && rm -rf ${_gitname}-build
	cp -a ${_gitname} ${_gitname}-build

	msg "Starting make..."
	cd ${_gitname}-build
	
	sed -i -e 's@/etc/sysconfig@/etc/conf.d@g' \
		snapper/SnapperDefines.h \
		scripts/* \
		doc/*

	aclocal
	libtoolize --force --automake --copy
	autoheader
	automake --add-missing --copy
	autoconf
	export CPPFLAGS="-I/usr/include/libxml2 -I/usr/include/python2.7"
	./configure --prefix=/usr
	make
}

package() {
	cd ${_gitname}-build
	msg2 "Running make install"
	make DESTDIR=${pkgdir} install
	rm -rf ${pkgdir}/usr/lib/zypp/
	mkdir ${pkgdir}/etc/conf.d
	echo '#SNAPPER_CONFIGS=""' > ${pkgdir}/etc/conf.d/snapper
}
