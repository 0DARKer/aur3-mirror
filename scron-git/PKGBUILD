# Maintainer: Ariel Dabalsa <arieldabalsa@gmail.com>
pkgname=scron-git
_gitname=scron
pkgver=r138.082c98f
pkgrel=3
pkgdesc='Suckless cron daemon - git checkout'
arch=('i686' 'x86_64')
url='http://git.2f30.org/scron'
license=('MIT/X')
makedepends=('git')
provides=('cron')
conflicts=('cron')
backup=('etc/crontab')
source=('git://git.2f30.org/scron'
        'service')
md5sums=('SKIP'
         '8e41784c5c21d1be99cb3b0d87e96709')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"

  # Fix crontab install dir
  sed -i 's=$(DESTDIR)$(PREFIX)/etc=$(DESTDIR)/etc=' Makefile

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm644 LICENSE           "$pkgdir/usr/share/licenses/$_gitname/LICENSE"
  install -Dm644 README            "$pkgdir/usr/share/doc/$_gitname/README"
  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/scron.service"
}

# vim:set ts=2 sw=2 et:
