# Maintainer: Ariel Dabalsa <arieldabalsa@gmail.com>
pkgname=scron-git
pkgver=r138.082c98f
pkgrel=2
pkgdesc='Suckless cron daemon'
arch=('i686' 'x86_64')
url='http://git.2f30.org/scron'
license=('MIT/X')
makedepends=('git')
provides=('cron')
conflicts=('cron')
backup=('etc/crontab')
source=("$pkgname"::'git://git.2f30.org/scron'
        'service')
md5sums=('SKIP'
         'e0535590cbe60425672ca9aee43962bb')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"

  sed -i '1s/^/# Separators must be tabs.  No lists supported (1,5,7,...)\n/' crontab

  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm755 crond             "$pkgdir/usr/bin/crond"
  install -Dm644 crontab           "$pkgdir/etc/crontab"
  install -Dm644 LICENSE           "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/scron.service"
}

# vim:set ts=2 sw=2 et:
