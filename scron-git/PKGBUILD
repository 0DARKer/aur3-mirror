# Maintainer: Ariel Dabalsa <arieldabalsa@gmail.com>

pkgname=scron-git
_gitname=scron
pkgver=r138.082c98f
pkgrel=2
pkgdesc='Suckless cron daemon'
arch=('i686' 'x86_64')
url='http://git.2f30.org/scron'
license=('MIT/X')
makedepends=('git')
provides=('cron')
conflicts=('cron')
backup=('etc/crontab')
source=('git://git.2f30.org/scron'
        'service')
md5sums=('SKIP'
         '8e41784c5c21d1be99cb3b0d87e96709')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"

  sed -i '1s/^/# Use tabs for separators.  No lists supported (1,5,7,...)\n/' crontab

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm755 crond             "$pkgdir/usr/bin/crond"
  install -Dm644 crontab           "$pkgdir/etc/crontab"
  install -Dm644 LICENSE           "$pkgdir/usr/share/licenses/$_gitname/LICENSE"
  install -Dm644 README            "$pkgdir/usr/share/doc/$_gitname/README"
  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/scron.service"
}

# vim:set ts=2 sw=2 et:
