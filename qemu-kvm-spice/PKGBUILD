# $Id: PKGBUILD 110855 2011-02-22 19:17:39Z tpowa $
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=qemu-kvm-spice
_pkgname=qemu-kvm
pkgver=0.14.1
pkgrel=1
pkgdesc="Latest KVM QEMU is a generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://www.linux-kvm.org"
depends=('libjpeg' 'libpng' 'libsasl' 'curl' 'sdl' 'alsa-lib' 'esound' 'gnutls>=2.4.1' 'bluez' 'vde2' 'util-linux-ng' 'spice')
makedepends=('texi2html' 'perl' )
backup=('etc/qemu/target-x86_64.conf')
install=qemu-kvm.install
conflicts=('qemu' 'qemu-kvm')
provides=('qemu')
replaces=('kvm')
options=(!strip)
source=("http://downloads.sourceforge.net/kvm/${_pkgname}-${pkgver}.tar.gz"
        "65-kvm.rules")

build()
{
    cd ${srcdir}/${_pkgname}-${pkgver}
    # fix esound building
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --audio-drv-list=alsa,sdl,oss,esd \
                --audio-card-list=ac97,sb16,es1370,adlib \
		--enable-spice \
                --enable-docs
    make
}
package()
{
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    # symbolic link for backwards compatibility
    ln -s qemu-system-x86_64 ${pkgdir}/usr/bin/qemu-kvm
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 ${pkgdir}/usr/bin/qemu
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 ${pkgdir}/usr/bin/kvm
    # fix man page
    mv ${pkgdir}/usr/share/man/man1/qemu.1 \
                     ${pkgdir}/usr/share/man/man1/qemu-kvm.1
    # install udev rules
    install -D -m644 ${srcdir}/65-kvm.rules \
                     ${pkgdir}/lib/udev/rules.d/65-kvm.rules
}

md5sums=('ed37a2c0b1981220b842920140574c63'
         'b316a066d2f1bb57d8f5b7ea1d0d1caf')
