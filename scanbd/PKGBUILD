# Contributor: Milan Knizek <knizek@volny.cz>
# Maintainer: Milan Knizek <knizek@volny.cz>
pkgname=scanbd
pkgver=1.3.1
pkgrel=1
pkgdesc="Scanner button daemon looking for scanner button pressed"
arch=('i686' 'x86_64')
url="http://scanbd.sourceforge.net/"
license=('GPL')
depends=('sane' 'systemd' 'libusb-compat' 'confuse')
source=("http://downloads.sourceforge.net/scanbd/scanbd--$pkgver.zip")
install=scanbd.install
backup=('etc/scanbd/scanbd.conf')
_trunkrev=181

## Note that original scanbd documentation is based on --prefix=/usr/local
## This package uses standard hierarchy (/usr, /etc)

build() {
  cd ${srcdir}/${pkgname}-code-${_trunkrev}-trunk
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd ${srcdir}/${pkgname}-code-${_trunkrev}-trunk
  make DESTDIR=${pkgdir} install

  install -d $pkgdir/etc/dbus-1/system.d || return 1
  install -m644 integration/scanbd_dbus.conf $pkgdir/etc/dbus-1/system.d || return 1
  sed -i 's@<policy user="saned">@<policy user="nobody">@' $pkgdir/etc/dbus-1/system.d/scanbd_dbus.conf || return 1

  install -d $pkgdir/usr/lib/systemd/system || return 1
  install -m644 integration/systemd/scanbd.service  $pkgdir/usr/lib/systemd/system || return 1
  sed -i 's@\(Environment=SANE_CONFIG_DIR=/etc/scanbd\)$@\1/sane.d@' $pkgdir/usr/lib/systemd/system/scanbd.service || return 1

  install -m644 integration/systemd/scanbm@.service $pkgdir/usr/lib/systemd/system || return 1
  sed -i 's@\(User=\)saned@\1nobody@'                                $pkgdir/usr/lib/systemd/system/scanbm@.service || return 1
  sed -i 's@\(Group=\)saned@\1scanner@'                              $pkgdir/usr/lib/systemd/system/scanbm@.service || return 1
  sed -i 's@\(Environment=SANE_CONFIG_DIR=/etc/scanbd\)$@\1/sane.d@' $pkgdir/usr/lib/systemd/system/scanbm@.service || return 1
 
  install -m644 integration/systemd/scanbm.socket   $pkgdir/usr/lib/systemd/system || return 1

  install -d $pkgdir/usr/share/dbus-1/system-services || return 1
  install -m644 integration/systemd/de.kmux.scanbd.server.service $pkgdir/usr/share/dbus-1/system-services || return 1

  install -d $pkgdir/usr/share/doc/$pkgname || return 1
  install -m644 doc/README.txt AUTHORS ${pkgdir}/usr/share/doc/$pkgname || return 1

  install -d $pkgdir/etc/scanbd/sane.d || return 1

  sed -i 's@\(        user    = \)saned$@\1nobody@' $pkgdir/etc/scanbd/scanbd.conf || return 1
  sed -i 's@\(        group   = \)lp$@\1scanner@'   $pkgdir/etc/scanbd/scanbd.conf || return 1
  sed -i 's@/var\(/run/scanbd.pid\)@\1@'            $pkgdir/etc/scanbd/scanbd.conf || return 1
  sed -i 's@\(debug-level =\)7$@\1 2@'              $pkgdir/etc/scanbd/scanbd.conf || return 1
  sed -i 's@\(SANE_CONFIG_DIR=\)/usr/local\(/etc/scanbd\)@\1\2/sane.d@'  $pkgdir/etc/scanbd/scanbd.conf || return 1

}
md5sums=('f69bf3c6f41d1afe0b986e9ec67b4f83')
