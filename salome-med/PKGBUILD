# Maintainer: Michele Mocciola <mickele>

pkgname=salome-med
pkgver=5.1.4
pkgrel=1
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation. MED Module."
url="http://www.salome-platform.org"
depends=('salome-kernel' 'salome-gui' 'python' 'qt' 'boost' 'opencascade' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'hdf5' 'med' 'vtk>=5.6' 'graphviz' 'scotch' 'metis' 'sip' 'cppunit' 'flex' 'bison' 'sed')
makedepends=('gcc' 'automake' 'autoconf' 'libtool' 'make' 'doxygen' 'docutils' 'patch' 'coreutils')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
source=("http://files.opencascade.com/Salome/Salome${pkgver}/src${pkgver}.tar.gz" "salome-med-5.1.4_med_int.diff" "salome-med-5.1.4_scotch-metis.diff" "salome-med-5.1.4_missing-link.diff" "${pkgname}.profile")

_source=src${pkgver}
_installdir=/opt/salome/med
_pythonver=2.6
_vtkver=5.6

build() {
  source /etc/profile.d/salome-kernel.sh || return 1
  source /etc/profile.d/salome-gui.sh || return 1

  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
  export LDFLAGS="${LDFLAGS//,--as-needed}"
  export LDFLAGS="${LDFLAGS//--as-needed}"

  if [ "$CARCH" = "x86_64" ]; then
     # to solve compatibility problem with libmed
     export CPPFLAGS="${CPPFLAGS} -DHAVE_F77INT64"
  fi

  # flag usefull to solve link problems
  # export LDFLAGS="-Wl,--no-undefined ${LDFLAGS}"

  cd ${srcdir}/${_source}/MED_SRC_${pkgver} || return 1

  patch -Np1 -i ${srcdir}/salome-med-5.1.4_med_int.diff || return 1
  patch -Np1 -i ${srcdir}/salome-med-5.1.4_missing-link.diff || return 1
  patch -Np1 -i ${srcdir}/salome-med-5.1.4_scotch-metis.diff || return 1

  # Builds and install
  ./clean_configure || return 1
  ./build_configure || return 1
  ./configure --prefix=${_installdir} \
	      --with-python-site=/usr/lib/python-${_pythonver}/site-packages/salome \
	      --with-python-site-exec=/usr/lib/python-${_pythonver}/site-packages/salome \
	      --with-qt=/usr \
	      --with-vtk=/usr \
	      --with-vtk-version=-${_vtkver} \
	      --with-scotch=/usr \
 	      --with-metis=/usr \
      	      --enable-production || return 1

  make || return 1
}

package() {
  cd ${srcdir}/${_source}/MED_SRC_${pkgver} || return 1

  make DESTDIR="${pkgdir}" install || return 1

  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/profile.d/${pkgname}.sh" || return 1

}

md5sums=('ec363b2ff2dbdb889c973e9e19b2970f'
         'a1e136d250d40fe74973a28341ad3dc4'
         '5023e1aca8908e8df1bf2fb70ed863fa'
         '7a0eab93cfb114f18f46288af83c04c9'
         '99a82599773abf8f0c77c18c7466af54')
