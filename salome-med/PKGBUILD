# Maintainer: Michele Mocciola <mickele>

pkgname=salome-med
pkgver=7.4.0
pkgrel=2
pkgdesc="Generic platform for Pre and Post-Processing for numerical simulation (KERNEL Module)"
url="http://www.salome-platform.org"
depends=('python2' 'libxml2' 'scotch>=5.1.11' 'boost-libs' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'graphviz' 'metis4' 'paraview-salome' 'salome-gui=7.4.0' 'opencascade>=6.7.1' 'cppunit')
if test "$CARCH" == x86_64; then
  depends+=('med-salome>=3.0.7')
else
  depends+=('med>=3.0.7')
fi
makedepends=('doxygen' 'boost' 'swig' 'python2-sphinx')
arch=('i686' 'x86_64')
license=('LGPL')
source=(http://files.salome-platform.org/Salome/Salome${pkgver}/src${pkgver}.tar.gz salome-med.profile)

_source=MED_SRC_${pkgver}
_installdir=/opt/salome/med
_paraviewrootdir=/usr
_paraviewver=4.1

prepare() {
  cd "${srcdir}/${_source}"
  
  # python -> python2
  for _FILE in `grep -Rl "/usr/bin/env python" * `
  do
	sed -e "s|/usr/bin/env python|/usr/bin/env python2|" -i ${_FILE}
  done
}
build() {
  source /etc/salome/profile.d/salome-kernel.sh
  source /etc/salome/profile.d/salome-gui.sh

  rm -rf "${srcdir}/${_source}/build"
  mkdir "${srcdir}/${_source}/build"
  cd "${srcdir}/${_source}/build"

  cmake .. \
     -DCMAKE_INSTALL_PREFIX=$_installdir \
     -DPYTHON_EXECUTABLE=/usr/bin/python2 \
     -DSPHINX_EXECUTABLE=/usr/bin/sphinx-build2 \
     -DSALOME_MED_PARTITIONER_METIS=BOOL:ON \
     -DMETIS_INCLUDE_DIRS=/usr/include/metis-4 \
     -DMETIS_LIBRARIES=/usr/lib/libmetis-4.so \
     -DSALOME_MED_PARTITIONER_SCOTCH=BOOL:ON \
     -DSCOTCH_ERR_LIBRARIES=/usr/lib/libscotcherr.so \
     -DSCOTCH_INCLUDE_DIRS=/usr/include/scotch \
     -DSCOTCH_LIBRARIES=/usr/lib/libscotch.so \
     -DVTK_DIR="${_paraviewrootdir}/lib/cmake/paraview-${_paraviewver}"
  make
}

package() {
  cd "${srcdir}/${_source}/build"

  make DESTDIR="$pkgdir/" install

  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/salome/profile.d/${pkgname}.sh"

}
md5sums=('18055915a14e0921563072f830614e7f'
         '9f967165dfe92aff3a5f58aae334e2d1')
