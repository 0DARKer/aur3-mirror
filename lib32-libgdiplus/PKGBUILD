_pkgbasename=libgdiplus
pkgname=lib32-$_pkgbasename
pkgver=2.8.1
pkgrel=1
pkgdesc="An Open Source Implementation of the GDI+ API (32 bit, library only)"
arch=('x86_64')
license=('MPL' 'LGPL')
url="http://www.mono-project.com"
depends=('lib32-libtiff>=3.9.2' 'lib32-cairo>=1.8.10' 'lib32-giflib' 
		 'lib32-glib2>=2.24.0' 'lib32-libexif' "$_pkgbasename>=$pkgver")
makedepends=('pkgconfig' 'gcc-multilib')
options=('!libtool')
source=(http://ftp.novell.com/pub/mono/sources/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('b9be687fe7decea69128df252faa33e1')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --with-cairo=system \
					--libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm ${pkgdir}/usr/lib32/*.a
  # Install licenses
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  ln -fs ../common/MPL/license.txt \
  				${pkgdir}/usr/share/licenses/${pkgname}/MPL
  ln -fs ../common/LGPL/license.txt \
  				${pkgdir}/usr/share/licenses/${pkgname}/LGPL
}
