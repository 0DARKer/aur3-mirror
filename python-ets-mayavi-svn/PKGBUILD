# Contributor: giniu <gginiu@gmail.com>
pkgname=python-ets-mayavi-svn
pkgver=25986
pkgrel=1
pkgdesc="The Mayavi scientific data 3-dimensional visualizer."
arch=('i686' 'x86_64')
url="http://code.enthought.com/projects/mayavi"
license=('BSD')
depends=('python2' 'python-numpy>=1.1' 'python-configobj' 'ipython' 'vtk>=5'
         'python-ets-apptools-svn' 'python-ets-enthoughtbase-svn'
         'python-ets-envisagecore-svn' 'python-ets-envisageplugins-svn'
         'python-ets-traits-svn' 'python-ets-traitsgui-svn')
makedepends=('subversion' 'gcc>=3.4' 'python2-distribute')
conflicts=('python-enthought-mayavi')
options=(!emptydirs)

_svnmod=Mayavi
_svntrunk=https://svn.enthought.com/svn/enthought/"${_svnmod}"/trunk

build() {
  cd "$srcdir"

  if [ -d "$_svnmod"/.svn ]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_svnmod-build"
  svn export "$_svnmod" "$_svnmod-build"
  cd "$_svnmod-build"

  python2 setup.py install --root="$pkgdir"/ --optimize=1 || return 1

  # get rid of docs, they are in separate package
  _target="$pkgdir`python2 -c 'from distutils.sysconfig import get_python_lib; print get_python_lib()'`"
  rm -rf "${_target}"/enthought/docs
  rm -rf "${_target}"/enthought/mayavi/html
  rm -rf "${_target}"/enthought/tvtk/html
}

