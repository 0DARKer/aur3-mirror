# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=chessx
pkgname=$_pkgname-svn
pkgver=0.9.6.1451
pkgrel=1
pkgdesc="A free chess database"
arch=(i686 x86_64)
url=http://$_pkgname.sourceforge.net
license=(GPL2)
depends=(qt4)
makedepends=(subversion)
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
options=(!emptydirs)
install=$_pkgname.install
source=($pkgname::svn+http://$_pkgname.svn.sourceforge.net/svnroot/$_pkgname/trunk
    $_pkgname.desktop
    $_pkgname.png)
sha256sums=('SKIP'
    '3c68442c3a06bcbe9c5cc976c6ba263647e3a49f134b8e731006709bdd9f41a8'
    'b7cfb00a9fd533011529a18e0c0d52deb7b65ae872c68963df7b99116d3f4256')
sha512sums=('SKIP'
    '1f7837d191b99485fc77736abd0d343773b3170ffe260a3cad5fd7d6a02decdae6d332a70c04a22aa530ef747ba38a8ace898c08fa430a24146389bf5a4649d2'
    'f2e42766b166f56aed4cc65e79c187162b8e15aa22407ccc97a449e8380b33d034458d1318dbaab43e26c90af2f67373ae50d86188609c1b5642204bd247fe41')

pkgver() {
    echo $(grep -o 'Version [0-9.]\+' -m1 $pkgname/ChangeLog | tr -d '[a-zA-Z ]').$(svnversion "$SRCDEST"/$pkgname/)
}

prepare() {
    sed -i 's:qm:ts:g' $pkgname/resources.qrc
}

build() {
    cd $pkgname/
    qmake-qt4
    make
}

package() {
    desktop-file-install $_pkgname.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 $_pkgname.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png
    install -Dm755 $pkgname/release/$_pkgname "$pkgdir"/usr/bin/$_pkgname
}
