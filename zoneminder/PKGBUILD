
# Contributor: Troy Will                 <troydwill@gmail.com>
# Contributor: /dev/rs0                  </dev/rs0@secretco.de.com>
# Contributor: Jacek Burghardt           <jacek@hebe.us>
# Contributor: Vojtech Aschenbrenner     <v@asch.cz>
# Contributor: Jason Gardner             <buhrietoe@gmail.com>
# Contributor: Ross melin                <rdmelin@gmail.com>
# Contributor (Parabola): Márcio Silva   <coadde@lavabit.com>
# Contributor (Parabola): André Silva    <emulatorman@lavabit.com>
# Orginally based on a Debian Squeeze package

pkgbase=zoneminder
pkgname=zoneminder
pkgver=1.27.0
pkgrel=4
pkgdesc='Capture, analyse, record and monitor video security cameras'
arch=( i686 x86_64 mips64el arm )

backup=( etc/zm.conf )
url="https://github.com/ZoneMinder/ZoneMinder/releases"
license=( GPL )

depends=(
    apache
    cambozola
    gnutls
    mariadb
    perl-archive-zip
    perl-date-manip
    perl-dbd-mysql
    perl-dbi
    perl-expect
    perl-libwww
    perl-mime-lite
    perl-mime-tools
    perl-php-serialization
    perl-net-sftp-foreign
    perl-sys-mmap
    perl-time-modules
    perl-x10
    php
    php-apache
    php-gd
    php-mcrypt
)

makedepends=(
    netpbm
    cmake
)

optdepends=(
    netpbm
)

install=$pkgbase.install

    source=(
        https://github.com/ZoneMinder/ZoneMinder/archive/v$pkgver.tar.gz
        httpd-zoneminder.conf
        zoneminder.service
    )

    sha512sums=(
'8a349558399381a9062365ddc8bd8f815e3800929914096b2e4ea63e4d6dd12054f7b849fab5bea4bcfc87ea60739479a55734c7075a74aab0622d35f1d2bb14'
'fb9bf263c37fae30d775872a33cb319f2f2a7a4f38faff8c143253dbefd7278b295d0805e11ace6423a8ec2b50ef60f3426b6e6a53548c867ef7f109baa52c36'
'd04aede00d2f008e7851f69a62633f27d4f747b6fa4350e3096415cc7c2659d677707af3e397295010fa05794ff9cbb995c3904e6989ebfbd58ba6b4bfcc002c'
    )

build() {
    cd $srcdir/ZoneMinder-$pkgver

    cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DZM_CGIDIR=/srv/http/cgi-bin \
        -DZM_WEBDIR=/srv/http/zoneminder \
        -DZM_WEB_USER=http \
        -DZM_CONTENTDIR=/var/cache/zoneminder
    make V=0
}

package() {

    cd $srcdir/ZoneMinder-$pkgver

    DESTDIR=$pkgdir make install

    mkdir -p $pkgdir/var/{cache/$pkgbase,log/$pkgbase}
    mkdir -p $pkgdir/srv/zoneminder/socks
    chown -Rv http.http $pkgdir/{etc/zm.conf,var/{cache/$pkgbase,log/$pkgbase}}
    chown -Rv http.http $pkgdir/srv/zoneminder/socks
    chmod 0700 $pkgdir/etc/zm.conf

    # for i in events images temp; do
    
    # /srv/http/zoneminder/events will be a link to /var/cache/zoneminder/events
    # /srv/http/zoneminder/images will be a link to /var/cache/zoneminder/images
    for i in events images; do
        mkdir $pkgdir/var/cache/$pkgbase/$i
        ln -s /var/cache/$pkgbase/$i         $pkgdir/srv/http/$pkgbase/$i
        chown -v    http.http                $pkgdir/var/cache/$pkgbase/$i
        chown -v -h http.http                $pkgdir/srv/http/$pkgbase/$i
    done
    
    # Usage: $0 [-q] [-z zm.conf] [-w WEB DIRECTORY] [CONTENT DIRECTORY]
    # ./zmlinkcontent.sh -w $pkgdir/srv/http/zoneminder $pkgdir/var/cache/zoneminder
    # ./zmlinkcontent.sh -w /srv/http/zoneminder /var/cache/zoneminder

    # /srv/http/zoneminder/cgi-bin will be a link to /srv/http/cgi-bin
    ln -sv /srv/http/cgi-bin $pkgdir/srv/http/$pkgbase

    chown -h http.http $pkgdir/srv/http/{cgi-bin,$pkgbase,$pkgbase/cgi-bin}

    ln -s /usr/share/cambozola/cambozola.jar $pkgdir/srv/http/$pkgbase

    mkdir -p $pkgdir/etc/httpd/conf/extra
    install -D -m 644 $srcdir/httpd-$pkgbase.conf $pkgdir/etc/httpd/conf/extra
    # install -D -m 644 $srcdir/$pkgbase            $pkgdir/etc/rc.d # Removed
    mkdir -p $pkgdir/usr/lib/systemd/system
    install -D -m 644 $srcdir/$pkgbase.service    $pkgdir/usr/lib/systemd/system
    install -D -m 644 COPYING                     $pkgdir/usr/share/license/$pkgbase
    install -D -m 644 db/zm*.sql                  $pkgdir/usr/share/$pkgbase/db     

}
