# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Michal Soltys <soltys@ziu.info>
pkgname=arptables
pkgver=0.0.3.4
_realver='v0.0.3-4'
pkgrel=1
pkgdesc="ARP filtering utility."
arch=(i686 x86_64)
backup=(etc/conf.d/arptables)
url="http://ebtables.sourceforge.net/"
depends=('glibc' 'perl')
license=('GPL')
source=(http://downloads.sourceforge.net/ebtables/$pkgname-$_realver.tar.gz
        arptables.sh)

md5sums=('1d4ab05761f063b0751645d8f2b8f8e5'
         'b2e57fcd9d96a4a0978db5ac6f655b13')

build() {
  cd "$srcdir/$pkgname-$_realver"
  make
}

package() {
  cd "$srcdir/$pkgname-$_realver"
  mkdir -p "$pkgdir/etc/rc.d"
  mkdir -p "$pkgdir/etc/arptables"
  make install \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    LIBDIR=/usr/lib/arptables \
    BINDIR=/usr/sbin \
    MANDIR=/usr/share/man \
    INITDIR=/etc/rc.d \
    SYSCONFIGDIR=/etc/arptables
  # use adjusted script
  install -o root -m0755 -D ../arptables.sh "$pkgdir/etc/rc.d/arptables"
}
