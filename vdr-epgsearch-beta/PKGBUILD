# Contributor: David Spicer <azleifel at gmail dot com>

pkgname=vdr-epgsearch-beta
_pluginname=epgsearch
pkgver=1.0.1.beta4
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin providing a replacement schedule with extended functionality (Current Beta)"
url="http://winni.vdr-developer.org/epgsearch/"
license=('GPL2')
depends=('vdr>=1.7.42' 'perl' 'pcre')
provides=('vdr-epgsearch')
conflicts=('vdr-epgsearch')
backup=('var/lib/vdr/plugins/epgsearch/epgsearchcats.conf'
        'var/lib/vdr/plugins/epgsearch/epgsearchcmds.conf'
        'var/lib/vdr/plugins/epgsearch/epgsearchmenu.conf')
install=${pkgname}.install
source=(http://winni.vdr-developer.org/epgsearch/downloads/beta/vdr-${_pluginname}-${pkgver}.tgz)
md5sums=('69fa10c35e9f4a617b4d31a64e402abb')

build() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  # VDR directory environment
  _vdrCONFDIR="/var/lib/vdr"
  _vdrINCDIR="/usr/include"
  _vdrLIBDIR="/usr/lib/vdr"
  _vdrLOCDIR="/usr/share/locale"

  make DESTDIR="${pkgdir}" install

  # Install scripts
  install -d -m755 "${pkgdir}/usr/bin"
  install -m755 scripts/*.{pl,sh} "${pkgdir}/usr/bin"
  find "${pkgdir}/usr/bin" -name "*.sh" -exec sed -i "s:\#\!/bin/bash:\#\!/usr/bin/bash:" '{}' \;

  # Install configuration etc. files
  install -d -m755 "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"
  install -m644 conf/* "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"
  install -m644 conf/epgsearchcats.conf-epgdata "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}/epgsearchcats.conf"
  install -m644 scripts/*.conf "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"

  # Install documents (NB. They are also installed as man pages!)
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  for _file in README README.DE HISTORY HISTORY.DE MANUAL; do
    install -m644 ${_file} "${pkgdir}/usr/share/doc/${pkgname}"
  done
}
