# Maintainer: Christian Hesse <mail@eworm.de>

_pkgbasename=libpng
pkgname=lib32-libpng15
pkgver=1.5.18
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files (32 bit, 1.5 branch)"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('lib32-zlib')
makedepends=('gcc-multilib')
conflicts=('lib32-libpng<1.6')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/libpng/libpng-${pkgver}.tar.xz"
	"http://downloads.sourceforge.net/sourceforge/libpng/libpng-${pkgver}.tar.xz.asc"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${pkgver}-apng.patch.gz")

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd libpng-${pkgver}

	# Add animated PNG (apng) support
	# see http://sourceforge.net/projects/libpng-apng/
	patch -p1 -i ../libpng-${pkgver}-apng.patch

	./configure --prefix=/usr --libdir=/usr/lib32
	make
}

package() {
	cd libpng-${pkgver}

	make DESTDIR="${pkgdir}" install

	rm -rf "${pkgdir}/usr/"{bin,include,share}
	rm -rf "${pkgdir}/usr/lib32/"{libpng.so,libpng.a}
	rm -fr "${pkgdir}/usr/lib32/pkgconfig/libpng.pc"
}


sha256sums=('c093c2f779fecb029e51b73d6d646b77edce882474326485f50347606e383585'
            'SKIP'
            '104e1ad2b79fdfc53f30f0aff97f882898360b3c083a8652a9a44101a3511401')
