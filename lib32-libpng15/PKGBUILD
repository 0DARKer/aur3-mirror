# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

_pkgbasename=libpng
pkgname=lib32-libpng15
pkgver=1.5.17
_apngver=1.5.17
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files (32 bit, 1.5 branch)"
arch=('x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('lib32-zlib')
makedepends=('gcc-multilib')
conflicts=('lib32-libpng<1.6')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/libpng/libpng-${pkgver}.tar.xz"
	"http://downloads.sourceforge.net/sourceforge/libpng/libpng-${pkgver}.tar.xz.asc"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${_apngver}-apng.patch.gz")

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd libpng-${pkgver}

	# Add animated PNG (apng) support
	# see http://sourceforge.net/projects/libpng-apng/
	patch -p1 -i ../libpng-${_apngver}-apng.patch

	./configure --prefix=/usr --libdir=/usr/lib32
	make
}

package() {
	cd libpng-${pkgver}
	
	make DESTDIR="${pkgdir}" install

	rm -rf "${pkgdir}/usr/"{bin,include,share}
	rm -rf "${pkgdir}/usr/lib32/"{libpng.so,libpng.a}
	rm -fr "${pkgdir}/usr/lib32/pkgconfig/libpng.pc"
}
sha256sums=('572631353d4ac12433765f7db55e824c2fd4b16019fa41d6738b5e92471292a7'
            'SKIP'
            '3f09f15cc2377dec84c7d2ff8111a7652b27a80cb39c03061e6ccb555cb2416b')
