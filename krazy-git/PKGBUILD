# Maintainer: spider-mario <spidermario@free.fr>
pkgname=krazy-git
pkgver=20130301
pkgrel=1
pkgdesc="Scans KDE source code looking for issues."
arch=('i686' 'x86_64')
url="http://ebn.kde.org/krazy/"
license=('GPL')
depends=('qt4' 'perl-xml-libxml' 'perl-yaml' 'perl-tie-ixhash' 'perl-html-parser')
makedepends=('git' 'inetutils')
provides=('krazy')
conflicts=('krazy2')
options=('!emptydirs')
source=('git://gitorious.org/krazy/krazy.git')
sha1sums=('SKIP')

package() {
  cd "$srcdir/krazy/"

  export QMAKE=qmake-qt4

  ./install.sh "$pkgdir"/usr # let’s just begin with the helper script.

  # These should go to vendor_perl but that makes them fail to detect some
  # directories.
  mv "$pkgdir"/usr/bin/site_perl/* "$pkgdir"/usr/bin/

  rm -r "$pkgdir"/usr/conf/ # not needed and in a non-standard directory.

  # Fix a link that refers to $pkgdir/… instead of /….
  rm "$pkgdir"/usr/lib/Krazy
  ln -s /usr/share/perl5/site_perl/Krazy "$pkgdir"/usr/lib/Krazy
}

# vim:set ts=2 sw=2 et:
