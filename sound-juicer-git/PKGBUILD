# Maintainer Phillip Wood <phillip.wood@dunelm.org.uk>
# Based on the sound-juicer PKGBUILD
#   - Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
#   - Contributor: Jan de Groot <jgc@archlinux.org>
#   - Contributor: Ben <contrasutra@myrealbox.com>

pkgname=sound-juicer-git
_pkgname=${pkgname%-git}
pkgver=1
pkgrel=1
pkgdesc="A cd ripper application"
arch=(i686 x86_64)
license=(GPL)
conflicts=(sound-juicer)
replaces=(sound-juicer)
depends=(libmusicbrainz5 libdiscid gtk3 gst-plugins-base gst-plugins-good brasero hicolor-icon-theme)
optdepends=('gst-plugins-ugly: for MP3 encoding'
            'gst-plugins-bad: for Opus encoding')
makedepends=(intltool yelp-tools gnome-common)
options=(!emptydirs)
url="http://www.gnome.org"
install="$pkgname.install"
source=("git://git.gnome.org/sound-juicer.git"
        "$pkgname.install")
sha256sums=('SKIP'
            'b9e4f14cd493e0b9066a4a22e8a477bbfda18999f994524454b42249646461ee')

pkgver() {
  cd ${srcdir}/${_pkgname}
  echo $(git describe | sed 's#-#_#g;s#v##')
}

build() {
  cd "${srcdir}/${_pkgname}"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --libexecdir=/usr/lib/${_pkgname}

  # drop unneeded direct library deps with --as-needed
  # libtool doesn't make this easy, so we do it the hard way
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  # Remove RPATH
  sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
  sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool

  export tagname=CC
  make AM_CFLAGS=-export-dynamic
}

package() {
  cd "${srcdir}/${_pkgname}"
  make GSETTINGS_DISABLE_SCHEMAS_COMPILE=1 DESTDIR="${pkgdir}" install
}
