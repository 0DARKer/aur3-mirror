# Maintainer: Jekyll Wu <adaptee [at] gmail [dot] com>
# Contributor: Hugo Doria <ly50247@gmail.com>

pkgname=nmap-nogui
pkgver=5.51
pkgrel=3
pkgdesc="A network exploration tool and security/port scanner, without zenmap"
_realname=nmap
arch=('i686' 'x86_64')
url="http://nmap.org"
license=('GPL')
depends=('pcre' 'openssl' 'libpcap' 'lua' 'python2') # python2 for ndiff
makedepends=('python2')
options=('!makeflags')
provides=('nmap')
conflicts=('nmap')
source=(http://nmap.org/dist/${_realname}-${pkgver}.tar.bz2)

build() {
  cd ${srcdir}/${_realname}-${pkgver}

  # Force usage of external libpcap
  # OSX bug check fails because of missing types
  # autoreconf fails because our autotools are too new (?)
  sed -i 's/have_libpcap=no/have_libpcap=yes/g' {.,ncat,nping}/configure

  PYTHON=/usr/bin/python2 ./configure --without-zenmap \
      --prefix=/usr --mandir=/usr/share/man \
      --libexecdir=/usr/lib
  make
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # install custom GPL2 license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('0b80d2cb92ace5ebba8095a4c2850275')
