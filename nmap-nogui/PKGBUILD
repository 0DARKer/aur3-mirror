# $Id: PKGBUILD 173177 2012-12-12 00:31:59Z eric $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=nmap-nogui
_pkgname=nmap
pkgver=6.25
pkgrel=1
pkgdesc='Utility for network discovery and security auditing'
url='http://nmap.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('pcre' 'openssl' 'libpcap' 'lua')
source=("http://nmap.org/dist/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('769943a1a5c10f67bf9738e26da42b3312db752f')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	PYTHON=/usr/bin/python2 ./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--mandir=/usr/share/man \
        --without-zenmap
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
