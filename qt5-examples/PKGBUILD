# Maintainer: Michael Hansen <zrax0111 gmail com>

pkgname=qt5-examples
_srcpkg=qt5
pkgdesc='Examples and demos from qt5 documentation'
pkgver=5.2.0
_qtver=5.2
pkgrel=1
arch=('any')
url='http://qt-project.org/'
license=('LGPL')
makedepends=()
depends=(qt5-base qt5-tools)

_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://download.qt-project.org/official_releases/qt/${_qtver}/${pkgver}/single/${_pkgfqn}.tar.xz")
sha1sums=('7d9d8cad4434bc200d9ed87b3c7cb39654a2f1b4')

package() {
    base=${pkgdir}/usr/share/doc/qt/examples

    # The various example dirs have conflicting .pro files, so keep
    # them split into subdirs
    fdirs=$(find "${srcdir}/${_pkgfqn}" -maxdepth 2 -type d -name examples)
    for dir in $fdirs
    do
        dest=${dir#${srcdir}/${_pkgfqn}/}
        dest=${base}/${dest%/examples}
        mkdir -p ${dest}
        cp -r ${dir}/* ${dest}/
    done
}
