# Maintainer: BrainDamage
swname=scikit-rf
pkgname="python2-$swname-git"
pkgver=0.14.r145.g57f0b32
pkgrel=1
pkgdesc="Scikit-rf (aka skrf) is a python package for RF/Microwave engineering"
arch=(any)
url="http://scikit-rf-web.readthedocs.org"
license=("BSD")
depends=("python2" "python2-numpy" "python2-scipy" "python2-matplotlib")
makedepends=("git")
provides=("python2-$swname")
conflicts=("python2-$swname")
optdepends=(
	"python2-pyvisa: for instrument control"
	"python2-pandas: for xls export"
	"python2-xlrd: for xls reading"
	"python2-xlwt: for xls writing"
	"ipython2: for interactive shell"
)
md5sums=("SKIP")
source=("git+https://github.com/$swname/$swname.git")

pkgver() {
	cd "$swname"
	git describe | sed 's/^v//; s/-/.r/; s/-/./'
}

package() {
	cd "$swname"
	python2 setup.py install --optimize=1 --root="$pkgdir/" --prefix="/usr"
}
