# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=mingw-w64-gconf
pkgver=3.2.6
pkgrel=1
pkgdesc="A configuration database system (mingw-w64)"
arch=('any')
url="http://www.gtk.org"
license=("LGPL")
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config')
depends=('mingw-w64-dbus' 'mingw-w64-dbus-glib' 'mingw-w64-libxml2')
options=('!strip' '!buildflags' 'staticlibs')
source=("http://ftp.gnome.org/pub/gnome/sources/GConf/${pkgver%.*}/GConf-${pkgver}.tar.xz")

sha512sums=('35f5f659f9d03f7531a7102adacbda0eb310d8a55a831c768c91a82e07dae21247726e00e0e411f63b1de9ade0f042ded572a3ea4a4b2ad3135231f344540b58')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/GConf-${pkgver}"

  autoreconf -vfi

  export CFLAGS="-O2 -mms-bitfields"
  export CXXFLAGS="${CFLAGS}"
  unset LDFLAGS

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --build=${CHOST} \
      --host=${_arch} \
      --enable-shared \
      --enable-static \
      --without-openldap \
      --disable-defaults-service \
      --disable-orbit
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/GConf-${pkgver}/build-${_arch}"
    make -j1 DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' -o -name '*.manifest' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/etc/xdg/"
    rm -r "$pkgdir/usr/${_arch}/share/man/"
  done
}
