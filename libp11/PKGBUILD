# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: kevku <kevku@msn.com>
# Contributor: Rene PÃ¤rts <rene87@hot.ee>
pkgname=libp11
pkgver=0.2.8
pkgrel=1
pkgdesc="A library implementing a small layer on top of the PKCS11 API"
arch=('i686' 'x86_64')
url="http://www.opensc-project.org/"
options=('!libtool')
license=('LGPL')
# libtool is required at runtime as it uses libltdl
depends=('openssl' 'libtool')
source=("https://github.com/OpenSC/libp11/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('edbfcb77c5657fb871a0e23836f42eb328333d79ab42cd1343103efef750e301')


build() {
  # Yes, it is actually $pkgname-$pkgname-$pkgver, because of the way github
  # generates the tar ball.
  cd "$srcdir/$pkgname-$pkgname-$pkgver"
  mkdir -p m4
  ./bootstrap
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  # Yes, it is actually $pkgname-$pkgname-$pkgver, because of the way github
  # generates the tar ball.
  cd "$srcdir/$pkgname-$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
