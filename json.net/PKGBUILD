pkgname=json.net
pkgdesc="Json.NET is a popular high-performance JSON framework for .NET"
pkgver=5.0.8
pkgrel=3
arch=(i686 x86_64)
license=("MIT")
options=(!makeflags)
url="http://james.newtonking.com/json"
source=("https://github.com/JamesNK/Newtonsoft.Json/archive/$pkgver.tar.gz")
depends=("mono>=2.11.3")
makedepends=(monodevelop)
sha1sums=('a30f9c3510494a3b84bb9c2e5821fc7a91c633c2')

prepare() {
	cd "${srcdir}/Newtonsoft.Json-$pkgver/Src"
	mdtool generate-makefiles -s -d:Debug Newtonsoft.Json.Net40.sln
}

build() {
	cd "${srcdir}/Newtonsoft.Json-$pkgver/Src"
	./configure --prefix=/usr
	cd Newtonsoft.Json
	make
	cd bin/Debug/Net40
	monodis Newtonsoft.Json.dll --output=Newtonsoft.Json.il
	ilasm /dll /key:../../../Dynamic.snk Newtonsoft.Json.il
}

package() {
	cd "${srcdir}/Newtonsoft.Json-$pkgver"
	install -Dm644 "LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
	cd Src/Newtonsoft.Json
	make DESTDIR="$pkgdir" install
	find "$pkgdir" -name '*.dll' -exec gacutil -i {} -root "$pkgdir/usr/lib" \;
	find "$pkgdir" -name '*.dll' -exec mono --aot {} \;
}
