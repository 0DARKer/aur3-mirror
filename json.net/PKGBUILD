pkgname=json.net
pkgdesc="Json.NET is a popular high-performance JSON framework for .NET"
pkgver=5.0.8
pkgrel=2
arch=(i686 x86_64)
license=("MIT")
options=(!emptydirs)
url="http://james.newtonking.com/json"
source=("https://github.com/JamesNK/Newtonsoft.Json/archive/$pkgver.tar.gz"
"jsonnet.pc.in")
depends=("mono>=2.11.3")
sha1sums=('a30f9c3510494a3b84bb9c2e5821fc7a91c633c2'
          'c18c1afb4b4a1596e1a60b3e5ff43dd7e5bd0ecd')
          
build() {
	cd "${srcdir}/Newtonsoft.Json-$pkgver/Src/Newtonsoft.Json"
	xbuild Newtonsoft.Json.csproj
	cd bin/Debug/Net45
	monodis Newtonsoft.Json.dll --output=Newtonsoft.Json.il
	ilasm /dll /key:../../../Dynamic.snk Newtonsoft.Json.il
}

package() {
	cd "${srcdir}/Newtonsoft.Json-$pkgver"
	install -Dm644 "LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
	cd Src/Newtonsoft.Json/bin/Debug/Net45
	find . -name '*.mdb' -o -name '*.dll' -o -name '*.xml' |
		xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/json.net/"{}
	find "$pkgdir/usr/lib/json.net/" -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
	find "$pkgdir/usr/lib/json.net/" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
	install -Dm644 "$srcdir/jsonnet.pc.in" "$pkgdir/usr/lib/pkgconfig/jsonnet.pc"
	sed -i "s,@VERSION@,$pkgver," "$pkgdir/usr/lib/pkgconfig/jsonnet.pc"
}
