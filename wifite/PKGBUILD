# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Fabiano Furtado < fusca14 (at) gmail (dot) com >

pkgname=wifite
pkgver=122
pkgrel=1
pkgdesc="A tool to attack multiple WEP and WPA encrypted networks at the same time"
arch=('any')
url="https://github.com/derv82/wifite"
license=('GPL2')
depends=('python2' 'aircrack-ng')
optdepends=('reaver: WPS attack support'
            'pyrit: detect WPA handshakes'
            'wireshark-cli: detect handshakes'
            'cowpatty: detect WPA handshakes'
			'macchanger: change MAC for attacks')
_pkghash='a6535c51d86d53821daede2e19783c95f433bbeb'
source=(https://github.com/derv82/${pkgname}/archive/${_pkghash}.tar.gz)
sha512sums=('92d709e8e2f27de6927de261efd2247082065e945d17d1347131ced2ca6d674ad9257e445789ec4e21df1a1092748659017304a264eb8dd700901907a5e72ad9')

prepare() {
  cd ${pkgname}-${_pkghash}
  sed -e 's|usr/bin/python$|/usr/bin/env python2|g' -i wifite.py
}

package() {
  cd ${pkgname}-${_pkghash}
  install -Dm 755 wifite.py "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 readme.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim: ts=2 sw=2 et:
