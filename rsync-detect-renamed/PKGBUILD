# Maintainer: Nicky726 <Nicky726@gmail.com>
# Contributor: David Watzke

pkgname=rsync-detect-renamed
_origname=rsync
pkgver=3.0.9
pkgrel=1
pkgdesc="A file transfer program to keep remote files in sync with detect renamed support"
arch=('i686' 'x86_64')
url="http://rsync.samba.org/"
license=('GPL3')
depends=('perl' 'popt')
provides=("${_origname}=${pkgver}-${pkgrel}")
conflicts=("${_origname}")
backup=('etc/rsyncd.conf' 'etc/xinetd.d/rsync')
source=("http://rsync.samba.org/ftp/rsync/src/$_origname-$pkgver.tar.gz"
        "http://rsync.samba.org/ftp/rsync/src/$_origname-$pkgver.tar.gz.asc"
        'rsyncd.conf' 'rsync.xinetd' 'rsyncd.service'
        'rsyncd.socket' 'rsyncd@.service'
        'detect-renamed.diff' 'detect-renamed-lax.diff')
md5sums=('5ee72266fe2c1822333c407e1761b92b'
         'SKIP'
         'bce64d122a8e0f86872a4a21a03bc7f3'
         'ea3e9277dc908bc51f9eddc0f6b935c1'
         '084140868d38cf3e937a2db716d47c0f'
         'ae4c381e0c02d6132c7f6ded3f473041'
         '53f94e613e0bc502d38dd61bd2cd7636'
         '2c8044eea52e72eee756f3bd45b535c1'
         'ec71cb95c62a970125a6ed8d3ccec474')

prepare() {
  cd $_origname-$pkgver
  patch -Np0 -i ../detect-renamed.diff
  patch -Np1 -i ../detect-renamed-lax.diff
}

build() {
	cd "$srcdir/$_origname-$pkgver"
	./configure --prefix=/usr \
		--with-included-popt=no \
		--with-included-zlib=no \
		--disable-debug
	make
}

check() {
	cd "$srcdir/$_origname-$pkgver"
	make test
}

package() {
	cd "$srcdir/$_origname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 ../rsyncd.conf "$pkgdir/etc/rsyncd.conf"
	install -Dm644 ../rsync.xinetd "$pkgdir/etc/xinetd.d/rsync"
	install -Dm644 ../rsyncd.service "$pkgdir/usr/lib/systemd/system/rsyncd.service"
	install -m644 ../rsyncd.socket "$pkgdir/usr/lib/systemd/system/rsyncd.socket"
	install -m644 ../rsyncd@.service "$pkgdir/usr/lib/systemd/system/rsyncd@.service"
	install -Dm755 support/rrsync "$pkgdir/usr/lib/rsync/rrsync"
}

