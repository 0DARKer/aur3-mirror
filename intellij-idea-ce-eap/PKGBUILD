# Maintainer: Gauthier P. <kendos.kenlen@gmail.com>

pkgname=intellij-idea-ce-eap
_pkgname=idea-IC
pkgver=139.791.2
_buildver=139.791.2
pkgrel=1
pkgdesc="Early access version of the upcoming version of Intellij Idea IDE (community version)"
arch=('i686' 'x86_64') # not 'any' because of fsnotifier
url="http://www.jetbrains.com/idea/"
backup=("usr/share/${pkgname}/bin/idea.vmoptions" "usr/share/${pkgname}/bin/idea64.vmoptions")
license=('Apache2')
depends=('java-environment' 'giflib' 'libxtst')
conflicts=('intellij-idea-community-edition')
provides=('intellij-idea-community-edition')
options=(!strip)
source=(http://download.jetbrains.com/idea/ideaIC-$pkgver.tar.gz)
md5sums=('ad5e63b92ca829a69b1e2bf46469ec63')

package() {
  cd ${srcdir}
  mkdir -p ${pkgdir}/opt/${pkgname}
  cp -R ${srcdir}/${_pkgname}-${_buildver}/* ${pkgdir}/opt/${pkgname}
  if [[ $CARCH = 'i686' ]]; then
     rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
     rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen64.so"
     rm -R -f "${pkgdir}/opt/${pkgname}/lib/libpty/linux/x86_64"
  fi
  if [[ $CARCH = 'x86_64' ]]; then
     rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier"
     rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen.so"
     rm -R -f "${pkgdir}/opt/${pkgname}/lib/libpty/linux/x86"
  fi

(
cat <<EOF
[Desktop Entry]
Version=${pkgver}
Name=IntelliJ IDEA Community Edition
Icon=idea
GenericName=IDE For Java Developmers
Comment=Early Access Program
Exec="/opt/${pkgname}/bin/idea.sh" %f
Terminal=false
Type=Application
Categories=Development
EOF
) > ${srcdir}/idea-IC.desktop

  mkdir -p "${pkgdir}/usr/bin/"
  mkdir -p "${pkgdir}/usr/share/applications/"
  mkdir -p "${pkgdir}/usr/share/pixmaps/"
  install -m 644 "${srcdir}/idea-IC.desktop" "${pkgdir}/usr/share/applications/"
  install -m 644 "${pkgdir}/opt/${pkgname}/bin/idea.png" "${pkgdir}/usr/share/pixmaps/idea.png"
  ln -s "/opt/$pkgname/bin/idea.sh" "$pkgdir/usr/bin/ideaIC"
}

# vim:set ts=2 sw=2 et:
