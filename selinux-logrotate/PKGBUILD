# $Id: PKGBUILD 136350 2011-08-27 04:08:30Z allan $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Maintainer: Nicky726 <Nicky726@gmail.com>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=selinux-logrotate
_origname=logrotate
pkgver=3.8.0
pkgrel=2
pkgdesc="Tool to rotate system logs automatically with SELinux support"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/logrotate/"
license=('GPL')
groups=('selinux' 'selinux-system-utilities')
depends=('popt' 'selinux-cronie' 'gzip' 'selinux-usr-libselinux')
conflicts=("${_origname}")
provides=("${_origname}=${pkgver}-${pkgrel}")
backup=('etc/logrotate.conf')
source=(https://fedorahosted.org/releases/l/o/logrotate/logrotate-${pkgver}.tar.gz
        logrotate-3.8.0-noasprintf.patch
        logrotate.conf
        logrotate.cron.daily)
md5sums=('590f77c13077a4c384dbec7ca9c5f242'
         '624801859beb9abe7958b6392c854d72'
         '462a5f364717461537eb2ae6394ad23e'
         '8e23d5d4cc29b1e055b24df87e355cdc')

build() {
  cd "$srcdir/${_origname}-${pkgver}"

  patch -Np1 -i "$srcdir"/logrotate-3.8.0-noasprintf.patch

  sed -i 's|#define DEFAULT_MAIL_COMMAND .*|#define DEFAULT_MAIL_COMMAND "/usr/bin/mail"|'\
    config.h
  sed -i "s|CFLAGS = -Wall|CFLAGS = -Wall $CFLAGS|" Makefile
  sed -i 's|$(BASEDIR)/man|$(BASEDIR)/share/man|' Makefile
  
  make WITH_SELINUX=yes
}

check() {
  cd "$srcdir/${_origname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_origname}-${pkgver}"
  make PREFIX="$pkgdir" install

  install -Dm644 "$srcdir/logrotate.conf" "$pkgdir/etc/logrotate.conf"
  install -Dm744 "$srcdir/logrotate.cron.daily" "$pkgdir/etc/cron.daily/logrotate"
}
