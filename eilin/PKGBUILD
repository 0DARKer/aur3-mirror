# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=eilin
pkgver=3.1
pkgrel=2
pkgdesc="Utility to control EIZO monitors under Linux"
arch=('i686' 'x86_64')
url="https://sourceforge.net/p/eilin/home/Home/"
license=('GPL3')
depends=('libusb' 'expat')
makedepends=('cmake' 'qt4')
optdepends=('qt4: system tray icon')
install=eilin.install
source=(http://downloads.sourceforge.net/project/eilin/$pkgname-$pkgver.tar.gz \
        51-eizo.rules)
md5sums=('1adc94f99735c4a24924b1858d267375'
         'ba56e2e2dfbd6c212574000a3b117bd3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # replace setuid bits with an udev rule
  chmod u-s "$pkgdir"/usr/bin/*
  install -Dm644 "$srcdir/51-eizo.rules" "$pkgdir/usr/lib/udev/rules.d/51-eizo.rules"
}

# vim:set ts=2 sw=2 et:
