# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=hotot
pkgname=$_pkg-git
pkgver=20111125
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca) in Python."
arch=('any')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('pywebkitgtk' 'python-notify' 'python-keybinder' 'dbus-python' 'hicolor-icon-theme')
makedepends=('git' 'python-distutils-extra' 'python2-distribute')
#optdepends=('python2-pyenchant: spell checking support')
provides=("$_pkg")
conflicts=("$_pkg")
replaces=("$_pkg-hg")
install="$pkgname.install"

_gitroot="https://github.com/shellex/Hotot.git"
_gitname="$_pkg"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # remove google analytics tracking code (tnx to ianux)
  sed -i '/\/\/ 6. run track code/,+14d' data/js/hotot.js
  sed -i '/ga.js/d' data/index.html

  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname-build"

  python2 setup.py install --prefix=/usr --root="$pkgdir"

  # python2 fixes
  sed -i 's/\#\!.*python$/&2/' `grep -rl '\#\!.*python' "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
