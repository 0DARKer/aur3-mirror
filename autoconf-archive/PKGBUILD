# Maintainer: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=autoconf-archive
pkgver=20110102
_pkgver=2011.01.02
pkgrel=1
pkgdesc="Provide a collection of reusable Autoconf macros as free software"
arch=('any')
url="http://www.gnu.org/software/autoconf-archive/"
license=('GPL3')
depends=('autoconf')
optdepends=('automake: macros for use with it')
changelog='ChangeLog'
source=(http://ftpmirror.gnu.org/$pkgname/${pkgname}-${_pkgver}.tar.xz)
md5sums=('a9cebc016377f7dcc7086e77194cf2f2')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
