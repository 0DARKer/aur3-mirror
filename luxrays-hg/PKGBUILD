# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Alex Combas <alex.combas@gmail.com>
pkgname=luxrays-hg
pkgver=901
pkgrel=1
pkgdesc="A renderer dedicated to accelerating the ray intersection process by using GPUs"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/wiki/LuxRays"
license=('GPL')
depends=('freeimage' 'freeglut' 'glew' 'libcl')
makedepends=('mercurial' 'cmake' 'boost' 'opencl-headers')
provides=('luxrays')
conflicts=('luxrays')

_hgroot="http://bitbucket.org/luxrender"
_hgrepo="luxrays"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u
  else
    hg clone $_hgroot/$_hgrepo $_hgrepo
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  mkdir "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr ../$_hgrepo
  make
}

package() {
  cd "$srcdir/$_hgrepo-build"

  install -d -m755 "$pkgdir"/usr/{bin,include,lib}
  install -m755 bin/* "$pkgdir"/usr/bin
  install -m644 lib/* "$pkgdir"/usr/lib
  cp -a "$srcdir"/$_hgrepo/include/luxrays "$pkgdir"/usr/include
}
