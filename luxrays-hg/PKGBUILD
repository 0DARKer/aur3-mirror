# Maintainer: Alex Combas <alex.combas@gmail.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=luxrays-hg
pkgver=574
pkgrel=1
pkgdesc="A renderer dedicated to accelerating the ray intersection process by using GPUs"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/wiki/index.php?title=LuxRays"
license=('GPL')
depends=('glew' 'freeglut' 'mesa' 'freeimage' 'boost-libs' 'nvidia-utils')
makedepends=('mercurial' 'cmake' 'opencl-headers')
provides=('luxrays')
conflicts=('luxrays')

_hgroot="https://luxrays.googlecode.com/hg/"
_hgrepo="luxrays"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u || return 1
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo || return 1
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  cmake .
  make
}

package() {
  cd "$srcdir/$_hgrepo-build"

  install -d -m755 "$pkgdir"/usr/{bin,include,lib}
  install -m755 bin/* "$pkgdir"/usr/bin
  install -m644 lib/* "$pkgdir"/usr/lib

  install -d -m755 "$pkgdir"/usr/share/luxrays/{samples,scenes}
  cp -a samples/* "$pkgdir"/usr/share/luxrays/samples
  cp -a scenes/* "$pkgdir"/usr/share/luxrays/scenes

  cp -a include/luxrays "$pkgdir"/usr/include
  
  install -D -m755 samples/smallluxgpu/blender/render_smallluxgpu.py \
    "$pkgdir"/usr/share/blender/2.53/scripts/io/render_smallluxgpu.py
}
