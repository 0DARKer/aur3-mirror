# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Alex Combas <alex.combas@gmail.com>
pkgname=luxrays-hg
pkgver=1775.baf376946089
pkgrel=1
pkgdesc="A renderer dedicated to accelerating the ray intersection process by using GPUs"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/wiki/LuxRays"
license=('GPL')
depends=('freeimage' 'freeglut' 'glew' 'libcl')
makedepends=('mercurial' 'cmake' 'boost' 'opencl-headers')
provides=('luxrays')
conflicts=('luxrays')
source=('luxrays::hg+https://bitbucket.org/luxrender/luxrays')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/luxrays"
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd "$srcdir/luxrays"

  export CXXFLAGS="$CXXFLAGS -lpthread"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DLUXRAYS_DISABLE_OPENCL=OFF \
    .
  make
}

package() {
  cd "$srcdir/luxrays"

  install -d -m755 "$pkgdir"/usr/{bin,include,lib}
  install -m755 bin/* "$pkgdir"/usr/bin
  install -m644 lib/* "$pkgdir"/usr/lib
  cp -a include "$pkgdir"/usr
}
