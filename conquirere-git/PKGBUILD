# Maintainer: A Rojas ( nqn1976 @ gmail.com )

pkgname=conquirere-git
_realname=conquirere
pkgver=20120123
pkgrel=1
pkgdesc="A Nepomuk powered research tool"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/edu/conquirere"
license=('GPL')
depends=('kdebase-runtime' 'kdepimlibs' 'kbibtex-kde4-svn')
makedepends=('cmake' 'automoc4' 'git' 'doxygen' 'boost')
md5sums=()

_gitroot="git://anongit.kde.org/${_realname}"
_gitname=${_realname}
 
build() {
  cd ${srcdir}

  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ]; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${srcdir}/build
  git clone ${srcdir}/${_gitname} ${srcdir}/build
  cd ${srcdir}/build
 
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}