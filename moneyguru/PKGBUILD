# Maintainer: Virgil Dupras <hsoft@hardcoded.net>
pkgname=moneyguru
pkgver=2.6.3
pkgrel=1
pkgdesc="Personal finance management application"
arch=(i686 x86_64)
url="http://www.hardcoded.net/moneyguru/"
license=('BSD')
depends=('python' 'pyqt')
makedepends=('mercurial' 'python-distribute' 'python-pip' 'python-virtualenv' 'gcc')
source=()
md5sums=()


build() {

  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [[ -d "$pkgname" ]]; then
    cd "$pkgname"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "https://bitbucket.org/hsoft/moneyguru" "$pkgname"
    cd "$pkgname"
  fi

  msg "Mercurial checkout done or server timeout"
  
  msg "Switching to Production Tag"
  # NOTE: The "-arch" suffix is temporary. Recent fixes have been made upstream to make Arch
  # packaging easier, but there wasn't a release with these changes yet, so I've made a special
  # "arch" tag in the upstream repo. After the next release, we can remove that suffix.
  hg checkout "$pkgver"-arch
  
  if [[ ! -d "env" ]]; then
    msg "Creating virtualenv"
    virtualenv --system-site-packages env
  fi
  
  msg "Installing dependencies"
  source env/bin/activate
  pip install -r requirements.txt
  
  msg "Starting build..."
  python configure.py
  python build.py --clean
}

package() {
  cd "$srcdir/$pkgname"
  
  mkdir -p "${pkgdir}/usr/share/applications"
  cp debian/${pkgname}.desktop "${pkgdir}/usr/share/applications"
  
  python package.py
  cd "build/${pkgname}-arch"
  
  mkdir -p "$pkgdir/usr/share/${pkgname}"
  cp -a * "$pkgdir/usr/share/${pkgname}/"
  chmod a+x "$pkgdir/usr/share/${pkgname}/run.py"
  
  mkdir -p "$pkgdir/usr/bin"
  ln -s ../share/${pkgname}/run.py "$pkgdir/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
