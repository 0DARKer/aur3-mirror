# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: kens

pkgname=finalterm-git
pkgver=124.7d2622e
pkgrel=1
pkgdesc="A new breed of terminal emulator"
arch=('x86_64' 'i686')
url="http://finalterm.org"
license=('GPL3')
depends=('clutter>=1.12' 'clutter-gtk' 'gtk3' 'libgee' 'libkeybinder3' 'libnotify' 'mx>=1.0')
makedepends=('cmake' 'git' 'vala' 'intltool')
provides=('finalterm')
conflicts=('finalterm')
source=("${pkgname%-*}::git+https://github.com/p-e-w/finalterm.git")
md5sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname%-git}"
    echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname%-git}"
    cmake -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {    
    cd "${srcdir}/${pkgname%-git}"
    make DESTDIR="${pkgdir}" install
}
