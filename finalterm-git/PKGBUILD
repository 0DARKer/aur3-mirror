# Maintainer : Martin Wimpress <code@flexion.org>

_pkgname=finalterm
pkgname=${_pkgname}-git
pkgver=134.b34c51b
pkgrel=1
pkgdesc="A new breed of terminal emulator"
arch=('x86_64' 'i686')
url="http://finalterm.org"
license=('GPL3')
depends=('clutter-gtk' 'desktop-file-utils' 'libgee' 'libkeybinder3' 'libnotify' 'mx')
makedepends=('cmake' 'git' 'intltool' 'vala')
provides=('finalterm')
conflicts=('finalterm')
source=("${pkgname}::git+https://github.com/p-e-w/${_pkgname}.git")
sha1sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd "${srcdir}/${pkgname}"
    echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd "${srcdir}/${pkgname}"
    cmake -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
