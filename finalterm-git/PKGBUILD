# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: kens

pkgname=finalterm-git
pkgver=120.728072f
pkgrel=1
pkgdesc="A new breed of terminal emulator"
arch=('x86_64' 'i686')
url="http://finalterm.org"
license=('GPL3')
depends=('clutter>=1.12' 'clutter-gtk' 'gtk3' 'libgee' 'libkeybinder3' 'libnotify' 'mx')
makedepends=('cmake' 'git' 'vala' 'intltool')
provides=('finalterm')
conflicts=('finalterm')
source=("${pkgname%-*}::git+https://github.com/p-e-w/finalterm.git")
md5sums=('SKIP')
install=${pkgname}.install

pkgver() {
    cd ${pkgname%-*}
    local COUNT=$(git rev-list --count HEAD)
    local HEAD=$(git rev-parse --short HEAD)
    echo "${COUNT}.${HEAD}"
}

build() {
    cd ${pkgname%-*}
    mkdir build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr ..
    make
}

package() {
    cd ${srcdir}/finalterm/build
    make DESTDIR="$pkgdir" install
}
