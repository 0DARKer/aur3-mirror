# Contributor: Auguste <auguste@gmail.com>

pkgname=xboard-git
pkgver=20110227
pkgrel=1
pkgdesc="A graphical user interfaces for chess"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/xboard/"
license=('GPL3')
depends=('gnuchess' 'libxaw' 'texinfo' 'xdg-utils')
makedepends=('git')
provides=('xboard')
conflicts=('xboard')
backup=(etc/xboard/xboard.conf)
install=xboard.install
source=(xboard.install xboard.desktop)
md5sums=('3c8cdfec60f606c6dca08f90343ba129'
         '1d9d4a7864465a703fe4a215e83f4806')

_gitroot="git://git.savannah.gnu.org/xboard.git"
_gitname="xboard"

build()
{
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [ -d $_gitname ]; then
        cd $_gitname && git pull origin
        msg "The local files are updated."
    else
        git clone $_gitroot $_gitname
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting make..."

    rm -rf "$srcdir/$_gitname-build"
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
    cd "$srcdir/$_gitname-build"

    #
    # BUILD HERE
    #

    sed -i 's/fairymax/gnuchess/' xboard.h xboard.conf.in
    ./autogen.sh
    ./configure --prefix=/usr --mandir=/usr/share/man \
        --sysconfdir=/etc/$_gitname
    make
}

package()
{
    cd "${srcdir}/$_gitname-build"
    make DESTDIR="${pkgdir}" install
    install -m 644 -D "${srcdir}/xboard.desktop" \
        "${pkgdir}/usr/share/applications/xboard.desktop"
}
