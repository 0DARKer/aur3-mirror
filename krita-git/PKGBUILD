# Maintainer: ledti <antegist at gmail.com>

# This package (only) builds and installs a standalone, developmental version
# of Krita (git) which conflicts with most or all of the Calligra suite found in
# the extra repository.

pkgname=krita-git
pkgver=91875.9714ae6
pkgrel=1
pkgdesc="Digital painting and illustration suite, devel version."
arch=('i686' 'x86_64')
url="http://www.krita.org/"
license=('GPL2')
depends=('kdebase-runtime' 'eigen2' 'exiv2' 'glew' 'fftw' 'opencolorio'
         'libkdcraw' 'libpng')
makedepends=('cmake' 'automoc4' 'vc' 'boost' 'lcms2' 'openjpeg' 'git')
provides=('calligra-krita')
conflicts=('calligra-krita' 'calligra-plugins' 'calligra-libs' 'calligra-extras')
install=krita.install
source=("calligra::git://anongit.kde.org/calligra#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/calligra"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/calligra"
  mkdir krita-build
}

build() {
  cd "$srcdir/calligra/krita-build"
  cmake ../ \
    -DCMAKE_BUILD_TYPE=Release \
    -DPRODUCTSET=KRITA \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/calligra/krita-build"
  make DESTDIR="${pkgdir}" install
}