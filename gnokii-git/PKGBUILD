# Maintainer: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gnokii-git
pkgver=20100218
pkgrel=1
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('i686' 'x86_64')
url="http://www.gnokii.org/"
license=('GPL')
depends=('libusb' 'libxpm' 'bluez' 'libical' 'postgresql-libs>=8.4.1')
makedepends=('gtk2' 'libmysqlclient>=5.1' 'perlxml' 'intltool')
optdepends=('gtk2: xgnokii GUI'
            'libmysqlclient: smsd mysql backend'
	    'postgresql-libs: smsd postgresql backend')
backup=('etc/gnokiirc')
options=('!libtool' '!makeflags')
source=()
md5sums=()

_gitname="gnokii"
_gitroot="git://git.savannah.nongnu.org/${_gitname}.git"

build() {
    cd ${srcdir}
    
    if [[ -d ${startdir}/src/${_gitname} ]]; then
        cd ${_gitname}
        git pull origin master || return 1
    else
        git clone ${_gitroot} || return 1
    fi
    
    cd ${srcdir}/${_gitname}

    ./autogen.sh
  # FS#13249
    sed -i 's|cellphone|phone|' xgnokii/xgnokii.desktop.in || return 1
    
    ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--enable-security \
	--disable-unix98test || return 1
    make || return 1
    make DESTDIR="${pkgdir}" install || return 1
    
    install -Dm644 Docs/sample/gnokiirc "${pkgdir}/etc/gnokiirc" || return 1
}
