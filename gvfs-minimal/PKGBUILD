# Maintainer: megadriver <megadriver at gmx dot com>

pkgname=gvfs-minimal
_pkgname=gvfs
pkgver=1.10.1
pkgrel=1
pkgdesc="A minimal build of gvfs with most backends disabled"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('intltool')
conflicts=('gvfs')
provides=('gvfs')
install=gvfs-minimal.install
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/1.10/$_pkgname-$pkgver.tar.bz2")
options=(!libtool)
md5sums=('7ca2acfa4dffd94b95f4b8713a346607')

build() {
  cd $srcdir/$_pkgname-$pkgver
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static \
  --disable-afc --disable-archive --disable-afp --disable-avahi --disable-bluray \
  --disable-fuse --disable-gdu --disable-gphoto2 --disable-gudev --disable-hal \
  --disable-http --disable-keyring --disable-obexftp --disable-samba \
  --libexecdir=/usr/lib/gvfs --with-bash-completion-dir=/etc/bash_completion.d 
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  sed -e 's/^am__append_3/#am__append_3/' \
	  -e 's/^am__append_4/#am__append_4/' \
	  -i monitor/Makefile
  make DESTDIR=$pkgdir install
}
