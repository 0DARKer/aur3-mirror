# $Id: PKGBUILD 20371 2008-12-03 02:25:58Z eric $
# Contributor: Manolis Tzanidakis
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Maintainer: Thayer Williams <thayer@archlinux.org>

pkgname=acpid-netlink2
pkgver=1.0.10
pkgrel=1
pkgdesc="A daemon for delivering ACPI power management events, with netlink support"
arch=('i686' 'x86_64')
url="http://acpid.sourceforge.net"
license=('GPL')
depends=(glibc)
source=(
    http://www.tedfelix.com/linux/acpid-$pkgver-netlink2.tar.bz2
    acpid
    anything
    handler.sh)
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything')
conflicts=(acpid acpid-ted1)
provides=(acpid acpid-ted1)
md5sums=('e0068ae80e935ecded773e974eda93ae'
         'b66d1e6ad3ae1a58310de4d52ec22f61'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '0e8dd13793b1baa79a745f4034888367')

build() {
    cd $srcdir/acpid-$pkgver-netlink2
    /usr/bin/make || return 1
    /bin/install -d -m0755 $pkgdir/usr/bin || return 1
    /usr/bin/make INSTPREFIX=$pkgdir \
        MAN8DIR=$pkgdir/usr/share/man/man8 install || return 1

    # install our supplementary scripts
    /bin/install -D -m0755 $srcdir/acpid $pkgdir/etc/rc.d/acpid || return 1
    /bin/install -D -m0644 $srcdir/anything $pkgdir/etc/acpi/events/anything || return 1
    /bin/install -D -m0755 $srcdir/handler.sh $pkgdir/etc/acpi/handler.sh || return 1

    # set world readable bit on the acpid binary
    /bin/chmod 755 $pkgdir/usr/sbin/acpid || return 1
}
