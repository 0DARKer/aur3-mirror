# Contributor: Antonio Rojas
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgname=plasma-workspace
pkgver=4.96.0
pkgrel=5
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('kded' 'kwin' 'libksysguard' 'kjsembed' 'knotifyconfig' 'ktexteditor' 'kde-cli-tools' 'khelpcenter' 'kio-extras' 'milou-frameworks' 'libkscreen2' 'breeze' 'gpsd' 'libqalculate' 'oxygen-icons') #'qimageblitz-qt5' 'akonadi-qt5' 'kdepimlibs-frameworks'
makedepends=('extra-cmake-modules' 'kdoctools' 'boost')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz"
        'kf5-plasma.desktop'
        '10-kf5' "fix-doc-conflicts.patch::http://quickgit.kde.org/?p=plasma-workspace.git&a=commitdiff&h=cb37527dd84e92effc4c5c1c425415f4adf477cb&o=plain")
md5sums=('cebd06411902dde2087b747d5a916f11'
         '9e8f350ff3aee562848b104b129a6669'
         '7dfda28b301b9cf21bae5758a5a8c055'
         '55cd983e0e7e1ab90c6cd88e30e0a23e')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -F3 -i ${srcdir}/fix-doc-conflicts.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed 's:qtpaths:qtpaths-qt5:' -i "$pkgdir/opt/kf5/bin/startkde"
  sed 's:/opt/kf5/lib/libexec/kf5/start_kdeinit_wrapper:/usr/lib/libexec/kf5/start_kdeinit_wrapper:' -i "$pkgdir/opt/kf5/bin/startkde"


  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  install -dm755 "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/kf5-plasma.desktop" "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -sr "${pkgdir}/usr/share/apps/kdm/sessions/kf5-plasma.desktop" -t "${pkgdir}/usr/share/xsessions/"
  
  install -Dm755 "${srcdir}/10-kf5" "${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5"
}
