# Contributor: Antonio Rojas
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgname=plasma-workspace
pkgver=5.0.0
pkgrel=2
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('qt5-tools' 'kded' 'kwin' 'libksysguard' 'kjsembed' 'knotifyconfig' 'ktexteditor' 'kde-cli-tools' 'khelpcenter' 'kio-extras' 
'milou' 'baloo-frameworks' 'libkscreen-frameworks' 'breeze' 'gpsd' 'libqalculate') # 'prison-qt5'
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('plasma-workspace-wallpapers: additional wallpapers')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz" '10-kf5')
md5sums=('e33d99c667cfb7b203d61ec235148f3a'
         '162ab0d2d984930321f07c19d3e5cbc9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed 's:qtpaths:qtpaths-qt5:' -i "$pkgdir"/opt/kf5/bin/startkde
  sed 's:/opt/kf5/lib/libexec/kf5/start_kdeinit_wrapper:/usr/lib/libexec/kf5/start_kdeinit_wrapper:' -i "$pkgdir"/opt/kf5/bin/startkde
  sed 's:install_share="/opt/kf5/share":install_share="/usr/share":' -i "$pkgdir"/opt/kf5/bin/startkde

# move session file and SDDM theme to /usr
  install -dm755 "$pkgdir"/usr/share/
  mv "$pkgdir"/opt/kf5/share/xsessions "$pkgdir"/usr/share/
  mv "$pkgdir"/opt/kf5/share/sddm "$pkgdir"/usr/share/

# keep KDM support for now
  install -dm755 "$pkgdir"/usr/share/apps/kdm/sessions/
  ln -sr "$pkgdir/usr/share/xsessions/plasma.desktop" -t "$pkgdir/usr/share/apps/kdm/sessions/"
  
# set env variables at startup
  install -Dm755 "${srcdir}/10-kf5" "${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5"
}
