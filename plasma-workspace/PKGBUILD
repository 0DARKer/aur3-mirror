# Contributor: Antonio Rojas
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgname=plasma-workspace
pkgver=4.97.0
pkgrel=3
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('qt5-tools' 'kded' 'kwin' 'libksysguard' 'kjsembed' 'knotifyconfig' 'ktexteditor' 'kde-cli-tools' 'khelpcenter' 'kio-extras' 
'milou-frameworks' 'libkscreen2' 'breeze' 'gpsd' 'libqalculate') # 'prison-qt5'
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz"
        'kf5-plasma.desktop'
        '10-kf5')
md5sums=('b6e433d71eb4ac3453d1f6df42c7e275'
         '9e8f350ff3aee562848b104b129a6669'
         '789cbc2822ce2c268b51b96a5110411c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed 's:qtpaths:qtpaths-qt5:' -i "$pkgdir/opt/kf5/bin/startkde"
  sed 's:/opt/kf5/lib/libexec/kf5/start_kdeinit_wrapper:/usr/lib/libexec/kf5/start_kdeinit_wrapper:' -i "$pkgdir/opt/kf5/bin/startkde"


  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  install -dm755 "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/kf5-plasma.desktop" "${pkgdir}/usr/share/xsessions/"
  ln -sr "${pkgdir}/usr/share/xsessions/kf5-plasma.desktop" -t "${pkgdir}/usr/share/apps/kdm/sessions/"
  
  install -Dm755 "${srcdir}/10-kf5" "${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5"
}
