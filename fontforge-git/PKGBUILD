# Contributor: Earnestly

pkgname=fontforge-git
pkgver=20120731b+7675+g68c3390
pkgrel=1
pkgdesc='Full-featured outline and bitmap font editor'
url="https://github.com/fontforge/fontforge/"
arch=('i686' 'x86_64')
license=('GPL3' 'BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'libpng'
         'python2' 'libspiro' 'desktop-file-utils' 'gtk-update-icon-cache' 
         'hicolor-icon-theme' 'zeromq' 'libunicodenames')
makedepends=('git')
provides=('fontforge')
conflicts=('fontforge')
install='fontforge-git.install'
source=('git://github.com/fontforge/fontforge.git'
        'http://fontforge.sf.net/cidmaps.tgz'
        'http://downloads.sourceforge.net/project/freetype/freetype2/2.5.0/freetype-2.5.0.tar.bz2')
sha256sums=('SKIP'
            '1bf9c7eb8835e6ed94e62cb49f1141bc046c562849e52e6c3c7f1d7cfc95c7b3'
            'b8c75164f9073809797da19b81fa89d2c0ea507d8913f3b70c744f501880d7de')

pkgver() {
  cd 'fontforge'
  git describe | sed 's/^v//;s/-//;s/-/+/g'
}

build() {
  cd 'fontforge'

  # cidmaps
  mkdir -p fontforge/cidmaps
  mv "$srcdir"/*.cidmap fontforge/cidmaps

  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --enable-type3 \
    --enable-zeromq \
    --enable-double \
    --with-regular-link \
    --enable-pyextension \
    --with-python=python2 \
    --mandir=/usr/share/man \
    --with-freetype-source="$srcdir"/freetype-2.5.0

  make
}

package() {
  cd 'fontforge'

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/fontforge/LICENSE
  install -Dm644 desktop/fontforge.desktop "$pkgdir"/usr/share/applications/fontforge.desktop

  # Remove unneeded osx binaries
  rm -rf "$pkgdir/usr/share/fontforge/osx"

  # Remove docs if required.  This is roughly 10MiB
  #rm -rf "$pkgdir/usr/share/doc/fontforge"
}

# vim: ts=2 sw=2 et
