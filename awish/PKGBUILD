# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>


pkgname=awish
pkgver=1.0.beta2
pkgrel=1
pkgdesc="Rewrite of the old MS-DOS game Avish"
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/awish.git"
license=('GPL3' 'custom:abandonware')
depends=('sdl_mixer' 'sh')

source=('http://repo.or.cz/w/awish.git/snapshot/881037d9e0afbf6bcabd812938748cbf195a8382.tar.gz'
        'http://files.abandonia.com/download.php?game=Avish&secure=4e1cdca4ae96348f0fcf3a6f49776368&td=1332682105'
        "${pkgname}.sh"
        "${pkgname}.desktop")

md5sums=('88bbea30252a28bee9707419374bb9a6'
         '08e07a049ae29529c255b0ae236e0806'
         '95d06e7efc8669f4f1e657024b0c6432'
         '8673f646ccdca6ef8caf2ae9e3a2dcda')


build() {
  cd "${srcdir}/${pkgname}"
  chmod +x 0build.sh
  ./0build.sh
}

package() {
  # main files
  install -dm755 "${pkgdir}/usr/share/${pkgname}/"

  install -m755 "${srcdir}/${pkgname}/${pkgname}" "${pkgdir}/usr/share/${pkgname}"
  cd "${srcdir}/${pkgname}"
  cp -r data "${pkgdir}/usr/share/${pkgname}/"

  cd "${srcdir}/Avish"
  install -m644 RESOURCE.* "${pkgdir}/usr/share/${pkgname}/"

  # readme
  install -D -m644 "${srcdir}/${pkgname}/README" "${pkgdir}/usr/share/doc/${pkgname}/readme-src"
  install -D -m644 "${srcdir}/Avish/readme.txt" "${pkgdir}/usr/share/doc/${pkgname}/readme-data"

  # bash script
  install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # install desktop entry file
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
