# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>


pkgname=awish
pkgver=1.0.beta2
pkgrel=2
pkgdesc="Rewrite of the old MS-DOS game Avish"
arch=('i686' 'x86_64')
url="http://repo.or.cz/w/awish.git"
license=('GPL3')
depends=('sdl_mixer' 'sh' 'awish-data')

source=("${pkgname}-${pkgver}.tar.gz::http://repo.or.cz/w/awish.git/snapshot/881037d9e0afbf6bcabd812938748cbf195a8382.tar.gz"
        "${pkgname}.sh"
        "${pkgname}.desktop")

md5sums=('dfed64a68ff69f6737a43e3d20b0cb98'
         '95d06e7efc8669f4f1e657024b0c6432'
         '8673f646ccdca6ef8caf2ae9e3a2dcda')


build() {
  cd "${srcdir}/${pkgname}"
  chmod +x 0build.sh
  ./0build.sh
}

package() {
  # main files
  install -dm755 "${pkgdir}/usr/share/${pkgname}/"

  install -m755 "${srcdir}/${pkgname}/${pkgname}" "${pkgdir}/usr/share/${pkgname}"
  cd "${srcdir}/${pkgname}"
  cp -r data "${pkgdir}/usr/share/${pkgname}/"

  # readme
  install -D -m644 "${srcdir}/${pkgname}/README" "${pkgdir}/usr/share/doc/${pkgname}/readme-src"

  # bash script
  install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # install desktop entry file
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
