# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>

pkgname=xscope-git
pkgver=xscope.1.4.r3.g73c82c6
pkgrel=1
pkgdesc="A program to monitor X11/Client conversations"
arch=(x86_64 i686)
url="http://cgit.freedesktop.org/xorg/app/xscope/"
license=('MIT')
depends=()
makedepends=('git' 'xproto' 'xtrans' 'xorg-util-macros')
conflicts=('xscope')
source=("git://anongit.freedesktop.org/xorg/app/xscope")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/xscope"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/xscope"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/xscope"

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 AUTHORS COPYING "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
