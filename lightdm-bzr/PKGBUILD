# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lightdm-bzr
pkgver=1485
pkgrel=1
pkgdesc="A lightweight display manager - bzr checkout"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm"
license=(GPL3 LGPL3)
depends=('gnome-themes-standard' 'gnome-icon-theme' 'dbus-glib' 'libxklavier' 'intltool' 'qt')
optdepends=('xorg-xserver-xephr: run lightdm in test mode'
	'lightdm-gtk-greeter-bzr: LightDM GTK+ Greeter'
	'lightdm-webkit-greeter-bzr: LightDM Webkit Greeter'
	'accountsservice: limit account shown by the greeter')
makedepends=('bzr' 'vala' 'gnome-common' 'gtk-doc' 'gnome-doc-utils' 'pkg-config')
conflicts=('lightdm')
provides=('lightdm')
source=('lightdm.rc' 'lightdm.service' 'lightdm.tmpfiles' 'lightdm.pam')
install=lightdm.install
backup=('etc/lightdm/lightdm.conf' 'etc/lightdm/keys.conf' 'etc/lightdm/users.conf' 'etc/pam.d/lightdm')

_bzrtrunk="lp:lightdm"
_bzrmod="lightdm-trunk"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_bzrmod}-build"
  cp -r "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
  cd "${srcdir}/${_bzrmod}-build"

  ./autogen.sh --prefix=/usr \
    --disable-static \
    --enable-introspection=no \
    --libexecdir=/usr/lib/lightdm \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --with-greeter-session=lightdm-gtk-greeter \
    --with-greeter-user=lightdm
  make

  # set minimum login uid to 1000
  sed -i '/^minimum-uid=/s/500/1000/' data/users.conf

  # fix hiddem shells
  sed -i '/^hidden-shells=/s|/usr/sbin/nologin|/sbin/nologin|' data/users.conf
}

package() {
  cd "${srcdir}/${_bzrmod}-build"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/etc/init"

  # install missing session wrapper
  install -D -m755 tests/src/lightdm-session ${pkgdir}/usr/sbin/lightdm-session

  install -d -m770 ${pkgdir}/var/run/lightdm
  install -D -m755 ${srcdir}/lightdm.rc ${pkgdir}/etc/rc.d/lightdm
  install -D -m644 ${srcdir}/lightdm.service ${pkgdir}/usr/lib/systemd/system/lightdm.service
  install -D -m644 ${srcdir}/lightdm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/lightdm.conf
  install -D -m644 ${srcdir}/lightdm.pam ${pkgdir}/etc/pam.d/lightdm
}

sha256sums=('0a23632283c7b53b4e99d0acdf8b1e496abfd0cbb121beae28a8bdcfb27cb9cd'
            '77a03e48d60531dbd8d6393e25504b6a30ec345b910af8aa9e60223233b3f19e'
            '154adaedb16ca612a933f3dbf3533a1a78d2a21fbb0c518b093320cec129c674'
            '191a006b09908f10779739250efdee578a60b7930bd49e14b4143374080e2cd1')
