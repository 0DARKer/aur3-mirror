# Maintainer: L42y <423300@gmail.com>
# Contributor: pisuka <tekmon@gmail.com>

pkgname=lightdm-bzr
pkgver=1270
pkgrel=1
pkgdesc="A lightweight display manager."
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm"
license=(GPL3 LGPL3)
depends=('gnome-themes-standard' 'gnome-icon-theme' 'dbus-glib' 'libxklavier' 'intltool' 'qt')
optdepends=(
  'xorg-xserver-xephr: run lightdm in test mode'
)
makedepends=('bzr' 'vala' 'gnome-common' 'gtk-doc' 'gnome-doc-utils' 'pkg-config')
conflicts=('lightdm')
source=('rc.lightdm' 'lightdm-bzr.install')
md5sums=('6699eb35f65ff498d1d05e6782f4f902'
         '8854d0ee91445357ed28da84aec87885')
install=${pkgname}.install
backup=('etc/lightdm/lightdm.conf' 'etc/lightdm/keys.conf' 'etc/lightdm/users.conf' 'etc/lightdm/lightdm-gtk-greeter.conf')
options=(!libtool)

_bzrtrunk="lp:lightdm"
_bzrmod="lightdm-trunk"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_bzrmod}-build"
  cp -r "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
  cd "${srcdir}/${_bzrmod}-build"

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm \
    --disable-static \
    --enable-introspection=no
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/etc/init"

  mkdir -p "${pkgdir}/etc/rc.d" "${pkgdir}/var/lib/${pkgname/-bzr/}"
  install -m755 "${srcdir}/rc.${pkgname/-bzr/}" "$pkgdir/etc/rc.d/${pkgname/-bzr/}"
}
