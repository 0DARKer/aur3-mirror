# Contributor: David Spicer <azleifel at googlemail dot com>

pkgname=vdr-radio
_pluginname=radio
pkgver=0.2.5
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin that displays a background image for radio channels"
url="http://www.linuxtv.org/vdrwiki/index.php/Radio-plugin"
license=('GPL2')
depends=('vdr')
install=${pkgname}.install
source=(http://www.egal-vdr.de/plugins/${pkgname}-${pkgver}.tgz
        vdr-radio-0.2.5-glibc-2.10.diff)
md5sums=('71d56ca88d3e78338c12fa72d5c30c4f'
         '73650e5e0d69e4da2d3a46a00bb412ad')

build() {
  cd ${srcdir}/${_pluginname}-${pkgver}

  patch -Np1 -i ${startdir}/vdr-radio-0.2.5-glibc-2.10.diff || return 1

  # Assumed VDR directory environment (VDRDIR/Make.config):
  # MANDIR       = /usr/share/man
  # BINDIR       = /usr/bin
  # LOCDIR       = /usr/share/locale
  # PLUGINLIBDIR = /usr/lib/vdr
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr
  
  install -d -m755 ${pkgdir}/etc/vdr/plugins/${_pluginname} || return 1
  install -d -m755 ${pkgdir}/usr/lib/vdr || return 1
  install -d -m755 ${pkgdir}/usr/share/doc/${pkgname} || return 1
  install -d -m755 ${pkgdir}/usr/share/locale || return 1
  
  make VDRDIR=/usr/include/vdr \
       LIBDIR=${pkgdir}/usr/lib/vdr \
       LOCALEDIR=${pkgdir}/usr/share/locale all || return 1

  # Install configuration files
  install -m644 config/mpegstill/rtext*.mpg ${pkgdir}/etc/vdr/plugins/${_pluginname} || return 1
  ln -s rtextUnten-kleo2-live.mpg ${pkgdir}/etc/vdr/plugins/${_pluginname}/radio.mpg || return 1
  ln -s rtextUnten-kleo2-replay.mpg ${pkgdir}/etc/vdr/plugins/${_pluginname}/replay.mpg || return 1
  install -m644 config/scripts/LIESMICH.Scripts ${pkgdir}/etc/vdr/plugins/${_pluginname} || return 1
  install -m755 config/scripts/radioinfo-* ${pkgdir}/etc/vdr/plugins/${_pluginname} || return 1

  # Install documents
  for _file in HISTORY LIESMICH.bitte README; do
    install -m644 ${_file} ${pkgdir}/usr/share/doc/${pkgname}/${_file} || return 1
  done
}
