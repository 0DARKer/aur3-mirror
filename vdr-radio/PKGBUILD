# Contributor: David Spicer <azleifel at gmail dot com>

pkgname=vdr-radio
_pluginname=radio
pkgver=0.9.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin that displays a background image for radio channels"
url="http://www.linuxtv.org/vdrwiki/index.php/Radio-plugin"
license=('GPL2')
depends=('vdr>=1.7.42')
install=${pkgname}.install
source=(http://www.egal-vdr.de/plugins/${pkgname}-${pkgver}.tgz)
md5sums=('fca658383ecd9893403c02b5a7b0e8cd')

build() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  # VDR directory environment
  _vdrCONFDIR="/var/lib/vdr"
  _vdrINCDIR="/usr/include"
  _vdrLIBDIR="/usr/lib/vdr"
  _vdrLOCDIR="/usr/share/locale"

  make DESTDIR="${pkgdir}" install

  # Install configuration etc. files
  install -d -m755 "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"
  install -m644 config/mpegstill/rtext*.mpg "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"
  ln -s rtextUnten-kleo2-live.mpg "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}/radio.mpg"
  ln -s rtextUnten-kleo2-replay.mpg "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}/replay.mpg"
  install -m755 config/scripts/radioinfo-* "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"

  # Install documents
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  for _file in HISTORY LIESMICH.bitte README config/scripts/LIESMICH.Scripts; do
    install -m644 ${_file} "${pkgdir}/usr/share/doc/${pkgname}"
  done
}
