# $Id:$
# Adapted from standard gdb package 
# Maintainer: Justin Vreeland <vreeland.justin+aur@gmail.com>
# Contributer: Andreas Messer <andi@surveycorner.de>
_target=arm-eabi
pkgname=${_target}-gdb
pkgver=7.7
pkgrel=1
pkgdesc="The GNU Debugger for ARM target"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python2')
makedepends=('texinfo')
optdepends=('openocd: for debugging jtag targets')
options=('!libtool' '!buildflags')
source=(http://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.bz2)
md5sums=('271a18f41858a7e98b28ae4eb91287c9')

build() {
  cd ${srcdir}/gdb-${pkgver}
  
  ./configure --prefix=/usr \
	--target=${_target} \
	--host=$CHOST \
	--build=$CHOST \
	--disable-nls --with-system-readline \
	--with-local-prefix=/usr/lib/${_target} \
        --with-sysroot=/usr/$CHOST/${_target}

  make || return 1
}

package() {
  cd ${srcdir}/gdb-${pkgver}
  make DESTDIR=${pkgdir} install || return 1

  # resolve conflicts with binutils / remove unneeded files
  rm -rf  ${pkgdir}/usr/include
  rm -f  ${pkgdir}/usr/lib/{libiberty,libbfd,libopcodes}.a
  rm -rf ${pkgdir}/usr/share/info
  rm -rf ${pkgdir}/usr/share/gdb
}
