# Maintainer: Splex
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
pkgname=secondlife-bin
pkgver=2.6.2.225998
pkgrel=1
pkgdesc="Second Life is a 3-D virtual world entirely built and owned by its residents (Binary version.)"
url="http://www.secondlife.com/"
license=('GPL')
[ "$CARCH" = "i686"   ] && depends=('alsa-lib' 'apr-util' 'freealut' 'gtk2' 'libgl' 'libidn' 'libjpeg6' 'mesa' 'nss' 'sdl')
[ "$CARCH" = "x86_64" ] && depends=('alsa-lib' 'apr-util' 'lib32-atk'  'lib32-freealut' 'lib32-gtk2' 'libgl' 'lib32-libidn' 'lib32-libjpeg6' 'lib32-libxinerama' 'mesa' 'lib32-nss' 'sdl')
arch=('i686' 'x86_64')
conflicts=('secondlife' 'secondlife-svn')
install=secondlife.install
source=('secondlife.desktop' 'secondlife.launcher'
        "http://download.cloud.secondlife.com/Viewer-2-6/SecondLife-i686-${pkgver}.tar.bz2")
sha256sums=('187d4b7ceffddbf80a9074d2401d4ea7c85d9a5b18e3cb154684ca8a3b5a318e'
            'dea068b24298c76496df1be4007ef73976ffb1dc95ed61523a7236b72696c0c6'
            'be03febd1cbbadda9bbed34a6cdeeb4ec11a4b3c5a21a6da7d152199db9198f4')

package() {
  cd $srcdir
  
  # Rename Data Directory
  mv SecondLife-i686-${pkgver} secondlife
  
  # Install Desktop File
  install -D -m644 $srcdir/secondlife.desktop \
    $pkgdir/usr/share/applications/secondlife.desktop
  
  # Install Icon File
  install -D -m644 $srcdir/secondlife/secondlife_icon.png \
    $pkgdir/usr/share/pixmaps/secondlife.png
  
  # Install Launcher
  install -D -m755 $srcdir/secondlife.launcher \
    $pkgdir/usr/bin/secondlife
  
  # Move Data to Destination Directory
  install -d $pkgdir/opt
  mv secondlife $pkgdir/opt/
  
  # Change Permissions of files to root:games
  chown -R root:games $pkgdir/opt/secondlife
  chmod -R g+rw $pkgdir/opt/secondlife
  
  # Make Binary Group-Executable
  chmod g+x $pkgdir/opt/secondlife/secondlife
}

# vim:set ts=2 sw=2 et:
