# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Marcus Schulderinsky <mmaacceess at gmail dot com>

pkgname=lxmusic-gtk3
_pkgname=lxmusic
pkgver=0.4.6
pkgrel=1
pkgdesc='Lightweight XMMS2 client (GTK+ 3 version)'
arch=('i686' 'x86_64')
url="http://lxde.org/"
license=('GPL')
groups=('lxde-gtk3')
depends=('gtk3' 'libnotify' 'xmms2')
makedepends=('intltool')
conflicts=($_pkgname)
provides=($_pkgname)
source=(http://downloads.sourceforge.net/lxde/$_pkgname-$pkgver.tar.xz
        0001-Fix-some-deprecations-and-add-an-option-to-compile-w.patch)
md5sums=('38fc31793b11c43b6a8fda70faedfd54'
         '45e82df42227c5a97b2739dc6103fd24')

prepare() {
  cd $_pkgname-$pkgver

  # Fix some deprecations and add an option to compile with gtk3
  patch -Np1 -i ../0001-Fix-some-deprecations-and-add-an-option-to-compile-w.patch

  autoreconf -fi
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --enable-gtk3
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
