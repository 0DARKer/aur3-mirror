pkgname=fpc32-lazarus
pkgver=0.9.30.4
pkgrel=3
pkgdesc='Lazarus Component Library for multilib'
url='http://www.lazarus.freepascal.org/'
license=('GPL2' 'MPL' 'custom:LGPL')
arch=('x86_64')
depends=('fpc-multilib' 'lazarus')
makedepends=('fpc-multilib')
options=('!emptydirs' '!makeflags')
source=("http://downloads.sourceforge.net/project/lazarus/Lazarus%20Zip%20_%20GZip/Lazarus%20$pkgver/lazarus-$pkgver-src.tar.bz2"
  'lazcontrols2.diff'
)
md5sums=('ebd40cdfdfc4b66d1ced7a0d48d6da84'
         '9addf949fa2ae547f82d70de83021d38')

build() {
  cd "$srcdir/lazarus"
  ( # patch lazcontrols
    cd components/lazcontrols
    patch -p0 -i "$srcdir/lazcontrols2.diff"
  )
  make FPC="/usr/bin/fpc" clean lcl packager/registration ideintf packager components OS_TARGET=linux CPU_TARGET=i386
}

package() {
  cd "$srcdir/lazarus"
  
  # skip the 'make install' mess completely and do everything manually
  mkdir -p "$pkgdir/usr/lib/lazarus"
  find . -type f -path *i386-linux* -exec cp --parents '{}' "$pkgdir/usr/lib/lazarus/" \;
}

