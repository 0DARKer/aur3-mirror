# Contributor: Gerhard Brauer <gerbra@archlinux.de>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Markus Opitz <mastero23 at gmail dot com>
# Maintainer: Milan Knížek <knizek@volny.cz>
pkgname=x2goserver
pkgver=3.0.99.10
pkgrel=6
pkgdesc="Open source terminal server"
arch=('i686' 'x86_64')
url="http://www.x2go.org/"
license=('GPL')
depends=('openssh' 'perl-config-simple' 'perl-dbd-sqlite' 'python' 'x2goagent' 'xorg-xauth')
makedepends=('man2html')
optdepends=('cups-x2go: printing support'
            'x2gofmbindings: file manager bindings for non-KDE/GNOME/LXDE desktops')
options=(emptydirs)
install=x2goserver.install
backup=('etc/x2go/x2goserver.conf' 'etc/x2go/x2gosql/sql')
groups=('x2go' 'alts')
source=('x2goserver.rc.d'
        'x2gostartagent-xauthority.patch'
        "http://code.x2go.org/releases/source/${pkgname}/${pkgname}_${pkgver}.tar.gz")
md5sums=('8083819ac423401440c0ad5feef1a128'
         '21fda653e9512346baecda3f6b53e00f'
         'fe5b2497244a08d062ad1e7f4f502591')

build() {
  cd "${srcdir}/${pkgname}_${pkgver}"

  # -r option does not exist in Arch linux
  # (However, html man pages do not get installed anyway...)
  for Makefile in $(find . -type f -name Makefile); do
    sed -i 's@(MAN2HTML_BIN) -r @(MAN2HTML_BIN) < @g' $Makefile
  done
  patch -p0 < ../x2gostartagent-xauthority.patch
  make
}

package() {
  cd "${srcdir}/${pkgname}_${pkgver}"
  make PREFIX=/usr DESTDIR="$pkgdir/" install

  install -D -m 755 "$srcdir/x2goserver.rc.d" "$pkgdir/etc/rc.d/x2goserver"

  install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 "debian/changelog" "${pkgdir}/usr/share/doc/${pkgname}/changelog.DEBIAN"
  install -m 644 "debian/copyright" "${pkgdir}/usr/share/doc/${pkgname}/copyright.DEBIAN"
}
