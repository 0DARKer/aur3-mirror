# Contributor: Gerhard Brauer <gerbra@archlinux.de>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Markus Opitz <mastero23 at gmail dot com>
# Maintainer: Milan Knížek <knizek@volny.cz>
pkgname=x2goserver
pkgver=3.0.99.9
pkgrel=3
pkgdesc="Open source terminal server"
arch=('i686' 'x86_64')
url="http://www.x2go.org/"
license=('GPL')
depends=('openssh' 'perl-config-simple' 'perl-dbd-sqlite' 'python' 'x2goagent' 'xorg-xauth')
options=(emptydirs)
install=x2goserver.install
groups=('x2go' 'alts')
source=('x2goserver.rc.d' "http://code.x2go.org/releases/source/${pkgname}/${pkgname}_${pkgver}.tar.gz")
md5sums=('8083819ac423401440c0ad5feef1a128'
         '62c029afe7d8808a9c598ac216d90b79')

build() {
  cd "${srcdir}/${pkgname}_${pkgver}"

  sed -i 's@mkdir -p /etc/@mkdir -p $(DESTDIR)/etc/@g' x2goserver-xsession/Makefile
  sed -i 's@touch /etc/@touch $(DESTDIR)/etc/@' x2goserver-xsession/Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}_${pkgver}"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -D -m 755 "$srcdir/x2goserver.rc.d" "$pkgdir/etc/rc.d/x2goserver"
}

