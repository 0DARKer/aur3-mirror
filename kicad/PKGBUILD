# Maintainer: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=20110429
_bzrver=2986
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
depends=('mesa' 'shared-mime-info' 'wxgtk')
makedepends=('boost' 'cmake' 'zlib')
optdepends=('kicad-doc-bzr' 'kicad-library-bzr')
install=kicad.install
source=(ftp://iut-tice.ujf-grenoble.fr/cao/${pkgname}-sources_${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}-BZR${_bzrver}.zip \
        ${pkgname}-icons.tar.bz2 )
md5sums=('e5d5311ad8a6387b1e96a9ee63088238'
         '51459cb884444df60e55c95d50564be7')

build() {
  cd ${srcdir}/${pkgname}_sources

  # build and install kicad
  mkdir -p build/Release && cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON    \
              -DCMAKE_BUILD_TYPE=Release   \
              -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}_sources/build/Release

  make DESTDIR=${pkgdir} install

  # copy updated linux icons
  cp -r -n ${srcdir}/${pkgname}-icons/resources/linux/mime/icons ${pkgdir}/usr/share/
}
