# Maintainer: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=20110707
_bzrver=3043
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
depends=('mesa' 'shared-mime-info' 'wxgtk')
makedepends=('boost' 'cmake' 'zlib')
optdepends=('kicad-doc-bzr' 'kicad-library-bzr')
install=kicad.install
source=(ftp://iut-tice.ujf-grenoble.fr/cao/sources/${pkgname}-sources_${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}-BZR${_bzrver}.zip \
        ${pkgname}-icons.tar.bz2 )

build() {
  cd ${srcdir}/${pkgname}-sources_${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}-BZR${_bzrver}

  # build and install kicad
  mkdir -p build/Release && cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON    \
              -DCMAKE_BUILD_TYPE=Release   \
              -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-sources_${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}-BZR${_bzrver}/build/Release

  make DESTDIR=${pkgdir} install

  # copy updated linux icons
  cp -r -n ${srcdir}/${pkgname}-icons/resources/linux/mime/icons ${pkgdir}/usr/share/
}
md5sums=('4684038a126ba2fc1c84d75a151382c8'
         '51459cb884444df60e55c95d50564be7')
