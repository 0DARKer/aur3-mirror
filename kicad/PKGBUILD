# Maintainer: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=20111228.bzr3254
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
depends=('mesa' 'shared-mime-info' 'wxgtk')
makedepends=('boost' 'cmake' 'zlib')
optdepends=('kicad-doc-bzr' 'kicad-library-bzr')
install=kicad.install
source=(ftp://iut-tice.ujf-grenoble.fr/cao/${pkgname}_sources-${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}-BZR${pkgver:12:4}-stable.zip)
md5sums=('ac274cc60651e53d158773d4b0ae0788')

build() {
  cd ${srcdir}/stable

  # build and install kicad
  mkdir -p build/Release && cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON    \
              -DCMAKE_BUILD_TYPE=Release   \
              -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/stable/build/Release

  make DESTDIR=${pkgdir} install

  # copy updated linux icons
  #cp -r -n ${srcdir}/${pkgname}-icons/resources/linux/mime/icons ${pkgdir}/usr/share/
}
