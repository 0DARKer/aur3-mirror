# Contributor: Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: C5OK5Y <claudiokozicky[at]gmail[dot]com>
# Maintainer: mirandir <mirandir[at]orange[dot]com>

pkgname=limbo
pkgver=1.3
pkgrel=5
pkgdesc="LIMBO is a modern classic, putting players in control of a boy's journey through a tense and hostile world. (This version uses installed wine and allows to choose the language of the game if zenity is installed.) (HIB)"
url="http://limbogame.org/"
license=('custom: "commercial"')
arch=('i686' 'x86_64')
depends=('wine')
optdepends=('wine_gecko: for wine stops to ask about gecko' 'zenity: for language choice')
provides=('limbo')
conflicts=('limbo_wine')
options=(!strip)
install=install
pkgname_real=limbo
source=("${pkgname_real}.desktop" "${pkgname_real}.sh" "launch_${pkgname_real}.sh" "hib://${pkgname_real}-${pkgver}.tar.gz")
md5sums=('d4ec36a5690362211bddbf15d09a05a1'
         'a2d779b07825b38ccd4520f772b461dd'
         '3ddbced3bc9b8661f1c381d7464e55f0'
         '723815846d4cc3f68604b94c055a775c')

DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

package(){
    cd ${srcdir}

    mkdir -p "${pkgdir}/opt/${pkgname_real}"
    cp "${srcdir}/${pkgname_real}/support" "${pkgdir}/opt/${pkgname_real}/" -R
    
    # Install new launch-limbo.sh
    install -Dm 755 "${srcdir}/launch_${pkgname_real}.sh" "${pkgdir}/opt/${pkgname_real}/launch-${pkgname_real}.sh"
    chmod +x "${pkgdir}/opt/${pkgname_real}/launch-${pkgname_real}.sh"
    
    # Instal readme
    install -Dm 644 "${srcdir}/${pkgname_real}/README" "${pkgdir}/opt/${pkgname_real}/README"

    # Install Desktop
    install -D -m644 "${srcdir}/${pkgname_real}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname_real}.desktop"
    
    # Install icon
    install -D -m644 "${srcdir}/${pkgname_real}/share/icons/${pkgname_real}.png" \
        "${pkgdir}/opt/${pkgname_real}/share/icons/${pkgname_real}.png"

    # Install license
    install -Dm 644 "${srcdir}/${pkgname_real}/license.txt" "${pkgdir}/usr/share/licenses/${pkgname_real}/license"

    # Install executable
    install -Dm 755 "${srcdir}/${pkgname_real}.sh" "${pkgdir}/usr/bin/${pkgname_real}"
    chmod +x "${pkgdir}/usr/bin/${pkgname_real}"
}
