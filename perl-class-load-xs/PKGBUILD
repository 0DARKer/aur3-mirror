# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-class-load-xs
_lastauthor=D/DR/DROLSKY
_pkgname=Class-Load-XS
pkgver=0.04
pkgrel=2
pkgdesc="XS implementation of parts of Class::Load"
arch=('i686' 'x86_64')
license=('PerlArtistic2')
options=('!emptydirs')
depends=('perl-class-load' 'perl-module-implementation>=0.06')
checkdepends=('perl-test-requires')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('d4731aee4d5484cd1eef9b2aa46d0370be3f4c9aeb8fd14fc11bbd48201e05e3')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  if [ -f "Build.PL" ]; then
    perl Build.PL
    perl Build
  elif [ -f "Makefile.PL" ]; then
    perl Makefile.PL
    make
  else
    return 1
  fi
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build test
  elif [ -f "Makefile.PL" ]; then
    make test
  fi
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build install
  elif [ -f "Makefile.PL" ]; then
    make install
  fi

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
