# Maintainer: XZS <d dot f dot fischer at web dot de>
# Contributor: Pi3R1k <pierrick.brun at gmail dot com>
pkgname=gnome-shell-extension-window-buttons-git
pkgver=10_e.g.o.r6.g9aad6a8
pkgrel=1
pkgdesc="Extension which puts minimize, maximize and close buttons in the top panel."
arch=('any')
url="https://github.com/danielkza/Gnome-Shell-Window-Buttons-Extension"
license=('GPL3')
depends=('gnome-shell>=3.4' 'gnome-shell<3.15')

# template start; name=git; version=1;
makedepends+=('git')
source+=("$pkgname::git+$url")
md5sums+=('SKIP')
provides+=("${pkgname%-git}")
conflicts+=("${pkgname%-git}")

pkgver() {
  cd $pkgname
  git describe --long --tags 2>/dev/null | sed 's/[^[:digit:]]*\(.\+\)-\([[:digit:]]\+\)-g\([[:xdigit:]]\{7\}\)/\1.r\2.g\3/'
  if [ ${PIPESTATUS[0]} -ne 0 ]; then
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  fi
}

# vim: filetype=sh
# template end;
# template start; name=modularize-package; version=1;
package() {
  for function in $(declare -F | grep -Po 'package_[[:digit:]]+[[:alpha:]_]*$')
  do
    $function
  done
}

# vim: filetype=sh
# template end;
# template start; name=install-code; version=1;
package_01_locate() {
  msg2 'Locating extension...'
  cd "$(dirname $(find -name 'metadata.json'))"
  extname=$(grep -Po '(?<="uuid": ")[^"]*' metadata.json)
  destdir=""$pkgdir/usr/share/gnome-shell/extensions/$extname""
}

package_02_install() {
  msg2 'Installing extension code...'
  find -maxdepth 1 \( -iname '*.js*' -or -iname '*.css' -or -iname '*.ui' \) -exec install -Dm644 -t "$destdir" '{}' +
}

# vim: filetype=sh
# template end;
# template start; name=install-schemas; version=1;
if [ -z "$install" ]
then
  install=gschemas.install
fi

package_10_schemas() {
  msg2 'Installing schemas...'
  find -name '*.xml' -exec install -Dm644 -t "$pkgdir/usr/share/glib-2.0/schemas" '{}' +
}

# vim: filetype=sh
# template end;
# template start; name=unify-conveniencejs; version=1;
depends+=(gnome-shell-extensions)

package_03_unify_conveniencejs() {
  ln -fs \
    ../user-theme@gnome-shell-extensions.gcampax.github.com/convenience.js \
    "$destdir/convenience.js"
}

# vim: filetype=sh
# template end;

package_09_theme() {
  cp -r --no-preserve=ownership,mode themes "$destdir"
}
