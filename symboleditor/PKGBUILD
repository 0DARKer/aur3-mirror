# Maintainer: Clayton G. Hobbs <clay@lakeserv.net>
pkgname=symboleditor
pkgver=1.1.0
pkgrel=1
pkgdesc="Symbol editor for KXStitch"
arch=('i686' 'x86_64')
url="http://kxstitch.sourceforge.net/"
license=('GPL')
depends=('kdebase-runtime' 'imagemagick')
makedepends=('cmake' 'automoc4' 'doxygen' 'patch')
install=symboleditor.install
source=(http://downloads.sourceforge.net/project/kxstitch/SymbolEditor/${pkgver}/SymbolEditor-${pkgver}-KDE4.tar.gz)
md5sums=('39fce34feed776b58a1fe842b8fc9a34')

build() {
    cd "$srcdir/SymbolEditor-$pkgver-KDE4"
    rm -rf build
    mkdir build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` .. \
        -DImageMagick_Magick++_INCLUDE_DIR:PATH=/usr/include/ImageMagick-6 \
        -DImageMagick_Magick++_LIBRARY:FILEPATH=/usr/lib/libMagick++-6.Q16.so \
        -DImageMagick_MagickCore_INCLUDE_DIR:PATH=/usr/include/ImageMagick-6 \
        -DImageMagick_MagickCore_LIBRARY:FILEPATH=/usr/lib/libMagickCore-6.Q16.so \
        -DCMAKE_CXX_FLAGS:STRING="-DMAGICKCORE_HDRI_ENABLE=0 -DMAGICKCORE_QUANTUM_DEPTH=16"
        # ^^^ workaround for broken cmake, remove once cmake is fixed
        #     see: https://bugs.archlinux.org/task/35280
    make
}

package() {
    cd "$srcdir/SymbolEditor-$pkgver-KDE4/build"
    make DESTDIR="${pkgdir}" install
}
