# Contributor: Markus M. May <mmay AT javafreedom DOT org>
# this work is heavily based on the work done at
# http://scm.narf.ssji.net/archlinux-packages/browser/redmine
pkgname=redmine-mysql-svn
pkgver=2924
pkgrel=1
pkgdesc="Redmine is a flexible project management web application written using Ruby on Rails framework."
url="http://www.redmine.org"
arch=(any)
license=('GPL2')
depends=('ruby>=1.8.6' 'rails>=2.3.4' 'rake' 'mysql' 'ruby-mysql')
optdepends=('apache: a full featured webserver'
	    'ruby-rmagick: to enable Gantt export to png image'
            'git: a distributed version control system'
	    'passenger: Apache Module for Rails')
	
makedepends=('subversion')
conflicts=('redmine')
provides=('redmine')
options=(!strip !docs)
install=redmine.install
backup=('etc/conf.d/redmine' 'usr/lib/redmine/config/database.yml' 'usr/lib/redmine/config/email.yml')
source=('database.yml'
        'email.yml'
	'redmine.init'
	'redmine.conf'
	'httpd-redmine.conf'
	'redmine-webrick')
md5sums=('17d3e23378691237ae7f5e3412e19aa0'
         '06312f351a2f5ae477fc2cd03ce5a776'
         'de618949cf8ff8b872d49ba96adedebb'
         'a644dd04bce0726b3f3daaead266862a'
         '9be1332a86523d3fa663a694194c5e5a'
         '8b73bc56174d92b09d596bae998384bb')

_svntrunk=http://redmine.rubyforge.org/svn/trunk
_svnmod=redmine

build() {
  _instdir=${startdir}/pkg/usr/lib/${pkgname/-*/}
  cd ${srcdir}

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod || exit 1
  fi

  msg "SVN checkout done"

  mkdir -p ${_instdir}
  cp -ra ${startdir}/src/redmine/* ${_instdir}
  rm ${_instdir}/{files,log}/delete.me
  for FILE in ${_instdir}/public/*.example; do
    mv ${FILE} ${FILE/.example/}
  done

  # link to the system rails installation
  ln -s ../../../../usr/share/rails ${_instdir}/vendor/

  install -m 0600 ${srcdir}/database.yml ${_instdir}/config/
  install -m 0600 ${srcdir}/email.yml ${_instdir}/config/
  install -m 0755 -D ${startdir}/redmine.init ${startdir}/pkg/etc/rc.d/redmine
  install -m 0644 -D ${startdir}/redmine.conf \
	${startdir}/pkg/etc/conf.d/redmine.conf

  install -m 0755 -D ${srcdir}/redmine-webrick \
	${startdir}/pkg/usr/sbin/redmine-webrick
  install -m 0644 -D ${startdir}/httpd-redmine.conf \
	${startdir}/pkg/etc/httpd/conf/extra/httpd-redmine.conf

}
