# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libsystemd-qt-git
pkgver=0.35.5376a6c
pkgrel=1
pkgdesc='Qt-only wrapper for Systemd API'
arch=('i686' 'x86_64')
url='https://github.com/ndr/libsystemd-qt'
license=('GPL')
depends=('qt5-base' 'systemd')
makedepends=('git')
provides=('libsystemd-qt')
conflicts=('libsystemd-qt')
source=('git://github.com/ndr/libsystemd-qt')
md5sums=('SKIP')

pkgver() {
  cd libsystemd-qt
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  mkdir -p build
  cd build

  cmake ../libsystemd-qt \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
