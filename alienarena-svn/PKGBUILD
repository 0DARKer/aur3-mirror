# Contributor: Paul Bredbury <brebs@sent.com>

pkgname=alienarena-svn
pkgver=1939
pkgrel=1
pkgdesc="Multiplayer retro sci-fi deathmatch game"
arch=('i686' 'x86_64')
url="http://icculus.org/alienarena/rpa/"
license=('GPL')
groups=('games')
depends=('curl' 'libgl' 'libjpeg' 'libxxf86dga' 'libxxf86vm' 'mesa' 'sdl' 'openal' 'libvorbis')
makedepends=('hd2u' 'subversion' 'unzip')
optdepends=()
provides=('alienarena')
conflicts=('alienarena')
replaces=()
backup=()
options=()
install=
source=(alienarena.desktop)
noextract=()
md5sums=('46bbe3e3a93b2e5009ffd663ad462915')


# View at https://svn.icculus.org/alienarena/trunk/
_svntrunk="svn://svn.icculus.org/alienarena/trunk"
_svnmod="trunk"
_datadir="/usr/share/${pkgname}"
_libdir="/usr/lib/${pkgname}"

build() {
	cd $srcdir

	#SVN
	
	# Force restoration of edited file
	rm -f $_svnmod/source/Makefile $_svnmod/source/unix/gl_glx.c

	msg "Checking for previous build"
	if [[ -d $_svnmod/.svn ]]; then
		msg "Retrieving updates"
		cd $_svnmod
		svn up || return 1
	else
		msg "Retrieving complete sources"
		svn co $_svntrunk || return 1
		cd $_svnmod
	fi
	
	#Build

	cd source
	# Arch isn't multilib
	sed -i -e "s:lib64:lib:" Makefile || return 1

	make clean || return 1

	# Needed to compile fine with new versions of xf86dgaproto (Thanks Calimero)
	sed -i "s/xf86dga.h/Xxf86dga.h/g" unix/gl_glx.c

	make PREFIX=/usr \
		WITH_DATADIR=yes WITH_LIBDIR=yes \
		DATADIR="${_datadir}" LIBDIR="${_libdir}" \
	 || return 1
	 
	#Binary
	install -D -m755 release/game.so $pkgdir/${_libdir}/arena/game.so || return 1
	install -D -m755 release/crx $pkgdir/usr/bin/alienarena || return 1
	install -D -m755 release/crded $pkgdir/usr/bin/alienarena-ded || return 1
	 
	cd ..

	# Data files
	find arena botinfo data1 -type f | while read _f ; do
	install -D -m644 "$_f" "$pkgdir/$_datadir/$_f" || return 1
	done
	 
	# Desktop entry
	install -D -m644 aa.png $pkgdir/usr/share/pixmaps/alienarena.png || return 1
	install -D -m644 $srcdir/alienarena.desktop $pkgdir/usr/share/applications/alienarena.desktop || return 1
	 
	# Docs
	pwd
	cd docs
	dos2unix README.txt
	mkdir -p $pkgdir/usr/share/doc/alienarena
	install -m644 -t $pkgdir/usr/share/doc/alienarena {license,README}.txt || return 1

	# Cleanup
	find $startdir/pkg -name "\.svn" -type d -print0 | xargs -0 rm -rf
}
