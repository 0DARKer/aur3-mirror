# Maintainer: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=gsdjvu
pkgver=1.3
_gsver=8.64
_fontsver=8.11
pkgrel=1
pkgdesc="Very efficient way of converting PostScript and PDF documents into DjVu"
arch=(i686 x86_64)
# This program mixes GPL and CPL licensed codes, so the binaries are not redistributable.
license=('custom:unredistributable')
depends=('libcups>=1.3.9-3' 'cairo')
optdepends=('djvulibre: for the djvudigital converter')
url="http://djvu.sourceforge.net/gsdjvu.html"
source=("http://downloads.sourceforge.net/sourceforge/djvu/gsdjvu-${pkgver}.tar.gz" 
        "http://downloads.sourceforge.net/ghostscript/ghostscript-${_gsver}.tar.bz2" 
        "http://downloads.sourceforge.net/ghostscript/ghostscript-fonts-std-${_fontsver}.tar.gz" 
        'patches.diff'
        'gdevbit.c.patch'
        'ghostscript-fPIC.patch'
        'ghostscript-gpl-8.64-gsdjvu-1.3.patch')
md5sums=('c72d2327708683ffc70151d49fbd6175'
         'b13289cb2115f38f40c5e064f87e228a'
         '6865682b095f8c4500c54b285ff05ef6'
         '097783074906a6f62d951d27dab100e3'
         'b026e3217a21cc83ebc2dcd7c31fd552'
         '1a8fcacf0005214db823225c870f093d'
         'e932f95d1bd72de490ee68ab83da0806')
noextract=("ghostscript-${_gsver}.tar.bz2"
           "ghostscript-fonts-std-${_fontsver}.tar.gz")
options=('!libtool' '!makeflags')

build() {
    cd "${srcdir}/gsdjvu-${pkgver}"

    patch -Np1 -i "${srcdir}/patches.diff" || return 1

    mkdir -p BUILD
    ln -sf "${srcdir}/ghostscript-${_gsver}.tar.bz2" BUILD
    ln -sf "${srcdir}/ghostscript-fonts-std-${_fontsver}.tar.gz" BUILD

    ln -sf "${srcdir}/ghostscript-gpl-8.64-gsdjvu-1.3.patch" BUILD
    ln -sf "${srcdir}/gdevbit.c.patch" BUILD

    if [ "$CARCH" = "x86_64" ]; then
        ln -sf "${srcdir}/ghostscript-fPIC.patch" BUILD
    fi

    echo -e 'YES\n\n' | "${srcdir}/gsdjvu-${pkgver}/build-gsdjvu" || return 1

    install -dm755 "${pkgdir}/usr/lib" || return 1
    cp -a BUILD/INST/gsdjvu "${pkgdir}/usr/lib" || return 1

    install -dm755 "${pkgdir}/usr/bin" || return 1
    ln -s "/usr/lib/gsdjvu/gsdjvu" "${pkgdir}/usr/bin/gsdjvu" || return 1

    sed -i -e 's,/usr/bin/gs,/usr/lib/gsdjvu/bin/gs,' "${pkgdir}/usr/lib/gsdjvu/gsdjvu" || return 1

    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}" || return 1
    install -m644 COPYING* "${pkgdir}/usr/share/licenses/${pkgname}" || return 1
}
