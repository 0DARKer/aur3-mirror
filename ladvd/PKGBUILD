# Maintainer: sh0 <mee@sh0.org>

pkgname=ladvd
pkgver=1.0.4
pkgrel=1
pkgdesc='LLDP daemon for Unix'
arch=('i686' 'x86_64')
url='http://ladvd.googlecode.com/'
license=('BSD')
source=("http://ladvd.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        'ladvd.confd'
        'ladvd')
install='ladvd.install'
backup=('etc/conf.d/ladvd')
depends=('pciutils' 'libcap-ng' 'libpcap' 'libevent')
md5sums=('9ac8e9f75b44540355179eddf697ed2b'
         '5204af37a359312c7a10aa43288112b7'
         'f290fd800d9850a36a8b26bcf8676b25')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-user=ladvd \
    --with-pid-dir=/var/run \
    --with-chroot-dir=/var/run/ladvd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  install -D -m0644 doc/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

  install -D -m755 "${srcdir}/ladvd" "${pkgdir}/etc/rc.d/ladvd"
  install -D -m644 "${srcdir}/ladvd.confd" "${pkgdir}/etc/conf.d/ladvd"
}
