# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: sh0 <mee@sh0.org>

pkgname=ladvd
pkgver=1.0.4
pkgrel=2
pkgdesc='LLDP daemon for Unix'
arch=('i686' 'x86_64')
url='http://ladvd.googlecode.com/'
license=('BSD')
source=("http://ladvd.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        'ladvd.conf'
        'ladvd.service')
install='ladvd.install'
depends=('pciutils' 'libcap-ng' 'libpcap' 'libevent')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr \
		--mandir=/usr/share/man \
		--with-user=ladvd \
		--with-pid-dir=/run \
		--with-chroot-dir=/run/ladvd
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}/" install

	install -D -m0644 doc/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

	install -D -m0644 "${srcdir}/ladvd.service" "${pkgdir}/usr/lib/systemd/system/ladvd.service"
	install -D -m0644 "${srcdir}/ladvd.conf" "${pkgdir}/usr/lib/tmpfiles.d/ladvd.conf"

	install -d -m0755 "${pkgdir}/run/ladvd"
}

sha256sums=('90033d04004bd30e058faca96c0e632000d44b2ef0a65c70f9d972abda4a22ba'
            'bdebaec3a51412e61d2620b2157daed1b4d68c8a9afa359931859ba370563bed'
            '545eae280d1d8a1235ced0f401281157ae6f3632285610f43d1e9051ec2892d6')
