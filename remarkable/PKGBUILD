# Maintainer: Michael Goehler <somebody.here@gmx.de>

pkgname=remarkable
pkgver=1.025
pkgrel=1
pkgdesc="A free fully featured markdown editor for Linux."
arch=('i686' 'x86_64')
url="http://remarkableapp.net"
license=('MIT')
depends=('python'
         'python-cairo'
         'python-gobject'
         'python-markdown'
         'python-beautifulsoup4'
         'python-gtkspellcheck'
         'webkitgtk'
         'wkhtmltopdf')
makedepends=('python')
options=('!emptydirs' '!strip')
install="${pkgname}.install"
source=("${pkgname}_${pkgver}_all.deb::http://remarkableapp.net/files/${pkgname}_${pkgver}_all.deb"
        "${pkgname}.install")

package() {

    _python_site=$(python -c 'import site; print(site.getsitepackages()[0]);')
    [ -z ${_python_site} ] && echo "error: could not identify python site_packages directory" && return 1

    msg2 "Extracting data.tar.gz..."
    tar -Jxf data.tar.xz -C "${pkgdir}/"

    msg2 "Moving parts in place..."

    # python
    install -d "${pkgdir}/${_python_site}"
    mv  "${pkgdir}/usr/lib/python3/dist-packages/"* "${pkgdir}/${_python_site}/"

    # license
    install -d "${pkgdir}/usr/share/licenses/${pkgname}/"
    curl -Ls "${url}/license.txt" \
         -o "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

    msg2 "Fixing module include path..."
    sed -i "s/import undobuffer/from remarkable import undobuffer/" \
           "${pkgdir}/${_python_site}/remarkable/RemarkableWindow.py"

    msg2 "Removing unnecessities..."
    rm -r "${pkgdir}/usr/lib/mime"
}

md5sums=('0d30b67931354d937244a9bb382ab1cb'
         '4230de2876e8789bcd5a7cdc84b2a30b')
