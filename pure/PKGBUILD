# -*- shell-script -*-
# Contributor: Mikko Sysikaski  mikko.sysikaski at gmail dot com
# Updated by Stephen Diehl - sdiehl@clarku.edu
# Maintainer: Alastair Pharo  asppsa at gmail dot com
# Maintainer: Albert Graef <aggraef@gmail.com>

pkgname=pure
pkgver=0.63
pkgrel=1
pkgdesc="Pure is a modern-style functional programming language based on term rewriting."
arch=("i686" "x86_64")
license=('GPL3')
url="http://purelang.bitbucket.org/"
depends=('llvm<=3.5.0' 'gmp' 'mpfr' 'readline')
makedepends=('make' 'gcc')
optdepends=("pure-docs: online documentation"
            "w3m: access to help in interactive mode"
            "emacs-pure-mode: editing Pure files from Emacs"
            "texmacs-pure: embedding Pure sessions in TeXmacs")
groups=(pure-complete pure-base)
source=("https://bitbucket.org/purelang/pure-lang/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('1aae45d9b8bd8d5fc863aa127de232bc')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --without-elisp --without-texmacs --enable-release
  make || return 1
  # check that the interpreter is working
  make check || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install || return 1
}
