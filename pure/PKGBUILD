# -*- shell-script -*-
# Contributor: Mikko Sysikaski  mikko.sysikaski at gmail dot com
# Updated by Stephen Diehl - sdiehl@clarku.edu
# Maintainer: Alastair Pharo  asppsa at gmail dot com
# Maintainer: Albert Graef <aggraef@gmail.com>

pkgname=pure
pkgver=0.59
pkgrel=1
pkgdesc="Pure is a modern-style functional programming language based on term rewriting."
arch=("i686" "x86_64")
license=('GPL3')
url="http://purelang.bitbucket.org/"
depends=('llvm<=3.4' 'gmp' 'mpfr' 'readline')
makedepends=('make' 'gcc')
optdepends=("pure-docs: online documentation"
            "w3m: access to help in interactive mode"
            "emacs-pure-mode: editing Pure files from Emacs"
            "texmacs-pure: embedding Pure sessions in TeXmacs")
groups=(pure-complete pure-base)
source=("https://bitbucket.org/purelang/pure-lang/downloads/$pkgname-$pkgver.tar.gz"
        "https://bitbucket.org/purelang/pure-lang/downloads/llvm-3.3-tools.tar.bz2")
md5sums=('d30c47d6b126665741862ecbd42bd438'
	 '0a3c4154e7d20f1d078d994ec47be1b7')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --without-elisp --without-texmacs --enable-release
  make || return 1
  # check that the interpreter is working
  make check || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install || return 1
  # llc in LLVM 3.4 is broken; these will work with both LLVM 3.3 and 3.4
  cd $srcdir/llvm-3.3-tools/$CARCH
  cp llc opt $pkgdir/usr/lib/pure || return 1
}
