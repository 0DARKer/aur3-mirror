# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=gnome-bluetooth-git
pkgver=20091008
pkgrel=1
pkgdesc="GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('hal' 'bluez>=4.34' 'libgnomeui>=2.18.1-2' 'gconf>=2.18.0.1-4'
         'libnotify>=0.4.4' 'libunique>=1.0')
makedepends=('gnome-common' 'desktop-file-utils' 'intltool' 'gtk-doc' 'git'
             'automake' 'autoconf')
optdepends=('nautilus>=2.22 & obex-data-server>=0.4: for browsing files on device'
            'obexd>=0.13: for sending files to device')
conflicts=('bluez-gnome')
install=gnome-bluetooth.install

_gitroot="git://git.gnome.org/gnome-bluetooth"
_gitname="gnome-bluetooth"

build() {
  cd ${srcdir}
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${srcdir}/build
  git clone ${srcdir}/$_gitname ${srcdir}/build
  cd ${srcdir}/build

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-desktop-update --disable-icon-update \
    --disable-gtk-doc --disable-schemas-install || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas \
    ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  # Temporary: create plugins directory (to suppress error)
  mkdir -p ${pkgdir}/usr/lib/gnome-bluetooth/plugins

  rm -rf ${srcdir}/build
}
