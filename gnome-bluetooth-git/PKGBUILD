# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=gnome-bluetooth-git
_pkgname=gnome-bluetooth
pkgver=20120813
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('gtk3' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'dconf' 'libnotify')
makedepends=('intltool' 'gnome-doc-utils' 'nautilus-sendto' 'gobject-introspection' 'gnome-common' 'git' 'automake' 'autoconf' 'yelp-tools')
options=('!libtool' '!emptydirs')
provides=('gnome-bluetooth')
conflicts=('gnome-bluetooth')
install=gnome-bluetooth.install
source=(61-gnome-bluetooth-rfkill.rules)
md5sums=('9fb6c8dcb5a3de3a953bf2ef42063e6e')

_gitroot="git://git.gnome.org/gnome-bluetooth"
_gitname="gnome-bluetooth"

build() {
  cd ${srcdir}
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${srcdir}/build
  git clone ${srcdir}/$_gitname ${srcdir}/build
  cd ${srcdir}/build

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install

  install -m644 -D "${srcdir}/61-gnome-bluetooth-rfkill.rules" \
    "${pkgdir}/usr/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}
