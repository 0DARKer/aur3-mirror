# Maintainer cars10t <carsten.1 gmx net>
# Contributor: zoulnix <http://goo.gl/HQaP>

pkgname=smashbattle
pkgver=110224
pkgrel=1
pkgdesc="8-bit style game where two player face off against each other."
arch=('i686' 'x86_64')
url="http://smashbattle.demontpx.com"
license=('GPL')
depends=('sdl_mixer' 'desktop-file-utils')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-src.zip")
md5sums=('23b7e49549fdf3f0d8713c9953b7f728')
install="${pkgname}.install"

build() {
  cd ${srcdir}/battle/Battle

  # fix launcher paths
  sed -i 's|/usr/share/games/smashbattle/gfx/SB.png|smashbattle|' linux/smash*.desktop
  sed -i 's|/usr/local/bin/||' linux/smash*.desktop
  sed -i 's|/games/|/|' linux/smashbattle
  sed -i 's|bash|sh|' linux/smashbattle

  # fix sdl include/libs and add our makeflags
  sed -i 's|CFLAGS=|CFLAGS += $(shell sdl-config --cflags) |' Makefile
  sed -i 's|LDFLAGS=-lSDL|LIBS = $(shell sdl-config --libs) |' Makefile
  sed -i 's|$(OBJS) -o battle|$(OBJS) $(LIBS) -o battle|' Makefile

  make
}

package() {
  cd ${srcdir}/battle/Battle

  # create directories
  install -d ${pkgdir}/usr/{bin,share/${pkgname}} \
	     ${pkgdir}/usr/share/{applications,pixmaps,${pkgname}/{gfx,music,sfx,stage}}

  install -m755 battle ${pkgdir}/usr/share/${pkgname}/
  install -m755 linux/${pkgname} ${pkgdir}/usr/bin/${pkgname}
  install -m644 linux/${pkgname}*.desktop ${pkgdir}/usr/share/applications/
  install -m644 gfx/SB.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
  install -m644 gfx/*.bmp ${pkgdir}/usr/share/${pkgname}/gfx/
  install -m644 music/*.ogg ${pkgdir}/usr/share/${pkgname}/music/
  install -m644 sfx/*.wav ${pkgdir}/usr/share/${pkgname}/sfx/
  install -m644 stage/*.lvl ${pkgdir}/usr/share/${pkgname}/stage/

  # fix permission for save support
  chgrp games ${pkgdir}/usr/share/${pkgname}/
  chmod g+w ${pkgdir}/usr/share/${pkgname}/
}
