# Maintainer: Det
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>

pkgname=iron-bin
pkgver=31.0.1700.0
pkgrel=3
pkgdesc="A browser based on Chromium without Google's 'tracking features'"
arch=('i686' 'x86_64')
url="http://www.srware.net/en/software_srware_iron.php"
license=('BSD')
depends=('alsa-lib' 'desktop-file-utils' 'gconf' 'gtk-update-icon-cache' 'gtk2' 'hicolor-icon-theme' 'libpng' 'libxslt' 'libxss' 'libxtst' 'nss' 'ttf-font' 'xdg-utils')
optdepends=('kdebase-kdialog: needed for file dialogs in KDE')
install=${pkgname}.install
[ "$CARCH" = 'x86_64' ] && _64=-64
source=("iron-linux-${pkgver}${_64}.tar.gz::http://www.srware.net/downloads/iron-linux${_64}.tar.gz"
        "nacl-${pkgver}.zip::http://srware.net/downloads/nacl.zip"
        iron.sh
        iron.default
        iron.desktop
        iron_16.png
        iron_22.png
        iron_24.png
        iron_32.png
        iron_48.png
        iron_64.png
        iron_128.png
        iron_256.png)
md5sums=('4592966535ffbedb7404c935f9abd9a6'
         '0a62377076abd63069a31ec31d01e555'
         '04e84fbe164291915591f1f2e1aa9727'
         '36f606a7e5ae3db2e28daa7c45c18c78'
         '2dc0bd42de6483b63c8ebc71dfcf5a9d'
         '2a763a98a0f419cc41f86fb9f109a98a'
         '2173c01ca1dc38997cca2d31a6a60c9c'
         '1b23bfb78717ab8f0583693a9428805f'
         '5856ebd5628ee5dd96a6160adb2c64b0'
         '3cf3e37376d0899e981dd733d9da6f89'
         'bf6f1be399efdeb2d3901c15b25285af'
         'ef9e15401cbb85bc4ee2ee6df92df02d'
         '60df76c38381e46577505b0c61cea017')
[ "${_64}" ] && md5sums[0]='655d52dfd5612a7aecaa6f0c24174546'

PKGEXT='.pkg.tar'

package() {
  msg2 "Moving stuff in place"
  install -d "${pkgdir}"/opt/
  mv iron*/ "${pkgdir}"/opt/iron/

  msg2 "Installing Native Client"
  if [ "${_64}" ]; then
    mv nacl_irt_x86_64.nexe "${pkgdir}"/opt/iron/
  else
    mv nacl_irt_x86_32.nexe "${pkgdir}"/opt/iron/
  fi

  msg2 "Correcting Sandbox binary"
  mv "${pkgdir}"/opt/iron/chrome_sandbox "${pkgdir}"/opt/iron/chrome-sandbox
  chmod 4755 "${pkgdir}"/opt/iron/chrome-sandbox

  msg2 "Symlinking missing Udev lib"
  ln -s /usr/lib/libudev.so "${pkgdir}"/opt/iron/libudev.so.0

  msg2 "Installing the launcher, icons, .desktop and settings"
  # Launcher
  install -Dm755 iron.sh "${pkgdir}"/usr/bin/iron

  # Icons
  for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 iron_${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/iron.png
  done

  # Desktop
  install -Dm644 iron.desktop "${pkgdir}"/usr/share/applications/iron.desktop

  # Settings
  install -Dm644 iron.default "${pkgdir}"/etc/iron/default
}
