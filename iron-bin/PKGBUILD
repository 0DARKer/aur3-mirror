# Maintainer: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Elkin Cruz <linuxero789@gmail.com>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: larsrh <lars.hupel@gmx.de>
# Contributor: Falcata <zauberexonar@gmail.com>

pkgname=iron-bin
pkgver=20.0.1150.1
pkgrel=2
pkgdesc="A browser based on Chromium without Google's 'tracking features'"
arch=('i686' 'x86_64')
url="http://www.srware.net/en/software_srware_iron.php"
license=('BSD')
depends=('gtk2' 'dbus-glib' 'nss' 'alsa-lib' 'xdg-utils' 'bzip2' 'libevent' 'libxss' 'libgcrypt' 'ttf-dejavu' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('kdebase-kdialog: needed for file dialogs in KDE')
install=$pkgname.install
md5sums=('ecd4e7488b80d834a0cd66badf463ec6'
         '2dc0bd42de6483b63c8ebc71dfcf5a9d'
         '60df76c38381e46577505b0c61cea017'
         '6ec430e5597388492b413b152c9587ac'
         '30032c6c531632af6547fa360395acd3')
if test "$CARCH" == 'x86_64'; then
    _64=-64
    md5sums[0]='642b3698eec80c4b6748f413ff92cd2e'
fi
source=("http://www.srware.net/downloads/iron-linux$_64.tar.gz"
        iron.desktop
        iron.png
        iron.sh
        LICENSE)
 
package() {
  install -Dm755 iron.sh "$pkgdir/usr/bin/iron"
  install -d "$pkgdir/opt"
  mv iron-linux$_64 "$pkgdir/opt/iron"
 
  # Remove the already provided logo
  rm -f "$pkgdir"/opt/iron/product_logo_48.png

  #udev workaround
  ln -s /usr/lib/libudev.so "$pkgdir/opt/iron/libudev.so.0"
 
  install -Dm644 iron.png "$pkgdir/usr/share/pixmaps/iron.png"
  install -Dm644 iron.desktop "$pkgdir/usr/share/applications/iron.desktop"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
