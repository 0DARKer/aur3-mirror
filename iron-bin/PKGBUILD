# Contributor: Elkin Cruz <linuxero789@gmail.com>
# Contributor: larsrh <lars.hupel@gmx.de>
# Maintainer: Falcata <zauberexonar@gmail.com>

pkgname=iron-bin
pkgver=18.0.1050.0
pkgrel=1
pkgdesc="A browser based on Chromium without Google's 'tracking features'"
arch=('i686' 'x86_64')
url="http://www.srware.net/en/software_srware_iron.php"
license=('BSD')
depends=('gtk2' 'gconf' 'dbus-glib' 'nss' 'alsa-lib' 'xdg-utils' 'libxss' 'ttf-dejavu' 'desktop-file-utils' 'hicolor-icon-theme' 'libpng12')
install=${pkgname}.install
 
[[ "$CARCH" == 'x86_64' ]] && _64=-64
source=(http://www.srware.net/downloads/iron-linux${_64}.tar.gz
        iron.desktop
        iron.png
        iron.sh
        LICENSE)
md5sums=('9276d2818dcae99f692cd7dbddeae158'
         '2dc0bd42de6483b63c8ebc71dfcf5a9d'
         '60df76c38381e46577505b0c61cea017'
         '6ec430e5597388492b413b152c9587ac'
         '30032c6c531632af6547fa360395acd3')
[[ "$CARCH" == 'x86_64' ]] && md5sums[0]='b89f37910c85309836affb6e00c9fa0e'
 
package() {
  install -Dm755 iron.sh "${pkgdir}/usr/bin/iron"
  install -d "${pkgdir}/opt"
  mv iron-linux${_64} "${pkgdir}/opt/iron"
 
  # Remove the already provided logo
  rm -f "${pkgdir}"/opt/iron/product_logo_48.png
 
  install -Dm644 iron.png "${pkgdir}/usr/share/pixmaps/iron.png"
  install -Dm644 iron.desktop "${pkgdir}/usr/share/applications/iron.desktop"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
