# Maintainer: Det
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>

pkgname=iron-bin
pkgver=31.0.1700.0
pkgrel=4
pkgdesc="A browser based on Chromium without Google's 'tracking features'"
arch=('i686' 'x86_64')
url="http://www.srware.net/en/software_srware_iron.php"
license=('BSD')
depends=('alsa-lib' 'desktop-file-utils' 'flac' 'gconf'  'gtk2' 'harfbuzz' 'harfbuzz-icu' 'hicolor-icon-theme' 'icu'
         'libgcrypt15' 'libpng' 'libxss' 'libxtst' 'nss' 'opus' 'snappy' 'speech-dispatcher' 'ttf-font' 'xdg-utils')
optdepends=('kdebase-kdialog: needed for file dialogs in KDE')
install=${pkgname}.install
if [ "${CARCH}" = 'x86_64' ]; then
    _64=64
    _x64=_x64
fi
source=("iron_${pkgver}${_x64}.deb::http://www.srware.net/downloads/iron${_64}.deb"
        "nacl_${pkgver}.zip::http://srware.net/downloads/nacl.zip"
        iron.sh
        iron.default
        iron.desktop
        iron_16.png
        iron_22.png
        iron_24.png
        iron_32.png
        iron_48.png
        iron_64.png
        iron_128.png
        iron_256.png)
md5sums=('a6f0ac6800547d246679688c1d27756b'  # iron_${pkgver}.deb
         '0a62377076abd63069a31ec31d01e555'  # nacl_${pkgver}.zip
         '04e84fbe164291915591f1f2e1aa9727'  # iron.sh
         '36f606a7e5ae3db2e28daa7c45c18c78'  # iron.default
         '917c7ea00febc7054543e0a21703c76d'  # iron.desktop
         '2a763a98a0f419cc41f86fb9f109a98a'  # iron_16.png
         '2173c01ca1dc38997cca2d31a6a60c9c'  # iron_22.png
         '1b23bfb78717ab8f0583693a9428805f'  # iron_24.png
         '5856ebd5628ee5dd96a6160adb2c64b0'  # iron_32.png
         '3cf3e37376d0899e981dd733d9da6f89'  # iron_48.png
         'bf6f1be399efdeb2d3901c15b25285af'  # iron_64.png
         'ef9e15401cbb85bc4ee2ee6df92df02d'  # iron_128.png
         '60df76c38381e46577505b0c61cea017') # iron_256.png
[ "${_64}" ] && md5sums[0]='d33b409789cbb007e1ce4bcef57db9f4' # iron_${pkgver}_x64.deb

PKGEXT='.pkg.tar'

package() {
  msg2 "Extracting the data.tar.lzma"
  bsdtar -xf data.tar.gz -C "${pkgdir}"/

  msg2 "Relocating: /usr/share -> /opt"
  install -d "${pkgdir}"/opt/
  mv "${pkgdir}"/usr/share/iron/ "${pkgdir}"/opt/

  msg2 "Installing Native Client"
  if [ "${_64}" ]; then
    mv nacl_irt_x86_64.nexe "${pkgdir}"/opt/iron/
  else
    mv nacl_irt_x86_32.nexe "${pkgdir}"/opt/iron/
  fi

  msg2 "Correcting Sandbox binary"
  mv "${pkgdir}"/opt/iron/chrome_sandbox "${pkgdir}"/opt/iron/chrome-sandbox
  chmod 4755 "${pkgdir}"/opt/iron/chrome-sandbox

  msg2 "Symlinking missing Udev lib"
  ln -s /usr/lib/libudev.so "${pkgdir}"/opt/iron/libudev.so.0

  msg2 "Installing additions"
  # Launcher
  install -Dm755 iron.sh "${pkgdir}"/usr/bin/iron

  # Icons
  for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 iron_${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/iron.png
  done

  # Desktop
  install -m644 iron.desktop "${pkgdir}"/usr/share/applications/iron.desktop

  # Settings
  install -Dm644 iron.default "${pkgdir}"/etc/iron/default

  msg2 "Removing duplicated icon"
  rm -r "${pkgdir}"/usr/share/pixmaps/
  
  msg2 "Changing permissions"
  find "${pkgdir}"/ -type d -exec chmod 755 {} +
}