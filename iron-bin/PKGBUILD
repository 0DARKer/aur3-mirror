# Contributor: larsrh <lars.hupel@gmx.de>
# Maintainer: Elkin Cruz <linuxero789@gmail.com>
pkgname=iron-bin
pkgver=10.0.650.0
pkgrel=1
pkgdesc="A fast web browser based on chromium without Google's tracking 'features'"
arch=('i686' 'x86_64')
url="http://www.srware.net/en/software_srware_iron.php"
license=('BSD')
depends=('alsa-lib' 'libjpeg6' 'libpng12' 'nss' 'ttf-dejavu' 'libxss' 'gconf')
install=$pkgname.install

# x86_64 source
[[ "$CARCH" == x86_64 ]] && _64=-64

source=(http://www.srware.net/downloads/iron-linux${_64}.tar.gz
        iron{.desktop,.png,.sh} LICENSE)

sha256sums=('ce82b5fc5f0353e424efddc41153d41ae66b20c579b73dd97dd85e4aa82eece0'
            'd4b7e0057e754d6e0c8c9c116103df2e4a36169b93679dd0bd6338ee51a5bcbf'
            '77c29198ff3079f4be5469023a5317c18dd7ef9ce34efd3ae4ca0be09d5de285'
            '746148eba30c710dd2ad091afae102bf609501ebbdf8e2633cc88f2fe5262189'
            'ec68693372a025d4cba5214a0b97c66ae62025c80cc34ef6c01c92b32bd0f2d6')

[[ "$CARCH" == x86_64 ]] && sha256sums[0]='dff6c63edc5af073d448c62bec88d8e25c0b849dbece5955eef6003af5265b55'

package() {
	install -Dm755 iron.sh "$pkgdir/usr/bin/iron"
	install -d "$pkgdir/opt"

	local suffix=
	[[ $CARCH = x86_64 ]] && suffix=-64

	mv iron-linux$suffix "$pkgdir/opt/iron"
	rm -f "$pkgdir/opt/iron/"{product_logo_48.png, README, xdg-settings}

	ln -s /lib/libBrokenLocale.so.1 "$pkgdir/opt/iron/libgconf-2.so.4"

	find "$pkgdir/opt/iron/" -type f -exec chmod 644 {} +
	find "$pkgdir/opt/iron/" -type d -exec chmod 755 {} +
	chmod a+x "$pkgdir/opt/iron/iron"
	chmod a+x "$pkgdir/opt/iron/libffmpegsumo.so"

	install -Dm644 iron.png "$pkgdir/usr/share/pixmaps/iron.png"
	install -Dm644 iron.desktop "$pkgdir/usr/share/applications/iron.desktop"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
