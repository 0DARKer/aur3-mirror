# Contributor: Médéric Boquien <mboquien@free.fr>
# Maintainer: Médéric Boquien <mboquien@free.fr>
pkgname=python2-astropy
pkgver=0.2.1
pkgrel=2
pkgdesc="A community python library for astronomy"
arch=('i686' 'x86_64')
url="http://www.astropy.org/"
license=("3-clause BSD")
depends=('python2>=2.7' 'python2-numpy>=1.4' 'python2-scipy' 'python2-h5py' 'cfitsio' 'expat' 'wcslib' 'sofa')
conflicts=('python2-pyfits' 'python-pywcs' 'python2-vo')
makedepends=('cython2')
source=("http://pypi.python.org/packages/source/a/astropy/astropy-${pkgver}.tar.gz")
md5sums=('10e08dbda909031961f3efab23292556')

build() {
  cd ${srcdir}/astropy-${pkgver}
  python2 setup.py build --use-system-libraries --enable-legacy
}

package() {
  cd ${srcdir}/astropy-${pkgver}
  python2 setup.py install --root=${pkgdir} --prefix=/usr --optimize=1

  mv ${pkgdir}/usr/bin/fitscheck ${pkgdir}/usr/bin/fitscheck2
  mv ${pkgdir}/usr/bin/fitsdiff ${pkgdir}/usr/bin/fitsdiff2
  mv ${pkgdir}/usr/bin/volint ${pkgdir}/usr/bin/volint2
}

