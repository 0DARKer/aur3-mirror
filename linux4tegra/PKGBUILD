pkgname=linux4tegra
pkgver=R15beta1
pkgrel=1
arch=(arm armv7h)
url="http://developer.nvidia.com/linux-tegra"
license=(custom)
options=(!strip)
pkgdesc="drivers and codecs for nvidia tegra platform"

platform=ventana # harmony / ventana / cardhu
xabi=12 #5..12

source=("http://developer.nvidia.com/sites/default/files/akamai/tools/files/l4t/r15_beta/${platform}_Tegra-Linux-R15.beta.1.0_armel.tbz2"
        "http://developer.nvidia.com/sites/default/files/akamai/tools/files/l4t/r15_beta/${platform}_Tegra-Linux-codecs-R15.beta.1.0_armel.tar.gz")
md5sums=('a3f68b98b79e19e99afbd4896c09aa03'
         'f0a16dbba197b765938370fef5de529e')

if [[ $CARCH = armv7h ]]; then
  source=("${source[@]}" "http://developer.nvidia.com/sites/default/files/akamai/mobile/files/l4t/${platform}_nv-tegra_base_R15-Beta.1.0_armhf.tbz2")
  md5sums=("${md5sums[@]}" "81c353326e9cd660746d48b9f051f23e")
fi;
package() {
  if [[ $CARCH = armv7h ]]; then
    tar xaf $srcdir/base.tgz -C $pkgdir
  else
    tar xaf $srcdir/Linux_for_Tegra/nv_tegra/base.tgz -C $pkgdir
  fi
  tar xaf $srcdir/restricted_codecs.tbz2 -C $pkgdir
  rm -rf $pkgdir/{home,usr/sbin,lib/modules}
  install -D -m644 "${srcdir}/Tegra_Software_License_Agreement-Tegra-Linux-codecs.pdf" "${pkgdir}/usr/share/licenses/${pkgname}/codecs_agreement.pdf"
  install -D -m644 ${srcdir}/Linux_for_Tegra/nv_tegra/LICENSE* ${pkgdir}/usr/share/licenses/${pkgname}
  mkdir $pkgdir/etc/X11/xorg.conf.d
  mv $pkgdir/etc/X11/xorg.conf $pkgdir/etc/X11/xorg.conf.d/10-tegra.conf
  rm $pkgdir/etc/nv_*
  mv $pkgdir/usr/lib/xorg/modules/drivers/tegra_drv.abi${xabi}.so $pkgdir/usr/lib/xorg/modules/drivers/tegra_drv.so
  rm $pkgdir/usr/lib/xorg/modules/drivers/tegra_drv.abi*
  tar xaf $srcdir/Linux_for_Tegra/nv_tegra/nv_sample_apps/nvgst/nvgstapps.tgz -C $pkgdir
}
