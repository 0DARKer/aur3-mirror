# Maintainer: Virgil Dupras <hsoft@hardcoded.net>
pkgname=dupeguru-se
pkgver=3.6.0
pkgrel=1
pkgdesc="Find duplicate files on your system"
arch=(any)
url="http://www.hardcoded.net/dupeguru/"
license=('BSD')
depends=('python' 'pyqt')
makedepends=(
  'mercurial'
  'python-distribute'
  'python-pip'
  'python-virtualenv'
  'python-sphinx'
  'gcc'
)
source=(dupeguru-se.desktop)
md5sums=('63c05c9f51317102339a9ac33dd3b755')


build() {

  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [[ -d "$pkgname" ]]; then
    cd "$pkgname"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "https://bitbucket.org/hsoft/dupeguru" "$pkgname"
    cd "$pkgname"
  fi

  msg "Mercurial checkout done or server timeout"
  
  # NOTE: The "-arch" suffix is temporary. Recent fixes have been made upstream to make Arch
  # packaging easier, but there wasn't a release with these changes yet, so I've made a special
  # "arch" tag in the upstream repo. After the next release, we can remove that suffix.
  msg "Switching to Production Tag (se${pkgver}-arch)"
  hg checkout "se${pkgver}-arch"
  
  if [[ ! -d "env" ]]; then
    msg "Creating virtualenv"
    virtualenv --system-site-packages env
  fi
  
  msg "Installing dependencies"
  source env/bin/activate
  pip install -r requirements.txt
  
  msg "Starting build..."
  python configure.py
  python build.py --clean
}

package() {
  cd "$srcdir"
  mkdir -p "${pkgdir}/usr/share/applications"
  cp ${pkgname}.desktop "${pkgdir}/usr/share/applications"
  
  cd "$pkgname"
  python package.py
  cd "build/${pkgname}-arch"
  
  mkdir -p "$pkgdir/usr/share/${pkgname}"
  cp -a * "$pkgdir/usr/share/${pkgname}/"
  chmod a+x "$pkgdir/usr/share/${pkgname}/run.py"
  
  mkdir -p "$pkgdir/usr/bin"
  ln -s ../share/${pkgname}/run.py "$pkgdir/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
