# Contributor: Roman Titov <titovroman@gmail.com>
pkgname=racket-minimal
_pkgname=racket
pkgver=6.0.1
pkgrel=1
pkgdesc="Minimal Racket installation, without useless DrRacket"
url='http://racket-lang.org/'
license=('GPL3' 'LGPL3' 'custom')
arch=('i686' 'x86_64')
options=('!strip' '!emptydirs')
provides=('racket')
conflicts=('racket')
source=("http://mirror.racket-lang.org/installers/$pkgver/$pkgname-$pkgver-src.tgz")
sha1sums=('d7420e40b7f7ed00af90df4d659f673b14aeab35')
build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/src"

  [ "$CARCH" == "x86_64" ] && export CFLAGS+=" -fPIC"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared
  make
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/src"

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING-libscheme.txt "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
