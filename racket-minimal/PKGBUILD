# Contributor: Roman Titov <titovroman@gmail.com>
pkgname=racket-minimal
_pkgname=racket
pkgver=6.0
pkgrel=1
pkgdesc="Minimal Racket installation, without useless DrRacket"
url='http://racket-lang.org/'
license=('GPL3' 'LGPL3' 'custom')
arch=('i686' 'x86_64')
options=('!strip' '!emptydirs')
provides=('racket')
conflicts=('racket')
source=("http://mirror.racket-lang.org/installers/6.0/$pkgname-$pkgver-src.tgz")
sha1sums=('3cc6556cc5174658660c4f9e004fef32eaffc4ba')

build() {
  cd ${_pkgname}-${pkgver}/src

  [ "$CARCH" == "x86_64" ] && export CFLAGS+=" -fPIC"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared
  make
}

package() {
  cd ${_pkgname}-${pkgver}/src

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING-libscheme.txt "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}
