# Maintainer: Keshav P R <skodabenz at rocketmail dot com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Hokum <hokum_at_mail_dot_ru>

pkgname=gptfdisk-git
pkgver=20110130
pkgrel=1
pkgdesc="A text-mode partitioning tool that works on Globally Unique Identifier (GUID) Partition Table (GPT) disks - GIT version"
arch=('i686' 'x86_64')
url="http://www.rodsbooks.com/gdisk"
depends=('gcc-libs' 'util-linux-ng' 'popt' 'man-db')
makedepends=('man2html')
license=('GPL2')
conflicts=('gdisk')
provides=('gdisk')
source=()
md5sums=()

_gitroot="git://gptfdisk.git.sourceforge.net/gitroot/gptfdisk/gptfdisk"
_gitname="gptfdisk"

update_git() {
	
	cd "${srcdir}"
	
	msg "Connecting to GIT server...."
	
	if [ -d ${srcdir}/${_gitname}/ ]
	then
		cd ${srcdir}/${_gitname}/
		git reset --hard
		git fetch
		git checkout master
		git merge remotes/origin/master
		msg "The local GIT repo has been updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi
	
	msg "GIT checkout done or server timeout"
	
}

build() {
	
	update_git
	
	rm -rf ${srcdir}/${_gitname}_1/ || true
	
	cp -r ${srcdir}/${_gitname} ${srcdir}/${_gitname}_1
	cd ${srcdir}/${_gitname}_1/
	
	CFLAGS= make || return 1
	echo
	
	man ./gdisk.8 | man2html -bare > ./gdisk.html
	echo
	man ./sgdisk.8 | man2html -bare > ./sgdisk.html
	echo
	
}

package() {
	
	cd ${srcdir}/${_gitname}_1/
	
	install -D -m755 gdisk ${pkgdir}/sbin/gdisk
	install -D -m755 sgdisk ${pkgdir}/sbin/sgdisk
	install -D -m644 gdisk.8 ${pkgdir}/usr/share/man/man8/gdisk.8
	install -D -m644 sgdisk.8 ${pkgdir}/usr/share/man/man8/sgdisk.8
	install -D -m644 gdisk.html ${pkgdir}/usr/share/gdisk/gdisk.html
	install -D -m644 sgdisk.html ${pkgdir}/usr/share/gdisk/sgdisk.html
	install -D -m644 README ${pkgdir}/usr/share/gdisk/README
	install -D -m644 NEWS ${pkgdir}/usr/share/gdisk/NEWS
	
}
