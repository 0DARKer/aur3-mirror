# Maintainer:  cuihao <cuihao dot leo at gmail dot com>
# Contributor: Guten <ywzhaifei at gmail dot com> 

pkgname=goagent-git
pkgver=20130528
pkgrel=1
pkgdesc="A gae proxy forked from gappproxy/wallproxy"
arch=("any")
url="http://goagent.googlecode.com"
license=("GPL2")
depends=('python2' 'python2-pyopenssl' 'python2-gevent')
optdepends=(
    'python26: (AUR) upload GAE code using /opt/server/uploader.zip'
)
conflicts=('goagent' 'systemd-goagent-units')
replaces=('systemd-goagent-units')
provides=('goagent')
source=(
  "$pkgname::git://github.com/goagent/goagent.git"
  "goagent.daemon"
  "goagent.service"
)
md5sums=('SKIP'
         '94e66bb9673157bc7fcfc2e7d01be389'
         '1369618bb20df7b2f09c9df69d560e87')
backup=('etc/goagent')
install='goagent-git.install'

pkgver() {
    cd "$srcdir/$pkgname"
    git log -1 --format="%cd" --date=short | sed 's|-||g'
}

package() {
    mkdir -p "$pkgdir/opt/goagent"
    cd "$pkgdir/opt/goagent"

    cp -r "$srcdir/$pkgname/"{local,server} ./
    chmod +x "local/proxy.py"
  
    # remove windows-only files
    rm -f */*.{vbs,dll,exe,manifest,bat}

    # config file
    install -Dm644 "local/proxy.ini" "${pkgdir}/etc/goagent"
    ln -sf "/etc/goagent" "local/proxy.ini"

    # rc.d daemon
    install -Dm755 "${srcdir}/goagent.daemon" "${pkgdir}/etc/rc.d/goagent"

    # systemd service
    install -Dm644 "${srcdir}/goagent.service" "${pkgdir}/usr/lib/systemd/system/goagent.service"
}

# vim:set ts=2 sw=2 et:
