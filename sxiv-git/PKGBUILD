# Maintainer: Army <uli.armbruster @t gmail>
# Contributor: Bastien Dejean <baskerville@lavabit.com>

pkgname=sxiv-git
pkgver=20121224
pkgrel=1
pkgdesc="a simple x image viewer"
arch=(i686 x86_64)
license=('GPL2')
url="https://github.com/muennich/sxiv"
depends=('imlib2')
makedepends=('git')
optdepends=(
'imagemagick: for the example commands in config.def.h'
'libjpeg-turbo: for the example commands in config.def.h'
'gimp: for the example commands in config.def.h'
)
conflicts=('sxiv')
provides=('sxiv')
install=sxiv.install

_gitroot="https://github.com/muennich/sxiv.git"
_gitname="sxiv"

build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."
	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull
	else
		git clone ${_gitroot} ${_gitname}
	fi
	msg "GIT checkout done or server timeout"
	
	rm -rf "${srcdir}/${_gitname}-build"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
	cd "${srcdir}/${_gitname}-build"

	if [ -e $startdir/config.h ]
	then
		msg "use custom config.h"
		cp ${startdir}/config.h .
	else
		msg "use default config.h"
	fi

	msg "Starting make..."
	make PREFIX="/usr"
}

package() {
	cd "${srcdir}/${_gitname}-build"
	make PREFIX="${pkgdir}/usr" install
	install -Dm644 sxiv.desktop "${pkgdir}/usr/share/applications/sxiv.desktop"
} 
