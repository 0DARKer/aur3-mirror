# Maintainer: Mantas M. <grawity@gmail.com>

pkgname=away
pkgver=0.9.5
pkgrel=2
pkgdesc="A terminal locking program"
arch=(i686 x86_64)
url="http://packages.debian.org/source/squeeze/away"
depends=('pam')
license=(GPL2)
source=("http://ftp.de.debian.org/debian/pool/main/a/away/away_0.9.5.orig.tar.gz"
        "http://ftp.de.debian.org/debian/pool/main/a/away/away_0.9.5-3.diff.gz")
sha1sums=('d54b7756197df9a755357617fadb20de433680de'
          '53efc2b00931453a467304c230357935b2f49afa')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # NESTED TARBALLS SUCK
  bsdtar xf "$pkgname-$pkgver.tar.gz"
  cd "$pkgname-$pkgver"

  rm -rf debian
  patch -Np1 < "$srcdir/away_0.9.5-3.diff"
  patch -Np1 < "debian/patches/05_ehance_security"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/$pkgname-$pkgver"

  install -Dm0755 away          "$pkgdir/usr/bin/away"
  install -Dm0644 doc/away.1    "$pkgdir/usr/share/man/man1/away.1"
  install -Dm0644 doc/awayrc.5  "$pkgdir/usr/share/man/man5/awayrc.5"
  install -Dm0644 data/awayrc   "$pkgdir/usr/share/doc/away/awayrc"

  #install -Dm0644 "$srcdir/away.pam" "$pkgdir/etc/pam.d/away"
  # too lazy to add to sources, sha1sum, &c.

  install -dm0755 "$pkgdir/etc/pam.d"

  cat >"$pkgdir/etc/pam.d/away" <<!
#%PAM-1.0
auth include system-auth
!
} 

# vim: ts=2:sw=2:et
