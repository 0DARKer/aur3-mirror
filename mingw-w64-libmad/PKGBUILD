pkgname=mingw-w64-libmad
pkgver=0.15.1b
pkgrel=3
pkgdesc="A high-quality MPEG audio decoder (mingw-w64)"
arch=(any)
url="http://www.underbit.com/products/mad"
license=("GPL")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("ftp://ftp.mars.org/pub/mpeg/libmad-${pkgver}.tar.gz"
"optimize.diff"
"frame_length.diff"
"libmad.patch")
md5sums=('1be543bc30c56fb6bea1d7bf6a64e66c'
         '787115a740a7b2675acb385cf2f11f6a'
         '3329b032118a68e18908197e65b3198d'
         'fbcca0267a423bae45389666f4cb04d5')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/libmad-$pkgver"
  patch -p1 -i "${srcdir}/libmad.patch"
  patch -p1 -i "${srcdir}/frame_length.diff"
  patch -p1 -i "${srcdir}/optimize.diff"
  
  CFLAGS="-march=i686 -mtune=generic -O2 -s -ftree-vectorize -ftree-vectorizer-verbose=1"
  unset LDFLAGS
  mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-i686-w64-mingw32"
  cd "${srcdir}/${pkgname}-${pkgver}-build-i686-w64-mingw32"
  ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
    --prefix=/usr/i686-w64-mingw32 \
    --build=$CHOST \
    --host=i686-w64-mingw32 \
    --disable-debugging
  make
  i686-w64-mingw32-gcc $CFLAGS -shared .libs/*.o -o .libs/libmad.dll -Xlinker --out-implib -Xlinker .libs/libmad.dll.a
  
  CFLAGS="-march=x86-64 -mtune=generic -O2 -s -ftree-vectorize -ftree-vectorizer-verbose=1"
  unset LDFLAGS
  mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-x86_64-w64-mingw32"
  cd "${srcdir}/${pkgname}-${pkgver}-build-x86_64-w64-mingw32"
  ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
    --prefix=/usr/x86_64-w64-mingw32 \
    --build=$CHOST \
    --host=x86_64-w64-mingw32 \
    --disable-debugging
  make
  x86_64-w64-mingw32-gcc $CFLAGS -shared .libs/*.o -o .libs/libmad.dll -Xlinker --out-implib -Xlinker .libs/libmad.dll.a
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -Dm644 ".libs/libmad.dll" "$pkgdir/usr/${_arch}/bin/libmad.dll"
    install -m644 ".libs/libmad.dll.a" "$pkgdir/usr/${_arch}/lib/libmad.dll.a"
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}