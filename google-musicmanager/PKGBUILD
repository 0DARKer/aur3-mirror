# Maintainer: Patrick Mullaney  <pajmullaney@gmail.com>
# Contributor: Techlive Zheng <techlivezheng@gmail.com>

pkgname=google-musicmanager
pkgver=1.0.16.6403
pkgrel=3
pkgdesc="A simple application for adding the music files on your computer to your Music library."
arch=('i686' 'x86_64')
url="http://music.google.com"
license=('custom:musicmanager')
depends=('flac' 'qt' 'fontconfig' 'libogg' 'freetype2' 'libvorbis' 'xdg-utils' 'libidn')
optdepends=('log4cxx')
install=${pkgname}.install

if [ "$CARCH" = "i686" ]; then
    options=(!strip)
    _arch='i386'    
    md5sums=('f2ea0e26a7dba057d452f5493ed54fa3')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='amd64'
    md5sums=('d663344f15d47d11022576556a583bc8')
fi

source=("http://dl.google.com/linux/direct/google-musicmanager-beta_current_${_arch}.deb")

build() {
    #Get binary sources.
    ar -xv google-musicmanager-beta_current_${_arch}.deb || return 1
    tar -xvf data.tar.lzma || return 1

    #This crontab script is for debian only
    rm -r $srcdir/etc

    #Change install path in all files.
    cd $srcdir/opt/google/musicmanager
    find . -type f | xargs sed -i 's:/google/musicmanager:/google-musicmanager:g' || return 1
}
package() {
    install -d "$pkgdir"/{opt,usr/{bin,share/applications}}

    #Install files.
    cd $srcdir/opt/google/musicmanager
    find . -type d -exec install -d {,${pkgdir}/opt/google-musicmanager/}{} \; || return 1
    find . -type f -exec install -D -m 644 {,${pkgdir}/opt/google-musicmanager/}{} \; || return 1

    mv $pkgdir/opt/google-musicmanager/google-musicmanager.desktop $pkgdir/usr/share/applications

    # Installing icons to /usr/share/icons/hicolor/
    rm $pkgdir/opt/google-musicmanager/product_logo_32.xpm
    for i in 16 32 48 128
    do
        install -D -m644 $pkgdir/opt/google-musicmanager/product_logo_${i}.png $pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/google-musicmanager.png
    done

    #Make google-musicmanager executable.
    chmod 755 ${pkgdir}/opt/google-musicmanager/{google-musicmanager,minidump_upload,xdg-settings,xdg-mime,MusicManager} || return 1

    ln -s /opt/google-musicmanager/google-musicmanager $pkgdir/usr/bin/
}
