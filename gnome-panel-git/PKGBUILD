# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=gnome-panel-git
_gitname=gnome-panel
pkgver=3.8.0.r65.g94be70d
pkgrel=4
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="Legacy GNOME panel (development version)"
url="https://wiki.gnome.org/Projects/GnomeFlashback"
depends=('dconf' 'evolution-data-server' 'gconf' 'gnome-desktop' 'gnome-menus' 'librsvg'
         'libwnck3' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'networkmanager' 'gnome-common' 'gtk-doc' 'yelp-tools' 'git')
optdepends=('gnome-screensaver: required for GNOME Flashback session'
            'gnome-session: required for GNOME Flashback session'
            'gnome-settings-daemon-compat: required for GNOME Flashback session'
            'gnome-themes-standard: required for GNOME Flashback session'
            'metacity: required for GNOME Flashback session'
            'notification-daemon: required for GNOME Flashback session'
            'polkit-gnome: required for GNOME Flashback session')
conflicts=('gnome-panel')
provides=('gnome-panel')
install=gnome-panel.install
options=('!libtool')
source=('git://git.gnome.org/gnome-panel'
        session.patch)
sha256sums=('SKIP'
            'cd4c21cf394ea5a702c660bb0d31cae31260bec1cee4e7ca8ee6c8aec2707ebb')

pkgver() {
  cd $srcdir/"$_gitname"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $srcdir/"$_gitname"

  # Add more required components to the session file
  patch -Np1 -i "$srcdir/session.patch"
}

build() {
  cd $srcdir/"$_gitname"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/"$_gitname" \
              --disable-static --disable-schemas-compile \
              PYTHON=/usr/bin/python2 
  make
}

package() {
  cd $srcdir/"$_gitname"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
