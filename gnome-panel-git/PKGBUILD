# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=gnome-panel-git
_gitname=gnome-panel
pkgver=3.8.0.r91.g7f69c41
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="Legacy GNOME panel (development version)"
url="https://wiki.gnome.org/Projects/GnomeFlashback"
depends=('dconf' 'evolution-data-server' 'gconf' 'gnome-desktop' 'gnome-menus' 'librsvg'
         'libwnck3' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'networkmanager' 'gnome-common' 'gtk-doc' 'yelp-tools' 'git')
optdepends=('gnome-applets: extra applets for the GNOME Panel'
            'gnome-screensaver: required for GNOME Flashback session'
            'gnome-session: required for GNOME Flashback session'
            'gnome-settings-daemon-compat: required for GNOME Flashback session'
            'gnome-themes-standard: required for GNOME Flashback session'
            'metacity: required for GNOME Flashback session'
            'nautilus: required for GNOME Flashback session'
            'notification-daemon: required for GNOME Flashback session'
            'polkit-gnome: required for GNOME Flashback session')
conflicts=('gnome-panel')
provides=('gnome-panel')
install=gnome-panel.install
options=('!libtool')
source=('git://git.gnome.org/gnome-panel'
        'session-modification.patch'
        '60_disable-cursor-plugin.gschema.override')
sha256sums=('SKIP'
            '629f3ceee52cbe6759c24ac78a6377c15898d7fccc66781f88095f64a3331959'
            '4187138b5416ef04ae7239749505fe77f0abc1b34dfe42b7fe51002c573adc05')

prepare() {
  cd "$srcdir/$_gitname"

  # Add more required components to the session
  patch -Np1 -i "$srcdir/session-modification.patch"
}

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/"$_gitname" \
              --disable-static --disable-schemas-compile \
              PYTHON=/usr/bin/python2 
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
  install -m 644 $srcdir/60_disable-cursor-plugin.gschema.override $pkgdir/usr/share/glib-2.0/schemas
}

# vim:set ts=2 sw=2 et:
