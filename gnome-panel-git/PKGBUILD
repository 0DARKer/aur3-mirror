# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=gnome-panel-git
_gitname=gnome-panel
pkgver=3.8.1.r10.g313a11d
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="Legacy GNOME panel (development version)"
url="https://wiki.gnome.org/Projects/GnomeFlashback"
depends=('dconf' 'evolution-data-server' 'gconf' 'gnome-desktop' 'gnome-menus' 'librsvg'
         'libwnck3' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'networkmanager' 'gnome-common' 'gtk-doc' 'yelp-tools' 'git')
optdepends=('gnome-applets: extra applets for the GNOME Panel'
            'gnome-flashback-git: required for GNOME Flashback session'
            'gnome-screensaver: required for GNOME Flashback session'
            'gnome-session: required for GNOME Flashback session'
            'gnome-settings-daemon-compat: required for GNOME Flashback session'
            'gnome-themes-standard: required for GNOME Flashback session'
            'metacity: required for GNOME Flashback session'
            'nautilus: required for GNOME Flashback session'
            'notification-daemon: required for GNOME Flashback session'
            'polkit-gnome: required for GNOME Flashback session')
conflicts=('gnome-panel')
provides=('gnome-panel=3.8.1')
install=gnome-panel.install
options=('!libtool')
source=(git://git.gnome.org/gnome-panel
        session-modification.patch
        add-compiz-session.patch
        sync-calls.patch)
sha256sums=('SKIP'
            '7b3889cd597c2187ac66bd045096c8dadbda4ecedc1be53f4ebef09a8eb42199'
            '1353d5e636291f6db0f539c109d7ff664a3670edb56c134745455b70ea46a076'
            '13768d9d6b74d98c7421c69633327a3a8281047552c277583de3e7db1a0164fa')

prepare() {
  cd "$srcdir/$_gitname"

  # Add more required components to the session
  patch -Np1 -i "$srcdir/session-modification.patch"

  # Ensure that the gnome-flashback-compiz.session file gets installed
  patch -Np1 -i "$srcdir/add-compiz-session.patch"

  # Don't make sync calls for Logout and Shutdown
  patch -Np1 -i "$srcdir/sync-calls.patch"
}

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/"$_gitname" \
              --disable-static --disable-schemas-compile \
              PYTHON=/usr/bin/python2 
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}

