# Contributir: Adam Hani Schakaki <adam@schakaki.net>
# Maintainer: Ner0

pkgname=muffin-wm
pkgver=1.0.3
pkgrel=2
pkgdesc="Cinnamon window manager based on Mutter"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gconf' 'gobject-introspection' 'libcanberra' 'libxxf86vm' 'startup-notification' 'zenity')
makedepends=('intltool' 'gnome-doc-utils')
conflicts=('muffin-git')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=("$pkgname.tar.gz::$url/tarball/$pkgver"
        '0001-frame-initialize-style-contexts.patch')
md5sums=('6d05a24e117e602a5803c90217ec1790'
         '37280be36433e634776cf93b8b80854d')

build() {
  cd linuxmint-muffin-*

  # https://bugzilla.gnome.org/show_bug.cgi?id=671796
  patch -Np0 -i "$srcdir/0001-frame-initialize-style-contexts.patch"

  CFLAGS="$CFLAGS -Wno-deprecated-declarations"
  autoreconf -vfi
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --libexecdir=/usr/lib/muffin \
              --enable-compile-warnings=yes --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir"/linuxmint-muffin-*
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

  install -dm755 "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/muffin.schemas" --domain muffin \
    "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
