# Maintainer: Adam Hani Schakaki <adam@schakaki.net>
# Maintainer: Ner0

pkgname=muffin-wm
pkgver=1.0.2
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter (stable version)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('gtk3<=3.2.9' 'clutter' 'gconf' 'gobject-introspection' 'libcanberra' 'startup-notification' 'zenity' 'libxxf86vm')
makedepends=('intltool' 'gnome-doc-utils')
conflicts=('muffin-git')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=($pkgname.tar.gz::$url/tarball/$pkgver)
md5sums=('7da13590ceb8677209c15d1f2d9e024f')

build() {
  cd linuxmint-muffin-*

  # Fix build with libx11 >= 1.4.99.1
  CFLAGS="${CFLAGS} -Wno-error=deprecated-declarations"

  autoreconf -vfi
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib/muffin \
              --localstatedir=/var \
              --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir"/linuxmint-muffin-*
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

  install -dm755 "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/muffin.schemas" --domain muffin "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
