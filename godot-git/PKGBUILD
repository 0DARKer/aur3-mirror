# Maintainer: Matthew Bentley <matthew@mtbentley.us>

_pkgname=godot
pkgname=${_pkgname}-git
pkgver=1.0.3917."beta1"
pkgrel=1
pkgdesc="Godot Game Engine."
url="http://www.godotengine.org"
license=('MIT')
arch=('i686' 'x86_64')
makedepends=('git' 'scons')
depends=('glu' 'libxcursor' 'alsa-lib' 'freetype2' 'gcc-libs-multilib' 'mesa')
optdepends=()
source=("git://github.com/okamstudio/${_pkgname}.git#branch=master")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"

    _major=$(cat version.py|grep "major" | sed 's/major=//')
    _minor=$(cat version.py|grep "minor" | sed 's/minor=//')
    _revision=$(cat version.py|grep revision|sed 's/revision="$Rev: //' | sed 's/ $"//')
    _status=$(cat version.py|grep "status" | sed 's/status=//')
    _git=$(git describe --always)
    echo "${_major}.${_minor}.${_revision}.${_status}"
}

build() {
    cd "${srcdir}"/${_pkgname}

    sed -n '/\/* Copyright/,/IN THE SOFTWARE./p' main/main.cpp | sed 's/\/\*//' | sed 's/\*\///' > LICENSE

    scons bin/godot
}

package() {
    cd "${srcdir}"/${_pkgname}

    install -D -m755 bin/godot "${pkgdir}"/usr/bin/godot
    install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/godot-git/LICENSE
}
