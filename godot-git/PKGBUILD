# Maintainer: Matthew Bentley <matthew@mtbentley.us>

_pkgname=godot
pkgname=${_pkgname}-git
pkgver=1.0.3917_beta1_b2ce682
pkgrel=1
pkgdesc="Godot Game Engine."
url="http://www.godotengine.org"
license=('MIT')
arch=('i686' 'x86_64')
makedepends=('git' 'scons')
depends=('glu' 'libxcursor' 'alsa-lib' 'freetype2' 'mesa')
optdepends=()
source=("git://github.com/okamstudio/${_pkgname}.git#branch=master")
sha256sums=('SKIP')


pkgver() {
    cd "${srcdir}/${_pkgname}"
#    pkgrel=4
    _major=$(cat version.py|grep "major" | sed 's/major=//')
    _minor=$(cat version.py|grep "minor" | sed 's/minor=//')
    _revision=$(cat version.py|grep revision|sed 's/revision="$Rev: //' | sed 's/ $"//')
    _status=$(cat version.py|grep "status" | sed 's/status=//' | sed 's/"//g')
    _git=$(git describe --always)
    echo "${_major}.${_minor}.${_revision}_${_status}_${_git}"
}

build() {
    cd "${srcdir}"/${_pkgname}

    sed -n '/\/* Copyright/,/IN THE SOFTWARE./p' main/main.cpp | sed 's/\/\*//' | sed 's/\*\///' > LICENSE

    scons bin/godot 
}

package() {
    cd "${srcdir}"/${_pkgname}

    install -D -m755 bin/godot "${pkgdir}"/usr/bin/godot
    install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/godot-git/LICENSE
}
