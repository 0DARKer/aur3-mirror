# Maintainer: Martin Wimpress <code@flexion.org>

_pkgname=yuyo-gtk-theme
pkgname=${_pkgname}-git
pkgver=60.e4e0b65
pkgrel=1
pkgdesc="The Official Ubuntu MATE GTK theme (forked from Moka Project's Orchis GTK theme) "
arch=('any')
url="https://github.com/snwh/yuyo-gtk-theme"
license=('GPL3')
depends=('gnome-themes-standard' 'gtk-engine-murrine')
makedepends=('git')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
replaces=("${_pkgname}")
options=(!strip)
source=(${_pkgname}::"git+https://github.com/snwh/${_pkgname}.git")
sha256sums=('SKIP')

pkgver() {
    cd ${srcdir}/${_pkgname}
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    cd ${srcdir}/${_pkgname}
    rm {Yuyo,Yuyo-Dark}/metacity-1/metacity-theme-1.xml
    cd ${srcdir}/${_pkgname}/Yuyo/metacity-1
    ln -s metacity-theme-2.xml metacity-theme-1.xml
    cd ${srcdir}/${_pkgname}/Yuyo-Dark/metacity-1
    ln -s metacity-theme-2.xml metacity-theme-1.xml
}

package() {
    install -d -m 755 "${pkgdir}"/usr/share/themes/{Yuyo,Yuyo-Dark}
    cp -dr --no-preserve=ownership "${_pkgname}"/{Yuyo,Yuyo-Dark} "${pkgdir}"/usr/share/themes/
    find "${pkgdir}"/usr/share/themes/ -type d -exec chmod 755 {} \;
    find "${pkgdir}"/usr/share/themes/ -type f -exec chmod 644 {} \;
}
