# Maintainer: Zohar Malamant <dotfloat gmail com>
pkgname=doom64ex-svn
_svnmod=doom64ex
pkgver=1228  
pkgrel=1
pkgdesc="A port of Doom64"
arch=('i686' 'x86_64')
license=('GPL')
url='http://doom64ex.sourceforge.net/'
depends=('sdl_net' 'glu' 'fluidsynth' 'libpng')
makedepends=('subversion')
install='doom64ex-svn.install'
source=("$_svnmod::svn+https://doom64ex.svn.sourceforge.net/svnroot/doom64ex/trunk" 'doom64ex.patch')
md5sums=('SKIP' 'f5b0e064293e5ce3ee34b9ecbb310055')

pkgver() {
  cd ${_svnmod}
  svnversion | tr -d [A-z]
}

build() {
  patch "$srcdir/$_svnmod/src/kex/CMakeLists.txt" < "$srcdir/doom64ex.patch"
  sed -i 's/png_sizeof/sizeof/' "$srcdir/$_svnmod/src/wadgen/Png.c"

  # Build Kex engine (the actual game)
  mkdir "$srcdir/$_svnmod/build"
  cd "$srcdir/$_svnmod/build"
  cmake "../src/kex" -DCMAKE_C_FLAGS:String=-lm
  make

  # Build WadGen
  mkdir "$srcdir/$_svnmod/wadgen"
  cd "$srcdir/$_svnmod/wadgen"
  cmake "../src/wadgen" -DCMAKE_C_FLAGS:String=-lm
  make
}

package() {
  # Install Kex engine
  install -D -m755 "$srcdir/$_svnmod/build/doom64ex" "$pkgdir/usr/bin/doom64ex"
  install -d "$pkgdir/usr/share/man/man6"
  gzip -9c "$srcdir/$_svnmod/linux/doom64ex.6" > "$pkgdir/usr/share/man/man6/doom64ex.6.gz"
  install -D -m755 "$srcdir/$_svnmod/linux/doom64ex.desktop" "$pkgdir/usr/share/applications/doom64ex.desktop"
  install -D -m755 "$srcdir/$_svnmod/linux/doom64ex.png" "$pkgdir/usr/share/pixmaps/doom64ex.png"

  # Install WadGen
  install -D -m755 "$srcdir/$_svnmod/wadgen/WadGen" "$pkgdir/usr/bin/doom64ex-wadgen"
  install -d "$pkgdir/usr/share/games/doom64/Content"
  cp -rf "$srcdir/$_svnmod/src/wadgen/Content/" "$pkgdir/usr/share/games/doom64/"
#  Copy DOCS
#  install -d "$pkgdir/usr/share/doc/doom64ex"
#  cp -rf "$srcdir/$_svnmod-build/src/kex/DOCS/" "$pkgdir/usr/share/doc/doom64ex/"
}

# vim:set ts=2 sw=2 et:
