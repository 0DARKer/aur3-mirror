# Maintainer: Zohar Malamant <dotfloat gmail com>
pkgname=doom64ex-svn
pkgver=1228
pkgrel=1
pkgdesc="A port of Doom64"
arch=('i686' 'x86_64')
license=('GPL')
url='http://doom64ex.sourceforge.net/'
depends=('sdl_net' 'glu' 'fluidsynth' 'libpng')
makedepends=('subversion')
install='doom64ex-svn.install'
source=('doom64ex.patch')
md5sums=('f5b0e064293e5ce3ee34b9ecbb310055')

_svntrunk=https://doom64ex.svn.sourceforge.net/svnroot/doom64ex/trunk
_svnmod=doom64ex

build() {

  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD HERE
  #

  patch "$srcdir/$_svnmod-build/src/kex/CMakeLists.txt" < "$srcdir/doom64ex.patch"

  # Build Kex engine (the actual game)
  mkdir "$srcdir/$_svnmod-build/build"
  cd "$srcdir/$_svnmod-build/build"
  cmake "../src/kex" -DCMAKE_C_FLAGS:String=-lm
  make

  # Build WadGen
  mkdir "$srcdir/$_svnmod-build/wadgen"
  cd "$srcdir/$_svnmod-build/wadgen"
  cmake "../src/wadgen" -DCMAKE_C_FLAGS:String=-lm
  make
}

package() {
  # Install Kex engine
  install -D -m755 "$srcdir/$_svnmod-build/build/doom64ex" "$pkgdir/usr/bin/doom64ex"
  install -d "$pkgdir/usr/share/man/man6"
  gzip -9c "$srcdir/$_svnmod-build/linux/doom64ex.6" > "$pkgdir/usr/share/man/man6/doom64ex.6.gz"
  install -D -m755 "$srcdir/$_svnmod-build/linux/doom64ex.desktop" "$pkgdir/usr/share/applications/doom64ex.desktop"
  install -D -m755 "$srcdir/$_svnmod-build/linux/doom64ex.png" "$pkgdir/usr/share/pixmaps/doom64ex.png"

  # Install WadGen
  install -D -m755 "$srcdir/$_svnmod-build/wadgen/WadGen" "$pkgdir/usr/bin/doom64ex-wadgen"
  install -d "$pkgdir/usr/share/games/doom64/Content"
  cp -rf "$srcdir/$_svnmod-build/src/wadgen/Content/" "$pkgdir/usr/share/games/doom64/"
#  Copy DOCS
#  install -d "$pkgdir/usr/share/doc/doom64ex"
#  cp -rf "$srcdir/$_svnmod-build/src/kex/DOCS/" "$pkgdir/usr/share/doc/doom64ex/"
}

# vim:set ts=2 sw=2 et:
