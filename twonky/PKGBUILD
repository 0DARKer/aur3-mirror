# Maintainer: Michael Goehler <somebody.here@gmx.de>
# Contributor: picard <picard@fr32k.de>

pkgname=twonky
pkgver=8.0.2
pkgrel=2
pkgdesc="A DLNA-compliant UPnP AV server software from PacketVideo."
arch=('i686' 'x86_64')
url="http://twonky.com/"
license=('custom')
depends=('imagemagick')
optdepends=('ffmpeg: video transcoding'
            'mencoder: video transcoding')
backup=('etc/twonky.ini')
install="${pkgname}.install"
options=('!strip')

if [[ ${CARCH} == 'x86_64' ]]; then
    _pkg="twonky-x86-64-glibc-2.9-${pkgver}.zip"
    noextract=("${_pkg}")
    source=("http://www.twonkyforum.com/downloads/${pkgver}/${_pkg}"
            "twonky.service"
            "twonky.ini")
    md5sums=('8d379b58b09db68d0ff70105ccf71475'
             '4c89bcaef67cc11f02dca29aea10de1e'
             '4a9fbabec48edd2d0b5f72c2ee763f49')
elif [[ ${CARCH} == 'i686' ]]; then
    _pkg="twonky-i686-glibc-2.9-${pkgver}.zip"
    noextract=("${_pkg}")
    source=("http://www.twonkyforum.com/downloads/${pkgver}/${_pkg}"
            "twonky.service"
            "twonky.ini")
    md5sums=('67ee7af8ed177e5fabdc89ff3bc3772a'
             '4c89bcaef67cc11f02dca29aea10de1e'
             '4a9fbabec48edd2d0b5f72c2ee763f49')
fi

package() {

    # twonky base install
    mkdir -p "${pkgdir}/usr/lib/twonky"
    unzip -q -d "${pkgdir}/usr/lib/twonky" "${srcdir}/${_pkg}"

    # twonky config file (including defaults for appdata dir)
    mkdir -p "${pkgdir}/etc"
    cp "${srcdir}/twonky.ini" "${pkgdir}/etc/twonky.ini"

    # twonky appdata dir (as defined in twonky.ini)
    mkdir -p "${pkgdir}/var/twonky/shared/"{music,pictures,videos}
    mkdir -p "${pkgdir}/var/twonky/managed/"{music,pictures,videos}

    # systemd unit file
    mkdir -p "${pkgdir}/usr/lib/systemd/system"
    cp "${srcdir}/twonky.service" "${pkgdir}/usr/lib/systemd/system/twonky.service"
}

