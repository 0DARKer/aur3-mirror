# Contributor: Helmut Henig <Helmut.Henig@gmx.at>
pkgname=aften
pkgver=0.0.8
pkgrel=2
pkgdesc="Audio encoder which generates compressed audio streams based on ATSC A/52 specification."
arch=('i686' 'x86_64')
url="http://aften.sourceforge.net/"
license=('GPL')
source=("http://downloads.sourceforge.net/aften/$pkgname-$pkgver.tar.bz2")
md5sums=('fde67146879febb81af3d95a62df8840')
makedepends=('gcc' 'cmake')

build() {
  DEFAULT="${srcdir}/${pkgname}-${pkgver}/default"
  mkdir -p "${DEFAULT}"
  cd "${DEFAULT}"
  # generate make files
  cmake .. -DCMAKE_INSTALL_PREFIX:STRING="/usr" -DSHARED=true
  # make package
  make 
}

package() {
  DEFAULT="${srcdir}/${pkgname}-${pkgver}/default"
  cd "${DEFAULT}"
  # install package
  make install DESTDIR=${pkgdir}
  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
