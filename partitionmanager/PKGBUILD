# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=partitionmanager
pkgver=1.0.3
pkgrel=2
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/partitionman/"
license=('GPL2')
depends=('kdebase-runtime' 'parted')
makedepends=('cmake' 'automoc4' 'pkgconfig')
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfsprogs: NTFS support'
	    'dosfstools: FAT32 support')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/partitionman/$pkgname-$pkgver.tar.bz2)
md5sums=('1f987d314c717ba2579c69eeef16336d')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
