# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=partitionmanager
pkgver=1.0.3
pkgrel=2
pkgdesc="A KDE utility that allows you to manage disks, partitions, and file systems"
arch=('i686' 'x86_64')
url="http://kde.org/applications/system/kdepartitionmanager/"
license=('GPL2')
depends=('kdebase-runtime' 'parted<3')
makedepends=('cmake' 'automoc4' 'pkgconfig')
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfsprogs: NTFS support'
	    'dosfstools: FAT32 support')
install=${pkgname}.install
source=(http://kde-apps.org/CONTENT/content-files/89595-$pkgname-$pkgver.tar.gz)
md5sums=('6e84d5bb8653d8899b145b36927ced6b')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
