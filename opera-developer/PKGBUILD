# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=opera-developer
pkgver=25.0.1614.5
pkgrel=1
pkgdesc='A fast and secure web browser and Internet suite - developer stream'
arch=('x86_64')
url='http://www.opera.com/browser/'
license=('custom:opera')
depends=('alsa-lib' 'nss' 'gtk2' 'gconf' 'libxss' 'libxtst' 'desktop-file-utils')
install=opera.install
options=(!strip)

if [[ "${CARCH}" == "i686" ]]; then
	# no i686 at this time!
	_arch="i386"
	source=()
	sha256sums=()
elif [[ "${CARCH}" == "x86_64" ]]; then
	_arch="${CARCH}"
	source=("http://get.geo.opera.com/pub/opera-developer/${pkgver}/linux/opera-developer_${pkgver}_amd64.deb")
	# alternative download
	#source=("ftp://ftp.opera.com/pub/opera-developer/${pkgver}/linux/opera-developer_${pkgver}_amd64.deb")
	sha256sums=('b58a7ec721a2356ffa867dfaa92fc3e6fc6dc6f56b73321bec12366fc47f09ed')
fi

prepare() {
	cd ${srcdir}/

	# extract the tarball
	tar xJf data.tar.xz

	# fix opera to use libudev.so.1
	sed -i -e 's/libudev.so.0/libudev.so.1/g' usr/lib/x86_64-linux-gnu/opera-developer/opera-developer
}

package() {
	cd ${srcdir}/

	# copy to pkgdir
	cp -a usr/ ${pkgdir}/

	# set suid bit for Opera sandbox
	chmod 4755 ${pkgdir}/usr/lib/x86_64-linux-gnu/opera-developer/opera_sandbox
}

