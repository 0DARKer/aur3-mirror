# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=opera-developer
pkgver=24.0.1555.0
pkgrel=1
pkgdesc='A fast and secure web browser and Internet suite - developer stream'
arch=('x86_64')
url='http://www.opera.com/browser/'
license=('custom:opera')
depends=('alsa-lib' 'nss' 'gtk2' 'gconf' 'libxss' 'libxtst' 'desktop-file-utils')
install=opera.install
options=(!strip)

if [[ $CARCH == i686 ]]; then
	# no i686 at this time!
	_arch=i386
	source=()
	sha256sums=()
elif [[ $CARCH == x86_64 ]]; then
	_arch=$CARCH
	source=("ftp://ftp.opera.com/pub/opera-developer/${pkgver}/linux/opera-developer_${pkgver}_amd64.deb")
	sha256sums=('6146671560747d739da7f609c0c1d49a7bcba636003895455c6da9f1da3f244a')
fi

prepare() {
	cd ${srcdir}/

	# extract the tarball
	tar xJf data.tar.xz

	# fix opera to use libudev.so.1
	sed -i -e 's/libudev.so.0/libudev.so.1/g' usr/lib/x86_64-linux-gnu/opera-developer/opera
}

package() {
	cd ${srcdir}/

	# copy to pkgdir
	cp -a usr/ ${pkgdir}/

	# set suid bit for Opera sandbox
	chmod 4755 ${pkgdir}/usr/lib/x86_64-linux-gnu/opera-developer/opera_sandbox
}
