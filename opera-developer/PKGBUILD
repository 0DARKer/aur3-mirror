# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=opera-developer
pkgver=24.0.1537.0
pkgrel=7
pkgdesc='A fast and secure web browser and Internet suite - developer stream'
arch=('x86_64')
url='http://www.opera.com/browser/'
license=('custom:opera')
depends=('alsa-lib' 'nss' 'gtk2' 'gconf' 'libxss' 'libxtst' 'desktop-file-utils')
#provides=('opera')
conflicts=('opera>24')
install=opera.install
options=(!strip)

if [[ $CARCH == i686 ]]; then
	# no i686 at this time!
	_arch=i386
	source=()
	sha256sums=()
elif [[ $CARCH == x86_64 ]]; then
	_arch=$CARCH
	source=("ftp://ftp.opera.com/pub/opera-developer/${pkgver}/linux/opera-developer_${pkgver}_amd64.deb")
	sha256sums=('78d9d52683f9ec20e0b6a930ffd0c282beda5425ae157a0d0c8ad87a53b45e3e')
fi

prepare() {
	cd ${srcdir}/

	# extract the tarball
	tar xJf data.tar.xz

	# fix opera to use libudev.so.1
	sed -i -e 's/libudev.so.0/libudev.so.1/g' usr/lib/x86_64-linux-gnu/opera-developer/opera
}

package() {
	cd ${srcdir}/

	# copy to pkgdir
	cp -a usr/ ${pkgdir}/

	# set suid bit for Opera sandbox
	chmod 4755 ${pkgdir}/usr/lib/x86_64-linux-gnu/opera-developer/opera_sandbox
}
