# $Id: PKGBUILD 40769 2011-02-26 18:40:13Z mherych $
# Maintainer: mar77i <mysatyre at gmail dot com>
# Maintainer: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

pkgname=lmms-vst
_pkgbasename="${pkgname%-vst}"
pkgver=0.4.13
pkgrel=2
pkgdesc="The Linux MultiMedia Studio, with VeSTige support enabled"
arch=('i686' 'x86_64')
url="http://lmms.sourceforge.net/"
license=('GPL')
depends=('sdl' 'glib2' 'fluidsynth' 'libpng' 'libvorbis' 'libxft' 'libxinerama' 'qt' 'sdl_sound' 'libsamplerate' 'shared-mime-info')
optdepends=('wine: VeSTige support'
            'fftw: SpectrumAnalyzer plugin'
            'stk: STK instruments plugins')
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2')
provides=('lmms-extras' 'lmms')
conflicts=('lmms-extras' 'lmms')
options=('!libtool' '!makeflags')
install=lmms.install
source=("http://downloads.sourceforge.net/sourceforge/lmms/${_pkgbasename}-${pkgver}.tar.bz2" zynaddsubfx_unlink.patch)
md5sums=('80db0dc5263041d443f474220410991f'
         'd2ea78ac0cf2979505f140c6845617cf')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  patch -Np1 <../zynaddsubfx_unlink.patch
  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -USE=vst
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="${pkgdir}" install
}

