# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Biginoz < biginoz AT free point fr>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>
# Contributor: Matthias Sobczyk <matthias.sobczyk@googlemail.com>
# Contributor: Kamil Kaminski <kyle@kkaminsk.com>
pkgname=minidlna-transcode-hg
pkgver=63.2da8df6862ba
pkgrel=1
pkgdesc="A DLNA/UPnP-AV Media server with transcode support"
arch=('i686' 'x86_64')
url="https://bitbucket.org/stativ/minidlna-transcode"
license=('GPL2')
depends=('libexif' 'libid3tag' 'ffmpeg' 'imagemagick' 'sqlite3')
makedepends=('mercurial')
conflicts=('minidlna')
provides=('minidlna')
backup=('etc/minidlna.conf'
        'etc/conf.d/minidlna')
source=('minidlna-transcode::hg+https://bitbucket.org/stativ/minidlna-transcode' \
        minidlna.rc tmpfiles.conf minidlna.service)
md5sums=('SKIP'
         '484bb94e886616afcb3aba00926aeee9'
         'af1b8c0fef9902f797b0324c255da5a1'
         '761c32ffe202f0c487eff136fa5c2be9')

pkgver() {
  cd "$srcdir/minidlna-transcode"
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd "$srcdir/minidlna-transcode"

  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/minidlna-transcode"

  make DESTDIR="$pkgdir" install
  install -Dm644 minidlna.conf "$pkgdir"/etc/minidlna.conf

  # install transcode scripts and use them in config
  install -D -m755 transcodescripts/transcode_audio "$pkgdir"/usr/bin/minidlna-transcode_audio
  install -D -m755 transcodescripts/transcode_video "$pkgdir"/usr/bin/minidlna-transcode_video
  install -D -m755 transcodescripts/transcode_image "$pkgdir"/usr/bin/minidlna-transcode_image
  sed -i 's|transcode_audio_transcoder=.*|transcode_audio_transcoder=/usr/bin/minidlna-transcode_audio|' "$pkgdir"/etc/minidlna.conf
  sed -i 's|transcode_video_transcoder=.*|transcode_video_transcoder=/usr/bin/minidlna-transcode_video|' "$pkgdir"/etc/minidlna.conf
  sed -i 's|transcode_image_transcoder=.*|transcode_image_transcoder=/usr/bin/minidlna-transcode_image|' "$pkgdir"/etc/minidlna.conf

  # install startup script
  install -Dm755 "$srcdir"/minidlna.rc "$pkgdir"/etc/rc.d/minidlna
  mkdir -p "$pkgdir"/etc/conf.d
  echo 'MINIDLNA_USER=nobody' > "$pkgdir"/etc/conf.d/minidlna
  echo 'MINIDLNA_OPTS=""' >> "$pkgdir"/etc/conf.d/minidlna
  
  # for systemd-tmpfiles
  install -Dm0644 "$srcdir"/tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/minidlna.conf
  # systemd service file
  install -Dm0644 "$srcdir"/minidlna.service "$pkgdir"/usr/lib/systemd/system/minidlna.service
}
