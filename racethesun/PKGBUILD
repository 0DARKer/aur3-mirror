# Maintainer: Philip 'Chais' Abernethy<chais.z3r0@gmail.com>
pkgname=racethesun
pkgver=1.10
pkgrel=1
pkgdesc="Hurtle towards the sunset at breakneck speed in a futile race against time."
url="http://flippfly.com/"
arch=('x86_64' 'i686')
license=('custom:commercial')

DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')
source=("hib://RaceTheSun_${pkgver}_LINUX.zip")
sha512sums=('0d30830cd1731d3fc6732d17ca07ca9e72c4a34b469711e4a04b15c5c66f3a9c1dc1546ec8cfcf32477214524811286e7a59934aae61a621ae64f063b41850e2')

# Uncomment for big packages
#PKGEXT=".pkg.tar"

package() {
	install -dm755 "${pkgdir}"/{opt/${pkgname},usr/{bin,share/{applications,pixmaps,licenses/${pkgname}}}}
	install -m755 "${srcdir}/RaceTheSun_${pkgver}_LINUX/RaceTheSun.${CARCH/i686/x86}" "${pkgdir}/opt/${pkgname}/RaceTheSun.${CARCH/i686/x86}"
	install -m644 "${srcdir}/RaceTheSun_${pkgver}_LINUX/EULA.rtf" "${pkgdir}/usr/share/licenses/${pkgname}/EULA.rtf"
	pushd "${srcdir}/RaceTheSun_${pkgver}_LINUX" > /dev/null
	find -type d -exec install -dm755 "${pkgdir}/opt/${pkgname}/{}" \;
	find RaceTheSun_Data -type f -exec install -m644 "{}" "${pkgdir}/opt/${pkgname}/{}" \;
	if [ $CARCH=="x86_64" ]; then
		rm -r "${pkgdir}/opt/${pkgname}/RaceTheSun_Data/Mono/x86"
	else
		rm -r "${pkgdir}/opt/${pkgname}/RaceTheSun_Data/Mono/x86_64"
	fi
	popd > /dev/null
	ln -s /opt/${pkgname}/RaceTheSun_Data/Resources/UnityPlayer.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	ln -s /opt/${pkgname}/RaceTheSun.${CARCH/i686/x86} "${pkgdir}/usr/bin/${pkgname}"
	echo "[Desktop Entry]
Type=Application
Version=${pkgver}
Name=Race The Sun
GenericName=Race The Sun
Comment=${pkgdesc}
Icon=${pkgname}
Categories=Game;
Exec=/usr/bin/${pkgname}
Path=/opt/${pkgname}" > "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
