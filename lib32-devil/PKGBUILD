_pkgbasename=devil
pkgname=lib32-$_pkgbasename
pkgver=1.7.8
pkgrel=3
pkgdesc="Library for reading several different image formats (32 bit, library only)"
arch=('x86_64')
url="http://openil.sourceforge.net/"
depends=('lib32-libpng' 'lib32-sdl' 'lib32-zlib' 'lib32-libjpeg' 'lib32-libtiff'
		 'lib32-libmng>=1.0.10-3' 'lib32-lcms>=1.18-3' 'lib32-freeglut' 'lib32-jasper>=1.900.1'
		 'lib32-openexr' 'lib32-allegro>=4.4.0.1')
makedepends=('bash' 'gcc-multilib')
options=('!libtool' '!docs')
license=('LGPL')
source=(http://downloads.sourceforge.net/openil/DevIL-$pkgver.tar.gz
		libpng14.patch)
md5sums=('7918f215524589435e5ec2e8736d5e1d'
         '0f839ccefd43b0ee8b4b3f99806147fc')
         
build() {
   cd ${srcdir}/${_pkgbasename}-${pkgver}
   patch -Np1 -i ${srcdir}/libpng14.patch
   ./configure --prefix=/usr --enable-ILU --enable-ILUT --enable-opengl \
   				--enable-sdl --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
   make
}

package() {
   cd ${srcdir}/${_pkgbasename}-${pkgver}   
   make prefix=${pkgdir}/usr libdir=${pkgdir}/usr/lib32 install
   rm -rf ${pkgdir}/usr/{bin,include,share}
}
