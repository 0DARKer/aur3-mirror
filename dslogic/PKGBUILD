# Maintainer: Thomas Krug <t.krug@elektronenpumpe.de>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>

pkgname=dslogic
pkgver=0.3
pkgrel=1
pkgdesc="A Qt based logic analyzer GUI for the DreamSourceLab logic analyzer (based on pulseview)"
arch=('i686' 'x86_64')
url="http://www.dreamsourcelab.com/"
license=('GPL3')
depends=('boost-libs' 'qt4' 'libsigrok4dslogic')
makedepends=('boost')
source=('http://www.dreamsourcelab.com/download/DSLogic-v0.3.tar.gz'
        'dslogic.desktop'
        'types.patch')
md5sums=('8c11f5b188673b7ac1343a7e5193cedb'
         'de092f197a97096aaf05da997cd55ea9'
         'dc1d5dee48bb305b7ec49704c77bf27a')
install=dslogic.install

prepare() {
  cd "$srcdir/DSLogic-v0.3/DSLogic-gui"

  patch -p1 -i "$srcdir/types.patch"
}

build() {
  cd "$srcdir/DSLogic-v0.3/DSLogic-gui"

  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

package() {
  cd "$srcdir/DSLogic-v0.3/DSLogic-gui"

  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 "$srcdir/dslogic.desktop" "$pkgdir/usr/share/applications/dslogic.desktop"
}

# vim:set ts=2 sw=2 et:
