# Maintainer: Thomas Krug <t.krug@elektronenpumpe.de>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>

pkgname=dslogic
pkgver=0.3
pkgrel=2
pkgdesc="A Qt based logic analyzer GUI for the DreamSourceLab logic analyzer (based on pulseview)"
arch=('i686' 'x86_64')
url="http://www.dreamsourcelab.com/"
license=('GPL3')
depends=('boost-libs' 'qt4' 'libsigrok4dslogic')
makedepends=('boost')
source=('http://www.dreamsourcelab.com/download/DSLogic-v0.3.tar.gz'
        'dslogic.desktop')
md5sums=('bae31204997545cee683e0b59ba815f6'
         'de092f197a97096aaf05da997cd55ea9')
install=dslogic.install

build() {
  cd "$srcdir/DSLogic/DSLogic-gui"

  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

package() {
  cd "$srcdir/DSLogic/DSLogic-gui"

  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 "$srcdir/dslogic.desktop" "$pkgdir/usr/share/applications/dslogic.desktop"
}

# vim:set ts=2 sw=2 et:
