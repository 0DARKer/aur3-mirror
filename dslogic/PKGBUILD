# Maintainer: Thomas Krug <t.krug@elektronenpumpe.de>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>

pkgname=dslogic
pkgver=0.4
pkgrel=2
pkgdesc="A Qt based logic analyzer GUI for the DreamSourceLab logic analyzer (based on pulseview)"
arch=('i686' 'x86_64')
url="http://www.dreamsourcelab.com/"
license=('GPL3')
depends=('boost-libs' 'qt4' 'libsigrok4dslogic' 'libsigrokdecode')
makedepends=('boost')
source=("http://www.dreamsourcelab.com/download/DSLogic-v${pkgver}.tar.gz"
        'dslogic.desktop'
        '10-dslogic.rules')
md5sums=('a90d8d4c0c601d9688ab03a772d80f8b'
         'de092f197a97096aaf05da997cd55ea9'
         '67d525f6af55d33b0b4631e3797dd74e')
install=dslogic.install

build() {
  cd "$srcdir/DSLogic-v0.4/DSLogic-gui"

  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DFORCE_QT4=1 .
  make
}

package() {
  cd "$srcdir/DSLogic-v0.4/DSLogic-gui"

  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm644 "$srcdir/dslogic.desktop" "$pkgdir/usr/share/applications/dslogic.desktop"
  install -Dm644 "$srcdir/10-dslogic.rules" "$pkgdir/usr/lib/udev/rules.d/10-dslogic.rules"
}

# vim:set ts=2 sw=2 et:
