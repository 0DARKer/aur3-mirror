pkgname=hawkey
pkgver=0.5.7
_gittag=${pkgname}-${pkgver}-1
pkgrel=1
pkgdesc="High-level API for the libsolv library"
arch=('i686' 'x86_64')
url="https://github.com/rpm-software-management/${pkgname}"
license=('LGPL2.1')
depends=('libsolv' 'zlib')
makedepends=('cmake' 'python' 'python-sphinx' 'rpm-org')
checkdepends=('check' 'python-nose')
optdepends=('python: for python bindings')
source=("${url}/archive/${_gittag}.tar.gz")
md5sums=('93adf5c058e1ff6d975b414f414ac2ba')

prepare() {
	cd "$pkgname-$_gittag"
	rm -rf build
	mkdir build

	sed -e 's/sphinx-build-3/sphinx-build/' \
	    -i doc/CMakeLists.txt
}

build() {
	cd "$pkgname-$_gittag"
	cd build
	cmake -DCMAKE_BUILD_TYPE='Release'  \
	      -DCMAKE_INSTALL_PREFIX='/usr' \
	      -DCMAKE_SKIP_RPATH='ON'       \
	      -DPYTHON_DESIRED='3'          \
	      ..
	make
	make doc-man
}

# check() function should be defined exists as CMake looks for check
# dependencies
check() {
	cd "$pkgname-$_gittag"
	cd build
	# TODO: Find out why tests fail and fill a bug report upstream if
	#       needed
	#make ARGS="-V" test
}

package() {
	cd "$pkgname-$_gittag"
	cd build
	make DESTDIR="$pkgdir/" install
	if [[ "$CARCH" == "x86_64" ]]; then
		mv "$pkgdir/"usr/lib64/* "$pkgdir/"usr/lib
		rmdir "$pkgdir/"usr/lib64
	fi
}
