# Contributor: Jan Cholasta <grubber@grubber.cz>
# Maintainer: Jan Cholasta <grubber@grubber.cz>

pkgname=gzdoom
pkgver=1.6.00
pkgrel=1
pkgdesc="Doom source port based on ZDoom with an OpenGL renderer."
arch=('i686' 'x86_64')
url="http://www.osnanet.de/c.oelckers/gzdoom/"
license=('custom')
depends=('bzip2' 'gtk2' 'fluidsynth' 'gxmessage' 'sdl' 'mesa')
makedepends=('nasm' 'cmake' 'imagemagick' 'subversion')
optdepends=('doom1-wad: Doom shareware IWAD (game data)'
            'harmony-wad: Harmony IWAD (game data)'
            'heretic1-wad: Heretic shareware IWAD (game data)'
            'hexen1-wad: Hexen 1 Demo IWAD (game data)'
            'strife0-wad: Strife shareware IWAD (game data)'
            'urbanbrawl-wad: Urban Brawl: Action Doom 2 IWAD (game data)')
source=(http://www.fmod.org/index.php/release/version/fmodapi43818linux.tar.gz \
        ${pkgname}-${pkgver}-sharedir.patch \
        ${pkgname}.desktop)
md5sums=('9b1af4ae3352fbe147c72c8430df74cd'
         '92817eb56e332007a1f55ffbcb998dfc'
         '3f5920d839086c9ad04ed1338c3fb546')

_pkg_svntrunk="http://mancubus.net/svn/hosted/gzdoom/tags/${pkgver}/"
_pkg_svnmod="${pkgname}-${pkgver}"

_fmodver=4.38.18
_fmod64=

if [ "$CARCH" = "x86_64" ]; then
  _fmod64=64
  makedepends[0]='yasm'
  source[0]=http://www.fmod.org/index.php/release/version/fmodapi${_fmodver//./}linux${_fmod64}.tar.gz
  md5sums[0]='619f32636aa2c1d9417fc8cdf52cef37'
fi

build() {
  cd "$srcdir"

  if [ -d $_pkg_svnmod/.svn ]; then
    (cd $_pkg_svnmod && svn up)
  else
    svn co $_pkg_svntrunk --config-dir ./ $_pkg_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_pkg_svnmod-build"
  cp -r "$srcdir/$_pkg_svnmod" "$srcdir/$_pkg_svnmod-build"
  cd "$srcdir/$_pkg_svnmod-build"

  #
  # BUILD
  #

  cp ../fmodapi${_fmodver//./}linux${_fmod64}/api/lib/libfmodex${_fmod64}-${_fmodver}.so ../libfmodex-${pkgname}.so

  convert src/win32/icon1.ico'[2]' ../${pkgname}.png

  patch -p1 < ../${pkgname}-${pkgver}-sharedir.patch

  cmake -DFMOD_INCLUDE_DIR=../fmodapi${_fmodver//./}linux${_fmod64}/api/inc -DFMOD_LIBRARY=../libfmodex-${pkgname}.so -DCMAKE_SKIP_RPATH=TRUE .
  make
}

package() {
  cd "$srcdir"/${_pkg_svnmod}-build

  install -m755 -D gzdoom "$pkgdir"/usr/bin/gzdoom
  install -m644 -D gzdoom.pk3 "$pkgdir"/usr/share/games/gzdoom/gzdoom.pk3
  install -m644 -D docs/BUILDLIC.TXT "$pkgdir"/usr/share/licenses/${pkgname}/BUILDLIC.TXT
  install -m644 -D docs/doomlic.txt "$pkgdir"/usr/share/licenses/${pkgname}/doomlic.txt

  install -m755 -D ../libfmodex-${pkgname}.so "$pkgdir"/usr/lib/libfmodex-${pkgname}.so
  install -m644 -D ../${pkgname}.png "$pkgdir"/usr/share/pixmaps/${pkgname}.png
  install -m644 -D ../${pkgname}.desktop "$pkgdir"/usr/share/applications/${pkgname}.desktop
}
