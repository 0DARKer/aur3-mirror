# Maintainer: Jan Cholasta <grubber at grubber cz>

_fmodver=42636
_fmodarch=linux
[ "$CARCH" = x86_64 ] && _fmodarch=linux64

pkgname=gzdoom
pkgver=1.6.00
pkgrel=2
pkgdesc="Doom source port based on ZDoom with an OpenGL renderer."
arch=('i686' 'x86_64')
url="http://www.osnanet.de/c.oelckers/gzdoom/"
license=('custom')
depends=('fluidsynth' 'gxmessage' 'sdl' 'glu')
makedepends=('nasm' 'cmake' 'imagemagick' 'subversion' 'mesa')
optdepends=('blasphemer: Blasphemer (free Heretic) game data'
            'chexquest3-wad: Chex Quest 3 game data'
            'doom1-wad: Doom shareware game data'
            'freedoom: FreeDoom game data'
            'hacx-wad: HacX game data'
            'harmony-wad: Harmony game data'
            'heretic1-wad: Heretic shareware game data'
            'hexen1-wad: Hexen demo game data'
            'strife0-wad: Strife shareware game data'
            'urbanbrawl-wad: Urban Brawl: Action Doom 2 game data')
source=(http://www.fmod.org/index.php/release/version/fmodapi${_fmodver}${_fmodarch}.tar.gz \
        doom-share-dir.patch \
        stack-noexec.patch \
        gl-types.patch \
        gzdoom.desktop)
md5sums=('220a54f330bdf3056d6207a0facf2096'
         'ed6c7d1175146bedac3f5a59d09d8f4b'
         'b83081e982b742010df0afe36b4d49b6'
         '9a98fd2e0cac77c72a79169199193bda'
         '3f5920d839086c9ad04ed1338c3fb546')

if [ "$CARCH" = x86_64 ]; then
  makedepends[0]='binutils'
  md5sums[0]='355cba00a34eb5f7d027da68b452f6d9'
fi

_svntag="http://mancubus.net/svn/hosted/gzdoom/tags/$pkgver/"
_svnmod="$pkgname-$pkgver"

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up)
  else
    svn co $_svntag --config-dir ./ $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  local _fmodlibver=${_fmodver:0:1}.${_fmodver:1:2}.${_fmodver:3:2}
  local _fmodlibname=libfmodex
  [ "$CARCH" = x86_64 ] && _fmodlibname=libfmodex64

  cp "$srcdir/fmodapi${_fmodver}${_fmodarch}/api/lib/$_fmodlibname-$_fmodlibver.so" libfmodex-$pkgname.so

  convert "src/win32/icon1.ico[2]" gzdoom.png

  patch -p1 <"$srcdir/doom-share-dir.patch"
  patch -p1 <"$srcdir/stack-noexec.patch"
  patch -p1 <"$srcdir/gl-types.patch"

  cmake -DFMOD_INCLUDE_DIR="$srcdir/fmodapi${_fmodver}${_fmodarch}/api/inc" -DFMOD_LIBRARY=libfmodex-$pkgname.so -DCMAKE_SKIP_RPATH=TRUE .
  make CFLAGS="$CFLAGS -DSHARE_DIR=/usr/share/games/gzdoom/"
}

package() {
  cd "$srcdir"/$_svnmod-build

  install -Dm755 gzdoom "$pkgdir/usr/bin/gzdoom"
  install -Dm644 gzdoom.pk3 "$pkgdir/usr/share/games/gzdoom/gzdoom.pk3"
  install -Dm644 brightmaps.pk3 "$pkgdir/usr/share/games/gzdoom/brightmaps.pk3"
  install -Dm644 lights.pk3 "$pkgdir/usr/share/games/gzdoom/lights.pk3"
  install -Dm644 docs/BUILDLIC.TXT "$pkgdir/usr/share/licenses/$pkgname/BUILDLIC.TXT"
  install -Dm644 docs/doomlic.txt "$pkgdir/usr/share/licenses/$pkgname/DOOMLIC.TXT"

  install -Dm755 libfmodex-$pkgname.so "$pkgdir/usr/lib/libfmodex-$pkgname.so"
  install -Dm644 gzdoom.png "$pkgdir/usr/share/pixmaps/gzdoom.png"
  install -Dm644 "$srcdir/gzdoom.desktop" "$pkgdir/usr/share/applications/gzdoom.desktop"

  ln -s /usr/share/doom/doom.wad "$pkgdir/usr/share/games/gzdoom/freedoomu.wad"
  ln -s /usr/share/doom/doom2.wad "$pkgdir/usr/share/games/gzdoom/freedoom.wad"
  ln -s /usr/share/doom/hexen.wad "$pkgdir/usr/share/games/gzdoom/hexendemo.wad"
}
