# Maintainer: Thomas Jost <schnouki@schnouki.net>
# Contributor: Carson Reynolds <carson@k2.t.u-tokyo.ac.jp>
#
# WARNING: this is a package for a beta version of the CUDA SDK, which is
# only available to NVIDIA registered developers who signed a NDA. As a
# consequence, this package does NOT include source URLs: you must download
# the corresponding files yourself from https://nvdeveloper.nvidia.com/,
# put them in the same folder as this PKGBUILD, and run makepkg. This
# package will NOT be available in my custom repository neither, for the
# very same reason.

pkgname=cuda-sdk-beta
_pkgname=cuda-sdk
pkgver=4.0.11
pkgrel=1
pkgdesc="NVIDIA's CUDA architecture can be programmed in the only C language environment that unlocks the processing power of GPUs to solve the most complex compute-intensive challenges."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/object/cuda_home.html"
license=('custom')
depends=("cuda-toolkit-beta>=${pkgver}"
         'nvidia>=270.27'
	 'freeglut'
	 'gcc44')
provides=("cuda-sdk=${pkgver}")
conflicts=("cuda-sdk")
source=(gpucomputingsdk_${pkgver}_linux.run
	makefiles.patch
        data-paths.patch)
md5sums=('0bb4c0c01877f1e09721a5fa3601248e'
         '1559520b6bf0a82436af1de7bcbabd5d'
         '141e66bfbfd1120a7fff4e72d98d45ba')

PKGEXT=".pkg.tar"

build() {
  cd "$srcdir"

  # Extract the SDK
  msg2 "Uncompressing the CUDA SDK..."  
  sh gpucomputingsdk_${pkgver}_linux.run --noexec --keep --nox11 >/dev/null
  
  # Inspection hook
  #echo "Inspect srcdir"
  #read 

  # Patch Makefiles. It used to be simple, but CUDALibraries is a mess. Really.
  msg2 "Patching Makefiles..."
  patch -p0 -i "$srcdir/makefiles.patch"

  # Funny stuff: some examples from CUDALibraries don't work when installed in
  # /usr/... because they use wrong hard-coded relative paths. And the
  # "shrFindFile()" function causes all OpenCL examples to fail because it does
  # not look in the correct path.
  msg2 "Patching paths to data files..."
  patch -p0 -i "$srcdir/data-paths.patch"

  # Build all the examples. The little "sed" lines are here to enable parallel
  # builds and to avoid being spammed with a warning message (see
  # http://www.gnu.org/s/hello/manual/make/Error-Messages.html for details)
  msg2 "Building the CUDA libraries examples..."
  cd "$srcdir/pkg/sdk/CUDALibraries"
  sed -i 's/make/$(MAKE)/' Makefile
  make

  msg2 "Building the CUDA C examples..."
  cd "$srcdir/pkg/sdk/C"
  sed -i 's/make/$(MAKE)/' Makefile
  make

  msg2 "Building the OpenCL examples..."
  cd "$srcdir/pkg/sdk/OpenCL"
  sed -i 's/make/$(MAKE)/' Makefile
  make
}

package() {
  # Make directories
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mkdir -p "$pkgdir"/usr/share/doc/${_pkgname}/{C,CUDALibraries,OpenCL}/releaseNotesData
  mkdir -p "$pkgdir"/usr/share/${_pkgname}/{C,CUDALibraries,OpenCL}/bin/data
  
  # Install the binaries
  for d in C CUDALibraries OpenCL; do
    install -m755 "$srcdir"/pkg/sdk/$d/bin/linux/release/* "$pkgdir"/usr/share/${_pkgname}/$d/bin
  done
 
  # Install documentation files
  cp -R "$srcdir"/pkg/sdk/doc/* "$pkgdir"/usr/share/doc/${_pkgname}
  cp -R "$srcdir"/pkg/sdk/C/doc/* "$srcdir"/pkg/sdk/C/Samples.html "$pkgdir"/usr/share/doc/${_pkgname}/C
  cp -R "$srcdir"/pkg/sdk/C/releaseNotesData/* "$pkgdir"/usr/share/doc/${_pkgname}/C/releaseNotesData
  cp -R "$srcdir"/pkg/sdk/CUDALibraries/Samples.html "$pkgdir"/usr/share/doc/${_pkgname}/CUDALibraries
  cp -R "$srcdir"/pkg/sdk/CUDALibraries/releaseNotesData/* "$pkgdir"/usr/share/doc/${_pkgname}/CUDALibraries/releaseNotesData
  cp -R "$srcdir"/pkg/sdk/OpenCL/doc/* "$srcdir"/pkg/sdk/OpenCL/Samples.html "$pkgdir"/usr/share/doc/${_pkgname}/OpenCL
  cp -R "$srcdir"/pkg/sdk/OpenCL/releaseNotesData/* "$pkgdir"/usr/share/doc/${_pkgname}/OpenCL/releaseNotesData

  # Install license information
  ln -s ../../doc/${_pkgname}/C/cudpp_license.txt "$pkgdir"/usr/share/licenses/$pkgname
  ln -s ../../doc/${_pkgname}/C/License.txt "$pkgdir"/usr/share/licenses/$pkgname

  # Copy over source and lib folders
  cp -R "$srcdir"/pkg/sdk/C/{common,lib,src} "$pkgdir"/usr/share/${_pkgname}/C
  cp -R "$srcdir"/pkg/sdk/CUDALibraries/{common,src} "$pkgdir"/usr/share/${_pkgname}/CUDALibraries
  cp -R "$srcdir"/pkg/sdk/OpenCL/{common,src} "$pkgdir"/usr/share/${_pkgname}/OpenCL
  cp -R "$srcdir"/pkg/sdk/shared "$pkgdir"/usr/share/${_pkgname}

  # Remove left-over object and ptx files
  find "$pkgdir"/usr/share/${_pkgname} -depth -type d -name 'obj' -exec rm -rf "{}" \;
  find "$pkgdir"/usr/share/${_pkgname} -type f -name '*.ptx' -exec rm -rf "{}" \;

  # Copy data files for SDK
  for d in C CUDALibraries OpenCL; do
    find "$srcdir"/pkg/sdk/$d/ -path '*/data/*' | \
      xargs -I '{}' cp '{}' "$pkgdir"/usr/share/${_pkgname}/$d/bin/data
  done
}

# NOTE: demos such as bandwidthTest, dxtc and simpleTexture may pass tests 
#       when run as root since the data directory is not writable by default.
# NOTE: when building your own programs with nvcc, use the following flags:
#       --compiler-bindir=/opt/gcc-4.4
