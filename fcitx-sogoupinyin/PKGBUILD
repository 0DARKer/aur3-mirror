# Maintainer:  Jove Yu <yushijun110[AT]gmail.com>
# Contributor: csslayer <wengxt[AT]gmail.com>
#              Felix Yan <felixonmars@gmail.com>

pkgname=fcitx-sogoupinyin
pkgver=1.0.0.0024
pkgrel=2
pkgdesc="Sogou Pinyin for Linux"
arch=('x86_64' 'i686')
url="http://code.google.com/p/fcitx"
license=('custom')
depends=('fcitx' 'opencc' 'libgcrypt15' 'rtmpdump')

curl_ver="curl-7.23.1"

if [ "${CARCH}" = "i686" ]; then
    _LIB_DIR=i386-linux-gnu
    _ARCH=i386
    md5sums=('8e073cbbbc32d13e74f465e41ab9f832'
         '8e23151f569fb54afef093ac0695077d'
         '62b2b974c475bab9f6cf6d5ee83b984a')
else
    _LIB_DIR=x86_64-linux-gnu
    _ARCH=amd64
    md5sums=('902e35ad8039fa6f866d38ff89c97d6c'
         '8e23151f569fb54afef093ac0695077d'
         '62b2b974c475bab9f6cf6d5ee83b984a')

fi

source=("http://download.ime.sogou.com/1400209655/sogou_pinyin_linux_${pkgver}_${_ARCH}.deb"
        "http://curl.haxx.se/download/curl-7.23.1.tar.gz"
        fcitx-qimpanel)

install_libcurl()
{
    #Build libcurl
    cd ${srcdir}
    tar xf ${curl_ver}.tar.gz
    cd ${curl_ver}

    ./configure \
        --prefix=/ \
        --disable-ldap \
        --disable-ldaps \
        --enable-ipv6 \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --without-libidn \
        --with-random=/dev/urandom \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
        --with-ssl \
        --with-nss

    make

    local dst_dir="${pkgdir}/opt/curl-7.23"

    #install libcurl
    cd ${srcdir}/${curl_ver}
    make DESTDIR="$dst_dir" install

}

package(){
    install_libcurl

    cd ${srcdir}
  
    bsdtar -xf data.tar.xz -C "${pkgdir}"
  
    mv "$pkgdir"/usr/lib/{$_LIB_DIR/,}fcitx
    rmdir "$pkgdir/usr/lib/${_LIB_DIR}"
    
    # Workaround curl problem - install an old version and use LD_LIBRARY_PATH to use it
    mv "$pkgdir"/usr/bin/fcitx-qimpanel{,.real}
    install -Dm755 fcitx-qimpanel "$pkgdir/usr/bin/"
    cp "$pkgdir/opt/curl-7.23/lib/libcurl.so.4.2.0" "$pkgdir/usr/share/fcitx-sogoupinyin/libcurl.so.4"
  
    ln -s /usr/lib/libgnutls.so "$pkgdir"/usr/share/fcitx-sogoupinyin/libgnutls.so.26
    ln -s /usr/lib/librtmp.so "$pkgdir"/usr/share/fcitx-sogoupinyin/librtmp.so.0
    ln -s /usr/share/fcitx-sogoupinyin/libcurl.so.4 "$pkgdir"/usr/share/fcitx-sogoupinyin/libcurl-gnutls.so.4
  
    rm -r "$pkgdir"/etc/apt
    rm -r "$pkgdir"/usr/share/keyrings
    rm -r "$pkgdir"/opt/curl-7.23
}


