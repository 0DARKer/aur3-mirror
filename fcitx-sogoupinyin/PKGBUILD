# Maintainer:  Jove Yu <yushijun110[AT]gmail.com>
# Contributor: csslayer <wengxt[AT]gmail.com>
#              Felix Yan <felixonmars@gmail.com>

pkgname=fcitx-sogoupinyin
pkgver=1.0.0.0014
pkgrel=2
pkgdesc="Sogou Pinyin for Linux"
arch=('x86_64' 'i686')
url="http://code.google.com/p/fcitx"
license=('custom')
depends=('fcitx' 'libcurl-gnutls')


if [ "${CARCH}" = "i686" ]; then
    _LIB_DIR=i386-linux-gnu
    _ARCH=i386
    md5sums=('568f710487f1c532d1b95a9cf555dc2e'
         '62b2b974c475bab9f6cf6d5ee83b984a'
         '9a88e363ea255a66be7791952e73542b'
         '02d401684c2d558c8d96fd4390c811c3')
else
    _LIB_DIR=x86_64-linux-gnu
    _ARCH=amd64
    md5sums=('9a57c5f36019faf8c16290d68e2f1ff6'
         '62b2b974c475bab9f6cf6d5ee83b984a'
         '9a88e363ea255a66be7791952e73542b'
         '02d401684c2d558c8d96fd4390c811c3')
fi

source=("http://download.ime.sogou.com/1398087082/sogou_pinyin_linux_${pkgver}_${_ARCH}.deb"
        fcitx-qimpanel
        libcurl.so.4.2.0_7.23.1_x86_64
	libcurl.so.4.2.0_7.23.1_i686)

package(){
  bsdtar -xf data.tar.xz -C "${pkgdir}"

  mv "$pkgdir"/usr/lib/{$_LIB_DIR/,}fcitx
  rmdir "$pkgdir/usr/lib/${_LIB_DIR}"
  
  # Workaround curl problem - install an old version and use LD_LIBRARY_PATH to use it
  mv "$pkgdir"/usr/bin/fcitx-qimpanel{,.real}
  install -Dm755 fcitx-qimpanel "$pkgdir/usr/bin/"
  install -Dm644 libcurl.so.4.2.0_7.23.1_${CARCH} "$pkgdir/usr/share/fcitx-sogoupinyin/libcurl.so.4"

  rm -r "$pkgdir"/etc/apt
  rm -r "$pkgdir"/usr/share/{keyrings,upstart}
}


