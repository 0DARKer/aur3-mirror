# Maintainer: revel <revelΘmuub·net>

pkgname=gopenvpn-git
pkgver=svn.migration.r12.gb4192eb
pkgrel=1
pkgdesc="gopenvpn is a simple graphical front-end for OpenVPN, the open source VPN solution."
arch=('i686' 'x86_64')
url="http://gopenvpn.sourceforge.net/"
license=('GPL')
depends=('libglade' 'libgnome-keyring' 'gtk2' 'openvpn')
makedepends=('git')
source=("$pkgname"::'git://gopenvpn.git.sourceforge.net/gitroot/gopenvpn/gopenvpn.git'
        )
md5sums=('SKIP'
         )

pkgver() {
  cd "$srcdir/$pkgname"
  # Use the tag of the last commit
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  sed -i "/ACLOCAL_AMFLAGS = -I m4/d" Makefile.am
  autoreconf -vi
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
}
