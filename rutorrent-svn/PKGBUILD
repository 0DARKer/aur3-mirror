# Maintainer: skydrome <irc.freenode.net>
# Contributor: smallcms <smallcms at mail dot ru>

pkgname=rutorrent-svn
true && pkgname=('rutorrent-svn' 'rutorrent-plugins-svn')
pkgver=2082
pkgrel=1
pkgdesc="Yet another web front-end for rTorrent"
arch=('any')
url="http://code.google.com/p/rutorrent"
license=('GPL')
makedepends=('subversion')
optdepends=()
source=('plugins.ini')
md5sums=('25a215875d47f7ec2cc0d32f6776031a')
options=(!strip !emptydirs)

_svntrunk="http://rutorrent.googlecode.com/svn/trunk"
_webdir="srv/http"

build() {
    cd "$srcdir"

    msg "Connecting to SVN server..."
    [[ -d "$_svnmod/.svn" ]] &&
        svn up trunk || svn co "$_svntrunk"
    msg "SVN checkout done or server timeout"

    cd trunk
    sed -i rutorrent/conf/config.php \
		-e "s:\$topDirectory .*:\$topDirectory = '/home';:" \
		-e "s:\$XMLRPCMountPoint .*:\$XMLRPCMountPoint = \"/rutorrent/RPC1\";:"

    # Uncomment to use a nicer dark theme
    #sed -i "s:\$defaultTheme .*:\$defaultTheme = \"Oblivion\";:" plugins/theme/conf.php
}

package_rutorrent-svn() {
    depends=('rtorrent' 'php>=5.3.0')
    provides=('rutorrent')
    conflicts=('rutorrent')
    install=rutorrent.install

    cd "$srcdir/trunk"
    install -dm755  "$pkgdir/$_webdir"
    cp -r rutorrent "$pkgdir/$_webdir"
    rm -r $pkgdir/$_webdir/rutorrent/{plugins,conf/plugins.ini}
}

package_rutorrent-plugins-svn() {
    pkgdesc="Plugins for ruTorrent"
    depends=('rutorrent-svn' 'curl')
    optdepends=('mktorrent: to create torrent files with mktorrent'
                'buildtorrent: to create torrent files with buildtorrent'
                'transmission-cli: to create torrent files with transmission'
                'php-geoip: to enable geoip plugin'
                'geoip: enable geoip plugin'
                'unrar: enable unpack plugin'
                'unzip: enable unpack plugin')
    install=rutorrent-plugins.install
    conflicts=('rutorrent-plugins')
    provides=('rutorrent-plugins')
    replaces=('rutorrent-plugins-svn')
    
    cd "$srcdir/trunk"
    install -dm755 "$pkgdir/$_webdir/rutorrent/conf"
    install -Dm755 "$srcdir/plugins.ini" "$pkgdir/$_webdir/rutorrent/conf/plugins.ini"
    cp -r plugins  "$pkgdir/$_webdir/rutorrent"
}

