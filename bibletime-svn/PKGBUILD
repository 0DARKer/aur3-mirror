# Maintainer: Dylon Edwards <deltaecho@archlinux.us>

pkgname=bibletime-svn
pkgver=1544
pkgrel=1

pkgdesc="Qt4 Bible software that uses the Sword Engine."
url="http://bibletime.info"
license=('GPL2')

arch=('i686' 'x86_64')

provides=("bibletime=$pkgver")

#depends=('kdebase' 'kdelibs' 'phonon' 'qt' 'sword' 'clucene' 'perl' 'openssl')
depends=('clucene' 'curl' 'e2fsprogs' 'expat' 'fontconfig' 'freetype2' 'gcc-libs' 'glib2' 'glibc' 'icu' 'libice' 'libpng' 'libsm' 'libx11' 'libxau' 'libxcb' 'libxdmcp' 'libxext' 'libxrender' 'openssl' 'pcre' 'qt' 'sqlite3' 'sword' 'zlib')

makedepends=('cmake' 'gettext' 'svn' 'automoc4' 'boost')

source=()
md5sums=()

_svntrunk='https://bibletime.svn.sourceforge.net/svnroot/bibletime/trunk/bibletime'
_svnmod='bibletime-svn'

build() {
	cd "${srcdir}"

	msg "Connecting to the SVN server..."

	# Determine whether the source needs to be updated or installed
	if [ -d "${_svnmod}/.svn" ]; then
		svn up -r ${pkgver} ${_svnmod}
	else
		svn co ${_svntrunk} -r ${pkgver} ${_svnmod}
	fi

	# Create a build directory
	mkdir -p ${pkgname}-build
	cd ${pkgname}-build

	# Configure the package
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		  -DCMAKE_BUILD_TYPE=Release \
		  ${srcdir}/${_svnmod} || return 1

	# Make the package
	make || return 1

	# Install the package to the fakeroot directory
	make DESTDIR=${pkgdir} install || return 1
}
