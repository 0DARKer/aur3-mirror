# Maintainer: Michael Lass <bevan@bi-co.net>
# Contributor: Szymon Jakubczak <szym-at-mit-dot-edu>

pkgname=openafs
pkgver=1.6.1pre2
pkgrel=1
pkgdesc="An open source client for the AFS distributed file system"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=(krb5)
makedepends=(bison flex 'linux-headers')
optdepends=('fuse: support for FUSE-based Cache Manager')
conflicts=(openafs-features openafs-features-libafs)
backup=(etc/openafs/ThisCell
        etc/openafs/cacheinfo
        etc/openafs/afs.conf
        etc/openafs/afs.conf.client
        etc/openafs/CellServDB)
install=openafs.install
source=(http://openafs.org/dl/openafs/candidate/$pkgver/$pkgname-$pkgver-src.tar.bz2
        openafs.rc)
md5sums=('9715d40c4226d2bc119f139392b2f57b'
         '85fa92bdd773085c850ced56fbb76021')

build() {
  _kernelver=$(uname -r)
  _kernelmajor=$(echo $_kernelver | sed 's/\([0-9]*\.[0-9]*\).*/\1/')
  _extramodules=extramodules-$_kernelmajor-ARCH

  cd $srcdir/$pkgname-$pkgver

  ./regen.sh
  ./configure --prefix=/usr --sysconfdir=/etc

  make
  make DESTDIR=$pkgdir install

  # rename some files that are already present
  #mv $pkgdir/usr/bin/afs_compile_et $pkgdir/usr/bin/compile_et-openafs
  mv $pkgdir/usr/bin/kpasswd $pkgdir/usr/bin/kpasswd-openafs
  #mv $pkgdir/usr/bin/pagsh $pkgdir/usr/bin/pagsh-openafs

  # create some directories
  mkdir -p $pkgdir/lib/modules/$_extramodules
  mkdir -p $pkgdir/etc/rc.d
  mkdir -p $pkgdir/etc/openafs
  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  mkdir -p $pkgdir/var/cache/openafs
  chmod 700 $pkgdir/var/cache/openafs
  chown root:root $pkgdir/var/cache/openafs

  # install kernel module
  cp $pkgdir/usr/lib/openafs/libafs-$_kernelver.mp.ko $pkgdir/lib/modules/$_extramodules/libafs.ko || \
  cp $pkgdir/usr/lib/openafs/libafs-$_kernelver.ko    $pkgdir/lib/modules/$_extramodules/libafs.ko

  # move some PAM libs
  mkdir -p $pkgdir/lib/security
  cp $pkgdir/usr/lib/pam_afs.krb.so.1 $pkgdir/usr/lib/pam_afs.so.1 $pkgdir/lib/security/ || true

  # install init script
  cp $srcdir/openafs.rc $pkgdir/etc/rc.d/$pkgname
  chmod u+x $pkgdir/etc/rc.d/$pkgname

  # install default CellServDB and afs.conf
  cp $srcdir/$pkgname-$pkgver/src/WINNT/install/wix/CellServDB $pkgdir/etc/$pkgname/
  cp $srcdir/$pkgname-$pkgver/src/afsd/afs.conf.linux $pkgdir/etc/$pkgname/afs.conf

  # set some default configs...

  cat >> $pkgdir/etc/$pkgname/afs.conf.client << EOF
AFS_CLIENT=true
AFS_AFSDB=true
AFS_CRYPT=true
AFS_DYNROOT=true
AFS_FAKESTAT=true
EOF

  cat >> $pkgdir/etc/$pkgname/cacheinfo << EOF
/afs:/var/cache/openafs:30000
EOF

  cat >> $pkgdir/etc/$pkgname/ThisCell << EOF
andrew.cmu.edu
EOF

  cp $srcdir/$pkgname-$pkgver/src/LICENSE $pkgdir/usr/share/licenses/$pkgname
}
