# Maintainer: Michael Lass <bevan@bi-co.net>
# Contributor: Szymon Jakubczak <szym-at-mit-dot-edu>

pkgname=openafs
pkgver=1.6.1
pkgrel=10
pkgdesc="An open source client for the AFS distributed file system"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=('krb5')
makedepends=('autoconf' 'automake' 'bison' 'flex' 'gzip' 'linux-headers')
conflicts=('openafs-features' 'openafs-features-libafs')
backup=(etc/conf.d/openafs
	etc/openafs/ThisCell
        etc/openafs/cacheinfo
        etc/openafs/CellServDB)
install=openafs.install
source=(http://openafs.org/dl/openafs/${pkgver}/${pkgname}-${pkgver}-src.tar.bz2
        openafs-client.rc
	openafs-client.service
	openafs-server.service
	fix-glibc-2.16.patch
	fix-linux-3.5.patch
	fix-linux-3.6.patch)
md5sums=('7215609990a2b253f8980fc1e1da0530'
         '2c7ec958f3dd4526c39b1794a450119a'
         '593631d9f7a0bdfed5c92d739d59374b'
         '4ada76a7cdd0c9d5a564bfa5f51d2431'
         'e6814b42b08500fd087db25f777ace1d'
         '04986e5bbeed757cfbb2046189533b11'
         '8adad1661833ca17ca5e0f2ea44a2ead')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Fix compatibility with glibc 2.16
  # Picked from: - http://gerrit.openafs.org/8155
  patch -p1 < ${srcdir}/fix-glibc-2.16.patch

  # Fix compatibility with linux 3.5
  # Picked from: - http://gerrit.openafs.org/7578
  #              - http://gerrit.openafs.org/7579
  patch -p1 < ${srcdir}/fix-linux-3.5.patch

  # Fix compatibility with linux 3.6
  # Picked from: - http://gerrit.openafs.org/8077
  #              - http://gerrit.openafs.org/8078
  #              - http://gerrit.openafs.org/8079
  #              - http://gerrit.openafs.org/7951
  #              - http://gerrit.openafs.org/8080
  #              - http://gerrit.openafs.org/8081
  #              - http://gerrit.openafs.org/8082
  #              - http://gerrit.openafs.org/8083
  #              - http://gerrit.openafs.org/8084
  patch -p1 < ${srcdir}/fix-linux-3.6.patch

  ./regen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --disable-fuse-client
  make
}


package() {
  _kernelver=$(uname -r)
  _extramodules=$(readlink -fn /usr/lib/modules/${_kernelver}/extramodules)

  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # rename kpasswd which is already provided by krb5
  mv ${pkgdir}/usr/bin/kpasswd ${pkgdir}/usr/bin/kpasswd-openafs

  # create cache directory
  install -dm700 ${pkgdir}/var/cache/openafs

  # create afs mount directory
  install -dm755 ${pkgdir}/afs

  # move kernel module
  install -dm755 ${pkgdir}${_extramodules}
  mv ${pkgdir}/usr/lib/openafs/libafs-${_kernelver}.mp.ko ${pkgdir}${_extramodules}/libafs.ko || \
  mv ${pkgdir}/usr/lib/openafs/libafs-${_kernelver}.ko    ${pkgdir}${_extramodules}/libafs.ko
  gzip -9 ${pkgdir}${_extramodules}/libafs.ko

  # move PAM libs
  install -dm755 ${pkgdir}/usr/lib/security
  mv ${pkgdir}/usr/lib/pam_afs.krb.so.1 ${pkgdir}/usr/lib/pam_afs.so.1 ${pkgdir}/usr/lib/security/

  # install init script and systemd service files
  install -Dm755 ${srcdir}/openafs-client.rc ${pkgdir}/etc/rc.d/openafs-client
  install -Dm644 ${srcdir}/openafs-client.service ${pkgdir}/usr/lib/systemd/system/openafs-client.service
  install -Dm644 ${srcdir}/openafs-server.service ${pkgdir}/usr/lib/systemd/system/openafs-server.service

  # install default configs
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/afsd/CellServDB ${pkgdir}/etc/${pkgname}/CellServDB
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/packaging/RedHat/openafs.sysconfig ${pkgdir}/etc/conf.d/openafs
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/packaging/RedHat/openafs-ThisCell ${pkgdir}/etc/${pkgname}/ThisCell
  sed 's#/usr/vice/cache#/var/cache/openafs#' ${srcdir}/${pkgname}-${pkgver}/src/packaging/RedHat/openafs-cacheinfo > ${pkgdir}/etc/${pkgname}/cacheinfo

  # install license
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
