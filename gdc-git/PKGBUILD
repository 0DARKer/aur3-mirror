# Contributor: Jerome Berger <jeberger@free.fr>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>

# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=gdc-git
_gitname=gdc
_gccver=4.9
_gccsnapshot=20130721
pkgver=4.9.20130721.1181.1a573d5
pkgrel=1
pkgdesc="GDC, The D Programming Language (D2) frontend for GCC. GIT master branch compiled with GCC trunk snapshot."
arch=('i686' 'x86_64')
url="https://github.com/D-Programming-GDC/GDC"
license=('GPL')
provides=('gdc')
depends=('perl' 'libmpc' 'cloog')
makedepends=('binutils>=2.22' 'git')
conflicts=('gdc1-bin' 'gdc1-hg')
options=('!libtool' '!emptydirs' '!buildflags')
source=(ftp://gcc.gnu.org/pub/gcc/snapshots/${_gccver}-${_gccsnapshot}/gcc-${_gccver}-${_gccsnapshot}.tar.bz2
        ${_gitname}::git://github.com/D-Programming-GDC/GDC.git)
md5sums=('bdd43b6aba4897440926c4f050d7db36'
         'SKIP')
sha256sums=('b9fab909667b9d79bce3b6c66291b73cca61dd75327031fd1d6865f353f93b43'
            'SKIP')

pkgver()
{
  cd ${srcdir}/${_gitname}
  echo ${_gccver}.${_gccsnapshot}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare()
{
  cd ${srcdir}/gcc-${_gccver}-${_gccsnapshot}

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # Arch Linux installs x86_64 libraries in /lib
  [[ $CARCH == "x86_64" ]] && sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

  cd ${srcdir}/${_gitname}
  ./setup-gcc.sh ../gcc-${_gccver}-${_gccsnapshot}

  mkdir ${srcdir}/gcc-build
}

build()
{
  cd ${srcdir}/gcc-build

  ${srcdir}/gcc-${_gccver}-${_gccsnapshot}/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --with-bugurl=http://gdcproject.org/bugzilla/ \
      --enable-languages=d \
      --disable-multilib --disable-nls --disable-bootstrap \
      --disable-libgomp --disable-libmudflap --disable-libquadmath \
      --disable-shared
  make
}

package()
{
  cd ${srcdir}/gcc-build

  # Easier to just do a full install and remove the excess later
  make DESTDIR=${pkgdir} install

  # Delete all the stuff we don't need
  # rm -rf ${pkgdir}/usr/lib/gcc/${CHOST}/${_gccver}-${_gccsnapshot}/{*.a,*.so*,lto-wrapper,lto1,plugin*}
  # rm -rf ${pkgdir}/usr/lib/gcc/${CHOST}/${_gccver}-${_gccsnapshot}/{crt*,cc1,collect2,include*,install*}
  rm -rf ${pkgdir}/usr/share/info
  rm -rf ${pkgdir}/usr/lib/{libgcc*,libiberty*,libitm*,libstdc++*,libsupc++*,libasan*,libtsan*}
  rm -rf ${pkgdir}/usr/share/man/man1/{cpp,gc,g++}*
  rm -rf ${pkgdir}/usr/share/man/man7/{fsf,gfdl,gpl}*
  rm -rf ${pkgdir}/usr/bin/{gcc,gcov,cpp,${CHOST}-gcc,${CHOST}-gcc-${_gccver}-${_gccsnapshot},c++,g++,${CHOST}-c++,${CHOST}-g++}
  rm -rf ${pkgdir}/usr/bin/{gcc-ar,gcc-nm,gcc-ranlib,${CHOST}-gcc-ar,${CHOST}-gcc-nm,${CHOST}-gcc-ranlib}

  # Fix permissions
  chmod 644 ${pkgdir}/usr/lib/*
  chmod 755 ${pkgdir}/usr/lib/gcc
}
