# Contributor: Jerome Berger <jeberger@free.fr>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>

# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

# See http://gcc.gnu.org/install/prerequisites.html

pkgname=gdc-git
pkgver=20120528
pkgrel=1
_gccver=4.8.0
pkgdesc="GDC, The D Programming Language (D2) frontend for GCC. GIT master branch compiled with GCC trunk (necessary)."
arch=('i686' 'x86_64')
url="http://bitbucket.org/goshawk/gdc/wiki/Home"
license=('GPL')
provides=('gdc')
depends=(perl libmpc)
makedepends=('binutils>=2.22' 'git' 'svn')
conflicts=('gdc' 'gdc-bin' 'gdc1-hg')
options=('!libtool' '!emptydirs')
source=(gdc-gcc-4.8.0.patch)
md5sums=('9eb9f4a0380d2b8f5aa4e3e9962f3831')

_gitroot=https://github.com/D-Programming-GDC/GDC.git
_gitname=gdc
_gitrev=92da3d2472ac6a5d3d8a8eec9ab34c81dc93136a

_svnroot=svn://gcc.gnu.org/svn/gcc/trunk
_svnname="gcc-$_gccver"
_svnrev=184805

_basedir="$srcdir/gcc-$_gccver"

build()
{
  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname"
    # remove the version patch before the pull
    patch -R -Np1 -s -i "$srcdir"/gdc-gcc-4.8.0.patch
    git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname"
    git checkout "$_gitrev"
  fi

  msg "GIT checkout done or server timeout"

  # Fixes the missing 4.8 version check
  # in the setup-gcc.sh script
  patch -Np1 -s -i "$srcdir"/gdc-gcc-4.8.0.patch

  cd "$srcdir"
  msg "Connecting to GCC SVN server..."

  if [[ -d "$_svnname" ]]; then
    cd "$_svnname" && svn update -r "$_svnrev"
    msg "The local files are updated."
  else
    svn checkout "$_svnroot" "$_svnname" -r "$_svnrev"
    cd "$_svnname"
  fi

  msg "GCC SVN checkout done or server timeout"

  msg "Starting build..."

  cd "$_basedir"

  echo "$_gccver" > gcc/BASE-VER

  cd "$srcdir/$_gitname"
  ./update-gcc.sh ../gcc-${_gccver} --setup

  cd "$srcdir"
  if [ ! -d "gcc-build" ]; then
    mkdir gcc-build
  fi
  cd gcc-build

  "$_basedir"/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --with-bugurl=https://github.com/D-Programming-GDC/GDC/issues/ \
      --enable-languages=d --enable-checking=yes \
      --disable-multilib --disable-nls --disable-bootstrap \
      --disable-libgomp --disable-libmudflap --disable-libquadmath \
      --disable-shared
  make -j3 2>&1 | tee build.log
}

package()
{
  cd "$srcdir"/gcc-build

  # Easier to just do a full install and remove the excess later
  make DESTDIR=$pkgdir install

  # Delete all the stuff we don't need
  # rm -rf $pkgdir/usr/lib/gcc/$CHOST/$_gccver/{*.a,*.so*,lto-wrapper,lto1,plugin*}
  # rm -rf $pkgdir/usr/lib/gcc/$CHOST/$_gccver/{crt*,cc1,collect2,include*,install*}
  rm -rf $pkgdir/usr/share/info
  rm -rf $pkgdir/usr/lib/{libgcc*,libiberty*}
  rm -rf $pkgdir/usr/share/man/man1/{cpp,gc}*
  rm -rf $pkgdir/usr/share/man/man7/{fsf,gfdl,gpl}*
  rm -rf $pkgdir/usr/bin/{gcc,gcov,cpp,$CHOST-gcc,$CHOST-gcc-$_gccver}
  rm -rf $pkgdir/usr/bin/{gcc-ar,gcc-nm,gcc-ranlib,$CHOST-gcc-ar,$CHOST-gcc-nm,$CHOST-gcc-ranlib}

  # Fix permissions
  chmod 644 $pkgdir/usr/lib/*
  chmod 755 $pkgdir/usr/lib/gcc
}
