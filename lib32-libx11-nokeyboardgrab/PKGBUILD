pkgname=lib32-libx11-nokeyboardgrab
pkgver=1.6.2
pkgrel=4
pkgdesc='Core X11 protocol client library'
arch=(x86_64)
url=http://www.x.org/wiki/
license=(custom)
depends=(lib32-libxcb libx11)
makedepends=(gcc-multilib inputproto xextproto xtrans)
provides=(lib32-libx11=$pkgver)
conflicts=(lib32-libx11)
source=(ftp://ftp.x.org/pub/individual/lib/libX11-$pkgver.tar.bz2)
sha256sums=(2aa027e837231d2eeea90f3a4afe19948a6eb4c8b2bec0241eba7dbc8106bd16)

_confopts=(
  --disable-specs
  --disable-static
  --libdir=/usr/lib32
  --prefix=/usr
)

prepare() {
  cd libX11-$pkgver

  sed -i '/xGrabKeyboardReply/i return (GrabSuccess);' src/GrKeybd.c

  sed -i s/actute/acute/ nls/en_US.UTF-8/Compose.pre
}

build() {
  cd libX11-$pkgver

  CFLAGS+=' -m32'

  ./configure ${_confopts[@]}

  make
}

check() {
  cd libX11-$pkgver

  make check
}

package() {
  cd libX11-$pkgver

  make DESTDIR=$pkgdir install

  rm -r $pkgdir/usr/{include,share}

  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
