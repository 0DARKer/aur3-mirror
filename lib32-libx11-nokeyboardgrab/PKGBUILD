pkgname=lib32-libx11-nokeyboardgrab
pkgver=1.6.2
pkgrel=2
pkgdesc='Core X11 protocol client library'
arch=(x86_64)
url=http://www.x.org/wiki/
license=(custom)
depends=(lib32-libxcb libx11)
makedepends=(gcc-multilib inputproto xextproto xtrans)
provides=(lib32-libx11=$pkgver)
conflicts=(lib32-libx11)
source=(http://www.x.org/releases/individual/lib/libX11-$pkgver.tar.bz2)
sha256sums=(2aa027e837231d2eeea90f3a4afe19948a6eb4c8b2bec0241eba7dbc8106bd16)

_confopts=(
  --disable-specs
  --disable-static
  --disable-xf86bigfont
  --libdir=/usr/lib32
  --prefix=/usr
)

prepare() {
  cd libX11-$pkgver

  sed -i '/xGrabKeyboardReply/i return (GrabSuccess);' src/GrKeybd.c
}

build() {
  cd libX11-$pkgver

  CFLAGS+=' -m32'

  ./configure ${_confopts[@]}

  make
}

check() {
  cd libX11-$pkgver

  make check
}

package() {
  cd libX11-$pkgver

  make DESTDIR=$pkgdir install

  rm -r $pkgdir/usr/{include,share}

  install -d $pkgdir/usr/share/licenses/$pkgname
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
