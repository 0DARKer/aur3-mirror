# Maintainer: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
pkgver=20140519
pkgrel=1
pkgdesc="A library for handling DWARF Debugging Information Format"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://www.prevanders.net/dwarf.html"
depends=('elfutils')
source=(http://www.prevanders.net/libdwarf-${pkgver}.tar.gz)
md5sums=('4f7331cb8fe7acb6fe1ca4e45d13d55e')

build() {
  cd "$srcdir"/dwarf-$pkgver
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd "$srcdir"/dwarf-$pkgver/libdwarf

  install -d $pkgdir/usr/lib
  install -m 644 libdwarf.a $pkgdir/usr/lib
  install -m 644 libdwarf.so $pkgdir/usr/lib

  install -d $pkgdir/usr/include/libdwarf
  install dwarf.h libdwarf.h $pkgdir/usr/include/libdwarf

  install -dm 755 $pkgdir/usr/share/doc/$pkgname
  install README NEWS *.pdf $pkgdir/usr/share/doc/$pkgname

  # dwarfdump
  cd "$srcdir"/dwarf-$pkgver/dwarfdump2
  install -Dm755 dwarfdump $pkgdir/usr/bin/dwarfdump
  install -Dm644 dwarfdump.1 $pkgdir/usr/share/man/man1/dwarfdump.1
  install -Dm644 dwarfdump.conf $pkgdir/usr/lib/dwarfdump.conf
}
