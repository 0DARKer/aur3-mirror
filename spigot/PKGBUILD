pkgname=spigot
pkgver=1.8.3_R0.1
pkgrel=1
pkgdesc="High performance Minecraft server implementation"
arch=(any)
url="http://www.spigotmc.org/"
license=("LGPL")
depends=(java-runtime-headless tmux sudo)
makedepends=(java-environment git)
conflicts=(bukkit craftbukkit spigot-patcher)
provides=(bukkit "craftbukkit=${pkgver%_*}")
install=spigot.install
source=("https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar"
"spigot.service"
"spigot.sh")
noextract=("BuildTools.jar")
md5sums=('SKIP'
         '55eb03d443e1aa1a013aa01798e056d4'
         '8933d1710304dfdf99632f53551672ef')
build() {
	cd "$srcdir"
	java -jar BuildTools.jar
}
package() {
  install -Dm644 "$srcdir/Spigot/Spigot-Server/target/spigot-${pkgver//_/-}-SNAPSHOT.jar" "$pkgdir/srv/craftbukkit/spigot.jar"
  install -m755 "$srcdir/spigot.sh" "$pkgdir/srv/craftbukkit/spigot.sh"
  install -Dm644 "$srcdir/spigot.service" "$pkgdir/usr/lib/systemd/system/spigot.service"
}

# vim:set ts=2 sw=2 et:
