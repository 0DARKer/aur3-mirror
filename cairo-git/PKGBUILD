# Contributor: dx <dx@dxzone.com.ar>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=cairo-git
pkgver=1.12.14.60.gc51b850
pkgrel=1
pkgdesc="Cairo vector graphics library (git version)"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng>=1.4.0' 'libxrender' 'fontconfig>=2.8.0' 'pixman>=0.18.4' 'xcb-util>=0.3.6' 'glib2>=2.24.0')
makedepends=('gtk-doc' 'git')
options=('!libtool')
provides=('cairo=1.10.0' 'cairo-xcb=1.10.0')
conflicts=('cairo' 'cairo-xcb')
source=('git://anongit.freedesktop.org/git/cairo' 'cairo-1.10.0-buggy_gradients.patch')
sha1sums=('SKIP'
          '8b843a9934e5112b6188e5bcf4adfc1fdaf9fa04')
_gitname='cairo'

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "${srcdir}/${_gitname}"
  patch -Np1 -i "$srcdir/cairo-1.10.0-buggy_gradients.patch"
}

build() {
  cd "${srcdir}/${_gitname}"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-xcb --disable-static
  make
}

package(){
  cd "${srcdir}"/${_gitname}
  make DESTDIR="${pkgdir}" install
}
