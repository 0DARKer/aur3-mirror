# Mantainer: Kozec (kozec2 <at> kozec <dot> com)

pkgname=google2srt
pkgver=0.5.5
pkgrel=2
pkgdesc="Google2SRT allows you to convert subtitles from Google Video and YouTube to SubRip (.srt) format."
arch=('i686' 'x86_64')
url=(http://sourceforge.net/projects/google2srt/)
license=('GPL3')
depends=('java-runtime' 'java-jdom1')
makedepends=('unzip' 'java-environment')
source=("http://heanet.dl.sourceforge.net/project/google2srt/Google2SRT/${pkgver}/Google2SRT-${pkgver}.zip"
		google2srt
		google2srt.desktop
		icon.png
		)

noextract=("Google2SRT-${pkgver}.zip")

build() {
	cd "$srcdir"
	unzip -o "Google2SRT-${pkgver}.zip" >/dev/null || return 1
	cd "Google2SRT-${pkgver}/src"
	rm *.class &>/dev/null || true
	CLASSPATH="$CLASSPATH:/usr/share/java/jdom/jdom.jar" \
		javac Common.java Converter.java NetSubtitle.java \
			  Network.java SRTFilter.java TableModel.java \
			  XMLFilter.java GUI.java \
		|| return 1
}

package() {
	mkdir -p "$pkgdir/usr/doc/google2srt"
	mkdir -p "$pkgdir/usr/lib/google2srt"
	mkdir -p "$pkgdir/usr/share/applications"
	mkdir -p "$pkgdir/usr/share/icons"
	mkdir -p "$pkgdir/usr/bin"
	cd "$srcdir"
	install -m0755 google2srt "$pkgdir/usr/bin" || return 1
	install -m0644 google2srt.desktop "$pkgdir/usr/share/applications" || return 1
	install -m0644 icon.png "$pkgdir/usr/share/icons/google2srt.png" || return 1
	cd "Google2SRT-${pkgver}/src"
	cp -R ../doc/* "$pkgdir/usr/doc/google2srt" || return 1
	cp Bundle* *.class *.form *.jpg "$pkgdir/usr/lib/google2srt" || return 1
}

md5sums=('2388dc68e542d0c88eb041d8e86245f1'
         'ab480f22865b16c4beef8442e8e08e49'
         '06c19e48725df1476945d73cb28744ce'
         '7878c439c81412767874bfbf6c6683ad')
