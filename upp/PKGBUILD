# Maintainer: Jan Dolinar <dolik.rce@gmail.com>

pkgname=upp
pkgver=3211
pkgrel=2
pkgdesc="Radical and innovative multiplatform C++ framework (known as U++)"
arch=('any')
url="http://www.ultimatepp.org"
license=('BSD')
groups=()
depends=('theide')
makedepends=()
optdepends=('libnotify: Enables compiling gtk-styled apps')
provides=('upp')
conflicts=('upp')
replaces=()
backup=()
options=(emptydirs !strip)
install='upp.install'
source=('http://downloads.sourceforge.net/project/upp/upp/'$pkgver'/upp-x11-src-'$pkgver'.tar.gz'
        'GCC.bm'
        'license.txt')
noextract=()

build() {
  msg2 "Nothing to build..."
}

package() {
  #copy source files
  mkdir -p "$pkgdir/usr/share/upp"
  msg2 "Moving the source codes..."
  cp -r "$srcdir/upp-x11-src-$pkgver/"{bazaar,examples,reference,tutorial,uppsrc} "$pkgdir/usr/share/upp/"
  echo "#define IDE_VERSION \"$pkgver-Arch\"" > "$pkgdir/usr/share/upp/uppsrc/ide/version.h"
  msg2 "Copying additional files..."
  #license
  mkdir -p "$pkgdir/usr/share/licenses/upp"
  cp "$srcdir/license.txt" "$pkgdir/usr/share/licenses/upp"
  #build method
  cp "$srcdir/GCC.bm" "$pkgdir/usr/share/upp"
  #fix permissions
  msg2 "Setting permissions..."
  find "$pkgdir/usr/" -type f -exec chown root:root {} \; -exec chmod 644 {} \;
  #patch
  cd "$pkgdir/usr/share/upp"
  sed -i 's/ptrdiff_t/std::ptrdiff_t/' uppsrc/Core/Algo.h
  sed -i 's/ifdef __clang__/if 1/' uppsrc/Core/Convert.h
  sed -i '/include <libnotify/a\
		#ifdef NOTIFY_CHECK_VERSION\
			#if NOTIFY_CHECK_VERSION(0,7,0)\
				#define NOTIFY_VERSION_GT_0_7_0\
			#endif\
		#endif
    s/: "gtk-dialog-error", NULL);/: "gtk-dialog-error"\
	#ifndef NOTIFY_VERSION_GT_0_7_0\
						, NULL\
	#endif\
						);/' uppsrc/CtrlLib/TrayIconX11.cpp
}

md5sums=('05d5227da145d681f57286e19bd59ff2'
         '4a057aeaa906c8486cf463558b9a7d5a'
         'b214709f096e4f50d61f50988359241e')
