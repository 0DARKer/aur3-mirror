# Maintainer: Vinycius Maia <suportevg@uol.com.br>
# Contributor: DonVla <donvla@users.sourceforge.net>
#              Reza Jelveh <reza.jelveh at tuhh.de>

pkgname=lib32-caps
_pkgname=caps
pkgver=0.4.5
pkgrel=3
pkgdesc="The C* Audio Plugin Suite (32 bit)"
arch=('x86_64')
url="http://quitte.de/dsp/caps.html"
license=('GPL')
options=(docs emptydirs)
depends=('lib32-gcc-libs')
#makedepends=('python2')  # optional CFLAGS check
source=("http://quitte.de/dsp/${_pkgname}_${pkgver}.tar.gz"
	'fix-lib32-makefile.patch'
	'double-free-corruption.patch')
md5sums=('f3fda0eec716202b658cc6075daa35d0'
	 '63103dfeb432e19c6de631f7cab843ca'
	 '88edcc7f652ad42a8a418d6a141ac838')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make clean
  type -p python2 1>/dev/null && python2 configure.py
  patch -Np1 -i ../fix-lib32-makefile.patch
  patch -Np0 -i ../double-free-corruption.patch
  make
 }
 
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -v -m 0755 -D caps.so "${pkgdir}/usr/lib32/ladspa/caps.so"
  install -v -m 0644 -D caps.rdf "${pkgdir}/usr/share/ladspa/rdf/lib32-caps.rdf"
  install -v -m 0644 -D caps.html "${pkgdir}/usr/share/doc/caps/lib32-caps.html"
}
