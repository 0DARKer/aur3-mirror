# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Contributor:  Ashren <edgar [at] archlinux [dot] us>
# Based on rtorrent [community]

pkgname=rtorrent-color
_pkgname="rtorrent"
#_pkgname="${pkgname%-*}" not supported by AUR ;)
pkgver=0.8.9
pkgrel=2
pkgdesc="Ncurses BitTorrent client based on libTorrent with color patch."
url="http://libtorrent.rakshasa.no"
arch=("i686" "x86_64")
license=("GPL")
depends=("curl" "libtorrent=0.12.9" "xmlrpc-c")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
install="${pkgname}.install"
source=("${url}/downloads/${_pkgname}-${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}_canvas_color.patch"
        "${_pkgname}-${pkgver}_buildfix_gcc47.patch")
sha1sums=("0ac51c185e98b5a386e5f1a07bca9a9963e2d6ce"
          "a4c2247a62b60c3ffb07dc688f2371cccfa3664d"
          "f674c32031023807f8aa160862bb9a3ed12be446")

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -uNp1 -i "${srcdir}/${_pkgname}-${pkgver}_canvas_color.patch"
  patch -uNp1 -i "${srcdir}/${_pkgname}-${pkgver}_buildfix_gcc47.patch"

  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing -fpermissive"
  export LIBS="${LIBS} -lpthread"
  ./configure \
    --prefix=/usr \
    --enable-debug \
    --with-xmlrpc-c \

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 doc/faq.xml      "${pkgdir}/usr/share/doc/rtorrent/faq.xml"
  install -Dm644 doc/rtorrent.1   "${pkgdir}/usr/share/man/man1/rtorrent.1"
  install -Dm644 doc/rtorrent.rc  "${pkgdir}/usr/share/doc/rtorrent/rtorrent.rc"
}
