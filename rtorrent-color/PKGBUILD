# Maintainer:    David <david.e [at] r3v0luti0n [dot] com>
# Contributor:   Ashren <edgar [at] archlinux [dot] us>
# Contributors:  Lucky <archlinux@builds.lucky.li> and Hleem
# Based on rtorrent-color from AUR

pkgname=rtorrent-color
_pkgname="rtorrent"
#_pkgname="${pkgname%-*}" not supported by AUR ;)
pkgver=0.9.4
pkgrel=1
pkgdesc="Ncurses BitTorrent client based on libTorrent with color patch."
url="http://libtorrent.rakshasa.no"
arch=("i686" "x86_64")
license=("GPL")
depends=("curl" "libtorrent=0.13.4" "xmlrpc-c" "libsigc++")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
install="${pkgname}.install"
source=("${url}/downloads/${_pkgname}-${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}_color.patch")
sha1sums=('e997822e9b0d53cf8ecfb6b836e380065890e10d'
          '78429b5cf5976270dc1a55d8dc0ef4644675512b')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -uNp1 -i "${srcdir}/${_pkgname}-${pkgver}_color.patch"

  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
  ./configure \
    --prefix=/usr \
    --enable-debug \
    --with-xmlrpc-c \

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 doc/rtorrent.rc  "${pkgdir}/usr/share/doc/rtorrent/rtorrent.rc"
}
