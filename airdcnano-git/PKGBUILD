# Maintainer: Jan Holthuis <holthuis.jan@googlemail.com> 
pkgname=airdcnano-git
pkgver=1.00.r3.ga02583a
pkgrel=1
pkgdesc="AirDC++ nano is a high-performance text-mode client for sharing and chatting."
arch=('any')
url="http://www.airdcpp.net/nano"
license=('GPL')
depends=('bzip2' 'ncurses' 'zlib' 'glib2' 'miniupnpc' 'geoip' 'intel-tbb' 'boost' 'leveldb')
optdepends=(
  'natpmp: NAT-PMP port mapping support'
)
makedepends=('git' 'scons')
conflicts=('airdcnano')
provides=('airdcnano')
source=('git+https://github.com/airdcnano/airdcnano.git')
md5sums=( 'SKIP' )

pkgver() {
  cd "$srcdir/airdcnano"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/airdcnano"
  scons -Q PREFIX=/usr
}

package() {
  cd "$srcdir/airdcnano"
  scons -Q PREFIX=/usr --install-sandbox="$pkgdir" install
}
