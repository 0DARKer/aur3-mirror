# Original firehol contributors:
#    Jakob Gruber <jakob.gruber@gmail.com>
#    Geoffroy Carrier <geoffroy.carrier@koon.fr>
#    Filip Dvorak <fila@pruda.com>
# Maintainer: SanskritFritz (gmail)

pkgname=sanewall
pkgver=0.9.6
pkgrel=3
pkgdesc='Fork of Firehol (the iptables stateful packet filtering firewall builder)'
url="http://www.sanewall.org/"
arch=('any')
license=('GPL')
depends=('iptables' 'gawk' 'iproute')
backup=('etc/sanewall/sanewall.conf')
#conflicts=('firehol')
source=("http://download.sanewall.org/releases/${pkgver}/sanewall-${pkgver}.tar.gz" sanewall.service)

build() {
  cd "$srcdir"
  true
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir/etc/sanewall/examples"
  install -d "${pkgdir}/usr/share/man/man1"
  install -d "${pkgdir}/usr/share/man/man5"

  install -D -m744 sanewall "$pkgdir/usr/sbin/sanewall"
  install -D -m744 admin/* "$pkgdir/etc/sanewall/"

  install -D -m644 examples/*.conf "$pkgdir/etc/sanewall/examples/"

  install -D -m644 README "$pkgdir/usr/share/doc/sanewall/README"
  install -D -m644 COPYING "$pkgdir/usr/share/doc/sanewall/COPYING"
  install -D -m644 INSTALL "$pkgdir/usr/share/doc/sanewall/INSTALL"
  install -D -m644 doc/* "$pkgdir/usr/share/doc/sanewall/"

  install -D -m644 man/man1/* "$pkgdir/usr/share/man/man1/"
  install -D -m644 man/man5/* "$pkgdir/usr/share/man/man5/"

  install -D -m644 "$srcdir/sanewall.service" "$pkgdir/usr/lib/systemd/system/sanewall.service"

  # backup does not work if the file is not contained in the package
  # plus, creating it in post_install will set 777 permissions and we dont want that

  touch "$pkgdir/etc/sanewall/sanewall.conf"
  chmod 600 "$pkgdir/etc/sanewall/sanewall.conf"
}

md5sums=('9ec2ea3b629bf9965d098c951586d758'
         '518c276470090744c6bc4822d85da228')
