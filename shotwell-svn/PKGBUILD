# Maintainer:  Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: florianbw <florian(dot)bw(at)gmail(dot)com>
# Contributor: Frederic Bezies <fredbezies(at)gmail(dot)com>
# Contributor: Robert Chmielowiec <robert(at)chmielowiec(dot)net>

pkgname=shotwell-svn
_pkgname=shotwell
pkgver=2413
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('gconf' 'gexiv2-svn' 'json-glib' 'libgee' 'libgphoto2' 'libquicktime' 'librsvg' 'libunique' 'libwebkit')
makedepends=('intltool' 'libraw' 'pkgconfig' 'subversion' 'vala>=0.9.7' 'vala<0.11.0')
provides=('shotwell')
conflicts=('shotwell')
install=${_pkgname}.install
         
_svntrunk=svn://svn.yorba.org/${_pkgname}/trunk
_svnmod=${_pkgname}

build() {
  cd ${srcdir}

  msg "Updating from SVN"
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up)
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done."

  msg "Starting make..."
  rm -rf ./${_svnmod}-build
  svn export ./${_svnmod} ./${_svnmod}-build
  cd ./${_svnmod}-build
  
  ./configure --prefix=/usr \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
    --disable-schemas-install \
    --disable-desktop-update \
    --disable-icon-update
  make
}

package() {
  cd ${srcdir}/${_svnmod}-build

  # Temporary fix (build 2431)
  mkdir -p ${pkgdir}/usr/share/icons/hicolor/16x16/apps/
  mkdir -p ${pkgdir}/usr/share/icons/hicolor/24x24/apps/

  make DESTDIR=${pkgdir} install
}
