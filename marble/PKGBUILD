 # Contributor: mangus <mangus[_at_]deprecated.it>
### requires only Qt as a dependency. 
###The only drawback is that Marble-Qt doesn't provide the whole functionality of Marble 
###  (e.g. the settings dialog or automatic map download via "GHNS")
pkgname=marble
pkgver=1.2.2
_kver=4.7.2
pkgrel=1
pkgdesc="A generic geographical map widget - qt version"
arch=('i686' 'x86_64')
url="http://edu.kde.org/marble"
license=('LGPL')
depends=('qt' 'openssl')
makedepends=('cmake' 'gpsd')
optdepends=("gpsd: GPS daemon and library to support USB/serial GPS devices")
source=(http://download.kde.org/stable/${_kver}/src/marble-${_kver}.tar.bz2)
md5sums=('a7512fdd4571a57d36afafc13fa61f17')
install=marble.install
conflicts=('kdeedu-marble')

build() {
  cd  ${srcdir}

  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build 
  fi
  mkdir ${srcdir}/build
  cd ${srcdir}/build
  cmake ${srcdir}/${pkgname}-${_kver} -DCMAKE_INSTALL_PREFIX=/usr  \
			-DCMAKE_BUILD_TYPE=Release -DQTONLY=ON
  make
}
package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}

