# Maintainer: wallnuss <v dot churavy at gmail dot com
# Contributor: WonderWoofy <sugar.and.scruffy@gmail.com>
pkgname=thermald
_name=thermal_daemon
pkgver=1.3.rc1
_pkgver="${pkgver//_/-}"
_srcdir="${_name}-${_pkgver}"
pkgrel=7
pkgdesc="The Linux Thermal Daemon program from 01.org"
arch=('i686' 'x86_64')
url="https://github.com/01org/thermal_daemon"
license=('GPL2')
depends=('dbus-glib>=0.94' 'libxml2>=2.4')
backup=('etc/thermald/thermal-conf.xml')
conflicts=('thermald')
provides=('thermald')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${_pkgver}.tar.gz"
        'modules-load-thermald.conf'
        '00-hotfix-seqfaults.patch::https://github.com/01org/thermal_daemon/commit/add12bc11865ba854849cff907d0babe928dfe2e.patch')
md5sums=('7a5dea7cd01442a289200c1327447ee3'
         '1f8349416a7532cfd436e34002a1f270'
         'e549ad5b072f06601fb31b594ecbee24')

prepare() {
    cd "${_srcdir}" 

    patch -p1 < ../00-hotfix-seqfaults.patch
}

build() {
  cd "${_srcdir}"

  ./autogen.sh
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin
  make
}

package() {
  cd "${_srcdir}"
  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}/usr/lib/modules-load.d"
  install -m644 "${srcdir}/modules-load-thermald.conf" "${pkgdir}/usr/lib/modules-load.d/thermald.conf"

  # Remove upstart
  rm -rf "${pkgdir}/etc/init"
}
