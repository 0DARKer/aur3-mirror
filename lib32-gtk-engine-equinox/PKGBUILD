# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkgbasename=gtk-engine-equinox
pkgname=(lib32-$_pkgbasename)
pkgver=1.30.2
pkgrel=2
pkgdesc="A heavily modified version of the beautiful Aurora engine. Multilib."
arch=('x86_64')
url='http://gnome-look.org/content/show.php/Equinox+GTK+Engine?content=121881'
license=('GPL')
depends=('lib32-gtk2' "$_pkgbasename")
makedepends=('gcc-multilib' 'intltool' 'perl')
options=('!libtool')
source=("http://gnome-look.org/CONTENT/content-files/121881-equinox-1.30.tar.bz2")
md5sums=('07d43dede6bdc17ba74f1740a3743601')

build() {

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  tar xzf equinox-gtk-engine.tar.gz
  cd "$srcdir/equinox-1.30"

  # Toolbar Fix
  sed -i "985 s_\(EQUINOX.*\)_widget \&\& \1_" src/equinox_style.c

  ./configure --prefix=/usr \
	--libdir=/usr/lib32 \
	--enable-animation

  make
}

package() {

  cd "$srcdir/equinox-1.30"

  make DESTDIR="$pkgdir" install
}