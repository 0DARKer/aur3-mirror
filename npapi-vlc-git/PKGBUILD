# Maintainer: Det
# Contributor: Pimper (M0Rf30)

_pkgname=npapi-vlc
pkgname=$_pkgname-git
pkgver=0.2.213.gc992dba
pkgrel=1
pkgdesc="The modern VLC Mozilla plugin"
arch=('i686' 'x86_64')
url="http://git.videolan.org/?p=$_pkgname.git;a=summary"
license=('LGPL-2.1')
options=('!libtool')
depends=('vlc')
makedepends=('git' 'npapi-sdk')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("git://git.videolan.org/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --always | sed 's/-/./g'
}

build() {
  cd $_pkgname
  msg2 "Starting autogen.sh..."
  sh ./autogen.sh
  msg2 "Starting configure..."
  ./configure --prefix=/usr
  msg2 "Starting make..."
  make
}

package() {
  cd $_pkgname
  msg2 "Starting make install..."
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}