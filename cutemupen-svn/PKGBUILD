# Contributor: Anton Shestakov <engored*ya.ru>
# Maintainer: Anton Shestakov <engored*ya.ru>
pkgname=cutemupen-svn
pkgver=49
pkgrel=1
pkgdesc='A graphical front-end for Mupen64Plus emulator, written using Qt and the Mupen64Plus v2.0 API.'
arch=(i686 x86_64)
url='http://sourceforge.net/userapps/mediawiki/vknecht/index.php?title=CuteMupen'
license=('GPL2')
depends=('mupen64plus>=1.99' 'qt')
makedepends=('subversion')
provides=('cutemupen')
conflicts=('cutemupen')
source=(CuteMupen.conf.sample)
install=cutemupen-svn.install
md5sums=('01d93f9ca7558a6c24685cdc35d6154b')

_svntrunk='https://cutemupen.svn.sourceforge.net/svnroot/cutemupen/trunk'
_svnmod='cutemupen'

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg 'SVN checkout done or server timeout'
  msg 'Starting make...'

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  qmake cutemupen.pro
  make
}

package() {
  install -Dm755 "$srcdir/$_svnmod-build/cutemupen" "$pkgdir/usr/bin/cutemupen"
  install -Dm644 "$srcdir/CuteMupen.conf.sample" "$pkgdir/usr/share/cutemupen/CuteMupen.conf.sample"
}
