
#Maintainer: Jesse Jaara <gmail.com: jesse.jaara>

pkgname=yle-dl
pkgver=2.3.0
pkgrel=1
_gitid=6e63822
pkgdesc="Download video and audio from YLE Areena."
arch=("any")
url="http://aajanki.github.io/yle-dl/"
license=("GPL2")
depends=('python2-crypto' 'rtmpdump' 'php')
conflicts=('rtmpdump-yle')
replaces=('rtmpdump-yle')
source=("yle-dl-${pkgver}.tar.gz::https://github.com/aajanki/yle-dl/tarball/${pkgver}"
        "https://raw.githubusercontent.com/K-S-V/Scripts/master/AdobeHDS.php")

prepare() {
  cd "${srcdir}/aajanki-${pkgname}-${_gitid}"

  sed 's|/usr/local/bin/|/usr/share/yle-dl/|' -i yle-dl
  sed "s|'php'|'php', '-d extension=bcmath.so'|" -i yle-dl
}

package() {
  cd "${srcdir}/aajanki-${pkgname}-${_gitid}"

  make prefix=/usr DESTDIR="${pkgdir}" install

  mkdir -p "${pkgdir}/usr/share/yle-dl"
  cp "${srcdir}/AdobeHDS.php" "${pkgdir}/usr/share/yle-dl/"
}

md5sums=('cbfd47286191cc7636a4cf053bfe4e81'
         'b10a5d757ae661224dd629f1d9e749e6')
