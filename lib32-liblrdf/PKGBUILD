# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=liblrdf
pkgname=lib32-${_pkgbase}
pkgver=0.4.0
pkgrel=1
pkgdesc="A library for the manipulation of RDF file in LADSPA plugins (32 bit)"
arch=('x86_64')
url="http://sourceforge.net/projects/lrdf"
depends=('lib32-raptor' 'ladspa' "$_pkgbase")
makedepends=('gcc-multilib')
license=('GPL')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/lrdf/${_pkgbase}-${pkgver}.tar.gz
	md5.patch
	raptor2.diff
        pkgconfig-raptor.patch)
md5sums=('327a5674f671c4b360c6353800226877'
         'a6d231d052dc188cbc4c1039cf3a2003' 
         '80e938469da06a178e03107ca5b41e55'
         '982ae1bd4b39c2fb5cba108fbcbfbad1')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -Np1 -i "${srcdir}/md5.patch"
  patch -Np0 -i "${srcdir}/raptor2.diff"
  patch -Np1 -i "${srcdir}/pkgconfig-raptor.patch"
  autoreconf -vfi
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}
