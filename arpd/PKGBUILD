# Maintainer: Pierre Bourdon <delroth@gmail.com>

pkgname=arpd
pkgver=0.2
pkgrel=1
pkgdesc="A daemon that respond to unresponded ARP requests"

arch=(i686 x86_64)
backup=(etc/conf.d/arpd)

url=http://www.honeyd.org/tools.php
depends=('glibc' 'libdnet' 'libpcap' 'libevent' 'bash')
license=('BSD')

source=(http://www.citi.umich.edu/u/provos/honeyd/$pkgname-$pkgver.tar.gz
        arpd-__function__-is-not-standard.patch
        arpd.conf
        arpd.rc)
md5sums=('e2911fa9de1b92ef50deda1489ae944d'
         '09b4c7fa7225496d02f2dee2aff4c8fa'
         'dc30ee488865e3ed071f9142f8f356f1'
         'c098f57c5e2261fac806119e1ed66812')

build() {
    cd $srcdir/$pkgname

    patch -Np0 -i $srcdir/arpd-__function__-is-not-standard.patch

    ./configure --prefix=/usr --mandir=/usr/share/man

    make
    make install DESTDIR=$pkgdir

    install -D -m755 "$srcdir/arpd.rc" "$pkgdir/etc/rc.d/arpd"
    install -D -m644 "$srcdir/arpd.conf" "$pkgdir/etc/conf.d/arpd"
    install -D -m644 "$srcdir/arpd/LICENSE" \
        "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
