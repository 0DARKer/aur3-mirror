pkgname=zelda-roth_en
pkgver=6.11
pkgrel=1
pkgdesc="Zelda: Return of the Hylian is a fand made Zelda Game"
url="http://www.zeldaroth.fr/"
arch=('i686' 'x86_64')
depends=('sdl_gfx' 'sdl_mixer' 'sdl_image')
makedepends=(imagemagick)
license=('Unknown')
source=("http://www.zeldaroth.fr/us/files/ROTH/Linux/ZeldaROTH_US-src-linux.zip" zroth.desktop)
md5sums=('2dbbbb68192904ce9b2fcad542eb2651'
         '8560a99a2f9bd65c7c9e0dcf9f4f8951')
build () {
	cd $srcdir/ZeldaROTH_US-src-linux/src
	
	# Fix the Game so that it reads save files from $HOME/.zroth
	sed -i -e s~\"data/save/~string\(getenv\(\"HOME\"\)\)+\"/.zroth/~g Joueur.cpp
	
	#Make the game treat /usr/share/zroth as its data directory
	for i in *.cpp
	do
		sed -i -e s~data/~/usr/share/zroth/~g $i
	done
	
	# Create the directory $HOME/.zroth if it doesnt exist
	sed -i -e s~'(NULL));'~'&\n\tif (system(\"stat $HOME/.zroth \&> /dev/null")) system (\"mkdir $HOME/.zroth\");'~ main.cpp
	make
}

package () {
	
	install -Dm644 zroth.desktop $pkgdir/usr/share/applications/zroth.desktop
	cd $srcdir/ZeldaROTH_US-src-linux/src/
	
	install -Dm755 ZeldaROTH_US $pkgdir/usr/bin/zroth
	
	cd data
	for i in {map,music,sound}/*
	do
		install -Dm644 $i $pkgdir/usr/share/zroth/$i
	done
	
	cd images
	for i in */*
        do
                install -Dm644 $i $pkgdir/usr/share/zroth/images/$i
        done
	mkdir -p $pkgdir/usr/share/icons/hicolor/256x256/apps
	convert logos/fond.png -resize 256x256\! $pkgdir/usr/share/icons/hicolor/256x256/apps/zroth.png
}
