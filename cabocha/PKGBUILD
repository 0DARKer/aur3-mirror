# Maintainer: nosada <nosada271@gmail.com>

pkgname=cabocha
pkgver=0.68
pkgrel=1
pkgdesc="Yet Another Japanese Dependency Structure Analyzer"
url="https://code.google.com/p/cabocha/"
arch=('x86_64' 'i686')
license=('LGPL2.1' 'BSD')
depends=('crfpp>=0.55' 'mecab>=0.993')
optdepends=()
makedepends=()
conflicts=()
replaces=()
backup=()
source=("https://cabocha.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('5e22a71eb86d778fdeb1b725c0b27f1fb4af7f4b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # '--with-charset=UTF8' and '--enable-utf8-only' is optional.
  # you can remove it (see 'INSTALL')
  ./configure --prefix=/usr --with-charset=UTF8 --enable-utf8-only
  make ${MAKEFLAGS}
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 BSD \
          "${pkgdir}/usr/share/licenses/${pkgname}/BSD"
  install -Dm644 LGPL \
          "${pkgdir}/usr/share/licenses/${pkgname}/LGPL"
  install -Dm644 COPYING \
          "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
