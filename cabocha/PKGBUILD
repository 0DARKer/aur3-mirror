# Maintainer: nosada <ngsksdt@gmail.com>

pkgname=cabocha
pkgver=0.69
pkgrel=1
pkgdesc="Yet Another Japanese Dependency Structure Analyzer"
url="https://code.google.com/p/cabocha/"
arch=('x86_64' 'i686')
license=('LGPL2.1' 'BSD')
depends=('crfpp' 'mecab' 'mecab-ipadic')

if [ ! -f ./${pkgname}-${pkgver}.tar.bz2 ]; then
  msg2 "from version 0.69, Cabocha is hosted on Google Drive."
  msg2 "This means I cannot get permalink to source code."
  msg2 "You can get latest source from https://drive.google.com/folderview?id=0B4y35FiV1wh7cGRCUUJHVTNJRnM&usp=sharing#list"
  return 1
else
  source=("./${pkgname}-${pkgver}.tar.bz2")
  sha1sums=("9196098628c5d1f0b83b371a03352b6652c04001")
fi

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # '--with-charset=UTF8' and '--enable-utf8-only' is optional.
  # you can remove it (see INSTALL in source directory)
  ./configure --prefix=/usr --with-charset=UTF8 --enable-utf8-only
  make ${MAKEFLAGS}
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING \
    ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
