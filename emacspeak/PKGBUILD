# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
pkgname=emacspeak
pkgver=36.0
pkgrel=1
pkgdesc="Emacs extension that provides spoken output"
arch=('i686' 'x86_64')
url="http://emacspeak.sourceforge.net/"
license=('GPL' 'LGPL' 'APACHE')
depends=(emacs tcl tclx espeak)
optdepends=('eflite: software speech via the FLite TTS engine'
            'python: Google client, and wrapper for Emacspeak speech servers.')
install='emacspeak.install'
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.bz2")

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make config
  # Prevent shr-url.elc from being compiled.  It's an optional module,
  # and it won't work with the emacs version distributed in the official repos.
  touch lisp/shr-url.elc
  make

  # This one isn't compiled by default, but a lot of folks use it.
  cd servers/linux-espeak
  make TCL_VERSION=8.5
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  # Remember, shr-url.elc is bogus.
  # Fixme: remove these workarounds when Arch's emacs includes shr.el.
  rm "$pkgdir/usr/share/emacs/site-lisp/emacspeak/lisp/shr-url.elc"
  cd servers/linux-espeak
  make DESTDIR="$pkgdir" install
  # Interestingly, the source files are installed under DESTDIR.
  cd "$pkgdir/usr/share/emacs/site-lisp/emacspeak/servers/linux-espeak"
  rm -f tclespeak.cpp Makefile
  # A handful of files have permissions of 750 and 640; fix.
  cd "$pkgdir"
  find . -perm 640 -print0
  find . -perm 750 -print0
  gzip -9nf "${pkgdir}"/usr/share/info/*
  rm -f "$pkgdir/usr/share/info/dir"
}
md5sums=('d617719195fc5ea204a99ee51be17270')
