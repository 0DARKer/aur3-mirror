# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubikey-neo-manager-git
pkgver=0.2.2.r10.g98179ca
pkgrel=1
pkgdesc="Cross platform personalization tool for the YubiKey NEO - git checkout"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubikey-neo-manager"
license=('BSD')
depends=('ykpers' 'libykneomgr' 'libu2f-host' 'python2' 'python2-pyside')
makedepends=('git' 'python2-setuptools')
provides=('yubikey-neo-manager')
conflicts=('yubikey-neo-manager')
source=('git://github.com/Yubico/yubikey-neo-manager.git')

pkgver() {
	cd yubikey-neo-manager/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

package() {
	cd yubikey-neo-manager/

	python2 setup.py install --root="${pkgdir}/" --optimize=1

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/yubikey-neo-manager/COPYING"
	install -D -m0644 README "${pkgdir}/usr/share/doc/yubikey-neo-manager/README"
}

sha256sums=('SKIP')
