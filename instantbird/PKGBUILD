# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=instantbird
pkgver=1.2
pkgrel=2
pkgdesc="Instant messenger using libpurple and xulrunner"
arch=(i686 x86_64)
license=('MPL' 'GPL' 'LGPL')
url="http://instantbird.com"
depends=('xulrunner<15.0')
makedepends=('unzip' 'zip' 'pkg-config' 'diffutils' 'python2' 'wireless_tools' 'yasm' 'libidl2' 'mesa' 'autoconf2.13')
source=(http://instantbird.com/downloads/${pkgver}/${pkgname}-${pkgver}.src.tgz nss.patch mozconfig ${pkgname}.desktop)
options=('!buildflags')
_xulnum=`ls /usr/lib | grep -m1 xulrunner-devel | sed -e 's/.*-//'`

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}-src

	cp "${srcdir}"/mozconfig .
	patch -Np1 -i ../nss.patch
	echo "ac_add_options --with-libxul-sdk=/usr/lib/xulrunner-devel-"${_xulnum} >> mozconfig

	grep -rc 'include <glib/' purple/libpurple | grep -v ':0' | sed -e 's|:.*||g' | xargs sed -i -e 's|include <glib/.*|include <glib.h>|g'
	sed -i -e "s|<glib.h>|<glib.h>'|" purple/libpurple/Makefile.in
	sed -i -e "/SDK_BINARY/d" mozilla/build/unix/Makefile.in
	sed -i -e 's|\$LIBXUL_SDK/sdk|\$LIBXUL_SDK|g' configure.in
	sed -i -e 's|$(DIST)/sdk/bin|$(LIBXUL_DIST)/sdk/bin|g' \
	config/config.mk \
	mozilla/config/config.mk \
	mozilla/js/src/config/config.mk

	# May be affected by mozilla bug 559278
        CXXFLAGS="-std=gnu++0x" make -j1 -f client.mk build
}

package() {
	cd "${srcdir}"/obj-${pkgname}
	sed -i -e "s|.*mozilla/toolkit/locales.*||g" -e "s|.*mozilla/extensions/spellcheck/locales.*||g" instantbird/locales/Makefile
	make package
	cd mozilla/dist
	bsdtar -x -f ${pkgname}-${pkgver}.en-US.linux-${CARCH}.tar.bz2

	mkdir -p "${pkgdir}"/usr/bin
	mkdir -p "${pkgdir}"/usr/lib
	mkdir -p "${pkgdir}"/usr/share/pixmaps
	mkdir -p "${pkgdir}"/usr/share/applications
	cp -R ./instantbird "${pkgdir}"/usr/lib/instantbird
	cd "${pkgdir}"/usr/bin
	ln -s /usr/lib/instantbird/instantbird instantbird

	# No idea why this is needed now
	cd "${pkgdir}"/usr/lib/instantbird
	ln -s /usr/lib/xulrunner-${_xulnum} xulrunner

	install -D -m 644 icons/mozicon50.xpm ../../share/pixmaps/instantbird.xmp
	install -D -m 644 "${srcdir}"/instantbird.desktop ../../share/applications/instantbird.desktop
}
md5sums=('00ae613c4392844c7b92accffbe63d7b' '4f7bbaf0aeb48c6a0c0611f0ee27f586' '10f760008d98c1a0353708d0dec4bf9d' 'eb364c644eb5a598909999b1c47260cc')
