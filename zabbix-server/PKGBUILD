# Maintainer: Idares <idares@seznam.cz>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

### I AM ONLY THE PACKAGER, NOT THE DEVELOPER
### Please ask support questions about this software in one of:
###   1) The AUR comments; OR
###   2) Upstream forums/maillist etc; OR
###   3) The ArchLinux forums
### I do not always know enough about the software itself, or don't have the
### time to promptly respond to direct emails.
### If you have found a problem with the package/PKGBUILD (as opposed to
### the software) then please do email me or post an AUR comment.

pkgname=zabbix-server
pkgver=2.0.6
pkgrel=1
pkgdesc="Software for monitoring of your applications, network and servers."
arch=('i686' 'x86_64')
url="http://www.zabbix.com"
license=('GPL')
depends=('apache' 'postgresql' 'php' 'php-pgsql' 'php-gd' 'fping' 'net-snmp' 'curl' 'iksemel')
backup=('etc/zabbix/zabbix_server.conf'
        'etc/conf.d/zabbix-server'
        )
install='zabbix-server.install'
options=('emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/zabbix/zabbix-$pkgver.tar.gz"
        'rc.zabbix-server'
        'conf.zabbix-server'
        'zabbix-server.install'
        'zabbix-server.service'
        )

_HTMLPATH='srv/http/zabbix'

build() {
  cd "$srcdir/zabbix-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-server \
    --with-net-snmp \
    --with-jabber \
    --with-libcurl \
    --with-postgresql \
	--sysconfdir=/etc/zabbix

  make
}

package() {
  cd "$srcdir/zabbix-$pkgver"

  make DESTDIR="$pkgdir" install

  # Create data dirs required
  install -d -m0750 $pkgdir/var/run/zabbix
  install -d -m0750 $pkgdir/var/log/zabbix

  # database schema
  _DBSCHEMADIR="$pkgdir/etc/zabbix/database/postgresql"
  mkdir -p $_DBSCHEMADIR
  for _SQLFILE in {schema,data,images}.sql; do
    install -D -m 0444 database/postgresql/$_SQLFILE $_DBSCHEMADIR/$_SQLFILE
  done

  # database upgrade patch
  _DBPATCHDIR="$pkgdir/etc/zabbix/database/postgresql/upgrade/2.0"
  mkdir -p $_DBPATCHDIR
  install -D -m 0444 upgrades/dbpatches/2.0/postgresql/patch.sql $_DBPATCHDIR/patch.sql

  # frontends (user:group = root:apache)
  mkdir -p $pkgdir/$_HTMLPATH/
  cp -r frontends/php/* $pkgdir/$_HTMLPATH/
  chown -R 0:33 $pkgdir/$_HTMLPATH/
  chmod -R u=rwX,g=rX,o= $pkgdir/$_HTMLPATH/

  # default configuration files
  install -D -m 0640 conf/zabbix_server.conf $pkgdir/etc/zabbix/zabbix_server.conf

  # change pid file location
  sed -i 's:# PidFile=.*:PidFile=/var/run/zabbix/zabbix_server.pid:' $pkgdir/etc/zabbix/zabbix_server.conf

  # init script
  install -D -m 0755 $srcdir/rc.zabbix-server $pkgdir/etc/rc.d/$pkgname

  install -D -m 0622 $srcdir/conf.zabbix-server $pkgdir/etc/conf.d/$pkgname
  install -D -m 0644 $srcdir/zabbix-server.service $pkgdir/usr/lib/systemd/system/zabbix-server.service
}

md5sums=('f7261987731dd74b58cb1da890655ddc'
         'c56242d58e60c167fb31ac34b25ee64d'
         'cc60404fc2344545ff90546959342302'
         '2f6abf3a6bd7821ecf71296630e7a2c3'
         '5c77a06654962d5d4e8f74da7fcfcd6e')
sha1sums=('75a747ddcfa4bcd5792f69dc8d7de9c5839b8595'
          'ff7acb3012ff95a0b6b02496aab85eb0fc281581'
          '40af899208b89e51429d4274f7e57cf87fd76c56'
          'a8bd721884cedbae4217dee4666945a1ee693d13'
          'dc2faaf9de8235a4aa01ecd38e8170f1d330e834')

