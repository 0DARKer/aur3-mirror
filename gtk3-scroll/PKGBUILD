# $Id$
# Maintainer: Lukas Kramer <lu@0x83.eu>

pkgname=gtk3-scroll
pkgver=3.0.9
pkgrel=1
pkgdesc="The GTK+ Toolkit (v3) with scrolling in GtkNotebook and GtkSwitch"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'gnutls' 'heimdal' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info')
makedepends=('gobject-introspection')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
conflicts=('gtk3')
provides=('gtk3=3.0.9')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.0/gtk+-${pkgver}.tar.bz2
        settings.ini
        scroller.patch
        switch.patch)
sha256sums=('88a9dda6f2a23155ac3d7aca6b414ca3b55e2817b46bcc87733f1a407e16678c'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            'e93a3c06974d9ef82a6f77dc3da8c2c1d143e941de12e3a757a5746ac45dfc9e'
            '84b4e74b1d98bef69b9feb369aabed585a7dde99d05178c30e8e534f66d0d089')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
     patch gtk/gtknotebook.c < ../../scroller.patch
     patch gtk/gtkswitch.c < ../../switch.patch
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile
    make
}

package() {
    cd "${srcdir}/gtk+-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}
