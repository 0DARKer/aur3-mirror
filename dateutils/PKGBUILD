# Maintainer: philomath <philomath868 AT gmail DOT com>
pkgname=dateutils
pkgver=0.2.1
pkgrel=1
pkgdesc="nifty utilities and library for fast date-time calculations and conversion (strptime, dadd, dconv, ddiff, dgrep, dround, dseq, dtest)"
arch=('i686' 'x86_64')
url="http://hroptatyr.github.com/dateutils/"
license=('BSD')
depends=('glibc')
install=dateutils.install
source=(https://github.com/downloads/hroptatyr/dateutils/$pkgname-$pkgver.tar.xz)
md5sums=('9732434f6a6a36898e60758e68ffb252')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  mkdir -p $pkgdir/usr/share/licenses/dateutils
  sed -n '/* Copyright/,/ DAMAGE.$/p' src/dadd.c > $pkgdir/usr/share/licenses/dateutils/LICENSE
}

# vim:set ts=2 sw=2 et:
