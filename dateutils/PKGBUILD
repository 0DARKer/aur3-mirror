# Maintainer: philomath <philomath868 AT gmail DOT com>
pkgname=dateutils
pkgver=0.2.4
pkgrel=1
pkgdesc="nifty utilities and library for fast date-time calculations and conversion (strptime, dadd, dconv, ddiff, dgrep, dround, dseq, dtest)"
arch=('i686' 'x86_64')
url="http://hroptatyr.github.com/dateutils/"
license=('BSD')
depends=('glibc')
changelog=Changelog
install=dateutils.install
source=("https://bitbucket.org/hroptatyr/${pkgname}/downloads/${pkgname}-${pkgver}.tar.xz")
sha256sums=('8695d012e2588fcb2705b037b07ab58ce3473f3789877013873f2f0d0ad30d82')

build() {
	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr
	make
}

check() {
	cd $srcdir/$pkgname-$pkgver
	make -k check
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	# license
	install -dm755 $pkgdir/usr/share/licenses/$pkgname
	sed -n '/* Copyright/,/ DAMAGE.$/p' src/dadd.c > $pkgdir/usr/share/licenses/dateutils/LICENSE

	# readme
	install -Dm644 README.md ${pkgdir}/usr/share/doc/${pkgname}
}

