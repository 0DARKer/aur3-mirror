# Maintainer: Christopher Rosell <chrippa@gmail.com>
# Contributor: Army
# Contributor: tomegun
_pkgname=rtmpdump
pkgname=$_pkgname-git
pkgver=0.484.df6c518
pkgrel=1
pkgdesc="A tool to download rtmp:// and rtmpe:// streams"
url="http://rtmpdump.mplayerhq.hu/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('openssl')
makedepends=('git' 'zlib')
provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-svn")
source=("$_pkgname::git+git://git.ffmpeg.org/rtmpdump")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd "$srcdir/$_pkgname"
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make -j1 prefix="/usr" mandir="/usr/share/man" DESTDIR="$pkgdir" install

  cd "$pkgdir"
  mv usr/sbin/* usr/bin
  rmdir usr/sbin
}

# vim:set ts=2 sw=2 et:
