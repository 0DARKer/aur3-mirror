#Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
#Contributor: Andrea Scarpino <andrea@archlinux.org>
#Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kiconedit
pkgver=4.4.0
pkgrel=1
pkgdesc="KDE Icon Editor"
arch=('i686' 'x86_64')
url='http://www.kde.org/applications/graphics/kiconedit'
license=('GPL2')
depends=('kdebase-runtime')
makedepends=('pkgconfig' 'cmake' 'automoc4')
install=install
source=("ftp://ftp.kde.org/pub/kde/stable/extragear/$pkgname-$pkgver.tar.bz2")
md5sums=('f2db6aff093cea2fcefceb3eb02a94c3')

build() {
  cd "$srcdir"
  # fix compilation handbook issue
  sed -i '/add_subdirectory( hu_kiconedit\/kiconedit )/d' \
    "$pkgname-$pkgver/doc-translations/CMakeLists.txt"
  mkdir build || true
  cd build
  cmake "../$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 ft=sh et:
