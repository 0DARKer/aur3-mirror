# Maintainer: Mantas M. <grawity@gmail.com>
# Contributor: ying <Jinoto Systems>
pkgname="audit"
pkgver="2.1.3"
pkgrel="1"
pkgdesc="Tools and libraries for the audit framework"
url="http://people.redhat.com/sgrubb/audit/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('krb5')
makedepends=('python2' 'swig')
conflicts=('libaudit')
replaces=('libaudit')
source=("http://people.redhat.com/sgrubb/audit/$pkgname-$pkgver.tar.gz"
        "auditd.service")
sha1sums=('86f56a5b9e4c73b47299dba6c26017791e0e4ce7'
          'a625b06711f5567d79d9cac9df234a5068f11dec')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON='python2'
  ./configure \
    --prefix=/usr \
    --sbindir=/sbin \
    --libdir=/lib \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/audit \
    --enable-gssapi-krb5 \
    ;
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  mkdir -pm0700 "$pkgdir/var/log/audit"
  install -Dm0644 "$srcdir/auditd.service" "$pkgdir/lib/systemd/system/auditd.service"
  # To do.
  rm -rf "$pkgdir/etc/rc.d"
  rm -rf "$pkgdir/etc/sysconfig"
}

# vim: ts=2:sw=2:et
