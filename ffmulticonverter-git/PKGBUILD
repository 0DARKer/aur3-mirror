# Maintainer: Ilias Stamatis (stamatis.iliass) <> (gmail)
# Contributor: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

pkgname=ffmulticonverter-git
pkgver=1.7.0.r0.g2822c38
pkgrel=2
pkgdesc="Convert among several file types to other formats."
arch=(any)
url="http://sites.google.com/site/ffmulticonverter/"
license=('GPL')
depends=('python' 'python-pyqt4')
optdepends=('unoconv' 'imagemagick' 'ffmpeg')
makedepends=('git')
provides=('ffmulticonverter')
conflicts=('ffmulticonverter')
source=("${pkgname}::git+https://github.com/Ilias95/FF-Multi-Converter.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  # cutting off 'v' prefix that presents in the git tag
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package()
{
  cd "$pkgname"
  python setup.py install --root="${pkgdir}/" --optimize=1
}
