# Contributor: kasa <biuta.jr@gmail.com>
# Contributor: L42y <423300@gmail.com>

pkgname=networkmanager-git
pkgver=20110707
pkgrel=1
pkgdesc="Network Management daemon"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'udev' 'wireless_tools' 'wpa_supplicant' 'ppp' 'dhcpcd')
makedepends=('intltool' 'dhclient' 'iptables' 'gobject-introspection' 'gtk-doc' 'git')
optdepends=('modemmanager: for modem management service'
            'dhclient: alternative DHCP/DHCPv6 client'
            'iptables: Connection sharing'
            'dnsmasq: Connection sharing'
            'bluez: Bluetooth support')
options=('!libtool')
backup=('etc/NetworkManager/NetworkManager.conf')
replaces=('networkmanager')
provides=('networkmanager=0.8.9997')
conflicts=('networkmanager')
install=${pkgname}.install
source=('NetworkManager.conf' 'disable_set_hostname.patch')
md5sums=('1653159d6634fb62d3a5c548b7a56151'
         'b7deda5bb47b3df0eb42782eabbaefe7')

_gitroot="git://anongit.freedesktop.org/NetworkManager/NetworkManager.git"
_gitname="NetworkManager"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd ${srcdir}/$_gitname-build

  patch -Np1 -i "${srcdir}/disable_set_hostname.patch"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --with-crypto=nss \
    --with-distro=arch \
    --with-dhclient=/usr/sbin/dhclient \
    --with-dhcpcd=/sbin/dhcpcd \
    --with-iptables=/usr/sbin/iptables \
    --disable-static \
    --enable-more-warnings=no \
    --disable-wimax

  make
}

package() {
  cd ${srcdir}/$_gitname-build

  make DESTDIR="${pkgdir}" install
  install -m644 "${srcdir}/NetworkManager.conf" "${pkgdir}/etc/NetworkManager/"

  rm -rf "${pkgdir}/var/run/"
}
