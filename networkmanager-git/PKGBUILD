# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from network-manager package)
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

_pkgname=networkmanager
pkgname=$_pkgname-git
pkgver=20121009
pkgrel=1
pkgdesc="Network Management daemon"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/NetworkManager/"
license=(GPL)
depends=(dbus-glib iproute2 nss polkit wpa_supplicant ppp dhcpcd libsoup)
makedepends=(intltool dhclient iptables gobject-introspection gtk-doc git)
optdepends=('modemmanager: for modem management service'
    'dhclient: alternative DHCP/DHCPv6 client'
    'iptables: Connection sharing'
    'dnsmasq: Connection sharing'
    'bluez: Bluetooth support')
provides=($_pkgname=0.9.4)
conflicts=($_pkgname)
backup=(etc/NetworkManager/NetworkManager.conf)
options=(!emptydirs !libtool)
install=$pkgname.install
source=(NetworkManager.conf disable_set_hostname.patch dnsmasq-path.patch nm-polkit-permissive.patch)
sha256sums=('44b048804c7c0b8b3b0c29b8632b6ad613c397d0a1635ec918e10c0fbcdadf21'
    '25056837ea92e559f09563ed817e3e0cd9333be861b8914e45f62ceaae2e0460'
    '65124505048cc8396daf0242c9f5d532fa669b4bbca305998c248ab2329490cb'
    '3291fa1e71c677e40706ff7c7f495f3a9564aab2716eac5633b06e53a032b626')
sha512sums=('1f4e6c09da5a8921ee7759861cfa5e808c64cd27e733cd62cfb35776db05a226a73443c071730035480c9ba4abe85fa7daeb14aeffbbdae20ed193d78341f451'
    '166ddacf910fb0d8fc74fad527a1519d2279774f6df90a0b1cd710e388750918a0f17804da391d0eba045883b18c3033bd004316886df30fe778f6004a97c265'
    '0bf3534a670a19afb3d8559e7868adca1779d6e309631afa380c4266efa41b64ce8b303a17a7ae4d63fbe80dd2bb2a8d0cef0b2102048320d0a1da42c82a5caf'
    'f36b95c866cec3f8c627a2879894071bcec0e362701e283abfc5120afe5e0ef29ac4c923a3afc013fe8b734f7208422fd33c2c6a622c2307251cc6c59c9f7fb1')

_gitroot=git://anongit.freedesktop.org/NetworkManager/NetworkManager.git
_gitname=NetworkManager

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."
    if [[ -d $_gitname/.git ]] ; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."

    patch -Np1 -i ../disable_set_hostname.patch
    patch -Np1 -i ../dnsmasq-path.patch
    patch -Np1 -i ../nm-polkit-permissive.patch

    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/networkmanager \
        --with-crypto=nss \
        --with-distro=arch \
        --with-dhclient=/usr/sbin/dhclient \
        --with-dhcpcd=/usr/sbin/dhcpcd \
        --with-iptables=/usr/sbin/iptables \
        --with-systemdsystemunitdir=/usr/lib/systemd/system \
        --with-udev-dir=/usr/lib/udev \
        --with-resolvconf=/usr/sbin/resolvconf \
        --with-session-tracking=ck \
        --disable-static \
        --enable-more-warnings=no \
        --disable-wimax

    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install

    install -m644 ../$_gitname.conf "$pkgdir"/etc/$_gitname/

    rm -r "$pkgdir"/var/run

    # Provide native service for arch-daemons generator
    ln -s $_gitname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
