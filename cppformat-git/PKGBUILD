# Maintainer: Xentec <artificial.i7 at gmail dot com>

pkgname=cppformat-git
pkgver=v0.11.0.r93.gfae3ae6
pkgrel=2
pkgdesc="Small, safe and fast formatting library for C++"
arch=('i686' 'x86_64')
url="http://cppformat.github.io"
license=('Simplified BSD')
depends=('gcc-libs')
conflicts=('glbindings-git')
makedepends=('cmake' 'git')
source=("$pkgname"::'git+https://github.com/cppformat/cppformat.git')
md5sums=('SKIP')

pkgver() {
	cd "$pkgname"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "$pkgname"

	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DFMT_SHARED=ON \
		-Wno-dev \
		.
	make
}

check() {
	cd "$pkgname"
	make test
}

package() {
	cd "$pkgname"

	sed -n '/License/{:a;n;/Documentation License/b;p;ba}' README.rst | tail -n +1 >> LICENSE

	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m644 format.h "${pkgdir}/usr/include/format.h"
	install -D -m755 libformat.so "$pkgdir/usr/lib/libformat.so.$pkgver"
	ln -s libformat.so.$pkgver "$pkgdir/usr/lib/libformat.so"
}

