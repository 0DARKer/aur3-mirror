# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=xforms
pkgver=1.0.94pre8
pkgrel=1
pkgdesc="A graphical user interface toolkit for X Window Systems."
arch=('i686' 'x86_64')
license=('LGPL')
url='http://xforms-toolkit.org/'
depends=('libxpm' 'libjpeg-turbo' 'libgl')
makedepends=('texlive-core')
source=(http://mirror.veriportal.com/savannah/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('57979d91f6f8e3e50c96aff1579f8e08')
provides=('xforms-beta')
conflicts=('xforms-beta')
install=$pkgname.install

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr 
  make
  cd doc
  make info
  make pdf
}
package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir/ install
  rm $pkgdir/usr/lib/*.la
  install -d $pkgdir/usr/share/info 
  install -m644 $srcdir/$pkgname-$pkgver/doc/$pkgname.info* \
    $pkgdir/usr/share/info 
  install -Dm644 $srcdir/$pkgname-$pkgver/doc/$pkgname.pdf \
    $pkgdir/usr/share/doc/$pkgname/$pkgname.pdf
}
