# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=xforms
pkgver=1.0.94pre27
pkgrel=1
pkgdesc="A graphical user interface toolkit for X Window Systems."
arch=('i686' 'x86_64')
license=('LGPL')
url='http://xforms-toolkit.org/'
depends=('libxpm' 'libjpeg' 'libgl')
source=(http://download.savannah.gnu.org/releases/xforms/$pkgname-$pkgver.tar.gz)
md5sums=('eea921ed8262516553718085f3b7cafa')
provides=('xforms-beta')
conflicts=('xforms-beta')
install=$pkgname.install

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr 
  make
  cd doc
  make info
}
package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir/ install
  rm $pkgdir/usr/lib/*.la
  install -d $pkgdir/usr/share/info 
  install -m644 $srcdir/$pkgname-$pkgver/doc/$pkgname.info* \
    $pkgdir/usr/share/info 
}
