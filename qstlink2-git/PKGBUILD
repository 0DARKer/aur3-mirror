# Maintainer: Christopher Arndt <chris@chrisarndt.de>
pkgname=qstlink2-git
pkgver=1.1.7r168.034198c
_pkgver=1.1.7
pkgrel=1
pkgdesc="A ST-Link V2 (Debugger/Programmer) client."
arch=(i686 x86_64 arm)
url="https://github.com/fpoussin/QStlink2"
license=('GPL3')
depends=('qt5-base' 'libusb')
provides=('qstlink2')
conflicts=('qstlink2-svn')
source=("$pkgname::git+https://github.com/fpoussin/QStlink2.git")
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    echo ${_pkgver}r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare(){
    cd "${srcdir}/${pkgname}"

    git submodule init
    git submodule update
}

build() {
    cd "${srcdir}/${pkgname}"

    qmake-qt5
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    make INSTALL_ROOT="$pkgdir" install
}
