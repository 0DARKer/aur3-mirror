# Maintainer: Leonard de Ruijter <leonard@aur.archlinux.org>
pkgname=icecast-kh-git
pkgver=2_kh
pkgrel=1
pkgdesc='The KH branche extends the official release of Icecast with features that may be (if found to be working out well) merged into the next official release. This package install the most recent version from GitHub.'
arch=('i686' 'x86_64')
url='http://karlheyes.github.com'
license=('GPL2')
conflicts=('icecast' 'icecast' 'icecast-svn' 'icecast-kh-git')
provides=("icecast=${pkgver/_*}")
depends=('libxslt' 'libvorbis' 'libtheora' 'speex' 'curl')
optdepends=('ices-kh: client for icecast server')
makedepends=('git')
backup=('etc/icecast.xml' 'etc/logrotate.d/icecast')
install=$pkgname.install
source=("icecast-kh-git::git://github.com/karlheyes/icecast-kh.git"
        'icecast-kh-git.logrotate'
        'icecast-kh-git.service')
md5sums=('SKIP'
         'c619028040f547621e62ebd6f644c233'
         '123cdd32d48f2f2839565d2746be4db2')

pkgver() {
  cd $pkgname
  git describe --always | sed 's|-|_|g' | cut -c9-
}

prepare() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
}

build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install

  # install logrotate config
  install -Dm644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/icecast"
  # install systemd unit
  install -Dm0644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/icecast.service"
find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}

