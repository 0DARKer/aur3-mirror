# Maintainer: Antonio Rojas
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepimlibs-git
pkgver=r18430.4c0b8cd
pkgrel=1
pkgdesc="KDE PIM Libraries"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepimlibs'
license=('GPL' 'LGPL')
depends=('gpgme' 'kf5-akonadi-git' 'libical' 'prison' 'qjson' 'kdelibs4support-git' 'kidletime-git' 'kitemmodels-git' 'kinit-git' 
'kemoticons-git')
makedepends=('extra-cmake-modules' 'boost' 'cyrus-sasl' 'git' 'kdoctools-git')
source=('git://anongit.kde.org/kdepimlibs.git')
md5sums=('SKIP')

pkgver() {
  cd kdepimlibs
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  export PATH="/opt/kf5/bin:$PATH"
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
  export CMAKE_PREFIX_PATH="/opt/kf5"

  mkdir build
  cd build
  cmake ../kdepimlibs \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
