# Packager: Walter Dworak <preparationh67@gmail.com>

pkgname=perl-path-finddev
pkgver=0.5.2
_modnamespace=Path
_perlmod=Path-FindDev
pkgrel=1
pkgdesc="Find a development path somewhere in an upper hierarchy"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl' 'perl-file-homedir' 'perl-path-isdev')
url="http://search.cpan.org/dist/Path-FindDev"
source=("http://search.cpan.org/CPAN/authors/id/K/KE/KENTNL/${_perlmod}-${pkgver}.tar.gz")
md5sums=('b494d775273230d3542851073c2a9da2')

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd ${_perlmod}-${pkgver}
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd ${_perlmod}-${pkgver}
    make test
)

package() {
    export PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" 

    cd ${_perlmod}-${pkgver}
    make install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
