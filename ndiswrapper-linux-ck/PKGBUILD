# Maintainer: simke <goranvxiii@gmail.com>
# Contributor:  Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_krel=3.2
_knextrel=3.3
_extramodules=extramodules-$_krel-ck
_kver="$(cat /lib/modules/${_extramodules}/version)"
_pkgname=ndiswrapper
pkgname=ndiswrapper-linux-ck
pkgver=1.57
pkgrel=2
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors, for linux-ck-$_krel"
license=('GPL')
arch=('i686' 'x86_64')
url="http://ndiswrapper.sourceforge.net"
install=ndiswrapper.install
depends=("linux-ck>=$_krel" "linux-ck<$_knextrel" 'wireless_tools' 'perl')
makedepends=('linux-ck-headers')
conflicts=("$_pkgname")
provides=("$_pkgname-utils" "$_pkgname-bin")
replaces=("$_pkgname-utils" "$_pkgname-bin")
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz")
options=('!strip')
md5sums=('7a401dc540938bf07893c67f418b6152')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # modinfo path fix
  sed -i "/modinfo/s/s/usr\//" driver/Makefile

  make KVERS=$_kver
}

(
cat <<EOF
post_install()
{
  post_upgrade
}
post_upgrade()
{
  depmod $_kver
}
post_remove()
{
  post_upgrade
}
EOF
) > $startdir/ndiswrapper.install

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make INST_DIR="lib/modules/$_extramodules" \
    KVERS=$_kver DESTDIR="$pkgdir/" install

  gzip "$pkgdir/lib/modules/$_extramodules/$_pkgname.ko"
}

# vim:set ts=2 sw=2 et:
