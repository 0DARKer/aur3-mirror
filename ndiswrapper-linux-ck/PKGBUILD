# Maintainer: simke <goranvxiii@gmail.com>
#
# from ABS
# $Id: PKGBUILD 67349 2012-03-09 16:43:42Z ttopper $
# Maintainer:  Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>


_extramodules=extramodules-3.2-ck
_kver="$(cat /lib/modules/${_extramodules}/version)"
_pkgname=ndiswrapper
pkgname=ndiswrapper-linux-ck
pkgver=1.57
pkgrel=1
pkgdesc="Module for NDIS (Windows Network Drivers), for linux-ck."
license=('GPL')
arch=('i686' 'x86_64')
url="http://ndiswrapper.sourceforge.net"
install=ndiswrapper.install
depends=('linux-ck>=3.2' 'linux-ck<3.3' 'wireless_tools' 'perl')
makedepends=('linux-ck-headers')
conflicts=("$_pkgname")
provides=("$_pkgname-utils" "$_pkgname-bin")
replaces=("$_pkgname-utils" "$_pkgname-bin")
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz")
options=('!strip')
md5sums=('7a401dc540938bf07893c67f418b6152')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # modinfo path fix
  sed -i "/modinfo/s/s/usr\//" driver/Makefile

  make KVERS=$_kver
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make INST_DIR="lib/modules/$_extramodules" \
    KVERS=$_kver DESTDIR="$pkgdir/" install

  gzip "$pkgdir/lib/modules/$_extramodules/$_pkgname.ko"
}

# vim:set ts=2 sw=2 et:
