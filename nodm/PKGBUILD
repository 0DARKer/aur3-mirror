# Maintainer: Ben Morgan <neembi@googlemail.com>
pkgname=nodm
pkgver=0.7
pkgrel=11
pkgdesc='X display manager for automatic logins'
arch=('i686' 'x86_64')
url="http://enricozini.org/sw/nodm/"
license=('GPL')
depends=(pam)
makedepends=(help2man)
optdepends=()
provides=()
options=()
backup=("etc/nodm.conf")
install=("nodm.install")
source=("http://enricozini.org/sw/nodm/$pkgname-$pkgver.tar.gz"
        "nodm-wrapper.sh"
        "nodm.conf"
        "norestart.patch")
md5sums=('6a569e9be7baf56de5c5813483d0357c'
         '39adaad2782da4be9b7b1cb2d651107c'
         'f59813acc2bc962bd8a2522c7f321cbb'
         '5076b2fe19551c42e3f5cd8bf445927f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i ../norestart.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir/usr" install

  cd "$srcdir"
  install -m755 nodm-wrapper.sh $pkgdir/usr/sbin/nodm-wrapper
  install -Dm644 nodm.conf $pkgdir/etc/nodm.conf
}

# vim:set ts=2 sw=2 et:
