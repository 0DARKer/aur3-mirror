# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname='perl-x-osd'
_lastauthor="G/GO/GOZER"
_pkgname='X-Osd'
pkgver='0.7'
pkgrel='2'
pkgdesc="Perl extension to the X On Screen Display library (xosd)"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'xosd')
url='http://search.cpan.org/dist/X-Osd/'
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('e3bafe11cd51ad9b1e3d1df18516f9bb1ba462f981483b922f2b627b08190391')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  export PERL_MM_USE_DEFAULT=1
  export PERL_AUTOINSTALL='--skipdeps'
  perl Makefile.PL INSTALLDIRS=vendor
  make
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
