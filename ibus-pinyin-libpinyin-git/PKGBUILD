# Maintainer:  Yangtse Su<yangtsesu@gmail.com>
# Contributor:  Jekyll Wu<adaptee [at] gmail [dot] com>
# Contributor: riverscn<riverscn at gmail.com>
# Contributor: rainy<rainylau at gmail.com>
# Contributor: Lee.MaRS<leemars at gmail.com>

pkgname=ibus-pinyin-libpinyin-git
pkgver=20111230
pkgrel=1
pkgdesc="The PinYin Engine for IBus Input Framework"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://ibus.googlecode.com"
depends=('ibus' 'libsigc++2.0' 'python2' 'libpinyin')
makedepends=('git' 'intltool' 'gnome-common')
provides=('ibus-pinyin')
conflicts=('ibus-pinyin' 'ibus-pinyin-git')
install=ibus-pinyin.install
source=(http://ibus.googlecode.com/files/pinyin-database-1.2.99.tar.bz2)
noextract=(pinyin-database-1.2.99.tar.bz2)
md5sums=('d0951b8daa7f56a2cbd3b6b4e42532e0')

_gitname="ibus-pinyin"
_gitroot="git://github.com/ibus/ibus-pinyin.git"
_patchname="libpinyinpatch"
_patchroot="git://pkgs.fedoraproject.org/ibus-pinyin.git"

build() {
  cd ${srcdir}

  msg "Connecting to ${_gitname} GIT server..."
  if [ -d ${_gitname} ]; then
      cd ${_gitname} && git pull origin master
      msg "The local files are updated."
  else
      git clone ${_gitroot}
  fi

  cd ${srcdir}
  msg "Connecting to Patch GIT server..."
  if [ -d ${_patchname} ]; then
    cd ${_patchname} && git pull origin master
    msg "The local files are updated."
  else
    git clone ${_patchroot} ${_patchname}
  fi

  msg "GIT checkout done or server timeout. Preparing sources..."
  rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  ln -s "${srcdir}/pinyin-database-1.2.99.tar.bz2" "${srcdir}/${_gitname}-build/data/db/open-phrase"

  msg "Starting make..."
  cd "${srcdir}/${_gitname}-build"

  patch -Np1 -i ${srcdir}/${_patchname}/ibus-pinyin-libpinyin-integration.patch
  patch -Np1 -i ${srcdir}/${_patchname}/ibus-pinyin-xx-icon-symbol.patch
  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  for file in setup/ibus-setup-pinyin.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  ./autogen.sh --break-configure || :
  ./configure \
    PYTHON=/usr/bin/python2 \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --disable-boost \
    --enable-db-open-phrase || return 1

  make || return 1
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make NO_INDEX=true DESTDIR=${pkgdir} install || return 1
}
