# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=searchandrescue
pkgver=1.4.0
pkgrel=1
pkgdesc="Flight simulator in which the player finding and rescuing people in distress"
arch=('i686' 'x86_64')
url="http://searchandrescue.sourceforge.net/"
license=('GPL2')
depends=('mesa' 'sdl_mixer' 'libxmu' 'libxpm')
source=(
  "http://downloads.sourceforge.net/project/${pkgname}/Program/SearchAndRescue-${pkgver}.tar.gz"
  "http://downloads.sourceforge.net/project/${pkgname}/Data_Files/SearchAndRescue-data-1.3.0.tar.gz"
  "${pkgname}.sh")
md5sums=('9b39ea55eaded48028c5fc6bf9304945'
         '53d6986bd53e7e2b5de0d7bd996a1910'
         '617227e7db32b6f8d3e19f711609b343')

build() {
  cd "${srcdir}/${pkgname}_${pkgver}"
  ./configure Linux --prefix=/usr --disable=Y2 || return 1
  sed -i "s/-DHAVE_LIBXPM/-DHAVE_LIBXPM -DHAVE_SDL_MIXER/" sar/Makefile
  make all || return 1
}

package() {
  cd "${srcdir}/${pkgname}_${pkgver}"

  # Install game files
  sed -i 's_share/games_share_g' sar/Makefile.install.UNIX
  sed -i 's_/games_/bin_g' sar/Makefile.install.UNIX
  make PREFIX="${pkgdir}/usr" install || return 1

  # Install data files
  mkdir -p data
  tar -xzvf "../SearchAndRescue-data-1.3.0.tar.gz" -Cdata
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  cp -r data/* "${pkgdir}/usr/share/${pkgname}"
  find "${pkgdir}/usr/share/${pkgname}" -type d -exec chmod 755 '{}' \;
  find "${pkgdir}/usr/share/${pkgname}" -type f -exec chmod 644 '{}' \;

  # Intstall launcher and .desktop file
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  sed -i "s/SearchAndRescue/searchandrescue/" SearchAndRescue.desktop
  install -Dm644 SearchAndRescue.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
