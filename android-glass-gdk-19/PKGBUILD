# Maintainer: Michael Hansen <zrax0111 gmail com>

_rev=r05
_gdkver=xe17.1
_sdkint=19
pkgname=android-glass-gdk-${_sdkint}
pkgver=${_gdkver}_${_rev}
pkgrel=1
pkgdesc="Google Glass Development Kit Preview, API-${_sdkint}"
arch=('any')
url='https://developers.google.com/glass/develop/gdk/index'
license=('custom')
depends=("android-platform-${_sdkint}")
options=('!strip')
source=("http://dl.google.com/glass/${_gdkver}/google-gdk.zip"
        'source.properties.xz')
sha1sums=('90922353ab510eb1dac7fdb77442c1967e92467e'
          '438e9fe6601378bbab6bf4c955722641ddcb694b')

package() {
    _addons="${pkgdir}/opt/android-sdk/add-ons"
    mkdir -p "${_addons}"
    mv "${srcdir}/google-gdk" "${_addons}/addon-google_gdk-google-${_sdkint}"

    chmod -R ugo+rX "${pkgdir}/opt"

    xz -cd "${srcdir}/source.properties.xz" \
         > "${_addons}/addon-google_gdk-google-${_sdkint}/source.properties"
}
