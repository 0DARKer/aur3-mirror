# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=sssd
pkgver=1.7.0
pkgrel=2
pkgdesc="System Security Services Daemon"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/sssd/"
license=('GPL3')
depends=(
  'bind'          # for nsupdate
  'c-ares'
  'dbus-core'
  'ding-libs'
  'krb5'
  'ldb'
  'libldap'
  'libnl'
  'libsasl'
  'libunistring'
  'libxml2'
  'nspr'
  'nss'
  'pcre'
  'talloc'
  'tdb'
  'tevent'
)
makedepends=(
  'docbook-xsl'
  'doxygen'
)
source=("https://fedorahosted.org/released/$pkgname/$pkgname-$pkgver.tar.gz"
        "https://fedorahosted.org/released/$pkgname/$pkgname-$pkgver.tar.gz.asc"
        'sssd.service'
        'sssd.rcd')
sha1sums=('c46600b6686ef0eabd09bfa07a78fa285f2275e0'
          'f7c805b6d5adb1f10a2ea43152794d9592815f80'
          '10945fa294c5ace62f2dcc7db6091710b46d6ffe'
          'e40e22cecc2cd6d842a040e3266554bff17ba079')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr                             \
    --sysconfdir=/etc                         \
    --localstatedir=/var                      \
    --libexecdir=/usr/lib/sssd                \
    --enable-pammoddir=/lib/security          \
    --with-os=fedora                          \
    --with-pid-path=/run                      \
    --without-python-bindings                 \
    --without-selinux                         \
    --without-semanage                        \
    --with-initscript=systemd                 \
    --with-systemdunitdir=/lib/systemd/system \
    --enable-all-experimental-features        \
    ;
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/etc/rc.d"
  rm -f "$pkgdir/usr/modules/ldb/memberof.la"
  find "$pkgdir/usr/share/man" -depth -type d -exec rmdir --ignore-fail-on-non-empty {} \;
  # The included unit requires way too much sed'ing.
  install -Dm0644 "$srcdir/sssd.service" "$pkgdir/lib/systemd/system/sssd.service"
  install -Dm0755 "$srcdir/sssd.rcd" "$pkgdir/etc/rc.d/sssd"
} 

# vim: ts=2:sw=2:et
