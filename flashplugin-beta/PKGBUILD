# Contributor : Anish Bhatt <anish[removethis][at]gatech[dot]edu>

pkgname=flashplugin-beta
_licensefile='PlatformClients_PC_WWEULA_Combined_20100108_1657.pdf'
pkgver=11.2.202.197
pkgrel=1

pkgdesc='Adobe Flash Player 11 Beta'
url='http://labs.adobe.com/technologies/flashplatformruntimes/flashplayer11/'
arch=('i686' 'x86_64')

source=('http://download.macromedia.com/pub/labs/flashplatformruntimes/flashplayer11-2/flashplayer11-2_p5_install_lin_32_013112.tar.gz'
	mms.cfg
	LICENSE)
md5sums=('1e5fd6e6c688a39ddfadae96fe74f69d'
	 'dcd4454dc4aa8116b4d7a6c3ba159ea4'
	 '05ccdb2de8f04af6641675975a807cf5')
_lib=lib
if [[ $CARCH == x86_64 ]]; then
source[0]=http://download.macromedia.com/pub/labs/flashplatformruntimes/flashplayer11-2/flashplayer11-2_p5_install_lin_64_013112.tar.gz
md5sums[0]=e7a2269b81db8a56eea307c659650855
_lib=lib64
fi
_licensefile='LICENSE'

depends=('mozilla-common' 'libxt' 'gtk2' 'nss' 'curl')
optdepends=('libvdpau: GPU acceleration on Nvidia card')
conflicts=('flashplugin')
provides=('flashplayer' 'flashplugin')
license=('custom')
options=(!strip)
backup=(etc/adobe/mms.cfg)

package() {
  # flash plugin
  install -D -m755 libflashplayer.so ${pkgdir}/usr/lib/mozilla/plugins/libflashplayer.so
 
  # kde kcm plugin & standalone settings executable 
  install -D -m755 ${srcdir}/usr/${_lib}/kde4/kcm_adobe_flash_player.so ${pkgdir}/usr/lib/kde4/kcm_adobe_flash_player.so
  install -D -m755 ${srcdir}/usr/bin/flash-player-properties ${pkgdir}/usr/bin/flash-player-properties

  # desktop entries for kde & gnome
  install -D -m644 ${srcdir}/usr/share/kde4/services/kcm_adobe_flash_player.desktop ${pkgdir}/usr/share/kde4/services/kcm_adobe_flash_player.desktop
  install -D -m644 ${srcdir}/usr/share/applications/flash-player-properties.desktop ${pkgdir}/usr/share/applications/flash-player-properties.desktop

  # icons
  install -D -m644 ${srcdir}/usr/share/pixmaps/flash-player-properties.png ${pkgdir}/usr/share/pixmaps/flash-player-properties.png
  for i in 16 22 24 32 48; do
    install -D -m644 usr/share/icons/hicolor/${i}x${i}/apps/flash-player-properties.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/flash-player-properties.png"
  done

  # license & config file (defaults to hardware offload disabled)
  install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/${_licensefile}
  install -D -m644 mms.cfg ${pkgdir}/etc/adobe/mms.cfg
}


