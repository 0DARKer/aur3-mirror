# Maintainer: Thomas Favre-Bulle <thomas.favre-bulle at bull dot net>
pkgname=nfs-ganesha
pkgver=2.0.0
pkgrel=1
pkgdesc="User space NFS server"
arch=(x86_64)
url="https://github.com/nfs-ganesha/nfs-ganesha/wiki"
license=('LGPL3')
depends=('dbus')
makedepends=('cmake' 'bison' 'flex' 'doxygen' 'krb5' 'dbus' 'xfsprogs' 'nfsidmap' 'libwbclient' 'libcap')
optdepends=('jemalloc: more scalable malloc implementation')
provides=('nfs-ganesha')
source=('https://github.com/nfs-ganesha/nfs-ganesha/archive/V2.0.0/nfs-ganesha-2.0.0.tar.gz'
        'https://github.com/nfs-ganesha/ntirpc/archive/v1.0.0/ntirpc-1.0.0.tar.gz'
        'ganesha.conf'
        'dbus.nfs-ganesha.conf'
        'nfs-ganesha.service')
md5sums=('30335404075b43fd328708aa8ff2d39f'
         '5a7c90879d6ce3d3a927defe3fbd0939'
         '7b40e18ee839f4141babc6476a95a3c2'
         'e5590aea4cd82ff56d2236e253197158'
         'cd2c3e71ae90418fb40ea614feba81e1')

build() {
  # Move libntirpc to ganesha's sources
  # This is only required for the build process
  mv $srcdir/ntirpc-1.0.0/* $srcdir/$pkgname-$pkgver/src/libntirpc
  # Build Ganesha
  cd $srcdir/$pkgname-$pkgver/src

  cmake -DCMAKE_BUILD_TYPE=Release -DBUILD_CONFIG=everything -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/src
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/etc/ganesha"
  install -Dm644 "$srcdir/nfs-ganesha.service" "$pkgdir/usr/lib/systemd/system/nfs-ganesha.service"
  install -Dm644 "$srcdir/ganesha.conf" "$pkgdir/etc/ganesha/ganesha.conf"
  install -Dm644 "$srcdir/dbus.nfs-ganesha.conf" "$pkgdir/etc/dbus-1/system.d/nfs-ganesha.conf"
  install -Dm644 "$srcdir/$pkgname-$pkgver/src/LICENSE.txt" "$pkgdir/usr/share/licences/nfs-ganesha/LICENSE.txt"
}
