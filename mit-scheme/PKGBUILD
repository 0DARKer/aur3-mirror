# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: peter feigl <peter.feigl@gmail.com>

pkgname=mit-scheme
pkgver=9.1.1
pkgrel=1
pkgdesc='MIT/GNU Scheme'
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.gnu.org/software/mit-scheme/'
depends=('glibc' 'ncurses' 'zlib')
optdepends=('openssl: support for openssl')

if [[ $CARCH == i686 ]]; then
  _arch=i386
  md5sums=('e35590a06549d71edba723a719329070')
else
  _arch=x86-64
  md5sums=('268cb5ac97646f34742828ebc370586d')
fi
source=(http://ftp.gnu.org/gnu/$pkgname/stable.pkg/$pkgver/$pkgname-$pkgver-$_arch.tar.gz)

build() {
  cd $pkgname-$pkgver/src
  ./configure --prefix=/usr \
    --with-x \
    --enable-native-code
  make
}

package() {
  cd $pkgname-$pkgver/src
  make DESTDIR="$pkgdir" install
}
