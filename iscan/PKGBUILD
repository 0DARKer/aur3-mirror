# Maintainer: garion < garion @ mailoo.org >
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=iscan
pkgver=2.28.1
pkgrel=3
pkgdesc="A front-end for EPSON scanner/all-in-ones"
arch=('i686' 'x86_64')
url="http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX"
license=('GPL2' 'custom:AVASYSPL')
depends=('gtk2' 'iscan-data' 'sane')
makedepends=('gettext')
conflicts=(${pkgname}-legacy)
install=${pkgname}.install
backup=(etc/sane.d/epkowa.conf)
options=('!libtool')
source=("http://a1227.g.akamai.net/f/1227/40484/1d/download.ebz.epson.net/dsc/f/01/00/01/67/03/17e040cd65ee7f4c64634cc42cb653968a527822/${pkgname}_${pkgver}-3.tar.gz"
        'iscan-2.28.1-libpng15.patch')
md5sums=('df777c77425cde132ed94c13a47eb8d7'
         'fc7db347c301683c01376056898f2792')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export LDFLAGS="${LDFLAGS} -ldl -lpng15"

  # patch for building iscan against libpng15 by giovanni
  patch -Np0 -i "${srcdir}/iscan-2.28.1-libpng15.patch" || return 1

  ./configure --prefix=/usr \
              --enable-dependency-reduction \
              --enable-frontend \
              --enable-jpeg \
              --enable-tiff \
              --enable-png || return 1 #--enable-gimp
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install || return 1

  # install sane file config
  install -d -m755 "$pkgdir/etc/sane.d"
  install -m644 backend/epkowa.conf "$pkgdir/etc/sane.d/epkowa.conf"

  # install desktop file
  install -d -m755 "$pkgdir/usr/share/applications"
  install -m644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  # install license
  install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "non-free/AVASYSPL.en.txt" "$pkgdir/usr/share/licenses/$pkgname/AVASYSPL"
}
