# Maintainer: Frederic Bezies < fredbezies at gmail dot com>
# Contributor: garion < garion @ mailoo.org >
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=iscan
pkgver=2.29.3
pkgrel=6
pkgdesc="A front-end for EPSON scanner/all-in-ones"
arch=('i686' 'x86_64')
url="http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX"
license=('GPL2' 'custom:AVASYSPL')
depends=('gtk2' 'sane' 'libtiff')
optdepends=('iscan-data: Image Scan! for Linux data files')
makedepends=('gettext')
conflicts=(${pkgname}-legacy)
install=${pkgname}.install
backup=(etc/sane.d/epkowa.conf)
options=('!libtool')
source=('http://a1227.g.akamai.net/f/1227/40484/7d/download.ebz.epson.net/dsc/f/01/00/02/77/29/b62954b89585dd0b18ec5c7975ca3f8b9a48b8df/iscan_2.29.3-1.tar.gz'
        'iscan-2.28.1-libpng15.patch')
md5sums=('530138ec9c58e5b2dd21c61c3c90e693'
         'fc7db347c301683c01376056898f2792')


build() {
  cd "$srcdir/$pkgname-$pkgver"
  export LDFLAGS="${LDFLAGS} -ldl -lpng16"

  # patch for building iscan against libpng15 by giovanni
  patch -Np0 -i "${srcdir}/iscan-2.28.1-libpng15.patch"

  ./configure --prefix=/usr \
              --enable-dependency-reduction \
              --enable-frontend \
              --enable-jpeg \
              --enable-tiff \
              --enable-png --disable-gimp
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  # install iscan-registry in /usr/bin
  # it is not really clean, will try something better later :)
  install -D -m655 ${pkgdir}/usr/sbin/iscan-registry "${pkgdir}/usr/bin/iscan-registry"
  rm -rf ${pkgdir}/usr/sbin/

  # install sane file config
  install -d -m755 "$pkgdir/etc/sane.d"
  install -m644 backend/epkowa.conf "$pkgdir/etc/sane.d/epkowa.conf"

  # install desktop file
  install -d -m755 "$pkgdir/usr/share/applications"
  install -m644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  # install license
  install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "non-free/AVASYSPL.en.txt" "$pkgdir/usr/share/licenses/$pkgname/AVASYSPL"
}


