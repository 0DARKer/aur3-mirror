# Contributor: Alessio 'alexwizard' Sergi <sergi dot alessio at gmail dot com>
# Contributor & Maintainer: mutlu_inek <mutlu_inek@yahoo.de>

pkgname=iscan
pkgver=2.26.1
pkgrel=2
pkgdesc="A front-end for EPSON scanner/all-in-ones"
arch=('i686' 'x86_64')
url="http://www.avasys.jp/english/linux_e/dl_scan.html"
license=('GPL2' 'custom:AVASYSPL')
depends=('gtk2' 'sane' 'iscan-data')
makedepends=('pkgconfig')
conflicts=('iscan-plugin-cx4400' 'iscan-legacy')
backup=('etc/sane.d/epkowa.conf')
install=${pkgname}.install
options=('!libtool')
source=(http://linux.avasys.jp/drivers/${pkgname}/${pkgver}/${pkgname}_${pkgver}-3.tar.gz \
        epkowa.conf iscan.png iscan.desktop)
md5sums=('106a40eb5b40bfc53311238ed47c3f07' '8a82eaf3fdc720e6f424ab30a6bae47b'
	 '9dc7e21aae4bd7d1c4ca96cfa3dafaa7' '82a67a6ad8d2464492106797f5acab55')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --enable-jpeg --enable-png \
                --enable-frontend --with-pic || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # install epkowa.conf template, icon and desktop file
  install -Dm644 ${srcdir}/epkowa.conf ${pkgdir}/etc/sane.d/epkowa.conf
  install -Dm644 ${srcdir}/iscan.png ${pkgdir}/usr/share/pixmaps/iscan.png
  install -Dm644 ${srcdir}/iscan.desktop ${pkgdir}/usr/share/applications/iscan.desktop
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/non-free/AVASYSPL.en.txt ${pkgdir}/usr/share/licenses/$pkgname/AVASYSPL
}
