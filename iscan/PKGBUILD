# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=iscan
pkgver=2.28.1
pkgrel=1
pkgdesc="A front-end for EPSON scanner/all-in-ones"
arch=('i686' 'x86_64')
url="http://avasys.jp/eng/linux_driver/"
license=('GPL2' 'custom:AVASYSPL')
depends=('gtk2' 'iscan-data' 'sane')
makedepends=('gettext')
conflicts=(${pkgname}-legacy)
install=${pkgname}.install
options=('!libtool')
source=(http://linux.avasys.jp/drivers/${pkgname}/${pkgver}/${pkgname}_${pkgver}-3.tar.gz)
md5sums=('df777c77425cde132ed94c13a47eb8d7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export LDFLAGS="${LDFLAGS} -ldl -lpng14"

  ./configure --prefix=/usr \
              --enable-dependency-reduction \
              --enable-frontend \
              --enable-jpeg \
              --enable-tiff \
              --enable-png #--enable-gimp
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install sane file config
  install -d -m755 "${pkgdir}/etc/sane.d"
  install -m644 backend/epkowa.conf "${pkgdir}/etc/sane.d/epkowa.conf"

  # install desktop file
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -m644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # install license
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/${pkgname}-${pkgver}/non-free/AVASYSPL.en.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/AVASYSPL"
}

# vim:set ts=2 sw=2 et:
