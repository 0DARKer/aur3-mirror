# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=seafile-client-cli
pkgver=2.1.1
pkgrel=1
pkgdesc="Seafile is an online file storage and collaboration tool. This is seafile cli client for synchronizing your local files with seafile server."
arch=('any')
url="https://github.com/haiwen/seafile/"
license=('GPL')
depends=('seafile-shared>=2.1.1' 'libevent' 'python2-simplejson')
makedepends=('vala' 'sed')
optdepends=()
options=('!libtool' '!emptydirs')
source=("https://github.com/haiwen/seafile/archive/v${pkgver}.tar.gz"
        "0003-Add-autoconfiguration-for-libjansson.patch")
md5sums=('f205f7e4088341a1805a725305b4cd37'
         '64678c3f4ab28955b77876c77d6c62a9')
sha256sums=('f07b09ab1eb1cb33f92cee74e499d0350941624396910a4c1a7d79abb45acaa6'
            '8769e677b6925e0f413b098fca413ed816ce6edcad40390e10de738c49ed05f6')

prepare ()
{
	cd "$srcdir/seafile-${pkgver}"
	patch -p1 -i ${srcdir}/0003-Add-autoconfiguration-for-libjansson.patch
}

build ()
{
	cd "$srcdir/seafile-${pkgver}"
	./autogen.sh
	./configure --disable-server --enable-client --disable-console --prefix=/usr PYTHON=/usr/bin/python2
	make -j1

	# Fix cli client's python 2 requirement
	sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' ${srcdir}/seafile-${pkgver}/app/seaf-cli
}

package ()
{
	# Install cli client
	install -D -m755 ${srcdir}/seafile-${pkgver}/app/seaf-cli ${pkgdir}/usr/bin/seaf-cli

	# Install license
	install -D -m644 ${srcdir}/seafile-${pkgver}/LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE.txt
}
