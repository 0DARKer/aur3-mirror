# Maintainer: jtts
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbase=dconf
pkgname=lib32-$_pkgbase
pkgver=0.14.1
pkgrel=2
pkgdesc="A low-level configuration system (32-bit)."
arch=(x86_64)
url="http://live.gnome.org/dconf"
license=('LGPL2.1')
depends=('lib32-glib2' $_pkgbase) # 'lib32-gtk-update-icon-cache'
makedepends=('gcc-multilib' 'intltool') # 'gtk3' 'vala' 'docbook-xsl'
#optdepends=('gtk3: for dconf-editor')
install=dconf.install
source=(http://download.gnome.org/sources/$_pkgbase/${pkgver%.*}/$_pkgbase-$pkgver.tar.xz)
sha256sums=('59c58c5eafa0ebd616287d078f4441d5342870a44634445d324e88fc0287f0b8')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "$_pkgbase-$pkgver"

  # Remove compilation of man-pages  
  sed -i 's/docs //' Makefile.am Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32/dconf \
    --disable-editor \
    --disable-gtk-doc \
    --disable-gtk-doc-html \
    --disable-gtk-doc-pdf \
    --disable-schemas-compile \
    --disable-nls
    # --enable-gcov

  make
}

package() {
  cd "$_pkgbase-$pkgver"
  make  DESTDIR="$pkgdir" install

  # cleanup for lib32 package
  rm -rf "${pkgdir}"/{etc,bin,sbin,usr/{bin,lib,include,share},var}
}

# vim:set ts=2 sw=2 et:
