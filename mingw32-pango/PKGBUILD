pkgname=mingw32-pango
pkgver=1.28.4
pkgrel=2
arch=(any)
pkgdesc="A library for layout and rendering of text (mingw32)"
depends=('mingw32-cairo>=1.7.6' 'mingw32-glib2>=2.24')
makedepends=(mingw32-gcc)
options=(!strip !buildflags)
license=('LGPL')
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/pango/1.28/pango-${pkgver}.tar.bz2)
sha256sums=('7eb035bcc10dd01569a214d5e2bc3437de95d9ac1cfa9f50035a687c45f05a9f')

build()
{
  cd "$srcdir/pango-$pkgver"

  export CXX=i486-mingw32-g++
  export CFLAGS="-O2 -pipe -march=i686 -mms-bitfields"
  export CXXFLAGS="$CFLAGS"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS PKG_CONFIG_PATH

  sed -i 's/have_libthai=true/have_libthai=false/' configure

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --with-included-modules=yes \
    --with-dynamic-modules=no \
    --disable-introspection

  make
}

package() {
  cd "$srcdir/pango-$pkgver"
  mkdir -p "$pkgdir/usr/i486-mingw32/lib/"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir"
#  sed -i 's/-lfontconfig//' usr/i486-mingw32/lib/pkgconfig/pangocairo.pc
#  sed -i 's/-lfreetype//' usr/i486-mingw32/lib/pkgconfig/pangocairo.pc
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/{share,man}
}

