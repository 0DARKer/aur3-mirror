# Maintainer: spider-mario <spidermario@free.fr>
pkgname=qtdesktopcomponents-git
pkgver=20130322
pkgrel=1
pkgdesc="QtÂ Quick components styled with QStyle"
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/QtDesktopComponents"
license=('custom:BSD')
depends=('qt5-declarative')
makedepends=('git')
provides=('qtdesktopcomponents' 'qt-components-desktop' 'qt-desktop-components')
conflicts=('qtdesktopcomponents' 'qt-components-desktop' 'qt-desktop-components')
replaces=('qt-components-desktop' 'qt-desktop-components')

_gitname=qtdesktopcomponents
_gitroot=https://git.gitorious.org/qt/qtquickcontrols.git

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git fetch
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname" && cd "$_gitname"
  fi

  # newer revisions require Qt 5.1
  git checkout 5fd9dec6595a4c41d789ef4648253525168c04d4

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # fix hard-coded path
  echo DESTDIR="$pkgdir/usr/lib/qt/qml/QtDesktop/" >> src/qtdesktop/qtdesktop.pro

  qmake
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make INSTALL_ROOT="$pkgdir/" install

  install --directory "$pkgdir/usr/share/licenses/$pkgname"
  cp header.BSD "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
