pkgname=c-ares-git
_pkgname=c-ares
pkgver=20120620
pkgrel=2
pkgdesc='C library that performs DNS requests and name resolves asynchronously'
arch=('i686' 'x86_64')
url='http://c-ares.haxx.se/'
license=('custom')
depends=('glibc')
conflicts=('c-ares')
provides=('c-ares')
options=('!libtool')
source=('LICENSE')
sha256sums=('8f7ef984b727cac428431406e5f4b1f66a34abc177f5caee6b22471b95c5a161')

_gitname="c-ares"
_gitroot="git://github.com/bagder/c-ares.git"

build() {

  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # LDFLAGS+=" -Wl,--no-undefined"

  autoreconf -fiv

  sed -i "s|-g0|-g|g" configure

  ./configure --prefix=/usr \
    --disable-static --enable-shared --with-pic \
    --enable-symbol-hiding \
    --with-random=/dev/urandom

  make V=1
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/doc/$_pkgname"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  find . -type f -maxdepth 1 | grep -v "[a-z]" | while read somefile ; do
    install -v "$somefile" "$pkgdir/usr/share/doc/$_pkgname"
  done
}

# vim:set ts=2 sw=2 et:
