# Maintainer: Patrick Burroughs (Celti) <celticmadman@gmail.com>

pkgname=snb-git
pkgver=86a8b97
pkgrel=1
pkgdesc="A simple Unicode-aware hierarchical notebook."
arch=("i686" "x86_64")
url="https://github.com/drbig/snb"
license=('custom:BSD')
depends=('ncurses')
makedepends=('git')
source=("git+https://github.com/drbig/snb.git")
sha256sums=('SKIP')

pkgver() {
	cd snb
	git describe --tags --always --dirty --match "[0-9A-Z]*.[0-9A-Z]*"
}

prepare() {
	cd snb

	# Open the help file by default if no arguments are specified.
	sed -i src/user.h -e 's/\/\/#define DEFAULT_FILE.*/#define DEFAULT_FILE    "\/usr\/share\/docs\/snb\/help.md"/'
}

build() {
	cd snb
	make debug
}

package() {
	cd snb
	install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/snb-git/LICENSE"
	install -Dm644 help.md "$pkgdir/usr/share/docs/snb/help.md"
	install -Dm755 bin/snb "$pkgdir/usr/bin/snb"
}
