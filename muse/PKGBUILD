# Contributor: damir <damir@archlinux.org>

pkgname=muse
pkgver=1.1
pkgrel=1
pkgdesc="A MIDI/Audio sequencer with recording and editing capabilities"
arch=('i686' 'x86_64')
url="http://muse-sequencer.org/"
license=('GPL')
depends=('qt3' 'fluidsynth' 'graphviz' 'libsamplerate')
makedepends=('jade')
options=('!libtool')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/lmuse/$pkgname-$pkgver.tar.gz")
md5sums=('ddbba9b8b92948643e5e2013910c5c6e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  . /etc/profile.d/qt3.sh
  unset LDFLAGS
  with_python=no ./configure --prefix=/usr \
		    --disable-doxy-treeview \
		    --enable-optimize \
		    --enable-vst \
		    --disable-suid-build 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
