# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
pkgname=anura-git
pkgver=0.0.91.154ef74
pkgrel=1
pkgdesc="The technology behind the 2D platformer frogatto. But it is so much more."
arch=(i686 x86_64)
url="http://anura-engine.github.io/anura/"
license=('GPL')
groups=()
depends=(libgl mesa glew sdl sdl_image sdl_ttf sdl_mixer libpng boost-libs)
optdepends=('frogatto-git: the default game module')
makedepends=('git')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
source=(git+https://github.com/anura-engine/anura.git
        anura.sh)
noextract=()
md5sums=(SKIP
         be92b369692e16bbd5847c5e7e238310)

_gitname=anura

pkgver() {
  cd $_gitname
  _tag=$(git describe 2>/dev/null)
  if [ -n $_tag ]
  then
    # Use the tag of the last commit
    echo $_tag | sed 's|-|.|g'
  else
    # The project currently has no tags yet
    echo 0.0.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  fi
}

build() {
  cd $_gitname
  sed -i 's/ccache //g' Makefile
  sed -i 's/-lprofiler//g' Makefile
  sed -i 's/-lboost_\(regex\|system\|thread\|iostreams\)-mt/-lboost_\1/g' Makefile
  make
}

package() {
  install -D -m755 anura.sh $pkgdir/usr/bin/anura-git
  cd $_gitname

  install -D -m755 game $pkgdir/usr/lib/anura-git/game

  _installdir=$pkgdir/usr/share/anura-git
  mkdir -p $_installdir
  cp -r data $_installdir
  cp -r images $_installdir
  cp -r music $_installdir
  mkdir -p $_installdir/modules
}

# vim:set ts=2 sw=2 et:
