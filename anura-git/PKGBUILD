# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
pkgname=anura-git
pkgver=0.0.301.g9832727
pkgrel=1
pkgdesc="The technology behind the 2D platformer frogatto. But it is so much more."
arch=(i686 x86_64)
url="http://anura-engine.github.io/anura/"
license=('GPL')
depends=(libgl mesa glew glm sdl2-hg sdl2-image-hg sdl2-ttf-hg sdl2-mixer-hg libpng boost-libs)
optdepends=('frogatto-git: the default game module'
            'box2d: box2d physics')
makedepends=('git')
source=(git+https://github.com/anura-engine/anura.git
        anura.sh)
md5sums=(SKIP
         af771788c0bd1a39bf576a5e2a84edad)
install=anura.install

_gitname=anura

pkgver() {
  cd $_gitname
  if _tag=$(git describe 2>/dev/null)
  then
    # Use the tag of the last commit
    echo $_tag | sed 's|-|.|g'
  else
    # The project currently has no tags yet
    echo 0.0.$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  fi
}

build() {
  cd $_gitname

  # USE_CCACHE=no to honor makepkg's ccache setting.
  make USE_CCACHE=no
}

package() {
  install -D -m755 anura.sh $pkgdir/usr/bin/anura-git
  cd $_gitname

  install -D -m755 game $pkgdir/usr/lib/anura-git/game

  _installdir=$pkgdir/usr/share/anura-git
  mkdir -p $_installdir
  cp -r data $_installdir
  cp -r images $_installdir
  cp -r music $_installdir
  cp -r modules $_installdir
}

# vim:set ts=2 sw=2 et:
