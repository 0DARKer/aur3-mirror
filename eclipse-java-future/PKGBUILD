# $Id$
# Maintainer: Chris Fordham <chris [at] fordham [hyphon] nagy [dot] id [dot] au> aka flaccid
# Package Build Source: https://github.com/flaccid/archlinux-packages/blob/master/eclipse-java-future/PKGBUILD

_eclipse_name=mars
_eclipse_release=M5
_eclipse_timestamp=201202080800

# releases:
# http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/release.xml

# mirror path:
# /technology/epp/downloads/release/mars/M3/eclipse-java-mars-M3-linux-gtk-x86_64.tar.gz

pkgname=eclipse-java-future
_pkgname=eclipse-java
pkgver="4.5.0$_eclipse_release"
pkgrel=1
pkgdesc="Eclipse IDE for Java Developers (future release: $_eclipse_release)."
arch=('i686' 'x86_64')
url="http://www.eclipse.org"
license=("EPL")
depends=('java-environment>=6' 'gtk3' 'xulrunner>=1.9.0.10')
makedepends=('unzip' 'patch')
provides=('eclipse')
conflicts=('eclipse' 'eclipse-java')
replaces=('eclipse' 'eclipse-java')
changelog=${pkgname}.ChangeLog.markdown
install=${_pkgname}.install

source=(
  http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/eclipse-java-${_eclipse_name}-${_eclipse_release}-linux-gtk-x86_64.tar.gz
  eclipse-java.sh
  eclipse-java.desktop
  eclipse-java.svg
)

sha256sums=('0d6530d83cebca7f3ba1d11b7ea0cc4691a19c881a911d3130e43a703a40cf3e'
            '6dcb02c5282ea11e661f2dfda95193b1622b0703b10fe14a6a6ab76e9bf3a5bf'
            '4eb2189c96fcfa340886b049b34dc3636d7b2bfa865140dc72edb61455d900c3'
            '6fe3ab198af244f9c8c2463b6837855506e811f61e5fd8ac7c9d5fe348830a14')

# override source for 32-bit
[ "$CARCH" = "i686" ] && source[0]="http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/eclipse-java-${_eclipse_name}-${_eclipse_release}-linux-gtk.tar.gz"
[ "$CARCH" = "i686" ] && sha256sums[0]='2d02ce482e6d2a7838a243bac0877e200cd061c1ac5def8a2f4ca659cc9e1a17'

package() {
	local _icon_path=/usr/share/eclipse-java/plugins/org.eclipse.platform_${pkgver}.v${_eclipse_timestamp}

	install -m755 -d $pkgdir/usr/{bin,share/applications}
	install -m755 -d $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48,scalable}/apps

 	cd "$srcdir"
  	mv eclipse "$pkgdir/usr/share/eclipse-java"

	install -D -m755 eclipse-java.sh $pkgdir/usr/bin/eclipse-java
	install -D -m644 eclipse-java.desktop $pkgdir/usr/share/applications
	install -D -m644 eclipse-java.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/eclipse-java.svg

  ln -s ${_icon_path}/eclipse.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse-java.png
	ln -s ${_icon_path}/eclipse32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse-java.png
	ln -s ${_icon_path}/eclipse48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse-java.png
}

# vim:set ts= 2 sw=2 Et:
