# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=steins-gate
pkgver=0.3.1
pkgrel=1
arch=('i686' 'x86_64')
license=('MIT' 'LGPL3' 'GPL3')
pkgdesc='Libre reimplementation of the Steins;Gate visual novel'
url='http://dev.pulsir.eu/krofna/'
depends=('boost-libs' 'sfml' 'gst-plugins-base-libs' 'ttf-arphic-uming')
makedepends=('cmake' 'boost')
source=(libnpa-$pkgver.tar.gz::"https://github.com/FGRE/libnpa/archive/v$pkgver.tar.gz"
        libnpengine-$pkgver.tar.gz::"https://github.com/FGRE/libnpengine/archive/v$pkgver.tar.gz"
        $pkgname-$pkgver.tar.gz::"https://github.com/FGRE/steins-gate/archive/v$pkgver.tar.gz")
sha256sums=('76d7ee2365b7a02309ee96b85d737a9ad017edcfdbe72b5070d13bae9f941dc0'
            '167c15e82593b2c6aafc28e3db010664315717e4e32553b8d0e3edf7c7325f98'
            '730284fc7eda5a207bd8315292477b80e33d2f6fe828d292ef4485e9494cb4d8')

prepare() {
  for _name in libnpa libnpengine steins-gate; do
    # shuffle the directories
    rm -rf "$srcdir"/$_name
    mv "$srcdir"/$_name-$pkgver "$srcdir"/$_name
  done

  # set font path
  sed 's|cjkuni-uming|TTF|' -i libnpengine/src/nsbinterpreter.cpp
}

build() {
  for _name in libnpa libnpengine steins-gate; do
    msg2 "Building $_name..."
    cd "$srcdir"/$_name

    cmake . -DCMAKE_INSTALL_PREFIX=/usr
    make
  done
}

package() {
  for _name in libnpa libnpengine steins-gate; do
    msg2 "Installing $_name..."
    cd "$srcdir"/$_name

    make DESTDIR="$pkgdir/" install

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-$_name
  done
}
