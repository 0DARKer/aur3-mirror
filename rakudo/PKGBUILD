# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo
pkgver=2013.02
pkgrel=2
pkgdesc="Perl6 compiler for the Parrot virtual machine"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp>=2013.02.1')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/rakudo-star-$pkgver.tar.gz)
md5sums=('d23528c453ef51ab339b7a102a421de7')

build() {
    cd "$srcdir/$pkgname-star-$pkgver"
    perl Configure.pl --prefix=/usr \
                      --with-nqp=/usr/bin/nqp
    make
}
package() {
    cd "$srcdir/$pkgname-star-$pkgver"

    make DESTDIR="$pkgdir" install

    # remove references to $pkgdir
    find "$pkgdir" -iname '*.pir' -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +
}
