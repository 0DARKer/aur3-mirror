# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo
pkgver=2012.10
pkgrel=2
pkgdesc="Perl6 compiler for the Parrot virtual machine"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp>=2012.10')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(https://github.com/downloads/$pkgname/star/$pkgname-star-$pkgver.tar.gz)
md5sums=('c77d863402e00f4b43762b666253b509')

build() {
    cd "$srcdir/$pkgname-star-$pkgver"
    perl Configure.pl --prefix=/usr \
                      --with-nqp=/usr/bin/nqp
    make
}
package() {
    cd "$srcdir/$pkgname-star-$pkgver"

    # thanks to http://hoelz.ro/blog/building-rakudo-perl-6-for-arch-linux
    # for the following lines
    export PERL6LIB="$pkgdir$(parrot_config libdir)$(parrot_config versiondir)/languages/perl6/lib"
    unlink Perl6 || true
    ln -s "$pkgdir$(parrot_config libdir)$(parrot_config versiondir)/languages/nqp/lib/Perl6" .
    export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$pkgdir$(parrot_config libdir)$(parrot_config versiondir)/dynext"

    make DESTDIR="$pkgdir" install
    mv "$pkgdir"/usr/man "$pkgdir"/usr/share/man

    # remove references to $pkgdir
    find "$pkgdir" \( -iname '*.pir' -o -name 'panda' -o -name 'ufo' \) \
        -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +
}

