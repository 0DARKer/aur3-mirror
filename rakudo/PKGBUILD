# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo
pkgver=2012.09
pkgrel=2
pkgdesc="Perl6 compiler for the Parrot virtual machine"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp>=2012.09')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(https://github.com/downloads/$pkgname/star/$pkgname-star-$pkgver.tar.gz)
md5sums=('89b41ecf730c91404b6ec27a214e42e7')

build() {
    cd "$srcdir/$pkgname-star-$pkgver"
    perl Configure.pl --prefix=/usr \
                      --with-nqp=/usr/bin/nqp
    make
}
package() {
    cd "$srcdir/$pkgname-star-$pkgver"

    # thanks to http://hoelz.ro/blog/building-rakudo-perl-6-for-arch-linux
    # for the following lines
    export PERL6LIB="$pkgdir$(parrot_config libdir)$(parrot_config versiondir)/languages/perl6/lib"
    unlink Perl6 || true
    ln -s "$pkgdir$(parrot_config libdir)$(parrot_config versiondir)/languages/nqp/lib/Perl6" .
    export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$pkgdir$(parrot_config libdir)$(parrot_config versiondir)/dynext"

    make DESTDIR="$pkgdir" install
    mv "$pkgdir"/usr/man "$pkgdir"/usr/share/man
}

