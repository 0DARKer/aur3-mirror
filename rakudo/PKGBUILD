# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo
pkgver=2013.05
pkgrel=1
pkgdesc="Perl6 compiler for the Parrot virtual machine"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp>=2013.05')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/rakudo-star-$pkgver.tar.gz)
sha512sums=('98314886df30928129eb4c21c527736ca74307425cbcd52126c25c7ac3a96dc8fa7e61c9d869ea33fa419ee8682d11e8f9fdbfd37da40b3fd4c87b4f5beffc1f')

build() {
  cd "$pkgname-star-$pkgver"
  perl Configure.pl --prefix=/usr \
                      --with-nqp=/usr/bin/nqp
  make
}

package() {
  cd "$pkgname-star-$pkgver"

  # thanks to http://hoelz.ro/blog/building-rakudo-perl-6-for-arch-linux
  # for the following lines
  export PERL6LIB="$pkgdir$(parrot_config libdir)$(parrot_config versiondir)/languages/perl6/lib"
  unlink Perl6 || true
  ln -s "$pkgdir$(parrot_config libdir)$(parrot_config versiondir)/languages/nqp/lib/Perl6" .
  export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$pkgdir$(parrot_config libdir)$(parrot_config versiondir)/dynext"

  make DESTDIR="$pkgdir" install

  # remove references to $pkgdir
  find "$pkgdir" -iname '*.pir' -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +
}
