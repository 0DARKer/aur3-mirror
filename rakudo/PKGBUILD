# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
pkgname=rakudo
pkgver=2013.12
pkgrel=1
pkgdesc="Perl6 compiler for the Parrot virtual machine"
arch=('i686' 'x86_64')
url='http://rakudo.org/'
license=(PerlArtistic)
depends=('nqp>=2013.12')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/star/rakudo-star-$pkgver.tar.gz)
sha512sums=('51b76189d0bc8154b12be2fe556e2876d1bcb53130cc694b28ccf65936792164ec95336b3474897bcd24422ce6259d46523808c1f0c46373ed2f72c155540500')

build() {
  cd "$pkgname-star-$pkgver"
  perl Configure.pl --prefix=/usr --backends=parrot
  make
}

package() {
  cd "$pkgname-star-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove references to $pkgdir
  find "$pkgdir" -iname '*.pir' -exec env strip="$pkgdir" perl -pe 's/\Q$ENV{strip}//g' -i {} +
}
