# Maintainer: Perberos <perberos@gmail.com>
# Contributor: Spyros Stathopoulos <foucault.online@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
pkgname=mate-file-archiver
pkgver=20120308
pkgrel=1
pkgdesc="Archive manipulator for MATE"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'mate-conf' 'hicolor-icon-theme')
makedepends=('git' 'mate-doc-utils' 'intltool')
conflicts=()
optdepends=('unrar: for RAR uncompression'
            'zip: for ZIP archives' 'unzip: for ZIP archives'
            'p7zip: 7zip compression utility' 'arj: for ARJ archives'
            'unace: extraction tool for the proprietary ace archive format')
options=('!libtool' '!emptydirs')
install=mate-file-archiver.install
url="http://matsusoft.com.ar/projects"
groups=('mate-extras')
source=()
md5sums=()

_gitroot=git://github.com/mate-desktop/mate-file-archiver.git
_gitname=mate-file-archiver

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"

	#mate-doc-prepare --force
	#autoreconf -i
	#aclocal

	./autogen.sh --prefix=/usr \
		--disable-static \
		--localstatedir=/var \
		--libexecdir=/usr/lib/${pkgname} \
		--disable-scrollkeeper \
		--disable-schemas-install \
		--sysconfdir=/etc \
		--disable-packagekit
	make
}

package() {
	cd "$srcdir/$_gitname-build"
	
	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
	install -d -m755 "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} "${pkgdir}/etc/mateconf/schemas/*.schemas"
	rm -f "${pkgdir}/etc/mateconf/schemas/*.schemas"
}

# vim:set ts=2 sw=2 et:
