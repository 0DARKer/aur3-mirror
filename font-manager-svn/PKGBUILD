# Maintainer: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=font-manager-svn
_svnname=font-manager
pkgver=r276
pkgrel=1
pkgdesc="A simple font management application for Gtk+ Desktop Environments"
url="http://code.google.com/p/font-manager/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgee' 'libxml2' 'fontconfig' 'sqlite' 'gucharmap' 'file-roller')
makedepends=('subversion' 'vala-git')
install=font-manager.install
provides=('font-manager')
conflicts=('font-manager')
source=("${_svnname}::svn+http://font-manager.googlecode.com/svn/trunk/")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${_svnname}
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd ${srcdir}/${_svnname}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${_svnname}

  # disable schemas compile
  sed -i "s|glib-compile-schemas|#glib-compile-schemas|" resources/Makefile
  # fix destop file install path
  sed -i "s|desktop-file-install|desktop-file-install --dir=$\(DESTDIR\)$\(datadir\)\/applications|" resources/Makefile

  make DESTDIR=${pkgdir} install
}
