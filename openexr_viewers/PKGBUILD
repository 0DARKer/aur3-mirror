# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=openexr_viewers
pkgver=2.0.1
pkgrel=1
pkgdesc="exrdisplay and playexr OpenEXR image viewers"
arch=('i686' 'x86_64')
url="http://www.openexr.com/"
license=('custom')
depends=('fltk' 'freeglut' 'libgl' 'openexr')
# this makepepend is optional but without it playexr is not built
makedepends=('nvidia-cg-toolkit')
options=()
source=(http://download.savannah.nongnu.org/releases/openexr/$pkgname-$pkgver.tar.gz)
md5sums=('983627bc8c6fabfb026895a05d7c613b')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr --disable-static

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  # pdf documentation is enough
  rm "$pkgdir"/usr/share/doc/OpenEXR_Viewers-$pkgver/OpenEXRViewers.odt
}

# vim:set ts=2 sw=2 et:
