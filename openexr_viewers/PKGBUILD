# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=openexr_viewers
pkgver=1.0.2
pkgrel=1
pkgdesc="exrdisplay and playexr OpenEXR image viewers"
arch=('i686' 'x86_64')
url="http://www.openexr.com/"
license=('custom')
depends=('fltk' 'freeglut' 'libgl' 'openexr')
# this makepepend is optional but then playexr will not be build
makedepends=('nvidia-cg-toolkit')
options=()
source=(http://download.savannah.nongnu.org/releases/openexr/$pkgname-$pkgver.tar.gz \
        gcc-4.4_fix.diff)
md5sums=('5d4c74ebcaf942267b0353e4c53f8b9e'
         'e7a1cc2c0e6f330ec2f5151d7038c2c3')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  # patch for gcc 4.4
  patch -Np1 < "$srcdir"/gcc-4.4_fix.diff || true

  ./configure --prefix=/usr --disable-static

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  # pdf documentation is enough
  rm "$pkgdir"/usr/share/doc/OpenEXR_Viewers-$pkgver/OpenEXRViewers.odt
}

# vim:set ts=2 sw=2 et:
