pkgname=sks
pkgver=1.1.4
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc='Synchronizing OpenPGP Key Server'
makedepends=('ocaml' 'db')
url='https://bitbucket.org/skskeyserver/sks-keyserver/'
install='sks.install'
backup=('etc/conf.d/sks'
        'etc/sks/sksconf'
        'etc/sks/forward.exim'
        'etc/sks/forward.postfix'
        'etc/sks/mailsync'
        'etc/sks/membership'
        'etc/sks/procmail')
source=("https://bitbucket.org/skskeyserver/sks-keyserver/downloads/sks-${pkgver}.tgz"
        'sks.conf.d'
        'sks.init'
        'http://helios.planetcyborg.de/files/aur/sks/sks_web.tar.gz'
        '500_debian_fhs.patch'
        "http://archive.ubuntu.com/ubuntu/pool/universe/s/sks/sks_1.1.3-1ubuntu3.debian.tar.gz")
md5sums=('cdd2b4e9467bac24dfb9d97f515b1b29'
         '04cf445e39de1446e604aca204832fc7'
         '81602c740f236f6090309dfbf1262aa9'
         '8d421d71ef3e4add73c30ba8ad7f5b25'
         '9cf5495b95e84ed91788c04c9ce1b8c1'
         '274a4045df054c4f65216eeb283d0cb7')

build() {
  cd $pkgname-$pkgver

  # patch path
  patch -Np1 -i ${srcdir}/500_debian_fhs.patch

  cp Makefile.local.unused Makefile.local
  sed -i -e 's#LIBDB=-ldb-4.6#LIBDB=-ldb-5.3#g' ${srcdir}/${pkgname}-${pkgver}/Makefile.local
  sed -i -e "s#/usr/local#$pkgdir/usr#g" Makefile.local
  sed -i -e "s#/usr/share/man#$pkgdir/usr/share/man#g" Makefile.local

  unset MAKEFLAGS
  unset CFLAGS
  unset LDFLAGS
  make dep
  make CFLAGS="$CFLAGS -I`ocamlc -where` -I ." all
}

package() {
  cd $pkgname-$pkgver

  make PREFIX="$pkgdir/usr" MANDIR="$pkgdir/usr/share/man" install

  install -m755 -D $srcdir/${pkgname}.init $pkgdir/etc/rc.d/$pkgname
  install -m644 -D $srcdir/${pkgname}.conf.d $pkgdir/etc/conf.d/$pkgname

  mkdir -p $pkgdir/etc/sks $pkgdir/var/lib/sks
  cp $srcdir/debian/debcfg/* $pkgdir/etc/sks
  sed -i -e "s#/usr/lib/sendmail#/usr/sbin/sendmail#g" $pkgdir/etc/sks/sksconf

  cp -R $srcdir/www $pkgdir/var/lib/sks
}
