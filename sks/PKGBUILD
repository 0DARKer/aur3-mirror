pkgname=sks
pkgver=1.1.3
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc='Synchronizing OpenPGP Key Server'
makedepends=('ocaml' 'db')
url='https://code.google.com/p/sks-keyserver/'
install='sks.install'
backup=('etc/conf.d/sks'
        'etc/sks/sksconf'
        'etc/sks/forward.exim'
        'etc/sks/forward.postfix'
        'etc/sks/mailsync'
        'etc/sks/membership'
        'etc/sks/procmail')
source=("https://bitbucket.org/skskeyserver/sks-keyserver/downloads/sks-${pkgver}.tgz"
        'sks.conf.d'
        'sks.init'
        'http://wiresphere.de/files/sks_web.tar.gz'
        'sks-libdb5.3.patch'
	"http://archive.ubuntu.com/ubuntu/pool/universe/s/sks/sks_${pkgver}-1ubuntu2.debian.tar.gz")
md5sums=('d1820c7c1a1e0d0eb7825db4e8003f03'
         '04cf445e39de1446e604aca204832fc7'
         '81602c740f236f6090309dfbf1262aa9'
         '8d421d71ef3e4add73c30ba8ad7f5b25'
         'd4e1abc764404f39f79ea5b28000a1b1'
         '027c3d328b61032b1cbf277164f1324a')

build() {
  cd $pkgname-$pkgver

  # got from fedora
  patch -Np1 -i ${srcdir}/sks-libdb5.3.patch

  # got from ubuntu
  patch -Np1 -i ${srcdir}/debian/patches/500_debian_fhs.patch
  patch -Np1 -i ${srcdir}/debian/patches/511_gcc44.patch

  cp Makefile.local.unused Makefile.local
  sed -i -e 's#/usr/include#/usr/include/db4.6#g' Makefile.local
  sed -i -e "s#/usr/local#$pkgdir/usr#g" Makefile.local
  sed -i -e "s#/usr/share/man#$pkgdir/usr/share/man#g" Makefile.local

  make dep
  make CFLAGS="$CFLAGS -I`ocamlc -where` -I ." all
}

package() {
  cd $pkgname-$pkgver

  make PREFIX="$pkgdir/usr" MANDIR="$pkgdir/usr/share/man" install

  install -m755 -D $srcdir/${pkgname}.init $pkgdir/etc/rc.d/$pkgname
  install -m644 -D $srcdir/${pkgname}.conf.d $pkgdir/etc/conf.d/$pkgname

  mkdir -p $pkgdir/etc/sks $pkgdir/var/lib/sks
  cp $srcdir/debian/debcfg/* $pkgdir/etc/sks
  sed -i -e "s#/usr/lib/sendmail#/usr/sbin/sendmail#g" $pkgdir/etc/sks/sksconf

  cp -R $srcdir/www $pkgdir/var/lib/sks
}
