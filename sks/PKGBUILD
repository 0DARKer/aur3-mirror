pkgname=sks
pkgver=1.1.4
pkgrel=3
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc='Synchronizing OpenPGP Key Server'
makedepends=('ocaml' 'db')
url='https://bitbucket.org/skskeyserver/sks-keyserver/'
install='sks.install'
backup=('etc/conf.d/sks'
        'etc/sks/sksconf'
        'etc/sks/forward.exim'
        'etc/sks/forward.postfix'
        'etc/sks/mailsync'
        'etc/sks/membership'
        'etc/sks/procmail')
source=("https://bitbucket.org/skskeyserver/sks-keyserver/downloads/sks-${pkgver}.tgz"
        'sks.conf.d'
        'sks.init'
        '500_debian_fhs.patch'
        'sks-db.service'
        'sks-recon.service')
md5sums=('cdd2b4e9467bac24dfb9d97f515b1b29'
         '04cf445e39de1446e604aca204832fc7'
         '81602c740f236f6090309dfbf1262aa9'
         '9cf5495b95e84ed91788c04c9ce1b8c1'
         '96e26a8c9d76c16c8159f76cb6071a47'
         'e012a871a27a50623e1039751ca07aab')

build() {
  cd $pkgname-$pkgver

  # patch path
  patch -Np1 -i "${srcdir}/500_debian_fhs.patch"

  cp Makefile.local.unused Makefile.local
  sed -i -e 's#LIBDB=-ldb-4.6#LIBDB=-ldb-5.3#g' Makefile.local
  sed -i -e "s#/usr/local#$pkgdir/usr#g" Makefile.local
  sed -i -e "s#/usr/share/man#$pkgdir/usr/share/man#g" Makefile.local

  unset MAKEFLAGS
  make dep
  make CFLAGS="$CFLAGS -I`ocamlc -where` -I ." all
}

package() {
  cd "$pkgname-$pkgver"

  make PREFIX="$pkgdir/usr" MANDIR="$pkgdir/usr/share/man" install

  install -Dm755 "$srcdir/${pkgname}.init" "$pkgdir/etc/rc.d/$pkgname"
  install -Dm644 "$srcdir/${pkgname}.conf.d" "$pkgdir/etc/conf.d/$pkgname"
  install -Dm644 "$srcdir/sks-db.service" "$pkgdir/usr/lib/systemd/system/sks-db.service"
  install -Dm644 "$srcdir/sks-recon.service" "$pkgdir/usr/lib/systemd/system/sks-recon.service"

  mkdir -p "$pkgdir/etc" "$pkgdir/var/lib/sks"

  cp -r sampleWeb/OpenPKG "$pkgdir/var/lib/sks"
  cp -r sampleConfig/debian "$pkgdir/etc/sks"

  sed -i -e 's#/usr/lib/sendmail#/usr/sbin/sendmail#g' "$pkgdir/etc/sks/sksconf"
}
