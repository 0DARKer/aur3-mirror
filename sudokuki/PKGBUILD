# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=sudokuki
pkgver=1.1.3
pkgrel=1
pkgdesc="A free graphical sudoku game written in Java"
arch=('any')
url="http://sudokuki.sourceforge.net/"
license=('GPL3')
depends=('java-runtime')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-binary.zip"
        "${pkgname}.desktop"
        "${pkgname}.sh")
md5sums=('9c6d5cfd220c2c96ea10b54817d113c3'
         'fa5cdb2563d430ff7a5df1df01e3cf66'
         '11bbe5398c79de98e7bf1e9dc604a06e')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-binary"

  # Install a launcher
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Install game files
  install -Dm644 "${pkgname}-${pkgver}.jar" "${pkgdir}/usr/share/${pkgname}/${pkgname}-${pkgver}.jar"
  install -Dm644 libsuexg_proxy.so "${pkgdir}/usr/lib/libsuexg_proxy.so"

  # Install a desktop entry
  bsdtar -xOf "${pkgname}-${pkgver}.jar" \
    net/jankenpoi/sudokuki/resources/images/logo.png > logo.png
  install -Dm644 "logo.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 NEWS README "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
