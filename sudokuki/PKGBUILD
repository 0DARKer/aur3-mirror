# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=sudokuki
pkgver=1.1.4
pkgrel=1
pkgdesc="A free graphical sudoku game written in Java"
arch=('any')
url="http://sudokuki.sourceforge.net/"
license=('GPL3')
depends=('java-runtime')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-binary.zip"
        "${pkgname}.desktop"
        "${pkgname}.sh")
md5sums=('12d144bb743db9d7797188e4f1e70799'
         '976422d63a1052bb2110e6e8e31cb5de'
         'a8b0736aeb5af633588aad2b486c643f')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-binary"

  # Install a launcher
  install -Dm755 ../${pkgname}.sh "${pkgdir}/usr/bin/${pkgname}"

  # Install game files
  install -Dm644 ${pkgname}-${pkgver}.jar "${pkgdir}/usr/share/${pkgname}/${pkgname}-${pkgver}.jar"
  install -Dm644 libsuexg_proxy.so "${pkgdir}/usr/lib/libsuexg_proxy.so"

  # Install a desktop entry
  bsdtar -xOf ${pkgname}-${pkgver}.jar \
    net/jankenpoi/sudokuki/resources/images/logo.png > logo.png
  install -Dm644 logo.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 NEWS README "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
