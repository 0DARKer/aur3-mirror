# Maintainer: Trizen <echo dHJpemVueEBnbWFpbC5jb20K | base64 -d>

_pkgname=youtube-viewer
pkgname=gtk-youtube-viewer
pkgver=3.1.0.15.ga789899
pkgrel=1
pkgdesc="A Gtk2 application for searching and streaming videos from YouTube."
arch=('any')
url="https://github.com/trizen/youtube-viewer"
license=('GPLv3')
makedepends=('git')

provides=('youtube-viewer')
conflicts=('youtube-viewer')

depends=('perl>=5.16.0' 'perl-data-dump' 'perl-libwww' 'gtk2-perl' 'perl-file-sharedir')

optdepends=(
            'perl-xml-fast: faster XML to HASH conversion'
            'perl-text-charwidth: to print the results in a fixed-width format (-W)'
            'perl-term-readline-gnu: for better STDIN support'
            'perl-lwp-protocol-https: for login and https support'
            'gcap: for retrieving YouTube closed captions. [in AUR]'
            'wget: for downloading videos with wget'
            'mplayer: for playing the videos with MPlayer'
            'vlc: for playing the videos with VLC'
            'mpv: for playing the videos with MPV'
            )

source=('git://github.com/trizen/youtube-viewer.git')
md5sums=('SKIP')

pkgver() {
    cd $_pkgname
    git describe --always | sed -e 's|-|.|g'
}

package() {
    cd "$_pkgname/WWW-YoutubeViewer"
    perl Build.PL --destdir "$pkgdir" --installdirs vendor --gtk-youtube-viewer
    ./Build
    ./Build test
    ./Build install --install_path script=/usr/bin

    install -Dm 644 "share/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm 644 "share/icons/$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
