# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libdbusmenu
pkgver=0.6.1
pkgrel=1
pkgdesc="Small little library that passes a menu structure across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('LGPL')
depends=('glib2')
makedepends=('gtk2' 'intltool' 'gnome-doc-utils' 'gobject-introspection' 'vala' 'python2')
optdepends=('python2: for dbusmenu-dumper tool'
            'gtk2: for dbusmenu-bench tool')
options=('!libtool' '!emptydirs')
source=(http://launchpad.net/dbusmenu/0.6/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('820b6999dd1008328bfa442575d859a1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/dbusmenu-bench

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-static --disable-scrollkeeper --disable-tests --disable-gtk
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -C libdbusmenu-glib DESTDIR="$pkgdir/" install
  make -C tools DESTDIR="$pkgdir/" install
  make -C docs DESTDIR="$pkgdir/" install
}
