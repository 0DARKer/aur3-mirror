# Maintainer: M Rawash <mrawash@gmail.com>

pkgname=textadept-beta
_pkgname=textadept
pkgver=4.0
pkgrel=1
pkgdesc="A ridiculously fast and extensible text editor - beta version."
arch=('i686' 'x86_64')
url="http://code.google.com/p/textadept"
license=('MIT')
depends=('gtk2' 'lua')
makedepends=('unzip')
provides=('textadept')
install=${pkgname}.install
source=(http://textadept.googlecode.com/files/${_pkgname}_${pkgver}_beta_2.src.zip
        textadept.desktop
        pkgbuild.lua)
md5sums=('68e21531aabdec612d88ccf1af6cbb2d'
         '483d700a3dd95f84754bd61ae524dc74'
         'f0b2f243460b231dbeb6f547d8c1822b')

build() {
  cd "$srcdir/${_pkgname}_${pkgver}_beta_2"

  cd src/scintilla/gtk
  make || return 1

  cd ../../
  sed -i 's/\/usr\/local\//\/usr\//' lua/include/luaconf.h
  make  || return 1

  #there's no target install - installing manually
  mkdir -p ${pkgdir}/usr/{bin,share}
  cp -r  ${srcdir}/${_pkgname}_${pkgver}_beta_2 ${pkgdir}/usr/share/${pkgname}
  ln -sf /usr/share/${pkgname}/textadept ${pkgdir}/usr/bin/${pkgname}
  install -Dm644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  #now clean this mess
  rm -r ${pkgdir}/usr/share/${pkgname}/{src,LICENSE}

  #make ta pkgbuild friendly
  install -Dm644 $srcdir/pkgbuild.lua $pkgdir/usr/share/${pkgname}/lexers/pkgbuild.lua
  echo -e "\n% PKGBUILD\nPKGBUILD pkgbuild\ninstall pkgbuild" >> \
        $pkgdir/usr/share/${pkgname}/modules/textadept/mime_types.conf

  #freedesktop.org stuff
  install -Dm644 ${pkgdir}/usr/share/${pkgname}/core/images/ta_128x128.png \
			${pkgdir}/usr/share/pixmaps/${pkgname}.png
  install -Dm644 ${srcdir}/${_pkgname}.desktop \
			${pkgdir}/usr/share/applications/${pkgname}.desktop
}
