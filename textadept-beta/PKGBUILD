# Maintainer: M Rawash <mrawash@gmail.com>

pkgname=textadept-beta
_pkgname=textadept
pkgver=3.0
pkgrel=1
pkgdesc="A ridiculously fast and extensible text editor."
arch=('i686' 'x86_64')
url="http://code.google.com/p/textadept"
license=('MIT')
depends=('gtk2' 'lua')
makedepends=('unzip')
conflicts=('textadept')
provides=('textadept=$pkgver')
install=${pkgname}.install
source=(http://textadept.googlecode.com/files/${_pkgname}_${pkgver}_beta.src.zip
          textadept.desktop
          pkgbuild.lua)
md5sums=('efea467a4be8dacd1e7c371d4dcf1bb8'
         '16352a54ad59c89a78fded6d140107a6'
         'f0b2f243460b231dbeb6f547d8c1822b')

build() {
  cd "$srcdir/${_pkgname}_${pkgver}_beta"

  cd src/scintillua/gtk
  make INCLUDEDIRS='-I ../include -I ../src -I ../../lua/include' || return 1

  cd ../../
  sed -i 's/\/usr\/local\//\/usr\//' lua/include/luaconf.h
  make  || return 1

  #there's no target install - installing manually
  mkdir -p ${pkgdir}/usr/{bin,share}
  cp -r  ${srcdir}/${_pkgname}_${pkgver}_beta ${pkgdir}/usr/share/${_pkgname}
  ln -sf /usr/share/${_pkgname}/textadept ${pkgdir}/usr/bin/textadept
  install -Dm644 ../LICENSE ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE

  #now clean this mess
  rm -r ${pkgdir}/usr/share/${_pkgname}/{src,LICENSE,xcode}

  #make ta pkgbuild friendly
  install -Dm644 $srcdir/pkgbuild.lua $pkgdir/usr/share/${_pkgname}/lexers/pkgbuild.lua
  echo -e "\n% PKGBUILD\nPKGBUILD pkgbuild\ninstall pkgbuild" >> \
        $pkgdir/usr/share/${_pkgname}/modules/textadept/mime_types.conf

  #freedesktop.org stuff
  install -Dm644 ${pkgdir}/usr/share/${_pkgname}/core/images/ta_128x128.png \
			${pkgdir}/usr/share/pixmaps/${_pkgname}.png
  install -Dm644 ${srcdir}/${_pkgname}.desktop \
			${pkgdir}/usr/share/applications/${_pkgname}.desktop
}
