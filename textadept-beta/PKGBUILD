# Maintainer: Niklas Wall√©n <nikw at gmx dot com>
# Contributor: M Rawash <mrawash@gmail.com>

pkgname=textadept-beta
_version=7.2_beta_2
pkgver=7.2b2
pkgrel=2
pkgdesc="A fast, minimalist, and ridiculously extensible cross-platform text editor"
arch=('i686' 'x86_64')
url="http://foicica.com/textadept"
license=('MIT')
depends=('gtk2' 'desktop-file-utils')
optdepends=(
  'textadept-modules-beta: for official modules'
  'ncurses: for the terminal version of Textadept'
  'discount: for generating docs for your own modules'
  'lua51-doc: for generating docs for your own modules'
)
makedepends=()
provides=('textadept')
conflicts=('textadept')
install='textadept.install'
source=("http://foicica.com/textadept/download/textadept_${_version}.i386.tgz"
        'textadept.patch'
        'textadept.desktop'
        'textadept.install')
md5sums=('95cf567022274f7a4035ec900a3657ba'
         'e3ddd651798bd4d68ed0f411cc810da5'
         'cafd8271857dca3fa2d5c19852beaef0'
         'b94be5feb9378a2e55b1295ddae8a49b')

prepare() {
  cd "$srcdir/textadept_${_version}.i386/src"
  patch textadept.c "$srcdir/textadept.patch"
  make -j1 deps
  cp lua.sym luajit/src/
}

build() {
  cd "$srcdir/textadept_${_version}.i386/src"
  make -j1
#  make -j1 curses

  # don't run this - the docs are already fine, this messes it up
#  make -j1 doc
}

package() {
  cd "$srcdir/textadept_${_version}.i386/src"

  # NOTE! The curses version won't compile because gtdialog needs CDKSelection
  # functions which are removed by the cdk patch.
  # For now, let's use the precompiled i386 binaries (only for curses version):
  # (It can't go in /usr/bin since it's unpatched)
  install -d "$pkgdir/usr/bin"
  for _f in textadept{,jit}-curses; do
    install -Dm755 ../$_f "$pkgdir/usr/share/textadept/$_f"
    ln -s /usr/share/textadept/$_f "$pkgdir/usr/bin/"
  done

  make -j 1 DESTDIR="$pkgdir" PREFIX=/usr install
#  make -j 1 DESTDIR="$pkgdir" PREFIX=/usr curses install

  # freedesktop.org desktop entry
  install -Dm644 "$srcdir/textadept.desktop" \
                 "$pkgdir/usr/share/applications/textadept.desktop"

  # move binaries
  install -d "$pkgdir/usr/bin"
  mv -f "$pkgdir/usr/share/textadept/textadept"{,jit} \
        "$pkgdir/usr/bin/"
#  mv -f "$pkgdir/usr/share/textadept/textadept"{,jit}-curses \
#        "$pkgdir/usr/bin/"

  # move license
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  mv "$pkgdir/usr/share/textadept/LICENSE" \
     "$pkgdir/usr/share/licenses/$pkgname/"

  # move documentation (and link to it)
  install -d "$pkgdir/usr/share/doc"
  mv "$pkgdir/usr/share/textadept/doc" "$pkgdir/usr/share/doc/textadept"
  ln -s /usr/share/doc/textadept "$pkgdir/usr/share/textadept/doc"
  # remove doc generating script (and the lua dependency)
  rm "$pkgdir/usr/share/doc/textadept/bombay"

  # remove windows icons
  rm "$pkgdir"/usr/share/textadept/core/images/*.ico
  rm "$pkgdir"/usr/share/textadept/core/images/textadept.icns
  # move icons (and link to them)
  for res in 16 32 48 64 128 256; do
    install -d "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps"
    mv "$pkgdir/usr/share/textadept/core/images/ta_${res}x${res}.png" \
       "$pkgdir/usr/share/icons/hicolor/${res}x${res}/apps/textadept.png"
    ln -s "/usr/share/icons/hicolor/${res}x${res}/apps/textadept.png" \
	   "$pkgdir/usr/share/textadept/core/images/ta_${res}x${res}.png"
  done
  install -d "$pkgdir/usr/share/icons/hicolor/scalable/apps"
  mv "$pkgdir/usr/share/textadept/core/images/textadept.svg" \
     "$pkgdir/usr/share/icons/hicolor/scalable/apps/textadept.svg"
  ln -s "/usr/share/icons/hicolor/scalable/apps/textadept.svg" \
        "$pkgdir/usr/share/textadept/core/images/textadept.svg"
}
