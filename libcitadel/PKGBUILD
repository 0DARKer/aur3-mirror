# Contributor: Marco Lima <cipparello gmail com>
# Maintainer: Mitchell Ferguson <teh.dawg at gmail dot com>
pkgname=libcitadel
pkgver=8.02
pkgrel=1
pkgdesc="Common code shared across all of the components of the Citadel system"
arch=('i686' 'x86_64')
url="http://www.citadel.org/"
license=('GPL3' 'custom')
depends=('glibc' 'zlib')
options=('!libtool')
source=("http://easyinstall.citadel.org/$pkgname-$pkgver.tar.gz")
md5sums=('46b6753107f08adb7e66c6e4d3638f8a')
build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --build=${CHOST} \
    --host=${CHOST}
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # custom license
  install -D -m0644 COPYING "$pkgdir/"usr/share/licenses/$pkgname/COPYING
}

# vim: set noexpandtab tabstop=8 shiftwidth=8 textwidth=132 autoindent
# kate: indent-mode normal; indent-width 8; tab-indents on; tab-width 8; word-wrap on; word-wrap-column 132
