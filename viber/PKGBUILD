# Maintainer: Galin Petrov <topgunbg[at]gmail[dot]com>
# Contributor: Morealaz <morealaz@gmail.com>
# Contributor: Lev Lybin <lev.lybin@gmail.com>
# Contributor: Özgür Sarıer <ozgursarier1011601115[at]gmail[dot]com>
# Contributor: Mladen Pejaković <pejakm[at]autistici[dot]org>

pkgname=viber
pkgver=4.2.2.6
pkgrel=15
pkgdesc="Proprietary cross-platform IM and VoIP software"
arch=('x86_64')
url="http://viber.com"
license=('custom')
depends=('gstreamer0.10-base')
source=("http://download.cdn.viber.com/cdn/desktop/Linux/${pkgname}.deb"
        "eula.html"
        "viber.sh"
        "viber.desktop")
sha256sums=('46b453aa2bad31d4ac3b4c58f7c2731ff194bb9545a819180d225a9d7e3262bb'
            '037194f911c193b039fd5bcdb5296f8d65e5075006d67822519b5bc7da0c8788'
            'ab2e0b8fe15b5411e7fb2e57210f2aa32ef288d418d739fc4c724d2641140d3c'
            'bb7be1472a4ea3657e4c7a80d4a8187fc8cdc6651d90c50275308dfac29eb54e')

package() {
  cd "${srcdir}"
  tar -zxf data.tar.gz -C "${pkgdir}"
  install -Dm755 viber.sh "${pkgdir}/usr/bin/viber"
  install -Dm644 eula.html "${pkgdir}/usr/share/licenses/viber/eula.html"
  install -Dm644 viber.desktop "${pkgdir}/usr/share/applications/viber.desktop"
    # install icons
  for isize in 16x16 24x24 32x32 48x48 64x64 96x96 128x128 256x256; do
    install -Dm644 "${pkgdir}/opt/viber/icons/${isize}.png" "${pkgdir}/usr/share/icons/hicolor/${isize}/apps/viber.png"
  done
}
