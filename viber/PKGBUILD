# Maintainer: Lev Lybin <lev.lybin@gmail.com>
# Contributor: Özgür Sarıer <ozgursarier1011601115[at]gmail[dot]com>
# Contributor: Mladen Pejaković <pejakm[at]autistici[dot]org>

pkgname=viber
pkgver=4.2.2.6
pkgrel=6
pkgdesc="Free Text & Calls. More than 200 million Viber users text, call, and send photo and video messages worldwide - for free."
arch=('x86_64')
url="http://viber.com"
license=('custom')
depends=('libxss' 'qt5-base' 'qt5-webkit' 'qt5-script' 'qt5-websockets' 'qt5-svg' 'qt5-quick1' 'qt5-declarative' 'qt5-xmlpatterns' 'qt5-imageformats' 'gstreamer0.10-base-plugins')
conflicts=('viber<4.2.2.6')
replaces=('viber<4.2.2.6')
source=("http://download.cdn.viber.com/cdn/desktop/Linux/${pkgname}.deb"
        "eula.html"
        "viber")
md5sums=('4dec3ade37a3738c6f084858a4554861'
         'd128190efcd7312829583e682cf46b98'
         '2f9b728eb5f9bb727cbba57499d63605')

prepare() {
  cd "${srcdir}"
  tar -zxf data.tar.gz
  sed -e 's|Exec=/opt/viber/Viber|Exec=viber|g' \
      -e 's|Network;Application;|Network;Application;InstantMessaging;|g' \
      -e 's|/usr/share/pixmaps/viber.png|viber.png|g' \
      -i usr/share/applications/viber.desktop
}

package() {
  cd "${srcdir}"
  install -m 755 -d "${pkgdir}"/opt/viber/imageformats
  install -m 755 -d "${pkgdir}"/opt/viber/Sound/{DTMF,Messages,PTT}
  install -Dm755 viber "${pkgdir}/usr/bin/viber"
  install -Dm644 eula.html "${pkgdir}/usr/share/licenses/viber/eula.html"
  install -Dm644 usr/share/applications/viber.desktop "${pkgdir}/usr/share/applications/viber.desktop"

  cd "${srcdir}/opt/viber"
  install -Dm755 Viber "${pkgdir}/opt/viber"

  # there's no AUR package for QFacebook yet, so leave this
  install -Dm644 libqfacebook.so "${pkgdir}/opt/viber"

  # crashes on image sending/recieving with system WebKit, so leave this for now
  install -Dm644 {libicudata.so.48,libicui18n.so.48,libicuuc.so.48,libQt5WebKit.so.5,libQt5WebKitWidgets.so.5} "${pkgdir}/opt/viber"

  # crashes without this, even if qt5-imageformats installed, because this libs from qt4
  install -Dm644 imageformats/libqgif.so "${pkgdir}/opt/viber/imageformats/libqgif.so"
  install -Dm644 imageformats/libqjpeg.so "${pkgdir}/opt/viber/imageformats/libqjpeg.so"

  install -Dm644 Sound/*.wav "${pkgdir}/opt/viber/Sound"
  install -Dm644 Sound/DTMF/*.wav "${pkgdir}/opt/viber/Sound/DTMF"
  install -Dm644 Sound/Messages/*.wav "${pkgdir}/opt/viber/Sound/Messages"
  install -Dm644 Sound/PTT/*.wav "${pkgdir}/opt/viber/Sound/PTT"
  install -Dm644 Sound/Ringtone/ringtone.wav "${pkgdir}/opt/viber/Sound/Ringtone/ringtone.wav"
  install -Dm644 icons/16x16.png "${pkgdir}/usr/share/icons/hicolor/16x16/apps/viber.png"
  install -Dm644 icons/24x24.png "${pkgdir}/usr/share/icons/hicolor/24x24/apps/viber.png"
  install -Dm644 icons/32x32.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps/viber.png"
  install -Dm644 icons/48x48.png "${pkgdir}/usr/share/icons/hicolor/48x48/apps/viber.png"
  install -Dm644 icons/64x64.png "${pkgdir}/usr/share/icons/hicolor/64x64/apps/viber.png"
  install -Dm644 icons/96x96.png "${pkgdir}/usr/share/icons/hicolor/96x96/apps/viber.png"
  install -Dm644 icons/128x128.png "${pkgdir}/usr/share/icons/hicolor/128x128/apps/viber.png"
  install -Dm644 icons/256x256.png "${pkgdir}/usr/share/icons/hicolor/256x256/apps/viber.png"
  install -Dm644 icons/Viber.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/viber.svg"
}
