# Maintainer: Prurigro

pkgname=adobe-air-sdk
pkgver=2.6
pkgrel=8
pkgdesc="Adobe Air Software Development Kit and Runtime Environment"
arch=('i686' 'x86_64')
url="http://get.adobe.com/air/"
license=('custom')
options=('!strip' 'staticlibs')
depends=('gcc-libs' 'libxml2' 'nss' 'gtk2' 'libxslt')
[[ "$CARCH" = "x86_64" ]] && depends=('lib32-gcc-libs' 'lib32-libxml2' 'lib32-nss' 'lib32-gtk2' 'lib32-libxslt')
install=${pkgname}.install

source=("http://airdownload.adobe.com/air/lin/download/latest/AdobeAIRSDK.tbz2"
        "${pkgname}.sh")
sha512sums=('8185db9749f05bd60f3e52bf12de779c9f22da73142aca41326f57e24ef9b62c8f4bd9b7ba28cd110c7c4288d61870a8a9b8806bd51ae5cfd860aa57745d4527'
            '63f075c6d81018182d531ad17b2df11737cc0710ec75e27531cef7b43c614e74a1871d083692fbbe0408132e1e90017dca44916000b556d975f34089a2246649')

package(){
    install -d "${pkgdir}"/opt/adobe-air-sdk
    cd "${srcdir}"
    find . ! -name AdobeAIRSDK.tbz2 ! -name AIR\ SDK\ license.pdf | sed -e 's/\.\///g' | while read file; do
        if [ -d "$file" ]; then
            install -d "${pkgdir}"/opt/adobe-air-sdk/"$file"
        elif [ "$file" = "${pkgname}.sh" ]; then
            install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/etc/profile.d/${pkgname}.sh"
        elif [ -h "$file" ]; then
            if [ $(file "$file" | grep 'broken' -c) = "0" ]; then
                ln -s $(echo $(file "$file" | grep -o -e \`[^\']*) | sed -e "s/\`//g") "${pkgdir}"/opt/adobe-air-sdk/"$file"
            fi
        else
            install -D "$file" "${pkgdir}/opt/adobe-air-sdk/${file}"
        fi
    done
    install -Dm644 "${srcdir}/AIR SDK license.pdf" "${pkgdir}/usr/share/licenses/adobe-air-sdk/sdk-license.pdf"
}
