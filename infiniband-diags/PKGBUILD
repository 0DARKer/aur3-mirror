# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs-ofed
pkgname=infiniband-diags
pkgver=1.5.13
pkgrel=1
pkgdesc="OpenFabrics Alliance InfiniBand diagnostic tools"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibmad' 'opensm' 'perl')
source=("http://www.openfabrics.org/downloads/management/${pkgname}-${pkgver}.tar.gz")
md5sums=('367affa587f346f9ddc064bcce4206cf')
sha512sums=('ecf593443d145d3b8d06fb6f0b5e43d8d7905b86d0a10681dc03e8f0ca6e27acc292767b3fd7456f6783a6aafbcb1b9fd14229307e1db1148a6c1018c5e5a8b3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-ibpath_override=/usr/sbin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
