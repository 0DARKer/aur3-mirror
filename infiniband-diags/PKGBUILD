# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs-ofed
pkgname=infiniband-diags
pkgver=1.6.1
pkgrel=1
pkgdesc="OpenFabrics Alliance InfiniBand diagnostic tools"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibmad' 'opensm' 'perl')
source=("http://www.openfabrics.org/downloads/management/${pkgname}-${pkgver}.tar.gz")
# cksum start
md5sums=('d250e63f849b017f7149046c003cc8f6')
sha512sums=('8a9285c2f95a714430411af6242b3d16728ee89c7e080e412aed99443e684d2f2fce5ae696b3b3f31e3bebdcc05c5fb0e13d3783af93e18a8e37c266b7f569dc')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-ibpath_override=/usr/sbin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}