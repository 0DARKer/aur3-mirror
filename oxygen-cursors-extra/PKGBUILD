# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>

pkgname=oxygen-cursors-extra
basename=kde-workspace
pkgver=4.7.4
pkgrel=1
pkgdesc="KDE's Oxygen mouse cursor theme - the extra flavours"
arch=("any")
url="http://www.kde.org"
license=("GPL3")
makedepends=('pkgconfig' 'cmake' 'automoc4' 'inkscape' 'libltdl' 'xorg-xcursorgen')
#optdepends=("kdebase-workspace: contains the default cursor colours")
options=(!emptydirs)
source=("http://download.kde.org/stable/${pkgver}/src/${basename}-${pkgver}.tar.bz2")

# Cursor size
cursorsize=""
#cursorsize="-big"

# Theme list
themelist="bluecurve brown cherry chrome desert emerald green grey honeycomb hot_orange lilac midnight_meadow navy norway obsidian obsidian-hc olympus olympus-inv orchid oxygen peach purple red red-argentina sea_blue steel terra terra_green violet viorange whitewater wonton"
#themelist="${themelist} black blue white yellow zion"

build() {
	cd "${srcdir}"/${basename}-${pkgver}/cursors/src

	cmake .

	for theme in ${themelist}; do
		make theme-${theme}${cursorsize}
	done
}

package() {
	cd "${srcdir}"/${basename}-${pkgver}/cursors/src

	# Directories
	install -d "${pkgdir}"/usr/share/icons/

	# Files
	for theme in ${themelist}; do
		cp -r --parents "oxy-${theme}${cursorsize}/cursors" "${pkgdir}"/usr/share/icons/
		cp "theme-${theme}/index.theme" "${pkgdir}"/usr/share/icons/oxy-${theme}${cursorsize}/
	done
}

sha1sums=('b7810ba13f6f2a1c4783b153ad9349a1dd27b495')
