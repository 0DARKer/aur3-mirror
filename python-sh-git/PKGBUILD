# Maintainer:  jyantis <yantis@yantis.net>

# NOTE this is branch release-1.10 since its a year ahead of master
# Need to check for future branches in the future and adjust accordingly.
# https://github.com/amoffat/sh/branches
_branch='release-1.10'
_version='1.10'
pkgname=python-sh-git
pkgver=1.10.r613.ba9fef7
pkgrel=2
pkgdesc='A full-fledged subprocess interface for Python 3 that allows you to call any program as if it were a function'
arch=('any')
url='https://github.com/amoffat/sh'
license=('MIT')
depends=('python')
source=("sh::git+https://github.com/amoffat/sh/#branch=${_branch}")
sha256sums=('SKIP')
makedepends=('git' 'python-setuptools')
provides=('python-sh')
conflicts=('python-sh')

pkgver() {
  cd sh
  printf "%s.r%s.%s" "${_version}"  "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd sh
  python setup.py build
}

package() {
  cd sh

  # We don't need anything related to git in the package
  rm -rf .git*

  python setup.py install --root="${pkgdir}" --optimize=1

  # Install License
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install Documentation
  install -D -m644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
