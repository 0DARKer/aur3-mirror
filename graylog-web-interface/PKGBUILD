# Maintainer: Spider.007 <aur@spider007.net>
# Don't forget updating the version @ *.service
pkgname=graylog-web-interface
replaces=graylog2-web-interface
pkgver=1.0.0
pkgrel=1
pkgdesc="Web interface for Graylog, an open source syslog implementation"
arch=(any)
url="http://graylog.org"
license=('GPL')
depends=(java-runtime)
source=(
	https://packages.graylog2.org/releases/$replaces/$pkgname-$pkgver.tgz
	graylog-web-interface.service
	graylog-web-interface.conf
)
backup=(etc/webapps/$pkgname/$pkgname.conf)
options=(!strip)
sha256sums=('b2025a4c4da84593f57c9fcd2cae725f69a2dd7f82fad872ad833c966b949727'
            'c848001a8e22c4a4a658e77659c5a65b8cbb6944d65d5c629a29af57bfc755f0'
            'cdab4f3a6e8dd2c417d530a516d0316f6b5de3c72e3d45e4772c8d49740c1d46')

package() {
	cd "$srcdir/$pkgname-$pkgver"

	install -Dm644 "$srcdir/graylog-web-interface.conf" "$pkgdir/etc/conf.d/graylog-web-interface.conf"
	install -Dm644 "$srcdir/graylog-web-interface.service" "$pkgdir/usr/lib/systemd/system/graylog-web-interface.service"

	install -dm755 -o nobody "$pkgdir/etc/webapps/$pkgname/logs"
	cp conf/* "$pkgdir/etc/webapps/$pkgname/"

	install -dm755 "$pkgdir/usr/share/webapps/$pkgname"
	cp -R lib/ "$pkgdir/usr/share/webapps/$pkgname/"

	ln -s $pkgname.$pkgname-$pkgver.jar "$pkgdir/usr/share/webapps/$pkgname/lib/$pkgname.jar"
	ln -s /etc/webapps/$pkgname/ $pkgdir/usr/share/webapps/$pkgname/conf
}
