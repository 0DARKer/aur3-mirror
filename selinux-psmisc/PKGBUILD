# $Id: PKGBUILD 149212 2012-02-06 11:42:35Z eric $
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Nicky726 <Nicky726@gmail.com>

pkgname=selinux-psmisc
_origname=psmisc
pkgver=22.15
pkgrel=1
pkgdesc="Miscellaneous procfs tools with SELinux support"
arch=('i686' 'x86_64')
url="http://psmisc.sourceforge.net/index.html"
license=('GPL')
groups=('selinux' 'selinux-system-utilities')
depends=('ncurses' 'selinux-usr-libselinux')
conflicts=("${_origname}")
provides=("${_origname}=${pkgver}-${pkgrel}")
source=(http://downloads.sourceforge.net/psmisc/${_origname}-${pkgver}.tar.gz
        COMM_LEN-to-18.patch)
sha1sums=('06f507d889afab70ac7ff20fbd419ca2ffcf90c0'
          '6f45338fc34e453bea5273691a0cf81ed3e20fb8')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  patch -p1 -i ../COMM_LEN-to-18.patch
  ./configure --prefix=/usr --enable-selinux
  make
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
