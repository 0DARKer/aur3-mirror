# Contributor: Eric BÃ©langer (eric <at> archlinux <dot> org)
# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)

pkgname=selinux-psmisc
_origname=psmisc
pkgver=22.14
pkgrel=1
pkgdesc="SELinux aware miscellaneous procfs tools"
arch=('i686' 'x86_64')
url="http://psmisc.sourceforge.net/index.html"
license=('GPL')
groups=('selinux' 'selinux-system-utilities')
depends=('glibc' 'ncurses' 'selinux-usr-libselinux')
conflicts=("${_origname}")
provides=("${_origname}=${pkgver}-${pkgrel}")
source=(http://downloads.sourceforge.net/psmisc/${_origname}-${pkgver}.tar.gz)
sha1sums=('dc6fc0ec131c11796d01512bbd80089719b04a66')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  ./configure --prefix=/usr --enable-selinux
  make
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
