# $Id: PKGBUILD 18880 2008-11-09 22:59:07Z jeff $
# Maintainer: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Maintainer: aurelien <aurelien@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>
pkgname=zsh-neo
_pkgname=zsh
pkgver=4.3.9
pkgrel=1
pkgdesc="zsh with patch for the NEO 2.0 keyboard layout"
arch=('i686' 'x86_64')
url="http://www.zsh.org/"
license=('custom')
depends=('ncurses' 'gdbm')
conflicts=('zsh')
provides=("zsh=${pkgver}")
options=('!zipman')
install="zsh-neo.install"
source=(ftp://ftp.zsh.org/pub/$_pkgname-$pkgver.tar.bz2 neo.patch)
md5sums=('0539d0a590e545ad8c40ff8c97e94538'
         '73f0d8b16b03164a5596745ad6dfbe7f')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  ./configure --prefix=/usr --bindir=/bin \
    --enable-etcdir=/etc/zsh \
	--enable-zshenv=/etc/zsh/zshenv \
    --enable-zlogin=/etc/zsh/zlogin \
    --enable-zlogout=/etc/zsh/zlogout \
    --enable-zprofile=/etc/profile \
    --enable-zshrc=/etc/zsh/zshrc \
    --enable-maildir-support \
	--with-term-lib="ncursesw" \
	--enable-multibyte \
	--enable-function-subdirs \
	--enable-zsh-mem \
	--with-tcsetpgrp \
    --enable-zsh-secure-free
  patch -p1 -i $srcdir/neo.patch

  make || return 1
  make DESTDIR="$pkgdir/" install

  install -D -m644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

