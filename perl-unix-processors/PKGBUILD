# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

pkgname=perl-unix-processors
_pkgname=Unix-Processors
pkgver=2.042
pkgrel=2
pkgdesc="Unix::Processors - Interface to per-processor information"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url="http://search.cpan.org/dist/$_pkgname"
source=(http://search.cpan.org/CPAN/authors/id/W/WS/WSNYDER/$_pkgname-$pkgver.tar.gz)
sha512sums=('8a5e8ca02fbe7844a6528f212a69c355c0b48641b50df781e59169c5d56efca3469ff0ab3060a01b148a7429e3bd2b8bfc8509ac5f0f5f0d81748aad33cf42bf')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
