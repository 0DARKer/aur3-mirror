# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributor: Zuf <kontakt.zuf(at)gmail.com>

pkgname=macrofusion
pkgver=0.7.4
pkgrel=2
pkgdesc="GUI to combine photos to get deeper DOF or HDR"
url="http://sourceforge.net/projects/macrofusion/"
depends=('python' 'python-pillow' 'exiv2' 'perl-image-exiftool'
'enblend-enfuse' 'hugin' 'pyexiv2')

arch=('i686' 'x86_64')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver}/${pkgname}_${pkgver}.orig.tar.gz")
license=('GPL')
install=$pkgname.install
sha256sums=('2d061f0a2e858c86ae700351cf9785bc7058938e02ce6fcf40b192fbb0b60b11')

package() {
    cd "${pkgname}-${pkgver}"
    # upstream really needs to provide a proper Makefile for this
    # as a guide, inspect the deb in this PPA:
    # https://launchpad.net/~dhor/+archive/myway/+packages
    install -Dm755 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop

    install -Dm644 ui/progress.xml       "$pkgdir"/usr/share/mfusion/ui/progress.xml
    install -Dm644 ui/ui.xml             "$pkgdir"/usr/share/mfusion/ui/ui.xml
    install -Dm644 images/logoSplash.png "$pkgdir"/usr/share/mfusion/images/logoSplash.png

    install -Dm644 images/$pkgname.png   "$pkgdir"/usr/share/pixmaps/$pkgname.png
    install -Dm755 $pkgname.py           "$pkgdir"/usr/bin/$pkgname

    for locale in fr pl ru; do
        install -Dm644 locale/$locale/LC_MESSAGES/MacroFusion.mo "$pkgdir"/usr/share/locale/$locale/LC_MESSAGES/MacroFusion.mo
        install -Dm644 locale/$locale/LC_MESSAGES/MacroFusion.po "$pkgdir"/usr/share/locale/$locale/LC_MESSAGES/MacroFusion.po
    done

}

# vim:set ts=4 sw=4 ft=sh et:
