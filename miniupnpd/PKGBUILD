# $Id$
# Maintainer: None
pkgname=miniupnpd
pkgver=1.7.20120824
pkgrel=2
pkgdesc="Lightweight UPnP IGD daemon"
arch=('i686' 'x86_64')
url="http://miniupnp.free.fr"
license=('BSD')
backup=(etc/miniupnpd/miniupnpd.conf)
depends=('libnfnetlink')

eval $(wget "https://projects.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/iptables" -O - | awk -F= '
	$1 == "pkgver" { pkgver=$2 } 
	$1 == "source" { sub(/\${pkgname}/, "iptables"); sub(/\${pkgver}/, pkgver); print $0 "); " } 
	$1 == "sha1sums" { print $0 ");" }
	END { print "_iptables=iptables-" pkgver }')

source+=("http://miniupnp.free.fr/files/$pkgname-$pkgver.tar.gz" miniupnpd.init miniupnpd.systemd)
sha1sums+=('7abca8ecbcd8596392175eb312941083ed5faf21'
          'c84031d7af122c5bede8609bcfa80bc31f2a5e61'
          'bc0ae02a88eceef4f172b07f6f67d5fd54cc6ae0')

build() {
	cd "$srcdir/$_iptables"
	./configure --enable-static
	make
	
	cd "$srcdir/$pkgname-$pkgver"
	make -f Makefile.linux config.h
	IPTABLESPATH="$srcdir/$_iptables" make -f Makefile.linux
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	mkdir -p "$pkgdir/usr/share/man/man8"
	make PREFIX="$pkgdir/" -f Makefile.linux install
	rm -r "${pkgdir}/etc/init.d"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm755 "${srcdir}/miniupnpd.init" "${pkgdir}/etc/rc.d/miniupnpd"
	install -Dm644 "${srcdir}/miniupnpd.systemd" "${pkgdir}/usr/lib/systemd/system/miniupnpd.service"
}

