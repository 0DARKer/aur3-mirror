# $Id$
# Maintainer: None
pkgname=miniupnpd
pkgver=1.7.20121005
pkgrel=3
pkgdesc="Lightweight UPnP IGD daemon"
arch=('i686' 'x86_64')
url="http://miniupnp.free.fr"
license=('BSD')
backup=(etc/miniupnpd/miniupnpd.conf)
depends=('libnfnetlink')
source=("http://miniupnp.free.fr/files/$pkgname-$pkgver.tar.gz" miniupnpd.init miniupnpd.systemd)
sha1sums=('4579c4711a8455bf455f3757ac9ce0dc40088901'
          'c84031d7af122c5bede8609bcfa80bc31f2a5e61'
          'bc0ae02a88eceef4f172b07f6f67d5fd54cc6ae0')

_ipt_arch_ver=$(pacman -Ss iptables | awk '/\/iptables/ { print $2 }')
depends+=("iptables=$_ipt_arch_ver")

build() {
	ipt_upstream_ver=iptables-${_ipt_arch_ver%%-*}	
	if [ ! -d $ipt_upstream_ver ]; then
		curl -O "http://www.iptables.org/projects/iptables/files/${ipt_upstream_ver}.tar.bz2"
		bsdtar -jxvf $ipt_upstream_ver.tar.bz2
	fi

	cd "$srcdir/$ipt_upstream_ver"
	./configure --enable-static
	make

	cd "$srcdir/$pkgname-$pkgver"
	make -f Makefile.linux config.h
	IPTABLESPATH="$srcdir/$ipt_upstream_ver" make -f Makefile.linux
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	mkdir -p "$pkgdir/usr/share/man/man8"
	make PREFIX="$pkgdir/" -f Makefile.linux install
	rm -r "${pkgdir}/etc/init.d"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm755 "${srcdir}/miniupnpd.init" "${pkgdir}/etc/rc.d/miniupnpd"
	install -Dm644 "${srcdir}/miniupnpd.systemd" "${pkgdir}/usr/lib/systemd/system/miniupnpd.service"
}

