# $Id$
# Maintainer: None
pkgname=miniupnpd
pkgver=1.6
pkgrel=1
pkgdesc="Lightweight UPnP IGD daemon"
arch=('i686' 'x86_64')
url="http://miniupnp.free.fr"
license=('BSD')

_iptables="iptables-1.4.12.2"
source=("http://miniupnp.free.fr/files/$pkgname-$pkgver.tar.gz" 
		"http://www.iptables.org/projects/iptables/files/$_iptables.tar.bz2")
md5sums=('828083fda8d10deba7a7bf16f6e5b64f'
         '212112389c7f10c72efb31a4ed193a4c')

build() {
	cd "$srcdir/$_iptables"
	./configure --enable-static
	make

	cd "$srcdir/$pkgname-$pkgver"
	make -f Makefile.linux config.h
	IPTABLESPATH="$srcdir/$_iptables" make -f Makefile.linux
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make PREFIX="$pkgdir/" -f Makefile.linux install
	mv "${pkgdir}/etc/init.d" "${pkgdir}/etc/rc.d"
	install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

