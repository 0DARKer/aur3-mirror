# $Id$
# Maintainer: None
pkgname=miniupnpd
pkgver=1.8.20130426
pkgrel=1
pkgdesc="Lightweight UPnP IGD daemon"
arch=('i686' 'x86_64')
url="http://miniupnp.free.fr"
license=('BSD')
backup=(etc/miniupnpd/miniupnpd.conf)
depends=('libnfnetlink' 'iptables>=1.4.3')
makedepends=('lsb-release')
source=("http://miniupnp.free.fr/files/$pkgname-$pkgver.tar.gz" miniupnpd.systemd
        miniupnpd_linux_make.patch)
sha1sums=('f68febd3be82a4bd4ca6e5b87da891318e963a48'
          '9cf48feb98dadee8a566b2dc7811a95020ad3895'
          'ffedae515b0e992778bd983374cd50d9efa55c06')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p 1 -i "$srcdir/miniupnpd_linux_make.patch"
	CONFIG_OPTIONS="--strict" make -f Makefile.linux config.h
	make -f Makefile.linux
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	mkdir -p "$pkgdir/usr/share/man/man8"
	make PREFIX="$pkgdir/" -f Makefile.linux install
	rm -r "${pkgdir}/etc/init.d"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 "${srcdir}/miniupnpd.systemd" "${pkgdir}/usr/lib/systemd/system/miniupnpd.service"
	sed -i 's:/sbin/iptables:/usr/sbin/iptables:
	        s:eth0:"`cat /etc/miniupnpd/miniupnpd.conf | '"awk -F= '/^ext_ifname/ { print \$2 }'"'`":' "${pkgdir}"/etc/miniupnpd/*.sh
	sed -i -e "s/^uuid=[-0-9a-f]*/uuid=00000000-0000-0000-0000-000000000000/
	           s/make genuuid/uuidgen/" "${pkgdir}/etc/miniupnpd/miniupnpd.conf"
}

