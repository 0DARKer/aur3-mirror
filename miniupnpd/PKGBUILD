# $Id$
# Maintainer: None
pkgname=miniupnpd
pkgver=1.8.20130207
pkgrel=1
pkgdesc="Lightweight UPnP IGD daemon"
arch=('i686' 'x86_64')
url="http://miniupnp.free.fr"
license=('BSD')
backup=(etc/miniupnpd/miniupnpd.conf)
depends=('libnfnetlink')
source=("http://miniupnp.free.fr/files/$pkgname-$pkgver.tar.gz" miniupnpd.systemd)
sha1sums=('a3ddfef655c8b9f60f2e3aca4f8676732b2fc359'
          '9cf48feb98dadee8a566b2dc7811a95020ad3895')

_ipt_arch_ver=$(pacman -Ss iptables | awk '/\/iptables/ { print $2 }')
depends+=("iptables=$_ipt_arch_ver")

build() {
	ipt_upstream_ver=iptables-${_ipt_arch_ver%%-*}	
	if [ ! -d $ipt_upstream_ver ]; then
		curl -O "http://www.iptables.org/projects/iptables/files/${ipt_upstream_ver}.tar.bz2"
		bsdtar -jxvf $ipt_upstream_ver.tar.bz2
	fi

	cd "$srcdir/$ipt_upstream_ver"
	./configure --enable-static
	make

	cd "$srcdir/$pkgname-$pkgver"
	make -f Makefile.linux config.h
	IPTABLESPATH="$srcdir/$ipt_upstream_ver" make -f Makefile.linux
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	mkdir -p "$pkgdir/usr/share/man/man8"
	make PREFIX="$pkgdir/" -f Makefile.linux install
	rm -r "${pkgdir}/etc/init.d"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 "${srcdir}/miniupnpd.systemd" "${pkgdir}/usr/lib/systemd/system/miniupnpd.service"
	sed -i 's:/sbin/iptables:/usr/sbin/iptables:
	        s:eth0:"`cat /etc/miniupnpd/miniupnpd.conf | '"awk -F= '/^ext_ifname/ { print \$2 }'"'`":' "${pkgdir}"/etc/miniupnpd/*.sh
	sed -i -e "s/^uuid=[-0-9a-f]*/uuid=00000000-0000-0000-0000-000000000000/
	           s/make genuuid/uuidgen/" "${pkgdir}/etc/miniupnpd/miniupnpd.conf"
}

