# $Id$
# Maintainer: None
pkgname=miniupnpd
pkgver=1.7.20120711
pkgrel=1
pkgdesc="Lightweight UPnP IGD daemon"
arch=('i686' 'x86_64')
url="http://miniupnp.free.fr"
license=('BSD')
backup=(etc/miniupnd/miniupnpd.conf)
depends=('libnfnetlink')

eval $(wget "https://projects.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/iptables" -O - | awk -F= '
	$1 == "pkgver" { pkgver=$2 } 
	$1 == "source" { sub(/\${pkgname}/, "iptables"); sub(/\${pkgver}/, pkgver); print $0 "); " } 
	$1 == "sha1sums" { print $0 ");" }
	END { print "_iptables=iptables-" pkgver }')

source+=("http://miniupnp.free.fr/files/$pkgname-$pkgver.tar.gz" )
sha1sums+=('ad9c8ed029c206574effb524ab113ed189d4d03a')

build() {
	cd "$srcdir/$_iptables"
	./configure --enable-static
	make
	
	cd "$srcdir/$pkgname-$pkgver"
	make -f Makefile.linux config.h
	IPTABLESPATH="$srcdir/$_iptables" make -f Makefile.linux
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make PREFIX="$pkgdir/" -f Makefile.linux install
	mv "${pkgdir}/etc/init.d" "${pkgdir}/etc/rc.d"
	install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

