# Maintainer: almostalive <almostalive2003 at lavabit dot com>

pkgname=libretro-dosbox-git
_gitname=dosbox-libretro
pkgver=2013.04.08_a296919
pkgrel=1
pkgdesc="libretro implementation of DOSBox."
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/dosbox-libretro"
license=('custom')
makedepends=('git')
source=('git://github.com/libretro/dosbox-libretro.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-|.|g')_$(git describe --always | sed 's/-/_/g')"
}

build() {
  cd $_gitname
  make -f Makefile.libretro
}

package()
{
  mkdir -p $pkgdir/usr/lib/libretro
  install -v -m644 $srcdir/$_gitname/dosbox_libretro.so $pkgdir/usr/lib/libretro/libretro-dosbox.so
}