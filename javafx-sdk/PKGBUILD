# Maintainer: Aliasbody <aliasbody@gmail.com>
pkgname=javafx-sdk
pkgver=2.2b16
pkgrel=1
pkgdesc="A lightweight, hardware-accelerated Java UI platform for enterprise business applications"
url="http://javafx.com/"
arch=("x86_64" "i686")
license=("custom: Oracle Technology Network")
depends=("java-runtime" "ffmpeg" "gtk2")
install=javafx-sdk.install
if [ $CARCH == "i686" ]; then
	source=("https://dl.dropbox.com/s/s31f944zf3qbarm/javafx_sdk-2_2_0-beta-b16-linux-i586.zip")
	md5sums=("37e9da9cfd744e23f63b72ad202d188f")
fi

if [ $CARCH == "x86_64" ]; then
	source=("https://dl.dropbox.com/s/3optkfmq6j79xgw/javafx_sdk-2_2_0-beta-b16-linux-x64.zip")
	md5sums=("2601dbc38819b8f8a820dd9ec14fa20e")
fi

package() {
	# Move all the files from the package into the /usr/lib/jvm/javafx-sdk folder
	mkdir -p "${pkgdir}/usr/lib/jvm/${pkgname}"
	mv "${srcdir}/javafx-sdk2.2.0-beta/"* "${pkgdir}/usr/lib/jvm/${pkgname}/"
	
	# Make a copy of the licences into the /usr/share/licences/javafx-sdk folder
	mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
	mv "${pkgdir}/usr/lib/jvm/$pkgname/"{THIRDPARTYLICENSEREADME.txt,COPYRIGHT.html} "${pkgdir}/usr/share/licenses/${pkgname}"
	
	# Adding man page
	mv "${pkgdir}/usr/lib/jvm/${pkgname}/man/"{ja_JP.UTF-8,ja_JP}
	gzip -9 "${pkgdir}/usr/lib/jvm/${pkgname}/man/"{ja_JP/man1,man1}/javafxpackager.1
	mv "${pkgdir}/usr/lib/jvm/${pkgname}/man" "${pkgdir}/usr/share/man"
	
	# Create a symlink of the javafxpackager binary and send it to the /usr/bin folder
	mkdir -p "${pkgdir}/usr/bin"
	chmod +x "${pkgdir}/usr/lib/jvm/${pkgname}/bin/javafxpackager"
	ln -s "/usr/lib/jvm/${pkgname}/bin/javafxpackager" "${pkgdir}/usr/bin/javafxpackager"
}
