# Maintainer: naelstrof <naelstrof@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Paul Bienkowski <opatutlol@aol.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
pkgname=mingw-w64-bullet
pkgver=2.80
pkgrel=1
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and  animation (mingw-w64)"
arch=('any')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
depends=('mingw-w64-binutils' 'mingw-w64-crt')
options=(!strip !buildflags)
optdepends=('bullet-docs: documentation')
makedepends=('cmake' 'mingw-w64-gcc')
source=(http://bullet.googlecode.com/files/bullet-${pkgver}-rev2531.tgz)
md5sums=('d1eec0332110264a21d0fc0f2fb127c3')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}/bullet-${pkgver}-rev2531
  #Requires fpermissive to allow losing precision in casts. (Should be safe)
  echo 'set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fpermissive")' >> CMakeLists.txt
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p ${srcdir}/build-${_arch} && cd ${srcdir}/build-${_arch}
    echo "SET(CMAKE_SYSTEM_NAME Windows)
      SET(CMAKE_C_COMPILER ${_arch}-gcc)
      SET(CMAKE_CXX_COMPILER ${_arch}-g++)
      SET(CMAKE_RC_COMPILER ${_arch}-windres)
      SET(CMAKE_FIND_ROOT_PATH /usr/${_arch})
      SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
      SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
      SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)" > temp.cmake

    cmake .. \
      -DCMAKE_TOOLCHAIN_FILE=temp.cmake  \
      -DCMAKE_INSTALL_PREFIX=/usr/${_arch} \
      -DBUILD_SHARED_LIBS=1 \
      -DBUILD_DEMOS=0 \
      -DBUILD_EXTRAS=1 \
      -DINSTALL_LIBS=1 \
      -DUSE_GLUT=0 \
      -DINSTALL_EXTRA_LIBS=1 \
      -DCMAKE_BUILD_TYPE=Release \
      ../bullet-${pkgver}-rev2531
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/build-${_arch}
    make DESTDIR=${pkgdir} install
    install -Dm644 ${srcdir}/bullet-${pkgver}-rev2531/BulletLicense.txt ${pkgdir}/usr/${_arch}/share/licenses/${pkgname}/LICENSE
    mkdir -p $pkgdir/usr/${_arch}/bin
    mv ${pkgdir}/usr/${_arch}/lib/*.dll $pkgdir/usr/${_arch}/bin/
  done
}
