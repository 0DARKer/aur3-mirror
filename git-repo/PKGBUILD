#Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
#Contributor: Gabriel Pickl <peacemotion[at]gmail[dot]com>
pkgname=git-repo
pkgver=1.12.13
_pkgtag=5db69f3f6616ea199a7840f0602b988f8d5504b9
pkgrel=1
pkgdesc="The Multiple Git Repository Tool."
url="https://code.google.com/p/git-repo/"
arch=('any')
license=('Apache')
depends=('python2' 'git')
source=("git-repo.${pkgver}.tar.gz::https://gerrit.googlesource.com/git-repo/+archive/${_pkgtag}.tar.gz" "repo")
noextract=("git-repo.${pkgver}.tar.gz")
md5sums=('eba0d2cc262ebe40595b2d402e06fbbb'
         '380b8b5cc2f84cf92f698b7af6aef3e9')

prepare() {
  cd "${srcdir}"
  rm -rf "${pkgname}"
  mkdir "${pkgname}"
  tar -xzf "git-repo.${pkgver}.tar.gz" -C "${pkgname}"
}

build() {
  cd "${srcdir}/${pkgname}"
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" main.py repo
  rm .p* .g*
}

package() {
  cd "${srcdir}"
  install -d "${pkgdir}/usr/share"
  mv "${pkgname}" "${pkgdir}/usr/share/${pkgname}"
  install -D -m 755 repo "${pkgdir}/usr/bin/repo"
}

