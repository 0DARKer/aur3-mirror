# Maintainer: Alain Kalker <a.c.kalker@gmail.com>
_pkgbase=chromiumos-ui
pkgname=$_pkgbase-bin
pkgver=237968
# Build 237973 breaks syncing
#_pkgver=$(curl -s http://commondatastorage.googleapis.com/chromium-browser-snapshots/Linux_ChromiumOS/LAST_CHANGE)
_pkgver=$pkgver
pkgrel=3
pkgdesc="Chromium OS user interface (prebuilt binary)"
arch=('x86_64')
url="http://www.chromium.org/chromium-os"
license=('BSD')
depends=('chromium') # for chrome-sandbox
optdepends=('chromiumos-ui-session: display manager session'
			'chromiumos-ui-tts: ChromeVox text-to-speech, with accessibility support')
provides=("$_pkgbase")
conflicts=("$_pkgbase")
backup=("etc/$_pkgbase/default")
options=('!strip')
install=$_pkgbase.install
_srcname=chrome-linux
source=($_srcname-$_pkgver.zip::http://commondatastorage.googleapis.com/chromium-browser-snapshots/Linux_ChromiumOS/${_pkgver}/$_srcname.zip
		https://chromium.googlesource.com/chromium/src/+/master/LICENSE.chromium_os
		$_pkgbase.desktop
		$_pkgbase.default
		$_pkgbase.sh
		$_pkgbase-login.desktop
		$_pkgbase-login.sh)
noextract=()
#md5sums=('SKIP'
md5sums=('d5cc7c62fb8f8b4944685f220e252bbb'
         'c1f986c9d625a1d64d2f6f2130ff7b51'
         '0aa55e3947e85e3774bba3da274e09f8'
         '001a472621cace5c2e140df95c632af1'
         '0ebf8e9f389f25222fae7cff383618f7'
         '43c5c1ab714ffdcd88a6d90c47000044'
         '542624c8cf50548af01b477b305c53f7')

pkgver() {
	echo $_pkgver
}

package() {
	cd "$srcdir/$_srcname"

	install -d "$pkgdir/usr/lib/$_pkgbase"
	cp -a * "$pkgdir/usr/lib/$_pkgbase/"

	# Fix permissions
	find "$pkgdir/usr/lib/$_pkgbase" -type d -exec chmod 755 {} ';'
	find "$pkgdir/usr/lib/$_pkgbase" -type f -exec chmod 644 {} ';'
	chmod 755 "$pkgdir/usr/lib/$_pkgbase"/chrome{,-*}
	chmod 755 "$pkgdir/usr/lib/$_pkgbase"/nacl*
	chmod 755 "$pkgdir/usr/lib/$_pkgbase"/xdg-settings
	chmod 755 "$pkgdir/usr/lib/$_pkgbase"/xdg-mime

	# Allow users to override command-line options
	install -Dm644 "$srcdir/$_pkgbase.default" "$pkgdir/etc/$_pkgbase/default"

	install -Dm644 "$srcdir/$_pkgbase.desktop" \
		"$pkgdir/usr/share/applications/$_pkgbase.desktop"
	install -D "$srcdir/$_pkgbase.sh" \
		"$pkgdir/usr/bin/$_pkgbase"
	install -Dm644 "$srcdir/$_pkgbase-login.desktop" \
		"$pkgdir/usr/share/applications/$_pkgbase-login.desktop"
	install -D "$srcdir/$_pkgbase-login.sh" \
		"$pkgdir/usr/bin/$_pkgbase-login"

	install -Dm644 "$srcdir/LICENSE.chromium_os" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.chromium_os"
}
