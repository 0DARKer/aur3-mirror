# Maintainer: jsteel <jsteel at vorx dot com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>
# Contributor: boenki <boenki at gmx dot de>
# Contributor: sujith_m82 <m.sujith@gmail.com>

pkgname=consonance
pkgver=0.5.1
pkgrel=9
pkgdesc="A lightweight GTK+ music manager"
arch=('i686' 'x86_64')
url="http://sites.google.com/site/consonancemanager/"
license=('GPL')
depends=('dbus-glib' 'gtk2' 'libao' 'libmad' 'libnotify' 'libsndfile'
         'sqlite3' 'libmodplug' 'libcdio' 'curl' 'taglib' 'desktop-file-utils')
install="$pkgname.install"
source=("${url}releases-1/$pkgname-$pkgver.tar.gz"
        libnotify.patch)
md5sums=('2a195dd2314f7b65cc57cc871a4c5a47'
         'caa1e8b41723950fd926bfefccbbd8ac')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i "$srcdir/libnotify.patch"
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
