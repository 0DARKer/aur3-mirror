# Maintainer: Michael Schubert <mschu.dev at gmail>

pkgname=pythonnet-svn
pkgver=139
pkgrel=1
pkgdesc="Call .NET Common Language Runtime (CLR) from Python"
arch=('i686' 'x86_64')
url="http://pythonnet.sourceforge.net/"
license=('LGPL')
depends=('python2' 'mono')
makedepends=('svn')

_svntrunk=https://pythonnet.svn.sourceforge.net/svnroot/pythonnet/trunk/pythonnet
_svnmod=pythonnet

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  
  cd "$_svnmod"

  rm -f src/runtime/clrmodule.il
  sed -i "s/mono)/mono-2)/;\
    /^CFLAGS =/s|$| \$(shell pkg-config --cflags glib-2.0)|" \
    src/monoclr/Makefile
  sed -i 's:"libpython2:"/usr/lib/libpython2:' Python.Runtime.dll.config
  sed -i "s://mono_config_parse:mono_config_parse:" src/monoclr/pynetinit.c

  make PYTHON=/usr/bin/python2
  python2 setup.py build
}

package() {
  pydir=`python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()"`

  cd "$srcdir/$_svnmod"
  python2 setup.py install --skip-build --root="$pkgdir"
  install -m755 Python.Runtime.dll "$pkgdir/$pydir/Python.Runtime.dll"
  install -m755 Python.Runtime.dll.config "$pkgdir/$pydir/Python.Runtime.dll.config"
}
