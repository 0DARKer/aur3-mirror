# Maintainer:  Ainola <opp310@alh.rqh> (ROT13)

pkgname=broken-age-hib
pkgver=2014_07_03
pkgrel=1
pkgdesc="Broken Age is a point-and-click adventure telling the stories of a young boy and girl leading parallel lives. (Humble Bundle Version)"
arch=('i686' 'x86_64')
url="http://www.brokenagegame.com/"
license=('custom: commercial')
if [ "$CARCH" = "x86_64" ]; then
    depends=('lib32-libgl')
else
    depends=('libgl')
fi
source=("hib://BrokenAge_linux_1404412094.tar.gz"
        "${pkgname}.png"
        "${pkgname}.desktop")
sha256sums=("fa4acf8b41423e8b3ab3e3d8310c9d784158a94192e0d67411a402744e34762e"
            "96a97646e52807c2e3202d1c8e375ed1832a6a5968897c897f1a0c83007bafb7"
            "2dea9b0dddd426545c9bb55a780d832b7ea946d142744628a5a6b19daafd20c8")

# Prevent compressing final package
PKGEXT='.pkg.tar'

# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download the file manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf"; echo "Read this PKGBUILD for more info."; exit 1')


package() {
    # Install .desktop
    install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    # Move all the gamedata
    mkdir -p "${pkgdir}/opt/${pkgname}"
    cd "$srcdir"
    mv lib *.pck ReadMe.txt BrokenAge "${pkgdir}/opt/${pkgname}"
    install -Dm755 ${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

    # Create Launcher
    mkdir -p "${pkgdir}/usr/bin"
    ln -s "/opt/${pkgname}/BrokenAge" "${pkgdir}/usr/bin/${pkgname}"
}
