# Maintainer:  Ainola <opp310@alh.rqh> (ROT13)

pkgname=broken-age-hib
pkgver=2014_07_03
pkgrel=2
pkgdesc="Broken Age is a point-and-click adventure telling the stories of a young boy and girl leading parallel lives. (Humble Bundle Version)"
arch=('i686' 'x86_64')
url="http://www.brokenagegame.com/"
license=('custom: commercial')
depends_i686=('libgl')
depends_x86_64=('lib32-libgl')
source=("hib://BrokenAge_linux_1404412094.tar.gz"
        "${pkgname}.png"
        "${pkgname}.desktop"
        "${pkgname}.sh")
sha256sums=("fa4acf8b41423e8b3ab3e3d8310c9d784158a94192e0d67411a402744e34762e"
            "96a97646e52807c2e3202d1c8e375ed1832a6a5968897c897f1a0c83007bafb7"
            "1344c36f86b3b8530a0d68f2e47908eaf950cc6ba28a680837097cceffe36506"
            "9582f8603a4927fff9a2d07306416603293cfbfa3ffc2ac0715da83b2c757a48")

# Prevent compressing final package
PKGEXT='.pkg.tar'

# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download the file manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf"; echo "Read this PKGBUILD for more info."; exit 1')


package() {
    # Install .desktop
    install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    # Move all the gamedata
    mkdir -p "${pkgdir}/opt/${pkgname}"
    cd "$srcdir"
    mv lib *.pck ReadMe.txt BrokenAge "${pkgdir}/opt/${pkgname}"
    install -Dm755 ${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

    # Launcher
    install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
