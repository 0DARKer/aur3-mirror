# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=eclipse-ajdt
pkgver=2.1.1
pkgrel=1
pkgdesc="AspectJ Development Tools (Eclipse Plugin)"
arch=('i686' 'x86_64')
url="http://www.eclipse.org/ajdt/"
license=('EPL')
depends=('eclipse')
source=(http://download.eclipse.org/tools/ajdt/36/update/ajdt_${pkgver}_for_eclipse_3.6.zip)
md5sums=('65419ee975c46f90aacfbdbe55b6d474')

build() {
  # nothing to do
  true
}

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature}
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
