# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=eclipse-ajdt
pkgver=2.1.3
pkgrel=1
pkgdesc="AspectJ Development Tools (Eclipse Plugin)"
arch=('any')
url="http://www.eclipse.org/ajdt/"
license=('EPL')
depends=('eclipse>=3.7')
source=(http://download.eclipse.org/tools/ajdt/37/update/ajdt_${pkgver}_for_eclipse_3.7.zip)
md5sums=('148a7ebe526e2bf9e9a6f7652dfbe9c9')

build() {
  # nothing to do
  true
}

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature}
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
