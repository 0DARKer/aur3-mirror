# Maintainer: David Adler <david dot jo dot adler at gmail dot com>
# Contributor: orbisvicis <''.join(chr(ord(c)-1) for c in "pscjtwjdjtAhnbjm/dpn")>
pkgname=jack-smf-utils
pkgver=1.0
pkgrel=3
pkgdesc="JACK MIDI player and recorder with transport sync"
arch=('i686' 'x86_64')
url="http://jack-smf-utils.sourceforge.net/"
license=('custom:BSD')
depends=('jack' 'libsmf')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "jack-smf-utils.build-system.patch"
        "jack-smf-utils.libsmf-compatibility.patch")
sha256sums=('8f2a086fbb5935375590ac45310ca92f17b8d2684299b37b5366de1cbc167a71'
            'aac8801dedeae3797edaa0dc98c5f5e99597b731643db6249cd7516579b4b4a5'
            '8611dfad7547e321009e3ea5150748352bed13fdd03d2fc6c27a411d3270301f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/jack-smf-utils.build-system.patch"
  patch -p1 -i "${srcdir}/jack-smf-utils.libsmf-compatibility.patch"
  autoreconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install

  # license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 -t "${pkgdir}/usr/share/licenses/${pkgname}" COPYING
}

# vim:set ts=2 sw=2 et:
