# Maintainer: James Reed <supplantr at archlinux dot info>

pkgname=ftw-git
_gitname=ftw
pkgver=2013.05.09.geb7304a
pkgrel=1
pkgdesc='A configurable power saving script with udev rules.'
arch=('any')
url='https://github.com/supplantr/ftw'
license=('GPL3')
depends=('udev' 'hdparm' 'iw')
makedepends=('git')
provides=('ftw')
conflicts=('ftw')
backup=('etc/conf.d/ftw')
source=('git://github.com/supplantr/ftw.git')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

package() {
    cd "$srcdir/$_gitname"
    install -Dm755 ftw "$pkgdir/usr/bin/ftw"
    install -Dm644 ftw.rules "$pkgdir/usr/lib/udev/rules.d/99-ftw.rules"
    install -Dm644 etc-conf.d/ftw "$pkgdir/etc/conf.d/ftw"
}
