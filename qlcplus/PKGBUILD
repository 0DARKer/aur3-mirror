# Maintainer: Jerome Lebleu <jerome dot lebleu at mailoo dot org>

pkgname=qlcplus
pkgver=4.7.1
pkgrel=1
pkgdesc="Q Light Controller Plus - The open DMX lighting desk software for controlling professional lighting fixtures."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/qlcplus"
license=('APACHE')
depends=('qt4' 'liblo' 'libftdi-compat' 'libsndfile' 'libmad' 'shared-mime-info' 'fftw')
optdepends=('qlcplus-plugin-ola: Open Lighting Architecture plugin')
conflicts=('qlc' 'qlc-svn')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/project/qlcplus/${pkgver}/qlcplus_${pkgver}.tar.gz")
md5sums=('72393acd2a8ef74982f76ebb406d9253')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # don't build ola plugin
  sed -i "/ola/s/^/#/" plugins/plugins.pro

  qmake-qt4 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/" install
}
