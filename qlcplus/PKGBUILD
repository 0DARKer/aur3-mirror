# Maintainer: Jerome Lebleu <lebleu.jerome@gmail.com>

pkgname=qlcplus
pkgver=4.3.3
pkgrel=2
pkgdesc="Q Light Controller Plus - The open DMX lighting desk software for controlling professional lighting fixtures."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/qlcplus"
license=('GPL2')
depends=('qt4' 'liblo' 'libftdi' 'libsndfile' 'libmad' 'shared-mime-info')
optdepends=('qlcplus-plugin-ola: Open Lighting Architecture plugin')
conflicts=('qlc' 'qlc-svn')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/project/qlcplus/${pkgver}/qlcplus_${pkgver}.tar.gz")
md5sums=('99ee59bd3d2ec8e3c2dfcad16d1cb7ca')

build() {
  cd "${srcdir}/qlcplus-${pkgver}"
  
  # don't build ola plugin
  sed -i "/ola/s/^/#/" plugins/plugins.pro

  qmake-qt4 
  make
}

package() {
  cd "${srcdir}/qlcplus-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/" install
}
