# Maintainer: Jerome Lebleu <lebleu.jerome@gmail.com>

pkgname=qlcplus
pkgver=4.3.2
pkgrel=2
pkgdesc="Q Light Controller Plus - The open DMX lighting desk software for controlling professional lighting fixtures."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/qlcplus"
license=('GPL2')
depends=('qt4' 'liblo' 'libftdi' 'libsndfile' 'libmad' 'shared-mime-info')
conflicts=('qlc' 'qlc-svn')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgver}/${pkgname}_${pkgver}.tar.gz")
md5sums=('8a45ab394f6e957308a34c5c2444db3c')

build() {
  cd "${srcdir}/$pkgname-${pkgver}"
  
  # fix build issue
  sed -i "s/-Werror/-Werror -Wno-unused-local-typedefs/" variables.pri
  
  # don't build ola plugin
  sed -i "/ola/s/^/#/" plugins/plugins.pro

  qmake-qt4 
  make
}

package() {
  cd "${srcdir}/$pkgname-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/" install
}
