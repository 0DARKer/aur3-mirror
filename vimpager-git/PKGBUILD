# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=vimpager-git
pkgver=1.8.2.r0.gc0e7ade
pkgrel=1
pkgdesc="A vim-based script to use as a PAGER - git checkout"
arch=('any')
url='https://github.com/rkitover/vimpager'
license=('custom:vim')
depends=('vim')
conflicts=('vimpager')
provides=('vimpager')
source=('git://github.com/rkitover/vimpager.git'
	'license.txt')
backup=('etc/vimpagerrc')

pkgver() {
	cd vimpager/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

package() {
	cd vimpager/

	make PREFIX="/usr" SYSCONFDIR="/etc" DESTDIR="${pkgdir}" install

	install -Dm644 "${srcdir}/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

sha256sums=('SKIP'
            '446c67d93c43addf076fe103a71844c2d875d478f82186436567dd221f2652f3')
