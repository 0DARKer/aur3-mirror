# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=vimpager-git
pkgver=1.8.0.0.gc76eb5e
pkgrel=1
pkgdesc="A vim-based script to use as a PAGER - git checkout"
arch=('any')
url='https://github.com/rkitover/vimpager'
license=('custom:vim')
depends=('vim')
conflicts=('vimpager')
provides=('vimpager')
source=('git://github.com/rkitover/vimpager.git'
	'license.txt')
backup=('etc/vimpagerrc')

pkgver() {
	cd vimpager/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

package() {
	cd vimpager/

	make PREFIX="/usr" SYSCONFDIR="/etc" DESTDIR="${pkgdir}" install

	install -Dm644 "${srcdir}/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

sha256sums=('SKIP'
            '446c67d93c43addf076fe103a71844c2d875d478f82186436567dd221f2652f3')
