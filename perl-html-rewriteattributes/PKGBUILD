# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=perl-html-rewriteattributes
_cpanname="HTML-RewriteAttributes"
pkgver=0.04
pkgrel=2
pkgdesc="Concise attribute rewriting"
arch=('any')
url="http://search.cpan.org/~sartak/$_cpanname-$pkgver/"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.5.0' 'perl-regexp-common')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/S/SA/SARTAK/$_cpanname-$pkgver.tar.gz")
md5sums=('2f14d37ad31e91f2ee0c13d2f2cd329b')

# Function to change to the working directory and set
# environment variables to override undesired options.
prepareEnvironment() {
	cd "$srcdir/$_cpanname-$pkgver"
	export \
		PERL_MM_USE_DEFAULT=1 \
		PERL_AUTOINSTALL=--skipdeps \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
		PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
		MODULEBUILDRC=/dev/null
}

build() {
	prepareEnvironment
	/usr/bin/perl Makefile.PL
	make
}

check() {
	prepareEnvironment
	make test
}

package() {
	prepareEnvironment
	make install

	# Remove "perllocal.pod" and ".packlist".
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
