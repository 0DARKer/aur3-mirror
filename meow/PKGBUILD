# Maintainer: hbdee <hbdee.arch@gmail.com>

pkgname=meow
_sname=meow_0.9.16-3
pkgver=0.9.16
pkgrel=2
pkgdesc="An unobtrusive and cuddly music player - KDE4 version"
arch=('i686' 'x86_64')
url=("http://meowplayer.org/")
license=('GPL')
depends=('kdebase-runtime' 'mpg123' 'taglib' 'libvorbis' 'flac' 'opusfile')
makedepends=('cmake' 'automoc4')
conflicts=('meow-qt')
install="meow.install"
source=("http://meowplayer.org/${_sname}.tar.bz2")
md5sums=('3bd50b2e73e9e698c930203293cb13ac')


prepare() {
  if [[ -d build ]]
  then
    rm -rf build
  fi
   mkdir build
}

build() {
  cd build
  cmake ../${_sname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` \
    -DMEOW_WITH_OPUS=1
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
