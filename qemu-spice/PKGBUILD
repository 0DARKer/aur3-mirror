# Maintainer: Patryk Kowalczyk <patryk at kowalczyk dot ws> 

pkgname=qemu-spice
_pkgname=qemu
pkgver=1.4.0
pkgrel=3
rc=
pkgdesc="Latest stable QEMU with KVM spice usbredir virtfs smartcard and iscsi"
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://wiki.qemu.org/Index.html"
depends=('pixman' 'libjpeg' 'libpng' 'libsasl' 'curl' 'sdl' 'alsa-lib' 'nss' 'glib2' 'gnutls>=2.4.1' 'bluez' 'vde2' 'libcap-ng' 'texinfo' 
'util-linux' 'libpulse' 'seabios' 'spice>=0.12.2' 'spice-protocol>=0.12.4' 'usbredir>=0.6' 'libiscsi' 'libaio' 'libseccomp')
makedepends=('texi2html' 'perl' 'python2' 'libcacard')
backup=('etc/qemu/target-x86_64.conf')
install=qemu.install
conflicts=('qemu' 'libcacard')
provides=('qemu' 'libcacard')
replaces=('kvm' 'qemu' 'qemu-kvm' 'qemu-kvm-spice' 'libcacard')
backup=('etc/qemu/target-x86_64.conf')
source=(http://wiki.qemu.org/download/${_pkgname}-${pkgver}${rc}.tar.bz2
        65-kvm.rules
	qemu-texinfo01.patch
	qemu-texinfo02.patch
	qemu-texinfo03.patch
	qemu-texinfo04.patch
	qemu-texinfo05.patch
	qemu-texinfo06.patch
	qemu-vnc-tls.patch
	qemu-img.1.gz
	qemu-kvm.1.gz
	qemu-nbd.8.gz
	virtfs-proxy-helper.1.gz
)
options=(!strip)

build()
{
        cd "${srcdir}/${_pkgname}-${pkgver}${rc}"
	patch -p0 < ../qemu-texinfo01.patch
	patch -p0 < ../qemu-texinfo02.patch
	patch -p0 < ../qemu-texinfo03.patch
        patch -p0 < ../qemu-texinfo04.patch
        patch -p0 < ../qemu-texinfo05.patch
        patch -p0 < ../qemu-texinfo06.patch
        patch -p0 < ../qemu-vnc-tls.patch
     
	./configure --prefix=/usr \
                --python=/usr/bin/python2 \
                --sysconfdir=/etc \
		--libexecdir=/usr/lib/qemu \
                --audio-drv-list=alsa,sdl,oss,pa \
                --audio-card-list=ac97,sb16,es1370,hda \
		--enable-mixemu \
		--enable-kvm \
		--enable-spice \
		--enable-usb-redir \
		--enable-guest-agent \
		--enable-sdl \
		--enable-linux-aio \
		--enable-virtfs \
		--enable-smartcard-nss \
		--enable-libiscsi \
                --disable-docs
    make
}
	#	--enable-glusterfs
package()
{
    cd "${srcdir}/${_pkgname}-${pkgver}${rc}"
    make DESTDIR="${pkgdir}" install
    rm "${pkgdir}/usr/share/qemu/bios.bin"

    # symbolic link for backwards compatibility
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/qemu-kvm"
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/qemu"
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/kvm"
    # fix man page
##    mv "${pkgdir}/usr/share/man/man1/qemu.1" \
##                     "${pkgdir}/usr/share/man/man1/qemu-kvm.1"
	install -D -m644  ${srcdir}/qemu-kvm.1 ${pkgdir}/usr/share/man/man1/qemu-kvm.1
        install -D -m644  ${srcdir}/qemu-img.1 ${pkgdir}/usr/share/man/man1/qemu-img.1
        install -D -m644  ${srcdir}/virtfs-proxy-helper.1 ${pkgdir}/usr/share/man/man1/virtfs-proxy-helper.1
        install -D -m644  ${srcdir}/qemu-nbd.8 ${pkgdir}/usr/share/man/man8/qemu-nbd.8

    # install udev rules
    install -D -m644 "${srcdir}/65-kvm.rules" \
                     "${pkgdir}/usr/lib/udev/rules.d/65-kvm.rules"
    # bridge_helper needs suid
    # https://bugs.archlinux.org/task/32565
    chmod u+s "${pkgdir}/usr/lib/qemu/qemu-bridge-helper"
    # add sample config
    echo "allow br0" > ${pkgdir}/etc/qemu/bridge.conf.sample

    # strip scrpts directory
    find "${pkgdir}/usr/bin"  -type f -perm -u+w 2>/dev/null | while read binary ; do
      case "$(file -bi "$binary")" in
        *application/x-executable*) # Binaries
        /usr/bin/strip $STRIP_BINARIES "$binary";;
      esac
    done
}

md5sums=('78f13b774814b6b7ebcaf4f9b9204318'
         'b316a066d2f1bb57d8f5b7ea1d0d1caf'
         '94f41871609e7694ac18d3013184f124'
         'b4ccc059d5a656c4934743d855591275'
         '4275d851418b09aaf1fe79a14d472752'
         'd784984f6561dbbea4f2a7b8cd15d20b'
         'c795b1644ef5f9b22a03d30b194956ec'
         'f76d93193c5fffac3bfb7e6dd911b447'
         '76ecf4037812b89f2c8f5fc290b5a6fd'
         '935a6943bebe80851be0279cf929746d'
         '347eca54fa409078c6a936f5363c1223'
         'a3c003137f8b843d156244457d261623'
         '4b20665b5331d638dd7870a28841efc5')
