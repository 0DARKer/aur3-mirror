w
# $Id: PKGBUILD 158721 2012-05-08 10:22:47Z tpowa $
w
# Maintainer: Patryk Kowalczyk  
pkgname=qemu-spice
_pkgname=qemu
pkgver=1.3.0
pkgrel=7
rc=
pkgdesc="Latest stable QEMU with KVM spice usbredir virtfs smartcard and iscsi"
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://wiki.qemu.org/Index.html"
depends=('pixman' 'libjpeg' 'libpng' 'libsasl' 'curl' 'sdl' 'alsa-lib' 'nss' 'glib2' 'gnutls>=2.4.1' 'bluez' 'vde2' 'libcap-ng' 
'util-linux' 'libpulse' 'seabios' 'spice>=0.12.0' 'spice-protocol>=0.12.2' 'libcacard>=0.1.2' 'usbredir>=0.6' 'libiscsi' 'libaio')
makedepends=('texi2html' 'perl' 'python2')
backup=('etc/qemu/target-x86_64.conf')
install=qemu.install
conflicts=('qemu')
provides=('qemu')
replaces=('kvm' 'qemu' 'qemu-kvm')
backup=('etc/qemu/target-x86_64.conf')
source=(http://wiki.qemu.org/download/${_pkgname}-${pkgver}.tar.bz2
        65-kvm.rules)
options=(!strip)

build()
{
     cd "${srcdir}/${_pkgname}-${pkgver}${rc}"
     sed -i -e 's/lib64/lib/g' x86_64.ld
     ./configure --prefix=/usr \
                --python=/usr/bin/python2 \
                --sysconfdir=/etc \
                --audio-drv-list=alsa,sdl,oss,pa \
                --audio-card-list=ac97,sb16,es1370,hda \
		--enable-mixemu \
		--enable-kvm \
		--enable-spice \
		--enable-usb-redir \
		--enable-guest-agent \
		--enable-sdl \
		--enable-linux-aio \
		--enable-virtfs \
		--enable-smartcard \
		--enable-libiscsi \
                --enable-docs
    make
}
	#	--enable-glusterfs
package()
{
    cd "${srcdir}/${_pkgname}-${pkgver}${rc}"
    make DESTDIR="${pkgdir}" install
    rm "${pkgdir}/usr/share/qemu/bios.bin"

    # symbolic link for backwards compatibility
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/qemu-kvm"
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/qemu"
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/kvm"
    # fix man page
    mv "${pkgdir}/usr/share/man/man1/qemu.1" \
                     "${pkgdir}/usr/share/man/man1/qemu-kvm.1"
    # install udev rules
    install -D -m644 "${srcdir}/65-kvm.rules" \
                     "${pkgdir}/usr/lib/udev/rules.d/65-kvm.rules"
    # strip scrpts directory
    find "${pkgdir}/usr/bin"  -type f -perm -u+w 2>/dev/null | while read binary ; do
      case "$(file -bi "$binary")" in
        *application/x-executable*) # Binaries
        /usr/bin/strip $STRIP_BINARIES "$binary";;
      esac
    done
}
md5sums=('a4030ddd2ba324152a97d65d3c0b247d'
         'b316a066d2f1bb57d8f5b7ea1d0d1caf')
