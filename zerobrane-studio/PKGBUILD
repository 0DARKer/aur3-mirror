# Maintainer: Harley Laue <losinggeneration@gmail.com>
pkgname=zerobrane-studio
pkgver=0.32
pkgrel=2
pkgdesc="A lightweight Lua-based IDE for Lua with code completion, syntax highlighting, live coding, remote debugger, and code analyzer."
arch=(any)
url="http://studio.zerobrane.com/"
license=('MIT')
depends=('wxlua' 'luasocket' 'hicolor-icon-theme')
makedepends=('cmake')
provides=('zerobrane-studio')
conflicts=('zerobrane-studio-git')
optdepends=('love: to debug love programs')
install=zerobrane-studio.install
_github_user="pkulchenko"
_github_project="ZeroBraneStudio"
_github_rev="ab17270"
source=("https://github.com/$_github_user/$_github_project/tarball/${pkgver}" "icons.tar.gz" "cmake.patch")
md5sums=('9c397facf3c76d8f92bbd86df68bcbe0'
         'd1f1d12081ece2f27464c5d3bde266ec'
         'd06bb57cff3a35a110ac3bba8c552e66')

build() {
  cd "$srcdir/$_github_user-$_github_project-$_github_rev"
  patch -p1 < "$srcdir/cmake.patch"
  (cd zbstudio/res && tar xf "$srcdir/icons.tar.gz")
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo
  make
}

package() {
  cd "$srcdir/$_github_user-$_github_project-$_github_rev/build"
  make DESTDIR="$pkgdir/" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  cp ../LICENSE "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
