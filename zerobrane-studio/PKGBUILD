# Maintainer: Harley Laue <losinggeneration@gmail.com>
pkgname=zerobrane-studio
pkgver=0.35
pkgrel=2
pkgdesc="A lightweight Lua-based IDE for Lua"
arch=(any)
url="http://studio.zerobrane.com/"
license=('MIT')
depends=('wxlua' 'lua51-socket' 'hicolor-icon-theme')
makedepends=('cmake')
provides=('zerobrane-studio')
conflicts=('zerobrane-studio-git')
optdepends=('love: to debug love programs')
install=zerobrane-studio.install
_github_user="pkulchenko"
_github_project="ZeroBraneStudio"
_github_rev="547c710"
source=("https://github.com/$_github_user/$_github_project/tarball/${pkgver}"
        "zbstudio.patch")
md5sums=('8ee751759ffdd21689391e53d69eda6b'
         '593fcb6e04215df27f337b6794967007')

build() {
  cd "$srcdir/$_github_user-$_github_project-$_github_rev"

  patch -p1 < "$srcdir/zbstudio.patch"
  # default to Lua 5.1 since luasockets isn't currently packaged with Lua 5.2
  echo 'path.lua = "/usr/bin/lua5.1"' > cfg/user.lua

  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo \
        -DLUA_EXECUTABLE=/usr/bin/lua5.1

  make
}

package() {
  cd "$srcdir/$_github_user-$_github_project-$_github_rev/build"
  make DESTDIR="$pkgdir/" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  cp ../LICENSE "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
