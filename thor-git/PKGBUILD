# Maintainer: Advocatus Diaboli <fanael4[at]gmail[dot]com>
pkgname='thor-git'
pkgver=20120624
pkgrel=1
pkgdesc='SFML extension library (Git version)'
arch=('i686' 'x86_64')
url='http://www.bromeon.ch/libraries/thor/'
license='custom'
depends=('sfml-git')
makedepends=('cmake' 'git')
provides=('thor')
conflicts=('thor')
_gitroot='git://github.com/Bromeon/Thor.git'
_gitname='thor'
source=()
sha512sums=()

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [ -d $_gitname ]; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname --depth=1
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting cmake..."

  rm -rf "$srcdir/$_gitname-build"
  cp -R "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/$_gitname-build/build"
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  make clean
}
