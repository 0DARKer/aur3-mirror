# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
pkgname=gnome-boxes
pkgver=3.3.2
pkgrel=1
pkgdesc="A simple GNOME 3 application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('LGPL2.1')
depends=('gtk3' 'clutter-gtk' 'spice-gtk3' 'libvirt-glib'
         'gtk-vnc' 'udev' 'libosinfo' 'dconf' 'hicolor-icon-theme'
         'desktop-file-utils' 'shared-mime-info')
optdepends=('qemu-kvm-spice: to create new VMs')
install=gnome-boxes.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        'gtk-version-check.patch'
        'gtk-fix-1.patch'
        'gtk-fix-2.patch')

sha256sums=('2a307f7fd49d4cdf9269b3c8a745cbb75f1886fd9ff5f5d1d67565d27e570881'
            '7a35b6916d21d97aa4044cab7667d7add72db94d07851ae194a4936cc52dd740'
            'e92fc980c491ad6f7ec20a12efc69cec708dffc739e7ae1f593974b5cff9cea9'
            '5c9341337f2f3e60928608160246168e7bfa86c7339ae0b0d568650b810bbffc')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # pretend we can run well with gtk+ 3.2...
  patch -p1 < "$srcdir/gtk-version-check.patch"

  # Unbreak gnome-boxes with gtk+ 3.2
  # (gnome-boxes would require gtk+ commit 5cf0450c69f7bbdb0d1ca22ec54355e3b3e8c9c2)
  patch -p1 < "$srcdir/gtk-fix-1.patch"
  patch -p1 < "$srcdir/gtk-fix-2.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
