# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
pkgname=gnome-boxes
pkgver=3.3.3
pkgrel=1
pkgdesc="A simple GNOME 3 application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Boxes"
license=('LGPL2.1')
depends=('gtk3' 'clutter-gtk' 'spice-gtk3' 'libvirt-glib'
         'gtk-vnc' 'udev' 'libosinfo' 'dconf' 'hicolor-icon-theme'
         'desktop-file-utils' 'shared-mime-info')
optdepends=('qemu-kvm-spice: to create new VMs')
install=gnome-boxes.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        'gtk-version-check.patch')
sha256sums=('48a1bed83c65be60148ac2e0f376cadaf12e5e3a995ede5b4ef14c7c8c9f4ca3'
            '7bc6f3dd834ea0d5d627cf8e4c67c00f7bd05e1a05511669efe6deed3325550d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # pretend we can run well with gtk+ 3.2.3...
  patch -p1 < "$srcdir/gtk-version-check.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
