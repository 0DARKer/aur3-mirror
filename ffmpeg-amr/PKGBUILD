# Contributor: Marcin Merda <exine@jun.pl>
# Contributor: Jose Negron <josenj.arch@mailnull.net>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ffmpeg-amr
pkgver=0.5.1
pkgrel=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution for Linux/Unix"
arch=(i686 x86_64)
url="http://ffmpeg.mplayerhq.hu/"
license=('GPL')
depends=('lame' 'sdl' 'libvorbis' 'faad2>=2.7' 'faac' 'xvidcore' 'zlib' 'imlib2' 'x264>=20090216' 'libtheora' 'amrnb' 'amrwb')
options=('force')
provides=("ffmpeg=$pkgver")
conflicts=('ffmpeg')
source=(http://ffmpeg.org/releases/ffmpeg-${pkgver}.tar.bz2)
md5sums=('c7b0e1729f7aafb10496d79bb963bb26')

build() {
  cd "$srcdir/ffmpeg-$pkgver" || return 1

  ./configure \
  --prefix=/usr \
  --enable-gpl \
  --enable-libmp3lame \
  --enable-libvorbis \
  --enable-libfaac \
  --enable-libfaad \
  --enable-libxvid \
  --enable-libx264 \
  --enable-libtheora \
  --enable-postproc \
  --enable-shared \
  --enable-pthreads \
  --enable-x11grab \
  --enable-swscale \
  --enable-nonfree \
  --enable-libamr-nb \
  --enable-libamr-wb \
  || return 1

  make || return 1
  make tools/qt-faststart || return 1
  make doc/ff{mpeg,play,server}.1 || return 1

  make DESTDIR="$pkgdir" install install-man || return 1
  install -D -m755 tools/qt-faststart $pkgdir/usr/bin/qt-faststart || return 1

  # since makepkg currently declines to strip .a files, do this for now
  strip --strip-debug "$pkgdir"/usr/lib/*.a || return 1
}

# vim:set ts=2 sw=2 et:
