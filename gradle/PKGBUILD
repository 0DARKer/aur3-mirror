# Maintainer: Chris Molozian (novabyte) <chris DOT molozian AT gmail DOT com>
# Contributor: Sanjuro Makabe (itti) <vuck AT gmx DOT de>
pkgname=gradle
pkgver=1.0milestone5
_pkgver=1.0-milestone-5
pkgrel=1
pkgdesc="A powerful build system for the JVM"
arch=('any')
url="http://www.gradle.org/"
license=('APACHE')
depends=('java-runtime')
source=("http://repo.gradle.org/gradle/distributions/gradle-1.0-milestone-5-bin.zip" "${pkgname}.sh")
package(){
    # install profile.d script
    install -dm755 "${pkgdir}/etc/profile.d"
    install -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/etc/profile.d/"
    cd "${srcdir}/${pkgname}-${_pkgver}"

    # create the necessary directory structure
    mkdir -p "${pkgdir}/usr/share/java/${pkgname}/bin"
    mkdir -p "${pkgdir}/usr/share/java/${pkgname}/lib/plugins"
    mkdir -p "${pkgdir}/usr/bin"

    # copy across jar files
    install -Dm644 lib/*.jar "${pkgdir}/usr/share/java/${pkgname}/lib" || return 1
    install -Dm644 lib/plugins/*.jar "${pkgdir}/usr/share/java/${pkgname}/lib/plugins" || return 1

    # copy across supporting text documentation and scripts
    install -m644 *.* "${pkgdir}/usr/share/java/${pkgname}" || return 1
    install -m755 "bin/gradle" "${pkgdir}/usr/share/java/${pkgname}/bin" || return 1

    # link gradle script to /usr/bin
    ln -s "/usr/share/java/${pkgname}/bin/${pkgname}" "${pkgdir}/usr/bin"
}

md5sums=('d96faf4efcc5b66cf21067e997583379'
         '41da33f95d22802b979ed9a484f526cc')

