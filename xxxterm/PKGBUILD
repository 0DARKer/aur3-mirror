# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=xxxterm
pkgver=1.399
pkgrel=1
pkgdesc="A minimalists web browser, vi-like while maintaining traditional web browser behavior."
arch=('i686' 'x86_64')
url="http://opensource.conformal.com/wiki/XXXTerm"
license=('custom:ISC')
depends=('libwebkit' 'gtk2' 'libbsd')
optdepends=()
source=(http://opensource.conformal.com/snapshots/$pkgname/$pkgname-$pkgver.tgz \
        LICENSE )
md5sums=('ee1e7c10b16ffcbbbc28afa260fbb24a'
         'f3eeb6e8b70a3dcccb8ee57daf584c9e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # no spam
  sed -i 's|https://www.cyphertite.com|http://archlinux.org|' xxxterm.{c,conf}
  cd linux
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver/linux"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  cd ..
  install -Dm644 xxxterm.conf "$pkgdir/etc/skel/xxxterm.conf"
  cd ../..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

