# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm
pkgver=4.98.0
pkgrel=1
pkgdesc='Plasma applet written in QML for managing network connections'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/network/plasma-nm'
license=('GPL2')
depends=('plasma-framework' 'kdelibs4support' 'libmm-qt5' 'libnm-qt5')
makedepends=('extra-cmake-modules' 'openconnect')
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers' 'openconnect: Cisco AnyConnect VPN plugin')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('aeb2c86d88a92af285d2afb57f0560a5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-nm-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
