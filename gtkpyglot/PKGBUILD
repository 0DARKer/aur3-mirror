# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gtkpyglot
pkgver=1.1
pkgrel=1
pkgdesc="A python desktop tool for software translators"
arch=(any)
url="https://bitbucket.org/vkolev/gtkpyglot"
license=('GPL')
depends=('pygtk' 'python-simplejson')
install="$pkgname.install"
source=("https://bitbucket.org/vkolev/gtkpyglot/get/13027b0c7201.tar.bz2"
        "$pkgname.desktop")
md5sums=('c36e814aafee53db9cdb48fcd0aea460'
         '13ee4c6e36d012a5029f6f38f0dde31f')

build() {
  cd "$srcdir"/vkolev*

  sed -i "s|\(python2\).6|\1|;s|sys\.path\[0\]|\"/usr/share/$pkgname\"|" $pkgname.py
}

package() {
  cd "$srcdir"/vkolev*

  # bin
  install -Dm755 $pkgname.py "$pkgdir/usr/bin/$pkgname"

  # data
  install -d "$pkgdir/usr/share/$pkgname/data"
  install -Dm644 data/* "$pkgdir/usr/share/$pkgname/data"

  # desktop
  install -Dm644 ../$pkgname.desktop \
          "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 data/pyglot_64.png \
          "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
