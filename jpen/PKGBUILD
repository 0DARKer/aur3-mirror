# Contributer: N30N <archlinux@alunamation.com>
# Contributer: niQo <niqooo@gmail.com>
# Contributer: trontonic <rodseth@gmail.com>
# Contributer: lolilolico

pkgname=jpen
pkgver=2.110322
pkgrel=1
pkgdesc="A Java library for accessing pen/digitizer tablets and pointing devices."
url="http://jpen.wiki.sf.net"
license=("GPL3")
arch=("i686" "x86_64")
depends=()
makedepends=("java-environment")
source=("http://downloads.sf.net/${pkgname}/${pkgname}-${pkgver/./-}-src.zip")
sha256sums=("473b84d46f79b8ef57b12b7787e8668db9f65c6181837147c81570b9ca94ae1f")

build() {
	cd "${pkgname}-${pkgver/./-}/src/main/c/linux"

	gcc ${CFLAGS} \
		-I/opt/java/include \
		-I/opt/java/include/linux \
		-I/usr/lib/jvm/java-6-openjdk/include \
		-I/usr/lib/jvm/java-6-openjdk/include/linux \
		-Wall -fpic -shared -o libjpen-2.so -lXi *.c
}

package() {
	cd "${pkgname}-${pkgver/./-}/src/main/c/linux"

	install -D -m755 libjpen-2.so "${pkgdir}/usr/lib/libjpen-2.so"
}

# vim: set noet ff=unix
