# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-io-event
pkgver=0.809
pkgrel=1
pkgdesc="Tied Filehandles for Nonblocking IO with Object Callbacks"
arch=('any')
url="http://search.cpan.org/dist/IO-Event"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-anyevent' 'perl-event')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MU/MUIR/modules/IO-Event-$pkgver.tar.gz")
md5sums=('28c9abf785e1286084ebe6ab256523bd')
sha512sums=('4e56bc931c0c1b95e45fe699565c3924eddfb11d29ba9b5b28f9da3da19888440b567f8c96f58f659b3db7c34a2e8e3841dfc880e3a4d5f95124213188e25784')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/IO-Event-${pkgver}"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/IO-Event-${pkgver}"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "${srcdir}/IO-Event-${pkgver}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
