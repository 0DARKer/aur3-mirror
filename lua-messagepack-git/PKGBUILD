pkgname=lua-messagepack-git
pkgver=0.3.0.r34.g6b10d3e
pkgrel=1
pkgdesc='MessagePack implementation in lua '
arch=('any')
url='https://github.com/fperrad/lua-MessagePack'
license=('custom:MIT')
depends=('lua')
provides=('lua-messagepack')
conflicts=('lua-messagepack')
source=("${pkgname}::git://github.com/fperrad/lua-MessagePack.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  cd "${pkgname}"

  # install for lua version 5.1 (used by luajit)
  make LUAVER=5.1 PREFIX="/usr" DESTDIR="${pkgdir}" install
  # install for lua version 5.2 (default lua interpreter)
  make LUAVER=5.2 PREFIX="/usr" DESTDIR="${pkgdir}" install

  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
