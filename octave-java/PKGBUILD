# Maintainer: David Runge <david[d]runge[a]frqrec[d]com>
_pack=java
pkgname=octave-$_pack
pkgver=1.2.8
pkgrel=1
pkgdesc="Provides Java interface with OO-like Java objects manipulation."
arch=(any)
url="http://octave.sourceforge.net/$_pack/"
license=('GPL' 'custom:simplified BSD')
groups=('octave-forge')
depends=('octave>=3.2.0' 'java-runtime')
makedepends=()
optdepends=()
backup=()
options=()
install=$pkgname.install
_archive=$_pack-$pkgver.tar.gz
source=("http://downloads.sourceforge.net/octave/$_archive")
noextract=("$_archive")
md5sums=('e94bcb73e21f2e8f3e5cf43c143ba029')

build() {
	cd "$srcdir"
	mkdir -p builddir
	msg "Setting JAVA_HOME in Octave to: $JAVA_HOME"
	octave -q -f --eval "setenv('JAVA_HOME','$JAVA_HOME')"
	octave -q -f --eval "pkg build -verbose -nodeps builddir $_archive"
}

package() {
	mkdir -p "$pkgdir/usr/share/octave/packages"
	mkdir -p "$pkgdir/usr/lib/octave/packages"
	cp "$srcdir/builddir/$_archive" "$pkgdir/usr/share/octave/$_pack.tar.gz"
	if [[ "$CARCH" == 'x86_64' ]]; then
		msg "Fixing platform folder problem"
		mkdir -p "$pkgdir$JAVA_HOME/jre/lib/amd64/server"
		ln -s $JAVA_HOME/jre/lib/amd64/server/ $pkgdir$JAVA_HOME/jre/lib/amd64/client
	fi
}
