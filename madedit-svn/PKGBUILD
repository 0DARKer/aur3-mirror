# Maintainer: Mauro Santos <registo.mailling@gmail.com>
pkgname=madedit-svn
pkgver=589
pkgrel=2
_boostver=1.47.0
_boost_ver="$(echo $_boostver | tr . _)"
pkgdesc="Open-Source & Cross-Platform Text/Hex Editor written in C++ and wxWidgets."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/madedit/develop"
license=('GPL')
depends=('gtk2>=2.6.0' 'glib2>=2.6.0' 'wxgtk>=2.6.1')
makedepends=('subversion' 'wxgtk>=2.6.1')
provides=('madedit')
conflicts=('madedit')
source=("http://downloads.sourceforge.net/project/boost/boost/$_boostver/boost_$_boost_ver.tar.bz2"
        madedit-highlight-blink-fix.patch
		madedit-onpaint-fix.patch
		madedit-add-file-changed-event.patch
		madedit-max-linelength-fix.patch
		madedit-desktop-file.patch)
md5sums=('a2dc343f7bc7f83f8941e47ed4a18200'
         '5e89e938d1122a5fe299a68fc583edb2'
		 '7985f66c07a1e27ee65bc01524274796'
         '661acd7e6c5f3c53177157f681335459'
         '9d9e75d6519c723e5870801d5d43bf37'
         'cdce3aba22e7106a1278a512b7f6a028')

_svntrunk="http://svn.code.sf.net/p/madedit/code/trunk"
_svnmod="madedit"

build() {
  cd "$srcdir"
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build/$_svnmod"

  patch -p0 < "$srcdir/madedit-highlight-blink-fix.patch"
  patch -p0 < "$srcdir/madedit-onpaint-fix.patch"
  patch -p0 < "$srcdir/madedit-add-file-changed-event.patch"
  patch -p0 < "$srcdir/madedit-max-linelength-fix.patch"
  patch -p0 < "$srcdir/madedit-desktop-file.patch"

  ./configure LIBS=-lX11 --prefix=/usr --with-boost="$srcdir/boost_$_boost_ver/"
  make
}

package() {
  cd "$srcdir/$_svnmod-build/$_svnmod"
  make DESTDIR="$pkgdir/" install
}
