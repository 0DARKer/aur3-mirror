# Maintainer: Ner0

pkgname=cinnamon-dev
pkgver=20120429
pkgrel=1
pkgdesc="A fork of GNOME Shell with layout similar to GNOME 2 (Development git release)"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL2')
depends=('gnome-menus' 'libpulse' 'gnome-desktop' 'python2' 'gnome-bluetooth' 'gjs' 'libcroco' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'nautilus' 'gnome-session' 'muffin-wm' 'python2-gconf' 'libgnomekbd' 'gnome-menus2' 'metacity' 'notification-daemon' 'gnome-panel' 'gnome-settings-daemon' 'accountsservice')
makedepends=('intltool' 'gnome-common')
optdepends=('cinnamon-applets: A collection of applets [AUR]'
            'cinnamon-themes: Themes for Cinnamon')
provides=('cinnamon')
conflicts=('cinnamon' 'cinnamon-git')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=("$pkgname.zip::https://github.com/linuxmint/Cinnamon/zipball/8c7dad0c001e50bb320568db227aaaeb1a8d1871"
	'fix-cinnamon-settings.patch'
	'fix-build-with-gnome-bluetooth-3.4.patch'
	'cinnamon-1.4-notification.patch')
md5sums=('4a9e33369173473b7f219c15ec04f2ca'
         'de3485c3b05856e7a210c37332281c7a'
         '50b40f7fef34dc7ca75bfe749e18d83c'
         '28b6828e6d4d40829fe56c06a6e2fa51')

build() {
  cd linuxmint-*

  CFLAGS="$CFLAGS -Wno-deprecated-declarations"
  export PYTHON=python2

  patch -Np0 --no-backup-if-mismatch -i "$srcdir/fix-cinnamon-settings.patch"
  patch -Np1 -i "$srcdir/fix-build-with-gnome-bluetooth-3.4.patch"
  patch -Np1 -i "$srcdir/cinnamon-1.4-notification.patch"

  sed -i 's/python/python2/' files/usr/bin/cinnamon-{settings,menu-editor}
  sed -i 's/-DG_DISABLE_DEPRECATED//g' src/Makefile.in browser-plugin/Makefile.in

  chmod +x ./autogen.sh ./configure ./makepot ./restore ./test
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon \
   --localstatedir=/var --disable-static --disable-schemas-compile \
   --disable-schemas-install --enable-compile-warnings=yes

  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir"/linuxmint-*
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

  install -dm755 "$pkgdir/usr/share/gconf/schemas/"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/cinnamon.schemas" --domain cinnamon \
    "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
  chmod +x "$pkgdir"/usr/bin/* "$pkgdir"/usr/lib/cinnamon-settings/*.py
}
