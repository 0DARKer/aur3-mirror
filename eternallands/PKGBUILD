# Contributor: Adam Griffiths <adam_griffithsAATTdart.net.au>
# Contributor: Todd Maynard <arch@toddmaynard.com>
# Many thanks to AlexExtreme <alex@alex-smith.me.uk> (Frugalware pkg maintainer) from which much of this was borrowed.
# Many thanks to Stefan for patch for x86_64 support and el.ini fix.
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=eternallands
pkgver=1.9.1
_tagver=elc_1_9_1
pkgrel=1
pkgdesc="A free 3D MMORPG game with thousands of on-line players"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.eternal-lands.com/"
depends=('sdl_net' 'sdl_image' 'openal' 'cal3d' 'libxml2' 'libvorbis' 'libgl' 'mesa')
makedepends=('unzip' 'cvs')
options=('!emptydirs')
source=('http://www.eternal-lands.com/el_linux_191.zip' 'eternallands.desktop')
md5sums=('346dd0ffe99d4b0065d9487227988b2d' '4564fba195fc39fce438f717dde0ad9e')

build()
{
  cd $srcdir

  #Download the client...
  cvs -z3 -d:pserver:anonymous@cvs.elc.berlios.de:/cvsroot/elc co -r $_tagver elc
  
  #Compile the client...
  cd elc
  if [ "$CARCH" == "x86_64" ]; then
    sed -i "s/i686/x86-64/" make.conf
    sed -i "s/OPTIONS = /OPTIONS = -DX86_64 /" Makefile.linux
  fi
  sed -i "s@OPTIONS = @OPTIONS = -DDATA_DIR="\\\\\"/usr/share/eternallands/"\\\\\" @g" Makefile.linux
  make -f Makefile.linux release || return 1
  
  #Install the binary
  mkdir -p $pkgdir/usr/bin
  install -m755 el.x86.linux.bin $pkgdir/usr/bin/el
  
  #Install the license
  mkdir -p $pkgdir/usr/share/licenses/eternallands/
  install -m644 eternal_lands_license.txt $pkgdir/usr/share/licenses/eternallands/

  
  #Setup the desktop file
  cd $srcdir

  mkdir -p $pkgdir/usr/share/applications
  install -m644 eternallands.desktop $pkgdir/usr/share/applications
  
  mkdir -p $pkgdir/usr/share/pixmaps
  install -m644 elc/elc.png $pkgdir/usr/share/pixmaps/eternallands.png
  
  
  #Install all the support files
  mkdir -p $pkgdir/usr/share/eternallands
  cd $srcdir/el_linux

  #Compress textures and maps
  find \( -name *.bmp -or -name *.elm \) -exec gzip {} \;

  for dir in 2dobjects 3dobjects actor_defs animations languages maps meshes particles shaders skeletons skybox textures; do
	cp -R $dir $pkgdir/usr/share/eternallands/
  done
  
  for file in *.ini *.txt *.lst *.xml ; do
    install -m644 $file $pkgdir/usr/share/eternallands/
  done
}
