# Maintainer: Yukicanis <yukicanis@gmail.com>
# Contributor: Michael Eckert <michael.eckert@linuxmail.org>
# Edited by: Zachary Lund <admin@computerquip.com>

pkgname=xwayland-git
pkgver=1.15.0.43.g2660665
pkgrel=1
pkgdesc="Xorg Server with Wayland Support"
arch=('i686' 'x86_64')
url="http://wayland.freedesktop.org/"
license=('custom')
depends=('libxfont' 'pixman>=0.28.0' 'mesa-libgl')
conflicts=('xorg-server' 'xorg-server-devel' 'xorg-server-common' 'xwayland')
makedepends=('git' 'xtrans' 'xorg-util-macros' 'xorg-font-util'
             'libdmx' 'libxaw' 'libxkbfile' 'libxrender' 'libxres' 'libxtst'
             'xcb-util' 'xcb-util-wm' 'xcb-util-image' 'xcb-util-keysyms'
             'dri2proto' 'dri3proto' 'glproto' 'xcmiscproto' 'bigreqsproto'
             'randrproto' 'videoproto' 'compositeproto' 'scrnsaverproto'
             'resourceproto' 'xf86driproto' 'presentproto' 'xineramaproto')
provides=('xorg-server' 'xorg-server-common' 'xwayland' 'xorg-server-devel')
options=('!libtool')
source=('git+git://anongit.freedesktop.org/xorg/xserver#branch=xwayland'
        xserver-less-acpi-brokenness.patch
        10-quirks.conf)
sha256sums=('SKIP'
            '32cc2863656423ed99ec54a68d932efe9bc947185db2bb81ccf86293cac4cecc'
            '94612f5c0d34a3b7152915c2e285c7b462e9d8e38d3539bd551a339498eac166')

prepare() {
  cd $srcdir/xserver
  git reset --hard HEAD
  git clean -dfx

  # From Fedora. Do not build acpid code, it is buggy and we do not need it
  patch -Np0 -i ../xserver-less-acpi-brokenness.patch
}

build() {
  cd $srcdir/xserver
  autoreconf -fi
  ./configure --prefix=/usr \
      --enable-wayland \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-kdrive-evdev \
      --enable-kdrive-kbd \
      --enable-kdrive-mouse \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make

  # Disable subdirs for make install rule
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package() {
  cd $srcdir/xserver
  make DESTDIR="${pkgdir}" install

  # license
  install -m755 -d "${pkgdir}/usr/share/licenses/xwayland-git"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xwayland-git"
  
  # configuration
  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  # remove empty directories
  rmdir "${pkgdir}/usr/share/X11"
}

pkgver() {
  cd $srcdir/xserver
  git describe --always | sed 's|xorg-server-||g' | sed 's|-|.|g'
}
