# Maintainer: FadeMind <fademind@gmail.com>

pkgname=kde-thumbnailer-mmap
pkgver=1.0
pkgrel=2
pkgdesc="KDE thumbnailer plugin for Mind Mapping File created by MindManager."
arch=('i686' 'x86_64')
license=('GPL')
url="http://kde-apps.org/content/show.php/KDE+Mind+Mapping+File+Thumbnailer?content=148746"
depends=('kdebase-workspace' 'freemind')
makedepends=('automoc4' 'cmake' 'kdelibs')
source=("${pkgname}-${pkgver}.tar.bz2::http://kde-apps.org/CONTENT/content-files/148746-${pkgname}-${pkgver}.tar.bz2"
        'LICENSE')
sha256sums=('7f2f807d381c1511c676bf900dcfbed630e9c484ee2655cb33f9c4f04e88ee0c'
            '8ceb4b9ee5adedde47b31e975c1d90c73ad27b6b165a1dcd80c7c545eb65b903')

build() {
    install -d build/
    cd build/
    cmake ../${pkgname}/ \
        -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4
    make
}

package() {
    install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    make -C build DESTDIR="${pkgdir}" install
}
