# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=fltk
_ver=fltk-1.1
pkgname=fltk11
pkgver=1.1.10
pkgrel=2
pkgdesc="Graphical user interface toolkit for X. Legacy 1.1.xx version."
arch=(i686 x86_64)
url="http://www.fltk.org/"
license=('GPL')
depends=('libjpeg' 'libxft' 'libxext')
makedepends=('mesa')
provides=("$_name=$pkgver")
source=("http://ftp.easysw.com/pub/$_name/$pkgver/$_name-$pkgver-source.tar.bz2")
md5sums=('a1765594bc427ff892e36089fe1fa672')

build() {
  cd "$srcdir/$_name-$pkgver"

  export LDFLAGS="${LDFLAGS//,--as-needed}"

  # DSO link fix
  export LIBS=" -lfontconfig "

  ./configure --prefix=/usr \
              --libdir=/usr/lib/$_ver \
              --includedir=/usr/include/$_ver \
              --enable-threads \
              --enable-xft \
              --enable-shared \
              --enable-xdbe \
              --enable-localpng
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"
  make DESTDIR="$pkgdir/" install

  # remove unneeded stuff
  rm -rf "$pkgdir/usr/share"

  # rename apps
  cd "$pkgdir/usr/bin"
  mv $_name-config $_ver-config
  mv fluid fluid-1.1

  # static libs permission fix
  chmod 644 "$pkgdir/usr/lib/$_ver/"*.a
}

# vim:set ts=2 sw=2 et:
