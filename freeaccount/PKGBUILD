# Maintainer: speps <speps at aur dot archlinux dot org>

_name=freemedforms
pkgname=freeaccount
pkgver=0.7.6
pkgrel=1
pkgdesc="The FreeMedForms Medical billing and accountancy"
arch=(i686 x86_64)
url="http://www.freemedforms.com/en/"
license=('GPL')
depends=('qt')
install="$pkgname.install"
source=("http://freemedforms.googlecode.com/files/${_name}fullsources-$pkgver.tgz")
md5sums=('e4ba2e15eaa5e2e27d088e61077065c7')

build() {
  cd "$srcdir/$_name-$pkgver"

  qmake $pkgname.pro -r -config release \
    "CONFIG+=LINUX_INTEGRATED" \
    "INSTALL_ROOT_PATH=$pkgdir/usr/" \
    "LOWERED_APPNAME=$pkgname"

  make
}

package() {
  cd "$srcdir/$_name-$pkgver"

  make install

  # fix doc directories exec bit
  chmod a+x "$pkgdir/usr/share/$pkgname/forms"{,/subforms/emergencies}
}

# vim:set ts=2 sw=2 et:
