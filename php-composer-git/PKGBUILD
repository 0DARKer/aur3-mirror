# Maintainer: Joris Steyn <jorissteyn@gmail.com>
#
# Todo: compile and install phar file

pkgname=php-composer-git
pkgver=1.0.0.alpha7.3448.0822683
pkgrel=1
pkgdesc="A dependency manager for PHP (non-phar development snapshot)"
arch=('any')
url='http://getcomposer.org/'
license=('MIT')
depends=('php')
makedepends=('git')
provides=('php-composer')
conflicts=('php-composer')
# yes, that's a karaoke program 
conflicts=('composer' 'composer-git')
install='composer.install'

# we actually need a composer.phar to build a new composer
source=('git://github.com/composer/composer.git'
        'http://getcomposer.org/download/1.0.0-alpha7/composer.phar')

md5sums=('SKIP'
         'ef51599395560988ea3e16912bfd70f8')
        
pkgver() {
  cd "$srcdir"/composer

  # upstream doesn't use annotated tags, guess the last tag name
  lasttag=$(git tag|grep '1.*'|tail -n1)

  # desired format: [tagname]-[no. of commits]-[commit]
  echo ${lasttag}-$(git rev-list --count master).$(git rev-parse --short master)| sed 's|-|.|g'
}

build() {
  cd "$srcdir"/composer
  php ../composer.phar install

  sed -i "s/@package_version@/${pkgver}/g" ./src/Composer/Composer.php
}

package() {
  cd "$srcdir"
  install -d "$pkgdir/usr/share/php/composer/"
  cp -dr --no-preserve=ownership composer/* "$pkgdir/usr/share/php/composer/"
  chmod +x "$pkgdir/usr/share/php/composer/bin/composer"
}

# vim:set ts=2 sw=2 et:
