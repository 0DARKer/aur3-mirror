# Maintainer: Daniel Jour <daniel.oertwig at gmail dot com>
# Previous maintainer: Techlive Zheng <techlivezheng at gmail dot com>
# Contributor: Max Riveiro <kavu13@gmail.com>

_pkgname=tig
pkgname=tig-git
pkgdesc="Text-mode interface for git"
pkgver=2.0.2.r44.g73478d9
pkgrel=1
url="http://jonas.nitro.dk/tig/"
arch=('i686' 'x86_64')
license=('GPL')
source=('git://github.com/jonas/tig.git')
depends=('git' 'ncurses')
makedepends=('git'  'xmlto' 'asciidoc' 'docbook-xsl')
provides=('tig')
conflicts=('tig')
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"

  git describe --always | sed -E 's/^tig-//;s/([^-]*-g)/r\1/;s|-|.|g'
}

build() {
  cd "${_pkgname}"

  make configure

  ./configure --prefix=/usr --sysconfdir=/etc
}

package() {
  cd "${_pkgname}"

  make DESTDIR=${pkgdir} install
}
