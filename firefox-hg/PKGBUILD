# Contributor: Limao Luo <luolimao@gmail.com>
# Contributor: MutantMonkey <mutantmonkey@gmail.com>
# Contributor: Simon Gomizelj <simongmzlj@gmail.com>

pkgname=firefox-hg
pkgver=126011
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org, latest development version"
_ffver="10.0a1"
url="http://www.mozilla.org/projects/firefox/"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=(alsa-lib cairo dbus-glib desktop-file-utils gtk2 hunspell lcms libevent libevent libnotify
    libvpx libxrender libxt mime-types mozilla-common nspr-git nss sqlite3 startup-notification)
# Note - Mar 23, 2013: nspr-git package doesn't exist, but needs to be created because
# firefox needs version higher than the one in the main repos ([extra])
makedepends=(autoconf2.13 gconf mercurial python2 unzip wireless_tools xorg-server-xvfb yasm zip)
provides=("firefox=$_ffver")
url="http://www.mozilla.org/projects/firefox"
install=firefox.install
source=(mozconfig
    firefox-hg.desktop)
md5sums=('b2a0786b12f828c8c72230ad8205b4af'
    '4022581eb05405b82c489d5ea16b1a17')

_hgroot=http://hg.mozilla.org/mozilla-central
_hgrepo=mozilla-central

build() {
    cd "$srcdir"
    msg "Connecting to Mercurial server...."
    if [[ -d $_hgrepo/.hg ]]; then
        pushd $_hgrepo/ && hg pull -u
        msg2 "The local files are updated."
        popd
    else
        hg clone $_hgroot $_hgrepo
    fi
    msg2 "Mercurial checkout done or server timeout"

    cd $_hgrepo/
    msg "Building..."

    cp ../mozconfig .mozconfig

    # Kill @PRE_RELEASE_SUFFIX@ from browser.xul because it
    # gets set to \177 for an unknown reason
    sed -i 's/@PRE_RELEASE_SUFFIX@//g' browser/base/content/browser.xul

    export LDFLAGS="-Wl,-rpath,/usr/lib/firefox-$_ffver -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"
    export PYTHON="/usr/bin/python2"

    # PGO
    # sed -i '/^NO_PROFILE_GUIDED_OPTIMIZE = 1$/d' \
    #   memory/jemalloc/Makefile.in
    # echo 'LDFLAGS += -lX11 -lXrender' \
    #   >> layout/build/Makefile.in

    # LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
    # LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
    # kill $! || true
    make -j1 -f client.mk MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
    cd $_hgrepo/
    make -j1 -f client.mk DESTDIR="$pkgdir" install

    mv "$pkgdir"/usr/bin/firefox{,-hg}

    # update firefox.sh launcher with proper Firefox version
    # sed -i "s/firefox-hg/firefox-$_ffver/g" "$srcdir/firefox.sh"
    # install -m755 "$srcdir/firefox.sh" "$pkgdir/usr/bin/firefox-hg"

    for i in 16x16 32x32 48x48; do
        install -Dm644 browser/branding/nightly/default${i/x*/}.png \
            "$pkgdir/usr/share/icons/hicolor/$i/apps/firefox-hg.png"
    done

    install -Dm644 browser/branding/nightly/content/icon64.png \
        "$pkgdir/usr/share/icons/hicolor/64x64/apps/firefox-hg.png"

    install -Dm644 browser/branding/nightly/mozicon128.png \
        "$pkgdir/usr/share/icons/hicolor/128x128/apps/firefox-hg.png"

    install -Dm644 "$srcdir/firefox-hg.desktop" \
        "$pkgdir/usr/share/applications/firefox-hg.desktop"

    # We don't want the development stuff
    rm -r "$pkgdir"/usr/{include,lib/firefox-devel-$_ffver,share/idl}
}
