# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: MutantMonkey <mutantmonkey@gmail.com>
# Contributor: Simon Gomizelj <simongmzlj@gmail.com>

pkgname=firefox-hg
pkgver=23.0a1.129430.a09acc1ed635
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org, latest development version"
arch=(i686 x86_64)
url=http://www.mozilla.org/projects/firefox
license=(GPL LGPL MPL)
depends=(alsa-lib cairo dbus-glib desktop-file-utils gtk2 hunspell lcms libevent libevent libnotify
    libvpx libxrender libxt mime-types mozilla-common nspr nss sqlite3 startup-notification)
makedepends=(autoconf2.13 gconf mercurial python2 unzip wireless_tools xorg-server-xvfb yasm zip)
provides=(firefox=$pkgver)
install=firefox.install
source=($pkgname::hg+http://hg.mozilla.org/mozilla-central
    mozconfig
    firefox-hg.desktop)
sha256sums=('SKIP'
    'e85d5b3097fe6a6ae159cb17b494a3d78c5c48717ddb91c13a5ae36772cd0e49'
    'd550c56cbef2db09acf0a46f9e47e40646d5f8627e6272b59ec366936ebd4009')
sha512sums=('SKIP'
    'f0ecc4f1638f57bf3a13075ced8a813e6a815fd766730bfa76d79378adf5cb769086069485e1ad7888d8943d70eb056aa348d674f5156a95f8be57aa8e77ce47'
    'a8be37631253aaa505144e1d81b17e7d32ceb7fcf166623ed1e80703372165e1151ee5482b5a7006ec1981f6474141262f4eb3129e1870721424825f7702a7d1')

pkgver() {
    cd $pkgname/
    echo $(tail -n1 config/milestone.txt).$(hg identify -n).$(hg identify -i)
}

prepare() {
    cd $pkgname/
    cp ../mozconfig .mozconfig

    # Kill @PRE_RELEASE_SUFFIX@ from browser.xul because it
    # gets set to \177 for an unknown reason
    sed -i 's/@PRE_RELEASE_SUFFIX@//g' browser/base/content/browser.xul

    # PGO
    # sed -i '/^NO_PROFILE_GUIDED_OPTIMIZE = 1$/d' memory/jemalloc/Makefile.in
    # echo 'LDFLAGS += -lX11 -lXrender' >> layout/build/Makefile.in
}

build() {
    cd $pkgname/

    export LDFLAGS="-Wl,-rpath,/usr/lib/firefox-$_ffver -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"
    export PYTHON="/usr/bin/python2"

    # LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
    # LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
    # kill $!
    make -j1 -f client.mk MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
    cd $pkgname/
    make -j1 -f client.mk DESTDIR="$pkgdir" install

    mv "$pkgdir"/usr/bin/firefox{,-hg}

    # update firefox.sh launcher with proper Firefox version
    # sed -i "s/firefox-hg/firefox-$_ffver/g" "$srcdir/firefox.sh"
    # install -m755 "$srcdir/firefox.sh" "$pkgdir/usr/bin/firefox-hg"

    for i in default{16,32,48} icon64 mozicon128; do
        local n=${i//[a-z]}
        install -Dm644 browser/branding/nightly/$i.png \
            "$pkgdir"/usr/share/icons/hicolor/${n}x${n}/apps/$pkgname.png
    done

    desktop-file-install ../$pkgname.desktop --dir "$pkgdir"/usr/share/applications/

    # We don't want the development stuff
    rm -rf "$pkgdir"/usr/{include,lib/firefox-devel-$_ffver,share/idl}
}
