# Maintainer: Daniel Kirchner <daniel AT ekpyron DOT org>
# Contributor: Davorin Uƒçakar <davorin.ucakar@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Mihai Militaru <mihai.militaru@gmx.com>
# Contributor: scippio <scippio@berounet.cz>

pkgname=mingw-w64-freeimage
pkgver=3.15.3
pkgrel=1
pkgdesc="Library project for developers who would like to support popular graphics image formats (mingw-w64)."
arch=(any)
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=('mingw-w64-gcc')
options=(!strip !buildflags)
source=("http://downloads.sourceforge.net/sourceforge/freeimage/FreeImage${pkgver//./}.zip"
        'libraw.patch' 'mingw64.patch')
md5sums=('6c305b4086500ff8541c596ea6fc7cd7'
         '440a85a8519f8fa5c6ff2eff7dddb78c'
         '341b1b873aa990e8819971128ae8cf70')

build() {
  cd ${srcdir}

  cd FreeImage
  patch -Np1 < ${srcdir}/libraw.patch

  cd ..

  rm -rf FreeImage-build32 FreeImage-build64
  cp -r FreeImage FreeImage-build32
  cp -r FreeImage FreeImage-build64

  cd FreeImage-build32
  
  make \
    CC="i686-w64-mingw32-gcc" \
    CXX="i686-w64-mingw32-g++" \
    LD="i686-w64-mingw32-g++" \
    RC="i686-w64-mingw32-windres" \
    AR="i686-w64-mingw32-ar" \
    DLLTOOL="i686-w64-mingw32-dlltool" \
    -f Makefile.mingw
  
  cd ../FreeImage-build64

  patch -Np0 < ${srcdir}/mingw64.patch

  make \
    CC="x86_64-w64-mingw32-gcc" \
    CXX="x86_64-w64-mingw32-g++" \
    LD="x86_64-w64-mingw32-g++" \
    RC="x86_64-w64-mingw32-windres" \
    AR="x86_64-w64-mingw32-ar" \
    DLLTOOL="x86_64-w64-mingw32-dlltool" \
    -f Makefile.mingw

}

package() {
  cd ${srcdir}/FreeImage-build32

  install -Dm644 Dist/FreeImage.dll ${pkgdir}/usr/i686-w64-mingw32/bin/FreeImage.dll
  install -Dm644 Dist/FreeImage.lib ${pkgdir}/usr/i686-w64-mingw32/lib/FreeImage.lib
  install -Dm644 Dist/FreeImage.h   ${pkgdir}/usr/i686-w64-mingw32/include/FreeImage.h

  cd ${srcdir}/FreeImage-build64

  install -Dm644 Dist/FreeImage.dll ${pkgdir}/usr/x86_64-w64-mingw32/bin/FreeImage.dll
  install -Dm644 Dist/FreeImage.lib ${pkgdir}/usr/x86_64-w64-mingw32/lib/FreeImage.lib
  install -Dm644 Dist/FreeImage.h   ${pkgdir}/usr/x86_64-w64-mingw32/include/FreeImage.h
}
