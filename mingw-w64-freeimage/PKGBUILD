# Maintainer: Daniel Kirchner <daniel AT ekpyron DOT org>
# Contributor: Davorin Uƒçakar <davorin.ucakar@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Mihai Militaru <mihai.militaru@gmx.com>
# Contributor: scippio <scippio@berounet.cz>

pkgname=mingw-w64-freeimage
pkgver=3.16.0
pkgrel=1
pkgdesc="Library project for developers who would like to support popular graphics image formats (mingw-w64)."
arch=(any)
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=(!strip !buildflags staticlibs)
source=("http://downloads.sourceforge.net/sourceforge/freeimage/FreeImage${pkgver//./}.zip" 'mingw64.patch')
md5sums=('1a2d1fff6204adbd479cc98818892fc1'
         '971b7953b820e7c608317567abe0c300')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}

  cd FreeImage
  patch -Np1 -i ${srcdir}/mingw64.patch
  sed -i "s|IMPORTLIB = \$(TARGET).lib|IMPORTLIB = \$(TARGET).dll.a|g" Makefile.mingw
  sed -i 's/#include "..\\x86\\x86.h"/#include "..\/x86\/x86.h"/' Source/LibJXR/image/sys/strcodec.h

  # force the use of mingw-w64 guiddef.h
  rm Source/LibJXR/common/include/guiddef.h
  cd ..

  for _arch in ${_architectures}; do
    rm -rf FreeImage-${_arch}
    cp -r FreeImage FreeImage-${_arch}
    pushd FreeImage-${_arch}
  
  make \
    CC=${_arch}-gcc \
    CXX=${_arch}-g++ \
    LD=${_arch}-g++ \
    RC=${_arch}-windres \
    AR=${_arch}-ar \
    DLLTOOL=${_arch}-dlltool \
    FREEIMAGE_LIBRARY_TYPE="STATIC" \
    -f Makefile.mingw
  
  make \
    CC=${_arch}-gcc \
    CXX=${_arch}-g++ \
    LD=${_arch}-g++ \
    RC=${_arch}-windres \
    AR=${_arch}-ar \
    DLLTOOL=${_arch}-dlltool \
    FREEIMAGE_LIBRARY_TYPE="SHARED" \
    -f Makefile.mingw

    popd
  done  

}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/FreeImage-${_arch}
    install -d "${pkgdir}"/usr/${_arch}/lib
    install -d "${pkgdir}"/usr/${_arch}/bin
    install -d "${pkgdir}"/usr/${_arch}/include
    install -m755 Dist/FreeImage.dll "${pkgdir}"/usr/${_arch}/bin/
    install -m644 Dist/FreeImage.dll.a "${pkgdir}"/usr/${_arch}/lib/libFreeImage.dll.a
    install -m644 Dist/libFreeImage.a "${pkgdir}"/usr/${_arch}/lib/
    install -m644 Dist/FreeImage.h   "${pkgdir}"/usr/${_arch}/include/
       
    ${_arch}-strip --strip-unneeded "${pkgdir}"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "${pkgdir}"/usr/${_arch}/lib/*.a
  done
}
