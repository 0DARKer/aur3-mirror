# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=jabref-git
pkgver=v_2.9b.69.g8cdd3bc
pkgrel=2
pkgdesc="GUI frontend for BibTeX, written in Java --built from git"
arch=('any')
url="http://jabref.sourceforge.net/"
license=('GPL')
depends=('java-runtime' 'sh')
makedepends=('git' 'apache-ant')
provides=('jabref')
conflicts=('jabref')
source=('git://jabref.git.sourceforge.net/gitroot/jabref/jabref' 'jabref.desktop' 'jabref.sh')
md5sums=('SKIP'
         '496d2094ee194a5568baee7af5b70fa2'
         '9da2f8a5010e25bb04c81225309cc9e5')
_gitname="jabref"

pkgver() {
  cd $_gitname
  # Git tag
  echo $(git describe --always|sed 's/-/./g')
}

build() {
  cd "$srcdir/$_gitname/$_gitname"
  /etc/profile.d/apache-ant.sh
  ant
}

package() {
  cd "$srcdir/$_gitname"
  install -Dm644 jabref/build/lib/JabRef-2.9b.jar \
    ${pkgdir}/usr/share/java/JabRef.jar 
  install -Dm755 $srcdir/jabref.sh ${pkgdir}/usr/bin/jabref
  install -Dm644 $srcdir/jabref.desktop \
    ${pkgdir}/usr/share/applications/jabref.desktop
  install -Dm644 jabref/build/images/JabRef-icon-48.png \
    ${pkgdir}/usr/share/pixmaps/jabref.png
  install -d ${pkgdir}/usr/share/doc/$pkgname
  cp -r htdocs/{help,manuals} ${pkgdir}/usr/share/doc/$pkgname
}
