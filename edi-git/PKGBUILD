# Maintainer: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=edi-git
pkgver=fc6cd2e
pkgrel=1
pkgdesc="The EFL IDE"
arch=('i686' 'x86_64')
url="https://phab.enlightenment.org/w/projects/edi/"
license=('GPL2')
depends=('clang' 'efl' 'elementary')
makedepends=('git')
source=("git://git.enlightenment.org/tools/edi.git" "tardest.patch")
md5sums=('SKIP'
         '56b27a047c61a5eb83262c5612d95c5d')

_gitname=edi

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git describe --always | sed 's/-/./g')
}

prepare() {
  cd "$srcdir/$_gitname"
  patch -Np1 -i "$srcdir/tardest.patch"
}

build() {
  cd "$srcdir/$_gitname"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
  chown -R root:root "$pkgdir/usr/share/edi/skeleton"
}
