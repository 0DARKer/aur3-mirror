# AUR workaround
pkgname=python-regex
true && pkgbase=python-regex
true && pkgname=('python-regex' 'python2-regex')
pkgver=2014.01.30
pkgrel=1
pkgdesc="Alternative regular expression module, to replace re."
arch=('i686' 'x86_64' 'armv7h')
url="https://code.google.com/p/mrab-regex-hg/"
license=('custom')
depends=('python2')
makedepends=('python-setuptools')
conflicts=('python2-regex-hg')
source=(https://pypi.python.org/packages/source/r/regex/regex-${pkgver}.tar.gz)
md5sums=('a35afff4e984a000fea3f14c6bd461b3')

prepare() {
  cd $srcdir
  cp -r regex-$pkgver python2-regex-$pkgver
}

build() {
  cd "$srcdir/regex-$pkgver"
  python setup.py build

  cd $srcdir/python2-regex-$pkgver
  python2 setup.py build
}

package_python-regex() {
  depends=('python')
  cd "$srcdir/regex-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir"
}

package_python2-regex() {
  depends=('python2')
  cd "$srcdir/python2-regex-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir"
}
