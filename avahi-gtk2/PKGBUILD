# Maintainer: Brian Bidulock <bidulock@openss7.org>

_pkgname=avahi
pkgname=avahi-gtk2
pkgver=0.6.31
pkgrel=6
pkgdesc='Multicast/unicast DNS-SD framework (with Gtk2 client apps)'
url='http://www.avahi.org/'
license=('LGPL')
arch=('i686' 'x86_64')
options=('!libtool' '!emptydirs')
depends=('expat' 'libdaemon' 'glib2' 'libcap' 'gdbm')
provides=("${_pkgname}=${pkgver}")
conflicts=(${_pkgname})
replaces=(${_pkgname})
optdepends=('dbus: communicating with client applications'
            'gtk2: avahi-discover-standalone, bshell, bssh, bvnc, gtk2 bindings'
            'qt3: qt3 bindings'
            'qt4: qt4 bindings'
            'pygtk: avahi-bookmarks, avahi-discover'
            'twisted: avahi-bookmarks'
            'python2-dbus: avahi-discover'
            'nss-mdns: NSS support for mDNS')
makedepends=('qt3' 'qt4' 'pygtk' 'intltool' 'python2-dbus'
             'gobject-introspection' 'gtk2' 'xmltoman')
backup=('etc/avahi/hosts'
        'etc/avahi/avahi-daemon.conf'
        'etc/avahi/services/ssh.service'
        'etc/avahi/services/sftp-ssh.service'
	'usr/lib/avahi/service-types.db'
	'usr/share/avahi/service-types')
source=("http://www.avahi.org/download/avahi-${pkgver}.tar.gz"
        'rc.d.patch')
sha1sums=('7e05bd78572c9088b03b1207a0ad5aba38490684'
          '625ad7c131c0c1c383caeddef18fc7a32d8f3ab9')

conflicts=('howl' 'mdnsresponder')
provides=('howl' 'mdnsresponder')

install=install

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	sed -i 's/netdev/network/g' avahi-daemon/avahi-dbus.conf
	patch -p1 -i ../rc.d.patch

	export MOC_QT3=/opt/qt/bin/moc
	export MOC_QT4=/usr/lib/qt4/bin/moc
	source /etc/profile.d/qt3.sh
	source /etc/profile.d/qt4.sh

	# pygtk requires python2; make it explicit in case other python are installed: FS#21865
	PYTHON=python2 \
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-mono \
		--disable-gtk3 \
		--disable-static \
		--disable-monodoc \
		--enable-compat-libdns_sd \
		--enable-compat-howl \
		--with-distro=archlinux \
		--with-avahi-priv-access-group=network \
		--with-autoipd-user=avahi \
		--with-autoipd-group=avahi \
		--with-systemdsystemunitdir=/usr/lib/systemd/system \

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	cd "${pkgdir}"
	sed -i '1c #!/usr/bin/python2' usr/bin/avahi-{bookmarks,discover}
	sed -i 's:/sbin/resolvconf:/usr/sbin/resolvconf:g' etc/avahi/avahi-dnsconfd.action

	# howl and mdnsresponder compatability
	(cd usr/include; ln -s avahi-compat-libdns_sd/dns_sd.h dns_sd.h; ln -s avahi-compat-howl howl)
	(cd usr/lib/pkgconfig; ln -s avahi-compat-howl.pc howl.pc)
}
