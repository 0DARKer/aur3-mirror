# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: hokasch <hokasch at operamail dot com>
# Contributor: Dennis Brendel <buddabrod at gmail dot com>
# Contributor: Mathias Buren <mathias.buren at gmail dot com>
# Contributor: Benjamin Mtz (Cruznick) <cruznick at archlinux dot us>

pkgname=compat-wireless-patched
pkgver=3.2_1_s
_upver="${pkgver//_/-}"
pkgrel=1
pkgdesc='Compat wireless driver patched for fixing "fixed-channel -1" issue and better injection'
url='http://wireless.kernel.org/en/users/Download/stable/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('linux')
makedepends=('linux-api-headers' 'linux-headers')
source=("http://www.orbit-lab.org/kernel/compat-wireless-${pkgver:0:1}-stable/v${pkgver:0:3}/compat-wireless-${_upver}.tar.bz2" \
        'eeprom_93xx46.h::http://git.kernel.org/?p=linux/kernel/git/stable/linux-stable.git;a=blob_plain;f=include/linux/eeprom_93xx46.h;h=06791811'
        'eeprom_93xx46.c::http://git.kernel.org/?p=linux/kernel/git/stable/linux-stable.git;a=blob_plain;f=drivers/misc/eeprom/eeprom_93xx46.c;h=0c7ebb1e'
        'mac80211.compat08082009.wl_frag+ack_v1.patch'
        'channel-negative-one-maxim.patch')
sha1sums=('591d6ebe7b1319e7d40bb5decd7100067b83e6da'
          '7631b5a883c605b5c352fa69e004872796f6e11b'
          'a50247dacd10b4e744014b35b5930e0db5aa7b21'
          '85f7a1b141549b774f5631fba259bc414aeeffb8'
          'a611acdd7994b07b0b39417ef7a5a6ffc866a733')

install=install

build() {
	cd "${srcdir}/compat-wireless-${_upver}"

	echo '#define br_port_exists(dev) (dev->priv_flags & IFF_BRIDGE_PORT)' >> net/wireless/core.h # rfkill.h does not use compat-3.1.h
	mv ../eeprom_93xx46.c drivers/misc/eeprom/eeprom_93xx46.c # missing file from compat-wireless tarball
	mv ../eeprom_93xx46.h include/linux/eeprom_93xx46.h # missing file from compat-wireless tarball

	patch -p1 -i ../mac80211.compat08082009.wl_frag+ack_v1.patch 
	patch -p1 -i ../channel-negative-one-maxim.patch 

	# Uncomment the line below if you just want one specific driver or group to be built (otherwise, all will be).
	# Supported drivers: ath5k ath9k ath9k_htc carl9170 b43 zd1211rw rt2x00 wl1251 wl12xx ath6kl brcm80211 
	# Supported groups: atheros ath iwlagn rtl818x rtlwifi wl12xx atlxx bt
	#scripts/driver-select wl12xx
	
	# Uncomment the line below if you encounter problems with rt2870.
	#sed -i 's/^\# CONFIG_RT2800USB_RT30XX/CONFIG_RT2800USB_RT30XX/' config.mk

	make
}

package() {
	cd "${srcdir}/compat-wireless-${_upver}"

	make INSTALL_MOD_PATH="${pkgdir}" install-modules
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;

	install -d "${pkgdir}"/usr/sbin
	install scripts/{athenable,athload,b43enable,b43load,iwl-enable,iwl-load,madwifi-unload} "${pkgdir}"/usr/sbin/

	install -d "${pkgdir}"/usr/lib/compat-wireless
	install scripts/{check_depmod,modlib.sh} "${pkgdir}"/usr/lib/compat-wireless/

	install -d "${pkgdir}"/lib/udev/rules.d
	install udev/50-compat_firmware.rules "${pkgdir}"/lib/udev/rules.d/
	install udev/compat_firmware.sh	"${pkgdir}"/lib/udev/
}
