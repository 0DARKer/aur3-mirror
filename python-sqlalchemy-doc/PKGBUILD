# $Id: PKGBUILD 48930 2011-06-07 18:55:04Z angvp $ 
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
pkgname="python-sqlalchemy-doc"
pkgver=0.7.1
pkgdesc="Python SQL toolkit and Object Relational Mapper"
pkgrel=1
arch=('any')
url="http://www.sqlalchemy.org/"
license=('MIT')
makedepends=('python-sphinx' 'python-mako')
source=(http://downloads.sourceforge.net/sourceforge/sqlalchemy/SQLAlchemy-$pkgver.tar.gz)
md5sums=('76e2e1e48b8047f1ca7023f143374aef')

build() {
  cd "${srcdir}/SQLAlchemy-${pkgver}"
  sed -i -r "s@^(templates_path = \[')templates@\1doc/build/templates@" doc/build/conf.py
  python2 setup.py build_sphinx
}

package() {
  cd "${srcdir}/SQLAlchemy-${pkgver}"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  while read -d ''; do
    install -Dm644 "$REPLY" "$pkgdir/usr/share/doc/$pkgname/${REPLY#build/sphinx/html/}"
  done < <(find build/sphinx/html -type f -print0)
}
