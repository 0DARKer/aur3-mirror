# Maintainer: Prurigro

pkgname=wla_dx
_pkgver=9.5
pkgver=${_pkgver}a
pkgrel=2
pkgdesc="Multi Platform Cross Assembler Package for GB-Z80 (Game Boy), Z80 (GG, SMS, MSX, Spectrum, Ti86, etc), 6502 (NES, etc), 65C02 (VIC-20, etc), 6510 (C64, etc), 65816 (SNES, etc), HUC6280 (PC-Engine) and SPC-700 (SNES sound chip)"
url="http://www.villehelin.com/wla.html"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('bash')
source=("http://www.villehelin.com/${pkgname}_${pkgver}.tar.gz")
sha512sums=('1c94ad3e851cd3885a2ff5a7660c7838540297eeb4c40c1c46041713412a94c27e04de77d65f6d75fa6ca532f2a18facfb258a629a1e0af6c645a7b6826fbbaf')

build() {
    cd ${pkgname}_${_pkgver}

    unset CFLAGS
    export CC=gcc LD=gcc
    sh unix.sh $(cat /proc/cpuinfo | grep "cpu cores" -m 1 | grep -o -E [0-9]*$)
}

package() {
    # Install assemblers
    install -d "${pkgdir}/usr/bin"
    install -m755 ${pkgname}_${_pkgver}/binaries/* "${pkgdir}/usr/bin/"

    # Install supplimentary files
    install -d "${pkgdir}/usr/share/${pkgname}"
    cp -R ${pkgname}_${_pkgver}/txt "${pkgdir}/usr/share/${pkgname}/"
    cp -R ${pkgname}_${_pkgver}/examples "${pkgdir}/usr/share/${pkgname}/"
    cp -R ${pkgname}_${_pkgver}/memorymaps "${pkgdir}/usr/share/${pkgname}/"
    cp -R ${pkgname}_${_pkgver}/makefiles "${pkgdir}/usr/share/${pkgname}/"
    find "${pkgdir}/usr/share" -type f -exec chmod 644 '{}' \;
    find "${pkgdir}/usr/share" -type d -exec chmod 755 '{}' \;
}
