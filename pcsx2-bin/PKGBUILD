# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>
# Contributor: 

pkgname=pcsx2-bin
pkgver=0.9.8
pkgrel=3
_pkgrev=r4594
_extrainfo=-linux~translation_fix
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
if [ $CARCH == "x86_64" ]; then
	depends=('lib32-alsa-lib' 'lib32-bzip2' 'lib32-glew1.5'
		 'lib32-libjpeg6' 'lib32-nvidia-cg-toolkit'
		 'lib32-portaudio' 'lib32-sdl' 'lib32-wxgtk'
		 'lib32-zlib')
else
	depends=('alsa-lib' 'bzip2' 'glew1.5' 'libjpeg6'
		 'nvidia-cg-toolkit' 'portaudio' 'sdl'
		 'wxgtk' 'zlib')
fi
provides=('pcsx2')
conflicts=('pcsx2' 'pcsx2-svn')
options=('!emptydirs')
install="pcsx2.install"
source=("http://pcsx2.googlecode.com/files/pcsx2-$pkgver-${_pkgrev}${_extrainfo}.tar.gz"
	'pcsx2.sh' 'pcsx2.xpm' 'pcsx2.desktop')
md5sums=('7441387fd223827248c23727d5bc87c7'
	 'c3f5a0dc776362db1950c8b0eb4c0a69'
         '18cb3c5a243a27363553d3e1008210c0'
         'b055ff7c0f236f33702f4813ae4b980d')

package() {
	cd "${srcdir}/pcsx2-$pkgver-$_pkgrev-linux"

	mkdir -p "${pkgdir}/opt/pcsx2" \
			"${pkgdir}/usr/share/man/man1" \
			"${pkgdir}/usr/share/locale"
	# Install PCSX2			
	cp -rPf * "${pkgdir}/opt/pcsx2/"
	# Install translation files
	mv "${pkgdir}/opt/pcsx2/Langs/"* \
		"${pkgdir}/usr/share/locale/"
	# Install man page
	mv "${pkgdir}/opt/pcsx2/docs/pcsx2.man" \
		"${pkgdir}/usr/share/man/man1/pcsx2.1"
	# Install runnable script, icon and desktop item
	install -Dm755 "${srcdir}/pcsx2.sh" \
			"${pkgdir}/usr/bin/pcsx2"
	install -Dm644 "${srcdir}/pcsx2.xpm" \
			"${pkgdir}/usr/share/pixmaps/pcsx2.xpm"
	install -Dm644 "${srcdir}/pcsx2.desktop" \
			"${pkgdir}/usr/share/applications/pcsx2.desktop"
	# Removing uneeded stuff / cleanup
	rmdir "${pkgdir}/opt/pcsx2/"{Langs,bios,help}
	rm "${pkgdir}/opt/pcsx2/launch_pcsx2_linux.sh"
}
