# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=tigervnc-svn
pkgver=4906
pkgrel=1
_xorgver=1.12.1
pkgdesc="TigerVNC is a suite of VNC servers and clients that have a focus on performance and remote display functionality. Originally this software was based on the (never released) VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('pam' 'gnutls' 'zlib' 'libjpeg-turbo' 'libxft' 'libxinerama' 'libxtst' 'libxfont' 'libxmu' 'libxkbfile' 'libxrender' 'pixman' 'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config' 'libgl')
makedepends=('subversion' 'cmake' 'nasm' 'xorg-font-util' 'xorg-util-macros' 'bigreqsproto' 'compositeproto' 'damageproto' 'randrproto' 'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xf86vidmodeproto' 'xtrans' 'mesa')
provides=('tigervnc')
conflicts=('tigervnc' 'tightvnc')
source=(cmakelists.patch
        xorg111.patch
        xorg112.patch
        gnutls.patch
        gethomedir.patch
        ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2)
md5sums=('069f8e38df187c4909372bac284a3981'
         'b680b55f7f9f569cd26ac0b330012a8b'
         'dcb1910b3f89a051a4dce480ae777d53'
         '1a5fe8867ab3e49e20aafc70c415494f'
         'fa059baabe72308bf82622bca73cb3e0'
         '20e73b422bda6f0d1405af676983cc70')

_svntrunk=https://tigervnc.svn.sourceforge.net/svnroot/tigervnc/trunk
_svnmod=tigervnc

build() {
  cd ${srcdir}

  msg "Connecting to SVN server..."
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done or server timeout"

  if [ -d ${_svnmod}-build ]; then
    msg "Deleting old build directory..."
    rm -rf ${_svnmod}-build
  fi

  msg "Setting up build environment..."
  svn export ${_svnmod} ${_svnmod}-build
  cd ${_svnmod}-build
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver
  for p in ${srcdir}/*.patch; do patch -p1 < $p; done

  msg "Starting build..."
  cmake -G "Unix Makefiles" \
	-DCMAKE_INSTALL_PREFIX=/usr -DUSE_INCLUDED_FLTK=yes
  make

  cd unix/xserver
  patch -p1 < ../xserver110.patch
  autoreconf -fiv
  ./configure --prefix=/usr \
	--disable-static --disable-xinerama --without-dtrace \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-config-dbus --disable-config-hal --disable-config-udev \
	--disable-unit-tests --disable-devel-docs --disable-dri \
	--enable-dri2 --enable-glx --enable-glx-tls
  make
}

package() {
  cd ${srcdir}/${_svnmod}-build
  make DESTDIR=${pkgdir} install

  cd unix/xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/xorg/modules/extensions/libvnc.la

  # start vncconfig in no-window mode, the window is annoying :)
  sed -i 's/iconic/nowin/' ${pkgdir}/usr/bin/vncserver
}
