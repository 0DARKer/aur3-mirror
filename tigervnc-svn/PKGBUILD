# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=tigervnc-svn
pkgver=4731
pkgrel=1
_xorgver=1.11.1
pkgdesc="TigerVNC is a suite of VNC servers and clients that have a focus on performance and remote display functionality. Originally this software was based on the (never released) VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('zlib' 'libjpeg-turbo' 'freetype2' 'libsm' 'libxtst' 'libxi' 'libxfont' 'libxmu' 'libxkbfile' 'libxrender' 'libgl' 'pixman' 'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config' 'xorg-fonts-alias' 'xorg-fonts-misc')
makedepends=('subversion' 'cmake' 'nasm' 'xorg-util-macros' 'xorg-font-util' 'inputproto' 'xextproto' 'xproto' 'bigreqsproto' 'damageproto' 'dri2proto' 'fixesproto' 'fontsproto' 'randrproto' 'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xineramaproto' 'xf86driproto' 'xf86vidmodeproto' 'glproto' 'renderproto' 'mesa' 'libpciaccess' 'xtrans')
provides=('tigervnc')
conflicts=('tigervnc' 'tightvnc')
source=(cmakelists.patch
        xorg111.patch
        ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2)
md5sums=('3c9a47acf2d68ca81aced86c1b07ac87'
         '0e17923898a483af847ad99bec73d3ea'
         '4cdf54dc569b6a5548257c93a45dcd3d')

_svntrunk=https://tigervnc.svn.sourceforge.net/svnroot/tigervnc/trunk
_svnmod=tigervnc

build() {
  cd ${srcdir}

  msg "Connecting to SVN server..."
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done or server timeout"

  if [ -d ${_svnmod}-build ]; then
    msg "Deleting old build directory..."
    rm -rf ${_svnmod}-build
  fi

  msg "Setting up build environment..."
  svn export ${_svnmod} ${_svnmod}-build
  cd ${_svnmod}-build
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver
  patch -Np1 -i ${srcdir}/cmakelists.patch
  patch -Np1 -i ${srcdir}/xorg111.patch

  msg "Starting build..."
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr -DUSE_INCLUDED_FLTK=yes \
                            -DENABLE_GNUTLS=no -DENABLE_PAM=no .
  make

  cd unix/xserver
  patch -Np1 -i ../xserver110.patch
  autoreconf -fiv
  ./configure --prefix=/usr \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-static --disable-xinerama --disable-composite \
	--disable-config-dbus --disable-config-hal --disable-config-udev \
	--enable-dri2 --enable-glx --enable-glx-tls --disable-unit-tests
  make
}

package() {
  cd ${srcdir}/${_svnmod}-build
  make DESTDIR=${pkgdir} install

  cd unix/xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/xorg/modules/extensions/libvnc.la
}
