# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=tigervnc-svn
pkgver=5048
pkgrel=1
_xorgver=1.13.2
pkgdesc="suite of VNC servers and clients. Based on the VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('fltk-snapshot' 'pam' 'gnutls' 'libxtst' 'libxfont' 'pixman'
         'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config' 'libgl')
makedepends=('cmake' 'nasm' 'xorg-font-util' 'xorg-util-macros' 'bigreqsproto'
             'compositeproto' 'damageproto' 'dri2proto' 'glproto' 'randrproto'
             'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto'
             'xf86vidmodeproto' 'xtrans' 'mesa' 'subversion')
options=(!libtool)
conflicts=('tigervnc' 'tightvnc')
provides=('tigervnc')
source=(ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2
	vncserver.service
	vncviewer.desktop
        gethomedir.patch
        glx.patch)
md5sums=('553fd7902e1156115f15cc1656f46a6f'
         '0903d5a0dfa38e0b04964505b644585c'
         'c93fd99581200be4cb4716cda2122065'
         '22f1523a0eca56ad79cfabd0db6e2cf6'
         '3af7711177600ba726ee490e7978ae9b')

_svntrunk=https://tigervnc.svn.sourceforge.net/svnroot/tigervnc/trunk
_svnmod=tigervnc

build() {
  cd ${srcdir}

  msg "Connecting to SVN server..."
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done or server timeout"

  if [ -d ${_svnmod}-build ]; then
    msg "Deleting old build directory..."
    rm -rf ${_svnmod}-build
  fi

  msg "Setting up build environment..."
  svn export -q ${_svnmod} ${_svnmod}-build
  cd ${srcdir}/${_svnmod}-build
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver
  patch -Np1 -i ${srcdir}/gethomedir.patch
  patch -Np1 -i ${srcdir}/glx.patch

  msg "Starting build..."
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd unix/xserver
  patch -Np1 -i ../xserver113.patch
  autoreconf -fiv
  ./configure --prefix=/usr \
	--disable-static --disable-xinerama --without-dtrace \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-config-dbus --disable-config-hal --disable-config-udev \
	--disable-unit-tests --disable-devel-docs --disable-selective-werror \
	--disable-dri --enable-dri2 --enable-glx --enable-glx-tls
  make
}

package() {
  cd ${srcdir}/${_svnmod}-build
  make DESTDIR=${pkgdir} install
  cd unix/xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  sed -i 's/iconic/nowin/' ${pkgdir}/usr/bin/vncserver
  install -Dm0644 $srcdir/vncserver.service $pkgdir/usr/lib/systemd/system/vncserver.service
  install -Dm0644 $srcdir/vncviewer.desktop $pkgdir/usr/share/applications/vncviewer.desktop
}
