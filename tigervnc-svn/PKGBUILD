# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>
# cmake nasm xorg-util-macros xorg-font-util bigreqsproto resourceproto xcmiscproto xf86driproto xtrans gnutls

pkgname=tigervnc-svn
pkgver=4826
pkgrel=2
_xorgver=1.11.3
pkgdesc="TigerVNC is a suite of VNC servers and clients that have a focus on performance and remote display functionality. Originally this software was based on the (never released) VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('zlib' 'libjpeg-turbo' 'fontconfig' 'libsm' 'libxtst' 'libxi' 'libxfont' 'libxmu' 'libxkbfile' 'libxrender' 'libgl' 'pixman' 'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config' 'xorg-fonts-alias' 'xorg-fonts-misc')
makedepends=('subversion' 'cmake' 'nasm' 'xorg-util-macros' 'xorg-font-util' 'inputproto' 'xextproto' 'xproto' 'bigreqsproto' 'compositeproto' 'damageproto' 'dri2proto' 'fixesproto' 'fontsproto' 'randrproto' 'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xineramaproto' 'xf86driproto' 'xf86vidmodeproto' 'glproto' 'renderproto' 'mesa' 'libpciaccess' 'xtrans')
provides=('tigervnc')
conflicts=('tigervnc' 'tightvnc')
source=(cmakelists.patch
        xorg111.patch
        ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2)
md5sums=('3b35ee48742c5ea104d7fd104a3c6ef1'
         'b680b55f7f9f569cd26ac0b330012a8b'
         'a7194c437963627e1db0dd2d6c1a1984')

_svntrunk=https://tigervnc.svn.sourceforge.net/svnroot/tigervnc/trunk
_svnmod=tigervnc

build() {
  cd ${srcdir}

  msg "Connecting to SVN server..."
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done or server timeout"

  if [ -d ${_svnmod}-build ]; then
    msg "Deleting old build directory..."
    rm -rf ${_svnmod}-build
  fi

  msg "Setting up build environment..."
  svn export ${_svnmod} ${_svnmod}-build
  cd ${_svnmod}-build
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver
  for p in ${srcdir}/*.patch; do patch -p1 < $p; done

  msg "Starting build..."
  cmake -G "Unix Makefiles" \
	-DCMAKE_INSTALL_PREFIX=/usr -DUSE_INCLUDED_FLTK=yes \
	-DENABLE_GNUTLS=no -DENABLE_PAM=no .
  make

  cd unix/xserver
  patch -p1 < ../xserver110.patch
  autoreconf -fiv
  ./configure --prefix=/usr \
	--disable-static --disable-xinerama \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-config-dbus --disable-config-hal --disable-config-udev \
	--disable-unit-tests --disable-devel-docs --disable-dri \
	--enable-dri2 --enable-glx --enable-glx-tls
  make
}

package() {
  cd ${srcdir}/${_svnmod}-build
  make DESTDIR=${pkgdir} install

  cd unix/xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/xorg/modules/extensions/libvnc.la
}
