# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=tigervnc-svn
pkgver=4360
pkgrel=1
_xorgver=1.10.0.901
pkgdesc="TigerVNC is a suite of VNC servers and clients that have a focus on performance and remote display functionality. Originally this software was based on the (never released) VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('pam' 'gnutls' 'zlib' 'libjpeg-turbo' 'freetype2' 'libsm' 'libxtst' 'libxi' 'libxfont' 'libxmu' 'libxkbfile' 'libxrender' 'pixman' 'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config' 'xorg-fonts-alias' 'xorg-fonts-misc')
makedepends=('subversion' 'nasm' 'xorg-util-macros' 'xorg-font-util' 'inputproto' 'xextproto' 'xproto' 'bigreqsproto' 'compositeproto' 'damageproto' 'dri2proto' 'fixesproto' 'fontsproto' 'randrproto' 'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xineramaproto' 'xf86driproto' 'xf86vidmodeproto' 'glproto' 'renderproto' 'mesa' 'libpciaccess' 'xtrans')
provides=('tigervnc')
conflicts=('tigervnc' 'tightvnc')
source=(ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2
        fix-xserver110patch.patch)
md5sums=('d750cf5f6342b548b7ac2be56e9d1841'
         '7d3429aa7c1341b097a6b56c5f3de68d')

_svntrunk=https://tigervnc.svn.sourceforge.net/svnroot/tigervnc/trunk
_svnmod=tigervnc

build() {
  cd ${srcdir}

  msg "Connecting to SVN server..."
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done or server timeout"

  if [ -d ${_svnmod}-build ]; then
    msg "Deleting old build directory..."
    rm -rf ${_svnmod}-build
  fi

  msg "Setting up build environment..."
  svn export ${_svnmod} ${_svnmod}-build
  cd ${_svnmod}-build
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver
  patch -Np1 -i ${srcdir}/fix-xserver110patch.patch

  msg "Starting build..."
  autoreconf -fiv
  cd unix
  patch -Np0 -i xserver110.patch
  cd xserver
  autoreconf -fiv

  cd ${srcdir}/${_svnmod}-build
  ./configure --prefix=/usr --without-included-zlib
  make
  cd unix/xserver
  ./configure --prefix=/usr \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-static --disable-xinerama --disable-composite \
	--enable-dri2 --enable-glx --enable-glx-tls \
	--with-dri-driver-path=/usr/lib/xorg/modules/dri \
	--with-default-font-path=/usr/share/fonts/misc,/usr/share/fonts/75dpi,/usr/share/fonts/100dpi,/usr/share/fonts/TTF,/usr/share/fonts/Type1 \
	--disable-config-dbus --disable-config-hal --disable-config-udev
  make
  cd ..

  make DESTDIR=${pkgdir} install
  cd xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/xorg/modules/extensions/libvnc.la
}
