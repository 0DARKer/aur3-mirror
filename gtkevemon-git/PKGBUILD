# Contributor: Andrey <gornet[at]gmail[dot]com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=gtkevemon-git
pkgver=r182.6a97295
pkgrel=1
pkgdesc="GTK clone of EveMon skill monitor for EVE Online"
arch=('i686' 'x86_64')
url="https://github.com/gtkevemon/gtkevemon/"
license=('GPL3')
depends=('gtk2' 'gtkmm' 'libxml2' 'openssl')
makedepends=('git')
provides=('gtkevemon' 'gtkevemon-svn')
conflicts=('gtkevemon' 'gtkevemon-svn' 'gtkevemon-hg')
replaces=('gtkevemon-hg')
options=()
install=
source=('git+https://github.com/gtkevemon/gtkevemon/')
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/gtkevemon/"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/gtkevemon/"
  make
}

package() {
  cd "${srcdir}/gtkevemon/"
  make INSTALL_BIN=$pkgdir/usr/bin \
       INSTALL_DIR_ICON=$pkgdir/usr/share/pixmaps \
       INSTALL_DIR_DESK=$pkgdir/usr/share/applications \
       install
}
