# Contributor: Alexander Mezin <mezin.alexander@gmail.com>

_pkgname=kcm-touchpad
pkgname=${_pkgname}-git
pkgver=266.a8ea1c9
pkgrel=1
pkgdesc="A KCModule for configuring the touchpad."
arch=('i686' 'x86_64')
url="https://github.com/sanya-m/kde-touchpad-config"
license=('GPL')
depends=('kdelibs' 'xf86-input-synaptics' 'libxcb' 'libx11' 'libxi')
makedepends=('cmake' 'automoc4' 'git' 'xorg-server-devel')
provides=("${_pkgname}")
conflicts=("${_pkgname}" 'kde-touchpad-config-git')
source=('git://anongit.kde.org/kcm-touchpad.git')
md5sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p "${_pkgname}/build"
}

build() {
  cd "${_pkgname}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX="$(kde4-config --prefix)"
  make
}

package() {
  cd "${_pkgname}/build"
  make DESTDIR="${pkgdir}" install
}
