# Maintainer: Lukas Braun <koomi+aur at hackerspace-bamberg dot de>
# Contributor: David Arroyo <droyo@aqwari.us>
pkgname=s6-musl
pkgver=2.0.0.1
pkgrel=1
pkgdesc="skarnet's small supervision suite."
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/s6"
license=('custom:ISC')
makedepends=('skalibs>=2.1.0.0' 'execline>=2.0.0.0' 'musl')
provides=(s6=2.0.0.1)
options=('staticlibs')
changelog=CHANGELOG
source=("${url}/s6-$pkgver.tar.gz")
sha256sums=('c09d4874b8adc5685ac857a9ce5c573d8beecce053c0f4c3826a8aa35577156d')

build() {
  cd "$srcdir/s6-$pkgver"

  export CPPFLAGS='-nostdinc -isystem /usr/lib/musl/include -isystem /usr/include'
  export CC="musl-gcc"
  ./configure --enable-static-libc --bindir=/usr/bin
  make
}

package() {
  cd "$srcdir/s6-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
