# Maintainer: Lukas Braun <koomi+aur at hackerspace-bamberg dot de>
# Contributor: David Arroyo <droyo@aqwari.us>
pkgname=s6-musl
pkgver=2.0.0.0
pkgrel=1
pkgdesc="skarnet's small supervision suite."
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/s6"
license=('custom:ISC')
makedepends=('skalibs>=2.0.0.0' 'execline>=2.0.0.0' 'musl')
provides=(s6=2.0.0.0)
options=('staticlibs')
changelog=CHANGELOG
source=("${url}/s6-$pkgver.tar.gz")
sha256sums=('9a824e3e2f1cd49e0181fc5330c1bdee47a67efff6e84fc64e4c9470fba0a493')

build() {
  cd "$srcdir/s6-$pkgver"

  export CC="musl-gcc"
  export CPPFLAGS='-nostdinc -isystem /usr/lib/musl/include -isystem /usr/include'
  ./configure --enable-static-libc --bindir=/usr/bin
  make
}

package() {
  cd "$srcdir/s6-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
