# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=libcaca
pkgname=lib32-$_pkgname
pkgver=0.99.beta18
pkgrel=1
pkgdesc="Color AsCii Art library"
arch=(i686 x86_64)
url=http://caca.zoy.org/wiki/libcaca
license=('LGPL')
depends=($_pkgname lib32-imlib2)
options=(!emptydirs !libtool)
source=(http://libcaca.zoy.org/files/$_pkgname/$_pkgname-$pkgver.tar.gz
    limit-build.patch)
sha256sums=('77ea06633f4a6022dccd4f76c1823e167a40cb05308fc1c7ea0fa1f80f6c33a1'
    '8bb22aa4f267d661422c66ba2ca6e12f2ebdcfdd293760464be75297ec097ca9')
sha512sums=('b2a8264ec36b74a01ff415a1fd7cb1806c76e28795b73607d4f8fa5ed5cc776b215f2817f45f06c672688f010622ee5c5e2f9dc4d0c43417dedbff545d575ca5'
    '59371a7626aca79c0c48bda54e4dca177e065222263a1208ce6f454fba484c6c2475f5e469b1c4e2c7efdbc482e545e9caee30855308c519af7f1626368880b1')

prepare() {
    cd $_pkgname-$pkgver/
    sed -i 's:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:' configure.ac
    patch -Np0 -i ../limit-build.patch
}

build() {
    export CC="gcc -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd $_pkgname-$pkgver/
    autoreconf -fi
    ./configure \
        --prefix=/usr \
        --enable-shared \
        --disable-doc \
        --disable-cxx \
        --disable-gl \
        --disable-csharp \
        --disable-python \
        --libdir=/usr/lib32/ \
        --build=i686-unknown-linux-gnu
    make
}

package() {
    make -C $_pkgname-$pkgver DESTDIR="$pkgdir" install
    rm -rf "$pkgdir"/usr/{bin,include,lib,share}/
}
