# Contributor: Harley Laue <losinggeneration@gmail.com>

pkgname=ulatencyd-git
pkgver=692.4f9be8a
pkgrel=2
# poelzi's official repo seems to be inactive
pkgdesc="A daemon to minimize latency on a linux system using cgroups (gajdusek's fork)"
arch=('i686' 'x86_64')
url="https://github.com/gajdusek/ulatencyd/"
license=('GPL')
depends=('dbus-glib' 'lua51' 'lua51-posix' 'libxcb' 'python2' 'dbus-python' 'lua51-bitop')
optdepends=('python2-qt: Qt GUI')
makedepends=('cmake' 'git' 'procps-ng-static')
backup=("etc/ulatencyd/ulatencyd.conf")
provides=('ulatencyd')
conflicts=('ulatencyd')
source=(
	"git+${url}"
	"rc.ulatencyd"
	"docs-fhs-dir.patch"
	"ulatencyd-python2.patch"
	"ulatencyd-bin.patch"
)
md5sums=('SKIP'
         '17e4420fcc6ed313a7574475bcc28aa6'
         '5b35e11bc947fb14294254491681175f'
         '6d65f90a475725d6ae66a4a4bf85b0b1'
         '5f8bffd787a023e70bb2bf3f27ca5df9')

pkgver() {
	cd "$srcdir/ulatencyd"
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd "$srcdir/ulatencyd"

	patch -p1 -ti "$srcdir/ulatencyd-python2.patch"
	patch -p1 -ti "$srcdir/ulatencyd-bin.patch"
	patch -p1 -ti "$srcdir/docs-fhs-dir.patch"

	cmake \
		-DRUNTIME_DESTINATION=bin \
		-DLUA_INCLUDE_DIR=/usr/include/lua5.1 \
		-DDEVELOP_MODE=false \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSTEMD_DIR=/usr/lib/systemd/system \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo

	make
}

package() {
	cd "$srcdir/ulatencyd"
	make DESTDIR="$pkgdir" install

	install -Dm755 "$srcdir"/rc.ulatencyd "$pkgdir/etc/rc.d/ulatencyd"
}

