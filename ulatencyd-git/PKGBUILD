# Contributor: Harley Laue <losinggeneration@gmail.com>

pkgname=ulatencyd-git
pkgver=20110118
pkgrel=5
pkgdesc="A daemon to minimize latency on a linux system using cgroups"
arch=('i686' 'x86_64')
url="https://github.com/poelzi/ulatencyd/"
license=('GPL')
depends=('dbus-glib' 'lua' 'luaposix' 'libxcb')
makedepends=('cmake' 'git')
backup=("etc/ulatencyd/ulatencyd.conf")
provides=('ulatencyd')
conflicts=('ulatencyd')
source=(rc.ulatencyd)
md5sums=('17e4420fcc6ed313a7574475bcc28aa6')

_gitroot="git://github.com/poelzi/ulatencyd.git"
_gitname="ulatencyd"

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [ -d $_gitname ] ; then
		cd $_gitname && git pull origin && git reset --hard
	else
		git clone $_gitroot $_gitname
	fi

	msg "GIT checkout done or server timeout"

	msg "Starting make..."
	[ -d "$srcdir/build" ] && rm -rf "$srcdir/build"
	mkdir -p "$srcdir/build"
	cd "$srcdir/build"

	cmake ../${_gitname} \
		-DDEVELOP_MODE=false \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo

	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR="$pkgdir" install

	install -Dm755 $srcdir/rc.ulatencyd "$pkgdir/etc/rc.d/ulatencyd"
}

