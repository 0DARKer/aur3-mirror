# Contributor: Harley Laue <losinggeneration@gmail.com>

pkgname=ulatencyd-git
pkgver=20120712
pkgrel=1
pkgdesc="A daemon to minimize latency on a linux system using cgroups"
arch=('i686' 'x86_64')
url="https://github.com/poelzi/ulatencyd/"
license=('GPL')
depends=('dbus-glib' 'lua' 'luaposix' 'libxcb' 'python2' 'dbus-python' 'luabitop')
optdepends=('python2-qt: Qt GUI')
makedepends=('cmake' 'git')
backup=("etc/ulatencyd/ulatencyd.conf")
provides=('ulatencyd')
conflicts=('ulatencyd')
source=(rc.ulatencyd ulatency-python2.patch)
md5sums=('17e4420fcc6ed313a7574475bcc28aa6' '6d65f90a475725d6ae66a4a4bf85b0b1')

_gitroot="git://github.com/poelzi/ulatencyd.git"
_gitname="ulatencyd"

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [ -d "$_gitname" ] ; then
		cd "$_gitname" && git pull origin && git reset --hard
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"

	msg "Patching for Python 2"
	(cd "$srcdir/$_gitname" && patch -p1 -ti "$srcdir/ulatency-python2.patch")

	msg "Starting make..."
	[ -d "$srcdir/build" ] && rm -rf "$srcdir/build"
	mkdir -p "$srcdir/build"
	cd "$srcdir/build"

	cmake ../"${_gitname}" \
		-DDEVELOP_MODE=false \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RelWithDebInfo

	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR="$pkgdir" install

	install -Dm755 "$srcdir"/rc.ulatencyd "$pkgdir/etc/rc.d/ulatencyd"
}

