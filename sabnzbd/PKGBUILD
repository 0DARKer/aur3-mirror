pkgname=sabnzbd
pkgver=0.6.5
pkgrel=2
pkgdesc="A web-interface based binary newsgrabber with NZB file support"
url="http://www.sabnzbd.org/"
arch=("any")
license=("GPL")
depends=("par2cmdline" "python2" "python-cheetah" "python2-feedparser"
         "python-yenc" "python2-pyopenssl" "unrar" "unzip" "sqlite3" "curl")
install=sabnzbd.install
backup=("etc/conf.d/sabnzbd" "opt/sabnzbd/sabnzbd.ini")
source=("http://downloads.sourceforge.net/sabnzbdplus/SABnzbd-${pkgver}-src.tar.gz"
        "sabnzbd" "sabnzbd.init" "sabnzbd.confd" "sabnzbd.desktop" "x-nzb.xml"
        "nzb-2.png" "sab2_64.png" "addnzb.sh")
md5sums=("d11d0805b5f1febc8c775f8baf0fb180"
         "0e90eab9ea5d80de91e2af41e84eb4cc"
         "2cd5c68bedc609dcff49871e0c94df54"
         "9c4347b568a1fafecf904a78b3fbffc7"
         "f9bd5485072714b11f8c30a28024dc4d"
         "11fb2cd1451e3725b08bfc2bd045be54"
         "2a49c07b1e3e6448eabe92644315f983"
         "fdc878dd0f6f25617e627b04409abbbd"
         "c0d52ba8d47f731790cd717fde46b829")

package() {
  mkdir -p "${pkgdir}/opt/sabnzbd"
  touch "${pkgdir}/opt/sabnzbd/sabnzbd.ini"
  mv -v "${srcdir}/SABnzbd-${pkgver}/"* "${pkgdir}/opt/sabnzbd/"

  # Fix for issues with Python 3
  find "${pkgdir}/opt/sabnzbd" -type f -exec sed -i 's/python/python2/g' {} \;
  find "${pkgdir}/opt/sabnzbd" -type d -exec chmod 755 {} \;
  find "${pkgdir}/opt/sabnzbd" -type f -exec chmod 644 {} \;
  chmod 755 "${pkgdir}/opt/sabnzbd/SABnzbd.py"
  chmod 755 "${pkgdir}/opt/sabnzbd/Sample-PostProc.sh"

  install -Dm755 "${srcdir}/sabnzbd"          "${pkgdir}/usr/bin/sabnzbd"
  install -Dm755 "${srcdir}/sabnzbd.init"     "${pkgdir}/etc/rc.d/sabnzbd"
  install -Dm644 "${srcdir}/sabnzbd.confd"    "${pkgdir}/etc/conf.d/sabnzbd"
  install -Dm755 "${srcdir}/sabnzbd.desktop" \
    "${pkgdir}/usr/share/applications/sabnzbd.desktop"
  install -Dm644 "${srcdir}/nzb-2.png"    "${pkgdir}/opt/sabnzbd/nzb-2.png"
  install -Dm644 "${srcdir}/sab2_64.png"  "${pkgdir}/opt/sabnzbd/sab2_64.png"
  install -Dm755 "${srcdir}/addnzb.sh"    "${pkgdir}/opt/sabnzbd/addnzb.sh"
  install -Dm770 "${srcdir}/x-nzb.xml"    "${pkgdir}/opt/sabnzbd/x-nzb.xml"
}
