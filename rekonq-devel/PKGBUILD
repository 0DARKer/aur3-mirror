# Maintainer: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>
# Contributor: Emeric Letavernier <emeric.letavernier AT laposte DOT net>

_pkgname=rekonq
pkgname=$_pkgname-devel
pkgver=0.9.80
pkgrel=1
pkgdesc='A WebKit based web browser for KDE - unstable development snapshot'
arch=('i686' 'x86_64')
url='http://rekonq.kde.org/'
license=('GPL')
depends=('kdebase-keditbookmarks')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
provides=('rekonq')
conflicts=('rekonq' 'rekonq-git')
install=${_pkgname}.install
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2")
sha256sums=('9847a98838845eb5d1863614a728fbca7575cd9432ee4bbc810c2516b33e4d82')

build(){
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
