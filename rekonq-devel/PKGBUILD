# Maintainer: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

_pkgname=rekonq
pkgname=$_pkgname-devel
pkgver=0.6.95
pkgrel=1
pkgdesc='A WebKit based web browser for KDE - unstable development snapshot'
arch=('i686' 'x86_64')
url='http://rekonq.kde.org/'
license=('GPL')
depends=('kdebase-keditbookmarks')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
provides=('rekonq')
conflicts=('rekonq' 'rekonq-git')
install=${_pkgname}.install
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2")
sha256sums=('25c651f5f73cb4107450e805fb8e9084a69b21bad2fa92ff8acf5b7d76016659')

build(){
  cd ${srcdir}
  mkdir build 
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}

