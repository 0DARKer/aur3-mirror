# Maintainer: Enno Güttler <Lucki at holarse-linuxgaming dot de>
# Contributor: Gerardo Marset <gammer1994@gmail.com>

pkgname=lci-git
_pkgname=lci
_pkgurl=git://github.com/justinmeza/lci.git

# Set this to true if you want to install the syntax highlight for gedit:
_syntax=false

# Set this  to true for the 1.3 draft (fileio, sockets, bukkits, …):
_usedraft=false

if test "$_usedraft" == "true"; then
    # 1.3 draft:
    _pkgurl=$_pkgurl#branch=future
fi

pkgver=0.10.5.r4.ge017dc6
pkgrel=4
pkgdesc="A simple and fast 1.2 lolcode interpreter written in C."
arch=('i686' 'x86_64')
url="http://lolcode.org/"
license=('GPL3')
depends=('glibc')
makedepends=('git' 'cmake')
provides=('lci')
conflicts=('lci')
source=( "$_pkgname::$_pkgurl"

         # Syntax highlight for gedit:
         "syntax::git+https://gist.github.com/2409d96cae36e1856ef8.git"
)
sha512sums=('SKIP'
			'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install  

  if test "$_syntax" == "true"; then
    mkdir -p "$pkgdir/usr/share/gtksourceview-3.0/language-specs/"
    cp "$srcdir/syntax/lolcode.lang" "$pkgdir/usr/share/gtksourceview-3.0/language-specs/lolcode.lang"
  fi
}
