# Maintainer: Enno Güttler <Lucki1 ät hotmail dot de>
# Contributor: Gerardo Marset <gammer1994@gmail.com>

pkgname=lci-git
_pkgname=lci
_gistname=syntax
pkgver=0.10.5.r4.ge017dc6
pkgrel=3
pkgdesc="A simple and fast 1.2 lolcode interpreter written in C."
arch=('i686' 'x86_64')
url="http://lolcode.org/"
license=('GPL3')
depends=('glibc')
makedepends=('git' 'cmake')
provides=('lci')
conflicts=('lci')
source=( 
  # Use this for the 1.2 spec:
  "$_pkgname::git://github.com/justinmeza/lci.git"
  # or
  # Use this for the 1.3 draft (fileio, sockets, bukkits, …):
  #"$_pkgname::git://github.com/justinmeza/lci.git#branch=future"

  # Syntax highlight for gedit:
  "syntax::git+https://gist.github.com/2409d96cae36e1856ef8.git"
)
sha512sums=('SKIP'
			'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install  

  mkdir -p "$pkgdir/usr/share/gtksourceview-3.0/language-specs/"
  cp "$srcdir/$_gistname/lolcode.lang" "$pkgdir/usr/share/gtksourceview-3.0/language-specs/lolcode.lang"
}
