# Maintainer: Enno Güttler <Lucki1 ät hotmail dot de>
# Contributor: Gerardo Marset <gammer1994@gmail.com>

pkgname=lci-git
_pkgname=lci
pkgver=0.10.5.r4.ge017dc6
pkgrel=2
pkgdesc="A simple and fast 1.2 lolcode interpreter written in C."
arch=('i686' 'x86_64')
url="http://lolcode.org/"
license=('GPL3')
depends=('glibc')
makedepends=('git' 'cmake')
provides=('lci')
conflicts=('lci')
source=("$_pkgname::git://github.com/justinmeza/lci.git")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install  
}
