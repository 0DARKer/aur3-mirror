# Maintainer: Lucki <Lucki at holarse-linuxgaming dot de>
# Contributor: Gerardo Marset <gammer1994@gmail.com>

pkgname=lci-git
_pkgurl=git://github.com/justinmeza/lci.git

# Set this  to true for the 1.3 draft (fileio, sockets, bukkits, â€¦):
_usedraft=false

if test "$_usedraft" == "true"; then
    # 1.3 draft:
    _pkgurl=$_pkgurl#branch=future
fi

pkgver=0.10.5.r4.ge017dc6
pkgrel=6
pkgdesc="A simple and fast 1.2 lolcode interpreter written in C."
arch=('i686' 'x86_64')
url="http://lolcode.org/"
license=('GPL3')
changelog=.CHANGELOG
depends=('glibc')
makedepends=('git' 'cmake')
optdepends=('gtksourceview3-lolcode: syntax highlight')
provides=('lci')
conflicts=('lci')
source=( ${pkgname}::${_pkgurl})
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install

  # update .CHANGELOG
  if [ ${_usedraft} == "false" ]; then
    git --git-dir=${startdir}/${pkgname} log --graph -10 > ${startdir}/.CHANGELOG
  else
    git --git-dir=${startdir}/${pkgname} log --graph -10 future > ${startdir}/.CHANGELOG
  fi
}
