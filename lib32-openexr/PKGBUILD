# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=openexr
pkgname=lib32-$_pkgbasename
pkgver=1.7.0
pkgrel=2
pkgdesc="Openexr library for EXR images (32 bit)"
arch=('x86_64')
url="http://www.openexr.org"
license=('BSD' 'custom:OpenEXR')
depends=('lib32-zlib' 'lib32-ilmbase' "$_pkgbasename")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://download.savannah.nongnu.org/releases/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz"
	'gcc43.patch')
md5sums=('27113284f7d26a58f853c346e0851d7a'
         'd91fae759053a70bcedcf02eb33ca4a2')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  patch -p0 -i ${srcdir}/gcc43.patch
  ./configure --prefix=/usr --libdir=/usr/lib32 CXX='g++ -m32'
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
