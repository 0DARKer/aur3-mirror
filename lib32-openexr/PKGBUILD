_pkgbasename=openexr
pkgname=lib32-$_pkgbasename
pkgver=1.7.0
pkgrel=1
pkgdesc="Openexr library for EXR images (32 bit, library only)"
arch=('x86_64')
url="http://www.openexr.org"
license=('BSD' 'custom:OpenEXR')
depends=('lib32-zlib' 'lib32-ilmbase' "$_pkgbasename") # >=$pkgver")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://savannah.nongnu.org/download/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz"
		'fix-blurimage.patch')
md5sums=('27113284f7d26a58f853c346e0851d7a'
         '4c735b5a9c02f5ba20146b90b737f00d')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  patch -p1 -i ${srcdir}/fix-blurimage.patch
  ./configure --prefix=/usr --libdir=/usr/lib32 CXX='g++ -m32'
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  
  make DESTDIR=${pkgdir} install
  
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{bin,include,share,lib32/*.a}
  # Link to existing license folder, instead of creating new one
  mkdir -p ${pkgdir}/usr/share/licenses && cd ${pkgdir}/usr/share/licenses
  ln -sf ${_pkgbasename} ${pkgname}
}
