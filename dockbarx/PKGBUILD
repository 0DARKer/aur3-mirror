# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: rayman2200 

pkgname=dockbarx
_tarballname=DockbarX
pkgver=0.91
pkgrel=2
pkgdesc="TaskBar with groupping and group manipulation"
arch=('i686' 'x86_64')
url="https://code.launchpad.net/~dockbar-main/dockbar/experimental"
license=('GPL3')
depends=('python2-wnck' 'pygtk' 'python2-xdg' 'python2-dbus' 'python2-numpy' 
         'python2-pillow' 'python2-keybinder2' 'hicolor-icon-theme' 'python2-xlib'
         'python2-gconf')
optdepends=('avant-window-navigator: AWN DockBarX Plugin'
            'xfce4-dockbarx-plugin: Xfce4 Panel Plugin'
            'zeitgeist: recently used file list'
            'compiz-fusion-plugins-main: opacify plugin'
            'dockmanager: dockmanager plugins'
            'cardapio-bzr: required to run menu applet for dockx (standalone dock)')
conflicts=('dockbarx-bzr')
source=(${pkgname}-${pkgver}.tar.gz::http://launchpad.net/dockbar/${pkgname}/${pkgver}${_pkgsubver}/+download/${_tarballname}%20${pkgver}.tar.gz )
sha256sums=('ad37cb79e5ad8c24dd8289a140f93c5d5b25aeb940fa56c9f285c350e1974328')

package() {
  cd ${srcdir}/${_tarballname}\ ${pkgver}

  python2 setup.py install --root ${pkgdir}

  mkdir -p ${pkgdir}/usr/share/avant-window-navigator/applets
  cp -r AWN/* ${pkgdir}/usr/share/avant-window-navigator/applets
  
  mkdir -p ${pkgdir}/usr/share/pixmaps
  install -Dm644 ${srcdir}/${_tarballname}\ ${pkgver}/icons/hicolor/128x128/apps/dockbarx.png ${pkgdir}/usr/share/pixmaps/dockbarx.png

  sed -i 's:^Categories=.*:Categories=GTK;GNOME;Settings;X-GNOME-PersonalSettings;:' ${pkgdir}/usr/share/applications/dbx_preference.desktop
  sed -i 's:\(/usr/bin/python\)\([^2]\):\12\2:' ${pkgdir}/usr/bin/{dockbarx_factory,dbx_preference,dockx}
}

