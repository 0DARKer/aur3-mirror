# Maintainer: 3ED <krzysztof1987@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgbase=gdm-old
pkgname=gdm-old
true && pkgname=(gdm-old gdm-old-faces)
_pkgname=gdm
pkgver=2.20.11
pkgrel=3
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=(i686 x86_64)
license=('GPL')
depends=('pam>=1.0.2' 'libdmx' 'tcp_wrappers>=7.6' 'libgnomecanvas>=2.26.0' 'librsvg>=2.26.0' 'gksu>=2.0.0' 'dbus-glib>=0.80' 'consolekit' 'zenity>=2.26.0')
makedepends=('intltool' 'gnome-doc-utils>=0.16.0' 'pkgconfig' 'xorg-server')
install=gdm.install
url="http://www.gnome.org"
groups=('gnome-extra')
backup=('etc/gdm/custom.conf' 'etc/pam.d/gdm' 'etc/pam.d/gdm-autologin')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/2.20/${_pkgname}-${pkgver}.tar.bz2
        gdm
        defaults.conf
        gdm.pam
        gdm-autologin.pam
        gdm-old.service
        01_gdmchooser-ipv6_bindv6only-workaround.patch
        02_xdmcp-ipv6_bindv6only.patch)
sha256sums=('2e21c9a44941cd0033aaa6b381b563488bbdd0ad1a28ef05f7e0178891f1eaa2'
            'e2b134179e8dbb08b88b0069a1449d54279583d92faf190e7667ddae92fd33aa'
            '85952caa19cbf1308febfc6bcfdd851d0f64cf54dd833f1e3232d4490153a64e'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '3daff680ff6b7ea56f84f40843e46e72477c81e9e405028203c942af04d07ae5'
            'bfa33279e600d0aaca8b1ebd365852c498c0fd2a3fcb57d6aa4eee89488eb0f0'
            '790eaa15a0a2f069325d5e875745dc834a866b7a4c4c5dbaf03580447d1ae94d'
            'e1036348765b6329dcf80eed22b5b61cf5c0c838efbd9cb5616966e941dbdfc0')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -Np1 -i "$srcdir/01_gdmchooser-ipv6_bindv6only-workaround.patch"
  patch -Np1 -i "$srcdir/02_xdmcp-ipv6_bindv6only.patch"

  ./configure \
    --disable-dependency-tracking \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gdm \
    --localstatedir=/var/lib \
    --disable-static \
    --with-xevie=yes \
    --disable-scrollkeeper \
    --enable-secureremote=yes \
    --enable-ipv6=yes \
    LDFLAGS="-lXau -lm"

  sed -i -e 's|${prefix}|/usr|' config.h
  make
}
package_gdm-old() {
  true && provides=('gdm')
  true && conflicts=('gdm')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  #PAM, we use our own, not Redhat stuff
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin"
  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm"

  #sysv init script
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/"

  #systemd init script (need some tests, I hope it works)
  install -m755 -d "${pkgdir}/usr/lib/systemd/system"
  install -m644 "${srcdir}/gdm-old.service" "${pkgdir}/usr/lib/systemd/system/gdm-old.service"

  #configuration
  install -m444 "${srcdir}/defaults.conf" "${pkgdir}/usr/share/gdm/" 
  rm -f ${pkgdir}/usr/share/xsessions/gnome.desktop || true

  #fix gdmsetup 
  sed -i -e 's|^Exec=|Exec=gksu |' "${pkgdir}/usr/share/gdm/applications/gdmsetup.desktop" || return 1

  # fix gdm files conflict with gnome-control-center (usr/share/pixmaps/faces/*)
  rm -rf "${pkgdir}/usr/share/pixmaps/faces/"
}

package_gdm-old-faces() {
  # true but not for AUR
  true && depends=('gdm-old')
  true && conflicts=('gnome-control-center')

  cd "${srcdir}/${_pkgname}-${pkgver}"

  # fix gdm files conflict with gnome-control-center (usr/share/pixmaps/faces/*)
  install -m755 -d "${pkgdir}/usr/share/pixmaps/faces/"
#  for i in astronaut.jpg baseball.png butterfly.png cat-eye.jpg chess.jpg coffee.jpg dice.jpg energy-arc.jpg fish.jpg flake.jpg flower.jpg grapes.jpg guitar.jpg launch.jpg leaf.jpg lightning.jpg penguin.jpg puppy.jpg sky.jpg soccerball.png sunflower.jpg sunset.jpg tennis-ball.png yellow-rose.jpg; do
#    install -m644 "${srcdir}/gui/faces/$i" "${pkgdir}/usr/share/pixmaps/faces/"
#  done
  make DESTDIR="${pkgdir}" install -C gui/faces
}
