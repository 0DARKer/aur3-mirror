# Maintainer: 3ED <krzysztof1987@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=gdm-old
_pkgname=gdm
pkgver=2.20.11
pkgrel=2
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=(i686 x86_64)
license=('GPL')
provides=('gdm')
conflicts=('gdm')
depends=('pam>=1.0.2' 'libdmx' 'tcp_wrappers>=7.6' 'libgnomecanvas>=2.26.0' 'librsvg>=2.26.0' 'gksu>=2.0.0' 'dbus-glib>=0.80' 'consolekit' 'zenity>=2.26.0')
makedepends=('intltool' 'gnome-doc-utils>=0.16.0' 'pkgconfig' 'xorg-server')
install=gdm.install
url="http://www.gnome.org"
groups=('gnome-extra')
backup=('etc/gdm/custom.conf' 'etc/pam.d/gdm' 'etc/pam.d/gdm-autologin')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/2.20/${_pkgname}-${pkgver}.tar.bz2
        gdm
        defaults.conf
        gdm.pam
        gdm-autologin.pam)
sha256sums=('2e21c9a44941cd0033aaa6b381b563488bbdd0ad1a28ef05f7e0178891f1eaa2'
            'e2b134179e8dbb08b88b0069a1449d54279583d92faf190e7667ddae92fd33aa'
            '85952caa19cbf1308febfc6bcfdd851d0f64cf54dd833f1e3232d4490153a64e'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '3daff680ff6b7ea56f84f40843e46e72477c81e9e405028203c942af04d07ae5')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm \
      --localstatedir=/var/lib --disable-static \
      --with-xevie=yes --disable-scrollkeeper \
      --enable-secureremote \
      LDFLAGS="-lXau -lm"

  sed -i -e 's|${prefix}|/usr|' config.h
  make
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  #PAM, we use our own, not Redhat stuff
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin"
  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm"

  #init script and configuration
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/"
  install -m444 "${srcdir}/defaults.conf" "${pkgdir}/usr/share/gdm/" 
  rm -f ${pkgdir}/usr/share/xsessions/gnome.desktop || true
  # fix gdmsetup 
  sed -i -e 's|^Exec=|Exec=gksu |' "${pkgdir}/usr/share/gdm/applications/gdmsetup.desktop" || return 1
}
