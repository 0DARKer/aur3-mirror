# Maintainer: 3ED <krzysztof1987@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgbase=gdm-old
pkgname=gdm-old
true && pkgname=(gdm-old gdm-old-faces)
_pkgname=gdm
pkgver=2.20.11
pkgrel=7
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=(i686 x86_64)
license=('GPL')
depends=('pam' 'libdmx' 'tcp_wrappers' 'libgnomecanvas' 'librsvg' 'dbus-glib' 'zenity' 'polkit' 'systemd')
makedepends=('intltool' 'gnome-doc-utils' 'pkgconfig' 'xorg-server')
install=gdm.install
url="http://www.gnome.org"
groups=('gnome-extra')
backup=('etc/gdm/custom.conf' 'etc/pam.d/gdm' 'etc/pam.d/gdm-autologin')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/2.20/${_pkgname}-${pkgver}.tar.bz2
        gdm
        defaults.conf
        gdm.pam
        gdm-autologin.pam
        gdm.service
        org.gnome2.pkexec.gdmsetup.policy
        01_gdmchooser-ipv6_bindv6only-workaround.patch
        02_xdmcp-ipv6_bindv6only.patch
        03_arch-sysres-paths.patch)
sha256sums=('2e21c9a44941cd0033aaa6b381b563488bbdd0ad1a28ef05f7e0178891f1eaa2'
            'e2b134179e8dbb08b88b0069a1449d54279583d92faf190e7667ddae92fd33aa'
            '85952caa19cbf1308febfc6bcfdd851d0f64cf54dd833f1e3232d4490153a64e'
            'aa91bda59199b4cf980089d3fc30879410853934aa65ea7cd1f6207f6248a021'
            'a1d0c556369a1da61f9d68cc05aff91b4a8636a498aef7e9904cdcb6ca4e0ad3'
            '6d35ffd1091a9f36fd0dbdeee999b35d520f186a9dcb849ae991845a9708f7df'
            'beb7a8cb825ef7e56f16ff68fc27c4cc924a6fcf06d65b970c3e7eb32379d4ec'
            '790eaa15a0a2f069325d5e875745dc834a866b7a4c4c5dbaf03580447d1ae94d'
            'e1036348765b6329dcf80eed22b5b61cf5c0c838efbd9cb5616966e941dbdfc0'
            'f19a3a23afb1d7124df2020e670efdb9588150740dfc40e1af5313061fdf0d09')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # probably better idea to do it inside makepkg instead in pkgbuilds
  for i in "${source[@]}"; do
    if [ "${i##*.}" = "patch" ]; then
      patch -Np1 -i "$srcdir/$i"
    fi
  done

  ./configure \
    --disable-dependency-tracking \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gdm \
    --localstatedir=/var/lib \
    --disable-static \
    --with-xevie=yes \
    --disable-scrollkeeper \
    --enable-secureremote=yes \
    --enable-ipv6=yes \
    LDFLAGS="-lXau -lm"

  sed -i -e 's|${prefix}|/usr|' config.h
  make
}

package_gdm-old() {
  # true && not for AUR
  true && provides=('gdm')
  true && conflicts=('gdm')
  true && optdepends=("polkit-gnome: PolicyKit authentication agent for users without gnome-shell")

  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  #PAM, we use our own, not Redhat stuff
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin"
  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm"

  #sysv init script
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/"

  #systemd init script
  install -m755 -d "${pkgdir}/usr/lib/systemd/system"
  install -m644 "${srcdir}/gdm.service" "${pkgdir}/usr/lib/systemd/system/gdm.service"
  install -m644 "${srcdir}/gdm.service" "${pkgdir}/usr/lib/systemd/system/gdm-old.service"

  #configuration
  install -m444 "${srcdir}/defaults.conf" "${pkgdir}/usr/share/gdm/" 
  rm -f ${pkgdir}/usr/share/xsessions/gnome.desktop || true

  #PolicyKit execution scheme
  install -m755 -d "${pkgdir}/usr/share/polkit-1/actions/"
  install -m644 "${srcdir}/org.gnome2.pkexec.gdmsetup.policy" "${pkgdir}/usr/share/polkit-1/actions/"

  #fix gdmsetup.desktop
  sed -i -e 's|^Exec=|Exec=pkexec |' "${pkgdir}/usr/share/gdm/applications/gdmsetup.desktop"

  #fix gdm files conflict with gnome-control-center (usr/share/pixmaps/faces/*)
  rm -f "${pkgdir}/usr/share/pixmaps/faces/"*
}

package_gdm-old-faces() {
  true && depends=('gdm-old')
  true && conflicts=('gnome-control-center')
  true && pkgdesc="Default faces for GDM (you may install gnome-control-center instead)"

  cd "${srcdir}/${_pkgname}-${pkgver}"

  #fix gdm files conflict with gnome-control-center (usr/share/pixmaps/faces/*)
  install -m755 -d "${pkgdir}/usr/share/pixmaps/faces/"

  make DESTDIR="${pkgdir}" install -C gui/faces
}
