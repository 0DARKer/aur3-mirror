# Contributor: Loui Chang <louipc.ist at NOSPAM gmail.com>

pkgname=mutt-hg
pkgver=0.0.0
pkgrel=1
pkgdesc="A small but very powerful text-based mail client"
url="http://www.mutt.org/"
arch=(i686 x86_64)
license=('GPL')
depends=('openssl>=0.9.8e' 'gdbm' 'mime-types' 'zlib' 'libsasl' 'gpgme' 'ncurses')
makedepends=('mercurial' 'gnupg' 'libxslt')
conflicts=('mutt')
provides=('mutt')
options=('!strip')
_hgrepo=mutt
source=('hg+http://dev.mutt.org/hg/mutt#branch=default')
md5sums=(SKIP)

pkgver() {
  cd "$srcdir/${_hgrepo}"
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

build() {
	cd $srcdir

	msg "Copying files"
	rm -r ${_hgrepo}-build || true
	cp -r ${_hgrepo} ${_hgrepo}-build

	cd ${srcdir}/${_hgrepo}-build

	./prepare \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-debug \
		--enable-hcache \
		--enable-gpgme \
		--enable-imap \
		--enable-pgp \
		--enable-pop \
		--enable-smtp \
		--with-idn \
		--with-sasl \
		--with-ssl=/usr \
		--with-regex

	make
}

package() {
	cd ${srcdir}/${_hgrepo}-build

	make DESTDIR=${pkgdir} install
	rm -f ${pkgdir}/etc/mime.types*
	install -Dm644 contrib/gpg.rc ${pkgdir}/etc/Muttrc.gpg.dist
}

