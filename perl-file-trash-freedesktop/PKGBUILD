# Maintainer: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-file-trash-freedesktop
_lastauthor=S/SH/SHARYANTO
_pkgname=File-Trash-FreeDesktop
pkgver=0.13
pkgrel=1
pkgdesc='freedesktop.org trash specification'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-file-remove' 'perl-file-chdir' 'perl-log-any' 'perl-file-homedir' 'perl-sys-filesystem' 'perl-sys-filesystem-mountpoint' 'perl-sharyanto-file-util' 'perl-list-moreutils')
checkdepends=('perl-test-exception')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('bfe0f91bd13ee7c4a867e58277e3e8d3f5dd2db692fe185493b9fdda0ef5a8aa')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd ${_pkgname}-${pkgver}
  perl Makefile.PL
  make
}
check() {
  cd ${_pkgname}-${pkgver}
  make test
}
package() {
  cd ${_pkgname}-${pkgver}
  make install
}

