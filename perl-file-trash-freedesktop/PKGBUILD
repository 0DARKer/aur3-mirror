# Maintainer: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-file-trash-freedesktop
_lastauthor=S/SH/SHARYANTO
_pkgname=File-Trash-FreeDesktop
pkgver=0.11
pkgrel=1
pkgdesc='freedesktop.org trash specification'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-file-remove' 'perl-file-chdir' 'perl-log-any' 'perl-file-homedir' 'perl-sys-filesystem' 'perl-sys-filesystem-mountpoint' 'perl-sharyanto-file-util' 'perl-list-moreutils')
checkdepends=('perl-test-exception')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('0849f6fe1d7d8a14bbf26b31dd7a8c774dc7f47d8faf73439fdbe8c170933468')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Build.PL
  perl Build
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Build test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Build install
}

