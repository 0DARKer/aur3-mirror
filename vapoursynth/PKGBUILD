# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth
pkgver=r22
_pkgver=R22
pkgrel=3
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL2.1')
depends=('ffmpeg' 'python')
makedepends=('cython' 'yasm' 'python-sphinx')
source=("https://github.com/vapoursynth/vapoursynth/archive/${_pkgver}.tar.gz")
md5sums=('4618e99460fa8686b843954cde55673a')

prepare() {
  rm -fr build
  cp -R "${pkgname}-${_pkgver}" build
  cd build
  ./bootstrap.py
  mkdir -p doc/_static
  rm -fr _test
}

build() {
  cd build
  ./waf configure --prefix="/usr" --docs="true" --filters="true" --mode="release" --examples="true" --pipe="true" --script="true" --core="true"
  ./waf build
  python setup.py build
}

package() {
  cd build
  ./waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 test/test.py "${pkgdir}/usr/share/vapoursynth/test/test.py"
  install -Dm644 test/filter_test.py "${pkgdir}/usr/share/vapoursynth/test/filter_test.py"
}
