# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>
# Contributor: jackoneill <cantabile dot desu at gmail dot com>

pkgname=vapoursynth
pkgver=r22
_pkgver=R22
pkgrel=2
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL2.1')
depends=('ffmpeg' 'python')
makedepends=('cython' 'waf' 'yasm' 'python-sphinx')
source=("https://github.com/vapoursynth/vapoursynth/archive/${_pkgver}.tar.gz")
#source=('git://github.com/vapoursynth/vapoursynth.git#commit=d8521a8e2')
md5sums=('4618e99460fa8686b843954cde55673a')
#md5sums=('SKIP')

prepare() {
  rm -fr "${pkgname}"
  cp -R "${pkgname}-${_pkgver}" "${pkgname}"
  mkdir -p doc/_static
  rm -fr _test
}

build() {
  cd "${pkgname}"
  waf configure --prefix="/usr" --docs="true" --filters="true" --mode="release" --examples="true" --pipe="true" --script="true" --core="true"
  waf build ${MAKEFLAGS}
  python setup.py build
}

package() {
  cd "${pkgname}"
  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 test/test.py "${pkgdir}/usr/share/vapoursynth/test/test.py"
  install -Dm644 test/filter_test.py "${pkgdir}/usr/share/vapoursynth/test/filter_test.py"
}
