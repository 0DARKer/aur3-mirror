# Maintainer: sl1pkn07 <sl1pkn07@gmail.com>

pkgname=vapoursynth
_pkgver=18
pkgver="r${_pkgver}"
pkgrel=4
pkgdesc="A frame server for the 21st century - a portable replacement for Avisynth"
arch=('i686' 'x86_64')
url="http://www.vapoursynth.com/"
license=('LGPL')
depends=('ffmpeg' 'python' 'qt4')
makedepends=('cython' 'subversion' 'waf' 'yasm' 'python-sphinx')
optdepends=('ffmpegsource: Avisynth/Vapoursynth plugin for easy frame accurate access')
source=("vapoursynth::svn+http://vapoursynth.googlecode.com/svn/tags/R${_pkgver}")
md5sums=('SKIP')

prepare() {
  cd  "${pkgname}"
  mkdir doc/_static
}

build() {
  cd  "${pkgname}"
  waf configure --prefix="/usr" --docs="true"
  waf build ${MAKEFLAGS}
  python setup.py build
}

package() {
  cd  "${pkgname}"
  waf install --destdir="${pkgdir}"
  python setup.py install --root="${pkgdir}" --optimize=1
}
