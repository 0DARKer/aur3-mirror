# Maintainer: Prurigro

pkgname=dawnoftime
pkgver=1.69r
pkgrel=3
pkgdesc="A Mud codebase developed by Kalahn originally based on ROM"
url="http://www.dawnoftime.org"
license=('custom')
depends=('gcc')
makedepends=('make')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
install="${pkgname}.install"

source=("http://www.dawnoftime.org/download/dawn${pkgver}-src.tgz"
        "http://www.dawnoftime.org/download/dawn${pkgver}-support.tgz"
        "makefile.in.patch"
        "lockers.h.patch"
        "dawnstat.cpp.patch"
        "${pkgname}.service")
sha512sums=('5a439743241511ee903e864c7d52bb44b8bcfd831af933441f4e481e901ad447edb8575f192dab7f74a9e4fca48da301719635ef4fa96e40ba2115f8f08fdc1e'
            '6ce591f7c7ce5ccfbb1fb0001a803ecc40c16c7772944fa556091ef3ac4bae9fdcaa7592aafd3d5085b6a339f4565a4f3c678614ae1121c7fdda4a927a9c3795'
            'd71fa7f1e9e22afb56dfb7b1687fa5e49e56f60b76294b82faecc9135b748f75e8cb7eb1cdec8f1aa66e7c8170aee378533c9f769fb987abdd68f3f952799012'
            '6ebf4cf446ab3ca67d065e271de96faa54c48254c1151727054921f5803f79d5beca66d6de43cbc831563ea5beff6dff7ca1addb2c6033326710837b43c3f0e0'
            '7443d771e51d8e006e98c4991c45c405974fffecc91037a0be25b63283174a3c8e80bd792c8e77376d8447761776633735df4f89622418ceae8d764b0273bcef'
            'df87438aabb0f99dc6304c59bf3dda3ffc75dabeebb9dfbd3db1ccf2b9a656693eed32bad69b9cc7168a3b48686e87148017afcbed1e23b545942f6f06e320bf')

_installdir="/usr/share/${pkgname}/"

prepare() {
    cd dawn${pkgver}
    patch -p0 < ../makefile.in.patch
    patch -p0 < ../lockers.h.patch
    patch -p0 < ../dawnstat.cpp.patch
}

build() {
    cd dawn${pkgver}/src/configure
    ./configure --prefix=/usr; cd ..
    make -f makefile
}

package() {
    # Install license
    install -Dm644 dawn${pkgver}/src/docs/licenses.txt "${pkgdir}/usr/share/licenses/${pkgname}/licenses.txt"

    # Install base config and data files
    install -d "${pkgdir}${_installdir}"
    cp -R dawn${pkgver}/area "${pkgdir}${_installdir}/"
    cp -R dawn${pkgver}/help "${pkgdir}${_installdir}/"
    cp -R dawn${pkgver}/system "${pkgdir}${_installdir}/"

    # Install binaries
    install -Dm755 dawn${pkgver}/dawn "${pkgdir}/usr/bin/dawn"
    install -Dm755 dawn${pkgver}/resolver "${pkgdir}/usr/bin/resolver"

    # Install systemd service
    install -Dm644 ${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
