# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski  <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xf86-input-synaptics-xrandr
_basename=xf86-input-synaptics
pkgver=1.4.1
pkgrel=1
pkgdesc="Synaptics driver for notebook touchpads"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst')
makedepends=('xorg-server-devel' 'libxi' 'libx11')
conflicts=('xorg-server<1.10.0' 'xf86-input-synaptics')
provides=('xf86-input-synaptics')
groups=('xorg-drivers' 'xorg')
options=(!libtool)
backup=('etc/X11/xorg.conf.d/10-synaptics.conf')
source=(http://xorg.freedesktop.org/releases/individual/driver/${_basename}-${pkgver}.tar.bz2
        10-synaptics.conf
	synaptics-xrandr.patch)
sha1sums=('e41201476f4bc8658291808d2d6ef2e0535179ae'
          '68e1f4ef5e1038231d210eb422fa4d18c5922f0f'
          '2754a735802b987bb3736443402f118161b4633e')

build() {
  cd "${srcdir}/${_basename}-${pkgver}"
  patch -p1 < ../synaptics-xrandr.patch
  ./configure --prefix=/usr
  make
}
package() {
  cd "${srcdir}/${_basename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/10-synaptics.conf" "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/usr/share/X11"
}
