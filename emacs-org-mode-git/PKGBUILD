# contributor: zhuqin <zhuqin83@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
 
pkgname=emacs-org-mode-git
pkgver=8.2.4.367.g387e9c3
pkgrel=1
pkgdesc="Emacs Org Mode from git"
arch=('any')
url="http://orgmode.org/"
depends=('emacs')
makedepends=('git' 'texlive-core' 'perl')
optdepends=('java-environment: for using ditaa.jar in the contrib directory')
license=('GPL')
provides=("emacs-org-mode=$pkgver")
conflicts=('emacs-org-mode')
install=emacs-org-mode.install
source=('emacs-org-mode::git://orgmode.org/org-mode.git')
md5sums=('SKIP')
_gitname="emacs-org-mode"

pkgver() {
  cd "$srcdir"/$_gitname
  git describe --tags | sed 's+-+.+'g|cut -c9-
}

package () {
  cd $srcdir/$_gitname
  make prefix=$pkgdir/usr/share install 
  install -d $pkgdir/usr/share/emacs/site-lisp/org_contrib
  cp -r contrib/{lisp,scripts} $pkgdir/usr/share/emacs/site-lisp/org_contrib
}
