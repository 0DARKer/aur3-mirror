# contributor: zhuqin <zhuqin83@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
 
pkgname=emacs-org-mode-git
pkgver=release_8.0.pre.26.gccee7e4
pkgrel=1
pkgdesc="Emacs Org Mode from git"
arch=('i686' 'x86_64')
url="http://orgmode.org/"
depends=(emacs)
makedepends=('git' 'texlive-core')
license=('GPL')
provides=('emacs-org-mode=8.0pre')
conflicts=('emacs-org-mode')
install=emacs-org-mode.install
source=('git://orgmode.org/org-mode.git')
md5sums=('SKIP')
_gitname="emacs-org-mode"

pkgver() {
  cd "$srcdir"/$_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $srcdir/$_gitname
  make all doc
}
package () {
  cd $srcdir/$_gitname
  make prefix=$pkgdir/usr \
    lispdir=$pkgdir/usr/share/emacs/site-lisp/org install install-info
  install -d $pkgdir/usr/share/doc/$_gitname
  cp doc/*.pdf $pkgdir/usr/share/doc/$_gitname
  install -Dm644 doc/org $pkgdir/usr/share/info/orgmode
  install -d $pkgdir/usr/share/emacs/site-lisp/org_contrib
  cp -r contrib/{lisp,scripts} $pkgdir/usr/share/emacs/site-lisp/org_contrib
}
