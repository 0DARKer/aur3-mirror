# Contributor: drewbs

# Includes a tweaked patch (since it didn't work without it) [1] for this bug [2]:
#  [1] https://bugzilla.redhat.com/attachment.cgi?id=461916&action=diff
#  [2] http://sourceforge.net/tracker/?func=detail&aid=1658720&group_id=239&atid=100239
#      "When selecting some text, xchat claims not only PRIMARY but also CLIPBOARD.
#       This is incredibly annoying and inconsistent with how other X11 applications work.
#       CLIPBOARD should only be claimed when explicitly copying stuff (i.e. C-c)."

# Spell-checking disabled due to maintainer receiving compile errors.

pkgname=xchat-svn
pkgver=1521
pkgrel=1
pkgdesc='A full-featured GTK+ IRC client.'
arch=('i686' 'x86_64')
url='http://xchat.org'
license=('GPL')
depends=('gtk2' 'dbus-glib')
makedepends=('svn')
optdepends=(
	'enchant: for spell checking support'
	'perl: for perl plugin'
	'perl-yaml: for some perl scripts'
	'python: for python plugin'
	'tcl: for tcl plugin')
conflicts=('xchat' 'xchat-cvs')
source=('selection-copy.patch')
sha1sums=('febd624e0e94ca48bc0d50dcb03db6d113f73b72')

_svntrunk=http://xchat.svn.sourceforge.net/svnroot/xchat/
_svnmod=src

build()
{
	cd "${startdir}"

	if [ -d "${_svnmod}"/.svn ]; then
		(cd "${_svnmod}" && svn up)
	else
		svn co "${_svntrunk}" "${_svnmod}"
	fi

	msg "SVN checkout done or server timeout."

	patch -d "${srcdir}"/src/fe-gtk <selection-copy.patch
	cd "${srcdir}"
	./autogen.sh
	touch configure
	./configure --prefix=/usr --sysconfdir=/etc \
		--enable-openssl \
		--enable-dbus \
		--disable-gnome \
		--disable-textfe \
		--enable-shm
#		--enable-spell=static
	make
}

package()
{
	make DESTDIR="${pkgdir}" install
}