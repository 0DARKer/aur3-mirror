# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>

_gitname=minirc
pkgname=$_gitname-git
pkgver=117.a793d2a
pkgrel=3
pkgdesc="A minimalist init system using busybox"
arch=('any')
url="https://github.com/hut/minirc"
license=('GPL2')
depends=('busybox' 'sh')
makedepends=('git')
optdepends=('eudev: udev implementation'
            'mdev: udev implementation'
            'systemd: udev implementation')
conflicts=('systemd-sysvcompat' 'sysvinit')
backup=('etc/inittab' 'etc/minirc.conf')
install=$_gitname.install
source=(git://github.com/hut/$_gitname.git)
md5sums=('SKIP')

pkgver() {
  cd $_gitname

  echo $(git rev-list --count HEAD).$(git describe --always)
}

prepare() {
  cd $_gitname

  sed -i 's./sbin/./usr/bin/.g' ./setup.sh
}

package() {
  cd $_gitname

  ROOT="$pkgdir" ./setup.sh --force
}
