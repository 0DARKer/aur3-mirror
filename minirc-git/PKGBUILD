# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>

_gitname=minirc
pkgname=$_gitname-git
pkgver=113.f6eeb32
pkgrel=1
pkgdesc="A minimalist init system using busybox"
arch=('any')
url="https://github.com/hut/minirc"
license=('GPL2')
depends=('busybox' 'bash')
makedepends=('git')
conflicts=('systemd-sysvcompat' 'sysvinit')
backup=('etc/inittab' 'etc/minirc.conf')
source=(git://github.com/hut/$_gitname.git)
md5sums=('SKIP')

pkgver() {
  cd "$srcdir"/$_gitname

  echo $(git rev-list --count HEAD).$(git describe --always )
}

prepare() {
  cd "$srcdir"/$_gitname

  sed -i 's./sbin/./usr/bin/.g' ./setup.sh
}

package() {
  cd "$srcdir"/$_gitname

  ROOT="$pkgdir" ./setup.sh --force
}
