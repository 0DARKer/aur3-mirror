# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Maintainer: Matthias matthiaskrgr Krüger < matthias · krueger _strange_curverd_character_ famsik · de >

pkgname=cppcheck-git
pkgver=1.59.104.ge23038c
pkgver() {
    cd cppcheck
    git describe --tags | sed -e 's/^cppcheck\-//' -e 's/-/./g'
}
pkgrel=1
pkgdesc='A tool for static C/C++ code analysis'
arch=('i686' 'x86_64')
url='http://cppcheck.wiki.sourceforge.net'
license=('GPL3')
conflicts=('cppcheck')
depends=('gcc-libs' 'pcre')
source=('cppcheck::git://github.com/danmar/cppcheck.git')
sha1sums=('SKIP')


build() {
    cd "$srcdir"/cppcheck
    make
}

check() {
    cd "$srcdir"/cppcheck
    make check
}

package() {
    cd "$srcdir"/cppcheck
    make DESTDIR=${pkgdir} install
}
