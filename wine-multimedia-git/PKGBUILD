# Maintainer:  John Schoenick <john@pointysoftware.net>
# Maintainer:  Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=wine-multimedia-git
_basename=wine
pkgver=20120513
# Equivilent non-git version, for provides
_equivver=1.5.4
pkgrel=1

pkgdesc="A compatibility layer for running Windows programs"
url="http://www.winehq.com"
arch=(i686 x86_64)
license=(LGPL)
install=wine.install

depends=(
  fontconfig      lib32-fontconfig
  mesa            lib32-mesa 
  libxcursor      lib32-libxcursor
  libxrandr       lib32-libxrandr
  libxdamage      lib32-libxdamage
  libxi           lib32-libxi
  gettext         lib32-gettext
  desktop-file-utils
)

makedepends=(autoconf ncurses bison perl fontforge flex prelink
  'gcc>=4.5.0-2'  'gcc-multilib>=4.5.0-2'
  giflib          lib32-giflib
  libpng          lib32-libpng
  gnutls          lib32-gnutls
  libxinerama     lib32-libxinerama
  libxcomposite   lib32-libxcomposite
  libxmu          lib32-libxmu
  libxxf86vm      lib32-libxxf86vm
  libxml2         lib32-libxml2
  libldap         lib32-libldap
  lcms            lib32-lcms
  mpg123          lib32-mpg123
  openal          lib32-openal
  v4l-utils       lib32-v4l-utils
  alsa-lib        lib32-alsa-lib
  libpulse        lib32-libpulse
  oss
  samba
)
  
optdepends=(
  giflib          lib32-giflib
  libpng          lib32-libpng
  libldap         lib32-libldap
  gnutls          lib32-gnutls
  lcms            lib32-lcms
  libxml2         lib32-libxml2
  mpg123          lib32-mpg123
  openal          lib32-openal
  v4l-utils       lib32-v4l-utils
  libpulse        lib32-libpulse
  alsa-plugins    lib32-alsa-plugins
  alsa-lib        lib32-alsa-lib
  libpulse        lib32-libpulse
  oss             cups
  samba
)

# Set to 1 to auto-rebase multimedia-git onto the latest wine-git
# Set to 0 if you just want to use the raw multimedia-git, which doesn't
# rebase itself all that often.
_rebase=1

# Wine gitweb:             http://source.winehq.org/git/wine.git
# Multimedia tree gitweb:  http://repo.or.cz/w/wine/multimedia.git
_gitroot=git://repo.or.cz/wine/multimedia.git
_gitname=wine-multimedia
_upstream=git://source.winehq.org/git/wine.git

if [[ $CARCH == i686 ]]; then
  # Strip lib32 etc. on i686
  depends=(${depends[@]/*32-*/})
  makedepends=(${makedepends[@]/*32-*/})
  makedepends=(${makedepends[@]/*-multilib*/})
  optdepends=(${optdepends[@]/*32-*/})
  provides=("wine=$_equivver")
  conflicts=('wine')
else
  provides=("wine=$_equivver" "bin32-wine=$_equivver" "wine-wow64=$_equivver")
  conflicts=('wine' 'bin32-wine' 'wine-wow64')
  replaces=('bin32-wine')
fi

source=(stencilreset.patch acceptex.patch)
sha256sums=('bf1cd3f1fa4875b1a73d57c6c924a5de08edd3aab56142c68655f92646268ae8'
            '0ca336a7b64689c409e81e63ec708bb49bce4cd0f8d0be16c6bf9ecaa3777e15')

build() {
  cd "$srcdir"
  
  # Get source
  if [ ! -d "$_gitname" ]; then
      (
          # The reason we clone upstream and add a remote
          # to multimedia is mainly because repo.or.cz
          # is much slower - it could be done either way.
          msg2 "Cloning wine git"
          git clone "$_upstream" "$_gitname"
          cd "$_gitname"
          
          msg2 "Fetching multimedia git"
          git remote add multimedia "$_gitroot"
          git fetch multimedia
          git checkout -b multimedia multimedia/master
      )
  fi
  (
      cd "$_gitname"
      msg2 "Updating multimedia git"
      git checkout -f multimedia
      git fetch multimedia
      git reset --hard multimedia/master && git clean -xfd # FIXME neph-only (actually might be needed for force rebases?)
      
      if [ "$_rebase" -gt 0 ]; then
        msg2 "Pulling upstream patches onto multimedia git"
        git fetch origin
        # Make a 'temp' branch to rebase on upstream
        git branch -D temp || true
        git checkout -b temp

        git merge --squash -Xours origin/master || true
        # -Xours resolves conflicts by just keeping the multimedia branch versions,
        # this might return errors due to deleted files, which don't present a problem
        # to the build
      fi
      
      # NEPH ONLY PATCHES FIXME
      patch -Np1 -i "${srcdir}/stencilreset.patch"
      patch -Np1 -i "${srcdir}/acceptex.patch"
  )
  
  # Get rid of old build dirs
  rm -rf $_basename-{32,64}-build
  mkdir $_basename-32-build

  # These additional CFLAGS solve FS#27662
  export CFLAGS="${CFLAGS/-D_FORTIFY_SOURCE=2/} -D_FORTIFY_SOURCE=0"
  export CXXFLAGS="${CFLAGS/-D_FORTIFY_SOURCE=2/} -D_FORTIFY_SOURCE=0"

  if [[ $CARCH == x86_64 ]]; then
    msg2 "Building Wine-64..."

    mkdir $_basename-64-build
    cd "$srcdir/$_basename-64-build"
    ../$_gitname/configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --libdir=/usr/lib \
      --with-x \
      --enable-win64

    make

    _wine32opts=(
      --libdir=/usr/lib32
      --with-wine64="$srcdir/$_basename-64-build"
    )

    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

  msg2 "Building Wine-32..."
  export CFLAGS="-mstackrealign $CFLAGS"
  export CXXFLAGS="-mstackrealign $CXXFLAGS"
  export CC="gcc -mstackrealign"
  export CXX="g++ -mstackrealign"
  cd "$srcdir/$_basename-32-build"
  ../$_gitname/configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-x \
    CC="$CC" CXX="$CXX" \
    CFLAGS="$CFLAGS" CXXFLAGS="$CXXFLAGS" \
    "${_wine32opts[@]}"

  # These additional CFLAGS solve FS#27560
  make CFLAGS+="-mstackrealign" CXXFLAGS+="-mstackrealign"
}

package() {
  msg2 "Packaging Wine-32..."
  cd "$srcdir/$_basename-32-build"

  if [[ $CARCH == i686 ]]; then
    make prefix="$pkgdir/usr" install
  else
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib32" \
      dlldir="$pkgdir/usr/lib32/wine" install

    msg2 "Packaging Wine-64..."
    cd "$srcdir/$_basename-64-build"
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib" \
      dlldir="$pkgdir/usr/lib/wine" install
  fi
}

# vim:set ts=8 sts=2 sw=2 et:
