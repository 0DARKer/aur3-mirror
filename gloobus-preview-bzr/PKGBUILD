# Maintainer: Alessio Sergi <asergi [at] archlinux [dot] us>

pkgname=gloobus-preview-bzr
_realname=gloobus-preview
pkgver=262
pkgrel=2
pkgdesc="A quicklook for GNU/Linux"
arch=('i686' 'x86_64')
url="https://launchpad.net/gloobus-preview"
license=('GPL3')
depends=('desktop-file-utils' 'djvulibre' 'gstreamer0.10-base' \
         'gtksourceview2' 'hicolor-icon-theme' 'libgnomeui' \
         'libspectre' 'poppler-glib' 'python2' 'swfdec<0.9' 'taglib')
makedepends=('bzr' 'intltool')
optdepends=('gdk-pixbuf-psd: PSD images support'
            'gdk-pixbuf-xcf: XCF images support'
            'libicns: ICNS files support'
            'nautilus-actions: GNOME menu integration'
            'nautilus-elementary-bzr: GNOME integration'
            'unoconv: OOo files support')
provides=(${_realname})
conflicts=(${_realname})
options=('!libtool')
install=${pkgname}.install
source=('no_loaders.patch'
        'fix_autotools.patch')
md5sums=('e9339672ea2982920b7b4b02caba04b8'
         'b7f398724d5004703816a62f77215ae0')

_bzrtrunk="lp:${_realname}"
_bzrmod=${_realname}

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  # exclude psd and xcf loaders from building
  patch -Np1 -i ${srcdir}/no_loaders.patch

  # autotools fix
  patch -Np1 -i ${srcdir}/fix_autotools.patch

  # python2 fix
  sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' src/${_realname}-configuration

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  make DESTDIR=${pkgdir} install
}

