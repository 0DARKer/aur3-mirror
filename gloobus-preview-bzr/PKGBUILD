# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=gloobus-preview-bzr
_realname=gloobus-preview
pkgver=263
pkgrel=1
pkgdesc="A quicklook for GNU/Linux"
arch=('i686' 'x86_64')
url="https://launchpad.net/gloobus-preview"
license=('GPL3')
depends=('djvulibre' 'gstreamer0.10-base' 'gtksourceview2' \
         'libspectre' 'poppler-glib' 'python2' 'taglib')
makedepends=('bzr' 'intltool')
optdepends=('gdk-pixbuf-psd: PSD images support'
            'gdk-pixbuf-xcf: XCF images support'
            'libicns: ICNS files support'
            'nautilus-actions: GNOME menu integration'
            'nautilus-elementary-bzr: GNOME integration'
            'unoconv: OOo/Libo files support')
provides=(${_realname})
conflicts=(${_realname})
options=('!libtool')
install=${pkgname}.install
source=('gcc46.patch'
        'no_loaders.patch')
md5sums=('fd5c576aedd840a1b80eefd40cb0c616'
         'ed02c34d379e6db4231ccdb54ba5115c')

_bzrtrunk="lp:${_realname}"
_bzrmod="${_realname}"

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  # gcc 4.6 fix
  patch -Np1 -i "${srcdir}/gcc46.patch"

  # exclude psd and xcf loaders from building
  patch -Np1 -i "${srcdir}/no_loaders.patch"

  # python2 fix
  sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' src/${_realname}-configuration

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  make DESTDIR="${pkgdir}" install
}

