# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=gloobus-preview-bzr
_realname=gloobus-preview
pkgver=272
pkgrel=1
pkgdesc="A quicklook for GNU/Linux"
arch=('i686' 'x86_64')
url="https://launchpad.net/gloobus-preview"
license=('GPL3')
depends=('djvulibre' 'gconf' 'gstreamer0.10-base' \
         'gtksourceview3' 'libspectre' 'poppler-glib' \
         'taglib')
makedepends=('bzr' 'intltool')
optdepends=('gdk-pixbuf-psd: PSD images support'
            'gdk-pixbuf-xcf: XCF images support'
            'gloobus-sushi-bzr: nautilus 3.x support'
            'libicns: ICNS files support'
            'marlin-bzr: file manager integration support'
            'nautilus-actions: nautilus menu integration'
            'nautilus-elementary-bzr: file manager integration support')
provides=(${_realname})
conflicts=(${_realname})
options=('!libtool')
install=${pkgname}.install

_bzrtrunk="lp:${_realname}"
_bzrmod="${_realname}"

build() {
  cd "${srcdir}"

  msg "Connecting to the server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${_bzrmod}-build"
  cp -r "${_bzrmod}" "${_bzrmod}-build"
  cd "${_bzrmod}-build"

  # no psd and xcf loaders
  sed -i '/loaders/d' src/Makefile.am
  sed -i '/src\/loaders/d' configure.ac

  # python2 fix
  sed -i 's_python_python2_' src/${_realname}-configuration

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
