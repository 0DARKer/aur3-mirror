# Maintainer: TDY <tdy@gmx.com>

pkgname=tubemaster-svn
pkgver=6
pkgrel=1
pkgdesc="A YouTube video downloader (forked from QtTube)"
arch=('i686' 'x86_64')
url="http://tubemaster.googlecode.com/"
license=('GPL')
depends=('ffmpeg>=0.4.9' 'pyqt' 'youtube-dl>=2008.11.01')
makedepends=('subversion')
provides=('tubemaster')
conflicts=('tubemaster')
source=(setup.py)
md5sums=('68a0c21cf51de512aaed5e962415a1fa')

_svntrunk=http://tubemaster.googlecode.com/svn/trunk
_svnmod=tubemaster

build() {
  cd "$srcdir"

  msg "Connecting to $_svntrunk..."
  if [[ -d $_svnmod/.svn ]]; then
    cd $_svnmod && svn up -r $pkgver && cd ..
    msg2 "Local files updated"
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    msg2 "SVN checkout done"
  fi

  rm -rf $_svnmod-build
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  msg "Starting make..."
  ln -sf "$srcdir/setup.py" .
  install -m755 src/$_svnmod.py src/$_svnmod
  python setup.py build || return 1
}

package() {
  cd "$srcdir/$_svnmod-build"
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 data/$_svnmod.desktop \
    "$pkgdir/usr/share/applications/$_svnmod.desktop"
}
