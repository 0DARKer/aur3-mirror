# Maintainer: Luka Perkov <luka.perkov@sartura.hr>

pkgname=rpcd-git
_gitname=rpcd
pkgver=r108.d99e8ad
# commit d99e8adc4fbcf9e7a0b36df05ee1bccbedd45a2a
pkgrel=1
pkgdesc='OpenWrt ubus RPC backend server'
url='http://nbd.name/gitweb.cgi?p=luci2/rpcd.git'
arch=('i686' 'x86_64')
license=('ISC')
depends=('libubox-lua-git' 'ubus-lua-git' 'uci-lua-git')
makedepends=('git' 'cmake' 'gcc' 'make')
conflicts=('rpcd')
provides=('rpcd')
source=('git://nbd.name/luci2/rpcd.git' '001-json-c-include.patch' '002-install-headers.patch')
md5sums=('SKIP' '5d533610db3525bf090f4ce945cda759' 'afc1944b04042937e430f52d10e3e502')

pkgver() {
	cd "$srcdir/$_gitname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_gitname"

	patch -p1 -i "$srcdir/001-json-c-include.patch"
	patch -p1 -i "$srcdir/002-install-headers.patch"
}

build() {
	cd "$srcdir/$_gitname"

	cmake CMakeLists.txt \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DIWINFO_SUPPORT=NO

	make
}

package() {
	cd "$srcdir/$_gitname"

	make DESTDIR="$pkgdir" install

	install -d "$pkgdir"/usr/lib/rpcd/
	mv "$pkgdir"/usr/lib/file.so "$pkgdir"/usr/lib/rpcd/
}
