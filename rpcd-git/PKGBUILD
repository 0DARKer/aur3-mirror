# Maintainer: Luka Perkov <luka.perkov@sartura.hr>

pkgname=rpcd-git
_gitname=rpcd
pkgver=r105.7fd4564
# commit 7fd4564b8acb60254817cb341b84c6468c8c6440
pkgrel=1
pkgdesc='OpenWrt ubus RPC backend server'
url='http://nbd.name/gitweb.cgi?p=luci2/rpcd.git'
arch=('i686' 'x86_64')
license=('ISC')
depends=('libubox-lua-git' 'ubus-lua-git' 'uci-lua-git')
makedepends=('git' 'cmake' 'gcc' 'make')
conflicts=('rpcd')
provides=('rpcd')
source=('git://nbd.name/luci2/rpcd.git')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/$_gitname"

	cmake CMakeLists.txt \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DIWINFO_SUPPORT=NO

	make
}

package() {
	cd "$srcdir/$_gitname"

	make DESTDIR="$pkgdir" install

	install -d "$pkgdir"/usr/lib/rpcd/
	mv "$pkgdir"/usr/lib/file.so "$pkgdir"/usr/lib/rpcd/
}
