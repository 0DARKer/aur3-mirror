# $Id: PKGBUILD 109795 2014-04-18 17:39:03Z bpiotrowski $
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

_pkgname=cantata
pkgname="${_pkgname}-kde"
pkgver=1.4.0
pkgrel=1
conflicts=cantata
provides=('cantata')
pkgdesc="KDE client for the music player daemon (MPD)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/cantata/"
license=('GPL')
depends=('kdebase-runtime' 'libmtp' 'libcddb' 'libmusicbrainz5' 'mpg123'
         'taglib-extras' 'hicolor-icon-theme')
optdepends=('perl-uri: dynamic playlist'
            'mpd: playback')
makedepends=('automoc4' 'cmake')
install="$pkgname.install"
source=(ftp://ftp.archlinux.org/other/community/${_pkgname}/${_pkgname}-$pkgver.tar.bz2)
md5sums=('ebe189390bf3ef54c8ac7e8a8ebb98ec')

build() {
  cd "$srcdir/${_pkgname}-$pkgver"
  mkdir build

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_HTTP_STREAM_PLAYBACK=ON \
    -DENABLE_KDE=YES

  make
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"

  make DESTDIR="$pkgdir" PREFIX="/usr" install
}
