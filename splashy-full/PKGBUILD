# Contributors: Det, Lexiw, Angel 'angvp' Velasquez, dongiovanni, Darwin Bautista, Jeremy Sands
#               Tons of people here: http://bbs.archlinux.org/viewtopic.php?id=48978

pkgname=splashy-full
pkgver=20121016
pkgrel=1
pkgdesc="A boot splashing system"
arch=('i686' 'x86_64')
url="http://anonscm.debian.org/gitweb/?p=splashy/splashy.git;a=summary"
license=('GPL')
depends=('glib2' 'directfb')
makedepends=('automake1.10' 'git')
options=('!libtool' '!distcc' '!makeflags')
provides=('splashy')
install=$pkgname.install
backup=('etc/splash.conf'
        'etc/splashy/config.xml')
source=(splash.conf
        splash-initscripts
        splashy-functions
        initcpio_hook
        initcpio_install)
sha1sums=('7b1f6661665932379032e6d0da430e71ecfc8a45'
          '524d2df3f5d22da814000e68acb37ec8a8475dd3'
          'f6135b85eb336865c3f52d004ee5f4b50409aa0f'
          '300620ed933ac14ce7beb9bfb233ba3d3b2f7627'
          'd99972841c666500f9ffc0ed0e5e93d343a3ae73')

_gitroot='git://anonscm.debian.org/git/splashy/splashy.git'
_gitname='splashy'

build() {
  if [ -d "${_gitname}" ] ; then
    cd "${srcdir}/${_gitname}"
    msg2 "Updating local tree..."
    git pull --depth=1 origin master
  else
    cd "${srcdir}"
    msg2 "Cloning initial copy of ${_gitname}..."
    git clone --depth=1 "${_gitroot}" "${_gitname}"
    cd "${srcdir}/${_gitname}"
  fi

  # Fix the build
  sed -e 's|-Werror||g' -i configure.ac

  ./autogen.sh --prefix=/usr \
               --libdir=/usr/lib \
               --sysconfdir=/etc \
               --sbindir=/sbin \
               --datarootdir=/usr/share \
               --mandir=/usr/share/man \
               --includedir=/usr/include

  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/"{etc/{console-tools,default,init.d,lsb-base-logging.sh},usr/share/initramfs-tools}

  install -Dm644 ../initcpio_install "${pkgdir}/usr/lib/initcpio/install/splashy"
  install -Dm644 ../initcpio_hook "${pkgdir}/usr/lib/initcpio/hooks/splashy"
  install -Dm644 ../splashy-functions "${pkgdir}/etc/rc.d/splashy-functions"
  install -Dm644 ../splash-initscripts "${pkgdir}/etc/rc.d/functions.d/splash"
  install -Dm644 ../splash.conf "${pkgdir}/etc/splash.conf"

  sed -e 's|>/etc/splashy/themes<|>/usr/share/splashy/themes<|' -i "${pkgdir}/etc/splashy/config.xml"
}