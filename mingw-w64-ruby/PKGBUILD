pkgname=mingw-w64-ruby
pkgver=1.9.3_p286
pkgrel=2
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=(any)
url="http://www.ruby-lang.org/en"
license=("BSD" "custom")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config ruby)
depends=(mingw-w64-crt mingw-w64-openssl)
options=(!libtool !strip !buildflags)
source=("ftp://ftp.ruby-lang.org/pub/ruby/${pkgver%.*}/ruby-${pkgver//_/-}.tar.bz2")
md5sums=('e76848a86606a4fd5dcf14fc4b4e755e')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/ruby-${pkgver//_/-}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-install-doc
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' -o -name '*.so' | xargs -rtl1 ${_arch}-strip -g
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' | xargs -rtl1 rm
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
