# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=ibus-qt
pkgname=$_pkgname-git
pkgver=20130329
pkgrel=1
pkgdesc="It includes ibus qt library and ibus qt input method plugin"
arch=(i686 x86_64)
url=http://ibus.googlecode.com
license=(LGPL2.1)
depends=(ibus icu qt4)
makedepends=(cmake doxygen git)
provides=($_pkgname=1.4.2)
conflicts=($_pkgname)
install=$_pkgname.install

_gitroot=git://github.com/phuang/$_pkgname.git
_gitname=$_pkgname

build(){
    msg "Connecting to GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone --depth 1 $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    install -d $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    cmake ../$_gitname/ \
        -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="-Wall" \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4
    make VERBOSE=1
}

package() {
    make -C $_gitname-build DESTDIR="$pkgdir" install
}
