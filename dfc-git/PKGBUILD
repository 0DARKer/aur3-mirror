# Maintainer: Rolinh <robinDOThahlingATgw-computingDOTnet>
pkgname=dfc-git
pkgver=20120415
pkgrel=1
pkgdesc="DEVEL - Display file system space usage using graph and colors"
arch=('x86_64' 'i686')
url="http://projects.gw-computing.net/projects/dfc"
license=('BSD')
depends=('glibc')
makedepends=('git')
provides=('dfc')
conflicts=('dfc')
install='dfc.install'

_gitroot="git://gw-computing.net/dfc.git"
_gitname="dfc"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX="/usr" DESTDIR="${pkgdir}" MANDIR="/usr/share/man" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/dfc/LICENSE"
  install -Dm644 conf/dfcrc "${pkgdir}/etc/xdg/dfc/dfcrc"
}

# vim:set ts=2 sw=2 et:
