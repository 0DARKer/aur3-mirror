# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Daenyth <Daenyth+Arch@gmail.com>

pkgname=gnushogi
pkgver=1.4.0
pkgrel=3
pkgdesc="A program that plays shogi (Japanese chess). Also includes xshogi."
arch=(i686 x86_64)
url=http://www.cs.caltech.edu/~mvanier/hacking/$pkgname/$pkgname.html
license=(GPL2)
depends=(libxaw xorg-fonts-100dpi)
provides=(xshogi)
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('466730f54ee6d6ad61a06ccfccf2dc63e47559697116b143b93c624a9ca9b301')
sha512sums=('9b27cd5d257dc3db718678e6c9601ef7bf9f0981ec9838c26d50fb6d8973a6ebcfb0dc8d22f1f35c3a40d2851a353832ee8a8faed926c83b25391ff59909afb3')

build() {
    cd $pkgname-$pkgver/
    for i in gnushogi/Makefile{,.profile}.in xshogi/Makefile.in; do
        sed -i -e 's:/man/man6\|/info:/share&:g' -e 's:-ltermcap::g' $i
    done
    ./configure --prefix=/usr --with-xshogi
    make
    make -C $pkgname $pkgname
}

package() {
    make -C $pkgname-$pkgver DESTDIR="$pkgdir" prefix="$pkgdir/usr" install
}
