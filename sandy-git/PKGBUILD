# Contributor: Army
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=sandy-git
pkgver=20111229
pkgrel=1
pkgdesc="an ncurses text editor with an easy-to-read, hackable C source"
arch=('i686' 'x86_64')
url="http://tools.suckless.org/sandy"
license=('custom:MIT/X')
depends=('ncurses')
makedepends=('git')
provides=('sandy' 'sandy-hg')
conflicts=('sandy' 'sandy-hg')
source=('git+http://git.suckless.org/sandy')
md5sums=('SKIP')
_gitname=sandy

pkgver() {
  cd "$srcdir"/$_gitname
  echo $(git log -1 --format="%cd" --date=short | sed 's|-||g')
}

build() {
  cd "$srcdir/$_gitname"

  if [ -e $startdir/config.h ];
  then
    msg "use custom config.h"
    cp $startdir/config.h .
  else msg "use default config.h"
  fi
  make PREFIX=/usr
}

package() {
  cd "$srcdir/$_gitname"
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
