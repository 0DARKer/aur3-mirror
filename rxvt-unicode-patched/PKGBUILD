# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org

pkgname=rxvt-unicode-patched
_pkgname=rxvt-unicode
pkgver=9.11
pkgrel=1
pkgdesc="Unicode enabled rxvt-clone terminal emulator (urxvt) with layout/font-width patches."
arch=(i686 x86_64)
depends=('gcc-libs' 'libxft' 'gdk-pixbuf2')
makedepends=('ncurses' 'perl>=5.12.2' 'pkgconfig')
optdepends=('perl: lots of utilities' 'gtk2-perl: to use the urxvt-tabbed')
provides=("rxvt-unicode")
conflicts=("rxvt-unicode")
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
license=("GPL")
source=(http://dist.schmorp.de/rxvt-unicode/${_pkgname}-${pkgver}.tar.bz2 \
        ${_pkgname}.desktop ${_pkgname}.png popup-menu-hang.diff \
        font-width-fix.patch layoutgaps.patch)
sha1sums=('21d0fad9a6032dbcf2c43a85f288543c6bd5ab3d'
          '79b7cacb7d6ce4a34f8a31a2e33475a8f7bf69cb'
          '9ff58d7b1d60838c51c9045bc0be342fac456790'
          'ec4013f4af3a45da928b75f40fbf6389070e7fdd'
          '01ee8f212add79a158dcd4ed78d0ea1324bdc59b'
          '1299c1d46931a456ff73179ccb6df4c5d6a0c2e0')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p0 < ../popup-menu-hang.diff
  patch -p0 -i ../font-width-fix.patch
  patch -p0 -i ../layoutgaps.patch

  ./configure --prefix=/usr \
    --with-terminfo=/usr/share/terminfo \
    --enable-256-color \
    --enable-font-styles \
    --enable-xim \
    --enable-keepscrolling \
    --enable-selectionscrolling \
    --enable-smart-resize \
    --enable-pixbuf \
    --enable-transparency \
    --enable-utmp \
    --enable-wtmp \
    --enable-lastlog \
    --disable-frills
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -d "${pkgdir}/usr/share/terminfo"
  export TERMINFO="${pkgdir}/usr/share/terminfo"
  make DESTDIR="${pkgdir}" install
 # install the tabbing wrapper ( requires gtk2-perl! )
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
  install -Dm 755 doc/rxvt-tabbed "${pkgdir}/usr/bin/urxvt-tabbed"
 # install freedesktop menu and icon ( icon from cvs checkout )
  install -Dm644 ../${_pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 ../${_pkgname}.png \
    "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
}
