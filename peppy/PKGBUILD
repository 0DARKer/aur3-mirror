# Maintainer: TDY <tdy@gmx.com>

pkgname=peppy
pkgver=0.16.0
pkgrel=1
pkgdesc="(ap)Proximated (X)Emacs Powered by PYthon"
arch=('i686' 'x86_64')
url="http://peppy.flipturn.org/"
license=('GPL3')
depends=('wxpython>=2.8')
optdepends=('gdal>=1.4.0: broader range of image formats'
            'mpd: MPD mode'
            'pyenchant>=1.2.0: spell checking support'
            'python-numpy>=1.0: hyperspectral image view mode'
            'python-scipy>=0.5.2: extended mathematical functionality')
source=(http://$pkgname.flipturn.org/archive/$pkgname-$pkgver.tar.bz2
        $pkgname.desktop)
md5sums=('89963a596c80b5f8e2863344ca82ae70'
         '6666c9657bf97aad6b8f25ac48f05e21')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 $pkgname/icons/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
