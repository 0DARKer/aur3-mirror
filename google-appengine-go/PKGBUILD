# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=google-appengine-go
pkgver=1.5.0
pkgrel=1
pkgdesc="Google App Engine SDK for Go"
arch=('x86_64' 'i686')
depends=("python2")
url="http://code.google.com/appengine/docs/go/gettingstarted/"
license=("apache2")
if [ $CARCH == "i686" ]; then
	source=("http://googleappengine.googlecode.com/files/go_appengine_sdk_linux_386-$pkgver.zip")
	md5sums=('d20b0f70e5e6f5d08d6619345e27db34')
else
	source=("http://googleappengine.googlecode.com/files/go_appengine_sdk_linux_amd64-$pkgver.zip")
	md5sums=('dad3d7e06d559df1c871fe563c15c10d')
fi
options=(!strip)

build() {
  cd "$srcdir/google_appengine"

  msg2 "Fixing file permissions..."
  # Only make the .py files executable
  find . -type f -exec chmod 644 '{}' \;
  chmod +x *.py goroot/bin/*

  msg2 "Make the scripts use python2..."
  # Thanks Guten <ywzhaifei@gmail.com> and Lee.MaRS <leemars@gmail.com> (google-appengine package)
  grep -r -l python . | xargs sed -i '/#!\/usr\/bin\/env python/s|python|python2|'

  msg2 "Creating a wrapperscript for dev_appserver.py..."
  echo '#!/bin/sh' > "$srcdir/appserver"
  echo 'python2 /opt/google-appengine-go/dev_appserver.py $*' >> "$srcdir/appserver"
}

package() {
  cd "$srcdir/google_appengine"

  msg2 "Packaging files..."
  mkdir "$pkgdir/opt"
  cp -R "./" "$pkgdir/opt/$pkgname"

  msg2 "Creating a symbolic link for the python modules..."
  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages"
  ln -s "/opt/$pkgname/google" "$pkgdir/usr/lib/python2.7/site-packages/google" 
  
  msg2 "Packaging wrapperscript..."
  mkdir -p "$pkgdir/usr/bin"
  install -Dm755 "$srcdir/appserver" "$pkgdir/usr/bin/appserver"

  msg2 "Cleaning up deprecated files..."
  rm -r "$pkgdir/opt/$pkgname/tools"

  msg2 "Packaging license..."
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
