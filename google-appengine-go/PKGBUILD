# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Richard Hoffman <coverslide@gmail.com>
# Contributor: Max <fuzymonkey@gmail.com>

pkgname=google-appengine-go
pkgver=1.8.0
pkgrel=1
pkgdesc='Google App Engine SDK for Go'
arch=('x86_64' 'i686')
depends=('python2')
url='http://code.google.com/appengine/downloads.html#Google_App_Engine_SDK_for_Go'
license=('APACHE')
options=('!strip')
noextract=("go_appengine_sdk_linux_amd64-$pkgver.zip" "go_appengine_sdk_linux_386-$pkgver.zip")
makedepends=('unzip')
optdepends=('mysql-python: MySQL support for Python 2'
            'python2-imaging: Image processing capabilities for Python')

if [ $CARCH != "i686" ]; then
  source=("http://googleappengine.googlecode.com/files/go_appengine_sdk_linux_amd64-$pkgver.zip")
  sha256sums=('d7dd90496232da9a380c0e6b8397ae21c7a2dd0f31b0397b6b92e509cbcbf0d1')
else
  source=("http://googleappengine.googlecode.com/files/go_appengine_sdk_linux_386-$pkgver.zip")
  sha256sums=('39babb570cd8722903dae65e616a4ae6fdf9a37b58c147a12118277f35d13294')
fi

prepare() {
  cd "$srcdir"

  unzip -q "go_appengine_sdk_linux_"*"-$pkgver.zip"
}

build() {
  cd "$srcdir/google_appengine"

  msg2 'Correcting file permissions...'
  # Only make the .py files executable
  find . -type f -exec chmod 644 '{}' \;
  chmod +x *.py goroot/bin/*

  for f in *.py
  do
    if [ "$f" != "dev_appserver.py" ]; then
      msg2 "Creating wrapperscript for $f [`basename $f .py`-go]..."
      echo -e "#!/bin/sh\npython2 /opt/google-appengine-go/$f \$*" \
        > "$srcdir/`basename $f .py`-go"
    else
      msg2 "Creating wrapperscript for $f [appserver]..."
      echo -e "#!/bin/sh\npython2 /opt/google-appengine-go/$f \$*" \
        > "$srcdir/appserver"
    fi
    # Modifying script to use Python 2
	  sed -i '0,/on/s//on2/' $f
  done
}

package() {
  cd "$srcdir/google_appengine"

  msg2 'Packaging files...'
  mkdir "$pkgdir/opt"
  cp -R "./" "$pkgdir/opt/$pkgname"

  msg2 'Creating a symbolic link for the python modules...'
  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages"
  ln -s "/opt/$pkgname/google" "$pkgdir/usr/lib/python2.7/site-packages/google" 
  
  msg2 'Packaging wrapperscripts...'
  mkdir -p "$pkgdir/usr/bin"
  install -Dm755 "$srcdir/appserver" "$pkgdir/usr/bin/appserver"
  install -Dm755 $srcdir/*-go "$pkgdir/usr/bin/"

  msg2 'Cleaning up deprecated files...'
  rm -r "$pkgdir/opt/$pkgname/tools"

  msg2 'Packaging license...'
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  msg2 'Fixing permissions...'
  find "$pkgdir/opt/$pkgname" -type d -exec chmod 755 {} \;
  if [ $CARCH != "i686" ]; then
    chmod +x "$pkgdir/opt/$pkgname/goroot/pkg/tool/linux_amd64/"*
  else
    chmod +x "$pkgdir/opt/$pkgname/goroot/pkg/tool/linux_386/"*
  fi
}

# vim:set ts=2 sw=2 et:
