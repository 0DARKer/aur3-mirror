#Contributor:	Spyros	Stathopoulos	<gmail.com: foucault.online>
#Maintainer:	Jesse	Jaara		<gmail.com: jesse.jaara>

pkgname=harfbuzz-git
pkgver=20120816
pkgrel=1
pkgdesc="HarfBuzz is an OpenType text shaping engine."
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('icu'  'cairo' 'graphite')
makedepends=('ragel' 'gtk-doc')
provides=('harfbuzz=0.9.2')
conflicts=('harfbuzz')
options=(!libtool)

_gitroot="git://anongit.freedesktop.org/harfbuzz"
_gitname="harfbuzz"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname} --depth 1
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  mkdir -p "${pkgdir}/usr/share/licenses/harfbuzz-git"
  cp COPYING "${pkgdir}/usr/share/licenses/harfbuzz-git/"

  make DESTDIR="${pkgdir}" install
}
