#Contributor:	Spyros	Stathopoulos	<gmail.com: foucault.online>
#Maintainer:	Jesse	Jaara		<gmail.com: jesse.jaara>

pkgname=harfbuzz-git
pkgver=20111021
pkgrel=1
pkgdesc="HarfBuzz is an OpenType text shaping engine."
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('freetype2' 'icu' 'glib2')
makedepends=('ragel' 'gtk-doc')
provides=('harfbuzz=0.7.0')
conflicts=('harfbuzz')

_gitroot="git://anongit.freedesktop.org/harfbuzz"
_gitname="harfbuzz"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname} --depth 1
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  make DESTDIR="${pkgdir}/" install
} 

# vim:set ts=2 sw=2 et:
