# Maintainer: Mauro Santos <registo.mailling@gmail.com>
pkgname=systemd-hddapm
pkgver=1.0
pkgrel=1
pkgdesc="Hard Disk APM level service for systemd"
arch=('any')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL')
depends=('hdparm' 'systemd')
backup=('etc/hddapm.conf')
source=('hddapm'
        'hddapm.conf'
        'hddapm.service')
md5sums=('17f638686b6ebbe07752aa0d69b858b9'
         'c39a40ff91170d65b9366b50ba39df7f'
         'b50e87db085589dd6e6a6fd0498e92a6')

package() {
    # Install files
    install -Dm755 "${srcdir}/hddapm" "${pkgdir}/usr/bin/hddapm"
    install -Dm644 "${srcdir}/hddapm.service" "${pkgdir}/usr/lib/systemd/system/hddapm.service"
    install -Dm644 "${srcdir}/hddapm.conf" "${pkgdir}/etc/hddapm.conf"
    
    # Enable unit
    mkdir "${pkgdir}/usr/lib/systemd/system/basic.target.wants"
    ln -s ../hddapm.service "${pkgdir}/usr/lib/systemd/system/basic.target.wants/hddapm.service"
    
    # Take care of Suspend/Hibernate cases
    mkdir "${pkgdir}/usr/lib/systemd/system-sleep"
    ln -s "/usr/bin/hddapm" "${pkgdir}/usr/lib/systemd/system-sleep/hddapm"
}




