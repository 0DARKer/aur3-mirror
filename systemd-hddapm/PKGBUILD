# Maintainer: Mauro Santos <registo.mailling@gmail.com>
pkgname=systemd-hddapm
pkgver=1.2
pkgrel=1
pkgdesc="Hard Disk APM level service for systemd"
arch=('any')
url="https://sourceforge.net/projects/hddapm"
license=('GPL')
depends=('hdparm' 'systemd')
backup=('etc/hddapm.conf')
install='hddapm.install'
source=("https://downloads.sourceforge.net/project/hddapm/hddapm-${pkgver}.tar.gz")
md5sums=('d85d5e76676ad492a37712a262f98358')

package() {
    # Install files
    install -Dm755 "${srcdir}/hddapm" "${pkgdir}/usr/bin/hddapm"
    install -Dm644 "${srcdir}/hddapm.service" "${pkgdir}/usr/lib/systemd/system/hddapm.service"
    install -Dm644 "${srcdir}/hddapm.conf" "${pkgdir}/etc/hddapm.conf"
    install -Dm644 "${srcdir}/hddapm.initinstall" "${pkgdir}/usr/lib/initcpio/install/hddapm"
    install -Dm644 "${srcdir}/hddapm.inithook" "${pkgdir}/usr/lib/initcpio/hooks/hddapm"
    
    # Ship the service file but don't enable it
    mkdir "${pkgdir}/usr/lib/systemd/system/basic.target.wants"
    #ln -s ../hddapm.service "${pkgdir}/usr/lib/systemd/system/basic.target.wants/hddapm.service"
    
    # Take care of Suspend/Hibernate cases
    mkdir "${pkgdir}/usr/lib/systemd/system-sleep"
    ln -s "/usr/bin/hddapm" "${pkgdir}/usr/lib/systemd/system-sleep/hddapm"
}




