# Maintainer: Carl George < arch at cgtx.us >

_module="click"
pkgname=("python-${_module}" "python2-${_module}")
pkgver="4.0"
pkgrel="1"
pkgdesc="A simple wrapper around optparse for powerful command line utilities."
arch=("any")
url="http://click.pocoo.org/"
license=("BSD")
makedepends=("python-setuptools" "python2-setuptools")
checkdepends=("python-pytest" "python2-pytest")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('f49e03611f5f2557788ceeb80710b1c67110f97c5e6740b97edf70245eea2409')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

check() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py test
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py test
}

package_python-click() {
    depends=("python>=3.3")
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-click() {
    depends=("python2>=2.6")
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
