# Maintainer: Carl George <carl@cgtx.us>

_module="click"
pkgname=("python-${_module}" "python2-${_module}")
pkgver="3.2"
pkgrel="3"
pkgdesc="Command Line Interface Creation Kit"
arch=("any")
url="http://click.pocoo.org/"
license=("BSD")
makedepends=("python-setuptools" "python2-setuptools")
checkdepends=("python-pytest" "python2-pytest")
source=(
	"https://github.com/mitsuhiko/${_module}/archive/${pkgver}.tar.gz"
	"0001-Change-exception-check-of-TypeError.patch"
)
sha256sums=(
	"06b41a13707697b3fe8339388db2a7f55ad27d42744b5d9ae1b1c766499d8196"
	"253a38efb11fcd74dc2e54b578c7ec8c1c595f5617ce6392fd31cdffbcd92478"
)

prepare() {
	# https://github.com/mitsuhiko/click/pull/209
	patch -p1 -d "${srcdir}/${_module}-${pkgver}" < 0001-Change-exception-check-of-TypeError.patch
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

check() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py test
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py test
}

package_python-click() {
	depends=("python")
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-click() {
	depends=("python2")
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
