# Maintainer: Carl George <carl@carlgeorge.us>

_module="click"
pkgname=("python-${_module}" "python2-${_module}")
pkgver="1.1"
pkgrel="3"
pkgdesc="Command Line Interface Creation Kit"
arch=("any")
url="http://click.pocoo.org/"
license=("BSD")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://github.com/mitsuhiko/${_module}/archive/${pkgver}.tar.gz")
sha256sums=("c1c52686b9f071718c449497f99f9ee595a832761d9ac8a5d2ce9dad5d38d91f")

build() {
	cd ${srcdir}/${_module}-${pkgver}
	python setup.py build --build-base build3
	python2 setup.py build --build-base build2
}

package_python-click() {
	depends=("python")
	cd ${srcdir}/${_module}-${pkgver}
	ln -sfn build3 build
	python setup.py install --skip-build --root=${pkgdir} --optimize=1
}

package_python2-click() {
	depends=("python2")
	cd ${srcdir}/${_module}-${pkgver}
	ln -sfn build2 build
	python2 setup.py install --skip-build --root=${pkgdir} --optimize=1
}
