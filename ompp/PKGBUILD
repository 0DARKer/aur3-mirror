# Maintainer: lynix <lynix47@googlemail.com>
# Contributor: justinzane <justin@justinzane.com>

pkgname=ompp
pkgver=0.8.5
pkgrel=1
pkgdesc="Profiling tool for OpenMP applications"
arch=('i686' 'x86_64')
url='http://www.ompp-tool.com'
license=('GPL2')
depends=('bash' 'openmpi')
source=("http://projekt17.pub.lab.nm.ifi.lmu.de/ompp/downloads/${pkgname}-${pkgver}.tgz")
sha256sums=('6164d9673df15536f8d9f0a4d50bf039066f6140b740df4c7d0ba1fa095bd8b5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make OMPCC=gcc OMPFLAG=-fopenmp
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make OMPCC=gcc OMPFLAG=-fopenmp \
    INSTDIR="${pkgdir}/usr" \
    DOCDIR="${pkgdir}/usr/share/doc/${pkgname}" \
    TESTDIR="${pkgdir}/usr/share/doc/${pkgname}/examples" \
    install

  # Remove opari to avoid conflicts with openmpi
  rm "${pkgdir}/usr/bin/opari"
  rm "${pkgdir}/usr/include/opari_omp.h"

  # Fix reference to $pkgdir
  sed -i -e "s%${pkgdir}%%" "${pkgdir}/usr/bin/kinst-ompp"
}

# vim:set ts=2 sw=2 et:
