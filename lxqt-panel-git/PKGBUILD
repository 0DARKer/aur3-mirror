# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=lxqt-panel
pkgname=$_pkgname-git
pkgver=809.3ceb70f
pkgrel=1
pkgdesc="The LXQt desktop panel"
arch=("i686" "x86_64")
url="http://www.lxde.org"
license=("GPL2")
depends=("icu" "libqtxdg-git" "liblxqt-mount-git" "libsysstat" "lxqt-globalkeys-git")
optdepends=(
	"alsa-lib: Recompile for Alsa support in volume control plugin"
	"pulseaudio: Recompile for PulseAudio support in volume control plugin"
	"lm_sensors: Recompile for Sensors (battery) plugin"
	"libstatgrab: Recompile for network and CPU monitor plugin"
)
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git://git.lxde.org/git/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	echo "$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
