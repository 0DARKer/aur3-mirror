# Maintainer: pisuka <tekmon@gmail.com>
# Contributor: Estevao Valadao <estevao@archlinux-br.org>
# Contributor: Guten <ywzhaifei@gmail.com>
# Contributor: Lee.MaRS <leemars@gmail.com>

pkgname=google-appengine-python
pkgver=1.5.1
pkgrel=1
arch=(any)
pkgdesc="Google App Engine SDK for Python."
url=http://code.google.com/appengine/
license=(APACHE)
depends=(python25 python25-ssl python25-pil)
makedepends=(unzip)
conflicts=(google-appengine)
options=(!strip)
install=install
source=(http://googleappengine.googlecode.com/files/google_appengine_${pkgver}.zip)
noextract=(google_appengine_${pkgver}.zip)
sha1sums=(2b2900ec5f2d9d51282645015b4f1bef07b1cc07)

build() {
  cd "$pkgdir"
  mkdir -p opt usr/bin

  # Extract with unzip as bsdtar screws up permissions
  unzip -q "$srcdir/google_appengine_${pkgver}.zip" -d opt

  grep -r -l python opt/google_appengine | xargs sed -i '/#!\/usr\/bin\/env python/s|python|python2\.5|'

  find opt/google_appengine -maxdepth 1 -type f -executable -printf "/opt/google_appengine/%f\n" | xargs ln -st usr/bin/
}

