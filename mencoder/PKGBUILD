# Maintainer: Slumslayer <paul.smet [at] gmail [dot] com>

# NOTE: This package relies on Mplayer's configure script to detect what is
# useful on your system. Build it on the machine you will use it on.
# If some features don't work, install the related optdepends *before*
# building.

pkgname=mencoder
pkgver=32833
pkgrel=1
pkgdesc="A command line video decoding, encoding and filtering tool"
arch=('i686' 'x86_64')
url="http://www.mplayerhq.hu/"
license=('GPL')
depends=('lame' 'libtheora' 'xvidcore' 'x264' 'faac')
makedepends=('subversion' 'yasm' 'git')
source=()
md5sums=()

_svnmod="mplayer"
_svntrunk=svn://svn.mplayerhq.hu/mplayer/trunk/

__gitroot="git://git.videolan.org/ffmpeg.git"
__gitname="ffmpeg"

build() {
	cd $srcdir
	if [ -d $_svnmod/.svn ]; then
		cd $_svnmod
		svn up -r $pkgver
	else
		svn co $_svntrunk --config-dir . -r $pkgver $_svnmod
	fi
	msg "SVN checkout done or server timeout"

	cd $srcdir
	msg "Fetching FFmpeg"
	msg "Connecting to GIT server...."
	if [ -d $__gitname ] ; then
		cd $__gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $__gitroot $__gitname
	fi
	msg "GIT checkout done or server timeout"

	cd $srcdir
	cp -r $_svnmod $_svnmod-build
	cp -r $__gitname $_svnmod-build/$__gitname

	cd $_svnmod-build

	msg "Building the package..."
	unset CFLAGS LDFLAGS

	./configure --prefix=/usr --confdir=/etc/mplayer --disable-gui \
		--enable-mencoder \
		--enable-largefiles \
		--disable-mplayer \
		--disable-nas \
		--disable-mga \
		--disable-smb \
		--disable-liblzo \
		--disable-libdv \
		--disable-musepack \
		--disable-speex \
		--disable-cdparanoia \
		--disable-dvdnav \
		--disable-live \
		--disable-mad \
		--extra-cflags=-fno-strict-aliasing

	make || return 1

}

package() {
	cd $srcdir/$_svnmod-build
	make -j1 DESTDIR=$pkgdir install || return 1

	# Rename the man page, otherwise conflicts with mplayer
	mv ${startdir}/pkg/usr/share/man/man1/mplayer.1 ${startdir}/pkg/usr/share/man/man1/mencoder.1
	msg "Cleaning build files..."
	rm -rf $srcdir/$_svnmod-build
}
