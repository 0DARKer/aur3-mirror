# Maintainer: Tom Moore <tmoore01 at gmail dot com>
# Contributer: Allen Choong <allencch at hotmail dot com>
# Contributer: Andrea Scarpino <andrea@archlinux.org>
# Contributer: Daniel YC Lin <dlin.tw at gmail>
# Contributer: Jarek Sedlacek <JarekSedlacek@gmail>

pkgname=grive-git
pkgver=r435.9717352
pkgrel=1
pkgdesc='Open source Linux client for Google Drive'
arch=('i686' 'x86_64')
url='http://www.lbreda.com/grive/start'
license=('GPL2')
depends=('json-c' 'curl' 'boost-libs' 'expat' 'libgcrypt' 'yajl')
makedepends=('cmake' 'git' 'boost' 'yajl' 'json-c' 'qt4')
provides=('grive')
conflicts=('grive')
source=("$pkgname"::'git://github.com/linwiz/grive.git')
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


build() {

  cd "${srcdir}/$pkgname"
  rm -rf "${srcdir}"/build
  mkdir "${srcdir}"/build
  cd "${srcdir}"/build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS=-ljson-c
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
