# Contributer: Andrea Scarpino <andrea@archlinux.org>
# Contributer: Daniel YC Lin <dlin.tw at gmail>
# Maintainer: Jarek Sedlacek <JarekSedlacek@gmail>

pkgname=grive-git
pkgrel=1
pkgver=20130619
pkgdesc='Open source Linux client for Google Drive'
arch=('i686' 'x86_64')
url='http://www.lbreda.com/grive/start'
license=('GPL2')
depends=('json-c' 'curl' 'boost-libs' 'expat' 'libgcrypt' 'yajl')
makedepends=('cmake' 'git' 'boost' 'yajl' 'json-c' 'qt4')
provides=('grive')
conflicts=('grive')

_gitroot="https://github.com/JarekSed/grive.git"
_gitname="grive"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}"/build
  mkdir "${srcdir}"/build
  cd "${srcdir}"/build
  cmake ../${_gitname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS=-ljson-c
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
