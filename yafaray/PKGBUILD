# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=yafaray
pkgver=0.1.1
pkgrel=2
pkgdesc="Raytracing open source render engine"
arch=('i686' 'x86_64')
url="http://www.yafaray.org/"
license=('LGPL2')
depends=('openexr' 'libxml2')
optdepends=('qt: QT GUI' 'python2: Blender exporter')
makedepends=('scons' 'swig' 'qt')
conflicts=('yafaray-svn')
source=(http://static.yafaray.org/sources/YafaRay.$pkgver.zip \
        http://static.yafaray.org/sources/YafaRay-blender.$pkgver.zip)
md5sums=('d1722dec25299f6f3fcc1d7c661a4e90'
         'd7e7f86b9e90e7f960707ebaea1843ab')

build() {
  cd "$srcdir/$pkgname"

  msg "Writing user-config.py"
  echo "PREFIX = '"$pkgdir"/usr'" > user-config.py
  echo "REL_CCFLAGS = '$CFLAGS -ffast-math'" >> user-config.py
  echo "WITH_YF_QT = 'true'" >> user-config.py
  echo "YF_QTDIR = '/usr'" >> user-config.py

  sed -i 's|$YF_PLUGINPATH|/usr/lib/yafaray|' tools/writeconfig.py

  scons
  scons swig
}

package() {
  cd "$srcdir/$pkgname"
  scons install

  # install python bindings and blender export scripts
  install -d -m755 "$pkgdir"/usr/share/blender/scripts
  install -m644 bindings/python/* "$pkgdir"/usr/share/blender/scripts
  install -m644 "$srcdir"/yafaray-blender/*.py "$pkgdir"/usr/share/blender/scripts
}

# vim:set ts=2 sw=2 et:
