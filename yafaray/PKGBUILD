# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=yafaray
pkgver=0.1.2_beta5
blendver=2.66
pkgrel=1
pkgdesc="Raytracing open source render engine"
arch=('i686' 'x86_64')
url="http://www.yafaray.org/"
license=('LGPL2')
depends=('libxml2' 'openexr' 'libjpeg')
optdepends=('qt4: QT GUI' 'python: Python bindings, Blender exporter')
makedepends=('cmake' 'git' 'qt4' 'swig' 'python')
source=("$pkgname::git+git://github.com/YafaRay/Core.git#tag=${pkgver//_/-}" \
        'exporter::git+git://github.com/YafaRay/Blender-Exporter.git')
md5sums=('SKIP' 'SKIP')

build() {
  cd "$srcdir/$pkgname"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DYAF_PY_VERSION=3.3 \
    -DYAF_BINDINGS_PY_DIR=/usr/lib/python3.3 \
    .
  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir/" install

  # install exporter
  cd "$srcdir/exporter"
  git checkout-index -a -f --prefix="$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/"
  # create symlinks to make exporter fully work
  install -d -m755 "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin"
  ln -sf "/usr/lib/libyafaraycore.so" "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin/libyafaraycore.so"
  ln -sf "/usr/lib/libyafarayplugin.so" "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin/libyafarayplugin.so"
  ln -sf "/usr/lib/yafaray" "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin/plugins"
}

# vim:set ts=2 sw=2 et:
