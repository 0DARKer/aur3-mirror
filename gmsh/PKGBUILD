# Maintainer: gborzi <gborzi@ieee.org>
# Contributor: mickele <mimocciola@yahoo.com>
pkgname=gmsh
pkgver=2.6.0
pkgrel=1
pkgdesc="An automatic 3D finite element mesh generator with pre and post-processing facilities."
arch=('i686' 'x86_64')
url="http://www.geuz.org/gmsh/"
license=('custom')
depends=('fltk' 'gmp' 'lapack' 'med' 'opencascade>=6.5.3')
#depends=('fltk' 'lapack' 'med' 'mesa')
makedepends=('cmake' 'desktop-file-utils' 'sed')
optdepends=('gmsh-docs: docs for gmsh')
options=(!emptydirs)
source=("${url}src/${pkgname}-${pkgver}-source.tgz" gmsh.desktop gmsh.completion)
md5sums=('404fc5ad6194b959c72ec9b0d9b190e7'
         'e63dc24ba025741fc1a82633b475e4a8'
         '9ee4b5bf27956de5aa412bbc939660d3')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}-source"

   # patches for CMakeLists.txt, libpng 1.5.8, gcc 4.7.0
   #patch -Np1 -i "${srcdir}/misc.patch"
   # patch for OpenCascade 6.5.3
   #patch -Np0 -i "$srcdir/occ653.patch"
   # Help links to local doc (package gmsh-docs)
   sed -e "s|http://geuz.org/gmsh/doc/texinfo/|file:///usr/share/doc/gmsh/gmsh.html|" \
       -i Fltk/menuWindow.cpp
   sed -e "s|http://geuz.org/gmsh/doc/|/usr/share/licenses/gmsh/|" \
       -i Fltk/aboutWindow.cpp

   mkdir -p build

   cd build

   cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .. 
   #cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DENABLE_OCC=0 .. 

   make
}

package() {

   cd "${srcdir}/${pkgname}-${pkgver}-source/build"
   make DESTDIR=${pkgdir} install

   install -D -m644 "${srcdir}/${pkgname}-${pkgver}-source/utils/icons/gmsh128x128-smooth.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

   desktop-file-install --dir="${pkgdir}/usr/share/applications" \
    	"${srcdir}/${pkgname}.desktop"

   install -D -m 644 "${srcdir}/${pkgname}-${pkgver}-source/doc/LICENSE.txt" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE.txt"
   install -D -m 644 "${srcdir}/${pkgname}-${pkgver}-source/doc/CREDITS.txt" "${pkgdir}/usr/share/licenses/$pkgname/CREDITS.txt"
   install -D -m644 $srcdir/gmsh.completion $pkgdir/etc/bash_completion.d/gmsh

   rm -rf ${pkgdir}/usr/share/doc
}
