# Maintainer: gborzi <gborzi@ieee.org>
# Contributor: mickele <mimocciola@yahoo.com>
pkgname=gmsh
pkgver=2.5.0
pkgrel=2
pkgdesc="An automatic 3D finite element mesh generator with pre and post-processing facilities."
arch=('i686' 'x86_64')
url="http://www.geuz.org/gmsh/"
license=('custom')
depends=('gcc-libs' 'fltk' 'lapack' 'med' 'opencascade>=6.3.0-5' 'lua')
makedepends=('cmake' 'desktop-file-utils' 'sed' 'coreutils')
optdepends=('gmsh-docs: docs for gmsh')
source=("$url/src/${pkgname}-${pkgver}-source.tgz" gmsh.desktop gmsh.completion
        CMakeLists.txt.patch)
md5sums=('afa48c0296dba08641bc6312b0ccabf5'
         'e63dc24ba025741fc1a82633b475e4a8'
         '9ee4b5bf27956de5aa412bbc939660d3'
         '3ed372b562052df5e9c27df7004f3608')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}-source"

   # patch CMakeLists.txt to work with lapack from extra
   patch -Np0 -i "${srcdir}/CMakeLists.txt.patch"
   # Help links to local doc (package gmsh-docs)
   sed -e "s|http://geuz.org/gmsh/doc/texinfo/|file:///usr/share/doc/gmsh/gmsh.html|" \
       -i Fltk/menuWindow.cpp
   sed -e "s|http://geuz.org/gmsh/doc/|/usr/share/licenses/gmsh/|" \
       -i Fltk/aboutWindow.cpp

   mkdir -p build

   cd build

   cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .. 

   make
}

package() {

   cd "${srcdir}/${pkgname}-${pkgver}-source/build"
   make DESTDIR=${pkgdir} install

   install -D -m644 "${srcdir}/${pkgname}-${pkgver}-source/utils/icons/gmsh128x128-smooth.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

   desktop-file-install --dir="${pkgdir}/usr/share/applications" \
    	"${srcdir}/${pkgname}.desktop"

   install -D -m 644 "${srcdir}/${pkgname}-${pkgver}-source/doc/LICENSE.txt" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE.txt"
   install -D -m 644 "${srcdir}/${pkgname}-${pkgver}-source/doc/CREDITS.txt" "${pkgdir}/usr/share/licenses/$pkgname/CREDITS.txt"
   install -D -m644 $srcdir/gmsh.completion $pkgdir/etc/bash_completion.d/gmsh

   rm -rf ${pkgdir}/usr/share/doc
}
