# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=fslint
pkgver=2.42
pkgrel=2
pkgdesc="A utility to find and clean various forms of lint on a filesystem"
arch=('any')
url="http://www.pixelbeat.org/fslint/"
license=('GPL')
depends=('pygtk' 'python2')
install=$pkgname.install
source=(http://www.pixelbeat.org/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('babbef4a34f42ab1d396152fd79b4353adc114b2e8d68b838e20a8067c5a3273')

package() {
  cd ${srcdir}/$pkgname-$pkgver

#install directories
  install -d ${pkgdir}/usr/share/{man/man1,$pkgname/{doc,$pkgname/{fstool,supprt/rmlint}}}

  sed -i -e s_^liblocation=.*_liblocation=\'/usr/share/fslint\'_ fslint-gui

  install -D -m755 $pkgname-gui ${pkgdir}/usr/bin/$pkgname-gui
  install -D -m644 $pkgname.glade ${pkgdir}/usr/share/$pkgname/$pkgname.glade
  install -D -m755 -t ${pkgdir}/usr/share/$pkgname/$pkgname $pkgname/find* $pkgname/zipdir $pkgname/$pkgname
  install -D -m755 -t ${pkgdir}/usr/share/$pkgname/$pkgname/fstool $pkgname/fstool/*
  install -D -m755 -t ${pkgdir}/usr/share/$pkgname/$pkgname/supprt $pkgname/supprt/get* $pkgname/supprt/fslver
  install -D -m755 -t ${pkgdir}/usr/share/$pkgname/$pkgname/supprt/rmlint fslint/supprt/rmlint/*

#icons
  install -D -m644 fslint_icon.png ${pkgdir}/usr/share/pixmaps/fslint_icon.png
  install -D -m644 fslint_icon.png ${pkgdir}/usr/share/fslint/fslint_icon.png

#shortcut
  install -D -m644 $pkgname.desktop ${pkgdir}/usr/share/applications/$pkgname.desktop

#locales
  make -C po DESTDIR=${pkgdir}/usr DATADIR=share install

#man pages
  cp -r ${srcdir}/$pkgname-$pkgver/man/* ${pkgdir}/usr/share/man/man1

#doc files
  cp -r ${srcdir}/$pkgname-$pkgver/doc/* ${pkgdir}/usr/share/$pkgname/doc

#make it use Python2
  sed -i 's/env python/env python2/g' ${pkgdir}/usr/bin/fslint-gui
  sed -i 's/env python/env python2/g' ${pkgdir}/usr/share/fslint/fslint/supprt/rmlint/merge_hardlinks
  sed -i 's/env python/env python2/g' ${pkgdir}/usr/share/fslint/fslint/supprt/rmlint/fixdup
  sed -i 's/env python/env python2/g' ${pkgdir}/usr/share/fslint/fslint/fstool/dupwaste
}
