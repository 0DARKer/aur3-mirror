# Maintainer: joseph28cb

pkgname=vuze-extreme-mod
pkgver=5.0.0.0
pkgrel=1
pkgdesc="A modded version of vuze BitTorrent client"
arch=('i686' 'x86_64')
url="http://www.sb-innovation.de/f41/"
license=('GPL')
depends=('bash' 'desktop-file-utils' 'java-runtime')
optdepends=('xulrunner192: needed for the channels GUI')
provides=('vuze')
conflicts=('vuze')
install=vuze.install
options=('strip')
source=("http://downloads.sourceforge.net/project/azureus/vuze/Vuze_5000/Vuze_5000_linux.tar.bz2"
        "http://www.sb-innovation.de/attachments/f41/14078d1368113387-vuze-extreme-mod-sb-innovation-5-0-0-0-vpem_5000-00.zip"
        "http://plugins.vuze.com/plugins/azrating_1.4.2.jar"
        "http://plugins.vuze.com/plugins/azupnpav_0.4.7.zip"
        "mono_icons.tar.xz")
noextract=('14078d1368113387-vuze-extreme-mod-sb-innovation-5-0-0-0-vpem_5000-00.zip' 'mono_icons.tar.xz' 'azrating_1.4.2.jar' 'azupnpav_0.4.7.zip')
md5sums=('fd86f61f2ba0b5ef3543b49ea8e6c27f'
         '83bb2731e6cedf874be1660f947d669f'
         '79fb6580b7965c705717fc096b4dbca9'
         '6f84dc639b9baaef23674621330a8f57'
         '4a17a214f5dd35c3b2c50c85ace88534')

  # Install mono systray icon (change to "yes")
  _mono="no"

package() {
  install -dm755 "$pkgdir/usr/share"
  cp -rf --no-preserve=ownership vuze "$pkgdir/usr/share/$pkgname"

  [[ "$CARCH" == 'i686' ]] && cp -f --no-preserve=ownership vuze/swt/swt32.jar "$pkgdir/usr/share/$pkgname/swt.jar"
  [[ "$CARCH" == 'x86_64' ]] && cp -f --no-preserve=ownership vuze/swt/swt64.jar "$pkgdir/usr/share/$pkgname/swt.jar"

  install -Dm755 vuze/vuze "$pkgdir/usr/bin/$pkgname"
  install -Dm644 vuze/vuze.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 vuze/vuze.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 vuze/vuze.schemas "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas"
  install -m644 "$srcdir/azrating_1.4.2.jar" "$pkgdir/usr/share/$pkgname/plugins/azrating/"
  bsdtar --no-same-owner -xf "$srcdir/azupnpav_0.4.7.zip" -C "$pkgdir/usr/share/$pkgname/plugins/azupnpav/"
  #unzip -qqod "$pkgdir/usr/share/$pkgname/plugins/azupnpav/" "$srcdir/azupnpav_0.4.7.zip"

  sed -i 's|#PROGRAM_DIR=.*|PROGRAM_DIR="/usr/share/vuze-extreme-mod"|' "$pkgdir/usr/bin/$pkgname"
  sed -i 's|vuze|vuze-extreme-mod|' "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas"
  sed -e 's|Name=Vuze|Name=Vuze Extreme Mod|' \
      -e 's|Exec=vuze %f|Exec=vuze-extreme-mod %U|' \
      -e 's|Icon=vuze.png|Icon=vuze-extreme-mod.png|' \
      -i "$pkgdir/usr/share/applications/$pkgname.desktop"

  chmod 644 "$pkgdir"/usr/share/$pkgname/plugins/azupnpav/*

  rm -rf "$pkgdir/usr/share/$pkgname"/{swt,azureus,vuze,vuze.{desktop,schemas},plugins/{azrating/azrating_1.3.1.jar,azupnpav/azupnpav_0.4.{4,6}.jar}}

  bsdtar -xf $(basename ${source[1]}) -C "$pkgdir/usr/share/$pkgname"

  if [[ $_mono = "yes" ]]; then
    rm -rf tmp/
    mkdir tmp
    bsdtar -xf $(basename ${source[1]}) Azureus2.jar
    bsdtar -xf Azureus2.jar -C tmp
    bsdtar -xf mono_icons.tar.xz -C tmp/org/gudy/azureus2/ui/icons/
    cd tmp
    jar cf Azureus2.jar ./*/
    install -m644 Azureus2.jar "$pkgdir/usr/share/$pkgname"
  fi
}
