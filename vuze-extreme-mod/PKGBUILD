# Maintainer: joseph28cb

pkgname=vuze-extreme-mod
pkgver=5.2.0.0
pkgrel=1
pkgdesc="A modded version of vuze BitTorrent client"
arch=('i686' 'x86_64')
url="http://www.sb-innovation.de/f41/"
license=('GPL')
depends=('desktop-file-utils' 'java-runtime')
optdepends=('xulrunner192: needed for the channels GUI'
            'vuze-plugin-mldht: Mainline DHT for Vuze. Talk DHT to ÂµTorrent'
            'vuze-plugin-i2p: A plugin for the I2P darknet network')
provides=('vuze')
conflicts=('vuze')
install=vuze.install
options=('!strip')
source=('http://downloads.sourceforge.net/project/azureus/vuze/Vuze_5200/Vuze_5200_linux.tar.bz2'
        'http://www.sb-innovation.de/attachments/f41/14437d1385568044-vuze-extreme-mod-sb-innovation-5-2-0-0-vpem_5200-00.zip'
        'mono_icons.tar.xz')
noextract=('mono_icons.tar.xz')
md5sums=('87f56a3fd0a845cfc88969be56b00edf'
         '256819ae25edf710e113130793c78f62'
         '4a17a214f5dd35c3b2c50c85ace88534')

  # Install mono systray icon (change to "yes")
  _mono='no'

package() {
  install -dm755 "$pkgdir/usr/share"
  cp -rf --no-preserve=ownership vuze "$pkgdir/usr/share/$pkgname"

  [[ "$CARCH" == 'i686' ]] && cp -f --no-preserve=ownership vuze/swt/swt32.jar "$pkgdir/usr/share/$pkgname/swt.jar"
  [[ "$CARCH" == 'x86_64' ]] && cp -f --no-preserve=ownership vuze/swt/swt64.jar "$pkgdir/usr/share/$pkgname/swt.jar"

  install -Dm755 vuze/vuze "$pkgdir/usr/bin/$pkgname"
  install -Dm644 vuze/vuze.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 vuze/vuze.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 vuze/vuze.schemas "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas"

  sed -i 's|#PROGRAM_DIR=.*|PROGRAM_DIR="/usr/share/vuze-extreme-mod"|' "$pkgdir/usr/bin/$pkgname"
  sed -i 's|vuze|vuze-extreme-mod|' "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas"
  sed -e 's|Name=Vuze|Name=Vuze Extreme Mod|' \
      -e 's|Exec=vuze %f|Exec=vuze-extreme-mod %U|' \
      -e 's|Icon=vuze.png|Icon=vuze-extreme-mod.png|' \
      -i "$pkgdir/usr/share/applications/$pkgname.desktop"

  chmod 644 "$pkgdir"/usr/share/$pkgname/plugins/azupnpav/*

  rm -rf "$pkgdir/usr/share/$pkgname"/{swt,azureus,vuze,vuze.{desktop,schemas}}

  cp -rf --no-preserve=ownership VPEM_5200-00/* "$pkgdir/usr/share/$pkgname"

  if [[ $_mono = "yes" ]]; then
    rm -rf tmp/
    mkdir tmp
    bsdtar -xf $(basename ${source[1]}) VPEM_5200-00/Azureus2.jar
    bsdtar -xf VPEM_5200-00/Azureus2.jar -C tmp
    bsdtar -xf mono_icons.tar.xz -C tmp/org/gudy/azureus2/ui/icons/
    cd tmp
    jar cf Azureus2.jar ./*/
    install -m644 Azureus2.jar "$pkgdir/usr/share/$pkgname"
  fi
}
