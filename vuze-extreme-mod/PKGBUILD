# Maintainer: joseph28cb

pkgname=vuze-extreme-mod
pkgver=4.8.0.1_b01
pkgrel=1
pkgdesc="A modded version of vuze BitTorrent client"
arch=('i686' 'x86_64')
url="http://www.sb-innovation.de/f41/"
license=('GPL')
depends=('java-runtime' 'libgnomeui')
optdepends=('xulrunner: needed for browsing the vuze content')
makedepends=('libarchive')
provides=('vuze')
conflicts=('vuze')
install=vuze.install
options=('strip')
source=("http://downloads.sourceforge.net/project/azureus/vuze/Vuze_4800/Vuze_4800_linux.tar.bz2"
        "http://www.sb-innovation.de/attachments/f41/13654d1351447338-vuze-extreme-mod-sb-innovation-4-8-0-1_b01-beta-vpem_4801-01.zip"
        "mono_icons.tar.xz")
noextract=("13405d1342998988-vuze-extreme-mod-sb-innovation-4-7-1-3_b03-beta-vpem_4713-03.zip" "mono_icons.tar.xz")
md5sums=('17759c2e09d961803b1e3ccc31ff2036'
         '6e300947c456684bfe2adcc7868e364e'
         '4a17a214f5dd35c3b2c50c85ace88534')

  # Install mono systray icon (change to "yes")
  _mono="no"

package() {
  install -dm755 "$pkgdir/usr/share"
  cp -rf vuze "$pkgdir/usr/share/$pkgname"

  [[ "$CARCH" == 'i686' ]] && cp -f vuze/swt/swt32.jar "$pkgdir/usr/share/$pkgname/swt.jar"
  [[ "$CARCH" == 'x86_64' ]] && cp -f vuze/swt/swt64.jar "$pkgdir/usr/share/$pkgname/swt.jar"

  install -Dm755 vuze/vuze "$pkgdir/usr/bin/$pkgname"
  install -Dm644 vuze/vuze.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 vuze/vuze.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 vuze/vuze.schemas "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas"

  sed -i 's|#PROGRAM_DIR=.*|PROGRAM_DIR="/usr/share/vuze-extreme-mod"|' "$pkgdir/usr/bin/$pkgname"
  sed -i 's|vuze|vuze-extreme-mod|' "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas"
  sed -e 's|Name=Vuze|Name=Vuze Extreme Mod|' \
      -e 's|Exec=vuze %f|Exec=vuze-extreme-mod %U|' \
      -e 's|Icon=vuze.png|Icon=vuze-extreme-mod.png|' \
      -i "$pkgdir/usr/share/applications/$pkgname.desktop"

  rm -rf "$pkgdir/usr/share/$pkgname"/{swt,azureus,vuze,vuze.{desktop,schemas}}

  bsdtar -xf $(basename ${source[1]}) -C "$pkgdir/usr/share/$pkgname"

  if [[ $_mono = "yes" ]]; then
    rm -rf tmp/
    mkdir tmp
    bsdtar -xf $(basename ${source[1]}) Azureus2.jar
    bsdtar -xf Azureus2.jar -C tmp
    bsdtar -xf mono_icons.tar.xz -C tmp/org/gudy/azureus2/ui/icons/
    cd tmp
    jar cf Azureus2.jar ./*/
    install -m644 Azureus2.jar "$pkgdir/usr/share/$pkgname"
  fi
}
