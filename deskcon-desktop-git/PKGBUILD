# Maintainer: Prurigro

_pkgname=deskcon-desktop
pkgname=${_pkgname}-git
pkgver=20140727.r66.2678041
pkgrel=2
pkgdesc="Integrates your Smartphone in your Desktop"
url="https://github.com/screenfreeze/deskcon-desktop"
license=('GPL3')
arch=('any')
depends=('python2-pyopenssl' 'python2-gobject')
makedepends=('git')
options=('!strip')
install=${_pkgname}.install

source=("git://github.com/screenfreeze/${_pkgname}.git"
        "deskcon.1")
sha512sums=('SKIP'
            'cf057defd83e945c9bbe1a0fdd07854e2e4cb0d66f1ade4885cab064e1b51694629a3b552bed3c27d2fd2d31b8b6fd571f5d820718dff27cf83a85ebd9624d1d')

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd $_pkgname
    [[ $(grep "3\.14" gnome-shell/deskcon@screenfreeze.net/metadata.json) ]] \
        || sed -i 's|"3\.12"|"3.12","3.14"|' gnome-shell/deskcon@screenfreeze.net/metadata.json
}

package() {
    install -Dm644 deskcon.1 "$pkgdir"/usr/share/man/man1/deskcon.1

    cd $_pkgname
    install -d "$pkgdir"/usr/share/gnome-shell/extensions
    cp -r --no-preserve=ownership gnome-shell/deskcon@screenfreeze.net "$pkgdir"/usr/share/gnome-shell/extensions/

    cd server
    for file in *; do
        [[ -f "$file" ]] && install -D "$file" "$pkgdir"/usr/share/deskcon-server/${file}
    done
    cp -r --no-preserve=ownership share "$pkgdir"/usr/share/deskcon-server/
    install -Dm755 bin/deskcon-server "$pkgdir"/usr/bin/deskcon-server
    install -Dm755 bin/deskcon-server-settings "$pkgdir"/usr/bin/deskcon-server-settings
    sed '$d' /usr/bin/deskcon-server > "$pkgdir"/usr/bin/deskcon-setupdevice
    echo './setupdevice.py' >> "$pkgdir"/usr/bin/deskcon-setupdevice
    chmod 755 "$pkgdir"/usr/bin/deskcon-setupdevice
}
