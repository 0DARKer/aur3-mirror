# Maintainer: Fernando Jiménez <fjim@sdfeu.org>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>
# Contributor: Maciej Piechotka

pkgname=cachefilesd
pkgver=0.10.5
pkgrel=3
pkgdesc="Userspace daemon acting as a backend for FS-Cache"
arch=('i686' 'x86_64')
url="http://people.redhat.com/~dhowells/fscache/"
license=('GPL')
depends=('glibc')
source=(http://people.redhat.com/~dhowells/fscache/${pkgname}-${pkgver}.tar.bz2)
backup=(etc/cachefilesd.conf)
md5sums=('9e85dd0ace346ff47e188ded8c05ab3b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make CFLAGS="${CFLAGS}" SBINDIR=/usr/bin
  sed -i -e 's@/sbin/cachefilesd@/usr/bin/cachefilesd@' cachefilesd.service
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" SBINDIR=/usr/bin install
  install -D -m 755 cachefilesd.service "${pkgdir}/usr/lib/systemd/system/cachefilesd.service"
  mkdir -p "${pkgdir}/var/cache/fscache"
}
