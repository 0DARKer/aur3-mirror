# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributer: Peter Sutton (foxxy) <foxxy@foxdogstudios.com>

pkgname=ngrok-git
pkgver=latest
pkgrel=3
pkgdesc='A tunneling, reverse proxy for developing and understanding networked, HTTP services (development version)'
url='http://github.com/inconshreveable/ngrok'
license=('MIT')
source=('git://github.com/inconshreveable/ngrok.git')
md5sums=('SKIP')
sha1sums=('SKIP')
sha256sums=('SKIP')
arch=('i686' 'x86_64')
depends=()
makedepends=('bzr' 'git' 'go' 'mercurial')
conflicts=('ngrok')
provides=('ngrok')

pkgver() {
  cd $srcdir/ngrok

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd $srcdir/ngrok

  # Build it!
  # Makefile is partially broken, so we can't build in parallel.
  make -j1 release-all
}

package() {
  cd $srcdir/ngrok

  # Install the program.
  install -Dm755 bin/ngrok  $pkgdir/usr/bin/ngrok
  install -Dm755 bin/ngrokd $pkgdir/usr/bin/ngrokd
}

# vim:set ts=2 sw=2 et:
