# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributer: Peter Sutton (foxxy) <foxxy@foxdogstudios.com>

pkgroot=ngrok
pkgname=ngrok-git
pkgver=latest
pkgrel=2
pkgdesc='A tunneling, reverse proxy for developing and understanding networked, HTTP services (development version)'
url='http://github.com/inconshreveable/ngrok'
license=('Apache')
source=('git+https://github.com/inconshreveable/ngrok.git')
sha1sums=('SKIP')
arch=('i686' 'x86_64')
depends=()
makedepends=('bzr' 'git' 'go' 'mercurial')
conflicts=('ngrok')
provides=('ngrok')

pkgver() {
  cd "${srcdir}/${pkgroot}"

  # Get the version number.
  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgroot}"

  # Build it!
  make release-all
}

package() {
  cd "${srcdir}/${pkgroot}"

  # Install the program.
  install -Dm755 bin/ngrok  "${pkgdir}/usr/bin/ngrok"
  install -Dm755 bin/ngrokd "${pkgdir}/usr/bin/ngrokd"
}

# vim: ft=sh ts=2 sw=2 et
