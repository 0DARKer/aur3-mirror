# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgname=perl-xls2csv
pkgver=1.06
pkgrel=2
pkgdesc="A script that recodes a spreadsheet's charset and saves as CSV."
arch=('any')
url="http://search.cpan.org/~ken/xls2csv-1.06/script/xls2csv"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-spreadsheet-parseexcel' 'perl-unicode-map' 'perl-text-csv-xs' 'perl-libintl-perl')
source=(http://search.cpan.org/CPAN/authors/id/K/KE/KEN/xls2csv-$pkgver.tar.gz)
md5sums=('0ed77bd4dd2f9d15fa0b8dc0ad0a0480')
sha256sums=('9d9653eea5577f9ed54203d08d90ae07266a6a10ae34a95288a2e97830ed4f1b')

build() {
  cd "${srcdir}/xls2csv-${pkgver}"

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/xls2csv-${pkgver}"
  make install DESTDIR="${pkgdir}/"

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}

# vim:set ts=2 sw=2 et:
