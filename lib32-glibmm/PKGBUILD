# Maintainer: Gicu Gorodenco <cyclopsihus 'at' gmail 'dot' com>
# Contributor: rafael ff1 <rafael.f.f1 'at' gmail 'dot' com> 
_pkgbasename=glibmm
pkgname=lib32-$_pkgbasename
pkgver=2.24.2
pkgrel=3
pkgdesc="Glib-- (glibmm) is a C++ interface for glib (32 bit, library only)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2>=2.24.1' 'libsigc++2.0>=2.2.5')
makedepends=('pkgconfig' 'gcc-multilib')
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/2.24/${_pkgbasename}-${pkgver}.tar.bz2)
options=('!libtool')
url="http://gtkmm.sourceforge.net/"
sha256sums=('d4687c0b27f9c9f955a5306649008a1f8d8935edff916278fb7b65b44292ff7a')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{include,lib32/giomm*,lib32/glibmm*,share}
  # Install license
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
