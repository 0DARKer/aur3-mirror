# Maintainer: Gicu Gorodenco <cyclopsihus 'at' gmail 'dot' com>
# Contributor: rafael ff1 <rafael.f.f1 'at' gmail 'dot' com> 
_pkgbasename=glibmm
pkgname=lib32-$_pkgbasename
pkgver=2.28.0
pkgrel=1
pkgdesc="Glib-- (glibmm) is a C++ interface for glib (32 bit, library only)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2' 'libsigc++2.0')
makedepends=('pkgconfig' 'gcc-multilib')
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.bz2)
options=('!libtool')
url="http://gtkmm.sourceforge.net/"
sha256sums=('b1b99868896b028354141e3b2470d98799283e90154550e12614f692db17a5a3')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{include,lib32/giomm*,lib32/glibmm*,share}
  # Install license
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
