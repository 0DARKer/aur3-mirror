# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: hb <hb.at.music-nerds.dot.net>

pkgname=scgraph
pkgver=0.13
pkgrel=1
pkgdesc="A 3D graphics server for SuperCollider."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/$pkgname/"
license=('GPL')
depends=('supercollider' 'qt' 'graphicsmagick')
makedepends=('boost')
install=("$pkgname.install")
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tgz"
        "$pkgname.patch"
        "GServer.sc"
        "GSynth.sc")
md5sums=('08ff7452929f520e8d5bf4690e58565b'
         '15230ada094fadb9f2d1439362e2df5a'
         '6bd4dd294d24b5fedaaadcf4c8553aa0'
         '2f86f0873f3ac915336c35691d308c29')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # build patches
  patch -p1 -i ../$pkgname.patch

  cp Makefile.include.template Makefile.include

  make PREFIX=/usr \
       QTDIR=/usr \
       HAVE_JACK=1 \
       HAVE_SHADERS=1 \
       HAVE_GLEW=1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="$pkgdir/usr" install

  # install util classes
  install -Dm644 ../GS{erver,ynth}.sc \
    "$pkgdir/usr/share/scgraph/sclang"
}
