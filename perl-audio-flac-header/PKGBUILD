# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-audio-flac-header
_pkgname=Audio-FLAC-Header
pkgver=2.4
pkgrel=4
pkgdesc="Audio::FLAC::Header - Access to FLAC audio metadata"
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl>=5.10.0')
url="http://search.cpan.org/dist/$_pkgname"
source=(http://search.cpan.org/CPAN/authors/id/D/DA/DANIEL/$_pkgname-$pkgver.tar.gz)
sha512sums=('53728279b79d3c320c63b66dff1e3e4d8bb4fd4bde164ad775a52d8b54b3d77861daa2d4e93d8e2c2ff2f1708ece8c4d2256d96b810266760bb837e557e3b8a9')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make install DESTDIR="$pkgdir/"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# vim:set ts=2 sw=2 et:
