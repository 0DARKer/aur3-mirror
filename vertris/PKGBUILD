# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Travis Fickett <tfickett@ufl.edu>

pkgname=vertris
pkgver=0.3.2
pkgrel=4
pkgdesc="A simple cross-platform Tetris clone"
arch=('i686' 'x86_64')
url="http://vertris.googlepages.com/"
license=('GPL')
depends=('allegro')
makedepends=('cmake')
source=(http://vertris.googlecode.com/files/$pkgname-$pkgver-src.zip)
md5sums=('012a9cc8836a1383c5eb23fc19ad8061')

_allegro="-L/usr/lib -Wl,--export-dynamic -lalleg-`allegro-config --version` -lalleg_unsharable"

build() {
  cd "$srcdir"
  # fix installation paths
  sed -i "s_images/_/usr/share/$pkgname/images/_" src/output.c
  sed -i "s_sounds/_/usr/share/$pkgname/sounds/_" src/output.c
  sed -i "s_hiscores.dat_/var/lib/$pkgname/hiscores.dat_" src/state.c
  sed -i "s_\"preferences\"_\"/var/lib/$pkgname/preferences\"_" src/state.c
  sed -i "s_vertris.sav_/var/lib/$pkgname/vertris.sav_" src/state.c
  # make
  cmake . || return 1
  sed -i "s|\`allegro-config --libs\`|$_allegro|" src/CMakeFiles/$pkgname.dir/link.txt
  make || return 1
  # install binary
  install -Dm755 dist/$pkgname "$pkgdir"/usr/bin/$pkgname
  # install images and sounds
  install -m755 -d "$pkgdir"/usr/share/$pkgname/{images,sounds}
  install -m644 -t "$pkgdir"/usr/share/$pkgname/images dist/images/*
  install -m644 -t "$pkgdir"/usr/share/$pkgname/sounds dist/sounds/*
  # install hiscores, preferences and saves
  install -m755 -d "$pkgdir"/var/lib/$pkgname
  install -m666 -t "$pkgdir"/var/lib/$pkgname dist/{hiscores.dat,preferences,vertris.sav}
  # install pixmap
  install -Dm644 dist/$pkgname.xpm "$pkgdir"/usr/share/pixmaps/$pkgname.xpm
}
