# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgbase=pyexiftool
pkgname=('python-pyexiftool' 'python2-pyexiftool')
pkgver=0.1.29
pkgrel=1
_githash=3db3764895e687d75b42d3ae4e554ca8664a7f6f
pkgdesc="Library to communicate with an instance of ExifTool command-line application"
url="https://github.com/smarnach/pyexiftool"
arch=('any')
license=('GPL3')
makedepends=('python' 'python2' 'python-sphinx')
source=(https://github.com/smarnach/${pkgbase}/archive/${_githash}.zip)
sha512sums=('59b161c8f984f2aa457b996ba4166bb7d0ce206c06d94a72dd8188954e095e068ea5776e1051c334b68baabdd78e71580786d72cf4eb7ac01fceb40516bdf2e8')

prepare() {
  cd ${pkgbase}-${_githash}
  sed -ri 's|(import exiftool)|import sys, os\nsys.path.append(os.getcwd())\n\1|' test/test_exiftool.py
}

check() {
  cd ${pkgbase}-${_githash}
  python test/test_exiftool.py
  python2 test/test_exiftool.py
}

build() {
  cd ${pkgbase}-${_githash}/doc
  make text man
}

package_python-pyexiftool() {
  depends=('python' 'perl-image-exiftool')

  cd ${pkgbase}-${_githash}
  python setup.py install -O1 --prefix=/usr --root="${pkgdir}"
  install -Dm 644 doc/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 doc/_build/man/${pkgbase}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-pyexiftool() {
  depends=('python2' 'perl-image-exiftool')

  cd ${pkgbase}-${_githash}
  python2 setup.py install -O1 --prefix=/usr --root="${pkgdir}"
  install -Dm 644 doc/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 doc/_build/man/${pkgbase}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
