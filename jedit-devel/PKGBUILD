# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

# PKGBUILD edit mode http://code.pi.co.ua/jedit-pkgbuild
_pkgbuildver=3

pkgname=jedit-devel
pkgver=4.4pre2
pkgrel=2
pkgdesc="Programmer's text editor"
arch=('i686' 'x86_64')
url="http://www.jedit.org"
license=('GPL2')
depends=('java-runtime')
makedepends=('sed')
provides=('jedit')
conflicts=('jedit' 'jedit-pkgbuild')
backup=(usr/share/jedit/modes/catalog)
source=(#http://downloads.sourceforge.net/jedit/jedit${pkgver}install.jar
		http://www.tellurianring.com/projects/jedit-daily/jEdit_pre_release/2011-04-05_00-11-23/jedit${pkgver}install.jar
		http://code.pi.co.ua/jedit-pkgbuild/downloads/pkgbuild-${_pkgbuildver}.xml
		jedit-bin jedit.desktop)
install=jedit.install
changelog=${pkgname}.ChangeLog.markdown

package() {
	mkdir -p $pkgdir/usr/share/java/jedit $pkgdir/usr/share/{jedit,pixmaps}

	cd $srcdir
	install -D -m 644 jedit.1 $pkgdir/usr/man/man1/jedit.1
	install -D jedit-bin $pkgdir/usr/bin/jedit
	install -D -m 644 jedit.desktop $pkgdir/usr/share/applications/jedit.desktop
	ln -s ../jedit/doc/jedit.png $pkgdir/usr/share/pixmaps/jedit.png

	cd installer
	tar xfj jedit-api.tar.bz2 -C $pkgdir/usr/share/jedit
	tar xfj jedit-macros.tar.bz2 -C $pkgdir/usr/share/jedit
	tar xfj jedit-program.tar.bz2 -C $pkgdir/usr/share/jedit
	mv $pkgdir/usr/share/jedit/jedit.jar $pkgdir/usr/share/java/jedit

	sed -e s:'</MODES>':'<MODE NAME="PKGBUILD"\t\tFILE="pkgbuild.xml"\n\t\t\t\tFILE_NAME_GLOB="PKGBUILD"/>\n\n</MODES>': \
			-i $pkgdir/usr/share/jedit/modes/catalog
	install -D -m 644 $srcdir/pkgbuild-${_pkgbuildver}.xml $pkgdir/usr/share/jedit/modes/pkgbuild.xml
}

md5sums=('9c808a8188b25f8bc2ac649e11e850ed'
         '3ebd666c3f75494521679777bd5228a7'
         '81b8a80a361bf296346bfd2a79acfbd3'
         '65caaa7807c3f2b864a4edcb50e3ceca')
sha256sums=('433a6a92146a031930a4f96fd7847435a187f8748e8d349dc66606da6ae106ab'
            '88ec2fff060de8e10a9c742a5ec62ea8fe36ba3e20c80378aeec659839608992'
            '216cfd7824a2835c1c6f509a497701ceab8bad356fb01db12c2bb6c0cac1c097'
            '023a04e170e9dae0ae3a78e2e06806d1b789bf4f0d5e2b71e9cca941354e34f0')
