# Maintainer:

pkgname=adchpp-lan-bzr
pkgver=672
pkgrel=1
pkgdesc="ADC protocol hub made by the people behind DC++ and modified for use at lan parties. (Bazaar Version)"
url="http://dc4lan.sourceforge.net/"
arch=('i686' 'x86_64')
license=("GPL")
depends=('ruby' 'python2')
makedepends=('scons' 'swig' 'bzr' 'asciidoc' 'gcc47')
provides=('adchpp-lan')
conflicts=('adchpp-lan')
backup=('etc/adchpp-lan/Script.xml'
        'etc/adchpp-lan/adchpp.xml')
source=('adchpp-lan.sh'
        'adchpp-lan.service'
        'adchpp-lan.tmpfile'
        'http://data.gpo.zugaina.org/klondike/net-p2p/adchpp-lan/files/adchpp-lan-2.8.0-fix_config_paths.patch'
        'http://data.gpo.zugaina.org/klondike/net-p2p/adchpp-lan/files/adchpp-lan-2.9.0-fix_cflags.patch'
        'https://dl.dropboxusercontent.com/u/6596386/adchpp-2.9.0-fix_store_files_in_config_dir_access.guard_plugin.patch'
        'https://dl.dropboxusercontent.com/u/6596386/adchpp-2.9.0-fix_log_path.patch'
        'gcc_4.8.diff::http://bazaar.launchpad.net/~dcplusplus-team/adchpp/trunk/diff/638'
        'bzr+lp:adchpp-lan')
md5sums=('7c2b8b71b78a80ddd7360fe05d684452'
         '86b91d9d4ba27fa748f95bf148c2ed73'
         'c4a100ba29ebb7591fa1a18b3114d1b7'
         '521d84fb6ef43b286a5767a20ba432f9'
         'b031f8c586bc86cb2976a15ceb0f72dc'
         '083d2ac163382023f2d5b26a7235735a'
         'c38fa8001edea9e58405f84cdd8f6988'
         'f78be5c64cfe62cf90fd59184d70b121'
         'SKIP')
install="adchpp-lan.install"
_bzrmod="adchpp-lan"

[ "$CARCH" = "i686" ] && _arch="x86"
[ "$CARCH" = "x86_64" ] && _arch="x64"

pkgver() {
  cd "${_bzrmod}"
  echo "$(bzr revno)"
}

prepare() {
  cd "${_bzrmod}"

  # Use python2
  rm -rf "${srcdir}/python"
  mkdir "${srcdir}/python"
  ln -s /usr/bin/python2 "${srcdir}/python/python"
  export PATH="${srcdir}"/python:$PATH

  # Patch
  patch -p1 -i "${srcdir}/adchpp-lan-2.8.0-fix_config_paths.patch"
  patch -p1 -i "${srcdir}/adchpp-lan-2.9.0-fix_cflags.patch"
  patch -p0 -i "${srcdir}/adchpp-2.9.0-fix_store_files_in_config_dir_access.guard_plugin.patch"
  patch -p0 -i "${srcdir}/adchpp-2.9.0-fix_log_path.patch"
  patch -p0 -i "${srcdir}/gcc_4.8.diff"
  sed -e "s|%%ADCHPPLIB%%|/usr/lib/${_bzrmod}|g" \
      -e "s|%%ADCHPPSHARE%%|/usr/share/${_bzrmod}|g" \
      -e "s|%%ADCHPPETC%%|/etc/${_bzrmod}|g" \
      -e "s|%%ADCHPPLOG%%|/var/log/${_bzrmod}|g" \
      -i etc/adchpp.xml -i etc/Script.xml -i rbutil/adchpp.rb -i pyutil/adchpp.py

  # Ugly patch: set path for lua scripts volatile files
  sed -e "/luadchpp.)/a-- set path of volatile files\nlocal varfilepath = \"/var/lib/${_bzrmod}/\"\n" \
      -e 's|adchpp.Util_getCfgPath()|varfilepath|g' \
      -i plugins/Script/examples/*lua

  # Dos2Unix
  find -type f -exec perl -pi -e 's/\r\n?/\n/g' "{}" \;
}

build() {
  cd "${_bzrmod}"

  # Build
  scons mode=release docs=yes ssl=yes langs=all tools=default arch="${_arch}"
}

package() {
  cd "${_bzrmod}"

  install -Dm755 "${srcdir}/adchpp-lan.sh" "${pkgdir}/usr/bin/adchpp-lan"
  install -Dm755 "build/release-default-${_arch}/bin/adchppd" "${pkgdir}/usr/lib/${_bzrmod}/adchpp"
  pushd "build/release-default-${_arch}/bin" &> /dev/null
  for i in $(find . -maxdepth 1 -type f -name "*.so"); do install -Dm755 "${i}" "${pkgdir}/usr/lib/${_bzrmod}/${i}"; done
  popd &> /dev/null
  install -Dm755 linux/generate_certs.sh "${pkgdir}/usr/share/${_bzrmod}/generate_certs.sh"

  install -d "${pkgdir}/etc/${_bzrmod}"
  install -dm750 -o126 -g126 "${pkgdir}/etc/${_bzrmod}/certs"
  install -dm750 -o126 -g126 "${pkgdir}/etc/${_bzrmod}/certs/trusted"
  install -d -o126 -g126 "${pkgdir}/var/log/${_bzrmod}"
  install -dm750 -o126 -g126 "${pkgdir}/var/lib/${_bzrmod}"

  install -Dm644 etc/adchpp.xml "${pkgdir}/etc/${_bzrmod}/adchpp.xml"
  install -Dm644 etc/Script.xml "${pkgdir}/etc/${_bzrmod}/Script.xml"

  pushd plugins/Script/examples &> /dev/null
  for i in $(find . -type f); do install -Dm644 -oroot -g126 "${i}" "${pkgdir}/usr/share/${_bzrmod}/scripts/${i}"; done
  popd &> /dev/null
  for i in $(find pyutil rbutil -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/${_bzrmod}/${i}"; done

  pushd build/docs &> /dev/null
  for i in $(find . -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/doc/${_bzrmod}/${i}"; done
  popd &> /dev/null

  install -Dm644 "${srcdir}/adchpp-lan.service" "${pkgdir}/usr/lib/systemd/system/adchpp-lan.service"
  install -Dm644 "${srcdir}/adchpp-lan.tmpfile" "${pkgdir}/usr/lib/tmpfiles.d/adchpp-lan.conf"
}