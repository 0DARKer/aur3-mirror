_pkgname=grilo
pkgname=$_pkgname-git
pkgver=0.2.5.30.gcccd542
pkgrel=1
pkgdesc="Framework that provides access to various sources of multimedia content"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'libxml2' 'libsoup')
makedepends=('gobject-introspection' 'gtk-doc' 'vala')
optdepends=('grilo-plugins-git: Plugins for grilo')
options=('!libtool' '!emptydirs')
provides=($_pkgname)
conflicts=($_pkgname)
source=(git://git.gnome.org/$_pkgname)
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe | sed 's/grilo-//' | sed 's|-|.|g'
}

build() {
  cd $_pkgname
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --disable-debug \
    --libdir=/usr/lib
  make
}

package() {
  cd $_pkgname
  make DESTDIR="${pkgdir}" install
}
