# Maintainer: Daniel Albers <daniel@lbe.rs>
# bacula-fd.service by Christian Hesse <arch@eworm.de>

pkgbase=bacula
pkgname='bacula-bat'
pkgver=7.0.2
pkgrel=1
arch=(i686 x86_64)
pkgdesc='Bacula - A Network Backup Tool'
url="http://www.bacula.org"
license=('AGPL3')
depends=('qt4')
makedepends=('tcp_wrappers')
source=("http://downloads.sourceforge.net/sourceforge/${pkgbase}/${pkgbase}-${pkgver}.tar.gz"
        '00-qmake4.patch'
        'bacula-fd.service')

md5sums=('5c85acb539118df0baded1d004d532a4'
         '9ac88867a9ad3a4fe0486a26d0cdb542'
         '230754737aefdfb57973457acd3f0697')
sha1sums=('a9b4a6a0664f2e98e5c487714686c263ce050e56'
          '58a60e8af9b4735c564c7223c2bf0c25803927f3'
          'e1f038e7feca1042858ae9b4d27b84b36ae96aab')

_instdir="$PWD/install"

build() {
    pushd "${srcdir}/${pkgbase}-${pkgver}"

    patch -Np3 -i ${srcdir}/00-qmake4.patch || true

    ./configure                              \
    --enable-bat                             \
    --enable-client-only                     \
    --enable-smartalloc                      \
    --prefix=/usr                            \
    --sbindir=/usr/bin                       \
    --sysconfdir=/etc/${pkgbase}             \
    --with-openssl                           \
    --with-scriptdir=/etc/${pkgbase}/scripts     \
    --with-systemd=/usr/lib/systemd/system/  \
    --with-tcp-wrappers                      \
    --with-x
    
    make DESTDIR="$_instdir" install
}

package_bacula-bat() {
  pkgdesc='Bacula - A Network Backup Tool (management GUI)'
  backup=('etc/bacula/bat.conf')
  depends=('bacula-common')

  install -D ${_instdir}/usr/bin/bat ${pkgdir}/usr/bin/bat
  install -D ${_instdir}/etc/bacula/bat.conf ${pkgdir}/etc/bacula/bat.conf
  install -D ${_instdir}/usr/share/man/man1/bat.1.gz ${pkgdir}/usr/share/man/man1/bat.1.gz

  install -D ${srcdir}/${pkgbase}-${pkgver}/scripts/bat.desktop ${pkgdir}/usr/share/applications/bat.desktop
  install -D ${srcdir}/${pkgbase}-${pkgver}/src/qt-console/images/bat_icon.png ${pkgdir}/usr/share/pixmaps/bat_icon.png
}

