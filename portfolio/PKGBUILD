# Maintainer: Hannes Gr√§uler <hgraeule [at] uos [dot] de>
pkgname=portfolio
pkgver=0.12.1
pkgrel=1
pkgdesc="Track your portfolio performance (finance)"
arch=('i686' 'x86_64')
url="http://buchen.github.io/portfolio/"
license=('GPL3')
depends=('jre7-openjdk')
makedepends=('maven')

_DEST="/usr/share/portfolio"

[ "$CARCH" = "i686" ]   && _platform="x86"
[ "$CARCH" = "x86_64" ] && _platform="x86_64"

source=("https://codeload.github.com/buchen/portfolio/tar.gz/0.12.1")
sha1sums=("2c3ebec4ab53540405c518b4580679a9f2e6f8d3")

build() {
    export MAVEN_OPTS="-Xmx1g -XX:MaxPermSize=500m"
    export JAVA_HOME=/usr/lib/jvm/java-7-openjdk
    cd $pkgname-$pkgver/portfolio-app
    mvn clean install -Dgenerate-target-platform=true
}

package() {
    cd $pkgname-$pkgver/portfolio-product/target/products/name.abuchen.portfolio.product/linux/gtk/$_platform/portfolio/

    install -dm755 ${pkgdir}${_DEST}
    cp -r * ${pkgdir}${_DEST}
    chmod a+x ${pkgdir}${_DEST}/PortfolioPerformance

    msg2 "Symlink /usr/bin/portfolio -> ${_DEST}/PortfolioPerformance"
    install -dm755 "${pkgdir}/usr/bin"
    ln -s "${_DEST}/PortfolioPerformance" "${pkgdir}/usr/bin/portfolio"
}

