# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Faheem Pervez <trippin1@gmail.com>

pkgname=kde-thumbnailer-epub
pkgver=0.2
pkgrel=1
pkgdesc="Thumbnailer to show ePub cover previews in KDE file managers (Dolphin and Konqueror)"
arch=(i686 x86_64)
url=http://kde-apps.org/content/show.php/KDE+ePub+Thumbnailer?content=151210
license=('GPL')
depends=(ebook-tools kdebase-workspace)
makedepends=(automoc4 cmake kdelibs)
source=(http://kde-apps.org/CONTENT/content-files/151210-$pkgname-$pkgver.tar.gz)
sha256sums=('cbbe628efe8d358b72006f2157c170a268c917d75076c4123f7ad6c8adb1967c')
sha512sums=('67f6bef042c70864cf2c94a11cc330622acc10b015a437b3c3dea14c14666fcbc557ca3bbd8a2af3ee21ba598c0d0fdb8c9ac05aacd580a6a4cd822e017cadb3')

build() {
    install -d build/
    cd build/
    cmake ../$pkgname-$pkgver/ \
        -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
        -DCMAKE_BUILD_TYPE=Release \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4
    make
}

package() {
    make -C build DESTDIR="$pkgdir" install
}
