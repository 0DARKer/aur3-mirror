# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=amule-daemon
pkgver=10834
pkgrel=2
pkgdesc='An eMule-like client for the eD2k and Kademlia p2p networks. (Only Daemon, CLI tools and Webserver)'
url='http://www.amule.org'
arch=('i686' 'x86_64')
license=('GPL')
depends=('wxgtk' 'crypto++' 'libupnp')
provides=('amule')
conflicts=('amule')
optdepends=('kamule: AmuleGUI for KDE')
source=("http://amule.sourceforge.net/tarballs/aMule-SVN-r${pkgver}.tar.bz2"
        'amuled.service'
        'amuled@.service'
        'amuleweb.service')
sha1sums=('9caa724b99be609d6300659afb971656330302e6'
          '3a2b451c3a35ab1745dd88726fe1454bc1227542'
          '4c4638e39f01c782e0e32d950fd29b4a6f4a398a'
          '128049ba8dce9dedc16357b7027736d99d583761')
install="amule-daemon.install"
options=('!emptydirs')

prepare() {
  rm -fr build
  cp -R -p "aMule-SVN-r${pkgver}" build
}


build() {
cd build

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --disable-monolithic \
              --enable-alcc \
              --disable-debug \
              --enable-upnp \
              --enable-geoip \
              --enable-optimize \
              --enable-webserver \
              --enable-amule-daemon \
              --enable-fileview \
              --enable-ccache \
              --enable-amulecmd
   make
}

package() {
cd build

  make DESTDIR="${pkgdir}" install

  install -Dm755 src/utils/fileview/mulefileview "${pkgdir}/usr/bin/mulefileview"
  install -Dm755 src/utils/scripts/mldonkey_importer.pl "${pkgdir}/usr/share/amule/mldonkey_importer.pl"
  install -Dm755 src/utils/scripts/kadnodescreate.pl "${pkgdir}/usr/share/amule/kadnodescreate.pl"

  install -Dm644 ../amuled.service "${pkgdir}/usr/lib/systemd/system/amuled.service"
  install -Dm644 ../amuled@.service "${pkgdir}/usr/lib/systemd/system/amuled@.service"
  install -Dm644 ../amuleweb.service "${pkgdir}/usr/lib/systemd/system/amuleweb.service"
}
