# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=amule-daemon
pkgver=10845
pkgrel=1
pkgdesc='An eMule-like client for the eD2k and Kademlia p2p networks. (Only Daemon, CLI tools and Webserver)'
url='http://www.amule.org'
arch=('i686' 'x86_64')
license=('GPL')
depends=('wxgtk' 'crypto++' 'libupnp')
provides=('amule')
conflicts=('amule')
optdepends=('kamule: AmuleGUI for KDE')
source=("http://amule.sourceforge.net/tarballs/aMule-SVN-r${pkgver}.tar.bz2"
        'amuled.service'
        'amuled@.service'
        'amuleweb.service')
sha1sums=('193b63c4787338b7d16088f77cc8f3197184f11a'
          '3a2b451c3a35ab1745dd88726fe1454bc1227542'
          '4c4638e39f01c782e0e32d950fd29b4a6f4a398a'
          '128049ba8dce9dedc16357b7027736d99d583761')
install="amule-daemon.install"

prepare() {
  rm -fr build
  cp -Rp "aMule-SVN-r${pkgver}" build
}


build() {
  cd build
  ./configure --prefix=/usr --disable-{monolithic,debug} --enable-{alcc,amule-daemon,amulecmd,ccache,fileview,geoip,optimize,upnp,webserver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  install -dm750 "${pkgdir}/var/lib/amule"

  install -Dm755 src/utils/fileview/mulefileview "${pkgdir}/usr/bin/mulefileview"
  install -Dm755 src/utils/scripts/mldonkey_importer.pl "${pkgdir}/usr/share/amule/mldonkey_importer.pl"
  install -Dm755 src/utils/scripts/kadnodescreate.pl "${pkgdir}/usr/share/amule/kadnodescreate.pl"

  install -Dm644 ../amuled.service "${pkgdir}/usr/lib/systemd/system/amuled.service"
  install -Dm644 ../amuled@.service "${pkgdir}/usr/lib/systemd/system/amuled@.service"
  install -Dm644 ../amuleweb.service "${pkgdir}/usr/lib/systemd/system/amuleweb.service"

  rm -fr "${pkgdir}/usr/share/"{pixmaps,applications}
}
