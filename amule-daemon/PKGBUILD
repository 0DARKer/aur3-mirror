# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=amule-daemon
pkgver=10803
pkgrel=1
pkgdesc='An eMule-like client for the eD2k and Kademlia p2p networks (Daemon Function)'
url='http://www.amule.org'
arch=('i686' 'x86_64')
license=('GPL')
depends=('wxgtk2.9' 'crypto++' 'libupnp')
makedepends=('ccache')
provides=('amule')
conflicts=('amule')
optdepends=('kamule: AmuleGUI for KDE')
source=('amuled.service' 'amuled@.service'
        "http://amule.sourceforge.net/tarballs/aMule-SVN-r${pkgver}.tar.bz2")
md5sums=('dbb6c6fc6849ae0ca8ce08fd4e52ff4a'
         '253895ed04947fa95ca8fcaee9aeee10'
         'febf14f5b1185f25acde64f61b78ed38')
options=('!emptydirs' 'ccache')

build() {

  cd "${srcdir}"
  rm -fr build
  mkdir build
  cd build

  ../aMule-SVN-r${pkgver}/configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-monolithic \
      --enable-alcc \
      --disable-debug \
      --enable-upnp \
      --enable-geoip \
      --enable-optimize \
      --enable-webserver \
      --enable-amule-daemon \
      --enable-fileview \
      --enable-amulecmd \
      --enable-ccache \
      --with-wxdir=/usr/include/wx-2.9 \
      --with-wx-config=/usr/bin/wx-config-2.9
    make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/amuled.service" "${pkgdir}/usr/lib/systemd/system/amuled.service"
  install -D -m644 "${srcdir}/amuled@.service" "${pkgdir}/usr/lib/systemd/system/amuled@.service"
}

