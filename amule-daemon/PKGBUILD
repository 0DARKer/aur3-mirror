# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=amule-daemon
pkgver=10829
pkgrel=1
pkgdesc='An eMule-like client for the eD2k and Kademlia p2p networks (Daemon Function)'
url='http://www.amule.org'
arch=('i686' 'x86_64')
license=('GPL')
depends=('wxgtk3.0' 'crypto++' 'libupnp')
makedepends=('ccache')
provides=('amule')
conflicts=('amule')
optdepends=('kamule: AmuleGUI for KDE')
source=("http://amule.sourceforge.net/tarballs/aMule-SVN-r${pkgver}.tar.bz2"
        'amuled.service'
        'amuled@.service'
        'amuled.tmpfile')
sha1sums=('7f0ab5a8bc5ec13695080858c559c486d1bc2f63'
          '30dec63345793e904976f00ecc71da18f9b54f15'
          'db7e031c6af272ab01119e1330c4affc40d79c9f'
          'c1d0f14042d93d57f2f62906d476a3b3933dac82')
options=('ccache')
install="amule-daemon.install"

build() {

  cd "${srcdir}"
  rm -fr build
  mkdir build
  cd build

  ../aMule-SVN-r${pkgver}/configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-monolithic \
      --enable-alcc \
      --disable-debug \
      --enable-upnp \
      --enable-geoip \
      --enable-optimize \
      --enable-webserver \
      --enable-amule-daemon \
      --enable-fileview \
      --enable-amulecmd \
      --enable-ccache \
      --with-wxdir=/usr/include/wx-3.0 \
      --with-wx-config=/usr/bin/wx-config-3.0
    make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/amuled.service" "${pkgdir}/usr/lib/systemd/system/amuled.service"
  install -Dm644 "${srcdir}/amuled@.service" "${pkgdir}/usr/lib/systemd/system/amuled@.service"
  install -Dm644 "${srcdir}/amuled.tmpfile" "${pkgdir}/usr/lib/tmpfiles.d/amuled.conf"

  install -dm750 -o129 -g129 "${pkgdir}/var/lib/amuled"
  rm -fr "${pkgdir}/usr/share"/{pixmaps,applications}
}

