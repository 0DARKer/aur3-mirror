# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=arachne
pkgver=1.1.1
pkgrel=1
pkgdesc="Resize your images without distorting the shape of the things in the image"
arch=('i686' 'x86_64')
url="http://seam-carver.sourceforge.net/"
license=('GPL')
depends=('gdk-pixbuf' 'gtk2' 'desktop-file-utils')
install=$pkgname.install
source=(http://downloads.sourceforge.net/seam-carver/$pkgname-$pkgver.tar.gz
        $pkgname.desktop)
md5sums=('e96447ded3d5fef6f7142e89a798f6dc'
         '2569ac78f59c3494e1adba47122b7ad4')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

# .desktop file
  install -D -m644 "${srcdir}"/$pkgname.desktop \
    "${pkgdir}"/usr/share/applications/$pkgname.desktop

# icons
  install -D -m644 "${srcdir}"/$pkgname-$pkgver/icons/${pkgname}16.png \
    "${pkgdir}"/usr/share/icons/hicolor/16x16/apps/$pkgname.png
  install -D -m644 "${srcdir}"/$pkgname-$pkgver/icons/${pkgname}24.png \
    "${pkgdir}"/usr/share/icons/hicolor/24x24/apps/$pkgname.png
  install -D -m644 "${srcdir}"/$pkgname-$pkgver/icons/${pkgname}32.png \
    "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/$pkgname.png
  install -D -m644 "${srcdir}"/$pkgname-$pkgver/icons/${pkgname}48.png \
    "${pkgdir}"/usr/share/icons/hicolor/48x48/apps/$pkgname.png
}
