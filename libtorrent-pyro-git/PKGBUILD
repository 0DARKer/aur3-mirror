# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

# Compile a debug build?
_debug='n'


# Compile from a specific commit?
#_commit=31e57dd  #v0.13.2
#_commit=4202ad3  #v0.13.3
_commit=HEAD

# Add the following to your rtorrent.rc if your
# filesystem supports the fallocate() function
# system.file.allocate.set = yes
[[ $(stat -Lfc %T /home) = @(ext4|xfs|btrfs) ]] &&
    _falloc='--with-posix-fallocate'

pkgname=libtorrent-pyro-git
pkgver=20130403
pkgrel=1
pkgdesc='BitTorrent library written in C++ (git version)'
url='http://libtorrent.rakshasa.no'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libsigc++' 'openssl')
makedepends=('git' 'cppunit')
options=(!libtool)
conflicts=('libtorrent' 'libtorrent-git')
provides=('libtorrent')
install=libtorrent-pyro.install

[[ $_debug = 'n' ]] &&
    _debug='--disable-debug' || options=(!strip)

source=("git://github.com/rakshasa/libtorrent.git#commit=$_commit"
        "automake.patch")
md5sums=('SKIP'
         'f8fe85f6fdc2a2a9d14b93cc23fa8ce4')

pkgver() {
    cd "$srcdir/libtorrent"
    git log -1 --format="%cd" --date=short | tr -d '-'
}

prepare() {
    cd "$srcdir/libtorrent"
    patch -Np1 -i "${srcdir}/automake.patch"
    #patch -Np1 -i "${startdir}/libtorrent.patch"
    ./autogen.sh
}

build() {
    cd "$srcdir/libtorrent"
    export CXXFLAGS+=" -std=c++11"

    ./configure $_falloc $_debug \
        --prefix=/usr \
        --disable-dependency-tracking

    make
}

package() {
    make -C "$srcdir/libtorrent" DESTDIR="$pkgdir" install
}
