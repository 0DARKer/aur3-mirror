# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

# This is the v0.13.2 tag if you wish to use whats in
# the official tarball instead of git head
#_gitrev='4202ad3058a23c25ebba9c988b9bfc5389fb897f'

# Compile in a debug build?
_debug='n'

# Add the following to your rtorrent.rc if your
# filesystem supports the fallocate() function
# system.file.allocate.set = yes
[[ $(stat -Lfc %T /home) = @(ext4|xfs|btrfs) ]] &&
_falloc='--with-posix-fallocate'

pkgname=libtorrent-pyro-git
_pkgname='libtorrent'
pkgver=20121024
pkgrel=1
pkgdesc='BitTorrent library written in C++ (git version)'
url='http://libtorrent.rakshasa.no'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libsigc++' 'openssl')
makedepends=('git' 'cppunit')
options=('!libtool' '!strip')
conflicts=('libtorrent' 'libtorrent-git')
provides=('libtorrent')
install=libtorrent-pyro.install

_gitname="${_pkgname}"
_gitroot="git://github.com/rakshasa/${_pkgname}.git"
[[ ! $_gitrev ]] && _gitopts='--depth 1'
[[ $_debug = 'n' ]] && _debug='--disable-debug'

build() {
    cd "$srcdir"

    msg "Connecting to GIT server..."
    if [ -d "${_gitname}/.git" ]; then
        cd "$_gitname" && git pull origin
        msg "The local repository was updated."
    else
        git clone $_gitopts $_gitroot $_gitname
        cd "$_gitname"
        [[ $_gitrev ]] && git checkout $_gitrev
        msg "The remote repository was cloned."
    fi

    msg "Starting build.."
    export CXXFLAGS+=" -fno-strict-aliasing -std=c++11"

    #temporary fix
    sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' configure.ac

    ./autogen.sh
    ./configure $_falloc $_debug \
        --prefix=/usr \
        --disable-dependency-tracking

    make
}

package() {
    make -C "$srcdir/$_pkgname" DESTDIR="$pkgdir" install
}
