# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

# Compile a debug build?
_debug='n'

# Add the following to your rtorrent.rc if your
# filesystem supports the fallocate() function
# system.file.allocate.set = yes
[[ $(stat -Lfc %T /home) = @(ext4|xfs|btrfs) ]] &&
_falloc='--with-posix-fallocate'

pkgname=libtorrent-pyro-git
pkgver=20130403
pkgrel=1
pkgdesc='BitTorrent library written in C++ (git version)'
url='http://libtorrent.rakshasa.no'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libsigc++' 'openssl' 'pacman>=4.1')
makedepends=('git' 'cppunit')
options=(!libtool)
conflicts=('libtorrent' 'libtorrent-git')
provides=('libtorrent')
install=libtorrent-pyro.install

[[ $_debug = 'n' ]] &&
    _debug='--disable-debug' || options=(!strip)

_gitname=libtorrent
source=("git://github.com/rakshasa/${_gitname}.git")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    git log -1 --format="%cd" --date=short | tr -d '-'
}

build() {
    cd "$srcdir/$_gitname"

    export CXXFLAGS+=" -fno-strict-aliasing -std=c++11"
    ./autogen.sh
    ./configure $_falloc $_debug \
        --prefix=/usr \
        --disable-dependency-tracking

    make
}

package() {
    make -C "$srcdir/$_gitname" DESTDIR="$pkgdir" install
}
