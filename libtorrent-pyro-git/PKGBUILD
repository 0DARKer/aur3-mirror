# Maintainer: Skydrome <irc.freenode.net>

## This is the v0.13.2 tag if you wish to use whats in the official tarball instead of git head
#_gitrev='31e57dd1a838a69112c7ba2a20e8689769431fd3'
_use_stable_rev='n'

## Compile in debugging symbols cause this is a git version and you are most likely looking for bugs
#_debug="--disable-debug"
_debug="--enable-debug"

## Add the following to your rtorrent.rc if you system supports fallocate()
# system.file.allocate.set = yes
[[ $(stat -Lfc %T /home) = @(ext4|xfs|btrfs) ]] &&
_fsfa='--with-posix-fallocate'

pkgname=libtorrent-pyro-git
_pkgname='libtorrent'
pkgver=20120702
pkgrel=1
pkgdesc='BitTorrent library written in C++ (git version)'
url='http://libtorrent.rakshasa.no'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libsigc++' 'openssl')
makedepends=('git' 'gcc>=4.7.0' 'cppunit')
options=('!libtool' '!strip')
conflicts=("$_pkgname")
provides=("$_pkgname")
install=libtorrent-pyro.install

_gitname="${_pkgname}"
_gitroot="git://github.com/rakshasa/${_pkgname}.git"
[[ $_use_stable_rev = 'n' ]] && _gitoptions="--depth 1"

build() {
    cd $srcdir

    msg "Connecting to GIT server..."
    if [ -d "${_gitname}/.git" ]; then
        cd $_gitname && git pull origin
        msg "The local repository was updated."
    else
        git clone ${_gitoptions} ${_gitroot} ${_gitname}
        cd $_gitname
        [[ $_use_stable_rev = 'y' ]] && git checkout $_gitrev
        msg "The remote repository was cloned."
    fi

    msg "Starting build.."
    #export CFLAGS="-march=native -O3 -pipe -fstack-protector-all -fno-strict-overflow -fno-strict-aliasing -D_FORTIFY_SOURCE=2 --param=ssp-buffer-size=4 -Wall"
    #export CXXFLAGS="$CFLAGS -std=c++11"
    #export LDFLAGS="-Wl,-O1,--sort-common,--as-needed,-z,relro,-z,now"
    export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing -std=c++11"

    ./autogen.sh
    ./configure --prefix=/usr $_fsfa $_debug
    make
}

package() {
    cd ${srcdir}/${_pkgname}
    make DESTDIR="${pkgdir}" install
}
