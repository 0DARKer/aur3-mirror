# Maintainer: Skydrome <irc.freenode.net>

# this is release of 0.13.2 if you wish to use whats in the official tarball instead of git head
_use_stable_rev='n'
_gitrev='31e57dd1a838a69112c7ba2a20e8689769431fd3'

pkgname=libtorrent-pyro-git
_pkgname='libtorrent'
pkgver=20120614
pkgrel=1
pkgdesc='BitTorrent library written in C++ (git version)'
url='http://libtorrent.rakshasa.no'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libsigc++' 'openssl')
makedepends=('git' 'gcc>=4.7.0' 'cppunit')
options=('!libtool')
conflicts=("${_pkgname}")
provides=("${_pkgname}")

_gitname="${_pkgname}"
_gitroot="git://github.com/rakshasa/${_pkgname}.git"
[[ $_use_stable_rev = 'n' ]] && _gitoptions="--depth 1"

build() {
    cd $srcdir

    msg "Connecting to GIT server..."
    if [ -d "${_gitname}/.git" ]; then
        cd $_gitname && git pull origin
        msg "The local repository was updated."
    else
        git clone ${_gitoptions} ${_gitroot} ${_gitname}
        cd $_gitname
        [[ $_use_stable_rev = 'y' ]] && git checkout $_gitrev
        msg "The remote repository was cloned."
    fi

    msg "Starting build.."
    export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing -std=c++11"
    # Add the following to your rtorrent.rc if you system supports fallocate()
    # system.file.allocate.set = yes
    [[ $(stat -Lfc %T /home) = @(ext4|xfs|btrfs) ]] && _fsfa='--with-posix-fallocate'
    ./autogen.sh
    # compile in debugging symbols cause this is a git version and you are most likely looking for bugs
    ./configure --prefix=/usr --enable-debug $_fsfa
    make
}

package() {
    cd ${srcdir}/${_pkgname}
    make DESTDIR="${pkgdir}" install
}
