pkgname=libwebcam
pkgver=0.2.1
pkgrel=1
pkgdesc="Webcam Library"
arch=('i686' 'x86_64')
url="http://packages.debian.org/sid/libwebcam0"
license=('GPL3')
depends=('libxml2')
makedepends=('cmake')
source=("http://ftp.de.debian.org/debian/pool/main/libw/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
        'cmake.patch')
md5sums=('685452604b4f246cda718d40bbe04de7'
         '3f3aaec68a1aff85c2203ecca4b42a3a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e "s:/etc/udev/data:/usr/share/uvcdynctrl/data:g" uvcdynctrl/udev/scripts/uvcdynctrl

  patch -Np1 -i $srcdir/cmake.patch
  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_SKIP_RPATH=ON \
           -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR=$pkgdir install

  install -d "$pkgdir"/lib/udev/
  install -d "${pkgdir}"/usr/share/uvcdynctrl
  mv "${pkgdir}"/etc/udev/rules.d "${pkgdir}/lib/udev/"
  mv "${pkgdir}"/etc/udev/data "${pkgdir}/usr/share/uvcdynctrl"
  rm -rf "${pkgdir}"/etc

  # Makefile doesn't install includes by default
  install -Dm644 ../common/include/webcam.h "$pkgdir"/usr/include/webcam.h
  install -Dm644 ../common/include/dynctrl-logitech.h "$pkgdir"/usr/include/dynctrl-logitech.h
}
