# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributer: Serge Ziryukin <ftrvxmtrx@gmail.com>

pkgname=split2flac-git
_pkgname=split2flac
pkgver=0.108.fe1f5a2
pkgrel=1
pkgdesc="Split flac/ape/wv/wav + cue sheet into separate tracks."
arch=('any')
url=("https://github.com/ftrvxmtrx/split2flac")
license=('MIT')
depends=('cuetools' 'shntool' 'flac')
makedepends=('git')
provides=('split2flac')
optdepends=('mac:          to split APE format'
'wavpack:      to split WV format'
'flake:        faster FLAC encoding'
'lame:         to split into mp3'
'vorbis-tools: to split into ogg vorbis'
'faac:         to split into m4a'
'libmp4v2:     to set tags in m4a'
'mutagen:      to set tags in mp3 (with unicode support)'
'id3lib:       to set tags in mp3 (unicode not supported)'
'mp3gain:      mp3 volume normalization'
'aacgain:      m4a volume normalization'
'vorbisgain:   ogg vorbis volume normalization'
'imagemagick:  to convert/resize album cover images'
'enca:         to automatically detect cue sheet charset')
source=("git://github.com/ftrvxmtrx/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
	cd "$_pkgname"
	echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

package () {
	cd "$_pkgname"
	install -Dm755 split2flac "${pkgdir}/usr/bin/split2flac"
	cd "$pkgdir/usr/bin"
	ln -s split2flac split2mp3
	ln -s split2flac split2ogg
	ln -s split2flac split2m4a
	ln -s split2flac split2wav
}
