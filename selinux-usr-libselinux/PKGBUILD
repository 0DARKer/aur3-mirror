# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)
# Contributor: Angelux (xangelux) (xangelux <at> gmail <dot> com)

pkgname=selinux-usr-libselinux
_origname=libselinux
_release=20120924
pkgver=2.1.12
pkgrel=2
pkgdesc="SELinux userspace (libselinux including python bindings)"
arch=('i686' 'x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
groups=('selinux' 'selinux-userspace')
depends=('selinux-usr-libsepol>=2.1.0' 'glibc' 'python2' 'pcre')
makedepends=('swig')
conflicts=('selinux-usr-libselinux-python')
options=(!emptydirs)
source=(http://userspace.selinuxproject.org/releases/${_release}/${_origname}-${pkgver}.tar.gz)
sha256sums=('8dad879380e0ce1e4ab67195a08f6052c1396493bcb12fe92a033f49f7dbca9e')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

	CFLAGS="${CFLAGS} -lpcre"

  # Set python version
  sed -i -e "s/ python/ python2/" src/Makefile
  sed -i -e "s/PYLIBDIR ?= \$(LIBDIR)\/\$(PYLIBVER)/PYLIBDIR ?=\$(LIBDIR)\/python2.7/g" "src/Makefile"

  # /lib -> /usr/lib
  sed -i -e "s/SHLIBDIR ?= \$(DESTDIR)\/lib/SHLIBDIR ?= \$(PREFIX)\/lib/" src/Makefile
  make 

  # Build python wrapper, which is needed for semanage tool
  make pywrap
}

package(){
  cd "${srcdir}/${_origname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-pywrap

  # /lib -> /usr/lib
	rm "${pkgdir}/usr/lib/libselinux.so"
	cd "${pkgdir}/usr/lib/"
	ln -s libselinux.so.1 libselinux.so
}
