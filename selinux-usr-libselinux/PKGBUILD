# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)

pkgname=selinux-usr-libselinux
_origname=libselinux
_release=20120216
pkgver=2.1.9
pkgrel=1
pkgdesc="SELinux userspace (libselinux including python bindings)"
arch=('i686' 'x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
groups=('selinux' 'selinux-userspace')
depends=('selinux-usr-libsepol>=2.1.0' 'glibc' 'python2')
makedepends=('swig')
conflicts=('selinux-usr-libselinux-python')
options=(!emptydirs)
source=(http://userspace.selinuxproject.org/releases/${_release}/${_origname}-${pkgver}.tar.gz)
sha256sums=('749d4b39c80aa9df8247b8b3187ab72442c0dbad6e70bf312e25052bd4e7063f')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  # Set python version
  sed -i -e "s/ python/ python2/" "src/Makefile"

  make

  # Build python wrapper, which is needed for semanage tool
  make pywrap
}

package(){
  cd "${srcdir}/${_origname}-${pkgver}"

  make DESTDIR="${pkgdir}" LIBDIR="${pkgdir}/usr/lib" SHLIBDIR="${pkgdir}/usr/lib" install
  make DESTDIR="${pkgdir}" LIBDIR="${pkgdir}/usr/lib" SHLIBDIR="${pkgdir}/usr/lib" install-pywrap

	# /lib -> /usr/lib
	rm "${pkgdir}/usr/lib/libselinux.so"
	cd "${pkgdir}/usr/lib/"
	ln -s libselinux.so.1 libselinux.so
}
