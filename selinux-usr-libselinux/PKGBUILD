# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)

pkgname=selinux-usr-libselinux
_origname=libselinux
_release=20101221
pkgver=2.0.98
pkgrel=1
pkgdesc="SELinux userspace (libselinux including python bindings)"
arch=('i686' 'x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
groups=('selinux' 'selinux-userspace')
depends=('selinux-usr-libsepol>=2.0.41' 'glibc' 'python2')
conflicts=('selinux-usr-libselinux-python')
options=(!emptydirs)
source=(http://userspace.selinuxproject.org/releases/${_release}/devel/${_origname}-${pkgver}.tar.gz)
md5sums=('f0990732590a9f6e83596f290aac9093')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  # Meant for python2
  sed -i -e "s/shell python -c/shell python2 -c/" "src/Makefile"
  make 
  # Build python wrapper, which is needed for semanage tool
  make pywrap
}

package(){
  cd "${srcdir}/${_origname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-pywrap
}
