# Maintainer: Nikolay Amiantov <nikoamia@gmail.com>
# Contributor: Andre Wayand <aur-davical@awayand.sleepmail.com>
# Contributor: <zenlord@gmail.com>
# Contributor: Michael Seiwald <michael@mseiwald.at>
pkgname=davical
pkgver=1.1.1
pkgrel=5
pkgdesc="CalDAV CardDAV Server"
arch=('any')
url="http://davical.org/"
license=('GPL')
options=('!strip')
depends=('php>=5.1' 'awl>=0.53' 'php-pgsql' 'perl-dbd-pg' 'perl-yaml' 'perl-dbi')
optdepends=(
	'postgresql: run postgresql locally'
	'apache: Web server to run davical'
	'nginx: Web server to run davical'
)
backup=(
	"etc/httpd/conf/extra/httpd-davical.conf"
	"etc/webapps/davical/localhost-conf.php"
)
install="$pkgname.install"
changelog=ChangeLog
source=(
	"http://debian.mcmillan.net.nz/packages/$pkgname/$pkgname-$pkgver.tar.gz"
	httpd-davical.conf
	README.archlinux
)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  find -type f -exec sed -i "s%/etc/davical/%/etc/webapps/davical/%g" {} \;
  cd dba
  find -name \*.sql -exec sed -i "s/'SQL'/'sql'/g; s/'PlPgSQL'/'plpgsql'/g" {} \;
}

package() {
  _instdir="$pkgdir/usr/share/webapps/davical"
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  mkdir -p "$_instdir" "$pkgdir/etc/webapps/davical"
  cp -ra . "$_instdir"

  install -D -m 0644 "config/example-config.php" "${pkgdir}/etc/webapps/davical/localhost-conf.php"
  install -D -m 0644 "${srcdir}/README.archlinux" "${_instdir}/README.archlinux"

  install -D -m 0644 "${srcdir}/httpd-davical.conf" "${pkgdir}/etc/httpd/conf/extra/httpd-davical.conf"
}
sha1sums=('680ca71e54fa3c3dfa026692c7c434572029731b'
          'a3d7860a6da5966d53b3e4b798c17d0d8785952b'
          '5040b5dfa278cf2a5b6a097a318a50086f55602a')
