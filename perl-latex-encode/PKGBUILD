# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-latex-encode
pkgver=0.091.3
pkgrel=1
pkgdesc="Encode characters for LaTeX formatting"
arch=(any)
license=('PerlArtistic')
url="http://search.cpan.org/dist/LaTeX-Encode/"
depends=('perl-html-parser' 'perl-readonly')
makedepends=('perl-carp-always')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/E/EI/EINHVERFR/LaTeX-Encode-${pkgver}.tar.gz)
md5sums=('4cfb510af9f7e95ecaaf78894a21d6ef')
sha512sums=('b2059e3a59d4ff560063fd3b195014967b1a8783c34698bb95d8ca623adb1edf900852290a5947ebf7c52a372924ed0e113cdb63aae5c46616ce17f0d6fb298f')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/LaTeX-Encode-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
