# Maintainer: Paul Burton <paulburton89@gmail.com>
pkgname=via-chrome9-dkms
pkgver=92.006.75396
pkgrel=1
pkgdesc="VIA chrome9 DRM/KMS driver"
arch=('i686' 'x86_64')
url="http://linux.via.com.tw"
license=('custom')
depends=('dkms' 'linux-headers>=3.4')
makedepends=()
provides=('via-chrome9')
conflicts=('via-chrome9')
install=${pkgname}.install
source=('5.76.52.92-006-75396-20130304.zip::http://linux.via.com.tw/support/beginDownload.action?eleid=701&fid=1001'
        'via-chrome9-kernel38.patch'
        'dkms.conf')
md5sums=('80521c39dccd3c60cdec9e8a96cd2c79'
         '9a173721c2be3835409f1599a1d5dbb6'
         '83637ffab5ae1ee7df3800602840f4b1')

build() {
	cd "$srcdir/TTM/3.4.0-3.6.0/via_chrome9_ttm"

	# move cbios under the driver directory
	mv "../../cbios/" .
	sed -i 's|\.\./\.\./cbios|cbios|' Makefile

	# patch for kernel version
	patch -p1 <"${srcdir}/via-chrome9-kernel38.patch"

	# extract license
	sed -n '/Copyright/,/DEALINGS IN THE SOFTWARE/p' via_chrome9_drm.c | sed 's/^ \* *//' >"${srcdir}/LICENSE"
}

package() {
	cd "$srcdir/TTM/3.4.0-3.6.0/via_chrome9_ttm"

	# install source
	install -dm 755 "${pkgdir}/usr/src/via-chrome9-${pkgver}"
	cp -a * "${pkgdir}/usr/src/via-chrome9-${pkgver}/"

	# install dkms configuration
	cp "$srcdir/dkms.conf" "${pkgdir}/usr/src/via-chrome9-${pkgver}/"

	# blacklist viafb
	install -dm 755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist viafb" >"${pkgdir}/usr/lib/modprobe.d/viafb.conf"

	# set permissions
	find "${pkgdir}" -type d -exec chmod 755 {} +
	find "${pkgdir}" -type f -exec chmod 644 {} +

	# install license
	install -dm755 "${pkgdir}/usr/share/licenses/via-chrome9-dkms"
	install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/via-chrome9-dkms"
}
