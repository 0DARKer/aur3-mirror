# Contributor: Marti Raudsepp <marti@juffo.org>
# Original author: Jonathan Wiersma <archaur at jonw dot org>

pkgname=virt-manager-hg
pkgver=1795
pkgrel=1
pkgdesc="A desktop user interface for managing virtual machines."
arch=('i686' 'x86_64')
url="http://virt-manager.et.redhat.com/"
license=('GPL')
depends=('dbus-python' 'gnome-python-desktop' 'libvirt' 'libxml2' 'vte' 'virtinst>=0.500.2' 'gtk-vnc' 'rarian' 'gconf')
makedepends=('gnome-doc-utils' 'intltool>=0.35.0' 'mercurial')
optdepends=('x11-ssh-askpass: for ssh authentication to remote servers')
provides=('virt-manager=0.8.2')
conflicts=('virt-manager')
install=virt-manager.install
source=()

_hgroot="http://hg.fedorahosted.org/hg"
_hgrepo="virt-manager"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u || return 1
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo || return 1
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  #
  # BUILD HERE
  #

  # workaround breakage in http://hg.fedorahosted.org/hg/virt-manager/rev/e0f406f266ad
  touch config.status

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexec=/usr/lib/virt-manager \
    --localstatedir=/var || return 1

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install || return 1
  # Set-up schema file in correct location
  install -m755 -d $pkgdir/usr/share/gconf/schemas || return 1
  gconf-merge-schema \
    $pkgdir/usr/share/gconf/schemas/virt-manager.schemas \
    $pkgdir/etc/gconf/schemas/*.schemas || return 1
  rm -rf $pkgdir/etc/gconf/schemas
  rmdir --ignore-fail-on-non-empty $pkgdir/etc/gconf $pkgdir/etc || return 1
}
