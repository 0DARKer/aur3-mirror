# Contributor: nesl247 <nesl247@gmail.com>
# Contributor: DaNiMoTh <jjdanimoth@gmail.com>

pkgname=compiz-git
pkgver=20070913
pkgrel=1
pkgdesc="Composite and window manager"
url="http://en.opensuse.org/Compiz"
license=('GPL')
arch=('i686' 'x86_64')
depends=('libgl' 'xorg-server' 'libxcb' 'dbus' 'gconf' 'librsvg')
makedepends=('git' 'intltool' 'perlxml')
conflicts=('compiz-core' 'compiz' 'compiz-decorator-gnome' \
           'compiz-decorator-kde')
install=compiz.install
source=()
md5sums=()


_gitroot="git://anongit.freedesktop.org/git/xorg/app/compiz"
_gitname="compiz"

build() {
  msg "Connecting to the GIT server...."
 
  if [[ -d $startdir/src/$_gitname ]] ; then
    cd $_gitname
    git-pull origin || return 1
    msg "The local files are updated."
  else
    git clone $_gitroot || return 1
  fi

  msg "GIT checkout done"
  msg "Starting make..."

  rm -rf $startdir/src/$_gitname-build
  cp -r $startdir/src/$_gitname $startdir/src/$_gitname-build

  cd $startdir/src/$_gitname-build

  if [[ ! -f /etc/profile.d/kde.sh ]]; then
    EXTRAOPS="--disable-kde"
  else
    source /etc/profile.d/qt.sh
    source /etc/profile.d/kde.sh
    EXTRAOPS="--enable-kde"
  fi

  ./autogen.sh \
    --prefix=/usr \
    --infodir=/usr/share/info \
    --mandir=/usr/man \
    --sysconfdir=/etc \
    --enable-gnome \
    --enable-librsvg \
    --enable-gconf \
    $EXTRAOPS || return 1

  make || return 1
  make DESTDIR=$startdir/pkg install || return 1

  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas

  rm -rf ${startdir}/src/$_gitname-build
}
