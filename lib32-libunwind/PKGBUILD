# Maintainer: Moritz Lipp <mlq@pwmt.org>

_pkgbasename=libunwind
pkgname=lib32-$_pkgbasename
pkgver=1.0.1
pkgrel=1
pkgdesc="Portable and efficient C programming interface (API) to determine the call-chain of a program"
arch=('x86_64')
url="http://www.nongnu.org/libunwind/"
license=('GPL')

depends=('glibc' 'libunwind')
options=(!libtool)
provides=(lib32-libunwind)

source=(http://download.savannah.gnu.org/releases/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz)
md5sums=('993e47cca7315e22239c3e0d987e94e0')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  CFLAGS="-m32 -U_FORTIFY_SOURCE" LDFLAGS="-m32" ./configure --prefix=/usr \
    --libdir=/usr/lib32 --target=i686
  make -j5
}

check() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make check || return 0
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir/" install

  rm "$pkgdir/usr/include/$_pkgbasename-common.h"
  rm "$pkgdir/usr/include/$_pkgbasename-dynamic.h"
  rm "$pkgdir/usr/include/$_pkgbasename-ptrace.h"
}

# vim:set ts=2 sw=2 et:
