# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=python2-capstone
_pkgname=capstone
pkgver=2.1.2
pkgrel=1
pkgdesc='Python bindings for multi-platform, multi-architecture capstone disassembly framework'
arch=('any')
url='http://www.capstone-engine.org/index.html'
license=('BSD')
depends=('capstone' 'python2')
source=(https://github.com/aquynh/capstone/archive/${pkgver}.tar.gz)
sha256sums=('5312ba5fc09fe193f7fc2fd6cbbc34b6ea7a7b002c0fd7acb2b38ee6c492fb9c')

build() {
  cd ${_pkgname}-${pkgver}/bindings/python
  python2 setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}/bindings/python
  python2 test.py
}

package() {
  cd ${_pkgname}-${pkgver}/bindings/python
  python2 setup.py install -O1 --prefix="${pkgdir}/usr"
  install -Dm 644 ../../LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
