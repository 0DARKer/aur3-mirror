# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=python2-capstone
_pkgname=capstone
pkgver=3.0
pkgrel=1
pkgdesc='Python bindings for multi-platform, multi-architecture capstone disassembly framework'
arch=('any')
url='http://www.capstone-engine.org/index.html'
license=('BSD')
depends=('capstone' 'python2')
source=(https://github.com/aquynh/capstone/archive/${pkgver}.tar.gz)
sha512sums=('cf6efe3159e7abb54ea747dee91eb4b9be42f4acc245cefb034aa37cac63afe7aa15cfdf49c5a1a79448bdad5beed030d58bf9593bf1010436f416a7ff35cceb')

build() {
  cd ${_pkgname}-${pkgver}/bindings/python
  python2 setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}/bindings/python
  python2 test.py
}

package() {
  cd ${_pkgname}-${pkgver}/bindings/python
  python2 setup.py install -O1 --prefix="${pkgdir}/usr"
  install -Dm 644 ../../LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
