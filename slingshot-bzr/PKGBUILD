# Maintainer: Dan Serban
# Contributor: Flamelab <panosfilip@gmail.com>

pkgname=slingshot-bzr
pkgver=latest
pkgrel=1
pkgdesc="An application launcher by the Elementary project"
arch=(i686 x86_64)
url=https://launchpad.net/slingshot-launcher
license=(GPL)
depends=(glib2 gnome-menus gtk2 libgee libunique libwnck)
makedepends=(bzr intltool vala)
options=('!libtool' '!emptydirs')
install=slingshot-bzr.install

_bzrtrunk=lp:slingshot
_bzrmod=slingshot

build()
{
  msg "Connecting to Launchpad..."
  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg2 "The local files have been updated."
  else
    bzr co ${_bzrtrunk} ${_bzrmod}
  fi
  msg2 "BZR checkout done or server timeout"
  [ -d ${_bzrmod}-build ] && rm -r ${_bzrmod}-build
  cp -a ${_bzrmod} ${_bzrmod}-build
  cd ${_bzrmod}-build
  valac --pkg gtk+-2.0 \
        --pkg unique-1.0 \
        --pkg gee-1.0 \
        --pkg gio-unix-2.0 \
        --pkg libgnome-menu --Xcc='-DGMENU_I_KNOW_THIS_IS_UNSTABLE' \
        --pkg libwnck-1.0 --Xcc='-DWNCK_I_KNOW_THIS_IS_UNSTABLE' \
        slingshot.vala \
        frontend/widgets/*.vala \
        frontend/*.vala \
        backend/*.vala
  install -Dm755 slingshot "${pkgdir}"/usr/bin/slingshot
}

