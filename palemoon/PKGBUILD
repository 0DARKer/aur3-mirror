# Maintainer: artiom <a.mv at gmx dot fr>
pkgname=palemoon
pkgver=24.5.0
pkgrel=3
pkgdesc="Open source web browser based on Firefox focusing on efficiency. This is source package, so compilation will take a significant amount of time!"
arch=('i686' 'x86_64')
url="http://www.palemoon.org/sourcecode.shtml"
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'dbus-glib' 'desktop-file-utils' 'libxt' 'mime-types' 'nss' 'alsa-lib')
makedepends=('zip' 'unzip' 'freetype2' 'fontconfig' 'pkg-config' 'iw' 'libidl2' 'python2' 'mercurial' 
'curl' 'libnotify' 'mesa' 'autoconf2.13' 'yasm' 'gstreamer0.10' 'p7zip')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'gstreamer0.10-base-plugins: vorbis decoding, ogg demuxing'
            'gstreamer0.10-good-plugins: webm and mp4 demuxing'
            'gstreamer0.10-bad-plugins: aac, vp8 and opus decoding'
            'gstreamer0.10-ugly-plugins: h.264 and mp3 decoding'
            'gstreamer0.10-ffmpeg: more decoders'
            'libpulse: PulseAudio audio driver'
            'hunspell: spell checker and morphological analyzer'
            'hyphen: library for hyphenation and justification')
install=palemoon.install
source=(ftp://source:current@ftp.palemoon.org/palemoon-24.5.0-source.7z
        palemoon.desktop mozconfig.in)
sha256sums=('fc9927ede5bab413918251f57045e12218b5c045b529fb6ffa89bbfdd15d9f2e'
            '60990b0776213d04cde6eb5deb46df83fe68288b3ed79f41831e064ae8681763'
            'cef52659026bdfb53b62d401b11a5f320e57559e4bfdf2fe9907c7f4193799f1')

build() {

	export MOZBUILD_STATE_PATH=$srcdir/mozbuild
	export MOZCONFIG=$srcdir/mozconfig
	echo  MOZBUILD_STATE_PATH=$MOZBUILD_STATE_PATH

	python2 mach build || echo "Continue"

	# Autoconf bugfix
	sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" configure

	chmod +x build/autoconf/* configure
	find . -name '*.sh' -exec chmod +x {} \;
	sed 's#%SRCDIR%#'"$srcdir"'#g' mozconfig.in > mozconfig
	make -f client.mk build
	
}

package() {
    cd $srcdir/pmbuild
    make package
    cd dist
  install -d "$pkgdir"/usr/{bin,lib}
  cp -r palemoon/ "$pkgdir/usr/lib/$pkgname"
  ln -s "../lib/$pkgname/palemoon" "$pkgdir/usr/bin/palemoon"
  install -Dm644 $srcdir/palemoon.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icons
  install -Dm644 palemoon/browser/chrome/icons/default/default16.png \
    "$pkgdir/usr/share/icons/hicolor/16x16/apps/$pkgname.png"
  install -Dm644 palemoon/browser/chrome/icons/default/default32.png \
    "$pkgdir/usr/share/icons/hicolor/32x32/apps/$pkgname.png"
  install -Dm644 palemoon/browser/chrome/icons/default/default48.png \
    "$pkgdir/usr/share/icons/hicolor/48x48/apps/$pkgname.png"
  install -Dm644 palemoon/browser/icons/mozicon128.png \
    "$pkgdir/usr/share/icons/hicolor/128x128/apps/$pkgname.png"

  # use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/$pkgname/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/$pkgname/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/$pkgname/hyphenation"

  # avoid duplicate binaries
  # https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  #ln -sf palemoon "$pkgdir/usr/lib/$pkgname/palemoon-bin"
  rm -f "$pkgdir/usr/lib/$pkgname/palemoon-bin"
}
