# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=indicator-network
pkgver=0.3.8
pkgrel=1
pkgdesc="Network Menu for controlling network connections. Uses Connection Manager."
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-network"
license=('GPL')
depends=('libindicator>=0.3.0' 'libdbusmenu>=0.3.90' 'libnotify>=0.7.1' 'libgee' 'connman' 'dbus-python' 'pygtk' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool>=0.35.0' 'vala')
options=(!libtool)
install=$pkgname.install
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('70ebd79a1087981adacf3107a17136af')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@'  scripts/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
