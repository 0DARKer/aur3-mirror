# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-readline
pkgver=5.2
pkgrel=1
pkgdesc="MinGW port of readline for editing typed command lines (mingw-w64)"
arch=('any')
depends=('mingw-w64-crt' 'mingw-w64-termcap')
makedepends=('mingw-w64-gcc')
options=('!libtool' '!strip')
license=('GPL')
url="http://cnswww.cns.cwru.edu/php/chet/readline/rltop.html"
source=("ftp://ftp.gnu.org/gnu/readline/readline-${pkgver}.tar.gz"
        "readline-5.2-shlib.patch"
	"readline-5.2-001.patch"
	"readline-5.2-002.patch"
	"readline-5.2-003.patch"
	"readline-5.2-004.patch"
	"readline-5.2-005.patch"
	"readline-5.2-006.patch"
	"readline-5.2-007.patch"
	"readline-5.2-008.patch"
	"readline-5.2-009.patch"
	"readline-5.2-010.patch"
	"readline-5.2-011.patch"
	"readline-5.2-redisplay-sigint.patch")
md5sums=('e39331f32ad14009b9ff49cc10c5e751'
         'fc298dfad01ea628797e930e64d70043'
         '9d4d41622aa9b230c57f68548ce87d8f'
         'f03e512d14206e37f7d6a748b56b9476'
         '252b42d8750f1a94b6bdf086612dceb2'
         'a32333c2e603a3ed250514e91050e552'
         '8106796c09b789523a3a78ab69c04b6d'
         '512188e2bf0837f7eca19dbf71f182ae'
         'ac17aca62eb6fb398c9f2fe9de540aff'
         '2484c392db021905f112cf97a94dfd4c'
         'fc6eb35d07914fae5c57d49c12483ff7'
         '7a2bf3dc7ac7680b1461a5701100e91b'
         'ef6cef6822663470f6ac8c517c5a7ec6'
         '2f51d805b2a99f6475cbc44e883b8e09')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/readline-${pkgver}"

  patch -Np1 -i ${srcdir}/readline-5.2-shlib.patch
  patch -Np0 -i ${srcdir}/readline-5.2-001.patch
  patch -Np0 -i ${srcdir}/readline-5.2-002.patch
  patch -Np0 -i ${srcdir}/readline-5.2-003.patch
  patch -Np0 -i ${srcdir}/readline-5.2-004.patch
  patch -Np0 -i ${srcdir}/readline-5.2-005.patch
  patch -Np0 -i ${srcdir}/readline-5.2-006.patch
  patch -Np0 -i ${srcdir}/readline-5.2-007.patch
  patch -Np0 -i ${srcdir}/readline-5.2-008.patch
  patch -Np0 -i ${srcdir}/readline-5.2-009.patch
  patch -Np0 -i ${srcdir}/readline-5.2-010.patch
  patch -Np0 -i ${srcdir}/readline-5.2-011.patch
  patch -Np1 -i ${srcdir}/readline-5.2-redisplay-sigint.patch

  pushd examples
  rm -f rlfe/configure
  iconv -f iso8859-1 -t utf8 -o rl-fgets.c{_,}
  touch -r rl-fgets.c{,_}
  mv -f rl-fgets.c{_,}
  popd

  unset LDFLAGS
  unset CFLAGS

  for _arch in ${_architectures}; do
    mkdir build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --enable-shared
    make SHLIB_LIBS="-ltermcap"

    # Rebuild the DLLs correctly and create implibs.
    pushd shlib
    ${_arch}-gcc -shared -o readline.dll -Wl,--out-implib,libreadline.dll.a readline.so vi_mode.so funmap.so keymaps.so parens.so search.so rltty.so complete.so bind.so isearch.so display.so signals.so util.so kill.so undo.so macro.so input.so callback.so terminal.so text.so nls.so misc.so xmalloc.so history.so histexpand.so histfile.so histsearch.so shell.so mbutil.so tilde.so compat.so -ltermcap
    ${_arch}-gcc -shared -o history.dll -Wl,--out-implib,libhistory.dll.a history.so histexpand.so histfile.so histsearch.so shell.so mbutil.so xmalloc.so
    popd

    popd
  done
}

package() {
  cd "${srcdir}/readline-${pkgver}"

  for _arch in ${_architectures} ; do
    pushd build-${_arch}

    make install DESTDIR=${pkgdir}

    # Remove the fake .so files and install our DLLs and implibs.
    pushd shlib
    rm ${pkgdir}/usr/${_arch}/lib/lib*.so.*
    mkdir -p ${pkgdir}/usr/${_arch}/bin
    install readline.dll ${pkgdir}/usr/${_arch}/bin
    install libreadline.dll.a ${pkgdir}/usr/${_arch}/lib
    install history.dll ${pkgdir}/usr/${_arch}/bin
    install libhistory.dll.a ${pkgdir}/usr/${_arch}/lib
    popd

    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/bin/*.dll
    mv ${pkgdir}/usr/${_arch}/lib/*.dll.a ${pkgdir}/
    ${_arch}-strip --strip-unneeded ${pkgdir}/*.dll.a
    ${_arch}-strip --strip-debug ${pkgdir}/usr/${_arch}/lib/*.a
    mv ${pkgdir}/*.dll.a ${pkgdir}/usr/${_arch}/lib/

    rm -rf ${pkgdir}/usr/${_arch}/{info,man}
    popd
  done
}

# vim:set ts=2 sw=2 et:
