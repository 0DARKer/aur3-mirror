# Maintainer: DonVla <donvla@users.sourceforge.net>

pkgname=kernel26-n130
_kernelname=-N130
_basekernel=2.6.37
_minrev=.3
#pkgver=${_basekernel}
pkgver=${_basekernel}${_minrev}
pkgrel=1
_patchname="patch-${_basekernel}${_minrev}"
_ck_patchver=ck2
_ck_patchname=patch-${_basekernel}-${_ck_patchver}
_bfqver="v2"
_bfqpath="http://algo.ing.unimo.it/people/paolo/disk_sched/patches/${_basekernel}${_minrev}-${_bfqver}"
pkgdesc="The Linux kernel and modules for SAMSUNG N130 - optionally with Con Kolivas' ck patchset and/or BFQ patches"
arch=('i686')
license=('GPL2')
provides=('kernel26' 'kernel26-headers')
groups=('base')
url="http://www.kernel.org"
depends=('coreutils' 'lzop' 'module-init-tools' 'mkinitcpio>=0.5.20')
replaces=('kernel24' 'kernel24-scsi' 'kernel26-scsi' 'alsa-driver' 'ieee80211' 'hostap-driver26' 'pwc' 'squashfs' 
          'unionfs' 'ivtv' 'zd1211' 'kvm-modules' 'iwlwifi' 'rt2x00-cvs' 'gspcav1' 'atl2' 'wlan-ng26' 'rt2500')
install="kernel26${_kernelname}.install"
source=("http://www.kernel.org/pub/linux/kernel/v2.6/linux-${_basekernel}.tar.bz2"
        "http://www.kernel.org/pub/linux/kernel/v2.6/${_patchname}.bz2"
        "http://www.kernel.org/pub/linux/kernel/people/ck/patches/2.6/${_basekernel}/${_basekernel}-${_ck_patchver}/${_ck_patchname}.bz2" "ck2_${pkgver}.patch"
        "${_bfqpath}/0001-block-prepare-I-O-context-code-for-BFQ.patch"
        "${_bfqpath}/0002-block-add-cgroups-kconfig-and-build-bits-for-BFQ.patch"
        "${_bfqpath}/0003-block-add-the-BFQ-${_bfqver}-I-O-scheduler-for-${pkgver}.patch"
        "config" "mkinitcpio-N130.conf" "kernel26-N130.preset"
        logo_linux_{clut224.ppm,mono.pbm,vga16.ppm})
optdepends=('crda: to set the correct wireless channels of your country')
md5sums=('c8ee37b4fdccdb651e0603d35350b434'
         'c0a38dd3b5bad43d5f5d07ed0fcf3692'
         '1728f86fb9af46a2222a9280a87f6488'
         '2b13a513b7fe267d15a7f6088c8db8d7'
         '49fee9b8abf3ee281a60076a314a95aa'
         'fdf42c4196cf4ff84d35b6b3c6cf3dab'
         '7db19b946135c08d9f561b70f833942c'
         '98332e4cc4d8dc884153027ee0812c65'
         '4b6c69e33f89072200192f550517c5f0'
         'c92dbaeffaf04501ef9fc15dce4c6a1f'
         '6a5a1925501fe20fafd04fdb3cb4f6ed'
         'e8c333eaeac43f5c6a1d7b2f47af12e2'
         'c120adbd9c0daa0136237a83adeabd1e')
sha256sums=('edbf091805414739cf57a3bbfeba9e87f5e74f97e38f04d12060e9e0c71e383a'
            'af6ccb6c85db09e78a85657b1013b2aa1399ef252ce552aafcf25684e6e5ad98'
            'f0b59d2266dae26a929417f9657a11bf7e0fc717495b6c894fe1331b3899af57'
            '5fd6f4a719eda4bcd440787648d2f5e00f420866d05d64eaefc2f4ac2602abb9'
            '59d8f63799e692f46e8931cf4272478f368ba83cf54072cc7222a18833908ff0'
            '2b9c368aace49acae679b6b86c4bd065b39a9699a6b967153fa20328c3a99c8f'
            '75edcf2b694643260ebfe6851b3489b584d30a32902d891bc7d6b07af07251cf'
            'a7eed65a07f770e000660ef4864362f201b52bf0dd3c2ccdcb4a053377a57830'
            '3ba1cd67d422b7420d1bfc6deec3fe5344f0bd9b7915cdc3d21f28009e54feb9'
            '1dd2fd989325bce55652cb816dcd2c886dfbd03be95a7b50a7427ccc8104d7df'
            '4274579ccf42a9acc03283edffea2dda2c4a48e3fd734bbaeada4c16dff9d156'
            '1e5bea8de1c2cc24498fb9a4fdbb313f36f38f671f2bfc46ccf7acbd7958a4b9'
            'f9c7c1275313890fc12f6bab92e2c0794b5041e223d868eb0e34cd99baee3d7a')

build() {
  # change to 1 (or else) if you want to use the BF scheduler
  # http://users.on.net/~ckolivas/kernel/
  _USE_CK_PATCHSET=0
  # change to 1 (or else) if you want to use the BFQ i/o scheduler
  # http://algo.ing.unimo.it/people/paolo/disk_sched/
  _USE_BFQ_PATCHES=0
  # default resume partition -> your swap partition. eg: 
  # _RESUME_SWAP_PARTITION="/dev/sda3"
  # NO PERSISTENT NAMING! If in doubt, leave it empty
  _RESUME_SWAP_PARTITION=""

  cd "${srcdir}/linux-${_basekernel}"

  # Add revision patches
  [[ ! -z "${_minrev}" ]] && msg "Adding revision patches..." && (patch -Np1 < "${srcdir}/${_patchname}")

  # Add Arch Linux logo to the source
  msg "Adding Arch Linux logo..."
  install -v -m 0644 "${srcdir}/logo_linux_clut224.ppm"  drivers/video/logo/logo_linux_clut224.ppm &&
  install -v -m 0644 "${srcdir}/logo_linux_mono.pbm"     drivers/video/logo/logo_linux_mono.pbm &&
  install -v -m 0644 "${srcdir}/logo_linux_vga16.ppm"    drivers/video/logo/logo_linux_vga16.ppm

  # Copy config
  cat ../config > ./.config
  if [[ "${_RESUME_SWAP_PARTITION}" ]]; then 
    sed -e "s|CONFIG_PM_STD_PARTITION=.*|CONFIG_PM_STD_PARTITION=\""${_RESUME_SWAP_PARTITION}"\"|g" -i ./.config
  fi

  ### Additional patches 
  # Add CK's BFS patch
  if (( _USE_CK_PATCHSET )); then 
    msg "Adding CK patchset..." 
    patch "${srcdir}/${_ck_patchname}" "${srcdir}/ck2_${pkgver}.patch"
    patch -Np1 < "${srcdir}/${_ck_patchname}"
  fi

  if (( _USE_BFQ_PATCHES )); then
    msg "Adding BFQ patches..."
    local _patch
    for _patch in "${srcdir}"/000*-block-*.patch; do
        patch -Np1 < "${_patch}"
    done 
  fi

  # Remove EXTRAVERSION from Makefile. Version will remain ${_basekernel}-${_kernelname}
  sed -e "s/^EXTRAVERSION\ .*/EXTRAVERSION\ = "${_kernelname}"/g" -i Makefile

  make prepare
  ####################
  ###
  ### Configure the kernel and stop afterwards
  ### this is useful to configure the kernel
  #make oldconfig
  ###
  ### This one also moves the config file to ${startdir} for later use. 
  ### Don't forget to rebuild the checksums afterwards! 
  #make menuconfig && cp -v .config ${startdir}/config && msg "Stopping build" && return 1
  ### new nconfig
  #make nconfig && cp -v .config ${startdir}/config && msg "Stopping build" && return 1
  ####################
  yes "" | make config
  # build!
  make ${MAKEFLAGS} bzImage modules
  #make bzImage modules
}

package() {
  KARCH=x86

  cd "${srcdir}/linux-${_basekernel}"
  # Get kernel version  
  _kernver="$(make kernelrelease)"

  ###
  ### install kernel and modules (kernel package)
  ###
  mkdir -p ${pkgdir}/{lib/modules,boot}
  make INSTALL_MOD_PATH=${pkgdir} modules_install
  cp System.map ${pkgdir}/boot/System.map26${_kernelname}
  cp arch/${KARCH}/boot/bzImage ${pkgdir}/boot/vmlinuz26${_kernelname}
  # add vmlinux
  install -m644 -D vmlinux ${pkgdir}/usr/src/linux-${_kernver}/vmlinux

  # install N130 mkinitcpio.conf and preset file for kernel
  install -m644 -D ${srcdir}/mkinitcpio-N130.conf ${pkgdir}/etc/mkinitcpio-N130.conf
  install -m644 -D ${srcdir}/kernel26${_kernelname}.preset ${pkgdir}/etc/mkinitcpio.d/kernel26${_kernelname}.preset

  sed \
    -e "s|source .*|source /etc/mkinitcpio.d/kernel26${_kernelname}.kver|g" \
    -e "s|default_image=.*|default_image=\"/boot/kernel26${_kernelname}.img\"|g" \
    -e "s|fallback_image=.*|fallback_image=\"/boot/kernel26${_kernelname}-fallback.img\"|g" \
    -i ${pkgdir}/etc/mkinitcpio.d/kernel26${_kernelname}.preset

  echo -e "# DO NOT EDIT THIS FILE\nALL_kver='${_kernver}'" > ${pkgdir}/etc/mkinitcpio.d/kernel26${_kernelname}.kver

  # set correct depmod command for install
  sed \
    -e  "s/KERNEL_NAME=.*/KERNEL_NAME=${_kernelname}/g" \
    -e  "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
    -i ${startdir}/kernel26${_kernelname}.install

  ###
  ### install kernel headers (kernel headers package)
  ###
  install -D -m644 Makefile ${pkgdir}/usr/src/linux-${_kernver}/Makefile
  install -D -m644 kernel/Makefile ${pkgdir}/usr/src/linux-${_kernver}/kernel/Makefile
  install -D -m644 .config ${pkgdir}/usr/src/linux-${_kernver}/.config

  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include
  for i in acpi asm-generic config crypto generated linux math-emu media net pcmcia scsi sound trace video xen; do
    cp -a include/$i ${pkgdir}/usr/src/linux-${_kernver}/include/
  done

  # copy arch includes for external modules
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/x86
  cp -a arch/x86/include ${pkgdir}/usr/src/linux-${_kernver}/arch/x86/

  # copy files necessary for later builds, like nvidia and vmware
  cp Module.symvers ${pkgdir}/usr/src/linux-${_kernver}
  cp -a scripts ${pkgdir}/usr/src/linux-${_kernver}
  
  # fix permissions on scripts dir
  chmod og-w -R ${pkgdir}/usr/src/linux-${_kernver}/scripts
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/.tmp_versions
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel

  cp arch/$KARCH/Makefile ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
  cp arch/$KARCH/Makefile_32.cpu ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
  cp arch/$KARCH/kernel/asm-offsets.s ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel/

  # add headers for virtualbox
  cp -a include/drm $pkgdir/usr/src/linux-${_kernver}/include/
  # add wireless headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  cp net/mac80211/*.h ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  # add dm headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  cp drivers/md/*.h  ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  # add inotify.h
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/linux
  cp include/linux/inotify.h ${pkgdir}/usr/src/linux-${_kernver}/include/linux/
  # add dvb and tuner headers for external modules
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  cp include/config/dvb/*.h ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core
  cp drivers/media/dvb/dvb-core/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core/
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-usb
  cp drivers/media/dvb/dvb-usb/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-usb/
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends
  cp drivers/media/dvb/frontends/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/common/tuners
  cp drivers/media/common/tuners/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/common/tuners/

  # copy in Kconfig files
  for i in `find . -name "Kconfig*"`; do 
    mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/`echo $i | sed 's|/Kconfig.*||'`
    cp $i ${pkgdir}/usr/src/linux-${_kernver}/$i
  done

  cd ${pkgdir}/usr/src/linux-${_kernver}/include && ln -s asm-$KARCH asm
  
  chown -R root.root ${pkgdir}/usr/src/linux-${_kernver}
  find ${pkgdir}/usr/src/linux-${_kernver} -type d -exec chmod 755 {} \;
  cd ${pkgdir}/lib/modules/${_kernver} && (rm -f source build; ln -sf ../../../usr/src/linux-${_kernver} build)

  # remove unneeded architectures
  rm -rf ${pkgdir}/usr/src/linux-${_kernver}/arch/{alpha,arm,arm26,avr32,blackfin,cris,frv,h8300,ia64,m32r,m68k,m68knommu,mips,microblaze,mn10300,parisc,powerpc,ppc,s390,sh,sh64,sparc,sparc64,um,v850,xtensa}

  # remove the firmware
  rm -rf ${pkgdir}/lib/firmware
}
