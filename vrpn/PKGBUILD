# Maintainer: Kniyl <mathias(dot)ettinger(at)gmail(dot)com>

pkgname=vrpn
pkgver=07.30
pkgrel=3
pkgdesc='the Virtual Reality Peripheral Network lib and tools'
arch=('i686' 'x86_64')
url="http://www.cs.unc.edu/Research/vrpn"
license=('GPL')
depends=('gpm' 'libusbx')
makedepends=('cmake')
source=("http://www.cs.unc.edu/Research/${pkgname}/downloads/${pkgname}_${pkgver//./_}.zip")
md5sums=('c83cd2f24b072d71962baad50585bffa')

prepare() {
  cd "${srcdir}/${pkgname}/server_src"

  msg "Fixing vrpn.cfg path finding"
  sed -i 's|"vrpn.cfg"|"/etc/vrpn.cfg"|' \
    aureal_sound_server/vrpn_Sound_A3D.cpp \
    ausim_sound_server/vrpn_Sound_ASM.cpp \
    directx_sound_server/vrpn_Sound_DX9.cpp \
    miles_sound_server/v5.0/sound_server.cpp \
    miles_sound_server/v6.0/sound_server.cpp \
    vrpn.C \
    vrpn_Generic_server_object.h
  msg "Fixing -f option help text"
  sed -i 's|default vrpn.cfg|default /etc/vrpn.cfg|' vrpn.C
}

build(){
  cd "${srcdir}"
  [[ "${pkgname}-build" ]] && rm -rf "${pkgname}-build"
  mkdir "${pkgname}-build"
  cd "${pkgname}-build"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DVRPN_BUILD_JAVA=OFF \
    -DVRPN_BUILD_EXTRA_COMPILER_WARNINGS=ON \
    "${srcdir}/${pkgname}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-build"

  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}/usr/etc" "$pkgdir"
}
