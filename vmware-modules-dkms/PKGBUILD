# Maintainer: adytzu2007 <adybac "at" gmail {dot} com>
# Contributor : Shaumux <shaumya "at" gmail {dot} com>

pkgname=vmware-modules-dkms
pkgver=271.1
pkgrel=1
pkgdesc="VMware Workstation kernel modules (DKMS)"
arch=('i686' 'x86_64')
url="http://www.vmware.com/"
license=('GPL2')
conflicts=('open-vm-tools-modules')

_vmware_location=/opt/vmware/lib/vmware/modules/source
_vmware_module_list="vmblock vmci vmmon vmnet vsock"

makedepends=('vmware-workstation')
options=('!buildflags')
install=${pkgname}.install
source=('271-putname.patch'
        '271-apic.patch'
        '271-vmci-only.patch'
        '60-vmware.rules')
sha256sums=('13ddc01ae6a77c7d3c258dd91c6db6f039d2d7cdf06cabe427c49a7d426706ac'
        '60e7df881281fedcabe9ea4427b324b5e1142a1a2b6ab5236ac0843bd1051048'
        '51b78b6b5e833588bfab9ed236abaefbb756d1404d01861580e45c71e17461bb'
        '654a11be0b0a6f0f4f8fdc32f8a92542cec637e0f6c62dd7097d3cf679c06f4d')

build()
{
    # copy modules from vmware-workstation directory
    for mod in ${_vmware_module_list}; do
        tar -xf ${_vmware_location}/${mod}.tar -C ${srcdir}
    done
    patch -p1 < ${srcdir}/271-apic.patch
    patch -p1 < ${srcdir}/271-vmci-only.patch
    chmod +w ${srcdir}/vmblock-only/linux/control.c
    patch -p1 < ${srcdir}/271-putname.patch

    ln -s -f ${srcdir}/vmci-only/Module.symvers ${srcdir}/vsock-only/Module.symvers

    for mod in ${_vmware_module_list}; do
        cd ${srcdir}/${mod}-only
cat > dkms.conf <<EOF
PACKAGE_NAME="${mod}"
PACKAGE_VERSION="${pkgver}"
BUILT_MODULE_NAME[0]="${mod}"
DEST_MODULE_LOCATION[0]="/kernel/drivers/vmware"
MAKE[0]="make MODULEBUILDDIR=/tmp/vmware-modules"
PRE_BUILD="setup_env.sh"
CLEAN="make clean"
AUTOINSTALL="yes"
EOF
cat > setup_env.sh <<EOF
mkdir -p /tmp/vmware-modules
EOF
    chmod +x setup_env.sh
    done
}

package()
{
    # make folders for dkms
    for mod in ${_vmware_module_list}; do
        install -m755 -d ${pkgdir}/usr/src/${mod}-${pkgver}
        cp -r ${srcdir}/${mod}-only/* ${pkgdir}/usr/src/${mod}-${pkgver}
    done

    install -D -m0644 ${srcdir}/60-vmware.rules ${pkgdir}/etc/udev/rules.d/60-vmware.rules
}
