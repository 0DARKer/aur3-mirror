# Maintainer: Faule Socke <github@socker.lepus.uberspace.de>

pkgname=panda3d-git
pkgver=r19896.7b7a4ff
pkgrel=1
pkgdesc="A 3D game engine with Python bindings. SDK with Bullet physics engine included. Git version."
arch=('i686' 'x86_64')
url="https://www.panda3d.org/"
license=('BSD')
depends=('desktop-file-utils' 'shared-mime-info' 'xorg-server' 'libgl' 'python2'
         'openssl' 'libjpeg' 'libpng' 'libtiff' 'freetype2' 'gtk2'
         'nvidia-cg-toolkit' 'openal' 'libxrandr' 'libxcursor' 'libxxf86dga')
makedepends=('git' 'python2' 'bison' 'subversion' 'cmake')
conflicts=('panda3d')
provides=('panda3d')
install='panda3d.install'

# The git repo is detected by the 'git:' or 'git+' beginning. The branch
# '$pkgname' is then checked out upon cloning, expediating versioning:
source=("$pkgname"::'git://github.com/panda3d/incremental.git'
        "bullet"::'svn+http://bullet.googlecode.com/svn/trunk/'
        "panda3d.install")
# Because the sources are not static, skip Git checksum:
md5sums=('SKIP'
         'SKIP'
         '781da785acb14d547624b505ef01e064')
pkgver() {
    cd "$srcdir/$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$pkgname"

    # bullet
    tpdir="../thirdparty/linux-libs-a/bullet"
    if [ "$CARCH" == "x86_64" ]; then
        tpdir="../thirdparty/linux-libs-x64/bullet"
    fi
    mkdir -p $tpdir/include

    mkdir -p bullet-build
    cd bullet-build
    cmake ../../bullet -G "Unix Makefiles" \
        -DINSTALL_LIBS=ON -DBUILD_DEMOS=OFF \
        -DCMAKE_CXX_FLAGS="-fPIC" \
        -DCMAKE_INSTALL_PREFIX=${tpdir} \
        -DINCLUDE_INSTALL_DIR=${tpdir}/include/

    make -j8
    make install

    # end bullet
    cd ..

    # disable broken/unwanted extensions
    # you can use PANDAFLAGS and BUILD_THREADS to control building
    # building is very memory intensive so you might want to use less threads
    python2 makepanda/makepanda.py --everything --no-opencv --no-ode --no-ffmpeg --no-maya2012 --no-fmodex --no-gles --no-gles2 --use-bullet ${PANDAFLAGS} --threads ${BUILD_THREADS:-4}

}

package() {
    cd "$srcdir/$pkgname"
    python2 makepanda/installpanda.py --prefix=/usr --destdir="$pkgdir"
    #rm "$pkgdir/usr/bin/bin2c"
    install -D -m644 "$srcdir/$pkgname/doc/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

