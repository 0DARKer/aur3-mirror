# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=wxhexeditor
pkgver=0.22
pkgrel=2
pkgdesc="a free hex editor / disk editor for Linux, Windows and MacOSX"
arch=('i686' 'x86_64' 'mips64el' 'armv6h')
url="http://wxhexeditor.sourceforge.net/"
license=('GPL')
depends=('udis86-git' 'wxgtk')
makedepends=('webkitgtk2')
provides=("wxhexeditor=${pkgver}")
conflicts=('wxhexeditor')
source=("http://downloads.sourceforge.net/project/wxhexeditor/wxHexEditor/v${pkgver}%20Beta/wxHexEditor-v${pkgver}-src.tar.bz2"
        'use_shared_udis86.patch'
        'wxgtk3.0_fix.patch')
sha512sums=('d846b30110bf2c864ec8dc45d1a4e9e333ec79379e335d7ecf5c8eed18a22bf2af26de1bcdca1977c94f2789a82c6eaf5fd2c2a91389f4c20f219a974c5e18ac'
            '9f58f691585e418eb9ec784576a44c4db635773f3d32fb0d3b0ce8fac41e9ca4b0cb99e155aa579e787f80c438b4324afefd3e03a9a7aef2fbd7073c70a3e5c7'
            '0944c6316dda6306b83afafb2438d9b282fbacf599d0ea9b286b5b79be930c8e6ab926c30c2ed9fae06e428ca26e0e93d187e81fa2705fd4f40c15d1e4aeefd4')

prepare() {
  cd "${srcdir}/wxHexEditor"

  # Use shared libraries
  #rm -rvf udis86 mhash
  rm -rv udis86
  patch -p1 -i "${srcdir}/wxgtk3.0_fix.patch"
  patch -p1 -i "${srcdir}/use_shared_udis86.patch"
}

build() {
  cd "${srcdir}/wxHexEditor"

  make -j1
}

package() {
  cd "${srcdir}/wxHexEditor"
  make PREFIX="${pkgdir}/usr/" install
}

# vim:set ts=2 sw=2 et:
