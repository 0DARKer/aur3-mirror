# Maintainer: Lev Lybin <lev.lybin@gmail.com>
# Contributor: Alucryd <alucryd at gmail dot com>
# Contributor: Paolo Stivanin <admin at polslinux dot it>

pkgname=keepassx2-git
pkgver=20130403
pkgrel=1
pkgdesc="KeePassX is a cross platform port of the windows application 'Keepass Password Safe'. It is an OpenSource password safe which helps you to manage your passwords in an easy and secure way. It uses a highly encrypted database locked with one master key."
arch=('i686' 'x86_64')
url="http://gitorious.org/keepassx"
license=('GPL2')
depends=('hicolor-icon-theme' 'libgcrypt' 'libxtst' 'qt4')
makedepends=('git' 'intltool' 'cmake')
conflicts=('keepassx-svn' 'keepassx')
options=('!emptydirs')
install=${pkgname}.install
source=("keepassx2.desktop")
sha256sums=('d4897690a91db996e366d44bd651f962289d377fa918e3e0faff6a7609cdbf0c')

_gitroot=https://git.gitorious.org/keepassx/keepassx.git
_gitname=keepassx

build() {
cd "${srcdir}"

# Clone
  msg "Connecting to GIT server...."

  if [[ -d ${_gitname} ]]; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_gitname}-build
  git clone "${srcdir}"/${_gitname} "${srcdir}"/${_gitname}-build
  cd "${srcdir}"/${_gitname}-build

# Build
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_VERBOSE_MAKEFILE=ON -DWITH_GUI_TESTS=ON
  make
}

package() {
  cd "${srcdir}"/${_gitname}-build/build

# Install
  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/share/applications
  install -m 644 "${srcdir}"/keepassx2.desktop "${pkgdir}"/usr/share/applications/keepassx2.desktop
}

# vim: ts=2 sw=2 et:
