# Maintainer: Lev Lybin <lev.lybin@gmail.com>
# Contributor: Alucryd <alucryd at gmail dot com>
# Contributor: Paolo Stivanin <admin at polslinux dot it>

pkgname=keepassx2-git
_gitname=keepassx
pkgver=2.0.alpha4
pkgrel=2
pkgdesc="KeePassX is a cross platform port of the windows application 'Keepass Password Safe'. It is an OpenSource password safe which helps you to manage your passwords in an easy and secure way. It uses a highly encrypted database locked with one master key."
arch=('i686' 'x86_64')
url="http://gitorious.org/keepassx"
license=('GPL2')
depends=('pacman' 'hicolor-icon-theme' 'libgcrypt' 'libxtst' 'qt4')
makedepends=('git' 'intltool' 'cmake')
conflicts=('keepassx-svn' 'keepassx')
install=$pkgname.install
source=('git://git.gitorious.org/keepassx/keepassx.git'
        'keepassx2.desktop')
md5sums=('SKIP'
         '888cdaaeef093af7d59e7d2efe20db43')

pkgver() {
  cd $_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_VERBOSE_MAKEFILE=ON -DWITH_GUI_TESTS=ON
  make
}

package() {
  cd $_gitname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/keepassx2.desktop" "$pkgdir/usr/share/applications/keepassx2.desktop"
}
