# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Konsta Kokkinen <kray@tsundere.fi>

pkgname=minetest-git
_pkgname=minetest
pkgver=20130504.a888b23
pkgrel=1
pkgdesc='Infiniminer/Minecraft inspired game'
url='http://www.minetest.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('git' 'cmake' 'irrlicht')
depends=('bzip2' 'libpng' 'libjpeg' 'mesa' 'sqlite' 'openal' 'libvorbis')
source=('git+https://github.com/celeron55/minetest.git'
        'git+https://github.com/celeron55/minetest_game.git')
sha1sums=('SKIP'
          'SKIP')

conflicts=("${_pkgname}")
provides=("${_pkgname}")
install=install

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}"
	mv minetest_game minetest
}

build() {
	cd "${srcdir}/${_pkgname}"
	cmake . -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "${srcdir}/${_pkgname}"
	make DESTDIR="${pkgdir}" install
}
