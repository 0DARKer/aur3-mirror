#Maintainer zigarrre <zigarrreATgmailDOTcom>

pkgname=asteroids
pkgver=0.2
pkgrel=1
_gitname="asteroids"
pkgdesc="A remake of the game 'asteroids' in C++"
url="https://github.com/zigarrre/asteroids"
arch=('x86_64' 'i686')
license=('Simplified BSD License')
depends=('boost-libs' 'thor' 'sfml')
optdepends=()
makedepends=('boost' 'cmake' 'git')
conflicts=('asteroids-git')
replaces=()
backup=()
install='asteroids.install'
source=("git://github.com/zigarrre/asteroids.git"
        "asteroids.desktop")
sha256sums=('SKIP'
            'af31a596b155594787af4c9020d8bf7547a3756dc04e828bcf5fedf945e4ae87')

prepare() {
  cd "${srcdir}"

  cd "${srcdir}/${_gitname}"
  git checkout "v${pkgver}"
}

build() {
  cd "${srcdir}/${_gitname}"

  mkdir build
  cd build
  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DASTEROIDS_PATHS_HIGHSCORE=.asteroids_highscore.csv \
    -DASTEROIDS_PATHS_HIGHSCORE_HOME=true \
    -DASTEROIDS_PATHS_RES=/usr/share/asteroids \
    -DASTEROIDS_PATHS_CONFIG=/usr/share/asteroids/config.cfg

  make
}

package() {
  install -Dm644 ${_gitname}.desktop ${pkgdir}/usr/share/applications/${_gitname}.desktop

  cd "${srcdir}/${_gitname}"

  install -Dm644 LICENSE.txt    "$pkgdir/usr/share/licenses/$_gitname/LICENSE"

  cd build

  install -Dm755 asteroids      "$pkgdir/usr/bin/asteroids"

  install -Dm644 res/ship.png   "$pkgdir/usr/share/pixmaps/asteroids.png"

  mkdir -p "$pkgdir/usr/share/asteroids/"
  install -Dm644 res/*          "$pkgdir/usr/share/asteroids/"

  install -Dm644 config.cfg     "$pkgdir/usr/share/asteroids/config.cfg"
}

# vim:set ts=2 sw=2 et:
