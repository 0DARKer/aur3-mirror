# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=xfce4-clipman-plugin-passive
pkgver=1.2.2
pkgrel=2
pkgdesc="Clipman without buggy selection stealing"
arch=(i686 x86_64)
license=('custom')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-clipman-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libunique' 'libxtst')
makedepends=('intltool')
replaces=('xfce4-clipman-plugin')
conflicts=('xfce4-clipman-plugin')
provides=('xfce4-clipman-plugin')
options=('!libtool')
install=xfce4-clipman-plugin.install
source=(nosteal.patch http://archive.xfce.org/src/panel-plugins/xfce4-clipman-plugin/1.2/xfce4-clipman-plugin-${pkgver}.tar.bz2)

build() {
  cd "${srcdir}"/xfce4-clipman-plugin-${pkgver}
  patch -Np1 -i "${srcdir}"/nosteal.patch
  sed -i -e 's|$(TARGET_DIR)/C/media|$(docdir)/html/C/media|' doc/Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static --enable-unique
  make
}

package() {
  cd "${srcdir}"/xfce4-clipman-plugin-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
md5sums=('45dd76bef06b1035d4c34cf18e6ae1b1' '60df7769a8271d7fcab177210f4291e8')
