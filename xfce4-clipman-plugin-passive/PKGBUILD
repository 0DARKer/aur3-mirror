# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=xfce4-clipman-plugin-passive
pkgver=1.2.3
pkgrel=1
pkgdesc="Clipman without buggy selection stealing"
arch=(i686 x86_64)
license=('GPL')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-clipman-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libunique' 'libxtst')
makedepends=('intltool')
replaces=('xfce4-clipman-plugin')
conflicts=('xfce4-clipman-plugin')
provides=('xfce4-clipman-plugin')
options=('!libtool')
install=xfce4-clipman-plugin.install
source=(nosteal.patch http://archive.xfce.org/src/panel-plugins/xfce4-clipman-plugin/1.2/xfce4-clipman-plugin-${pkgver}.tar.bz2)

build() {
  cd "${srcdir}"/xfce4-clipman-plugin-${pkgver}
  patch -Np1 -i "${srcdir}"/nosteal.patch
  sed -i -e 's|$(TARGET_DIR)/C/media|$(docdir)/html/C/media|' doc/Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static --enable-unique
  make
}

package() {
  cd "${srcdir}"/xfce4-clipman-plugin-${pkgver}
  make DESTDIR="${pkgdir}" install
}
md5sums=('13e31835071ea506fee684154975b2cf' '61f3be97efa379cb358980c94e14692a')
