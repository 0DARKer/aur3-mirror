# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=stlport
_realpkgname=STLport
pkgver=5.2.1
pkgrel=1
pkgdesc="An implementation of the C++ Standard Library"
arch=('i686' 'x86_64')
url="http://www.stlport.org/"
license=('custom')
depends=('gcc-libs')
source=("http://downloads.sourceforge.net/project/${pkgname}/${_realpkgname}/${_realpkgname}-${pkgver}/${_realpkgname}-${pkgver}.tar.gz"
        "LICENSE")
md5sums=('b20ace9f4a487b2614dfbb205bee112c'
         '3a759c6b31d076057bea2a78d5f53748')

build() {
  cd "${srcdir}/${_realpkgname}-${pkgver}"
  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  sed -i -e 's#/usr/bin/install#/bin/install#' build/Makefiles/gmake/linux/sys.mak
  ./configure --prefix="${pkgdir}/usr"
  make
}

check() {
  cd "${srcdir}/${_realpkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${_realpkgname}-${pkgver}"
  install -d -m755 "${pkgdir}/usr/include"
  install -d -m755 "${pkgdir}/usr/lib"
  make install
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
