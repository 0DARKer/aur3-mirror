# Maintainer: roger <roger@rogerpc.com.ar>

pkgname=qtile-git
pkgver=20121021
pkgrel=1
pkgdesc="A full-featured, pure-Python tiling window manager. (git version)"
arch=('i686' 'x86_64')
url="http://www.qtile.org/index.html"
license=('MIT')
depends=('python2' 'cairo>=1.12' 'pycairo-xcb-git' 'pygtk' 'xorg-xpyb-git')
makedepends=()
provides=('qtile=0.4')
conflicts=('qtile')
install=${pkgname}.install
source=()
md5sums=()

_gitroot="git://github.com/qtile/qtile.git"
_gitname="qtile"

_gitrootexamples="git://github.com/qtile/qtile-examples.git"
_gitnameexamples="qtile-examples"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  cd "$srcdir"
  if [ -d $_gitnameexamples ] ; then
    cd $_gitnameexamples
    git pull origin
    msg "The local example files are updated."
  else
    git clone $_gitrootexamples
  fi

  msg "GIT checkout done or server timeout"
  msg "Preparing sources..."

  if [ -d $srcdir/$_gitname-build ] ; then
    rm -rf "$srcdir/$_gitname-build"
  fi

  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"

  # license
  msg "Copying license..."
  install -D -m 644 LICENSE $startdir/pkg/usr/share/licenses/$pkgname/LICENSE

  # install
  msg "Running setup.py"
  python2 setup.py install --root=${pkgdir} --prefix=/usr

  cd "$srcdir/$_gitnameexamples"
  # example config
  msg "Copying example config"
  mkdir -p $pkgdir/usr/share/doc/$pkgname/examples/
  git checkout-index -a -f --prefix=$pkgdir/usr/share/doc/$pkgname/examples/
}
