# Maintainer: Kyle <kyle@gmx.ca>

pkgname=espeak-test
pkgver=1.48.02
pkgrel=1
pkgdesc="Text to Speech engine for good quality English, with support for other languages: latest test version"
arch=('i686' 'x86_64')
url="http://espeak.sourceforge.net/test/latest.html"
license=('GPL')
depends=('gcc-libs' 'portaudio')
conflicts=('espeak')
provides=('espeak')
source=(http://espeak.sourceforge.net/test/espeak-$pkgver.zip)
md5sums=('3452bd1b046df4901f87e3edb3d1c789')

# Extracted dir has different version number from $pkgver and zip file. Fixed here.
prepare() {
	mv ${srcdir}/espeak-1.48.01 ${srcdir}/espeak-${pkgver}
}

build() {
	cd $srcdir/espeak-${pkgver}/src
	cp portaudio19.h portaudio.h
	make
}

package() {
	cd $srcdir/espeak-${pkgver}/src
	make DESTDIR=$pkgdir install
	find "$pkgdir/usr/share/espeak-data" -type f -print0 |xargs -0 chmod 644
}
