# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Thomas Conneely tc116 at le dot ac dot uk

pkgname=cocos2d
pkgver=0.5rc0
pkgrel=1
pkgdesc="A framework for building graphical/interactive applications."
arch=('any')
url="http://cocos2d.org/"
license=('BSD' 'LGPL' 'custom')
depends=('pyglet' 'python2-numpy')
makedepends=('python2-distribute')
source=("http://los-cocos.googlecode.com/files/$pkgname-$pkgver.zip")
options=(!emptydirs)
md5sums=('817e6e91fd4c9eea1b87354d9bc66df8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  sed -i "s|\(env python\).*|\12|" `grep -rl "env python" .`

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir/" --optimize=1

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -a doc/* "$pkgdir/usr/share/doc/$pkgname"

  # examples
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a samples "$pkgdir/usr/share/$pkgname"

  # licenses
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  cp -a LICENSE* "$pkgdir/usr/share/licenses/$pkgname"
}
