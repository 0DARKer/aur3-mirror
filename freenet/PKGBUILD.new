#   Maintainer: skydrome <skydrome at@at i2pmail do.t org>
# Contributors:
#
# TODO:
#   replace systemd service file without calling a shell script
#   create a read protected home dir for the freenet user
#   make sure native bigint lib gets loaded (something about the naming?)
#   build plugins
#

pkgname=freenet
pkgver=0.7.5
pkgrel=6
pkgdesc="An encrypted network without censorship"
url="https://downloads.freenetproject.org"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('java-runtime' 'unzip' 'gmp' 'java-service-wrapper')
makedepends=('java-environment' 'apache-ant' 'apache-ant-contrib')
install='freenet.install'

# these are packages we need to download to prevent ant from
# downloading them itself we are also going to build as much
# as we can from source, including this array
m_deps=("https://downloads.sourceforge.net/project/sevenzip/LZMA%20SDK/4.65/lzma465.tar.bz2"
        "league-lzmajio-0.95-0-gd38bf5c.tar.gz::https://codeload.github.com/league/lzmajio/legacy.tar.gz/0.95"
        "https://downloads.sourceforge.net/project/bitcollider/jBitcollider%20%28Java%29/0.8/jBitcollider-0.8.zip"
        "https://www.spaceroots.org/software/mantissa/mantissa-7.2-src.zip")
noextract=('lzma465.tar.bz2'
           'league-lzmajio-0.95-0-gd38bf5c.tar.gz'
           'jBitcollider-0.8.zip'
           'mantissa-7.2-src.zip'
           *."jar")

# here we have only java-commons-compress and java-db4o coming
# prebuilt by the freenet project
source=("freenet-src.zip::https://github.com/freenet/fred/archive/master.zip"
        "freenet-contrib.zip::https://github.com/freenet/contrib/archive/master.zip"
        "https://raw.githubusercontent.com/i2p/i2p.i2p/master/core/c/jcpuid/src/jcpuid.c"
        "https://raw.githubusercontent.com/i2p/i2p.i2p/master/core/c/jcpuid/include/jcpuid.h"
        "bcprov.jar::https://www.bouncycastle.org/download/bcprov-jdk15on-151.jar"
        #"https://downloads.bouncycastle.org/betas/bcprov-jdk15on-152b01.jar"
        #"${url}/alpha/installer/${pkgname}07.tar.gz"
        #"${url}/contrib/jar/latest/bdb-je.jar"
        #"${url}/contrib/jar/latest/freenet-ext.jar"
        #"${url}/contrib/jar/latest/lzmajio.jar"
        #"${url}/contrib/jar/latest/mantissa.jar"
        #"${url}/contrib/jar/latest/wrapper.jar"
        "${url}/contrib/jar/latest/commons-compress.jar"
        "${url}/contrib/jar/latest/db4o.jar"
        "${url}/alpha/opennet/seednodes.fref"
        'fred.properties' 'contrib.properties' 'run.sh' 'freenet.service' 'freenet.ini' 'wrapper.config'
        "${m_deps[@]}")

sha256sums=('882047ee4b39a0c8cd6b16f2372e65cbb7b528a8a6572fe4e6d017fa390866f5'
            'da4c7b71a8e15eb64a5211705d6a484f5141003ab87e33b8a18b159406a6d5ed'
            'f1ecddb5395892e0b2e6282bc3a1437d06afa52758057850ecccfa0a79c45c5d'
            '9ec758801a9864ae10caf851ee60ed22c3ef44428e77689c203d9b890921a6d2'
            '8748f0ec73895f7f18c1a9c13cf754fddddf0451cf472463ef02f93c3e7a7de7'
            '16924be3c8f1322b659f3ff08060a43f45f2e8de6f95af28d86fe9876e79008d'
            'bb98650344e65138d694dfa54f89b5690088cc14f42d1ace2ae0063d35f417bd'
            '8f170fbabc19c082b42b370ea0f09ce7664adf1fc0d79df561a9169c8f844f92'
            'd38d5b60aff79620f05741a9acd698fb189a713d4a5fce2554ece9c75d2a1f37'
            'a217ab667adabdc71fe5a55c4a1f5b19fcb08d1689eb38dc7c068fdf5caab14f'
            '3e48a77324277569b93a6e4082ddcf4303435c86ebd005594f164cb0047997b3'
            '9f96126bf326ed5f82d1e653f71b0e288534b23a48b62a5288efe336fe0e739a'
            '162c4784443769296a6cd144bfc01f5ad339d6e621e339c0a04958f54abcbf07'
            '997b354379d1bd303a24d4944f84d923108b8817baa77af1dab878f32135ea5f'
            'c935fd04dd8e0e8c688a3078f3675d699679a90be81c12686837e0880aa0fa1e'
            '265f7ed2dd4fecb058884d3f8974674b06e0be46131c3b2bc6a310373937d2ef'
            'b36482ee9e919c669bb1797ff7e50f57edf505af67664e280fe1dff361861044'
            'e438135d69139ed4fa44400f416ea73935d16afe50dfe490b7bba0602ee89476')

prepare () {
    cd "$srcdir/fred-master"

    # we're building a newer freenet-ext.jar so we link up with the contrib module
    rm -rf contrib
    ln -sf ../contrib-master contrib
    mkdir -p contrib/freenet-ext/{dist,lib}

    # had a hard time building these two sources, not worth the effort
    for dep in db4o commons-compress # \
               # wrapper bdb-je lzmajio mantissa freenet-ext
        do
        cp "$srcdir/${dep}.jar" contrib/freenet-ext/dist
    done

    # this is done just to satisfy ant
    ln -sf /usr/share/java/wrapper.jar contrib/freenet-ext/dist/
    ln -sf "$srcdir"/bcprov.jar lib/
    cp "$srcdir"/{lzma465.tar.bz2,league-lzmajio-0.95-0-gd38bf5c.tar.gz,jBitcollider-0.8.zip,mantissa-7.2-src.zip} contrib/freenet-ext/lib

    # we're going to build our own jcpuid and jbigi libraries
    cd "$srcdir/contrib-master/NativeBigInteger"
    rm "$srcdir"/contrib-master/NativeBigInteger/lib/net/i2p/util/*

    cd "$srcdir/contrib-master/jcpuid"
    rm lib/freenet/support/CPUInformation/* include/jcpuid.h
    # these are from I2P
    ln -sf "$srcdir"/jcpuid.h include/
    ln -sf "$srcdir"/jcpuid.c src/
}

build() {
    build_jbigi
    build_jcpuid

    cd "$srcdir/fred-master/contrib/freenet-ext"
    ant -propertyfile "$srcdir/contrib.properties" package

    cd "$srcdir/fred-master"
    ant -propertyfile "$srcdir/fred.properties" -f build-clean.xml package

    # unneeded anymore
    rm -f contrib/freenet-ext/dist/wrapper.jar
}

build_jbigi() {
local _objdir
msg "Building libjbigi..."
    cd "$srcdir/contrib-master/NativeBigInteger"

    _objdir='lib/net/i2p/util'
    CFLAGS+=" -fPIC -Wall"
    INCLUDES="-I./jbigi/include -I${JAVA_HOME}/include -I${JAVA_HOME}/include/linux"
    LDFLAGS="-shared -Wl,-O1,--sort-common,-z,relro,-soname,libjbigi.so"
    gcc -c $CFLAGS $INCLUDES jbigi/src/jbigi.c
    gcc $LDFLAGS $INCLUDES -o "$_objdir"/libjbigi-linux.so jbigi.o -lgmp
    install -Dm644 "$_objdir"/libjbigi-linux.so \
        "$srcdir"/contrib-master/freenet-ext/dist/libjbigi-linux.so
}

build_jcpuid() {
local _objdir
msg "Building libjcpuid..."
    cd "$srcdir/contrib-master/jcpuid"

    _objdir='lib/freenet/support/CPUInformation'
    INCLUDES="-I./include -I${JAVA_HOME}/include -I${JAVA_HOME}/include/linux"
    LDFLAGS="-shared -Wl,-O1,--sort-common,-z,relro,-soname,libjcpuid-x86-linux.so"
    gcc $CFLAGS $LDFLAGS $INCLUDES src/jcpuid.c -o "$_objdir"/libjcpuid-x86-linux.so
    install -Dm644 "$_objdir"/libjcpuid-x86-linux.so \
        "$srcdir"/contrib-master/freenet-ext/dist/libjcpuid-x86-linux.so
}

package() {
    cd "$srcdir/fred-master"

    install -dm755 "$pkgdir"/usr/bin
    install -dm700 "$pkgdir"/run/freenet
    install -dm755 "$pkgdir"/opt/freenet/{lib,fred}

    install -m644 "$srcdir"/{wrapper.config,run.sh} \
                  "$pkgdir"/opt/freenet
    install -m644 "$srcdir"/{freenet.ini,seednodes.fref} \
                  "$pkgdir"/opt/freenet #/fred # future readprotected homedir?
    install -m644 "$srcdir"/bcprov.jar \
                  contrib/freenet-ext/dist/* \
                  dist/freenet.jar \
                  "$pkgdir"/opt/freenet/lib

    # launcher
    chmod +x "$pkgdir"/opt/freenet/run.sh
    ln -s /opt/freenet/run.sh "$pkgdir"/usr/bin/freenet

    # systemd
    install -dm755 "$pkgdir"/usr/lib/tmpfiles.d
    install -Dm644 "$srcdir"/freenet.service    "$pkgdir"/usr/lib/systemd/system/freenet.service
    echo 'd /run/freenet 0700 freenet freenet' >"$pkgdir"/usr/lib/tmpfiles.d/freenet.conf

    # license
    install -dm755 "$pkgdir"/usr/share/licenses/freenet
    install -m644 LICENSE "$pkgdir"/usr/share/licenses/freenet/LICENSE
}
