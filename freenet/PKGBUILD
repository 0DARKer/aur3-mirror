#   Maintainer: skydrome <skydrome at@at i2pmail do.t org>
# Contributors:
#

pkgname=freenet
pkgver=0.7.5.1467
pkgrel=1
pkgdesc="An encrypted network without censorship"
url="https://downloads.freenetproject.org"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('java-runtime' 'unzip' 'gmp' 'java-service-wrapper')
makedepends=('java-environment' 'apache-ant' 'apache-ant-contrib' 'git')
install='freenet.install'

# these are packages we need to download to prevent ant from
# downloading them itself we are also going to build as much
# as we can from source, including this array
_deps=("https://downloads.sourceforge.net/project/sevenzip/LZMA%20SDK/4.65/lzma465.tar.bz2"
       "league-lzmajio-0.95-0-gd38bf5c.tar.gz::https://codeload.github.com/league/lzmajio/legacy.tar.gz/0.95"
       "https://downloads.sourceforge.net/project/bitcollider/jBitcollider%20%28Java%29/0.8/jBitcollider-0.8.zip"
       "https://www.spaceroots.org/software/mantissa/mantissa-7.2-src.zip")
noextract=('lzma465.tar.bz2'
           'league-lzmajio-0.95-0-gd38bf5c.tar.gz'
           'jBitcollider-0.8.zip'
           'mantissa-7.2-src.zip'
           *."jar")

# here we have only java-commons-compress and java-db4o coming
# prebuilt by the freenetproject, the rest we attempt to build ourselves
source=("git+https://github.com/freenet/fred.git#branch=next"
        "git+https://github.com/freenet/contrib.git"
        "git+https://github.com/freenet/plugin-JSTUN.git"
        "git+https://github.com/freenet/plugin-UPnP.git"
        "https://raw.githubusercontent.com/i2p/i2p.i2p/master/core/c/jcpuid/src/jcpuid.c"
        "https://raw.githubusercontent.com/i2p/i2p.i2p/master/core/c/jcpuid/include/jcpuid.h"
        "bcprov.jar::https://www.bouncycastle.org/download/bcprov-jdk15on-151.jar"
        #"https://downloads.bouncycastle.org/betas/bcprov-jdk15on-152b01.jar"
        "${url}/contrib/jar/latest/commons-compress.jar"
        "${url}/contrib/jar/latest/db4o.jar"
        "${url}/alpha/opennet/seednodes.fref"
        "${_deps[@]}"
        'fred.properties' 'contrib.properties' 'run.sh'
        'freenet.service' 'freenet.ini' 'wrapper.config')

sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'f1ecddb5395892e0b2e6282bc3a1437d06afa52758057850ecccfa0a79c45c5d'
            '9ec758801a9864ae10caf851ee60ed22c3ef44428e77689c203d9b890921a6d2'
            '8748f0ec73895f7f18c1a9c13cf754fddddf0451cf472463ef02f93c3e7a7de7'
            '16924be3c8f1322b659f3ff08060a43f45f2e8de6f95af28d86fe9876e79008d'
            'bb98650344e65138d694dfa54f89b5690088cc14f42d1ace2ae0063d35f417bd'
            '8f170fbabc19c082b42b370ea0f09ce7664adf1fc0d79df561a9169c8f844f92'
            'c935fd04dd8e0e8c688a3078f3675d699679a90be81c12686837e0880aa0fa1e'
            '265f7ed2dd4fecb058884d3f8974674b06e0be46131c3b2bc6a310373937d2ef'
            'b36482ee9e919c669bb1797ff7e50f57edf505af67664e280fe1dff361861044'
            'e438135d69139ed4fa44400f416ea73935d16afe50dfe490b7bba0602ee89476'
            'd38d5b60aff79620f05741a9acd698fb189a713d4a5fce2554ece9c75d2a1f37'
            'a217ab667adabdc71fe5a55c4a1f5b19fcb08d1689eb38dc7c068fdf5caab14f'
            '3e48a77324277569b93a6e4082ddcf4303435c86ebd005594f164cb0047997b3'
            '9f96126bf326ed5f82d1e653f71b0e288534b23a48b62a5288efe336fe0e739a'
            '08227b1649aed81c2c9c886f20947d045988fbcab9233b5a248ad998be7392ed'
            '7803d4e87e428d071a9a69be1e0da09e7239017727c555a8cbd7da7619920aa3')

pkgver() {
    cd "$srcdir/fred"
    echo "${pkgver}.$(git describe |cut -d- -f3)"
}

prepare() {
    cd "$srcdir/fred"

    if [[ "$CARCH" = 'i686' ]]; then
         _arch=x86
        __arch=i386
    else
         _arch=x86_64
        __arch=x86_64
    fi

    rm -rf contrib
    ln -sf ../contrib contrib
    mkdir -p contrib/freenet-ext/{dist,lib}

    # had a hard time building these two sources, we'll use the binaries
    for dep in db4o commons-compress ;do
        cp "$srcdir/${dep}.jar" contrib/freenet-ext/dist
    done

    # this is done just to satisfy ant
    ln -sf /usr/share/java/wrapper.jar contrib/freenet-ext/dist/
    ln -sf "$srcdir"/bcprov.jar lib/
    cp "$srcdir"/{lzma465.tar.bz2,league-lzmajio-0.95-0-gd38bf5c.tar.gz,jBitcollider-0.8.zip,mantissa-7.2-src.zip} contrib/freenet-ext/lib

    # we're going to compile our own c libraries
    cd "$srcdir/contrib"
    rm NativeBigInteger/lib/net/i2p/util/*
    rm NativeThread/lib/freenet/support/io/*
    rm -fr onion-fec/bin/lib/{freebsd,linux,win32}-*

    cd "$srcdir/contrib/jcpuid"
    rm lib/freenet/support/CPUInformation/* include/jcpuid.h
    # these are from the I2P project
    ln -sf "$srcdir"/jcpuid.h include/
    ln -sf "$srcdir"/jcpuid.c src/

    # append wrapper.jar location to JSTUN build
    sed -i "$srcdir/plugin-JSTUN/build.xml" \
        -e 's|<pathelement location="${freenet-ext.location}"/>|<pathelement location="${freenet-ext.location}"/>\n<pathelement location="/usr/share/java/wrapper.jar"/>|'
}

build() {
    build_jbigi
    build_jcpuid
    build_fec

    source /etc/profile.d/apache-ant.sh
    source /etc/profile.d/jre.sh

    msg "Building Freenet-ext..."
    cd "$srcdir/fred/contrib/freenet-ext"
    ant package \
        -propertyfile "$srcdir/contrib.properties"

    msg "Building Freenet..."
    cd "$srcdir/fred"
    ant package -f build-clean.xml \
        -propertyfile "$srcdir/fred.properties"

    build_nativethread
    build_plugins

    # not needed anymore
    rm -f contrib/freenet-ext/dist/wrapper.jar
}

build_jbigi() {
    msg "Building libjbigi..."
    cd "$srcdir/contrib/NativeBigInteger"

    _objdir='net/i2p/util'
    CFLAGS+=" -fPIC -Wall"
    INCLUDES="-I./jbigi/include -I${JAVA_HOME}/include -I${JAVA_HOME}/include/linux"
    LDFLAGS="-shared -Wl,-O1,--sort-common,-z,relro,-soname,libjbigi.so -lgmp"
    gcc -c $CFLAGS $INCLUDES jbigi/src/jbigi.c
    gcc $LDFLAGS $INCLUDES -o lib/"$_objdir"/libjbigi-linux-none.so jbigi.o
}

build_jcpuid() {
    msg "Building libjcpuid..."
    cd "$srcdir/contrib/jcpuid"

    _objdir='freenet/support/CPUInformation'
    INCLUDES="-I./include -I${JAVA_HOME}/include -I${JAVA_HOME}/include/linux"
    LDFLAGS="-shared -Wl,-O1,--sort-common,-z,relro,-soname,libjcpuid-x86-linux.so"
    gcc $CFLAGS $LDFLAGS $INCLUDES src/jcpuid.c -o lib/"$_objdir"/libjcpuid-${_arch}-linux.so
}

build_nativethread() {
    msg "Building libNativeThread..."
    cd "$srcdir/contrib/NativeThread"

    _objdir='freenet/support/io'
    LDFLAGS="-shared -Wl,-soname,libnative.so -lc"
    INCLUDES="-I${JAVA_HOME}/include -I${JAVA_HOME}/include/linux"
    javah -o NativeThread.h -classpath ../../fred/build/main freenet.support.io.NativeThread
    gcc -Wall -O3 -pipe -march=native -fPIC $LDFLAGS $INCLUDES NativeThread.c -o lib/"$_objdir"/libNativeThread-${__arch}.so
    jar -uf ../freenet-ext/dist/freenet-ext.jar -C lib "$_objdir"/libNativeThread-${__arch}.so
}

build_fec() {
    msg "Building onion-fec..."
    cd "$srcdir/contrib/onion-fec/src/csrc"

    CFLAGS+=" -fPIC -I${JAVA_HOME}/include" make
    _DEST="../../bin/lib/linux-${_arch}"
    mkdir -p "$_DEST"
    cp libfec*.so "$_DEST"
}

build_plugins() {
    for plugin in JSTUN UPnP; do
        msg "Building Plugin ${plugin}..."
        cd "$srcdir/plugin-${plugin}"
        ant dist \
            -Dfreenet-cvs-snapshot.location=../fred/dist/freenet.jar \
            -Dfreenet-ext.location=../contrib/freenet-ext/dist/freenet-ext.jar
    done
}

package() {
    cd "$srcdir/fred"

    install -dm755 "$pkgdir"/usr/bin
    install -dm700 "$pkgdir"/run/freenet
    install -dm755 "$pkgdir"/opt/freenet/{lib,plugins}

    #/fred # future readprotected homedir?
    install -m644 "$srcdir"/{wrapper.config,run.sh}      "$pkgdir"/opt/freenet
    install -m644 "$srcdir"/{freenet.ini,seednodes.fref} "$pkgdir"/opt/freenet
    install -m644 "$srcdir"/plugin-JSTUN/dist/JSTUN.jar  "$pkgdir"/opt/freenet/plugins
    install -m644 "$srcdir"/plugin-UPnP/dist/UPnP.jar    "$pkgdir"/opt/freenet/plugins
    install -m644 "$srcdir"/seednodes.fref               "$pkgdir"/opt/freenet/conf/noderef
    install -m644 "$srcdir"/bcprov.jar \
                  contrib/freenet-ext/dist/* \
                  dist/freenet.jar                       "$pkgdir"/opt/freenet/lib

    # launcher
    chmod +x "$pkgdir"/opt/freenet/run.sh
    ln -s /opt/freenet/run.sh "$pkgdir"/usr/bin/freenet

    # systemd
    install -dm755 "$pkgdir"/usr/lib/tmpfiles.d
    install -Dm644 "$srcdir"/freenet.service    "$pkgdir"/usr/lib/systemd/system/freenet.service
    echo 'd /run/freenet 0700 freenet freenet' >"$pkgdir"/usr/lib/tmpfiles.d/freenet.conf

    # license
    install -dm755 "$pkgdir"/usr/share/licenses/freenet
    install -m644 LICENSE "$pkgdir"/usr/share/licenses/freenet/LICENSE
}
