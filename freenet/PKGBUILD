pkgname=freenet
pkgver=0.7.5
pkgrel=6
pkgdesc="An encrypted network without censorship"
url="http://freenetproject.org/"
license=('GPL2')
arch=('any')
depends=('java-runtime')
install=freenet.install
source=(https://downloads.freenetproject.org/alpha/installer/${pkgname}07.tar.gz
        http://www.bouncycastle.org/download/bcprov-jdk15on-149.jar
        'fred.properties' 'contrib.properties' 'run.sh' 'freenet.service'
        'freenet.ini' 'wrapper.config' 'PKGBUILD.new')

sha256sums=('1f9b64e548eca091bfa96d93a7ab83ef78515924a6ba72dca5bd2ec8c5f2efd7'
            '96b4b8d46e38e5a08b5ee6eec23c5951d95bdac35c08f935e999bbbe6b15ba9d'
            'd38d5b60aff79620f05741a9acd698fb189a713d4a5fce2554ece9c75d2a1f37'
            'a217ab667adabdc71fe5a55c4a1f5b19fcb08d1689eb38dc7c068fdf5caab14f'
            '3e48a77324277569b93a6e4082ddcf4303435c86ebd005594f164cb0047997b3'
            '9f96126bf326ed5f82d1e653f71b0e288534b23a48b62a5288efe336fe0e739a'
            '162c4784443769296a6cd144bfc01f5ad339d6e621e339c0a04958f54abcbf07'
            '997b354379d1bd303a24d4944f84d923108b8817baa77af1dab878f32135ea5f'
            'SKIP')

build() {
  msg "Nothing to compile for $pkgname"
}

package() {
  cd "${srcdir}/${pkgname}"

  echo 'wrapper.java.classpath.3=bcprov-jdk15on-149.jar' >> wrapper.conf
  cp -aL "${srcdir}"/bcprov-jdk15on-149.jar .
  mkdir -p "${pkgdir}"/opt/${pkgname}
  cp -a . "${pkgdir}"/opt/${pkgname}/.
  chmod -R ug=rwX,o= "${pkgdir}"/opt/freenet

  # systemd unit file
  install -D -m 644 "${srcdir}"/freenet.service \
    "${pkgdir}"/usr/lib/systemd/system/freenet.service

  # Licenses
  install -d -m 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 license/* "${pkgdir}"/usr/share/licenses/${pkgname}
}
