# Maintainer: Valere Monseur <valere_monseur@hotmail.com>

pkgname=iptrafvol
_pkgname=iptrafficvolume
pkgver=0.3.4
pkgrel=3
pkgdesc="IP traffic volume logger using iptables"
arch=('i686' 'x86_64')
url="http://iptrafficvolume.sourceforge.net/"
license=('GPL')
depends=(iptables)
source=("http://downloads.sourceforge.net/project/${_pkgname}/${_pkgname}/${pkgver}/${pkgname}-${pkgver}.tgz")
md5sums=('21d96650dd6452792ad47a5636954053')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # generate man page
  pod2man iptrafvol.pl > iptrafvol.1
  gzip iptrafvol.1

  # install directories
  install -d ${pkgdir}/var/log/iptrafvol

  # install files
  install -D -m755 iptrafvol.pl ${pkgdir}/usr/sbin/iptrafvol.pl
  install -D -m644 iptrafvol.cgi ${pkgdir}/usr/share/${pkgname}/iptrafvol.cgi
  install -D -m644 orange.css ${pkgdir}/usr/share/${pkgname}/orange.css
  install -D -m644 Examples/iptrafvol_cron.d ${pkgdir}/usr/share/${pkgname}/iptrafvol_cron.d
  install -D -m644 Examples/iptrafvol_init.d ${pkgdir}/usr/share/${pkgname}/iptrafvol_init.d
  install -D -m644 iptrafvol.1.gz ${pkgdir}/usr/share/man/man1/iptrafvol.1.gz
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # adapt config to suits arch
  sed \
    -e 's|/sbin/iptables|/usr/sbin/iptables|' \
    -i "$pkgdir"/usr/sbin/iptrafvol.pl

  sed \
    -e 's|/usr/local/sbin/iptrafvol.pl|/usr/sbin/iptrafvol.pl|' \
    -i "$pkgdir"/usr/share/${pkgname}/iptrafvol.cgi \
    -i "$pkgdir"/usr/share/${pkgname}/iptrafvol_cron.d
}
