# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>

pkgname=psychopy
pkgver=1.79.01
pkgrel=1
pkgdesc="An experiment builder for neuroscience, psychology and psychophysics"
arch=(any)
url="http://www.psychopy.org"
license=(GPL)
# I think this is the minimal set of dependencies
makedepends=('python2-distribute')
depends=('python2' 'avbin' 'python2-numpy'
'python2-scipy' 'python2-pyglet<1.2' 'wxpython2.8' 'python2-imaging' 
'python2-matplotlib' 'python-lxml' 'python2-openpyxl' 'python2-pyo'
'python2-pyparallel')

optdepends=('python2-pyserial: Serial/Parallel port communication'
'parallel-python: parallel processing'
'flac: for working with google speech')
options=(!emptydirs)
source=(http://downloads.sourceforge.net/project/psychpy/PsychoPy/PsychoPy-${pkgver}.zip
python2.patch psychopy)
md5sums=('16dda75b7e3add7b809a0959ff3c042c'
         'd48861f6799bc0b4624b58274d2f61c2'
         'd1ade5e4870e8e6f69a4a3480579d135')

build() {
  cd "${srcdir}"/PsychoPy-${pkgver}
  patch -Np1 -i ../python2.patch
}
	 

package() {
  cd "$srcdir/PsychoPy-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  # remove this; it's Windows-only
  # rm ${pkgdir}/usr/bin/psychopy_post_inst.py
  install -m 755 ../psychopy ${pkgdir}/usr/bin/
}

