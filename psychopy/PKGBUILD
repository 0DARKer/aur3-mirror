# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>

pkgname=psychopy
pkgver=1.75.01
pkgrel=1
pkgdesc="An experiment builder for neuroscience, psychology and psychophysics"
arch=(any)
url="http://www.psychopy.org"
license=(GPL)
# I think this is the minimal set of dependencies
makedepends=('python2-distribute')
depends=('python2' 'avbin' 'python2-numpy'
'python2-scipy' 'pyglet<1.2' 'wxpython<2.9' 'python2-imaging' 
'python2-matplotlib' 'python-lxml' 'python2-openpyxl' 'python2-pyo')

optdepends=('python2-pyserial: Serial/Parallel port communication'
'parallel-python: parallel processing'
'flac: for working with google speech')
options=(!emptydirs)
source=(http://psychopy.googlecode.com/files/PsychoPy-${pkgver}.zip
python2.patch psychopy)
md5sums=('4b3281607ab26e12e070969740567765'
         '281f8bcfd3abeb3facd1aa3ec6941b77'
         'd1ade5e4870e8e6f69a4a3480579d135')

build() {
  cd "${srcdir}"/PsychoPy-${pkgver}
  patch -Np1 -i ../python2.patch
}
	 

package() {
  cd "$srcdir/PsychoPy-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  # remove this; it's Windows-only
  rm ${pkgdir}/usr/bin/psychopy_post_inst.py
  install -m 755 ../psychopy ${pkgdir}/usr/bin/
}

