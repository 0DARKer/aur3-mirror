# Maintainer: Daniel E. Shub <Daniel.Shub@nottingham.ac.uk>
# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>

pkgname=psychopy
pkgver=1.81.00
pkgrel=1
pkgdesc="An experiment builder for neuroscience, psychology and psychophysics"
arch=(any)
url="http://www.psychopy.org"
license=(GPL)
makedepends=('python2-setuptools')
depends=('glu' 'avbin7' 'xorg-fonts-type1' 'ttf-freefont' 'python2' 'wxpython' 'python2-numpy' 'python2-scipy' 'python2-imaging' 'python2-lxml' 'python2-pyglet' 'python2-matplotlib' 'python2-pygame' 'python2-opengl' 'python2-openpyxl1' 'python2-psutil' 'python2-yaml' 'python2-pytables' 'python2-gevent' 'python2-msgpack' 'python2-xlib')

optdepends=(
'python2-pyo: To use sound'
'python2-pyparallel: To use the parallel port'
'python2-pyserial: For interfacing with the serial port'
'parallel-python: For parallel processing'
'flac: For working with google speech'
'python2-pytest: For running unit tests'
'python2-coverage: For running unit tests'
'python2-sphinx: For building documentation'
)

options=(!emptydirs)
source=("http://downloads.sourceforge.net/project/psychpy/PsychoPy/PsychoPy-${pkgver}.zip")
md5sums=('885bc6457c95f9a29bf15d82a22646f8')

prepare() {
  sed -i 's/py.test /py.test2 /' "${srcdir}/PsychoPy-${pkgver}/${pkgname}/tests/run.py"
}

package() {
  (cd "${srcdir}/PsychoPy-${pkgver}"; python2 setup.py install --root="${pkgdir}/" --optimize=1)
  install -m 755 "${srcdir}/PsychoPy-${pkgver}/${pkgname}/app/psychopyApp.py" "${pkgdir}/usr/bin/"
  install -Dm644 "${srcdir}/PsychoPy-${pkgver}/${pkgname}/app/Resources/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${srcdir}/PsychoPy-${pkgver}/${pkgname}/app/Resources/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
