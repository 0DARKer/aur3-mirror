# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs-ofed
pkgname=opensm
pkgver=3.3.15
pkgrel=1
pkgdesc="InfiniBand subnet manager and administration"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibumad' 'bash')
source=("http://www.openfabrics.org/downloads/management/${pkgname}-${pkgver}.tar.gz"
        "opensm.service"
        "opensm@.service")
# cksum start
md5sums=('ad4c179c119bbb78be740e33b59746ad'
         '2d33e2a2ed5645cad1945ff273a13f7c'
         '055bfddb692af9fdb184fc09e10b1335')
sha1sums=('602405b0ecfeb7e616449d2c618c6a141c199d21'
          'e3bdf2bfb3f61422393415102b63311b00af48fb'
          '11f1d78ebcce657d1f1219fac484d5baf444ba06')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  install -Dm644 opensm.service "${pkgdir}/usr/lib/systemd/system/opensm.service"
  install -Dm644 "opensm@.service" "${pkgdir}/usr/lib/systemd/system/opensm@.service"
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  rm -rf "${pkgdir}/etc/init.d"
  mkdir -p "${pkgdir}/var/cache/opensm"
  mkdir -p "${pkgdir}/etc/opensm"
  touch "${pkgdir}/etc/opensm/partitions.conf"
}
