# Maintainer: Anthony Cornehl <accornehl[at]gmail[dot]com>
# https://github.com/twinshadow/abs-ofed
pkgname=opensm
pkgver=3.3.13
pkgrel=1
pkgdesc="InfiniBand subnet manager and administration"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibumad' 'bash')
source=("http://www.openfabrics.org/downloads/management/${pkgname}-${pkgver}.tar.gz"
        "opensmd.conf"
        "opensmd.rc")
md5sums=('6e667badb126a5e21ea2ab90cd950226'
         '656b728db029c55329754b99ebfdba7d'
         '15b7726746ff9f049475518b689b2f3e')
                  sha512sums=('a8185a13c833ce06dc40032f92fd0fb16625d3f10df5064d66558547f9a3078bcee35beb8d9dbca8d31367407e78b548513145751b021e53ac7a128383685aef'
'0c4feaf98e79288a0185740b3fcadd792eca706457ba2906aee6a8d1fcc9dc2547077ee84b927600689ee8c574ba87a298fc8678ad8e23148e2f5f909933834f'
'0e85c11766a0a2639b7dd58e308d96d7575ce15c22bee65920e806a8c1eec70d2da2c2adb113a413444551b564513047413c954ab9e0b1f659ed93f17118865f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  install -Dm755 opensmd.rc "${pkgdir}/etc/rc.d/opensmd"
  install -Dm644 opensmd.conf "${pkgdir}/etc/conf.d/opensmd"

  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  rm -rf "${pkgdir}/etc/init.d"
}
