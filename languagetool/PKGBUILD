#Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
#Contributor: Julien MISCHKOWITZ <wain@archlinux.fr>
#Contributor: Panagiotis Papadopoulos pano_90 (AT) gmx (DOT) net

pkgname=languagetool
pkgver=1.8
pkgrel=2
pkgdesc="An open source language checker"
arch=('x86_64' 'i686')
url="http://www.languagetool.org" 
license=("LGPL")
[ "$CARCH" = "i686"   ] && depends=('java-runtime>=6')
[ "$CARCH" = "x86_64" ] && depends=('java-runtime>=6' 'gcc-libs-multilib')
makedepends=('unzip' 'java-environment>=6')
optdepends=(
    'vim-languagetool: for Vi Improved support'
)
install=languagetool.install
source=(http://www.languagetool.org/download/LanguageTool-stable.zip?$pkgver languagetool.sh)
noextract=(LanguageTool-stable.zip?$pkgver)
md5sums=('1982ada9ba7578bce0c377cbe35dd25e'
         'dba78ff4f035ba079ba062924a7e121e')

build() {
  true
}

package() {
  cd "$srcdir"
  install -d "$pkgdir"/usr/{bin,share/languagetool}
  unzip -q LanguageTool-stable.zip?$pkgver -d "$pkgdir"/usr/share/languagetool/
  install -m755 languagetool.sh "$pkgdir"/usr/bin/languagetool
  [ "$CARCH" = "x86_64" ] && _otherarch=32 || _otherarch=64
  rm "$pkgdir"/usr/share/languagetool/libhunspell-linux-x86-${_otherarch}.so
  rm "$pkgdir"/usr/share/languagetool/hunspell-darwin*
  rm "$pkgdir"/usr/share/languagetool/hunspell-win*
}

