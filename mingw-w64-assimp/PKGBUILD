# Maintainer: Daniel Kirchner <daniel at ekpyron dot org>

pkgname=mingw-w64-assimp
pkgver=3.0.1270
pkgrel=6
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner (mingw-w64)"
arch=('any')
license=('BSD')
depends=('mingw-w64-boost' 'mingw-w64-crt')
makedepends=('mingw-w64-cmake' 'mingw-w64-gcc' 'unzip')
url=('http://assimp.sourceforge.net/index.html')
source=("http://downloads.sourceforge.net/project/assimp/assimp-3.0/assimp--${pkgver}-source-only.zip")
options=(!strip !buildflags staticlibs)
sha1sums=('e80a3a4326b649ed6585c0ce312ed6dd68942834')

_basename=assimp
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare()
{
    cd "${srcdir}"/assimp--${pkgver}-source-only

    # https://github.com/assimp/assimp/pull/255
    sed -i "48i#include <cstring>" include/assimp/types.h

    # https://github.com/assimp/assimp/pull/260
    sed -i "53ifind_package ( Boost )" CMakeLists.txt
}

build() 
{
	unset LDFLAGS
	
	for _arch in ${_architectures}; do
		rm -rf "${srcdir}/build-${_arch}-static"
		mkdir -p "${srcdir}/build-${_arch}-static"
		cd "${srcdir}/build-${_arch}-static"
	
		${_arch}-cmake ../${_basename}--${pkgver}-source-only \
			-DBUILD_ASSIMP_TOOLS=NO -DBUILD_STATIC_LIB=ON \
			-DCMAKE_BUILD_TYPE=RELEASE
		make
	done
	
	for _arch in ${_architectures}; do
		rm -rf "${srcdir}/build-${_arch}"
		mkdir -p "${srcdir}/build-${_arch}"
		cd "${srcdir}/build-${_arch}"
	
		${_arch}-cmake ../${_basename}--${pkgver}-source-only \
			-DBUILD_ASSIMP_TOOLS=NO \
			-DCMAKE_BUILD_TYPE=RELEASE
		make
	done
}

package () {
	for _arch in ${_architectures}; do
		cd "${srcdir}/build-${_arch}"
		make DESTDIR="${pkgdir}" install
		cd "${srcdir}/build-${_arch}-static"
		make DESTDIR="${pkgdir}" install
		${_arch}-strip -g ${pkgdir}/usr/${_arch}/lib/*.a
		install -d "${pkgdir}"/usr/${_arch}/bin
		mv "${pkgdir}"/usr/${_arch}/lib/*.dll "${pkgdir}"/usr/${_arch}/bin/
		${_arch}-strip --strip-unneeded "${pkgdir}"/usr/${_arch}/bin/*.dll
	done	

	install -Dm644 "${srcdir}/${_basename}--${pkgver}-source-only/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
