# Maintainer: Daniel Kirchner <daniel at ekpyron dot org>

pkgname=mingw-w64-assimp
pkgver=3.0.1270
pkgrel=2
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner (mingw-w64)"
arch=('any')
license=('BSD')
depends=('mingw-w64-boost' 'mingw-w64-crt')
makedepends=('cmake' 'mingw-w64-gcc' 'unzip')
url=('http://assimp.sourceforge.net/index.html')
source=("http://downloads.sourceforge.net/project/assimp/assimp-3.0/assimp--${pkgver}-source-only.zip")
options=(!strip !buildflags)
sha1sums=('e80a3a4326b649ed6585c0ce312ed6dd68942834')

_basename=assimp
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() 
{
	unset LDFLAGS
	
	for _arch in ${_architectures}; do
		rm -rf "${srcdir}/build-${_arch}"
		mkdir -p "${srcdir}/build-${_arch}"
		cd "${srcdir}/build-${_arch}"
			
    	echo "SET(CMAKE_SYSTEM_NAME Windows)" > win32.cmake
    	echo "SET(CMAKE_C_COMPILER ${_arch}-gcc)" >> win32.cmake
    	echo "SET(CMAKE_CXX_COMPILER ${_arch}-g++)" >> win32.cmake
    	echo "SET(CMAKE_RC_COMPILER ${_arch}-windres)" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH /usr/${_arch})" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)" >> win32.cmake
    	echo "SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)" >> win32.cmake	
	
		cmake ../${_basename}--${pkgver}-source-only \
			-DCMAKE_TOOLCHAIN_FILE=win32.cmake \
			-DCMAKE_INSTALL_PREFIX="/usr/${_arch}" \
			-DBUILD_ASSIMP_TOOLS=NO -DBUILD_STATIC_LIB=ON \
			-DCMAKE_BUILD_TYPE=RELEASE
		make
	done
}

package () {
	for _arch in ${_architectures}; do
		cd "${srcdir}/build-${_arch}"
		make DESTDIR="${pkgdir}" install
		${_arch}-strip -g ${pkgdir}/usr/${_arch}/lib/*.a
	done	

	install -Dm644 "${srcdir}/${_basename}--${pkgver}-source-only/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
