# Maintainer: Alexander Minges <alexander.minges@gmail.com>
pkgname=clipper
pkgver=2.1
_pkgver=2.1-110915
pkgrel=1
pkgdesc="A set of object-oriented libraries for the organisation of crystallographic data"
arch=('i686' 'x86_64')
url="http://www.ysbl.york.ac.uk/~cowtan/clipper/clipper.html"
license=('LGPL')
depends=('ccp4-libs' 'mmdb' 'fftw2-float' 'ssmlib')
source=(http://www2.mrc-lmb.cam.ac.uk/groups/murshudov/coot/dependencies/$pkgname-$_pkgver-fixup-2.tar.gz
        http://www2.mrc-lmb.cam.ac.uk/groups/murshudov/coot/dependencies/clipper_test_contrib.cpp.patch)
sha256sums=('622a6d021d2fe524d81ba25f5d096bab52c85a5fba72c957833bdb2abf82cd9f'
            'cc93c20bedeb890f95b780649a42ce14bb8e43fb75156d7580531c98ce5e550e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i "$srcdir/clipper_test_contrib.cpp.patch"

  ./configure --prefix=/usr \
              --with-mmdb=`pkg-config mmdb --variable=prefix` \
              --enable-shared \
              --enable-ccp4 \
              --enable-cif \
              --enable-mmdb \
              --enable-minimol \
              --enable-cns
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
} 
