# Maintainer: Ner0

pkgname=elementary-icons-bzr
pkgver=1072
pkgrel=1
pkgdesc="An icon theme designed to be smooth, sexy, clear, and efficient (Development branch)"
arch=('any')
url="https://launchpad.net/elementaryicons"
license=('GPL2')
groups=('pantheon')
depends=('hicolor-icon-theme')
makedepends=('bzr')
optdepends=('egtk-bzr: Development branch of the elementary GTK theme. [AUR]')
conflicts=('elementary-icons')
provides=('elementary-icons')
options=('!strip' '!emptydirs')
source=('elementary-icons::bzr+http://bazaar.launchpad.net/~danrabbit/elementaryicons/3.0/'
        'chrome-chromium.patch')
md5sums=('SKIP'
         'f1ac694e313d9fe991c036aac62ee425')

pkgver() {
  cd "$srcdir/elementary-icons"
  bzr revno
}

prepare() {
  cd "$srcdir/elementary-icons"

  # Add Desktop icon from elementary-xfce and fix home icon in Nemo's sidebar
  for _size in 16 22 24 32 48 64 128; do
    curl -so places/$_size/user-desktop.svg \
    https://raw.github.com/shimmerproject/elementary-xfce/master/elementary-xfce/places/$_size/user-desktop.svg
    ln -fs user-home.svg places/$_size/folder-home.svg
    ln -fs user-home.svg places/$_size/folder_home.svg
  done

  # Add Chrome/Chromium icons (http://spg76.deviantart.com/art/Chrome-Chromium-elementary-201289232)
  patch -Np0 -i "$srcdir/chrome-chromium.patch"
}

package() {
  install -dm755 "$pkgdir/usr/share/icons/"
  cp -dr --no-preserve=ownership "$srcdir/elementary-icons" "$pkgdir/usr/share/icons/elementary"
  rm -rf "$pkgdir"/usr/share/icons/elementary/{.bzr*,AUTHORS,CONTRIBUTORS,COPYING}
}
