# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: monson <holymonson@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=cgminer
pkgver=2.4.4
_build=
pkgrel=1
pkgdesc="Multi-threaded multi-pool CPU and GPU miner for bitcoin, forked from cpuminer."
url='http://forum.bitcoin.org/index.php?topic=28402.0'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ncurses' 'curl' 'libcl')
makedepends=('opencl-headers')
optdepends=('opencl-nvidia: OpenCL implementation for NVIDIA'   \
            'intel-opencl-sdk: OpenCL implementation for Intel' \
            'amdstream: OpenCL implementation for AMD')
source=("cgminer.tar.gz::https://github.com/ckolivas/cgminer/tarball/v$pkgver${_build}"
        "$pkgname.conf.d"
        "$pkgname")
backup=("etc/conf.d/$pkgname" "etc/$pkgname.conf")

md5sums=('f5c83d340e1b5b7ad6cef567deb3483c'
         'fe4a243fabe24608f5c05e40f0f118f6'
         'ee39698273671fee0e98d4af16014c36')

[ $CARCH == 'x86_64' ] && makedepends+=('yasm')

build() {
  cd "$srcdir"
  rm -rf "$pkgname-$pkgver${_build}"
  mv ckolivas-cgminer* $pkgname-$pkgver${_build}
  cd $pkgname-$pkgver${_build}

  # Use in-tree jansson since it is not compatible with jansson 2.0
  #sed -e 's/^AC_CHECK_LIB(jansson, json_loads, request_jansson=false, request_jansson=true)$/request_jansson=true/' -i configure.ac

  # Here you may want to use custom CFLAGS
  #export CFLAGS="-O2 -march=native -mtune=native -msse2"

  ./autogen.sh
  ./configure --prefix=/usr --enable-cpumining
  
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver${_build}

  make DESTDIR="$pkgdir" install

  install -Dm755 "$srcdir"/$pkgname "$pkgdir"/etc/rc.d/$pkgname
  install -Dm644 "$srcdir"/$pkgname.conf.d "$pkgdir"/etc/conf.d/$pkgname
  sed 's#/usr/local/bin#/usr/bin#g' example.conf > $pkgname.conf
  install -Dm644 $pkgname.conf "$pkgdir"/etc/$pkgname.conf
}
