# Maintainer: monson <holymonson@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=cgminer
pkgver=2.2.4
pkgrel=1
pkgdesc="Multi-threaded multi-pool CPU and GPU miner for bitcoin, forked from cpuminer."
url='http://forum.bitcoin.org/index.php?topic=28402.0'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ncurses' 'curl' 'libcl')
makedepends=('opencl-headers')
optdepends=('opencl-nvidia: OpenCL implementation for NVIDIA'   \
            'intel-opencl-sdk: OpenCL implementation for Intel' \
            'amdstream: OpenCL implementation for AMD')
source=("http://ck.kolivas.org/apps/cgminer/$pkgname-$pkgver.tar.bz2"
        "$pkgname.conf.d"
        "$pkgname")
backup=("etc/conf.d/$pkgname" "etc/$pkgname.conf")
md5sums=('f034210161676458d01ef7ca5b54e11d'
         'f619d7b83172dc9cb8463535f1b73091'
         '5a5a02c2fd8f01a4bcb301a85b6c8957')

[ $CARCH == 'x86_64' ] && makedepends+=('yasm')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  # Use in-tree jansson since it is not compatible with jansson 2.0
  #sed -e 's/^AC_CHECK_LIB(jansson, json_loads, request_jansson=false, request_jansson=true)$/request_jansson=true/' -i configure.ac

  # Here you may want to use custom CFLAGS
  #export CFLAGS="-O2 -march=native -mtune=native -msse2"

  ./configure --prefix=/usr --enable-cpumining
  
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm755 "$srcdir"/$pkgname "$pkgdir"/etc/rc.d/$pkgname
  install -Dm644 "$srcdir"/$pkgname.conf.d "$pkgdir"/etc/conf.d/$pkgname
  install -Dm644 "example.conf" "$pkgdir"/etc/$pkgname.conf
}
