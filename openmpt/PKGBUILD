# Maintainer: Simon Thorpe <simon@hivetechnology.com.au>
pkgname=openmpt
pkgver=1.24.04.00
pkgrel=1
pkgdesc="Audio module tracker formerly known as ModPlug Tracker"
arch=('i686' 'x86_64')
url="http://openmpt.org/"
license=('BSD')
depends=('wine-mono' 'wine_gecko')
makedepends=('gendesk' 'imagemagick')
optdepends=('bash-completion: tab completion support')
source_i686=("http://sagamusix.de/openmpt/download/OpenMPT-$pkgver.zip")
source_x86_64=("http://sagamusix.de/openmpt/download/OpenMPT-$pkgver-x64.zip")
sha256sums_i686=('0e77ab7438402e83586ae8d76450ebcfcf9e349a2f41d627d2922fe114ae17f4')
sha256sums_x86_64=('1a6aa0b4e1a5242b51477c5809d310142ff3c59982c7988cd1ee1761be314d19')

prepare(){
  convert "$srcdir/OpenMPT-$pkgver/mpt.ico" "$srcdir/icon.png"
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --name='OpenMPT' \
    --mimetype='audio/x-mod;audio/x-s3m;audio/x-xm;audio/x-it;audio/x-mptm' \
    --categories 'Audio;Sequencer;Midi;AudioVideoEditing;Music;AudioVideo;'
}

package(){
  mkdir -p $pkgdir/usr/share
  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/share/bash-completion/completions
  cp -R $srcdir/OpenMPT-$pkgver $pkgdir/usr/share/openmpt
  echo -e '#!/bin/bash\n[[ "$1" == "" ]] && wine /usr/share/openmpt/mptrack.exe\n[[ "$1" != "" ]] && wine /usr/share/openmpt/mptrack.exe "$(winepath -w "$1")"' >$pkgdir/usr/bin/openmpt
  chmod +x $pkgdir/usr/bin/openmpt
  install -Dm644 "$srcdir/icon-2.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  
  mkdir -p $pkgdir/usr/share/mime/application
  echo -e '<?xml version="1.0" encoding="utf-8"?><mime-type xmlns="http://www.freedesktop.org/standards/shared-mime-info" type="audio/x-mptm"><glob pattern="*.mptm"/><comment>OpenMPT Module</comment></mime-type>' >"$pkgdir/usr/share/mime/application/x-mptm.xml"
  
  # This list of supported file extensions was taken from the features page of the website. There is probably a more complete list somewhere.
  echo "_openmpt() { local cur prev words cword split; _init_completion -s || return; _filedir '@(669|AMF|AMS|DBM|DIGI|DMF|DSM|FAR|GDM|ICE|ST26|IMF|IT|ITP|J2B|M15|STK|MDL|MED|MO3|MOD|MPTM|MT2|MTM|OKT|PLM|PSM|PTM|S3M|STM|ULT|UMX|WOW|XM)'; } && complete -F _openmpt openmpt" >$pkgdir/usr/share/bash-completion/completions/openmpt
}
