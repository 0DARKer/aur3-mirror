# Maintainer: Christopher Arndt <chris@chrisarndt.de>
# Contributor: Laszlo Papp <djszapi at archlinux dot us>
# $Id: PKGBUILD 1326 2015-04-17 21:26:20Z carndt $

_pkgname=python-parched
pkgname="${_pkgname}-git"
pkgver=0.1.r25.4c3ec3f
pkgrel=2
pkgdesc="A Pacman package and PKGBUILD parser module for Python"
arch=('any')
url="http://github.com/sebnow/parched"
license=('MIT')
depends=('python')
makedepends=('git')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
options=(!emptydirs)
source=("${_pkgname}::git+http://github.com/sebnow/parched.git"
        'LICENSE'
        'parched-assignment.patch'
        'parched-python2-tests.patch')
md5sums=('SKIP'
         '31eccf56949504b5ce33110068948cf2'
         'a3397cfc40ce25f7455360c6481dbeff'
         'a71968991213d10e25f4ee5f08c5edf8')


pkgver() {
  cd "${srcdir}/${_pkgname}"

  ver="$(PYTHONPATH=docs python -c 'import conf; print(conf.version)')"
  echo ${ver}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${srcdir}/${_pkgname}"

  patch -p1 -i "${srcdir}/parched-assignment.patch"
  patch -p1 -i "${srcdir}/parched-python2-tests.patch"
}

build() {
  cd "${srcdir}/${_pkgname}"

  python setup.py build
}

package() {
  cd "${srcdir}/${_pkgname}"

  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
