# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: florianbw <florian(dot)bw(at)gmail(dot)com>
# Contributor: Frederic Bezies <fredbezies(at)gmail(dot)com>
# Contributor: Robert Chmielowiec <robert(at)chmielowiec(dot)net>
# Contributor: Daniel <quite(at)hack(dot)org>
# Contributor: mariusz <my(dot)swiat(at)o2(dot)pl>
# Contributor: p-we <wdirksen(at)gmail(dot)com>

pkgname=shotwell-git
_pkgname=shotwell
pkgver=20120325
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('desktop-file-utils' 'gconf' 'gnome-vfs' 'json-glib' 'libgee' 'libgexiv2-git' 'libgphoto2' 'libquicktime' 'librsvg' 'libunique3' 'libwebkit3' 'dconf' 'rest')
makedepends=('git' 'intltool' 'libraw' 'pkgconfig' 'vala>=0.15.2')
provides=('shotwell')
conflicts=('shotwell')
install=${_pkgname}.install
         
_gitroot=git://yorba.org/${_pkgname}
_gitname=${_pkgname}

build() {
  cd ${srcdir}
  msg "Connecting to GIT server...."
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done or server timeout"

  msg "Starting make..."
  rm -rf ${srcdir}/${_gitname}-build
  git clone ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
  cd ${srcdir}/${_gitname}-build
  
  ./configure --prefix=/usr \
    --disable-schemas-compile \
    --disable-desktop-update \
    --disable-icon-update
  make
}

package() {
  cd ${srcdir}/${_gitname}-build
  make DESTDIR=${pkgdir} install
}
