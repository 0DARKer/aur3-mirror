# Maintainer:  Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: florianbw <florian(dot)bw(at)gmail(dot)com>
# Contributor: Frederic Bezies <fredbezies(at)gmail(dot)com>
# Contributor: Robert Chmielowiec <robert(at)chmielowiec(dot)net>
# Contributor: Daniel <quite(at)hack(dot)org>

pkgname=shotwell-git
_pkgname=shotwell
pkgver=20110427
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('desktop-file-utils' 'gconf' 'gexiv2-svn' 'gnome-vfs' 'json-glib' 'libgee' 'libgphoto2' 'libquicktime' 'librsvg' 'libunique' 'libwebkit')
makedepends=('git' 'intltool' 'libraw' 'pkgconfig' 'vala>=0.11.7')
provides=('shotwell')
conflicts=('shotwell')
install=${_pkgname}.install
         
_gitroot=git://yorba.org/${_pkgname}
_gitname=${_pkgname}

build() {
  cd ${srcdir}

  msg "Connecting to GIT server..."
  if [ -d ${_gitname} ]; then
    (cd ${_gitname} && git pull)
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT cloning done or server timeout."

  msg "Starting make..."
  rm -rf ./${_gitname}-build
  git clone ${_gitname} ${_gitname}-build

  cd ${srcdir}/${_gitname}-build
  
  ./configure --prefix=/usr \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
    --disable-schemas-install \
    --disable-desktop-update \
    --disable-icon-update
  make
}

package() {
  cd ${srcdir}/${_gitname}-build

  make DESTDIR=${pkgdir} install
}
