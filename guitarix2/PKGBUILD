# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=guitarix2
pkgver=0.23.3
pkgrel=1
pkgdesc="A simple mono amplifier to be used in a 'JACKified' environment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/wordpress/guitarix/"
license=('GPL')
depends=('gtkmm' 'jack' 'fftw' 'liblrdf' 'boost-libs')
makedepends=('python2' 'boost' 'intltool')
optdepends=('meterbridge: sound meters')
conflicts=("guitarix" 'gx_head')
replaces=("guitarix" 'gx_head')
install="$pkgname.install"
source=("http://download.sourceforge.net/project/guitarix/guitarix/$pkgname-$pkgver.tar.bz2")
md5sums=('e5963cc77c152f6bcccffccf178fda5b')

build() {
  cd "$srcdir/guitarix-$pkgver"

  # do not call both ldconfig
  # and update-desktop-database
  sed -e '/bld.env..GX_LIB_SHARED/,+1d' \
      -e "/\.commands/,/pass/d" -i wscript

  python2 waf configure --prefix=/usr \
                        --includeresampler \
                        --includeconvolver \
                        --no-faust \
                        --optimization \
                        --shared-lib
  python2 waf build
}

package() {
  cd "$srcdir/guitarix-$pkgver"
  python2 waf install --destdir="$pkgdir"
}
