# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=guitarix
pkgname=${_name}2
pkgver=0.21.0
pkgrel=1
pkgdesc="A simple mono amplifier to be used in a 'JACKified' environment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/wordpress/guitarix/"
license=('GPL')
depends=('gtkmm' 'jack' 'fftw' 'boost-libs')
makedepends=('python2' 'boost' 'intltool')
optdepends=('meterbridge: sound meters')
conflicts=("$_name" 'gx_head')
replaces=("$_name" 'gx_head')
install="$pkgname.install"
source=("http://switch.dl.sourceforge.net/project/$_name/$_name/$pkgname-$pkgver.tar.bz2")
md5sums=('ed40664453c9b2b61240f46ffee62f14')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # strip ldconfig call
  sed -i '/bld.env.*GX_LIB_SHARED/,+1d' wscript

  python2 waf configure --prefix=/usr \
                        --includeresampler \
                        --includeconvolver \
                        --no-faust \
                        --optimization \
                        --shared-lib
  python2 waf build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"
}
