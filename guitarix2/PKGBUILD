# Maintainer: speps <speps at aur dot archlinux dot org>

_name=guitarix
pkgname=${_name}2
pkgver=0.23.1
pkgrel=1
pkgdesc="A simple mono amplifier to be used in a 'JACKified' environment."
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/wordpress/guitarix/"
license=('GPL')
depends=('gtkmm' 'jack' 'fftw' 'liblrdf' 'boost-libs')
makedepends=('python2' 'boost' 'intltool')
optdepends=('meterbridge: sound meters')
conflicts=("$_name" 'gx_head')
replaces=("$_name" 'gx_head')
install="$pkgname.install"
source=("http://download.sourceforge.net/project/$_name/$_name/$pkgname-$pkgver.tar.bz2")
md5sums=('5f5d5734333330818c559aebbc5e609d')

build() {
  cd "$srcdir/$_name-$pkgver"

  # do not call both ldconfig
  # and update-desktop-database
  sed -e '/bld.env.*GX_LIB_SHARED/,+1d' \
      -e "/\.commands/,/pass/d" -i wscript

  python2 waf configure --prefix=/usr \
                        --includeresampler \
                        --includeconvolver \
                        --no-faust \
                        --optimization \
                        --shared-lib
  python2 waf build
}

package() {
  cd "$srcdir/$_name-$pkgver"
  python2 waf install --destdir="$pkgdir"
}
