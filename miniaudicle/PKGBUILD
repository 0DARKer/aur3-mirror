# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: masutu <masutu dot arch at googlemail dot com>

_name=miniAudicle
pkgname=miniaudicle
pkgver=0.2.2
pkgrel=2
pkgdesc="A lightweight IDE for the ChucK digital audio programming language"
arch=('i686' 'x86_64')
url="http://audicle.cs.princeton.edu/mini/"
license=('GPL')
if [[ "$CARCH" = 'x86_64' ]]; then
  provides=('bin32-miniaudicle')
  conflicts=('bin32-miniaudicle')
  replaces=('bin32-miniaudicle')
fi
depends=('jack' 'wxgtk')
source=("http://$pkgname.googlecode.com/files/$_name-$pkgver.tgz")
md5sums=('3951ef3c59dca796c7c3668f1208a4e5')

build() {
  cd "$srcdir/$_name-$pkgver/src"

  # do not force 32bit build for x86_64
  sed -i "s/-m32//g" makefile.jack

  make -f makefile.jack
}

package() {
  cd "$srcdir/$_name-$pkgver/src/wxw"
  install -Dm755 $_name "$pkgdir/usr/bin/miniaudicle"
}