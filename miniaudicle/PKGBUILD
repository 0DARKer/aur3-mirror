# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: masutu <masutu dot arch at googlemail dot com>

_name=miniAudicle
pkgname=miniaudicle
pkgver=0.2.2
pkgrel=1
pkgdesc="A lightweight IDE for the ChucK digital audio programming language"
arch=('i686' 'x86_64')
url="http://audicle.cs.princeton.edu/mini/"
license=('GPL')
if [[ "$CARCH" = 'x86_64' ]]; then
  depends=('lib32-jack' 'lib32-alsa-lib' 'lib32-libsndfile' 'lib32-wxgtk')
  makedepends=('gcc-multilib')
  provides=('bin32-miniaudicle')
  conflicts=('bin32-miniaudicle')
  replaces=('bin32-miniaudicle')
else
  depends=('jack' 'wxgtk')
fi
source=("http://$pkgname.googlecode.com/files/$_name-$pkgver.tgz")
md5sums=('3951ef3c59dca796c7c3668f1208a4e5')

build() {
  cd "$srcdir/$_name-$pkgver/src"

  # x86_64 multilib build configuration
  if [ $CARCH = 'x86_64' ]; then
    unset CFLAGS
    _WXCONFIG="/usr/lib32/wx/config/gtk2-unicode-release-2.8"

    sed "s|wx-config|$_WXCONFIG|g" \
        -i makefile.jack
  fi

  make -f makefile.jack
}

package() {
  cd "$srcdir/$_name-$pkgver/src/wxw"
  install -Dm755 $_name "$pkgdir/usr/bin/miniaudicle"
}