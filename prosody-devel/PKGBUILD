# Maintainer: Dwayne Bent <dbb.1@liqd.org>
# Contributor: Paul-Sebastian Manole <brokenthorn@gmail.com>

pkgname=prosody-devel
pkgver=0.7.0rc1
pkgrel=1
pkgdesc="Lightweight and extensible Jabber/XMPP server written in Lua."
arch=('i686' 'x86_64')
url="http://prosody.im/"
license=('MIT')
depends=('lua>=5.1' 'luasocket' 'luaexpat' 'luafilesystem' 'libidn>=0.5.18')
optdepends=('lua-cyrussasl: Cyrus SASL support'
            'lua-zlib: stream compression support'
            'luaevent-prosody: high performance eventing support'
            'luasec: TLS stream encryption suport')
conflicts=('prosody')
backup=('etc/prosody/prosody.cfg.lua'
        'etc/logrotate.d/prosody')
install=prosody.install
source=("http://prosody.im/depot/$pkgver/prosody-$pkgver.tar.gz"
        'prosody.cfg.lua'
        'prosody.rcd'
        'prosody.logrotated')
md5sums=('abce03d8715ba43ee591826be5c7eed8'
         'e0a9d47f94d89ae84e774e7a16870b81'
         'f3471e32539c918c0ff2bc07c49f1915'
         '535f71eff00e4f9bc4e4f9a95677c780')

build() {
    cd "$srcdir/prosody-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc/prosody \
        --datadir=/var/lib/prosody || return 1
    make || return 1
    make DESTDIR="$pkgdir" install || return 1

    rm $pkgdir/etc/prosody/certs/*

    install -d "$pkgdir/etc/rc.d" || return 1
    install -d "$pkgdir/etc/logrotate.d" || return 1
    install -d "$pkgdir/var/log/prosody" || return 1
    install -d "$pkgdir/var/run/prosody" || return 1
    install -d "$pkgdir/usr/share/licenses/prosody" || return 1

    install -o root -g root -m 644 "$startdir/prosody.cfg.lua" \
        "$pkgdir/etc/prosody/prosody.cfg.lua" || return 1
    install -o root -g root -m 755 "$startdir/prosody.rcd" \
        "$pkgdir/etc/rc.d/prosody" || return 1
    install -o root -g root -m 644 "$startdir/prosody.logrotated" \
        "$pkgdir/etc/logrotate.d/prosody" || return 1
    install -o root -g root -m 644 "COPYING" \
        "$pkgdir/usr/share/licenses/prosody" || return 1
}

