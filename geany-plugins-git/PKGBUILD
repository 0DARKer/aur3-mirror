# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: eht16 <enrico.troeger@uvena.de>
# Contributor: Addict7 <nicolasfloquet@gmail.com>

pkgname=geany-plugins-git
pkgver=1.24.33.gd97316d
pkgrel=1
pkgdesc='Various plugins for Geany'
arch=('x86_64' 'i686')
url='http://plugins.geany.org/'
license=('GPL')
depends=('geany' 'vte' 'lua' 'libwebkit' 'ctpl' 'gpgme' 'gtkspell' 'hicolor-icon-theme')
makedepends=('git' 'libtool' 'python' 'gpgme' 'ctpl' 'lua' 'intltool' 'vala')
optdepends=('hspell: hebrew spell checker')
install="$pkgname.install"
provides=('geany-plugins')
conflicts=('geany-plugins-svn' 'geany-plugins')
source=('geany-plugins::git://github.com/geany/geany-plugins.git')
sha256sums=('SKIP')

pkgver() {
  cd geany-plugins

  git describe --tags | sed 's/-/./g'
}

prepare() {
  cd geany-plugins

  ./autogen.sh
}

build() {
  cd geany-plugins

  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  make -C geany-plugins DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
