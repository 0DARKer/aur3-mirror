# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: eht16 <enrico.troeger@uvena.de>
# Contributor: Addict7 <nicolasfloquet@gmail.com>

pkgname=geany-plugins-git
pkgver=1.23.0.108.ga486ecf
pkgrel=1
pkgdesc='Various plugins for Geany'
arch=('x86_64' 'i686')
url='http://plugins.geany.org/'
license=('GPL')
depends=('geany-git' 'vte' 'lua' 'libwebkit' 'ctpl' 'gpgme' 'gtkspell' 'hicolor-icon-theme')
makedepends=('git' 'libtool' 'python' 'gpgme' 'ctpl' 'lua')
optdepends=('hspell: hebrew spell checker')
install='geany-plugins.install'
provides=('geany-plugins')
conflicts=('geany-plugins-svn' 'geany-plugins')
source=('geany-plugins::git://github.com/geany/geany-plugins.git')
sha256sums=('SKIP')

pkgver() {
  cd geany-plugins
  git describe --tags | sed 's/-/./g'
}

build() {
  cd "$srcdir/geany-plugins"

  ./waf configure --prefix=/usr --libexecdir=/usr/lib --skip-plugins=geanylua
  ./waf build
}

package() {
  cd "$srcdir/geany-plugins"

  ./waf --destdir="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
