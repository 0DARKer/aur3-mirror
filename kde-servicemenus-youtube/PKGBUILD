pkgname=kde-servicemenus-youtube
pkgver=1.10
pkgrel=1
pkgdesc="Konqueror/KDE service menu to download youtube videos"
arch=('any')
url="http://kde-look.org/content/show.php/Get+YouTube+Video+(improved)?content=41456"
license=('GPL')
depends=('kdebase-workspace' 'python2-lxml')
conflicts=('youtube-servicemenu')
replaces=('youtube-servicemenu')
source=("http://kde-look.org/CONTENT/content-files/41456-youtube-servicemenu-${pkgver}.tar.bz2")
md5sums=('680247e63a1c85e5a6e8e2c4ec3aa419')

package() {
  cd youtube-servicemenu-${pkgver}

  sed -i 's/^ServiceTypes=.*/Type=Service\nServiceTypes=KonqPopupMenu\/Plugin,text\/html/' get_yt_video.desktop
  
  sed -i 's/python/python2/' get_yt_video.py

  install -d ${pkgdir}/usr/{bin,share/kde4/services,lib/get_yt_video/}
  install -m755 get_yt_video.py ${pkgdir}/usr/bin/
  install -m644 get_yt_video.desktop ${pkgdir}/usr/share/kde4/services/
  
  find l10n -mindepth 1 -maxdepth 1 -type d -exec cp -r {} ${pkgdir}/usr/lib/get_yt_video/ \;
}
