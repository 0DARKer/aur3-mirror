#Contributor: Sam Stuewe <halosghost at archlinux dot info>
_name=epoptes
pkgname="${_name}-bzr"
pkgver=0.5.7.417
pkgrel=1
pkgdesc='An opensource computer lab management and monitoring tool'
makedepends=('bzr' 'python-distutils-extra')
url='http://www.epoptes.org/'
license=('GPL3')
depends=('python')
arch=('i686' 'x86_64')
conflicts=("${_name}")
provides=("${_name}")
source=("${_name}::bzr+http://bazaar.launchpad.net/~${_name}/${_name}/trunk/")
sha256sums=('SKIP')

pkgver () {
    cd "${srcdir}/${_name}"
    printf '%s.%s' "$(bzr tags --sort=time | tail -n1 | cut -d '-' -f1)" "$(bzr revno)"
}

prepare () {
    cd "${srcdir}/${_name}"
    sed -e 's/sbin/bin/g' \
        -i setup.py
}

package () {
    cd "${srcdir}/${_name}"
    python setup.py install --root="${pkgdir}/" --optimize=1
}
