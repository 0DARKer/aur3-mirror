# Maintainer: Ariel Popper <a@arielp.com>
pkgname=vim-airline-git
pkgver=65.d102428
pkgrel=1
pkgdesc="A lean & mean statusline for vim that's light as air."
arch=(any)
url="https://github.com/bling/vim-airline"
license=('GPL')
groups=()
depends=('vim-runtime')
optdepends=('otf-powerline-symbols-git: Use the Powerline symbols')
makedepends=('git')
install=airline.install
md5sums=('SKIP')

_gitname=airline
_gitbranch=master
source=("${_gitname}::git+https://github.com/bling/vim-airline.git#branch=${_gitbranch}")

pkgver() {
	cd "${_gitname}"
	echo "$(git rev-list --count ${_gitbranch}).$(git rev-parse --short ${_gitbranch})"
}

package() {
  cd "$srcdir/$_gitname"
  local vimfiles="$pkgdir/usr/share/vim/vimfiles"
  install -Dm644 plugin/airline.vim "$vimfiles/plugin/airline.vim"
  install -Dm644 doc/airline.txt "$vimfiles/doc/airline.txt"
  install -dm755 "$vimfiles/autoload/airline/themes"
  install -m644 autoload/airline/themes/*.vim "$vimfiles/autoload/airline/themes"
}

# vim:set ts=2 sw=2 et:
