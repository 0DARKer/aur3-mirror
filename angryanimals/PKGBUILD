# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=angryanimals
pkgver=27.03_01
pkgrel=1
arch=('any')
pkgdesc='Launch the animals into the building to destroy it! (Angry Birds-like game)'
url='http://www.free-games.net/play/angry-animals'
license=('custom')
depends=('flashplayer-standalone')
makedepends=('imagemagick')
source=("http://content.free-games.net/games/${pkgname}.swf"
	'http://www.angrybirdspc.it/games/images/angry-animals.jpg')
md5sums=('a9523aaeedef77266375fa91606a5653'
         '05ed55216a18d335ab19264ce32880d1')
package(){
    # Install icon
    convert angry-animals.jpg $pkgname.png
    install -D -m644  $pkgname.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png

    # Install the .swf file
    install -D -m644 ${pkgname}.swf ${pkgdir}/usr/share/${pkgname}/${pkgname}.swf

    # Make desktop  launcher and install it
    DESKTOP=angryanimals.desktop 	
    cat >  $DESKTOP << EOF
[Desktop Entry]
Type=Application
Terminal=false
Exec=flashplayer /usr/share/${pkgname}/${pkgname}.swf
Name=Angry Animals
Comment=Launch the animals into the building to destroy it!
Icon=${pkgname}.png
Categories=Game;ActionGame;
EOF
    install -D -m644 $DESKTOP ${pkgdir}/usr/share/applications/$DESKTOP
    
}
