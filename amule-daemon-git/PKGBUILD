# Maintainer: Ivan Zenin <i.zenin@gmx.com>

pkgname=amule-daemon-git
pkgver=head
pkgrel=1
pkgdesc='An eMule-like client for the eD2k and Kademlia p2p networks (daemon only, git version)'
url='http://www.amule.org'
arch=('i686' 'x86_64')
license=('GPL')
depends=('crypto++' 'wxbase>=3.1')
makedepends=('git')
conflicts=('amule')
source=('git+git://repo.or.cz/amule.git'
        'amuled.service'
        'amuled@.service'
        'amuled.tmpfile')
md5sums=('SKIP'
         '42452aa3b4a2847022b3f450f5bc7e78'
         '253895ed04947fa95ca8fcaee9aeee10'
         '1bfc1a1974803352d11f656c58448f15')
install="amule-daemon-git.install"

pkgver() {
  cd "${SRCDEST}/amule"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/amule"
  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-monolithic \
      --disable-amule-gui \
      --disable-webserver \
      --disable-plasmamule \
      --disable-kde-in-home \
      --disable-xas \
      --disable-upnp \
      --disable-nls \
      --disable-cas \
      --disable-wxcas \
      --disable-alc \
      --disable-alcc \
      --disable-optimize \
      --disable-ccache \
      --disable-debug \
      --disable-geoip \
      --disable-fileview \
      --disable-amulecmd \
      --enable-amule-daemon \
      --with-wxdir=/usr/include/wx-3.1 \
      --with-wx-config=/usr/bin/wx-config
  make
} 

package() {
  cd "${srcdir}/amule"
  make DESTDIR="${pkgdir}" install
  find "${pkgdir}" -type d -name .git -exec rm -r '{}' +
  install -Dm644 "${srcdir}/amuled.service" "${pkgdir}/usr/lib/systemd/system/amuled.service"
  install -Dm644 "${srcdir}/amuled@.service" "${pkgdir}/usr/lib/systemd/system/amuled@.service"
  install -Dm644 "${srcdir}/amuled.tmpfile" "${pkgdir}/usr/lib/tmpfiles.d/amuled.conf"
  install -dm750 -o129 -g129 "${pkgdir}/var/lib/amuled"
  rm -fr "${pkgdir}/usr/share"/{pixmaps,applications}
}
