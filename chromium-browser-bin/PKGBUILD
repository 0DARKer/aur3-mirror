# $Id:$
# Credits: alexwizard, thotypous, jdhore, xduugu and randypenguin
# Contributor: Balwinder S "bsd" Dheeman (bdheeman AT gmail.com)
# Maintainer: Ner0

pkgname=chromium-browser-bin
_realname=chromium
pkgver=LATEST-35
pkgrel=1
pkgdesc="The open-source project behind Google Chrome (Web/HTTP/FTP Browser)"
arch=('i686' 'x86_64')
url="http://code.google.com/chromium/"
license=('custom:BSD')
depends=('alsa-lib' 'desktop-file-utils' 'gconf' 'gtk2' 'nss' 'libxss' 'libxtst')
optdepends=('ttf-hannom: Unicode Han and Nom (Chinese and Vietnamese) fonts [extra]'
    'otf-ipafont: Unicode Gothic/sans and Mincho/serif (Japanese) fonts [AUR]'
    'ttf-indic-otf: Unicode collection various (Indian) language fonts [extra]'
    'xdg-utils: for setting a default browser on desktop environments [extra]'
    'chromium-pepper-flash: Pepper Flash integration [AUR]')
provides=("$_realname")
conflicts=("$_realname" "$_realname-svn")
backup=("etc/$_realname/default")
# let the 'makepkg' decide what and how to strip archives, binaries and, or libs
STRIP_DIRS=("opt/$_realname")
options=('emptydirs' '!strip')
noextract=('chrome-linux.zip' 'chromium.1.gz')
install=$_realname.install
PKGEXT='.pkg.tar'

if [[ "$CARCH" == 'i686' ]]; then
   _bldarch='Linux'
elif [[ "$CARCH" == 'x86_64' ]]; then
   _bldarch='Linux_x64'
fi

_pkgver=$(curl -s "http://commondatastorage.googleapis.com/chromium-browser-continuous/$_bldarch/LAST_CHANGE")

source=("http://commondatastorage.googleapis.com/chromium-browser-continuous/$_bldarch/$_pkgver/chrome-linux.zip"
	'chrome-wrapper.patch'
	"$_realname.1.gz"
	"$_realname.default"
	"$_realname.desktop"
	"$_realname.sh"
	'LICENSE.txt')
md5sums=('SKIP'
         'e68cda006cb3ba00e2d2fd84f28be567'
         '1774b5d79cfc67403fb336147a17e9a6'
         '001a472621cace5c2e140df95c632af1'
         '7cc5d72aa4aaf528fb94e32c42a11493'
         '7295ea12f6e24e3143f5686a166f7aba'
         'b689219f39e74e0c0b19f10a1db1839d')

pkgver(){
  echo $_pkgver
}

package() {
  mkdir -p "$pkgdir"/usr/{lib,share/pixmaps}
  bsdtar -xf chrome-linux.zip -C "$pkgdir/usr/lib"
  mv -f "$pkgdir/usr/lib/chrome-linux" "$pkgdir/usr/lib/$_realname"

  msg2 "Patching script 'chrome-wrapper'..."
  cd "$pkgdir/usr/lib/$_realname"
  patch -sp1 < "$srcdir/chrome-wrapper.patch"

  msg2 "Making it nice..."
  # adjust the permissions on directories and, or files, eh
  chown -R 0:0 "$pkgdir/usr/lib/$_realname"
  find "$pkgdir/usr/lib/$_realname" -type d -exec chmod 0755 {} ';'
  find "$pkgdir/usr/lib/$_realname" -type f -exec chmod 0644 {} ';'
  chmod 755 "$pkgdir/usr/lib/$_realname/chrome"
  chmod 755 "$pkgdir"/usr/lib/$_realname/chrome[_-]*
  chmod 755 "$pkgdir/usr/lib/$_realname/xdg-settings"

  # install default, wrapper-script, desktop, license and manpages
  install -Dm644 "$srcdir/$_realname.default" "$pkgdir/etc/$_realname/default"
  install -Dm755 "$srcdir/$_realname.sh" "$pkgdir/usr/bin/$_realname"
  install -Dm644 "$srcdir/$_realname.desktop" "$pkgdir/usr/share/applications/$_realname.desktop"
  install -Dm644 "$srcdir/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  install -Dm644 "$srcdir/$_realname.1.gz" "$pkgdir/usr/share/man/man1/$_realname.1.gz"
  mv "$pkgdir/usr/lib/$_realname/chrome.1" "$pkgdir/usr/share/man/man1/chromium.1"

  # symlink the icon file and missing libs
  ln -fs /usr/lib/$_realname/product_logo_48.png "$pkgdir/usr/share/pixmaps/$_realname.png"
  ln -fs /usr/lib/libudev.so.1 "$pkgdir/usr/lib/libudev.so.0"
  # any localization, eh
  if [ -f "$startdir/PKGBUILD.local" ]; then
    msg2 "Executing PKGBUILD.local..."
    source "$startdir/PKGBUILD.local"
  fi
}
