# Maintainer: SanskritFritz (gmail)

pkgname=erebus-git
_gitname="erebusrpg-code"
pkgver=2013.05.30
_stablever=0.8
pkgrel=2
epoch=1
pkgdesc="2D real-time role-playing game."
arch=('i686' 'x86_64')
url="http://erebusrpg.sourceforge.net/"
license=("GPLv3")
depends=('phonon')
makedepends=('qt4' 'git')
provides=('erebus')
conflicts=('erebus')
# Source zip is still needed, see erebus_source.txt
source=("$_gitname::git://git.code.sf.net/p/erebusrpg/code"
        "http://launchpad.net/erebus/trunk/$_stablever/+download/erebussrc.zip")

pkgver() {
	cd "$_gitname"
	git log -1 --format="%cd" --date=short | sed 's|-|.|g'
}

build() {
        cd "$srcdir/$_gitname"
	rm -fr sound; mv -f "$srcdir/erebussrc/sound" .
	rm -fr music; mv -f "$srcdir/erebussrc/music" .
	
	qmake-qt4 erebus.pro
	make

}

package() {
	cd "$srcdir/$_gitname"
	make INSTALL_ROOT="$pkgdir" install
	mkdir --parents $pkgdir/usr/bin
	ln -s /opt/erebus/erebus $pkgdir/usr/bin/erebus
}

md5sums=('SKIP' 'c2619e05ba2da9e4123030c8e57f52e8')
