# Maintainer: Gilrain <pierre.buard+aur gmail com>
pkgname=python2-stratum
_pkgname=stratum
pkgver=0.2.12
pkgrel=1
pkgdesc="Stratum server implementation based on Twisted."
url="http://stratum.bitcoin.cz/"
arch=('any')
license=('unknown')
depends=('twisted')
optdepends=('python2-ecdsa: for message signatures'
	    'python2-pyopenssl: enables SSL-based transports'
	    'python2-autobahn: for WebSocket transports')
source=(https://pypi.python.org/packages/source/s/${_pkgname}/${_pkgname}-${pkgver}.tar.gz)
md5sums=('a685da0dce43263db08ae949568b0f54')

prepare() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  sed -i -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' $(find ${srcdir} -name '*.py')
}

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  python2 setup.py build --build-base="${srcdir}/${_pkgname}-${pkgver}"
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  python2 setup.py install --root ${pkgdir}

  # setup /etc/webapps/stratum/ for configuration files
  install -d -m755 ${pkgdir}/etc/webapps/${_pkgname}/
  ln -s /usr/lib/python2.7/site-packages/${_pkgname}/config_default.py ${pkgdir}/etc/webapps/${_pkgname}/config_default.py
  ln -s /etc/webapps/${_pkgname}/config.py ${pkgdir}/usr/lib/python2.7/site-packages/${_pkgname}/config.py

  install -Dm644 README ${pkgdir}/usr/share/doc/${_pkgname}/README
}
