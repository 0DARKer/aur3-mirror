# Contributor: Tobias Luther <tobias [at] tonstrom [dot] de>
# Contributor: GraveDigger
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=idjc
pkgver=0.8.5
pkgrel=1
pkgdesc='Powerful yet easy-to-use source-client for individuals interested in streaming live radio shows over the Internet'
url='http://idjc.sourceforge.net/'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('desktop-file-utils' 'python2' 'mutagen' 'jack' 'pygtk' 'vorbis-tools' 'libsndfile' 'libsamplerate' 'glib2' 'lame' 'libmad' 'ffmpeg' 'flac' 'speex' 'libshout')
optdepends=('qjackctl: quite popular qt based graphical frontend for jack')
makedepends=('pkgconfig')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('9a71f0ea95f86b05985e965961f49930186a3242')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed 's:^#!/usr/bin/python$:#!/usr/bin/python2:' -i idjcpython/mutagentagger.py
	sed 's/^python /python2 /' -i configure
	export PYTHON=/usr/bin/python2

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--disable-static \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
