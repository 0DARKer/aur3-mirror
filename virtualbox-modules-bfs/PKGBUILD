# Contributor: Piotr Gorski <sir_lucjan@bridgelinux.pl>
# Contributor: Kamil Kolosowski <kamelowaty@bridgelinux.pl>


pkgbase=virtualbox-modules-bfs
pkgname=('virtualbox-host-modules-bfs' 'virtualbox-guest-modules-bfs')
pkgver=4.3.12
pkgrel=3
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-bfs>=3.14' 'linux-bfs<3.15'
             'linux-bfs-headers>=3.14' 'linux-bfs-headers<3.15'
             "virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver")

# remember to also adjust the .install files and the package deps below
_extramodules=extramodules-3.14-bfs

build() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-host-modules-bfs(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Host kernel modules for VirtualBox'
  depends=('linux-bfs>=3.14' 'linux-bfs<3.15')
  replaces=('virtualbox-modules-bfs')
  conflicts=('virtualbox-modules-bfs')
  install=host.install

  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

package_virtualbox-guest-modules-bfs(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=('linux-bfs>=3.14' 'linux-bfs<3.15')
  replaces=('virtualbox-modules-bfs')
  conflicts=('virtualbox-modules-bfs')
  install=guest.install

  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

# vim:set ts=2 sw=2 et:
md5sums=()