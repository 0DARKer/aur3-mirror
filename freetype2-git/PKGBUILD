# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2-git
pkgver=2.4.11
pkgrel=1
pkgdesc="TrueType font rendering library (from Git)"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh')
options=('!libtool')
provides=("freetype2=$pkgver")
conflicts=("freetype2")
source=(git://git.sv.gnu.org/freetype/freetype2.git
        freetype-2.3.0-enable-spr.patch
        freetype-2.4.11-enable-sph.patch
        freetype-2.2.1-enable-valid.patch)
md5sums=('SKIP'
         '816dc8619a6904a7385769433c0a8653'
         '2422f8902224fedb46ae9eec08c8ceac'
         '214119610444c9b02766ccee5e220680')

pkgver() {(
  cd freetype2
  eval $(sed -nf version.sed include/freetype/freetype.h)
  echo ${freetype_major}${freetype_minor}${freetype_patch}
)}

prepare() {
  cd freetype2
  patch -Np1 -i ../freetype-2.3.0-enable-spr.patch
  patch -Np1 -i ../freetype-2.4.11-enable-sph.patch
  patch -Np1 -i ../freetype-2.2.1-enable-valid.patch
}

build() {
  cd freetype2
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

check() {
  cd freetype2
  make -k check
}

package() {
  cd freetype2
  make DESTDIR="$pkgdir" install
}
