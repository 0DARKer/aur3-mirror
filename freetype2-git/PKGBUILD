# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2-git
pkgver=2.5.0.1+125+gbc25de6
pkgrel=1
pkgdesc="TrueType font rendering library (from git)"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh' 'libpng')
makedepends=('git')
options=('!libtool')
provides=("freetype2=$pkgver" libfreetype.so)
conflicts=("freetype2")
source=(git://git.sv.gnu.org/freetype/freetype2.git
        freetype-2.2.1-enable-valid.patch
        freetype-2.5.1-enable-spr.patch
        freetype-2.5.1-enable-sph.patch)
md5sums=('SKIP'
         '214119610444c9b02766ccee5e220680'
         '80a14cce234f3f190cd936ca9060c398'
         '0636ba2ff074201e2ea5c47ac408bc63')

pkgver() {
  local _tag _count

  cd freetype2
  _tag=$(git describe --abbrev=0 )
  _count=$(git rev-list --count ${_tag}..HEAD)
  _tag=${_tag#VER-}
  echo ${_tag//-/.}+$_count+g$(git rev-parse --short HEAD)
}

prepare() {
  cd freetype2
  patch -Np1 -i ../freetype-2.2.1-enable-valid.patch
  patch -Np1 -i ../freetype-2.5.1-enable-spr.patch
  patch -Np1 -i ../freetype-2.5.1-enable-sph.patch
}

build() {
  cd freetype2
  ./autogen.sh
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd freetype2
  make -k check
}

package() {
  cd freetype2
  make DESTDIR="$pkgdir" install
}
