# Contributor: localizator <localizator@ukr.net>

# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=seafile-client
pkgver=1.7.1.1
pkgrel=1
pkgdesc="Seafile is an online file storage and collaboration tool. This is seafile client for synchronizing your local files with seafile server."
arch=('i686' 'x86_64')
url="https://github.com/haiwen/seafile/"
license=('GPL')
depends=('ccnet>=1.3.4' 'python2-mako' 'python2-simplejson' 'python2-webpy' 'gtk2' 'libnotify' 'hicolor-icon-theme' 'libevent' 'intltool')
makedepends=()
optdepends=()
options=('!libtool' '!emptydirs')
install=seafile-client.install
source=("http://seafile.googlecode.com/files/seafile-${pkgver}.tar.gz"
        "seafile-cli.patch")
md5sums=('9367b943f69d1f4f448cce75211a50e1'
         'af0159a4035bcdd61f42cbd16fdea901')
sha256sums=('840ca277756c3e2c4014a4b62ea1ed4eed732381910048387afcf52cbe2b4054'
            '31031014e887e23827677f761be62076403c7e7419f6bc6a7631f76f770747ea')

build ()
{
	cd "$srcdir/seafile-$pkgver"
	patch -p1 -i "$srcdir/seafile-cli.patch"
	./configure --disable-server --enable-client --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package ()
{
	cd "$srcdir/seafile-$pkgver"
	make DESTDIR="$pkgdir/" install

	install -D -m644 $srcdir/seafile-$pkgver/LICENCE.txt $pkgdir/usr/share/licenses/$pkgname/LICENCE.txt
}
