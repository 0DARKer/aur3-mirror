# Maintainer: sausageandeggs <sausageandeggs@archlinux.us>
# Contributor: György Balló <ballogy@freestart.hu>

pkgbase=appmenu-gtk
pkgname=appmenu-gtk
true && pkgname=(appmenu-gtk2 appmenu-gtk3)
pkgver=12.10.2
pkgrel=1
arch=('i686' 'x86_64')
url="https://launchpad.net/appmenu-gtk"
license=(LGPL)
depends=('gtk2-ubuntu' 'gtk3-ubuntu')
makedepends=('libdbusmenu-gtk2' 'libdbusmenu-gtk3')
options=('!libtool')
source=("http://launchpad.net/appmenu-gtk/12.10/${pkgver}/+download/$pkgbase-$pkgver.tar.gz")
md5sums=('6fd51655db9b252d010acff5cfe1528a')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --with-gtk2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
  popd
}

package_appmenu-gtk2() {
  pkgdesc="Adds appmenu support for GTK+ 2 apps"
  depends=('libdbusmenu-gtk2' 'gtk2-ubuntu')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make DESTDIR="$pkgdir/" install
  install -Dm755 80appmenu "$pkgdir/etc/profile.d/80appmenu.sh"
  rm -r "$pkgdir/etc/X11"
}

package_appmenu-gtk3() {
  pkgdesc="Adds appmenu support for GTK+ 3 apps"
  depends=('libdbusmenu-gtk3' 'gtk3-ubuntu')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make DESTDIR="$pkgdir/" install
  install -Dm755 80appmenu-gtk3 "$pkgdir/etc/profile.d/80appmenu-gtk3.sh"
  rm -r "$pkgdir/etc/X11"
}

pkgdesc="Adds appmenu support for GTK+ apps"
depends=('libdbusmenu-gtk2' 'libdbusmenu-gtk3' 'gtk2-ubuntu' 'gtk3-ubuntu')
true && depends=()

