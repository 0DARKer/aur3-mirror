# Maintainer: Sam S. <smls75@gmail.com>

pkgname=rochard-hib
pkgver=1.32_20121002
_hibver=20121002_1.32
pkgrel=0
pkgdesc='A science fiction platformer game (Humble Bundle version)'
url='http://www.rochardthegame.com'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('mesa' 'libxext' 'libxdmcp')
source=('rochard-hib.desktop'
        'rochard.png::http://www.humblebundle.com/static/hashed/8975a4ad5f062b8981f21097c18b789cdd74da05.png')
md5sums=('78d57546174aedb43d0d7cbcbaf8e073'
         'ea3e77cba6897af7a7228421ddebc2b8')
options=("!upx")
PKGEXT='.pkg.tar'

case $CARCH in
    i686)   _arch=32; _archive_md5='34cd4c1ecd1fa28b7a4929e13debe8ec'
                      _archive_subdir="Rochard_x86" ;;
    x86_64) _arch=64; _archive_md5='bc1497f4f04ddb2dbfc6153aaa0d31aa'
                      _archive_subdir="Rochard_x64" ;;
esac
_archive="rochard-linux-${_hibver}-${_arch}bit.tar.gz"

package() {

  # Get game archive
  _get_local_source "${_archive}" --md5 "${_archive_md5}" || {
    error "Unable to find the game archive. Please download it from your Humble
           Bundle page, and place it into one of the above locations."
    exit 1; }

  msg "Starting setup..."
  cd $srcdir
  
  # Unpack game archive
  [[ -d ${_archive_subdir} ]] && rm -r ${_archive_subdir}
  tar -xvf "${_archive}"

  # Install game files
  install -d "${pkgdir}/opt/Rochard"
  cp -r ${_archive_subdir}/* "${pkgdir}/opt/Rochard"

  # Install launcher script
  echo -e "#!/bin/sh\ncd /opt/Rochard && ./Rochard" > "launcher.sh"
  install -Dm755 "launcher.sh" "${pkgdir}/usr/bin/rochard"

  # Install icon
  install -Dm644 "rochard.png" \
                 "${pkgdir}/usr/share/pixmaps/rochard.png"

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}


###### HELPER FUNCTIONS ######

# Locates a file or folder provided by the user, and symlinks it into $srcdir
_get_local_source() {
  msg "Looking for '$1'..."; rm -f "$srcdir/$1"
  declare -A _search=(['build dir']="$startdir"
                      ['$LOCAL_PACKAGE_SOURCES']="$LOCAL_PACKAGE_SOURCES")
  for _key in "${!_search[@]}"; do local _dir="${_search["$_key"]}"
    echo -n "    - in $_key [${_dir:-<undefined>}] ... ";
    if [[ -z "$_dir" || ! -e "$_dir/$1" ]]; then
      echo "NOT FOUND"
    elif [[ -n $2 && "$(${2#--}sum "$_dir/$1"|awk '{print $1}')" != $3 ]]; then
      echo "CHECKSUM FAILED";
    else
      echo "FOUND"; ln -sfT "$(readlink -f "$_dir/$1")" "$srcdir/$1"; break; fi
  done
  if [ ! -e "$srcdir/$1" ]; then return 1; fi
}