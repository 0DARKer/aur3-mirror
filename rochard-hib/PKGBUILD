# Maintainer: Sam S. <smls75@gmail.com>

pkgname=rochard-hib
pkgver=1.32_20121002
_hibver=20121002_1.32
pkgrel=0
pkgdesc='A science fiction platformer game (Humble Bundle version)'
url='http://www.rochardthegame.com'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('mesa' 'libgl' 'glu' 'libxext' 'libxdmcp')
options=("!upx")
PKGEXT='.pkg.tar'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

case $CARCH in
    i686)   _arch=32; _archive_md5='34cd4c1ecd1fa28b7a4929e13debe8ec' ;;
    x86_64) _arch=64; _archive_md5='bc1497f4f04ddb2dbfc6153aaa0d31aa' ;;
esac
source=("hib://rochard-linux-${_hibver}-${_arch}bit.tar.gz"
        'rochard.png::http://www.humblebundle.com/static/hashed/8975a4ad5f062b8981f21097c18b789cdd74da05.png'
        'rochard-hib.desktop')
md5sums=("$_archive_md5"
         'ea3e77cba6897af7a7228421ddebc2b8'
         '78d57546174aedb43d0d7cbcbaf8e073')

package() {
  cd $srcdir

  # Install game files
  install -d "${pkgdir}/opt/"
  mv -T Rochard_x$_arch "${pkgdir}/opt/Rochard"

  # Install launcher script
  echo -e "#!/bin/sh\ncd /opt/Rochard && ./Rochard" > "launcher.sh"
  install -Dm755 "launcher.sh" "${pkgdir}/usr/bin/rochard"

  # Install icon
  install -Dm644 "rochard.png" \
                 "${pkgdir}/usr/share/pixmaps/rochard.png"

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
