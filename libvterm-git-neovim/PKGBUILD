# Maintainer: Florian Walch <florian+aur@fwalch.com>

pkgname=libvterm-git-neovim
_commit=dd92e5ba5b0907571df938ac7601b1641f05ed16
pkgrel=1
pkgver=0.r669.dd92e5b
pkgdesc="Abstract library implementation of a VT220/xterm/ECMA-48 terminal emulator; experimental version needed for Neovim."
arch=('i686' 'x86_64')
url="http://www.leonerd.org.uk/code/libvterm"
license=('MIT')
makedepends=('git')
conflicts=('libvterm' 'libvterm-bzr')
provides=('libvterm')
source=("${pkgname}::git://github.com/neovim/libvterm#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
