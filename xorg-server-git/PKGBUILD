# Contributor: Det <nimetonmaili@gmail.com>
# Contributor: SpeedVin <speedvin at archlinux dot us>

pkgname=xorg-server-git
pkgver=20101007
pkgrel=1
pkgdesc="Xorg X servers - Git version"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org"
license=(custom)

# NOTE: Here you choose whether you already have a libgl providing package installed (a Catalyst or Nvidia driver):

_screwlibgl=1

if [ $_screwlibgl = 1 ]; then	# Libgl won't be a dependency

     depends=(dri2proto fontsproto inputproto kbproto libdrm libgcrypt libpciaccess libdmx libxau libxaw libxdmcp libxfixes libxfont libxi libxrender libxv pixman randrproto renderproto udev videoproto xextproto xf86-input-evdev xineramaproto xkeyboard-config xorg-fonts-misc xorg-xkb-utils xorg-util-macros xproto)

else				# Libgl WILL be a dependency

     depends=(${depends[*]} libgl)

fi

makedepends=(bigreqsproto compositeproto dmxproto git libx11 libxkbfile libxmu libxres libxtst mesa recordproto resourceproto scrnsaverproto xcmiscproto xf86dgaproto xf86driproto xtrans)
conflicts=(xorg-server xorg-server-common)
provides=(x-server xorg-server xorg-server-common)
options=(!libtool)
groups=(xorg)
backup=(etc/X11/xorg.conf.d/10-evdev.conf etc/X11/xorg.conf.d/10-quirks.conf)
source=(xorg-redhat-die-ugly-pattern-die-die-die.patch
	xvfb-run
	xvfb-run.1
	10-quirks.conf)
md5sums=('1a336eb22e27cbf443ec5a2ecddfa93c'
	 '52fd3effd80d7bc6c1660d4ecf23d31c'
	 '376c70308715cd2643f7bff936d9934b'
	 'd4f7dfc6be4ef4e2c6dd7632a9d88abe')
 
_gitroot="git://anongit.freedesktop.org/xorg/xserver"
_gitname="xserver"
 
build() {
  cd "$srcdir"

  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d $_gitname ] ; then
      cd $_gitname && git pull origin
      msg "The local files are updated."
  else
      git clone $_gitroot
      cd "$_gitname"
  fi

  msg "GIT checkout done or server timeout"

  # Get rid of the ugly pattern
  patch -Np3 -i "$srcdir/xorg-redhat-die-ugly-pattern-die-die-die.patch"


  ./autogen.sh --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-dri-driver-path=/usr/lib/xorg/modules/dri \
      --with-fontrootdir=/usr/share/fonts
  make

  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install

  install -m755 "$srcdir/xvfb-run" "$pkgdir/usr/bin/"
  install -m644 "$srcdir/xvfb-run.1" "$pkgdir/usr/share/man/man1/"

  rm -rf "$pkgdir/var/log"

  install -m755 -d "$pkgdir/var/lib/xkb"

  # Install sane config files in /etc
  install -m755 -d "$pkgdir/etc/X11/xorg.conf.d"
  install -m644 "$srcdir/10-quirks.conf" "$pkgdir/etc/X11/xorg.conf.d/"
  rm -rf "$pkgdir/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "$pkgdir/usr/lib/xorg/modules/extensions/libglx.so" \
     "$pkgdir/usr/lib/xorg/modules/extensions/libglx.xorg"

  install -m755 -d "$pkgdir/usr/share/licenses/${pkgname/-g*}"
  install -m644 COPYING "$pkgdir/usr/share/licenses/${pkgname/-g*}/"
}
