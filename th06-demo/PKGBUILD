# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgbase=th06-demo
pkgname="$pkgbase" # Hack for AUR.
true && pkgname=("$pkgbase-data" "$pkgbase")
pkgver=0.13
pkgrel=1
pkgdesc='Embodiment of Scarlet Devil trial version. Sixth game of the Touhou Project serie.'
arch=('i686')
url='http://www16.big.or.jp/~zun/html/th06.html'
license=('custom')
makedepends=('lha-autoconf-git')
source=('http://www16.big.or.jp/~zun/data/soft/kouma_tr013.lzh' "$pkgbase.desktop")
sha1sums=('dfeb4dd358c6613ec4d8b701d41672ca75b6a0c9'
          'd28ea005b9ef4a901a3e0fb52969972904071126')

build() {
	cd "$srcdir"
	lha e 'kouma_tr013.lzh'
}

package_th06-demo-data() {
	arch=('any')
	makedepends=('lha-autoconf-git' 'icoutils')

	cd "$srcdir/東方紅魔郷　体験版"

	install -d -m755 "$pkgdir/usr/share/$pkgbase"
	install -m644 *.DAT "$pkgdir/usr/share/$pkgbase"

	install -d -m755 "$pkgdir/usr/share/doc/$pkgbase/html"
	install -m644 *.txt 'マニュアル/index.html' "$pkgdir/usr/share/doc/$pkgbase"
	install -m644 マニュアル/html/* "$pkgdir/usr/share/doc/$pkgbase/html"

	install -d -m755 "$pkgdir/usr/share/pixmaps"
	wrestool -xo. -t14 '東方紅魔郷.exe'
	install -m644 '東方紅魔郷.exe_14_105_1041.ico' "$pkgdir/usr/share/pixmaps/$pkgbase.ico"
}

package_th06-demo() {
	depends=("$pkgbase-data" 'wine')
	optdepends=('timidity++: for MIDI musics')

	install -d -m755 "$pkgdir/usr/share/applications"
	install -m644 "$pkgname.desktop" "$pkgdir/usr/share/applications"

	cd "$srcdir/東方紅魔郷　体験版"
	install -d -m755 "$pkgdir/usr/share/$pkgbase"
	install -m755 *.exe "$pkgdir/usr/share/$pkgbase"
}
