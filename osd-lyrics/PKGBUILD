# Maintainer: Mark, Huo Mian <markhuomian[at]gmail[dot]com>
# Contributor: Xiang Yixiang <lovelinux229[at]gmail[dot]com>
# Edited by: Mark, Huo Mian <markhuomian[at]gmail[dot]com> 31Mar2010

pkgname=osd-lyrics
_realname=osdlyrics
pkgver=0.3.20100604
pkgrel=1
pkgdesc="An OSD lyric show supporting multiple media players and downloading."
arch=('i686' 'x86_64')
url="http://code.google.com/p/osd-lyrics/"
license=('GPL3')
depends=('gtk2' 'dbus-glib' 'curl' 'libnotify' 'libmpd')
makedepends=('pkgconfig')
conflicts=('osd-lyrics-svn')
install='osd-lyrics.install'
source=(http://osd-lyrics.googlecode.com/files/$_realname-$pkgver.tar.gz)
md5sums=('88be9cdad0671de6f2b39b74c52cb681')

_amarok1=false
_mpd=false
_xmms2=false

if [ "$_mpd" = "false" ]; then
     depends=('gtk2' 'dbus-glib' 'curl' 'libnotify')
fi

build() {
    cd "$srcdir/$_realname-$pkgver"

    aclocal || return 1
    autoconf || return 1
    automake --add-missing || return 1

    if [ "$_amarok1" = "true" ]; then
	    configure="$configure --enable-amarok1"
    fi

    if [ "$_mpd" = "false" ]; then
	    configure="$configure --disable-mpd"
    fi

    if [ "$_xmms2" = "false" ]; then
	    configure="$configure --disable-xmms2"
    fi

    ./configure --prefix=/usr $configure || return 1
    make || return 1
    make DESTDIR="$pkgdir/" install || return 1
}

# vim:set ts=2 sw=2 et:
