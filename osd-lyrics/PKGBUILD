pkgname=osd-lyrics
_pkgalias=osdlyrics
pkgver=0.4.1
pkgrel=1
pkgdesc="A lyric show compatible with various media players"
arch=('i686' 'x86_64')
url="http://code.google.com/p/osd-lyrics/"
license=('GPL3')
depends=('gtk2>=2.12.0' 'dbus-glib' 'curl' 'libnotify' 'sqlite3>=3.3.0')
optdepends=('libmpd: MPD support'
            'xmm2:   XMMS2 support')
conflicts=('osd-lyrics-stable')
install=$_pkgalias.install
source=(http://osd-lyrics.googlecode.com/files/$_pkgalias-$pkgver.tar.gz)
md5sums=('958c8dcf6af0f94529d013c28cdad998')

_ods=()
_optlen=${#optdepends[@]}
_tmpfile="$pkgdir/.tmp"

add_ods()
{
    pkg-config $1 && echo "$3" >> "$_tmpfile" || _ods=("${_ods[@]}" "$2")
}

remove_optdepends()
{
    for (( _idx = 0; _idx < $_optlen; _idx++ ))
    do
        if [[ "${optdepends[$_idx]}" =~ ^$1:* ]]
        then
            unset optdepends[$_idx]
            break
        fi
    done
}

build()
{
    cd "$srcdir/$_pkgalias-$pkgver"

    aclocal
    autoheader
    autoconf
    automake --add-missing

    add_ods libmpd "--disable-mpd" libmpd
    add_ods xmms2-client "--disable-xmms2" xmms2

    ./configure --prefix=/usr "${_ods[@]}"
    make
}

package()
{
    cd "$srcdir/$_pkgalias-$pkgver"
    make DESTDIR="$pkgdir" install

    while read _ch
    do
        if [ 1 -gt 0 ]; then depends=("${depends[@]}" "$_ch"); fi
        remove_optdepends "$_ch"
    done < "$_tmpfile"
    rm -rf "$_tmpfile"
}
