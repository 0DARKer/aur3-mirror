# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=lxqt-config-randr
pkgname=$_pkgname-git
pkgver=41.957abe6
pkgrel=1
pkgdesc="LXQt monitor configuration module"
arch=("i686" "x86_64")
url="http://www.lxde.org"
license=("GPL2")
depends=("liblxqt")
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git://git.lxde.org/git/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	echo "$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
