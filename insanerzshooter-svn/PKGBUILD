# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: b4283 <b4283 at gmail>
# Contributor: Dany Martineau <dany.luc.martineau gmail.com>
pkgname=insanerzshooter-svn
pkgver=30
pkgrel=1
pkgdesc="Old school 2D shoot 'em up game"
url="http://insanerzshooter.googlepages.com/"
license=('GPL3')
arch=('x86_64' 'i686')
depends=('sdl_image' 'sdl_ttf' 'sdl_mixer' 'sdl_gfx' 'ttf-bitstream-vera' 'gcc-libs')
makedepends=('setconf' 'subversion')
provides=('insanerzshooter-svn')
conflicts=('insanerzshooter')
_svntrunk=http://insanerzshooter.googlecode.com/svn/trunk/
_svnmod=insanerzshooter
install=insanerzshooter.install

build() {
  cd "$srcdir"

  if [ -d "$_svnmod/.svn" ]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi
  msg "SVN checkout done or server timeout"

  cd "$_svnmod"

  msg2 "Configuring paths..."
  sed -i "s:res\/:/usr/share/$_svnmod\/:" main.cpp

  msg2 "Configuring font rendering..."
  sed -i "s:TTF_RenderText_Solid:TTF_RenderText_Blended:g" main.cpp

  msg2 "Configuring shortcut..."
  echo "Icon=$_svnmod.png" >> "$_svnmod.desktop"
  setconf "$_svnmod.desktop" Exec "$_svnmod"

  msg2 "Compiling..."
  g++ main.cpp -lSDLmain -lSDL -lSDL_image -lSDL_ttf -lSDL_mixer -lSDL_gfx \
    -Wno-write-strings -o "$_svnmod"
}

package() {
  cd "$srcdir/$_svnmod"

  msg2 "Packaging executables..."
  install -Dm755 "$_svnmod" "$pkgdir/usr/bin/$_svnmod"

  msg2 "Packaging resources..."
  install -d "$pkgdir/usr/share/$_svnmod"
  install -Dm644 res/{*.wav,*.png} "$pkgdir/usr/share/$_svnmod/"
  ln -s /usr/share/fonts/TTF/VeraBd.ttf \
    "$pkgdir/usr/share/$_svnmod/FreeSans_bold.ttf"

  msg2 "Packaging shortcut and icon..."
  install -Dm644 "$_svnmod.desktop" \
    "$pkgdir/usr/share/applications/$_svnmod.desktop"
  install -Dm644 "icon0.png" "$pkgdir/usr/share/pixmaps/$_svnmod.png"

  msg2 "Packaging license..."
  install -Dm644 gpl.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
