# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=fly8
pkgver=1.11.1
pkgrel=1
pkgdesc="Portable multiplayer flight simulator for X11/SVGAlib"
arch=('i686' 'x86_64')
url="http://www.samba.org/ftp/eyal/fly8/"
license=('GPL')
depends=('libxext' 'svgalib')
makedepends=('unzip')
install="${pkgname}.install"
source=("http://www.samba.org/ftp/eyal/fly8/released/${pkgname}${pkgver//./}.zip"
        "${pkgname}.patch"
        "${pkgname}.sh")
noextract=("${pkgname}${pkgver//./}.zip")
md5sums=('514048276072b6fe477c9b69702fe987'
         'bccda81eb83b75a7f87e1e551685dd5a'
         '0b0eb5bd9b0bca7a44bd165094a40b70')

build() {
  cd "${srcdir}"

  # Unzip the archive
  unzip -La "${pkgname}${pkgver//./}.zip"

  # Apply the patch
  patch -p0 -i "${pkgname}.patch"

  # Configure
  cd "${pkgname}"
  sh unix/setfly8
  sh UNIX/config lx

  # Compile
  make all || return 1

  # Create fly8.doc
  cd docs
  ./dodoc
}

package() {
  cd "${srcdir}/${pkgname}"

  # Install game files
  mkdir -p "${pkgdir}/opt/${pkgname}"
  install -m755 "${pkgname}" \
                utils/{mac2max,max2mac} \
                UNIX/{fly,flytty,flyfifo,flyudp,fly8udp,udpmgr} \
                "${pkgdir}/opt/${pkgname}"
  install -m644 *.{adv,max} \
                parms/*.{lnd,nav,prm} \
                shapes/*.vxx \
                UNIX/*.{ini,vmd} \
                "${pkgdir}/opt/${pkgname}"
  install -m644 shapes/wf16.vxx "${pkgdir}/opt/${pkgname}/plane.vxx"
  install -m666 UNIX/fly.ini "${pkgdir}/opt/${pkgname}"

  # Install the launcher
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Install docs
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 docs/{*.doc,*.txt,readme,todo} \
                UNIX/linux.txt \
                "${pkgdir}/usr/share/doc/${pkgname}"
}
