# Maintainer: Piotr Mro≈ºek <dnadesigned[at]gmail[dot]com>
# Contributor: Sven Nobis <aur[at]sven[dot]to>
pkgname=modelio-open
pkgver=3.1.2
pkgrel=1
pkgdesc="An Open Source UML tool that supports the UML2 and BPMN2 standards."
url="http://www.modelio.org/"
arch=('i686' 'x86_64')
license=('GPL3' 'APACHE')
depends=('glibc' 'gcc-libs' 'atk' 'cairo' 'gtk2' 'glib2' 'libwebkit' 'libxtst' 'java-environment')
makedepends=()
conflicts=()
replaces=()
backup=()

_pkg_build_timestamp='201405191231'
source=("http://downloads.sourceforge.net/modeliouml/${pkgname}-${_pkg_build_timestamp}-linux.gtk.x86.tar.gz"
        "http://downloads.sourceforge.net/modeliouml/${pkgname}-${_pkg_build_timestamp}-linux.gtk.x86_64.tar.gz"
        modelio.desktop
        .AURINFO)
md5sums=('e55e02f79b6bbdc8360dd007f4aa8d90'
         'de6fcaab22210e8cff777a67a537c94a'
         '57e4d9d398ff2b118891c67a19b3880f'
         SKIP)
options=(!strip)


if (( ! GENINTEG )); then
  if [[ $CARCH == x86_64 ]]; then
    source=("${source[@]:1}")
    md5sums=("${md5sums[@]:1}")
  else
    source=("${source[0]}" "${source[@]:2}")
    md5sums=("${md5sums[0]}" "${md5sums[@]:2}")
  fi
fi

prepare() {
  # rearrange the sources to prepare a "/opt"-friendly version
  mkdir -p ${srcdir}/opt
  mv ${srcdir}/Modelio\ 3.1 ${srcdir}/opt/${pkgname}
  # remove the /jre and /lib directories (use environment-provided java libraries)
  rm -rf ${srcdir}/opt/${pkgname}/jre
  rm -rf ${srcdir}/opt/${pkgname}/lib
  # prepare the icon
  mkdir -p ${srcdir}/usr/share/icons
  cd ${srcdir}/usr/share/icons
  ln -s ../../../opt/${pkgname}/modelio.xpm
  # prepare the desktop file
  mkdir -p ${srcdir}/usr/share/applications
  cp ${srcdir}/modelio.desktop ${srcdir}/usr/share/applications/
}

package() {
  # copy the /opt and /usr directories for packaging
  cp -r ${srcdir}/opt ${pkgdir}/
  cp -r ${srcdir}/usr ${pkgdir}/
}
