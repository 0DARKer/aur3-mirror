# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=blackpenguin
pkgver=0.2
pkgrel=1
pkgdesc='Arcade style jump-on-cubes game with Penguin and Evil Window'
arch=('i686' 'x86_64')
url='http://www.ibiblio.org/pub/linux/games/arcade/'
license=('GPL2')
depends=('qt2' 'libxext')
source=("http://www.ibiblio.org/pub/linux/games/arcade/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.patch"
        "${pkgname}.sh")
md5sums=('91ac8ac0f5588122c469e3f65b98c3df'
         'dd43fbc5d922f86ef3a9b5d79c472301'
         'ab0c0f9cf8af30be81d54221a7bc95bf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  mv appwindow.cpp{.alt,}
  sed -i 's/static char/static const char/g' *.xpm
  patch -p0 -i "../../${pkgname}.patch"
  QTDIR=/opt/qt2 make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "src/${pkgname}" "${pkgdir}/usr/bin/${pkgname}.bin"
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "doc/README" "${pkgdir}/usr/share/${pkgname}/README"
}

# vim:set ts=2 sw=2 et:
