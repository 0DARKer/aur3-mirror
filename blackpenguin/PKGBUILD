# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=blackpenguin
pkgver=0.2
pkgrel=2
pkgdesc="An arcade style jump-on-cubes game with Penguin and Evil Window"
arch=('i686' 'x86_64')
url="http://www.ibiblio.org/pub/linux/games/arcade/"
license=('GPL')
depends=('libxext' 'qt2')
source=("http://www.ibiblio.org/pub/linux/games/arcade/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.patch"
        "${pkgname}.png"
        "${pkgname}.desktop"
        "${pkgname}.sh")
md5sums=('91ac8ac0f5588122c469e3f65b98c3df'
         'f3ca77928e512e263fbf9e769d614260'
         '9204e564d47ea7ce9592232e2bd31c92'
         '55d2533c522dae44e9ac739a6fb287c0'
         'ab0c0f9cf8af30be81d54221a7bc95bf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"

  patch -Np2 -i ../../${pkgname}.patch
  sed -i "s/static char/static const char/" *.xpm
  mv appwindow.cpp{.alt,}

  QTDIR=/opt/qt2 make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install game files
  install -Dm755 src/${pkgname} "${pkgdir}/usr/bin/${pkgname}.bin"
  install -Dm755 ../${pkgname}.sh "${pkgdir}/usr/bin/${pkgname}"

  # Install a desktop entry
  install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install a readme file
  install -Dm644 doc/README "${pkgdir}/usr/share/${pkgname}/README"
}

# vim:set ts=2 sw=2 et:
