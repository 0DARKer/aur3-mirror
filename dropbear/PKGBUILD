# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jason Pierce <`echo 'moc tod liamg ta nosaj tod ecreip' | rev`>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>

pkgname=dropbear
pkgver=0.52
pkgrel=3
pkgdesc="Lightweight replacement for sshd"
arch=('i686' 'x86_64')
url="http://matt.ucc.asn.au/dropbear/dropbear.html"
license=('custom')
depends=('zlib')
source=(http://matt.ucc.asn.au/dropbear/releases/$pkgname-$pkgver.tar.gz \
        $pkgname-conf.d \
        $pkgname-rc.d)
md5sums=('1c69ec674481d7745452f68f2ea5597e'
         '43e74541a345f149f1b5a82057a354b8'
         '033ce263f0570ec8603fc39e7ab3e743')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr
  make || return 1
  make prefix=${pkgdir}/usr install

#man pages
  install -D -m644 dbclient.1 ${pkgdir}/usr/share/man/man1/dbclient.1 || return 1
  install -D -m644 $pkgname.8 ${pkgdir}/usr/share/man/man8/$pkgname.8 || return 1
  install -D -m644 dropbearkey.8 ${pkgdir}/usr/share/man/man8/dropbearkey.8 || return 1

#configuration files
  install -d ${pkgdir}/etc/$pkgname || return 1
  install -D -m644 ${srcdir}/$pkgname-conf.d ${pkgdir}/etc/conf.d/$pkgname || return 1
  install -D -m755 ${srcdir}/$pkgname-rc.d ${pkgdir}/etc/rc.d/$pkgname || return 1

#license file
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE || return 1
}
