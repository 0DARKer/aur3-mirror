# Maintainer: Det
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: max1m <keyworker@rambler.ru>

pkgname=utorrent
_version=3.4
_build=31227
pkgver=${_version}.${_build}
pkgrel=1
pkgdesc="The world's most popular BitTorrent client (Wine version)"
arch=('i686' 'x86_64')
url="https://www.utorrent.com/"
license=('freeware')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'wine')
install=${pkgname}.install
source=("uTorrent-${pkgver}.exe::http://download-new.utorrent.com/endpoint/utorrent/os/windows/track/stable/"
        'uTorrent'
        'uTorrent.desktop'
        'uTorrent_16.png'
        'uTorrent_24.png'
        'uTorrent_32.png'
        'uTorrent_48.png')
md5sums=('SKIP'
         '295fb0b478e1b94c7b3b37e09acb5558'
         'd2f78d163c5fb32af9d2836f74a681f9'
         '94f1857386cf0056ef9f43b0c64b4243'
         'b0e424b2a6a80355efa4136f8f10663f'
         '2ec4ae76259cb1ad2950c850603795f1'
         '7318f187b603d1b79aedc45090df9c96')

package() {
  msg2 "Installing launcher"
  install -Dm755 uTorrent "${pkgdir}"/usr/bin/uTorrent
  ln -s uTorrent "${pkgdir}"/usr/bin/${pkgname}

  msg2 "Installing program"
  install -Dm644 uTorrent-${pkgver}.exe "${pkgdir}"/usr/share/utorrent/uTorrent.exe

  msg2 "Installing .desktop"
  install -Dm644 uTorrent.desktop "${pkgdir}"/usr/share/applications/uTorrent.desktop

  msg2 "Installing icons"
  for i in 16 24 32 48; do
    install -Dm644 uTorrent_${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/uTorrent.png
  done
}