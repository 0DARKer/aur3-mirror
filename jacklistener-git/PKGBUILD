# Maintainer: AlexanderR <rvacheva at nxt dot ru>
# Contributor: Maxim A. Mikityanskiy <maxtram95@gmail.com>
_gitname=jacklistener
_gitroot="git://github.com/gentoo-root/${_gitname}.git"
pkgname=$_gitname-git
pkgver=20120312
pkgrel=1
pkgdesc="Headphones Jack Listener Daemon"
arch=('i686' 'x86_64')
url="https://github.com/gentoo-root/$_gitname"
license=('GPL3')
depends=('dbus')
makedepends=('git' 'linux-headers')
provides=("$_gitname")
conflicts=("$_gitname")
options=(!emptydirs)
changelog=ChangeLog

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install

  install -D -m755 init.d/jacklistener-arch "$pkgdir/etc/rc.d/${_gitname}d"
  install -D -m644 README "$pkgdir/usr/share/doc/$pkgname/README"

  cd "$pkgdir/"

  rm etc/init.d/jacklistener usr/sbin/jacklistenerd

  mkdir -p sbin
  mv usr/lib/jacklistener/jacklistenerd sbin/jacklistenerd
}
