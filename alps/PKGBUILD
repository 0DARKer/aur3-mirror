# Maintainer: Kevin Slagle <kjslag at gmail dot com>

# uncomment if you want 350MB of debug symbols
#debug=true

pkgname=alps
_pkgver=2.1.1-r6176
pkgver=2.1.1_r6176
pkgrel=2
pkgdesc="Provides physics simulation code for strongly correlated quantum mechanical systems"
arch=('i686' 'x86_64')
url="http://alps.comp-phys.org"
license=('custom:ALPS')
depends=('boost' 'fftw' 'python2-scipy' 'python2-h5py' 'python2-matplotlib')
makedepends=('cmake' 'gcc-fortran')
#optdepends=('foo: description')
source=(http://alps.comp-phys.org/static/software/releases/${pkgname}-${_pkgver}-src.tar.gz)
md5sums=('f4c9b044ed9ee8964c449a473bd75842')
[ "$debug" = "true" ] && options=(!strip)

build() {
  # http://alps.comp-phys.org/mediawiki/index.php/Download_and_install_ALPS_2#Build_ALPS
  
  cd "${srcdir}/${pkgname}-${_pkgver}-src/alps"
  
  # this line is recommended, but gave a compilation error
  # [ "$CARCH" = "x86_64" ] && _CMAKE=-DLAPACK_64_BIT=ON
  
  if [ "$debug" = "true" ]
  then CFLAGS="-g $CFLAGS"
       CXXFLAGS="-g $CXXFLAGS"
  fi
  
  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_INTERPRETER=/usr/bin/python2 ${_CMAKE}
  make
  make test || true
  
  # 94% tests passed, 6 tests failed out of 98
  # The following tests FAILED:
  #          23 - test_deque (Failed)
  #          24 - test_vector (Failed)
  #          95 - python_hlist_test (Failed)
  #          96 - python_mcdata (Failed)
  #          97 - python_pyhdf5 (Failed)
  #          98 - python_mcanalyze (Failed)
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}-src/alps"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  # uncomment if installing into /usr
  cd "${pkgdir}"
  mv usr/tutorials usr/share/alps/
  
  # make python see alps (the alternative is to run with alpspython)
  mkdir -p usr/lib/python2.7/site-packages/
  ln -s ../../pyalps usr/lib/python2.7/site-packages/
}
