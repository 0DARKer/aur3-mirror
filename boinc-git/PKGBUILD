# Maintainer: ponsfoot <cabezon dot hashimoto at gmail dot com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

## If you want to use X less version, uncomment below.
#_X="no"

## You can change branch or tag to be built.
## Uncomment and edit below.
#_branch=client_release_7.0.52

_apply_patch="yes"

_pkgname=boinc
_gitroot="git://boinc.berkeley.edu/${_pkgname}.git"
_gitname=$_pkgname

pkgname=${_pkgname}-git
pkgver=20130214 
pkgrel=1
arch=('i686' 'x86_64')
url="http://boinc.berkeley.edu/"
license=('LGPL')
pkgdesc="Berkeley Open Infrastructure for Network Computing"
depends=('curl' 'sqlite3')
makedepends=('pkg-config' 'libxslt' 'perl-xml-sax' 'subversion')
provides=('boinc')
replaces=('boinc-svn')
conflicts=('boinc' 'boinc-nox')
options=('!libtool')
install=${_pkgname}.install
changelog=ChangeLog
source=(boinc.bash
        boinc.desktop
        boinc.service
)
md5sums=('05ed267db973ef7cbaf1118bb20bf9ce'
         'db62de2f08117e6379a3c613b58fa7ff'
         'fe02cfd5bddf64307fc4111f0689e1b0')

if [[ "$_X" != "no" ]]; then
  depends+=('wxgtk>=2.8.11.0' 'libnotify')
fi

build() {
  cd "$srcdir"

  msg "Connecting to boinc.berkeley.edu GIT server..."
  if [[ -d "${srcdir}/${_gitname}" ]]; then
    cd $_gitname
    git checkout master
    git pull
    cd ~-
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  msg "GIT checkout done or server timeout"

  msg "Starting make..."
  rm -rf "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

  cd "${srcdir}/${_gitname}-build"

  [[ -n "$_branch" ]] && git checkout $_branch

  if [[ "$_apply_patch" == "yes" ]]; then
    if [[ "$_branch" == "client_release_7.0.28" ]]; then
      # build fails if automake >=1.12
      sed -i -e 's:AC_PROG_CC:&\nAC_PROG_OBJCXX:' configure.ac
    fi
  fi

  if [[ "$_X" == "no" ]]; then
    _copt="--disable-manager --without-wxdir"
  else
    _copt="--with-x --with-wxdir=/usr/lib --with-wx-config=`which wx-config`"
  fi

  #configure
  LC_ALL=C ./_autosetup # Possibility to fail ver. check depending on the localization.
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-server \
              --enable-unicode \
              --with-ssl \
              --disable-xss \
              --enable-dynamic-client-linkage \
              --disable-static \
              $_copt
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  make DESTDIR=$pkgdir install

  #install unit file for systemd
  install -D -m644 "${srcdir}/boinc.service" ${pkgdir}/usr/lib/systemd/system/boinc.service

  #install bash-completion
  install -D -m644 "${srcdir}/boinc.bash" ${pkgdir}/usr/share/bash_completion.d/boinc

  if [[ "$_X" != "no" ]]; then
    #install .desktop File
    install -D -m644 "${srcdir}/boinc.desktop" \
      ${pkgdir}/usr/share/applications/boinc.desktop

    #install icons
    install -D -m644 "${srcdir}/${_gitname}-build/clientgui/res/boincmgr.48x48.png" \
      ${pkgdir}/usr/share/pixmaps/boinc.png
  fi

  #killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d
}
