# Maintainer: ponsfoot <cabezon dot hashimoto at gmail dot com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

## If you want to use X less version, uncomment below.
#_X="no"

## If you want to use latest stable branch, uncomment below.
#_branch=client_release_7.0a
## Note: this will not necessarily be the equivalent to stable binary release.

_apply_patch="yes"

_pkgname=boinc
_gitroot="git://boinc.berkeley.edu/${_pkgname}.git"
_gitname=$_pkgname

pkgname=${_pkgname}-git
pkgver=20130116 
pkgrel=1
arch=('i686' 'x86_64')
url="http://boinc.berkeley.edu/"
license=('LGPL')
pkgdesc="Berkeley Open Infrastructure for Network Computing"
depends=('curl' 'sqlite3')
makedepends=('pkg-config' 'libxslt' 'perl-xml-sax' 'subversion')
provides=('boinc')
replaces=('boinc-svn')
conflicts=('boinc' 'boinc-nox' 'boinc-svn')
options=('!libtool')
install=${_pkgname}.install
changelog=ChangeLog
source=(boinc.rc
        boinc.bash
        boinc.desktop
        boinc.service
        r24240-ignore_ret_value.patch
)
md5sums=('43605168e310f50cf426d2f9a7b39847'
         '05ed267db973ef7cbaf1118bb20bf9ce'
         'db62de2f08117e6379a3c613b58fa7ff'
         '485c1abef2c9b98ea589f35da67e437e'
         '68bb3dfac41c20251b543665659f0281')

if [[ "$_X" != "no" ]]; then
  depends+=('wxgtk>=2.8.11.0' 'libnotify')
fi

build() {
  cd "$srcdir"

  msg "Connecting to boinc.berkeley.edu GIT server..."
  if [[ -d "${srcdir}/${_pkgname}" ]]; then
    (cd $_gitname ; git pull)
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  msg "GIT checkout done or server timeout"

  msg "Starting make..."
  rm -rf "${srcdir}/${_pkgname}-build"

  if [[ -n "$_branch" ]]; then
    cd "${srcdir}/${_pkgname}"
    git checkout $_branch
    cd ~-
  fi
  cp -R "${srcdir}/${_pkgname}" "${srcdir}/${_pkgname}-build"

  cd "${srcdir}/${_pkgname}-build"

  if [[ "$_apply_patch" == "yes" ]]; then
    # build fails if set -D_FORTIFY_SOURCE=2
    patch -p1 -i "${srcdir}/r24240-ignore_ret_value.patch"
  fi

  if [[ "$_X" == "no" ]]; then
    _copt="--disable-manager --without-wxdir"
  else
    _copt="--with-x --with-wxdir=/usr/lib --with-wx-config=`which wx-config`"
  fi

  #configure
  LC_ALL=C ./_autosetup # Possibility to fail ver. check depending on the localization.
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-server \
              --enable-unicode \
              --with-ssl \
              --disable-xss \
              --enable-dynamic-client-linkage \
              --disable-static \
              $_copt
  make
}

package() {
  cd "${srcdir}/${_pkgname}-build"

  make DESTDIR=$pkgdir install

  #install unit file for systemd
  install -D -m644 "${srcdir}/boinc.service" ${pkgdir}/usr/lib/systemd/system/boinc.service

  #install rc-script
  install -D -m755 "${srcdir}/boinc.rc" ${pkgdir}/etc/rc.d/boinc

  #install bash-completion
  install -D -m644 "${srcdir}/boinc.bash" ${pkgdir}/usr/share/bash_completion.d/boinc

  if [[ "$_X" != "no" ]]; then
    #install .desktop File
    install -D -m644 "${srcdir}/boinc.desktop" \
      ${pkgdir}/usr/share/applications/boinc.desktop

    #install icons
    install -D -m644 "${srcdir}/${_pkgname}-build/clientgui/res/boincmgr.48x48.png" \
      ${pkgdir}/usr/share/pixmaps/boinc.png
  fi

  #killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d
}
