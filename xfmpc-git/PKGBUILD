# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=xfmpc-git
pkgver=0.2.2.74.g5980fd1
pkgrel=1
pkgdesc="A graphical GTK+ MPD client focusing on low footprint - git checkout"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfmpc/"
license=('GPL')
provides=('xfmpc')
conflicts=('xfmpc')
depends=('libxfce4ui' 'libmpd')
makedepends=('intltool' 'pkgconfig' 'vala')
source=('git://git.xfce.org/apps/xfmpc')

pkgver() {
	cd xfmpc/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd xfmpc/
  
	sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.in.in
	sed -i 's/CHARSET/UTF-8/' po/sl.po
	./autogen.sh --prefix=/usr
	# building fails with parallel processes... so we unset MAKEFLAGS and do
	# not use -j5...
	MAKEFLAGS="" make
}

package() {
	cd xfmpc/
	
	make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP')
