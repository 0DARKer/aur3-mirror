# Maintainer: ksj <podhorsky.ksj@gmail.com>
# Contributor: ksj <podhorsky.ksj@gmail.com>

_gitname=geany
_gitnameplug=geany-plugins
pkgname=geany-gtk3-git
pkgver=20140114
pkgrel=1
pkgdesc="Fast and lightweight IDE (git version) + plugins"
arch=('x86_64' 'i686')
url="https://github.com/b4n/geany/tree/wip/gtk3-support"
license=('GPL2')
depends=('python' 'gpgme' 'desktop-file-utils' 'lua51' 'ctpl' 'enchant' 'libsoup' 'gtk3')
makedepends=('git' 'automake' 'autoconf' 'intltool' 'libtool' 'python' 'lua')
optdepends=('hspell: hebrew spell checker')
conflicts=('geany' 'geany-plugins-svn' 'geany-plugins')
provides=('geany')
install='geany-gtk3-git.install'
source=('git://github.com/geany/geany.git' 'git://github.com/geany/geany-plugins.git')
md5sums=('SKIP' 'SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    git log -1 --format='%cd' --date=short | tr -d -- '-'
}

prepare() {
    cd "$srcdir/$_gitname"

    # Syntax highlighting for PKGBUILD files
    sed -i 's/Sh=/Sh=PKGBUILD;/' data/filetype_extensions.conf
}

build() {
    cd "$srcdir/$_gitname"

    ./autogen.sh --enable-gtk3 --prefix=/usr
    make

    cd "$srcdir/$_gitnameplug"

    ./autogen.sh --prefix=/usr --libexecdir=/usr/lib
    make
}

package() {

    cd "$srcdir/$_gitnameplug"
    make PREFIX=/usr DESTDIR="$pkgdir" install

    cd "$srcdir/$_gitname"
    make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
