# Maintainer: ksj <podhorsky.ksj@gmail.com>
# Contributor: ksj <podhorsky.ksj@gmail.com>

_gitname=geany
pkgname=geany-gtk3-git
pkgver=20130510
pkgrel=1
pkgdesc="Fast and lightweight IDE (git version)"
arch=('x86_64' 'i686')
url="https://github.com/b4n/geany/tree/wip/gtk3-support"
license=('GPL2')
depends=('gtk3' 'hicolor-icon-theme' 'desktop-file-utils' 'python')
makedepends=('git' 'automake' 'autoconf' 'intltool')
conflicts=('geany')
provides=('geany')
install='geany-gtk3-git.install'
source=("git+https://github.com/b4n/geany.git")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {
    cd "$srcdir/$_gitname"
    
    ./autogen.sh --prefix=/usr
    ./configure --enable-gtk3 --prefix=/usr
    make
}

package() {
    cd "$srcdir/$_gitname"
    make PREFIX=/usr DESTDIR="$pkgdir" install
}
