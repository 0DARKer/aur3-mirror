# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

pkgname=thunderbird-nightly
pkgver=3.3a3pre
pkgrel=1
pkgdesc="Standalone Mail/News reader - Nightly build"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'gcc-libs' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell' 'sqlite3>=3.7.4')
optdepends=('libcanberra: for sound support'
	    'yasm: for WebM support')
provides=("thunderbird=${pkgver}")
conflicts=('thunderbird')
install=${pkgname}.install
source=(http://ftp.mozilla.org/pub/thunderbird/nightly/latest-comm-central/thunderbird-${pkgver}.en-US.linux-${CARCH}.tar.bz2
        thunderbird.desktop)
sha512sums=(`wget ${source/ta*}checksums -qO - | grep bz | cut -d " " -f1`
            'e26b639cc2cfbc0c461ab937ef79d81e3af0340ed3edbe151f0ef6f105a99bf34a532487fd1ff3c1edcf1a5f3d7862cf195e3b546c6bc16916e6c5554ae68941')

package() {
  # Fix the lib path
  sed -i 's|/usr/local/lib/thunderbird-3.3a3pre|/usr/lib/thunderbird|' thunderbird/thunderbird

  # Create the required dirs
  install -dm755 "${pkgdir}"/usr/{bin,lib/thunderbird,share/{applications,pixmaps}}

  # Install the bin
  install -m755 thunderbird/thunderbird "${pkgdir}/usr/bin"

  # Install the libs
  cp -a thunderbird/* "${pkgdir}/usr/lib/thunderbird"

  # Install the icon and the desktop files
  install -m644 thunderbird/chrome/icons/default/default48.png "${pkgdir}/usr/share/pixmaps/thunderbird.png"
  install -m644 thunderbird.desktop "${pkgdir}/usr/share/applications"
}