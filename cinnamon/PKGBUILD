# Maintainer: M0Rf30

pkgname=cinnamon
pkgver=1.4UP1
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL')
depends=('gnome-menus' 'libpulse' 'gnome-desktop' 'python2' 'gnome-bluetooth' 'gjs' 'libcroco' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'nautilus' 'gnome-session' 'muffin-wm' 'python2-gconf' 'libgnomekbd' 'gnome-menus2' 'metacity' 'notification-daemon' 'gnome-panel' 'gnome-settings-daemon' 'accountsservice')
makedepends=('git' 'intltool' 'gnome-common')
provides=('cinnamon')
conflicts=('cinnamon-git')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=(${pkgname}-1.4-UP1.tar.gz::https://github.com/linuxmint/Cinnamon/tarball/1.4-UP1
fix-build-with-gnome-bluetooth-3.4.patch
fix-cinnamon-settings.patch)

build() {
  cd ${srcdir}
  cd linuxmint*

  patch -Np1 -i ../fix-build-with-gnome-bluetooth-3.4.patch
  patch -Np0 -i ../fix-cinnamon-settings.patch

  sed -i 's/-DG_DISABLE_DEPRECATED//g' src/Makefile.in browser-plugin/Makefile.in

  CFLAGS="${CFLAGS} -Wno-deprecated-declarations $(pkg-config --libs gmodule-2.0)"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon --localstatedir=/var --disable-static --disable-schemas-compile --disable-schemas-install --enable-compile-warnings=yes
  make GLIB_COMPILE_SCHEMAS=glib-compile-schemas
}

package() {
  cd ${srcdir}
  cd linuxmint*
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  
  # Python2 fix
  sed -i \
    -e 's|^\(#!.*python\)$|\12|g' \
    -e '/python[ \t]\+[-a-zA-Z0-9]\+$/ s|^\(#!.*python\)[ \t]*\(.*\)$|\12 \2|g' \
    "${pkgdir}/usr/bin/$pkgname-extension-tool" \
    "${pkgdir}/usr/lib/$pkgname-settings/$pkgname-settings.py" \
    "${pkgdir}/usr/bin/$pkgname-settings" \
    "${pkgdir}/usr/bin/$pkgname-menu-editor"
    
  # Kdm fix
  mkdir -p  $pkgdir/usr/share/apps/kdm/sessions/
  cp $pkgdir/usr/share/xsessions/$pkgname.desktop $pkgdir/usr/share/apps/kdm/sessions/
  
  # GConf2 stuff
  install -dm755 "${pkgdir}/usr/share/gconf/schemas/"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" --domain cinnamon "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -r "${pkgdir}/etc/gconf/"

}

md5sums=('03a9546c73793fa69a40139b3b38f66f'
         '50b40f7fef34dc7ca75bfe749e18d83c'
         'de3485c3b05856e7a210c37332281c7a')
