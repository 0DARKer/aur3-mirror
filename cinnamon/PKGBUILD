# Maintainer: M0Rf30

pkgname=cinnamon
pkgver=1.3.1
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL')
depends=('gnome-menus' 'libpulse' 'gnome-desktop' 'python' 'gnome-bluetooth' 'gjs' 'libcroco' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'nautilus' 'gnome-session' 'muffin-git' 'python2-gconf' 'libgnomekbd')
makedepends=('intltool' 'gnome-common')
provides=('cinnamon')
conflicts=('cinnamon-git')
options=('!libtool')
install=${pkgname}.install
source=('cinnamon.session'
	'cinnamon.xsession'
	'gnome-session-cinnamon')

build() {
  cd ${srcdir}
  wget -O cinnamon.tar.gz https://github.com/linuxmint/Cinnamon/tarball/$pkgver
  tar xvf cinnamon.tar.gz
  cd linuxmint*
  CFLAGS="${CFLAGS} -Wno-deprecated-declarations $(pkg-config --libs gmodule-2.0)"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon --localstatedir=/var --disable-static --disable-schemas-compile --disable-schemas-install --enable-compile-warnings=yes
  make GLIB_COMPILE_SCHEMAS=/usr/bin/glib-compile-schemas
}

package() {
  cd ${srcdir}
  cd linuxmint*
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}/" install

  # GConf2 stuff
  install -dm755 "${pkgdir}/usr/share/gconf/schemas/"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" --domain cinnamon "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm "${pkgdir}"/etc/gconf/schemas/*.schemas

  # Python 2 fix
  sed -i 's|^\(#!.*python\)$|\12|g' "${pkgdir}/usr/bin/cinnamon-extension-tool"
  sed -i 's|^\(#!.*python\)$|\12|g' "${pkgdir}/usr/bin/cinnamon-settings"
  sed -i 's|^\(#!.*python\)$|\12|g' "${pkgdir}/usr/lib/cinnamon-settings/cinnamon-settings.py"
  
  # Install GNOME Session manager session file
  install -dm755 "${pkgdir}/usr/share/gnome-session/sessions/"
  install -m644 "${srcdir}/cinnamon.session" "${pkgdir}/usr/share/gnome-session/sessions/cinnamon.session"
  
  # Install X11 session file
  install -dm755 "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/cinnamon.xsession" "${pkgdir}/usr/share/xsessions/cinnamon.desktop"
  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions"
  install -m644 "${srcdir}/cinnamon.xsession" "${pkgdir}/usr/share/apps/kdm/sessions/cinnamon.desktop"
  
  # Install Session Wrapper
  install -m644 "${srcdir}/gnome-session-cinnamon" "${pkgdir}/usr/bin/gnome-session-cinnamon"
  chmod +x ${pkgdir}/usr/bin/gnome-session-cinnamon
}

md5sums=('e55304f5859e17226658dc237044c17f'
         '5d4e252efcb688406eadba5c93e29bff'
         '106774e10707554ea49fc223a59e5d21')
