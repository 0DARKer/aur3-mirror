# Maintainer: jsteel <jsteel at vorx dot com>
# Contributor: Muhammed Uluyol <muhammedu@gmail.com>

pkgname=scratch
pkgver=1.4.0.1
pkgrel=2
pkgdesc="Create and share your own interactive stories, games, music and art"
arch=('any')
url="http://scratch.mit.edu"
license=('Proprietary')
install=$pkgname.install
depends=('pango' 'libv4l')
source=http://info.scratch.mit.edu/sites/infoscratch.media.mit.edu/files/file/source-package/$pkgname-$pkgver.tar.gz
md5sums=('42f15730c74156dbc47243682aaea328')

package() {
  cd "$srcdir/$pkgname"
  install -Dm 755 App/scratch_squeak_vm "$pkgdir/usr/bin/scratch_squeak_vm"
  sed -i 's/-vm-sound-pulse/-vm-sound-ALSA/' src/scratch
  install -Dm 755 src/scratch "$pkgdir/usr/bin/scratch"
  install -Dm 644 Scratch.image "$pkgdir/usr/lib/scratch/Scratch.image"
  install -Dm 644 Scratch.ini "$pkgdir/usr/lib/scratch/Scratch.ini"
  install -Dm 644 src/scratch.desktop "$pkgdir/usr/share/applications/scratch.desktop"
  install -Dm 644 src/man/scratch.1.gz "$pkgdir/usr/share/man/man1/scratch.1.gz"
  install -Dm 644 src/man/scratch_squeak_vm.1.gz "$pkgdir/usr/share/man/man1/scratch_squeak_vm.1.gz"
  install -Dm 644 src/scratch.xml "$pkgdir/usr/share/mime/packages/scratch.xml"
  install -Dm 644 README.txt "$pkgdir/usr/share/scratch/README.txt"

  mkdir -p "$pkgdir/usr/share/scratch"
  cp -rp Help locale Media Projects "$pkgdir/usr/share/scratch/"

  mkdir -p "$pkgdir/usr/share/icons/hicolor"
  cp -rp src/icons/* "$pkgdir/usr/share/icons/hicolor/"

  for i in Plugins/*; do
    install -Dm 644 "$i" "$pkgdir/usr/lib/scratch/$i"
  done
}
