# Maintainer: Gadget3000 <gadget3000 at msn dot com>
# Contributor: Pnevma <Klavious@gmail.com>
# Contributor: djnm <nmihalich [at} gmail dott com>
# Contributor: ignus <m.a.lukasiewicz at gmail>
# Contributor: xnitropl <xnitropl at gmail dot com>

pkgname=dfg
pkgver=34_10
pkgrel=1
pkgdesc="Mike Mayday's combined pack (DFG) is an unofficial semi-graphic version of Dwarf Fortress based on the work of the DF community."
arch=('i686' 'x86_64')
url="http://mayday.w.staszic.waw.pl/df.php"
license=('custom')
depends=('gtk2' 'sdl' 'sdl_image' 'libgl' 'sdl_ttf')
[ "$CARCH" = "x86_64" ] && depends=('lib32-glibc' 'lib32-gtk2' 'lib32-sdl_image' 'lib32-libgl' 'lib32-mesa' 'lib32-libxdamage' 'lib32-sdl_ttf') 
makedepends=('unzip')
conflicts=('dwarffortress' 'bin32-dwarffortress')
provides=('dwarffortress' 'bin32-dwarffortress')
install=$pkgname.install
source=(http://www.bay12games.com/dwarves/df_${pkgver}_linux.tar.bz2
        http://artgoblin.pl/upload/DFG/dfg_${pkgver}_win.zip
        dwarffortress
        dfg.desktop
        init.txt)
md5sums=('81ef446e3701839211d9b0ff9cd06ffc'
         '02eeace7e069b9c2790cbd4c799bdc26'
         '8b081a0b081d5d204308de152420f24d'
         'e6bc3fedcfe87b7978efe688464969c0'
         'd7572d643c2cb1c8f47060333995b4ba')

package() {
  cd ${srcdir}

  rm -r $srcdir/df_linux/raw
  cp -r $srcdir/raw $srcdir/df_linux/raw

  rm -r $srcdir/df_linux/data/art
  cp -r $srcdir/data/art $srcdir/df_linux/data/art

  cp $srcdir/data/init/embark_profiles.txt $srcdir/df_linux/data/init/embark_profiles.txt
  cp $srcdir/init.txt $srcdir/df_linux/data/init/init.txt

  mkdir -p $pkgdir/opt/
  cp -r $srcdir/df_linux $pkgdir/opt/df_linux

  if [ "$CARCH" = "x86_64" ] ; then
    ln -s /opt/lib32/usr/lib/libjpeg.so $pkgdir/opt/df_linux/libs/libjpeg.so.62
  fi
  
  chown -R root:games $pkgdir/opt/df_linux
  chmod -R 775 $pkgdir/opt/df_linux
  chmod 755 $pkgdir/opt/df_linux/df
  
  install -D -m755 $srcdir/dwarffortress $pkgdir/usr/bin/dwarffortress
  install -D -m644 $srcdir/df_linux/readme.txt $pkgdir/usr/share/licenses/dwarffortress/readme.txt
  install -D -m644 $srcdir/dfg.desktop $pkgdir/usr/share/applications/dfg.desktop
}
