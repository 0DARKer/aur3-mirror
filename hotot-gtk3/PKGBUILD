# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=hotot-gtk3
pkgver=0.9.8.10
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)."
arch=('any')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('hotot-data' 'python2-gobject' 'python2-dbus'
         'python2-pycurl' 'webkitgtk3' 'desktop-file-utils')
makedepends=('cmake' 'intltool')
optdepends=('libappindicator3: unity menubar integration')
install="hotot-gtk3.install"
source=("https://github.com/shellex/Hotot/tarball/$pkgver")
md5sums=('ce4fdb6b635cae008e8c8e861bedb6d5')

build() {
  cd ${srcdir}/shellex-Hotot-*
  [ -d bld ] || mkdir bld && cd bld

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DWITH_GTK=ON \
           -DWITH_GIR=ON \
           -DWITH_QT=OFF \
           -DWITH_KDE=OFF \
           -DWITH_CHROME=OFF \
           -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd ${srcdir}/shellex-Hotot-*/bld

  # bin
  install -Dm755 scripts/hotot \
    "$pkgdir/usr/bin/hotot-gtk3"
  sed -i "s/hotot/&_gir/" "$pkgdir/usr/bin/hotot-gtk3"

  # libs
  install -d "$pkgdir/usr/lib/python2.7/site-packages/hotot_gir"
  install -Dm644 hotot-gir/*.py* \
    "$pkgdir/usr/lib/python2.7/site-packages/hotot_gir"

  # desktop file
  install -Dm644 misc/hotot.desktop \
    "$pkgdir/usr/share/applications/hotot-gtk3.desktop"
  sed -i "/Exec/s/hotot/&-gtk3/;/^Name/s/$/ Gtk3/;s/2/3/" \
    "$pkgdir/usr/share/applications/hotot-gtk3.desktop"
}

# vim:set ts=2 sw=2 et:
