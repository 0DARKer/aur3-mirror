pkgname=mingw-w64-sdl2_image
pkgver=2.0.0
pkgrel=3
pkgdesc="SDL2 image libraries (mingw-w64)"
arch=(any)
url="http://www.libsdl.org"
license=("MIT")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-sdl2
mingw-w64-libpng
mingw-w64-libwebp
mingw-w64-libjpeg-turbo
mingw-w64-libtiff)
options=(!libtool !strip !buildflags)
source=("http://www.libsdl.org/tmp/SDL_image/release/SDL2_image-$pkgver.tar.gz")
md5sums=('c0d4cdc9a443134d32197308e5ca43d8')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/SDL2_image-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-sdltest
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
