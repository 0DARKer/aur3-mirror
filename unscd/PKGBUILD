# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=unscd
pkgver=0.49
pkgrel=1
pkgdesc='Drop-in replacement for glibc nscd which is designed for simplicity and stability'
arch=('i686' 'x86_64')
url='http://busybox.net/~vda/unscd/'
license=('GPL2')
source=("http://busybox.net/~vda/unscd/nscd-$pkgver.c"
  'rc.d' 'rc.conf' 'unscd.conf' 'unscd.8')
md5sums=('ee9cdaac340635e0c14551febbc0fd22'
         '31562cdafef913f7fbed61d8d0054a84'
         '60f03abf55a2a13fc59b190962c21e37'
         'cfbab3d1c5b0fe362a218ad15ad2d6e3'
         '9069ba3a3a07673ab1a7cc70018b387e')

build() {
  sed -i 's,/etc/nscd.conf,/etc/unscd.conf,g' "nscd-$pkgver.c"
  gcc -fomit-frame-pointer -Wl,--sort-section -Wl,alignment -Wl,--sort-common \
    -Os $CFLAGS $LDFLAGS -o $pkgname "nscd-$pkgver.c"
}

package() {
  install -D -m 755 unscd "$pkgdir/usr/sbin/unscd"
  install -D -m 644 unscd.conf "$pkgdir/etc/unscd.conf"
  install -D -m 644 unscd.8 "$pkgdir/usr/share/man/man8/unscd.8"
  install -D -m 755 rc.d "$pkgdir/etc/rc.d/unscd"
  install -D -m 644 rc.conf "$pkgdir/etc/conf.d/unscd"
}

# vim:set ts=2 sw=2 ft=sh et:
