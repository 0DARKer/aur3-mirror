# Maintainer: Arthur Zamarin <arthurzam@gmail.com> 

pkgbase=libpcap++
pkgname=('libpcappp' 'libpcappp-doc')
pkgver=0.0.2
pkgrel=2
arch=(i686 x86_64)
url="http://libpcappp.sourceforge.net/"
license=('GPL2')
makedepends=('doxygen')
source=("http://downloads.sourceforge.net/libpcappp/$pkgbase-$pkgver.tar.gz"
        "fix.patch")
sha256sums=('92240a42de09e847b315a5205f791252e9498e7fe1304d6e852e41655d6ae26a'
            '3066e5d3acd06f89952c98058aec56c1cdbdce747c40518e2d1bc5cc6d417d97')

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"
  patch -p1 -i ../fix.patch
  ./configure --prefix=/usr
}

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  make
}

package_libpcappp() {
  pkgdesc="C++ wrapper for libpcap"
  depends=('libpcap>=0.8')

  cd "$pkgbase-$pkgver/pcap++"
  make DESTDIR="$pkgdir" install
  cd ..
  make DESTDIR="$pkgdir" install-pkgconfigDATA
}

package_libpcappp-doc() {
  pkgdesc="C++ wrapper for libpcap"
  arch=(any)

  cd "$pkgbase-$pkgver/doc"
  make DESTDIR="$pkgdir" install
}
