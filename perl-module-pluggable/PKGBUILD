# Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-module-pluggable'
pkgver='5.0'
pkgrel=3
pkgdesc="automatically give your module the ability to have plugins"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=()
makedepends=()
url='http://search.cpan.org/dist/Module-Pluggable'
source=("http://search.cpan.org/CPAN/authors/id/S/SI/SIMONW/Module-Pluggable-${pkgver}.tar.gz")
md5sums=('15a539c8d0b5e61d8a475949127fc682')
sha512sums=('985df1fe1f5fb9c1a2916525a3bb72ece92a0f94c327e232f68c7150b9d8c550c0a31e610eb927335c89497413aa63092cef688584ac0a919b07b063c9798cac')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/Module-Pluggable-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
