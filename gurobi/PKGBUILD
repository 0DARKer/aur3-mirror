# Maintainer: Ghost of Kendo <ghostofkendo@gmail.com>
pkgname=gurobi
pkgver=5.6.3
pkgrel=1
pkgdesc="State-of-the-art solver for mathematical programming"
arch=('x86_64')
url="http://www.gurobi.com/products/gurobi-optimizer/gurobi-overview"
license=('custom')
depends=('python2>=2.7.0' 'python2<2.8.0')
optdepends=(
  'gcc: C/C++ programming interfaces support'
  'java-environment: Java programming interface support'
  'mono: .NET programming interface support'
  'python32: Python 3 programming interface support'
  'r: R programming interface support, version 3.0.*'
  'matlab: MATLAB programming interface support, version 2008b to 2014a'
)
install=${pkgname}.install
source=(
  "${pkgname}${pkgver}_linux64.tar.gz::file://${pkgname}${pkgver}_linux64.tar.gz"
  "gurobi.sh"
  "gurobi_setup.m"
)
sha256sums=(
  '9331793e56d064e5c322d5a44c4dc9e106657eceec2ef85555f9361ca7a9ef33'
  'aac41cdb2427ad087132425aedae2e8ddef2b3dce7f40f32066f2a760a1638be'
  'cee5d05ee3ee6690989f2de4b457514bd474a43c265a76d4487ba72edb7a9d5b'
)

package() {
  cd "$srcdir/${pkgname}${pkgver//./}/linux64/"

  # License
  install -D -m644 EULA.pdf "${pkgdir}/usr/share/licenses/${pkgname}/EULA.pdf"

  # Examples
  install -d "${pkgdir}/usr/share/${pkgname}/"
  install -m644 bin/gurobi.env "${pkgdir}/usr/share/${pkgname}/"

  rm -r examples/build/
  mv examples/ "${pkgdir}/usr/share/${pkgname}/"
  
  # Binaries and related files
  install -d "${pkgdir}/usr/bin/"
  install bin/grb* bin/gurobi_cl ${srcdir}/gurobi.sh -t "${pkgdir}/usr/bin/"

  install -d "${pkgdir}/usr/lib/${pkgname}/"
  sed -ie "s/python/python2/" lib/gurobi.py
  install lib/gurobi.py "${pkgdir}/usr/lib/${pkgname}/"

  # Documentation
  install -d "${pkgdir}/usr/share/doc/${pkgname}/"
  mv -T docs/ "${pkgdir}/usr/share/doc/${pkgname}/"

  # Headers
  install -d "${pkgdir}/usr/include/"
  install -m644 include/*.h "${pkgdir}/usr/include/"

  # Programming interfaces e.g C/C++, Java, Python, etc.
  install lib/*.so* "${pkgdir}/usr/lib/"
  install -m644 lib/*.a "${pkgdir}/usr/lib/"

  python2 setup.py install --root="$pkgdir" --optimize=1
  if [ -x /usr/bin/python3.2 ]; then
    python3.2 setup.py install --root="$pkgdir" --optimize=1
  fi

  install -d "${pkgdir}/usr/share/java/${pkgname}/"
  install -m644 lib/gurobi.jar "${pkgdir}/usr/share/java/${pkgname}/"

  install -d "${pkgdir}/usr/lib/${pkgname}/matlab/"
  install matlab/*.mexa64 "${pkgdir}/usr/lib/${pkgname}/matlab/"
  rm matlab/gurobi_setup.m
  install -m644 matlab/*.m ${srcdir}/gurobi_setup.m -t "${pkgdir}/usr/lib/${pkgname}/matlab/"

  if [ -x /usr/bin/R ]; then
    install -d "${pkgdir}/usr/lib/R/library"
    R CMD INSTALL -l "${pkgdir}/usr/lib/R/library" \
	    R/${pkgname}_5.6-3_R_${arch}-unknown-linux-gnu.tar.gz
  fi
}
