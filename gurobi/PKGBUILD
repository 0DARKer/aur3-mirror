# Maintainer: Ghost of Kendo <ghostofkendo@gmail.com>

# Before building this PKGBUILD, you must download the Gurobi Optimizer
# archive at http://www.gurobi.com/download/gurobi-optimizer and put it
# in the same directory as this PKGBUILD. Registration at gurobi.com is
# required, though free of charge, to be able to download the archive.

pkgname=('gurobi' 'gurobi-python32' 'gurobi-r')
_basename=gurobi
pkgver=5.6.3
pkgrel=2
pkgdesc="State-of-the-art solver for mathematical programming (split package)"
arch=('x86_64')
url="http://www.gurobi.com/products/gurobi-optimizer/gurobi-overview"
license=('custom')
depends=('python2>=2.7.0' 'python2<2.8.0')
optdepends=(
  'gcc: C/C++ programming interfaces support'
  'java-environment: Java programming interface support'
  'mono: .NET programming interface support'
  'gurobi-python32: Python 3 programming interface support, version 3.2'
  'gurobi-r: R programming interface support, version 3.0.*'
  'matlab: MATLAB programming interface support, versions 2008b to 2014a'
)
source=(
  "${_basename}${pkgver}_linux64.tar.gz::file://${_basename}${pkgver}_linux64.tar.gz"
  "gurobi-python2.sh"
  "gurobi_setup.m"
)
sha256sums=('9331793e56d064e5c322d5a44c4dc9e106657eceec2ef85555f9361ca7a9ef33'
            '069c1beae5e2c0ed186985b3277300081bab75ad8427ee4184642c4dc9096017'
            'cee5d05ee3ee6690989f2de4b457514bd474a43c265a76d4487ba72edb7a9d5b')

prepare() {
  cd "$srcdir/${_basename}${pkgver//./}/linux64/"

  rm bin/python2.7
  rm -r examples/build/

  # Adapt cross-platform scripts to Arch Linux
  cp ${srcdir}/gurobi-python2.sh bin/
  cp ${srcdir}/gurobi_setup.m matlab/
  sed -ie "s/python/python2/" lib/gurobi.py
}

package_gurobi() {
  install=${_basename}.install

  cd "$srcdir/${_basename}${pkgver//./}/linux64/"

  install -d "${pkgdir}/usr/bin/" "${pkgdir}/usr/share/doc/${_basename}/" \
          "${pkgdir}/usr/include/" "${pkgdir}/usr/lib/${_basename}/matlab/"

  # License
  install -D -m644 EULA.pdf "${pkgdir}/usr/share/licenses/${_basename}/EULA.pdf"

  # Examples
  install -D -m644 bin/gurobi.env "${pkgdir}/usr/share/${_basename}/gurobi.env"
  cp -r examples/ "${pkgdir}/usr/share/${_basename}/"
  
  # Binaries and related files
  install bin/* "${pkgdir}/usr/bin/"
  rm "${pkgdir}/usr/bin/gurobi.env"
  ln -sf gurobi-python2.sh "${pkgdir}/usr/bin/gurobi.sh"
  install -D lib/gurobi.py "${pkgdir}/usr/lib/${_basename}/gurobi.py"

  # Documentation
  cp -rT docs/ "${pkgdir}/usr/share/doc/${_basename}/"

  # Headers
  install -m644 include/*.h "${pkgdir}/usr/include/"

  # Programming interfaces
  install lib/*.so* "${pkgdir}/usr/lib/"
  install -m644 lib/*.a "${pkgdir}/usr/lib/"
  ln -sf ./libgurobi.so.5.6.3 "${pkgdir}/usr/lib/libgurobi56.so"
  ln -sf ./libgurobi_g++4.2.a "${pkgdir}/usr/lib/libgurobi_c++.a"

  python2 setup.py install --root="$pkgdir" --optimize=1

  install -D -m644 lib/gurobi.jar "${pkgdir}/usr/share/java/${_basename}/gurobi.jar"

  install matlab/*.mexa64 "${pkgdir}/usr/lib/${_basename}/matlab/"
  install -m644 matlab/*.m "${pkgdir}/usr/lib/${_basename}/matlab/"
}

package_gurobi-python32() {
  depends=('gurobi' 'python32')
  optdepends=()

  cd "$srcdir/${_basename}${pkgver//./}/linux64/"

  sed -e "s/2\.7/3\.2/" ${srcdir}/gurobi-python2.sh > gurobi-python3.sh
  install -D gurobi-python3.sh "${pkgdir}/usr/bin/gurobi-python3.sh"
  python3.2 setup.py install --root="$pkgdir" --optimize=1
}

package_gurobi-r() {
  depends=('gurobi' 'r')
  optdepends=()
  
  cd "$srcdir/${_basename}${pkgver//./}/linux64/"
  install -d "${pkgdir}/usr/lib/R/library/"
  R CMD INSTALL -l "${pkgdir}/usr/lib/R/library/" \
          R/${_basename}_5.6-3_R_${arch}-unknown-linux-gnu.tar.gz
}
