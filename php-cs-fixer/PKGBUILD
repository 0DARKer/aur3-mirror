# Mantainer: Bruno Galeotti <bgaleotti at gmail dot com>

pkgname=php-cs-fixer
_pkgname=PHP-CS-Fixer
pkgver=1.1
pkgrel=1
pkgdesc="Analyzes some PHP source code and tries to fix coding standards issues (PSR-1 and PSR-2 compatible)."
url="http://cs.sensiolabs.org/"
license="MIT"
arch=("any")
depends=("php>=5.3.6")
makedepends=("php-box" "php-composer")
source=("https://github.com/FriendsOfPHP/${_pkgname}/archive/v${pkgver}.tar.gz")
#source=("${pkgname}::git+https://github.com/FriendsOfPHP/${_pkgname}.git")
sha512sums=('3b4a35d9e15aaa7f2df75491683d999b0ab134d034a3520028374952da580b094b6651cb95c334d373afd41c237fa7b41f0a521aca0fb3ac097c86fa36f29619')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    composer install --prefer-dist --no-dev
    sed -i '/git-commit/d' box.json
    php-box build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m755 "${pkgname}.phar" "${pkgdir}/usr/share/webapps/bin/${pkgname}.phar"
  install -d "${pkgdir}/usr/bin"
  ln -s "/usr/share/webapps/bin/${pkgname}.phar" "${pkgdir}/usr/bin/${pkgname}"
}
