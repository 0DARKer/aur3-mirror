# Mantainer: Bruno Galeotti <bgaleotti at gmail dot com>

pkgname=php-cs-fixer
_pkgname=PHP-CS-Fixer
pkgver=0.5.7
pkgrel=1
pkgdesc="Analyzes some PHP source code and tries to fix coding standards issues (PSR-1 and PSR-2 compatible)."
url="http://cs.sensiolabs.org/"
license="MIT"
arch=("any")
depends=("php>=5.3.6")
makedepends=("php-box" "php-composer")
source=("https://github.com/fabpot/${_pkgname}/archive/v${pkgver}.tar.gz")
#source=("${pkgname}::git+https://github.com/fabpot/${_pkgname}.git")
sha512sums=('3c11794764b09ceefd8f2a25b17b94d02d0cf8b86890b66bb812de20a901f3183fb225f8e36057148af1f25614d3a5e5d47fe00f8651269f6a2b242f8e60ba39')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    composer install --prefer-dist --no-dev
    sed -i '/git-commit/d' box.json
    # Fix version
    sed -i "s/0.5-DEV/${pkgver}/g" Symfony/CS/Fixer.php
    php-box build
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m755 "${pkgname}.phar" "${pkgdir}/usr/share/webapps/bin/${pkgname}.phar"
  install -d "${pkgdir}/usr/bin"
  ln -s "/usr/share/webapps/bin/${pkgname}.phar" "${pkgdir}/usr/bin/${pkgname}"
}
