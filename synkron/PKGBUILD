# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=synkron
pkgver=1.6.2
pkgrel=4
pkgdesc="A folder synchronization tool"
arch=('i686' 'x86_64')
url="http://synkron.sourceforge.net/"
license=('GPL2')
depends=('qt4')
source=("http://downloads.sourceforge.net/$pkgname/Synkron-$pkgver-src.tar.gz"
        "config.patch")
install=$pkgname.install
sha1sums=('0909caf101ed3d5b25235f22a83c6ca537f2b564'
          '9cbea9bc957b8b29c3aad60ec1212ad65ce53af3')

build() {
  cd "Synkron-$pkgver-src"

  patch -Np1 -i "$srcdir"/config.patch

  lrelease-qt4 Synkron.pro
  qmake-qt4 -config release PREFIX=/usr
  make
}

package() {
  cd "Synkron-$pkgver-src"

  make INSTALL_ROOT="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
