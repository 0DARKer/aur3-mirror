# Maintainer: Javier Torres <javitonino [at] gmail [dot] com>

pkgname=opendnssec
pkgver=1.3.5
pkgrel=1
pkgdesc="Turn-key solution for DNSSEC (sqlite3, no auditor)"
arch=('i686' 'x86_64')
url="http://www.opendnssec.org/"
license=('BSD')
groups=()
depends=('libxml2>=2.6.16' 'ldns>=1.6.12' 'sqlite3>=3.3.9')
makedepends=()
optdepends=('softhsm: key storage')
provides=()
conflicts=()
replaces=()
backup=("etc/opendnssec/conf.xml"
        "etc/opendnssec/kasp.xml"
        "etc/opendnssec/zonelist.xml"
        "etc/opendnssec/zonefetch.xml")
options=()
install="opendnssec.install"
changelog=
source=("http://www.opendnssec.org/files/source/${pkgname}-${pkgver}.tar.gz"
        "conf.xml.patch"
        "kasp.xml.patch"
        "LICENSE"
        "opendnssec.rcd")
noextract=()
md5sums=('e82098192f4a3965de7a84e6ae9f1f75'
         'a4d82e2aa6a06ce50f9b7ed92b5ae583'
         'e1c6a76cbcff27bd8033344ed51e606e'
         '367b9415779d31a737712f17df1f865b'
         'a124a550163be248a8baf0857c5101f9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # /var/lib/run -> /var/run
  sed -i "s/OPENDNSSEC_PID_DIR=\"\$full_localstatedir\/run\/opendnssec\"/OPENDNSSEC_PID_DIR=\"\/var\/run\/opendnssec\"/" configure

  ./configure --prefix=/usr --datarootdir=/usr/share --localstatedir=/var/lib --sysconfdir=/etc --with-pkcs11-softhsm=/usr/lib/libsofthsm.so --disable-auditor 

  # Create the correct (/var/run/opendnssec) directory
  sed -i \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run\/opendnssec/#Removed/" \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run/\$(INSTALL) -d \$(DESTDIR)\$(OPENDNSSEC_PID_DIR)/" \
  Makefile

  # Patch configuration (take out auditor)
  patch -p0 < "${srcdir}/conf.xml.patch"
  patch -p0 < "${srcdir}/kasp.xml.patch"

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  install -d "${pkgdir}/etc/rc.d"
  install -o root -g root -m 755 "${srcdir}/opendnssec.rcd" \
        "${pkgdir}/etc/rc.d/opendnssec"
  install -D -m644 "${srcdir}/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  chown 227:227 "${pkgdir}/etc/opendnssec" -R
  chown 227:227 "${pkgdir}/var/lib/opendnssec" -R
  chown 227:227 "${pkgdir}/var/run/opendnssec" -R
}
