# Maintainer: Javier Torres <javitonino [at] gmail [dot] com>

pkgname=opendnssec
pkgver=1.4.6
pkgrel=1
pkgdesc="Turn-key solution for DNSSEC (sqlite3)"
arch=('i686' 'x86_64')
url="http://www.opendnssec.org/"
license=('BSD')
groups=()
depends=('libxml2>=2.6.16' 'ldns>=1.6.16' 'sqlite3>=3.3.9')
makedepends=()
optdepends=('softhsm: key storage')
provides=()
conflicts=()
replaces=()
backup=("etc/opendnssec/conf.xml"
        "etc/opendnssec/kasp.xml"
        "etc/opendnssec/zonelist.xml")
options=()
install="opendnssec.install"
changelog=
source=("http://www.opendnssec.org/files/source/${pkgname}-${pkgver}.tar.gz"
	"pid-path.patch"
        "ods-signer.service"
        "ods-enforcer.service")
noextract=()

md5sums=('d241a6e4660aad92044f61568d32d4dd'                                                                                                                  
         'b3415f3ab4df58c73c7add7e40aa8652'                                                                                                                  
         'e762cdc2c3f8be40aeff379aa4bade76'                                                                                                                  
         'c5464f7be7984bb9a925da0b157eafdf')                                                                                                                 
sha1sums=('2318b31546d0d4118cd03b9591ba76d259e1b0b0'                                                                                                         
          'c9b3fd625affc5f2dc408be3dc8f18a69299a0f5'                                                                                                         
          '2a4fac3a16fea3f89b281f0933b6920524978d49'                                                                                                         
          'fa28111fdce06c389813ff6ed2d4cae136252488')                                                                                                        
sha256sums=('53f9c454f331822925d76c9d9e5e7cb3fe2dfb03e3c467f67f9412f10d0fd5ec'                                                                               
            '3bc577c91a11dd1a08edcceed7e5e280b9b8adaa00d1e38f34284fa4791e0838'                                                                               
            '596d238ad219de1c88f79fd26a8b829250bf0512a308b34c11fd231d0b4eb0f4'                                                                               
            '75cecbfb0ece13957a68a5bc39c20a1d69b95373e7473545d70621e1732733d8')                                                                              
sha384sums=('bc3c2a69bc7e60d8d9e9629ae45c33f3662ad895bc5fb4438b2056a90c43f2675347d25351bd01a6b0fd46c9e24b70e6'                                               
            'bc4d14c9d10992111ae98e227ebbb8ac8753b2b4935490c33bffd4c0a195f26008dc7ab556a1eda6d8d7432da2364f36'
            '29d68ccca64339ab190518f6c5bffedba71287548634e305a12c98b7744984cc37f6a1748394ca0d96e709dbd520fe19'
            '0869168e8c5a5064cbac0d2f0afe71539a68785d548752c609759381648b04015fc7c3ed9684aec944a914143c777a66')
sha512sums=('c01e6e46e2007d0ae4035253484590d9a892be8284b179b6d3cdb0f8481789a67a79f9043d04de0aecc165fb44d88dac0eb02444f48e0ccd366f118a2bbb5c18'
            '1319d561d2197427c8aa5e17185b3fc3a95c50656c1d256fd91cc15d608d8e910f330ac74f10f64282216a9cfcba02ec844cc39f3d2d07ea7a81d31f1a4ed3d5'
            '39068133b3bfd075f3555491096be50ea0973a73ac716abb19faed0aa972ef043a6012491d4c6c208443352a2a508b8ebfbd7273fd84df43b3d6d478e72e7957'
            'a3700c82e6577bdacbce9cfd749e71e3c749814884ad4e9a1359e97105f9c045dc1472ba231ecb52c23855cacf67874623c8eef715955bfd41239b199d03a0db')

build() 
{
  cd "${srcdir}/${pkgname}-${pkgver}"

  # /var/lib/run -> /var/run
  patch -p0 < "${srcdir}/pid-path.patch"

  aclocal
  autoconf
  automake --add-missing

  ./configure --prefix=/usr --datarootdir=/usr/share --localstatedir=/var/lib --sysconfdir=/etc --with-pkcs11-softhsm=/usr/lib/libsofthsm.so --sbindir=/usr/bin

  # Create the correct (/var/run/opendnssec) directory
  sed -i \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run\/opendnssec/#Removed/" \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run/\$(INSTALL) -d \$(DESTDIR)\$(OPENDNSSEC_PID_DIR)/" \
  Makefile

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d "${pkgdir}/var/run/opendnssec"

  make DESTDIR="${pkgdir}" install

  install -Dm0644 "${srcdir}/ods-signer.service" \
        "${pkgdir}/usr/lib/systemd/system/ods-signer.service"
  install -Dm0644 "${srcdir}/ods-enforcer.service" \
        "${pkgdir}/usr/lib/systemd/system/ods-enforcer.service"
  install -Dm0644 "enforcer/utils/migrate_adapters_1.sqlite3" \
        "${pkgdir}/usr/share/opendnssec"

  chown 227:227 "${pkgdir}/etc/opendnssec" -R
  chown 227:227 "${pkgdir}/var/lib/opendnssec" -R
  chown 227:227 "${pkgdir}/var/run/opendnssec" -R

  chmod 750 "${pkgdir}/etc/opendnssec" \
            "${pkgdir}/var/lib/opendnssec" \
            "${pkgdir}/var/run/opendnssec"
}
