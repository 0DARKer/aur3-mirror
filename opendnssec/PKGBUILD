# Maintainer: Javier Torres <javitonino [at] gmail [dot] com>

pkgname=opendnssec
pkgver=1.3.4
pkgrel=1
pkgdesc="Turn-key solution for DNSSEC (sqlite3, no auditor)"
arch=('i686' 'x86_64')
url="http://www.opendnssec.org/"
license=('BSD')
groups=()
depends=('libxml2>=2.6.16' 'ldns>=1.6.9' 'sqlite3>=3.3.9')
makedepends=()
optdepends=('softhsm: key storage')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install="opendnssec.install"
changelog=
source=("http://www.opendnssec.org/files/source/${pkgname}-${pkgver}.tar.gz"
        "conf.xml"
        "LICENSE"
        "opendnssec.rcd"
        "softhsm.conf")
noextract=()
md5sums=('73196da477bfda620bd3b34e0d5b0a34'
         'd0e718d31648b9d12d31f4ca92c8273b'
         '367b9415779d31a737712f17df1f865b'
         '7f679379c8c2023ce6781b299767e5a2'
         '0985059bd71141f1aa24993f615ab5f6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # /var/lib/run -> /var/run
  sed -i "s/OPENDNSSEC_PID_DIR=\"\$full_localstatedir\/run\/opendnssec\"/OPENDNSSEC_PID_DIR=\"\/var\/run\/opendnssec\"/" configure

  ./configure --prefix=/usr --datarootdir=/usr/share --localstatedir=/var/lib --sysconfdir=/etc --with-pkcs11-softhsm=/usr/lib/libsofthsm.so --disable-auditor 

  # Create the correct (/var/run/opendnssec) directory
  sed -i \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run\/opendnssec/#Removed/" \
  -e "s/\$(INSTALL) -d \$(DESTDIR)\$(localstatedir)\/run/\$(INSTALL) -d \$(DESTDIR)\$(OPENDNSSEC_PID_DIR)/" \
  Makefile

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install

  install -d "${pkgdir}/etc/rc.d"
  install -o root -g root -m 755 "${srcdir}/opendnssec.rcd" \
        "${pkgdir}/etc/rc.d/opendnssec"
  install -o root -g root -m 644 "${srcdir}/softhsm.conf" \
        "${pkgdir}/etc/opendnssec"
  install -o root -g root -m 644 "${srcdir}/conf.xml" \
        "${pkgdir}/etc/opendnssec"
  install -D -m644 "${srcdir}/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
