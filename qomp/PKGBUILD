# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=qomp
pkgver=0.4
pkgrel=1
pkgdesc="Online Music Player that supports Yandex.Music, Myzuka.ru and ProstoPleer.com"
arch=('i686' 'x86_64')
url="https://code.google.com/p/qomp/"
license=('GPL2')
depends=('phonon-qt4')
source=(http://sourceforge.net/projects/qomp/files/qomp-$pkgver-beta-src.tar.gz)
md5sums=('e64dc515b69175ed02ff84d9dd3a735f')

prepare() { 
  cd $srcdir/$pkgname

  # Fix install path
  sed -i 's|usr/local|usr|' qomp.pro

  qmake-qt4

  # Fix lrelease -> lrelease-qt4
  sed -i 's|lrelease|lrelease-qt4|' Makefile

  # Fix phonon paths
  cd src
  sed -i 's|<Phonon/MediaObject>|"/usr/include/qt4/phonon/mediaobject.h"|' qompphonon{player.{cpp,h},metadataresolver.h}
  sed -i 's|<Phonon/AudioOutput>|"/usr/include/qt4/phonon/audiooutput.h"|' qompphononplayer.cpp
  sed -i 's|<Phonon/SeekSlider>|"/usr/include/qt4/phonon/seekslider.h"|' qompphononplayer.cpp
  sed -i 's|<Phonon/VolumeSlider>|"/usr/include/qt4/phonon/volumeslider.h"|' qompphononplayer.cpp
}

build() {
  cd $srcdir/$pkgname
  make
}

package() {
  cd $srcdir/$pkgname
  make INSTALL_ROOT=$pkgdir install

  # Desktop icon
  install -Dm644 src/icons/qomp.png  $pkgdir/usr/share/pixmaps/qomp.png
}