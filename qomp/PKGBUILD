# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=qomp
pkgver=0.5
pkgrel=1
pkgdesc="Online Music Player that supports Yandex.Music, Myzuka.ru and ProstoPleer.com"
arch=('i686' 'x86_64')
url="https://code.google.com/p/qomp/"
license=('GPL2')
depends=('phonon-qt4')
conflicts=('qomp-git')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-${pkgver}_beta-1.src.rpm)
md5sums=('3d6c357b144a074bf722ac87da2257a9')

prepare() { 
  cd $srcdir
  # Extract source RPM
  bsdtar -xf $pkgname-$pkgver*.tar.gz
  
  cd $pkgname-$pkgver*
  cmake -DCMAKE_INSTALL_PREFIX=/usr

  # Fix phonon paths
  cd src
  sed -i 's|<Phonon/MediaObject>|"/usr/include/qt4/phonon/mediaobject.h"|' qompphonon{player.{cpp,h},metadataresolver.h}
  sed -i 's|<Phonon/AudioOutput>|"/usr/include/qt4/phonon/audiooutput.h"|' qompphononplayer.cpp
  sed -i 's|<Phonon/SeekSlider>|"/usr/include/qt4/phonon/seekslider.h"|' qompphononplayer.cpp
  sed -i 's|<Phonon/VolumeSlider>|"/usr/include/qt4/phonon/volumeslider.h"|' qompphononplayer.cpp
}

build() {
  cd $srcdir/$pkgname-$pkgver*
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver*
  make DESTDIR=$pkgdir install

  # Desktop icon
  install -Dm644 src/icons/qomp.png $pkgdir/usr/share/pixmaps/qomp.png
}