# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=qomp
pkgver=0.7
pkgrel=1
pkgdesc="Quick(Qt) Online Music Player for different online music hostings."
arch=('i686' 'x86_64')
url="http://qomp.sourceforge.net"
license=('GPL2')
depends=('hicolor-icon-theme' 'taglib' 'qt5-multimedia')
makedepends=('cmake' 'qt5-tools')
conflicts=('qomp-git')
source=(http://sourceforge.net/projects/qomp/files/$pkgver/qomp-${pkgver}_beta-1.src.rpm)
md5sums=('5761f33ebbc23c641befd0dca19e60b3')

prepare() { 
  # I use the source RPM to build this package because 'normal' sources stop building due to
  # missing translation file.

  cd $srcdir
  # Extract tar.gz
  tar -xf $pkgname-${pkgver}_beta.tar.gz
  
  # Set library path to /usr/lib instead of /usr/lib64
  cd $pkgname-${pkgver}_beta
  sed -i 's|${LIB_SUFFIX}||' CMakeLists.txt
  
  # Fix syntax error
  sed -i 's|updates found|update found|' src/updateschecker.cpp
  
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DUSE_QT5=ON
}

build() {
  cd $pkgname-${pkgver}_beta
  make
}

package() {
  cd $pkgname-${pkgver}_beta
  make DESTDIR=$pkgdir install
}