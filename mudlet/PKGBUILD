# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=mudlet
pkgver=1.1.1
pkgrel=2
pkgdesc="A modern MUD client with a GUI and built-in Lua scripting"
arch=('i686' 'x86_64')
url="http://mudlet.sourceforge.net/"
license=('GPL')
depends=('lua' 'qt')
makedepends=('qscintilla')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-src.tar.gz")
md5sums=('0710ec2c9c146633ef8b659a1183b612')

build() {
  cd "${srcdir}/${pkgname}/src"
  qmake
  make SUBLIBS=-lz
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm755 "src/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${pkgname}.svg" "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
