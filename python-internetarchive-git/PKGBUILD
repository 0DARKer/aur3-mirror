# Maintainer:  jyantis <yantis@yantis.net>

pkgname=python-internetarchive-git
pkgver=0.7.3.r640.baa90bd
pkgrel=1
pkgdesc='A Python 3 wrapper for the various Internet Archive APIs (IA-S3, Metadata API, etc)'
arch=('any')
url='https://github.com/jjjake/ia-wrapper'
license=('AGPL3')
depends=('python'
         'python-setuptools'
         'python-clint'
         'python-six'
         'python-yaml'
         'python-requests'
         'python-jsonpatch'
         'python-pytest'
         'python-docopt'
         'python-jsonpointer'
         'python-args-git'
        )
optdepends=('python-ujson: faster json parsing'
            'python-gevent: concurrent downloads'
            'cython: speedups')
source=('git+https://github.com/jjjake/ia-wrapper.git')
sha256sums=('SKIP')

pkgver() {
  cd ia-wrapper
  set -o pipefail
  git describe --long | sed -r 's/([^-]*-g)/r/;s/-/./g' ||

  # Pull the version tag from the file 
  printf "%s." "$(grep -R "version=" setup.py | awk -F\' '{print $2}')"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ia-wrapper

  # Patch requirments.txt from == to >=
  sed -i 's/==/>=/g' setup.py

  python setup.py build
}

check() {
  cd ia-wrapper
  python setup.py test --verbose
}

package() {
  cd ia-wrapper

  # We don't need anything related to git in the package
  rm -rf .git* 

  python setup.py install --root="${pkgdir}" --optimize=1

  # Install License
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install Documentation
  install -D -m644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}

# vim:set ts=2 sw=2 et:
