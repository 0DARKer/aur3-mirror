# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=piedock
pkgver=1.4.3
pkgrel=1
pkgdesc="A task bar and application launcher in shape of a circular pie menu."
arch=(i686 x86_64)
url="http://www.markusfisch.de/"
license=('custom:MIT')
depends=('libpng' 'libxft' 'libxmu')
install="$pkgname.install"
source=("${url}downloads/PieDock-${pkgver}.tar.bz2"
        "$pkgname.desktop")
md5sums=('f34c5ac9b11dac6baedc05a20c25f76e'
         '82e7ee4040dc7a75d7486eb784eb9022')

build() {
  cd "$srcdir/PieDock-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/PieDock-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Sample configuration file
  install -Dm644 res/PieDockrc.sample "$pkgdir/usr/share/$pkgname/PieDockrc.sample"

  # Desktop file and pixmaps
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 res/active-indicators/gnome-emblem-new.png "$pkgdir/usr/share/pixmaps/PieDock.png"

  # License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
