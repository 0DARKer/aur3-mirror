# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: nggtony <nggtony@yahoo.it>

pkgname=darksnow
pkgver=0.7.1
pkgrel=1
pkgdesc="simple GTK2 GUI for darkice"
arch=(i686 x86_64)
url="http://darksnow.radiolivre.org/index.en.html"
license=('GPL')
depends=('darkice' 'gtk2' 'hicolor-icon-theme')
install="$pkgname.install"
source=("http://darksnow.radiolivre.org/pacotes/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('ce71796ea6195516a184230d9190aa5d'
         '3a75ce3a91acde294464c3fdea5cad06')


build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="$pkgdir/usr" install

  # desktop file
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
}
