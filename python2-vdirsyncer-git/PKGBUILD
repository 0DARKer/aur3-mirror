# Maintainer: eolianoe <eolianoe At GoogleMAIL DoT com>

pkgname=python2-vdirsyncer-git
_pkgname=vdirsyncer
pkgver=0.2.0.r33.g6a63dce
pkgrel=1
pkgdesc="Synchronize CalDAV and CardDAV."
arch=('any')
url="https://github.com/untitaker/vdirsyncer"
license=('MIT')
depends=('python2-click' 'python2-setuptools' 'python2-lxml' 'python2-requests'
         'python2-icalendar' 'python2-requests-toolbelt')
source=("git://github.com/untitaker/${_pkgname}.git")
md5sums=('SKIP')
conflicts=('python2-vdirsyncer')
provides=('python2-vdirsyncer')

pkgver() {
  cd "$srcdir/${_pkgname}"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${_pkgname}"
  python2 setup.py build
}

package() {
  cd "$srcdir/${_pkgname}"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

