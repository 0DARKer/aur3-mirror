# Maintainer: Frank Siegert <frank.siegert@googlemail.com>
pkgname=lhapdf
pkgver=6.1.2
pkgrel=1
pkgdesc="A particle physics tool for evaluating PDFs from discretised data files."
arch=('x86_64' 'i686')
url="http://lhapdf.hepforge.org/"
license=('GPL3')
depends=('python')
install=lhapdf.install
source=(http://www.hepforge.org/archive/lhapdf/LHAPDF-$pkgver.tar.gz)
noextract=()
md5sums=('23877fa03ef79542c4f75b2dea6d6caf')

prepare() {
	cd "$srcdir/LHAPDF-$pkgver"
        2to3 --no-diffs -w lhapdf.in
        sed -e "s/getheaders/get/g" -e "s/'rb'/newline=''/g" -i lhapdf.in
}

build() {
	cd "$srcdir/LHAPDF-$pkgver"
	./configure --prefix=/usr --disable-python
	make
}

check() {
	cd "$srcdir/LHAPDF-$pkgver"
	make -k check
}

package() {
	cd "$srcdir/LHAPDF-$pkgver"
	make DESTDIR="$pkgdir/" install
}
