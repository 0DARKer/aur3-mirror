# Maintainer: lspci <agm2819 at gmail> 
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: TDY <tdy@gmx.com>

pkgname=faangband
pkgver=1.3.1
pkgrel=2
pkgdesc="An Angband variant set in the First Age of Tolkien's Silmarillion world"
arch=('i686' 'x86_64')
url="http://angband.oook.cz/faangband/"
license=('GPL' 'custom')
depends=('sdl_image' 'sdl_mixer' 'sdl_ttf')
source=("http://angband.oook.cz/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('4617322659a4e605fe860f450f9fcda0')

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --with-configpath=/etc/${pkgname} \
    --enable-sdl
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  # Install game files
  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}/usr/share/${pkgname}/info"
  find "${pkgdir}/usr/share/${pkgname}" -name delete.me -delete

  # Install readme and license
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 copying.txt "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
