# Maintainer: TDY <tdy@gmx.com>

pkgname=faangband
pkgver=1.1.5
pkgrel=1
pkgdesc="An Angband variant set in the First Age of Tolkien's Silmarillion world"
arch=('i686' 'x86_64')
url="http://angband.oook.cz/faangband/"
license=('GPL')
depends=('sdl_image' 'sdl_mixer' 'sdl_ttf')
install=$pkgname.install
source=(http://angband.oook.cz/$pkgname/FAangband${pkgver//.}.tar.gz)
md5sums=('fac506516adb02fd98d6d646e50f48b6')

build() {
  cd "$srcdir/FAangband${pkgver//.}"
  ./configure --prefix=/usr --enable-sdl
  make || return 1
}

package() {
  cd "$srcdir/FAangband${pkgver//.}"
  make DESTDIR="$pkgdir" install
  find "$pkgdir/usr/share/$pkgname" -name delete.me -delete
}
