# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: TDY <tdy@gmx.com>

pkgname=faangband
pkgver=1.2.10
_pkgver=1210
pkgrel=1
pkgdesc="An Angband variant set in the First Age of Tolkien's Silmarillion world"
arch=('i686' 'x86_64')
url="http://angband.oook.cz/faangband/"
license=('GPL' 'custom')
depends=('sdl_image' 'sdl_mixer' 'sdl_ttf')
source=("http://angband.oook.cz/faangband/FAangband${_pkgver}.tar.gz")
md5sums=('5ebaf6fabba2bd19ebba32eb3381c76d')

build() {
  cd "${srcdir}/NickMcConnell-FAangband-e378b75"

  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --with-configpath=/etc/${pkgname} \
    --enable-sdl
  make
}

package() {
  cd "${srcdir}/NickMcConnell-FAangband-e378b75"

  # Install game files
  make DESTDIR="${pkgdir}" install
  mkdir "${pkgdir}/usr/share/${pkgname}/info"
  find "${pkgdir}/usr/share/${pkgname}" -name delete.me -delete

  # Install readme and license
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 copying.txt "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
