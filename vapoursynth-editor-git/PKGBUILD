# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Maxime Gauduin <alucryd@gmail.com>

pkgname=vapoursynth-editor-git
pkgver=r3.0.g54b3cd0
pkgrel=1
pkgdesc="A simple program for edit/create VapourSynth scripts. (GIT version)"
arch=('i686' 'x86_64')
url="http://forum.doom9.org/showthread.php?p=1688477"
license=('CCPL' 'MIT')
depends=('desktop-file-utils' 'qt5-base' 'shared-mime-info' 'vapoursynth')
makedepends=('git')
provides=('vapoursynth-editor')
conflicts=('vapoursynth-editor')
install=vapoursynth-editor-git.install
source=('git+https://bitbucket.org/mystery_keeper/vapoursynth-editor.git'
        'vsedit.desktop'
        'vsedit.sh'
        'x-vpy.xml')
sha1sums=('SKIP'
          '3596d1d264ccbfe9177428e2443f29bfa80f972b'
          '278134e1d29ccd9b78c76be41695b899ea7a5d24'
          'b0ca854f1864443e556b51fbcfeb9f243741038e')
_gitname='vapoursynth-editor'

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}/pro"
  qmake-qt5
  make
}

package() {
  install -Dm755 ../vsedit.sh "${pkgdir}/usr/bin/vsedit"
  install -Dm644 ../vsedit.desktop "${pkgdir}/usr/share/applications/vsedit.desktop"
  install -Dm644 ../x-vpy.xml "${pkgdir}/usr/share/mime/packages/x-vpy.xml"
  cd "${_gitname}/build/bin"
  install -Dm755 vsedit "${pkgdir}/usr/share/vsedit/vsedit"
  install -Dm644 vsedit.svg "${pkgdir}/usr/share/pixmaps/vsedit.svg"
  install -Dm644 fonts/DigitalMini.ttf "${pkgdir}/usr/share/vsedit/fonts/DigitalMini.ttf"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/vsedit/README"
}
