# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Maxime Gauduin <alucryd@gmail.com>

pkgname=vapoursynth-editor-git
pkgver=r3.2.g6faf036
pkgrel=1
pkgdesc="A simple program for edit/create VapourSynth scripts. (GIT version)"
arch=('i686' 'x86_64')
url="http://forum.doom9.org/showthread.php?p=1688477"
license=('CCPL' 'MIT')
depends=('desktop-file-utils' 'qt5-base' 'vapoursynth')
makedepends=('git')
provides=('vapoursynth-editor')
conflicts=('vapoursynth-editor')
install=vapoursynth-editor-git.install
source=('git+https://bitbucket.org/mystery_keeper/vapoursynth-editor.git'
        'vsedit.desktop')
sha1sums=('SKIP'
          'f08cc43120aa0ea1456f1bab8ee9ea51c22ad8db')
_gitname='vapoursynth-editor'

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}/pro"
  qmake-qt5
  make
}

package() {
  install -Dm644 "${srcdir}/vsedit.desktop" "${pkgdir}/usr/share/applications/vsedit.desktop"

  cd "${_gitname}/build/bin"
  install -Dm755 vsedit "${pkgdir}/usr/bin/vsedit"
  install -Dm644 vsedit.svg "${pkgdir}/usr/share/pixmaps/vsedit.svg"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README "${pkgdir}/usr/share/doc/vsedit/README"
}
