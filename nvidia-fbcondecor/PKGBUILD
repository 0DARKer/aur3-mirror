# $Id: PKGBUILD 83850 2010-06-23 10:39:40Z pierre $
# Maintainer : wuyingren <keitaroh@gmail.com>
# Contributor : Thomas Baechler <thomas@archlinux.org>
# Adapted for fbcondecor by: Conrad Nelson

pkgname=nvidia-fbcondecor
pkgver=295.20
_extramodules=extramodules-3.2-fbcondecor
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=1
pkgdesc="NVIDIA drivers for linux kernel 3.2 patched with fbcondecor."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-fbcondecor>=3.2' 'linux-fbcondecor<3.3' "nvidia-utils=${pkgver}")
makedepends=('linux-headers>=3.2' 'linux-headers<3.3')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia-fbcondecor.install


if [ "$CARCH" = "i686" ]; then
        _arch='x86'
        _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('a6f702271da49930a17e28b5928fe75a~')
elif [ "$CARCH" = "x86_64" ]; then
        _arch='x86_64'
        _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('79b05cffa482234833ae1c4bb40664c3')
fi

build() {
        cd "${srcdir}"
        sh "${_pkg}.run" --extract-only
        cd "${_pkg}/kernel"
        make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
        install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
                "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
        install -d -m755 "${pkgdir}/etc/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"
        sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-fbcondecor.install"
	gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}

