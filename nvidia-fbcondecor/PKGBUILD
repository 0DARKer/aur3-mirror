# $Id: PKGBUILD 161948 2012-06-16 22:01:05Z ibiru $
# Maintainer: nemesys <nemstar AT zoho DOT com>
# Contributor: wuyingren <keitaroh@gmail.com>
# Contributor : Thomas Baechler <thomas@archlinux.org>
# Adapted for fbcondecor by: Conrad Nelson

pkgname=nvidia-fbcondecor
pkgver=334.16
_extramodules=extramodules-3.13-fbcondecor
pkgrel=1
pkgdesc="NVIDIA drivers for linux kernel patched with fbcondecor."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-fbcondecor>=3.13' 'linux-fbcondecor<3.14' "nvidia-utils=${pkgver}")
makedepends=('linux-fbcondecor-headers>=3.13' 'linux-fbcondecor-headers<3.14')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia-fbcondecor.install

# doing this to force md5sum generating for i686
# CARCH=i686

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('2503a9ce1c17154099b2bfbdde86caf7')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('54be06165c88cda2235c2c7eee7a2575')
fi

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
        cd "${srcdir}"
        sh "${_pkg}.run" --extract-only
        cd "${_pkg}/kernel"
        make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
        install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
                "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
        install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
	echo "blacklist nvidiafb" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
        sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-fbcondecor.install"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
