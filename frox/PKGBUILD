# Contributor: Nathan Owe. <ndowens.aur at gmail dot com>

# Heavily inspired from Peter Pramberger's RHEL RPM
# http://www.pramberger.at/peter/services/repository/

pkgname=frox
pkgver=0.7.18
pkgrel=4
pkgdesc='A transparent ftp proxy that optionally supports caching'
arch=('i686' 'x86_64')
url="http://${pkgname}.sourceforge.net/"
license='GPL2'
backup=("etc/${pkgname}.conf" "etc/conf.d/${pkgname}")
options=('!purge')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}-clientdata_socket.patch"
        "${pkgname}.conf.patch"
        "${pkgname}.d"
        "${pkgname}.confd")
md5sums=('d4ba3a22db472922c71b0e63ca64b10b'
         '4f69c821c75d870837baa7f4d0af75a7'
         '774646b5c80f1b22be6c3f48e130c5df'
         '3208b799315a9bfdfae75e50105a4ed9'
         '15fc4c6c0369bc598d52814e39d1bfda')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # client active mode source address fix
  patch -Nup1 < "${srcdir}/${pkgname}-${pkgver}-clientdata_socket.patch"
  # default config file fixes
  patch -Nup1 < "${srcdir}/${pkgname}.conf.patch"

  ./configure \
    --prefix=/usr                               \
    --enable-ipfilter                           \
    --enable-http-cache                         \
    --enable-local-cache                        \
    --enable-virus-scan                         \
    --enable-ssl                                \
    --enable-ccp                                \
    --enable-procname                           \
    --enable-configfile="/etc/${pkgname}.conf"

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make install DESTDIR="${pkgdir}"

  local _cachedir="/var/cache/${pkgname}"
  local _mandir='/usr/share/man'

  install -dm755 -odaemon -gdaemon         "${pkgdir}${_cachedir}"
  install -Dm644 "doc/${pkgname}.conf.man" "${pkgdir}${_mandir}/man5/${pkgname}.conf.5"
  install -Dm644 "doc/${pkgname}.man"      "${pkgdir}${_mandir}/man8/${pkgname}.8"
  install -Dm644 "src/${pkgname}.conf"     "${pkgdir}/etc/${pkgname}.conf"

  install -Dm755 "${srcdir}/${pkgname}.d"     "${pkgdir}/etc/rc.d/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.confd" "${pkgdir}/etc/conf.d/${pkgname}"
}

