# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=szstools
pkgver=1.0
pkgrel=1
pkgdesc="Extract data from gcm and szs files. Contains gcmdump, yaz0dec, and rarcdump"
arch=('i686' 'x86_64')
url="http://www.amnoid.de/gc/"
license=('GPL')
source=('http://www.amnoid.de/gc/szstools.zip')
sha512sums=('c75bf30f39291687242be89cbf6f33a82fb6d152c05089066065b8324763ff6314116306c6df557fa072e1c1311bf566318c658de32f4ce195eee89b8cc640bd')

build() {
  cd "${srcdir}"

  # Compile yaz0dec
  g++ yaz0dec.cpp -o yaz0dec

  # The other source code needs to be modified as it was written to compile with the
  #   Digital Mars compiler (includes direct.h)

  # Replace 'direct.h' with 'sys/stat.h'
  sed -i 's/direct.h/sys\/stat.h/' rarcdump.cpp gcmdump.cpp

  # Change '_mkdir' to 'mkdir' with '0755' permissions
  sed -i 's/_mkdir(\(.*\));/mkdir(\1, S_IRWXU | S_IRGRP | S_IXGRP | S_IROTH | S_IXOTH);/g' rarcdump.cpp gcmdump.cpp

  # Change '_chdir' to 'chdir'
  sed -i 's/_chdir/chdir/g' rarcdump.cpp gcmdump.cpp

  # Compile rarcdump
  g++ rarcdump.cpp -o rarcdump

  # Compile gcmdump
  g++ gcmdump.cpp -o gcmdump
}

package() {
  cd "${srcdir}"

  # Install yaz0dec
  install -Dm755 yaz0dec "${pkgdir}/usr/bin/yaz0dec"

  # Install rarcdump
  install -Dm755 rarcdump "${pkgdir}/usr/bin/rarcdump"

  # Install gdmdump
  install -Dm755 gcmdump "${pkgdir}/usr/bin/gcmdump"
}

# vim:set ts=2 sw=2 et:
