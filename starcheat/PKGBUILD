pkgname=starcheat
epoch=6
pkgver=04610a0
pkgrel=1
pkgdesc="Starbound player save editor and Python library"
arch=(any)
url="http://community.playstarbound.com/index.php?resources/starcheat.699/"
license=("MIT")
depends=(python-pyqt5 python-pillow)
makedepends=(python git)
source=("https://github.com/wizzomafizzo/starcheat/archive/$pkgver.tar.gz"
"git+https://github.com/McSimp/StarDB.git"
"starcheat.desktop"
"starcheat.sh")
md5sums=('5d1f7d0d089172259a70cefa09f8efaf'
         'SKIP'
         'ef05183a35e289f6e036068db0d8c30b'
         'b1bdefdf82633ee135ea8980304f5513')

prepare() {
	cd "${srcdir}/starcheat-$pkgver/starcheat/stardb"
	cp -r "$srcdir/StarDB/"* .
}
         
build() {
	cd "${srcdir}/starcheat-$pkgver"
	./build.py -v
}

package() {
	cd "${srcdir}/starcheat-$pkgver"
	install -Dm755 "$srcdir/starcheat.sh" "$pkgdir/usr/bin/starcheat"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/starcheat/LICENSE"
	install -Dm644 "$srcdir/starcheat.desktop" "$pkgdir/usr/share/applications/starcheat.desktop"
	install -Dm644 mac/icon.iconset/icon_512x512@2x.png "$pkgdir/usr/share/icons/starcheat.png"
	cd build
	find . -name '*.py' -exec install -Dm644 {} "$pkgdir/usr/share/starcheat/"{} \;
	python -m compileall "$pkgdir/usr/share/starcheat"
} 
