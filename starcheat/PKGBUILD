pkgname=starcheat
epoch=9
pkgver=0.16
pkgrel=1
pkgdesc="Starbound player save editor and Python library"
arch=(any)
url="http://starcheat.org"
license=("MIT")
depends=(python-pyqt5 python-pillow)
makedepends=(python dos2unix)
source=("https://github.com/wizzomafizzo/starcheat/archive/$pkgver.tar.gz"
"py-starbound.zip::https://github.com/blixt/py-starbound/archive/8403ae8a3dacbea9a12db4b966d62f37b1a4f0b0.zip"
"starcheat.desktop"
"starcheat.sh")
md5sums=('090ae5909ce4ae694f9ebf3b2bbb731f'
         '6e450ce04d9b27aa93e8b1f8f66f2e0c'
         'ef05183a35e289f6e036068db0d8c30b'
         'b1bdefdf82633ee135ea8980304f5513')

prepare() {
	cd "${srcdir}"
	cd py-starbound-*
	find . -type f -exec dos2unix {} \;
	cp -r * ../starcheat-$pkgver/starcheat/starbound/
}
         
build() {
	cd "${srcdir}/starcheat-$pkgver"
	./build.py -v
}

package() {
	cd "${srcdir}/starcheat-$pkgver"
	install -Dm755 "$srcdir/starcheat.sh" "$pkgdir/usr/bin/starcheat"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/starcheat/LICENSE"
	install -Dm644 "$srcdir/starcheat.desktop" "$pkgdir/usr/share/applications/starcheat.desktop"
	install -Dm644 mac/icon.iconset/icon_512x512@2x.png "$pkgdir/usr/share/icons/starcheat.png"
	cd build
	find . -name '*.py' -exec install -Dm644 {} "$pkgdir/usr/share/starcheat/"{} \;
	python -m compileall "$pkgdir/usr/share/starcheat"
} 
