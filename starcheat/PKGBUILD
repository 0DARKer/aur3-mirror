pkgname=starcheat
epoch=9
pkgver=0.19
pkgrel=1
pkgdesc="Starbound player save editor and Python library"
arch=(any)
url="https://github.com/wizzomafizzo/starcheat"
license=("MIT")
depends=(python-pyqt5 python-pillow)
makedepends=(python dos2unix)
source=("https://github.com/wizzomafizzo/starcheat/archive/$pkgver.tar.gz"
"py-starbound.zip::https://github.com/wizzomafizzo/py-starbound/archive/262fb9f86dbe6130012e3a4a1db5458b9783f7ec.zip"
"starcheat.desktop"
"starcheat.sh")
md5sums=('77ef721de9818ad03e31b0602ed1fb0e'
         '0a2b1be7ec79bc3bb27289e05a5824a8'
         'ef05183a35e289f6e036068db0d8c30b'
         'b1bdefdf82633ee135ea8980304f5513')

prepare() {
	cd "${srcdir}"
	cd py-starbound-*
	find . -type f -exec dos2unix {} \;
	cp -r * ../starcheat-$pkgver/starcheat/starbound/
}
         
build() {
	cd "${srcdir}/starcheat-$pkgver"
	./build.py -v
}

package() {
	cd "${srcdir}/starcheat-$pkgver"
	install -Dm755 "$srcdir/starcheat.sh" "$pkgdir/usr/bin/starcheat"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/starcheat/LICENSE"
	install -Dm644 "$srcdir/starcheat.desktop" "$pkgdir/usr/share/applications/starcheat.desktop"
	install -Dm644 starcheat/images/starcheat_64.png "$pkgdir/usr/share/icons/starcheat.png"
	cd build
	find . -name '*.py' -exec install -Dm644 {} "$pkgdir/usr/share/starcheat/"{} \;
	cd ../starcheat/images/icons
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/starcheat/icons/"{} \;
	python -m compileall "$pkgdir/usr/share/starcheat"
} 
