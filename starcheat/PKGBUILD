pkgname=starcheat
epoch=9
pkgver=0.11
pkgrel=1
pkgdesc="Starbound player save editor and Python library"
arch=(any)
url="http://starcheat.org"
license=("MIT")
depends=(python-pyqt5 python-pillow)
makedepends=(python git)
source=("git+https://github.com/wizzomafizzo/starcheat.git#tag=$pkgver"
"starcheat.desktop"
"starcheat.sh")
md5sums=('SKIP'
         'ef05183a35e289f6e036068db0d8c30b'
         'b1bdefdf82633ee135ea8980304f5513')

prepare() {
	cd "${srcdir}/starcheat"
	git submodule update --init
}
         
build() {
	cd "${srcdir}/starcheat"
	./build.py -v
}

package() {
	cd "${srcdir}/starcheat"
	install -Dm755 "$srcdir/starcheat.sh" "$pkgdir/usr/bin/starcheat"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/starcheat/LICENSE"
	install -Dm644 "$srcdir/starcheat.desktop" "$pkgdir/usr/share/applications/starcheat.desktop"
	install -Dm644 mac/icon.iconset/icon_512x512@2x.png "$pkgdir/usr/share/icons/starcheat.png"
	cd build
	find . -name '*.py' -exec install -Dm644 {} "$pkgdir/usr/share/starcheat/"{} \;
	python -m compileall "$pkgdir/usr/share/starcheat"
} 
