pkgname=starcheat
epoch=6
pkgver=c505766
pkgrel=1
pkgdesc="Starbound player save editor and Python library"
arch=(any)
url="http://community.playstarbound.com/index.php?resources/starcheat.699/"
license=("MIT")
depends=(python-pyqt5)
makedepends=(python)
source=("https://github.com/wizzomafizzo/starcheat/archive/$pkgver.tar.gz"
"starcheat.png"
"starcheat.desktop"
"starcheat.sh")
md5sums=('ce6d3344fee637fd4f132e9ec5ca4deb'
         'c8b490099414f478973dcfc5077c4fba'
         'ef05183a35e289f6e036068db0d8c30b'
         'b1bdefdf82633ee135ea8980304f5513')

build() {
	cd "${srcdir}/starcheat-$pkgver"
	./build.py
}

package() {
	cd "${srcdir}/starcheat-$pkgver"
	install -Dm755 "$srcdir/starcheat.sh" "$pkgdir/usr/bin/starcheat"
	install -Dm644 "$srcdir/starcheat.png" "$pkgdir/usr/share/icons/starcheat.png"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/starcheat/LICENSE"
	install -Dm644 "$srcdir/starcheat.desktop" "$pkgdir/usr/share/applications/starcheat.desktop"
	cd build
	find . -name '*.py' -exec install -Dm644 {} "$pkgdir/usr/share/starcheat/"{} \;
	python -m compileall "$pkgdir/usr/share/starcheat"
} 
