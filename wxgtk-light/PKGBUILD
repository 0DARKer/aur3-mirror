# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: megadriver <megadriver at gmx dot com>
# Based on wxgtk from [extra]

pkgname=wxgtk-light
pkgver=2.8.12.1
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'libgl' 'libxxf86vm' 'libsm' 'sdl')
makedepends=('mesa' 'glu' 'libxt')
provides=('wxgtk')
conflicts=('wxgtk')
#source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2")
source=("http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2")

sha1sums=('05688dc03d61631750f5904273122bb40a2115f5')

build() {
  rm -fr "${srcdir}/wxWidgets-build"
  cp -R "${srcdir}/wxPython-src-${pkgver}" "${srcdir}/wxWidgets-build"
  cd "${srcdir}/wxWidgets-build"

  ./configure --prefix=/usr --libdir=/usr/lib --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,opengl,regex=builtin,sdl} --enable-{graphics_ctx,unicode} --disable-{mediactrl,optimize,precomp-headers}
  make
  make -C locale allmo
  cd contrib/src
  make
}

package() {
  cd "${srcdir}/wxWidgets-build"

# Install
  make DESTDIR="${pkgdir}" install
  make -C contrib/src DESTDIR="${pkgdir}" install
# License
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
