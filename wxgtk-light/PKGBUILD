# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=wxgtk-light
pkgver=3.0.1
pkgrel=2
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'libgl' 'libxxf86vm' 'libsm' 'sdl')
makedepends=('git' 'mesa' 'glu' 'libxt')
provides=('wxgtk=3.0.1')
conflicts=('wxgtk')
source=("wxwidgets::git+https://github.com/wxWidgets/wxWidgets.git#tag=WX_3_0_1")
md5sums=('SKIP')
_gitname=wxwidgets

build() {
  cd "${_gitname}"
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,regex=builtin,opengl,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode} \
              --disable-{mediactrl,precomp-headers,webview,visibility}
  make
  make -C locale allmo
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
