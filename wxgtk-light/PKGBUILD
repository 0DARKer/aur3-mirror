# Maintainer: megadriver <megadriver at gmx dot com>
# Based on wxgtk from [extra]

pkgname=wxgtk-light
pkgver=2.8.12.1
pkgrel=3
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'libgl' 'libxxf86vm' 'libsm')
makedepends=('mesa')
conflicts=('wxgtk')
provides=('wxgtk')
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2)
md5sums=('8c06c5941477beee213b4f2fa78be620')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"
#  cd "${srcdir}/wxGTK-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx  --disable-optimize --with-regex=builtin --with-libpng=sys \
    --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys --disable-precomp-headers
  make
  make -C locale allmo
  cd contrib/src
  make
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}"
#  cd "${srcdir}/wxGTK-${pkgver}"
  make DESTDIR="${pkgdir}" install
  cd contrib/src
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
