# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=wxgtk-light
pkgver=3.0.0
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!) (Git Version)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'libgl' 'libxxf86vm' 'libsm' 'sdl')
makedepends=('mesa' 'glu' 'libxt')
provides=('wxgtk')
conflicts=('wxgtk' 'wxgtk3.0')
source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2")
sha1sums=('756a9c54d1f411e262f03bacb78ccef085a9880a')
_gitname="wxWidgets"

prepare() {
  rm -fr build
  cp -R "wxWidgets-${pkgver}" build
}

build() {
  cd build
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,regex=builtin,opengl,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode,utf8,stl} \
              --disable-{mediactrl,precomp-headers,webview}
  make
  make -C locale allmo
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
