pkgname=cuwo-git
pkgver=109
pkgrel=2
pkgdesc="Open server and utilities for Cube World"
arch=(any)
license=("GPLv3")
depends=(twisted)
makedepends=(python2 git)
url="https://github.com/matpow2/cuwo"
source=("${pkgname%-*}::git+https://github.com/matpow2/cuwo.git"
"convertqmo.sh"
"cuwo.sh"
"mitm.sh")
backup=("srv/cuwo/config/base.py"
"srv/cuwo/config/irc.py")
conflicts=(cuwo)
provides=(cuwo)
md5sums=('SKIP'
         'b320b2bfdbdde8170afeeff809867678'
         'ba08f972d224681733427d8c9d4ec7c8'
         'cb3dd7826f50ad862883455e3f21c829')

pkgver () {
  cd "$srcdir/${pkgname%-*}"
  echo $(git rev-list --count master)
}

package() {
  cd "${srcdir}/${pkgname%-*}/"
  rm -r py2exe
  find . -name '*.py' -o -name '*.txt' -o -name '*.md' | xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/srv/cuwo/"{}
  python2 -m compileall "$pkgdir/srv/cuwo"
  install -Dm755 "$srcdir/cuwo.sh" "$pkgdir/usr/bin/cuwo"
  install -m755 "$srcdir/convertqmo.sh" "$pkgdir/usr/bin/convertqmo"
  install -m755 "$srcdir/mitm.sh" "$pkgdir/usr/bin/mitm"
  install -Dm644 "$pkgdir/srv/cuwo/COPYING.txt" "$pkgdir/usr/share/licenses/cuwo/COPYING.txt"
}
