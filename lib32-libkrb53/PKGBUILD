# Maintainer: p2k <Patrick.Schneider@uni-ulm.de>
_pkgbasename=libkrb53
pkgname=lib32-$_pkgbasename
pkgver=1.9
pkgrel=2
pkgdesc="MIT Kerberos runtime libraries (32-bit libraries only)"
url="http://people.redhat.com/~dhowells/keyutils/"
license=('custom')
arch=('x86_64')
source=(http://web.mit.edu/kerberos/dist/krb5/1.9/krb5-$pkgver-signed.tar)
depends=('lib32-e2fsprogs')
makedepends=('pkgconfig' 'gcc-multilib')
options=('!libtool')
md5sums=('220c3dbb05a1bae1e6ad0bec4a6ac331')

build() {
  cd "$srcdir"
  msg2 "Unpacking..."
  bsdtar -xf krb5-$pkgver.tar.gz
  msg2 "Configuring..."
  cd krb5-$pkgver/src
  ./configure --with-system-et --with-system-ss --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32' CPPFLAGS="-I/usr/include/et" || return 1
  msg2 "Compiling..."
  make -C include || return 1
  make -C util || return 1
  make -C lib/crypto || return 1
  make -C lib/krb5 || return 1
  make -C lib/gssapi
}

package() {
  cd "$srcdir/krb5-$pkgver/src"
  msg2 "Installing..."
  mkdir -p "$pkgdir/usr/lib32"
  mkdir -p $pkgdir/usr/include/{gssapi,krb5}
  make -C include DESTDIR="$pkgdir" install || return 1
  rm "$pkgdir/usr/include/kdb.h" "$pkgdir/usr/include/profile.h"
  make -C util/support DESTDIR="$pkgdir" install || return 1
  make -C lib/crypto DESTDIR="$pkgdir" install || return 1
  make -C lib/krb5 DESTDIR="$pkgdir" install || return 1
  make -C lib/gssapi DESTDIR="$pkgdir" install || return 1
  # We have to remove the unversioned symlink and the include
  # files in order to avoid conflicts with the heimdal package
  rm "$pkgdir/usr/lib32/libkrb5.so"
  rm -r "$pkgdir/usr/include"
  cd ..
  install -Dm644 NOTICE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
