# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=virtualdub
pkgver=1.9.11
pkgrel=1
pkgdesc="Video editing software (uses the WIN32 API)"
arch=('any')
url="http://www.virtualdub.org/"
license=('GPL2')
depends=('wine')
install=virtualdub.install
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgname-win/$pkgver.32842/VirtualDub-$pkgver.zip"
        'virtualdub.sh'
        'virtualdub.png::https://a.fsdn.com/con/icons/vi/virtualdub@sf.net/virtuald.png'
        'virtualdub.desktop')
md5sums=('d72bda914fe13c4e05a8dba79d46e7fb'
         'b8fe6d488559e39edd9f1af36177ae9c'
         '33e7d805166c15d8a78b9e8e7253ec1d'
         '7bc960fd3eac68947ae4e580498f9b2e')

package() {
  cd "$srcdir"

  install -d "$pkgdir"/usr/share/{"$pkgname",doc/"$pkgname",pixmaps,applications}
  install -Dm755 virtualdub.sh "$pkgdir/usr/bin/virtualdub"
  install -Dm644 *.exe "$pkgdir/usr/share/$pkgname"
  install -Dm644 *.vdi "$pkgdir/usr/share/$pkgname"
  install -Dm644 *.dll "$pkgdir/usr/share/$pkgname"
  install -Dm644 VirtualDub.chm "$pkgdir/usr/share/doc/$pkgname"
  ln -s "/usr/share/doc/$pkgname/VirtualDub.chm" "$pkgdir/usr/share/$pkgname/VirtualDub.chm"
  cp -r plugins aviproxy "$pkgdir/usr/share/$pkgname"
  install -Dm644 virtualdub.png "$pkgdir/usr/share/pixmaps"
  install -Dm644 virtualdub.desktop "$pkgdir/usr/share/applications"
  install -Dm644 copying "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
