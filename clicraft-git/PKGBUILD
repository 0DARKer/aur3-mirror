# Maintainer: DMBuce <dmbuce@gmail.com>
pkgname=clicraft-git
pkgver=20120221
pkgrel=3
pkgdesc="A command-line wrapper for a minecraft or bukkit server."
arch=("any")
url="https://github.com/DMBuce/clicraft"
license=('BSD')
depends=("bash" "tmux")
makedepends=("git")
#checkdepends=()
optdepends=("c10t: for map.sh action script" "mcexplore: for explore.sh action script")
provides=('clicraft')
conflicts=('clicraft')
install=
source=()
md5sums=() #generate with 'makepkg -g'

_gitname="clicraft"
_gitroot="git://github.com/DMBuce/clicraft.git"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  make -C "$srcdir/$_gitname-build" prefix=/usr DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/bin"
  cd "$pkgdir/usr/bin"
  ln -s "../share/clicraft/bin/clicraft"

  cd "$pkgdir/usr/share/clicraft"

  install -d "$pkgdir/etc"
  mv etc "$pkgdir/etc/clicraft"
  ln -s "../../../etc/clicraft" etc

  install -d "$pkgdir/usr/lib"
  mv lib "$pkgdir/usr/lib/clicraft"
  ln -s "../../../usr/lib/clicraft" lib

  install -d "$pkgdir/srv/minecraft"
  ln -s "../../../srv/minecraft" srv

  install -d "$pkgdir/etc/bash_completion.d"
  install -D -m 644 "$srcdir/$_gitname-build/scripts/bash_completion" "$pkgdir/etc/bash_completion.d/clicraft"
}

# vim:set ts=2 sw=2 et:
