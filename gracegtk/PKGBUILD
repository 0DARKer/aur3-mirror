# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=gracegtk
pkgver=0.4.0
pkgrel=2
pkgdesc="A port of the Grace plotting tool to gtk2"
arch=(i686 x86_64)
url="http://plasma-gate.weizmann.ac.il/Grace/"
depends=('libjpeg' 'fftw' 't1lib' 'netcdf' 'pdflib-lite' 'gtk2')
makedepends=('linuxdoc-tools')
optdepends=('libundo: ability to undo and redo'
	    'gcc-fortran: computing level curves')
license=('GPL')
install=gracegtk.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}_2011_06_16_18h56.tgz $pkgname.png $pkgname.desktop $pkgname-mimetypes ggrace)

build() {
  cd $srcdir/$pkgname-$pkgver
  grep -rl "'H', TRUE" src | xargs sed -i -e "s/'H', TRUE/'H', FALSE/g"
  grep -rl "'H' ,TRUE" src | xargs sed -i -e "s/'H' ,TRUE/'H', FALSE/g"
  sed -i -e "s/.*right_justified.*//g" src/gg.c
  sed -i -e "s/.*right_justified.*//g" src/gg_gtkutils.c

  ./configure --prefix=/usr --with-f77=/usr/bin/gfortran
  make || return 1
}

package() {
  export TEXINPUTS=$TEXINPUTS:/usr/share/linuxdoc-tools
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install || return 1

  mkdir $pkgdir/usr/share
  mv $pkgdir/usr/$pkgname $pkgdir/usr/share/
  install -D -m755 "$srcdir/ggrace" "$pkgdir/usr/bin/ggrace"

  # Desktop integration
  install -D -m644 "$srcdir/$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -D -m644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -D -m644 "$srcdir/$pkgname-mimetypes" "$pkgdir/usr/share/mime/packages/$pkgname.xml"
}


md5sums=('420217dbcf81ed41fced1b9f8cc25194' 'ef085e503d30207035f5dfbef648ed36' '8427067a73698b5425901866ca3195d0' '3beba5237f56a2ff28b6cd878865b25c' 'c22c24f810bf0e1f9b8ca8713be19e3e')
