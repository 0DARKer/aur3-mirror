# Maintainer: Maxime Morel <maxime@mmorel.eu>

pkgname=opentrack-git
pkgver=r2228
pkgrel=2
pkgdesc="opentrack is an application dedicated to tracking user's head movements and relaying the information to games and flight simulation software"
arch=('x86_64' 'i686')
url="https://github.com/opentrack/opentrack"
license=('GPL')
depends=('opencv' 'qt5-serialport')
makedepends=('git' 'cmake')
provides=('opentrack')
conflicts=('opentrack')
source=("git+https://github.com/opentrack/opentrack.git" "opentrack.desktop")
md5sums=('SKIP' '80bf1ad9243a23788d4cec98ec156ebb')

pkgver() {
  cd "$srcdir/opentrack"
  printf "r%s" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "$srcdir/opentrack"
}

build() {
  cd "$srcdir"
  mkdir -p cmake_build
  cd cmake_build
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/opt/opentrack ../opentrack
  make
}

package() {
  cd "$srcdir/cmake_build"
  make DESTDIR="$pkgdir/" install

  install -Dm644 $srcdir/opentrack.desktop $pkgdir/usr/share/applications/opentrack.desktop
}

