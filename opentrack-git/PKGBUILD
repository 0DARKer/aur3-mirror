# Maintainer: Maxime Morel <maxime@mmorel.eu>

pkgname=opentrack-git
pkgver=r2218
pkgrel=1
pkgdesc="opentrack is an application dedicated to tracking user's head movements and relaying the information to games and flight simulation software"
arch=('x86_64' 'i686')
url="https://github.com/opentrack/opentrack"
license=('GPL')
depends=('opencv' 'qt5-serialport')
makedepends=('git' 'cmake')
provides=('opentrack')
conflicts=('opentrack')
source=("git+https://github.com/opentrack/opentrack.git" "0001-fix.patch")
md5sums=('SKIP' '847ed5c0825a6cbe669f528ea9330029')

pkgver() {
  cd "$srcdir/opentrack"
  printf "r%s" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "$srcdir/opentrack"
  patch -p1 < ../0001-fix.patch
}

build() {
  cd "$srcdir"
  mkdir -p cmake_build
  cd cmake_build
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/opt/opentrack ../opentrack
  make
}

package() {
  cd "$srcdir/cmake_build"
  make DESTDIR="$pkgdir/" install
}
