# This is the PKGBUILD for mosquitto, an MQTT broker and example clients

# Maintainer: Alexander Rust <mail at alr dot st> 
# Contributor: Dan Anderson <dan-anderson at cox dptnet>

pkgname=mosquitto
pkgver=1.3.1
pkgrel=1
pkgdesc="An Open Source MQTT v3.1 Broker"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="http://mosquitto.org/"
depends=('openssl' 'c-ares')
makedepends=('python' 'docbook-xsl' 'c-ares')
conflicts=('mosquitto-hg')
provides=('mosquitto')
license=('BSD')
source=(http://mosquitto.org/files/source/$pkgname-$pkgver.tar.gz{,.asc} "$pkgname.service" "$pkgname.install" "docbook.patch" "usr_move.patch")
install=$pkgname.install
md5sums=('2f2870e965cf6f0b5df4ecaf2a0955d2'
         'SKIP'
         'a44fa1a85964b7309f5fb6de774aaa07'
         'bfabddbce1d8c856cbb52517a7917d4c'
         '8e1c14e99d7eba210b874e80b5153f0d'
         'b37551bbdccf751cdc5ea5b25afd2f5a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
 # patch -p1 < ../docbook.patch
  patch -p1 < ../usr_move.patch

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make prefix=/usr DESTDIR="$pkgdir/" install

  # systemd service file
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  echo 'pid_file /run/mosquitto.pid' >> "$pkgdir/etc/mosquitto/mosquitto.conf.example"

  # license files
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 LICENSE-3rd-party.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE-3rd-party"
}
