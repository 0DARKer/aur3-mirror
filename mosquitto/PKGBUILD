# This is the PKGBUILD for mosquitto, an MQTT broker and example clients
# Maintainer: Alexander Rust <mail at alr dot st> 
# Contributor: Dan Anderson <dan-anderson at cox dptnet>

pkgname=mosquitto
pkgver=1.1.3
pkgrel=2
pkgdesc="An Open Source MQTT v3.1 Broker"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="http://mosquitto.org/"
makedepends=('python')
optdepends=('python: python support')
license=('BSD')
source=(http://mosquitto.org/files/source/$pkgname-$pkgver.tar.gz{,.asc} "$pkgname.service" )
md5sums=('fd0cae17221d778b0a002c31e6c3de9e'
         'SKIP'
         'b7cdd7a65d9a7ac5a8c2456f4b1f2d8f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  #Remove hardcoded prefix
  find -name Makefile -exec sed '/prefix=/d' -i {} \;
  make prefix=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix=/usr DESTDIR="$pkgdir/" install
  
  # Systemd service file
  install -Dm644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  
  # License files
  install -Dm644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 LICENSE-3rd-party.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE-3rd-party
}

