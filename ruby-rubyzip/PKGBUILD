# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=ruby-rubyzip
pkgver=1.1.6
pkgrel=2
pkgdesc='Ruby module for reading and writing zip files'
arch=(any)
url='http://github.com/rubyzip/rubyzip'
license=('BSD')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/rubyzip-${pkgver}.gem"
        'LICENSE')
noextract=("rubyzip-$pkgver.gem")
sha256sums=('a996435ee9698be6a09d3748f4d23ee15aaf45cbfef1749def165af6ea3c0a9e'
            '5572bf03c3c8c4770e8153038f5f54faad9c3db9900864a7c82a19de4434d74b')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install \
    -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" "rubyzip-${pkgver}.gem"

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"  
}
