# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=artyfx
pkgver=1.2
pkgrel=1
pkgdesc="A LV2 plugin bundle of artistic realtime effects"
arch=('i686' 'x86_64')
url="http://openavproductions.com/artyfx/"
license=('GPL')
groups=('lv2-plugins')
depends=('lv2' 'ntk-git')
makedepends=('cmake')
source=("https://github.com/harryhaaren/openAV-ArtyFX/archive/release-$pkgver.tar.gz")
md5sums=('73a2499a9effca880e4ac1344c7b1fdf')

prepare() {
  cd openAV-ArtyFX-release-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd openAV-ArtyFX-release-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd openAV-ArtyFX-release-$pkgver/b
  make DESTDIR="$pkgdir/" install
}
