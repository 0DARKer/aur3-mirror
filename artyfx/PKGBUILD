# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=artyfx
pkgver=1.1
pkgrel=1
pkgdesc="A LV2 plugin bundle of artistic realtime effects"
arch=('i686' 'x86_64')
url="http://openavproductions.com/artyfx/"
license=('GPL')
groups=('lv2-plugins')
depends=('lv2' 'cairomm' 'libsndfile' 'ntk-git')
source=("https://github.com/harryhaaren/openAV-ArtyFX/archive/release-$pkgver.tar.gz")
md5sums=('647a9dfea52063e17d021f87caf19063')

prepare() {
  cd openAV-ArtyFX-release-$pkgver
  # install systemwide
  sed -i 's|~/\.|$(DESTDIR)/usr/lib/|' */makefile
}

build() {
  cd openAV-ArtyFX-release-$pkgver
  make
}

package() {
  cd openAV-ArtyFX-release-$pkgver
  install -d "$pkgdir/usr/lib/lv2"
  make DESTDIR="$pkgdir/" install
}
