# Maintainer: Carl George < arch at cgtx.us >

_module="python-openstackclient"
pkgname=("python-openstackclient" "python2-openstackclient")
pkgver="1.0.3"
pkgrel="1"
pkgdesc="OpenStack Command-line Client"
arch=("any")
url="https://github.com/openstack/python-openstackclient"
license=("Apache")
makedepends=(
	"python-setuptools"
	"python-pbr"
	"python2-setuptools"
	"python2-pbr"
)
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('d39a2e6cf98d409f8545b9d3a207eb8cbf3b2fc5ea17b0f8d9bed52326ca9f49')

prepare() {
	sed -i '/pbr/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

package_python-openstackclient() {
	depends=(
		"python-six>=1.9.0"
		"python-babel>=1.3"
		"python-cliff>=1.7.0"
		"python-cliff-tablib>=1.0"
		"python-oslo-config>=1.9.0"
		"python-oslo-i18n>=1.3.0"
		"python-oslo-utils>=1.2.0"
		"python-oslo-serialization>=1.2.0"
		"python-glanceclient>=0.15.0"
		"python-keystoneclient>=1.1.0"
		"python-novaclient>=2.18.0"
		"python-cinderclient>=1.1.0"
		"python-neutronclient>=2.3.11"
		"python-requests>=2.2.0"
		"python-stevedore>=1.1.0"
	)
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-openstackclient() {
	depends=(
		"python2-six>=1.9.0"
		"python2-babel>=1.3"
		"python2-cliff>=1.7.0"
		"python2-cliff-tablib>=1.0"
		"python2-oslo-config>=1.9.0"
		"python2-oslo-i18n>=1.3.0"
		"python2-oslo-utils>=1.2.0"
		"python2-oslo-serialization>=1.2.0"
		"python2-glanceclient>=0.15.0"
		"python2-keystoneclient>=1.1.0"
		"python2-novaclient>=2.18.0"
		"python2-cinderclient>=1.1.0"
		"python2-neutronclient>=2.3.11"
		"python2-requests>=2.2.0"
		"python2-stevedore>=1.1.0"
	)
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
