# Maintainer: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Zariel <c.bannister@gmail.com>

pkgname=irssi-git
pkgver=20121031
pkgrel=2
pkgdesc='Modular text mode IRC client with Perl scripting'
arch=('i686' 'x86_64')
url='http://irssi.org/'
license=('GPL')
depends=('glib2' 'openssl')
makedepends=('git' 'lynx')
optdepends=('perl-libwww: for the scriptassist script')
conflicts=('irssi')
provides=('irssi') 
backup=('etc/irssi.conf')
options=('!libtool')

_gitroot='git://git.irssi.org/irssi'
_gitname='irssi'

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [ -d $_gitname ] ; then
		cd $_gitname
		git fetch origin
		git merge origin/trunk
		msg "The local files are updated."
	else
		git clone $_gitroot $_gitname
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	[ -d $srcdir/$_gitname-build ] && rm -rf "$srcdir/$_gitname-build"
	git clone $srcdir/$_gitname $srcdir/$_gitname-build
	cd "$srcdir/$_gitname-build"

	./autogen.sh --prefix=/usr \
				--enable-ipv6 \
				--with-proxy \
				--sysconfdir=/etc \
				--with-perl-lib=vendor
	make
}

package() {
	cd "$srcdir/$_gitname-build"
	make DESTDIR="$pkgdir/" install
}
