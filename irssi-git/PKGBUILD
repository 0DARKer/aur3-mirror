#  Maintainer: sudokode <sudokode@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Zariel <c.bannister@gmail.com>

pkgname=irssi-git
pkgver=4212.8e2900c
pkgrel=1
pkgdesc="Modular text mode IRC client with Perl scripting"
arch=('i686' 'x86_64')
url="http://irssi.org/"
license=('GPL')
depends=('glib2' 'openssl')
makedepends=('git' 'html2text')
optdepends=('perl-libwww: for the scriptassist script')
conflicts=('irssi')
provides=('irssi') 
backup=('etc/irssi.conf')
source=("$pkgname::git://git.irssi.org/irssi"
        'html2text.patch')
sha256sums=('SKIP'
            'a56c2ca4f4aaa2edcc6c3361ed1f4bfc64de0d9dcb6b955c2e61a2aeb8367462')

pkgver() {
  cd $pkgname

  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname

  # Requiring lynx or elinks to format your stupid docs is stupid and gtfo
  # Patch to allow html2text or nothing at all
  # If you don't care about the FAQ, just comment this line out (and remove html2text from the makedeps)
  patch -p1 -i "$srcdir"/html2text.patch
}

build() {
  cd $pkgname

  ./autogen.sh \
        --prefix=/usr \
        --enable-ipv6 \
        --with-proxy \
        --sysconfdir=/etc \
        --with-perl-lib=vendor

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
