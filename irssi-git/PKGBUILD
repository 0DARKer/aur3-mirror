#  Maintainer: sudokode <sudokode@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Zariel <c.bannister@gmail.com>

pkgname=irssi-git
pkgver=0.8.16.98.g2e6f16c
pkgrel=1
pkgdesc="Modular text mode IRC client with Perl scripting"
arch=('i686' 'x86_64')
url="http://irssi.org/"
license=('GPL')
depends=('glib2' 'openssl')
makedepends=('git' 'html2text')
optdepends=('perl-libwww: for the scriptassist script')
conflicts=('irssi')
provides=('irssi') 
backup=('etc/irssi.conf')
source=("$pkgname::git+https://github.com/irssi/irssi.git"
        'html2text.patch')
sha256sums=('SKIP'
            '94bcce32e30ecde9432d2994d029085d25b9060a73654245b7712f1760d4373b')

pkgver() {
  cd $pkgname

  git describe --tags | sed 's/-/./g'
}

prepare() {
  cd $pkgname

  # Requiring lynx or elinks to format your stupid docs is stupid and gtfo
  # Patch to allow html2text or nothing at all
  # If you don't care about the FAQ, just comment this line out (and remove html2text from the makedeps)
  patch -p1 -i "$srcdir"/html2text.patch
}

build() {
  cd $pkgname

  ./autogen.sh \
        --prefix=/usr \
        --enable-ipv6 \
        --with-proxy \
        --sysconfdir=/etc \
        --with-perl-lib=vendor

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
