# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=vogl-git
pkgver=114.f151cce
pkgrel=1
pkgdesc="An OpenGL debugger"
arch=('i686' 'x86_64')
url="https://github.com/ValveSoftware/vogl"
license=('MIT')
# if building the glxspheres64 demo, add following dependencies:
#     'glu' 'freeglut' 'libxxf86vm' 'sdl2'
depends=('qt4' 'libjpeg-turbo' 'tinyxml' 'libunwind')
makedepends=('git' 'cmake')
#options=('!buildflags' '!makeflags')
source=('git+https://github.com/ValveSoftware/vogl.git')
md5sums=('SKIP')

[ $CARCH == 'x86_64' ] && _EXTRAFLAGS="-DBUILD_X64=On" || _EXTRAFLAGS="-DBUILD_X64=Off"

pkgver() {
  cd "$srcdir/vogl"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/vogl"
  # disable building of glxspheres64 demo to keep the dependencies minimal
  sed -i 's|^add_subdirectory.*glxspheres.*|#&|' CMakeLists.txt
}

build() {
  mkdir -p "$srcdir/build"

  cd "$srcdir/build"
  cmake ../vogl \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=True \
    $_EXTRAFLAGS
  make
}

package() {
  cd "$srcdir/vogl"

  install -d -m755 "$pkgdir"/usr/{bin,lib}

  install -m644 vogl_build/bin/*.so "$pkgdir"/usr/lib
  install -m755 vogl_build/bin/vogl* "$pkgdir"/usr/bin

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
