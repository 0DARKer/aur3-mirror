# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=vogl-git
pkgver=418.b3a52c2
pkgrel=1
pkgdesc="An OpenGL debugger"
arch=('i686' 'x86_64')
url="https://github.com/ValveSoftware/vogl"
license=('MIT')
# if building the glxspheres64 demo, add following dependencies:
#     'glu' 'freeglut' 'libxxf86vm' 'sdl2'
depends=('qt4' 'libjpeg-turbo' 'tinyxml' 'libunwind' 'sdl2')
makedepends=('git' 'cmake')
optdepends=('steam: use steamlauncher to trace games from Steam'
            'lib32-vogl-git: trace 32bit applications in 64bit Arch')
#options=('!buildflags' '!makeflags')
source=('git+https://github.com/ValveSoftware/vogl.git' \
        'https://bitbucket.org/raddebugger/vogl_chroot/raw/HEAD/bin/src/sl.cpp' \
        'steamlauncher_libraries.diff' 'vogleditor_paths.diff'
        'vogl_no_suffix.diff')
md5sums=('SKIP'
         'SKIP'
         '7991a02e0ea5903a86436e4a17453ea2'
         'a8cc334db199f2844f156264f01447d9'
         '422bc63ee0007f6264300ae6f8c1f696')

[ $CARCH == 'x86_64' ] && _EXTRAFLAGS="-DBUILD_X64=On" || _EXTRAFLAGS="-DBUILD_X64=Off"

pkgver() {
  cd "$srcdir/vogl"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/vogl"
  # disable building of glxspheres64 demo to keep the dependencies minimal
  sed -i 's|add_subdirectory.*glxspheres.*|#&|' CMakeLists.txt

  # allow the steam launcher to use the system wide vogl libraries
  cd "$srcdir"
  patch --follow-symlinks -Np0 < steamlauncher_libraries.diff || true

  # use our system-wide steamlauncherbinary
  cd "$srcdir/vogl"
  patch -Np1 < "$srcdir"/vogleditor_paths.diff

  # don't use any strange suffix for binaries/libraries
  cd "$srcdir/vogl"
  patch -Np1 < "$srcdir"/vogl_no_suffix.diff
}

build() {
  mkdir -p "$srcdir/build"

  # build vogl
  cd "$srcdir/build"
  cmake ../vogl \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=True \
    $_EXTRAFLAGS
  make

  # build steamlauncher
  g++ $CXXFLAGS -o steamlauncher ../sl.cpp
}

package() {
  cd "$srcdir/vogl"

  install -d -m755 "$pkgdir"/usr/{bin,lib}

  install -m644 vogl_build/bin/*.so "$pkgdir"/usr/lib
  install -m755 vogl_build/bin/vogl* "$pkgdir"/usr/bin

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # install steamlauncher
  install -D -m755 "$srcdir"/build/steamlauncher "$pkgdir"/usr/bin/steamlauncher
}
