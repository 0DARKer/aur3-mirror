# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=vogl-git
pkgver=28.f5c6fef
pkgrel=1
pkgdesc="An OpenGL debugger"
arch=('i686' 'x86_64')
url="https://github.com/ValveSoftware/vogl"
license=('MIT')
# 'glu' 'freeglut' 'libxxf86vm' 'sdl2' are dependencies of glxspheres64 demo
# that is disabled in this package
depends=('qt4' 'libjpeg-turbo')
makedepends=('git' 'cmake')
#options=('!buildflags' '!makeflags')
source=('git+https://github.com/ValveSoftware/vogl.git')
md5sums=('SKIP')

[ $CARCH == 'x86_64' ] && _EXTRAFLAGS="-DBUILD_X64=On" || _EXTRAFLAGS="-DBUILD_X64=Off"

pkgver() {
  cd "$srcdir/vogl"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/vogl"
  # disable building of glxspheres64 demo to keep the dependencies minimal
  sed -i 's|^add_subdirectory(glxspheres)|#&|' src/CMakeLists.txt
  # fix library name
  sed -i 's|libturbojpeg\.a|turbojpeg|' src/vogltrace/CMakeLists.txt
}

build() {
  mkdir -p "$srcdir/build"

  # build static libunwind (I was not able to make everything work with dynamic library)
  cd "$srcdir/vogl/external/libunwind"
  CFLAGS="-fPIC" ./configure --prefix="$srcdir/build/external" --enable-debug-frame --enable-shared=no --enable-static=yes --disable-minidebuginfo
  make
  make install

  cd "$srcdir/build"
  cmake ../vogl/src \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=True \
    -DCMAKE_PREFIX_PATH="$srcdir/build/external" \
    -DBUILD_GL_TESTS=False \
    -DBUILD_GL_SAMPLES=False \
    $_EXTRAFLAGS
  make
}

package() {
  cd "$srcdir/vogl"

  install -d -m755 "$pkgdir"/usr/{bin,lib}

  install -m644 vogl_build/bin/*.so "$pkgdir"/usr/lib
  install -m755 vogl_build/bin/vogl* "$pkgdir"/usr/bin

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
