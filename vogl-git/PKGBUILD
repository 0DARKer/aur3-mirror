# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=vogl-git
pkgver=2.5c57824
pkgrel=1
pkgdesc="An OpenGL debugger"
arch=('i686' 'x86_64')
url="https://github.com/ValveSoftware/vogl"
license=('MIT')
depends=('glu' 'freeglut' 'libxxf86vm' 'sdl2' 'qt4' 'libjpeg-turbo')
makedepends=('git' 'cmake')
options=('!buildflags' '!makeflags')
source=('git+https://github.com/ValveSoftware/vogl.git' \
        'fix_build.diff')
md5sums=('SKIP'
         'c0d4452b740def582f174fca7a582de5')

[ $CARCH == 'x86_64' ] && _EXTRAFLAGS="-DBUILD_X64=On" || _EXTRAFLAGS="-DBUILD_X64=Off"

pkgver() {
  cd "$srcdir/vogl"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/vogl"
  patch -Np1 < "$srcdir/fix_build.diff"
}

build() {
  mkdir -p "$srcdir/build"

  # build static libunwind (I was not able to make everything work with dynamic library)
  cd "$srcdir/vogl/external/libunwind"
  CFLAGS="-fPIC" ./configure --prefix="$srcdir/build/external" --enable-debug-frame --enable-shared=no --enable-static=yes --disable-minidebuginfo
  make
  make install

  cd "$srcdir/build"
  cmake ../vogl/src \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=True \
    -DCMAKE_PREFIX_PATH="$srcdir/build/external" \
    $_EXTRAFLAGS
  make
}

package() {
  cd "$srcdir/vogl"

  install -d -m755 "$pkgdir"/usr/{bin,lib}
  install -d -m755 "$pkgdir"/opt

  install -m644 vogl_build/bin/*.so "$pkgdir"/usr/lib
  cp -a vogl_build/bin "$pkgdir"/opt/vogl
  rm "$pkgdir"/opt/vogl/*.so

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
