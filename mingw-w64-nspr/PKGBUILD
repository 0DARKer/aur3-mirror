pkgname=mingw-w64-nspr
pkgver=4.10.4
pkgrel=1
pkgdesc="Netscape Portable Runtime (mingw-w64)"
arch=(any)
url="http://www.mozilla.org/projects/nspr/"
license=('MPL' 'GPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config')
options=('staticlibs' '!buildflags' '!strip')
source=("ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/nspr-${pkgver}.tar.gz"
        mingw32-nspr-4.7.2-build.patch)
md5sums=('db8e5c40dadcf3f71a20c01f503c573a'
        'SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"


prepare () {
  cd "$srcdir/nspr-$pkgver/nspr"
#   patch -p0 -i "$srcdir"/mingw32-nspr-4.7.2-build.patch
}


build() {
  cd "$srcdir/nspr-$pkgver/nspr"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    if [ "${_arch}" = "x86_64-w64-mingw32" ]; then
      _confflags="--enable-64bit"
    else
      _confflags=""
    fi
    mkdir -p build-${_arch} && pushd build-${_arch}
    CC=${_arch}-gcc ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --enable-win32-target=WINNT \
                 --enable-shared \
                 --enable-optimize \
                 --disable-debug ${_confflags}
    make -C config export CC=gcc CFLAGS="-DXP_UNIX=1"
    make NSINSTALL=$(pwd)/config/nsinstall RC=${_arch}-windres RANLIB=${_arch}-ranlib
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir"/nspr-${pkgver}/nspr/build-${_arch}
    make install DESTDIR="$pkgdir" NSINSTALL="$srcdir"/nspr-$pkgver/build-${_arch}/config/nsinstall
    rm -r "$pkgdir"/usr/${_arch}/share
    rm -r "$pkgdir"/usr/${_arch}/bin/{compile-et.pl,prerr.properties} \
          "$pkgdir/usr/${_arch}/include/nspr/md"
    mv "$pkgdir"/usr/${_arch}/lib/*.dll "$pkgdir"/usr/${_arch}/bin/
    
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
