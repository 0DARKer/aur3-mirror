# Maintainer: Ner0

pkgname=nemo-git
pkgver=20130601.8b01eb5
pkgrel=1
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('libexif' 'gvfs' 'dconf' 'desktop-file-utils' 'exempi' 'python2'
         'gnome-desktop' 'gnome-icon-theme' 'libnotify' 'libtracker-sparql')
makedepends=('git' 'gtk-doc' 'gobject-introspection' 'intltool' 'gnome-common')
optdepends=('nemo-dropbox-git: Dropbox integration'
            'nemo-fileroller-git: File Roller integration'
            'gksu: Open as Root')
provides=('nemo' 'nemo-fm' 'nautilus')
conflicts=('nemo' 'nemo-fm')
options=('!emptydirs' '!libtool')
install=nemo.install
source=('git://github.com/linuxmint/nemo.git' 'tracker-0.16.patch' 'remove-desktop-background.patch')
md5sums=('SKIP' '9e170cc74eee901634b3367b06a209c6' 'f10af52377e44e1efe8ec4934aa4d4d2')

pkgver() {
  cd "$srcdir/nemo"
  git log -1 --format='%cd.%h' --date=short | tr -d - | sed 's/-/./'
}

prepare() {
  cd "$srcdir/nemo"

  # Python2 fix
  sed -i 's/bin\/python/bin\/python2/g' files/usr/share/nemo/actions/myaction.py

  # https://github.com/linuxmint/nemo/pull/258
  patch -Np1 -i ../tracker-0.16.patch

  # https://github.com/linuxmint/nemo/pull/263
  patch -Np1 -i ../remove-desktop-background.patch
}

build() {
  cd "$srcdir/nemo"

  AUTOMAKE=automake \
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-gtk-doc-html \
      --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/nemo"
  make DESTDIR="$pkgdir/" install
}
