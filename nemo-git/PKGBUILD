# Maintainer: Ner0

pkgname=nemo-git
pkgver=20130407.25a76e4
pkgrel=1
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('libexif' 'gvfs' 'dconf' 'desktop-file-utils' 'exempi' 'python2'
         'gnome-desktop' 'gnome-icon-theme' 'libnotify' 'libtracker-sparql')
makedepends=('git' 'gtk-doc' 'gobject-introspection' 'intltool' 'gnome-common')
optdepends=('nemo-dropbox-git: Dropbox integration'
            'nemo-fileroller-git: File Roller integration'
            'gksu: Open as Root')
provides=('nemo' 'nemo-fm' 'nautilus')
conflicts=('nemo' 'nemo-fm')
options=('!emptydirs' '!libtool')
install=nemo.install
source=('git://github.com/linuxmint/nemo.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/nemo"
  git log -1 --format='%cd.%h' --date=short | tr -d - | sed 's/-/./'
}

build() {
  cd "$srcdir/nemo"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-gtk-doc-html \
      --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/nemo"

  make DESTDIR="$pkgdir/" install

  sed -i 's/python/python2/g' "$pkgdir/usr/share/nemo/actions/myaction.py"
}
