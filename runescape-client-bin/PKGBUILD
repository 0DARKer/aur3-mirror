# Maintainer: Ivan Puntiy <ivan.puntiyatgmail.com>
pkgname=runescape-client-bin
pkgver=2011.10.17
pkgrel=1
epoch=1
pkgdesc="Official RuneScape client"
arch=(any)
url="http://www.runescape.com"
license=('custom')
depends=('java-runtime')
makedepends=(p7zip)
source=(
    "${url}/downloads/runescape.msi"
    runescape
    runescape.desktop
)
md5sums=('087d7fbc49ba5d8fa2b6b9a00f8cbc04'
         'dca808ed546f609c6149718597a29715'
         '208ac9da73acad13690284c186dc9a9d')

build() {
  cd "$srcdir"
  mkdir -p "$pkgname-$pkgver"
  7z e -y -o"$pkgname-$pkgver" runescape.msi 'JagexAppletViewer*' 'LicenseFile*'
}

package() {
  _extra='F77C1A97_9A40_44D4_983D_751571B24CE4'

  cd "$srcdir/$pkgname-$pkgver"

  # the client
  install -Dm644 JagexAppletViewerJarFile.${_extra} \
    "$pkgdir"/usr/share/$pkgname/jagexappletviewer.jar
  install -Dm644 JagexAppletViewerPngFile \
    "$pkgdir"/usr/share/pixmaps/jagexappletviewer.png

  # invocation script
  install -Dm755 "$srcdir"/runescape "$pkgdir"/usr/bin/runescape

  # desktop entry
  install -Dm644 "$srcdir"/runescape.desktop \
    "$pkgdir"/usr/share/applications/runescape.desktop

  # license
  install -Dm644 LicenseFile.${_extra} \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
