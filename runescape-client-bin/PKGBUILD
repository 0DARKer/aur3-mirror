# Maintainer: Ivan Puntiy <ivan.puntiyatgmail.com>
pkgname=runescape-client-bin
pkgver=2010.11.26
pkgrel=1
epoch=1
pkgdesc="Official RuneScape client"
arch=(any)
url="http://www.runescape.com"
license=('custom')
depends=('java-runtime')
makedepends=(p7zip)
source=(
    "${url}/downloads/runescape.msi"
    runescape
    runescape.desktop
)
md5sums=('a6077cd2b4cc14ee65b5070d0c7432a1'
         'dca808ed546f609c6149718597a29715'
         '208ac9da73acad13690284c186dc9a9d')

build() {
  cd "$srcdir"
  mkdir -p "$pkgname-$pkgver"
  7z e -y -o"$pkgname-$pkgver" runescape.msi 'JagexAppletViewer*' 'LicenseFile*'
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # the client
  install -Dm644 JagexAppletViewerJarFile.144916C8_7582_46A8_9FD7_39A1D6CB1A19 \
    "$pkgdir"/usr/share/$pkgname/jagexappletviewer.jar
  install -Dm644 JagexAppletViewerPngFile \
    "$pkgdir"/usr/share/pixmaps/jagexappletviewer.png

  # invocation script
  install -Dm755 "$srcdir"/runescape "$pkgdir"/usr/bin/runescape

  # desktop entry
  install -Dm644 "$srcdir"/runescape.desktop \
    "$pkgdir"/usr/share/applications/runescape.desktop

  # license
  install -Dm644 LicenseFile.144916C8_7582_46A8_9FD7_39A1D6CB1A19 \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
