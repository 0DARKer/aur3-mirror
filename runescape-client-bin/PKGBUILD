# Maintainer: Ivan Puntiy <ivan.puntiyatgmail.com>
pkgname=runescape-client-bin
pkgver=20101126
_filever=$(echo $pkgver | sed 's/..\(..\)\(..\)\(..\)/\3\2\1/;s/^0//')
pkgrel=2
pkgdesc="Official RuneScape client"
arch=('i686' 'x86_64')
url="http://www.runescape.com"
license=('custom')
depends=('java-runtime')
makedepends=(p7zip)
source=(
    "runescape.msi::${url}/downloads/runescape.msi?${_filever}"
    runescape
    runescape.desktop
)
md5sums=(
    'a6077cd2b4cc14ee65b5070d0c7432a1'
    '6e28f49edce5779564c3cede56587ceb'
    'f5d1e032afaafaacb3afc01811acee99'
)

build() {
  cd "$srcdir"
  mkdir -p "$pkgname-$pkgver"
  7z e -o"$pkgname-$pkgver" runescape.msi 'JagexAppletViewer*' 'LicenseFile*'
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # the client
  install -Dm644 JagexAppletViewerJarFile.144916C8_7582_46A8_9FD7_39A1D6CB1A19 \
    "$pkgdir"/usr/share/java/jagexappletviewer.jar
  install -Dm644 JagexAppletViewerPngFile \
    "$pkgdir"/usr/share/pixmaps/jagexappletviewer.png

  # invocation script
  install -Dm755 "$srcdir"/runescape "$pkgdir"/usr/bin/runescape

  # desktop entry
  install -Dm644 "$srcdir"/runescape.desktop \
    "$pkgdir"/usr/share/applications/runescape.desktop

  # license
  install -Dm644 LicenseFile.144916C8_7582_46A8_9FD7_39A1D6CB1A19 \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
