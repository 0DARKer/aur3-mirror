# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=eliot
pkgver=2.0
pkgrel=1
pkgdesc="An open source Scrabble game that offers three different game modes (standard, duplicate and training)."
arch=(i686 x86_64)
url="http://www.nongnu.org/eliot/en/"
license=('GPL')
depends=('arabica' 'qt' 'libconfig')
makedepends=('boost')
install="$pkgname.install"
source=("http://dl.sv.nongnu.org/releases-noredirect/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('359a95206db8dc258701e8d90e2ed34f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix curses headers path
  sed -i "s|ncursesw[/_]||g" `grep -rl ncursesw .`

  # boost 1.50 fix
  sed -i "s/resize/this->&/" game/matrix.h

  ./configure --prefix=/usr \
              --enable-ncurses \
              --enable-qt
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
