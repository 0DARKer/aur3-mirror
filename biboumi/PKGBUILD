# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=biboumi
pkgver=1.0
pkgrel=1
pkgdesc="XMPP gateway to IRC"
arch=('i686' 'x86_64' 'armv7h')
url="http://biboumi.louiz.org/"
license=('ZLIB')
depends=('expat' 'libidn')
makedepends=('cmake')
source=("http://biboumi.louiz.org/${pkgname}-${pkgver}.tar.xz")
md5sums=('3175fc7492a48563a8216fd678260063')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir build
  cd build
  cmake -DCMAKE_BUILD_TYPE=release -DCMAKE_INSTALL_PREFIX=/usr ..
  make biboumi
}

check() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make test_suite/fast
  ./test_suite
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install

  cd "$srcdir/$pkgname-$pkgver"
  install -Dm644 doc/biboumi.1.md "$pkgdir/usr/share/doc/$_pkgname/$_pkgname.md"

  # TODO: generate that manpage.
  #install -Dm644 doc/biboumi.1 "$pkgdir/usr/share/man/man1/$_pkgname.1"
}
