# Contributor: quantax -- contact via Arch Linux forum or AUR
# Maintainer: Ner0

pkgname=ags-git
pkgver=20121204
pkgrel=1
pkgdesc="Linux port of the Adventure Game Studio"
arch=('i686' 'x86_64')
url="https://github.com/adventuregamestudio/ags"
license=('Artistic2.0')
depends=('allegro4' 'dumb' 'freetype2' 'libogg' 'libtheora' 'libvorbis')
makedepends=('git' 'wxgtk')
optdepends=('wine: for installing and configuring the game')
install=ags.install

_gitroot=https://github.com/adventuregamestudio/ags.git
_gitname=ags

build() {
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin && cd ..
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$_gitname-build"
  git clone "$_gitname" "$_gitname-build"
  cd "$_gitname-build"

  sed -i 's/-Wfatal-errors\ //' Engine/Makefile-defs.linux

  make -C Engine

  cd Manual/
  ./compile_documentation_unix.sh
  
}

package() {
  install -Dm755 "$_gitname-build/Engine/ags" "$pkgdir/usr/bin/ags"

  install -Dm644 "$_gitname/License.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/share/doc/$pkgname/html"
  install -m644 "$_gitname-build/Manual"/*.htm "$pkgdir/usr/share/doc/$pkgname/html/"
}

# vim:set ts=2 sw=2 et:
