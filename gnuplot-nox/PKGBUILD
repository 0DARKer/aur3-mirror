# Maintainer: jokot3 <jokot3 at gmail dot com>
# Contributor: enzzzy <enzzzy ta gmail dot com>

pkgname=gnuplot-nox
_pkgname=gnuplot
pkgver=4.6.4
pkgrel=1
pkgdesc="Plotting package which outputs PostScript, PNG, GIF, and others. Without X/cairo/wxgtk/lua/tex support." 
arch=('i686' 'x86_64') 
url="http://www.gnuplot.info" 
provides=('gnuplot')
conflicts=('gnuplot')
license=('custom') 
depends=('readline' 'gd' 'libjpeg')
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz" "gnuplot.install")
sha1sums=('54ee5ce9a0a2698b046064bd275e772673350013' 'ec362fe7ad56faaf8d995a395d3bc3465e7e2b25')
install='gnuplot.install'

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --disable-x11-mbfonts \
              --disable-x11-eternal \
              --disable-mouse \
              --disable-wxwidgets \
              --without-latex \
              --without-lua \
              --without-lisp-files \
              --without-x \
              --without-cairo \
              --infodir=/usr/share/info \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --datadir=/usr/share \
              --with-readline=gnu
  make pkglibexecdir=/usr/bin
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make pkglibexecdir=/usr/bin DESTDIR="$pkgdir" install
  # install-info is not in default target since 4.6.4
  make pkglibexecdir=/usr/bin DESTDIR="$pkgdir" "docs/Makefile" install-info

  install -Dm644 "docs/${_pkgname}.info" "$pkgdir/usr/share/info/${_pkgname}.info"

  install -Dm644 Copyright "$pkgdir/usr/share/licenses/$pkgname/Copyright"
  # see https://mailman.archlinux.org/pipermail/aur-general/2013-November/026151.html
  cd "$pkgdir/usr/share/licenses/" && ln -s "$pkgname" "$_pkgname"

}
