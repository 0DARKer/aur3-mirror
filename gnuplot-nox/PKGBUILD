# Maintainer: jokot3 <jokot3 at gmail dot com>
# Contributor: enzzzy <enzzzy ta gmail dot com>

pkgname=gnuplot-nox
_pkgname=gnuplot
pkgver=4.6.3
pkgrel=1
pkgdesc="Plotting package which outputs PostScript, PNG, GIF, and others. Without X/cairo/wxgtk/lua/tex support." 
arch=('i686' 'x86_64') 
url="http://www.gnuplot.info" 
provides=('gnuplot')
conflicts=('gnuplot')
license=('custom') 
depends=('readline' 'gd' 'libjpeg')
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz" "gnuplot.texi.patch" "gnuplot.install")
sha1sums=('f01e417dc9504a05fd5cc2595b05ccb58bcea5b2' 'e9c6f036ec9b963fc5a18c3c0a6fae7e254ffa02' 'ec362fe7ad56faaf8d995a395d3bc3465e7e2b25')
install='gnuplot.install'

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch docs/gnuplot.texi < "$srcdir/gnuplot.texi.patch"
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --disable-x11-mbfonts \
              --disable-x11-eternal \
              --disable-mouse \
              --disable-wxwidgets \
              --without-latex \
              --without-lua \
              --without-lisp-files \
              --without-x \
              --without-cairo \
              --infodir=/usr/share/info \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --datadir=/usr/share \
              --with-readline=gnu
  make pkglibexecdir=/usr/bin
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make pkglibexecdir=/usr/bin DESTDIR="$pkgdir" install

  install -Dm644 Copyright "$pkgdir/usr/share/licenses/$pkgname/Copyright"
  install -Dm644 docs/gnuplot.info "$pkgdir/usr/share/info/gnuplot.info"
}
