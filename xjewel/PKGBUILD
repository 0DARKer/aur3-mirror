# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>

pkgname=xjewel
pkgver=1.6
pkgrel=3
pkgdesc="A falling block game like Tetris and Sega Columns"
arch=('i686' 'x86_64')
url="http://www.happypenguin.org/show?XJewel"
license=('custom')
depends=('libx11')
install="${pkgname}.install"
source=("http://ftp.debian.org/debian/pool/main/x/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
        "${pkgname}.patch"
        "LICENSE")
md5sums=('fab72e5aadeb899e32dcae2ec4da9d62'
         'aa203668b0739d9e73a63530fc8e87e4'
         'ec94e3b7cc14edba9d2930bdf42ce4a2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}.orig"
  patch -Np1 -i "../${pkgname}.patch"
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}.orig"

  # Install game files
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -dm775 -g games "${pkgdir}/var/lib/${pkgname}"

  # Install man page, readme and license
  install -Dm644 "${pkgname}.man" "${pkgdir}/usr/share/man/man6/${pkgname}.6"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
