#Maintainer: Alexey Stukalov <astukalov@gmail.com>
#forked from smartsvn AUR package, maintainer Rene Peinthor
pkgname=smartgit
pkgver=3.0.4
pkgrel=2
pkgdesc="A Git GUI client written in Java"
arch=("any")
url="http://www.syntevo.com/smartgit/index.html"
license=('custom')
depends=("java-runtime" "desktop-file-utils" "sh" "git")
optdepends=("mercurial: hg repositiories support")
provides=(smartgit=$pkgver)
# package version as it appears in the name of tar.gz archive file
_pkgver=$(echo $pkgver | sed 's/\./_/g')
# folder within tar.gz archive
_pkgfolder="$pkgname-$_pkgver"
# tar.gz archive filename
_pkgfile="$pkgname-generic-$_pkgver.tar.gz"
source=(http://www.syntevo.com/download/${pkgname}/${_pkgfile}
        smartgit.desktop)
install="smartgit.install"
md5sums=('c880d84fd27dec73fc4ec0a5ba176f1e'
	'1d4bb1a70a3c6a6d04fe938beb28808b')

package() {
	cd "$srcdir"

	install -D -m644 "${_pkgfolder}"/license.html "${pkgdir}/usr/share/licenses/${pkgname}/license.html"
	mkdir -p "${pkgdir}"/opt
	mv "${_pkgfolder}" ${pkgdir}/opt/$pkgname || return 1

	install -D -m644 smartgit.desktop "${pkgdir}"/usr/share/applications/smartgit.desktop
}
