#Maintainer: Alexey Stukalov <astukalov@gmail.com>
#forked from smartsvn AUR package, maintainer Rene Peinthor
pkgname=smartgit
pkgver=3.0.4
pkgrel=1
pkgdesc="A Git GUI client written in Java"
arch=("any")
url="http://www.syntevo.com/smartgit/index.html"
license=('custom')
depends=("java-runtime" "desktop-file-utils" "sh" "git")
optdepends=("mercurial: hg repositiories support")
makedepends=("python>=3")
provides=(smartgit=$pkgver)
source=(smartgit.desktop downloader.py)
install="smartgit.install"
md5sums=('1d4bb1a70a3c6a6d04fe938beb28808b'
         '26e95a9df9a4fdf1321d3e531ec9a977')
# MD5 of smartgit tar.gz archive file
smartgitmd5sum='c880d84fd27dec73fc4ec0a5ba176f1e'


# package version as it appears in the name of tar.gz archive file
_pkgver=$(echo $pkgver | sed 's/\./_/g')
# folder within tar.gz archive
_pkgfolder="$pkgname-$_pkgver"
# tar.gz archive filename
_pkgfile="$pkgname-generic-$_pkgver.tar.gz"
# download url path
_pkgdownload_path="$pkgname/download.html"
# ?file=value
_pkgfile_param="$pkgname/$_pkgfile"

build() {
	cd "$srcdir"
	python3 downloader.py $_pkgdownload_path $_pkgfile_param

	echo "${smartgitmd5sum} *${_pkgfile}" | md5sum -c || (error "md5sum doesn't match" && return 1)

	tar xzf ${_pkgfile}
}

package() {
	cd "$srcdir"

	install -D -m644 "${_pkgfolder}"/license.html "${pkgdir}/usr/share/licenses/${pkgname}/license.html"
	mkdir -p "${pkgdir}"/opt
	mv "${_pkgfolder}" ${pkgdir}/opt/$pkgname || return 1

	install -D -m644 smartgit.desktop "${pkgdir}"/usr/share/applications/smartgit.desktop
}
