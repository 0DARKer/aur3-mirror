# Contributor: Rod Kay     <charlie5 on #ada at freenode.net>

pkgname=gnat_util
pkgver=4.9.0
pkgrel=1
pkgdesc="Provides internal GNAT compiler components in a library which is used by (at least) ASIS and GNATColl."

_islver=0.12.2
_cloogver=0.18.1
_snapshot=4.9-20150304
_basedir=gcc-${_snapshot}
options=('!emptydirs')

arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/gnatutil"
license=('GPL V3.0 without any Runtime Exception')

depends=('gcc-ada' 'xmlada' 'gprbuild')
makedepends=('binutils>=2.25' 'libmpc' 'gcc-ada')

source=(http://downloads.sourceforge.net/project/gnatutil/$pkgver/$pkgname-$pkgver-1.tar.gz
        ftp://gcc.gnu.org/pub/gcc/snapshots/${_snapshot}/gcc-${_snapshot}.tar.bz2
        http://isl.gforge.inria.fr/isl-${_islver}.tar.bz2
        http://www.bastoul.net/cloog/pages/download/cloog-${_cloogver}.tar.gz)

md5sums=('395f55e7f0e39bde4336be8dfc4eca44'
         '863bb9d2a9551c9b7447cfc8b7cc7498'
         'e039bfcfb6c2ab039b8ee69bf883e824'
         'e34fca0540d840e5d0f6427e98c92252')


prepare() {
  cd ${srcdir}/${_basedir}

  # link isl/cloog for in-tree builds
  ln -s ../isl-${_islver} isl
  ln -s ../cloog-${_cloogver} cloog

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  # Arch Linux installs x86_64 libraries /lib
  [[ $CARCH == "x86_64" ]] && sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64

  echo 4.9.2 > gcc/BASE-VER

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  mkdir ${srcdir}/gcc-build
}



build() 
{
  ## Build gcc-ada (note: much of this section is derived from the gcc-ada PKGBUILD).
  #

  cd ${srcdir}/gcc-build

  # using -pipe causes spurious test-suite failures
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=48565
  CFLAGS=${CFLAGS/-pipe/}
  CXXFLAGS=${CXXFLAGS/-pipe/}

  ${srcdir}/${_basedir}/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --with-bugurl=https://bugs.archlinux.org/ \
      --enable-languages=ada \
      --enable-shared --enable-threads=posix \
      --with-system-zlib --enable-__cxa_atexit \
      --disable-libunwind-exceptions --enable-clocale=gnu \
      --disable-libstdcxx-pch --disable-libssp \
      --enable-gnu-unique-object --enable-linker-build-id \
      --enable-cloog-backend=isl \
      --enable-lto --enable-plugin --enable-install-libiberty \
      --with-linker-hash-style=gnu \
      --disable-multilib --disable-werror \
      --enable-checking=release
  make -j7


  ## Build gnat util.
  #

  cd "$srcdir/$pkgname-$pkgver-1"

  export GCC_SRC_BASE="$srcdir/gcc-4.9-20150304"
  export GCC_BLD_BASE="$srcdir/gcc-build"

  make
}



package() 
{
  cd "$srcdir/$pkgname-$pkgver-1"
  make prefix="$pkgdir/usr"  install
}


