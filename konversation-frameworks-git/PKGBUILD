# Maintainer : Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=konversation-frameworks-git
pkgver=r7902.60ab015
pkgrel=1
pkgdesc="A user friendly IRC client for KF5 Frameworks. (GIT Version)"
arch=('x86_64' 'i686')
url="http://konversation.kde.org"
makedepends=('git' 'extra-cmake-modules' 'kdoctools' 'python')
depends=('knotifyconfig' 'kemoticons' 'kparts' 'kidletime' 'qca-qt5')
optdepends=('python: python scripting support')
conflicts=('konversation')
provides=('konversation')
license=('GPL2' 'FDL')
install=konversation-git.install
source=('git://anongit.kde.org/konversation#branch=frameworks')
sha1sums=('SKIP')
_gitroot=konversation

pkgver() {
  cd "${_gitroot}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_gitroot}"
  mkdir -p build
}

build() {
  cd "${_gitroot}"/build
  cmake .. \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DLIB_INSTALL_DIR=lib \
      -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${_gitroot}"/build
  make DESTDIR="${pkgdir}" install
}
