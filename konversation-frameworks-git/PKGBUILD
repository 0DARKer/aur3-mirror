# Maintainer : Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=konversation-frameworks-git
pkgver=1.5.197.gf52a0ef
pkgrel=1
pkgdesc="A user friendly IRC client for KF5 Frameworks. (GIT Version)"
arch=('x86_64' 'i686')
url="http://konversation.kde.org"
makedepends=('git' 'cmake' 'docbook-xml' 'optipng')
depends=('knotifyconfig' 'kdelibs4support' 'kidletime' 'qca-qt5-git')
optdepends=('python: python scripting support')
license=('GPL2' 'FDL')
install=konversation-git.install
source=('git+git://anongit.kde.org/konversation#branch=frameworks')
sha1sums=('SKIP')
_gitroot=konversation

pkgver() {
  cd "${_gitroot}"
  echo "$(git describe --long --tags | tr -d v | tr - .)"
}

prepare() {
  cd "${_gitroot}"
  find -name '*.png' -exec optipng -quiet -force -fix {} +
}

build() {
  cd "${_gitroot}"
  cmake . \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
      -DLIB_INSTALL_DIR=lib \
      -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins
  make
}

package() {
  cd "${_gitroot}"
  make DESTDIR="${pkgdir}" install
}