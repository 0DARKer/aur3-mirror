# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Patrik Ilg <p.ilg@arcor.de>

pkgname=gnujump
pkgver=1.0.7
pkgrel=1
pkgdesc="A clone of the simple yet addictive game Xjump"
arch=('i686' 'x86_64')
url="http://gnujump.es.gnu.org/index.php/Main_Page"
license=('GPL3')
depends=('libgl' 'sdl_image' 'sdl_mixer')
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.png"
        "${pkgname}.desktop")
md5sums=('149fb0ec9b356ac033127911a55ae549'
         'aff6af66e632095aa3154e952571f9b5'
         'd875e92130b669a4148908bc391a2a8c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  LDFLAGS=-lm ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # Install pixmap and .desktop file
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
