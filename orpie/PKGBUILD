# Maintainer: Kazuo Teramoto <kaz.rag gmail com>
# Contributor: scj <scj archlinux us>
# Contributor: tracer <jcdenton513 gmail com>
pkgname=orpie
pkgver=1.5.1
pkgrel=6
pkgdesc='Fullscreen RPN calculator for the console.'
arch=('i686' 'x86_64')
url='http://pessimization.com/software/orpie/'
license=('GPL')
depends=('ncurses' 'gsl')
makedepends=('ocaml' 'ocaml-gsl')
_patches=('orpie-1.5.1-nogsl.patch'
          'orpie-1.5.1-ocaml311.patch'
          'orpie-1.5.1-orpierc.patch')
source=("http://pessimization.com/software/orpie/${pkgname}-${pkgver}.tar.gz"
        "${_patches[@]}")
md5sums=('4511626ed853354af1b4b5dbbf143a1f'
         '7365ae021230f4de04575618bb372221'
         'c0779680d113dd0a9817cf36d4c92f80'
         'ab313270d7ac9e59ee7e031e50d8761b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # patches from gentoo
  for patchfile in "${_patches[@]}"; do
    patch -p1 -i "${srcdir}/${patchfile}"
  done
}
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --sysconfdir=/etc --prefix=/usr --mandir=/usr/man
  make
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:
