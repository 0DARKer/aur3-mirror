# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: T.Cunly

pkgname=jaxodraw
pkgver=2.1.0
_pkgver=2.1-0
pkgrel=1
pkgdesc="A Java program for drawing Feynman diagrams"
arch=('any')
url="http://jaxodraw.sourceforge.net/"
license=('GPL')
depends=('java-runtime')
source=("http://jaxodraw.sourceforge.net/download/pkgs/${pkgname}-${_pkgver}-bin.tar.gz"
        "${pkgname}.png"
        "${pkgname}.desktop"
        "${pkgname}.sh")
md5sums=('1bf230aa874cd5477be6c6f971f2ff0d'
         '9852872ae78a79c1082d93975563ce86'
         '7cb6588c39401e23ca292d315ed7e5b9'
         '3fb9673955efd6f70d6b504436a44c68')

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"

  # Install program files
  install -Dm644 *.jar "${pkgdir}/usr/share/${pkgname}/${pkgname}-${_pkgver}.jar"
  install -Dm755 ../${pkgname}.sh "${pkgdir}/usr/bin/${pkgname}"

  # Install a desktop entry
  install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r src/doc/* "${pkgdir}/usr/share/doc/${pkgname}"
}
