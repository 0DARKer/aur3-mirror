# Contributor: Piotr Gorski <sir_lucjan@birdgelinux.pl>
# Contributor: Kamil Kolosowski <sameal@openlinux.pl>

pkgname=nvidia-bfs
pkgver=331.49
_extramodules=extramodules-3.12-bfs
pkgrel=11
pkgdesc="NVIDIA drivers for linux-bfsl"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-bfs>=3.13' 'linux-bfs<3.14' "nvidia-libgl" "nvidia-utils=${pkgver}")
makedepends=('linux-bfs-headers>=3.13' 'linux-bfs-headers<3.14')
conflicts=('nvidia-bfs-96xx' 'nvidia-bfs-173xx')
provides=("nvidia=${pkgver}")
license=('custom')
install=nvidia.install
options=(!strip)
[ "$CARCH" = "i686" ] && _arch='x86' && _pkg="NVIDIA-Linux-${_arch}-${pkgver}" && md5sums=('176c77f9a10eaa652860dbc24042bff2')
[ "$CARCH" = "x86_64" ] && _arch='x86_64' && _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32" && md5sums=('d0c944466a36cae8b1e23330b27a721c')
#source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC="/usr/lib/modules/${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia_bfs.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
