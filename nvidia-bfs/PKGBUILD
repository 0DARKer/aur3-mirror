# Contributor: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: fsckd <fsckdaemon at gmail dot com>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-bfs
pkgver=260.19.44
_kernver='2.6.38-bfs'
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26-bfs."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-bfs>=2.6.38' 'kernel26-bfs<2.6.39' "nvidia-utils=${pkgver}")
makedepends=('kernel26-bfs-headers>=2.6.38' 'kernel26-bfs-headers<2.6.39')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-all' 'nvidia-beta-all')
provides=("nvidia=${pkgver}")
license=('custom')
install=nvidia.install

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('c99b0710a43db3aafb4f27408194e910')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('3dfce350616ad70d626b531a15c8604f')
fi

build() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC="/lib/modules/${_kernver}/build" module
}

package() {
    install -D -m644 "$srcdir/${_pkg}/kernel/nvidia.ko" \
        "$pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
    install -d -m755 "$pkgdir/etc/modprobe.d"
    echo "blacklist nouveau" >> "$pkgdir/etc/modprobe.d/nouveau_blacklist_bfs.conf"
    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "$startdir/nvidia.install"
    find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
