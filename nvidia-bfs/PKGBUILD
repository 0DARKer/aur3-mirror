# Contributor: JokerBoy <jokerboy@punctweb.ro>
# Contributor: fsckd <fsckdaemon at gmail dot com>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: anjanaya <anjanaya at gmail dot com>
# Maintainer : Luca Bennati <lucak3 at gmail dot com>

pkgname=nvidia-bfs
pkgver=260.19.36
_kernver='2.6.37-bfs'
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26-bfs."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-bfs>=2.6.37' 'kernel26-bfs<2.6.38' "nvidia-utils=${pkgver}")
makedepends=('kernel26-bfs>=2.6.37' 'kernel26-bfs<2.6.38')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('2826484d87827351774449382e45c4a9')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('6ebc5db9066e920b0b0e48377ce5eeb5')
fi

build() {
	cd "$srcdir"
	sh "${_pkg}.run" --extract-only
	cd "${_pkg}/kernel"
 	make SYSSRC="/lib/modules/${_kernver}/build" module
}

package() {
	install -D -m644 "$srcdir/${_pkg}/kernel/nvidia.ko" \
		"$pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
    install -d -m755 "$pkgdir/etc/modprobe.d"
    echo "blacklist nouveau" >> "$pkgdir/etc/modprobe.d/nouveau_blacklist_bfs.conf"
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "$startdir/nvidia.install"
}
