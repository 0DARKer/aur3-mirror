# Maintainer: Jakob Gahde <j5lx@fmail.co.uk>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=openbve
pkgver=1.4.3
pkgrel=1
pkgdesc="Free-as-in-freedom train simulator placed in the public domain"
arch=('any')
url="http://odakyufan.zxq.net/openbve/"
license=('custom:public domain')
depends=('mono' 'taoframework' 'sdl' 'openal')
makedepends=('p7zip')
source=(http://odakyufan.zxq.net/$pkgname/$pkgname.7z
        $pkgname.sh
        $pkgname.desktop)
noextract=($pkgname.7z) # makepkg isn't able to extract it
options=('!strip')
md5sums=('5d0a5d90ca298ff43a5e7b247614d7ab'
         '9d7f9a749acdb776dc258c565be482e9'
         'acb0dd3ea602910181b8cdb8375c794e')

prepare() {
  cd "$srcdir"

  if [ ! -d SourceCode ]; then
    7z x $pkgname.7z
  fi
}

build() {
  cd "$srcdir/SourceCode/openBVE"

  xbuild /p:Configuration=Release OpenBve/OpenBve.csproj
}

package() {
  cd "$srcdir"

  # Binaries
  install -d "$pkgdir/usr/lib/$pkgname/"

  install -Dm644 SourceCode/openBVE/OpenBve/bin/Release/OpenBve.exe "$pkgdir/usr/lib/$pkgname/"
  install -Dm644 SourceCode/openBVE/OpenBve/bin/Release/OpenBveApi.dll "$pkgdir/usr/lib/$pkgname/"

  # Data
  mkdir -p "$pkgdir/usr/share/games/$pkgname"
  cp -r Data "$pkgdir/usr/share/games/$pkgname/"
  ln -s ../../share/games/$pkgname/Data "$pkgdir/usr/lib/$pkgname/Data"

  # Executable
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  # Desktop file and icon
  install -Dm644 "$srcdir/$pkgname.desktop" $pkgdir/usr/share/applications/$pkgname.desktop
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s ../games/$pkgname/Data/Menu/logo.png "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # License
  install -Dm644 Credits.txt "$pkgdir/usr/share/licenses/$pkgname/Credits.txt"
}
