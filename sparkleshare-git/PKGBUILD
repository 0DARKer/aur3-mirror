# Maintainer: melchips <truphemus dot francois at gmail dot com>
# Contributor: atie H. <atie.at.matrix@gmail.com>
# Contributor: L42y <423300@gmail.com>
# Contributor: gesh <gesh@gesh.uni.cx>

pkgname=sparkleshare-git
_gitname=SparkleShare
pkgver=1.2.0.r107.g6400ccc
pkgrel=1
pkgdesc="An open-source clone of Dropbox, written in C#"
arch=('i686' 'x86_64')
url="http://www.sparkleshare.org/"
license=('LGPL')
depends=('mono' 'gtk-sharp-3' 'ndesk-dbus' 'ndesk-dbus-glib' 'intltool'
'webkit-sharp' 'notify-sharp' 'desktop-file-utils')
makedepends=('git')
options=('!libtool')
source=("$_gitname"::'git://github.com/hbons/SparkleShare.git')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    # Use the unannotated tag of the last commit
    git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$_gitname"

  # checkout commit that can be built without webkitgtk-sharp 3.0
  git checkout bca7ad5a87a9dadf26768d0a29f1aa795763302b

  msg2 "Starting make..."
  ./autogen.sh --prefix=/usr
  make || return 1
}

package() {
    cd ${srcdir}/${_gitname}
    make DESTDIR=${pkgdir} install || return 1

    install -m755 -d ${pkgdir}/usr/share/sparkleshare/html
    install -m755 -d ${pkgdir}/usr/share/sparkleshare/pixmaps
}
