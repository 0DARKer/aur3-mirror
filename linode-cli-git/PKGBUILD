# Maintainer: Graham Edgecombe <graham@grahamedgecombe.com>
pkgname=linode-cli-git
pkgver=24.a9bf494
pkgrel=1
pkgdesc='A simple command-line interface to the Linode platform'
arch=('any')
url='https://github.com/linode/cli'
license=('GPL2')
depends=('perl-try-tiny' 'perl-libwww' 'perl-json' 'perl-crypt-ssleay')
makedepends=('git')
source=("$pkgname"::'git+https://github.com/linode/cli.git')
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$pkgname"
  install -D linode "$pkgdir/usr/bin/linode"
  install -D linode-account "$pkgdir/usr/bin/linode-account"
  install -D linode-linode "$pkgdir/usr/bin/linode-linode"
  install -D linode-stackscript "$pkgdir/usr/bin/linode-stackscript"
  for file in $(find lib -type f -name "*.pm"); do
    install -Dm644 "$file" "$pkgdir/usr/lib/perl5/vendor_perl/${file/lib\//}"
  done
}
