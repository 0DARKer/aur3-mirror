# Maintainer: Graham Edgecombe <graham@grahamedgecombe.com>
pkgname=linode-cli-git
pkgver=38.30a96d3
pkgrel=1
pkgdesc='A simple command-line interface to the Linode platform'
arch=('any')
url='https://github.com/linode/cli'
license=('GPL2')
depends=('perl-try-tiny' 'perl-libwww' 'perl-json' 'perl-crypt-ssleay')
makedepends=('git')
source=("$pkgname"::'git+https://github.com/linode/cli.git')
md5sums=('SKIP')
_executables='linode linode-account linode-linode linode-stackscript'

pkgver() {
  cd "$pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$pkgname"
  for file in $_executables; do
    in_file=$file
    if [ "$in_file" = "linode" ]; then
      in_file=linode-linode
    fi
    pod2man $in_file | gzip > ${file}.1.gz
  done
}

package() {
  cd "$pkgname"
  for file in $_executables; do
    install -D $file "$pkgdir/usr/bin/$file"
    install -D ${file}.1.gz "$pkgdir/usr/share/man/man1/${file}.1.gz"
  done
  for file in $(find lib -type f -name "*.pm"); do
    install -Dm644 "$file" "$pkgdir/usr/lib/linode-cli/${file/lib\//}"
  done
}
