# Maintainer: Alucryd <alucryd at gmail dot com>
# Contributor: Padfoot <padfoot at exemail dot com dot au>

pkgname=lightdm-gtk3-greeter-devel
pkgver=1.5.1
pkgrel=1
pkgdesc="GTK+ greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3' 'LGPL3')
depends=('gtk3' "lightdm>=1.5.1")
makedepends=('exo' 'gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("lightdm-gtk-greeter=${pkgver}")
conflicts=('lightdm-gtk-greeter')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/lightdm-gtk-greeter/1.6/${pkgver}/+download/lightdm-gtk-greeter-${pkgver}.tar.gz"
        "onscreen-keyboard.patch")
sha256sums=('ecce7e917a79fa8f2126c3fafb6337f81f2198892159a4ef695016afecd2d621'
            '381c4da15ed2482800aaf3258217cd48c2313aee5da4f7ade77f7ef55659d0b9')

build() {
  cd "${srcdir}"/lightdm-gtk-greeter-${pkgver}

# Patch
  patch -Np1 -i ../onscreen-keyboard.patch

# Build
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/lightdm --disable-static
  make
}

package() {
  cd "${srcdir}"/lightdm-gtk-greeter-${pkgver}

# Install
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
