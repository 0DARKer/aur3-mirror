# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Martin Devera <devik@cdi.cz>

pkgname=mnogosearch
pkgver=3.3.13
pkgrel=1
pkgdesc="Search Engine Software"
arch=(i686 x86_64)
url=http://www.mnogosearch.org
license=(GPL2)
depends=(libmysqlclient)
makedepends=(mysql)
backup=(etc/$pkgname/{{indexer,langmap,stopwords}.conf,search.htm})
options=(!emptydirs !libtool)
source=($url/Download/$pkgname-$pkgver.tar.gz)
sha256sums=('49be79efcbd1de299e7c7e9834646232d330c2bef496f0020c7cf40cfd6b6cee')
sha512sums=('83750b0b72a6991829f25ec70c4fcb08aa25a4c074463c978b97990fe24752925e9bb4f2598029d2f97785a78507d0a04eeb536472b2b77e43c11ce8e4639802')

build() {
    cd "$srcdir"/$pkgname-$pkgver/
    ./configure --prefix=/usr \
        --sysconfdir=/etc/$pkgname \
        --datadir=/usr/share/$pkgname \
        --localstatedir=/var \
        --with-mysql
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    make DESTDIR="$pkgdir" install

    cd "$pkgdir"
    mv usr/doc/ usr/share/doc/
    for f in ${backup[@]};do
        install -Dm644 $f-dist $f
    done
}
