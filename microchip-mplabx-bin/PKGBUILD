# Maintainer: BxS <bxsbxs at gmail dot com>

pkgname=microchip-mplabx-bin
pkgver=1.10
pkgrel=2
pkgdesc="IDE for Microchip PIC and dsPIC development"
arch=(i686 x86_64)
url=http://www.microchip.com/mplabx
license=(custom)
depends=(java-runtime=6 libusb desktop-file-utils)
makedepends=(unzip patchelf)
[ $CARCH = x86_64 ] && depends=(${depends[@]} lib32-glibc)
optdepends=('microchip-mplabc18_bin: C compiler for PIC18 MCUs'
            'microchip-mplabc30_bin: C compiler for PIC24 MCUs and dsPIC DSCs'
            'microchip-mplabc32_bin: C Compiler for PIC32 MCUs'
            'hitech-picc_bin: C compiler for PIC10/12/16 MCUs'
            'hitech-picc-18_bin: C compiler for PIC18 MCUs'
            'hitech-dspicc-bin: C Compiler for PIC24 MCUs and dsPIC DSCs'
            'hitech-picc32-bin: C compiler for PIC32 MCUs'
            'sdcc: C compiler for PIC16/18 MCUs')
provides=(mplab)
conflicts=(mplab)
options=(!strip docs libtool emptydirs !zipman)
install=$pkgname.install
user=$USER
instdir=/opt/microchip/mplabx
installer=mplabx-ide-v$pkgver-linux-installer.run
releasenotes=mplabx-ide-v$pkgver-release-notes.01.zip
source=(http://ww1.microchip.com/downloads/mplab/X/$installer
        http://ww1.microchip.com/downloads/mplab/X/$releasenotes
        http://schlunix.org/archlinux/core/os/i686/glibc-2.14.1-4-i686.pkg.tar.xz
        z010_mchp_tools.rules
        microchip-mplabx.desktop
        microchip-mplabx.png
        LICENSE)
noextract=($releasenotes)
md5sums=(e6515697068f3f1b2db2e19d7a07d875
         3c4548c2c4838efa8338e1a4beb85243
         771053502502cf88d97a1380b47191c7
         86a3981c6401b1569d622d86b96a9dec
         6f2a8170965d4963c468f4766e15fff7
         1887fb51576009e5f96f6e7ac0b9bad4
         537587a0ad5c7381499f0da4cb6eda7e)

package() {
  echo -e "Creating Package\n  Please Wait..."

  cd $pkgdir

  rm -r * &> /dev/null || true

  mkdir -p $pkgdir$instdir

  echo -e "#! /bin/sh\necho \"java version \\\"1.6\"" > java
  chmod 0755 java
  PATH=$pkgdir:$PATH
  chmod 0755 $srcdir/$installer
  echo -e "\n\n\n\n\n\n\n\n\n\n\n\n\n\n\ny\n\ny\ni\ni\ni\ni\ni\ni\ni\ni\ni\ni\ni\ni\ni\ni\ni\ni\n\n" > inst_input
  $srcdir/$installer --mode text --installdir $pkgdir$instdir < inst_input &> /dev/null || true
  rm inst_input java

  rm "$pkgdir$instdir/Uninstall MPLAB X IDE"{," v$pkgver.desktop",".dat"} &> /dev/null || true
  rm -r $pkgdir$instdir/rollbackBackupDirectory &> /dev/null || true

  sed 's/\x2F\x75\x73\x72\x2F\x6C\x6F\x63\x61\x6C\x2F\x6C\x69\x62\x2F\x6C\x69\x62\x6D\x63\x68\x70\x75\x73\x62\x2D\x31\x2E\x30\x2E\x73\x6F\x00/\x6C\x69\x62\x75\x73\x62\x2D\x31\x2E\x30\x2E\x73\x6F\x2E\x30\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00\x00/' -i $pkgdir/opt/microchip/mplabx/mplab_ide/mplablibs/modules/lib/libUSBAccessLink.so
  rm $pkgdir$instdir/mplab_ide/mplablibs/modules/lib/libusb-1.0*

  sed -i 's/\/usr\/hitech/\/opt\/hitech/g' $pkgdir/opt/microchip/mplabx/mplab_ide/mplab_ide/modules/com-microchip-mplab-nbide-toolchainhitech.jar

  unzip -o $srcdir/$releasenotes -d $pkgdir$instdir/mplab_ide/mplab_ide/modules/docs &> /dev/null

  mkdir -p $pkgdir$instdir/asm30/glibc2.14.1
  mv $srcdir/lib/* $pkgdir$instdir/asm30/glibc2.14.1
  find $pkgdir$instdir/asm30/bin -type f -executable -exec patchelf --set-rpath $instdir/asm30/glibc2.14.1 '{}' \;
  ln -s /usr/share/licenses/common/LGPL/ $pkgdir$instdir/asm30/glibc2.14.1/
  ln -s /usr/share/licenses/common/GPL/ $pkgdir$instdir/asm30/glibc2.14.1/

  chmod -R a+r $pkgdir$instdir
  find $pkgdir$instdir -type d -exec chmod a+rx '{}' \;

  install -Dm 644 $srcdir/z010_mchp_tools.rules $pkgdir/etc/udev/rules.d/z010_mchp_tools.rules
  install -Dm 644 $srcdir/microchip-mplabx.desktop $pkgdir/usr/share/applications/microchip-mplabx.desktop
  install -Dm 644 $srcdir/microchip-mplabx.png $pkgdir/usr/share/pixmaps/microchip-mplabx.png

  install -Dm 644 $srcdir/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  mkdir -p $pkgdir/usr/bin
  ln -s $instdir/mplab_ide/bin/mplab_ide $pkgdir/usr/bin/
}
