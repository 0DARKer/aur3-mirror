# Maintainer: joat <joat at lavabit dot com>
# Submitter: BxS <bxsbxs at gmail dot com>

pkgname=microchip-mplabx-bin
pkgver=1.51
pkgrel=1
pkgdesc="IDE for Microchip PIC and dsPIC development"
arch=(i686 x86_64)
url=http://www.microchip.com/mplabx
license=(custom)
depends=(desktop-file-utils)
makedepends=(fakechroot)
[ $CARCH = x86_64 ] && depends+=(lib32-glibc)
optdepends=('microchip-mplabxc8-bin: C compiler for PIC10 PIC12 PIC16 PIC18 MCUs'
            'microchip-mplabxc16-bin: C compiler for PIC24 MCUs and dsPIC DSCs'
            'microchip-mplabxc32-bin: C Compiler for PIC32 MCUs'
            'microchip-mplabc18_bin: C compiler for PIC18 MCUs'
            'sdcc: C compiler for PIC16/18 MCUs')
provides=(mplab)
conflicts=(mplab)
options=(!strip docs libtool emptydirs !zipman)
install=$pkgname.install
instdir=/opt/microchip/mplabx
installer=mplabx-ide-v1_51-linux-installer.run
source=(http://ww1.microchip.com/downloads/en/DeviceDoc/$installer
        LICENSE)
[ $CARCH = x86_64 ] && source+=(fakechroot-i686.pkg.tar.xz::http://www.archlinux.org/packages/extra/i686/fakechroot/download/
                                fakeroot-i686.pkg.tar.xz::http://www.archlinux.org/packages/core/i686/fakeroot/download/)
md5sums=(10cb9b7d24f3294b60809c1c1019bc5c
         a34a85b2600a26f1c558bcd14c2444bd)
[ $CARCH = x86_64 ] && md5sums+=(0a300f12b73d5bc18548f5b50682236f
                                 440a72910d23b25b5a57b7fe251e12e9)

build() {
  cd $srcdir

  mkdir -p $pkgdir/{bin,etc,usr/{bin,lib,local/lib},tmp}

  ln -s /bin/bash $pkgdir/bin/
  ln -s /bin/ln $pkgdir/bin/

  echo "root:x:0:0:root:/root:/bin/bash" > $pkgdir/etc/passwd
  echo "root:x:0:root" > $pkgdir/etc/group

  cp $srcdir/$installer $pkgdir/
  chmod 0755 $pkgdir/$installer

  echo -e "\n\n\n\n\n\n\n\n\n\ny\n\ny\n\nn" > $pkgdir/inst_input

  echo "#!/bin/bash
  LD_LIBRARY_PATH=$srcdir/usr/lib/libfakeroot/fakechroot:$srcdir/usr/lib/libfakeroot:\$LD_LIBRARY_PATH
  ./$installer --mode text < inst_input &> /dev/null || true"> $pkgdir/chroot_input.sh
  chmod 0755 $pkgdir/chroot_input.sh

  echo -e "Creating the Package\n  Please wait..."

  fakechroot fakeroot chroot $pkgdir ./chroot_input.sh

  cd $pkgdir$instdir/sys/java
  if test "$CARCH" == x86_64; then
    ./jre-6u33-linux-x64.bin &> /dev/null
    rm $pkgdir$instdir/sys/java/jre-6u33-linux-x64.bin
  else
    ./jre-6u33-linux-i586.bin &> /dev/null
    rm $pkgdir$instdir/sys/java/jre-6u33-linux-i586.bin
  fi
  sed -i 's|#jdkhome="/path/to/jdk"|jdkhome=/opt/microchip/mplabx/sys/java/jre1.6.0_33/|g' $pkgdir$instdir/mplab_ide/etc/mplab_ide.conf
  sed -i 's|#jdkhome="/path/to/jdk"|jdkhome=/opt/microchip/mplabx/sys/java/jre1.6.0_33/|g' $pkgdir$instdir/mplab_ide/bin/mplab_ipe

  rm $pkgdir/{chroot_input.sh,inst_input,$installer,etc/{group,passwd}}
  rm -r $pkgdir/{bin,tmp}

  rm $pkgdir/usr/{bin/{mplab_ide,mplab_ipe},local/lib/libmchpusb-1.0.so}
  ln -s $instdir/mplab_ide/bin/mplab_ide $pkgdir/usr/bin/mplab_ide
  ln -s $instdir/mplab_ide/bin/mplab_ipe $pkgdir/usr/bin/mplab_ipe
  ln -s $instdir/mplab_ide/mplablibs/modules/lib/libusb-1.0.so.0.0.0 $pkgdir/usr/local/lib/libmchpusb-1.0.so

  rm $pkgdir$instdir/Uninstall*

  install -Dm 644 $srcdir/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package() {
  true
}
