# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Berkus <berkus@madfire.net>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>
# Contributor: Gimmeapill <gimmeapill at gmail dot com>

pkgname=neil-hg
pkgver=1971
pkgrel=1
pkgdesc="A modular Buzz-like tracker, fork of aldrin"
arch=('i686' 'x86_64')
license=('GPL')
url="http://sites.google.com/site/neilsequencer/"
depends=('pygtk' 'python2-numpy' 'python-opengl' 'wxpython'
         'libmad' 'gtkglarea' 'gtkglext' 'fftw' 'mpg123' 'jack'
         'ladspa' 'dssi' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('mercurial' 'scons' 'boost' 'docbook-xsl' 'xmlto')
provides=('neil' 'libneil' 'libneil-hg')
conflicts=('neil' 'libneil' 'libneil-hg')
install="$pkgname.install"

_hgroot="http://bitbucket.org/bucket_brigade"
_hgrepo=neil

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [[ -d "$_hgrepo" ]]; then
    cd "$_hgrepo"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "$_hgroot" "$_hgrepo"
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  #
  # BUILD HERE
  #

  # python2 build fix
  sed -i "s|'python |'python2 |g" libneil/SConscript

  scons configure PREFIX=/usr
  scons
}

package() {
  cd "$srcdir/$_hgrepo-build"
  scons install DESTDIR="$pkgdir/"

  # python2 fix
  sed -i "s|env python|&2|" `grep -rl "env python" "$pkgdir"`

  # config file path fix
  sed -i "s|$pkgdir||" "$pkgdir/etc/neil/path.cfg"
}

