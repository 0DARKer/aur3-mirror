# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Andreas Sch√∂nfelder <passtschu at freenet dot de>

pkgname=wingide-101
pkgver=5.0.0
pkgrel=1
_pkgarch=`uname -m`
pkgdesc="Wing IDE 101 is free scaled down Python IDE designed for teaching introductory programming classes."
url="http://www.wingware.com"
license=("custom")
arch=('i686' 'x86_64')
source=("http://wingware.com/pub/$pkgname/$pkgver/$pkgname-$pkgver-$pkgrel-x86_64-linux.tar.gz"
        "$pkgname.desktop")
options=(!strip)
sha1sums=('c3cd1a75941e9a58c247cae58c677d1d662769d7'
          '5f4904cc84debe052dd5b0d40e156261f7bd1cf6')

if [ "$CARCH" == "i686" ]; then
    _pkgarch="i386"
    sha1sums[0]='9af3217dbd9939d52e6d04cf2b3e48b9f1b4abd7'
    source[0]="http://wingware.com/pub/$pkgname/$pkgver/$pkgname-$pkgver-$pkgrel-i386-linux.tar.gz"
fi

package() {
    cd $srcdir/$pkgname-$pkgver-$pkgrel-$_pkgarch-linux
    install -d $pkgdir/opt/$pkgname $pkgdir/usr/bin $pkgdir/usr/share/applications $pkgdir/usr/share/pixmaps
    tar -xC $pkgdir/opt/$pkgname -f binary-package-$pkgver-$pkgrel.tar
    sed -e "s/^DIR=.*$/DIR=\/opt\/$pkgname/g" -i $pkgdir/opt/$pkgname/wing-101
    install -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/
    ln -s /opt/$pkgname/wing-101 $pkgdir/usr/bin/$pkgname
    ln -s /opt/$pkgname/resources/wing128.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
