# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Andreas Sch√∂nfelder <passtschu at freenet dot de>

pkgname=wingide-101
pkgver=4.1.10
pkgrel=1
_pkgarch=`uname -m`
pkgdesc="Wing IDE 101 is free scaled down Python IDE designed for teaching introductory programming classes."
url="http://www.wingware.com"
license=("custom")
arch=('i686' 'x86_64')
source=("http://wingware.com/pub/$pkgname/$pkgver/$pkgname-$pkgver-$pkgrel-x86_64-linux.tar.gz"
        "$pkgname.desktop")
options=(!strip)
md5sums=('fb7f6bfd692887c4616ab64368ed6f24'
         '8a36bf9de947ee68fa8ba148d50eee4f')

if [ "$CARCH" == "i686" ]; then
    _pkgarch="i386"
    md5sums[0]='dbf30ce7f8397451baefc3ac07a49a2f'
    source[0]="http://wingware.com/pub/$pkgname/$pkgver/$pkgname-$pkgver-$pkgrel-i386-linux.tar.gz"
fi

package() {
    cd $srcdir/$pkgname-$pkgver-$pkgrel-$_pkgarch-linux
    install -d $pkgdir/opt/$pkgname $pkgdir/usr/bin $pkgdir/usr/share/applications $pkgdir/usr/share/pixmaps
    tar -xC $pkgdir/opt/$pkgname -f binary-package-$pkgver-$pkgrel.tar
    sed -e "s/^DIR=.*$/DIR=\/opt\/$pkgname/g" -i $pkgdir/opt/$pkgname/wing-101
    install -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/
    ln -s /opt/$pkgname/wing-101 $pkgdir/usr/bin/$pkgname
    ln -s /opt/$pkgname/resources/wing128.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
