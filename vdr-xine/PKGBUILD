# Contributor: Franz Burgmann <f dot burgmann at gmail dot com>
# Maintainer: David Spicer <azleifel at googlemail dot com>

pkgname=vdr-xine
_pluginname=xine
pkgver=0.9.3
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin that allows VDR to be used with budget cards"
url="http://sourceforge.net/projects/xine/"
license=('GPL2')
depends=('vdr' 'xine-lib-vdr')
makedepends=('pkgconfig')
install=${pkgname}.install
source=(http://home.vrweb.de/rnissl/${pkgname}-${pkgver}.tgz
        vdr-xine.desktop)
md5sums=('79b18570e3354f47319f77c5f01366ea'
         '20080b5e0aa3e848d56d363e4a7fdf95')

build() {
  cd ${srcdir}/${_pluginname}-${pkgver}

  # Assumed VDR directory environment (VDRDIR/Make.config):
  # MANDIR       = /usr/share/man
  # BINDIR       = /usr/bin
  # LOCDIR       = /usr/share/locale
  # PLUGINLIBDIR = /usr/lib/vdr
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr

  install -d -m755 ${pkgdir}/etc/vdr/plugins/${_pluginname} || return 1
  install -d -m755 ${pkgdir}/usr/lib/vdr || return 1
  install -d -m755 ${pkgdir}/usr/share/doc/${pkgname} || return 1
  install -d -m755 ${pkgdir}/usr/share/locale || return 1
  
  make VDRDIR=/usr/include/vdr \
       LIBDIR=${pkgdir}/usr/lib/vdr \
       LOCALEDIR=${pkgdir}/usr/share/locale all || return 1
  
  cp -Rp data/* ${pkgdir}/etc/vdr/plugins/xine || return 1
  
  # Install documents
  for _file in README HISTORY MANUAL; do
    install -m644 ${_file} ${pkgdir}/usr/share/doc/${pkgname}/${_file##*/} || return 1
  done

  # Install homebrew desktop file
  install -D -m644 ${startdir}/vdr-xine.desktop ${pkgdir}/usr/share/applications/vdr-xine.desktop || return 1
}
