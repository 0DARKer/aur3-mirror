# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=gerris
pkgname=gerris-snapshot
pkgver=110330
pkgrel=1
pkgdesc="GNU Flow Solver (GFS) aims to be a generic tool for the numerical simulation of fluid flows."
arch=(i686 x86_64)
url="http://gfs.sourceforge.net/wiki/index.php/Main_Page"
license=('GPL')
groups=("$_pkg")
depends=('python2' 'gts-snapshot' 'netcdf' 'gsl' 'ode' 'openmpi' 'proj' 'hicolor-icon-theme' 'shared-mime-info')
provides=("$_pkg")
conflicts=("$_pkg")
options=('!libtool')
install="$pkgname.install"
source=("http://gerris.dalembert.upmc.fr/$_pkg/$pkgname.tar.gz")
md5sums=('569bc33dded090b5ce249177a9ead017')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  sed -i "s|\(bin/python\).*|\12|" doc/examples/gfs2doc.in

  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
