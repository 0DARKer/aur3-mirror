# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Magnus Therning <magnus@therning.org>

pkgname=ocaml-odn
pkgver=0.0.7
pkgrel=3
pkgdesc="OCaml data notation"
arch=('i686' 'x86_64')
url="https://forge.ocamlcore.org/projects/odn"
# LGPL + linking exception
license=('LGPL')
depends=('ocaml>=3.12' 'ocaml-type-conv>=3.0.4' 'ocaml-fileutils')
makedepends=('ocaml-findlib' 'ocaml-ounit')
source=("http://forge.ocamlcore.org/frs/download.php/739/ocaml-data-notation-$pkgver.tar.gz")
md5sums=('3730d44c5dea17688afa0d034cc3808b')
options=(!strip)

build() {
  cd "$srcdir/ocaml-data-notation-$pkgver"
  make CONFIGUREFLAGS="--prefix /usr --destdir $pkgdir" build
}

package() {
  cd "$srcdir/ocaml-data-notation-$pkgver"
  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR"
  make install
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
