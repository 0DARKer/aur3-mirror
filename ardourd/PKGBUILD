# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=ardourd
pkgver=0.1.7
pkgrel=2
pkgdesc="A proof-of-concept libardour-based record/playback, song, and mixer engine daemon as part of a modular audio production system."
arch=(i686 x86_64)
url="http://ayyi.org/"
license=('GPL')
depends=('dbus-glib' 'gtkmm' 'taglib' 'slv2'
         'liblo' 'liblrdf' 'aubio' 'libsmf')
source=("http://ayyi.org/files/$pkgname-$pkgver.tar.bz2")
md5sums=('a607503412df125c298dddb018272eed')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  export PYTHON="python2"

  # Architecture portability fix
  sed -i "s|unsigned)sp|uintptr_t)sp|;s|0400|0600|" libs/ayyi/shm_seg.cc

  # adding missing header in rubberband source
  sed -i "22a#include <cstddef>" libs/rubberband/rubberband/RubberBandStretcher.h

  # fix ARDOUR namespace
  sed -i "s|.*\(namespace ARDOUR\).*|\1 {|;\$a}" libs/ardour/enums.cc

  python2 waf configure --prefix=/usr \
                        --aubio \
                        --freedesktop \
                        --lv2 \
                        --nls \
                        --versioned \
                        --dbus \
                        --build-docs
#                        --freesound \
  python2 waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"
}
