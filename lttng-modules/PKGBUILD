# Maintainer: Markus Opitz <mastero23 at gmail dot com>

pkgname=lttng-modules
pkgver=0.19.9
pkgrel=1
pkgdesc="Kernel Modules for LTTng"
arch=('i686' 'x86_64')
url="http://lttng.org/"
license=('GPL')
depends=('kernel26-lttng>=2.6.38')
install=$pkgname.install
source=(http://lttng.org/files/lttng/$pkgname-$pkgver.tar.bz2)
md5sums=('dcd4082c90362b80efb14861eee8bd50')

_kerneldir=/lib/modules/2.6.38-lttng/build

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '/depmod -a/d' Makefile
  make KERNELDIR="$_kerneldir"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make KERNELDIR="$_kerneldir" \
       INSTALL_MOD_PATH="$pkgdir/" \
       modules_install
}
