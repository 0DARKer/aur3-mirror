# Maintainer: Sam S. <smls75@gmail.com>

pkgname=spaz-hib
pkgver=2012_09_18
_hibver=09182012
pkgrel=0
pkgdesc='Space Pirates and Zombies, a top-down space combat game (Humble Bundle version)'
url='http://spacepiratesandzombies.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
if [ $CARCH == i686 ]; then
  depends=('gcc-libs' 'libgl' 'sdl' 'openal' 'alsa-lib')
  optdepends=('alsa-plugins: PulseAudio support'
              'libpulse: PulseAudio support')
else
  depends=('lib32-gcc-libs' 'lib32-libgl' 'lib32-sdl' 'lib32-openal' 'lib32-alsa-lib')
  optdepends=('lib32-alsa-plugins: PulseAudio support'
              'lib32-libpulse: PulseAudio support')
fi
source=('spaz-hib.desktop')
md5sums=('544fa2a99bc3d4733909e18f2df6515d')
PKGEXT='.pkg.tar'

_archive="spaz-linux-humblebundle-${_hibver}-bin"
_archive_md5='9b2f28009949f2dff9f3a737e46fabfd'

package() {
  cd $srcdir

  # Get installer
  _get_local_source "${_archive}" --md5 "${_archive_md5}" || {
    error "Unable to find the game archive. Please download it from your Humble
           Bundle page, and place it into one of the above locations."
    exit 1; }

  # Extract the installer
  unzip -o "${_archive}" || true

  # Remove bundled libraries (to force usage of system libs instead)
  rm -rf $srcdir/data/libSDL-1.2.so.0
  rm -rf $srcdir/data/libopenal.so

  # Install game files
  install -d "${pkgdir}/opt/SPAZ"
  cp -r "$srcdir/data/"* "${pkgdir}/opt/SPAZ/"

  # Install desktop entry
  install -Dm644 "$pkgname.desktop" \
                 "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Install icon
  install -Dm644 "data/SPAZ.png" "$pkgdir/usr/share/pixmaps/spaz.png"

  # Create launcher script
  install -d "$pkgdir/usr/bin"
  echo -e "#!/bin/sh\ncd /opt/SPAZ && ./SPAZ" > "$pkgdir/usr/bin/spaz"
  chmod 755 "$pkgdir/usr/bin/spaz"
}


###### HELPER FUNCTIONS ######

# Locates a file or folder provided by the user, and symlinks it into $srcdir
_get_local_source() {
  msg "Looking for '$1'..."; rm -f "$srcdir/$1"
  declare -A _search=(['build dir']="$startdir"
                      ['$LOCAL_PACKAGE_SOURCES']="$LOCAL_PACKAGE_SOURCES")
  for _key in "${!_search[@]}"; do local _dir="${_search["$_key"]}"
    echo -n "    - in $_key [${_dir:-<undefined>}] ... ";
    if [[ -z "$_dir" || ! -e "$_dir/$1" ]]; then
      echo "NOT FOUND"
    elif [[ -n $2 && "$(${2#--}sum "$_dir/$1"|awk '{print $1}')" != $3 ]]; then
      echo "CHECKSUM FAILED";
    else
      echo "FOUND"; ln -sfT "$(readlink -f "$_dir/$1")" "$srcdir/$1"; break; fi
  done
  if [ ! -e "$srcdir/$1" ]; then return 1; fi
}
