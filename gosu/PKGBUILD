# Maintainer: Justin Wilcox <nat1192@gmail.com>
# Contributor: Henning Bekel <h dot bekel at googlemail dot com>
# Contributor: Paolo Giangrandi <peoro.noob@gmail.com>

pkgname=gosu
pkgver=0.7.41
pkgrel=1
pkgdesc="2D game development library for the C++ programming language."
arch=('i686' 'x86_64')
url="http://libgosu.org/"
license="MIT"
depends=('sdl_ttf' 'pango' 'libgl' 'libxinerama' 'openal' 'libvorbis' 'libsndfile' 'freeimage')
source=("http://www.libgosu.org/downloads/gosu-linux-$pkgver.tar.gz")
#See comment below
noextract=("gosu-linux-$pkgver.tar.gz")
md5sums=('18779d0fa339dc505ddf9e3ff1b2cb24')

build() {
  #As of 0.7.41 (at least), the upstream tarballs can't be extracted by 
  # bsdtar. Use tar instead:
  cd "${srcdir}"
  tar -xzf "gosu-linux-${pkgver}.tar.gz"

  cd "${pkgname}/linux"
  ./configure --prefix=/usr
  make
}

package () {
  install -D -m644 $srcdir/$pkgname/lib/libgosu.a $pkgdir/usr/lib/libgosu.a
  install -D -m755 $srcdir/$pkgname/lib/libgosu.so $pkgdir/usr/lib/libgosu.so
  install -D -m755 $srcdir/$pkgname/bin/gosu-config $pkgdir/usr/bin/gosu-config

  install -d $pkgdir/usr/include/Gosu
  install -D -m644 $srcdir/$pkgname/Gosu/*.hpp $pkgdir/usr/include/Gosu

  install -D -m 644 $srcdir/$pkgname/COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
