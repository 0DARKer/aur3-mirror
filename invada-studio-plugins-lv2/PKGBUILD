# Maintainer : rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>, SpepS <dreamspepser at yahoo dot it>
# 
# invada-studio-fixes.patch taken from https://code.launchpad.net/~shanipribadi/invada-studio/fixes

pkgname=invada-studio-plugins-lv2
pkgver=1.2.0
pkgrel=4
pkgdesc="A set of LV2 audio effect plugins, ported from VST."
arch=('i686' 'x86_64')
url="https://launchpad.net/invada-studio/"
license=('GPL2')
groups=('lv2-plugins')
depends=('gtk2')
makedepends=('lv2core')
source=("https://launchpad.net/invada-studio/lv2/1.2/+download/${pkgname}_${pkgver}-nopkg.tgz"
		"invada-studio-fixes.patch")
md5sums=('fe05214dd65dd3096d03c91d05bc3f5d'
         'd49cf43ebe042297145bc55995f21a83')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i $srcdir/invada-studio-fixes.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_SYS_PLUGINS_DIR=/usr/lib/lv2 \
       DESTDIR="$pkgdir" install-sys
}

