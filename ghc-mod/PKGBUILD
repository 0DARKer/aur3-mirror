# custom variables
_hkgname=ghc-mod
_licensefile=LICENSE

# PKGBUILD options/directives
pkgname=ghc-mod
pkgver=0.6.2
pkgrel=6
pkgdesc="Happy Haskell programming on Emacs"
url="http://www.mew.org/~kazu/proj/ghc-mod/"
license=("BSD3")
arch=('i686' 'x86_64')
makedepends=("ghc"
             "haskell-attoparsec"
             "haskell-attoparsec-enumerator"
             "haskell-enumerator"
             "haskell-ghc-paths"
             "haskell-hlint"
             "haskell-transformers")
depends=()
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('99733b2deace5e04632b4e7bdda5b9edc3a34540dfaf91f65dc6b9831aa551ab')


# PKGBUILD functions
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O --prefix=/usr --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup copy --destdir=${pkgdir}
    cd ${pkgdir}/usr/share/$pkgname-$pkgver/
    make
    install -d ${pkgdir}/usr/share/emacs/site-lisp
    cp *.elc *.el ${pkgdir}/usr/share/emacs/site-lisp
}
