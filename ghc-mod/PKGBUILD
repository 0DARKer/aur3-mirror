# custom variables
_hkgname=ghc-mod
_licensefile=LICENSE

# PKGBUILD options/directives
pkgname=ghc-mod
pkgver=1.10.2
pkgrel=1
pkgdesc="Happy Haskell programming on Emacs"
url="http://www.mew.org/~kazu/proj/ghc-mod/"
license=('custom:BSD')
arch=('i686' 'x86_64')
makedepends=("ghc"
             "haskell-attoparsec"
             "haskell-attoparsec-enumerator"
             "haskell-enumerator"
             "haskell-ghc-paths"
             "haskell-hlint"
             "haskell-transformers"
	     "haskell-regex-posix")
depends=()
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('06f38b3ccd0f6541875516c1c56aa9804bcde9cf74a902fdd31bb82d8ff2070e')

# PKGBUILD functions
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O --prefix=/usr --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup copy --destdir=${pkgdir}
    cd ${pkgdir}/usr/share/$pkgname-$pkgver/
    make
    install -d ${pkgdir}/usr/share/emacs/site-lisp
    cp *.elc *.el ${pkgdir}/usr/share/emacs/site-lisp
    install -Dm644 ${srcdir}/${_hkgname}-${pkgver}/LICENSE \
      ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
