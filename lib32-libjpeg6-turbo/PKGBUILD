# Contributor: Martin Stolpe <martinstolpe [at] gmail dot com>
# based on libjpeg6-turbo and lib32-libjpeg-turbo


_pkgbasename=libjpeg6-turbo
pkgname=lib32-${_pkgbasename}
pkgver=1.1.1
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('glibc' "${_pkgbasename}")
makedepends=('nasm' gcc-multilib)
provides=('lib32-libjpeg6')
conflicts=('lib32-libjpeg6')
options=('!libtool')
source=(http://sourceforge.net/projects/libjpeg-turbo/files/$pkgver/libjpeg-turbo-$pkgver.tar.gz)
md5sums=('03b9c1406c7bfdc204313c2917ce6962')

build() {
  cd "$srcdir/libjpeg-turbo-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  sed -i "s|NAFLAGS='-felf64 -DELF -D__x86_64__'|NAFLAGS='-felf32 -DELF -D__x86_64__'|" configure
  ./configure --prefix=/usr --mandir=/usr/share/man \
                             --libdir=/usr/lib32 --without-simd
  make
}

package() {
  cd "$srcdir/libjpeg-turbo-$pkgver"

  make DESTDIR="$pkgdir/" install

  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}
  # Fix /usr/lib
  rm $pkgdir/usr/lib32/lib{turbo,}jpeg.{a,so}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
