# Maintainer: Michael Goehler <somebody.here@gmx.de>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=python2-smbus
pkgver=r6277
pkgrel=1
pkgdesc="Python wrapper for SMBus access over i2c-dev"
arch=('i686' 'x86_64' 'armv6h')
url="http://www.lm-sensors.org/wiki/I2CTools"
license=('GPL')
depends=('python2' 'i2c-tools-svn')
makedepends=('python2' 'subversion')
source=('python2-smbus::svn+http://lm-sensors.org/svn/i2c-tools/trunk'
        'py-smbus.patch')
md5sums=('SKIP'
         'ad940cbc08eac352d35f9c883653853f')

pkgver() {
  cd "${pkgname}"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p0 -i "${srcdir}/py-smbus.patch"
}

build() {
  cd "${srcdir}/${pkgname}/py-smbus"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}/py-smbus"
  python2 setup.py install --prefix "${pkgdir}/usr"
}

# vim:set ts=2 sw=2 et:
