# Maintainer:  Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>
pkgname=ocaml-zip
pkgver=1.04
pkgrel=4
pkgdesc="zip and gzip library for ocaml"
arch=('i686' 'x86_64')
url="http://cristal.inria.fr/~xleroy/software.html#camlzip"
license=('GPL')
depends=('ocaml' 'zlib' 'ocaml-findlib>=1.2.3')
options=(!strip)
source=(http://caml.inria.fr/distrib/bazar-ocaml/camlzip-$pkgver.tar.gz
        http://sources.gentoo.org/viewcvs.py/*checkout*/gentoo-x86/dev-ml/camlzip/files/camlzip-1.03-Makefile-findlib.patch?rev=1.2
        http://sources.gentoo.org/viewcvs.py/*checkout*/gentoo-x86/dev-ml/camlzip/files/META)
md5sums=('b3930f4f2e2563b9a5e1b17aa455e635'
         '0e0f9c6860c23f4efef72563969188dd'
         '357f403e3152810089ea2f7bb86bf77d')

build() {
  cd "$srcdir/camlzip-$pkgver"

  cp ../camlzip-1.03-Makefile-findlib.patch?rev=1.2 makefile.patch
  sed -ie 's|$(OCAMLFIND) install camlzip|$(OCAMLFIND) install $(OCAMLFIND_INSTFLAGS) zip|' makefile.patch || return 1
  patch -i makefile.patch || return 1

  # create the stublibs directory if it does not exist, so ocamlfind installs
  # the shared object there instead of adding it in the ld.conf
  mkdir -p "$pkgdir$(ocamlfind printconf destdir)/stublibs"
  mkdir -p $pkgdir$(ocamlfind printconf destdir)

  ln -s $startdir/META META
  sed -ie 's|version=.*|version="'$pkgver'"|' META || return 1

  make all allopt || return 1
}

package() {
  cd "$srcdir/camlzip-$pkgver"

  # let ocamlfind know where to put things
  OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)" make install || return 1
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
