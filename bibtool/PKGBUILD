# Contributor: Robson Peixoto <robsonpeixoto@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=bibtool
pkgver=2.54
pkgrel=1
pkgdesc="Command line manipulation of BibTeX files. "
arch=('i686' 'x86_64')
url="http://www.gerd-neugebauer.de/software/TeX/BibTool/"
license=('GPL')
depends=('texlive-bin')
source=(http://www.gerd-neugebauer.de/software/TeX/BibTool/BibTool-$pkgver.tar.gz regex.patch)
md5sums=('b3399a53550e73dea76af215e68fb2cb'
         'cf272e197255534aeeabc7f0a6402980')

build() {
  cd "$srcdir/BibTool"
  patch -p1 < $srcdir/regex.patch
  autoreconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/BibTool"
  install -d ${pkgdir}/usr/{bin,lib/BibTool}
  install -Dm755 bibtool ${pkgdir}/usr/bin/bibtool
  for _lib in Lib/*.*; do
    /bin/install -c -m 644 ${_lib} ${pkgdir}/usr/lib/BibTool
  done
}
