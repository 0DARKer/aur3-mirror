# Contributor: Markus Cisler <bl1nk@tlen.pl>
# Maintainer: Patrick Steinhardt <ps@pks.im>

pkgname=mpdas
pkgver=0.3.0
pkgrel=3
pkgdesc="An AudioScrobbler client for MPD written in C++."
arch=('i686' 'x86_64')
license=('BSD')
url="http://50hz.ws"
depends=('libmpd' 'curl')
backup=('etc/mpdasrc')
source=("https://github.com/hrkfdn/${pkgname}/archive/${pkgver}.tar.gz"
        'mpdas.service')
md5sums=('5e805529c406cc3338817da594499825'
         '472cb88288e6e8eea828143e841c1d2d')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}

    sed -i Makefile \
        -e 's|install -m 644|install -Dm644|' \
        -e 's|install mpdas ${PREFIX}/bin|install -Dm755 mpdas ${PREFIX}/bin/mpdas|'
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make PREFIX=/usr CONFIG=/etc
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make install PREFIX=${pkgdir}/usr MANPREFIX=${pkgdir}/usr/share/man/man1

    install -Dm644 ../mpdas.service ${pkgdir}/usr/lib/systemd/system/mpdas.service
    install -Dm644 mpdasrc.example ${pkgdir}/etc/mpdasrc
}

# vim: ft=sh
