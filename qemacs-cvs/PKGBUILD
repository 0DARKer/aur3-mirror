# Contributor: noonov <noonov@gmail.com>

pkgname=qemacs-cvs
pkgver=20100805
pkgrel=1
pkgdesc="A very small but powerful UNIX editor"
arch=('i686' 'x86_64')
url="http://bellard.org/qemacs/"
license=('LGPL2.1')
depends=('libpng' 'libxv')
makedepends=('texi2html')
provides=('qemacs')
conflicts=('qemacs')
source=()
md5sums=()

_cvsroot=":pserver:anonymous@cvs.savannah.nongnu.org:/sources/qemacs"
_cvsmod="qemacs"

build() {
  cd ${srcdir}

  if [[ -d ${_cvsmod}/CVS ]]; then
    (cd ${_cvsmod} && cvs -z3 update -d)
  else
    cvs -z3 -d ${_cvsroot} co -D ${pkgver} -f ${_cvsmod}
  fi
  msg "CVS checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_cvsmod}-build
  cp -r ${_cvsmod} ${_cvsmod}-build

  cd ${srcdir}/${_cvsmod}-build

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${_cvsmod}-build

  make DESTDIR=${pkgdir} install

  mv ${pkgdir}/usr/man ${pkgdir}/usr/share/man
  ln -s qe.1.gz ${pkgdir}/usr/share/man/man1/qemacs.1.gz
  install -D -m644 config.eg ${pkgdir}/usr/share/qe/config.eg
}
