# Maintainer: Your Name <arik.kfir@gmail.com>

###################################################
# General information about the package
###################################################
pkgname=artifactory
pkgver=3.1.1.1
pkgrel=1
pkgdesc="The worldâ€™s most advanced repository manager"
arch=('any')
url="http://www.jfrog.com/home/v_artifactory_opensource_overview"
license=('LGPL')
groups=('arikkfir')
depends=('java-environment' 'bash')
backup=('etc/artifactory/artifactory.config.xml'
        'etc/artifactory/artifactory.system.properties'
        'etc/artifactory/logback.xml'
        'etc/artifactory/mimetypes.xml'
        'etc/security/limits.d/artifactory.limits.conf'
        'usr/lib/artifactory/misc/tomcat/httpd-artifactory.conf'
        'usr/lib/artifactory/tomcat/conf/Catalina/localhost/artifactory.xml'
        'usr/lib/artifactory/tomcat/conf/catalina.policy'
        'usr/lib/artifactory/tomcat/conf/catalina.properties'
        'usr/lib/artifactory/tomcat/conf/logging.properties'
        'usr/lib/artifactory/tomcat/conf/server.xml'
        'usr/lib/artifactory/tomcat/conf/web.xml')
options=('emptydirs')
changelog=ChangeLog
source=("${pkgname}::http://sourceforge.net/projects/artifactory/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.zip/download"
        "artifactory.service"
        "artifactory.limits.conf"
        "httpd-artifactory.conf")
sha256sums=('9a720a0e94b82470a1717408f0e11e10ff82cac49d2ed9f4bc34feaac142b340'
            '07ed8bc80993688c56c6af5fdb4bf8ce713416658bd838e05189e6df250b7dd7'
            '33719c9ad15b1e4e3a8ab2d05cb246831adf1fd298314fa3627aebf7ac4766fe'
            '89bd8fb9e7e8ccbd559ee2ef9c8454bf58449f60e426aba064392b6ec3fa5119')
install=${pkgname}.install

###################################################
# Package
###################################################
package() {

    # Artifactory paths    
    artHome="/usr/lib/artifactory"
    artBackup="/var/backup/artifactory"    
    artBin="${artHome}/bin"
    artData="/var/lib/artifactory"    
    artEtc="/etc/artifactory"
    artLog="/var/log/artifactory"    
    artRun="/var/run/artifactory"    

    # Create artifactory paths
    mkdir -p "${pkgdir}${artHome}"
    mkdir -p "${pkgdir}${artBackup}"
    mkdir -p "${pkgdir}${artBin}"
    mkdir -p "${pkgdir}${artData}"
    mkdir -p "${pkgdir}${artEtc}"
    mkdir -p "${pkgdir}${artLog}"
    mkdir -p "${pkgdir}${artLog}/catalina"
    mkdir -p "${pkgdir}${artRun}"
    mkdir -p "${pkgdir}${artHome}/tomcat/temp"
    mkdir -p "${pkgdir}/etc/security/limits.d"
    mkdir -p "${pkgdir}/etc/systemd/system"
    
    # Copy resources
    artSrc="${srcdir}/${pkgname}-${pkgver}"
    mv ${artSrc}/bin/* ${pkgdir}${artBin}
    mv ${artSrc}/misc ${artSrc}/webapps ${pkgdir}${artHome}
    mv ${artSrc}/tomcat/* ${pkgdir}${artHome}/tomcat
    mv ${artSrc}/etc/* ${pkgdir}${artEtc}
    cp ${srcdir}/artifactory.service ${pkgdir}${artBin}
    cp ${srcdir}/artifactory.limits.conf ${pkgdir}/etc/security/limits.d
    cp ${srcdir}/httpd-artifactory.conf ${pkgdir}${artHome}/misc/tomcat
    ln -s ${artBin}/artifactory.service ${pkgdir}/etc/systemd/system/artifactory.service
    
    # Create links
    ln -s ${artEtc} ${pkgdir}${artHome}/etc
    ln -s ${artLog} ${pkgdir}${artHome}/logs
    ln -s ${artLog}/catalina ${pkgdir}${artHome}/tomcat/logs
    ln -s ${artBackup} ${pkgdir}${artHome}/backup
    ln -s ${artData} ${pkgdir}${artHome}/data
    ln -s ${artRun} ${pkgdir}${artHome}/run

    # Remove excess resources
    rm -f ${pkgdir}${artBin}/*.bat ${pkgdir}${artBin}/*.exe ${pkgdir}${artBin}/installService.sh ${pkgdir}${artBin}/uninstallService.sh ${pkgdir}${artHome}/tomcat/bin/*.bat
    
    # Create empty files
    touch ${pkgdir}${artBackup}/EMPTY
    touch ${pkgdir}${artData}/EMPTY
    touch ${pkgdir}${artLog}/catalina/EMPTY
    touch ${pkgdir}${artRun}/EMPTY
    touch ${pkgdir}${artHome}/tomcat/temp/EMPTY
    touch ${pkgdir}${artHome}/tomcat/work/EMPTY

}

