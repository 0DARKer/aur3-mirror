# $Id: PKGBUILD 57686 2011-11-01 09:55:36Z ebelanger $
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Previous Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Previous Maintainer: Mario J Steele <mario@ruby-im.net>
# Maintainer: Miguel Revilla <yo@miguelrevilla.com>

pkgname=dvdstyler-beta
pkgver=2.4b2
pkgrel=1
pkgdesc="Cross-platform DVD authoring application (beta)"
arch=('i686' 'x86_64')
url="http://www.dvdstyler.de/"
license=('GPL')
depends=('cdrkit' 'dvdauthor' 'dvd+rw-tools' 'wxsvg' 'xine-ui' 'mjpegtools' 'libgnomeui' 'libexif')
makedepends=('zip' 'xmlto' 'docbook-xsl')
optdepends=('dvdisaster: ECC support')
options=('!makeflags')
conflicts=('dvdstyler')
source=(http://downloads.sourceforge.net/dvdstyler/DVDStyler-${pkgver}.tar.bz2)
sha1sums=('1efb53bd31105345392bd56ae875411888178f3f')

build() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  ./configure --prefix=/usr
  sed -i 's/libwxvilla.a/libwxvilla.a \/usr\/lib\/libjpeg.so.8/' src/Makefile
  make
}

package() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
