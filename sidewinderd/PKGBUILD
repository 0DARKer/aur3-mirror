# Maintainer: Tolga Cakir <tolga@cevel.net>

pkgname=sidewinderd
pkgver=0.1.1
pkgrel=1
pkgdesc="Sidewinder daemon - Linux support for Microsoft Sidewinder X4 / X6 keyboards"
arch=('x86_64')
url="http://cevel.net/projects/sidewinderd/"
depends=('libconfig' 'systemd')
makedepends=('clang' 'cmake' 'git')
license=('MIT')
install=${pkgname}.install
source=("${pkgname}::git://github.com/tolga9009/sidewinderd.git")
md5sums=('SKIP')

prepare() {
	mkdir build
	cd ${pkgname}
	git checkout tags/${pkgver}
	git submodule update --init
}

build() {
	cd build
	cmake ../${pkgname} -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
