# Maintainer: Jesse Jaara <gmail.com: jesse.jaara>

pkgname=lib32-kmod
pkgver=5
pkgrel=1
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url="http://git.profusion.mobi/cgit.cgi/kmod.git"
license=('GPL2')
depends=('lib32-zlib' 'kmod')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://packages.profusion.mobi/kmod/kmod-${pkgver}.tar.xz")
md5sums=('b271c2ec54aba1c67bda63c8579d8c15')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/kmod-${pkgver}"

  ./configure \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-tools \
    --with-rootprefix= \
    --with-zlib

  make
}

package() {
  cd "${srcdir}/kmod-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # nuke manpages and headers
  rm -rf "${pkgdir}"/usr/{share,include,bin}
}

