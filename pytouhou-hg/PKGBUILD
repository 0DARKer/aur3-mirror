#Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgbase=pytouhou
pkgname="$_pkgbase-hg"
pkgver=581
pkgrel=1
pkgdesc='Libre reimplementation of the Touhou 6 engine.'
arch=('i686' 'x86_64')
url='http://pytouhou.linkmauve.fr/'
license=('GPL3')
depends=('python2' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'sdl2_ttf' 'libepoxy' 'libgl')
makedepends=('mercurial' 'cython2')
provides=('pytouhou')
conflicts=('pytouhou')
optdepends=('th06-demo-pytouhou: the freeware demo of EoSD.'
            'gtk3: for the optional main menu')

source=("$_pkgbase::hg+http://hg.linkmauve.fr/touhou")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbase"
  hg identify -n
}

build() {
  cd "$srcdir/$_pkgbase"
  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgbase"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sts=2 sw=2 et:
