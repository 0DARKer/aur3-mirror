# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=pytouhou-hg
pkgver=572
pkgrel=1
pkgdesc='Libre reimplementation of the Touhou 6 engine.'
arch=('i686' 'x86_64')
url='http://pytouhou.linkmauve.fr/'
license=('GPL3')
depends=('python2' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'sdl2_ttf' 'libepoxy' 'libgl')
makedepends=('mercurial' 'cython2')
provides=('pytouhou')
conflicts=('pytouhou')
optdepends=('th06-demo-data: the freeware demo of EoSD.'
            'gtk3: for the optional main menu')

source=('hg+http://hg.linkmauve.fr/touhou'
        'th06-demo.sh')
md5sums=('SKIP'
         'c77aa75885dbd50863e20d5e9262a75c')

pkgver() {
  cd "$srcdir/touhou"
  hg identify -n
}

build() {
  cd "$srcdir/touhou"
  python2 setup.py build
}

package() {
  cd "$srcdir/touhou"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm755 "$srcdir/th06-demo.sh" "$pkgdir/usr/bin/th06-demo-pytouhou"
}

# vim:set ts=2 sts=2 sw=2 et:
