# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kolor-manager-git
pkgver=v1.0.2.10.g06f9920
pkgrel=1
pkgdesc="Oyranos CMS cross desktop settings for the KDE system settings panel. (GIT version)"
arch=('i686' 'x86_64')
url="http://www.oyranos.org/kolormanager/"
license=('BSD')
depends=('synnefo-git' 'kdelibs')
makedepends=('git' 'cmake')
provides=('kolor-manager')
conflicts=('kolor-manager')
source=("git://anongit.kde.org/kolor-manager.git"
        'COPYING')
sha1sums=('SKIP'
          '37cb5e34ad08d853124a42d06e273b2c6f33fe4b')
_gitname="kolor-manager"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  rm -fr build
  mkdir -p build
}

build() {
  cd build
  cmake "../${_gitname}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
