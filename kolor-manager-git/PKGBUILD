
pkgname=kolor-manager-git
pkgver=v1.0.2.1.g995bac6
pkgrel=1
pkgdesc="Oyranos CMS cross desktop settings for the KDE system settings panel. (GIT version)"
arch=('i686' 'x86_64')
url="http://www.oyranos.org/kolormanager/"
license=('BSD')
depends=('oyranos-git' 'kdelibs')
makedepends=('git' 'cmake')
provides=('kolor-manager')
conflicts=('kolor-manager')
source=("git://anongit.kde.org/kolor-manager.git"
        'COPYING')
sha1sums=('SKIP'
          '37cb5e34ad08d853124a42d06e273b2c6f33fe4b')
_gitname="kolor-manager"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="$pkgdir" install
  install -Dm644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
