# Maintainer: Alucryd <alucryd at gmail dot com>

pkgname=steam-skin-manager
pkgver=15
pkgrel=2
pkgdesc="GUI for Steam skins management"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://iubuntu.cz/Steam/"
depends=('steam' 'zenity')
install=${pkgname}.install
source=('http://iubuntu.cz/Steam/steam-skin-installer.deb')
sha256sums=('29c0acbe3d0c0bc050d436b91e1b6cb96b685cec798326f8d8a0857581c650d7')


build() {
  cd ${srcdir}
  tar -xf data.tar.gz
  cd usr/share/steam-integration
  for i in borderless native
  do
    for j in Ambiance Radiance
    do 
      rm "$i/$j/Graphics/chkSelStd.tga"
      rm "$i/$j/Graphics/chkUnselStd.tga"
      ln -s "chkSelDis.tga" "$i/$j/Graphics/chkSelStd.tga"
      ln -s "chkUnselDis.tga" "$i/$j/Graphics/chkUnselStd.tga" 
    done
  done 
}

package() {
  cd ${srcdir}
  cp -R usr "${pkgdir}/"
  find "${pkgdir}" -type d -print0 | xargs -0 chmod 755
  find "${pkgdir}" -type f -print0 | xargs -0 chmod 644
  chmod 755 "${pkgdir}/usr/bin/steam-skin"
  chmod 755 "${pkgdir}/usr/share/steam-integration/steam-skin-gui"
  chmod 755 "${pkgdir}/usr/share/steam-integration/steam-wb"
  if  [ "$CARCH" == "i686" ]; then
    mv "${pkgdir}/usr/lib32" "${pkgdir}/usr/lib"
  fi
}
