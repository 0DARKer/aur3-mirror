# Contributor: Gergely Imreh <imrehg(at)gmail(dot)com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: boromil@gmail.com
# Contributor: Ner0

pkgname=ffmpegthumbnailer-svn
pkgver=247
pkgrel=1
pkgdesc="Lightweight video thumbnailer that can be used by file managers."
url="http://code.google.com/p/ffmpegthumbnailer/"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ffmpeg' 'libjpeg' 'libpng')
optdepends=('gvfs: support for gio uris')
makedepends=('subversion' 'gcc' 'automake' 'autoconf')
conflicts=('ffmpegthumbnailer')
provides=('ffmpegthumbnailer')
options=('!libtool')

_svntrunk=https://ffmpegthumbnailer.googlecode.com/svn/trunk/
_svnmod=ffmpegthumbnailer

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --enable-gio --enable-thumbnailer
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
