# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>

pkgname=ocaml-bisect
pkgver=1.1
pkgrel=2
pkgdesc="Code coverage tool for the Objective Caml language"
arch=('i686' 'x86_64')
url=("http://bisect.x9c.fr")
license=('GPLv3')
depends=('ocaml' 'ocaml-findlib')
makedepends=('ocaml')
_pkgname=bisect
source=(http://bisect.x9c.fr/distrib/${_pkgname}-${pkgver}.tar.gz
  fix-install-path.patch)
md5sums=('6e4be9696c902f74908fd835a84353e9'
         '625e0530860bb85c07a1954733389c13')

build () {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -N -p1 -i "${srcdir}/fix-install-path.patch" || return 1
  chmod +x ./configure
  ./configure || return 1
  make all || return 1
}

package () {
  OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
  cd "${srcdir}/${_pkgname}-${pkgver}"

  sed "s|PATH_INSTALL=|PATH_INSTALL=${pkgdir}/|" -i Makefile
  mkdir -p "${OCAMLFIND_DESTDIR}"
  OCAMLFIND_DESTDIR="${OCAMLFIND_DESTDIR}" DESTDIR="${pkgdir}" PREFIX="/usr" make install || return 1
  install -D "doc/bisect.pdf" "${pkgdir}/usr/share/doc/${_pkgname}/bisect.pdf"
}
