# Maintainer: Leo von Klenze <devel@leo.von-klenze.de>
pkgname=mingw-w64-plibc
pkgver=0.1.7
pkgrel=1
pkgdesc="A POSIX compliant libc for Windows"
arch=(any)
url="http://plibc.sourceforge.net/"
license=('LGPL')
makedepends=(libtool dos2unix mingw-w64-gcc)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=(http://freefr.dl.sourceforge.net/project/plibc/plibc/0.1.7/plibc-0.1.7-src.tar.gz http://mingw64-further.googlecode.com/files/PlibC-UTF8ization-patches.tar.xz http://mingw64-further.googlecode.com/files/PlibC-MinGW64-patches-121209.tar.xz mingw32.patch libtool)
md5sums=(817590ffe0e85b63c4b7a598ac1d280c 2916964c647c21ee30291e9700db274f f91da4272d99e05e3609b7423ab0146a d4e6a79c86c8fc85e47c56ba1f5dff2c 4fb73ffccf0e0af8500ef634f4d9bf6b)

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

_optimal_make_jobs() {
  if [ -r /proc/cpuinfo ]; then
    local core_count=$(grep -Fc processor /proc/cpuinfo)
  else
    local core_count=0
  fi  
  if [ $core_count -gt 1 ]; then
    echo -n $[$core_count-1]
  else
    echo -n 1
  fi  
}

build() {
  cd "${srcdir}/PlibC-$pkgver"
  dos2unix src/strerror.c
  cat ../000*.patch | patch -p 1
  patch -p 1 < ../../mingw32.patch


  export CPPFLAGS="$CPPFLAGS -DHAVE_DECL_GETADDRINFO -DHAVE_DECL_GETNAMEINFO -DHAVE_DECL_FREEADDRINFO" 

  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/PlibC-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
   
    if [ "${_arch}" = "x86_64-w64-mingw32" ]; then
      # we need an actual version of libtool for the x86 build
      # Unfortunately libtoolize does not work without autoreconf
      # but I was not able to get autoreconf to work with that
      # source, so I use the following hack
      cp ../../libtool .
    fi
 
    make -j$(_optimal_make_jobs)
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install    
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g 
    rm -f "$pkgdir/usr/${_arch}/lib/libplibc.a" # we only want to keep the shared files
  done
}

# vim:set ts=2 sw=2 et:
