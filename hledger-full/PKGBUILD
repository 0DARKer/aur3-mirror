# Maintainer: Peter Simons <simons@cryp.to>
pkgname=hledger-full
pkgver=0.12.1
pkgrel=5
pkgdesc="A command-line, curses, and web-based double-entry accounting tool."
url="http://hackage.haskell.org/package/hledger"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=()
depends=('gmp' 'ghc' 'haskell-cabal' 'haskell-hunit' 'haskell-containers' 'haskell-csv' 'haskell-directory' 'haskell-filepath' 'haskell-hledger-lib' 'haskell-mtl' 'haskell-old-locale' 'haskell-old-time' 'haskell-parsec' 'haskell-process' 'haskell-regexpr' 'haskell-safe' 'haskell-split' 'haskell-time' 'haskell-utf8-string' 'haskell-chart' 'haskell-colour' 'gtk2hs-buildtools' 'haskell-vty' 'haskell-io-storage' 'haskell-yesod' 'haskell-hamlet' 'haskell-convertible-text' 'haskell-data-object' 'haskell-failure')
options=('strip')
conflicts=('hledger')
provides=('hledger')
source=(http://hackage.haskell.org/packages/archive/hledger/$pkgver/hledger-$pkgver.tar.gz)
install=hledger-full.install
md5sums=('a39eab928361f17343875e96f032c439')

build() {
    cd ${srcdir}/hledger-${pkgver}
    sed -r -i '/^[\t ]+,gtk2hs-buildtools$/d' hledger.cabal
    runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname} -fvty -fweb -fchart
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
}

package() {
    cd ${srcdir}/hledger-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/$pkgname/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/$pkgname/unregister.sh
    install -d -m755 $pkgdir/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/hledger
    runhaskell Setup copy --destdir=${pkgdir}
}
