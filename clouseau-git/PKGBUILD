# Maintainer: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Leif Middelschulte <leif dot middelschulte at gmail dot com>

pkgname=clouseau-git
_pkgname=clouseau
pkgver=0.2.0.275.3ae0b13
pkgrel=1
pkgdesc="Evas/Edje (EFL) introspection utility - Development version"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('elementary>=1.7.99')
makedepends=('git')
options=('!libtool' '!staticlibs')
source=("git://git.enlightenment.org/tools/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"

  for i in v_maj v_min v_mic; do
    local _$i=$(grep -m 1 $i configure.ac | sed 's/m4//' | grep -o "[[:digit:]]*")
  done

  echo $_v_maj.$_v_min.$_v_mic.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 AUTHORS "$pkgdir/usr/share/licenses/$pkgname/AUTHORS"
}
