# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Casey McDonnell <c.j.mcdonn@gmail.com>

pkgname=garlic
pkgver=1.6
pkgrel=2
pkgdesc="A molecular visualization program."
arch=('i686' 'x86_64')
url="http://www.zucic.org/$pkgname/"
license=('GPL2')
depends=('libx11')
install="$pkgname.install"
source=(http://www.zucic.org/sources/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz
        http://www.zucic.org/sources/$pkgname-$pkgver/$pkgname-$pkgver-doc.tar.gz
        http://www.zucic.org/sources/$pkgname-$pkgver/$pkgname.1.gz
        $pkgname.desktop)
md5sums=('5fac1f13733d9700925a0cb9a407c651'
         '08ec479898102062250ebd60700e102c'
         '5b91358ce8a48e12e6c1d3650aa6ebb4'
         'da42765ab5a03e78121c7e7729b609ca')

build() {
  cd $srcdir/$pkgname-$pkgver
  #patch Makefile to use CFLAGS set in /etc/makepkg.conf
  sed -i "s|CCOPT = -O3|CCOPT = ${CFLAGS}|g" ./Makefile
  #tell make where libx11.so lives
  sed -i "s|-L/usr/X11R6/lib|-L/usr/lib|g" ./Makefile
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname
  install -Dm644 .garlicrc $pkgdir/usr/share/$pkgname/.garlicrc
  install -d $pkgdir/usr/share/$pkgname
  install -m644 -t $pkgdir/usr/share/$pkgname *.pdb
  install -d $pkgdir/usr/share/doc/$pkgname
  cp -r {favicon.ico,tolower.script,*.jpg,*.gif,*.html,*.png,*/} $pkgdir/usr/share/doc/$pkgname
  install -Dm644 $srcdir/$pkgname.1 $pkgdir/usr/share/man/man1/$pkgname.1
  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 $pkgname.xpm $pkgdir/usr/share/pixmaps/$pkgname.xpm
}