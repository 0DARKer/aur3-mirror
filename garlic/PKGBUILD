# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Casey McDonnell <c.j.mcdonn@gmail.com>

pkgname=garlic
pkgver=1.6
pkgrel=3
pkgdesc="A molecular visualization program."
arch=('i686' 'x86_64')
url="http://www.zucic.org/garlic/"
license=('GPL2')
depends=('libx11')
source=(${url/garlic/}/sources/$pkgname-$pkgver/$pkgname{.1.gz,-$pkgver{,-doc}.tar.gz}
        $pkgname.desktop)
md5sums=('5b91358ce8a48e12e6c1d3650aa6ebb4'
         '5fac1f13733d9700925a0cb9a407c651'
         '08ec479898102062250ebd60700e102c'
         'da42765ab5a03e78121c7e7729b609ca')

build() {
  cd $pkgname-$pkgver
  #tell Makefile to use CFLAGS set in /etc/makepkg.conf and where libx11.so is
  sed -i -e "s|CCOPT = -O3|CCOPT = ${CFLAGS}|" -e "s|-L/usr/X11R6/lib|-L/usr/lib|" Makefile
  make
}

package() {
  cd $pkgname-$pkgver
  install -d $pkgdir/usr/{bin,share/{$pkgname,applications,doc/$pkgname,man/man1,pixmaps}}
  install -m755 $pkgname $pkgdir/usr/bin
  install -m644 .garlicrc $pkgdir/usr/share/$pkgname/
  install -m644 -t $pkgdir/usr/share/$pkgname *.pdb
  install -m644 $pkgname.xpm $pkgdir/usr/share/pixmaps/
  cp -r {favicon.ico,tolower.script,*.jpg,*.gif,*.html,*.png,*/} $pkgdir/usr/share/doc/$pkgname
  install -m644 $srcdir/$pkgname.1 $pkgdir/usr/share/man/man1/
  install -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/
}