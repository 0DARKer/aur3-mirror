# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=thunar-devel
pkgver=1.3.0
pkgrel=2
pkgdesc="Modern file manager for Xfce (Development Version)"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
url="http://thunar.xfce.org"
groups=('xfce4-devel') 
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify>=0.7.1' 'udev'
         'gtk2' 'exo-devel>=0.6.0' 'libxfce4util-devel>=4.8.1' 'libxfce4ui-devel' 'libpng')
makedepends=('intltool' 'gtk-doc' 'xfce4-panel-devel>=4.8.0')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'xfce4-panel-devel: for trash applet'
            'tumbler-devel: for thumbnail previews'
            'thunar-volman-devel: manages removable devices'
            'thunar-archive-plugin-devel: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
options=('!libtool')
conflicts=('thunar' 'thunar-git')
install=thunar.install
backup=('etc/polkit-1/localauthority/50-local.d/org.freedesktop.udisks.pkla')
source=(http://archive.xfce.org/src/xfce/thunar/1.3/Thunar-${pkgver}.tar.bz2
	org.freedesktop.udisks.pkla)
md5sums=('ab6f728384c0d925b40afae2f41268f3'
         'a7ddb5eec02d9a8e91a2997862e73cd8')

build() {
  cd ${srcdir}/Thunar-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-gio-unix \
	--enable-dbus \
	--enable-startup-notification \
	--enable-gudev \
	--enable-notifications \
	--enable-exif \
	--enable-pcre \
	--enable-gtk-doc \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/Thunar-${pkgver}
  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop

  # install udisks permission file
  install -dm700 ${pkgdir}/etc/polkit-1/localauthority
  install -dm755 ${pkgdir}/etc/polkit-1/localauthority/50-local.d
  install -m644 ${srcdir}/org.freedesktop.udisks.pkla ${pkgdir}/etc/polkit-1/localauthority/50-local.d/
}
