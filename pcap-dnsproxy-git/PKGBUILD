# Maintainer: wenLiangcan <boxeed at gmail dot com>

pkgname=pcap-dnsproxy-git
_pkgname=pcap-dnsproxy
pkgver=20150317
pkgrel=1
pkgdesc="A local DNS server base on WinPcap and LibPcap."
arch=("any")
url="https://github.com/chengr28/Pcap_DNSProxy"
license=('GPL v2')
depends=('libpcap')
makedepends=('git' 'make')
provides=('pcap-dnsproxy')
conflicts=('pcap-dnsproxy')
source=("${pkgname}"::'git://github.com/chengr28/Pcap_DNSProxy.git'
        'Makefile'
        "${_pkgname}.service")
install="${_pkgname}.install"
md5sums=('SKIP'
         '6e2eb4c13af05525ec883ea4bbf105cb'
         'e61ae4662dd1e9838c9b7788e93cbc3b')

pkgver() {
    cd "${srcdir}/${pkgname}"
    git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {
    cd "${srcdir}/${pkgname}/Source/Linux"
    rm -f Makefile
    cp "${srcdir}/Makefile" .
    make
}

package() {
    cd "${srcdir}/${pkgname}/Source/Linux"
    install -Dm777 "Pcap_DNSProxy" "${pkgdir}/usr/share/${_pkgname}/Pcap_DNSProxy"

    install -Dm644 "Example/Config.conf" "${pkgdir}/usr/share/${_pkgname}/Config.conf"
    install -Dm644 "Example/Hosts.conf" "${pkgdir}/usr/share/${_pkgname}/Hosts.conf"

    install -dm755 "${pkgdir}/etc/${_pkgname}"
    ln -s "/usr/share/${_pkgname}/Config.conf" "${pkgdir}/etc/${_pkgname}/Config.conf"
    ln -s "/usr/share/${_pkgname}/Hosts.conf" "${pkgdir}/etc/${_pkgname}/Hosts.conf"

    install -Dm644 "${srcdir}/${_pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${_pkgname}.service"

    cd "${srcdir}/${pkgname}/Documents"
    find . -type f -exec install -Dm644 {} \
        "${pkgdir}/usr/share/doc/${_pkgname}/{}" \;
}
