# Maintainer: Andrea Fagiani <andfagiani {at} gmail {dot} com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>

# Installation order:  freetype2-ubuntu fontconfig-ubuntu libxft-ubuntu cairo-ubuntu
pkgname=freetype2-ubuntu
pkgver=2.4.5
_ubver=2.4.4-2
pkgrel=1
pkgdesc="TrueType font rendering library, with Ubuntu's LCD rendering patches"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntu/oneiric/+source/freetype"
license=('GPL')
depends=('zlib')
conflicts=('freetype2' 'freetype2-cleartype' 'freetype2-lcd')
provides=("freetype2=$pkgver")
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        https://launchpad.net/ubuntu/oneiric/+source/freetype/$_ubver/+files/freetype_$_ubver.diff.gz
        apple-offset.patch enable-valid.patch)

md5sums=('90428a6d8ec4876cd1eb94858c2a59b0'
         '8a170e472c2b1d60dff0987722a290d3'
         '1c964f90ae90aa391af574ceb0814cda'
         '214119610444c9b02766ccee5e220680')

build() {
  cd ${srcdir}/freetype-${pkgver}

  # Patch from ubuntu
  patch -Np1 -i $srcdir/freetype_$_ubver.diff

  sed -i -e "s#\ssrc# freetype-$pkgver/src#g" debian/patches-freetype/freetype-2.1.7-backwards.compat.patch
  sed -i -e "s/-p[0-9]\|.*otvalid\.patch//g" debian/patches-freetype/series

  for _f in $(cat debian/patches-freetype/series) ; do    
    patch -Np1 -i debian/patches-freetype/$_f    
  done

  # Patches from arch trunk
  patch -Np1 -i $srcdir/enable-valid.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/freetype-${pkgver}

  make DESTDIR=${pkgdir} install
}
