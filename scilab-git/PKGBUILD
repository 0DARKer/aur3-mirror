# Maintainer: Victor Dmitriyev <mrvvitek@gmail.com>

pkgname=scilab-git
pkgver=5.4.1.404.g1f0dfd3
pkgrel=1
pkgdesc='A scientific software package for numerical computations'
arch=('any')
url="http://www.scilab.org/"
license=('custom:CeCILL')
makedepends=('apache-ant' 'git' 'ocaml')
depends=('eigen' 'bwidget' 'bsh' 'xalan-java' 'hdf5' 'curl' 'arpack'
'tk' 'gettext' 'fftw' 'umfpack'

'java-skinlf' 'java-testng' 'jgoodies-looks' 'fop-hyph' 'docbook-xsl-saxon'
'java-qdox' 'flexdock' 'scirenderer' 'jrosetta' 'javahelp2' 'jlatexmath-fop'
'jgraphx' 'jeuclid-core' 'pvm' 'jing' 'saxon6'

'java-commons-beanutils' 'antlr2'
)
provides=('scilab')
conflicts=('scilab')
source=("git://git.scilab.org/scilab")
md5sums=('SKIP')

pkgver() {
  cd scilab
  # Use the tag of the last commit
  git describe --tags --long --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir"
  cd "$srcdir/scilab"
  rm -rf .git/
  cd scilab/
  ./configure \
    --prefix=/usr \
    --without-matio \
    --with-umfpack \
    --with-fftw \
    --with-modelica \
    --enable-build-localization \
    --with-gfortran  # Needed for 64 bits, don't hurt for 32 bits
  make all doc
}

package() {
  cd "$srcdir/scilab/scilab"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "$srcdir/scilab/COPYING" "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
  install -Dm644 "$srcdir/scilab/COPYING-FR" "$pkgdir/usr/share/licenses/${pkgname}/COPYING-FR"
  install -Dm644 "$srcdir/scilab/COPYING-BSD" "$pkgdir/usr/share/licenses/${pkgname}/COPYING-BSD"
}

# vim:set ts=2 sw=2 et:
