# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>

pkgname=haxe-svn
pkgver=4513
pkgrel=1
pkgdesc="A multiplatform open source programming language"
arch=('i686' 'x86_64')
url="http://haxe.org/"
license=('GPL')
groups=('devel')
depends=('neko>=1.8.2' 'bash')
makedepends=('subversion' 'ocaml' 'zlib' 'neko>=1.8.2')
provides=('haxe')
options=('!strip' 'emptydirs')
install=haxe.install
source=('haxeserver'
  'haxe.sh'
  'haxe.csh')
md5sums=('27d7af131da515355cca04b8b404feaa'
         '79cbf6ebdb3d56e980d1a17fa9cb3b2d'
         '5e0e656293e92a6437d76f2f406438b5')

_svntrunk=http://haxe.googlecode.com/svn/trunk/
_svnmod=haxe
_buildroot="$srcdir/$_svnmod"

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
    svn revert "$_svnmod"/main.ml
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."
  cd "$_buildroot"
  #
  # BUILD HERE
  #
  msg "SVN HEAD revision is ${pkgver}. Starting build..."
  sed -i -e "s/\\(Haxe Compiler %d.%.2d\\) - /\\1 [SVN r$pkgver] - /" main.ml &&
    make &&
    msg2 "done."
  export HAXE_LIBRARY_PATH="$_buildroot"/std
  for i in haxe{doc,lib}; do 
    cd "$_buildroot"/std/tools/$i && 
      "$_buildroot"/haxe $i.hxml &&
      install -D -T -m755 $i "$_buildroot"/$i
  done
  cd "$srcdir"/haxe/doc
  "$srcdir"/haxe/haxe all.hxml
  [ -f flash8.xml -a -f flash9.xml -a -f neko.xml -a -f js.xml -a -f php.xml -a -f cpp.xml ] &&
    msg "Generating api docs..." &&
    $_buildroot/haxedoc \
      "flash8.xml;flash8;flash" "neko.xml;neko" "js.xml;js" \
      "flash9.xml;flash" "php.xml;php" "cpp.xml;cpp" &&
    echo && msg2 "done."
}

package() {
  install -d "$pkgdir"{/usr/{bin,lib/haxe/lib},/opt/haxe/doc,/etc/profile.d}
  install -m755 "$srcdir"/haxeserver "$pkgdir"/usr/bin/
  install -m755 "$srcdir"/haxe.{,c}sh "$pkgdir"/etc/profile.d/
  svn export -q "$_buildroot"/std "$pkgdir"/opt/haxe/std
  install -m644 "$_buildroot"/doc/{LICENSE,CHANGES}.txt "$pkgdir"/opt/haxe/
  install -m755 "$_buildroot"/haxe{,lib,doc} "$pkgdir"/usr/bin/
  cp -r "$_buildroot"/doc/content "$pkgdir"/opt/haxe/doc/
  install -m644 "$_buildroot"/doc/index.html "$pkgdir"/opt/haxe/doc/
}

# vim:set ts=2 sw=2 et:
