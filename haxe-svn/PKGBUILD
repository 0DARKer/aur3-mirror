# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>

pkgname=haxe-svn
pkgver=4487
pkgrel=1
pkgdesc="A multiplatform open source programming language"
arch=(i686 x86_64)
url="http://haxe.org/"
license=(GPL)
groups=(devel)
depends=('neko>=1.8.2')
makedepends=(subversion ocaml 'neko>=1.8.2')
provides=(haxe)
options=(!strip emptydirs)
install=haxe.install
source=(haxeserver 
  haxe.sh 
  haxe.csh 
  install.ml.patch)
md5sums=('27d7af131da515355cca04b8b404feaa'
         '79cbf6ebdb3d56e980d1a17fa9cb3b2d'
         '5e0e656293e92a6437d76f2f406438b5'
         '5409986728a2f933cc4cbf6a330ac6fb')

_svntrunk=http://haxe.googlecode.com/svn/trunk/
_svnmod=haxe

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD HERE
  #
  #curl -O ${_svntrunk}doc/install.ml  
  _svnrev=$(svn info $_svntrunk|awk '/Revision:/{print $2}')
  msg "SVN HEAD revision is $_svnrev. Starting build..."
  cp doc/install.ml ..
  cd ..
  patch -p0 --verbose -i install.ml.patch
  ocaml install.ml $_svnrev
  msg2 "done."
  export HAXE_LIBRARY_PATH=$srcdir/haxe/std:.
  for i in haxe{doc,lib}; do
    bin/haxe haxe/std/tools/$i/$i.hxml
  done
  install -m755 haxe{lib,doc} $srcdir/bin
  cd $srcdir/haxe/doc
  $srcdir/bin/haxe all.hxml
  if [ -e flash8.xml -a -e flash9.xml -a -e neko.xml -a -e js.xml \
       -a -e php.xml -a -e cpp.xml ]; then
    msg "Generating api docs..."
    $srcdir/bin/haxedoc \
      "flash8.xml;flash8;flash" "neko.xml;neko" "js.xml;js" \
      "flash9.xml;flash9" "php.xml;php" "cpp.xml;cpp" \
    && echo && msg2 " done."
  fi
}

package() {
  install -d $pkgdir{/usr/{bin,lib/haxe/lib},/opt/haxe/doc,/etc/profile.d}
  cd "$srcdir"
  cp bin/* ${pkgdir}/usr/bin/
  if [ -d $pkgdir/opt/haxe/std ]; then
    rm -rf $pkgdir/opt/haxe/std
  fi
  svn export -q haxe/std $pkgdir/opt/haxe/std
  install "${srcdir}"/haxeserver "${pkgdir}"/usr/bin/
  install "${srcdir}"/haxe.{,c}sh "${pkgdir}"/etc/profile.d/
  cp -r haxe/doc/content ${pkgdir}/opt/haxe/doc/
  install haxe/doc/index.html ${pkgdir}/opt/haxe/doc/
}

# vim:set ts=2 sw=2 et:
