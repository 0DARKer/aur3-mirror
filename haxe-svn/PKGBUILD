# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>

pkgname=haxe-svn
pkgver=4491
pkgrel=1
pkgdesc="A multiplatform open source programming language"
arch=('i686' 'x86_64')
url="http://haxe.org/"
license=('GPL')
groups=('devel')
depends=('neko>=1.8.2')
makedepends=('subversion' 'ocaml' 'zlib' 'neko>=1.8.2')
provides=('haxe')
options=('!strip' 'emptydirs')
install=haxe.install
source=('haxeserver'
  'haxe.sh'
  'haxe.csh'
  'install.ml.patch')
md5sums=('27d7af131da515355cca04b8b404feaa'
         '79cbf6ebdb3d56e980d1a17fa9cb3b2d'
         '5e0e656293e92a6437d76f2f406438b5'
         '4db33159ef296691eb3440fb74027538')

_svntrunk=http://haxe.googlecode.com/svn/trunk/
_svnmod=haxe

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
    svn revert "$_svnmod"/main.ml
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."
  cd "$srcdir/$_svnmod"
  #
  # BUILD HERE
  #
  msg "SVN HEAD revision is ${pkgver}. Starting build..."
  cp doc/install.ml ..
  cd ..
  patch -p0 --verbose -i install.ml.patch
  ocaml install.ml "$pkgver"
  msg2 "done."
  export HAXE_LIBRARY_PATH="$srcdir/$_svnmod"/std
  for i in haxe{doc,lib}; do
    bin/haxe haxe/std/tools/$i/$i.hxml
  done
  install -m755 haxe{lib,doc} "$srcdir"/bin
  cd "$srcdir"/haxe/doc
  "$srcdir"/bin/haxe all.hxml
  if [ -e flash8.xml -a -e flash9.xml -a -e neko.xml -a -e js.xml \
       -a -e php.xml -a -e cpp.xml ]; then
    msg "Generating api docs..."
    $srcdir/bin/haxedoc \
      "flash8.xml;flash8;flash" "neko.xml;neko" "js.xml;js" \
      "flash9.xml;flash" "php.xml;php" "cpp.xml;cpp" \
    && echo && msg2 "done."
  fi
}

package() {
  install -d "$pkgdir"{/usr/{bin,lib/haxe/lib},/opt/haxe/doc,/etc/profile.d}
  cd "$srcdir"
  install haxe/doc/{LICENSE,CHANGES}.txt "$pkgdir"/opt/haxe/
  mv bin/std "$pkgdir"/opt/haxe/
  install bin/* "$pkgdir"/usr/bin/
  install "$srcdir"/haxeserver "$pkgdir"/usr/bin/
  install "$srcdir"/haxe.{,c}sh "$pkgdir"/etc/profile.d/
  cp -r haxe/doc/content "$pkgdir"/opt/haxe/doc/
  install haxe/doc/index.html "$pkgdir"/opt/haxe/doc/
}

# vim:set ts=2 sw=2 et:
