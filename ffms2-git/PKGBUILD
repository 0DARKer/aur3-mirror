#Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=ffms2-git
pkgver=2.20.5.g946e7f3
pkgrel=1
pkgdesc="An FFmpeg/Libav based source library and Avisynth/Vapoursynth plugin for easy frame accurate access (GIT version)"
url="https://github.com/FFMS/ffms2"
arch=('x86_64' 'i686')
license=('MIT')
depends=('ffmpeg')
makedepends=('git')
provides=('ffms2' 'vapoursynth-plugin-ffms2-git')
conflicts=('ffms2' 'vapoursynth-plugin-ffms2-git')
replaces=('ffmpegsource')
source=('git://github.com/FFMS/ffms2')
sha1sums=('SKIP')
_gitname="ffms2"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  ./autogen.sh --prefix=/usr --enable-shared=yes --enable-static=no --enable-avresample
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/lib/vapoursynth"
  ln -s /usr/lib/libffms2.so "${pkgdir}/usr/lib/vapoursynth/libffms2.so"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
