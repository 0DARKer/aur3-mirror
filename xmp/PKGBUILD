# Maintainer  : SpepS <dreamspepser at yahoo dot it>
# Contributor : john_schaf <john_schaf@hotmail.fr>

pkgname=xmp
pkgver=3.5.0
pkgrel=1
pkgdesc="The Extended Module Player is a portable command-line module player that supports over 90 module formats (Amiga, Atari, ..)"
arch=(i686 x86_64)
url="http://xmp.sourceforge.net/"
license=('GPL')
depends=('libpulse' 'esound' 'nas')
makedepends=('audacious' 'xmms' 'bmp')
optdepends=('audacious: audacious plugin'
            'xmms: xmms plugin'
            'bmp: bmp plugin'
            'pulseaudio: pulseaduio support')
backup=('etc/xmp/xmp.conf' 'etc/xmp/modules.conf')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        "xmp-3.4.0-audacious-3.1.patch")
md5sums=('47e54e6dfa88ce37370054d4a3ea955f'
         '21531718696a0041bf276b3a9b59b4c4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # audacious 3.1+ patch
  patch -p1 -i ../xmp-3.4.0-audacious-3.1.patch

  ./configure --prefix=/usr \
              --enable-nas \
              --enable-pulseaudio \
              --enable-xmms-plugin \
              --enable-bmp-plugin \
              --enable-audacious-plugin \
              --enable-esd
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
