# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=khotkeys-git
pkgver=r1641.08030b5
pkgrel=1
pkgdesc='KHotKeys'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('plasma-workspace-git')
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git')
conflicts=('khotkeys' 'kde-workspace<0')
provides=('khotkeys')
source=('git://anongit.kde.org/khotkeys.git')
md5sums=('SKIP')

pkgver() {
  cd khotkeys
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../khotkeys/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
