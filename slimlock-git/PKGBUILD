# Maintainer: Joel Burget <joelburget@gmail.com>
# Contributor: Guff <kasull88@gmail.com>
pkgname=slimlock-git
pkgver=20110521
pkgrel=1
pkgdesc="Unholy screen locker based on SLiM"
arch=('i686' 'x86_64')
url="http://programsthatsmellgood.com/slimlock/"
license=('GPL2')
depends=('libxmu' 'libxft' 'libjpeg>=8' 'libpng>=1.4' 'slim')
makedepends=('git')
provides=('slimlock')
conflicts=('slimlock')
source=()
md5sums=()

_gitroot="git://github.com/Guff/slimlock.git"
_gitname="slimlock"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" MANDIR=/usr/share/man install
}
