# Maintainer : Lluixhi Scura <lluixhi@gmail.com>

pkgname=libtorrent-rasterbar-1
_pkg=libtorrent-rasterbar
pkgver=1.0.2
pkgrel=1
epoch=1
pkgdesc="A C++ library that aims to be a good alternative to all the other bittorrent implementations around (1.x)"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python2')
makedepends=('boost')
conflicts=('libtorrent-rasterbar')
provides=('libtorrent-rasterbar')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/libtorrent/$_pkg-$pkgver.tar.gz)
sha1sums=('bfb161825306abbd5c03775e75f5e094ee757a9a')

build() {
  cd $_pkg-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --enable-python-binding \
      --with-libgeoip=system \
      --enable-deprecated-functions=no
  make
}

package() {
  cd $_pkg-$pkgver
  make DESTDIR="$pkgdir" install
  install -D COPYING "$pkgdir/usr/share/licenses/$_pkg/LICENSE"
}
