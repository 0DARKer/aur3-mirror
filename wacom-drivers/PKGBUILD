# Contributor: Ilya Galushko <helloy-ilya@yandex.ru>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com}
# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=wacom-drivers
pkgver=0.11.0
_driverver=0.11.0
pkgrel=2
pkgdesc="Kernel and X.Org drivers for Wacom tablets, with latest patches"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('xorg-server>=1.9.0' 'libxi' 'kernel26>=2.6.26')
makedepends=('xorg-server-devel' 'libxext' 'kernel26-headers')
options=('!libtool')
conflicts=('xf86-input-wacom' 'xf86-input-wacom-git' 'linuxwacom-cvs'
	   'input-wacom' 'inpput-wacom-cvs' 'linuxwacom-baboo-cth-ctl')
replaces=('linuxwacom' 'linuxwacom-dev' 'linuxwacom-cvs' 'linuxwacom-bamboo-cth-ctl')
provides=("xf86-input-wacom=$pkgver" "input-wacom=$_driverver")
install=wacom-drivers.install
source=(http://prdownloads.sourceforge.net/linuxwacom/input-wacom-${_driverver}.tar.bz2
        http://prdownloads.sourceforge.net/linuxwacom/xf86-input-wacom-${pkgver}.tar.bz2
        10-wacom.rules lenovo_x220.patch)
md5sums=('0f98ee3fff0abac2ecd9a99c9ddf7db8'
         'e0f6182cf7fba19c13f3db7e0da1baf3'
         'b1949a1985afcb594260d1885ee4832c'
         '6cddae626db09b5e31943ef996974bcb')

build() {
  msg "Building kernel module..."
  cd "${srcdir}"/input-wacom-${_driverver}
  patch -p1 < ../lenovo_x220.patch
  ./configure --prefix=/usr
  make
  msg "Building /dev/input/event support for serial devices..."
  cd inputattach
  gcc inputattach.c -o inputattach
  msg "Building X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  ./configure --prefix=/usr  --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  msg "Installing kernel modules..."
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 36
  then
    _ver=2.6.36
  else
    _ver=2.6.30
  fi
  cd "${srcdir}"/input-wacom-${_driverver}/$_ver
  install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
  install -D -m 644 wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
  msg "Installing /dev/input/event support for serial devices..."
  cd ../inputattach
  install -D -m 755 inputattach "${pkgdir}"/usr/bin/inputattach
  msg "Installing X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  make DESTDIR="${pkgdir}" install
  msg "Installing udev rules..."
  install -D -m644 "${srcdir}/10-wacom.rules" "${pkgdir}/etc/udev/rules.d/10-wacom.rules"
}

