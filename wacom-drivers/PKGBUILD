# Contributor: Ilya Galushko <helloy-ilya@yandex.ru>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com}
# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=wacom-drivers
pkgver=0.11.1
_driverver=0.11.1
pkgrel=3
pkgdesc="Kernel and X.Org drivers for Wacom tablets, with latest patches"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('xorg-server>=1.7.0' 'libxi' 'kernel26>=2.6.26')
makedepends=('xorg-server-devel' 'libxext' 'kernel26-headers')
options=('!libtool')
conflicts=('xf86-input-wacom' 'xf86-input-wacom-git' 'linuxwacom-cvs'
	   'input-wacom' 'inpput-wacom-cvs' 'linuxwacom-baboo-cth-ctl')
replaces=('linuxwacom' 'linuxwacom-dev' 'linuxwacom-cvs' 'linuxwacom-bamboo-cth-ctl')
provides=("xf86-input-wacom=$pkgver" "input-wacom=$_driverver")
install=wacom-drivers.install
source=(http://prdownloads.sourceforge.net/linuxwacom/input-wacom-${_driverver}.tar.bz2
        http://prdownloads.sourceforge.net/linuxwacom/xf86-input-wacom-${pkgver}.tar.bz2
        10-wacom.rules)
md5sums=('4c297d99f5be2264aa10027d1ba76adf'
         '0aec4a338cc583ed497b6af68d6d80ab'
         'b1949a1985afcb594260d1885ee4832c')

build() {
  cd "${srcdir}"/input-wacom-${_driverver}
  msg "Checking kernel version..."
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 37
  then
    msg "Found kernel newer than 2.6.36, using default wacom kernel module"
  else
    msg "Found kernel older than 2.6.37, building wacom kernel module..."
    ./configure --prefix=/usr
    make
  fi
  msg "Building /dev/input/event support for serial devices..."
  cd inputattach
  gcc inputattach.c -o inputattach
  msg "Building X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  ./configure --prefix=/usr  --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd "${srcdir}"/input-wacom-${_driverver}
  msg "Checking kernel version..."
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 37
  then
    msg "Found kernel newer than 2.6.36, using default wacom kernel module"
  else
    msg "Found kernel older than 2.6.37, installing wacom kernel module..."
    if test $_ver -ge 36
    then
      _ver=2.6.36
    else
      _ver=2.6.30
    fi
    cd $_ver
    install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
    install -D -m 644 wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
    cd ..
  fi
  msg "Installing /dev/input/event support for serial devices..."
  cd inputattach
  install -D -m 755 inputattach "${pkgdir}"/usr/bin/inputattach
  msg "Installing X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  make DESTDIR="${pkgdir}" install
  msg "Installing udev rules..."
  install -D -m644 "${srcdir}/10-wacom.rules" "${pkgdir}/etc/udev/rules.d/10-wacom.rules"
}

