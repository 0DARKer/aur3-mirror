# Contributor: Ilya Galushko <helloy-ilya@yandex.ru>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com}
# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=wacom-drivers
pkgver=0.11.1
_driverver=0.11.1
_kernelver=2.6.39.3
pkgrel=7
pkgdesc="Kernel and X.Org drivers for Wacom tablets, with latest patches"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('xorg-server>=1.7.0' 'libxi' 'kernel26>=2.6.26')
makedepends=('xorg-server-devel' 'libxext' 'kernel26-headers')
options=('!libtool')
conflicts=('xf86-input-wacom' 'xf86-input-wacom-git' 'linuxwacom-cvs'
	   'input-wacom' 'inpput-wacom-cvs' 'linuxwacom-baboo-cth-ctl')
replaces=('linuxwacom' 'linuxwacom-dev' 'linuxwacom-cvs' 'linuxwacom-bamboo-cth-ctl')
provides=("xf86-input-wacom=$pkgver" "input-wacom=$_driverver")
install=wacom-drivers.install
source=(http://prdownloads.sourceforge.net/linuxwacom/input-wacom-${_driverver}.tar.bz2
        http://prdownloads.sourceforge.net/linuxwacom/xf86-input-wacom-${pkgver}.tar.bz2
        cintiq21ux2.patch dtu2231.patch bamboopen.patch bamboo1.patch tool_finger.patch
        strip_orientation-x.patch bamboo1-x.patch cintiqv5-x.patch default_touch-x.patch
	bamboo_graphire_buttons.patch bamboo_graphire_serial.patch old_bamboo.patch
	twinview1-x.patch twinview2-x.patch twinview3-x.patch twinview4-x.patch
        10-wacom.rules)
_kernel_patches=(cintiq21ux2 dtu2231 bamboopen bamboo1 tool_finger
                 bamboo_graphire_buttons bamboo_graphire_serial old_bamboo)
_x_patches=(strip_orientation cintiqv5 bamboo1 default_touch
            twinview1 twinview2 twinview3 twinview4)
md5sums=('4c297d99f5be2264aa10027d1ba76adf'
         '0aec4a338cc583ed497b6af68d6d80ab'
         '9c077386458689640119f8ef6bb6f007'
         '54bef78dd7e5c8a751f3fa1b13834547'
         '38f3e17f68101ba4fddcbade9e6ab680'
         '073c0fcf6ffd62bbd28c8e85b5db901c'
         'b31a7b66d596c3ae138e3d1e76cdf5dd'
         '3afce0aa30bed6b0082e65f6323ac877'
         '5ca137f51f1eee69a138488a369e39bb'
         '1b6e5d7c72f26d97bbf558fba76868c1'
         '891d47d23f176a2cbbd21df24eaa8ad3'
         '3862742cf991af9392729df610cb00fc'
         '35d9afa4dff035141d0c82bf710b9e3f'
         '3daca1e171e718d5da958f0df5995c19'
         '05208180b92ec66680231b5c2522e18a'
         'c0ab04cb54c83907929f46fe2946737a'
         '281efdad8ddbcf11ee97d31f69dcdc9b'
         '02c9c3873b12941a9116952868c38d88'
         'c10f7ef6efa6663fd9c355f841298662')

_obtain() {
  msg "Downloading $1/$2 from kernel ${_kernelver} sources..."
  _kernelrel=`echo ${_kernelver} | sed 's/\([0-9]*\.[0-9]*\.[0-9]*\).*/\1.y/'`
  _url="http://git.kernel.org/?p=linux/kernel/git/stable/linux-${_kernelrel}.git"
  wget "${_url};a=blob_plain;f=$1/$2;hb=refs/tags/v${_kernelver}" -O $2
}

build() {
  cd "${srcdir}"/input-wacom-${_driverver}
  msg "Checking kernel version..."
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 37
  then
    msg "Found kernel newer than 2.6.36..."
    if [[ $_kernel_patches ]]
    then
      _kernelprepared=`echo ${_kernelver} | sed 's/\([0-9]*\.[0-9]*\.[0-9]*\).*/\1.y/'`
      _kernelinstalled=`echo ${_kernelver} | sed 's/\([0-9]*\.[0-9]*\.[0-9]*\).*/\1.y/'`
      if [[ $_kernelprepared == $_kernelinstalled ]]
      then
        rm -rf fresh
        mkdir fresh
        cd fresh
        _obtain drivers/input/tablet wacom.h
        _obtain drivers/input/tablet wacom_sys.c
        _obtain drivers/input/tablet wacom_wac.c
        _obtain drivers/input/tablet wacom_wac.h
        _obtain drivers/input/touchscreen wacom_w8001.c
        echo "wacom-objs := wacom_sys.o wacom_wac.o" > Makefile
        echo "obj-m += wacom.o" >> Makefile
        echo "obj-m += wacom_w8001.o" >> Makefile
        for p in ${_kernel_patches[@]}
        do
          msg "Applying kernel patch $p..."
          patch -p4 < ../../$p.patch
        done
        make -C /lib/modules/$(uname -r)/build SUBDIRS="$(pwd)" modules
        cd ..
      else
        msg "Patches were prepared for kernel ${_kernelprepared}, but ${_kernelinstalled} found - cannot patch"
      fi
    else
      msg "No need to patch, using default wacom kernel module"
    fi
  else
    msg "Found kernel older than 2.6.37, building wacom kernel module..."
    ./configure --prefix=/usr
    make
  fi
  msg "Building /dev/input/event support for serial devices..."
  cd inputattach
  gcc inputattach.c -o inputattach
  msg "Building X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  if [[ $_x_patches ]]
  then
    for p in ${_x_patches[@]}
    do
      msg "Applying X patch $p..."
      patch -p1 < ../$p-x.patch
    done
    autoreconf -ivf
  fi
  ./configure --prefix=/usr  --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd "${srcdir}"/input-wacom-${_driverver}
  msg "Checking kernel version..."
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 37
  then
    msg "Found kernel newer than 2.6.36..."
    if [[ $_kernel_patches ]]
    then
      _kernelprepared=`echo ${_kernelver} | sed 's/\([0-9]*\.[0-9]*\.[0-9]*\).*/\1.y/'`
      _kernelinstalled=`echo ${_kernelver} | sed 's/\([0-9]*\.[0-9]*\.[0-9]*\).*/\1.y/'`
      if [[ $_kernelprepared == $_kernelinstalled ]]
      then
        msg "Found patched driver, installing wacom kernel module..."
        cd fresh
        install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
        install -D -m 644 wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
        cd ..   
      fi
    else
      msg "No patched driver found, using default wacom kernel module"
    fi
  else
    msg "Found kernel older than 2.6.37, installing wacom kernel module..."
    if test $_ver -ge 36
    then
      _ver=2.6.36
    else
      _ver=2.6.30
    fi
    cd $_ver
    install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
    install -D -m 644 wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
    cd ..
  fi
  msg "Installing /dev/input/event support for serial devices..."
  cd inputattach
  install -D -m 755 inputattach "${pkgdir}"/usr/bin/inputattach
  msg "Installing X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  make DESTDIR="${pkgdir}" install
  msg "Installing udev rules..."
  install -D -m644 "${srcdir}/10-wacom.rules" "${pkgdir}/etc/udev/rules.d/10-wacom.rules"
}

