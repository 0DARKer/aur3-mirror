# Contributor: Ilya Galushko <helloy-ilya@yandex.ru>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com}
# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=wacom-drivers
pkgver=0.11.1
_driverver=0.11.1
pkgrel=12
pkgdesc="Kernel and X.Org drivers for Wacom tablets, with latest patches"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('xorg-server>=1.7.0' 'libxi' 'linux>=3.0' 'linux<3.1')
makedepends=('xorg-server-devel' 'libxext' 'linux-headers')
options=('!libtool')
conflicts=('xf86-input-wacom' 'xf86-input-wacom-git' 'linuxwacom-cvs'
	   'input-wacom' 'inpput-wacom-cvs' 'linuxwacom-baboo-cth-ctl')
replaces=('linuxwacom' 'linuxwacom-dev' 'linuxwacom-cvs' 'linuxwacom-bamboo-cth-ctl')
provides=("xf86-input-wacom=$pkgver" "input-wacom=$_driverver")
install=wacom-drivers.install
source=(http://prdownloads.sourceforge.net/linuxwacom/input-wacom-${_driverver}.tar.bz2
        http://prdownloads.sourceforge.net/linuxwacom/xf86-input-wacom-${pkgver}.tar.bz2
        https://raw.github.com/torvalds/linux/v3.0/drivers/input/tablet/wacom.h
        https://raw.github.com/torvalds/linux/v3.0/drivers/input/tablet/wacom_sys.c
        https://raw.github.com/torvalds/linux/v3.0/drivers/input/tablet/wacom_wac.c
        https://raw.github.com/torvalds/linux/v3.0/drivers/input/tablet/wacom_wac.h
        https://raw.github.com/torvalds/linux/v3.0/drivers/input/touchscreen/wacom_w8001.c
        cintiq21ux2.patch dtu2231.patch bamboopen.patch bamboo1.patch tool_finger.patch
        strip_orientation-x.patch bamboo1-x.patch cintiqv5-x.patch default_touch-x.patch
	bamboo_graphire_buttons.patch bamboo_graphire_serial.patch old_bamboo.patch
	twinview1-x.patch twinview2-x.patch twinview3-x.patch twinview4-x.patch
        inputattach-sync.patch inputattach-baud.patch bamboo-ctl-660-k.patch
        wac_msg_retries.patch report_id_4.patch w8001_open_close.patch
        w8001_simplify_remove.patch intuos4_oled.patch gesture_condition-x.patch
        scroll_time-x.patch 2-finger1-x.patch 2-finger2-x.patch gesture-none-mode-x.patch
        1-finger-tap-x.patch touch-during-gesture-x.patch maptooutput-zero-x.patch
        propflaginverted-x.patch
        10-wacom.rules)
_kernel_patches=(cintiq21ux2 dtu2231 bamboopen bamboo1 tool_finger bamboo_graphire_buttons
                 bamboo_graphire_serial old_bamboo bamboo-ctl-660-k wac_msg_retries
                 report_id_4 w8001_open_close w8001_simplify_remove intuos4_oled)
_x_patches=(strip_orientation cintiqv5 bamboo1 default_touch twinview1 twinview2
            twinview3 twinview4 gesture_condition scroll_time 2-finger1 2-finger2
            gesture-none-mode 1-finger-tap touch-during-gesture maptooutput-zero
            propflaginverted)
md5sums=('4c297d99f5be2264aa10027d1ba76adf'
         '0aec4a338cc583ed497b6af68d6d80ab'
         '7bb4f9be7347d065311e3e8941504fcc'
         'f29ab70f3c6b5dea7d509f3b5238cecb'
         '1bca9b8db83c6e1fa15ba6eb8337f42f'
         '8138111af31c2c6ad05198b87057a186'
         '709d52bc9c018cb15dcd14e955dffbb6'
         '9c077386458689640119f8ef6bb6f007'
         '54bef78dd7e5c8a751f3fa1b13834547'
         '38f3e17f68101ba4fddcbade9e6ab680'
         '073c0fcf6ffd62bbd28c8e85b5db901c'
         'b31a7b66d596c3ae138e3d1e76cdf5dd'
         '3afce0aa30bed6b0082e65f6323ac877'
         '5ca137f51f1eee69a138488a369e39bb'
         '1b6e5d7c72f26d97bbf558fba76868c1'
         '891d47d23f176a2cbbd21df24eaa8ad3'
         '3862742cf991af9392729df610cb00fc'
         '35d9afa4dff035141d0c82bf710b9e3f'
         '3daca1e171e718d5da958f0df5995c19'
         '05208180b92ec66680231b5c2522e18a'
         'c0ab04cb54c83907929f46fe2946737a'
         '281efdad8ddbcf11ee97d31f69dcdc9b'
         '02c9c3873b12941a9116952868c38d88'
         'd18dc2ed35cd9db5b354d28c8be6f21a'
         '7b0373c3c288b1255606ddd5e15aeac2'
         'c46d578d9a4956d51a51aa64c79cf949'
         '84b11383851d7a7b04a119a396bb1d5e'
         '5292cbfb291ea0f1ac437b14a77c8330'
         'c173da67f8dd04ce766880c197cba716'
         'aed90713d83b0a99300730cf395da596'
         '2ed53764cb44db4418f242aca247012c'
         'f0604abc4167dc06206e2fd0637af5d4'
         'c9320e8624f694537157bef98f04a560'
         'aa6beec770687e7683e7edd940337c79'
         'eb3a060986adbdf381d38f6e15d5f725'
         'c699d007199a0d6f466702d02ec66f26'
         'd0a5b060efd25b3906bf7d7513c02df5'
         '9c71df911b9e896c296787b655e41c09'
         'd1d4a01c878a3d4815ec3bf62c1bd26e'
         '544b39114eef8f1ef0b331514865ef65'
         'c10f7ef6efa6663fd9c355f841298662')

build() {
  cd "${srcdir}"/input-wacom-${_driverver}
  msg "Checking kernel version..."
  _ver=`uname -r | sed 's/\([0-9]*\.[0-9]*\).*/\1/'`
  if [[ $_kernel_patches ]]
  then
    rm -rf fresh
    mkdir fresh
    cd fresh
    cp "$srcdir"/{wacom.h,wacom_sys.c,wacom_wac.c,wacom_wac.h,wacom_w8001.c} .
    echo "wacom-objs := wacom_sys.o wacom_wac.o" > Makefile
    echo "obj-m += wacom.o" >> Makefile
    echo "obj-m += wacom_w8001.o" >> Makefile
    for p in ${_kernel_patches[@]}
    do
      msg "Applying kernel patch $p..."
      patch -p4 < ../../$p.patch
    done
    make -C /lib/modules/$(uname -r)/build SUBDIRS="$(pwd)" modules
    cd ..
  else
    msg "No need to patch, using default wacom kernel module"
  fi
  msg "Building /dev/input/event support for serial devices..."
  patch -p1 < ../inputattach-sync.patch
  patch -p1 < ../inputattach-baud.patch
  cd inputattach
  gcc inputattach.c -o inputattach
  msg "Building X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  if [[ $_x_patches ]]
  then
    for p in ${_x_patches[@]}
    do
      msg "Applying X patch $p..."
      patch -p1 < ../$p-x.patch
    done
    autoreconf -ivf
  fi
  ./configure --prefix=/usr  --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd "${srcdir}"/input-wacom-${_driverver}
  msg "Checking kernel version..."
  _ver=`uname -r | sed 's/\([0-9]*\.[0-9]*\).*/\1/'`
  if [[ $_kernel_patches ]]
  then
    msg "Found patched driver, installing wacom kernel module..."
    cd fresh
    install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
    install -D -m 644 wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
    cd ..   
  else
    msg "No patched driver found, using default wacom kernel module"
  fi
  msg "Installing /dev/input/event support for serial devices..."
  cd inputattach
  install -D -m 755 inputattach "${pkgdir}"/usr/bin/inputattach
  msg "Installing X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  make DESTDIR="${pkgdir}" install
  msg "Installing udev rules..."
  install -D -m644 "${srcdir}/10-wacom.rules" "${pkgdir}/etc/udev/rules.d/10-wacom.rules"
}

