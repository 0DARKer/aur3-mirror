# Contributor: Ilya Galushko <helloy-ilya@yandex.ru>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com}
# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=wacom-drivers
pkgver=0.12.0
_driverver=0.11.1
pkgrel=1
pkgdesc="Kernel and X.Org drivers for Wacom tablets, with latest patches"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('xorg-server>=1.7.0' 'libxi' 'linux>=3.1' 'linux<3.2')
makedepends=('xorg-server-devel' 'libxext' 'linux-headers')
options=('!libtool')
conflicts=('xf86-input-wacom' 'xf86-input-wacom-git' 'linuxwacom-cvs'
           'input-wacom' 'inpput-wacom-cvs' 'linuxwacom-baboo-cth-ctl')
replaces=('linuxwacom' 'linuxwacom-dev' 'linuxwacom-cvs' 'linuxwacom-bamboo-cth-ctl')
provides=("xf86-input-wacom=$pkgver" "input-wacom=$_driverver")
install=wacom-drivers.install
source=(http://prdownloads.sourceforge.net/linuxwacom/input-wacom-${_driverver}.tar.bz2
        http://prdownloads.sourceforge.net/linuxwacom/xf86-input-wacom-${pkgver}.tar.bz2
        https://raw.github.com/torvalds/linux/v3.1/drivers/input/tablet/wacom.h
        https://raw.github.com/torvalds/linux/v3.1/drivers/input/tablet/wacom_sys.c
        https://raw.github.com/torvalds/linux/v3.1/drivers/input/tablet/wacom_wac.c
        https://raw.github.com/torvalds/linux/v3.1/drivers/input/tablet/wacom_wac.h
        https://raw.github.com/torvalds/linux/v3.1/drivers/input/touchscreen/wacom_w8001.c
        intuos_oled_control.patch intuos_oled_inactive.patch
        lower_led_luminance.patch cintiq_21ux2_oled.patch
        oled_make_readable.patch bamboo_feature_report.patch
        bamboo_remove_pressure_initialization.patch parse_hid_comments.patch
        bamboo_add_abs_distance.patch bamboo_correct_y.patch
        bamboo_remove_hid_parsing.patch
        bamboo_3rd_gen_hid.patch  bamboo_3rd_gen_packet.patch
        bamboo_remove_unwanted_packets.patch bamboo_relax_stylus_check.patch
        wacom_w8001_open_and_close.patch  wacom_w8001_remove.patch
        inputattach-sync.patch inputattach-baud.patch
        10-wacom.rules)
_kernel_patches=(
        intuos_oled_control intuos_oled_inactive lower_led_luminance cintiq_21ux2_oled
        oled_make_readable bamboo_feature_report bamboo_remove_hid_parsing
        bamboo_remove_pressure_initialization bamboo_add_abs_distance
        bamboo_correct_y bamboo_relax_stylus_check parse_hid_comments
        bamboo_3rd_gen_hid bamboo_3rd_gen_packet
        bamboo_remove_unwanted_packets
        wacom_w8001_open_and_close wacom_w8001_remove)
_x_patches=()
md5sums=('4c297d99f5be2264aa10027d1ba76adf'
         'e1c41d143d5040982ae050c7ef9039e3'
         '7bb4f9be7347d065311e3e8941504fcc'
         '349e10d46fd747cce5d6b341df8b22b2'
         '687261696cee988238a599c2cfd6758f'
         '8138111af31c2c6ad05198b87057a186'
         '5f596fd24298ef99e165b5943b757fab'
         'fc1cd275de8c98439fd5f7a9518b352f'
         '0dcf847ffcc7c16475a32ad9d9c8e21e'
         '4229bc8effa60a5340364be1fd02c562'
         '3d93ea1e00963a4033932d121f251f23'
         '6b1e370a04ec21a5e1ef2517aad795b0'
         'df7afd81d0308c26f76dd9fc492c5e5a'
         'e945ff3d85758ef79de6a8fd8d7fb415'
         '648216bbe2f712993f7f8ea31d9f174b'
         '322585d5abc065b8edff770c3782684b'
         '42b9d620af739001f08dde06cebf64dd'
         'cfcec0a21c16d3bfe188bf56e0b56131'
         '7072850ac604c7bc480e6c83b5dddbd8'
         '6a32d1c43ad8fd5273095404d385f208'
         'd63cb8b7ab43860810164c1abc4d486a'
         '27c1b6ceedac98a5d0b0d209d4acda60'
         '0f45202bbb8f995c7506049f77d5e306'
         'a9a0f8037ed261ac46fe3bbb190e0fa5'
         'd18dc2ed35cd9db5b354d28c8be6f21a'
         '7b0373c3c288b1255606ddd5e15aeac2'
         'c10f7ef6efa6663fd9c355f841298662')

build() {
  cd "${srcdir}"/input-wacom-${_driverver}
  msg "Checking kernel version..."
  _ver=`uname -r | sed 's/\([0-9]*\.[0-9]*\).*/\1/'`
  if [[ $_kernel_patches ]]
  then
    rm -rf fresh
    mkdir fresh
    cd fresh
    cp "$srcdir"/{wacom.h,wacom_sys.c,wacom_wac.c,wacom_wac.h,wacom_w8001.c} .
    echo "wacom-objs := wacom_sys.o wacom_wac.o" > Makefile
    echo "obj-m += wacom.o" >> Makefile
    echo "obj-m += wacom_w8001.o" >> Makefile
    for p in ${_kernel_patches[@]}
    do
      msg "Applying kernel patch $p..."
      patch -p4 < ../../$p.patch
    done
    make -C /lib/modules/$(uname -r)/build SUBDIRS="$(pwd)" modules
    cd ..
  else
    msg "No need to patch, using default wacom kernel module"
  fi
  msg "Building /dev/input/event support for serial devices..."
  patch -p1 < ../inputattach-sync.patch
  patch -p1 < ../inputattach-baud.patch
  cd inputattach
  gcc inputattach.c -o inputattach
  msg "Building X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  if [[ $_x_patches ]]
  then
    for p in ${_x_patches[@]}
    do
      msg "Applying X patch $p..."
      patch -p1 < ../$p-x.patch
    done
    autoreconf -ivf
  fi
  ./configure --prefix=/usr  --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd "${srcdir}"/input-wacom-${_driverver}
  msg "Checking kernel version..."
  _ver=`uname -r | sed 's/\([0-9]*\.[0-9]*\).*/\1/'`
  if [[ $_kernel_patches ]]
  then
    msg "Found patched driver, installing wacom kernel module..."
    cd fresh
    install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
    install -D -m 644 wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
    cd ..   
  else
    msg "No patched driver found, using default wacom kernel module"
  fi
  msg "Installing /dev/input/event support for serial devices..."
  cd inputattach
  install -D -m 755 inputattach "${pkgdir}"/usr/bin/inputattach
  msg "Installing X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  make DESTDIR="${pkgdir}" install
  msg "Installing udev rules..."
  install -D -m644 "${srcdir}/10-wacom.rules" "${pkgdir}/etc/udev/rules.d/10-wacom.rules"
}

