# Maintainer: Antonio Rojas 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=libmm-qt5
pkgver=5.0.91
_plasmaver=5.0.0
pkgrel=1
pkgdesc='Qt-only wrapper for ModemManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libnm-qt'
license=('LGPL')
depends=('modemmanager' 'qt5-base')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/stable/plasma/$_plasmaver/libmm-qt-$pkgver.tar.xz")
md5sums=('a13d153d149e7f4cfd6df0090d8145f6')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libmm-qt-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
