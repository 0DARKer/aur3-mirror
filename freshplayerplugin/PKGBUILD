# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: intelfx <intelfx100 [at] gmail [dot] com>
# Contributor: Behem0th <grantipak@gmail.com>

pkgname=freshplayerplugin
pkgver=0.2.0
pkgrel=1
pkgdesc='PPAPI-host NPAPI-plugin adapter.'
arch=('i686' 'x86_64')
url='https://github.com/i-rinat/freshplayerplugin'
license=('MIT')
depends=('chromium-pepper-flash' 'pango' 'alsa-lib' 'freetype2' 'uriparser')
makedepends=('cmake')
provides=('freshplayerplugin-git')
conflicts=('freshplayerplugin-git')
source=($url/archive/v$pkgver.tar.gz)
md5sums=('2f8e484d8fc00049518a84c84c11330e')

prepare() {
  cd $pkgname-$pkgver/data
  sed -r -i 's|^(pepperflash_path).*|\1 = "/usr/lib/PepperFlash/libpepflashplayer.so"|' \
    freshwrapper.conf.example
}

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 LICENSE.MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  for lib in libfreshwrapper-*.so; do
    install -Dm755 $lib "$pkgdir"/usr/lib/mozilla/plugins/$lib
  done

  install -Dm644 data/freshwrapper.conf.example \
    "$pkgdir"/usr/share/$pkgname/freshwrapper.conf.example
}
