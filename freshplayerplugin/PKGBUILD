# Maintainer: Jack L. Frost <fbt@fleshless.org>
# vim: expandtab sts=2
# Contributor: Corelli <corelli AT sent DOT com>
# Contributor: BartÅ‚omiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: intelfx <intelfx100 [at] gmail [dot] com>
# Contributor: Behem0th <grantipak@gmail.com> 

# Set FP_USE_CHROME=1 to configure freshplayer to use
# Google Chrome's flash plugin instead of the pepper flash package.

pkgname=freshplayerplugin
pkgver=0.2.3
pkgrel=1
pkgdesc='PPAPI-host NPAPI-plugin adapter.'
arch=('i686' 'x86_64')
url='https://github.com/i-rinat/freshplayerplugin'
license=('MIT')
depends=( 'pango' 'alsa-lib' 'freetype2' 'libconfig' 'libevent' 'gtk2' 'libgl')
makedepends=('cmake' 'ragel')
conflicts=('freshplayerplugin-git')
source=($url/archive/v$pkgver.tar.gz)

# Set the flash package we depend on appropriately.
if (( FP_USE_CHROME )); then
  depends+=( 'google-chrome' )
else
  depends+=( 'chromium-pepper-flash' )
fi

prepare() {
  cd $pkgname-$pkgver/data
  sed -r -i 's|^(pepperflash_path).*|\1 = "/usr/lib/PepperFlash/libpepflashplayer.so"|' \
    freshwrapper.conf.example
}

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  install -d "$pkgdir/usr/lib/mozilla/plugins"
  install -m644 libfreshwrapper-*.so "$pkgdir/usr/lib/mozilla/plugins"
  install -Dm644 LICENSE.MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 data/freshwrapper.conf.example "$pkgdir/etc/freshwrapper.conf"

  if (( FP_USE_CHROME )); then
    # configure path to libpepflashplayer.so
    sed -r -e 's|^(pepperflash_path).*|\1 = "/usr/lib/PepperFlash/libpepflashplayer.so"|' \
      -i "$pkgdir/etc/freshwrapper.conf"
  fi
}

sha1sums=('db44b49fbcca6875328ec18802041bf7cd8c1570')
