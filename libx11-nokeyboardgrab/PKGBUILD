# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Patch by Procyon

pkgname=libx11-nokeyboardgrab
pkgver=1.4.99.901
pkgrel=1
pkgdesc="X11 client-side library with no keyboard grab patch"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb' 'xproto' 'kbproto')
makedepends=('xorg-util-macros' 'xextproto' 'xtrans' 'inputproto')
provides=(libx11=$pkgver)
conflicts=(libx11)
options=('!libtool')
license=('custom')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
        xorg.sh
        xorg.csh)
sha1sums=('af10b064c1c64ee9e7daae0a7f1f9719eb7e965a'
          '4b756387426afb2a5bb7a155631ec42348abf5c1'
          '45b0b310776cf5d9a94b4a18a5c63ddad9de6ba8')

build() {
  cd "${srcdir}/libX11-${pkgver}"

  #PATCH KEYBOARD GRAB PREVENTION
  sed -i '/xGrabKeyboardReply/i return (GrabSuccess);' src/GrKeybd.c

  #Mouse grab prevention. Doesn't work with Urban Terror.
#  sed -i '/xGrabPointerReply/i return (GrabSuccess);' src/GrPointer.c

  ./configure --prefix=/usr --disable-static --disable-xf86bigfont
  make
  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/"xorg.{sh,csh} "${pkgdir}/etc/profile.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
