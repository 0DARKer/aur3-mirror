# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=gtksourceview3-git
pkgver=3.8.0.20.gf50fac5
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME - git version"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/gtksourceview/development.html"
license=('GPL')
depends=('gtk3-git')
makedepends=('git' 'intltool' 'gobject-introspection-git' 'glade' 
'gnome-common-git' 'gtk-doc')
provides=('gtksourceview3')
conflicts=('gtksourceview3')
options=('!libtool')
source=('gtksourceview3::git://git.gnome.org/gtksourceview' 'gnome-autogen.sh')
md5sums=('SKIP' '4923cb46e02043ec7bb6c7327e2dae03')
_gitname="gtksourceview3"

pkgver() {
  cd $_gitname
  # Git tag
  echo $(git describe --always|sed 's/-/./g')
}

build() {
  cp $srcdir/gnome-autogen.sh "$srcdir/$_gitname"
  cd "$srcdir/$_gitname"
  ./gnome-autogen.sh  --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-glade-catalog
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
}
