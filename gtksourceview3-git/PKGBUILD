# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=gtksourceview3
pkgname=$_pkgname-git
_pkgbasename=gtksourceview
pkgver=3.11.3.1.g1138576
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=("gtk3>=3.11.0" 'libxml2' "glib2>=2.37.3")
makedepends=('intltool' 'gobject-introspection' 'glade' 'gtk-doc')
options=('!libtool')
url="http://www.gnome.org"
conflicts=('gtksourceview3')
replaces=('gtksourceview3')
provides=("gtksourceview3=3.11.4")
source=('git://git.gnome.org/gtksourceview')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgbasename"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgbasename"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-glade-catalog --enable-gtk-doc
  make
}

package() {
  cd "$srcdir/$_pkgbasename"
  make DESTDIR="$pkgdir" install
}
