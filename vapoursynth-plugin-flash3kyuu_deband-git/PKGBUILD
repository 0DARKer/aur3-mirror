# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_unittest=0 # use 1 if you want make/run unittest, this unittest takes over 20min

_plug=flash3kyuu_deband
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=1.5.1.135.g8060684
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/SAPikachu/${_plug}"
license=('GPL3')
depends=('vapoursynth')
makedepends=('git')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/SAPikachu/${_plug}.git")
sha1sums=('SKIP')
_gitname="${_plug}"

if [ "${_unittest}" = "1" ]; then
  _unittest_flag="--enable-test"
elif [ "${_unittest}" = "0" ]; then
  BUILDENV+=('!check')
fi

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  cd "${_gitname}"
  if [ "${_unittest}" = "1" ] ; then 
    git submodule update --init --recursive
  else
    true
  fi
}

build() {
  cd "${_gitname}"
  ./waf --prefix=/usr --libdir=/usr/lib/vapoursynth ${_unittest_flag} configure build
}

check() {
  cd "${_gitname}"
  build/test/f3kdb-test
}

package(){
  cd "${_gitname}"
  ./waf install --no-ldconfig --destdir="${pkgdir}"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
  install -Dm644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
