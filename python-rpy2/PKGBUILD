# Maintainer: Michael Schubert <mschu.dev at gmail>
# Contributor: David Scholl <djscholl@gmail.com> 
# Contributor: David Pretty <david.pretty@gmail.com>

pkgname=python-rpy2
pkgver=2.2.2
pkgrel=1
pkgdesc="A simple, robust Python interface to the R Programming Language."
arch=('i686' 'x86_64')
url="http://rpy.sourceforge.net/"
license=('MPL' 'GPL' 'LGPL')
depends=('python-numpy' 'r')
source=("http://downloads.sourceforge.net/sourceforge/rpy/rpy2-$pkgver.tar.gz")
md5sums=('7690d8fa747e62cd27cfacfa45f4a07b')

build() {
  cd "$srcdir/rpy2-$pkgver"
  sed -i "s:Rlapack:lapack:" setup.py
  sed -i "s:os.path.join(RHOME.strip(), 'include'):'/usr/include/R':" setup.py
}

package() {
  cd "$srcdir/rpy2-$pkgver"
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  mkdir -p "$pkgdir/etc/ld.so.conf.d"
  if [ "$CARCH" = "x86_64" ]; then
    echo /usr/lib64/R/lib > "$pkgdir/etc/ld.so.conf.d/rpy_x86_64.conf"
  else
    echo /usr/lib/R/lib > "$pkgdir/etc/ld.so.conf.d/rpy_i686.conf"
  fi
}

