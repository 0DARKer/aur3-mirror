# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>

pkgname=ruby-textutils
pkgver=1.0.0
pkgrel=1
pkgdesc="Text Filters and Helpers"
arch=(any)
url="https://github.com/rubylibs/textutils"
license=('custom')
depends=('ruby' 'ruby-activesupport' 'ruby-logutils' 'ruby-props' 'ruby-rubyzip')
source=("http://gems.rubyforge.org/gems/textutils-${pkgver}.gem"
        'LICENSE')
noextract=("textutils-${pkgver}.gem")
sha256sums=('242839929c81c3c594c6d62a3da57231e1d9d676261573892decc776ec7d141b'
            '827a633f1202ca9dd4dd6da61c1485b558eb6298def4da6979d0f9e3c9fbe643')

package() {
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies \
    -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" "textutils-${pkgver}.gem"

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
