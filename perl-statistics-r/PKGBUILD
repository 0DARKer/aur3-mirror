# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=perl-statistics-r
_pkgname='Statistics-R'
pkgver=0.27
pkgrel=1
pkgdesc="Statistics::R - Perl interface to the R statistical program"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/${_pkgname}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'r' 'perl-regexp-common' 'perl-ipc-run')
makedepends=('perl-test-pod')
source=("http://search.cpan.org/CPAN/authors/id/F/FA/FANGLY/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('f14a119fb3d9211b43602e50b032e821da30ad0a4de663f9960a91a6d3620c1188faadc0d1dd72de368cd6af83d2d7460e893ece410b7f89e8df612b1b05fe11')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
