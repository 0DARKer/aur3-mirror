# Maintainer: Jason St. John <jstjohn .. purdue . edu>

_perlmod=Statistics-R
_modnamespace=Statistics
pkgname=perl-statistics-r
pkgver=0.32
pkgrel=2
pkgdesc="Statistics::R - Perl interface to the R statistical program"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('GPL' 'PerlArtistic')
depends=('perl-ipc-run' 'perl-regexp-common' 'perl-text-balanced>=1.97' 'r')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('15e4dedebfcd90bfb137e6635917387197c10963b76d2be608e1b36dc08cd4e158bc371acded59ba53d261d8a55c8ed79693d4862239df4f31fa130971c6da45')

build() {
	cd "${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}"
}
