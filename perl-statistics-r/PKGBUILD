# Maintainer: Jason St. John <jstjohn .. purdue . edu>

_perlmod=Statistics-R
_modnamespace=Statistics
pkgname=perl-statistics-r
pkgver=0.31
pkgrel=2
pkgdesc="Statistics::R - Perl interface to the R statistical program"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
depends=('perl-ipc-run' 'perl-regexp-common' 'perl-text-balanced>=1.97' 'r')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('9b4e45a9402e0741333fae4226f6fc88cfa18227a907c4fa988bd425a2182497c2cb72b4f69a8af1750294bc42b28f53f2957819d1be4436ea5e64988e23345e')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	make test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	make install DESTDIR="$pkgdir/"
}
