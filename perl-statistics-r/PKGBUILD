# Maintainer: Jason St. John <jstjohn .. purdue . edu>

_perlmod=Statistics-R
_modnamespace=Statistics
pkgname=perl-statistics-r
pkgver=0.30
pkgrel=1
pkgdesc="Statistics::R - Perl interface to the R statistical program"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'r' 'perl-regexp-common' 'perl-ipc-run')
makedepends=('perl-test-pod')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('13cad5dadaf9f23a90d023512ff50361076ff115bde7574cb4b1633aaf4bed7ce83fbed50df1d9b1d1c24d8b3495b222c794080f4fcca27158fa4f1bfad33f80')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	make test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	make install DESTDIR="$pkgdir/"
}
