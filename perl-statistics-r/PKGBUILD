# Maintainer: Jason St. John <jstjohn .. purdue . edu>

_perlmod=Statistics-R
_modnamespace=Statistics
pkgname=perl-statistics-r
pkgver=0.29
pkgrel=1
pkgdesc="Statistics::R - Perl interface to the R statistical program"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'r' 'perl-regexp-common' 'perl-ipc-run')
makedepends=('perl-test-pod')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('0c29d95404f8c3a62ed6cdfbb059fec77ea213b94a5f696a560f12f52ede97db20046694aa91198358c429b857b0b64234f6e192c1f78a9ed330b6994a5010cf')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
