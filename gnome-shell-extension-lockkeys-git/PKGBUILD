# Maintainer: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>
# Contributor: mareex <marcus dot behrendt dot 86 at gmail dot com>

pkgname=gnome-shell-extension-lockkeys-git
pkgver=64
pkgrel=4
pkgdesc="A gnome shell numlock & capslock state indicator extension"
arch=('any')
url="https://github.com/kazysmaster/gnome-shell-extension-lockkeys"
license=('GPL')
depends=('gnome-shell' 'hicolor-icon-theme')
makedepends=('git')
provides=("${pkgname%-*}")
conflicts=("${pkgname%-*}")
install=${pkgname%-*}.install
source=("${pkgname%-*}::git+https://github.com/kazysmaster/gnome-shell-extension-lockkeys.git")
sha512sums=('SKIP')

pkgver() {
    cd ${pkgname%-*}
    git rev-list --count HEAD
}

prepare() {
    sed -i 's/"3.4", "3.6", "3.8", "3.10"/"3.4", "3.6", "3.8", "3.10", "3.12"/g' "${srcdir}"/gnome-shell-extension-lockkeys/lockkeys@vaina.lt/metadata.json
}


package() {
    cd ${pkgname%-*}
    for d in $(find ./lockkeys@vaina.lt -type d)
    do
        mkdir -p "${pkgdir}/usr/share/gnome-shell/extensions/$d"
    done

    for f in $(find ./lockkeys@vaina.lt -type f)
    do
        install -m0644 $f "${pkgdir}/usr/share/gnome-shell/extensions/$f"
    done
}
