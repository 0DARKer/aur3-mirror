# Maintainer: Moritz Bunkus <moritz@bunkus.org>
pkgname='perl-moo'
pkgver='1.004000'
pkgrel='1'
pkgdesc="Minimalist Object Orientation (with Moose compatiblity)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers>=1.1' 'perl-devel-globaldestruction>=0.11' 'perl-dist-checkconflicts>=0.02' 'perl-import-into>=1.002' 'perl-module-runtime>=0.012' 'perl-role-tiny>=1.003002' 'perl-strictures>=1.004003')
makedepends=('perl-test-fatal>=-0.003')
url='https://metacpan.org/release/Moo'
source=("http://search.cpan.org/CPAN/authors/id/H/HA/HAARG/Moo-${pkgver}.tar.gz")
md5sums=('c578ab11f40e1215f4243b300724b950')
sha512sums=('aab64eff65ac8420f04fdc2379ea07890f73188a23327e094c8be4c8f319e845806444a7ff9e91219a8335d71725d7faf4e2ba09dddaf276b29b7de7a6e2547c')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Moo-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
