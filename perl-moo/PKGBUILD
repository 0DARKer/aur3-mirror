# Maintainer: Moritz Bunkus <moritz@bunkus.org>
pkgname='perl-moo'
pkgver='1.001000'
pkgrel='1'
pkgdesc="Minimalist Object Orientation (with Moose compatiblity)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers' 'perl-devel-globaldestruction' 'perl-module-runtime' 'perl-role-tiny' 'perl-strictures')
makedepends=('perl-test-fatal')
url='https://metacpan.org/release/Moo'
source=("http://cpan.metacpan.org/authors/id/M/MS/MSTROUT/Moo-${pkgver}.tar.gz")
md5sums=('7448b2389c860abd91fe7b5cdcf383cf')
sha512sums=('55ac8c657245b067f5783e7fc24d2b1db07a98294831ee8ab4f9e1e942eac457ea999ca58a2f3b96c35dff3e1d0e67622d8498483439083c4ec4802a00c6bcb5')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Moo-${pkgver}"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/Moo-${pkgver}"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "${srcdir}/Moo-${pkgver}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
