# Maintainer: Moritz Bunkus <moritz@bunkus.org>
pkgname='perl-moo'
pkgver='1.004002'
pkgrel='1'
pkgdesc="Minimalist Object Orientation (with Moose compatiblity)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers>=1.1' 'perl-devel-globaldestruction>=0.11' 'perl-dist-checkconflicts>=0.02' 'perl-import-into>=1.002' 'perl-module-runtime>=0.012' 'perl-role-tiny>=1.003002' 'perl-strictures>=1.004003')
makedepends=('perl-test-fatal>=-0.003')
url='https://metacpan.org/release/Moo'
source=("http://search.cpan.org/CPAN/authors/id/H/HA/HAARG/Moo-${pkgver}.tar.gz")
md5sums=('9eea0ee548baa8f1b6c42e5753e6290f')
sha512sums=('b6a179cb3c6adc80e51c542eccbac4d6e781bd0de927db7cdad75502cc6d9527d6eebeabd9ede6c40d581ad8bcf435435151b953098f8c4e8a7fc8716b7f842d')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Moo-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
