# Maintainer: Christian Sturm <reezer@reezer.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname='perl-moo'
pkgver='0.091004'
pkgrel='1'
pkgdesc="Minimalist Object Orientation (with Moose compatiblity)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers>=1.07' 'perl-devel-globaldestruction' 'perl-role-tiny' 'perl-strictures>=1.001001')
makedepends=('perl-test-fatal>=0.003')
url='https://metacpan.org/release/Moo'
source=("http://cpan.metacpan.org/authors/id/M/MS/MSTROUT/Moo-${pkgver}.tar.gz")
md5sums=('7a7d31754f3e8e576326ca6dfdb75f25')
sha512sums=('a312561cca8f00be9e2cc708cf61c6e8eb8146dd24dd1dd9aa3e1b1a88b4aeb9ed0311f9585c13a95faac4b60e67a1a756a8f665672b0c4c46188c5ed977725a')
_distdir="${srcdir}/Moo-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$_distdir"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
