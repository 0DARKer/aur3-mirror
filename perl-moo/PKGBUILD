# Maintainer: Moritz Bunkus <moritz@bunkus.org>
pkgname='perl-moo'
pkgver='1.004005'
pkgrel='1'
pkgdesc="Minimalist Object Orientation (with Moose compatiblity)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers>=1.1' 'perl-devel-globaldestruction>=0.11' 'perl-import-into>=1.002' 'perl-module-runtime>=0.012' 'perl-role-tiny>=1.003003' 'perl-strictures>=1.004003')
makedepends=('perl-test-fatal>=-0.003')
url='https://metacpan.org/release/Moo'
source=("http://cpan.metacpan.org/authors/id/H/HA/HAARG/Moo-${pkgver}.tar.gz")
md5sums=('02608148ddc570944acf668b8ac92a88')
sha512sums=('db4f4e08b3ba90eb8b46eb820240504e4fa0d1508cf2848d96b173168304f16735fd202e23fec80a15c990d03ee6cfbad7f4bb4dff39cfae71f64ac86032b140')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Moo-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
