# Maintainer: Moritz Bunkus <moritz@bunkus.org>
pkgname='perl-moo'
pkgver='1.006001'
pkgrel="1"
pkgdesc="Minimalist Object Orientation (with Moose compatiblity)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers>=1.1' 'perl-devel-globaldestruction>=0.11' 'perl-import-into>=1.002' 'perl-module-runtime>=0.014' 'perl-role-tiny>=1.003004' 'perl-strictures>=1.004003')
makedepends=('perl-test-fatal>=-0.003')
url='https://metacpan.org/release/Moo'
source=("https://cpan.metacpan.org/authors/id/H/HA/HAARG/Moo-${pkgver}.tar.gz")
md5sums=('814b4b4f465a1d3c84f91bd0f30f1fbf')
sha512sums=('8dafa64e9b0dd23a0268035c5687f9f1f6776f0d7a0d2e4d0f03521ad8c33ee14754f901e02f4de92820d862b74bd134928bc79123ae50cac784b973bb6a8317')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Moo-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
