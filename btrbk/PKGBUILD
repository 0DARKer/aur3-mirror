# Maintainer: Daniel J. Campos <dcampos2015 at my dot fit dot edu>
pkgname=btrbk
pkgver=0.17.0
pkgrel=1
pkgdesc="btrbk is a backup tool for btrfs subvolumes, taking advantage of btrfs specific capabilities to create atomic snapshots and transfer them incrementally to your backup locations."
url="http://digint.ch/btrbk"
arch=('x86_64' 'i686') # Might change to any so long as the platform supports perl.
license=('GPLv3')
depends=('perl' 'perl-date-calc' 'btrfs-progs')
optdepends=('openssh: remote backup support')
source=("http://digint.ch/download/btrbk/releases/${pkgname}-${pkgver}.tar.bz2"
	"btrbk.service"
	"btrbk.timer")

sha256sums=('ac1543b0eab4961897501ecc252ad6d1728a44c15357e27a9bcaee18eab1d513'
            '79d5abb3d868fee64760f726ba3ea1cd3e13fda3ca85594950642c1b6bf25d12'
            'f28a1015f6cc7fee2c0d902ceecba9ff09e73753dd5eb17913ca4bee7f068a4c')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/sbin/bin/g' Makefile # To comply with the Arch filesystem layout
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  cd "${srcdir}/"
  install -Dm644 btrbk.service "$pkgdir/usr/lib/systemd/system/btrbk.service"
  install -Dm644 btrbk.timer "$pkgdir/usr/lib/systemd/system/btrbk.timer"
}


