# Maintainer: Rob McCathie <korrode AT gmail>
# Contributor: leepesjee <lpeschier at xs4all dot nl>
# Contributor: fancris3 <fancris3 at aol dot com>

_name=aqualung
pkgname=aqualung-svn
pkgver=1309
pkgrel=3
pkgdesc="Advanced music player featuring gapless playback"
arch=('i686' 'x86_64')
url="http://aqualung.factorial.hu/"
license=('GPL')
depends=('desktop-file-utils' 'gtk2' 'libxml2' 'alsa-lib' 'libpulse' 'liblrdf' 'libcdio-paranoia' 'libcddb' 'libsamplerate' 'lua51'
         'libsndfile' 'flac' 'libvorbis' 'liboggz' 'speex' 'libmad' 'lame' 'libmodplug' 'libmpcdec' 'mac' 'wavpack' 'ffmpeg')
makedepends=('gettext')
optdepends=('pulseaudio: Support for PulseAudio'
            'oss: Support for the Open Sound System'
            'jack: Support for JACK (requires rebuild)'
            'libifp: Support iRiver iFP driver (requires rebuild)')
conflicts=("${_name}")
provides=("${_name}")
source=("${pkgname}::svn+https://svn.code.sf.net/p/aqualung/code/trunk"
        "${_name}.desktop")
sha1sums=('SKIP'
          '498b4221445d046dcbc17619156425aa813dc635')
install="${_name}.install"

pkgver() {
  cd "${startdir}/${pkgname}"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "${srcdir}/${pkgname}"
  ## Remove annoying "http://" preset at 'Add URL'
  sed -i 's|(url_entry), "http://"|(url_entry), ""|' ./src/playlist.c
  ## Revert update for MAC/APE audio
  ## https://sourceforge.net/p/aqualung/code/1303/
  sed -i 's|wchar_t \* pUTF16 = CAPECharacterHelper::GetUTF16FromANSI(filename);|wchar_t \* pUTF16 = GetUTF16FromANSI(filename);|' src/decoder/dec_mac.cpp
}

build() {
  cd "${srcdir}/${pkgname}"
  ./autogen.sh
  ./configure --prefix=/usr
  ## Alternate configure line to force ALSA-only output
  #./configure --prefix=/usr --with-jack=no --with-pulse=no --with-oss=no
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "src/img/icon_16.png" "${pkgdir}/usr/share/icons/hicolor/16x16/apps/${_name}.png"
  install -Dm644 "src/img/icon_24.png" "${pkgdir}/usr/share/icons/hicolor/24x24/apps/${_name}.png"
  install -Dm644 "src/img/icon_32.png" "${pkgdir}/usr/share/icons/hicolor/32x32/apps/${_name}.png"
  install -Dm644 "src/img/icon_48.png" "${pkgdir}/usr/share/icons/hicolor/48x48/apps/${_name}.png"
  install -Dm644 "src/img/icon_64.png" "${pkgdir}/usr/share/icons/hicolor/64x64/apps/${_name}.png"
  install -Dm644 "${srcdir}/${_name}.desktop" "${pkgdir}/usr/share/applications/${_name}.desktop"
}
