# Maintainer : Rob McCathie <archaur at rmcc dot com dot au>
# Contributor: leepesjee <lpeschier at xs4all.nl>

pkgname=aqualung-svn
pkgver=1304
pkgrel=1
pkgdesc="Advanced music player featuring gapless playback."
arch=('i686' 'x86_64')
url="http://aqualung.factorial.hu/"
license=('GPL')
depends=('gtk2' 'libxml2')
makedepends=('subversion' 'gettext')
optdepends=('oss: support for the Open Sound System'
            'jack-audio-connection-kit: support for jack'
            'pulseaudio: support for pulseaudio')
conflicts=('aqualung')
provides=('aqualung')
source=("svn+https://svn.code.sf.net/p/aqualung/code/trunk"
		'aqualung.desktop')
sha1sums=('SKIP'
          'b02f3441944026e527ed30f87258766b83a2c801')

pkgver() {
  cd "$startdir"/trunk
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$srcdir"/trunk
  
  ## remove annoying "http://" preset at 'Add URL'
  sed -i 's|(url_entry), "http://"|(url_entry), ""|' src/playlist.c
}

build() {
  cd "$srcdir"/trunk

  ./autogen.sh
  
  ## No mac (APE) audio for now, because we need newer version of mac,
  ## but the upstream of Arch's mac package is not directly the mac developers,
  ## it's some 3rd party, and *they* need to update.
  #./configure --prefix=/usr
  ./configure --prefix=/usr --without-mac
  
  ## Alternate configure line to force ALSA-only output
  #./configure --prefix=/usr --with-jack=no --with-pulse=no --with-oss=no
  
  make
}
  
  package() {
  cd "$srcdir"/trunk
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/trunk/src/img/icon_64.png "$pkgdir"/usr/share/pixmaps/aqualung.png
  install -D -m644 "$srcdir"/aqualung.desktop "$pkgdir"/usr/share/applications/aqualung.desktop
}
