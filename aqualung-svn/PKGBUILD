# Contributor: fancris3 <fancris3 at aol.com>
# Contributor: leepesjee <lpeschier at xs4all.nl>
pkgname=aqualung-svn
pkgver=1084
pkgrel=1
pkgdesc="Gapless audio player with jack, alsa and oss support"
arch=('i686' 'x86_64')
url="http://aqualung.factorial.hu/"
license=('GPL')

depends=('ffmpeg' 'libmpcdec' 'gtk2' 'libcdio' 'libmad' 'libsamplerate' \
'liblrdf' 'liboggz' 'libmodplug' 'wavpack' 'speex' 'jack-audio-connection-kit' 'lua>=5.1.0' 'mac')
makedepends=('subversion' 'gettext' 'pkgconfig' 'autoconf' 'automake')
conflicts=('aqualung')
provides=('aqualung')
source=(aqualung.desktop)
md5sums=('1c726a4936a3275e66eca26c95d821fa')

_svntrunk=https://aqualung.svn.sourceforge.net/svnroot/aqualung/trunk
_svnmod=aqualung

build() {
  cd ${srcdir}

  if [ -d ${_svnmod}/.svn ]; then
    cd $_svnmod && svn up -r $pkgver && cd ${srcdir}
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

# remove annoying "http://" preset at 'Add URL'
  cd ${srcdir}
  sed -i 's|(url_entry), "http://"|(url_entry), ""|' ./aqualung/src/playlist.c

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  if [ -d $_svnmod-build ]; then rm -rf $_svnmod-build; fi
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  ./autogen.sh || automake -a || return 1
  ./configure LDFLAGS="-lm" --prefix=/usr \
              --with-ifp=no \
              --with-mac=yes \
              --with-oss=yes \
              --with-jack=yes \
              --with-alsa=yes \
              --with-src=yes \
              --with-sndfile=yes \
              --with-flac=yes \
              --with-ogg=yes \
              --with-vorbisenc=yes \
              --with-speex=yes \
              --with-mpeg=yes \
              --with-mod=yes \
              --with-mpc=yes \
              --with-lavc=yes \
              --with-lame=yes \
              --with-wavpack=yes \
              --with-ladspa=yes \
              --with-cdda=yes \
              --with-cddb=yes \
              --with-systray=yes \
              --with-loop=yes \
              --with-lua=yes

  make || return 1
  make DESTDIR=${pkgdir} install
  install -D -m644 src/img/icon_64.png $startdir/pkg/usr/share/pixmaps/aqualung.png
  install -D -m644 ${startdir}/aqualung.desktop $startdir/pkg/usr/share/applications/aqualung.desktop
}
