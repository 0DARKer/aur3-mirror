# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=libu2f-host
pkgver=0.0
pkgrel=2
pkgdesc="Yubico Universal 2nd Factor (U2F) Host C Library"
arch=('i686' 'x86_64')
url='https://github.com/Yubico/libu2f-host'
license=('BSD')
depends=('json-c' 'hidapi')
makedepends=('git' 'help2man' 'gtk-doc' 'gengetopt')
conflicts=('libu2f-host-git')
install=libu2f-host.install
source=("git://github.com/Yubico/libu2f-host.git#tag=libu2f-host-${pkgver}"
	'makefile.patch'
	'70-u2f.rules')

build() {
	cd libu2f-host/

	patch -Np1 < "${srcdir}/makefile.patch"

	autoreconf -fi
	./configure \
		--prefix=/usr \
		--enable-gtk-doc
	make
}


check() {
	cd libu2f-host/

	make check
}

package() {
	cd libu2f-host/

	make DESTDIR="${pkgdir}/" install

	install -D -m0644 "${srcdir}"/70-u2f.rules "${pkgdir}"/usr/lib/udev/rules.d/70-u2f.rules
}

sha256sums=('SKIP'
            '15355aade08033f8373d85017534297ebceb4e2874334109a24e368aea7ac2c0'
            'cc35640fce69684622aa44d4139650f4dda4b552406c10cde97f3e6b6f909e88')
