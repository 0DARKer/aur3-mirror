# $Id: PKGBUILD 1521 2011-08-11 02:16:11Z ffjia $
# http://bugs.kde.org/show_bug.cgi?id=105631
# Maintainer: Feifei Jia <feifei.j at gmail dot com>

pkgname=fingerprint-gui
pkgver=1.02
pkgrel=1
pkgdesc="Application for fingerprint-based authentication, automatically support UPEK fingerprint readers with non-free library"
arch=('i686' 'x86_64')
url="http://www.n-view.net/Appliance/fingerprint/index.php"
license=('GPL')
depends=('qca-ossl' 'libfprint' 'libfakekey')
makedepends=('qt' 'libfprint' 'libfakekey' 'polkit-qt')
optdepends=('libusb: for libbsapi')
source=(http://www.n-view.net/Appliance//fingerprint/download/${pkgname}-${pkgver}.tar.gz
        upek.cfg
        qt.patch)
install=${pkgname}.install
md5sums=('83fad46c47ca701512190b4683b81da4'
         '92cd8fff2b2c03bb881a0c4f59959785'
         '8cb7fdab495053cb8b5a26eb6c223ff5')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  #set -x
  patch -p0 < ../qt.patch || return 1
  qmake PREFIX=/usr LIBPOLKIT_QT=LIBPOLKIT_QT_1_1 || return 1
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make INSTALL_ROOT=${pkgdir}/ install || return 1
  install -D -m644 ./CHANGELOG ./COPYING ${pkgdir}/usr/share/doc/${pkgname}/
  # Detect devices for available proprietary drivers
  if [[ `lsusb -d 0483:` ]] || [[ `lsusb -d 147e:` ]]
  then
    install -d ${pkgdir}/usr/lib
    # Setup NVM emulation needed for some UPEK devices
    install -d -m770 ${pkgdir}/var/upek_data
    install -d ${pkgdir}/etc
    install -m640 ${srcdir}/upek.cfg ${pkgdir}/etc/
    # Setup udev rules for UPEK devices
    install -d ${pkgdir}/etc/udev/rules.d
    install -m644 ./upek/91-fingerprint-gui-upek.rules ${pkgdir}/etc/udev/rules.d/
    if [ `uname -m` == "i686" ]
    then
        install -m755 ./upek/lib/libbsapi.so ${pkgdir}/usr/lib/
    else
        install -m755 ./upek/lib64/libbsapi.so ${pkgdir}/usr/lib/
    fi
  fi
}
