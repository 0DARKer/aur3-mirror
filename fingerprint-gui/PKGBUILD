# $Id: PKGBUILD 1521 2011-08-11 02:16:11Z ffjia $
# http://bugs.kde.org/show_bug.cgi?id=105631
# Maintainer: Feifei Jia <feifei.j at gmail dot com>

pkgname=fingerprint-gui
pkgver=1.05
pkgrel=1
pkgdesc="Application for fingerprint-based authentication, automatically support UPEK fingerprint readers with non-free library"
arch=('i686' 'x86_64')
url="http://www.n-view.net/Appliance/fingerprint/index.php"
license=('GPL')
depends=('qca-ossl' 'libfprint' 'libfakekey')
makedepends=('qt4>=4.8.4' 'libfprint' 'libfakekey' 'polkit-qt')
optdepends=('libusb: for libbsapi')
source=(http://www.n-view.net/Appliance//fingerprint/download/${pkgname}-${pkgver}.tar.gz
        finger-swipe-animation.tar)
install=${pkgname}.install
md5sums=('3a653dfcf78ec91d27112ec13e1c2b5a'
         'c6b70dd036ae68ed9e068146e115a622')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  #set -x
  # Use new animated image created by Anton Krug
  tar xf ../finger-swipe-animation.tar || return 1
  qmake-qt4 PREFIX=/usr LIB=/usr/lib LIBPOLKIT_QT=LIBPOLKIT_QT_1_1 || return 1
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make -j1 INSTALL_ROOT=${pkgdir}/ install || return 1
  install -D -m644 ./CHANGELOG ./COPYING ${pkgdir}/usr/share/doc/${pkgname}/
  # Detect devices for available proprietary drivers
  if [[ `lsusb -d 0483:` ]] || [[ `lsusb -d 147e:` ]]
  then
    install -d ${pkgdir}/usr/lib
    # Setup NVM emulation needed for some UPEK devices
    install -d -m770 ${pkgdir}/var/upek_data
    install -d ${pkgdir}/etc
    install -m644 ./upek/upek.cfg ${pkgdir}/etc/
    # Setup udev rules for UPEK devices
    install -d ${pkgdir}/etc/udev/rules.d
    install -m644 ./upek/91-fingerprint-gui-upek.rules ${pkgdir}/etc/udev/rules.d/
    if [ `uname -m` == "i686" ]
    then
        install -m644 ./upek/lib/libbsapi.so.* ${pkgdir}/usr/lib/
    else
        install -m644 ./upek/lib64/libbsapi.so.* ${pkgdir}/usr/lib/
    fi
  fi
}
