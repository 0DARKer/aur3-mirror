# Maintainer: carstene1ns <carsten.1 gmx net>

pkgname=zelda-classic
pkgver=2.5.0.24
pkgrel=1
pkgdesc="A tribute to Nintendo's The Legend of Zelda (NES)"
arch=('i686' 'x86_64')
url="http://zeldaclassic.com"
license=('custom: Freeware')
depends=('allegro4' 'desktop-file-utils')
optdepends=('gtk-engine-murrine: needed for the gtk2-launcher')
[ "$CARCH" = "x86_64" ] && depends=('lib32-allegro4' 'desktop-file-utils')
[ "$CARCH" = "x86_64" ] && optdepends=('lib32-gtk-engine-murrine')
install="$pkgname.install"
source=("http://www.zeldaclassic.com/files/zc-250-linux.tar.gz"
	"$pkgname.sh"
	"$pkgname-zlaunch.sh"
	"$pkgname-zquest.sh"
	"$pkgname.desktop"
	"$pkgname-zlaunch.desktop"
	"$pkgname-zquest.desktop"
	"$pkgname.png"
	"$pkgname-zlaunch.png"
	"$pkgname-zquest.png")
md5sums=('13fc7a5550d69cce15aaa5afa87a917d'
	'87cfdb9935f0d73de007c9847d4a57d3'
	'744526f3be27d7c2585c0929fbe6ee99'
	'fd266dc68b394cbd13a339b9fe118b09'
	'789d5b42c9a5f2fd1e19dd69933215c6'
	'f223a92d52d7589777c422fd15386feb'
	'a6cca3820066ee95fd0392dcbfc2f57b'
	'13b15546648bd9ac31e1a9a921078360'
	'64691761318b1087a55329f5a6997255'
	'7523e9eccb419ed73ce9389bb5efe962')

package() {
  cd "$srcdir/Zelda Classic/"

  # install binaries
  for i in {romview,zelda,zlaunch,zquest}-l; do
    install -Dm755 "$srcdir/Zelda Classic/$i" "$pkgdir/opt/$pkgname/$i"
  done

  # install data files
  install -Dm644 "samplesoundset/patches.dat" "$pkgdir/opt/$pkgname/samplesoundset/patches.dat"
  for i in *.{qst,dat,nsf}; do
    install -Dm644 "$srcdir/Zelda Classic/$i" "$pkgdir/opt/$pkgname/$i"
  done

  # install doc files
  for i in *.{txt,zh}; do
    install -Dm644 "$srcdir/Zelda Classic/$i" "$pkgdir/usr/share/doc/$pkgname/$i"
  done

  # install launchers
  install -d "$pkgdir/usr/bin"
  install -m755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -m755 "$srcdir/$pkgname-zlaunch.sh" "$pkgdir/usr/bin/$pkgname-zlaunch"
  install -m755 "$srcdir/$pkgname-zquest.sh" "$pkgdir/usr/bin/$pkgname-zquest"

  # install icon and desktop files
  for i in zelda-classic{,-zlaunch,-zquest}; do
    install -Dm644 "$srcdir/$i.png" "$pkgdir/usr/share/pixmaps/$i.png"
    install -Dm644 "$srcdir/$i.desktop" "$pkgdir/usr/share/applications/$i.desktop"
  done

  # install soundset licence
  install -Dm644 "samplesoundset/COPYRIGHT" "$pkgdir/usr/share/licenses/zelda-classic/soundset"
}
