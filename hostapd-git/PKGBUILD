# Contributor: Tom < reztho at archlinux dot us >

pkgname=hostapd-git
pkgver=20140616
pkgrel=1
pkgdesc='Daemon for wireless software access points. Git version.'
arch=('i686' 'x86_64')
url='http://hostap.epitest.fi/hostapd/'
license=('custom')
depends=('openssl' 'libnl')
provides=('hostapd')
conflicts=('hostapd')
install=hostapd.install
source=('git://w1.fi/srv/git/hostap.git'
	'config'
	'hostapd.service')
md5sums=('SKIP'
         '72e8ecf8fadf8b06d87876ea8c3dfd07'
         'a0a16879eed5e4e41ae6b225a4809955')

build() {
  cd ${srcdir}/hostap/hostapd
  cp ${srcdir}/config ./.config
  sed -i 's#/etc/hostapd#/etc/hostapd/hostapd#' hostapd.conf
  export CFLAGS="$CFLAGS $(pkg-config --cflags libnl-3.0)"
  make
}

package() {
  # Systemd unit
  install -Dm644 hostapd.service "$pkgdir/usr/lib/systemd/system/hostapd.service"

  cd ${srcdir}/hostap

  # License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  cd hostapd

  # Binaries
  install -d "$pkgdir/usr/bin"
  install -t "$pkgdir/usr/bin" hostapd hostapd_cli

  # Configuration
  install -d "$pkgdir/etc/hostapd"
  install -d "$pkgdir/usr/share/doc/hostapd"
  install -m644 -t "$pkgdir/usr/share/doc/hostapd" \
    hostapd.{accept,conf,deny,eap_user,radius_clients,sim_db,vlan,wpa_psk} \
    wired.conf hlr_auc_gw.milenage_db

  # Man pages
  install -Dm644 hostapd.8 "$pkgdir/usr/share/man/man8/hostapd.8"
  install -Dm644 hostapd_cli.1 "$pkgdir/usr/share/man/man1/hostapd_cli.1"
}
