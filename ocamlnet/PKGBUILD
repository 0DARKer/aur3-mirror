# Maintainer:  Nicolas Pouillard <Nicolas.Pouillard@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>
pkgname=ocamlnet
pkgver=3.4
pkgrel=1
pkgdesc="OCaml libraries for web and Internet programming."
arch=('i686' 'x86_64')
url="http://projects.camlcity.org/projects/ocamlnet.html"
license=('GPL' 'LGPL')
depends=('ocaml' 'ocaml-findlib' 'pcre-ocaml')
options=('!strip')
install=ocamlnet.install
source=(http://download.camlcity.org/download/$pkgname-$pkgver.tar.gz ocamlnet-glibc.patch)
md5sums=('920414f896af983e7458c03a492f7c62'
         '36bfbc0386cc1353f45f31164aab7ef4')

build(){
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build with glibc >=2.8
  patch -Np1 -i ../ocamlnet-glibc.patch

  ./configure
  make -j1 all opt || return 1
}

package(){
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$pkgdir/$(ocamlfind printconf destdir)"
  mkdir -p "$pkgdir/$(ocamlfind printconf destdir)/stublibs"
  env DESTDIR="$pkgdir" \
      OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
      make install
}

# vim:set ts=2 sw=2 et:
