# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor:  Nicolas Pouillard <Nicolas.Pouillard@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>
pkgname=ocamlnet
pkgver=3.4
pkgrel=2
pkgdesc="OCaml libraries for web and Internet programming."
arch=('i686' 'x86_64')
url="http://projects.camlcity.org/projects/ocamlnet.html"
license=('GPL' 'LGPL')
depends=('ocaml' 'ocaml-pcre' 'ocaml-ssl' 'ocaml-cryptokit')
makedepends=('ocaml-findlib')
options=(!strip)
source=(http://download.camlcity.org/download/$pkgname-$pkgver.tar.gz)
md5sums=('920414f896af983e7458c03a492f7c62')

build(){
  cd "$srcdir/$pkgname-$pkgver"

  ./configure -enable-ssl -enable-crypto
  make all opt || return 1
}

package(){
  cd "$srcdir/$pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)"
  mkdir -p "$OCAMLFIND_DESTDIR/stublibs"
  env DESTDIR=$pkgdir make install
}
