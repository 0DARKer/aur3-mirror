# Maintainer: Mathias Anselmann <mathias.anselmann@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Michele Mocciola <mickele>
# Contributor: Simon Zilliken <simon____AT____zilliken____DOT____name>

pkgname=paraview
pkgver=3.98.0
pkgrel=1
pkgdesc='Parallel Visualization Application using VTK'
arch=('i686' 'x86_64')
url='http://www.paraview.org'
license=('custom')
depends=('qt' 'qtwebkit' 'openmpi' 'python2' 'ffmpeg')
makedepends=('cmake' 'mesa')
source=("http://paraview.org/files/v${pkgver:0:4}/ParaView-${pkgver}-src.tgz"
	'paraview.png'
	'paraview.desktop')
sha1sums=('3c3f82eba8ce9f82da6dcde3d1e532f819270bcf'
          'a2dff014e1235dfaa93cd523286f9c97601d3bbc'
          '1f94c8ff79bb2bd2c02d6b403ea1f4599616531b')

build() {
  cd "${srcdir}"/ParaView-${pkgver}-src
  
  rm -rf "${srcdir}"/build
  mkdir "${srcdir}"/build
  cd "${srcdir}"/build

   #flags to use python2 instead of python which is 3.x.x on archlinux
   local cmake_system_python_flags="-DPYTHON_EXECUTABLE:PATH=/usr/bin/python2 \
   	 -DPYTHON_INCLUDE_DIR:PATH=/usr/include/python2.7 -DPYTHON_LIBRARY:PATH=/usr/lib/libpython2.7.so"

  # enable when http://paraview.org/Bug/view.php?id=12852 gets fixed
  #-DCMAKE_SKIP_RPATH:BOOL=YES \
  # the following flags enable the feature request at https://bugs.archlinux.org/task/27525
  # -DPARAVIEW_USE_VISITBRIDGE:BOOL=ON -DVISIT_BUILD_READER_CGNS:BOOL=ON
  cmake \
  -DCMAKE_INSTALL_PREFIX:PATH=/usr \
  -DCMAKE_BUILD_TYPE=Release \
  -DBUILD_SHARED_LIBS:BOOL=ON \
  -DPARAVIEW_USE_MPI:BOOL=ON \
  -DVTK_USE_BOOST:BOOL=ON \
  -DPARAVIEW_ENABLE_PYTHON:BOOL=ON \
  -DPARAVIEW_ENABLE_FFMPEG:BOOL=ON \
  ${cmake_system_python_flags} \
  ../ParaView-${pkgver}-src

  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install

  #Install license
  install -Dm644 "${srcdir}"/ParaView-${pkgver}-src/License_v1.2.txt "${pkgdir}"/usr/share/licenses/paraview/LICENSE

  #Install desktop shortcuts
  install -Dm644 "${srcdir}"/paraview.png ${pkgdir}/usr/share/pixmaps/paraview.png
  desktop-file-install --dir="${pkgdir}"/usr/share/applications "${srcdir}"/paraview.desktop
}
