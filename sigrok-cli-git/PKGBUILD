# Maintainer: Noel Maersk <veox at wemakethings dot net>

pkgname=sigrok-cli-git
_gitname=sigrok-cli
pkgver=196.41602d3
pkgrel=3
pkgdesc="Client software that supports various hardware logic analyzers, CLI client (git version)"
arch=('i686' 'x86_64')
url="http://sigrok.org/wiki/Main_Page"
license=('GPL3')
depends=('glib2' 'libsigrok-git' 'libsigrokdecode-git')
optdepends=()
makedepends=('git')
provides=('sigrok-cli')
conflicts=('sigrok-cli')
source=("git://sigrok.org/${_gitname}"
        'sigrok-cli.c.patch')
md5sums=('SKIP'
         '776160615f30c5bae98c4732bc8f9730')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "${_gitname}"
  patch -p0 -i ${srcdir}/sigrok-cli.c.patch
}

build() {
  cd "$srcdir/${_gitname}"

  ./autogen.sh
  ./configure --prefix=/usr --includedir="/usr/include/libsigrokdecode"

  make
}

package() {
  cd "$srcdir/${_gitname}"
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
