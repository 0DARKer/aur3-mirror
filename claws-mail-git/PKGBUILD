# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=claws-mail-git
pkgver=3.9.1.r10.ge77b994
pkgrel=1
pkgdesc="A GTK+ based e-mail client - git checkout"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org/"
depends=('gtk2' 'gnutls' 'startup-notification' 'pilot-link' 'enchant'
         'gpgme' 'libetpan' 'libsm' 'db' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('git' 'compface' 'spamassassin' 'bogofilter' 'valgrind')
optdepends=('python2:           needed for some tools'
            'perl:              needed for some tools'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering')
replaces=('sylpheed-claws' 'claws-mail-cvs')
conflicts=('claws-mail' 'claws-mail-cvs' 'claws-mail-extra-plugins' 'claws-mail-extra-plugins-cvs')
provides=('claws' 'claws-mail')
options=(!libtool)
install=claws-mail.install
source=('claws-mail::git+http://git.claws-mail.org/readonly/claws.git'
	'http://www.eworm.de/download/linux/claws-timestamp.patch'
	'http://www.eworm.de/download/linux/claws-git-version.patch')

pkgver() {
	cd claws-mail/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

prepare() {
	cd claws-mail/

	patch -Np1 < ${srcdir}/claws-timestamp.patch
	git update-index --assume-unchanged src/gtk/about.c

	patch -Np1 < ${srcdir}/claws-git-version.patch
	git update-index --assume-unchanged configure.ac

	sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
	git update-index --assume-unchanged tools/*.py
}

build() {
	cd claws-mail/

	# add --enable-new-addrbook to the options to use the new address book
	# do not forget to add claws-contacts to the dependencies then
	./autogen.sh --prefix=/usr --disable-static \
		--enable-enchant \
		--enable-gnutls \
		--enable-ldap \
		--enable-crash-dialog \
		--enable-pgpmime-plugin \
		--enable-spamassassin-plugin \
		--enable-bogofilter-plugin \
		--enable-jpilot

	make
}

package() {
	cd claws-mail/

	# fix missing MKDIR_P
	sed -i '/^INSTALL/i MKDIR_P = /usr/bin/mkdir -p' po/Makefile
	make DESTDIR=${pkgdir} install

	# build and install extra tools
	cd tools
	make
	# all executables and .conf files ; only top directory
	for FILE in $(find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf'); do
		install -D -m755 ${FILE} ${pkgdir}/usr/lib/claws-mail/tools/${FILE}
	done
}

sha256sums=('SKIP'
            'bbf29f10602a74d73f1a30d791ae49c3b1d5abf20c48db0f4c81b0dca7bc0078'
            'e3fdd9ea9d022e18c956cab86fc6a07e0387280de2942a1e376db48cef45709f')
