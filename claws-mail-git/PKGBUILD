# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=claws-mail-git
pkgver=3.9.0.168.ge02c1b6
pkgrel=1
pkgdesc="A GTK+ based e-mail client - git checkout"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org/"
depends=('gtk2' 'gnutls' 'startup-notification' 'pilot-link' 'enchant'
         'gpgme' 'libetpan' 'libsm' 'db' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('git' 'compface' 'spamassassin' 'bogofilter' 'valgrind')
optdepends=('python2:           needed for some tools'
            'perl:              needed for some tools'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering')
replaces=('sylpheed-claws' 'claws-mail-cvs')
conflicts=('claws-mail' 'claws-mail-cvs' 'claws-mail-extra-plugins' 'claws-mail-extra-plugins-cvs')
provides=('claws' 'claws-mail')
options=(!libtool)
install=claws-mail.install
source=('claws-mail::git+http://git.claws-mail.org/readonly/claws.git')

pkgver() {
	cd claws-mail/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd claws-mail/

	sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py
	git update-index --assume-unchanged tools/*.py

	# add --enable-new-addrbook to the options to use the new address book
	# do not forget to add claws-contacts to the dependencies then
	./autogen.sh --prefix=/usr --disable-static \
		--enable-enchant \
		--enable-gnutls \
		--enable-ldap \
		--disable-dillo-viewer-plugin \
		--enable-crash-dialog \
		--enable-pgpmime-plugin \
		--enable-spamassassin-plugin \
		--enable-bogofilter-plugin \
		--enable-jpilot

	make
}

package() {
	cd claws-mail/

	# fix missing MKDIR_P
	sed -i '/^INSTALL/i MKDIR_P = /usr/bin/mkdir -p' po/Makefile
	make DESTDIR=${pkgdir} install

	# build and install extra tools
	cd tools
	make
	# all executables and .conf files ; only top directory
	for FILE in $(find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf'); do
		install -D -m755 ${FILE} ${pkgdir}/usr/lib/claws-mail/tools/${FILE}
	done
}
sha256sums=('SKIP')
