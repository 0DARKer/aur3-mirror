# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=claws-mail-git
pkgver=3.9.0.158.gb469545
pkgrel=1
pkgdesc="A GTK+ based e-mail client - git checkout"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org/"
depends=('claws-contacts' 'gtk2' 'gnutls' 'startup-notification' 'pilot-link' 'enchant'
         'gpgme' 'libetpan' 'libsm' 'db' 'dbus-glib' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('git' 'compface' 'spamassassin' 'bogofilter' 'valgrind')
optdepends=('python2:           needed for some tools'
            'perl:              needed for some tools'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering')
replaces=('sylpheed-claws' 'claws-mail-cvs')
conflicts=('claws-mail' 'claws-mail-cvs' 'claws-mail-extra-plugins' 'claws-mail-extra-plugins-cvs')
provides=('claws' 'claws-mail')
options=(!libtool)
install=claws-mail.install
source=('claws-mail::git+http://git.claws-mail.org/readonly/claws.git'
	'http://www.eworm.de/download/linux/claws_vcalendar.patch')

pkgver() {
	cd claws-mail/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd claws-mail/

#	sed -i 's@^#!.*python.*@#!/usr/bin/python2@' tools/*.py

	# fix compilation
	patch -Np1 < ${srcdir}/claws_vcalendar.patch

	./autogen.sh --prefix=/usr --disable-static \
		--enable-enchant \
		--enable-gnutls \
		--enable-ldap \
		--disable-dillo-viewer-plugin \
		--enable-crash-dialog \
		--enable-pgpmime-plugin \
		--enable-spamassassin-plugin \
		--enable-bogofilter-plugin \
		--enable-jpilot \
		--enable-new-addrbook

	make
}

package() {
	cd claws-mail/

	make DESTDIR=${pkgdir} install

#	# build and install extra tools
#	cd tools
#	make
#	# all executables and .conf files ; only top directory
#	for FILE in $(find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf'); do
#		install -D -m755 ${FILE} ${pkgdir}/usr/lib/claws-mail/tools/${FILE}
#	done
}
sha256sums=('SKIP'
            '0394cc96c2735a0cc7e0d655ac0303fb7c6be0a21cefdf86ba5465c0cdfcad81')
