# Maintainer: Thorsten TÃ¶pper <atsutane-aur@freethoughts.de>
# Contributor: Thiago Perrotta <perrotta dot thiago at poli dot ufrj dot br>
 
_gitname=i3lock-color
pkgname="$_gitname-git"
pkgver=2012.05.01.g8dc9350
pkgrel=1
pkgdesc="An improved screenlocker based upon XCB and PAM with color configuration support"
arch=('i686' 'x86_64')
url="https://github.com/eBrnd/i3lock-color"
license=('MIT')
depends=('xcb-util-image' 'xcb-util-keysyms' 'pam' 'libev' 'libx11' 'cairo')
provides=('i3lock')
conflicts=('i3lock')
makedepends=('git')
source=("git+https://github.com/eBrnd/$_gitname.git")
md5sums=('SKIP')
 
pkgver() {
cd "${srcdir}/${_gitname}"
git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}
 
build() {
cd "${srcdir}/${_gitname}"
make
}
 
package() {
cd "${srcdir}/${_gitname}"
make DESTDIR="$pkgdir" install
install -Dm644 i3lock.1 ${pkgdir}/usr/share/man/man1/i3lock.1
install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${_gitname}/LICENSE
}
 
# vim:set ts=2 sw=2 et: 
