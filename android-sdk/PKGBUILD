# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=android-sdk
pkgver=r09
pkgrel=5
pkgdesc='Google Android SDK'
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
depends=('java-runtime' 'alsa-lib' 'openal' 'libstdc++5' 'libxv' 'ncurses'
         'sdl' 'zlib')

[[ $CARCH = x86_64 ]] && depends=('java-runtime' 'lib32-alsa-lib'
                                  'lib32-openal' 'lib32-libstdc++5'
                                  'lib32-libxv' 'lib32-ncurses' 'lib32-sdl'
                                  'lib32-zlib')

install=$pkgname.install
optdepends=('udev-android-rules: udev rules for Android devices'
            'android-sdk-platform-tools: adb, aapt, aidl, dexdump and dx')
source=(http://dl.google.com/android/${pkgname}_$pkgver-linux_x86.tgz
        $pkgname.png
        $pkgname.desktop)

md5sums=('9fefac5ff85d329836439f6e77a78cae'
         '1379235d0b536096e08ffd162d0feea7'
         'bae60e64d5f3cab67f01a5b38c44d94b')

package() {
  cd "$pkgdir"
  mkdir -p opt etc/profile.d
  echo 'export PATH=$PATH:/opt/android-sdk/tools' > etc/profile.d/android.sh
  echo 'setenv PATH ${PATH}:/opt/android-sdk/tools' > etc/profile.d/android.csh
  chmod 755 etc/profile.d/android.{csh,sh}
  cp -a "$srcdir/$pkgname-linux_x86" opt/$pkgname
  chgrp 420 opt/$pkgname
  chmod g+w opt/$pkgname
  install -Dm655 "$srcdir/$pkgname.png" usr/share/pixmaps/$pkgname.png
  install -Dm655 "$srcdir/$pkgname.desktop" \
    usr/share/applications/$pkgname.desktop
}
