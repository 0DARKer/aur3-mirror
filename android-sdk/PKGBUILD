# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>
pkgname=android-sdk
pkgver=r12
pkgrel=2
pkgdesc='Google Android SDK'
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
depends=('java-runtime' 'alsa-lib' 'openal' 'libstdc++5' 'libxv'
         'ncurses' 'sdl' 'swt' 'zlib')
[[ $CARCH = x86_64 ]] && depends=('java-runtime' 'lib32-alsa-lib'
                                  'lib32-openal' 'lib32-libstdc++5'
                                  'lib32-libxv' 'lib32-ncurses' 'lib32-sdl'
                                  'lib32-zlib' 'swt')
install=$pkgname.install
optdepends=('android-udev: udev rules for Android devices'
            'android-sdk-platform-tools: adb, aapt, aidl, dexdump and dx')
source=(http://dl.google.com/android/${pkgname}_$pkgver-linux_x86.tgz
        $pkgname.png
        $pkgname.desktop
        $pkgname.sh
        $pkgname.csh
        license.html)
md5sums=('f8485275a8dee3d1929936ed538ee99a'
         '1379235d0b536096e08ffd162d0feea7'
         'bae60e64d5f3cab67f01a5b38c44d94b'
         'aa753536c3bb5f71443c16519054502c'
         '351d6b626ca016212cd55731bd4cdc3e'
         '8bffc1a9d6f1c9a6713ff5a329808047')

package() {
  cd "$pkgdir"
  install -Dm755 "$srcdir/$pkgname.sh" etc/profile.d/$pkgname.sh
  install -Dm755 "$srcdir/$pkgname.csh" etc/profile.d/$pkgname.csh
  mkdir -p opt
  cp -a "$srcdir/$pkgname-linux_x86" opt/$pkgname
  #chgrp 420 opt/$pkgname/{,platforms,add-ons}
  #chmod g+w opt/$pkgname/{,platforms,add-ons}
  install -Dm655 "$srcdir/$pkgname.png" usr/share/pixmaps/$pkgname.png
  install -Dm655 "$srcdir/$pkgname.desktop" \
    usr/share/applications/$pkgname.desktop
  install -Dm644 "$srcdir/license.html" \
    usr/share/licenses/$pkgname/license.html
}
