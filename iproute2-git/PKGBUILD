# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=iproute2-git
pkgver=3.17.0.r0.g50231ad
pkgrel=1
pkgdesc='IP Routing Utilities - git checkout'
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.linuxfoundation.org/collaborate/workgroups/networking/iproute2"
depends=('glibc' 'iptables')
makedepends=('git' 'linux-atm')
optdepends=('linux-atm: ATM support')
provides=('iproute2')
conflicts=('iproute2' 'iproute')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
source=('git://git.kernel.org/pub/scm/linux/kernel/git/shemminger/iproute2.git'
	'iproute2-fhs.patch'
	'unwanted-link-help.patch')


pkgver() {
	cd iproute2/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd iproute2/

	./configure
	make
}

package() {
	cd iproute2/

	make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP'
            '2a1ff211f1fcc57bf92dd3d19190ba72d094c4a4e82d409ba7f80d857778f0df'
            '3f72492554a5417c13b2b6f6222c158319ce91c0b6c25fd651f49add493323a7')
