pkgname=iproute2-git
pkgver=3.8.0.86.g5d8a752
pkgrel=1
pkgdesc='Advanced IP routing and network device configuration tools'
url='http://git.kernel.org/?p=linux/kernel/git/shemminger/iproute2.git;a=summary'
license=('GPL2')
depends=('glibc' 'bash')
provides=('iproute2')
conflicts=('iproute2')
optdepends=('db' 'iptables' 'linux-atm')
makedepends=('git' 'libnl' 'ipset' 'iptables' 'db' 'linux-atm')
arch=('i686' 'x86_64')

backup=('etc/iproute2/group'
'etc/iproute2/ematch_map'
'etc/iproute2/rt_dsfield'
'etc/iproute2/rt_protos'
'etc/iproute2/rt_realms'
'etc/iproute2/rt_scopes'
'etc/iproute2/rt_tables')

source=('git://git.kernel.org/pub/scm/linux/kernel/git/shemminger/iproute2.git')

pkgver() {
  cd iproute2
  git describe | sed 's/^v//;s/-/./g'
}

build() {
  cd iproute2
  ./configure --prefix=/usr
  find -name Makefile -exec sed -i "s|-Werror|-Werror -Wno-error=unused-result|g" {} +
  make CC="${CC:-gcc}" CCOPTS="-D_GNU_SOURCE ${CFLAGS}"
}

package() {
  cd iproute2
  make PREFIX=/usr SBINDIR="/usr/bin" DESTDIR="$pkgdir" install
  # libnetlink isn't installed, install it FS#19385

  #install -Dm644 include/libnetlink.h "$pkgdir/usr/include/libnetlink.h"
  #install -Dm644 lib/libnetlink.a "$pkgdir/usr/lib/libnetlink.a"
}

md5sums=('SKIP')
sha256sums=('SKIP')
