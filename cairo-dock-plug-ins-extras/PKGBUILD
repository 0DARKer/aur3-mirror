# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: sausageandeggs <sausageandeggs@archlinux.us>
# Contributor: lesebas

pkgname=cairo-dock-plug-ins-extras
pkgver=3.3.2
pkgrel=1
pkgdesc="Extra plugins for cairo-dock"
arch=(any)
url=https://launchpad.net/$pkgname
license=(GPL3 LGPL3)
depends=(cairo-dock-plugins python-dbus)
optdepends=('bsdmainutils: calendar'
    'deluge: Deluge'
    'gtg: GTG'
    'kdeplasma-addons-applets-lancelot: lancelot'
    'ktorrent: KTorrent'
    'mintmenu: MintMenu'
    python2-dbus
    'python-sgmllib: Google'
    vala)
source=($url/${pkgver:0:3}/$pkgver/+download/${pkgname}_$pkgver.tar.gz)
sha256sums=('f9f3ab637a1a991db2a58e4064927a307cc55c62c4cd0c36c324de8daffeed3e')
sha512sums=('d98d3d33f6944a228211a348976c7f97672624e6c36a329df2b086b26071447d7c30eeb48839d9b705e40918b92ef57140c16dfce3d734f7a19fc20ebf2b0ed4')

package() {
    _datadir=$(pkg-config gldi --variable=pluginsdatadir)

    install -d "$pkgdir"/$_datadir/Dbus/third-party/
    cp -r ${pkgname}_$pkgver/* "$pkgdir"/$_datadir/Dbus/third-party/
}
