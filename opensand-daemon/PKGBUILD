# Maintainer: Audric Schiltknecht <storm+arch@chemicalstorm.org>
pkgname=opensand-daemon
pkgver=0.9.0
pkgrel=1
pkgdesc="Daemon for OpenSAND entity (sat, gw, st or ws)"
arch=('i686')
url="http://www.opensand.org"
license=('GPL3')
groups=('opensand')
depends=('opensand-core' 'libnl' 'python2-prctl' 'python2-ipaddr' 'avahi')
makedepends=('python2-distribute' 'swig')
backup=('etc/opensand/daemon.conf')
options=(!emptydirs)
install='opensand-daemon.install'
source=("https://launchpad.net/opensand/0.9.x/0.9.0/+download/opensand-${pkgver}.tar.gz"
        "https://launchpadlibrarian.net/114721137/fix_bug_1045240_daemon.patch"
        "https://launchpadlibrarian.net/114772572/fix_bug_1045240_sand-daemon.patch"
        "daemon.conf"
        "opensand-daemon.rc"
        "opensand-daemon.tmpfiles")
md5sums=('16ccb9ce24259f7ca943f05b5b322bc1'
         'b0eedf918687d5444f7a2dae90d88043'
         'e5cc68fa3ef9a00ac9b96808b834ccb3'
         '663275733f8f804dd8a44ee98bd51ec0'
         '2ac91812391731ebcc1f9315913eb9cc'
         '1e57d80423b06d210bfe074f7fd90fd9')

build() {
  cd "$srcdir/opensand-$pkgver"
  tar xf ${pkgname}-${pkgver}.tar.gz
  cd "${pkgname}-${pkgver}"
  patch -p1 < ${srcdir}/fix_bug_1045240_daemon.patch
  patch -p1 < ${srcdir}/fix_bug_1045240_sand-daemon.patch
  python2 setup.py build
}

package() {
  cd "$srcdir/opensand-$pkgver/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1

  # Remove debian-specific stuff
  rm -rf ${pkgdir}/usr/libexec

  # Set default configuration
  install -Dm644 "${srcdir}/daemon.conf" "${pkgdir}/etc/opensand/daemon.conf"

  # Copy rc file
  install -Dm644 "${srcdir}/${pkgname}.rc" "${pkgdir}/etc/rc.d/${pkgname}"

  # Copy tmpfiles configuration
  install -Dm644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  
  # Set sand-daemon executable
  chmod +x "${pkgdir}/usr/bin/sand-daemon"
}

# vim:set ts=2 sw=2 et:
