# Maintainer: Jonathan Steel <jsteel at archlinux.org>

pkgbase=naemon
pkgname=('naemon-core' 'naemon-livestatus' 'naemon-thruk')
pkgver=0.8.0
pkgrel=3
arch=('i686' 'x86_64')
url="http://naemon.org"
license=('GPL2')
makedepends=('gperf' 'rsync' 'gd' 'mariadb-clients')
source=(http://labs.consol.de/naemon/release/v$pkgver/src/$pkgbase-$pkgver.tar.gz
        Scalar-List-Utils-1.33.tar.gz::https://github.com/Scalar-List-Utils/Scalar-List-Utils/archive/v1.33.tar.gz
        http://search.cpan.org/CPAN/authors/id/D/DA/DAGOLDEN/Parse-CPAN-Meta-1.4414.tar.gz
        apache_fcgid.conf
        $pkgbase.service)
md5sums=('4a8c2c1463558230d4dd7f5b75e115ab'
         '3ae8267e9e07d7372a7300dc167dda41'
         'f83ad609767ab52e9a452243f50c71ca'
         'abf2435d4b67dff87c31d36076ebde1a'
         'abcdcb924051cc880113f9aaadbe99ab')

prepare() {
  cd "$srcdir"/$pkgbase-$pkgver

  # Pull in some newer Perl modules to fix build issues
  sed -i 's/Scalar-List-Utils-1.31/Scalar-List-Utils-1.33/' thruk/libs/Makefile 
  cp "$srcdir"/Scalar-List-Utils-1.33.tar.gz thruk/libs/src/
  sed -i 's/Parse-CPAN-Meta-1.4405/Parse-CPAN-Meta-1.4414/' thruk/libs/Makefile 
  cp "$srcdir"/Parse-CPAN-Meta-1.4414.tar.gz thruk/libs/src/

  # Break up the install process so we can package the separate components
  sed -i '56iinstall-livestatus:' Makefile
  sed -i '58iinstall-thruk:' Makefile

  # Work around bug https://rt.cpan.org/Public/Bug/Display.html?id=87998
  sed -i '/Compress/d' thruk/gui/lib/Thruk.pm
}

build() {
  cd "$srcdir"/$pkgbase-$pkgver

  ./configure --prefix=/usr \
              --bindir=/usr/bin \
              --datadir="/usr/share/naemon" \
              --libdir="/usr/lib/naemon" \
              --localstatedir="/var/lib/naemon" \
              --sysconfdir="/etc/naemon" \
              --mandir="/usr/share/man" \
              --enable-event-broker \
              --with-pluginsdir="/usr/lib/naemon/plugins" \
              --with-tempdir="/var/cache/naemon" \
              --with-checkresultdir="/var/cache/naemon/checkresults" \
              --with-logdir="/var/log/naemon" \
              --with-logrotatedir="/etc/logrotate.d" \
              --with-naemon-user="naemon" \
              --with-naemon-group="naemon" \
              --with-lockfile="/var/run/naemon/naemon.pid" \
              --with-htmlurl="/naemon"
  make
}

package_naemon-core() {
pkgdesc="System and network monitoring application"
depends=('bash')
optdepends=('logrotate'
            'monitoring-plugins')
backup=('etc/logrotate.d/naemon' 'etc/naemon/conf.d/commands.cfg'
  'etc/naemon/conf.d/contacts.cfg' 'etc/naemon/conf.d/localhost.cfg'
  'etc/naemon/conf.d/printer.cfg' 'etc/naemon/conf.d/switch.cfg'
  'etc/naemon/conf.d/templates/contacts.cfg'
  'etc/naemon/conf.d/templates/hosts.cfg'
  'etc/naemon/conf.d/templates/services.cfg'
  'etc/naemon/conf.d/timeperiods.cfg'
  'etc/naemon/conf.d/windows.cfg' 'etc/naemon/init.d/naemon'
  'etc/naemon/naemon.cfg' 'etc/naemon/resource.cfg')
install=$pkgbase.install

  cd "$srcdir"/$pkgbase-$pkgver

  make DESTDIR="$pkgdir"/ install

  install -d "$pkgdir"/var/lib/$pkgbase
  install -d "$pkgdir"/var/run/$pkgbase

  chown -R 44:44 "$pkgdir"/var/{cache,lib,log}/$pkgbase \
    "$pkgdir"/etc/naemon/resource.cfg

  chmod 775 "$pkgdir"/var/{cache,lib,log}/$pkgbase

  install -Dm644 "$srcdir"/$pkgbase.service \
    "$pkgdir"/usr/lib/systemd/system/$pkgbase.service
}

package_naemon-livestatus() {
pkgdesc="Standard API for Naemon"
depends=('icu')

  cd "$srcdir"/$pkgbase-$pkgver

  make DESTDIR="$pkgdir"/ install-livestatus
}

package_naemon-thruk() {
pkgdesc="Monitoring Webinterface for Naemon"
depends=('gd' 'mariadb-clients' 'apache' 'mod_fcgid')
backup=('etc/naemon/cgi.cfg' 'etc/naemon/htpasswd'
  'etc/naemon/log4perl.conf' 'etc/naemon/menu_local.conf'
  'etc/naemon/naglint.conf' 'etc/httpd/conf/extra/naemon-thruk.conf'
  'etc/naemon/thruk_local.conf' 'etc/naemon/thruk.conf'
  'etc/naemon/conf.d/thruk_bp_generated.cfg'
  'etc/naemon/conf.d/thruk_templates.cfg' 'etc/logrotate.d/thruk')

  cd "$srcdir"/$pkgbase-$pkgver

  make DESTDIR="$pkgdir"/ install-thruk

  install -Dm644 "$srcdir"/apache_fcgid.conf \
    "$pkgdir"/etc/httpd/conf/extra/$pkgname.conf

  # Remove init script
  rm -rf "$pkgdir"/usr/etc/
}
