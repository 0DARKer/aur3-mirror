# Maintainer: Antonio Rojas 

pkgname=okular-frameworks-git
pkgver=r6792.b79e706
pkgrel=1
pkgdesc='Universal document viewer'
arch=('i686' 'x86_64')
url='http://kde.org/applications/graphics/okular/'
license=('GPL')
depends=('kpty' 'kdelibs4support' 'threadweaver' 'kactivities-frameworks' 'khtml' 'chmlib' 'djvulibre' 'libspectre' 'poppler-qt5' 
'libkscreen-frameworks')
makedepends=('extra-cmake-modules' 'git')
conflicts=('kdegraphics-okular')
provides=('kdegraphics-okular')
source=("git://anongit.kde.org/okular.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
  cd okular
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../okular \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
