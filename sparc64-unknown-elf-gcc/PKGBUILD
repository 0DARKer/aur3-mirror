# Maintainer: Andre Bartke <dev@bartke.cc>
# Contributor: Vojtech Horky
 
_pkgname=gcc
_target="sparc64-unknown-elf"
_sysroot="/usr/lib/cross-${_target}"
 
pkgname=${_target}-${_pkgname}
pkgver=4.7.2
pkgrel=1
pkgdesc="Bare-metal gcc/g++ for sparc targets"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gcc/"
license=('GPL')
depends=("${_target}-binutils")
options=('!ccache' '!distcc' '!emptydirs' '!libtool' '!strip' '!buildflags')
source=("ftp://ftp.gnu.org/gnu/gcc/gcc-${pkgver}/${_pkgname}-${pkgver}.tar.bz2")
 
build() {
  cd ${srcdir}/${_pkgname}-${pkgver}

  msg "Downloading prequisites..."
  # get mpfr mpc gmp
  ./contrib/download_prerequisites

  # cleanup/setup build directory
  rm -rf build
  mkdir build && cd build

  ../configure --prefix=${_sysroot} \
    --with-sysroot=${_sysroot} \
    --target=${_target} \
    --enable-long-long \
    --enable-languages=c,c++ \
    --disable-shared \
    --disable-multilib \
    --with-gnu-as \
    --with-gnu-ld \
    --with-newlib \
    --disable-nls \
    --disable-threads \
    --disable-libgcj

  make all-gcc all-target-libgcc || return 1
}
 
package() {
  cd ${srcdir}/${_pkgname}-${pkgver}/build

  make DESTDIR=${pkgdir} install-gcc install-target-libgcc || return 1

  msg "Cleaning-up cross compiler tree..."
  rm -rf ${pkgdir}/${_sysroot}/share/{info,man}

  msg "Creating out-of-path executables..."
  mkdir -p ${pkgdir}/${_sysroot}/${_target}/bin/
  cd ${pkgdir}${_sysroot}/${_target}/bin/
  for bin in ${pkgdir}${_sysroot}/bin/${_target}-*; do
    bbin=`basename "$bin"`;
    ln -s ${_sysroot}/bin/$bbin `echo "$bbin" | sed "s#^${_target}-##"`;
  done

  msg "Creating /usr/bin symlinks..."
  mkdir -p $pkgdir/usr/bin
  for bin in ${pkgdir}${_sysroot}/bin/${_target}-*; do
    bbin=`basename "$bin"`;
    ln -s "${_sysroot}/bin/${bbin}" "${pkgdir}/usr/bin/${bbin}";
  done
}

md5sums=('cc308a0891e778cfda7a151ab8a6e762')

# vim: set ts=2 sw=2 ft=sh noet:
