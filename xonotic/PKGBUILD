# Maintainer: Xyne

pkgname=xonotic
pkgver=0.1.0preview
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="a fast paced free, open-source first person shooter"
url="http://www.xonotic.org/"
license=("GPL")
depends=('alsa-lib' 'curl' 'libjpeg>=8' 'libmodplug' 'libvorbis' 'libxpm' 'libxxf86dga' 'libxxf86vm' 'sdl' 'libpng>=1.4.0')
makedepends=('mesa')
source=("http://dl.xonotic.org/${pkgname}-${pkgver}.zip"
        xonotic-dedicated
        xonotic-glx
        xonotic-glx.desktop
        xonotic-sdl
        xonotic-sdl.desktop
        )
md5sums=('aafb43893aa66e01488c817e3a60d96d'
         '1f81c51ee91bb332c95c4312458ad714'
         '6ec971e9ad4524422873b46fe746bcef'
         '887ca3de3fd7498cc7549bc790c74868'
         'c26d4d759c75db6cbd31afdeee8303e2'
         'acb5290b9e2bf65aa669c6a41c1546bb')

build() {
  cd "$srcdir/Xonotic/source/darkplaces"
  make CPUOPTIMIZATIONS="${CFLAGS}" DP_LINK_TO_LIBJPEG=1 cl-nexuiz
  make CPUOPTIMIZATIONS="${CFLAGS}" DP_LINK_TO_LIBJPEG=1 sdl-nexuiz
  make CPUOPTIMIZATIONS="${CFLAGS}" DP_LINK_TO_LIBJPEG=1 sv-nexuiz
}

package() {
  cd "$srcdir/Xonotic"
  _install_dir="$pkgdir/opt/xonotic"

  mkdir -p "$_install_dir"

  for _dir in data Docs misc server Xonotic.app Xonotic-SDL.app; do
    cp -a "$_dir" -t "$_install_dir"
  done

  cd "source/darkplaces"
  for _exe in nexuiz-glx nexuiz-sdl nexuiz-dedicated; do
    _xon_exe="${_exe/nexuiz/xonotic}"
    install -Dm755 "$_exe" "$_install_dir/$_xon_exe"
  done

  install -dm755 "$pkgdir"/usr/{bin,share/applications}
  install -Dm755 "$srcdir"/xonotic-{glx,sdl,dedicated} -t "$pkgdir"/usr/bin
  install -Dm644 "$srcdir"/*.desktop -t "$pkgdir"/usr/share/applications
}