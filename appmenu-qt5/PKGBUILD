# $Id: PKGBUILD 178947 2013-03-01 10:19:09Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=appmenu-qt5
pkgver=0.2.7daily13.03.28
pkgrel=1
pkgdesc="Allows Qt applications to export menus over DBus to a menu applet"
arch=('i686' 'x86_64')
url='https://launchpad.net/appmenu-qt'
license=('GPL')
depends=('libdbusmenu-qt5')
makedepends=('cmake')
source=("http://archive.ubuntu.com/ubuntu/pool/main/a/appmenu-qt/appmenu-qt_${pkgver}.orig.tar.gz")
md5sums=('24f99beff09a5bfaf54cad354bfb351b')
options=(strip debug)

build() {
  mkdir -p build
  cd build
  cmake ../appmenu-qt-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DUSE_QT5=On
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
