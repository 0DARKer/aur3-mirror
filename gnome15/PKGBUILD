# Maintainer: Nuno Araujo <nuno.araujo at russo79.com>
pkgname=gnome15
pkgver=0.6.5
pkgrel=1
pkgdesc="Gnome tools for the Logitech G Series Keyboards And Z-10 Speakers"
arch=(any)
url="http://www.gnome15.org"
license=('GPL')
depends=(python2 python-imaging python2-cairo dbus-python pygtk pyinotify
         python-lxml pygobject python-rsvg python-xlib pyusb python-pyalsaaudio
         python2-feedparser hicolor-icon-theme python2-pyopencv)
optdepends=('python2-setproctitle: Allows a process to change its title'
            'g15daemon:            G15Daemon driver'
            'g19d:                 G19D driver'
            'pylibg19:             G19 Direct driver'
           )
install=$pkgname.install
source=(http://www.gnome15.org/downloads/Gnome15/Required/${pkgname}-${pkgver}.tar.gz
        99-gnome15-kernel.rules
        python2.patch
        opencv.patch)
md5sums=('00edd6350c1666b0550427706df54f6d'
         'cda83e3a5f16e87567574eabf5362560'
         '322bc24b9bef3ce225a268adaec478f2'
         '0ee211b1fcffbb80e2b0bb64c6ecbb3a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np2 -i ${srcdir}/python2.patch
  patch -Np1 -i ${srcdir}/opencv.patch

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr --sysconfdir=/etc --enable-udev=/etc/udev/rules.d PYTHON=python2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
  cp ${srcdir}/99-gnome15-kernel.rules ${pkgdir}/etc/udev/rules.d
}

# vim:set ts=2 sw=2 et:
