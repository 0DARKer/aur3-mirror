# Maintainer: Nuno Araujo <nuno.araujo at russo79.com>
pkgname=gnome15
pkgver=0.7.5
pkgrel=3
pkgdesc="Gnome tools for the Logitech G Series Keyboards And Z-10 Speakers"
arch=(any)
url="http://www.gnome15.org"
license=('GPL')
depends=(python2 python-imaging python2-cairo dbus-python pygtk python2-pyinotify
         python-lxml pygobject pyxdg pyusb python-rsvg python-xlib 
         hicolor-icon-theme)
optdepends=('python2-setproctitle: Allows a process to change its title')
conflicts=('gnome15-systemtray')
install=$pkgname.install
source=(http://www.gnome15.org/downloads/Gnome15/Required/${pkgname}-${pkgver}.tar.gz
        python2.patch
        fix_macros.patch)
md5sums=('344047c0a5a78175edc7dd719a7e6115'
         'bd5abfdb0c8799b2fb24a1c3411b1f40'
         '23a7ee09dfc81e09438c395c4296de5d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np2 -i ${srcdir}/python2.patch
  patch -Np2 -i ${srcdir}/fix_macros.patch

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --disable-kernel-driver \
    --enable-g19direct-driver --enable-g19-driver \
    --enable-g15-driver --enable-g15direct-driver \
    --disable-indicator --enable-systemtray --disable-applet \
    --enable-udev=/etc/udev/rules.d --enable-udevgroup=gnome15 PYTHON=python2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
