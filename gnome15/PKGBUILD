# Maintainer: Nuno Araujo <nuno.araujo at russo79.com>
pkgname=gnome15
pkgver=0.7.2
pkgrel=2
pkgdesc="Gnome tools for the Logitech G Series Keyboards And Z-10 Speakers"
arch=(any)
url="http://www.gnome15.org"
license=('GPL')
depends=(python2 python-imaging python2-cairo dbus-python pygtk python2-pyinotify
         python-lxml pygobject pyxdg pyusb python-rsvg python-xlib 
         hicolor-icon-theme)
optdepends=('python2-setproctitle: Allows a process to change its title')
conflicts=('gnome15-systemtray')
install=$pkgname.install
source=(http://www.gnome15.org/downloads/Gnome15/Required/${pkgname}-${pkgver}.tar.gz
        99-gnome15-kernel.rules
        python2.patch)
md5sums=('b9ac3f8deff41e8352a59e1f41427bcb'
         'cda83e3a5f16e87567574eabf5362560'
         '3fbfcded58752d408febf3c60be7b0d5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np2 -i ${srcdir}/python2.patch

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --disable-kernel-driver \
    --enable-g19direct-driver --enable-g19-driver \
    --enable-g15-driver --enable-g15direct-driver \
    --disable-indicator --enable-systemtray --disable-applet \
    --enable-udev=/etc/udev/rules.d PYTHON=python2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
  cp ${srcdir}/99-gnome15-kernel.rules ${pkgdir}/etc/udev/rules.d
}

# vim:set ts=2 sw=2 et:
