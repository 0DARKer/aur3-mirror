# Maintainer: Nuno Araujo <nuno.araujo at russo79.com>
pkgname=gnome15
pkgver=0.4.1
pkgrel=2
pkgdesc="Gnome tools for the Logitech G Series Keyboards And Z-10 Speakers"
arch=(any)
url="http://www.tanktarta.pwp.blueyonder.co.uk/gnome15/index.html"
license=('GPL')
depends=(python2 pygtk dbus-python python-lxml python2-cairo python-imaging python-rsvg python-xlib hicolor-icon-theme python-evolution python-vobject python-pyalsaaudio  python-feedparser )
optdepends=('python2-setproctitle: Allows a process to change its title'
            'python-appindicator: Allows Gnome15 to add an indicator (replaces applet)'
            'python-wnck: for applet (must be installed when BAMF is not available for automatic macro profile switching)'
            'g15daemon: needed if you have a g15 keyboard'
            'g19d: needed if you have a g19 keyboard'
           )
install=$pkgname.install
source=(http://www.tanktarta.pwp.blueyonder.co.uk/$pkgname/$pkgname-$pkgver.tar.gz
        python2.patch)
md5sums=('8dca35a7e5d190a9782acd5456c1b5e0'
         '247305732bbafb7acbc043bf076b37e7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../python2.patch

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  export PYTHON=python2
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
