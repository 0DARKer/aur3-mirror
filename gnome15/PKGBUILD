# Maintainer: Nuno Araujo <nuno.araujo at russo79.com>
pkgname=gnome15
pkgver=0.5.1
pkgrel=1
pkgdesc="Gnome tools for the Logitech G Series Keyboards And Z-10 Speakers"
arch=(any)
url="http://www.gnome15.org"
license=('GPL')
depends=(python2 python-imaging python2-cairo dbus-python pygtk pyinotify
         python-lxml pygobject python-rsvg python-xlib pyusb python-pyalsaaudio
         python2-feedparser hicolor-icon-theme)
optdepends=('python2-setproctitle: Allows a process to change its title'
            'g15daemon: For using the G15Daemon driver'
            'g19d: For using the G19D driver'
            'pylibg19: For using the G19 Direct driver'
           )
install=$pkgname.install
source=(http://www.gnome15.org/downloads/Gnome15/Required/${pkgname}-${pkgver}.tar.gz
        python2.patch
        99-gnome15-kernel.rules)
md5sums=('a016fa44cd7e0c9f4cbd1ef63679f875'
         '322bc24b9bef3ce225a268adaec478f2'
         'cda83e3a5f16e87567574eabf5362560')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p2 < ../python2.patch

  cp ${srcdir}/99-gnome15-kernel.rules ${srcdir}/${pkgname}-${pkgver}/src/udev

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr --sysconfdir=/etc --enable-udev=/etc/udev/rules.d PYTHON=python2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
