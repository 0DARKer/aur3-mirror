# Maintainer: Nuno Araujo <nuno.araujo at russo79.com>
pkgname=gnome15
true && pkgname=('gnome15'
                 'gnome15-driver-g19direct'
                 'gnome15-driver-g15direct'
                 'gnome15-driver-kernel'
                 'gnome15-driver-g930'
                 'gnome15-driver-gtk'
                 'gnome15-systemtray')
pkgbase=gnome15
pkgver=0.9.3
pkgrel=1
pkgdesc="Gnome tools for the Logitech G Series Keyboards And Z-10 Speakers"
arch=('any')
url="http://www.russo79.com/gnome15"
license=('GPL')
depends=('python2'
         'python2-imaging'
         'python2-cairo'
         'python2-dbus'
         'pygtk'
         'python2-pyinotify'
         'python2-lxml'
         'python2-gobject2'
         'python2-xdg'
         'python2-pyusb'
         'python2-rsvg'
         'python-xlib'
         'hicolor-icon-theme'
         'python2-virtkey'
         'python2-pyinputevent'
         'python2-wnck'
         'python2-gconf'
         'python2-uinput'
         'python2-pyudev'
         'python2-keyring'
         'python2-setproctitle')
makedepends=('libg15-gnome15'
             'python2-pylibg19'
             'lg4l-gnome15')
source=(https://projects.russo79.com/attachments/download/71/${pkgname}-${pkgver}.tar.gz
        python2.patch)
md5sums=('01188d1ec3f2a5dc2ce9a7c574297bde'
         '8aa5312149d7c5cac569857b9dd74587')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np2 -i ${srcdir}/python2.patch

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --enable-kernel-driver --enable-g19direct-driver --enable-g930-driver --enable-g15direct-driver \
    --disable-indicator --enable-systemtray --disable-applet \
    --enable-udev=/usr/lib/udev/rules.d --enable-udevgroup=gnome15 PYTHON=python2
  make
}

package_gnome15() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  install=$pkgname.install

  install -d ${pkgdir}/etc/xdg/autostart
  install -d ${pkgdir}/usr/bin
  install -d ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
  install -d ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers
  install -d ${pkgdir}/usr/lib/udev/rules.d
  install -d ${pkgdir}/usr/share/applications
  install -d ${pkgdir}/usr/share/dbus-1/services
  install -d ${pkgdir}/usr/share/gnome15/glade
  install -d ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES
  install -d ${pkgdir}/usr/share/gnome15/images
  install -d ${pkgdir}/usr/share/gnome15/themes
  install -d ${pkgdir}/usr/share/gnome15/themes/default
  install -d ${pkgdir}/usr/share/gnome15/ukeys
  install -d ${pkgdir}/usr/share/icons/hicolor/16x16/status
  install -d ${pkgdir}/usr/share/icons/hicolor/22x22/apps
  install -d ${pkgdir}/usr/share/icons/hicolor/22x24/status
  install -d ${pkgdir}/usr/share/icons/hicolor/24x24/{apps,status}
  install -d ${pkgdir}/usr/share/icons/hicolor/48x48/apps
  install -d ${pkgdir}/usr/share/icons/hicolor/64x64/apps
  install -d ${pkgdir}/usr/share/icons/hicolor/scalable/{apps,devices,status}
  install -d ${pkgdir}/usr/share/man/man1

  install -t ${pkgdir}/etc/xdg/autostart \
          -m644 src/gnome/autostart/gnome15.desktop
  install -t ${pkgdir}/usr/bin \
          -m755 src/scripts/g15-config \
                src/scripts/g15-desktop-service \
                src/scripts/g15-diag \
                src/scripts/g15-launch \
                src/scripts/g15-support-dump
  install -t ${pkgdir}/usr/lib/python2.7/site-packages/gnome15 \
          -m644 src/main/python/gnome15/*.py
  install -t ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers \
          -m644 src/main/python/gnome15/drivers/__init__.py
  install -t ${pkgdir}/usr/lib/udev/rules.d \
          -m644 src/udev/98-gnome15.rules
  install -t ${pkgdir}/usr/share/applications \
          -m644 src/gnome/applications/g15-config.desktop
  install -t ${pkgdir}/usr/share/dbus-1/services \
          -m644 src/gnome/dbus/org.gnome15.Gnome15.service
  install -t ${pkgdir}/usr/share/gnome15/glade \
          -m644 src/main/resources/glade/accounts.glade \
                src/main/resources/glade/colorpicker.glade \
                src/main/resources/glade/g15-config.glade \
                src/main/resources/glade/macro-editor.glade \
                src/main/resources/glade/password.glade \
                src/main/resources/glade/script-editor.glade \
                src/main/resources/glade/redblue.png
  install -t ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES \
          -m644 src/i18n/en_GB/LC_MESSAGES/colorpicker.mo \
                src/i18n/en_GB/LC_MESSAGES/g15-config.mo \
                src/i18n/en_GB/LC_MESSAGES/gnome15-drivers.mo \
                src/i18n/en_GB/LC_MESSAGES/gnome15.mo \
                src/i18n/en_GB/LC_MESSAGES/macro-editor.mo
  install -t ${pkgdir}/usr/share/gnome15/images \
          -m644 src/main/resources/images/*.{svg,png}
  install -t ${pkgdir}/usr/share/gnome15/themes/default \
          -m644 src/themes/default/*.svg
  install -t ${pkgdir}/usr/share/gnome15/ukeys \
          -m644 src/main/resources/ukeys/*.keys \
          -m644 src/main/resources/ukeys/keysym-to-uinput
  install -t ${pkgdir}/usr/share/icons/hicolor/16x16/status \
          -m644 src/main/resources/icons/hicolor/16x16/status/*.png
  install -t ${pkgdir}/usr/share/icons/hicolor/22x22/apps \
          -m644 src/main/resources/icons/hicolor/22x22/apps/*.png
  install -t ${pkgdir}/usr/share/icons/hicolor/22x24/status \
          -m644 src/main/resources/icons/hicolor/22x22/status/*.png
  install -t ${pkgdir}/usr/share/icons/hicolor/24x24/apps \
          -m644 src/main/resources/icons/hicolor/24x24/apps/*.png
  install -t ${pkgdir}/usr/share/icons/hicolor/24x24/status \
          -m644 src/main/resources/icons/hicolor/24x24/status/*.png
  install -t ${pkgdir}/usr/share/icons/hicolor/48x48/apps \
          -m644 src/main/resources/icons/hicolor/48x48/apps/*.png
  install -t ${pkgdir}/usr/share/icons/hicolor/64x64/apps \
          -m644 src/main/resources/icons/hicolor/64x64/apps/*.png
  install -t ${pkgdir}/usr/share/icons/hicolor/scalable/apps \
          -m644 src/main/resources/icons/hicolor/scalable/apps/*.svg
  install -t ${pkgdir}/usr/share/icons/hicolor/scalable/devices \
          -m644 src/main/resources/icons/hicolor/scalable/devices/*.png
  install -t ${pkgdir}/usr/share/icons/hicolor/scalable/status \
          -m644 src/main/resources/icons/hicolor/scalable/status/*.svg
  install -t ${pkgdir}/usr/share/man/man1 \
          -m644 src/man/g15-config.1 \
                src/man/g15-desktop-service.1

  python2 -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
  python2 -O -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
}

package_gnome15-driver-g19direct() {
  true && pkgdesc="Gnome15 driver for the Logitech G19"
  true && depends=('gnome15' 'python2-pylibg19')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -d ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers
  install -d ${pkgdir}/usr/lib/udev/rules.d
  install -d ${pkgdir}/usr/share/gnome15/glade
  install -d ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES

  install -t ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers \
          -m644 src/main/python/gnome15/drivers/driver_g19direct.py
  install -t ${pkgdir}/usr/lib/udev/rules.d \
          -m644 src/udev/99-gnome15-g19direct.rules
  install -t ${pkgdir}/usr/share/gnome15/glade \
          -m644 src/main/resources/glade/driver_g19direct.glade
  install -t ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES \
          -m644 src/i18n/en_GB/LC_MESSAGES/driver_g19direct.mo

  python2 -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
  python2 -O -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
}

package_gnome15-driver-g15direct() {
  true && pkgdesc="Gnome15 driver for the Logitech G11, G13, G15, G110, Z-10"
  true && depends=('gnome15' 'libg15-gnome15')

  cd "${srcdir}/${pkgbase}-${pkgver}"


  install -d ${pkgdir}/usr/bin
  install -d ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers
  install -d ${pkgdir}/usr/lib/udev/rules.d
  install -d ${pkgdir}/usr/share/gnome15/glade
  install -d ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES

  install -t ${pkgdir}/usr/bin \
          -m755 src/scripts/libg15test
  install -t ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers \
          -m644 src/main/python/gnome15/drivers/driver_g15direct.py
  install -t ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers \
          -m644 src/main/python/gnome15/drivers/pylibg15.py
  install -t ${pkgdir}/usr/lib/udev/rules.d \
          -m644 src/udev/99-gnome15-g15direct.rules
  install -t ${pkgdir}/usr/share/gnome15/glade \
          -m644 src/main/resources/glade/driver_g15direct.glade
  install -t ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES \
          -m644 src/i18n/en_GB/LC_MESSAGES/driver_g15direct.mo

  python2 -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
  python2 -O -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
}

package_gnome15-driver-kernel() {
  true && pkgdesc="Gnome15 kernel driver"
  true && depends=('gnome15' 'lg4l-gnome15')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -d ${pkgdir}/etc/dbus-1/system.d
  install -d ${pkgdir}/usr/bin
  install -d ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers
  install -d ${pkgdir}/usr/lib/udev/rules.d
  install -d ${pkgdir}/usr/share/dbus-1/system-services
  install -d ${pkgdir}/usr/share/gnome15/glade
  install -d ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES
  install -d ${pkgdir}/usr/share/man/man1

  install -t ${pkgdir}/etc/dbus-1/system.d \
          -m644 src/gnome/dbus/g15-system-service.conf
  install -t ${pkgdir}/usr/bin \
          -m755 src/scripts/g15-system-service
  install -t ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers \
          -m644 src/main/python/gnome15/drivers/driver_kernel.py
  install -t ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers \
          -m644 src/main/python/gnome15/drivers/fb.py
  install -t ${pkgdir}/usr/lib/udev/rules.d \
          -m644 src/udev/99-gnome15-kernel.rules
  install -t ${pkgdir}/usr/share/dbus-1/system-services \
          -m644 src/gnome/dbus/org.gnome15.SystemService.service
  install -t ${pkgdir}/usr/share/gnome15/glade \
          -m644 src/main/resources/glade/driver_kernel.glade
  install -t ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES \
          -m644 src/i18n/en_GB/LC_MESSAGES/driver_kernel.mo
  install -t ${pkgdir}/usr/share/man/man1 \
          -m644 src/man/g15-system-service.1

  python2 -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
  python2 -O -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
}

package_gnome15-driver-g930() {
  true && pkgdesc="Gnome15 driver for the Logitech G930 and G35"
  true && depends=('gnome15')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -d ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers
  install -d ${pkgdir}/usr/lib/udev/rules.d
  install -d ${pkgdir}/usr/share/gnome15/glade
  install -d ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES

  install -t ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers \
          -m644 src/main/python/gnome15/drivers/driver_g930.py
  install -t ${pkgdir}/usr/lib/udev/rules.d \
          -m644 src/udev/99-gnome15-g930.rules
  install -t ${pkgdir}/usr/share/gnome15/glade \
          -m644 src/main/resources/glade/driver_g930.glade
  install -t ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES \
          -m644 src/i18n/en_GB/LC_MESSAGES/driver_g930.mo

  python2 -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
  python2 -O -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
}

package_gnome15-driver-gtk() {
  true && pkgdesc="Gnome15 emulator driver"
  true && depends=('gnome15')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -d ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers
  install -d ${pkgdir}/usr/share/gnome15/glade
  install -d ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES

  install -t ${pkgdir}/usr/lib/python2.7/site-packages/gnome15/drivers \
          -m644 src/main/python/gnome15/drivers/driver_gtk.py
  install -t ${pkgdir}/usr/share/gnome15/glade \
          -m644 src/main/resources/glade/driver_gtk.glade
  install -t ${pkgdir}/usr/share/gnome15/i18n/en_GB/LC_MESSAGES \
          -m644 src/i18n/en_GB/LC_MESSAGES/driver_gtk.mo

  python2 -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
  python2 -O -m compileall ${pkgdir}/usr/lib/python2.7/site-packages/gnome15
}

package_gnome15-systemtray() {
  true && pkgdesc="System tray icon for Gnome15"
  true && depends=('gnome15')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -d ${pkgdir}/etc/xdg/autostart
  install -d ${pkgdir}/usr/bin
  install -d ${pkgdir}/usr/share/man/man1

  install -t ${pkgdir}/etc/xdg/autostart \
          -m644 src/gnome/autostart/g15-systemtray.desktop
  install -t ${pkgdir}/usr/bin \
          -m755 src/scripts/g15-systemtray
  install -t ${pkgdir}/usr/share/man/man1 \
          -m644 src/man/g15-systemtray.1
}

# vim:set ts=2 sw=2 et:
