# Contributor: Jan de Groot <jgc@archlinux.org>
# Original Maintainer: megadriver <megadriver at gmx dot com>
# Based on gtk3 from [extra]
# Based on gtk3-ubuntu package from Luigi Ranghetti <ggranga@gmail.com>
	# Current AUR Package Maintainer: 3V0LU710N <db_eee at yahoo dot com>

pkgname=gtk3-light
pkgver=3.6.2
pkgrel=2
pkgdesc="GTK3 without colord and its baggage. Includes Ubuntu patches."
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' 'shared-mime-info' 'at-spi2-atk')
makedepends=('gobject-introspection')
provides=("gtk3=$pkgver")
conflicts=('gtk3')
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini
		patches.patch)
sha256sums=('aaccf2e1576338acf37816290578e8f19888935f381665b3847076be25fe513c'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621'
            '08b96d353b6ce6867f19c61435d92a82a0f6cfb95d41997e90f7449da184fd5b')

build() {
    cd "$srcdir/gtk+-$pkgver"

    # Apply wacom.patch and Ubuntu patches
    patch -Np1 -i "$srcdir/patches.patch"

    aclocal -I m4
    libtoolize --copy --force
    automake
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-colord \
        --disable-schemas-compile
    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "$srcdir/gtk+-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}
