# Maintainer: Nuno Araujo <nuno.araujo@russo79.com>
pkgname=gnome15-plugins
pkgver=0.9.2
pkgrel=2
pkgdesc="gnome15 plugins"
arch=(any)
url="http://www.gnome15.org/"
license=('GPL')
depends=(python2 gnome15 python-xlib)
conflicts=('gnome15-gnome-plugins')
install=$pkgname.install
source=(http://www.gnome15.org/downloads/Gnome15/Optional/${pkgname}-${pkgver}.tar.gz)
md5sums=('1c399d64c0a469dba563c858530b79da')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr \
  --enable-background --enable-cairo-clock --enable-clock --enable-fx \
  --enable-g15daemon-server --enable-im --enable-macro-recorder --enable-macros \
  --enable-profiles --enable-menu --enable-mounts --enable-mpris --enable-notify-lcd \
  --enable-panel --enable-screensaver --enable-stopwatch --enable-mediaplayer \
  --enable-weather --enable-weather-noaa --enable-weather-yahoo \
  --disable-indicator-me --disable-indicator-messages \
  --enable-volume --enable-rss --enable-sysmon --enable-processes --enable-debug \
  --enable-cal --enable-cal-evolution --enable-cal-google --enable-google-analytics \
  --enable-lcdbiff --enable-sense --enable-lcdshot --enable-tweak \
  --enable-tails --enable-display --enable-voip --enable-voip-teamspeak3 \
  PYTHON=python2 PYTHON_VERSION=2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
