# Maintainer: Nuno Araujo <nuno.araujo@russo79.com>
pkgname=gnome15-plugins
pkgver=0.7.5
pkgrel=1
pkgdesc="gnome15 plugins"
arch=(any)
url="http://www.gnome15.org/"
license=('GPL')
depends=(python2 gnome15 python-xlib)
conflicts=('gnome15-gnome-plugins')
install=$pkgname.install
source=(http://www.gnome15.org/downloads/Gnome15/Optional/${pkgname}-${pkgver}.tar.gz)
md5sums=('42b41d2e20af86109786bf6463795a93')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # python2 fix
  for file in $(find . -type f -name '*' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr \
  --enable-background --enable-cairo-clock --enable-clock --enable-fx \
  --enable-g15daemon-server --enable-im --enable-macro-recorder --enable-macros \
  --enable-menu --enable-mounts --enable-mpris --enable-notify-lcd --enable-panel \
  --enable-stopwatch --enable-videoplayer --enable-weather --enable-volume \
  --enable-rss --enable-sysmon --enable-processes --enable-webcam --enable-cal \
  --enable-lcdbiff --enable-sensors --enable-lcdshot --enable-tweak \
  --disable-lens --disable-indicator-me --disable-indicator-messages \
  PYTHON=python2 PYTHON_VERSION=2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
