# Maintainer: Fortunato Ventre (voRia) <vorione AT gmail DOT com>

pkgname=samsung-tools
pkgver=2.1
pkgrel=7
pkgdesc="Tools for Samsung laptops"
arch=('any')
url="https://launchpad.net/samsung-tools"
license=('GPL3')
depends=('pm-utils' 'xbindkeys' 'rfkill' 'python2-dbus' 'python2-notify' 'vbetool')
optdepends=('phc-intel: for CPU undervolting')
backup=(etc/samsung-tools/system.conf
        etc/samsung-tools/session.conf
        etc/samsung-tools/scripts/bluetooth-on
        etc/samsung-tools/scripts/bluetooth-off
        etc/samsung-tools/scripts/wireless-on
        etc/samsung-tools/scripts/wireless-off)
install=samsung-tools.install
source=(http://launchpad.net/samsung-tools/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        samsung-tools.service)
md5sums=('594be701b1dfc3d07b241ea3b4c53f95'
         '37f6b508a7c3c945e93ae0ee5635ff05')
         
package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # Fix shebangs in all .py files in order to use python2
  find . -name \*.py | while read FILE; do
    sed -i "1s:#!/usr/bin/env python:#!/usr/bin/python2:" "$FILE"
  done
  
  make DESTDIR="$pkgdir/" install || return 1

  # Install systemd service file
  install -D -m644 "${srcdir}/samsung-tools.service" "${pkgdir}/usr/lib/systemd/system/samsung-tools.service" || return 1
}

# vim:set ts=2 sw=2 et:

