# Maintainer: Gilbert Kennen <gilbert firewatcher org>
pkgname=elixir
pkgver=0.13.0
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="http://elixir-lang.org"
arch=('any')
license=('Apache' 'custom:EPL')
depends=('erlang')
conflicts=()
replaces=()
backup=()
source=("https://github.com/elixir-lang/elixir/tarball/v${pkgver}")
sha512sums=('55f5b8a8199d2d188bcb09761642dea78fa89e36d1af518d0170387ba811b2841d90c424924a06819b304fecf47b826cf0893c253fa664977358495f58ab967b')

_src_files_dir="elixir-lang-elixir"

build() {
  cd "${srcdir}/${_src_files_dir}"*
  make
}

check() {
  cd "${srcdir}/${_src_files_dir}"*
  make test_erlang
} 

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${srcdir}/${_src_files_dir}"*
  cp {LEGAL,LICENSE} "${pkgdir}/usr/share/licenses/${pkgname}"
  mkdir -p "${pkgdir}/usr/lib/elixir"
  cp -r "bin" "${pkgdir}/usr/lib/elixir"
  for D in `ls -F lib/ | grep \/$`
  do
    mkdir -p "${pkgdir}/usr/lib/elixir/lib/${D}"
    cp -r lib/${D}/ebin "${pkgdir}/usr/lib/elixir/lib/${D}"
  done
  mkdir -p "${pkgdir}/usr/bin"
  rm ${pkgdir}/usr/lib/elixir/bin/*.bat
  ln -sr ${pkgdir}/usr/lib/elixir/bin/* "${pkgdir}/usr/bin"
}
