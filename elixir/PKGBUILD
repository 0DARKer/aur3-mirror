# Maintainer: Gilbert Kennen <gilbert firewatcher org>
pkgname=elixir
pkgver=0.14.3
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="http://elixir-lang.org"
arch=('any')
license=('Apache' 'custom:EPL')
depends=('erlang-nox')
conflicts=()
replaces=()
backup=()
source=("https://github.com/elixir-lang/elixir/tarball/v${pkgver}")
sha512sums=('ab365a4ec3f0d25c8853dd123def5163486c3cbded2f9bb039434928f19fe5378e4630b1269e669764508023926a8b08d5a0c5a0b4522d7423dcdfc636887c96')

_src_files_dir="elixir-lang-elixir"

build() {
  cd "${srcdir}/${_src_files_dir}"*
  make
}

check() {
  cd "${srcdir}/${_src_files_dir}"*
  make test_erlang
} 

package() {
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${srcdir}/${_src_files_dir}"*
  cp {LEGAL,LICENSE} "${pkgdir}/usr/share/licenses/${pkgname}"

  make DESTDIR=$pkgdir PREFIX=/usr install
}
