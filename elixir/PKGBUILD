# Maintainer: Gilbert Kennen <gilbert firewatcher org>
pkgname=elixir
pkgver=0.14.0
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="http://elixir-lang.org"
arch=('any')
license=('Apache' 'custom:EPL')
depends=('erlang-nox')
conflicts=()
replaces=()
backup=()
source=("https://github.com/elixir-lang/elixir/tarball/v${pkgver}")
sha512sums=('cf0e7af827d9331de4d17c5c7e5453913588ae78e0ea6166024c60b76d2a251fd71f59e29fe7cd0712dfbec6e31800e46b2494e04a58ed4f4ebec58567ff020d')

_src_files_dir="elixir-lang-elixir"

build() {
  cd "${srcdir}/${_src_files_dir}"*
  make
}

check() {
  cd "${srcdir}/${_src_files_dir}"*
  make test_erlang
} 

package() {
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${srcdir}/${_src_files_dir}"*
  cp {LEGAL,LICENSE} "${pkgdir}/usr/share/licenses/${pkgname}"

  make DESTDIR=$pkgdir PREFIX=/usr install
}
