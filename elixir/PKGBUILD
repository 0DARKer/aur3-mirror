# Maintainer: Gilbert Kennen <gilbert firewatcher org>
pkgname=elixir
pkgver=0.13.1
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="http://elixir-lang.org"
arch=('any')
license=('Apache' 'custom:EPL')
depends=('erlang-nox')
conflicts=()
replaces=()
backup=()
source=("https://github.com/elixir-lang/elixir/tarball/v${pkgver}")
sha512sums=('a6d5cc7deb10e006fc238222c9abaff325dbf3cba5b17d9cad96baec25cb5e3e8e84262b5d074476ab1471389208083a489034aeb86cd086e138789ebbbabc2f')

_src_files_dir="elixir-lang-elixir"

build() {
  cd "${srcdir}/${_src_files_dir}"*
  make
}

check() {
  cd "${srcdir}/${_src_files_dir}"*
  make test_erlang
} 

package() {
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${srcdir}/${_src_files_dir}"*
  cp {LEGAL,LICENSE} "${pkgdir}/usr/share/licenses/${pkgname}"

  make DESTDIR=$pkgdir PREFIX=/usr install
}
