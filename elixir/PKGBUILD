# Maintainer: Gilbert Kennen <gilbert firewatcher org>
pkgname=elixir
pkgver=0.14.1
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="http://elixir-lang.org"
arch=('any')
license=('Apache' 'custom:EPL')
depends=('erlang-nox')
conflicts=()
replaces=()
backup=()
source=("https://github.com/elixir-lang/elixir/tarball/v${pkgver}")
sha512sums=('f2e77e1655986bb2e8c5948341d8a62232a0d8d2564781d868bad8dc7a1e7ed9b928c9b100cc727519560e73a3f396499fde10ba1c77622194a07cba1b1569ba')

_src_files_dir="elixir-lang-elixir"

build() {
  cd "${srcdir}/${_src_files_dir}"*
  make
}

check() {
  cd "${srcdir}/${_src_files_dir}"*
  make test_erlang
} 

package() {
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${srcdir}/${_src_files_dir}"*
  cp {LEGAL,LICENSE} "${pkgdir}/usr/share/licenses/${pkgname}"

  make DESTDIR=$pkgdir PREFIX=/usr install
}
