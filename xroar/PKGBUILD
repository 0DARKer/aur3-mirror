# Contributor: Boris Timofeev <mashin87@gmail.com>
pkgname=xroar
pkgver=0.27
pkgrel=1
pkgdesc="A Dragon and Tandy CoCo emulator."
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.6809.org.uk/dragon/xroar.shtml"
depends=('gtk2' 'sdl' 'libgl' 'libsndfile')
makedepends=('imagemagick')
install=xroar.install
source=(http://www.6809.org.uk/dragon/$pkgname-$pkgver.tar.gz
        xroar.desktop)
md5sums=('b1143058297fc44b1348d5b9d82b0c5f'
         'b72be2c92759966c932b6e61c1ae4fef')


build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --disable-jack || return 1
  sed -e 's|LDFLAGS =|LDFLAGS = -lm|g' -i config.mak
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m 755 xroar $pkgdir/usr/bin/xroar || return 1
  convert gp32/icon.bmp xroar.png
  install -D -m 644 xroar.png $pkgdir/usr/share/pixmaps/xroar.png || return 1
  install -D -m 644 ../xroar.desktop $pkgdir/usr/share/applications/xroar.desktop || return 1
  mkdir -p $pkgdir/usr/share/xroar/roms
  gzip ./doc/xroar.info || return 1
  install -D -m 644 ./doc/xroar.info.gz $pkgdir/usr/share/info/xroar.info.gz
}

