# Maintainer: xduugu
# Contributor: Vitaliy Berdinskikh <skipper13@archlinux.org.ua>
# Contributor: Andrea Agosti <cifvts@gmail.com>
pkgname=instantclient-basic
pkgver=11.2.0.1
pkgrel=4
pkgdesc="Light replacement for the Oracle client (files to run OCI, OCCI and JDBC-OCI programs)"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technology/tech/oci/instantclient/"
license=('custom:OTN')
depends=('gcc-libs' 'libaio')
install=$pkgname.install
#
# grab the source from
# i686  : http://www.oracle.com/technology/software/tech/oci/instantclient/htdocs/linuxsoft.html
# x86_64: http://www.oracle.com/technology/software/tech/oci/instantclient/htdocs/linuxx86_64soft.html
#
source=($pkgname-linux32-$pkgver.zip LICENSE)
md5sums=('5d8bba5d245b885dc8a6fda5ec6e6442'
         '2d62e22e4f2d7e6d220fafde0f692a7d')
sha256sums=('287c523db4681a625e9f5491598e9f69125f8580333d45223edc6c334feb3d7a'
            'f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')

if [[ $CARCH = x86_64 ]]; then
	source[0]=oracle-${pkgname%%-*}$(echo $pkgver | sed -r 's/^([0-9]+\.[0-9]+).*$/\1/')-${pkgname#*-}-${pkgver}.0-1.$CARCH.zip
	md5sums=('7d96ba339c3cb6d5ba5f2b40ed7ed02d'
           '2d62e22e4f2d7e6d220fafde0f692a7d')
	sha256sums=('0cd54a18867d831791748312e5b64ba35a25602471ac217eeffb628a8bc5ef6c'
              'f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')
fi

build() {
	local dest=$pkgdir/opt/${pkgname%%-*}

	# copy files
	local pkgver=${pkgver/./_}
	install -d "${dest%/*}" &&
	cp -a "$srcdir/${pkgname%%-*}_${pkgver%%.*}" "$dest" || return 1

	# create required symlinks
	cd "$dest" || return 1
	local lib link
	for lib in *.so*; do
		link=$lib
		while [[ ${link#*.} != so ]]; do
			link=${link%.*}
			ln -s $lib $link
		done
	done

	# fix permissions
	chmod a=rx,u+w * &&
	chmod a-x *_README *.jar || return 1

	# set ld path
	install -d "$pkgdir/etc/ld.so.conf.d" &&
	echo "/opt/instantclient" > "$pkgdir/etc/ld.so.conf.d/$pkgname.conf" || return 1

	install -Dm644 "$srcdir/LICENSE" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
}

# vim:set ts=2 sw=2 noet:
