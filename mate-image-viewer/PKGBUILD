# Maintainer: Perberos <perberos@gmail.com>
pkgname=mate-image-viewer
pkgver=20120417
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE"
arch=('i686' 'x86_64')
license=('GPL')
depends=('mate-desktop' 'libexif' 'lcms' 'desktop-file-utils' 'mate-icon-theme' 'exempi')
makedepends=('pkgconfig' 'mate-doc-utils' 'intltool' 'gtk-doc')
options=('!emptydirs' '!libtool')
conflicts=('eom')
url="http://matsusoft.com.ar/projects"
groups=('mate-extras')
install=mate-image-viewer.install
source=()
sha256sums=()

_gitroot=git://github.com/mate-desktop/mate-image-viewer.git
_gitname=mate-image-viewer

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"
	
	PYTHON=/usr/bin/python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-scrollkeeper || return 1

	make || return 1
}

package() {
	cd "$srcdir/$_gitname-build"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
