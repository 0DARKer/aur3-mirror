# Maintainer: Ren√© Herman <rene dot herman at gmail dot com>

pkgname=x48
pkgver=0.6.3
pkgrel=2
pkgdesc="Emulator for the HP-48 S/SX and G/G+/GX scientific calculator"
arch=('i686' 'x86_64')
url="http://x48.berlios.de"
license=('GPL')
depends=('readline' 'libxext')
conflicts=('x48-emulator')
install=$pkgname.install

source=(http://download.berlios.de/$pkgname/$pkgname-$pkgver.tar.gz
        app-defaults.diff)

md5sums=('1b9c815598c3ad73b8a21a421388a0b8'
         '94313aad992deb5f5bfd10ffda8c5582')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i $srcdir/app-defaults.diff
  autoreconf

  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install || return 1

  install -d $pkgdir/usr/share/$pkgname/romdump

  cd romdump
  install -m 644 -t $pkgdir/usr/share/$pkgname/romdump README ROMDump ROMDump.s
}

# vim:set ts=2 sw=2 et:
