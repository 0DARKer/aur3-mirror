# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=php-xapian
pkgver=1.2.7
pkgrel=1
pkgdesc="PHP bindings for Xapian"
arch=('i686' 'x86_64')
url="http://xapian.org/docs/bindings/php/"
license=('GPL')
depends=('php' 'xapian-core' 'util-linux')
source=(http://oligarchy.co.uk/xapian/$pkgver/xapian-bindings-$pkgver.tar.gz)
sha1sums=('79e9f0b01f1af3440d10d00469dbe248784ffa1c')

build() {
  cd "$srcdir/xapian-bindings-$pkgver"

  ./configure --prefix=/usr --with-php
  make
}

check() {
  cd "$srcdir/xapian-bindings-$pkgver"

  make -k check
}

package() {
  cd "$srcdir/xapian-bindings-$pkgver"

  make DESTDIR="$pkgdir" install

  echo ';extension=xapian.so' >xapian.ini
  install -Dm644 xapian.ini "$pkgdir/etc/php/conf.d/xapian.ini"
}

# vim:set ts=2 sw=2 et:
