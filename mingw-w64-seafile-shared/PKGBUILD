# Maintainer: julienCXX <archlinux@chmodplusx.eu>

# Contributor: Moritz Maxeiner <moritz@ucworks.org>

pkgname=mingw-w64-seafile-shared
pkgver=4.2.2
pkgrel=2
pkgdesc="Shared components of seafile: seafile-daemon, libseafile, libseafile python bindings (mingw-w64)."
arch=(any)
url="https://github.com/haiwen/seafile/"
license=('GPL')
makedepends=('mingw-w64-configure' 'vala' 'intltool')
depends=('mingw-w64-crt' 'mingw-w64-ccnet>=3.0' 'mingw-w64-curl'
'python2>=2.6')
options=('!strip' '!buildflags' 'staticlibs')
source=("seafile-${pkgver}.tar.gz::https://github.com/haiwen/seafile/archive/v${pkgver}.tar.gz")
sha256sums=('0044c8f1785a5da5298e7681ca8567084a6800f8b74164f6ff94e4320485eb8b')

#_architectures="i686-w64-mingw32 x86_64-w64-mingw32"
_architectures="i686-w64-mingw32"

prepare() {
  cd "$srcdir/seafile-${pkgver}"
  # Case fixing
  sed -i 's/<Rpc.h>/<rpc.h>/g' lib/utils.c
  sed -i 's/-lRpcrt4/-lrpcrt4/g' configure.ac
}

build()
{
  cd "$srcdir/seafile-${pkgver}"

  # Required for the build process to find searpc-codegen.py
  export PATH=/usr/i686-w64-mingw32/bin/:"$PATH"

  ./autogen.sh
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    cp -r ../lib lib
    ${_arch}-configure PYTHON=python2
    make
    popd 
  done
}

package()
{
  for _arch in ${_architectures}; do
    cd "${srcdir}/seafile-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    mkdir -p "$pkgdir"/usr/"$_arch"/lib/python2.7/site-packages/
    mv "$pkgdir"/Lib/site-packages/* "$pkgdir"/usr/"$_arch"/lib/python2.7/site-packages/
    rm -rf "$pkgdir"/Lib
    rm -rf "$pkgdir"/usr/"$_arch"/share
    ${_arch}-strip --strip-unneeded "${pkgdir}"/usr/${_arch}/bin/*.{dll,exe}
    ${_arch}-strip -g "${pkgdir}"/usr/${_arch}/lib/*.a
  done
}
