# Maintainer: William Osler <william@oslers.us> 
pkgname=willie
pkgver=4.1.0
pkgrel=1
pkgdesc="A modular IRC bot written in Python."
arch=('any')
url="http://willie.dftba.net/"
license=('custom:Eiffel Forum License, version 2')
depends=('python2' 'python2-pyopenssl')
optdepends=('mysql-python: bucket module'
'python2-feedparser: nws and rss modules'
'python2-pytz: remind module'
'python2-lxml: xkcd module'
'python2-pyenchant: spellchek module'
'python2-praw-git: reddit module'
'pygeoip'
)
conflicts=(willie-git)
source=("http://willie.dftba.net/files/$pkgname-$pkgver.tar.gz")
md5sums=('31a5b62767f44379c553600d9d612436') 
install='willie.install'

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  #Willie uses EFL2 which isn't a standard Arch license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname/"
  cp COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Config and systemd files
  mkdir -p "$pkgdir/etc/"
  cp contrib/willie.cfg "$pkgdir/etc/"
  mkdir -p "$pkgdir/usr/lib/tmpfiles.d/"
  cp contrib/willie.conf "$pkgdir/usr/lib/tmpfiles.d/"
  mkdir -p "$pkgdir/usr/lib/systemd/system/"
  cp contrib/willie.service "$pkgdir/usr/lib/systemd/system/"
  mkdir -p "$pkgdir/var/log/willie/"
}
