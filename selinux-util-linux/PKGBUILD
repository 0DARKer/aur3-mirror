# $Id: PKGBUILD 153636 2012-03-16 22:34:38Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Nicky726 <nicky726@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=selinux-util-linux
_origname=util-linux
pkgver=2.21
pkgrel=6
pkgdesc="SELinux aware miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('i686' 'x86_64')
groups=('selinux' 'selinux-system-utilities')
depends=('selinux-udev' 'selinux-usr-libselinux')
conflicts=('util-linux-ng' "${_origname}")
provides=("util-linux-ng=${pkgver}" "${_origname}=${pkgver}-${pkgrel}")
license=('GPL2')
options=('!libtool')
source=(ftp://ftp.kernel.org/pub/linux/utils/${_origname}/v${pkgver}/${_origname}-${pkgver}.tar.xz
	stable-fixes-2.21.patch)
optdepends=('perl: for chkdupexe support')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i include/pathnames.h

  patch -p1 -i ../stable-fixes-2.21.patch

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-write\
              --enable-raw\
              --disable-wall\
              --enable-libmount-mount\
              --with-selinux
  make
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"

  install -dm755 var/lib/hwclock

  # delete stray empty dir, fixed upstream
  rm -r usr/share/man/ru
}
md5sums=('208aa058f4117759d2939d1be7d662fc'
         '415b850a4efa9174cd9c34022949f177')
