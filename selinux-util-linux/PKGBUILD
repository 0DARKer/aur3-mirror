# $Id: PKGBUILD 138466 2011-09-24 11:28:44Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Nicky726 <nicky726@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=selinux-util-linux
_origname=util-linux
pkgver=2.20
pkgrel=3
pkgdesc="SELinux aware miscellaneous system utilities for Linux"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
arch=('i686' 'x86_64')
groups=('selinux' 'selinux-system-utilities')
depends=('filesystem' 'selinux-usr-libselinux')
replaces=('linux32' 'util-linux-ng')
conflicts=('linux32' 'util-linux-ng' 'e2fsprogs<1.41.8-2' "${_origname}")
provides=('linux32' "util-linux-ng=${pkgver}" "${_origname}=${pkgver}-${pkgrel}")
license=('GPL2')
options=('!libtool')
source=(http://mirror.be.gbxs.net/pub/linux/utils/${_origname}/v${pkgver}/${_origname}-${pkgver}.tar.bz2
	agetty-typo.patch
	write-freopen.patch
	dmesg-non-printk.patch
	dmesg-space.patch
	dont-close-0.patch
	fix-remount.patch)
optdepends=('perl: for chkdupexe support')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  # patches from master
  for patch in agetty-typo.patch write-freopen.patch dmesg-non-printk.patch dmesg-space.patch dont-close-0.patch fix-remount.patch; do
    patch -Np1 -i "${srcdir}/${patch}"
  done

  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i include/pathnames.h

  ./configure --enable-arch\
              --enable-write\
              --enable-raw\
              --disable-wall\
              --enable-partx\
              --enable-libmount-mount\
              --with-selinux
  make
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  install -dm755 "${pkgdir}/var/lib/hwclock"

  make DESTDIR="${pkgdir}" install
}
md5sums=('4dcacdbdafa116635e52b977d9d0e879'
         '13838c6dd8df686e0f01ad0f236d2690'
         '465817ff8f7c08411c8011ee91b50318'
         'f3ca75a1a22a2a739c5c22d92dc07ab0'
         'd9768f0b42d36d72c02ac7797b922ba1'
         '37eae52af90c214dda73874c701fa246'
         '344c780b9e08826dd8befe4271b41ed3')
