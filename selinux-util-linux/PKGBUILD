# $Id: PKGBUILD 163535 2012-07-15 00:25:36Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Nicky726 <nicky726@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=selinux-util-linux
_origname=util-linux
pkgver=2.21.2
pkgrel=5
pkgdesc="SELinux aware miscellaneous system utilities for Linux"
url="http://www.kernel.org/pub/linux/utils/util-linux/"
arch=('i686' 'x86_64')
groups=('selinux' 'selinux-system-utilities')
depends=('selinux-pam' 'selinux-usr-libselinux')
conflicts=('util-linux-ng' "${_origname}")
provides=("util-linux-ng=${pkgver}" "${_origname}=${pkgver}-${pkgrel}")
license=('GPL2')
options=('!libtool')
source=(ftp://ftp.kernel.org/pub/linux/utils/${_origname}/v2.21/${_origname}-${pkgver}.tar.xz
        pam-login
        pam-common)
backup=(etc/pam.d/chfn etc/pam.d/chsh etc/pam.d/login)
install=util-linux.install

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-fs-paths-extra=/usr/bin:/usr/sbin \
              --enable-write \
              --enable-raw \
              --disable-wall \
              --enable-new-mount \
              --enable-login-utils \
              --with-selinux
  make
}

check() {
	make -C "$_origname-$pkgver" check
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"

  # broken tool, going away in next major release, so just remove it now
  rm "${pkgdir}"/usr/{bin/chkdupexe,share/man/man1/chkdupexe.1}

  # delete stray empty dir, fixed upstream
  rm -r usr/share/man/ru

  # setuid chfn and chsh
  chmod 4755 "$pkgdir"/usr/bin/ch{sh,fn}

  # install PAM files for login-utils
  install -Dm644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chfn"
  install -m644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chsh"
  install -m644 "$srcdir/pam-login" "$pkgdir/etc/pam.d/login"
}
md5sums=('54ba880f1d66782c2287ee2c898520e9'
         '4368b3f98abd8a32662e094c54e7f9b1'
         'a31374fef2cba0ca34dfc7078e2969e4')
