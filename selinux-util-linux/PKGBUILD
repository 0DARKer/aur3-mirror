# Maintainer: Nicky726 (nicky726 <at> gmail <dot> com)
# Contributor: judd (jvinet <at> zeroflux <dot> org)
pkgname=selinux-util-linux
_origname=util-linux
pkgver=2.19.1
pkgrel=1
pkgdesc="SELinux aware miscellaneous system utilities for Linux"
arch=('i686' 'x86_64')
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
license=('GPL2')
groups=('selinux' 'selinux-system-utilities')
depends=('bash' 'ncurses>=5.7' 'zlib' 'filesystem' 'selinux-usr-libselinux')
optdepends=('perl: for chkdupexe support')
replaces=('linux32' "${_origname}-ng" "${pkgname}-ng")
conflicts=('linux32' "${_origname}" 'e2fsprogs<1.41.8-2' "${_origname}-ng" "${pkgname}-ng")
provides=('linux32' "${_origname}-ng=${pkgver}" "${_origname}=${pkgver}")
options=('!libtool')
source=(ftp://ftp.kernel.org/pub/linux/utils/${_origname}/v2.19/${_origname}-${pkgver}.tar.bz2)
md5sums=('3eab06f05163dfa65479c44e5231932c')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i hwclock/hwclock.c
  ./configure --enable-arch --enable-write --enable-raw --disable-wall \
	 --enable-partx --with-selinux
  make
}

package(){
  cd "${srcdir}/${_origname}-${pkgver}"
  install -dm755 "${pkgdir}/var/lib/hwclock"
  make DESTDIR="${pkgdir}" install
}
