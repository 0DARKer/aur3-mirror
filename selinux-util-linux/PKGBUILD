# $Id: PKGBUILD 146392 2012-01-10 11:39:58Z tomegun $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Nicky726 <nicky726@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=selinux-util-linux
_origname=util-linux
pkgver=2.20.1
pkgrel=2
pkgdesc="SELinux aware miscellaneous system utilities for Linux"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
arch=('i686' 'x86_64')
groups=('selinux' 'selinux-system-utilities')
depends=('filesystem' 'selinux-usr-libselinux')
replaces=('linux32' 'util-linux-ng')
conflicts=('linux32' 'util-linux-ng' 'e2fsprogs<1.41.8-2' "${_origname}")
provides=('linux32' "util-linux-ng=${pkgver}" "${_origname}=${pkgver}-${pkgrel}")
license=('GPL2')
options=('!libtool')
#source=(ftp://ftp.kernel.org/pub/linux/utils/${_origname}/v${pkgver}/${_origname}-${pkgver}.tar.bz2)
source=(ftp://ftp.infradead.org/pub/${_origname}/v2.20/${_origname}-${pkgver}.tar.bz2
	0001-findmnt-support-alternative-location-of-fstab.patch)
optdepends=('perl: for chkdupexe support')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i include/pathnames.h

  # backport patch needed for usr support in initramfs
  patch -p1 -i ../0001-findmnt-support-alternative-location-of-fstab.patch

  ./configure --enable-arch\
              --enable-write\
              --enable-raw\
              --disable-wall\
              --enable-partx\
              --enable-libmount-mount\
              --with-selinux
  make
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  install -dm755 "${pkgdir}/var/lib/hwclock"

  make DESTDIR="${pkgdir}" install
}
md5sums=('079b37517fd4e002a2e6e992e8b4e361'
         '823e2d87885b81f245b8c368e28f8cab')
