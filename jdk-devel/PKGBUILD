# Maintainer: Det
# Based on jdk: https://aur.archlinux.org/packages.php?ID=51906

pkgname=jdk-devel
_major=8
_build=b54
_date=30_aug_2012
pkgver=$_major$_build
pkgrel=1
pkgdesc="Java $_major Development Kit Snapshot"
url=http://jdk$_major.java.net/
arch=('i686' 'x86_64')
license=('custom')
depends=('jre-devel' 'libx11')
provides=("java-environment=$_major")
conflicts=("${provides[@]}")
backup=('etc/conf.d/derby-network-server-jre8'
        'etc/profile.d/jdk8.csh'
        'etc/profile.d/jdk8.sh')
install=$pkgname.install
_arch=i386; [ "$CARCH" = 'x86_64' ] && _arch=amd64
source=("http://download.java.net/jdk$_major/archive/$_build/binaries/jdk-$_major-ea-bin-$_build-linux-i586-$_date.tar.gz"
        'derby-network-server-jre8'
        'derby-network-server-jre8.conf'
        'java-monitoring-and-management-console8.desktop'
        'java-visualvm8.desktop'
        'jdk8.csh'
        'jdk8.sh')
md5sums=(`curl -Ls ${source/.t*}.md5 | cut -d " " -f4` # jdk-$_major-ea-bin-$_build-linux-i586/x64-$_date.tar.gz"
         '6af3e3e0f4b4262b2f9b0b757de99013'  # derby-network-server-jre8
         '4bdff6982c66d24a879c424aaac3d04d'  # derby-network-server-jre8.conf
         'ad0719b9a16c96da32ac013311eaadf6'  # java-monitoring-and-management-console8.desktop
         '0007415942182c7c209990e769e87d99'  # java-visualvm8.desktop
         'ed8584fa316608f4c263163a952d8765'  # jdk8.csh
         '63b5fd2cfd390ece0d1a48cbd03a672e') # jdk8.sh
[ "$CARCH" = 'x86_64' ] && source[0]="http://download.java.net/jdk$_major/archive/$_build/binaries/jdk-$_major-ea-bin-$_build-linux-x64-$_date.tar.gz"

package() {
  msg2 "Creating required dirs"
  cd jdk1.$_major.0
  mkdir -p "$pkgdir"/{opt/java8/{,jre/lib/$_arch},etc/{profile,rc}.d,usr/share/{applications,licenses}}

  msg2 "Fetching missing libraries from jre/ to fix jconsole8"
  mv jre/lib/$_arch/lib{saproc,attach}.so "$pkgdir"/opt/java8/jre/lib/$_arch/

  msg2 "Removing redundant dirs, licenses and .bat scripts"
  rm -r jre/ COPYRIGHT LICENSE THIRDPARTYLICENSEREADME.txt db/bin/*.bat # lib/desktop/ lib/visualvm/platform/docs/

  msg2 "Suffixing binaries, scripts and man pages with an '8'"
  for i in {,db/}bin/*; do
    mv $i ${i}8
  done
  for i in man{,/ja_JP.UTF-8}/man1/*; do
    mv $i ${i/.1}8.1
  done
  ln -sf jcontrol8 bin/ControlPanel8

  msg2 "Moving stuff in place"
  mv * "$pkgdir"/opt/java8/

  msg2 "Installing scripts, .desktop files, confs and symlinking licenses"
  cd "$srcdir"
  install -m755 jdk8.{c,}sh "$pkgdir"/etc/profile.d/
  install -Dm644 derby-network-server-jre8.conf "$pkgdir"/etc/conf.d/derby-network-server-jre8
  install -m755 derby-network-server-jre8 "$pkgdir"/etc/rc.d/
  install -m644 java-{visualvm,monitoring-and-management-console}8.desktop "$pkgdir"/usr/share/applications/
  ln -s jre8 "$pkgdir"/usr/share/licenses/jdk8
}