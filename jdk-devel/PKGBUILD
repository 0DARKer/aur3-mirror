# Maintainer: Det
# Based on jdk: https://aur.archlinux.org/packages.php?ID=51906

pkgname=jdk-devel
_major=8
_minor=20
_build=b22
_date=08_jul_2014
_pkgver=${_major}u$_minor
pkgver=${_major}u$_minor.$_build
pkgrel=1
pkgdesc="Java $_major Development Kit Snapshot"
arch=('i686' 'x86_64')
url=https://jdk$_major.java.net/
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libx11' 'libxrender' 'libxslt' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound'
            'ttf-dejavu: fonts')
provides=("java-environment=$_major" "java-runtime=$_major" "java-runtime-headless=$_major" "java-web-start=$_major")
conflicts=("${provides[@]}" "${provides[@]/$_major/7}")
backup=('etc/derby.conf'
        'opt/java/jre/lib/security/java.policy')
install=$pkgname.install
if [ "$CARCH" = 'x86_64' ]; then
  _arch=x64
  _arch2=amd64
else
  _arch=i586
  _arch2=i386
fi
source=("http://www.java.net/download/jdk$_pkgver/archive/$_build/binaries/jdk-$_pkgver-ea-bin-$_build-linux-$_arch-$_date.tar.gz"
        'derby.conf'
        'derby.service'
        'java-mission-control.desktop'
        'java-monitoring-and-management-console.desktop'
        'java-policy-settings.desktop'
        'java-visualvm.desktop'
        'javaws-launcher'
        'jdk.csh'
        'jdk.sh'
        'OTN-Early-Adopter-License-Terms.txt')
md5sums=(`curl -Ls ${source/.t*}.md5 | cut -d " " -f4` # jdk-$_pkgver-ea-bin-$_build-linux-$_arch-$_date.tar.gz"
         '4bdff6982c66d24a879c424aaac3d04d'  # derby.conf
         '77f3b7ddf55c112f97a665a825bf764f'  # derby.service
         '536ea84bba5eb6bfe8f601b41af9769c'  # java-mission-control.desktop
         'da10de5e6507c392fc9871076ef53ec6'  # java-monitoring-and-management-console.desktop
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '35fd89c5c170021d2183593335703703'  # java-visualvm.desktop
         '224bcf5e1f5d14bfa493ed243a5a5287'  # javaws-launcher
         'e81bb7853b071df6adca0b90f0c2ac2d'  # jdk.csh
         'a06d46b277e2926d73d2ee11c9950973'  # jdk.sh
         '55acc7d9282c3826afeac6157582384c') # OTN-Early-Adopter-License-Terms.txt

package() {
  msg2 "Creating required dirs"
  cd jdk1.$_major.0_$_minor
  mkdir -p "$pkgdir"/{opt/java/,usr/{lib/{mozilla/plugins,systemd/system},share/licenses/jdk/db},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Removing redundancies"
  rm -r db/bin/*.bat jre/{plugin/,COPYRIGHT,LICENSE,README,*.txt} man/ja

  msg2 "Moving stuff in place"
  mv jre/lib/desktop/* man "$pkgdir"/usr/share/
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/jdk/
  mv db/{3RDPARTY,LICENSE} "$pkgdir"/usr/share/licenses/jdk/db/
  mv * "$pkgdir"/opt/java/

  msg2 "Symlinking plugin"
  ln -s /opt/java/jre/lib/$_arch2/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing additions"
  cd "$srcdir"
  install -m755 jdk.{,c}sh "$pkgdir"/etc/profile.d/
  install -m644 derby.conf "$pkgdir"/etc/
  install -m644 derby.service "$pkgdir"/usr/lib/systemd/system/
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -m644 *.desktop "$pkgdir"/usr/share/applications/
  install -m644 OTN-Early-Adopter-License-Terms.txt "$pkgdir"/usr/share/licenses/jdk/

  msg2 "Tweaking javaws .desktop"
  sed -e 's/Exec=javaws/&-launcher %f/' \
      -e '/NoDisplay=true/d' \
      -i "$pkgdir"/usr/share/applications/sun-javaws.desktop

  # Copy/paste from system clipboard to unsigned Java applets has been disabled since 6u24:
  # - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java
  # - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html
  msg2 "Enabling copy+paste to unsigned applets"
  _line=$(awk '/permission/{a=NR}; END{print a}' "$pkgdir"/opt/java/jre/lib/security/java.policy)
  sed "$_line a\\\\n \
        // (AUR) Allow unsigned applets to read system clipboard, see:\n \
        // - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java\n \
        // - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html\n \
        permission java.awt.AWTPermission \"accessClipboard\";" \
     -i "$pkgdir"/opt/java/jre/lib/security/java.policy
}
