# Maintainer: Raimar Buehmann (raimar)

pkgname=lxpanel-git
pkgver=0.7.0.r2022.20140901
pkgrel=2
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde-git')
depends=('alsa-lib' 'lxmenu-data' 'libwnck' 'libfm')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'wireless_tools' 'git')
provides=('lxpanel')
conflicts=('lxpanel')
optdepends=('wireless_tools: netstat plugin')
source=(
	$pkgname::git+git://git.lxde.org/git/lxde/lxpanel.git
	lxpanel-volumealsa-volume-mapping.patch
	added-cpuinfo_cur_freq-for-reading-CPU-frequency.patch
)
sha256sums=('SKIP'
            'e0fd1403cfa2ce2e9b6c9b6d409017c6c316783b94784370bbae4b441a23882f'
            '23d5368dd04c9ea32ced975028dcb40603bb575a1c47bcdeb972f7ba3e274121')

pkgver() {
	# remove ".r*.*" from package version
	_pkgverTriple=$(echo $pkgver | sed "s/\.r.*//g")
	cd $pkgname
	# get number of last git commit
	_commitCount=$(git rev-list --count HEAD)
	# get time of last git commit
	_commitTime=$(git show -s --format="%ci" | grep -o "....-..-.." | sed "s/-//g")
	# add ".r*.*" from package version
	echo "$_pkgverTriple.r$_commitCount.$_commitTime"
}

prepare() {
	cd $pkgname
	# revert autohide by using timer on pointer every 300ms
	#git revert --no-edit e1fd11eee26bf6150c42216f2e825b35b48f50b7
	#git revert --no-edit 9a2c13d033233b273205e655600ff11d7e928af1
	patch -Np1 -i ../lxpanel-volumealsa-volume-mapping.patch
	patch -Np1 -i ../added-cpuinfo_cur_freq-for-reading-CPU-frequency.patch
}

build() {
	cd $pkgname
	./autogen.sh
	CFLAGS+=' -lgmodule-2.0'
	./configure --prefix=/usr --sysconfdir=/etc --enable-man
	make
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install
}
