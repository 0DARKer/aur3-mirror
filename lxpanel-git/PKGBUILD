# Contributor: Medical-Wei <in gmail dot com there is medicalwei>
# Derived from lxpanel-svn: zoulnix <z[o]ulnix.borke[dd]uck.c[o]m>
pkgname=lxpanel-git
pkgver=20100218
pkgrel=1
pkgdesc="Lightweight X11 desktop panel (part of LXDE)."
arch=('i686' 'x86_64')
url="http://lxde.org/"
license=('GPL')
depends=('alsa-lib' 'gtk2>=2.12.0' 'lxmenu-data' \
	 'menu-cache' 'startup-notification')
makedepends=('autoconf' 'automake' 'gcc' 'intltool' 'libtool' \
	     'make' 'pkgconfig' 'python' 'git')
options=('!libtool')
provides=('lxpanel')
conflicts=('lxpanel')
groups=('lxde-git')
source=()
md5sums=('')

_gitroot="git://lxde.git.sourceforge.net/gitroot/lxde/lxpanel"
_gitname="lxpanel"

build() {
  cd ${srcdir}

  #####
  msg "Connecting to GIT server..."
  if [[ -d $_gitname ]]; then
    (cd $_gitname && git pull origin)
  else
    git clone $_gitroot $_gitname
    (cd $_gitname && git checkout)
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."
  #####

  cd $_gitname

  # Disable the building of man
  sed -i -e 's:po man:po:' Makefile.am || return 1
  sed -i -e 's:man/Makefile::' configure.ac || return 1

  # Generating building system
  ./autogen.sh || return 1

  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --localstatedir=/var \
	      --disable-static

  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
