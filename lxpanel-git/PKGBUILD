# Maintainer: Raimar Buehmann (raimar)

pkgname=lxpanel-git
pkgver=0.6.99.r1862.20140612
pkgrel=1
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde-git')
depends=('alsa-lib' 'lxmenu-data' 'libwnck' 'libfm')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'wireless_tools' 'git')
provides=('lxpanel')
conflicts=('lxpanel')
optdepends=('wireless_tools: netstat plugin')
source=(
#	$pkgname::git+git://lxde.git.sourceforge.net/gitroot/lxde/lxpanel
	$pkgname::git+git://git.lxde.org/git/lxde/lxpanel.git
#	Show-power-calculated-by-voltage-and-current-in-battery-tooltip.patch
	lxpanel-volumealsa-volume-mapping.patch
	fix-error-in-i18n-file-tr.po-not-ending-with-n.patch
)
sha256sums=(
	'SKIP'
#	83bbaed8c175d5b666a5a6893fa3974f3332825b0b66ec97961ce819b2631a78
	a58a57abf9f31e0cd962742f5b97fb506c6f53d5f6d73cd3ac248e13acf745b8
	6f9b0ea75dc7b483becabda0c6fe08e608d71cef23b98289eb7328c3bcf54061
)

pkgver() {
	# remove ".r*.*" from package version
	_pkgverTriple=$(echo $pkgver | sed "s/\.r.*//g")
	cd $pkgname
	# get number of last git commit
	_commitCount=$(git rev-list --count HEAD)
	# get time of last git commit
	_commitTime=$(git show -s --format="%ci" | grep -o "....-..-.." | sed "s/-//g")
	# add ".r*.*" from package version
	echo "$_pkgverTriple.r$_commitCount.$_commitTime"
}

prepare() {
	cd $pkgname
#	patch -Np1 -i ../Show-power-calculated-by-voltage-and-current-in-battery-tooltip.patch
	patch -Np1 -i ../lxpanel-volumealsa-volume-mapping.patch
	patch -Np1 -i ../fix-error-in-i18n-file-tr.po-not-ending-with-n.patch
}

build() {
	cd $pkgname
	./autogen.sh
	CFLAGS+=' -lgmodule-2.0'
	./configure --prefix=/usr --sysconfdir=/etc --enable-man
	make
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install
}
