# Maintainer: Raimar Buehmann (raimar)

pkgname=lxpanel-git
pkgver=0.6.2.r1819.20140326
pkgrel=3
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde-git')
depends=('alsa-lib' 'lxmenu-data' 'libwnck' 'libfm')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'wireless_tools' 'git')
provides=('lxpanel')
conflicts=('lxpanel')
optdepends=('wireless_tools: netstat plugin')
source=(
	$pkgname::git+git://lxde.git.sourceforge.net/gitroot/lxde/lxpanel
	Show-power-calculated-by-voltage-and-current-in-battery-tooltip.patch
	lxpanel-volumealsa-volume-mapping.patch
)
sha256sums=(
	'SKIP'
	83bbaed8c175d5b666a5a6893fa3974f3332825b0b66ec97961ce819b2631a78
	5e52099e6e66354af28619ce92cf4c9ac4295c0bba3fcfc18e5643e25be40c85
)

pkgver() {
	# remove ".r*.*" from package version
	_pkgverTriple=$(echo $pkgver | sed "s/\.r.*//g")
	cd $pkgname
	# get number of last git commit
	_commitCount=$(git rev-list --count HEAD)
	# get time of last git commit
	_commitTime=$(git show -s --format="%ci" | grep -o "....-..-.." | sed "s/-//g")
	# add ".r*.*" from package version
	echo "$_pkgverTriple.r$_commitCount.$_commitTime"
}

prepare() {
	cd $pkgname
	patch -Np1 -i ../Show-power-calculated-by-voltage-and-current-in-battery-tooltip.patch
	patch -Np1 -i ../lxpanel-volumealsa-volume-mapping.patch
}

build() {
	cd $pkgname
	./autogen.sh
	CFLAGS+=' -lgmodule-2.0'
	./configure --prefix=/usr --sysconfdir=/etc --enable-man
	make
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install
}
