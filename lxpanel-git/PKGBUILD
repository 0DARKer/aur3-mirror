# Maintainer: Raimar Buehmann (raimar)

pkgname=lxpanel-git
pkgver=0.6.99.r1916.20140815
pkgrel=1
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde-git')
depends=('alsa-lib' 'lxmenu-data' 'libwnck' 'libfm')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'wireless_tools' 'git')
provides=('lxpanel')
conflicts=('lxpanel')
optdepends=('wireless_tools: netstat plugin')
source=(
	$pkgname::git+git://git.lxde.org/git/lxde/lxpanel.git
	dragging-of-button-do-not-toggle-it.patch
	lxpanel-volumealsa-volume-mapping.patch
	fix-error-in-i18n-file-tr.po-not-ending-with-n.patch
	changed-minimal-height-of-panel-from-2-to-1-for-autohide.patch
)
sha256sums=(
	'SKIP'
	8c417015ad98166f26b87e25e5c8deeafbec2cb3694598851e66d2c10016c53b
	60d160d849d5eb333e34fb48be346708baecabc5426bb66ce7cf91ae5e58c16f
	6f9b0ea75dc7b483becabda0c6fe08e608d71cef23b98289eb7328c3bcf54061
	da1340ebdcc955fda1491ce71fc25c20454573c03772c7fc32a49d41dd16de31
)

pkgver() {
	# remove ".r*.*" from package version
	_pkgverTriple=$(echo $pkgver | sed "s/\.r.*//g")
	cd $pkgname
	# get number of last git commit
	_commitCount=$(git rev-list --count HEAD)
	# get time of last git commit
	_commitTime=$(git show -s --format="%ci" | grep -o "....-..-.." | sed "s/-//g")
	# add ".r*.*" from package version
	echo "$_pkgverTriple.r$_commitCount.$_commitTime"
}

prepare() {
	cd $pkgname
	# revert autohide by using timer on pointer every 300ms
	git revert --no-edit e1fd11eee26bf6150c42216f2e825b35b48f50b7
	git revert --no-edit 9a2c13d033233b273205e655600ff11d7e928af1
	patch -Np1 -i ../dragging-of-button-do-not-toggle-it.patch
	patch -Np1 -i ../lxpanel-volumealsa-volume-mapping.patch
	patch -Np1 -i ../fix-error-in-i18n-file-tr.po-not-ending-with-n.patch
	patch -Np1 -i ../changed-minimal-height-of-panel-from-2-to-1-for-autohide.patch
}

build() {
	cd $pkgname
	./autogen.sh
	CFLAGS+=' -lgmodule-2.0'
	./configure --prefix=/usr --sysconfdir=/etc --enable-man
	make
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install
}
