# Maintainer: Raimar Buehmann (raimar)

pkgname=lxpanel-git
pkgver=0.7.0.r2004.20140821
pkgrel=1
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde-git')
depends=('alsa-lib' 'lxmenu-data' 'libwnck' 'libfm')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'wireless_tools' 'git')
provides=('lxpanel')
conflicts=('lxpanel')
optdepends=('wireless_tools: netstat plugin')
source=(
	$pkgname::git+git://git.lxde.org/git/lxde/lxpanel.git
	dragging-of-button-do-not-toggle-it.patch
	lxpanel-volumealsa-volume-mapping.patch
	fix-crash-after-click-on-empty-part-of-panel.patch
)
sha256sums=('SKIP'
            '570cd71d993397d4b5d9edb5115f3379a5a964468e3c6ae59f6ac395b4f74417'
            'e0fd1403cfa2ce2e9b6c9b6d409017c6c316783b94784370bbae4b441a23882f'
            'a7718eb90d5260a035589c113787705280829a4194e87e1811dc7f3fe9d5a66e')

pkgver() {
	# remove ".r*.*" from package version
	_pkgverTriple=$(echo $pkgver | sed "s/\.r.*//g")
	cd $pkgname
	# get number of last git commit
	_commitCount=$(git rev-list --count HEAD)
	# get time of last git commit
	_commitTime=$(git show -s --format="%ci" | grep -o "....-..-.." | sed "s/-//g")
	# add ".r*.*" from package version
	echo "$_pkgverTriple.r$_commitCount.$_commitTime"
}

prepare() {
	cd $pkgname
	# revert autohide by using timer on pointer every 300ms
	#git revert --no-edit e1fd11eee26bf6150c42216f2e825b35b48f50b7
	#git revert --no-edit 9a2c13d033233b273205e655600ff11d7e928af1
	patch -Np1 -i ../dragging-of-button-do-not-toggle-it.patch
	patch -Np1 -i ../lxpanel-volumealsa-volume-mapping.patch
	patch -Np1 -i ../fix-crash-after-click-on-empty-part-of-panel.patch
}

build() {
	cd $pkgname
	./autogen.sh
	CFLAGS+=' -lgmodule-2.0'
	./configure --prefix=/usr --sysconfdir=/etc --enable-man
	make
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install
}
