# Maintainer: Raimar Buehmann (raimar)

pkgname=lxpanel-git
pkgver=0.6.99.r1880.20140721
pkgrel=1
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde-git')
depends=('alsa-lib' 'lxmenu-data' 'libwnck' 'libfm')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'wireless_tools' 'git')
provides=('lxpanel')
conflicts=('lxpanel')
optdepends=('wireless_tools: netstat plugin')
source=(
	$pkgname::git+git://git.lxde.org/git/lxde/lxpanel.git
	lxpanel-volumealsa-volume-mapping.patch
	fix-error-in-i18n-file-tr.po-not-ending-with-n.patch
	changed-minimal-height-of-panel-from-2-to-1-for-autohide.patch
)
sha256sums=(
	'SKIP'
	9cd6d76ba84402f90cc178fb203c9b5501f1aea5d0895da8d76b4c9968ef5a84
	6f9b0ea75dc7b483becabda0c6fe08e608d71cef23b98289eb7328c3bcf54061
	6b4d5ddf3221c28bf7e28b3b6ed5cd572af4776e53641844d3786219a0ee523f
)

pkgver() {
	# remove ".r*.*" from package version
	_pkgverTriple=$(echo $pkgver | sed "s/\.r.*//g")
	cd $pkgname
	# get number of last git commit
	_commitCount=$(git rev-list --count HEAD)
	# get time of last git commit
	_commitTime=$(git show -s --format="%ci" | grep -o "....-..-.." | sed "s/-//g")
	# add ".r*.*" from package version
	echo "$_pkgverTriple.r$_commitCount.$_commitTime"
}

prepare() {
	cd $pkgname
	patch -Np1 -i ../lxpanel-volumealsa-volume-mapping.patch
	patch -Np1 -i ../fix-error-in-i18n-file-tr.po-not-ending-with-n.patch
	patch -Np1 -i ../changed-minimal-height-of-panel-from-2-to-1-for-autohide.patch
}

build() {
	cd $pkgname
	./autogen.sh
	CFLAGS+=' -lgmodule-2.0'
	./configure --prefix=/usr --sysconfdir=/etc --enable-man
	make
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install
}
