# Maintainer: jyantis <yantis@yantis.net>

pkgname=python2-cython-hidapi-git
pkgver=0.7.99.5.r52.8fac87b
_gitname=cython-hidapi
pkgrel=1
pkgdesc="A Cython interface to the HIDAPI from https://github.com/signal11/hidapi for Python 2"
arch=('i686' 'x86_64')
url='https://github.com/trezor/cython-hidapi'
license=('GPL')
depends=('python2' 'libusb')
source=('git+https://github.com/trezor/cython-hidapi.git')
sha256sums=('SKIP')
provides=('python2-cython-hidapi')
conflicts=('python2-cython-hidapi')
makedepends=('git')

pkgver() {
  cd ${_gitname}
  set -o pipefail

  _gitversion=$( git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)" )

  # If there is a setup.py then pull the version tag from the file
  if [ -f "setup.py" ]; then
    if grep --quiet "version = " setup.py; then
      printf "%s.%s" "$(grep -R "version = " setup.py | awk -F\' '{print $2}')" $_gitversion | sed 's/-/./g'
    elif grep --quiet "version=" setup.py; then
      printf "%s.%s" "$(grep -R "version=" setup.py | awk -F\' '{print $2}')" $_gitversion | sed 's/-/./g'
    else
      printf "%s" $_gitversion
    fi
  else
    printf "%s" $_gitversion
  fi
}

build() {
    cd ${_gitname}
    git submodule init
    git submodule update
    python2 setup.py build
}

package() {
    cd ${_gitname}
    python2 setup.py install --root="${pkgdir}" --optimize=1
}

# vim:set ts=2 sw=2 et:
