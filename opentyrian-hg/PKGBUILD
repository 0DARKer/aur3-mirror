# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=opentyrian-hg
pkgver=1018
pkgrel=1
pkgdesc="Port of the classic DOS game Tyrian"
url="http://code.google.com/p/opentyrian"
arch=(i686 x86_64)
license=(GPL3 GPL2)
depends=(sdl sdl_net)
makedepends=(mercurial)
provides=(tyrian opentyrian)
conflicts=(opentyrian)
replaces=(opentyrian-svn)
source=(opentyrian::hg+http://opentyrian.googlecode.com/hg/
        http://sites.google.com/a/camanis.net/opentyrian/tyrian/tyrian21.zip)
md5sums=('SKIP'
         '2a3b206a6de25ed4b771af073f8ca904')

pkgver() {
  cd opentyrian
  hg id -n
}

build() {
  cd opentyrian
  make release
}

package() {
  cd opentyrian

  install -D opentyrian $pkgdir/usr/bin/opentyrian
  install -Dm644 linux/man/opentyrian.6 "$pkgdir/usr/share/man/man6/opentyrian.6"
  install -Dm644 linux/opentyrian.desktop "$pkgdir/usr/share/applications/opentyrian.desktop"
  install -Dm644 linux/icons/tyrian-128.png "$pkgdir/usr/share/pixmaps/opentyrian.png"
  for _x in 22 24 32 48 128; do
    install -D -m644 "linux/icons/tyrian-$_x.png" \
      "$pkgdir/usr/share/icons/hicolor/${_x}x${_x}/apps/opentyrian.png"
  done

  cd ../tyrian21
  mkdir -p "$pkgdir/usr/share/opentyrian/data"
  for _x in *.dat *.lvl *.shp *.snd demo.* music.mus tyrend.anm tyrian.{cdt,hdt,pic} tshp2.pcx; do
    install -m644 "$_x" "$pkgdir/usr/share/opentyrian/data/"
  done
}
