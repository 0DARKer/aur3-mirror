# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
pkgname=opentyrian-hg
pkgver=946
pkgrel=1
pkgdesc="Port of the classic DOS game Tyrian"
url="http://code.google.com/p/opentyrian"
arch=(i686 x86_64)
license=(GPL3 GPL2)
depends=(sdl sdl_net)
makedepends=(mercurial)
source=("http://sites.google.com/a/camanis.net/opentyrian/tyrian/tyrian21.zip")
provides=(tyrian opentyrian)
conflicts=(opentyrian)
replaces=(opentyrian-svn)

_hgroot="http://opentyrian.googlecode.com/hg/"
_hgrepo="opentyrian"

build() {
  cd "$srcdir"

  msg "Connecting to googlecode.com Mercurial server...."
  if [ -d $_hgrepo ]; then
    (cd $_hgrepo && hg pull -u)
  else
    hg clone $_hgroot $_hgrepo
  fi

  rm -rf $_hgrepo-build
  cp -r $_hgrepo $_hgrepo-build
  cd $_hgrepo-build

  msg "Starting build process."
  
  make release || return 1
}

package() {
  cd "$srcdir/$_hgrepo-build"

  install -D opentyrian $pkgdir/usr/bin/opentyrian
  install -D -m644 linux/man/opentyrian.6 "$pkgdir/usr/share/man/man6/opentyrian.6"
  install -D -m644 linux/opentyrian.desktop "$pkgdir/usr/share/applications/opentyrian.desktop"
  install -D -m644 linux/icons/tyrian-128.png "$pkgdir/usr/share/pixmaps/opentyrian.png"
  for _x in 22 24 32 48 128; do
    install -D -m644 "linux/icons/tyrian-$_x.png" "$pkgdir/usr/share/icons/hicolor/${_x}x${_x}/apps/opentyrian.png"
  done

  cd "$srcdir/tyrian21"
  mkdir -p "$pkgdir/usr/share/opentyrian/data"
  for _x in *.dat *.lvl *.shp *.snd demo.* music.mus tyrend.anm tyrian.{cdt,hdt,pic} tshp2.pcx; do
    install -m644 "$_x" "$pkgdir/usr/share/opentyrian/data/"
  done
}

md5sums=('2a3b206a6de25ed4b771af073f8ca904')
