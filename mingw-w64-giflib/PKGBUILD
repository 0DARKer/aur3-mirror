pkgname=mingw-w64-giflib
pkgver=4.2.1
pkgrel=4
pkgdesc="A library for reading and writing gif images (mingw-w64)"
arch=(any)
url="http://sourceforge.net/projects/giflib"
license=("MIT")
makedepends=(mingw-w64-gcc xmlto)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("http://downloads.sourceforge.net/sourceforge/giflib/${pkgname#mingw-w64-}-${pkgver}.tar.bz2"
"giflib-4.2.1-quantize.patch")
md5sums=('bbd3324af24156d5d7e66ab1cee8508d'
         '26174405ba886a89b8a4db18fe653a99')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/giflib-$pkgver"
  patch -Np1 -i "$srcdir/giflib-4.2.1-quantize.patch"
  autoreconf -fi
}

build() {
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    unset LDFLAGS
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
    ${_arch}-gcc -shared lib/.libs/*.o -o giflib.dll -Xlinker --out-implib -Xlinker libgif.dll.a
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -m644 "giflib.dll" "${pkgdir}/usr/${_arch}/bin/"
    install -m644 "libgif.dll.a" "${pkgdir}/usr/${_arch}/lib/"
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm "$pkgdir/usr/${_arch}/bin/gif"{burst,compose}
  done
}