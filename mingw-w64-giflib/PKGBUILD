pkgname=mingw-w64-giflib
pkgver=5.1.0
pkgrel=3
pkgdesc="A library for reading and writing gif images (mingw-w64)"
arch=(any)
url="http://sourceforge.net/projects/giflib"
license=("MIT")
makedepends=(mingw-w64-gcc xmlto docbook-xml mingw-w64-configure)
depends=(mingw-w64-crt)
options=(staticlibs !strip !buildflags)
source=("http://downloads.sourceforge.net/sourceforge/giflib/${pkgname#mingw-w64-}-${pkgver}.tar.bz2")
md5sums=('c7e9f1c10b755ab955156d4c1ac7fc5d')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
	cd "${srcdir}/giflib-${pkgver}"
  for _arch in ${_architectures}; do
		unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
		${_arch}-configure
    make bin_PROGRAMS=''
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/giflib-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" bin_PROGRAMS='' install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
