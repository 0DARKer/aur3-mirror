# $Id$
# Maintainer: Brennan Taylor <taylor.brennan at gmail>

pkgname='golang-hg'
pkgver='latest'
pkgrel=1
pkgdesc='A compiler toolchain from Google for the Go programming language'
arch=('i686' 'x86_64')
url='http://golang.org/'
license=('custom')
depends=('perl' 'ed' 'bison' 'make' 'awk')
makedepends=('mercurial')
provides=('go')
conflicts=('go')
options=('!strip')
install=$pkgname.install
source=(go.sh)
md5sums=('bc2b023837973b4aeb6966a58564fa68')

build() {
  _hgroot="https://go.googlecode.com/hg/"
  _hgrepo="go"

  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  export GOROOT="$srcdir/$_hgrepo-build"
  export GOROOT_FINAL="/opt/go/"
  export GOOS="linux"
  export GOBIN="$GOROOT/bin"
  export PATH="$GOBIN:$PATH"

  mkdir -p "$GOROOT/bin"
  cd "$GOROOT/src"
  hg update

  . ./make.bash
}

package() {
  cd "$srcdir/go-build"

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/go/LICENSE
  install -Dm644 misc/bash/go $pkgdir/etc/bash_completion.d/go
  install -Dm644 misc/emacs/go-mode-load.el $pkgdir/usr/share/emacs/site-lisp/go-mode-load.el
  install -Dm644 misc/emacs/go-mode.el $pkgdir/usr/share/emacs/site-lisp/go-mode.el
  install -Dm644 misc/vim/syntax/go.vim $pkgdir/usr/share/vim/vimfiles/syntax/go.vim

  mkdir -p $pkgdir/{etc/profile.d,opt/go/{share,lib,lib/src}}

  cp -r bin $pkgdir/opt/go
  cp -r doc misc -t $pkgdir/opt/go/share
  cp -r pkg $pkgdir/opt/go/lib

  install -Dm644 src/Make.* $pkgdir/opt/go/lib/src

  # Headers for C modules
  install -Dm644 src/pkg/runtime/runtime.h $pkgdir/opt/go/src/pkg/runtime/runtime.h
  install -Dm644 src/pkg/runtime/cgocall.h $pkgdir/opt/go/src/pkg/runtime/cgocall.h

  install $srcdir/go.sh $pkgdir/etc/profile.d/

  # Point Python scripts to the python2 binary
  cd $pkgdir/opt/go/share/misc/dashboard
  sed -i 's/python$/\02/' googlecode_upload.py godashboard/app.yaml
}
