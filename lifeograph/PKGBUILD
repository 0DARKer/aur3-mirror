# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Anton Larionov <diffident dot cat at gmail dot com>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=lifeograph
pkgver=1.0.1
pkgrel=1
pkgdesc="An off-line and private journal and note taking application."
arch=('i686' 'x86_64')
url="https://launchpad.net/lifeograph"
license=('GPL3')
depends=('gtkmm3' 'enchant' 'desktop-file-utils')
makedepends=('cmake')
install="$pkgname.install"
source=("http://launchpad.net/lifeograph/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz")
md5sums=('05f954b0646942ab62d0755365782895')

prepare() {
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
