# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=sludge
pkgver=2.1.1
pkgrel=1
pkgdesc="Scripting Language for Unhindered Development of a Gaming Environment consists of an engine and development kit for the creation of 2D adventure games."
arch=(i686 x86_64)
url="http://opensludge.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('glee' 'alure' 'libvpx' 'gtkglext' 'libvorbis' 'sdl' 'desktop-file-utils' 'hicolor-icon-theme')
conflicts=("$pkgname-engine")
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/opensludge/SLUDGE%20Engine%20and%20Development%20Kit/$pkgver/$pkgname-$pkgver-source.tar.gz")
md5sums=('9781dba6c74fd76e3bd6529897f0ed83')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # include missing header
  sed -i "14i#include <cstddef>" source/Engine/libwebm/mkvparser.hpp

  ./configure --prefix=/usr \
              --enable-devkit \
              --enable-doc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
