# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=SLUDGE%20Engine%20and%20Development%20Kit
pkgname=sludge
pkgver=2.2
pkgrel=2
pkgdesc="An engine and development kit for the creation of 2D adventure games."
arch=(i686 x86_64)
url="http://opensludge.sourceforge.net/"
license=('GPL3' 'LGPL')
depends=('glee' 'alure' 'libvpx' 'gtkglext' 'sdl' 'dumb' 'desktop-file-utils' 'hicolor-icon-theme')
conflicts=("$pkgname-engine")
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/opensludge/$_name/$pkgver/$pkgname-$pkgver-source.tar.gz"
        "$pkgname-gcc47.patch")
md5sums=('29874d25678256065d296457771cc928'
         'ba2a7f2ded85230d5153d94be41a9b9a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.7 patch
  patch -p1 -i ../${source[1]}

  ./configure --prefix=/usr \
              --enable-devkit \
              --enable-doc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
