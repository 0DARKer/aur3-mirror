# Maintainer: Adam Nielsen <malvineous@shikadi.net>
# Based on mingw32-boost-static

pkgname=mingw32-boost
pkgver=1.49.0
_boostver=${pkgver//./_}
pkgrel=1
pkgdesc="Free peer-reviewed portable C++ source libraries (mingw32 version)"
arch=(any)
_arch_target=i486-mingw32
url="http://www.boost.org/"
license=('custom')
provides=('mingw32-boost')
conflicts=('mingw32-boost')
depends=('mingw32-gcc')
# Depends on native Boost so we can use its bjam instead of building it ourself first
makedepends=('boost' 'bzip2')
options=(!strip)
source=(http://downloads.sourceforge.net/sourceforge/boost/boost_${_boostver}.tar.bz2)
md5sums=('0d202cb811f934282dea64856a175698')

build() {
	cd ${srcdir}/boost_${_boostver}

	cat > user-config.jam << EOF
using gcc : 4.6 : ${_arch_target}-g++
	:
	<rc>${_arch_target}-windres
	<archiver>${_arch_target}-ar
;
EOF

	/usr/bin/bjam \
		toolset=gcc \
		target-os=windows \
		variant=release \
		threading=multi \
		threadapi=win32 \
		link=shared,static \
		runtime-link=shared \
		--prefix=${pkgdir}/usr/${_arch_target} \
		--user-config=user-config.jam \
		--without-mpi \
		--without-python \
		-sNO_BZIP2=1 \
		-sNO_ZLIB=1 \
		${MAKEFLAGS} \
		--layout=tagged install

	# --layout=tagged will add the "-mt" suffix for multithreaded libraries
	# and installs includes in /usr/include/boost.
	# --layout=system no longer adds the -mt suffix for multi-threaded libs.

#  find ${pkgdir}/usr/i486-mingw32 -name *.a | xargs i486-mingw32-ranlib
	${_arch_target}-strip -x $pkgdir/usr/${_arch_target}/lib/*.dll
	${_arch_target}-strip -g $pkgdir/usr/${_arch_target}/lib/*.a
}
