# Maintainer: ant32 <antreimer@gmail.com>

pkgname=mingw-w64-poppler
pkgver=0.26.2
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0 (mingw-w64)"
arch=(any)
url="http://poppler.freedesktop.org"
license=("GPL")
makedepends=('mingw-w64-gcc'
             'mingw-w64-pkg-config'
             'mingw-w64-glib2'
             'mingw-w64-qt5-base'
             'mingw-w64-qt4')
depends=('mingw-w64-crt'
         'mingw-w64-libtiff'
         'mingw-w64-openjpeg'
         'mingw-w64-cairo')
optdepends=("mingw-w64-glib2: libpoppler-glib"
            "mingw-w64-qt5-base: libpoppler-qt5"
            "mingw-w64-qt4: libpoppler-qt4")
options=('!strip' '!buildflags' 'staticlibs')
source=("http://poppler.freedesktop.org/poppler-${pkgver}.tar.xz"
        'poppler_mingw.patch'
        'poppler_pkgconfig_private_libs.patch'
        'poppler_qt5-configureac.patch')
sha256sums=('1dd9963a82eba00b7a8c9f57a7d1d787be22f5e6cb0aead5dfe602775d0d9526'
            'b212985ac522a949cbe3822859f486ba11e2bc6c8c15e55bbdddec7f936fbbe6'
            '56bd4120739b607e859d6adcd2bfa4071aed1c347f59df6e950c9b7aac1dc8ba'
            'c8c9d2837a7162773f5fbb2703fbf048611cd3038eda998bd90ef93612a49190')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/poppler-$pkgver"

  # Add Libs.private sections to pkg-config
  patch -p1 -i "$srcdir/poppler_pkgconfig_private_libs.patch"
  # Fix issues when building in mingw environment
  patch -p1 -i "$srcdir/poppler_mingw.patch"
  # Fix Qt5 detection
  patch -p1 -i "$srcdir/poppler_qt5-configureac.patch"
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/build-${_arch}"
    cd "${srcdir}/build-${_arch}"
    # curl is disabled because in my qt5 application I couldn't use the postgresql driver
    "${srcdir}"/poppler-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-xpdf-headers \
      --enable-cairo-output \
      --enable-poppler-qt4 \
      --enable-poppler-qt5 \
      --enable-poppler-glib \
      --enable-libjpeg \
      --enable-libopenjpeg \
      --disable-zlib \
      --disable-gtk-test \
      --disable-utils \
      --disable-gtk-doc-html \
      --with-font-configuration=win32
      
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}