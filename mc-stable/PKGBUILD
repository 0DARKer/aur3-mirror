# Contributer: siigot <vilosim@gmail.com>

pkgname=mc-stable
pkgver=4.7.5.6
pkgrel=1
pkgdesc="Midnight Commander - a visual file manager, stable branch"
arch=('i686' 'x86_64')
url="https://www.midnight-commander.org"
license=('GPL')
depends=('e2fsprogs' 'glib2' 'pcre' 'slang')
optdepends=('p7zip: support for 7zip archives' 'mtools: a+ extfs'
	'cdparanoia: audio extfs' 'gawk: hp48+ extfs' 'cdrkit: iso9660 extfs'
	'perl: needed by several extfs scripts' 'python-boto: s3+ extfs'
	'python2-pytz: s3+ extfs' 'p7zip: u7z extfs' 'unace: uace extfs'
	'unarj: uarj extfs' 'cabextract: ucab extfs' 'unrar: urar extfs'
	'zip: uzip extfs')
provides=('mcedit-pkgbuild-syntax')
conflicts=('mc' 'mc-utf8')
replaces=('mc' 'mc-utf8')
options=('!emptydirs' '!makeflags')
source=("http://www.midnight-commander.org/downloads/mc-${pkgver}.tar.bz2")
sha256sums=('61b9ec63ddee8dcb6dc3e74b40d02ba51d16829efbf9f9770fe6ff3b9b6607ac')

build() {
	cd ${srcdir}/mc-${pkgver}

	./configure \
		--prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
		--libexecdir=/usr/lib --disable-static --without-gpm-mouse \
		--without-x --enable-vfs-smb --with-screen=slang

	make
}

package() {
	cd ${srcdir}/mc-${pkgver}
	make DESTDIR=${pkgdir} install

	# Fix FS#18312
	rm "${pkgdir}/usr/lib/mc/extfs.d/u7z"

	sed 's#/usr/bin/env python#/usr/bin/python2#' \
		-i "${pkgdir}/usr/lib/mc/extfs.d/s3+"
}
