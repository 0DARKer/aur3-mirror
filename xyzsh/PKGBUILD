# Contributor: Doug Newgard <scimmia22 at outlook dot com>\

pkgname=xyzsh
pkgver=1.5.6
pkgrel=1
pkgdesc="A shell script language under linux, OSX, cygwin, and FreeBSD."
arch=('i686' 'x86_64')
url="http://sourceforge.jp/projects/xyzsh/"
license=('MIT')
depends=('oniguruma' 'ncurses')
options=('!emptydirs')
source=("$pkgname-$pkgver.tgz::http://sourceforge.jp/frs/redir.php?m=osdn&f=%2Fxyzsh%2F59934%2Fxyzsh-$pkgver.tgz")
md5sums=('5695746f2b2996f6a842126a1db23ff9')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-optimize

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
