# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: L42y <423300@gmail.com>

pkgname=oath-toolkit-git
pkgver=2.0.2.13.gdedc503
pkgrel=1
pkgdesc='OATH one-time password toolkit - git checkout'
arch=('i686' 'x86_64')
url='http://www.nongnu.org/oath-toolkit/'
license=('GPL3')
depends=('glibc' 'pam' 'xmlsec')
options=('!libtool')
makedepends=('help2man' 'gengetopt' 'datefudge')
provides=('oath-toolkit')
conflicts=('oath-toolkit')
source=('git://git.sv.gnu.org/oath-toolkit.git')

pkgver() {
	cd oath-toolkit/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd oath-toolkit/

	# create configure script
	make -f cfg.mk

	# configure and build
	./configure --prefix=/usr --disable-valgrind-tests
	make
}

check() {
	cd oath-toolkit/

	make check
}

package() {
	cd oath-toolkit/

	make DESTDIR="${pkgdir}" install
	
	# Install basic docs too
	install -D -m 644 pam_oath/README "${pkgdir}/usr/share/doc/${pkgname}/PAM_README"
	install -D -m 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

sha256sums=('SKIP')
