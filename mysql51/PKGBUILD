# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Danny Navarro <j@dannynavarro.net>

pkgname=mysql51
pkgver=5.1.66
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="A fast SQL database server"
backup=('etc/mysql/my.cnf')
url="http://www.mysql.com/"
depends=('openssl' 'gcc-libs')
makedepends=('zlib' 'perl' 'libtool')
optdepends=('perl-dbi' 'perl-dbd-mysql')
options=('!libtool')
provides=('mysql' 'libmysqlclient' 'mysql-clients')
conflicts=('mysql' 'libmysqlclient' 'mysql-clients')
install="${pkgname}.install"
source=("http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQL-5.1/mysql-${pkgver}.tar.gz"
        'mysqld'
        'my.cnf'
        'skip-abi-check.patch')

build() {
  cd "${srcdir}/mysql-${pkgver}"
  patch -Np0 -i "${srcdir}/skip-abi-check.patch"
  # CFLAGS/CXXFLAGS as suggested upstream
  CFLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
  CXXFLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
  ./configure --prefix=/usr \
    --libexecdir=/usr/sbin \
    --localstatedir=/var \
    --sysconfdir=/etc/mysql \
    --without-docs \
    --with-readline \
    --without-libedit \
    --with-ssl \
    --with-zlib-dir=/usr \
    --with-charset=utf8 \
    --with-collation=utf8_general_ci \
    --with-extra-charsets=complex \
    --with-embedded-server \
    --with-unix-socket-path=/var/run/mysqld/mysqld.sock \
    --enable-local-infile \
    --with-plugins=innobase,innodb_plugin \
    --datadir=/var/lib/mysql
  make
}

package() {
  cd "${srcdir}/mysql-${pkgver}"
  make DESTDIR=${pkgdir} install
  
  # create library symlinks in /usr/lib
  ln -sf mysql/libmysqlclient.so.16 ${pkgdir}/usr/lib/libmysqlclient.so.16
  ln -sf libmysqlclient.so.16 ${pkgdir}/usr/lib/libmysqlclient.so
  ln -sf libmysqlclient.so.16 ${pkgdir}/usr/lib/libmysqlclient.so.1
  ln -sf mysql/libmysqlclient_r.so.16  ${pkgdir}/usr/lib/libmysqlclient_r.so.16
  ln -sf libmysqlclient_r.so.16 ${pkgdir}/usr/lib/libmysqlclient_r.so
  ln -sf libmysqlclient_r.so.16 ${pkgdir}/usr/lib/libmysqlclient_r.so.1

  install -Dm644 ${srcdir}/my.cnf ${pkgdir}/etc/mysql/my.cnf
  install -Dm755 ${srcdir}/mysqld ${pkgdir}/etc/rc.d/mysqld

  rm -r ${pkgdir}/usr/{sql-bench,mysql-test}

  install -dm700 ${pkgdir}/var/lib/mysql
}
sha256sums=('eb85e3fa152949670f5b6f4379bd1d700a5f4726660ce18ff0ca628190cba907'
            '04c2b7f7c10d2ffd0a2c778e1a442a72512bb2debbf7b2bfeb8c08ee64655e67'
            'e8ce9078b9e758f83259957dc90ba30f9097a0cf0a2ae99efa20dd002c87ad3f'
            '6a3c4702097bd7e5575030d4c37ca7d628ff9f2d6ed499ca3f55638a3f99668e')
