# Contributor: Matthew Bauer <mjbauer95@gmail.com>
# Maintainer: Steven Allen <steven@stebalien.com>

_pkgname=pithos
pkgname=$_pkgname-git
pkgver=338
pkgrel=1
epoch=1
pkgdesc='Pandora Internet Radio player for GNOME'
arch=('any')
url="http://pithos.github.io/"
license=('GPL3')
depends=('python2' 'gtk3' 'python2-gobject' 'python2-dbus' 'gst-plugins-good' 'gst-plugins-bad' 'gst-plugins-base' 'hicolor-icon-theme' 'python2-pylast')
makedepends=('git' 'python2-setuptools' 'python2-distutils-extra')
optdepends=('libkeybinder3: for media keys plugin'
            'gst-plugins-ugly: MP3 playback support')
provides=("$_pkgname")
conflicts=("$_pkgname-bzr" "$_pkgname")
install="$pkgname.install"

source=("$pkgname.install" "git+https://github.com/pithos/pithos.git")

pkgver() {
  cd "$srcdir/$_pkgname"
  git rev-list HEAD --count
}

package() {
  cd "$srcdir/$_pkgname"
  python2 setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}"
}

md5sums=('c9db50a1fd3d86efe4d6f8a7b044e718'
         'SKIP')

# vim:set ts=2 sw=2 et:
