# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=orc
pkgname=lib32-$_pkg
pkgver=0.4.13
pkgrel=1
pkgdesc="The Oild Runtime Compiler. Multilib"
arch=('x86_64')
license=('custom:UNLICENSE')
url="http://code.entropywave.com/projects/orc/"
depends=('lib32-glibc' "$_pkg")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://code.entropywave.com/download/$_pkg/$_pkg-$pkgver.tar.gz")
md5sums=('f6d0aed003f67c91933c22d9e5f1696a')

build () {

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "$srcdir/$_pkg-$pkgver"

  ./configure --prefix=/usr \
	--libdir=/usr/lib32 \
	--disable-static

  make
}

package() {

  cd "$srcdir/$_pkg-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Removing unneeded files
  rm -rf "$pkgdir"/usr/{bin,include,share}

  # license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
