# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=orc
pkgname=lib32-$_pkg
pkgver=0.4.14
pkgrel=1
pkgdesc="The Oild Runtime Compiler. Multilib"
arch=('x86_64')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('lib32-glibc' "$_pkg")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://code.entropywave.com/download/$_pkg/$_pkg-$pkgver.tar.gz")
md5sums=('6b3ff209e9763ebe40e152538884bd71')

build() {
  cd "$srcdir/$_pkg-$pkgver"

  # multilib
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkg-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Removing unneeded files
  rm -rf "$pkgdir"/usr/{bin,include,share}

  # license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
