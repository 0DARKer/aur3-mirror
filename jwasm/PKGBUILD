# Maintainer: Andreas B. Wagner <AndreasBWagner@pointfree.net>
# Contributor: Florian LÃ©ger <florian6 dot leger at laposte dot net>

pkgname=jwasm
_pkgname=JWasm209s
pkgver=2.09
pkgrel=1
pkgdesc="a free MASM-compatible assembler"
arch=('i686' 'x86_64')
url="http://www.japheth.de/JWasm.html"
license=('custom')
depends=('glibc')
makedepends=('hd2u' 'unzip')
source=("http://www.japheth.de/Download/JWasm/${_pkgname}.zip")
noextract=("${_pkgname}.zip")
md5sums=('67245c8046a634a7bfb7dcb726fa3ef1')

build() {
	mkdir -p $srcdir/${pkgname}-${pkgver}
	cd $srcdir/${pkgname}-${pkgver}
	unzip ../${_pkgname}.zip
	make -f GccUnix.mak
	test -r README.TXT && mv README.TXT Readme.txt
	dos2unix *.txt Doc/*.txt
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	mkdir -p ${pkgdir}/usr/bin
	mkdir -p ${pkgdir}/usr/share/{doc,licenses}/jwasm
	install -m755 GccUnixR/jwasm ${pkgdir}/usr/bin/jwasm
	install -m644 History.txt Readme.txt Doc/enh.txt Doc/fixes.txt Doc/gencode.txt Doc/overview.txt ${pkgdir}/usr/share/doc/jwasm/
	install -m644 License.txt ${pkgdir}/usr/share/licenses/jwasm/
}
