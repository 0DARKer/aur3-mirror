# Contributor: Spyros Stathopoulos <foucault.online@gmail.com>
pkgname=python2-labjackpython-git
pkgver=20130206
pkgrel=1
pkgdesc="Python module for communicating with LabJack devices"
arch=('any')
url="http://labjack.com/support/labjackpython"
license=('MIT')
depends=('python2' 'labjack-exodriver')
conflicts=('python2-labjackpython')
provides=('python2-labjackpython')
makedepends=('git')
source=('LICENSE')
md5sums=('6b654d05134aa959b7db1f4b05c7f33d')

_gitroot="git://github.com/labjack/LabJackPython.git"
_gitname="LabJackPython"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  python2 setup.py build
}

package() {

  cd "${srcdir}/$_gitname-build"
  mkdir -p ${pkgdir}/usr/share/doc/labjackpython
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
  install -D -m644 ../LICENSE \
    ${pkgdir}/usr/share/licenses/python2-labjackpython/LICENSE
  cp -dpr --no-preserve=ownership \
    Examples ${pkgdir}/usr/share/doc/labjackpython

}

