# Maintainer:  Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>

pkgname=cgit
pkgver=0.9
pkgrel=1
pkgdesc="A web interface for git written in plain C"
arch=('i686' 'x86_64')
url="http://hjemli.net/git/cgit"
license=('GPL')
depends=('openssl')
makedepends=('git>=1.5.3' 'zlib' 'curl' 'asciidoc')
backup=('etc/cgitrc')
install=cgit.install
source=(http://hjemli.net/git/cgit/snapshot/${pkgname}-${pkgver}.tar.bz2
        cgitrc)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make get-git
  make
  make doc-man
}

package() {
  # The binary cgit.cgi stores the build directory. I'm not sure why yet.
  # makepkg will print a warning.

  cd ${srcdir}/${pkgname}-${pkgver}
  make CGIT_SCRIPT_PATH=/srv/http/cgit DESTDIR=${pkgdir} install install-man
  mkdir -p ${pkgdir}/var/cache/cgit
  mkdir -p ${pkgdir}/etc
  install -m0644 ${srcdir}/cgitrc ${pkgdir}/etc/cgitrc
}

md5sums=('b55f953c5c8a418bca72b3de3c297270'
         '8e167898efcb6a59b4649968e6595e61')
