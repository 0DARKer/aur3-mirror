# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=teeworlds-ddnet-git
_name=teeworlds-ddnet
pkgver=6.6.4.r5521.4ee13df
pkgrel=1
pkgdesc="A customized version by DDRaceNetwork of this 2D shooting game (Git)"
arch=('i686' 'x86_64')
url="http://ddnet.tw"
license=('custom')
depends=('alsa-lib' 'glu' 'sdl' 'freetype2')
makedepends=('git' 'bam' 'gendesk' 'imagemagick')
provides=('teeworlds' 'teeworlds-ddnet')
conflicts=('teeworlds')
source=($pkgname::git+https://github.com/def-/ddnet)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  v=`grep GAME_RELEASE_VERSION src/game/version.h | cut -d\" -f2` # get version
  r=`git rev-list --count HEAD` # get git revision
  h=`git rev-parse --short HEAD` # get sha1's short output
  echo $v.r$r.$h
}

prepare() {
  convert "$pkgname/other/icons/DDNet.ico" "$srcdir/$pkgname.png"
  gendesk -f -n --pkgname "$_name" --pkgdesc "$pkgdesc" \
    --name 'Teeworlds' --categories 'Game;ArcadeGame'
}

build() {
  cd $pkgname
  bam release
}

package() {
  cd $pkgname
  
    # install binaries
  install -Dm755 DDNet "$pkgdir"/usr/bin/teeworlds-ddnet
  install -Dm755 DDNet-Server "$pkgdir"/usr/bin/teeworlds-ddnet_srv
  install -Dm755 dilate "$pkgdir"/usr/bin/dilate

    # Install data files
  mkdir -p ${pkgdir}/usr/share/teeworlds/data
  cp -r data/* ${pkgdir}/usr/share/teeworlds/data
  
    # Install desktop and icon files
  install -Dm644 "$srcdir/$_name.desktop" "$pkgdir/usr/share/applications/$_name.desktop"
  install -Dm644 "$srcdir/$pkgname-0.png" "$pkgdir/usr/share/pixmaps/$_name.png"
  
    # Install license files
  install -Dm644 "license.txt" "$pkgdir/usr/share/licenses/$pkgname/license.txt"
  install -Dm644 "license_DDRace.txt" "$pkgdir/usr/share/licenses/$pkgname/license_DDRace.txt"
}
