# Note: Normal esd will work fine instead of esd-oss, but esd-oss is the OSS
#       version of esd, so... :)  Grab it in the AUR.

# Maintainer:  Devin Cofer <ranguvar{DOT]archlinux[DOT}us>
# Contributor: Hugo Doria  <hugo@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=mpg123-oss
_pkgname=mpg123
pkgver=1.12.3
pkgrel=1
pkgdesc="A console-based real-time MPEG-1/2/3 audio player (OSS version)"
arch=('i686' 'x86_64')
url="http://www.mpg123.org/"
license=('GPL2' 'LGPL2')

depends=('oss')
makedepends=('esound-oss' 'sdl')
optdepends=('esound-oss: For additional audio support'
            'sdl: For additional audio support')
provides=('mpg321' "mpg123=$pkgver")
conflicts=('mpg321' 'mpg123')
replaces=('mpg321')

source=("http://downloads.sourceforge.net/sourceforge/mpg123/mpg123-$pkgver.tar.bz2")
sha256sums=('ae0fe1bd85971421c24a35ebfc3662942a7815f57228091e69033279744246c6')


options=('libtool')
build()
{
	cd "$srcdir/$_pkgname-$pkgver"

	./configure --prefix=/usr --with-audio="oss esd sdl" || return 1
	make || return 1
}
package()
{
	cd "$srcdir/$_pkgname-$pkgver"
	make DESTDIR="$pkgdir" install || return 1
}
