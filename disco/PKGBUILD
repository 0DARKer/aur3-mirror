# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Yejun Yang (yejunx AT gmail DOT com)

pkgname=disco
pkgver=0.3.2
pkgrel=1
pkgdesc="Open-source implementation of the Map-Reduce framework for distributed computing"
arch=('any')
url="http://discoproject.org/"
license=('custom:BSD')
depends=(erlang python2 python-cjson)
makedepends=(setuptools)
optdepends=(openssh)
backup=(etc/disco/settings.py)
install=disco.install
source=(https://github.com/tuulos/$pkgname/tarball/$pkgver
        disco-master
        disco-node)

md5sums=('47a740b84ef61cea8ba3644610e25083'
         '98a2953fba3a0946b7e319b3be47d487'
         'b241a00d78b193a756d8c58907851287')

build() {
  cd "$srcdir"/*/

  make PYTHON=python2

  cd bin
    for script in *.py
    do
      sed --expression='s/python$/python2/' --in-place "$script"
    done
  cd ..
}

package() {
  cd "$srcdir"/*/

  make DESTDIR="$pkgdir" PYTHON=python2 PREFIX=/usr install

  install --directory "$pkgdir/srv/disco/data"
  install --directory "$pkgdir/var/run/disco"
  install --directory "$pkgdir/var/log/disco"
  install --directory "$pkgdir/etc/rc.d"
  install --directory "$pkgdir/usr/share/licenses/$pkgname"
  install --mode=755 "$srcdir/disco-master" "$pkgdir/etc/rc.d/"
  install --mode=755 "$srcdir/disco-node" "$pkgdir/etc/rc.d/"
  install --mode=755 LICENSE "$pkgdir/usr/share/licenses/$pkgname/"
}
