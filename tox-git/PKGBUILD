# Maintainer: Boohbah <boohbah at gmail.com>
# Contributor: naxuroqa <naxuroqa at gmail.com>

pkgname=tox-git
_gitname=ProjectTox-Core
pkgver=1681.706b6ec
pkgrel=1
pkgdesc="A FOSS instant messaging application aimed to replace Skype (git version)"
arch=('i686' 'x86_64')
url="http://tox.im/"
license=('GPL3')
depends=('libsodium-git' 'libconfig>=1.4' 'ffmpeg')
makedepends=('git')
conflicts=('tox')
provides=('tox')
source=('git://github.com/irungentoo/ProjectTox-Core.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  autoreconf -fi
  ./configure --prefix="/usr"
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
}
