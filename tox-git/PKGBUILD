# Maintainer: Boohbah <boohbah at gmail.com>
# Contributor: naxuroqa <naxuroqa at gmail.com>

pkgname=tox-git
pkgver=r2603.77df398
pkgrel=1
pkgdesc="A FOSS instant messaging application aimed to replace Skype (git version)"
arch=('i686' 'x86_64')
url="https://tox.im/"
license=('GPL3')
depends=('libconfig' 'libsodium' 'libvpx' 'opus')
makedepends=('check' 'git')
conflicts=('tox')
provides=('tox')
source=("${pkgname}::git+https://github.com/irungentoo/toxcore")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  autoreconf -i
  ./configure --prefix="/usr" --enable-ntox --enable-daemon
  make
}

check() {
  cd "${pkgname}"
  make check
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
}
