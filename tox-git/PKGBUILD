# Maintainer: Boohbah <boohbah at gmail.com> 

pkgname=tox-git
_gitname=ProjectTox-Core
pkgver=c3b5fe3
pkgrel=1
pkgdesc="A FOSS instant messaging application aimed to replace Skype."
arch=('i686' 'x86_64')
url="https://github.com/irungentoo/ProjectTox-Core"
license=('GPL')
depends=('libsodium' 'libconfig>=1.4')
makedepends=('git' 'cmake')
source=('git://github.com/irungentoo/ProjectTox-Core.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's/-/./g'
}

build() {
  cd $_gitname
  mkdir build && cd build
  cmake ..
  make
}

package() {
  cd $_gitname/build/testing
  install -Dm755 nTox "$pkgdir/usr/bin/nTox"
  install -Dm755 DHT_test "$pkgdir/usr/bin/"
  install -Dm755 Messenger_test "$pkgdir/usr/bin/"
  install -Dm755 Lossless_UDP_testclient "$pkgdir/usr/bin/"
  install -Dm755 Lossless_UDP_testserver "$pkgdir/usr/bin/"
  install -Dm755 toxic/toxic "$pkgdir/usr/bin/"
  install -Dm644 ../../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
