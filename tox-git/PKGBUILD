# Maintainer: Boohbah <boohbah at gmail.com>
# Contributor: naxuroqa <naxuroqa at gmail.com>

pkgname=tox-git
pkgver=2366.7a11c10
pkgrel=1
pkgdesc="A FOSS instant messaging application aimed to replace Skype (git version)"
arch=('i686' 'x86_64')
url="http://tox.im/"
license=('GPL3')
depends=('libsodium')
makedepends=('git')
conflicts=('tox')
provides=('tox')
source=("${pkgname}::git://github.com/irungentoo/ProjectTox-Core.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${pkgname}"
  autoreconf -i
  ./configure --prefix="/usr"
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
}
