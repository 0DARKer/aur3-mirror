# Packager: Walter Dworak <preparationh67@gmail.com>

pkgname=perl-path-isdev
pkgver=1.001002
_modnamespace=Path
_perlmod=Path-IsDev
pkgrel=1
pkgdesc="Determine if a given path resembles a development source tree"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl' 'perl-file-homedir' 'perl-test-fatal' 'perl-sub-exporter' 'perl-module-runtime' 'perl-role-tiny' 'perl-path-tiny' 'perl-class-tiny')
url="http://search.cpan.org/dist/Path-isDev"
source=("http://search.cpan.org/CPAN/authors/id/K/KE/KENTNL/${_perlmod}-${pkgver}.tar.gz")
md5sums=('8907c626bb16558151f7dc11f1c47719')

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd ${_perlmod}-${pkgver}
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd ${_perlmod}-${pkgver}
    make test
)

package() {
    cd ${_perlmod}-${pkgver}
    export PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"
    export PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" 
    make install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
