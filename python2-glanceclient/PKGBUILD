# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=glanceclient
pkgname=python2-$_pkgname
pkgver=0.8.0
pkgrel=1
pkgdesc="OpenStack image service (client libs)"
arch=(any)
url=https://launchpad.net/glance
license=(custom)
depends=(python2-anyjson python2-argparse python2-boto python2-crypto python2-eventlet
    python2-greenlet python2-httplib2 python2-iso8601 python2-kombu python2-lxml python2-migrate
    python2-passlib python2-paste python2-paste-deploy python2-routes python2-sendfile
    python2-sqlalchemy python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-distribute)
source=(http://pypi.python.org/packages/source/${pkgname:0:1}/${pkgname//2}/${pkgname//2}-$pkgver.tar.gz)
sha256sums=('46bfe15087437f19499c803394334a470081804f3f23f19dcc5a83608c01d383')
sha512sums=('1ca10562d1acecb8de46e84753328dc3cefe0a7afd554b93b3ed05edca70520677ddbaaa7aaa05e019bfc3ae1bd7ecec879ef961a3fe638c523e8ad41eb133e6')

build() {
    cd ${pkgname//2}-$pkgver/
    find -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    python2 setup.py build
}

package() {
    cd ${pkgname//2}-$pkgver/
    python2 setup.py install --root="$pkgdir"
}
