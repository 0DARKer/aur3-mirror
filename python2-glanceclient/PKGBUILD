# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=python2-glanceclient
pkgver=0.17.0
pkgrel=1
pkgdesc="OpenStack image service (client libs)"
arch=(any)
url=https://launchpad.net/glance
license=(custom)
depends=(python2-anyjson python2-crypto python2-eventlet python2-greenlet python2-httplib2
    python2-iso8601 python2-kombu python2-lxml python2-migrate python2-netaddr python2-passlib
    python2-paste python2-paste-deploy python2-routes python2-sendfile python2-sqlalchemy
    python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-setuptools)
source=(https://pypi.python.org/packages/source/${pkgname:0:1}/${pkgname//2}/${pkgname//2}-$pkgver.tar.gz)
sha256sums=('2e83b9100ad56beee9cbe343ee0a5233ac23ab015538feaa3e89400fa2d7fc81')
sha512sums=('c4837985228fde78e051638ea89fc79fad1a054b7db3f669c3c3cd4f4c4e84c89e867758a71739069b347bb9670626b3c81a789670e28ded9ac26e28b7a933d7')

prepare() {
    find ${pkgname//2}-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd ${pkgname//2}-$pkgver/
    python2 setup.py build
}

package() {
    cd ${pkgname//2}-$pkgver/
    python2 setup.py install --root="$pkgdir"
}
