# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=python2-glanceclient
pkgver=0.12.0
pkgrel=1
pkgdesc="OpenStack image service (client libs)"
arch=(any)
url=https://launchpad.net/glance
license=(custom)
depends=(python2-anyjson python2-crypto python2-eventlet python2-greenlet python2-httplib2
    python2-iso8601 python2-kombu python2-lxml python2-migrate python2-netaddr python2-passlib
    python2-paste python2-paste-deploy python2-routes python2-sendfile python2-sqlalchemy
    python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-setuptools)
source=(https://pypi.python.org/packages/source/${pkgname:0:1}/${pkgname//2}/${pkgname//2}-$pkgver.tar.gz)
sha256sums=('d7de76f8ccdd889c9c0cd7d035fc20247fcafb8405295bc6aa1505d5692b2fa3')
sha512sums=('79508049e60185aa77336b5c9db71ab7ec547206581ec5e1101b511201186fe653362b433c129cb766dacd66ff0e64c1f01564e8e53e4c2fbc2eb3704663c275')

prepare() {
    find ${pkgname//2}-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd ${pkgname//2}-$pkgver/
    python2 setup.py build
}

package() {
    cd ${pkgname//2}-$pkgver/
    python2 setup.py install --root="$pkgdir"
}
