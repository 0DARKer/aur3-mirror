# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=python2-glanceclient
pkgver=0.9.0
pkgrel=1
pkgdesc="OpenStack image service (client libs)"
arch=(any)
url=https://launchpad.net/glance
license=(custom)
depends=(python2-anyjson python2-argparse python2-boto python2-crypto python2-eventlet
    python2-greenlet python2-httplib2 python2-iso8601 python2-kombu python2-lxml python2-migrate
    python2-passlib python2-paste python2-paste-deploy python2-routes python2-sendfile
    python2-sqlalchemy python2-webob python2-wsgiref python2-xattr)
makedepends=(python2-distribute)
source=(http://pypi.python.org/packages/source/${pkgname:0:1}/${pkgname//2}/${pkgname//2}-$pkgver.tar.gz)
sha256sums=('708ecd7031b61d335624b51507607736f276504667a8a76421803cdfe2b32360')
sha512sums=('8c373bbce946c1c500e58a73a49233a13cf76422324d485387e55ddaf2482a5eacea9fba1a40938eff7c22738d067af3a5b4ea7e5631ce1018ec8f7193bf5a5a')

prepare() {
    find ${pkgname//2}-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd ${pkgname//2}-$pkgver/
    python2 setup.py build
}

package() {
    cd ${pkgname//2}-$pkgver/
    python2 setup.py install --root="$pkgdir"
}
