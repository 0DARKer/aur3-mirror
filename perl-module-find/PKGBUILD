# Maintainer: jsteel <jsteel at aur.archlinux.org>
# Contributor: Alan Young <harleypig@gmail.com>

pkgname='perl-module-find'
pkgver='0.11'
pkgrel='3'
pkgdesc="Find and use installed modules in a (sub)category"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.6.1')
url='http://search.cpan.org/dist/Module-Find'
source=(http://search.cpan.org/CPAN/authors/id/C/CR/CRENZ/Module-Find-$pkgver.tar.gz)
md5sums=('420a0796a1fbdfd5704df754872db7e1')

build() {
  export MODULEBUILDRC=/dev/null                                \
         PERL5LIB=""                                            \
         PERL_AUTOINSTALL=--skipdeps                            \
         PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
         PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
         PERL_MM_USE_DEFAULT=1

  cd "$srcdir"/Module-Find-$pkgver

  if [[ -e ./Build.PL ]]; then
    /usr/bin/perl Build.PL
    ./Build
  elif [[ -e ./Makefile.PL ]]; then
    /usr/bin/perl Makefile.PL
    make
  fi
}

check() {
  export MODULEBUILDRC=/dev/null                                \
         PERL5LIB=""                                            \
         PERL_AUTOINSTALL=--skipdeps                            \
         PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
         PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
         PERL_MM_USE_DEFAULT=1

  cd "$srcdir"/Module-Find-$pkgver

  if [[ -e ./Build.PL ]]; then
    ./Build test
  elif [[ -e ./Makefile.PL ]]; then
    make test
  fi
}

package() {
  export MODULEBUILDRC=/dev/null                                \
         PERL5LIB=""                                            \
         PERL_AUTOINSTALL=--skipdeps                            \
         PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
         PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
         PERL_MM_USE_DEFAULT=1

  cd "$srcdir"/Module-Find-$pkgver

  if [[ -e ./Build.PL ]]; then
    ./Build install
  elif [[ -e ./Makefile.PL ]]; then
    make install
  fi

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
