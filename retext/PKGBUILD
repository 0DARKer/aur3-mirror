# Maintainer:	Bartosz Chmura <chmurli@gmail.com>
# Contributor:	alfplayer

pkgname=retext
pkgver=2.1.4
pkgrel=1
_icons_rev=3
pkgdesc="Text editor for Markdown and reStructuredText"
arch=('any')
url="http://sourceforge.net/p/retext/"
license=('GPL3')
depends=('python2'
	 'python2-qt'
	 'python2-markdown'
	 'qt>=4')
optdepends=('python-gdata: needed if you want to use Google Docs export')
source=("http://sourceforge.net/projects/retext/files/ReText-${pkgver%.*}/ReText_${pkgver}.tar.gz"
	    "http://sourceforge.net/projects/retext/files/Icons/ReTextIcons_r${_icons_rev}.tar.gz"
	    'retext.desktop')
options=(!strip docs)
md5sums=('01f3d938ee366390110f6d1fb6f26b35'
         'bf54cc33ea1eab8bdd7d24cb46448a0d'
         '82186876da0a52991d3f81324a52401d')

build () {

	# Use Python 2
	sed -i '1s/python/python2/' retext.py wpgen/wpgen.py

}

package () {

	share_dir=${pkgdir}/usr/share

	retext_dir=${share_dir}/${pkgname}

	# Create directories
	install -d -m 755 ${pkgdir}/usr/bin ${share_dir}/{wpgen,icons,mime/packages,applications,${pkgname}/{locale,doc}}

	# Install scripts retext and wpgen
	install -m 755 ${pkgname}.py ${pkgdir}/usr/bin/retext
	install -m 755 wpgen/wpgen.py ${pkgdir}/usr/bin/wpgen

	install -m 644 README ${retext_dir}/doc/

	# Install translation files
	install -m 644 locale/* ${retext_dir}/locale/

	# Install wpgen data
	install -m 644 wpgen/templates/* ${share_dir}/wpgen/

	# Install icon
	install -m 644 icons/* ${share_dir}/icons/
	install -m 644 *.png ${share_dir}/icons/

	# Install .desktop file
	install -m 644 retext.desktop ${share_dir}/applications/

}

