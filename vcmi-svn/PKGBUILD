# Maintainer: Raziel23 <venom23 at runbox dot com>
# Contributor: Kevin Whitaker <eyecreate at gmail dot com>

pkgname=vcmi-svn
pkgver=3171
pkgrel=1
pkgdesc="Heroes of Might and Magic 3 game engine"
arch=('i686' 'x86_64')
url="http://forum.vcmi.eu/portal.php"
license=('GPL')
depends=('boost-libs' 'ffmpeg' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_ttf' 'zlib')
makedepends=('boost' 'cmake' 'subversion')
optdepends=('innoextract: required by vcmibuilder'
            'unshield: required by vcmibuilder'
            'unzip: required by vcmibuilder'
            'wget: required by vcmibuilder')
install=vcmi-svn.install

_svntrunk='https://vcmi.svn.sourceforge.net/svnroot/vcmi/trunk'
_svnmod='vcmi'

build() {
  cd "${srcdir}"

  if [[ -d "${_svnmod}" ]]; then
    (cd "${_svnmod}" && svn up -r "${pkgver}")
  else
    svn co "${_svntrunk}" -r "${pkgver}" "${_svnmod}"
  fi

  if [[ -d "${_svnmod}-build" ]]; then
    msg 'Removing the last build ...'
    rm -rf "${_svnmod}-build"
  fi

  mkdir "${_svnmod}-build" && cd "${_svnmod}-build"
  cmake "../${_svnmod}" -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_SKIP_RPATH='OFF' \
    -DCMAKE_BUILD_TYPE='Debug' # by default enable debugging
  make
}

package() {
  cd "${srcdir}/${_svnmod}-build"
  make DESTDIR="${pkgdir}" install
}
