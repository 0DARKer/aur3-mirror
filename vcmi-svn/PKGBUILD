# Contributor: Kevin Whitaker <eyecreate at gmail dot com>
# Fix: zidane

pkgname=vcmi-svn
pkgver=2672
pkgrel=1
pkgdesc="Heroes of Might and Magic 3 remake"
arch=('i686' 'x86_64')
url="http://forum.vcmi.eu/portal.php"
license=('GPL')
depends=('sdl' 'boost' 'sdl_mixer' 'sdl_image' 'sdl_ttf' 'zlib' 'ffmpeg')
makedepends=('subversion')
options=(!libtool)
source=('http://download.vcmi.eu/vcmi_088.zip')
md5sums=('569dadd51794dbfcb4ac64ab1892c0d5')

_svntrunk=https://vcmi.svn.sourceforge.net/svnroot/vcmi/trunk
_svnmod=vcmi

heroes3_path=/opt/heroes3

build() {
    if [ -d ${_svnmod} ]; then
        (cd ${_svnmod} && svn up -r ${pkgver})
    else
        svn co ${_svntrunk} -r ${pkgver} ${_svnmod}
    fi

    cd ${_svnmod}
    autoreconf -i
    ./configure --datadir=/opt --bindir=/opt/vcmi --libdir=/opt

    make
}

package() {
    install -d "${pkgdir}"/opt/vcmi/Sprites
    install -d "${pkgdir}"/opt/vcmi/Data/s
    install -d "${pkgdir}"/opt/vcmi/Maps
    install -d "${pkgdir}"/opt/vcmi/Games
    install -d "${pkgdir}"/opt/vcmi/config

    install -m644 Sprites/* "${pkgdir}"/opt/vcmi/Sprites
    install -m644 Data/* "${pkgdir}"/opt/vcmi/Data || true
    install -m644 Data/s/* "${pkgdir}"/opt/vcmi/Data/s
    install -m644 Maps/* "${pkgdir}"/opt/vcmi/Maps
    #install -m644 Games/* "${pkgdir}"/opt/vcmi/Games

    cd ${_svnmod}
    
    install -m644 config/* "${pkgdir}"/opt/vcmi/config
    
    make DESTDIR="${pkgdir}" install

    install -d "${pkgdir}"/usr/bin
    ln -s /opt/vcmi/vcmiclient "${pkgdir}"/usr/bin/vcmiclient
    ln -s /opt/vcmi/vcmiserver "${pkgdir}"/usr/bin/vcmiserver

    # symlink MP3 dir
    ln -s ${heroes3_path}/MP3 "${pkgdir}"/opt/vcmi/Mp3

    # symlink Maps dir
    find ${heroes3_path}/Maps -type d -printf "mkdir -p \"${pkgdir}/opt/vcmi/Maps/%P\"\n" -o -type f -printf "ln -s \"%p\" \"${pkgdir}/opt/vcmi/Maps/%P\"\n" | sh

    # symlink Data dir
    find ${heroes3_path}/Data -type d -printf "mkdir -p \"${pkgdir}/opt/vcmi/Data/%P\"\n" -o -type f -printf "ln -s \"%p\" \"${pkgdir}/opt/vcmi/Data/%P\"\n" | sh
}
