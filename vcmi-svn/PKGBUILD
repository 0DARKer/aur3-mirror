# Maintainer: Raziel23 <venom23 at runbox dot com>
# Contributor: Kevin Whitaker <eyecreate at gmail dot com>

pkgname=vcmi-svn
pkgver=3026
pkgrel=1
pkgdesc="Heroes of Might and Magic 3 game engine"
arch=('i686' 'x86_64')
url="http://forum.vcmi.eu/portal.php"
license=('GPL')
depends=('boost-libs' 'ffmpeg' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_ttf' 'zlib')
makedepends=('boost' 'cmake' 'subversion')
optdepends=('innoextract: required by vcmibuilder'
			'unshield: required by vcmibuilder'
			'unzip: required by vcmibuilder'
			'wget: required by vcmibuilder')
install=vcmi-svn.install

_svntrunk='https://vcmi.svn.sourceforge.net/svnroot/vcmi/trunk'
_svnmod='vcmi'
_installation_dir='/usr' # the installation root directory of vcmi

build() {
	cd $srcdir

    if [ -d $_svnmod ]; then
        (cd $_svnmod && svn up -r $pkgver)
    else
        svn co $_svntrunk -r $pkgver $_svnmod
    fi

	if [ -d build ]; then
		msg "Removing the last build ..."
		rm -rf build
	fi

	mkdir build && cd build
	cmake ../vcmi -DCMAKE_INSTALL_PREFIX=$_installation_dir \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_SKIP_RPATH=OFF \
		-DCMAKE_BUILD_TYPE=Debug # by default enable debugging
	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR=$pkgdir install
	chmod +x "$pkgdir$_installation_dir/bin/vcmibuilder"

	# if the installation root directory is different than /usr then symlink some files to /usr
	if [[ "$_installation_dir" != "/usr" ]]; then
		cd $pkgdir

		install -d "$pkgdir/usr/bin"
		ln -s "$_installation_dir/bin/vcmiclient" \
			"$pkgdir/usr/bin/vcmiclient"
		ln -s "$_installation_dir/bin/vcmiserver" \
			"$pkgdir/usr/bin/vcmiserver"
		ln -s "$_installation_dir/bin/vcmibuilder" \
			"$pkgdir/usr/bin/vcmibuilder"

		install -d "$pkgdir/usr/share/applications"
		ln -s "$_installation_dir/share/applications/vcmiclient.desktop" \
			"$pkgdir/usr/share/applications/vcmiclient.desktop"

		install -d "$pkgdir/usr/share/icons/hicolor/"{"32x32","48x48","64x64"}"/apps"
		ln -s "$_installation_dir/share/icons/hicolor/32x32/apps/vcmiclient.png" \
			"$pkgdir/usr/share/icons/hicolor/32x32/apps/vcmiclient.png"
		ln -s "$_installation_dir/share/icons/hicolor/48x48/apps/vcmiclient.png" \
			"$pkgdir/usr/share/icons/hicolor/48x48/apps/vcmiclient.png"
		ln -s "$_installation_dir/share/icons/hicolor/64x64/apps/vcmiclient.png" \
			"$pkgdir/usr/share/icons/hicolor/64x64/apps/vcmiclient.png"
	fi
}
