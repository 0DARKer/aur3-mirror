# Maintainer: Andreas Krinke <andreas dot krinke at gmx dot de>

pkgname=mingw32-tcl
pkgver=8.5.8
pkgrel=1
pkgdesc="The Tcl scripting language"
arch=('any')
url="http://www.tcl.sourceforge.net/"
license=('custom')
makedepends=('mingw32-gcc' 'mingw32-w32api' 'wine')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
md5sums=('7f123e53b3daaaba2478d3af5a0752e3')

build() {
  cd ${srcdir}/tcl${pkgver}/win
  
  # mingw32 have problems with --hash-style=gnu (default value)
  unset LDFLAGS

  RC=i486-mingw32-windres \
  AR=i486-mingw32-ar \
  RANLIB=i486-mingw32-ranlib \
  ./configure --host=i486-mingw32 --prefix=/usr/i486-mingw32 --enable-threads
  
  sed -i -e "s,\./\$(TCLSH),wine \$(TCLSH)," Makefile
  
  make || return 1
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf tclsh85.exe ${pkgdir}/usr/i486-mingw32/bin/tclsh.exe
  # install license
  install -Dm644 ../license.terms ${pkgdir}/usr/i486-mingw32/share/licenses/${pkgname}/LICENSE
  
  # remove buildroot traces / fixes #3602
  sed -i \
  -e "s,^TCL_BUILD_LIB_SPEC='-L.*/win,TCL_BUILD_LIB_SPEC='-L/usr/i486-mingw32/lib," \
  -e "s,^TCL_SRC_DIR='.*',TCL_SRC_DIR='/usr/i486-mingw32/include'," \
  -e "s,^TCL_BUILD_STUB_LIB_SPEC='-L.*/win,TCL_BUILD_STUB_LIB_SPEC='-L/usr/i486-mingw32/lib," \
  -e "s,^TCL_BUILD_STUB_LIB_PATH='.*/win,TCL_BUILD_STUB_LIB_PATH='/usr/i486-mingw32/lib," \
  -e "s,^TCL_CC_SEARCH_FLAGS='\(.*\)',TCL_CC_SEARCH_FLAGS='\1:/usr/i486-mingw32/lib'," \
  -e "s,^TCL_LD_SEARCH_FLAGS='\(.*\)',TCL_LD_SEARCH_FLAGS='\1:/usr/i486-mingw32/lib'," \
  ${pkgdir}/usr/i486-mingw32/lib/tclConfig.sh
}
