# Mantainer Jens Staal <staal1978@gmail.com>
# Copied from official ncurses build by: Allan McRae <allan@archlinux.org>

pkgname=ncurses-git
pkgver=20120613
_release=5.9
pkgrel=1
pkgdesc="unofficial git mirror of snapshots from ftp://invisible-island.net/ncurses/current/"
arch=('i686' 'x86_64')
url="http://ncurses.scripts.mit.edu/?p=ncurses.git;a=summary"
#url="http://www.gnu.org/software/ncurses/"
license=('MIT')
depends=('glibc')
makedepends=('git')
provides=('ncurses')
replaces=('ncurses')
conflicts=('ncurses')

_gitroot='git://ncurses.scripts.mit.edu/ncurses.git'
_gitname='ncurses'

build() {
  cd ${srcdir}/
  rm -rf ncurses{,w}-build # start cleam...
  mkdir ncurses{,w}-build

    msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting configure and make..."
  
  cd ${srcdir}/ncursesw-build
  ../ncurses/configure --prefix=/usr --mandir=/usr/share/man \
     --with-shared --with-normal --without-debug --without-ada \
     --with-install-prefix=${pkgdir} --enable-widec \
     --enable-pc-files
  # add --enable-ext-colors with next soname bump
  make

  # libncurses.so.5 for external binary support 
  cd ${srcdir}/ncurses-build
  [ $CARCH = "x86_64" ] && CONFIGFLAG="--with-chtype=long"
  ../ncurses/configure --prefix=/usr \
    --with-shared --with-normal --without-debug --without-ada \
    --with-install-prefix=${pkgdir} $CONFIGFLAG
  make
}

package() {
  cd ${srcdir}/ncursesw-build
  make install

  # Fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses form panel menu; do
    echo "INPUT(-l${lib}w)" >${pkgdir}/usr/lib/lib${lib}.so
    ln -s lib${lib}w.a ${pkgdir}/usr/lib/lib${lib}.a
  done
  ln -s libncurses++w.a ${pkgdir}/usr/lib/libncurses++.a

  for lib in ncurses ncurses++ form panel menu; do
    ln -s ${lib}w.pc ${pkgdir}/usr/lib/pkgconfig/${lib}.pc
  done

  # Some packages look for -lcurses during build
  echo "INPUT(-lncursesw)" >${pkgdir}/usr/lib/libcursesw.so
  ln -s libncurses.so ${pkgdir}/usr/lib/libcurses.so
  ln -s libncursesw.a ${pkgdir}/usr/lib/libcursesw.a
  ln -s libncurses.a ${pkgdir}/usr/lib/libcurses.a

  # non-widec compatibility library
  cd ${srcdir}/ncurses-build        
  install -Dm755 lib/libncurses.so.${_release} ${pkgdir}/usr/lib/libncurses.so.${_release}
  ln -s libncurses.so.${_release} ${pkgdir}/usr/lib/libncurses.so.5

  # install license, rip it from the readme
  cd ${srcdir}/ncurses
  install -dm755 ${pkgdir}/usr/share/licenses/$pkgname
  grep -B 100 '$Id' README > ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
} 
