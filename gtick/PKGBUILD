# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: damir <damir@archlinux.org>

pkgname=gtick
pkgver=0.5.1
pkgrel=1
pkgdesc="A Gtk metronome application supporting different meters and speeds."
arch=('i686' 'x86_64')
url="http://www.antcom.de/gtick/"
license=('GPL3')
depends=('gtk2' 'libpulse')
makedepends=('libsndfile')
optdepends=('pulseaudio: pulseaudio sound system support')
install="$pkgname.install"
source=("http://www.antcom.de/gtick/download/$pkgname-$pkgver.tar.gz"
        "http://www.antcom.de/gtick/mcenaney.png"
	"$pkgname.desktop")
md5sums=('281202be0cbc4849c8df86966c6725f2'
         '01f8a09c9f9486df868963c67adecf73'
         '862cbb21d3ddbd0cdbcc9afdc141414f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 ../mcenaney.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
