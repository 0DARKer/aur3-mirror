# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=netextender
pkgver=4.0.665
pkgrel=1
pkgdesc="Client for SonicWALL's sslvpn product"
arch=('i686' 'x86_64')
url="http://www.sonicwall.com/us/products/Secure_Remote_Access.html"
license=('custom')
depends=('ppp' 'openssl' 'module-init-tools')
source=("http://code.toofishes.net/uploads/NetExtender $pkgver.zip"
        netExtender.sh)
md5sums=('b46541ce44bb06d23bd712d2f3257746'
         'c334e073843aa551af5e24a77681e354')
sha256sums=('7b614de1631488d83bb9f4c185ad88a7c3216e52f6c3967a8d66e6e6208a3fc0'
            'e34eee048b4e949904572007daa031846c33afd173a8a1c4aabc0fa90734a87a')

build() {
  cd "$srcdir"
  bsdtar xf "NetExtender $pkgver.zip"

  msg "Using arch $CARCH"
  [[ $CARCH == 'i686' ]] && bsdtar xf NetExtender.Linux.$pkgver.i386.tgz
  [[ $CARCH == 'x86_64' ]] && bsdtar xf NetExtender.Linux.$pkgver.x86_64.tgz

  cd netExtenderClient
}

package() {
  cd "$srcdir/netExtenderClient"
  install -D -m644 sslvpn "$pkgdir/etc/ppp/peers/sslvpn"
  install -D -m755 sslvpnroute "$pkgdir/etc/ppp/ip-up.d/sslvpnroute"
  install -D -m755 sslvpnroute "$pkgdir/etc/ppp/ip-down.d/sslvpnroutecleanup"
  # ip-up and ip-down only look for *.sh by default, so link the above
  # paths, since the above files are hardcoded in the fucking binary
  ln -s sslvpnroute "$pkgdir/etc/ppp/ip-up.d/sslvpnroute.sh"
  ln -s sslvpnroutecleanup "$pkgdir/etc/ppp/ip-down.d/sslvpnroutecleanup.sh"
  # PID file? or executable script. sigh
  install -D -m755 sslvpn.pid "$pkgdir/etc/ppp/sslvpn.pid"
  install -D -m644 sslvpn.pid2 "$pkgdir/etc/ppp/sslvpn.pid2"

  # we use a script on $PATH, the real executable is hidden away
  install -D -m755 ../netExtender.sh "$pkgdir/usr/sbin/netExtender"

  # and the real stuff
  mkdir -p $pkgdir/usr/lib/netextender
  install -m755 netExtender "$pkgdir/usr/lib/netextender/"
  install -m755 libNetExtender.so "$pkgdir/usr/lib/"
  install -D -m644 netExtender.man "$pkgdir/usr/share/man/man1/netExtender.1"

  # fake some library links because their binary sucks ass
  cd "$pkgdir/usr/lib/netextender"
  ln -s /usr/lib/libcrypto.so libcrypto.so.6
  ln -s /usr/lib/libssl.so libssl.so.6

  # this is just bunk; another hardcoded path in the binary...
  mkdir "$pkgdir/sbin"
  ln -s /bin/lsmod "$pkgdir/sbin/lsmod"
}

# vim:set ts=2 sw=2 et:
