# Maintainer: Harley Laue <losinggeneration@gmail.com>
pkgname=zerobrane-studio-git
pkgver=20121213
pkgrel=2
pkgdesc="A lightweight Lua-based IDE for Lua"
arch=(any)
url="http://studio.zerobrane.com/"
license=('MIT')
depends=('wxlua' 'lua51-socket' 'hicolor-icon-theme')
makedepends=('git' 'cmake')
provides=('zerobrane-studio')
conflicts=('zerobrane-studio')
optdepends=('love: to debug love programs')
install=zerobrane-studio.install
source=('zbstudio.patch')
md5sums=('593fcb6e04215df27f337b6794967007')

_gitroot=https://github.com/pkulchenko/ZeroBraneStudio.git
_gitname=ZeroBraneStudio

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -p1 < "$srcdir/zbstudio.patch"

  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DLUA_EXECUTABLE=/usr/bin/lua5.1

  make
}

package() {
  cd "$srcdir/$_gitname-build/build"
  make DESTDIR="$pkgdir/" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  cp ../LICENSE "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
