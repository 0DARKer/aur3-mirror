# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-rose-db-object
pkgver=0.805
pkgrel=1
pkgdesc="Extensible, high performance object-relational mapper (ORM)"
arch=(any)
license=('PerlArtistic')
url="http://search.cpan.org/dist/Rose-DB-Object/"
depends=(perl-bit-vector
         perl-clone
         perl-dbi
         perl-datetime
         perl-list-moreutils
         perl-rose-db
         perl-rose-datetime
         perl-rose-object
         perl-time-clock)
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/J/JS/JSIRACUSA/Rose-DB-Object-${pkgver}.tar.gz)
md5sums=('0e273b5cf5f7c217bd25d6fb5c63ca98')
sha512sums=('2b9ac20f77a9372aadcfbd064c1ec079943f3326287e3125ddb85d9b5e8ebe96fc4dd946b74a53a246658faf85e13e6806f29ced994606ef151e90ab437e1b46')

build() {
  cd "${srcdir}/Rose-DB-Object-${pkgver}"

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/Rose-DB-Object-${pkgver}"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "${srcdir}/Rose-DB-Object-${pkgver}"
  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} '(' -name perllocal.pod -o -name .packlist ')' -delete
}
