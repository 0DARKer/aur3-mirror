# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-rose-db-object
pkgver=0.806
pkgrel=1
pkgdesc="Extensible, high performance object-relational mapper (ORM)"
arch=(any)
license=('PerlArtistic')
url="http://search.cpan.org/dist/Rose-DB-Object/"
depends=('perl-bit-vector>=0'
         'perl-clone>=0.29'
         'perl-datetime>=0'
         'perl-dbi>=1.40'
         'perl-list-moreutils>=0'
         'perl-rose-datetime>=0.532'
         'perl-rose-db>=0.763'
         'perl-rose-object>=0.854'
         'perl-time-clock>=1.00')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/J/JS/JSIRACUSA/Rose-DB-Object-${pkgver}.tar.gz")
md5sums=('c0823b91e1b8628092a3aa29756316b9')
sha512sums=('c0547bb6146fe2053f3885eb2e10ae4054bc7897495c76d77ee3bebdefc69a31382bb5da2495a5104e2286104a659ad6127441c1384646d50b51dff354b33a75')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/Rose-DB-Object-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
