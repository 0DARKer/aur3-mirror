# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-rose-db-object
pkgver=0.804
pkgrel=1
pkgdesc="Extensible, high performance object-relational mapper (ORM)"
arch=(any)
license=('PerlArtistic')
url="http://search.cpan.org/dist/Rose-DB-Object/"
depends=(perl-bit-vector
         perl-clone
         perl-dbi
         perl-datetime
         perl-list-moreutils
         perl-rose-db
         perl-rose-datetime
         perl-rose-object
         perl-time-clock)
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/J/JS/JSIRACUSA/Rose-DB-Object-${pkgver}.tar.gz)
md5sums=('81e93dde1d5c1603c1db119823e2e697')
sha1sums=('8c26649b532265f07e878fed3645f453be3411a4')

build() {
  cd ${srcdir}/Rose-DB-Object-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd ${srcdir}/Rose-DB-Object-${pkgver}
  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} '(' -name perllocal.pod -o -name .packlist ')' -delete
}
