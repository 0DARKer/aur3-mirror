# Maintainer: Mario Rodas <marsam@users.noreply.github.com>

pkgname=ats2-postiats
_pkgname=ATS2-Postiats
pkgver=0.1.10
pkgrel=1
pkgdesc="Statically typed programming language"
arch=('i686' 'x86_64')
url="http://www.ats-lang.org/"
license=('GPL3')
depends=('bash' 'gmp')
optdepends=('gmp')
options=('staticlibs' '!emptydirs')
source=("http://downloads.sourceforge.net/project/ats2-lang/ats2-lang/ats2-postiats-${pkgver}/${_pkgname}-${pkgver}.tgz"
        "$pkgname.sh"
        "$pkgname.csh")
sha256sums=('429f1281b48e5455bdd1099f4486963147acf25bf47dac50e18454c0fc7db243'
            '1a7ade3adf391746674d224b375c15ddb3af58406fa0646710e23f0eae3350cd'
            'a1916b91f30586244f65247b8ee2acf49d18985309e16ca75f5d0f2d7e4bd4f2')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make all
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm755 "${srcdir}/${pkgname}.sh" "${pkgdir}/etc/profile.d/${pkgname}.sh"
  install -Dm755 "${srcdir}/${pkgname}.csh" "${pkgdir}/etc/profile.d/${pkgname}.csh"
}
