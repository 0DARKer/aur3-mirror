# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>

pkgname=katawa-shoujo
pkgver=1.2
pkgrel=1
pkgdesc="Bishoujo-style visual novel game that tells a story of a young man and five other girls suffering with varying disabilities"
arch=('i686' 'x86_64')
url='http://katawa-shoujo.com/'
license=('CCPL:by-nc-nd' 'MIT')

if [ "$CARCH" == "x86_64" ]; then
  depends=('lib32-zlib')
fi

if [ "$CARCH" == "i686" ]; then
  optdepends=('alsa-lib: audio support'
              'pulseaudio: audio support')
else
  optdepends=('lib32-alsa-lib: audio support'
              'lib32-libpulse: audio support')
fi

install="$pkgname.install"

source=('[4ls]_katawa_shoujo_1.2-[linux-x86][8F3BA490].tar.bz2::http://dl.katawa-shoujo.com/gold_1.2/%5b4ls%5d_katawa_shoujo_1.2-%5blinux-x86%5d%5b8F3BA490%5d.tar.bz2'
        "$pkgname.sh"
        "$pkgname.desktop"
        "$pkgname.png")

sha512sums=('1d490ca8f953340091b16ccd39e9d198b34d7163a9af977d84e64a4ccb1bc941b91196b749e1a68594b4a8e0a6ac32aa28ad1faeed3a05374475ff32bc93f05d'
            '8e9a562fc66f1366716718921d6983d3e48c15701a6d3d2fd28c3de933873e180d44f13ceb837308e585c5f2f16345ab476b950f318aa92170f90b53258dbb74'
            'a2c4ff9d670a9c046350e8658ea9d174b2b71a237626f81bab014569acf710a8da98f359420516ccbec4391a4ef21ae6bf9593c516d2c25c28a875c7ded7c244'
            '26aa0b7f34a76ac8d31247f4e0c0b4d506df83bbf59e5b333401131b91c37a0b951dcb577f22a083a75d35dfe09f9ab2be62e23aed4167cdf70e38db602a3b90')

package() {
  # copy main files
  cd "${srcdir}/Katawa Shoujo-linux-x86/"
  install -dm755 "${pkgdir}/opt/${pkgname}"
  cp -r common game lib renpy "${pkgdir}/opt/${pkgname}"
  install -m644 'Katawa Shoujo.py' "${pkgdir}/opt/${pkgname}"
  install -m755 'Katawa Shoujo.sh' "${pkgdir}/opt/${pkgname}"

  # copy game manual
  install -D -m644 'Game Manual.pdf' "${pkgdir}/usr/share/doc/${pkgname}/gamemanual.pdf"

  # copy license
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # copy bash script
  install -D -m755 "${srcdir}/katawa-shoujo.sh" "${pkgdir}/usr/bin/${pkgname}"

  # install desktop entry file
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

# vim:set ts=2 sw=2 et:
