# Maintainer: Wang Jiajun <amesists AT gmail DOT com>

pkgname=telepathy-mission-control-slim
_pkgname=telepathy-mission-control
pkgver=5.14.0
pkgrel=1
pkgdesc="A Telepathy component providing abstraction of some of the details of connection managers. With no GNOME and NM/ConnMan dependecies."
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/Mission Control"
license=('LGPL2.1')
depends=('telepathy-glib' 'upower')
makedepends=('libxslt' 'python2')
conflicts=('telepathy-mission-control')
provides=('telepathy-mission-control')
install=$pkgname.install
options=('!libtool')
source=(http://telepathy.freedesktop.org/releases/$_pkgname/$_pkgname-$pkgver.tar.gz)
md5sums=('483fa8860891815a540ecf1bb9155122')

build() {
    cd "$_pkgname-$pkgver"

    # Use Python 2
    mkdir "$srcdir/python2-path"
    ln -s /usr/bin/python2 "$srcdir/python2-path/python"
    export PATH="$srcdir/python2-path:$PATH"

    ./configure --prefix=/usr \
        --libexecdir=/usr/lib/telepathy \
        --disable-schemas-compile \
        --disable-gnome-keyring \
        --with-connectivity=no
    make
}

package() {
    cd "$_pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
