# Maintainer : Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>
pkgname=ocaml-sexplib
pkgver=5.2.0
pkgrel=1
pkgdesc="Automated code generation for converting OCaml-values to S-expressions"
arch=('i686' 'x86_64')
url="http://ocaml.janestcapital.com/?q=node/13"
license=('LGPL')
depends=('ocaml' 'ocaml-type-conv')
makedepends=('ocaml-findlib')
options=(!strip !makeflags)
install=ocaml-sexplib.install
source=(http://www.janestreet.com/ocaml/${pkgname/ocaml-/}-$pkgver.tar.gz)
md5sums=('ab66bd96d2208beea12ef1393f34605f')

build() {
  cd "$srcdir/${pkgname/ocaml-/}"
  make || return 1
}

package(){
  cd "$srcdir/${pkgname/ocaml-/}"
  mkdir -p $pkgdir$(ocamlfind printconf destdir)/sexplib
  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  export OCAMLFIND_LDCONF=ignore
  make install
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
