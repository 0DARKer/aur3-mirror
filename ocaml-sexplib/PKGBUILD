# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor : Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>
pkgname=ocaml-sexplib
pkgver=7.0.4
pkgrel=1
pkgdesc="Automated code generation for converting OCaml-values to S-expressions"
arch=('i686' 'x86_64')
url="http://ocaml.janestcapital.com/?q=node/13"
license=('LGPL')
depends=('ocaml' 'ocaml-type-conv>=3.0.4')
makedepends=('ocaml-findlib')
options=(!strip !makeflags)
install=ocaml-sexplib.install
source=(https://forge.ocamlcore.org/frs/download.php/699/sexplib-$pkgver.tar.gz)
md5sums=('58a858d180107c925903fce4dc700de5')

build() {
  cd "$srcdir/${pkgname/ocaml-/}-${pkgver}"

  ocaml setup.ml -configure --prefix /usr --destdir ${pkgdir} --disable-tests
  ocaml setup.ml -build || return 1
}

package(){
  cd "$srcdir/${pkgname/ocaml-/}-${pkgver}"
  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  export OCAMLFIND_LDCONF=ignore
  mkdir -p $OCAMLFIND_DESTDIR
  make install
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
