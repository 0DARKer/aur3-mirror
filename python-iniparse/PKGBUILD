# $Id: PKGBUILD 114820 2014-07-04 07:35:36Z fyan $
# Maintainer:
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: Dino Krtanjek <krtanjekdino@gmail.com>

pkgname=python-iniparse
pkgver=0.4
pkgrel=1
pkgdesc='Better INI parser library for Python'
arch=('any')
license=('MIT')
url='http://code.google.com/p/iniparse'
depends=('python' 'python-six')
source=("http://iniparse.googlecode.com/files/iniparse-${pkgver}.tar.gz"
        "http://pkgs.fedoraproject.org/cgit/${pkgname}.git/plain/${pkgname}-python3-compat.patch")
sha1sums=('2b2af8a19f3e5c212c27d7c524cd748fa0b38650'
          '670a6a70633b297a9586679b7da8312fe9720698')

prepare() {
  cd ${srcdir}/iniparse-${pkgver}
  patch -p0 < ../${pkgname}-python3-compat.patch

  # Avoid conflict with doc files from python2-iniparse
  sed -e "s|share/doc/iniparse-|share/doc/${pkgname}-|" \
      -i setup.py
}

build() {
  cd ${srcdir}/iniparse-${pkgver}
  python ./setup.py build
}

check() {
  cd ${srcdir}/iniparse-${pkgver}
  python runtests.py
}

package() {
  cd ${srcdir}/iniparse-${pkgver}
  python ./setup.py install --root=${pkgdir} --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
