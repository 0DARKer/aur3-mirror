# $Id$
# Maintainer: Laszlo Papp <lpapp@kde.org>

pkgname=icecc
_pkgname=icecream
pkgver=1.0.1
pkgrel=2
pkgdesc="Distributed compiler with a central scheduler to share build load"
arch=('i686' 'x86_64')
url="https://github.com/icecc/icecream"
license=('GPLv2')
depends=('docbook2x')
backup=('etc/icecc.conf')
install='icecc.install'
source=('https://github.com/icecc/icecream/archive/1.0.1.tar.gz'
        'icecc.service'
        'icecc-scheduler.service'
        'icecc.conf')
sha1sums=('2c3e43d34e4cbe8da5fa49e0518fd4db4c665f81'
          '56189057cbdeaca833d64bb19d0a10acd4cdce3f'
          'c29b6a97e6f8152e0569692ed4042c432711e011'
          'fc8f90e9be356839f7795f0a85eee0715e00af81')

prepare() {
    sed -i s/docbook-to-man/docbook2man/ "${_pkgname}-${pkgver}/configure.ac"
}

build() {
    cd "${_pkgname}-${pkgver}"
    ./autogen.sh
    ./configure --prefix=/usr/lib/icecc
    make all
}

package() {
    cd "${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -D -m755 "$srcdir"/icecc.conf "$pkgdir/etc/icecc.conf"
    install -D -m644 ../icecc.service "${pkgdir}/usr/lib/systemd/system/icecc.service"
    install -D -m644 ../icecc-scheduler.service "${pkgdir}/usr/lib/systemd/system/icecc-scheduler.service"

    install -D -m644 "${pkgdir}/usr/lib/icecc/lib/pkgconfig/icecc.pc" "${pkgdir}/usr/lib/pkgconfig/icecc.pc"
    rm -rf "${pkgdir}/usr/lib/icecc/lib/pkgconfig"
}

