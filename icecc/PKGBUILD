# $Id$
# Maintainer: Laszlo Papp <lpapp@kde.org>

pkgname=icecc
_pkgname=icecream
pkgver=1.0.1
pkgrel=1
pkgdesc="Distributed compiler with a central scheduler to share build load"
arch=('i686' 'x86_64')
url="https://github.com/icecc/icecream"
license=('GPLv2')
depends=('docbook2x')
install='icecc.install'
source=('https://github.com/icecc/icecream/archive/1.0.1.tar.gz'
        'icecc.service'
        'icecc-scheduler.service')
sha1sums=('2c3e43d34e4cbe8da5fa49e0518fd4db4c665f81'
          '1472425dc050ff1c852ed1824aee47de9943770a'
          '8a500bfa67c51ef9bfb60ffeeabb239643963b97')

prepare() {
    sed -i s/docbook-to-man/docbook2man/ "${_pkgname}-${pkgver}/configure.ac"
}

build() {
    cd "${_pkgname}-${pkgver}"
    ./autogen.sh
    ./configure --prefix=/usr/lib/icecc
    make all
}

package() {
    cd "${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -D -m644 ../icecc.service "${pkgdir}/usr/lib/systemd/system/icecc.service"
    install -D -m644 ../icecc-scheduler.service "${pkgdir}/usr/lib/systemd/system/icecc-scheduler.service"

    install -D -m644 "${pkgdir}/usr/lib/icecc/lib/pkgconfig/icecc.pc" "${pkgdir}/usr/lib/pkgconfig/icecc.pc"
    rm -rf "${pkgdir}/usr/lib/icecc/lib/pkgconfig"
}

