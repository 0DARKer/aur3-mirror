pkgname=zelda-olb_en
pkgver=3.6
pkgrel=1
pkgdesc="Zelda: Oni Link Begins is the sequel of Zelda: Return of the Hylian"
url="http://www.zeldaroth.fr/"
arch=('i686' 'x86_64')
depends=('sdl_gfx' 'sdl_mixer' 'sdl_image')
makedepends=(imagemagick)
license=('Unknown')
source=("http://www.zeldaroth.fr/us/files/OLB/Linux/ZeldaOLB_US-src-linux.zip" zolb.desktop)
md5sums=('e7b6988e3f5344d35a4ffd5abc6e3bf9'
         '0eee36b5279df711800a6f2dc6cd3ff8')
build () {
	cd $srcdir/ZeldaOLB_US-src-linux/src
	
	# Fix the Game so that it reads save files from $HOME/.zroth
	sed -i -e s~\"data/save/~string\(getenv\(\"HOME\"\)\)+\"/.zolb/~g Joueur.cpp
	
	#Make the game treat /usr/share/zroth as its data directory
	for i in *.cpp
	do
		sed -i -e s~data/~/usr/share/zolb/~g $i
	done
	
	# Create the directory $HOME/.zroth if it doesnt exist
	sed -i -e s~'(NULL));'~'&\n\tif (system(\"stat $HOME/.zolb \&> /dev/null")) system (\"mkdir $HOME/.zolb\");'~ main.cpp
	make
}

package () {
	
	install -Dm644 zolb.desktop $pkgdir/usr/share/applications/zolb.desktop
	cd $srcdir/ZeldaOLB_US-src-linux/src/
	
	install -Dm755 ZeldaOLB $pkgdir/usr/bin/zolb
	
	cd data
	for i in {map,music,sound}/*
	do
		install -Dm644 $i $pkgdir/usr/share/zolb/$i
	done
	
	cd images
	for i in */*
        do
                install -Dm644 $i $pkgdir/usr/share/zolb/images/$i
        done
	mkdir -p $pkgdir/usr/share/icons/hicolor/256x256/apps
	convert logos/fond.png -resize 256x256\! $pkgdir/usr/share/icons/hicolor/256x256/apps/zolb.png
}
