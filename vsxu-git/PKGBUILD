# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=vsxu
pkgname=$_pkg-git
pkgver=20110728
pkgrel=1
pkgdesc="A free to use program that lets you create and perform real-time audio visual presets."
arch=(i686 x86_64)
url="http://$_pkg.com/"
license=('GPL')
depends=('libpulse' 'freetype2' 'libjpeg' 'glfw' 'libpng12' 'glew')
makedepends=('git' 'cmake')
provides=("$_pkg")

_gitroot="git://git.vsxu.com/$_pkg"
_gitname="$_pkg"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # Does not build with -Wl,--as-needed
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  # build against libpng12
  sed -i "s| png|&12|" {artiste,player}/CMakeLists.txt
  sed -i "s|<png\.h>|<libpng12/png\.h>|" engine_graphics/src/glpng.cpp

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
} 
