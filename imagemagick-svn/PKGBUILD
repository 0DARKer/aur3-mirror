# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

_pkgname=imagemagick
pkgname=$_pkgname-svn
pkgver=10128
pkgrel=1
pkgdesc="An image viewing/manipulation program"
arch=(i686 x86_64)
url=https://www.$_pkgname.org/
license=(custom)
depends=(djvulibre fontconfig lcms libtool)
makedepends=(ghostscript openexr libwmf librsvg libxml2 jasper libpng)
optdepends=('ghostscript: for Ghostscript support'
    'openexr: for OpenEXR support'
    'libwmf: for WMF support'
    'librsvg: for SVG support'
    'libxml2: for XML support'
    'jasper: for JPEG-2000 support'
    'libjpeg-turbo: for jpeg support'
    'libpng: for PNG support'
    'imagemagick-doc: full documentation')
provides=($_pkgname)
conflicts=($_pkgname)
options=(libtool !makeflags)
source=(libpng_mmx_patch_x86_64.patch perlmagick.rpath.patch)
for i in coder colors delegates log magic mime policy thresholds type{,-dejavu,-ghostscript,-windows}; do
    backup+=("etc/ImageMagick/$i.xml")
done
sha256sums=('4f3ab23349fd3958a88eb09a7107e08c2c6f3953287907103ec48cfa83575e87'
    '7560e2e33b6d4e178c33b74325cf7ccc43775251ad37b09ffebac6391c2b62bc')
sha512sums=('8537c2e38cfcd1e7b14d08358c83385792e3e0ed0b9f1210d4dcfad0d416bc3c1311355661f9b020480e641a9b47761ab483dc556d24a30ff92ea557cd8e78f9'
    '7e38e265bb98b95006f1aaefc1d52135a9815c22553492dee79ce371e89fb11959f9b16e88ed024a59b8b9bab8a7326f0886b60dfe814b7cc8894c3e977d43f6')

_svntrunk=$url/subversion/ImageMagick/trunk/
_svnmod=ImageMagick

build() {
    install -d "$srcdir"/$_pkgname/
    cd "$srcdir"/$_pkgname/

    msg "Starting SVN checkout..."
    if [[ -d $_svnmod/.svn ]]; then
        pushd $_svnmod && svn up -r $pkgver
        msg2 "The local files have been updated."
        popd
    else
        svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi
    msg2 "SVN checkout done or server timeout"

    rm -rf $_svnmod-build/
    cp -r $_svnmod/ $_svnmod-build/
    cd $_svnmod-build/

    msg "Compiling..."
    [[ $CARCH == "x86_64" ]] && patch -Np1 -i ../../libpng_mmx_patch_x86_64.patch
    patch -Np1 -i ../../perlmagick.rpath.patch
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --with-modules \
        --disable-static \
        --enable-openmp \
        --with-x \
        --with-wmf \
        --with-openexr \
        --with-xml \
        --with-gslib \
        --with-gs-font-dir=/usr/share/fonts/Type1 \
        --with-perl \
        --with-perl-options="INSTALLDIRS=vendor" \
        --without-gvc \
        --with-djvu \
        --without-autotrace \
        --with-jp2 \
        --with-jbig \
        --without-fpx \
        --without-dps \
        --without-fftw
    make
}

package() {
    cd "$srcdir"/$_pkgname/$_svnmod-build/
    make DESTDIR="$pkgdir" install
    install -d "$pkgdir"/usr/share/licenses/$pkgname/
    install -m644 LICENSE NOTICE "$pkgdir"/usr/share/licenses/$pkgname/
    rm -rf "$pkgdir"/usr/share/doc/
}
