# Maintainer: Vincent Szolnoky <v.szolnoky@tele2.se>
# Contributor: Trevor Turner <turn3r.tr3v0r@gmail.com>

pkgbase=ntfs-3g_ntfsprogs
pkgname=ntfs-3g-fuse
pkgver=2013.1.13
pkgrel=2
pkgdesc="Stable read and write NTFS driver and ntfsprogs. This package will allow normal users to mount NTFS Volumes."
arch=('i686' 'x86_64')
url="http://www.tuxera.com"
license=('GPL2')
depends=('glibc')
conflicts=('ntfs-3g' 'ntfsprogs')
provides=('ntfsprogs' 'ntfs-3g')
options=('!libtool')
install=ntfs-3g-fuse.install
source=(http://tuxera.com/opensource/$pkgbase-$pkgver.tgz)
sha1sums=('8c12b7644d90ae9fb8d0aca0d7ebd5f8fac2c818')

prepare() {
  cd $pkgbase-$pkgver
  sed -i 's|$(DESTDIR)/sbin|$(DESTDIR)/usr/bin|' {ntfsprogs,src}/Makefile.in
}

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr \
    --with-fuse=internal --disable-static \
    --enable-posix-acls --disable-ldconfig \
    --mandir=/usr/share/man --sbin=/usr/bin 
  make
}

package() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" rootbindir=/usr/bin rootsbindir=/usr/bin rootlibdir=/usr/lib install
  ln -s /usr/bin/ntfs-3g "$pkgdir/usr/bin/mount.ntfs"
}
