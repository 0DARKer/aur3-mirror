# Maintainer: Vincent Szolnoky <v.szolnoky@tele2.se>
# Contributor: Trevor Turner <turn3r.tr3v0r@gmail.com>

pkgname=ntfs-3g-fuse
pkgver=2013.1.13
pkgrel=1
pkgdesc="Stable read and write NTFS driver and ntfsprogs. This package will allow normal users to mount NTFS Volumes."
url="http://www.tuxera.com"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc')
makedepends=('pkgconfig')
conflicts=('ntfs-3g' 'ntfsprogs')
provides=('ntfsprogs' 'ntfs-3g')
options=('!libtool')
install=ntfs-3g-fuse.install
source=(http://tuxera.com/opensource/ntfs-3g_ntfsprogs-${pkgver}.tgz)
sha1sums=('8c12b7644d90ae9fb8d0aca0d7ebd5f8fac2c818')

build() {
  cd "${srcdir}/ntfs-3g_ntfsprogs-${pkgver}"
  ac_cv_path_LDCONFIG=/bin/true ./configure --prefix=/usr --with-fuse=internal --disable-static || return 1
  make || return 1
}

package() {
  cd "${srcdir}/ntfs-3g_ntfsprogs-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  ln -s /bin/ntfs-3g "${pkgdir}/sbin/mount.ntfs" || return 1
}
