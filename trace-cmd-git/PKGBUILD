# Current maintainer: Nikolay Amiantov <nikoamia@gmail.com>
# Maintainer: Netanel Shine <netanel at archlinux.org.il>

pkgname=trace-cmd-git
pkgver=2.3.0.r6.g50e80fc
pkgrel=1
pkgdesc="user-space front-end command-line tool for Ftrace, inclduing the GUI interface application kernelshark as well as trace-graph and trace-view."
arch=('i686' 'x86_64')
url="http://git.kernel.org/?p=linux/kernel/git/rostedt/trace-cmd.git"
license=('GPL2')
makedepends=('git' 'python2' 'swig' 'docbook-xsl' 'asciidoc')
optdepends=('python2' 'pygtk')
source=("git://git.kernel.org/pub/scm/linux/kernel/git/rostedt/trace-cmd.git"
	"use-python2.patch")
sha1sums=("SKIP"
          "3ee1159a184dc324514169abd78729bb0123d8e6")

pkgver() {
  cd "$srcdir/trace-cmd"

  git describe --long | sed -r 's/^trace.cmd.v//; s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/trace-cmd"

  patch -p1 -i "../use-python2.patch"
}

build() {
  cd "$srcdir/trace-cmd"
  
  make PYTHON_VERS=python2 all gui doc
}

package() {
  cd "${srcdir}/trace-cmd"

  make PYTHON_VERS=python2 prefix="/usr" DESTDIR="$pkgdir" install install_gui install_doc
}
