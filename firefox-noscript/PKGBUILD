# Contributor: Michael Witten <mfwitten>

### A true value indicates that the .xpi should be
### completely extracted anew:
_clean=yes

###############################################################################

pkgname=firefox-noscript
license=(GPL2)
pkgver=2.0.9.6
pkgrel=1
pkgdesc="Firefox extension for disabling the execution of untrusted Java/Javascript/Flash/etc."
arch=(any)
url=http://noscript.net/
depends=('firefox>=3.0')
makedepends=(unzip)

_xpi_file="noscript-${pkgver}.xpi"
source=(https://secure.informaction.com/download/releases/"$_xpi_file")
noextract=("$_xpi_file")
md5sums=(c4ef5dd3977d7a66f35ccc5319b2c762)

package()
{
  cd "$srcdir"

  if [[ $_clean ]]; then
    msg "Cleaning ..."
    rm -rf files && mkdir files && unzip -od files "$_xpi_file"
  fi

  msg "Configuring ..."

    local version_firefox=$(perl -e '`firefox -v` =~ /(\d+\.\d+[^\s.,;:]*)/; print $1')

    # This isn't very safe over time; XML parsing should really be used
    local extension_id=`awk -F'<em:id>|</em:id>' '/<em:id>/ {print $2; exit}' files/install.rdf`

    local path_install=$pkgdir/usr/lib/firefox-$version_firefox/extensions/$extension_id

  msg "Installing ..."

    install -d "$path_install" && cp -R files/* "$path_install"
}
