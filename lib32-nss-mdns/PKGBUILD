# Maintainer: Martin Wimpress <code@flexion.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Arthur Danskin <arthurdanskin@gmail.com>

_pkgbasename=nss-mdns
pkgname=lib32-${_pkgbasename}
pkgver=0.10
pkgrel=3
pkgdesc="glibc plugin providing host name resolution via mDNS"
arch=('x86_64')
url="http://0pointer.de/lennart/projects/nss-mdns/"
license=('LGPL')
depends=('lib32-glibc')
makedepends=('gcc-multilib')
source=("http://0pointer.de/lennart/projects/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz")
md5sums=('03938f17646efbb50aa70ba5f99f51d7')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libdir=/usr/lib32 \
        --disable-lynx
    make
}

package() {
    cd "${srcdir}/${_pkgbasename}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
