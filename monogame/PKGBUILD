pkgname=monogame
pkgver=2.5.1.0
pkgrel=1
pkgdesc="XNA Implementation for Mono based platforms"
arch=(any)
license=("Microsoft Public License")
depends=(mono opentk)
makedepends=(mono nant)
options=(!strip)
url="http://monogame.codeplex.com"
source=("v$pkgver.tar.gz::https://github.com/mono/MonoGame/tarball/v$pkgver")
md5sums=('e6bb61ca1505a6764f129d675c098303')

build() {
  cd "$srcdir/mono-MonoGame-"*
  nant buildlinux
  nant buildwindows
  cd "MonoGame.Framework/bin/Release"
  find . -name 'Gamepad*.dll' -o -name 'Lidgren.Network*.dll' -o -name 'MonoGame.Framework.*.dll' |
    xargs -rtl1 -I {} monodis {} --output={}.il
  for file in *.dll.il; do
    mv $file ${file%.dll.il}.il
  done
  sn -k 1024 MonoGame.snk
  find . -name '*.il' | xargs -rtl1 -I {} ilasm /dll /key:MonoGame.snk {}
}

package() {
  cd "$srcdir/mono-MonoGame-"*"/MonoGame.Framework/bin/Release"
  find . -name '*.dll' -o -name '*.mdb' |
    xargs -rtl1 -I {} install -m644 {} "$pkgdir/usr/lib/monogame/"{}
  find "$pkgdir/usr/lib/monogame" -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
  install -Dm644 "$srcdir/mono-MonoGame-"*"/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
