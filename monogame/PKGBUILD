pkgname=monogame
pkgver=3.0.1.0
pkgrel=20
pkgdesc="XNA Implementation for Mono based platforms"
arch=(any)
license=("MSPL")
depends=("opentk>=2012.03.15-5" sdl_mixer sdl_ttf sdl_image sdl_net smpeg sdl_gfx)
makedepends=(nant)
conflicts=(tao-framework lidgren-network)
provides=(tao-framework lidgren-network)
replaces=(tao-framework)
url="http://www.monogame.net"
source=("https://github.com/mono/MonoGame/archive/v$pkgver.tar.gz"
"MonoGame.snk"
"monogame.framework.pc.in"
"lidgren.network.pc.in"
"tao.sdl.pc.in")
md5sums=('eeeed2e8ffced53f5f072211c4609b2f'
         'c1da425a06132c94d678f4c0ccf82399'
         '40ed7fd3187cc7498540e91fc04d4bd3'
         '077ae9e5e8674a212a947845e9135f96'
         'a1bc0a97a4f530324384898b509c4784')

prepare() {
	cd "$srcdir/MonoGame-$pkgver/MonoGame.Framework"
	sed -i 's,<DebugType>none,<DebugType>pdbonly,g' MonoGame.Framework.Linux.csproj
}

build() {
  cd "$srcdir/MonoGame-$pkgver"
  nant buildlinux
  cd "MonoGame.Framework/bin/Linux/Release"
  monodis MonoGame.Framework.dll --output=MonoGame.Framework.il
  monodis Lidgren.Network.dll --output=Lidgren.Network.il
  find . -name '*.il' | xargs -rtl1 -I {} ilasm /dll /key:$srcdir/MonoGame.snk {}
}

package() {
  cd "$srcdir/MonoGame-$pkgver/MonoGame.Framework/bin/Linux/Release"
  find . -name '*.dll*' -exec install -Dm644 {} "$pkgdir/usr/lib/monogame.framework/"{} \;
  find . -name '*.mgfxo' -exec install -Dm644 {} "$pkgdir/usr/share/monogame/"{} \;
  cd "$pkgdir/usr/lib/monogame.framework"
  install -Dm644 "$srcdir/MonoGame-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/monogame.framework/LICENSE.txt"
  find . -name '*.dll' -exec gacutil {} -i  -root "$pkgdir/usr/lib" \;
  install -Dm644 "$srcdir/monogame.framework.pc.in" "$pkgdir/usr/lib/pkgconfig/monogame.framework.pc"
  install -Dm644 "$srcdir/lidgren.network.pc.in" "$pkgdir/usr/lib/pkgconfig/lidgren.network.pc"
  install -Dm644 "$srcdir/tao.sdl.pc.in" "$pkgdir/usr/lib/pkgconfig/tao.sdl.pc"
  sed -i "s|@VERSION@|$pkgver|" "$pkgdir/usr/lib/pkgconfig/monogame.framework.pc"
}
