pkgname=monogame
pkgver=3.0.1.0
pkgrel=8
pkgdesc="XNA Implementation for Mono based platforms"
arch=(i686 x86_64)
license=("MS-PL")
depends=(mono "opentk>=2012.03.15")
makedepends=(mono nant)
conflicts=(monogame-git tao-framework tao-framework-svn)
url="http://monogame.codeplex.com"
source=("https://github.com/mono/MonoGame/archive/v$pkgver.tar.gz"
"MonoGame.snk")
md5sums=('eeeed2e8ffced53f5f072211c4609b2f'
         'c1da425a06132c94d678f4c0ccf82399')

build() {
  cd "$srcdir/MonoGame-$pkgver"
  nant buildlinux
  cd "MonoGame.Framework/bin/Linux/Release"
  monodis Lidgren.Network.dll --output=Lidgren.Network.il
  monodis MonoGame.Framework.dll --output=MonoGame.Framework.il
  find . -name '*.il' | xargs -rtl1 -I {} ilasm /dll /key:$srcdir/MonoGame.snk {}
}

package() {
  cd "$srcdir/MonoGame-$pkgver/MonoGame.Framework/bin/Linux/Release"
  find . -name '*.dll' -o -name '*.mdb' -o -name '*.mgfxo' -o -name '*.config' |
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/monogame/"{}
  install -Dm644 "$srcdir/MonoGame-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/monogame/LICENSE.txt"
  gacutil "$pkgdir/usr/lib/monogame/MonoGame.Framework.dll" -i  -root "$pkgdir/usr/lib"
  gacutil "$pkgdir/usr/lib/monogame/Tao.Sdl.dll" -i  -root "$pkgdir/usr/lib"
  find "$pkgdir/usr/lib/monogame/" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
}
