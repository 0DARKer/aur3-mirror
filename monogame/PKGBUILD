pkgname=monogame
pkgver=3.0.0.0
pkgrel=2
pkgdesc="XNA Implementation for Mono based platforms"
arch=(any)
license=("Microsoft Public License")
depends=(mono "opentk>=2012.03.15")
makedepends=(mono nant monodevelop)
options=(!strip)
conflicts=(monogame-git monodevelop-monogame)
replaces=(monodevelop-monogame)
provides=(monodevelop-monogame)
optdepends=("monodevelop: MonoDevelop addon")
url="http://monogame.codeplex.com"
source=("https://github.com/mono/MonoGame/archive/v$pkgver.tar.gz")
md5sums=('e44cb9d85a46b67ac44bed7158f3a01b')

build() {
  cd "$srcdir/MonoGame-$pkgver"
  nant buildlinux
  sn -k 1024 "$srcdir/MonoGame.snk"
  cd "MonoGame.Framework/bin/Linux/Release"
  monodis Lidgren.Network.dll --output=Lidgren.Network.il
  monodis MonoGame.Framework.dll --output=MonoGame.Framework.il
  find . -name '*.il' | xargs -rtl1 -I {} ilasm /dll /key:$srcdir/MonoGame.snk {}
  rm Tao.Sdl.dll{,.config}
  cd "$srcdir/MonoGame-$pkgver/ProjectTemplates/MonoDevelop/MonoDevelop.MonoGame"
  nant
}

package() {
  cd "$srcdir/MonoGame-$pkgver/MonoGame.Framework/bin/Linux/Release"
  find . -name '*.dll' -o -name '*.mdb' -o -name '*.mgfxo' -o -name '*.config' |
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/monogame/"{}
  find "$pkgdir/usr/lib/monogame" -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
  cd "$srcdir/MonoGame-$pkgver/ProjectTemplates/MonoDevelop/MonoDevelop.MonoGame/MonoDevelop.MonoGame"
  install -Dm644 "bin/Release/MonoDevelop.MonoGame.dll" "$pkgdir/usr/lib/monodevelop/AddIns/MonoDevelop.MonoGame/MonoDevelop.MonoGame.dll"
  cp -r icons "$pkgdir/usr/lib/monodevelop/AddIns/MonoDevelop.MonoGame/"
  cp -r templates "$pkgdir/usr/lib/monodevelop/AddIns/MonoDevelop.MonoGame/"
  install -Dm644 "$srcdir/MonoGame-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
