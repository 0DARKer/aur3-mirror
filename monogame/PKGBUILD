pkgname=monogame
pkgver=3.0.1.0
pkgrel=15
pkgdesc="XNA Implementation for Mono based platforms"
arch=(any) #(i686 x86_64)
license=("MS-PL")
depends=("opentk>=2012.03.15-5" sdl_mixer)
makedepends=(nant)
conflicts=(tao-framework)
provides=(tao-framework)
replaces=(tao-framework)
optdepends=("lidgren-network-gen3: Lidgren.Network.dll")
url="http://www.monogame.net"
source=("https://github.com/mono/MonoGame/archive/v$pkgver.tar.gz"
"MonoGame.snk"
"monogame.framework.pc.in")
md5sums=('eeeed2e8ffced53f5f072211c4609b2f'
         'c1da425a06132c94d678f4c0ccf82399'
         '0f3f9882d92fc458c7d0e9731db02f3c')

prepare() {
	cd "$srcdir/MonoGame-$pkgver"
	sed -i "s,Configuration=Release,Configuration=Debug,g" default.build
}
         
build() {
  cd "$srcdir/MonoGame-$pkgver"
  nant buildlinux
  cd "MonoGame.Framework/bin/Linux/Debug"
  monodis MonoGame.Framework.dll --output=MonoGame.Framework.il
  find . -name '*.il' | xargs -rtl1 -I {} ilasm /dll /key:$srcdir/MonoGame.snk {}
}

package() {
  cd "$srcdir/MonoGame-$pkgver/MonoGame.Framework/bin/Linux/Debug"
  find . -name '*.dll' -o -name '*.mdb' -o -name '*.mgfxo' -o -name '*.config' |
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/monogame.framework/"{}
  rm "$pkgdir/usr/lib/monogame.framework/Lidgren.Network.dll"{,.mdb}
  cd "$pkgdir/usr/lib/monogame.framework"
  ln -s "/usr/lib/lidgren.network/Lidgren.Network.dll" .
  install -Dm644 "$srcdir/MonoGame-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/monogame.framework/LICENSE.txt"
  gacutil "$pkgdir/usr/lib/monogame.framework/MonoGame.Framework.dll" -i  -root "$pkgdir/usr/lib"
  gacutil "$pkgdir/usr/lib/monogame.framework/Tao.Sdl.dll" -i  -root "$pkgdir/usr/lib"
  #find "$pkgdir" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
  install -Dm644 "$srcdir/monogame.framework.pc.in" "$pkgdir/usr/lib/pkgconfig/monogame.framework.pc"
  sed -i "s|@VERSION@|$pkgver|" "$pkgdir/usr/lib/pkgconfig/monogame.framework.pc"
}
