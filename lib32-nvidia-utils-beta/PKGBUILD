# Maintainer  : Dan Vratil <vratil@progdansoft.com>
# Contributors: Jeremy Sands <cto@jeremysands.com>
# 		Thomas Baechler <thomas@archlinux.org>
#               James Rayner <iphitus@gmail.com>
#               Erik Hardesty <dalingrin@gmail.com>
#		josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-nvidia-utils-beta
pkgver=304.30
pkgrel=1
pkgdesc="NVIDIA drivers utilities and libraries. (32 bit)"
arch=(x86_64)
url="http://www.nvidia.com/"
license=('custom:NVIDIA')
groups=('lib32')
depends=('lib32-libxext' 'lib32-zlib' 'lib32-gcc-libs')
conflicts=('lib32-libgl' 'lib32-ati-fglrx-utils' 'lib32-nvidia-utils')
provides=('lib32-libgl' "lib32-nvidia-utils=${pkgver}")
source=(ftp://download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run)

build()
{
  cd "${srcdir}"
  if [ -d NVIDIA-Linux-x86-${pkgver} ]; then 
	rm -rf NVIDIA-Linux-x86-${pkgver}; 
   fi

  # Extract sources
  sh NVIDIA-Linux-x86-${pkgver}.run --extract-only
}

package() {
   cd $srcdir/NVIDIA-Linux-x86-${pkgver}

    # OpenGL library
    install -D -m755 libGL.so.$pkgver $pkgdir/usr/lib32/libGL.so.$pkgver
    # OpenGL core library
    install -D -m755 libnvidia-glcore.so.$pkgver $pkgdir/usr/lib32/libnvidia-glcore.so.$pkgver
    # XvMC
    install -D -m644 libXvMCNVIDIA.a $pkgdir/usr/lib32/libXvMCNVIDIA.a
    install -D -m755 libXvMCNVIDIA.so.$pkgver $pkgdir/usr/lib32/libXvMCNVIDIA.so.$pkgver
    # VDPAU
    install -D -m755 libvdpau_nvidia.so.$pkgver $pkgdir/usr/lib32/vdpau/libvdpau_nvidia.so.$pkgver
    # CUDA
    install -D -m755 libcuda.so.$pkgver $pkgdir/usr/lib32/libcuda.so.$pkgver
    install -D -m755 libnvcuvid.so.$pkgver $pkgdir/usr/lib32/libnvcuvid.so.$pkgver
    # nvidia-tls library
    install -D -m755 tls/libnvidia-tls.so.$pkgver $pkgdir/usr/lib32/libnvidia-tls.so.$pkgver
    # OpenCL
    install -D -m755 libnvidia-compiler.so.$pkgver $pkgdir/usr/lib32/libnvidia-compiler.so.$pkgver
    install -D -m755 libOpenCL.so.1.0.0 $pkgdir/usr/lib32/libOpenCL.so.1.0.0

    install -D -m755 libnvidia-cfg.so.$pkgver $pkgdir/usr/lib32/libnvidia-cfg.so.$pkgver
    install -D -m755 libnvidia-ml.so.$pkgver $pkgdir/usr/lib32/libnvidia-ml.so.$pkgver

    # create soname links
    for _lib in $(find $pkgdir -name '*.so*'); do
        _soname="$(dirname ${_lib})/$(readelf -d "$_lib" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
        if [ ! -e "${_soname}" ]; then
            ln -s "$(basename ${_lib})" "${_soname}"
            ln -s "$(basename ${_soname})" "${_soname/.[0-9]*/}"
        fi
    done

    rm -rf "${pkgdir}"/usr/{include,share,bin}
    mkdir -p "$pkgdir/usr/share/licenses"
    ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

md5sums=('2fb7a7e00404145a842d32b691ea94b6')
