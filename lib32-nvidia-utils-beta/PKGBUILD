# Maintainer  : Dan Vratil <vratil@progdansoft.com>
# Contributors: Jeremy Sands <cto@jeremysands.com>
# 		Thomas Baechler <thomas@archlinux.org>
#               James Rayner <iphitus@gmail.com>
#               Erik Hardesty <dalingrin@gmail.com>
#		josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-nvidia-utils-beta
pkgver=280.04
pkgrel=1
pkgdesc="NVIDIA drivers utilities and libraries. (32 bit)"
arch=(x86_64)
url="http://www.nvidia.com/"
license=('custom:NVIDIA')
groups=('lib32')
depends=('lib32-libxext' 'lib32-zlib' 'lib32-gcc-libs')
conflicts=('lib32-libgl' 'lib32-ati-fglrx-utils' 'lib32-nvidia-utils')
provides=('lib32-libgl' "lib32-nvidia-utils=${pkgver}")
source=(http://download.nvidia.com/XFree86/Linux-x86/${pkgver}/NVIDIA-Linux-x86-${pkgver}.run)

build()
{
  cd "${srcdir}"
  if [ -d NVIDIA-Linux-x86-${pkgver} ]; then 
	rm -rf NVIDIA-Linux-x86-${pkgver}; 
   fi

  # Extract sources
  sh NVIDIA-Linux-x86-${pkgver}.run --extract-only
}

package() {

  cd "${srcdir}/NVIDIA-Linux-x86-${pkgver}"

  # Create install dirs
  mkdir -p "${pkgdir}/usr/lib32/tls" "${pkgdir}/usr/share/licenses/${pkgname}"
  
  # Install libraries
  install {libGL,libnvidia-compiler,libnvidia-glcore,libcuda,libnvidia-tls,libvdpau_nvidia}.so.${pkgver} "${pkgdir}/usr/lib32"
  install tls/libnvidia-tls.so.${pkgver} "${pkgdir}/usr/lib32/tls"
  install -m755 libOpenCL.so.1.0.0 "${pkgdir}/usr/lib32"
  #install {libXvMCNVIDIA.a,libXvMCNVIDIA.so.${pkgver}} "${pkgdir}/usr/lib32"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname"
  
  # Create symlinks
  cd "${pkgdir}/usr/lib32"
  ln -s libOpenCL.so.1.0.0 libOpenCL.so.1
  ln -s libOpenCL.so.1 libOpenCL.so
  ln -s libGL.so.${pkgver} libGL.so.1
  ln -s libGL.so.${pkgver} libGL.so
  ln -s libnvidia-glcore.so.${pkgver} libnvidia-glcore.so.1
  ln -s libnvidia-glcore.so.${pkgver} libnvidia-glcore.so
  ln -s libnvidia-cfg.so.${pkgver} libnvidia-cfg.so.1
  ln -s libnvidia-cfg.so.${pkgver} libnvidia-cfg.so
  ln -s libnvidia-compiler.so.${pkgver} libnvidia-compiler.so.1
  ln -s libnvidia-compiler.so.${pkgver} libnvidia-compiler.so
  ln -s libnvidia-tls.so.${pkgver} libnvidia-tls.so.1
  ln -s libnvidia-tls.so.${pkgver} libnvidia-tls.so
  ln -s libcuda.so.${pkgver} libcuda.so.1
  ln -s libcuda.so.${pkgver} libcuda.so
  #ln -s libXvMCNVIDIA.so.${pkgver} libXvMCNVIDIA_dynamic.so.1
  ln -sf libvdpau_nvidia.so.${pkgver} "${pkgdir}/usr/lib32/libvdpau_nvidia.so.1"

  find "${pkgdir}/usr/" -type d -exec chmod 755 {} \;
  #chmod 644 ${pkgdir}/usr/lib32/libXvMCNVIDIA.a
}

md5sums=('15f030a5af37073eb7cf58ae6e08e7a9')
