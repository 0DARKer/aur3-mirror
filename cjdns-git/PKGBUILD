# Maintainer/Contributor: Prurigro
# Contributor: Werecat
# Contributor: Xyne

_pkgname=cjdns
pkgname=${_pkgname}-git
pkgver=0.3.1148
pkgrel=1
pkgdesc="A routing engine designed for security, scalability, speed and ease of use."
url="https://github.com/cjdelisle/${_pkgname}"
license="GPL3"
makedepends=('git' 'cmake')
optdepends=('python2: required by the cjdnslog script')
arch=('i686' 'x86_64' 'armv7h')
install=${pkgname}.install
backup=(etc/conf.d/${_pkgname})

# Because cjdns fails to build using the latest sources, a slightly older commit has been specified for download until the issue has been resolved
source=("git://github.com/cjdelisle/${_pkgname}.git#commit=324d6a7955" "git://github.com/prurigro/${pkgname}-sysvinit.git#branch=master")
sha1sums=('SKIP' 'SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git describe --always | sed 's|-|.|g;s|[^\.]*\.||;s|\.[^\.]*$||'
}

prepare() {
    find "${srcdir}/${_pkgname}" -type f -exec sed -i 's@/usr/bin/python\s*$@/usr/bin/python2@' {} \+
}

build() {
    # Disable Arch's generic makepkg optimizations (set via /etc/makepkg.conf) in favour of those specified by cjdns
    unset CFLAGS
    unset CPPFLAGS

    install -d "${srcdir}"/${_pkgname}/build
    cd "${srcdir}"/${_pkgname}/build
    
    NO_DEBUG=1 cmake .. #GCC 4.8.0 fails unless either the -g flag (by setting NO_DEBUG=1) or -O2 is unset
    make
    make test
}

# Package creation helper functions
_package_cjdns() {
    install -D -m755 "${srcdir}"/${_pkgname}/build/admin/angel/cjdroute2 "${pkgdir}"/usr/bin/cjdroute
    install -D -m755 "${srcdir}"/${_pkgname}/build/admin/angel/${_pkgname} "${pkgdir}"/usr/bin/${_pkgname}
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/systemd/${_pkgname}.service "${pkgdir}"/usr/lib/systemd/system/${_pkgname}.service
}
_package_sysvinit() {
    install -D -m644 "${srcdir}"/${_pkgname}-git-sysvinit/${_pkgname}.conf.d "${pkgdir}"/etc/conf.d/${_pkgname}
    install -D -m755 "${srcdir}"/${_pkgname}-git-sysvinit/${_pkgname}.rc.d "${pkgdir}"/etc/rc.d/${_pkgname}
    install -D -m755 "${srcdir}"/${_pkgname}/scripts/${_pkgname}.sh "${pkgdir}"/usr/bin/${_pkgname}.sh
}
_package_cjdnslog() {
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/python/${_pkgname}.py "${pkgdir}"/usr/lib/$(python2-config --libs | grep -o -E python2.*)/${_pkgname}.py
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/python/bencode.py "${pkgdir}"/usr/lib/$(python2-config --libs | grep -o -E python2.*)/bencode.py
    install -D -m755 "${srcdir}"/${_pkgname}/contrib/python/${_pkgname}log "${pkgdir}"/usr/bin/${_pkgname}log
}

# Package creation function: comment out a helper function to remove its associated functionality
package() {
    _package_cjdns #Adds core cjdns binaries and a systemd service to the package
    _package_sysvinit #Adds a legacy sysvinit (rc.d) service script and its associated config file to the package
    _package_cjdnslog #Adds a python2 script that can output logging data provided by the admin interface
}
