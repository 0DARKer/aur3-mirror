# Maintainer/Contributor: Prurigro
# Contributor: Werecat
# Contributor: Xyne

_pkgname=cjdns
pkgname=${_pkgname}-git
pkgver=0.3.1149
pkgrel=2
pkgdesc="A routing engine designed for security, scalability, speed and ease of use."
url="https://github.com/cjdelisle/${_pkgname}"
license="GPL3"
makedepends=('git' 'cmake')
optdepends=('python2: required by the cjdnslog script')
arch=('i686' 'x86_64' 'armv7h')
install=${pkgname}.install
backup=(etc/conf.d/${_pkgname})

#source=("${_pkgname}.conf.d" "${_pkgname}.rc.d" "git://github.com/cjdelisle/${_pkgname}.git#branch=master")
source=("${_pkgname}.conf.d" "${_pkgname}.rc.d" "git://github.com/cjdelisle/${_pkgname}.git#commit=9c556ebf84") #Set HEAD back to older commit, before nacl started causing issues with compiling
sha1sums=('3bd5a146323a074beb131f026def8f75b71cb6aa' '24223de2902210953c61bdeb4cda214f7fe74c88' 'SKIP')

# Disable Arch's default compilation optimizations because 'make test' fails without unsetting them (last I checked)
CFLAGS=""
OPTIONS=""

pkg_main() {
    #Main ${_pkgname} binaries and systemd service
    install -D -m755 "${srcdir}"/${_pkgname}/build/admin/angel/cjdroute2 "${pkgdir}"/usr/bin/cjdroute
    install -D -m755 "${srcdir}"/${_pkgname}/build/admin/angel/${_pkgname} "${pkgdir}"/usr/bin/${_pkgname}
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/systemd/${_pkgname}.service "${pkgdir}"/usr/lib/systemd/system/${_pkgname}.service
}

pkg_sysvinit() {
    #Sysvinit system service
    install -D -m644 "${srcdir}"/${_pkgname}.conf.d "${pkgdir}"/etc/conf.d/${_pkgname}
    install -D -m755 "${srcdir}"/${_pkgname}.rc.d "${pkgdir}"/etc/rc.d/${_pkgname}
    install -D -m755 "${srcdir}"/${_pkgname}/scripts/${_pkgname}.sh "${pkgdir}"/usr/bin/${_pkgname}.sh
}

pkg_log() {
    #Python2 script to access logging output through the admin interface
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/python/${_pkgname}.py "${pkgdir}"/usr/lib/$(python2-config --libs | grep -o -E python2.*)/${_pkgname}.py
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/python/bencode.py "${pkgdir}"/usr/lib/$(python2-config --libs | grep -o -E python2.*)/bencode.py
    install -D -m755 "${srcdir}"/${_pkgname}/contrib/python/${_pkgname}log "${pkgdir}"/usr/bin/${_pkgname}log
}

pkgver() {
    pushd -- "${srcdir}/$_pkgname"
        _tag="$(git describe --tags)"
        _ver="${_tag#*-}" #Strip the package name.
        _ver="${_ver%-*}" #Strip the hash tag because it confuses Pacman's vercmp.
        _ver="${_ver/-/.}" #Remove "-" for valid Pacman version.
        echo "$_ver"
    popd
}

prepare() {
    find "${srcdir}/${_pkgname}" -type f -exec sed -i 's@/usr/bin/python\s*$@/usr/bin/python2@' {} \+
}

build() {
    install -d "${srcdir}"/${_pkgname}/build
    pushd "${srcdir}"/${_pkgname}/build
        NO_DEBUG=1 cmake .. #GCC 4.8.0 fails unless either the -g flag (by setting NO_DEBUG=1) or -O2 is unset
        make
        make test
    popd
}

package() {
    pkg_main
    pkg_sysvinit
    pkg_log
}
