# Maintainer/Contributor: Prurigro
# Contributor: Werecat
# Contributor: Xyne

_pkgname=cjdns
pkgname=${_pkgname}-git
pkgver=0.3.1188
pkgrel=1
pkgdesc="A routing engine designed for security, scalability, speed and ease of use."
url="https://github.com/cjdelisle/${_pkgname}"
license="GPL3"
makedepends=('git')
optdepends=('python2: required by the python cjdnsadmin utilities')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
install=${pkgname}.install
backup=("etc/default/${_pkgname}")

source=("git://github.com/cjdelisle/${_pkgname}.git#branch=master" "git://github.com/prurigro/${pkgname}-sysvinit.git#branch=master")
sha1sums=('SKIP' 'SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git describe --always | sed 's|-|.|g;s|[^\.]*\.||;s|\.[^\.]*$||'
}

prepare() {
    find "${srcdir}/${_pkgname}"/contrib/python -type f -exec sed -i 's@/usr/bin/python\s*$@/usr/bin/python2@;s@/usr/bin/env\ python@/usr/bin/env\ python2@' {} \+
}

build() {
    # Disable Arch's generic makepkg optimizations (set via /etc/makepkg.conf) in favour of those specified by cjdns
    unset CFLAGS
    unset CPPFLAGS

    # Build using the ./do script
    cd "${srcdir}"/${_pkgname}
    NO_DEBUG=1 ./do

    # Build manually
    #install -d "${srcdir}"/${_pkgname}/build
    #cd "${srcdir}"/${_pkgname}/build
    #NO_DEBUG=1 cmake .. #GCC 4.8.0 fails unless either the -g flag (by setting NO_DEBUG=1) or -O2 is unset
    #make
    #make test
}

# Package creation helper functions
_package_cjdns() {
    install -D -m755 "${srcdir}"/${_pkgname}/build/admin/angel/cjdroute2 "${pkgdir}"/usr/bin/cjdroute
    install -D -m755 "${srcdir}"/${_pkgname}/build/admin/angel/${_pkgname} "${pkgdir}"/usr/bin/${_pkgname}
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/systemd/${_pkgname}.service "${pkgdir}"/usr/lib/systemd/system/${_pkgname}.service
    install -D -m755 "${srcdir}"/${_pkgname}/contrib/bash/i_am_stupid.sh "${pkgdir}"/usr/bin/cjdns-recoverconfig
}
_package_sysvinit() {
    install -D -m644 "${srcdir}"/${_pkgname}-git-sysvinit/${_pkgname}.default "${pkgdir}"/etc/default/${_pkgname}
    install -D -m755 "${srcdir}"/${_pkgname}-git-sysvinit/${_pkgname}.rc.d "${pkgdir}"/etc/rc.d/${_pkgname}
    install -D -m755 "${srcdir}"/${_pkgname}/scripts/${_pkgname}.sh "${pkgdir}"/usr/bin/${_pkgname}.sh
}
_package_pyutils() {
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/python/cjdnsadmin/cjdnsadmin.py "${pkgdir}"/usr/lib/$(python2-config --libs | grep -o -E python2.*)/cjdnsadmin/cjdnsadmin.py
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/python/cjdnsadmin/bencode.py "${pkgdir}"/usr/lib/$(python2-config --libs | grep -o -E python2.*)/cjdnsadmin/bencode.py
    install -D -m644 "${srcdir}"/${_pkgname}/contrib/python/cjdnsadmin/__init__.py "${pkgdir}"/usr/lib/$(python2-config --libs | grep -o -E python2.*)/cjdnsadmin/__init__.py
    install -D -m755 "${srcdir}"/${_pkgname}/contrib/python/cjdnslog "${pkgdir}"/usr/bin/cjdns-log
    install -D -m755 "${srcdir}"/${_pkgname}/contrib/python/dumptable "${pkgdir}"/usr/bin/cjdns-dumptable
    install -D -m755 "${srcdir}"/${_pkgname}/contrib/python/findnodes "${pkgdir}"/usr/bin/cjdns-findnodes
    install -D -m755 "${srcdir}"/${_pkgname}/contrib/python/pingAll.py "${pkgdir}"/usr/bin/cjdns-pingAll
}

# Package creation function: comment out a helper function to remove its associated functionality
package() {
    _package_cjdns #Adds core cjdns binaries, a systemd service and a config recovery tool
    _package_sysvinit #Adds a legacy sysvinit (rc.d) service script and its associated config file to the package
    _package_pyutils #Adds various utlities that can be used for accessing admin information
}
