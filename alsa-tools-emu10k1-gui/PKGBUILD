# Contributor: 3ED <krzysztof1987@gmail.com>
pkgname=alsa-tools-emu10k1-gui
pkgver=1.0.26.1
pkgrel=1
pkgdesc="Gui for alsa-tools-emu10k1"
url="http://alsa-project.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('alsa-tools-emu10k1' 'qt3')
source=("ftp://ftp.alsa-project.org/pub/tools/alsa-tools-$pkgver.tar.bz2"
        "qlo10k1.desktop")
sha256sums=('553338693707fe6ddfc430b9edc4cd2677390e200c9e38de82ede3394e733841'
            '4bc133ba0e97a155dd3bbf8b3eb54f792011a95a2618df543827c8e63e412cc5')

build() {
  cd "$srcdir/alsa-tools-$pkgver/qlo10k1"
  source /etc/profile.d/qt3.sh
  export PATH=$QTDIR/bin:$PATH

  ./configure --prefix=/usr --with-qtdir=$QTDIR --disable-ld10k1test
  make
}
package() {
  cd "$srcdir/alsa-tools-$pkgver/qlo10k1"
  source /etc/profile.d/qt3.sh
  export PATH=$QTDIR/bin:$PATH

  make DESTDIR="$pkgdir/" install
  mkdir -vp "$pkgdir/usr/share/applications"
  install -v -m 644 "$srcdir/qlo10k1.desktop" \
    "$pkgdir/usr/share/applications/qlo10k1.desktop"

  find "$pkgdir/" -name '*.la' -exec rm {} \;
}
#$cat=multimedia
