# Maintainer: Denis Dzenskevich <denis.dzenskevich at gmail.com>
# Contributor: scheffold <fscheffold (at) gmail.com>
# Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=intellij_idea_ue_12_eap
pkgver=123.72
pkgrel=1
install=intellijidea.install
pkgdesc="Early access version of the upcoming Intellij Idea 12 Java IDE (ultimate version)"
arch=('i686' 'x86_64')
url="http://confluence.jetbrains.net/display/IDEADEV/IDEA+12+EAP"
backup=( "usr/share/${pkgname}/bin/idea.vmoptions" 
    "usr/share/${pkgname}/bin/idea64.vmoptions" 
    "usr/share/${pkgname}/bin/idea.properties")
license=('Commercial')
depends=('java-environment')
makedepends=('clang')
_commit=ab92f5adf33d4ca497a45ab966faad8149b33ea8

source=(
	"fsnotifier.h::http://git.jetbrains.org/?p=idea/community.git;a=blob_plain;f=native/fsNotifier/linux/fsnotifier.h;hb=$_commit" \
	"inotify.c::http://git.jetbrains.org/?p=idea/community.git;a=blob_plain;f=native/fsNotifier/linux/inotify.c;hb=$_commit" \
	"main.c::http://git.jetbrains.org/?p=idea/community.git;a=blob_plain;f=native/fsNotifier/linux/main.c;hb=$_commit" \
	"util.c::http://git.jetbrains.org/?p=idea/community.git;a=blob_plain;f=native/fsNotifier/linux/util.c;hb=$_commit" \
	"make.sh::http://git.jetbrains.org/?p=idea/community.git;a=blob_plain;f=native/fsNotifier/linux/make.sh;hb=$_commit" \
    idea.vmoptions        intellijidea.sh intellijidea.desktop intellijidea.install \
    "http://download.jetbrains.com/idea/ideaIU-12.0.tar.gz")
#"http://download.jetbrains.com/idea/ideaIU-${pkgver}.tar.gz")

md5sums=('703feffa16c7c8485935e5de795793a4'
         'b29ad6a43cd22400c7fa2e9304b112ee'
         'b22ff5c4bb99fd1b14d7da5f0f60e79b'
         'b5e338a0a6ae28763c7c246dd36c1b6a'
         '0c4e4f9b94713040cc7cb9a4df856504'
         'c51777b27eed5cca4d0406b3cf060406'
         '69827154a4a8d15cb06e3c04a0d733a8'
         '5cf8190fa21d9fd3d54684f76d40297c'
         '48130e94e6b816978f90b6f05b788ffc'
         'e10609df4796f86d4b845bfede5a89f5')

build() {
  cd "$srcdir"

  if [ "$CARCH" = "i686" ]; then
    _rmpostfix="64"
    _rmarch="64"
    _retainpostfix=""
  elif [ "$CARCH" = "x86_64" ]; then
    _rmpostfix=""
    _rmarch="32"
    _retainpostfix="64"
  fi

  sed -i "s|.\+$_rmarch.\+|true|" make.sh
  sed -i "s|.\+chmod.\+|chmod 755 fsnotifier$_retainpostfix|" make.sh
  chmod +x make.sh
  ./make.sh

  install -d -m755 $pkgdir/usr/{bin,share}
  cp -a "idea-IU-$pkgver" "$pkgdir/usr/share/$pkgname"
  sed -i 's|\.IntelliJIdea\/|\.IntelliJIdea12\/|' "$pkgdir/usr/share/$pkgname/bin/idea.properties"

  cp fsnotifier$_retainpostfix "$pkgdir"/usr/share/$pkgname/bin/
  cp "$srcdir"/idea.vmoptions "$pkgdir"/usr/share/$pkgname/bin

  chmod +x $pkgdir/usr/share/$pkgname/bin/idea.sh
  chmod +x "$pkgdir"/usr/share/$pkgname/bin/fsnotifier$_retainpostfix

  rm $pkgdir/usr/share/intellij_idea_ue_12_eap/bin/fsnotifier$_rmpostfix
  rm $pkgdir/usr/share/intellij_idea_ue_12_eap/bin/libbreakgen$_rmpostfix.so
  rm $pkgdir/usr/share/intellij_idea_ue_12_eap/bin/libyjpagent-linux$_rmpostfix.so

  install -D -m755 $srcdir/intellijidea.sh $pkgdir/usr/bin/$pkgname
  install -D -m644 $srcdir/intellijidea.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -D -m644 $srcdir/idea-IU-$pkgver/bin/idea.png $pkgdir/usr/share/pixmaps/$pkgname.png
  install -D -m644 $srcdir/idea-IU-$pkgver/license/IDEA_license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
}
