# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=jbig-kit
pkgver=2.0
pkgrel=1
pkgdesc="Software implementation of the JBIG1 data compression standard (ITU-T T.82), which was designed for bi-level high-resolution image data such as scanned documents. "
arch=('i686' 'x86_64')
url="http://www.cl.cam.ac.uk/~mgk25/jbigkit/"
license=('GPL')
depends=('glibc')
source=("http://www.cl.cam.ac.uk/~mgk25/download/jbigkit-$pkgver.tar.gz")
md5sums=('3dd87f605abb1a97a22dc79d8b3e8f6c')

build() {
	cd "$srcdir/jbigkit"
	make
}

check() {
	cd "$srcdir/jbigkit"
	make test
}

package() {
	# Install executables.
	for file in 'jbgtopbm' 'jbgtopbm85' 'pbmtojbg' 'pbmtojbg85'; do
		install -D "$srcdir/jbigkit/pbmtools/$file" "$pkgdir/usr/bin/$file"
	done
	
	# Compress/install man pages.
	mkdir -p "$pkgdir/usr/share/man/man"{1,5}

	for file in 'jbgtopbm.1' 'pbmtojbg.1'; do
		gzip -c "$srcdir/jbigkit/pbmtools/$file" > "$pkgdir/usr/share/man/man1/$file.gz"
	done

	for file in 'pbm.5' 'pgm.5'; do
		gzip -c "$srcdir/jbigkit/pbmtools/$file" > "$pkgdir/usr/share/man/man5/$file.gz"
	done
}
