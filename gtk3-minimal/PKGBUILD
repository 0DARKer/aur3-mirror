# Maintainer: simargl < archpup@gmail.com >

pkgname=gtk3-minimal
pkgver=3.6.4
pkgrel=3
pkgdesc="Multi-platform toolkit for creating graphical user interfaces."
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libcups libxcursor libxinerama libxrandr libxi
         libxcomposite libxdamage pango shared-mime-info at-spi2-atk)
makedepends=()
options=('!libtool' '!docs')
backup=(etc/gtk-3.0/settings.ini)
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz
        settings.ini)
sha256sums=('dbda37986f934f986c0bb20f2b0c59f2717a2e5c098370ad69677ff89295f1a1'
            'c214d3dcdcadda3d642112287524ab3e526ad592b70895c9f3e3733c23701621')
conflicts=('gtk3')
provides=('gtk3')
replaces=('gtk3')

build() {
  cd "gtk+-$pkgver"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-x11-backend \
    --enable-gtk2-dependency \
    --disable-schemas-compile \
    --disable-introspection \
    --disable-packagekit \
    --disable-colord \
    --disable-gtk-doc \
    --disable-man \
    --disable-wayland-backend \
    --disable-debug \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "gtk+-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
  rm -rf "$pkgdir/usr/share/gtk-3.0"
  rm -rf "$pkgdir/usr/share/locale"
  rm -rf "$pkgdir/usr/share/man"
}
