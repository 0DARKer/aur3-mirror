# Maintainer: DJ Lucas <dj_AT_linuxfromscratch_DOT_org>
# Contributor: Steven Hiscocks <steven [at] hiscocks [dot] me [dot] uk>
# Contributor:  Andre Wayand <aur-sogo@awayand.sleepmail.com>
pkgname=sogo-openchange
pkgver=2.2.9a
pkgrel=1
pkgdesc="SOGo connector for OpenChange Server"
arch=('i686' 'x86_64')
url="http://www.sogo.nu/"
license=('GPL')
depends=('sogo=2.2.9a' 'openchange')
makedepends=('gcc-objc')
options=('!strip')
source=("http://www.sogo.nu/files/downloads/SOGo/Sources/SOGo-${pkgver}.tar.gz"
        "sogo_configure.patch"
        "UI_MailPartViewers_GNUmakefile.patch")
sha256sums=('02ff02ff0b509981049e14f4fc2f2d4d9e5cb62a53e65145fd41ab653d262e46'
            'e64ea4aa0ddf29785de8d786ab7ab09f940bfe316b6f1deeb8d04d9d16d35db1'
            'ef6ab2829d35c2abb5529ee8ea9a4cc541913b0a82bc91f4c9fa21c65d44a4aa')

prepare() {
  cd "${srcdir}/SOGo-${pkgver}"
  patch configure ../sogo_configure.patch
  patch -p1 < ../UI_MailPartViewers_GNUmakefile.patch
}

build() {
  cd "${srcdir}/SOGo-${pkgver}"
  ./configure --prefix=$(gnustep-config --variable=GNUSTEP_SYSTEM_ROOT) --disable-debug
  make
  cd "${srcdir}/SOGo-${pkgver}/OpenChange"
  # Set python interpreter to python2
  sed 's@/usr/bin/python@/usr/bin/python2@' \
      -i $(grep -R -e "/usr/bin/python" | \
           grep -v "/usr/bin/python2" | \
           cut -d ":" -f 1)
  sed 's@2\.6@2.7@' -i GNUmakefile
  # Remove -Werror
  sed 's@-Werror @@' -i GNUmakefile
  # Fix SoObjects include directory
  sed 's@-I../S@-I.. -I../S@' -i GNUmakefile
  make
}

package() {
  cd "${srcdir}/SOGo-${pkgver}/OpenChange"
  make install DESTDIR="${pkgdir}" GNU_SYSTEM_ADMIN_TOOLS="/usr/bin"
}
