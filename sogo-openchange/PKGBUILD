# Maintainer: DJ Lucas <dj@linuxfromscratch.org>
# Contributor: Steven Hiscocks <steven [at] hiscocks [dot] me [dot] uk>
# Contributor: Andre Wayand <aur-sogo@awayand.sleepmail.com>
pkgname=sogo-openchange
pkgver=2.2.12a
pkgrel=1
pkgdesc="groupware server built around OpenGroupware.org (OGo) and the SOPE application server"
arch=('i686' 'x86_64')
url="http://www.sogo.nu/"
license=('GPL')
depends=('sogo=2.2.12a' 'openchange')
makedepends=('gcc-objc')
options=('!strip')
source=(
  http://www.sogo.nu/files/downloads/SOGo/Sources/SOGo-${pkgver}.tar.gz
  sogo_configure.patch
  UI_MailPartViewers_GNUmakefile.patch
)
md5sums=('2cc9266936de1f4179495ff33c522ae3'
         '5fbd694ee94639697c5f635013d89327'
         '55f74eb6978ae43eac4279580d350dc5')

prepare() {
  cd "$srcdir/SOGo-${pkgver}"
  patch configure ../sogo_configure.patch
  patch -p1 < ../UI_MailPartViewers_GNUmakefile.patch
}

build() {
  cd "$srcdir/SOGo-${pkgver}"
  ./configure --prefix=$(gnustep-config --variable=GNUSTEP_SYSTEM_ROOT) --disable-debug
  make
}

package() {
  cd "${srcdir}/SOGo-${pkgver}/OpenChange"
  sed 's@-Werror@-fobjc-exceptions@' -i GNUmakefile
  make PYTHON=/usr/bin/python2 install DESTDIR="${pkgdir}" GNU_SYSTEM_ADMIN_TOOLS="/usr/bin"

}
