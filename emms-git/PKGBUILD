# Contributor: Joyfulgirl <joyfulgirl (at) archlinux.us>
pkgname=emms-git
pkgver=20110219
pkgrel=1
pkgdesc="EMMS: Emacs MultiMedia System"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emms/"
makedepends=('git')
depends=('emacs' 'taglib' 'emacs-w3m-cvs')
optdepends=('vorbis-tools: ogg support' 
            'mpg123: mp3 support' 
            'mp3info: mp3 tag support'
            'mpd: mpd support'
            'mplayer: mplayer support'
            'xine: xine support')
license=('GPL')
install=emms.install
conflicts=('emms')
provides=('emms')
source=()
md5sums=()

_gitname="emms"
_gitroot="git://git.savannah.gnu.org/emms.git"

build() {
    msg "Connecting to ${_gitroot}..."

    if [ -d ${srcdir}/${_gitname} ]; then
        cd ${srcdir}/${_gitname} && git pull origin master
        msg "Update complete!"
    else
        cd ${srcdir}
        git clone $_gitroot
        cd ${srcdir}/${_gitname}
    fi

    msg "GIT checkout done or server timeout."
    msg "Starting make..."

    make 
    make emms-print-metadata 
}
package() {
  cd ${srcdir}/${_gitname}
  install -d ${pkgdir}/usr/share/emacs/site-lisp/emms
  install -d ${pkgdir}/usr/share/info
  install -d ${pkgdir}/usr/bin
  
  install -m 644 -t ${pkgdir}/usr/share/emacs/site-lisp/emms lisp/*.el
  install -m 644 -t ${pkgdir}/usr/share/emacs/site-lisp/emms lisp/*.elc
  install -m 644 doc/emms.info ${pkgdir}/usr/share/info
  install -m 755 src/emms-print-metadata ${pkgdir}/usr/bin
}
