# Maintainer: Joermungand <joermungand at gmail dot com>

pkgname=loopauditioneer-svn
pkgver=r36
pkgrel=1
pkgdesc="Software for loop and cue handling in .wav files"
arch=('i686' 'x86_64')
url="http://loopauditioneer.sourceforge.net/"
license=('GPL3')
depends=('wxgtk2.8' 'alsa-lib' 'gcc-libs-multilib')
makedepends=('svn')
provides=('loopauditioneer')
conflicts=('loopauditioneer')
source=("${pkgname%-*}"::'svn://svn.code.sf.net/p/loopauditioneer/code/trunk'
        "${pkgname%-*}.sh"
        "${pkgname%-*}.desktop")
md5sums=(SKIP
         '1ea7cc51c813bfe8e37a7ebb15184948'
         '0e2286c155701065663461be6c1056ba')

pkgver(){
	cd "$srcdir/${pkgname%-*}"
	local ver="$(svnversion)"
	printf "r%s" "${ver//[[:alpha:]]}"
}

build(){
	cd "$srcdir/${pkgname%-*}/lib-src/libsndfile"
	chmod +x configure
	./configure --disable-external-libs
	make
	cd ..
	ln -sf libsndfile/src/.libs/libsndfile.a .
	cd rtaudio
	./configure --with-alsa
	make
	cd ..
	ln -sf rtaudio/librtaudio.a .
	cd ../src
	g++ -D__LINUX_ALSA__ -o LoopAuditioneer LoopAuditioneer.cpp MyFrame.cpp \
	MyPanel.cpp CueMarkers.cpp LoopMarkers.cpp FileHandling.cpp MySound.cpp \
	WaveformDrawer.cpp LoopParametersDialog.cpp BatchProcessDialog.cpp \
	AutoLoopDialog.cpp AutoLooping.cpp PitchDialog.cpp CrossfadeDialog.cpp \
	LoopOverlay.cpp FFT.cpp StopHarmonicDialog.cpp CutNFadeDialog.cpp \
	-I../lib-src/libsndfile/src -I../lib-src/rtaudio ../lib-src/libsndfile.a \
	../lib-src/librtaudio.a -lasound -lpthread -lm `wx-config-2.8 --cxxflags --unicode=yes --libs`
}

package() {
	cd "$srcdir"
	mkdir -p "$pkgdir/usr/bin"
	cp "${pkgname%-*}.sh" "$pkgdir/usr/bin/${pkgname%-*}"
	mkdir -p "$pkgdir/usr/share/applications"
	cp "${pkgname%-*}.desktop" "$pkgdir/usr/share/applications/"
    cd "$srcdir/${pkgname%-*}"
    mkdir -p "$pkgdir/opt/${pkgname%-*}/bin"
    cp -Rf "src/LoopAuditioneer" "$pkgdir/opt/${pkgname%-*}/bin"
    cp -Rf src/icons "$pkgdir/opt/${pkgname%-*}/bin"
    cp -Rf icons "$pkgdir/opt/${pkgname%-*}/"
    mkdir -p "$pkgdir/usr/share/pixmaps"
    cd "$pkgdir/usr/share/pixmaps"
    ln -s "../../../opt/${pkgname%-*}/bin/icons/LoopyIcon-48.png" "${pkgname%-*}.png"
    cd "$srcdir/${pkgname%-*}"
    mkdir -p "$pkgdir/usr/share/doc/${pkgname%-*}"
    cp LICENSE.txt "$pkgdir/usr/share/doc/${pkgname%-*}/"
    cp README.txt "$pkgdir/usr/share/doc/${pkgname%-*}/"
}

