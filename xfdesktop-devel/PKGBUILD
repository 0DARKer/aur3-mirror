# Maintainer:  cuihao <cuihao dot leo at gmail dot com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

# Original PKGBUILD (extra/xfdesktop):
# $Id: PKGBUILD 138129 2011-09-17 02:28:33Z allan $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop-devel
_pkgname=xfdesktop
pkgver=4.10.0
pkgrel=2
pkgdesc="A desktop manager for Xfce (Development Version)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('libxfce4ui-devel>=4.9.0' 'libxfce4util-devel>=4.9.0' 'thunar>=1.2.0' 'garcon>=0.1.2' 'libwnck>=2.30.0')
depends=('libxfce4ui' 'thunar' 'garcon' 'hicolor-icon-theme' 'libwnck')
makedepends=('intltool')
options=('!libtool')
install=xfdesktop.install
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.10/$_pkgname-$pkgver.tar.bz2
        add-backgrounds-xfce-to-search-path.patch)
sha256sums=('897ae6ee435dcc89809ad70c15c5d15347d1cf4fc8033238b17dcc47836c2d7b'
            'fcb27e24abfd50c5555fe01ae946a2bc5a133d3f5cee076eef517ccc5f668dd5')

groups=('xfce4-devel')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname" "$_pkgname-git")

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Add backgrounds/xfce/ to the single image list in the settings app
  # https://bugzilla.xfce.org/show_bug.cgi?id=8799
  patch -Np1 -i "$srcdir/add-backgrounds-xfce-to-search-path.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
