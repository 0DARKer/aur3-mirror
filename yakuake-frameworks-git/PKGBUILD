# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=yakuake-frameworks-git
pkgver=v2.9.9.37.g1b4e0b6
pkgrel=1
pkgdesc="A is a drop-down terminal emulator based on KDE Konsole technology. - KF5 Frameworks (GIT version)"
arch=('i686' 'x86_64')
url="http://yakuake.kde.org"
license=('GPL')
conflicts=('yakuake')
provides=('yakuake')
depends=('knotifyconfig' 'kparts' 'knewstuff' 'konsole-git')
makedepends=('cmake' 'git' 'extra-cmake-modules')
source=('git://anongit.kde.org/yakuake.git#branch=frameworks')
sha1sums=('SKIP')
_gitname="yakuake"
install=yakuake-frameworks-git.install

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
