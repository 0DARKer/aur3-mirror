# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=yakuake-frameworks-git
pkgver=r404.5d38c05
pkgrel=1
pkgdesc="A is a drop-down terminal emulator based on KDE Konsole technology. - KF5 Frameworks (GIT version)"
arch=('i686' 'x86_64')
url="http://yakuake.kde.org"
license=('GPL')
depends=('knotifyconfig' 'kparts' 'knewstuff')
makedepends=('cmake' 'git' 'extra-cmake-modules')
source=('git://anongit.kde.org/yakuake.git#branch=frameworks')
sha1sums=('SKIP')
_gitname="yakuake"

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_gitname}"
  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
