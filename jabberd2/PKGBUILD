# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=jabberd2
pkgver=2.2.13
pkgrel=1
pkgdesc='Scalable, architecturally sound, and extensible XMPP server - successor to jabberd14'
arch=('i686' 'x86_64')
url='http://codex.xiaoka.com/wiki/jabberd2:start'
license=('GPL')
depends=('expat' 'gsasl' 'udns' 'libidn' 'openssl' 'sqlite3')
source=("http://codex.xiaoka.com/pub/jabberd2/releases/jabberd-$pkgver.tar.bz2"
        'rc.d')
sha1sums=('31bc6babbf5105dbb2927f46d6033c98aaa99371'
          '2e44aee7115b0b790897cdb36f6167fe04e1024d')
backup=('etc/jabberd/c2s.xml'
        'etc/jabberd/jabberd.cfg'
        'etc/jabberd/router-filter.xml'
        'etc/jabberd/router-users.xml'
        'etc/jabberd/router.xml'
        'etc/jabberd/s2s.xml'
        'etc/jabberd/sm.xml'
        'etc/jabberd/templates/roster.xml')

install=install

build() {
	cd "$srcdir/jabberd-$pkgver"
	./configure \
		--prefix=/usr \
		--localstatedir=/var/lib \
		--sysconfdir=/etc/jabberd \
		--enable-sqlite=yes \
		--enable-mysql=no \

	make
}

package() {
	cd "$srcdir/jabberd-$pkgver"
	make DESTDIR="$pkgdir" install

	install -Dm755 ../rc.d "$pkgdir"/etc/rc.d/jabberd
	install -dm700 "$pkgdir"/var/lib/jabberd
	cp -r tools "$pkgdir"/var/lib/jabberd
}
