# Submitter: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Moritz Rudert (helios) <helios@planetcyborg.de>
# Maintainer: Janusz Lewandowski <lew21@enves.pl>

pkgname=jabberd2
pkgver=2.2.17
pkgrel=1
pkgdesc='Scalable, architecturally sound, and extensible XMPP server'
arch=('i686' 'x86_64')
url='http://jabberd2.org/'
license=('GPL')
options=('!libtool')
depends=('expat' 'gsasl' 'libidn' 'openssl')
optdepends=('sqlite3' 'postgresql-libs' 'libmysqlclient')
makedepends=('udns' 'sqlite3' 'postgresql-libs' 'libmysqlclient')
source=("https://github.com/downloads/Jabberd2/jabberd2/jabberd-${pkgver}.tar.xz"
	'jabberd.service'
	'pam_jabberd')
backup=('etc/jabberd/c2s.xml'
	'etc/jabberd/jabberd.cfg'
	'etc/jabberd/router-filter.xml'
	'etc/jabberd/router-users.xml'
	'etc/jabberd/router.xml'
	'etc/jabberd/s2s.xml'
	'etc/jabberd/sm.xml'
	'etc/jabberd/templates/roster.xml'
	'etc/pam.d/jabberd')

install=install

build() {
	cd "${srcdir}/jabberd-${pkgver}"

	./configure \
		--prefix=/usr \
		--localstatedir=/var/lib \
		--sysconfdir=/etc/jabberd \
		--enable-sqlite \
		--enable-mysql \
		--enable-pgsql \
		--enable-pam \
		--enable-pipe \
		--enable-anon

	make
}

package() {
	cd "${srcdir}/jabberd-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/lib/systemd/system/"
	install ../jabberd.service "${pkgdir}/usr/lib/systemd/system/jabberd.service"

	install -d "${pkgdir}/etc/pam.d/"
	install ../pam_jabberd "${pkgdir}/etc/pam.d/jabberd"

	install -d "${pkgdir}/usr/share/jabberd/"
	install tools/{db-setup.*,pipe-auth.pl,jabberd-authpipe-pam-0.1.pl} "${pkgdir}/usr/share/jabberd/"

	chmod o= "${pkgdir}/etc/jabberd/"

	rm -fr "${pkgdir}/usr/etc"
	rm -f "${pkgdir}/etc/jabberd/"jabberd-*.conf
	rm -f "${pkgdir}/etc/jabberd/"{,templates/}*.dist
}
md5sums=('8b7d654deaa6566e58ab6630112f9b10'
         'a6851af95d62d5e889f549115c654a04'
         '787db305724e1547a7bc7803c42b46b4')
