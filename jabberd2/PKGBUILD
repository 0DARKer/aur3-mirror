# Submitter: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Moritz Rudert (helios) <helios@planetcyborg.de>

pkgname=jabberd2
pkgver=2.2.16
pkgrel=1
pkgdesc='Scalable, architecturally sound, and extensible XMPP server - successor to jabberd14'
arch=('i686' 'x86_64')
url='http://codex.xiaoka.com/wiki/jabberd2:start'
license=('GPL')
options=('!libtool')
makedepends=('udns' 'cppunit')
depends=('expat' 'gsasl' 'libidn' 'openssl' 'sqlite3')
source=("https://github.com/downloads/Jabberd2/jabberd2/jabberd-${pkgver}.tar.gz"
        'set_debug_log_from_config.patch'
        'rc.d')
sha1sums=('d39f7475ec676a127e948f2c5f73d7605ec5a7f9'
          'c4c9c4b2a674b684ce712a503f1e1426aae6f7ff'
          '2e44aee7115b0b790897cdb36f6167fe04e1024d')
backup=('etc/jabberd/c2s.xml'
        'etc/jabberd/jabberd.cfg'
        'etc/jabberd/router-filter.xml'
        'etc/jabberd/router-users.xml'
        'etc/jabberd/router.xml'
        'etc/jabberd/s2s.xml'
        'etc/jabberd/sm.xml'
        'etc/jabberd/templates/roster.xml')

install=install

build() {
	cd "${srcdir}/jabberd-${pkgver}"

	patch -p1 -i "${srcdir}/set_debug_log_from_config.patch"

	./configure \
		--prefix=/usr \
		--localstatedir=/var/lib \
		--sysconfdir=/etc/jabberd \
		--enable-sqlite=yes \
		--enable-mysql=no \

	make
}

package() {
	cd "${srcdir}/jabberd-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -Dm755 "${srcdir}/rc.d" "${pkgdir}"/etc/rc.d/jabberd
	install -dm700 "${pkgdir}/var/lib/jabberd"
	cp -r tools "${pkgdir}/var/lib/jabberd"
	rm -fr "${pkgdir}/usr/etc"
}
