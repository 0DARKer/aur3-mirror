# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: slubman <slubman@slubman.net>
# Contributor: hardfalcon <hardfalcon@gpfclan.de>
# Contributor: Laszlo Papp <djszapi@gmail.com>

pkgname=ktigcc
pkgver=1.10
pkgrel=0
pkgdesc="IDE for the tigcc cross-toolchain"
arch=(i686 x86_64)
url=http://tigcc.ticalc.org
license=(GPL2)
depends=(desktop-file-utils kdelibs3 libticalcs tigcc)
groups=(ti68k)
source=($pkgname-$pkgver.tar.bz2::"http://sourceforge.net/projects/tigcc-linux/files/KTIGCC/KTIGCC 1.10/$pkgname-$pkgver.tar.bz2/download"
    http://downloads.sourceforge.net/sourceforge/tigcc-linux/ktigcc-completion-data-0.96b08r1-1.tar.bz2 
    $pkgname.desktop)
sha256sums=('ff585edb3a7c078ab3fd1630e67b07c2516383a4568cad463c10234fe7d1fa9b'
    'eafbab85cdd81af48e9806b3a0e162ea251243c0d1ceee04137542ffb31f502c'
    'c0c9be116b8564bed3019e334368e06c2b25866ade5de98d247836e5f9fe0df5')
sha512sums=('ae7ad58ceffd0331fc61f5cab9e8f502df6ab740f71c61ec44aa6478af531601a7e7a625301150bfb899aa5b606f8ebaf0a0464355aa70e0dd173679245b9b80'
    '5cd8523e3926a63f69852dafdf85cf008392006f3715aad7269dfc666928bb148c714ec6e9e38ad09a782094755ad1aef7463444fb8b7215dbf4f95c255368af'
    '90c79dea888a23d40f4d294b4fae9df61936cf7de0265da587f4ea88eedf316e081e57f871d7df8e8a9211623bc4577f0912ba27a15ecb666831c4aa896c416a')

prepare() {
    sed -i 's:kde-config:/opt/kde/bin/&:g' $pkgname/$pkgname.pro
}

build() {
    cd $pkgname/
    qmake-qt3 PREFIX=/usr
    make UIC=uic-qt3
}

package() {
    desktop-file-install $pkgname.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 completion "${pkgdir}"/$(kde-config --expandvars --install data)/ktigcc/completion

    cd $pkgname/
    make INSTALL_ROOT="$pkgdir" install
    install -Dm644 images/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
