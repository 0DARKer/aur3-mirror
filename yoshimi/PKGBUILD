# Maintainer: David Adler <david dot jo dot adler at gmail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=yoshimi
pkgver=0.060.12
pkgrel=1
pkgdesc="Experimental ZynAddSubFX with improved JACK audio & MIDI IO"
arch=('i686' 'x86_64')
url="http://yoshimi.sourceforge.net/"
license=('GPL')
depends=('jack' 'fltk' 'fftw')
makedepends=('cmake' 'boost' 'mxml' 'mesa')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/0.060/$pkgname-$pkgver.tar.bz2")
md5sums=('e5c7dfb0d655083b9c84b6a67d1a8cd4')

build() {
  cd "$srcdir/$pkgname-$pkgver/src" 

  cmake . -DCMAKE_INSTALL_PREFIX="$pkgdir/usr"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src" 

  make install

  # desktop file and pixmaps
  install -d "$pkgdir/usr/share/applications"
  sed "s|@Y.*@|0.060.10|;s|@C.*/||;s|pink.*png|$pkgname|" \
      ../desktop/$pkgname.desktop.in > \
      "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 ../desktop/yoshimi.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
