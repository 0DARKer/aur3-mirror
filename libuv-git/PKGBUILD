# Maintainer: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

_pkgname=libuv
pkgname=libuv-git
pkgver=0.11.19.56.g8c9d5dc
pkgrel=1
pkgdesc='New platform layer for Node.JS'
arch=('x86_64' 'i686')
url='https://github.com/joyent/libuv'
license=('MIT')
makedepends=('git')
depends=('glibc')
provides=('libuv')
conflicts=('libuv')
source=('git://github.com/joyent/libuv.git')
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"

  git describe --tags | sed 's/^v//;s/-/./g;s/node.v//'
}

build() {
  cd "$_pkgname"

  ./autogen.sh
  ./configure --prefix=/usr
  CFLAGS=-fPIC make
}

check() {
  cd "$_pkgname"

  make test
}

package() {
  cd "$_pkgname"

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
  install -Dm644 AUTHORS \
    "$pkgdir/usr/share/doc/$_pkgname/AUTHORS"
  install -Dm644 README.md \
    "$pkgdir/usr/share/doc/$_pkgname/README.md"

  cd include
  find -type f -exec \
    install -Dm644 {} "$pkgdir/usr/include/$pkgname/"{} \;
}

# vim:set ts=2 sw=2 et:
