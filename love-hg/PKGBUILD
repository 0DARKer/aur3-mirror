# Maintainer: Linus Sj√∂gren <thelinx@unreliablepollution.net>
# Contributor: Andrzej Giniewicz < gginiu@gmail.com >
pkgname=love-hg
pkgver=1017
pkgrel=1
pkgdesc="An open-source 2D game engine which uses the versatile Lua scripting language to create dynamic gaming experiences."
arch=(i686 x86_64)
url="http://love2d.org/"
license=('zlib')
depends=('luajit' 'physfs' 'freetype2' 'devil' 'mpg123' 'openal' 'libvorbis' 'libmodplug' 'sdl')
makedepends=('mercurial')
options=(!strip)

_hgroot="http://bitbucket.org/rude"
_hgrepo=love

build() {
  cd "$srcdir"
  rm -rf $_hgrepo-build
  cp -rf $_hgrepo $_hgrepo-build
  cd $_hgrepo-build

  msg "Updating version information"
  head -c 15 configure.in > configure.in.tmp
  echo " [`hg log -l1 --template '{node|short}'`-`date +%Y%m%d`])" >> configure.in.tmp
  tail -n +2 configure.in >> configure.in.tmp
  cp configure.in.tmp configure.in
  rm configure.in.tmp

  msg "Updating compiled modules"
  cd src/scripts
  luajit ./auto.lua audio boot graphics
  cd ../..

  msg "Generating makefiles"
  sh platform/unix/automagic
  ./configure --prefix=/usr --with-luajit LDFLAGS=""

  msg "Building"
  make
}

package() {
  cd "$srcdir/$_hgrepo-build"
  make DESTDIR="$pkgdir" install

  cd "$pkgdir"/usr/bin
  mv love love-hg
}

# vim:set ts=2 sw=2 et:
