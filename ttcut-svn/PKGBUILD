# Maintainer: hydro <hydro@freenet.de>

pkgname=ttcut-svn
pkgver=240
pkgrel=2
pkgdesc="MPEG-2 cutting tool for removing commercials from demuxed DVB recordings"
arch=('i686' 'x86_64')
url="http://ttcut.tritime.de/index.2.html"
license=('GPL')
depends=('qt' 'mesa' 'mplayer' 'transcode')
makedepends=('subversion' 'qt' 'libmpeg2')
conflicts=('ttcut')
provides=('ttcut')
source=(ttcut-0.19.6-no_implicit_GLU.patch)
md5sums=('ed95d6cf9d078bfc0fdd793f2c67773f')

_svntrunk=http://svn.berlios.de/svnroot/repos/ttcut/trunk
_svnmod=ttcut

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  patch -p0 -i $srcdir/ttcut-0.19.6-no_implicit_GLU.patch

  qmake ttcut.pro -o Makefile.ttcut 
  make -f Makefile.ttcut 
}

package() {
  cd "$srcdir/$_svnmod-build"
  install -D ttcut $pkgdir/usr/bin/ttcut 
}
