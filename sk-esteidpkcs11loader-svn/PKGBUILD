# Maintainer: Kevin <arc.mission at gmail>
pkgname=sk-esteidpkcs11loader-svn
_pkgname=esteid-pkcs11-module-loader
pkgver=2
pkgrel=1
pkgdesc="Estonian ID-card Firefox extension (Official version by AS Sertifitseerimiskeskus)"
arch=('x86_64' 'i686')
url="http://www.id.ee/"
license=('LGPL')
depends=('opensc>=0.12.0' 'firefox>=3.6')
makedepends=('cmake' 'subversion')
conflicts=('esteid-browser-plugin-hg-svn')

_svntrunk=https://svn.eesti.ee/projektid/idkaart_public/3.2/$_pkgname
_svnmod=$_pkgname

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build/Mozilla"

  sed -i 's|SET(LIB_SUFFIX 64)|SET(LIB_SUFFIX "")|g' CMakeLists.txt
  sed -i 's|(bits == 64) ? "/usr/lib64/" :||g' chrome/content/pkcs11-loader.js
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DLIB_SUFFIX="" -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "$srcdir/$_svnmod-build/Mozilla"
  make DESTDIR="$pkgdir/" install
  rm -r $pkgdir/usr/lib/{firefox,firefox-addons}
}
