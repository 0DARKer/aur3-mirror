# Maintainer:  TDY <tdy.aur@gmail.com>
# Contributor: Peter A. Shevtsov <pshevtsov at severusweb dot ru>
# Contributor: Renzo Carbonara <gnuk0001 at gmail>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Cilyan Olowen
# Contributor: Stanislav Karchebny <berkus+arch@madfire.net>

pkgname=skype4py
pkgver=1.0.32.1
_commit=3d8ff5917f231d658040ed6ac62fa1660cc5fe77
pkgrel=1
pkgdesc="Python wrapper for the Skype API"
arch=('any')
url="https://github.com/awahlig/skype4py"
license=('BSD')
depends=('python2-dbus' 'python2-gobject2' 'skype')
source=($pkgname-$pkgver.zip::https://github.com/awahlig/$pkgname/zipball/$_commit
        $pkgname-$pkgver-os_execlp.diff)
md5sums=('b4a6125d06dbb4f82527b906590ee66c'
         'f7fa4c2cd0892b8e21a562dde489820d')

build() {
  cd "$srcdir/awahlig-$pkgname-${_commit:0:7}"
  patch -Np1 -i ../$pkgname-$pkgver-os_execlp.diff
  python2 setup.py build
}

package() {
  cd "$srcdir/awahlig-$pkgname-${_commit:0:7}"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 Skype4Py/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
