# Maintainer: p2k <Patrick.Schneider@uni-ulm.de>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
_pkgbasename=keyutils
pkgname=lib32-$_pkgbasename
pkgver=1.4
pkgrel=2
pkgdesc="Userspace tools for the kernel key management system (32-bit libraries only)"
url="http://people.redhat.com/~dhowells/keyutils/"
license=('GPL' 'LGPL')
arch=('x86_64')
source=(http://people.redhat.com/~dhowells/$_pkgbasename/$_pkgbasename-$pkgver.tar.bz2)
depends=('lib32-glibc')
makedepends=('gcc-multilib')
md5sums=('e168c1bdaf5aa93c2cbf8a5e7f8ef27b')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make CFLAGS='-m32 -O2 -Wall'
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" LIBDIR=/usr/lib32 USRLIBDIR=/usr/lib32 install
  rm -rf $pkgdir/{bin,etc,sbin,usr/{include,share}}
}

