# Mantainer: Winston Wu

pkgname=bin32-dosbox-svn-patched
pkgver=3868
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with patches with more features"
arch=(i686 x86_64)
url="http://dosbox.sourceforge.net"
license=(GPL)
depends=(sdl_net sdl_sound libpng alsa-lib libpcap mesa)
makedepends=(subversion mesa glu munt-git)
provides=(dosbox dosbox-svn dosbox-svn-patched)
conflicts=(dosbox dosbox-svn dosbox-svn-patched)
source=(dosbox::svn+http://svn.code.sf.net/p/dosbox/code-0/dosbox/trunk
	dosbox.png::http://www.dosbox.com/dosbox_wiki.png
	dosbox.desktop
	01-beep.patch		# http://home.arcor.de/h-a-l-9000
	02-ctrlbreak.patch	# http://sourceforge.net/p/dosbox/patches/228 and 229
	03-ne2000.patch		# http://sourceforge.net/p/dosbox/patches/238
	04-newglide.patch		# http://www.vogons.org/viewtopic.php?t=16462
	05-swapstereo.patch	# http://www.vogons.org/viewtopic.php?t=25827
	06-mt32new.patch		# from munt
	07-mouselocked.patch        #  http://www.vogons.org/viewtopic.php?f=41&t=36780
	08-gameblaster.patch        #   http://www.vogons.org/viewtopic.php?f=41&t=38350
	09-opengl-rgb.patch
	)
md5sums=(SKIP
	 3dcfe45c5ed0433316eaea51e3620b36
	 cd22b603f07f3ca821861d7f15b87087
	 a18266c0be2ec945eaa0b47ff1cb22b3
	 a2acfc1790f6a820b39e6480992b81f3
	 550efe35485dd159c0868eeb8ecf7e27
	 c3a828aaa032e40720a059fe947b95e9 
	 cddfeba43bd032dc72ce92dfd1f3c2fb
	 986d19b96f9b03731e2f04254fbdb08a
	 885d1aa5e3d81c1cbcaa45088c0f72d3
	 a96c3c1c088d81b39c8e66f17d0c35d9
	 faef0347723844af54c7e9ef64ebac86)

pkgver() {
  cd "$SRCDEST/dosbox"
  svnversion
}

prepare() {
  cd "$srcdir/dosbox"

  for _f in "$srcdir"/*.patch; do
    msg2 "Applying $_f"
    patch -p1 -i "$_f"
  done
}

build() {
  cd "$srcdir/dosbox"
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  linux32 ./autogen.sh
  linux32 ./configure CPPFLAGS="-I /usr/local/include/openglide/" --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc/dosbox
  make -j8
}

package() {
  cd "$srcdir/dosbox"
  make DESTDIR="$pkgdir" install

  install -Dm644 README \
	"$pkgdir/usr/share/doc/dosbox/README"
  install -Dm644 docs/README.video \
	"$pkgdir/usr/share/doc/dosbox/README.video"
  install -Dm644 "$srcdir/dosbox.png" \
	"$pkgdir/usr/share/pixmaps/dosbox.png"
  install -Dm644 "$srcdir/dosbox.desktop" \
	"$pkgdir/usr/share/applications/dosbox.desktop"
}

