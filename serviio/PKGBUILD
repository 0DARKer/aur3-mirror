#Contributor: Casper Bruun <casperbruun@gmail.com>
#Maintainer: Bjoern Franke <bjo@nord-west.org>

pkgname=serviio
pkgver=0.6.2
pkgrel=2
pkgdesc="Free DLNA media server"
arch=(any)
url="http://www.serviio.org/"
license=(Proprietary)
depends=('java-environment' 'ffmpeg')
source=(http://download.serviio.org/releases/serviio-${pkgver}-linux.tar.gz
        ${pkgname}.sh
        ${pkgname}-console.sh
        ${pkgname}d)
md5sums=('b078adae5f6e26486ecf2ed0d9b3f68f'
         '4f237ec05468505f85aa21786e68d6c2'
         '5d415ceb61fd26553956e03251190081'
         'd96a82157c2910f1041ad3f0af279e25')

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    
    # define variable for Serviio home directory
    SERVIIO_HOME=${pkgdir}/usr/share/java/${pkgname}
    
    # create the folders needed in the main directory
    install -dm755 ${SERVIIO_HOME}/{config,lib,library,plugins}
    # ... and the bin folder
    install -dm755 ${pkgdir}/usr/bin
    
    # place the serviio scripts in the /usr/bin folder
    install -m755 ${srcdir}/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
    install -m755 ${srcdir}/${pkgname}-console.sh ${pkgdir}/usr/bin/${pkgname}-console
    
    # copy xml config files to the config folder
    install -m644 config/*.xml ${SERVIIO_HOME}/config
    
    # copy all jar files to the lib folder
    install -m644 lib/*.jar ${SERVIIO_HOME}/lib
    
    # copy derby.properties to library folder
    install -m644 library/derby.properties ${SERVIIO_HOME}/library
    
    # copy plugins-readme.txt to plugins folder
    install -m644 plugins/plugins-readme.txt ${SERVIIO_HOME}/plugins
    
    # create the log folder in /var/log
    install -dm755 ${pkgdir}/var/log/${pkgname}
    
    # create a symlink that puts all log files in /var/log
    ln -s /var/log/${pkgname} ${SERVIIO_HOME}/log
    
    # supply the license file
    install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
    install -m644 LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

    # install rc.d-script
    install -Dm755 "${srcdir}/serviiod" "${pkgdir}/etc/rc.d/serviiod"
    

}

