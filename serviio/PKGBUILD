# Maintainer: Casper Bruun <casperbruun@gmail.com>

pkgname=serviio
pkgver=0.5.2
pkgrel=3
pkgdesc="Free DLNA media server"
arch=(any)
url="http://www.serviio.org/"
license=(Proprietary)
depends=('java-environment' 'ffmpeg')
source=(http://download.serviio.org/releases/serviio-${pkgver}-linux.tar.gz
        ${pkgname}.sh
        ${pkgname}-console.sh)
md5sums=('4ec4e1192d09c6bd1419ebf453bbe207'
         '729c1c4662f53259221ea5d37634438a'
         '3bbeeaae19a204681aa27bfcc3ce539e')

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    
    # define variable for Serviio home directory
    SERVIIO_HOME=${pkgdir}/usr/share/java/${pkgname}
    
    # create the folders needed in the main directory
    install -dm755 ${SERVIIO_HOME}/{config,lib,library}
    # ... and the bin folder
    install -dm755 ${pkgdir}/usr/bin
    
    # place the serviio scripts in the /usr/bin folder
    install -m755 ${srcdir}/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
    install -m755 ${srcdir}/${pkgname}-console.sh ${pkgdir}/usr/bin/${pkgname}-console
    
    # copy xml config files to the config folder
    install -m644 config/*.xml ${SERVIIO_HOME}/config
    
    # copy all jar files to the lib folder
    install -m644 lib/*.jar ${SERVIIO_HOME}/lib
    
    # copy derby.properties to library folder
    install -m644 library/*.properties ${SERVIIO_HOME}/library
    
    # create the log folder in /var/log
    install -dm755 ${pkgdir}/var/log/${pkgname}
    
    # create a symlink that puts all log files in /var/log
    ln -s /var/log/${pkgname} ${SERVIIO_HOME}/log
    
    # supply the license file
    install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
    install -m644 LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

