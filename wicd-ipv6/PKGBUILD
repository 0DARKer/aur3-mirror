# Maintainer of PKGBUILD: Noctivivans <noctivivans@gmail.com>

pkgname=wicd-ipv6
_pkgname=wicd
pkgver=1.7.0
pkgrel=4
arch=(any)
url="http://wicd.sourceforge.net/"
license=('GPL2')
pkgdesc="Wired and wireless network manager for Linux - version with ipv6 patch"
depends=('python2' 'dbus-python' 'dhcpcd' 'wpa_supplicant' 'wireless_tools' 'ethtool' 'shared-mime-info' 'python-urwid' 'pygobject')
optdepends=('python-wpactrl:	needed if you want to use the new experimental ioctrl backend'
      	    'python-iwscan:	needed if you want to use the new experimental ioctrl backend'
            'wicd-gtk: needed if you want the GTK interface')
conflicts=('wicd-svn' 'wicd')
provides=('wicd')
replaces=('wicd' 'wicd-svn')

install=wicd.install

source=(http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz
        wicd-daemon
        wicd.desktop
        wicd-scripts-execution.patch
      	deepcopy+python27-fixes.patch
	support.ipv6.dns.patch)
options=('emptydirs')
backup=('etc/wicd/encryption/templates/active')
md5sums=('003d2e67240989db55934553437ba32a'
         'f40e5f59998d0829707a7c9976afa8f8'
         '326df163a5732d38741371baa4fce9e5'
         'f4c377a25aa077cb76955124adfcc03f'
         '1b7ec95efcb8dc0fe48111da19b395b6'
	 '694fb934623e87275cfb5c7499d97091')


build() {
  cd $srcdir/$_pkgname-$pkgver
  patch -p0 <$srcdir/wicd-scripts-execution.patch
  patch -p1 -i $srcdir/deepcopy+python27-fixes.patch
  patch -p1 -i $srcdir/support.ipv6.dns.patch
  find . -type f -exec sed -i 's@#!/usr.*python@#!/usr/bin/python2@' {} \;
  export PYTHON=python2
  python2 setup.py configure --no-install-init \
			     --resume=/usr/share/wicd/scripts/ \
                             --suspend=/usr/share/wicd/scripts/ \
                             --verbose \
                             --python=/usr/bin/python2
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  python2 setup.py install --optimize=1 --root=$pkgdir

  # Add custom rc.d script
  install -Dm755 $srcdir/wicd-daemon $pkgdir/etc/rc.d/wicd

  cd build/lib/wicd
  for i in *.py; do
    install -Dm 755 $i $pkgdir/usr/lib/wicd/$i
  done
  
  rm -rf $pkgdir/usr/share/autostart

  #deleting the GTK stuff
  rm -rf $pkgdir/etc/xdg
  rm -f $pkgdir/usr/bin/{wicd-client,wicd-gtk}
  rm -rf $pkgdir/usr/share/{applications,icons,pixmaps}
  rm -rf $pkgdir/usr/share/wicd/gtk  
}

