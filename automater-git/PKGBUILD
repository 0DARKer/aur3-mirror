pkgname=automater-git
_pkgname=automater
pkgver=r58.a034193
pkgrel=1
url='http://www.tekdefense.com/automater/'
pkgdesc='A IP and URL analysis tool'
arch=('any')
license=('GPL2')
depends=('python2')
_gitname='TekDefense-Automater'
source=("git://github.com/1aN0rmus/$_gitname.git"
        'automater')
md5sums=('SKIP'
         '2ba514d7cb0ee632aca407ca6091b8ef')

pkgver() {
  cd $srcdir/$_gitname
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  cd $srcdir/$_gitname
  sed -i -e "s|^#\!.*/usr/bin/python|#!/usr/bin/env python2|" Automater.py
}

package() {
  install -dm755 $pkgdir/usr/bin $pkgdir/usr/share/$_pkgname/docs
  cd $srcdir
  install -m755 automater $pkgdir/usr/bin
  cd $srcdir/$_gitname
  install -m755 *.py $pkgdir/usr/share/$_pkgname
  install -m644 README.md sites.xml $pkgdir/usr/share/$_pkgname
  install -m644 docs/* $pkgdir/usr/share/$_pkgname/docs
}

