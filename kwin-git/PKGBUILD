# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kwin-git
pkgver=r12395.078d5ac
pkgrel=1
pkgdesc='KDE Window Manager'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('qt5-multimedia' 'plasma-framework-git' 'kcmutils-git' 'knewstuff-git' 'kinit-git' 'libxcursor' 'libepoxy')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'qt5-tools')
conflicts=('kwin' 'kdebase-workspace')
provides=('kwin')
source=('git://anongit.kde.org/kwin.git')
md5sums=('SKIP')

pkgver() {
  cd kwin
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kwin \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
