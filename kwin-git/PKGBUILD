# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kwin-git
pkgver=r12153.6bd16ab
pkgrel=2
pkgdesc='KDE Window Manager'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('qt5-tools' 'qt5-multimedia' 'plasma-framework-git' 'kcmutils-git' 'knewstuff-git' 'kinit-git' 'libxcursor' 'libxkbcommon-x11')
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git')
conflicts=('kwin' 'kde-workspace<0')
provides=('kwin' 'kwin-compositing-kcm-git')
replaces=('kwin-compositing-kcm-git')
source=('git://anongit.kde.org/kwin.git')
md5sums=('SKIP')

pkgver() {
  cd kwin
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kwin \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
