# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kwin-git
pkgver=r11873.b15a97b
pkgrel=1
pkgdesc='KDE Window Manager'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('plasma-framework-git' 'kcmutils-git' 'knewstuff-git' 'kde4support-git')
makedepends=('extra-cmake-modules' 'git' 'kf5umbrella-git')
conflicts=('kwin' 'kde-workspace<0')
provides=('kwin')
source=('git://anongit.kde.org/kwin.git')
md5sums=('SKIP')

pkgver() {
  cd kwin
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kwin/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
