# $Id$
# Maintainer: Pierce Lopez <pierce.lopez@gmail.com>
# adapted from PKGBUILD for asix-module-4.4.0-1 by Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=ax88179
pkgver=1.4.0
pkgrel=1
pkgdesc="A kernel module for the ASIX USB 3.0 AX88179 ethernet controller"
url="http://www.asix.com.tw/download.php?sub=driverdetail&PItemID=131"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux')
makedepends=('linux-headers')
source=("http://www.asix.com.tw/FrootAttach/driver/AX88179_178A_LINUX_DRIVER_v${pkgver}_SOURCE.tar.bz2")
install=$pkgname.install
md5sums=('dfad74d32b82cde468b265fa34c9d85e')

build() {
	_kernver=$(pacman -Q linux | cut -d . -f 2 | cut -f 1 -d -)
	KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-3.$_kernver-ARCH/version)

	cd "$srcdir/AX88179_178A_LINUX_DRIVER_v${pkgver}_SOURCE"
	make SYSSRC=/usr/src/linux-3.$_kernver KDIR=/usr/lib/modules/$KERNEL_VERSION/build
	gzip -f9 ax88179_178a.ko
}

package() {
	_kernver=$(pacman -Q linux | cut -d . -f 2 | cut -f 1 -d -)
	depends=("linux>=3.${_kernver}" "linux<3.$(expr ${_kernver} + 1)")
	KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-3.$_kernver-ARCH/version)
	msg "Kernel = $KERNEL_VERSION"

	cd "$srcdir/AX88179_178A_LINUX_DRIVER_v${pkgver}_SOURCE"
	install -D -m644 ax88179_178a.ko.gz "$pkgdir/usr/lib/modules/extramodules-3.$_kernver-ARCH/ax88179_178a.ko.gz"
	sed -i "s|extramodules-.*-ARCH|extramodules-3.$_kernver-ARCH|" "$startdir/$pkgname.install"
}

