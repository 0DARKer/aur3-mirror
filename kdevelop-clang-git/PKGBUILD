# Maintainer: mensinda <daniel@mensinger-ka.de>
# Contributor: mosra <mosra@centrum.cz>

pkgname=kdevelop-clang-git
pkgver=r482.6bd51e3
pkgrel=1
pkgdesc="Clang integration plugin for KDevelop - Git build"
arch=( i686 x86_64 )
url="http://www.kdevelop.org/"
license=( GPL )
groups=( kde kdevelop-plugins )
depends=( kdevplatform-git clang )
optdepends=( 'kdevelop-git' )
provides=( 'kdevelop-clang' )
makedepends=( cmake automoc4 git )
install='kdevelop-clang.install'
source=( 'git+git://anongit.kde.org/kdev-clang' )
sha256sums=('SKIP')

pkgver() {
  cd kdev-clang
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  mkdir -p build
  cd build

  cmake ../kdev-clang \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON

  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
