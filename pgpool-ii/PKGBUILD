#Contributors:
#	Trương Xuân Tính <xuantinh@gmail.com>
#	henning mueller <henning@orgizm.net>
#	Jerome Rose <jrose dot pub at gmail dot com>

pkgname=pgpool-ii
_pkgname=pgpool-II
pkgver=3.0.5
_dlid=3164
pkgrel=1
pkgdesc='pgpool-II is a connection pool server for PostgreSQL'
arch=(i686 x86_64)
url='http://pgfoundry.org/projects/pgpool'
license=(custom)
depends=(postgresql-libs)
options=(!libtool)
replaces=(pgpool)
source=(
	http://pgfoundry.org/frs/download.php/$_dlid/$_pkgname-$pkgver.tar.gz
	$pkgname.rc
)

md5sums=(
)

build() {
	cd $srcdir/$_pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc/pgpool \
		--mandir=/usr/share/man
	make
}

package() {
	install -D -m 755 $srcdir/$pkgname.rc $pkgdir/etc/rc.d/pgpool

	cd $srcdir/$_pkgname-$pkgver

	make DESTDIR=$pkgdir install

	mkdir -p $pkgdir/{var/run/pgpool,usr/share/doc}

	install -D COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE
	cp -r doc $pkgdir/usr/share/doc/$pkgname

	mv $pkgdir/usr/share/$_pkgname $pkgdir/usr/share/$pkgname
	cp sample/* $pkgdir/usr/share/$pkgname
	
}
md5sums=('90c27473be31654febb0e030c302bb8d'
         '484ae8407ca6e5ce0dd82eb46f6eef32')
