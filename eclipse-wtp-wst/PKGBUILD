# Contributor: Jesus Jerez <jhuss@archlinux.org.ve>

pkgname=eclipse-wtp-wst
pkgver=3.2.3
_pkgbuild=R-3.2.3-20110217214612
pkgrel=1
pkgdesc="Web Developer Tools for the Eclipse platform - part of webtools, includes enough to do web development (e.g. HTML, CSS, Javascript, and XML)"
url="http://www.eclipse.org/webtools/"
arch=('any')
license=('EPL')
depends=('eclipse-emf' 'eclipse-gef')
makedepends=('unzip')
conflicts=('eclipse-wtp')
provides=('eclipse-wtp')
changelog=$pkgname.changelog
source=("http://www.eclipse.org/downloads/download.php?r=1&file=/webtools/downloads/drops/R${pkgver}/${_pkgbuild}/wtp-sdk-${_pkgbuild}.zip")
md5sums=('3335ebd6130aa576a57f43419a324acf')
sha256sums=('8b18ffaab8dc6c8a714e1ed4b729f13b111794bb9bc171a6434845a5e54f2e05')

build() {

  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}/eclipse

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done

}
