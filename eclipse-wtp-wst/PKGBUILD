# Contributor: Jesus Jerez <jhuss@archlinux.org.ve>

pkgname=eclipse-wtp-wst
pkgver=3.2.2
_pkgbuild=R-3.2.2-20100915173744
pkgrel=3
pkgdesc="Web Developer Tools for the Eclipse platform - part of webtools, includes enough to do web development (e.g. HTML, CSS, Javascript, and XML)"
url="http://www.eclipse.org/webtools/"
arch=('any')
license=('EPL')
depends=('eclipse-emf' 'eclipse-gef')
makedepends=('unzip')
conflicts=('eclipse-wtp')
provides=('eclipse-wtp')
changelog=$pkgname.changelog
#source=("http://download.eclipse.org/webtools/downloads/drops/R${pkgver}/${_pkgbuild}/wst-sdk-${_pkgbuild}.zip")
source=("http://www.eclipse.org/downloads/download.php?r=1&file=/webtools/downloads/drops/R${pkgver}/${_pkgbuild}/wst-sdk-${_pkgbuild}.zip")
md5sums=('105cf9fbbbe62f0509b2ace08e6ea9ef')
sha256sums=('49f9ad16c865cff4c40df1f166c4141c99713212ecb5fc8448017b3d81204c9f')

build() {

  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}/eclipse

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done

}
