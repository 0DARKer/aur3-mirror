# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname='profile-sync-daemon'
_pkgname_short='psd'
pkgver=2.3
pkgrel=1
pkgdesc='Symlinks and syncs browser profile dirs to RAM thus reducing HDD/SDD calls and speeding-up browsers.'
arch=('any')
url='https://wiki.archlinux.org/index.php/Profile-sync-daemon'
license=('GPL')
depends=('rsync' 'cron')
conflicts=('firefox-sync' 'firefox-tmpfs-daemon' 'chromium-tmpfs' 'tmpfs-store')
replaces=('sync-browsers-daemon')
source=("$pkgname" "$_pkgname_short.conf" "rc.$_pkgname_short" "$_pkgname_short.cron.hourly")
install=readme.install
backup=("etc/$_pkgname_short.conf")

package() {
	install -Dm 755 "$srcdir"/$pkgname "$pkgdir"/usr/bin/$pkgname
	install -Dm 644 "$srcdir"/$_pkgname_short.conf "$pkgdir"/etc/$_pkgname_short.conf
	install -Dm 755 "$srcdir"/rc.$_pkgname_short "$pkgdir"/etc/rc.d/"$_pkgname_short"
	install -Dm 755 "$srcdir"/$_pkgname_short.cron.hourly "$pkgdir"/etc/cron.hourly/$_pkgname_short-update
}

sha256sums=('f336b8c8f6520800c3d600ce655cfc71f21c041c286d06b1e733e539bfe47d11'
            '896f7f2697057ba263eb10ccca009996bf7391c5bab2f01524380e9d50f15252'
            '19f5af1499d9a4d909ea5ae27721afc32efbd21ddedf2479167772e4dc63fba2'
            'd7c03b9124b8cf76afe7478948ea0a55380d0c6c63c946b7b3a878749221c1a3')
