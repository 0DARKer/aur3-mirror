# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname='profile-sync-daemon'
pkgver=5.65
pkgrel=2
pkgdesc='Syncs browser profiles to tmpfs reducing SSD/HDD calls and speeding-up browsers.'
arch=('any')
url='https://github.com/graysky2/profile-sync-daemon'
license=('MIT')
depends=('procps-ng' 'rsync')
conflicts=('firefox-sync' 'goanysync' 'go-anysync-git' 'iceweasel-sync'
'tmpfs-store' 'tmpfs-sync' 'user-profile-sync-daemon')
source=("http://repo-ck.com/source/$pkgname/$pkgname-$pkgver.tar.xz")
backup=('etc/psd.conf')
install=psd.install
sha256sums=('0b6abf35471b35f777355549661e33a9decdb2e8690d6e151dc77e25d7a146a8')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
	
	# note if you want to use openrc rather than systemd, change the below make
	# target to 'install-openrc-all' before building and be sure that you read
	# the INSTALL document provided in the source tarball!
  make DESTDIR="$pkgdir" install-systemd-all
  install -Dm644 MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
