# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based On: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=sdlmame-wout-toolkits
pkgver="0.161"
pkgrel=1
pkgdesc="A port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support. Without toolkits (gtk and qt)"
url="http://mamedev.org/"
license=('custom:MAME License')
arch=('i686' 'x86_64')
conflicts=('sdlmame')
depends=('sdl2_ttf' 'alsa-lib')
makedepends=('nasm' 'mesa' 'glu' 'wget' 'python2')
optdepends=('sdlmamefamily-tools: Tools for manage MAME/MESS/UME roms')
source=("https://github.com/mamedev/mame/archive/mame${pkgver/./}.tar.gz"
        "sdlmame.sh"
        "extras.tar.gz")
sha1sums=('5ae85a734b986c70dfcec1bcf1046d8ef7f8e348'
          '1ed8016f41edecfca746fadcfb40eab78845a3d6'
          '75732974431844670aa3904d8f9ce3f5c5504827')
install="sdlmame.install"
noextract=("extras.tar.gz")

prepare() {
  cd "mame-mame${pkgver/./}"
  bsdtar -xf ../extras.tar.gz

  find . -type f -not -name "*.png" -exec perl -pi -e 's|\r\n?|\n|g' "{}" \;
}

build() {
  cd "mame-mame${pkgver/./}"

  [ "${CARCH}" = "i686" ] && _set_64=0
  [ "${CARCH}" = "x86_64" ] && _set_64=1

  make PYTHON_EXECUTABLE=/usr/bin/python2 \
       PTR64="${_set_64}" \
       SSE2="${_set_64}" \
       OPTIMIZE=2 \
       NOWERROR=1 \
       USE_OPENGL=1 \
       USE_QTDEBUG=0 \
       TOOLS=0 \
       STRIP_SYMBOLS=1 \
       TARGET=mame
}

package() {
  cd "mame-mame${pkgver/./}"

  # Install the sdlmame script
  install -Dm755 ../sdlmame.sh "${pkgdir}/usr/bin/sdlmame"

  [ "${CARCH}" = "i686" ] && _suffix=""
  [ "${CARCH}" = "x86_64" ] && _suffix="64"
  # Install the applications and the UI font in /usr/share
  install -Dm755 "mame${_suffix}" "${pkgdir}/usr/share/sdlmame/sdlmame"

  # Install the extra bits
  install -d "${pkgdir}/usr/share/sdlmame/"{artwork,ctrlr,keymaps,shader}
  install -m644 artwork/* "${pkgdir}/usr/share/sdlmame/artwork/"
  install -m644 ctrlr/* "${pkgdir}/usr/share/sdlmame/ctrlr/"
  install -m644 src/osd/sdl/keymaps/* "${pkgdir}/usr/share/sdlmame/keymaps/"
  install -m644 src/osd/modules/opengl/shader/glsl*.*h "${pkgdir}/usr/share/sdlmame/shader/"

  # Install man
  install -Dm644 src/osd/sdl/man/mame.6 "${pkgdir}/usr/share/man/man6/sdlmame.6"

  # Include the license
  install -Dm644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

  # FS#28203
  sed -i 's|KEYCODE_2_PAD|KEYCODE_2PAD|' "${pkgdir}/usr/share/sdlmame/ctrlr/"*.cfg
  sed -i 's|KEYCODE_4_PAD|KEYCODE_4PAD|' "${pkgdir}/usr/share/sdlmame/ctrlr/"*.cfg
  sed -i 's|KEYCODE_6_PAD|KEYCODE_6PAD|' "${pkgdir}/usr/share/sdlmame/ctrlr/"*.cfg
  sed -i 's|KEYCODE_8_PAD|KEYCODE_8PAD|' "${pkgdir}/usr/share/sdlmame/ctrlr/"*.cfg

  # documentation
  cd docs
  for i in $(find . -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/doc/${pkgname}/${i}"; done
}
