# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based On: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=sdlmame-wout-toolkits
pkgver="0.157.u0"
_pkgver="$(echo ${pkgver} | tr -d . | cut -d "u" -f1)"
pkgrel=1
pkgdesc="A port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support. Without toolkits (gtk and qt)"
url="http://mamedev.org/"
license=('custom:MAME License')
arch=('i686' 'x86_64')
conflicts=('sdlmame')
depends=('sdl2_ttf' 'flac' 'portmidi' 'libjpeg-turbo')
makedepends=('nasm' 'mesa' 'glu' 'wget' 'python2')
optdepends=('sdlmamefamily-tools: Tools for manage MAME/MESS/UME roms')
source=("https://github.com/mamedev/mame/archive/mame${_pkgver}.tar.gz"
        "sdlmame.sh"
        "extras.tar.gz")
sha1sums=('cac1ccb4194715be63dd4d4754a575b9e1c11ea3'
          '1ed8016f41edecfca746fadcfb40eab78845a3d6'
          '75732974431844670aa3904d8f9ce3f5c5504827')
install="sdlmame.install"
noextract=("extras.tar.gz")

prepare() {
  cd "mame-mame${_pkgver}"
  bsdtar -xf ../extras.tar.gz

  find . -type f -not -name "*.png" -exec perl -pi -e 's|\r\n?|\n|g' "{}" \;
}

build() {
  cd "mame-mame${_pkgver}"

  make PYTHON=python2 OPTIMIZE=2 NOWERROR=1 BUILD_EXPAT=0 BUILD_ZLIB=0 BUILD_FLAC=0 BUILD_JPEGLIB=0 BUILD_MIDILIB=0 BUILD_SQLITE3=0 NO_USE_QTDEBUG=1 FULLNAME=sdlmame
}

package() {
  cd "mame-mame${_pkgver}"

  # Install the sdlmame script
  install -Dm755 ../sdlmame.sh "${pkgdir}/usr/bin/sdlmame"

  # Install the applications and the UI font in /usr/share
  install -Dm755 sdlmame "${pkgdir}/usr/share/sdlmame/sdlmame"

  # Install the extra bits
  install -d "${pkgdir}/usr/share/sdlmame/"{artwork,ctrlr,keymaps,shader}
  install -m644 artwork/* "${pkgdir}/usr/share/sdlmame/artwork/"
  install -m644 ctrlr/* "${pkgdir}/usr/share/sdlmame/ctrlr/"
  install -m644 src/osd/sdl/keymaps/* "${pkgdir}/usr/share/sdlmame/keymaps/"
  install -m644 src/osd/sdl/shader/glsl*.*h "${pkgdir}/usr/share/sdlmame/shader/"

  # Install man
  install -Dm644 src/osd/sdl/man/mame.6 "${pkgdir}/usr/share/man/man6/sdlmame.6"

  # Include the license
  install -Dm644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

  # FS#28203
  sed -e 's|KEYCODE_2_PAD|KEYCODE_2PAD|' \
      -e 's|KEYCODE_4_PAD|KEYCODE_4PAD|' \
      -e 's|KEYCODE_6_PAD|KEYCODE_6PAD|' \
      -e 's|KEYCODE_8_PAD|KEYCODE_8PAD|' \
      -i "${pkgdir}/usr/share/sdlmame/ctrlr/"*.cfg

  # documentation
  cd docs
  for i in $(find . -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/doc/${pkgname}/${i}"; done
}
