# Maintainer: Sam S. <smls75@gmail.com>

pkgname=thomaswasalone-hib
pkgver=0_20130429
_hibver=1369349552
pkgrel=1
pkgdesc='Thomas Was Alone, a minimalistic puzzle-platformer (Humble Bundle version)'
url='http://www.thomaswasalone.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
if [ $CARCH == i686 ]; then
  depends=('glu')
else
  depends=('lib32-glu' 'lib32-libx11')
fi
source=('thomaswasalone-hib.desktop')
md5sums=('11eef2e5cf8e89439d9794c333de72f5')
options=('!strip' '!upx')
PKGEXT='.pkg.tar'

_archive="thomaswasalone-linux-${_hibver}.tar"
_archive_md5='e7f8e766188718e16880b1137c430f35'


package() {
  cd $srcdir
  _target="/opt/ThomasWasAlone"

  # Get installer
  _get_local_source "${_archive}" --md5 "${_archive_md5}" || {
    error "Unable to find the game installer. Please download it from your
           Humble Bundle page, and place it into one of the above locations."
    exit 1; }

  # Extract archive
  msg "Starting setup..."
  tar -xf ${_archive}

  # Fix permissions
  find "thomasLinuxStandalone" -type f -exec chmod 644 {} \;
  find "thomasLinuxStandalone" -type d -exec chmod 755 {} \;
  chmod 755 "thomasLinuxStandalone/thomasWasAlone"

  # Install game files
  mkdir -p "${pkgdir}${_target}"
  cp -rT "thomasLinuxStandalone" "${pkgdir}${_target}"

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

#  # Install icon
#  install -Dm644 "${pkgdir}${_target}/???.png" \
#                 "${pkgdir}/usr/share/pixmaps/thomaswasalone.png"

  # Install launcher script
  echo -e "#!/bin/sh\ncd ${_target} && ./thomasWasAlone" \
        > "launcher.sh"
  install -Dm755 "launcher.sh" "${pkgdir}/usr/bin/thomaswasalone"
}


###### HELPER FUNCTIONS ######

# Locates a file or folder provided by the user, and symlinks it into $srcdir
_get_local_source() {
  msg "Looking for '$1'..."; rm -f "$srcdir/$1"
  declare -A _search=(['build dir']="$startdir"
                      ['$LOCAL_PACKAGE_SOURCES']="$LOCAL_PACKAGE_SOURCES")
  for _key in "${!_search[@]}"; do local _dir="${_search["$_key"]}"
    echo -n "    - in $_key [${_dir:-<undefined>}] ... ";
    if [[ -z "$_dir" || ! -e "$_dir/$1" ]]; then
      echo "NOT FOUND"
    elif [[ -n $2 && "$(${2#--}sum "$_dir/$1"|awk '{print $1}')" != $3 ]]; then
      echo "CHECKSUM FAILED";
    else
      echo "FOUND"; ln -sfT "$(readlink -f "$_dir/$1")" "$srcdir/$1"; break; fi
  done
  if [ ! -e "$srcdir/$1" ]; then return 1; fi
}
