# Maintainer: Sam S. <smls75@gmail.com>

pkgname=thomaswasalone-hib
pkgver=0_20130429
_hibver=1369349552
pkgrel=2
pkgdesc='Thomas Was Alone, a minimalistic puzzle-platformer (Humble Bundle version)'
url='http://www.thomaswasalone.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
if [ $CARCH == i686 ]; then
  depends=('glu' 'lib32-libxcursor' 'alsa-lib' 'gcc-libs')
else
  depends=('lib32-glu' 'lib32-libxcursor' 'lib32-alsa-lib' 'lib32-gcc-libs')
fi
options=('!strip' '!upx')
PKGEXT='.pkg.tar'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

_installer="thomaswasalone-linux-${_hibver}.tar"
source=("hib://${_installer}"
        'http://humblepull-wolfire.netdna-ssl.com/misc/files/db2ae1a3ee/images/icons/thomaswasalone.png'
        'thomaswasalone-hib.desktop')
md5sums=('e7f8e766188718e16880b1137c430f35'
         'a62df3b98ffb8e5c339691a34a179d96'
         '8fe996d4b1a2b3ded634a9d487ea8893')

package() {
  cd $srcdir
  _target="/opt/ThomasWasAlone"

  # Fix permissions
  find "thomasLinuxStandalone" -type f -exec chmod 644 {} \;
  find "thomasLinuxStandalone" -type d -exec chmod 755 {} \;
  chmod 755 "thomasLinuxStandalone/thomasWasAlone"

  # Install game files
  mkdir -p "${pkgdir}${_target}"
  cp -rT "thomasLinuxStandalone" "${pkgdir}${_target}"

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "thomaswasalone.png" \
                "${pkgdir}/usr/share/pixmaps/thomaswasalone.png"

  # Install launcher script
  echo -e "#!/bin/sh\ncd ${_target} && ./thomasWasAlone" \
        > "launcher.sh"
  install -Dm755 "launcher.sh" "${pkgdir}/usr/bin/thomaswasalone"
}
