# Puppet: Installer: Arch
# Maintainer: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>

pkgname=puppet-ruby1.8
libname=facter-ruby1.8
pkgver=2.6.4
pkgrel=1
pkgdesc="A system for automating system administration tasks."
arch=("any")
url="http://puppetlabs.com/projects/puppet/"
license=("GPL")
depends=("ruby1.8" "ruby-shadow" "${libname}")
options=(emptydirs)
install="puppet.install"
source=("http://puppetlabs.com/downloads/puppet/puppet-${pkgver}.tar.gz"
    "puppet" "puppetmaster" "puppet.conf" "pacman.rb")
sha1sums=('b574246b7b067d9fd59e4629830ab4f02b67125f'
          '81cb2cac2618716f09ab2e49e8217ed159580dea'
          '215670c76d1c1029e0b76d1b92fb189419f3473a'
          '51fefdb6e7a58482a17b9766ab2ac6a7c219f489'
          '9b0cc1df6e5d4171b20058096e3675ce767e5ab0')

build() {
	cd ${srcdir}/puppet-${pkgver}

	# Build
	ruby-1.8 ./install.rb --destdir=${pkgdir} --sitelibdir=/opt/ruby1.8/lib/ruby/1.8/ --bindir=/usr/bin --sbindir=/usr/sbin

	# Directories
	install -d ${pkgdir}/etc/{puppet,rc.d}
	install -d ${pkgdir}/etc/puppet/{ssl,manifests}
	install -d ${pkgdir}/etc/puppet/ssl/{ca,certs,private}
	install -m 1777 -d ${pkgdir}/etc/puppet/ssl/{private_keys,public_keys}
	install -m 1777 -d ${pkgdir}/var/run/puppet
	install -d ${pkgdir}/var/lib/puppet/{yaml,bucket,state,rrd,reports,templates,lib,facts}
    install -d -m 750 ${pkgdir}/var/lib/puppet/{clientbucket,client_yaml}
	install -m 1755 -d ${pkgdir}/var/lib/puppet/state
	install -d ${pkgdir}/var/lib/puppet/state/{graphs,localconfig}
	install -m 0750 -d ${pkgdir}/var/log/puppet

	# Configuration
	install -D ${srcdir}/puppet.conf ${pkgdir}/etc/puppet/puppet.conf

	# Daemons
	install -D ${srcdir}/puppet ${pkgdir}/etc/rc.d/puppet
    install -D ${srcdir}/puppetmaster ${pkgdir}/etc/rc.d/puppetmaster

	# Pacman Provider
	install -m 644 ${srcdir}/pacman.rb ${pkgdir}/opt/ruby1.8/lib/ruby/1.8/puppet/provider/package/pacman.rb

    find ${pkgdir}/usr/bin -type f -exec sed -i 's,^#!/usr/bin/ruby,#!/usr/bin/ruby-1.8,' {} \;
}

