# Maintainer: mickele <mimocciola@yahoo.com>

pkgname=salome-yacs
pkgver=5.1.4
pkgrel=1
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation. YACS Module."
url="http://www.salome-platform.org"
depends=('salome-kernel' 'salome-gui' 'python' 'qt' 'opencascade' 'qscintilla' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'graphviz' 'libxml2' 'expat' 'sed')
makedepends=('gcc' 'automake' 'autoconf' 'libtool' 'make' 'doxygen' 'docutils' 'python-sphinx')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
source=("http://files.opencascade.com/Salome/Salome${pkgver}/src${pkgver}.tar.gz" "${pkgname}.profile")
options=('libtool')

_source=/src${pkgver}/YACS_SRC_${pkgver}
_installdir=/opt/salome/yacs
_pythonver=2.6

build() {
  source /etc/profile.d/salome-kernel.sh || return 1
    source /etc/profile.d/salome-gui.sh || return 1

  cd ${srcdir}${_source} || return 1

  # two problems caused by gcc-4.5
  sed -e "s|YACS::Exception::Exception|YACS::Exception|" \
      -i src/bases/Cstr2d.cxx
  sed -e "s|YACS::Exception::Exception|YACS::Exception|" \
      -i src/yacsloader/parserBase.cxx

  # Builds and install
  ./build_configure || return 1
  ./configure --prefix=${_installdir} \
	      --with-python-site=${_installdir}/lib/python-${_pythonver}/site-packages/salome \
	      --with-python-site-exec=${_installdir}/lib/python-${_pythonver}/site-packages/salome \
	      --with-qt4=/usr \
	      --disable-debug \
	      --enable-production || return 1

  make || return 1
}

package() {
  cd ${srcdir}${_source} || return 1

  make DESTDIR="${pkgdir}" install || return 1

  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/profile.d/${pkgname}.sh" || return 1
}

md5sums=('ec363b2ff2dbdb889c973e9e19b2970f'
         'f2beeb52e0a7ec86fa3ebf92c082403c')
