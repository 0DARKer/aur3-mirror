# Maintainer: twa022 <twa022 at gmail dot com>
# Based on gnome-settings-daemon PKGBUILD

_pkgname=cinnamon-settings-daemon
pkgname=${_pkgname}-git
pkgver=142.8733d5a
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cinnamon-desktop' 'libibus' 'libcanberra-pulse' 'librsvg' 'nss' 'pulseaudio-alsa' 'upower' 'libnotify')
makedepends=('intltool' 'docbook-xsl' 'gnome-common' 'git')
options=('!emptydirs' '!libtool')
install=${_pkgname}.install
url="http://cinnamon.linuxmint.com/"
source=(${_pkgname}::git+https://github.com/linuxmint/cinnamon-settings-daemon.git
        keyboard.patch
        automount-plugin.patch)
sha256sums=('SKIP'
            '3c05b978cb414c8fc1530f211bab58627a64ac9c156630c9d486f5926a85109c'
            '4fc2b3a354b53d41d92d7f95d175a6a18382330353a0579d29c2d33b9400a837')

pkgver() {
  cd $_pkgname
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd ${srcdir}/${_pkgname}

  # Fix crasher
  patch -Np1 -i ../keyboard.patch

  # Turn automount helper into a plugin
  patch -Np1 -i ../automount-plugin.patch
}

build() {
  cd ${srcdir}/${_pkgname}

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/${_pkgname} --disable-static --enable-systemd

  #https://bugzilla.cinnamon.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd ${srcdir}/${_pkgname}
  make DESTDIR="$pkgdir" install
}
