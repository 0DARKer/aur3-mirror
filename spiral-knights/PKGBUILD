# Maintainer: hauzer <alokinpwn at gmail dot com>
pkgname=spiral-knights
pkgver=04282011
pkgrel=1
pkgdesc="The Spiral Knights have awoken on an alien world. Their equipment stores have been raided and their starship, The Skylark, will not recover from the crash. They must work together to survive on a journey that will take them to the very core of the world."
arch=('any')
url="http://spiralknights.com/"
license=('custom:Three Rings License')
depends=('java-runtime')
install="${pkgname}.install"
source=("http://download.threerings.net/spiral/client/spiral-install.bin")
noextract=("spiral-install.bin")
md5sums=('427df069a1a5c8b029dabbb98849e018')

build() {
  chmod +x "${srcdir}/spiral-install.bin"
  mkdir -p "${srcdir}/${pkgname}"
  ./spiral-install.bin --noexec --keep --nochown --target "${srcdir}/${pkgname}"
  rm "${srcdir}/${pkgname}/finish_install.sh"
}

package() {
  mkdir -p "${pkgdir}/opt"
  cp -R "${srcdir}/${pkgname}" "${pkgdir}/opt"
  chgrp games "${pkgdir}/opt/${pkgname}" # An ordinary user needs write access
  chmod 775 "${pkgdir}/opt/${pkgname}"   # so he can download the game files

  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m664 "${srcdir}/${pkgname}/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # A link to java that's needed for the game to function properly
  ln -s "${JAVA_HOME}/bin/java" "${pkgdir}/opt/${pkgname}/java"

  # Create a sh launcher in /usr/bin
cat > "${pkgname}.sh" <<EOF
#!/bin/bash
/opt/${pkgname}/spiral
EOF
  mkdir -p "${pkgdir}/usr/bin"
  install -m755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Create a desktop launcher
cat > "${pkgname}.desktop" <<EOF
[Desktop Entry]
Type=Application
Version=1.0
Name=Spiral Knights
Comment=${pkgdesc}
Icon=/opt/${pkgname}/icon_32.png
Exec=${pkgname}
Terminal=false
Categories=Game;Java;
EOF
  mkdir -p "${pkgdir}/usr/share/applications"
  install -m644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications"
}
