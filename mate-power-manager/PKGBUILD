# Maintainer: Perberos <perberos@gmail.com>
pkgname=mate-power-manager
pkgver=20120309
pkgrel=1
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system for Mate Desktop"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmatenotify' 'libunique' 'libcanberra' 'hicolor-icon-theme' 'upower' 'libmatekeyring')
makedepends=('intltool' 'pkgconfig' 'mate-doc-utils' 'mate-panel' 'mate-control-center')
options=('!emptydirs')
install=mate-power-manager.install
url="http://matsusoft.com.ar/projects"
groups=('mate-extras')
source=()
sha256sums=()

_gitroot=git://github.com/mate-desktop/mate-power-manager.git
_gitname=mate-power-manager

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"

	./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/${pkgname} \
		--disable-scrollkeeper || return 1
		#--enable-applets
		#--with-dbus-services=/usr/share/dbus-1/services \
		#--with-dpms-ext \

	make || return 1
}

package() {
	cd "$srcdir/$_gitname-build"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
