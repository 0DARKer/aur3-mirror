# Maintainer: eolianoe <eolianoe At GoogleMAIL DoT com>

pkgname=python-vdirsyncer-git
_pkgname=vdirsyncer
pkgver=0.2.5.r2.g69505f4
pkgrel=1
pkgdesc="Synchronize CalDAV and CardDAV."
arch=('i686' 'x86_64')
url="https://github.com/untitaker/vdirsyncer"
license=('MIT')
depends=('python-click' 'python-setuptools' 'python-lxml' 'python-requests'
         'python-icalendar' 'python-requests-toolbelt')
source=("git://github.com/untitaker/${_pkgname}.git")
md5sums=('SKIP')
conflicts=('python-vdirsyncer')
provides=('python-vdirsyncer')

pkgver() {
  cd "$srcdir/${_pkgname}"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/${_pkgname}"
  python setup.py build
}

package() {
  cd "$srcdir/${_pkgname}"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

