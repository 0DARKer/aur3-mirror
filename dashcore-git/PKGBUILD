# Maintainer: Vertoe <vertoe AT darkcoin DOT io>
# Contributor: deusstultus <deusstultus AT gmail DOT com>
# Contributor: Viliam Kubis <viliam DOT kubis AT gmail DOT com>
# Based on PKGBUILD from vertcoin-git maintained by Lothar_m <lothar_m AT riseup DOT net>

pkgname='dashcore-git'
_gitname='darkcoin'
_gitbranch='master'
pkgver=6817.fea26cf
pkgrel=2
arch=('i686' 'x86_64')
url="https://www.dashpay.io/"
depends=('qt5-base' 'boost' 'boost-libs' 'miniupnpc' 'openssl')
makedepends=('autoconf' 'automake' 'binutils' 'gcc' 'libtool' 'make' 'pkg-config' 'git' 'qrencode' 'automoc4' 'protobuf' 'qt5-tools')
license=('MIT')
pkgdesc="Dash Core (DASH, formerly known as Darkcoin, DRK) is an open source, privacy-centric digital currency. (Includes the qt-client, the headless daemon and the command-line tool.)"
provides=('dashd' 'dash-qt' 'dash-cli')
conflicts=('darkcoin' 'dashcore' 'darkcoin-git' 'darkcoil' 'darkcoin-daemon-git' 'darkcoin-qt-git' 'darkcoin-cli-git')
source=("git://github.com/darkcoin/darkcoin.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git checkout "$_gitbranch" -q
  git pull -q
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  CXXFLAGS+=" -fPIE"
  cd "$srcdir/$_gitname"
  git checkout "$_gitbranch"
  git pull

  ./autogen.sh
  ./configure --with-incompatible-bdb --with-gui=qt5 --with-daemon --with-cli --disable-debug --disable-tests
  make
  strip ./src/qt/dash-qt
  strip ./src/dashd
  strip ./src/dash-cli
}

package() {
  install -D -m755 "$srcdir/$_gitname/src/qt/dash-qt" "$pkgdir/usr/bin/dash-qt"
  install -D -m755 "$srcdir/$_gitname/src/dashd" "$pkgdir/usr/bin/dashd"
  install -D -m755 "$srcdir/$_gitname/src/dash-cli" "$pkgdir/usr/bin/dash-cli"
  install -D -m644 "$srcdir/$_gitname/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 "$srcdir/$_gitname/share/pixmaps/dash128.png" "$pkgdir/usr/share/pixmaps/dash128.png"
  install -D -m644 "$srcdir/$_gitname/contrib/debian/dash-qt.desktop" "$pkgdir/usr/share/applications/dash-qt.desktop"
}
