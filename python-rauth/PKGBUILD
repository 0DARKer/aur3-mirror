# Maintainer: Carl George < arch at cgtx.us >

_module="rauth"
pkgname=("python-rauth" "python2-rauth")
pkgver="0.7.1"
pkgrel="1"
pkgdesc="A Python library for OAuth 1.0/a, 2.0, and Ofly"
arch=("any")
url="https://rauth.readthedocs.org"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://github.com/litl/${_module}/archive/${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

package_python-rauth() {
	depends=("python-requests")
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-rauth() {
	depends=("python2-requests")
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

sha256sums=('f2ce86a457bd41d8001755078172cccfaa08f14c489dadcac42526759f09655b')
