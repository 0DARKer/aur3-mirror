# Contributor: Thiago Kenji Okada <thiago DOT mast3r AT gmail DOT com>
pkgname=ppsspp
pkgver=0.9.7.2
pkgrel=1
pkgdesc="A PSP emulator for Android, Windows, Mac, and Linux, written in C++."
arch=(i686 x86_64)
url="http://www.ppsspp.org"
license=("GPL2")
depends=(zlib sdl ffmpeg glu)
makedepends=(cmake git)
conflicts=(ppsspp-git)
source=("ppsspp::git://github.com/hrydgard/ppsspp.git#tag=v$pkgver"
"ppsspp.desktop"
"ppsspp-sdl.sh"
"ppsspp-headless.sh")
md5sums=('SKIP'
         '7f1446f729de094efd0089fc786f9455'
         '31c47ff0bce3efcf3d87ad7f672717ca'
         'a6b06b03e2a49dffdb4fcdf279f7d20b')

prepare() {
	cd "$srcdir/ppsspp"
	git submodule update --init
}
         
build() {
	cd "$srcdir/ppsspp"
	export LDFLAGS="-lavutil -lavformat -lavcodec -lswscale -lswresample"
	mkdir build
	pushd build
	cmake -DHEADLESS=ON ..
	make
	popd
}

package() {
	cd "$srcdir/ppsspp"
	install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/ppsspp/LICENSE.TXT"
	install -Dm644 assets/icon.svg "$pkgdir/usr/share/icons/ppsspp.svg"
	install -Dm644 ../ppsspp.desktop "$pkgdir/usr/share/applications/ppsspp.desktop"
	install -Dm755 ../ppsspp-sdl.sh "$pkgdir/usr/bin/ppsspp-sdl"
	install -Dm755 ../ppsspp-headless.sh "$pkgdir/usr/bin/ppsspp-headless"
	install -Dm755 build/PPSSPPSDL "$pkgdir/usr/share/ppsspp/PPSSPPSDL"
	install -Dm755 build/PPSSPPHeadless "$pkgdir/usr/share/ppsspp/"
	cd build/assets
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/ppsspp/assets/"{} \;
}
