# Contributor: Thiago Kenji Okada <thiago DOT mast3r AT gmail DOT com>
# Contributor: carstene1ns <url/mail: arch carsten-teibes de>
pkgname=ppsspp
pkgver=0.9.8
pkgrel=2
pkgdesc="A PSP emulator for Android, Windows, Mac, and Linux, written in C++."
arch=(i686 x86_64)
url="http://www.ppsspp.org"
license=("custom, GPL2")
depends=(zlib sdl ffmpeg glu)
makedepends=(cmake setconf)
conflicts=(ppsspp-git)
source=("https://github.com/hrydgard/ppsspp/archive/v$pkgver.tar.gz"
"ppsspp-ffmpeg-for-$pkgver.tar.gz::https://github.com/hrydgard/ppsspp-ffmpeg/archive/6cffc1f6012e0091f46c031a023e877d621f83e4.tar.gz"
"ppsspp-lang-for-$pkgver.tar.gz::https://github.com/hrydgard/ppsspp-lang/archive/ec04fc188efc08ccf1c43ec337145f706e1fb807.tar.gz"
"native-for-$pkgver.tar.gz::https://github.com/hrydgard/native/archive/6b8a8a6c8d5277b295d5f0d53e5b218cb987a296.tar.gz"
"ppsspp-sdl.sh"
"ppsspp-headless.sh")
sha256sums=('8f1dfda85a5aa5c8be9c1556f897f3bbdc948553b4ebc42c86e6fa4fb2aad1d0'
            'c44cde0938a7a03d5f03ddcc529deaf04d0478951189fef9af685e5f3c91a82f'
            '22517a9a489ebc8392b95edb0dbe90f804fafc46f43f96ef081f953cec726d74'
            '93932843b2bd59f264a2804f3c9fbfa0c6b524a70fae57bee8fafd43b5bcb670'
            '81fdecc5efd17694d257d224f5ed1b5ebc10798590e644105f60d6ef885e78a9'
            'a80f43d7d9891556bb7c03cd05d822dc183a396fc20bc5a4084b1b655874fe5b')

prepare() {
	cd "$srcdir"
	# copy submodules to right location
  cp -rup native-*/* ppsspp-$pkgver/native
  cp -rup ppsspp-lang-*/* ppsspp-$pkgver/lang
  cp -rup ppsspp-ffmpeg-*/linux ppsspp-$pkgver/ffmpeg
  # disable git versioning
  sed 's|find_package(Git)|# &|' -i ppsspp-$pkgver/git-version.cmake
  # reset build folder
  rm -rf build
  mkdir build
}
         
build() {
	cd "$srcdir"
	cd build
  cmake ../ppsspp-$pkgver -DCMAKE_BUILD_TYPE=Release -DHEADLESS=ON
  make
}

package() {
	cd "$srcdir/build"
	install -Dm644 ../ppsspp-$pkgver/LICENSE.TXT "$pkgdir/usr/share/licenses/ppsspp/LICENSE.TXT"
	install -Dm644 ../ppsspp-$pkgver/assets/icon.svg "$pkgdir/usr/share/icons/ppsspp.svg"
	install -Dm644 ../ppsspp-$pkgver/Qt/PPSSPP.desktop "$pkgdir/usr/share/applications/PPSSPP.desktop"
	setconf "$pkgdir/usr/share/applications/PPSSPP.desktop" Exec /usr/bin/ppsspp-sdl
	setconf "$pkgdir/usr/share/applications/PPSSPP.desktop" Icon /usr/share/icons/ppsspp.svg
	install -Dm755 ../ppsspp-sdl.sh "$pkgdir/usr/bin/ppsspp-sdl"
	install -Dm755 ../ppsspp-headless.sh "$pkgdir/usr/bin/ppsspp-headless"
	install -Dm755 PPSSPPSDL "$pkgdir/usr/share/ppsspp/PPSSPPSDL"
	install -Dm755 PPSSPPHeadless "$pkgdir/usr/share/ppsspp/"
	cd assets
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/ppsspp/assets/"{} \;
}
