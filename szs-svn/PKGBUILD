# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=szs-svn
pkgver=3243
pkgrel=1
pkgdesc="A set of command line tools to manipulate SZS, U8, BRRES, BREFF, BREFT, BMG, KMP, TEX, TPL, and StaticR.rel files of Mario Kart Wii"
arch=('i686' 'x86_64')
url="http://szs.wiimm.de/"
license=('GPL')
depends=('libpng')
makedepends=('subversion')
provides=('szs' 'szstools')
conflicts=('szs' 'szstools')

_svntrunk='http://opensvn.wiimm.de/wii/trunk/wiimms-szs-tools/'
_svnmod='wiimms-szs-tools'

build() {
  cd "${srcdir}"
  msg "Connecting to SVN server...."

  if [[ -d "${_svnmod}/.svn" ]]; then
    (cd "${_svnmod}" && svn up -r "${pkgver}")
  else
    svn co "${_svntrunk}" --config-dir ./ -r "${pkgver}" "${_svnmod}"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_svnmod}-build"
  cp -r "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  # Fix linker error against libm
  export CFLAGS="${CFLAGS} -lm"
  make INSTALL_PATH="${pkgdir}/usr"
}

package() {
  cd "${srcdir}/${_svnmod}-build"
  ./install.sh
}

# vim:set ts=2 sw=2 et:
