# Maintainer: Florian Zeitz <florob at babelmonkeys dot de>

pkgname=objfw-git
pkgver=20121027
pkgrel=1
pkgdesc="A portable framework for the Objective C language."
arch=('i686' 'x86_64')
url="https://webkeks.org/objfw"
license=('custom:QPL' 'GPL2' 'GPL3')
provides=('objfw')
conflicts=('objfw')
replaces=('libobjfw-hg' 'objfw-hg')
depends=('sh' 'gcc-libs')
makedepends=('git' 'clang')

_gitroot=https://webkeks.org/git/objfw.git
_gitname=objfw
 
build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."

	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi
	
	msg "GIT checkout done or server timeout"

	rm -rf "${srcdir}/${_gitname}-build"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
	cd "${srcdir}/${_gitname}-build"
	./autogen.sh
	./configure --prefix=/usr OBJC="clang"
	make
}

package() {
	cd "${srcdir}/${_gitname}-build"
	make DESTDIR=${pkgdir} install

	install -Dm644 LICENSE.QPL "$pkgdir/usr/share/licenses/$pkgname/LICENSE.QPL"
}

# vim:set ts=2 sw=2 et:
