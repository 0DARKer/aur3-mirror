# $Id$
# Maintainer: Florian Zeitz <florob at babelmonkeys dot de>

pkgname=objfw-git
pkgver=20121007
pkgrel=1
pkgdesc="A portable framework for the Objective C language."
arch=('i686' 'x86_64')
url="https://webkeks.org/objfw"
license=('custom:QPL' 'GPL2' 'GPL3')
provides=('objfw')
conflicts=('objfw')
replaces=('libobjfw-hg' 'objfw-hg')
depends=('gcc-libs' 'sh')
makedepends=('git' 'gcc-objc')
optdepends=('gcc-objc: For active usage of this framework.')
 
_gitroot=https://webkeks.org/git/objfw.git
_gitname=objfw
 
build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."
	
	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi
	
	msg "GIT checkout done or server timeout"

	rm -rf "${srcdir}/${_gitname}-build"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
	cd "${srcdir}/${_gitname}-build"
	./autogen.sh
	./configure --prefix=/usr
	#./configure --prefix=/usr OBJC="clang"
	make
}

package() {
	cd "${srcdir}/${_gitname}-build"
	make DESTDIR=${pkgdir} install

	install -Dm644 LICENSE.QPL "$pkgdir/usr/share/licenses/$pkgname/LICENSE.QPL"
}
