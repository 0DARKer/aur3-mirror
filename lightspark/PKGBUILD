# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lightspark
pkgver=0.5.5
pkgrel=1
pkgdesc='An alternative Flash Player for Linux.'
arch=('i686' 'x86_64')
url='http://lightspark.sourceforge.net'
license=('GPL3' 'LGPL3')
depends=('gtk2' 'ffmpeg' 'glew' 'curl' 'boost-libs' 'libxml++' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('cmake' 'nasm' 'xulrunner' 'llvm' 'glproto' 'boost' 'fontconfig')
optdepends=('gnash-gtk: fallback support')
install="$pkgname.install"
source=(http://launchpad.net/lightspark/trunk/lightspark-$pkgver/+download/lightspark-$pkgver.tar.gz{,.asc})
md5sums=('e6279fcff6904620bf9e8ca543a0f742'
         '45da83b3c1f1e22f96bd002f9dd249de')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ -d bld ] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DGNASH_EXE_PATH=/usr/bin/gtk-gnash
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/bld"

  make DESTDIR="$pkgdir/" install
}
