# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lightspark
pkgver=0.6.0.1
pkgrel=1
pkgdesc='An alternative Flash Player for Linux.'
arch=('i686' 'x86_64')
url='http://lightspark.sourceforge.net'
license=('GPL3' 'LGPL3')
depends=('gtk2' 'ffmpeg' 'glew' 'curl' 'boost-libs' 'libxml++' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('cmake' 'nasm' 'xulrunner' 'llvm' 'glproto' 'boost' 'fontconfig')
optdepends=('gnash-gtk: fallback support')
install="$pkgname.install"
source=(http://launchpad.net/lightspark/trunk/lightspark-$pkgver/+download/lightspark-$pkgver.tar.gz{,.asc})
md5sums=('66e13a61d06dd0c8c81303fabaee8549'
         'e8bd73b4e6f3be0c7498c670be5a0919')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [ -d bld ] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DGNASH_EXE_PATH=/usr/bin/gtk-gnash
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/bld"
  make DESTDIR="$pkgdir/" install
}
