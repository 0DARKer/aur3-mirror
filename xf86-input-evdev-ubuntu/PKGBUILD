pkgname=xf86-input-evdev-ubuntu
pkgver=2.6.0
_ubuntu_ver=1ubuntu13
pkgrel=3
pkgdesc="X.org evdev input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc' 'xorg-server-ubuntu')
makedepends=('xorg-server-devel')
provides=('xf86-input-evdev')
conflicts=('xorg-server<1.10.0' 'xf86-input-evdev')
options=('!libtool' '!makeflags')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname%-*}-${pkgver}.tar.bz2
        http://archive.ubuntu.com/ubuntu/pool/main/x/xserver-xorg-input-evdev/xserver-xorg-input-evdev_${pkgver}-${_ubuntu_ver}.diff.gz)
sha512sums=('c5ff503d6bde7e5690333867595d54ddd5403e6d8f556b09a631bcb65629aa82f55c694f5257d925541cbe86cc47a9464a3c21b9ff62fda8b4bf0118c92637c5'
            'e15a8fa85263eef40e7bcc0b6a2e8a14fbdd0174a8083fdac8269f5c8c893cf3d8f005d834c60246a26d35a7c6e5372e3c1de86edc434373762c9180869d2673')

build() {
  cd "${srcdir}"

  patch -Np1 -i "${srcdir}/xserver-xorg-input-evdev_${pkgver}-${_ubuntu_ver}.diff"

  cd "${srcdir}/${pkgname%-*}-${pkgver}"

  for i in $(cat "${srcdir}/debian/patches/series" | grep -Ev '#'); do
    patch -Np1 -i "${srcdir}/debian/patches/${i}"
  done

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname%-*}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname%-*}/"
  install -m644 -d "${pkgdir}/usr/share/X11/xorg.conf.d"
  install -m644 "${srcdir}/debian/local/11-evdev-quirks.conf" "${pkgdir}/usr/share/X11/xorg.conf.d/"
  install -m644 "${srcdir}/debian/local/11-evdev-trackpoint.conf" "${pkgdir}/usr/share/X11/xorg.conf.d/"
}
