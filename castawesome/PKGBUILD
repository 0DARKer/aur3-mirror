# Maintainer: Alex Mason <alex@judgementstudios.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=castawesome
pkgver=0.14.5
pkgrel=1
pkgdesc="A GUI frontend for ffmpeg livestreaming"
arch=('any')
url="http://sourceforge.net/projects/castawesome/"
license=('GPL3')
depends=('ffmpeg' 'gtk3' 'python2' 'python2-gobject2')
source=(http://sourceforge.net/projects/castawesome/files/$pkgname-$pkgver.tar.gz)
sha256sums=('da3bb3dcbc050a6166803a48e118b0c588979b9014c2103ee9517099b7a0b27e')

prepare() {
  cd $srcdir/$pkgname

  msg 'Fixing Makefile...'
  sed -i 's#cp castawesome.py#install -Dm 755 castawesome.py#' Makefile
  sed -i 's#/usr/local#$(DESTDIR)/usr#g' Makefile
  sed -i 's#/usr/local#/usr#' castawesome.py
  sed -i 's#/usr/local#/usr#' uninstall_castawesome.sh
}

build() {
  cd $srcdir/$pkgname

  msg 'Building...'
  make
}

package() {
  cd $srcdir/$pkgname

  msg 'Installing...'
  install -dm 755 $pkgdir/usr/share/castawesome
  make DESTDIR="$pkgdir" install

  msg 'Cleaning up pkgdir...'
  rm -rf $pkgdir/usr/share/applications
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
