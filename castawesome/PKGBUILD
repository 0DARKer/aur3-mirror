# Maintainer: Dominik Pieczy≈Ñski <dominik.pieczynski@gmail.com>
# Contributor: Alex Mason <alex@judgementstudios.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=castawesome
pkgver=0.15.0
pkgrel=1
arch=('any')
pkgdesc='A GUI frontend for ffmpeg livestreaming'
url='http://sourceforge.net/projects/castawesome/'
license=('GPL3')
depends=('ffmpeg' 'gtk3' 'python2' 'python2-gobject' 'python2-gobject2')
source=(http://sourceforge.net/projects/castawesome/files/$pkgname-$pkgver.tar.gz)
sha256sums=('127ee592fdd81bd0e48cce1dfb9cadd4b37d904eb90a97cfeb999e32eab916ad')

prepare() {
  cd $pkgname

  sed -i 's#cp castawesome.py#install -Dm 755 castawesome.py#' Makefile
  sed -i 's#/usr/local#$(DESTDIR)/usr#g' Makefile
  sed -i 's#/usr/local#/usr#' castawesome.py
  sed -i 's#/usr/local#/usr#' uninstall_castawesome.sh
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/share/castawesome"
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/share/applications"
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
