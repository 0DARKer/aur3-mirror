# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=daggerfall
pkgver=1.07.213
pkgrel=2
pkgdesc="The Elder Scrolls II: Daggerfall"
arch=('any')
url="http://www.elderscrolls.com/daggerfall/"
license=('custom:daggerfall')
depends=(dosbox)
makedepends=(python2 unzip bsdiff)
options=(emptydirs)
install=dagger.install
source=(http://cms.elderscrolls.com/sites/default/files/tes/extras/DFInstall.zip
        http://www.uesp.net/dagger/files/addquest.zip
        http://www.uesp.net/dagger/files/fixsa175.zip
        license daggerfall.sh RUN.BAT Z.CFG HMISET.CFG unpk.py maps.patch dos32a.patch
        dagger.conf dagger-fixmaps.conf dagger-fixsave.conf dagger-setup.conf)
noextract=(addquest.zip fixsa175.zip)
md5sums=('3cdd09a5696c2b94c58b85488be7cba2'
         '1074c3e593375542e8e45256c6f9ada4'
         'e5647c7acba32973eb6b2fba621ae536'
         '2923e8e848462a4a05aa6b5473cd82b5'
         '7f368784ea99d887f57c0a93c7117916'
         '3bc199099cafde9d9b047d3738f71a7e'
         'eae2f2244cc23bc1f266438ca4d9b2ce'
         'bd94604036a62217617fd28092c9d956'
         'e27211d75b14ede06d98be39942c7ab3'
         '4e773bc05d54c36a53c7aec2c2d2f538'
         '37f3dc3e8ffc174bb4a946e18c8b90db'
         '9894ff607b3bcf7ee80ff36e93ca3e14'
         'a13e6e5ba98042905bc2c446a639b219'
         'a6b12e132983aeac42c07733cf7b89a8'
         '962841dd890ea7177c813703e5e9b1a2')

build() {
  cd "$srcdir"
  _target="${pkgdir}"/usr/share/games/daggerfall
  install -d "$_target"
  cp -rf "$srcdir/DFCD/DAGGER" "$_target"
  cp -rf "$srcdir/DFCD/DATA" "$_target/DAGGER"
  cp -rf "$srcdir/DFCD/INSTALL.EXE" "$_target/DAGGER"
  cp -rf "$srcdir/DFCD/INSTALL.PIF" "$_target/DAGGER"
  for i in {0..5}; do install -d "$_target/DAGGER/SAVE$i"; done
  mv "$_target/DAGGER/ARENA2/faction.txt" "$_target/DAGGER/ARENA2/FACTION.TXT"
  cp "$srcdir/RUN.BAT" "$_target/DAGGER"
  cp "$srcdir/Z.CFG" "$_target/DAGGER"
  cp "$srcdir/HMISET.CFG" "$_target/DAGGER"
  python2 unpk.py "$srcdir/DFCD/DAGGER/ARENA2/PACKED.DAT" "$_target/DAGGER"
  rm "$_target/DAGGER/ARENA2/PACKED.DAT"
  unzip -o "$srcdir/addquest.zip" -d "$_target/DAGGER/ARENA2"
  rm "$_target/DAGGER/ARENA2/readme.txt"
  _offset=$((`grep -Ubo --binary-files=text 'start of data' "$srcdir/DAGGER/DAG213.EXE" | head -1 | sed 's/:.*//g'`+13))
  python2 unpk.py "$srcdir/DAGGER/DAG213.EXE" "$_target/DAGGER" ${_offset}
  unzip -o "$srcdir/fixsa175.zip" -d "$_target/DAGGER"
  bspatch "$_target/DAGGER/ARENA2/MAPS.BSA" "$_target/MAPS.BSA" "$srcdir/maps.patch"
  mv "$_target/MAPS.BSA" "$_target/DAGGER/ARENA2/MAPS.BSA"
  bspatch "$_target/DAGGER/FALL.EXE" "$_target/FALL.EXE" "$srcdir/dos32a.patch"
  mv "$_target/FALL.EXE" "$_target/DAGGER/FALL.EXE"
  cp "$srcdir/dagger.conf" "$_target"
  cp "$srcdir/dagger-fixmaps.conf" "$_target"
  cp "$srcdir/dagger-fixsave.conf" "$_target"
  cp "$srcdir/dagger-setup.conf" "$_target"
  install -D -m644 "$srcdir/license" "$pkgdir/usr/share/licenses/daggerfall/license"
  install -D -m754 "$srcdir/daggerfall.sh" "$pkgdir/usr/bin/daggerfall"
  chgrp games "$pkgdir/usr/bin/daggerfall"
  chgrp -R games "$_target"
  chmod -R g+w "$_target"
}

