# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

pkgname='glxgears'
pkgver=8.1.0
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="The standalone glxgears binary from the mesa-demos project"
url="http://mesa3d.sourceforge.net"
license=('MIT')
depends=('libgl')
makedepends=('glew')
conflicts=('mesa-demos')
source=("ftp://ftp.freedesktop.org/pub/mesa/demos/${pkgver}/mesa-demos-${pkgver}.tar.bz2")
sha256sums=('9703fa0646b32a1e68d2abf5628f936f77bf97c69ffcaac90de380820a87a828')

build() {
	cd "${srcdir}/mesa-demos-${pkgver}"
	./configure --prefix=/usr
	make -C src/xdemos bin_PROGRAMS="glxgears"
}

package() {
	cd "${srcdir}/mesa-demos-${pkgver}"
	make DESTDIR="${pkgdir}" -C src/xdemos bin_PROGRAMS="glxgears" install
}
