# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FzerorubigD <Fzerorubigd {AT} GMail {DOT} com>

pkgname=gpaste-git
pkgver=2.99.2+29
pkgrel=1
pkgdesc="Clipboard management system with a GNOME Shell extension"
url="https://github.com/Keruspe/GPaste"
arch=(i686 x86_64)
license=(GPL3)
depends=(gnome-shell)
makedepends=(git intltool vala)
install=$pkgname.install
options=(!libtool !emptydirs)
provides=("gpaste=$pkgver")

source=(git://github.com/Keruspe/GPaste.git)
md5sums=('SKIP')

pkgver() {
  cd GPaste
  git describe | sed -r 's/^v([^-]+)-([0-9]+)-g.*$/\1+\2/'
}

build() {
  cd GPaste
  ./autogen.sh
  ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc \
    --enable-gnome-shell-extension --enable-systemd
  make
}

check () {
  cd GPaste
  make check
}

package() {
  cd GPaste
  make DESTDIR="$pkgdir" install

  cd data/completions
  install -Dm644 gpaste "$pkgdir/usr/share/bash-completion/completions/gpaste"
  install -Dm644 _gpaste "$pkgdir/usr/share/zsh/site-functions/_gpaste"
}
