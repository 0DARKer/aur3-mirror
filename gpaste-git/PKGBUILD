# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FzerorubigD <Fzerorubigd {AT} GMail {DOT} com>

pkgname=gpaste-git
pkgver=3.10+59+g65d6ed5
pkgrel=1
pkgdesc="Clipboard management system with a GNOME Shell extension"
url="https://github.com/Keruspe/GPaste"
arch=(i686 x86_64)
license=(GPL3)
depends=(gnome-shell)
makedepends=(git intltool vala appdata-tools)
install=$pkgname.install
options=(!libtool !emptydirs debug !strip)
provides=("gpaste=$pkgver")

source=(git://github.com/Keruspe/GPaste.git)
md5sums=('SKIP')

pkgver() {
  cd GPaste
  git describe --long | sed 's/^v//;s/-/+/g'
}

build() {
  cd GPaste
  ./autogen.sh
  ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc \
    --enable-gnome-shell-extension --enable-systemd
  make
}

check () {
  cd GPaste
  make -k check || :
}

package() {
  cd GPaste
  make DESTDIR="$pkgdir" install

  cd data/completions
  install -Dm644 gpaste "$pkgdir/usr/share/bash-completion/completions/gpaste"
  install -Dm644 _gpaste "$pkgdir/usr/share/zsh/site-functions/_gpaste"
}
