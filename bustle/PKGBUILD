# Maintainer: Alex Merry <dev@randomguy3.me.uk>
pkgname=bustle
pkgver=0.4.0
pkgrel=1
pkgdesc="A D-Bus profiler"
arch=(i686 x86_64)
url="http://willthompson.co.uk/bustle/"
license=('GPL')
depends=('dbus-core' 'libglade')
makedepends=('ghc'
             'haskell-binary'
             'haskell-cairo'
             'haskell-dbus-core>=0.9'
             'haskell-glade'
             'haskell-gtk'
             'haskell-mtl'
             'haskell-parsec'
             'haskell-pango'
             'haskell-pcap'
             'haskell-text')
source=(http://willthompson.co.uk/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz
        bustle-ghc-7.4.1.patch
        bustle.1 bustle-pcap.1)
md5sums=('4d5c72d1947681f5c0a2351afe313c41'
         '25550a0f5d6f06ac3e2f9715e794dfd4'
         '1ad8189edaa42c01e6f618705e9e9692'
         '01f8d9753f7b678367c60090f43de03b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/bustle-ghc-7.4.1.patch"

  # build the monitor
  make

  # build the graphing tool
  ghc -package Cabal Setup.hs -o setup
  ./setup configure --prefix /usr
  ./setup build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install the monitor
  make PREFIX="$pkgdir/usr" install

  # install the graphing tool
  ./setup copy --destdir "$pkgdir"

  cd "$srcdir"
  install -d -m755 "$pkgdir/usr/share/man/man1"
  install -m644 bustle.1 bustle-pcap.1 "$pkgdir/usr/share/man/man1/"

  install -d -m755 "$pkgdir/usr/share/licenses/bustle"
  mv "$pkgdir/usr/share/doc/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/bustle/LICENSE"
  rmdir "$pkgdir/usr/share/doc/$pkgname-$pkgver"
  rmdir "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
