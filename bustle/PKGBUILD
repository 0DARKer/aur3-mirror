# Maintainer: Alex Merry <dev@randomguy3.me.uk>
pkgname=bustle
pkgver=0.2.5
pkgrel=1
pkgdesc="A D-Bus profiler"
arch=(i686 x86_64)
url="http://willthompson.co.uk/bustle/"
license=('GPL')
depends=('dbus-core' 'libglade')
makedepends=('ghc' 'haskell-mtl' 'haskell-parsec' 'gtk2hs-gtk' 'gtk2hs-glade')
source=($pkgname-$pkgver.tar.gz bustle.1 bustle-count.1 bustle-dbus-monitor.1 bustle-dot.1 bustle-time.1)
md5sums=('3167e201815cb40727f8fcc13459aa97'
         '1ad8189edaa42c01e6f618705e9e9692'
         'c771545db820441783d53b13e9946cce'
         '23c0e382c7c4031dc8e72e4f9f3cf92f'
         '904ff50172ccc36c408df469dcb5499e'
         'bd08e02e3f8c4d17367be0f4d2ec6354')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # build the monitor
  make

  # build the graphing tool
  ghc -package Cabal Setup.lhs -o setup
  ./setup configure --prefix /usr
  ./setup build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  ./setup copy --destdir "$pkgdir"
  install -m755 bustle-dbus-monitor "$pkgdir/usr/bin/"

  cd "$srcdir"
  install -d -m755 "$pkgdir/usr/share/man/man1"
  install -m644 bustle.1 bustle-count.1 bustle-dbus-monitor.1 bustle-dot.1 bustle-time.1 "$pkgdir/usr/share/man/man1/"

  install -d -m755 "$pkgdir/usr/share/licenses/bustle"
  mv "$pkgdir/usr/share/doc/$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/bustle/LICENSE"
  rmdir "$pkgdir/usr/share/doc/$pkgname-$pkgver"
  rmdir "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
