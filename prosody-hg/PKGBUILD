#Maintainer: Nick Campbell <s0ma AT theangryfist DOT net>
#Contributor: Dwayne Bent <dbb.0@liqd.org>

pkgname=prosody-hg
pkgver=20091124
pkgrel=1
pkgdesc="Development tip of a lightweight and extensible Jabber/XMPP server written in Lua."
arch=('i686' 'x86_64')
url="http://prosody.im"
license=('MIT')
depends=('lua>=5.1' 'luasocket' 'luaexpat' 'luasec' 'luafilesystem' 'libidn>=0.5.18' 'openssl')
makedepends=('mercurial')
provides=('prosody')
conflicts=('prosody' 'prosody-devel')
backup=('etc/prosody/prosody.cfg.lua'
        'etc/logrotate.d/prosody')

source=('prosody.rcd' 'prosody.logrotated')

md5sums=('367ffff466ae63e565b350e6ce9042c1'
         '26466fdbea87963a3ca6f48f76fe4a29')

install=prosody.install

_hgroot=http://prosody.im/source/
_hgrepo=hg

build() {
   cd ${srcdir}

   if [ -d ${_hgrepo}/.hg ]; then
       cd ${_hgrepo}
       hg pull -u || return 1
   else
       hg clone ${_hgroot} ${_hgrepo} || return 1
   fi

   msg "Mercurial checkout done or server timeout"
   msg "Starting make..."
 
   cp -r ${srcdir}/${_hgrepo} ${srcdir}/${_hgrepo}-build
   cd ${srcdir}/${_hgrepo}-build

   ./configure --prefix=/usr --sysconfdir=/etc/prosody \
    --datadir=/var/lib/prosody || return 1

   make || return 1
   make DESTDIR="$pkgdir" install || return 1

   rm $pkgdir/etc/prosody/certs/*

   install -d "$pkgdir/etc/rc.d" || return 1
   install -d "$pkgdir/etc/logrotate.d" || return 1
   install -d "$pkgdir/var/log/prosody" || return 1
   install -d "$pkgdir/var/run/prosody" || return 1
   install -o root -g root -m 644 "${srcdir}/${_hgrepo}/prosody.cfg.lua.dist" \
      "$pkgdir/etc/prosody/prosody.cfg.lua" || return 1
   install -o root -g root -m 755 "$startdir/prosody.rcd" \
      "$pkgdir/etc/rc.d/prosody" || return 1
   install -o root -g root -m 644 "$startdir/prosody.logrotated" \
      "$pkgdir/etc/logrotate.d/prosody" || return 1
  
   rm -rf ${srcdir}/${_hgrepo}-build  
 
}
