# Contributor: Drew Liszewski <drew dot liszewski at gmail dot com>
# Maintainer: Daniel Varga <varga dot daniel at gmx dot de>
pkgname=emulationstation-git
_gitname=EmulationStation
pkgrel=1
pkgdesc="A graphical front-end for emulators with controller navigation. Developed for the Raspbery Pi, but runs on most Linux systems."
arch=('i686' 'x86_64' 'armv6h')
url="https://github.com/Aloshi/EmulationStation"
license=('MIT')
groups=()
makedepends=('git' 'boost' 'freetype2' 'eigen' 'cmake' 'mesa' 'libsm')
depends=('alsa-lib' 'sdl2' 'boost-libs' 'freeimage' 'ttf-dejavu' 'glu')
if [ "$CARCH" = "armv6h" ]; then
depends+=('raspberrypi-firmware')
else
depends+=('libgl')
fi
source=('git://github.com/Aloshi/EmulationStation.git')
md5sums=('SKIP')

pkgver() {
    cd $_gitname
    git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    mkdir -p ${_gitname}/build
    cd ${_gitname}/build
    cmake ..
    make
}

package() {
    cd $_gitname
    install -Dm755 "$srcdir/EmulationStation/emulationstation" "$pkgdir/usr/bin/emulationstation"
    install -Dm644 "$srcdir/EmulationStation/LICENSE.md" "$pkgdir/usr/share/licenses/emulationstation-git/LICENSE"
}
