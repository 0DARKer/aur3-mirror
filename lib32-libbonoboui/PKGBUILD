_pkgbasename=libbonoboui
pkgname=lib32-$_pkgbasename
pkgver=2.24.4
pkgrel=3
pkgdesc="User Interface library for Bonobo (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL' 'LGPL')
depends=('lib32-libgnome' 'lib32-libsm' 'lib32-libgnomecanvas')
makedepends=('intltool' 'gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('4b683a4a933392384267a0bf99771a99ef2ceacee22d67efe5648b894f8ba0c7')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
  		--disable-static --libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include,share} \
  	 ${pkgdir}/usr/lib32/{bonobo,bonobo-2.0}
}
