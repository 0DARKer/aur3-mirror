
pkgname=oyranos-git
pkgver=0.9.5.587.gb6df070
pkgrel=1
pkgdesc="A Colour Management System (CMS) on operating system level. (GIT version)"
arch=('i686' 'x86_64')
url="http://www.oyranos.org/kolormanager/"
license=('BSD')
depends=('qt4' 'exiv2' 'elektra-git' 'libxcm-git' 'libraw' 'fltk' 'xcalib')
makedepends=('git' 'cmake')
provides=('oyranos')
conflicts=('oyranos')
source=("git+https://github.com/oyranos-cms/oyranos.git")
sha1sums=('SKIP')
install=oyranos.install

pkgver() {
  cd oyranos
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../oyranos \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 oyranos/COPYING.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for i in $(find ${pkgdir}/usr/share/doc -type f); do sed 's|&#45;git|-git|g' -i ${i} ;done
  for i in $(find ${pkgdir}/usr/share/doc -type f); do sed "s|${srcdir}|/usr/share/doc|g" -i ${i} ;done
  sed -e "s|${srcdir}/build|\"\"|g" -e "s|${srcdir}/oyranos|\"\"|g" -i "${pkgdir}/usr/bin/oyranos-config"
}
