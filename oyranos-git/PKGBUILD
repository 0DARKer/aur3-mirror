
pkgname=oyranos-git
pkgver=0.9.5.163.g7f421da
pkgrel=1
pkgdesc="A Colour Management System (CMS) on operating system level. (GIT version)"
arch=('i686' 'x86_64')
url="http://www.oyranos.org/kolormanager/"
license=('BSD')
depends=('qt4' 'exiv2' 'elektra-git' 'libxcm' 'libraw')
makedepends=('git' 'cmake')
provides=('oyranos')
conflicts=('oyranos')
source=("git+https://github.com/oyranos-cms/oyranos.git")
md5sums=('SKIP')
_gitname="oyranos"
install=oyranos.install

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd "${_gitname}"
  make test
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for i in $(find ${pkgdir}/usr/share/doc -type f); do sed "s|${srcdir}|/usr/share/doc|g" -i ${i} ;done
  sed "s|${srcdir}/${_gitname}|\"Make by Makepkg\"|g" -i "${pkgdir}/usr/bin/oyranos-config"
}
