# Maintainer: Ner0

pkgname=scratch-bzr
pkgver=1017
pkgrel=1
pkgdesc="A text editor written in Vala by elementary"
arch=('i686' 'x86_64')
url="https://launchpad.net/scratch"
license=('GPL3')
depends=('gtksourceview3' 'libgee' 'gobject-introspection' 'glib2' 'desktop-file-utils' 'devhelp' 'gconf' 'gtk3'
         'python2-chardet' 'granite-bzr>=460' 'libpeas' 'pango' 'dconf' 'hicolor-icon-theme' 'vte3' 'libzeitgeist')
makedepends=('bzr' 'cmake' 'vala')
optdepends=('contractor-bzr: Contractor integration [AUR]'
            'libsoup: Pastebin plugin'
            'pantheon-files-bzr: File Manager plugin [AUR]')
conflicts=('scratch-text-editor')
provides=('scratch-text-editor')
options=('!makeflags')
install=scratch.install
source=('bzr+http://bazaar.launchpad.net/~elementary-apps/scratch/scratch/')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/scratch"
  bzr revno
}

prepare() {
  cd "$srcdir/scratch"
  sed -i '/enable_testing\ \(\)/d' CMakeLists.txt
  sed -i '/--warn-all/d' cmake/GObjectIntrospectionMacros.cmake
  sed -i '/core-tests/d' scratchcore/CMakeLists.txt
  sed -i 's/python$/python2/' scripts/*.py
  sed -i '/spell-check/d' plugins/CMakeLists.txt
}

build() {
  cd "$srcdir/scratch"
  rm -rf build/
  mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DGSETTINGS_LOCALINSTALL=OFF
  make
}

package() {
  cd "$srcdir/scratch/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
  chmod +x "$pkgdir"/usr/share/scratch/scripts/*.py
}
