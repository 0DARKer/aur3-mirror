# Maintainer: crazyelf5

pkgname=scratch-bzr
pkgver=458
pkgrel=1
pkgdesc="A text editor from the Elementary project"
arch=('i686' 'x86_64')
url="https://launchpad.net/scratch"
license=('GPL3')
depends=('gtksourceview3' 'libgee' 'gobject-introspection' 'glib2'
         'gconf' 'gtk3' 'granite-bzr' 'pango' 'dconf' 'hicolor-icon-theme')
makedepends=('bzr' 'cmake' 'vala' 'desktop-file-utils')
options=('!makeflags')
install=$pkgname.install

_bzrtrunk=lp:scratch
_bzrmod=scratch

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver
    msg "The local files are updated."
    cd "$srcdir"
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build
  [ -d build ] && rm -rf build
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make DESTDIR="$pkgdir/" install
}
