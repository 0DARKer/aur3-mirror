# Maintainer: thotypous <matiasΘarchlinux-br·org>
pkgname="chipmunksystem"
pkgver=20050630
pkgrel=1
pkgdesc="Circuit schematic and simulation environment"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.cs.berkeley.edu/~lazzaro/chipmunk/"
depends=('libx11')
makedepends=('sed' 'findutils')
source=("http://www.cs.berkeley.edu/~lazzaro/chipmunk/pickup/sources/psys-1.59.tar.gz"
        "http://www.cs.berkeley.edu/~lazzaro/chipmunk/pickup/sources/log-5.64.tar.gz"
        "http://www.cs.berkeley.edu/~lazzaro/chipmunk/pickup/sources/view-1.14.tar.gz"
        "http://www.cs.berkeley.edu/~lazzaro/chipmunk/pickup/sources/until-1.14.tar.gz"
        "http://www.cs.berkeley.edu/~lazzaro/chipmunk/pickup/sources/wol-1.14.tar.gz"
        "http://www.cs.berkeley.edu/~lazzaro/chipmunk/pickup/sources/wolcomp-1.14.tar.gz"
        "http://www.cs.berkeley.edu/~lazzaro/chipmunk/pickup/sources/netcmp-1.13.tar.gz"
        "http://www.cs.berkeley.edu/~lazzaro/chipmunk/pickup/sources/mosis-1.14.tar.gz"
        "http://www.cs.berkeley.edu/~lazzaro/chipmunk/pickup/sources/util-1.11.tar.gz")
md5sums=('710dd16137962b27ab6e48fe2629dee7'
         '96466efd12cc09e1d5ba454724f5e545'
         'dc75cbc39d10b8a515aeaf6ed3962046'
         '2e4cefe1305c80e15dec0f7989c8eda4'
         '2ab676624c5771104f3b7ddd8cbabab2'
         'c3c35fad428ae0059b3aff1ddb642f12'
         '50558d4a2791cab30a1a5e5d719b62ee'
         '4cb7791bb6144b878d2dced2febe75b7'
         '47f48f6fd638aac0251de82be097236d')

build() {
	cd "$srcdir"

	# Adjust Makefiles
	for _x in `find . -name Makefile`; do
		sed -i "s|^CHIPMUNKFLAGS =.*$|CHIPMUNKFLAGS = -DBSD -Dlinux -DF_OK=0 ${CFLAGS}|" "${_x}"
	done

	# Fix paths
	sed -i "s|-DCHIPLIB=.*$|-DCHIPLIB=\\\\\"/usr/share/${pkgname}/lib\\\\\"|" 'psys/src/Makefile'
	sed -i "s|^LOGLIBDIR =.*$|LOGLIBDIR = /usr/share/${pkgname}/lib|" 'log/src/Makefile'
	for _x in 'psys/src/plot.c' 'log/lib/log.doc' 'log/lib/logspc.cnf' 'log/lib/lplot.cnf' 'log/lib/lplot.cnf'; do
		sed -i "s|/usr/chipmunk|/usr/share/${pkgname}|" "${_x}"
	done
	for _x in 'wol/wol_drc.c' 'wol/wol_init.c'; do
		sed -i "s|/usr/%s|/home/%s/.wol|" "${_x}"
	done

	# Build everything
	for _x in 'psys/src' 'log/src' 'view' 'until/V1.2' 'wol' 'wolcomp' 'netcmp' 'mosis' 'util' 'util/spc-tools'; do
		cd "${srcdir}/${_x}"
		make LIBDIR="/usr/share/${pkgname}/lib" || return 1
		make install || return 1
	done

	# Copy everything
	install -m755 -d "${pkgdir}/usr/bin"
	install -m755 "${srcdir}/bin"/* "${pkgdir}/usr/bin"
	install -m755 -d "${pkgdir}/usr/share/${pkgname}/lib"
	install -m644 "${srcdir}/lib"/* "${pkgdir}/usr/share/${pkgname}/lib"
	install -m644 "${srcdir}/log/lib"/* "${pkgdir}/usr/share/${pkgname}/lib"
	for _x in view cnf; do
		install -m644 "${srcdir}/view/lib"/*.${_x} "${pkgdir}/usr/share/${pkgname}/lib"
	done
	install -m755 -d "${pkgdir}/usr/share/${pkgname}/until/designrules"
	install -m644 "${srcdir}/until/designrules"/* "${pkgdir}/usr/share/${pkgname}/until/designrules"
	install -m644 "${srcdir}/until/V1.2"/*.ff "${pkgdir}/usr/share/${pkgname}/until"
	install -m755 -d "${pkgdir}/usr/share/${pkgname}/netcmp"
	install -m644 "${srcdir}/netcmp"/*.ntk "${pkgdir}/usr/share/${pkgname}/netcmp"
}
