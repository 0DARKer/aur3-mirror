# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=dvdx-open-edition
pkgver=4.0.1.0
pkgrel=1
pkgdesc="DVDx 4.0 is an audio/video encoder as well as a powerful DVD copier. (Open Edition)"
arch=('i686' 'x86_64')
url="http://www.labdv.com/dvdx"
license=('LGPL' 'GPL3')
depends=('qt')
makedepends=('p7zip' 'patch')
optdepends=("mencoder: Encoder Engine"
            "ffmpeg: Encoder Engine"
            "mediainfo: Media Information"
            "mplayer: Player Engine")
install="${pkgname}".install
source=(http://downloads.sourceforge.net/project/dvdx/4.0/"${pkgver}"/"${pkgname}"-"${pkgver}"-src.7z
        http://www.labdv.com/dvdx/extras/tools.tar.gz
        dvdx4openicon.svg
        DVDx_4_Open_Edition.desktop
        interfacemplayer.patch)
md5sums=('b3e94cae5c83cf401ad6e261043e334b'
         '27255e4a565c40207e864782622beeb8'
         '1a184efc61b81abfa353524fe3b5eb91'
         '0d6a825126e33a67e6aefd5164f8c14e'
         'ca3cb4f75ea51305cad5040d87b048e1')

build() {
     cd "${srcdir}"
     7z x "${pkgname}"-"${pkgver}"-src.7z
     #change executable dependency paths
     sed -i 's|opt/dvdx4-open-edition|usr|g' "${srcdir}"/src/main/paths.h
     sed -i 's|mplayer|/usr/bin/mplayer|g' "${srcdir}"/src/main/paths.h
     sed -i 's|mencoder|/usr/bin/mencoder|g' "${srcdir}"/src/main/paths.h
     patch -p0 -d "${srcdir}"/src/player < interfacemplayer.patch
     cd src
     sh build-release-linux.sh
}

package() {
     cd "${srcdir}"
     mkdir -p "${pkgdir}"/usr/{bin,share}
     mkdir -p "${pkgdir}"/usr/share/{applications,doc,icons}
     mkdir -p "${pkgdir}"/usr/share/doc/"${pkgname}"
     mkdir -p "${pkgdir}"/usr/share/icons/hicolor/scalable/apps
     install DVDx_4_Open_Edition.desktop "${pkgdir}"/usr/share/applications/
     install dvdx4openicon.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/
     install src/LICENSE.{GPL3,LGPL} "${pkgdir}"/usr/share/doc/"${pkgname}"/
     install build/unix/release/bin/DVDx4 "${pkgdir}"/usr/bin/
}
