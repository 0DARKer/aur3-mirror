# Maintainer: mickele <mimocciola@yahoo.com>
pkgname=gmsh-docs
_pkgname=gmsh
pkgver=2.8.5
pkgrel=1
pkgdesc="TXT, HMTL and PDF doc for Gmsh"
arch=('any')
url="http://www.geuz.org/gmsh/"
license=('GPL2')
makedepends=('cmake' 'coreutils' 'lapack' 'gzip' 'perl' 'texinfo')
source=("${url}src/${_pkgname}-${pkgver}-source.tgz")
md5sums=('6fd196a4ee31d3adf53ac95e4a2d51ba')

build(){
   cd "${srcdir}/${_pkgname}-${pkgver}-source"

   mkdir -p build

   cd build

   cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DENABLE_FLTK=0 -DENABLE_OCC=0 \
        ..

   LC_ALL=C make doc
}

package() {
   cd "${srcdir}/${_pkgname}-${pkgver}-source/build"

   bsdtar -xf ${_pkgname}-${pkgver}-doc.tgz

   cd "doc/texinfo"

   install -D -m644 gmsh.html "${pkgdir}/usr/share/doc/gmsh/gmsh.html"
   install -D -m644 gmsh.txt "${pkgdir}/usr/share/doc/gmsh/gmsh.txt"
   install -D -m644 gmsh.pdf "${pkgdir}/usr/share/doc/gmsh/gmsh.pdf"
   install -D -m644 gmsh.info "${pkgdir}/usr/share/info/gmsh.info"
}
