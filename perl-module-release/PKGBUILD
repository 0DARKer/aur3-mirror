# Contributor: Piotr Rogo≈ºa <rogoza dot piotr at gmail dot com>
# vim:set ts=2 sw=2 et ft=sh tw=100: expandtab

_author=B/BD/BDFOY
_perlmod=Module-Release
pkgname=perl-module-release
pkgver=2.06
pkgrel=1
pkgdesc="Module::Releases - automate software releases"
arch=('any')
url="http://search.cpan.org/dist/Module-Release/"
license=('GPL' 'PerlArtistic')
depends=(
  'perl'
  'perl-configreader-simple'
  'perl-io-null'
  'perl-test-without-module'
)
makedepends=(
  'perl'
  # for check()
  'perl-test-output'
  'perl-test-pod-coverage'
)
provides=(
  'perl-module-release-ftp'
  'perl-module-release-kwalitee'
  'perl-module-release-manifest'
  'perl-module-release-pause'
  'perl-module-release-prereq'
  'perl-module-release-svn'
  'perl-module-release-sourceforge'
)
optdepends=(
  'perl-cacertorg-ca'
  'perl-lwp-protocol-https: for https support for LWP::UserAgent'
  'perl-test-prereq: to check prereqs'
)
options=(!emptydirs)
source=("http://cpan.perl.org/modules/by-authors/id/$_author/$_perlmod-$pkgver.tar.gz")

build(){
  cd "$srcdir/$_perlmod-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}
check(){
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}
package(){
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
md5sums=('017986629a8f76afb061985c74ccbb48')
