# Maintainer: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# Contributor: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# vim:set ts=2 sw=2 et ft=sh tw=100: expandtab

_author=B/BD/BDFOY
_perlmod=Module-Release
pkgname=perl-module-release
pkgver=2.06
pkgrel=2
pkgdesc="Module::Releases - automate software releases"
arch=('any')
url="http://search.cpan.org/dist/Module-Release/"
license=('GPL' 'PerlArtistic')
depends=(
  'perl'
  'perl-configreader-simple'
  'perl-io-null'
  'perl-test-without-module'
)
makedepends=(
  'perl-test-output'
  'perl-test-pod-coverage'
)
provides=(
  'perl-module-release-ftp'
  'perl-module-release-kwalitee'
  'perl-module-release-manifest'
  'perl-module-release-pause'
  'perl-module-release-prereq'
  'perl-module-release-svn'
  'perl-module-release-sourceforge'
)
optdepends=(
  'perl-cacertorg-ca'
  'perl-lwp-protocol-https: for https support for LWP::UserAgent'
  'perl-test-prereq: to check prereqs'
)
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/$_author/$_perlmod-$pkgver.tar.gz"
  encoding.patch
)

build(){
  cd "$srcdir/$_perlmod-$pkgver"

  patch -p1 < "$srcdir"/encoding.patch
  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}
check(){
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}
package(){
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
md5sums=('017986629a8f76afb061985c74ccbb48'
         'f3a261a086484db8b2985e99e34a6bc2')
