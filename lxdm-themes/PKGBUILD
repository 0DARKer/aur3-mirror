# Maintainer: Ilias Stamatis <stamatis.iliass at gmail dot com>

pkgname=lxdm-themes
pkgver=0.1
pkgrel=1
pkgdesc="Archlinux, ArchlinuxFull, ArchlinuxTop, Arch-Dark, Arch-Stripes and IndustrialArch lxdm themes."
arch=(any)
url="https://wiki.archlinux.org/index.php/LXDM#Themes"
license=('GPL' 'CCPL')
depends=('lxdm')
provides=('lxdm-themes')
conflicts=('archlinux-lxdm-theme' 'archlinux-lxdm-theme-full'
           'archlinux-lxdm-theme-top' 'industrial-arch-lxdm')
install=lxdm-themes.install
source=('http://allanmcrae.com/files/lxdm-theme.tar.gz'  # Archlinux
        'greeter.ui.ArchlinuxFull'                       # ArchlinuxFull
        'http://xdsl.hi2.ro/work/archlinux-lxdm.tar.gz'  # ArchlinuxTop
        'git+https://github.com/M4rQu1Nh0S/industrial-arch.git'    # IndustrialArch
	'http://dl.dropbox.com/u/4813005/lxdm/lxdm-themes.tar.gz') # Arch-Dark, Arch-Stripes
noextract=('lxdm-theme.tar.gz' 'archlinux-lxdm.tar.gz')
md5sums=('899b33309ee04f3200bb08aedb5717ed'
         '85afdf1cd688a78a601ab3bd49b72645'
         '5be4c58acfab8aa5d2d28db96833f406'
         'SKIP'
         '1cc5163253149952329671db34ce7907')

prepare() {
  # avoid file conflicts by extracting themes in different dirs
  cd "$srcdir"
  mkdir Archlinux ArchlinuxTop
  bsdtar -xf lxdm-theme.tar.gz -C Archlinux
  bsdtar -xf archlinux-lxdm.tar.gz -C ArchlinuxTop
}

package() {
  themesdir="${pkgdir}/usr/share/lxdm/themes"
  mkdir -p "${themesdir}/ArchlinuxFull"
  mkdir -p "${themesdir}/Arch-Dark"
  mkdir -p "${themesdir}/Arch-Stripes"
  mkdir -p "${themesdir}/IndustrialArch"

  cd "$srcdir"
  cp -r Archlinux "$themesdir"
  cp -a Archlinux/* "${themesdir}/ArchlinuxFull"
  cp greeter.ui.ArchlinuxFull "${themesdir}/ArchlinuxFull"
  cp -r ArchlinuxTop "$themesdir"
  cp -a lxdm-themes/ArchDark/* "${themesdir}/Arch-Dark"
  cp -a lxdm-themes/ArchStripes/* "${themesdir}/Arch-Stripes"
  cp -a industrial-arch/* "${themesdir}/IndustrialArch"
}
