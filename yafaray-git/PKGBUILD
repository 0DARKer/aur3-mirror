# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=yafaray-git
pkgver=448.1e134fb
_blendver=2.66
pkgrel=1
pkgdesc="A free open-source raytracing engine"
arch=('i686' 'x86_64')
url="http://www.yafaray.org/"
license=('LGPL2.1')
depends=('libxml2' 'openexr' 'libjpeg')
optdepends=('qt4: QT GUI' 'python: Python bindings, Blender exporter')
makedepends=('cmake' 'git' 'qt4' 'swig' 'python')
provides=('yafaray')
conflicts=('yafaray')
source=('yafaray::git+git://github.com/YafaRay/Core.git' \
         'exporter::git+git://github.com/YafaRay/Blender-Exporter.git')
md5sums=('SKIP' 'SKIP')

pkgver() {
  cd "$srcdir/yafaray"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/yafaray"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DYAF_PY_VERSION=3.3 \
    -DYAF_BINDINGS_PY_DIR=/usr/lib/python3.3 \
    .
  make
}

package() {
  cd "$srcdir/yafaray"
  make DESTDIR="$pkgdir/" install

  # install exporter
  cd "$srcdir/exporter"
  git checkout-index -a -f --prefix="$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/"
  # create symlinks to make exporter fully work
  install -d -m755 "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin"
  ln -sf "/usr/lib/libyafaraycore.so" "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin/libyafaraycore.so"
  ln -sf "/usr/lib/libyafarayplugin.so" "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin/libyafarayplugin.so"
  ln -sf "/usr/lib/yafaray" "$pkgdir/usr/share/blender/$_blendver/scripts/addons/yafaray/bin/plugins"
}

# vim:set ts=2 sw=2 et:
