# Maintainer: Devin J. Pohly <djpohly+arch@gmail.com>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>
pkgname=evtest
pkgver=1.29
pkgrel=2
pkgdesc="Input device event monitor and query tool"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/evtest/"
license=('GPL')
depends=(libxml2)
makedepends=(xmlto asciidoc)
optdepends=('libxslt: to parse output from evtest-capture')
source=("http://cgit.freedesktop.org/$pkgname/snapshot/$pkgname-$pkgver.tar.bz2")
md5sums=('6cdb733fc575ad7d38a50e6aaebd732b')
sha1sums=('6c57202d9a794c423ef07ad4f516edab80257215')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  autoreconf -iv
  ./configure --prefix=/usr 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
