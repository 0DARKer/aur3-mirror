# Contributor: Meow < meow at linux dot cn >

pkgname=rstudio-desktop-bin
pkgver=0.97.312
pkgrel=1
pkgdesc="A new integrated development environment (IDE) for R (binary version from RStudio official website)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.rstudio.org/"
depends=('r>=2.11.1' 'pango>=1.14.0' 'qtwebkit')
conflicts=('rstudio-desktop' 'rstudio-desktop-git')
provides=("rstudio-desktop=${pkgver}")
options=(!strip)
case "$CARCH" in
	'i686')
		_arch=i386
		md5sums=('342e214ed85826e2ca5dfd9728d3f239')
		;;
	'x86_64')
		_arch=amd64
		md5sums=('c42f88ee7ecf34651325e3b2399fa60d')
		;;
esac    
source=("http://download1.rstudio.org/rstudio-${pkgver}-${_arch}.deb")
install="$pkgname".install

package() {
  msg "Converting debian package..."
  cd "$srcdir"
  tar zxpf data.tar.gz -C "$pkgdir"
  install -dm755 "$pkgdir/usr/bin"
  cd "$pkgdir/usr/lib/rstudio/bin"
  rm lib*.so.*
  cd "$pkgdir/usr/bin"
  ln -s -f ../lib/rstudio/bin/rstudio rstudio-bin
}
