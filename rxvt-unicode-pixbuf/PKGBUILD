# Maintainer: Dirk Sohler <dirk@archlinux.de>
# Contributor: Dirk Sohler <dirk@archlinux.de>               
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.dot.org>
# vim: ts=4:sw=4

pkgname=rxvt-unicode-pixbuf
_dlname=rxvt-unicode
pkgver=9.18
pkgrel=1
pkgdesc="urxvt with pixbuf (custom icons, custom background) enabled (see FS36113, FS35329 and FS34862)"

url='http://software.schmorp.de/pkg/rxvt-unicode.html'
arch=('i686' 'x86_64')
license=('GPL')

depends=('rxvt-unicode-terminfo' 'gdk-pixbuf2' 'libxft' 'perl'
         'startup-notification')

optdepends=('gtk2-perl: to use the urxvt-tabbed')
makedepends=('libxft' 'perl' 'startup-notification')

conflicts=('rxvt-unicode')
provides=('rxvt-unicode')

source=("http://dist.schmorp.de/rxvt-unicode/$_dlname-$pkgver.tar.bz2"
        "urxvt.desktop"
        "urxvtc.desktop"
        "urxvt-tabbed.desktop"
        "01-fix-tabber-perl-ext.patch")

sha256sums=('87b72a43cac7c9362a984094c661a632ce1a829ad90f149142ab1953854b433f'
            '5f9c435d559371216d1c5b49c6ec44bfdb786b12d925d543c286b0764dea0319'
            '91536bb27c6504d6cb0d33775a0c4709a4b439670b900f0c278c25037f19ad66'
            'ccd7c436e959bdc9ab4f15801a67c695b382565b31d8c352254362e67412afcb'
            '5a461014d8196b7655f33a79b24c8da221898e88dc77d90c9589fb6f66def76e')

prepare() {
    patch -p1 -d $_dlname-$pkgver < 01-fix-tabber-perl-ext.patch
}

build() {
    cd $_dlname-$pkgver
    # we disable smart-resize (FS#34807)
    ./configure \
        --prefix=/usr \
        --with-terminfo=/usr/share/terminfo \
        --enable-256-color \
        --enable-combining \
        --enable-fading \
        --enable-font-styles \
        --enable-iso14755 \
        --enable-keepscrolling \
        --enable-lastlog \
        --enable-mousewheel \
        --enable-next-scroll \
        --enable-perl \
        --enable-pointer-blank \
        --enable-rxvt-scroll \
        --enable-selectionscrolling \
        --enable-slipwheeling \
        --disable-smart-resize \
        --enable-startup-notification \
        --enable-transparency \
        --enable-unicode3 \
        --enable-utmp \
        --enable-wtmp \
        --enable-xft \
        --enable-xim \
        --enable-xterm-scroll \
        --enable-pixbuf \
        --disable-frills
    make
}

package() {
    for _f in urxvt urxvtc urxvt-tabbed; do
        install -Dm644 $_f.desktop "$pkgdir/usr/share/applications/$_f.desktop"
    done

    cd $_dlname-$pkgver
    export TERMINFO="$srcdir/terminfo"
    install -d "$TERMINFO"

    make DESTDIR="$pkgdir" install
    sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
    install -Dm 755 doc/rxvt-tabbed "$pkgdir/usr/bin/urxvt-tabbed"
}

