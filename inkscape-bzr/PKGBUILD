# Maintainer: Splex

pkgname=inkscape-bzr
pkgver=9926
pkgrel=1
pkgdesc="A vector-based graphics editor - SVG compliant - bzr development branch"
url="https://launchpad.net/inkscape"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gc' 'gtkmm' 'poppler-glib' 'pyxml' 'libxslt' 'gsl' 'popt' 'python2'
         'gtkspell' 'imagemagick' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('boost' 'pkg-config' 'intltool' 'bzr' 'gettext' 'autoconf')
options=('!libtool')
optdepends=('ruby: for running /usr/share/inkscape/extensions/simplepath.rb')
install=inkscape-bzr.install
provides=('inkscape')
conflicts=('inkscape')

_bzrtrunk=lp:inkscape
_bzrmod=inkscape

build() {
  cd ${srcdir}
  msg "Connecting to the server..."
  if [ -d "${srcdir}/${_bzrmod}" ] ; then
    cd ${_bzrmod} && bzr update
	 msg "Local repository updated."
  else
    bzr co --lightweight ${_bzrtrunk}
  fi
  cd ${startdir}
  msg "BZR checkout done or server timeout"

  rm -rf "${srcdir}/${_bzrmod}-build"
  cp -r "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
  cd "${srcdir}/${_bzrmod}-build"

  ./autogen.sh || return 1
  ../${_bzrmod}-build/configure --prefix=/usr \
    --with-python \
    --with-perl \
    --without-gnome-vfs \
    --with-xft \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking || return 1

  # fix for inkscape to use python2 with the python 3 package installed.
  sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
  sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
  sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
  sed -i 's|\<python -c |python2 -c|g' share/extensions/uniconv*.py
  sed -i 's|"python"|"python2"|g' src/main.cpp

  make || return 1
}

package() {
  cd "${srcdir}/${_bzrmod}-build"
  make DESTDIR=${pkgdir} install || return 1
}
