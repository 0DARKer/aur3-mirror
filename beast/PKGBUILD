# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Corrado 'bardo' Primier <corrado.primiermail.polimi.it>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=beast
pkgver=0.7.4
pkgrel=1
pkgdesc="A music composition and modular synthesis application"
arch=('i686' 'x86_64')
url="http://beast.testbit.eu/"
license=('GPL' 'LGPL')
depends=('alsa-lib' 'guile' 'libgnomecanvas' 'libmad' 'libvorbis' 'shared-mime-info')
provides=('bse-alsa=0.7.4')
conflicts=('bse-alsa')
options=('!libtool')
install="$pkgname.install"
source=("${url}_dist/$pkgname/v0.7/$pkgname-$pkgver.tar.bz2")
md5sums=('f383762ef20a6ed1ee0ee0e43172bfd6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Assert fix
  sed -i "/(DBL/d" birnet/birnetutils.cc

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Removing unneeded files
  find "$pkgdir/usr/share/mime" -maxdepth 1 -type f -exec rm {} \;
}
