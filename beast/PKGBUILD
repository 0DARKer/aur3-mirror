# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor: Corrado 'bardo' Primier <corrado.primiermail.polimi.it>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=beast
pkgver=0.8.0
pkgrel=1
pkgdesc="A music composition and modular synthesis application"
arch=('i686' 'x86_64')
url="http://beast.testbit.eu/"
license=('GPL' 'LGPL')
depends=('alsa-lib' 'guile' 'libgnomecanvas' 'libmad'
         'libvorbis' 'rapicorn' 'desktop-file-utils')
provides=("bse-alsa=$pkgname")
conflicts=('bse-alsa')
options=('!libtool')
install="$pkgname.install"
source=("http://dist.testbit.eu/beast/v${pkgver%.*}/beast-$pkgver.tar.bz2")
md5sums=('37955036d44cbc715b253dd45893405b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # enable deprecated glib-2.0 API (g_thread_init)
  sed -i "s/-DG_DISABLE_DEPRECATED//" bse/{,zintern/}Makefile.*
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # rm mime cache
  find "$pkgdir/usr/share/mime" \
    -maxdepth 1 -type f -exec rm {} \;
}
