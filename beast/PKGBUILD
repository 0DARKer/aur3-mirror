# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Corrado 'bardo' Primier <corrado.primiermail.polimi.it>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

_durl=http://patch-tracker.debian.org/patch/series/dl/
pkgname=beast
pkgver=0.7.4
pkgrel=2
pkgdesc="A music composition and modular synthesis application"
arch=('i686' 'x86_64')
url="http://beast.testbit.eu/"
license=('GPL' 'LGPL')
depends=('alsa-lib' 'guile' 'libgnomecanvas' 'libmad'
         'libvorbis' 'desktop-file-utils')
provides=('bse-alsa=0.7.4')
conflicts=('bse-alsa')
options=('!libtool')
install="$pkgname.install"
source=(${url}_dist/$pkgname/v0.7/$pkgname-$pkgver.tar.bz2
        $_durl$pkgname/$pkgver-4/{203_gcc45,400-format-security}.patch)
md5sums=('f383762ef20a6ed1ee0ee0e43172bfd6'
         'cd0da95c3515e6b80940d07b70d4ddce'
         '9518d6b2eb8c37ef9f71cd8d53181c0b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # assert + permissive patches
  patch -p1 -i ../${source[1]/*\/}
  patch -p1 -i ../${source[2]/*\/}

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # rm mime cache
  find "$pkgdir/usr/share/mime" \
    -maxdepth 1 -type f -exec rm {} \;
}
