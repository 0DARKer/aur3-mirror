# Maintainer: TDY <tdy@gmx.com>

pkgname=wxhexeditor-svn
pkgver=95
pkgrel=1
pkgdesc="A hex editor for large files and devices"
arch=('i686' 'x86_64')
url="http://wxhexeditor.sourceforge.net/"
license=('GPL')
depends=('wxgtk')
makedepends=('subversion')
provides=('wxhexeditor')
conflicts=('wxhexeditor')

_svntrunk=https://wxhexeditor.svn.sourceforge.net/svnroot/wxhexeditor/trunk
_svnmod=wxhexeditor

build() {
  cd "$srcdir"

  if [[ -d $_svnmod/.svn ]]; then
    cd $_svnmod && svn up -r $pkgver && cd ..
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  rm -rf $_svnmod-build
  svn export $_svnmod $_svnmod-build
  make -C $_svnmod-build || return 1
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install
  ln -sf wxHexEditor "$pkgdir/usr/bin/$_svnmod"
}
