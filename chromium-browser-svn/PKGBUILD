# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: piojo <aur@zwell.net>
# Contributor:  MiakoMiyamura <miyamiyamura@gmail.com>
# Contributor: Frederic Bezies <fredbezies@gmail.com>

pkgname=chromium-browser-svn
pkgrel=74
pkgdesc='An open-source browser project that aims to build a safer, faster, and more stable way for all users to experience the web'
arch=(i686 x86_64)
url="http://www.chromium.org"
license=(BSD)
depends=(alsa-lib gconf hicolor-icon-theme libevent libxss libxslt nss gtk2 desktop-file-utils) 
makedepends=(subversion python2 gperf rsync libgnome-keyring yasm mesa)
provides=(chromium-browser)
conflicts=(chromium-browser)
source=(chromium.desktop .gclient chromium-browser.sh)
install=chromium.install
sha256sums=('be1036c3e932f0f7b3d61847da4508b1cfb1120057d6d05307ba6510c65a1dd1'
    'a8cde9817db998228dfd5a9f92f941bf8a7e10360ee7785badbe3b9ace8ad3a7'
    'f52c4f85ef4c846f238d4cef0eb8df7a1e703c66980fc8694a1e3dc8f1a56918')
sha512sums=('f51aeb54b358d38d16e54b333d1cdd1dd523bf199461be0bde88c72eb829d8463e10aedd5eb4cbc5c712a2c4284956c6106a5e8b0923f2456152c68f37a0854c'
    '68e6b8253e80e2da210790150043574495975ed54d1e8af15e115a03761231cabadc5b8bf3d6646b6745a683e214f7bd1d836f28400d7c9485804603bd5cb811'
    '6919178cf46c2bc048fd6a5f8ce2f0a7764fbc5923d8a2261ae5e1052ea71dae0e34b748c2052433f9d0f0c2577faeac30cc7b212fd5e9381dd14d8095e2202f')

# Change this to "trunk" or some other revision number to get that
# version instead of the "safe sync" version:
_revision=
_builddir=$srcdir/builddir

_svntrunk_gclient=http://src.chromium.org/svn/trunk/tools/depot_tools
_gclient=$srcdir/depot_tools

build() {
    if [ "x$CHROMIUM_WATERFALL_REMINDER" != x ]; then
        read -p "Remember to check http://build.chromium.org/buildbot/waterfall/console"
    fi
    
    cd $srcdir
    _source
    _setup_builddir
    
    msg "Preparing build of Chromium revision $pkgver..."
    cd $_builddir
    
    # Write out corrrect revision number in 'About Chromium'
    _revision > build/LASTCHANGE.in 
    
    # http://code.google.com/p/chromium/issues/detail?id=41887
    # http://code.google.com/p/chromium/issues/detail?id=94518
    export CXXFLAGS="$CXXFLAGS -fno-ipa-cp"
    export GYP_GENERATORS='make'
    export GYP_DEFINES="
     gcc_version=46
     no_strict_aliasing=1
     linux_sandbox_path=/usr/lib/chromium/chromium-sandbox
     linux_strip_binary=1
     proprietary_codecs=1
     remove_webcore_debug_symbols=1
     use_system_bzip2=1
     use_system_ffmpeg=0
     use_system_icu=0
     use_system_libevent=1
     use_system_libjpeg=1
     use_system_libpng=1
     use_system_libxml=0
     use_system_ssl=0
     use_system_yasm=1
     use_system_zlib=1
     use_gconf=0
     werror=
     disable_nacl=1 
    "
    
    if [ "$_shared_build" = "true" ]; then
        msg "Preparing a shared build..."
        GYP_DEFINES="$GYP_DEFINES library=shared_library"
    fi
    
    if [ "$CARCH" = 'x86_64' ]; then
        # 64-bit instructions from
        # http://code.google.com/p/chromium/wiki/LinuxBuildInstructions
        GYP_DEFINES="$GYP_DEFINES target_arch=x64"
    fi

    msg "Running gyp in builddir using these settings: "
    echo $GYP_DEFINES
    build/gyp_chromium -f make build/all.gyp --depth=. || return 1
    
    msg "Now starting build..."
    make $MAKEFLAGS BUILDTYPE=Release V=1 chrome chrome_sandbox || return 1
}

_source() {
    # Arch uses python3 by default, so we want Python in $PATH to point to python2
    rm -rf $srcdir/python
    mkdir $srcdir/python
    ln -s /usr/bin/python2 $srcdir/python/python
    export PATH=$srcdir/python:$PATH

    msg "Checking out gclient..."
    rm -rf "$_gclient"
    svn co $_svntrunk_gclient --config-dir ./ "$_gclient" || return 1
    find $_gclient -type f | xargs sed -i 's:/usr/bin/python:$srcdir/python/python:g'
    #$srcdir/depot_tools/gclient sync -rHEAD

    # There have been complaints about Google's servers closing connections:
    msg "Downloading Chromium's sources."
    msg "Will retry up to 5 times; press ctrl-c to abort."
    for n in {1..5}; do
        python2 "$_gclient"/gclient.py sync --force --nohooks --revision "src@$_revision" && break
    done
    
    if test $? -ne 0; then
        return 1
    fi
}

_setup_builddir() {
    # Always prepare a new, clean && save environment... ;-)
    # This technique will prevent the above "patch" invocations from
    # failing (when the source/build directory has already been patched),
    # but it's wasteful of disk space.
    _exclude_dirs="
    --exclude=.svn
    --exclude=gears
    --exclude=chrome/test/data
    --exclude=chrome/tools/test/reference_build
    --exclude=chrome_frame
    --exclude=net/data/cache_tests
    --exclude=o3d/documentation
    --exclude=o3d/samples
    --exclude=third_party/active_doc
    --exclude=third_party/activscp
    --exclude=third_party/apple
    --exclude=third_party/apple_apsl
    --exclude=third_party/bidichecker
    --exclude=third_party/harfbuzz-ng
    --exclude=third_party/hyphen
    --exclude=third_party/lighttpd
    --exclude=third_party/WebKit/LayoutTests
    --exclude=webkit/data/layout_tests
    --exclude=webkit/tools/test/reference_build
    "
    msg "Syncing local build directory (can take a while)..."
    rsync -rlpgo --delete $_exclude_dirs} $srcdir/src/ "$_builddir}" 
msg "Replacing Python interpreter in $_builddir} (can take a while)..."
find $_builddir -type f | xargs sed -i 's:/usr/bin/python:$srcdir/python/python:g'
}

_revision() {
    LC_ALL=C svn info $srcdir/src/ | awk '/Revision/ {print $2}'
}

package() {
    msg "Installing Chromium to $pkgdir..."
    cd $_builddir
    _revision
    pkgver=$(_revision)
    
    install -Dm755 out/Release/chrome $pkgdir/usr/lib/chromium/chromium
    install -Dm4555 -o root -g root out/Release/chrome_sandbox $pkgdir/usr/lib/chromium/chromium-sandbox
    install -Dm644 out/Release/chrome.pak $pkgdir/usr/lib/chromium/chrome.pak
    install -Dm644 out/Release/resources.pak $pkgdir/usr/lib/chromium/resources.pak
    cp -a out/Release/locales $pkgdir/usr/lib/chromium/
    cp -a out/Release/resources  $pkgdir/usr/lib/chromium/
    find $pkgdir/usr/lib/chromium/ -name '*.d' -type f -delete
    install -Dm755 out/Release/libffmpegsumo.so $pkgdir/usr/lib/chromium/libffmpegsumo.so
    install -Dm644 out/Release/chrome.1 $pkgdir/usr/share/man/man1/chromium.1
    install -Dm644 LICENSE $pkgdir/usr/share/licenses/chromium/LICENSE
    desktop-file-install $srcdir/chromium.desktop --dir=$pkgdir/usr/share/applications/
    install -Dm755 $srcdir/chromium-browser.sh $pkgdir/usr/bin/chromium
    for size in 16 22 24 32 48 128 256; do
        install -Dm644 chrome/app/theme/chromium/product_logo_$size.png $pkgdir/usr/share/icons/hicolor/${size}x$size/apps/chromium.png
    done
}

# This pkgver is for aur.archlinux.org and tools that quickly parse this
# PKGBUILD. The true version is set within _revision().
pkgver=2012
