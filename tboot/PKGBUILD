# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=tboot
pkgver=1.8.0
pkgrel=1
pkgdesc="A pre-kernel/VMM module that uses Intel(R) TXT to perform a measured and verified launch of an OS kernel/VMM."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/tboot/"
license="BSD"
depends=('trousers')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('df8bd1e05801696edb8798072d13e2ffa8bc90add825231395443fa4a99b3643a29c9c4c899996385ada8a1db849f12ffbcb8cd53210e21072846919f0cf89da')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  unset LDFLAGS
  make debug=y
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make debug=y DISTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  # Thanks to the Fedora packagers for this line!
  cp README COPYING docs/*.txt lcptools/lcptools2.txt lcptools/Linux_LCP_Tools_User_Manual.pdf "${pkgdir}/usr/share/doc/${pkgname}/"

  install -dm755 "${pkgdir}/usr/share/man/man8/"
  install -m644 docs/man/*.8 "${pkgdir}/usr/share/man/man8/"
}

# vim:set ts=2 sw=2 et:
