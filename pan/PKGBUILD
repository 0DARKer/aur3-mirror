# Contributor: Bernard Baeyens (berbae) <berbae52 at sfr dot fr>

pkgname=pan
pkgver=0.135
pkgrel=4
pkgdesc="A powerful Newsgroup Article reader"
arch=('i686' 'x86_64')
url="http://pan.rebelbase.com/"
license=('GPL2')
depends=('gtk3' 'gmime' 'gtkspell')
makedepends=('gnome-common' 'intltool')
conflicts=('pan-devel' 'pan-git')
options=('!makeflags')
source=(http://pan.rebelbase.com/download/releases/${pkgver}/source/${pkgname}-${pkgver}.tar.bz2
    build-with-gmime26.patch)
sha1sums=('6cd93facf86615761279113badd7462e59399ae4'
    'ed29c42f74bef3242a0a13b2d71bb7eddace3b26')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/build-with-gmime26.patch

  #./configure --prefix=/usr --with-gtk3 --without-gtkspell
  ./configure --prefix=/usr --with-gtk3
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
