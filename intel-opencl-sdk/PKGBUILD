# Contributor: Nicolas Bigaouette <nbigaouette@gmail.com>
# Maintainer: Vojtech "kralyk" Kral


pkgname='intel-opencl-sdk'
pkgver=2012
pkgrel=2
pkgdesc="Intelâ€™s implementation of the OpenCL standard optimized for Intel processors."
arch=('x86_64')
url="http://software.intel.com/en-us/articles/opencl-sdk/"
license=('intel')
depends=('opencl-headers' 'libcl' 'llvm' 'intel-tbb' 'numactl')
makedepends=('rpmextract')
provides=('opencl')
install='intel-opencl-sdk.install'
source=("http://registrationcenter.intel.com/irc_nas/2563/intel_sdk_for_ocl_applications_${pkgver}_x64.tgz"
        'iocgui.fix.patch')
sha256sums=('2044a9a62f40788b0dc4f4ea77a3d85df14e7909258e37eed3e63f7254a0bcc9'
            '30c56e3a47dd2e420b2d97d259b416978ac8549f5247d75d4b784d70ea1989b3')
_ipath="/opt/intel/opencl-sdk"

build() { true; } # Nothing to do

package()
{
  #Unpack RMP
  cd "${srcdir}"
  rpmextract.sh "intel_ocl_sdk_${pkgver}_x64.rpm"

  #Patch iocgui
  patch -p0 < iocgui.fix.patch

  #Register ICD
  mkdir -p "${pkgdir}/etc/OpenCL/vendors"
  echo "${_ipath}/libintelocl.so" > "${pkgdir}/etc/OpenCL/vendors/intel.icd"
  # The OpenCL ICD specifications: http://www.khronos.org/registry/cl/extensions/khr/cl_khr_icd.txt

  #Install files
  mkdir -p "${pkgdir}/${_ipath}"
  install "${srcdir}/usr/lib64/libOpenCL.so" -t "${pkgdir}/${_ipath}"
  cp -r "${srcdir}/usr/lib64/OpenCL/vendors/intel/"* "${pkgdir}/${_ipath}"
  rm "${pkgdir}/${_ipath}/"libtbb*

  #Symlink binaries
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "${_ipath}/ioc64" "${pkgdir}/usr/bin/ioc"
  ln -s "${_ipath}/iocgui64.sh" "${pkgdir}/usr/bin/iocgui"
}

