# Contributor: Nicolas Bigaouette <nbigaouette@gmail.com>
# Maintainer: Vojtech "kralyk" Kral


pkgname='intel-opencl-sdk'
pkgver=2013_R2
pkgrel=2
pkgdesc="Intelâ€™s implementation of the OpenCL standard optimized for Intel processors."
arch=('x86_64')
url="http://software.intel.com/en-us/vcsource/tools/opencl"
license=('intel')
depends=('opencl-headers' 'libcl' 'llvm' 'intel-tbb' 'numactl')
makedepends=('rpmextract')
provides=('opencl')
install='intel-opencl-sdk.install'
source=('http://registrationcenter.intel.com/irc_nas/3555/intel_sdk_for_ocl_applications_xe_2013_r2_sdk_3.1.1.11385_x64.tgz')
sha256sums=('317748cab4cff42163d6efa15f36865eb3fe4112f57a69a0185a43c2b244ead0')
_ipath="/opt/intel/opencl-sdk"

build() { true; } # Nothing to do

package() {
  #Unpack RMP
  cd "${srcdir}/intel_sdk_for_ocl_applications_xe_2013_r2_sdk_3.1.1.11385_x64/"
  for i in *.rpm; do  rpmextract.sh "$i"; done

  #Register ICD
  mkdir -p "${pkgdir}/etc/OpenCL/vendors"
  echo "${_ipath}/lib64/libintelocl.so" > "${pkgdir}/etc/OpenCL/vendors/intel.icd"
  # The OpenCL ICD specifications: http://www.khronos.org/registry/cl/extensions/khr/cl_khr_icd.txt

  #Install files
  mkdir -p "${pkgdir}/${_ipath}"
  cp -r opt/intel/opencl-*/* "${pkgdir}/${_ipath}"

  # #Symlink binaries
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "${_ipath}/bin/ioc64" "${pkgdir}/usr/bin/ioc"
  
  ln -s opencl-sdk "${pkgdir}"/opt/intel/opencl-1.2-3.1.1.11385
}
