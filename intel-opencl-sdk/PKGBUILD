# Contributor: Nicolas Bigaouette <nbigaouette@gmail.com>
# Maintainer: Vojtech "kralyk" Kral


pkgname='intel-opencl-sdk'
pkgver=2014_R2
pkgrel=1
pkgdesc="Intelâ€™s implementation of the OpenCL standard optimized for Intel processors."
arch=('x86_64')
url="http://software.intel.com/en-us/vcsource/tools/opencl"
license=('intel')
depends=('opencl-headers' 'libcl' 'llvm' 'intel-tbb' 'numactl' 'libpng12' )
makedepends=('rpmextract')
provides=('opencl')
install='intel-opencl-sdk.install'
source=('http://registrationcenter.intel.com/irc_nas/4181/opencl_runtime_14.2_x64_4.5.0.8.tgz')
sha256sums=('2f89eed6e8e1ebfd7d22ea4a10cfc63cbe8ffb41445d78e98de02f99806d2aac')
_ipath="/opt/intel/opencl-sdk"

package() {
  #Unpack RMP
  cd "${srcdir}"/pset_opencl_*/
  for i in rpm/*.rpm; do  rpmextract.sh "$i"; done

  #Register ICD
  mkdir -p "${pkgdir}/etc/OpenCL/vendors"
  echo "${_ipath}/lib64/libintelocl.so" > "${pkgdir}/etc/OpenCL/vendors/intel.icd"
  # The OpenCL ICD specifications: http://www.khronos.org/registry/cl/extensions/khr/cl_khr_icd.txt

  #Install files
  mkdir -p "${pkgdir}/${_ipath}"
  cp -r opt/intel/opencl-*/* "${pkgdir}/${_ipath}"

  # #Symlink binaries
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "${_ipath}/bin/ioc64" "${pkgdir}/usr/bin/ioc"
  
  ln -s opencl-sdk "${pkgdir}"/opt/intel/opencl-1.2-4.4.0.117
}
