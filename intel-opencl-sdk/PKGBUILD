# Contributor: Nicolas Bigaouette <nbigaouette@gmail.com>
# Maintainer: Vojtech "kralyk" Kral


pkgname='intel-opencl-sdk'
pkgver=2013
pkgrel=1
pkgdesc="Intelâ€™s implementation of the OpenCL standard optimized for Intel processors."
arch=('x86_64')
url="http://software.intel.com/en-us/vcsource/tools/opencl"
license=('intel')
depends=('opencl-headers' 'libcl' 'llvm' 'intel-tbb' 'numactl')
makedepends=('rpmextract')
provides=('opencl')
install='intel-opencl-sdk.install'
source=('http://registrationcenter.intel.com/irc_nas/3142/intel_sdk_for_ocl_applications_2013_xe_sdk_3.0.67279_x64.tgz')
sha256sums=('72c84a29b36f55b0cf356b70d3530830360c1e49206ecec62aee665f8d4954f5')
_ipath="/opt/intel/opencl-sdk"

build() { true; } # Nothing to do

package()
{
  #Unpack RMP
  cd "${srcdir}/intel_sdk_for_ocl_applications_2013_xe_sdk_3.0.67279_x64/"
  for i in *.rpm; do  rpmextract.sh "$i"; done

  #Register ICD
  mkdir -p "${pkgdir}/etc/OpenCL/vendors"
  echo "${_ipath}/lib64/libintelocl.so" > "${pkgdir}/etc/OpenCL/vendors/intel.icd"
  # The OpenCL ICD specifications: http://www.khronos.org/registry/cl/extensions/khr/cl_khr_icd.txt

  #Install files
  mkdir -p "${pkgdir}/${_ipath}"
  cp -r 'opt/intel/opencl-1.2-3.0.67279/'* "${pkgdir}/${_ipath}"

  # #Symlink binaries
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "${_ipath}/ioc64" "${pkgdir}/usr/bin/ioc"
}
