# Contributor: Nicolas Bigaouette <nbigaouette@gmail.com>
# Maintainer:  Vojtech Kral <kral.vojtech@gmail.com>

pkgname=intel-opencl-sdk
pkgver=1.1
pkgrel=2
pkgdesc="Intelâ€™s implementation of the OpenCL standard optimized for Intel processors."
arch=('x86_64')
url="http://software.intel.com/en-us/articles/opencl-sdk/"
license=('intel')
depends=('opencl-headers' 'numactl')
provides=('opencl' 'libcl')
install=intel-opencl-sdk.install
source=(http://software.intel.com/file/35820/${pkgname/-/_}_${pkgver}_lnx_64.rpm)
sha256sums=('4ef2d6ec4ea4c57d688bdbcd536dfb4e452a397e979bca2a961f455621ceafd5')
_ipath="/opt/${pkgname}"

build() { true; } # Nothing to do

package()
{
  #Register ICD
  mkdir -p "${pkgdir}/etc/OpenCL/vendors"
  echo "${_ipath}/libintelocl.so" > "${pkgdir}/etc/OpenCL/vendors/intel.icd"
  # The OpenCL ICD specifications: http://www.khronos.org/registry/cl/extensions/khr/cl_khr_icd.txt

  #Install files
  mkdir -p "${pkgdir}/${_ipath}"
  install "${srcdir}/usr/lib64/libOpenCL.so" -t "${pkgdir}/${_ipath}"
  install "${srcdir}/usr/lib64/OpenCL/vendors/intel/"{*.so*,*.rtl,llc} "${pkgdir}/${_ipath}"
  pushd "${pkgdir}/${_ipath}/" > /dev/null
  for i in *.so.2                                  # Because supplied .so were just ascii text really
  do
    rm "${i/.2/}"
    ln -s "${_ipath}/$i" "${i/.2/}"
  done
  popd > /dev/null

  #Symlink libs
  mkdir -p "${pkgdir}/usr/lib"
  ln -s "${_ipath}/libOpenCL.so" "${pkgdir}/usr/lib/libOpenCL.so"
}
