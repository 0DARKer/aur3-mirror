# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname=ring-daemon-git
pkgver=20150526
pkgrel=1
pkgdesc="SIP/IAX2 compatible softphone for Linux, based on a DHT (formerly known as SFLphone)"
arch=("i686" "x86_64")
url="http://ring.cx"
license=('GPL3')
groups=("ring")
depends=('opendht-git' 'yaml-cpp' 'alsa-lib' 'libpulse' 'jack'
         'libsamplerate' 'libsndfile' 'dbus-c++' 'ffmpeg' 'udev' 'gnutls'
         'expat' 'gsm' 'speex' 'speexdsp' 'opus' 'libupnp')
makedepends=('git' 'boost')
provides=('ring-daemon')
conflicts=('ring-daemon')
source=("git+https://gerrit-ring.savoirfairelinux.com/ring-daemon"
        "ipv6.patch"
        "nullptr-bool-gcc5.patch"
        "fix-isblank-gcc5.patch"
        "pjproject-apply-isblank.patch")
md5sums=('SKIP'
         '1daac5070c6ebe745129b8cf6b055395'
         '50a40e0dc25f060271f6a6360f02ed71'
         '26b03afc683fd62de8dfc8a6eb925d95'
         '2b8d61e324ea8483b65ac3782bd3d739')

pkgver() {
  cd "${pkgname%-git}"
  git log -1 --format="%cd" --date=short | sed "s|-||g"
}

build() {
  cd "${pkgname%-git}"

  msg2 'Patching...'
  # See https://projects.savoirfairelinux.com/issues/72857
  patch -p1 -i "$srcdir/ipv6.patch"
  # See https://gcc.gnu.org/gcc-5/porting_to.html ("C++ language issues")
  patch -p1 -i "$srcdir/nullptr-bool-gcc5.patch"

  # Patch bundled pjproject for gcc 5, see
  # https://projects.savoirfairelinux.com/issues/73855
  cp "$srcdir/fix-isblank-gcc5.patch" contrib/src/pjproject/
  patch -p1 -i "$srcdir/pjproject-apply-isblank.patch"

  msg2 'Building pjproject...'
  # Build the customised version of pjproject (heavily patched...)
  mkdir -p contrib/native
  cd contrib/native
  ../bootstrap
  make .pjproject

  cd ../..
  msg2 'Building...'
  # Build dring
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/bin \
    --sysconfdir=/etc \
    --enable-ipv6
  make
}

package() {
  cd "${pkgname%-git}"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

# vim:set ts=2 sw=2 et:
