# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

pkgname=freetype2-git-infinality
pkgver=2.4.99.git20111125
pkgrel=1
_infinality_version=20111125
_infinality_release=1
pkgdesc="TrueType font rendering library with infinality patch"
arch=(i686 x86_64)
url="http://freetype.sourceforge.net"
license=('GPL')
depends=('bzip2' 'zlib')
makedepends=('git')
optdepends=('ttf-win7-fonts: (AUR) Fonts included in Windows 7')
provides=("freetype2=$pkgver")
conflicts=('freetype2')
backup=('etc/profile.d/infinality-settings.sh'
        'etc/fonts/conf.avail/52-infinality.conf')
options=('!libtool')
install='infinality.install'
source=(http://www.infinality.net/fedora/linux/zips/freetype-infinality-2.4.8-${_infinality_version}_${_infinality_release}.tar.bz2
        freetype2-enable-valid.patch)
sha256sums=('0f54178baf3e4537c6a94fa7b478a7ed6278126545356c123e6598d50acdc15a'
            '97751d04014b7f2a42bce3ed99417eeb058563e2b7c94251f05f489666fec5bb')
_freetype_gitroot="git://git.sv.nongnu.org/freetype/freetype2.git"
_freetype_gitcommit=e01406bb91653fca42b0eb9bf88c5df854c04d66

build() {
  cd "${srcdir}"

  # Get rid of old build directories
  rm -rf freetype2-build

  # Clone or update sources from freetype2 git repository
  if [ -d ${srcdir}/freetype2 ]; then
    msg "Updating Git repository"
    cd freetype2 && git pull
  else
    msg "Cloning Git repository"
    git clone ${_freetype_gitroot}
  fi

  msg "Preparing build directory"
  cp -r ${srcdir}/freetype2 ${srcdir}/freetype2-build

  cd "${srcdir}/freetype2-build"

  # Check out specific commit for repeatability
  git checkout --detach ${_freetype_gitcommit}

  # Apply miscellaneous patches.
  patch -Np0 -i "${srcdir}/freetype2-enable-valid.patch"

  # Apply Infinality patches. See README file for correct order. Unfortunately,
  # the Infinality patches may not have the same version number, so we use
  # wildcards here.
  patch -Np1 -i "${srcdir}/freetype-add-subpixel-hinting-infinality-"*.patch
  patch -Np1 -i "${srcdir}/freetype-enable-subpixel-hinting-infinality-"*.patch
  patch -Np1 -i "${srcdir}/freetype-entire-infinality-patchset-"*.patch

  # Generate and run configure script
  sh autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/freetype2-build"

  make DESTDIR="${pkgdir}" install

  install -m 755 -D -T "${srcdir}/infinality-settings.sh" "${pkgdir}/etc/profile.d/infinality-settings.sh"
  install -m 644 -D -T "${srcdir}/local.conf" "${pkgdir}/etc/fonts/conf.avail/52-infinality.conf"
}
