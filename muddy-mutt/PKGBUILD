# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Patrick Brisbin <pbrisbin@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=muddy-mutt
pkgver=1.5.21
pkgrel=1
pkgdesc="A small but very powerful text-based mail client, muddied with potentially useful patches."
arch=('i686' 'x86_64')
url='http://www.mutt.org/'
license=('GPL')
depends=('openssl' 'gdbm' 'mime-types' 'libsasl' 'slang' 'gnupg' 'gpgme')
conflicts=('mutt')
provides=('mutt')
source=(ftp://ftp.mutt.org/mutt/devel/mutt-${pkgver}.tar.gz
        sidebar.patch
        trash_folder-1.5.18.patch
        muttrc.example
        mutt-unmailbox.patch
        mutt-attach.patch
        sidebar-dotted.patch
        sidebar-utf8.patch
        collapse-flagged.patch)

sha256sums=('2141f36e8d0f4f71c9ca6780001e7cc679fe313e643953fc07f001223e67c4a0'
            '787ad4047993f6795d081987d14c1623b106388c368bef235111781e240afab6'
            'e5012c018956c0848355453c308ff9e3efe7ea90a3519fadb59bd19db1f29673'
            '608423900a7ce6fc39a1da73d35825e9c456adfe535cb586926a53e314958942'
            '88f44f9bf90e28c3b08791bdcc63ec000439724726914e01b288b3a1bd19c22a'
            'da2c9e54a5426019b84837faef18cc51e174108f07dc7ec15968ca732880cb14'
            'e371de2f1f1dc2c061b649652bdb1a2ac86bd24a9d4599ec70e82b6f258642bf'
            '91161f28d181af2fd7bb521b0b066d1666befc2decc3d77acec304ce9aa82c6d'
            '458585fd7d2145e0f5a37132279e09f8bcb180e227fe02d8864f27fc03f4636b')

build() {
  cd "$srcdir/mutt-$pkgver"

  # forgotten attatchment detector
  patch -p1 < "$srcdir/mutt-attach.patch"

  # patch to add sidebar support
  patch -p1 < "$srcdir/sidebar.patch"
  patch -p1 < "$srcdir/sidebar-dotted.patch"
  patch -p1 < "$srcdir/sidebar-utf8.patch"

  # patch to add trash folder
  patch -p1 < "$srcdir/trash_folder-1.5.18.patch"

  # patch to not collapse threads with flagged messages
  patch -p1 < "$srcdir/collapse-flagged.patch"


  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/; /AM_C_PROTOTYPES/d;' configure.ac

  autoreconf -v
  ./configure --prefix=/usr --sysconfdir=/etc --enable-pop \
    --enable-imap --enable-smtp --enable-pgp --enable-hcache \
    --enable-gpgme --with-ssl=/usr --with-sasl --without-idn \
    --with-regex --with-slang

  make
}

package() {
  cd "$srcdir/mutt-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove unneeded or conflicting files
  rm -f "$pkgdir/usr/bin"/{flea,muttbug}
  rm -f "$pkgdir/usr/share/man/man1"/{flea,muttbug}.1
  rm -f "$pkgdir/etc"/mime.types*

  # install example muttrc file
  install -Dm644 "$srcdir/muttrc.example" "$pkgdir/etc/muttrc.example"
}
