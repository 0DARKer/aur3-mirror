# $Id$
# Maintainer: Andreas Messer <andi@bastelmap.de>
# Based on arm-elf-gcc-base by Sergej Pupykin <pupykin.s+arch@gmail.com>

_target=arm-eabi

pkgname=${_target}-gcc-base
pkgver=4.8.0
pkgrel=2
pkgdesc="The GNU Compiler Collection"
arch=(i686 x86_64)
groups=('devel')
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://gcc.gnu.org"
makedepends=("${_target}-binutils>=2.23" 'libmpc' 'cloog' 'ppl' 'doxygen')
checkdepends=('dejagnu' 'inetutils')
options=('!libtool' '!emptydirs' '!strip')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2)
md5sums=('e6040024eb9e761c3bea348d1fa5abb0')

prepare() {
  cd $srcdir/gcc-$pkgver

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  echo ${pkgver} > gcc/BASE-VER

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" {libiberty,gcc}/configure

  mkdir ${srcdir}/gcc-build
}

build() {
  cd ${srcdir}/gcc-build

  export CFLAGS='-Os'
  export CXXFLAGS='-Os'

  ${srcdir}/gcc-${pkgver}/configure --prefix=/usr \
	--target=${_target} \
  --disable-nls \
  --enable-languages=c \
	--with-local-prefix=/usr/$CHOST/${_target}/lib \
	--with-as=/usr/bin/${_target}-as \
  --with-ld=/usr/bin/${_target}-ld \
  --without-headers \
	--with-newlib \
  --enable-shared \
	--enable-softfloat \
  --enable-interwork \
	--with-sysroot=/usr/$CHOST/${_target}

  make all-gcc all-target-libgcc
}

check() {
  cd $srcdir/gcc-build

  ulimit -s 32768

  # Dont do anything, doesnt makes any sense
  #make -k check || true
  #${srcdir}/gcc-${pkgver}/contrib/test_summary
}

package() {
  cd $srcdir/gcc-build

  make DESTDIR=$pkgdir install-gcc install-target-libgcc

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info

  cp -r $pkgdir/usr/libexec/* $pkgdir/usr/lib/
  rm -rf $pkgdir/usr/libexec

  # strip it manually
  strip $pkgdir/usr/bin/* 2>/dev/null || true
  find $pkgdir/usr/lib -type f -exec ${_target}-strip --strip-debug --strip-unneeded {} \; 2>/dev/null || true
}
