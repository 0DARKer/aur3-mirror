# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-mednafen-supergrafx-git
pkgver=551.ac51785
pkgrel=1
pkgdesc="libretro implementation of Mednafen/Beetle SGX. (PC Engine SuperGrafx)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/beetle-supergrafx-libretro"
license=('GPL')
makedepends=('git')

_gitname=beetle-supergrafx-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/mednafen_supergrafx_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm644 "${_gitname}/mednafen_supergrafx_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-mednafen-supergrafx.so"
  install -Dm644 "mednafen_supergrafx_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-mednafen-supergrafx.info"
}
