# Contributor: Ivan Reche <ivan.reche[at]gmail[dot]com>
# Contributor: Masoud Naservand <relive[dot]mn[at]gmail[dot]com>
# Maintainer: SÃ¡ndor Nagy <sanya868[at]gmail[dot]com>
pkgname=rcssmonitor
pkgver=15.1.0
pkgrel=5
pkgdesc="The RoboCup 2D Soccer Simulator Monitor."
arch=('i686', 'x86_64')
url="https://sourceforge.net/apps/mediawiki/sserver/index.php?title=Main_Page"
license=('GPL')
groups=('rcss')
depends=('qt4' 'boost-libs>=1.32')
makedepends=('boost>=1.32' 'gcc-libs')
optdepends=(
	'rcssserver:	RCSS Server'
	'soccerwindow2:	extra RCSS viewer')
options=('buildflags' 'makeflags')
changelog=ChangeLog
source=(http://downloads.sourceforge.net/sourceforge/sserver/$pkgname-$pkgver.tar.gz
	$pkgname-$pkgver.patch)
sha512sums=('472e09c023d3a06fe7172e0249a0bdbfc9c511b2d57316abcaa3b380d939ef8ac3dc3ef6b8f15a842ee3c2cce4ef50668658839ebeb7d8f1696f6b7b12bf3d20'
            'fc81f32c763a820f44eb038ace2c35e402d33b3e728355296ab083cefcbb88a238a45c5927835342b7503c16e5798359779a24e0f5d910802c56155f296f4695')

prepare() {
        cd "$srcdir/$pkgname-$pkgver/src"
        patch -p0 -i "$srcdir/$pkgname-$pkgver.patch"
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
