# Maintainer:  cuihao <cuihao dot leo at gmail dot com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

# Original PKGBUILD (extra/xfce4-settings):
# $Id: PKGBUILD 143211 2011-11-23 12:04:45Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings-devel
_pkgname=xfce4-settings
pkgver=4.9.2
pkgrel=1
pkgdesc="Settings manager for xfce (Development Version)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('exo>=0.7.1' 'xfconf' 'libxfce4ui' 'libnotify' 'libxklavier' 'garcon' 'cairo')
makedepends=('intltool')
optdepends=('libcanberra: for sound control')
source=(http://archive.xfce.org/src/xfce/${_pkgname}/4.9/${_pkgname}-${pkgver}.tar.bz2
        xfce4-settings-4.9.2.patch)
sha1sums=('8718ee62d03ac68757b4d1eb8a8a675cd8412750'
          'a2e552d728d41e8811fdf48183d32664f526c52c')

groups=('xfce4-devel')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname" "$_pkgname-git")

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # enable gnome icon theme, clearlooks theme and font hinting by default
  patch -Np1 -i "$srcdir/xfce4-settings-4.9.2.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
