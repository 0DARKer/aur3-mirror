# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=xfce4-settings-devel
pkgver=4.8.2
pkgrel=1
pkgdesc="Settings manager for XFCE (Development Version)"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4-devel')
depends=('exo-devel>=0.6.0' 'libxfce4ui-devel' 'libnotify>=0.7.1' 'libxklavier' 'tango-icon-theme' 'gnome-icon-theme' 'gtk-engines' 'gtk-xfce-engine-devel')
makedepends=('intltool')
optdepends=('libcanberra: for sound control')
conflicts=('xfce4-settings' 'xfce4-settings-git' 'xfce-mcs-manager')
source=(http://archive.xfce.org/src/xfce/xfce4-settings/4.8/xfce4-settings-${pkgver}.tar.bz2
	xfce4-settings-4.6.0.patch)
md5sums=('a45420fea20f29265ab6785cb1d07377'
         '3046732e5d73c9939e7674602eadb1e2')

build() {
  cd ${srcdir}/xfce4-settings-${pkgver}
  # enable Tango icon theme, clearlooks theme and font hinting by default(taken from Fedora)
  patch -Np1 -i ${srcdir}/xfce4-settings-4.6.0.patch || return 1
    
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-xrandr \
	--enable-xcursor \
	--enable-libnotify \
	--enable-libxklavier \
	--enable-pluggable-dialogs \
	--enable-sound-settings \
	--disable-debug
  make
}

package() {
	cd ${srcdir}/xfce4-settings-${pkgver}
	make DESTDIR=${pkgdir} install
}
