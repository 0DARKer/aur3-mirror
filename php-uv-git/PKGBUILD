# Maintainer: Rowan Lewis <rl@nbsp.io>

_gitname=php-uv
pkgname=$_gitname-git
pkgver=r482.424faae
pkgrel=1
pkgdesc="PHP libuv extension"
arch=('i686' 'x86_64')
url="https://github.com/chobie/php-uv"
license=('PHP')
depends=('php>=5.5')
makedepends=('git')
backup=('etc/php/conf.d/uv.ini')
source=('git+https://github.com/chobie/php-uv.git')
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_gitname"
	#git clone gnome:libgd
	git submodule update --init
}

build() {
	cd "$srcdir/$_gitname"
	make -C libuv CFLAGS=-fPIC
	phpize
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$_gitname"
	make INSTALL_ROOT="$pkgdir" install
	echo 'extension=uv.so' > uv.ini
	install -Dm644 uv.ini "$pkgdir/etc/php/conf.d/uv.ini"
}