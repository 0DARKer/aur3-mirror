# Contributor: Dmitriy Morozov <archlinux@foxcub.org>

pkgname=ipe
_sfproject=ipe7
_dirver=7.1
pkgver=7.1.6
pkgrel=2
pkgdesc="The extensible drawing editor"
url="http://tclab.kaist.ac.kr/ipe/"
depends=('lua' 'qt4' 'freetype2' 'zlib' 'poppler' 'python2')
arch=('i686' 'x86_64')
license=("GPL")
conflicts=('ipe')
ipepresenter_version_=f29d75686e95
source=("http://downloads.sourceforge.net/project/$_sfproject/$pkgname/$_dirver/$pkgname-$pkgver-src.tar.gz"
        "http://hg.mrzv.org/IpePresenter/archive/$ipepresenter_version_.tar.gz"
        "ipe.bash-completion"
        "config.patch"
        )
md5sums=('da0a5f075ede86c529dd4d9d8c5d57ca'
         '07dc9c39181b63c4bc30400c9a1b67e3'
         '694f0d5402655901be385647e5d8d6e3'
         'af3db2930ff57decb63cd322a845b025')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/src"
  patch config.mak < "$srcdir/config.patch"
}

build() {
  # Ipe
  cd "$srcdir/$pkgname-$pkgver/src"
  make IPEPREFIX=/usr

  # IpePresenter
  cd "$srcdir/IpePresenter-$ipepresenter_version_"
  CPPFLAGS+=" -I ../$pkgname-$pkgver/src/include -I ../$pkgname-$pkgver/src/ipecanvas" \
    LIBS=" -L ../$pkgname-$pkgver/build/lib" \
    make
}

package() {
  # Ipe
  cd "$srcdir/$pkgname-$pkgver/src"
  INSTALL_ROOT="$pkgdir" make install IPEPREFIX=/usr

  # IpePresenter
  cd "$srcdir/IpePresenter-$ipepresenter_version_"
  install -m755 ipepresenter "$pkgdir/usr/bin"

  # Lua binding
  mkdir -p "$pkgdir/usr/lib/lua/5.1"
  ln -s /usr/lib/libipelua.so "$pkgdir/usr/lib/lua/5.1/ipe.so"

  # Bash completion
  mkdir -p "$pkgdir/etc/bash_completion.d"
  install "$srcdir/ipe.bash-completion" "$pkgdir/etc/bash_completion.d/ipe"
}

# vim: ft=sh syn=sh et ts=2 sw=2
