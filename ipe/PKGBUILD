# Contributor: Dmitriy Morozov <archlinux@foxcub.org>

pkgname=ipe
_sfproject=ipe7
_dirver=7.1
pkgver=7.1.5
pkgrel=1
pkgdesc="The extensible drawing editor"
url="http://tclab.kaist.ac.kr/ipe/"
depends=('lua' 'qt4' 'freetype2' 'zlib' 'poppler' 'python2')
arch=('i686' 'x86_64')
license=("GPL")
conflicts=('ipe')
ftiversion_=20091205
ptiversion_=20140303
stiversion_=20131107
ipepresenter_version_=f29d75686e95
source=("http://downloads.sourceforge.net/project/$_sfproject/$pkgname/$_dirver/$pkgname-$pkgver-src.tar.gz"
        "http://downloads.sourceforge.net/project/$_sfproject/tools/figtoipe-$ftiversion_.tar.gz"
        "http://downloads.sourceforge.net/project/$_sfproject/tools/pdftoipe-$ptiversion_-src.tar.gz"
        "http://downloads.sourceforge.net/project/$_sfproject/tools/svgtoipe-$stiversion_.tar.gz"
        "http://hg.mrzv.org/IpePresenter/archive/$ipepresenter_version_.tar.gz"
        "ipe.bash-completion"
        "svgtoipe-python2.patch"
        "config.patch"
        )
md5sums=('ff0cc1a64e6764df9e25dcfdce0a956d'
         'a19e0712df137939c37c194b551da6b8'
         'd0119e11bcc1cba9b3e6801a2ccec13d'
         '3bf0d57bca2c8a0ceec72a08a0689fc5'
         '07dc9c39181b63c4bc30400c9a1b67e3'
         '694f0d5402655901be385647e5d8d6e3'
         '967a191ca673aec93546640002c794ab'
         'af3db2930ff57decb63cd322a845b025')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/src"
  patch config.mak < "$srcdir/config.patch"

  cd "$srcdir/svgtoipe-$stiversion_"
  patch svgtoipe.py < "$srcdir/svgtoipe-python2.patch"
}

build() {
  # Ipe
  cd "$srcdir/$pkgname-$pkgver/src"
  make IPEPREFIX=/usr

  # figtoipe
  cd "$srcdir/figtoipe-$ftiversion_"
  make

  # pdftoipe
  cd "$srcdir/pdftoipe-$ptiversion_-src"
  make

  # IpePresenter
  cd "$srcdir/IpePresenter-$ipepresenter_version_"
  CPPFLAGS+=" -I ../$pkgname-$pkgver/src/include -I ../$pkgname-$pkgver/src/ipecanvas" \
    LIBS=" -L ../$pkgname-$pkgver/build/lib" \
    make
}

package() {
  # Ipe
  cd "$srcdir/$pkgname-$pkgver/src"
  INSTALL_ROOT="$pkgdir" make install IPEPREFIX=/usr

  # figtoipe
  cd "$srcdir/figtoipe-$ftiversion_"
  install -m755 figtoipe "$pkgdir/usr/bin"

  # pdftoipe
  cd "$srcdir/pdftoipe-$ptiversion_-src"
  install -m755 pdftoipe "$pkgdir/usr/bin"

  # svgtoipe
  cd "$srcdir/svgtoipe-$stiversion_"
  install -m755 svgtoipe.py "$pkgdir/usr/bin/svgtoipe"

  # IpePresenter
  cd "$srcdir/IpePresenter-$ipepresenter_version_"
  install -m755 ipepresenter "$pkgdir/usr/bin"

  # Lua binding
  mkdir -p "$pkgdir/usr/lib/lua/5.1"
  ln -s /usr/lib/libipelua.so "$pkgdir/usr/lib/lua/5.1/ipe.so"

  # Bash completion
  mkdir -p "$pkgdir/etc/bash_completion.d"
  install "$srcdir/ipe.bash-completion" "$pkgdir/etc/bash_completion.d/ipe"
}

# vim: ft=sh syn=sh et ts=2 sw=2
