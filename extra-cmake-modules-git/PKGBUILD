# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=extra-cmake-modules-git
pkgver=2356.316c775
pkgrel=1
pkgdesc='Extra CMake modules'
arch=('any')
url='http://quickgit.kde.org/index.php?p=extra-cmake-modules.git&a=summary'
license=('LGPL')
depends=('cmake')
makedepends=('git')
source=(git://anongit.kde.org/extra-cmake-modules.git)
md5sums=('SKIP')

pkgver() {
  cd extra-cmake-modules
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  mkdir -p build
  cd build
  cmake ../extra-cmake-modules \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/man "${pkgdir}"/usr/share
}
