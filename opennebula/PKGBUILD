# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Mikhail Kulemin <mihkulemin@gmail.com>
# Contributor: Michael Pusterhofer <pusterhofer (at) student (dot) tugraz (dot) at>

pkgname=opennebula
_unstable_pkg=opennebula-unstable
pkgver=4.4.1
pkgrel=1
pkgdesc="Virtual management infrastructure as a service (IaaS) toolkit for cloud computing (NOTE: Read the PKGBUILD!)"
arch=('i686' 'x86_64')
url='http://docs.opennebula.org/stable/release_notes441/index.html'
license=('Apache')
depends=('ruby>=1.8.7'
         'xmlrpc-c>=1.31'
         'openssl>=0.9.8'
         'sqlite3>=3.6'
         'openssh'
         'libxml2>=2.7'
         'curl'
         'libxslt'
         'expat'
         'cdrkit'
         'log4cpp>=1.0'
         'ruby-opennebula>=4.4.0')
makedepends=('xmlrpc-c>=1.31'
             'pkgconfig'
             'scons>=0.98')
optdepends=('nfs-utils: for using the shared file system storage model'
            'mariadb>=5.1: optional replacement for SQLite as the DB back-end'
            'libmariadbclient>=5.1: required if using MariaDB/MySQL instead of SQLite')
conflicts=('opennebula-unstable')
install=opennebula.install
changelog=ChangeLog
source=("http://downloads.opennebula.org/packages/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'opennebula.service'
        'chown_fix.patch')
sha512sums=('2a783cc4632c5d196fd8d4be6c711fed86f5e8b4612f4d3fa9025a9d7de4117a194e00c03366706f72b5a04c2527a6e89ab5cd5565d317c9cb67448e671014b1'
            '00db19d355a789fb27320d16917804354fc824c99a851e755a58943824377f87f158e0e560773957acfc3f959a2937462391271d309aff4e6aabb50cb964009d'
            '997a62e4fb07b7eedaa48f2f2dce2ad4a373d40b7a1c7cc3291f8f4d4be8a7faa0674dfbf2d3fdd92c8bd1d0c48f72803861b1d86ba9b72f79d34870b94e47e2')

prepare() {
	cd "${pkgname}-${pkgver}"

	# Patch upstream install script to not attempt to chown the install
	# directories because `makepkg` will otherwise fail on a fresh installation.
	# We do our own chown in post_install().
	patch < "${srcdir}/chown_fix.patch"
}

build() {
	cd "${pkgname}-${pkgver}"

	###########################################################################
	##                                                                       ##
	## It is highly recommended that you read the documentation and tweak    ##
	##     the PKGBUILD accordingly:                                         ##
	## http://docs.opennebula.org/stable/integration/references/compile.html ##
	##                                                                       ##
	## This package assumes a self-contained install. If you do NOT want a   ##
	##     self-contained install, then remove `-d /srv/cloud/one` from the  ##
	##     package() function and MAKE SURE you properly change the          ##
	##     appropriate sections of opennebula-unstable.install               ##
	##                                                                       ##
	###########################################################################

	# This builds the vanilla OpenNebula package. Tweak this line as desired.
	scons new_xmlrpc=yes
}

package() {
	cd "${pkgname}-${pkgver}"

	install -D -m644 "${srcdir}/opennebula.service" "${pkgdir}/usr/lib/systemd/system/opennebula.service"

	# This checks to see whether OpenNebula is currently installed. To avoid
	# a potentially scary message, errors are sent to /dev/null
	if [[ ("$(pacman -Qq ${pkgname} 2>/dev/null)" == "${pkgname}") || ("$(pacman -Qq ${_unstable_pkg} 2>/dev/null)" == "${_unstable_pkg}") ]]; then
		# Use -k when running ./install.sh to keep previous configuration files
		# Note: It is highly recommended to not keep the oned.conf file.
		DESTDIR="${pkgdir}" ./install.sh -k -u oneadmin -g cloud -d /srv/cloud/one
	else
		# Do not use -k when running ./install.sh for new installations
		DESTDIR="${pkgdir}" ./install.sh -u oneadmin -g cloud -d /srv/cloud/one
	fi
}
