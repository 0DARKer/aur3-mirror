# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Mikhail Kulemin <mihkulemin@gmail.com>
# Contributor: Michael Pusterhofer <pusterhofer (at) student (dot) tugraz (dot) at>

pkgname=opennebula
_unstable_pkg=opennebula-unstable
pkgver=3.6.0
pkgrel=1
pkgdesc="Virtual management infrastructure as a service (IaaS) toolkit for cloud computing (NOTE: Read the PKGBUILD!)"
arch=('i686' 'x86_64')
url="http://opennebula.org/software:rnotes:rn-rel3.6"
license=('Apache')
depends=('ruby>=1.8.7' 'xmlrpc-c-abyss>=1.06' 'openssl>=0.9.8' 'sqlite3>=3.6' 'openssh' 'libxml2>=2.7'
  'curl' 'libxslt' 'expat' 'cdrkit')
makedepends=('xmlrpc-c-abyss>=1.06' 'pkgconfig' 'scons>=0.98')
optdepends=('nfs-utils: for using the shared file system storage model'
  'mysql>=5.1: optional replacement for SQLite as the DB back-end'
  'libmysqlclient>=5.1: required if using MySQL instead of SQLite')
conflicts=('opennebula-unstable')
install=opennebula.install
changelog=ChangeLog
source=("http://dev.opennebula.org/packages/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz"
        "oned"
        "chown_fix.patch"
        "var_run.patch")
sha512sums=('697d2b0d270ca95c8f725214180651309e30cc25d9041d929a9c4f77f1c8fce41d021c43ddabe2f74fd5a6693084ca11d54305ac969276432f96c7ccf721f5e1'
            'c72d136d8b83e3c97d94e5898c0982f3f66c32caac1dcd4a8fd88b06a99bd72447e96642d6129ace6502ada15668c9f32345f370b9bc21e68097edb9b1ecc7bd'
            'cec484e2edcec0b70bbcef12266d7ab7c347e1a170181a95dbb5f7cb3ca7e14343b5449d93228bb463b5cdd531c26f0b5cb4020850e958dc56bcfa1f6aa8990c'
            'c5ab132bf2f13ac055678b1b1bf940bdf8b141331c0d5dee326f532b818b2f59c247488cb95eee5d4ded5b6cb95ad745897bda6b0c215b87dfc90a8fbf825029')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  #############################################################################
  ##                                                                         ##
  ## It is highly recommended that you read the documentation and tweak the  ##
  ##   PKGBUILD accordingly:                                                 ##
  ## http://opennebula.org/documentation:rel3.6:compile                      ##
  ##                                                                         ##
  ## This package assumes a self-contained install. If you do NOT want a     ##
  ##   self-contained install, then remove `-d /srv/cloud/one` from the      ##
  ##   package() function and MAKE SURE you properly change the appropriate  ##
  ##   sections of opennebula.install                                        ##
  ##                                                                         ##
  #############################################################################

  # This builds the vanilla OpenNebula package. Tweak this line as desired.
  scons
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  install -D -m755 "$srcdir"/oned "$pkgdir"/etc/rc.d/one

  # Patch upstream install script to not attempt to chown the install
  # directories because `makepkg` will otherwise fail on a fresh installation.
  # We do our own chown in post_install().
  patch < "$srcdir/chown_fix.patch"

  # Patch upstream install script to use /run/one instead of /var/run/one.
  patch < "$srcdir/var_run.patch"

  # This checks to see whether OpenNebula is currently installed.
  if [[ ("$(pacman -Qq ${pkgname})" == "${pkgname}") || ("$(pacman -Qq ${_unstable_pkg})" == "${_unstable_pkg}") ]]; then
    # Use -k when running ./install.sh to keep previous configuration files
    DESTDIR="$pkgdir/" ./install.sh -k -u oneadmin -g cloud -d /srv/cloud/one
  else
    # Do not use -k when running ./install.sh for new installations
    DESTDIR="$pkgdir/" ./install.sh -u oneadmin -g cloud -d /srv/cloud/one
  fi
}

# vim:set ts=2 sw=2 et:
