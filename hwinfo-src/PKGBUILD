pkgname=hwinfo-src
pkgver=21.6.2.1
pkgrel=1
pkgdesc='Hardware detection tool from openSUSE'
arch=('x86_64' 'i686' 'armv7h')
url='http://download.opensuse.org/source/factory/repo/oss/suse/src/'
license=('GPL2')
depends=('libx86emu-src' 'bash' 'perl')
makedepends=('sysfsutils' 'flex')
conflicts=('hwinfo')
options=('!emptydirs')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/hwinfo-${pkgver%.*.*}-${pkgver#*.*.}.src.rpm")
sha256sums=('910f829e0259bbdfc2e70ab2cd171540b24ff1e534df99b08033a7399793ba59')

prepare() {
  rpm2cpio "hwinfo-${pkgver%.*.*}-${pkgver#*.*.}.src.rpm" | bsdtar -xf -
  tar Jxf "hwinfo-${pkgver%.*.*}.tar.xz"
}

build() {
  make -C "hwinfo-${pkgver%.*.*}" -j1 \
    CFLAGS+="-fPIC -I$srcdir/hwinfo-${pkgver%.*.*}/src/hd" \
    LIBDIR=/usr/lib
}

package() {
  make -C "hwinfo-${pkgver%.*.*}" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}
