# Maintainer : josephgbr <rafael.f.f1 at gmail.com>
# Contributor : angvp <angvp at archlinux.org>

pkgname=lib32-libaio
pkgver=0.3.109
pkgrel=2
pkgdesc="The Linux-native asynchronous I/O facility (aio) library (32 bit)"
arch=('x86_64')
url="http://lse.sourceforge.net/io/aio.html"
license=('LGPL')
makedepends=('gcc-multilib')
source=(ftp://ftp.kernel.org/pub/linux/libs/aio/libaio-$pkgver.tar.bz2)
md5sums=('14f3a22fc14118a48be80ab5ea974f49')

build() {
  cd "$srcdir/libaio-$pkgver"

  # set lib32 environment in Makefiles
  # "/lib" -> "/lib32"
  sed -i \
	-e 's/\/lib/\/lib32/g' \
	Makefile
  # "CFLAGS :=" -> "CFLAGS := -m32"
  sed -i \
	-e '/^libdir/s/lib/lib32/' \
	-e '/^CFLAGS/s/:= /&-m32 /' \
	src/Makefile
  
  make
}

package() {
  cd "$srcdir/libaio-$pkgver"
  make prefix="$pkgdir/usr" install
  rm -rf ${pkgdir}/usr/include
}

