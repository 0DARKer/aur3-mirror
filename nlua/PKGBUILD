pkgname=nlua
pkgdesc="NLua is the bind between Lua world and the .NET world."
pkgver=1.3.0
pkgrel=2
arch=(i686 x86_64)
license=("MIT")
url="https://github.com/NLua/NLua"
source=("NLua.zip::https://github.com/NLua/NLua/archive/4228d7b53641a74be51ee0e1741c2bf47ec3b8c2.zip"
"KeraLua.zip::https://github.com/NLua/KeraLua/archive/5567e1e78e83c85543f057627baa077c9c494dae.zip"
"KopiLua.zip::https://github.com/NLua/KopiLua/archive/30c175dc13e3f35b4dd749850c232fbd4c189f5f.zip"
"lua.zip::https://github.com/NLua/lua/archive/92f42611ca0284ff313b6c7aae9011864c6c4888.zip"
"KeraLua.dll.config")
depends=(mono)
makedepends=(dos2unix cmake)
sha1sums=('de05d09b8ad15eaf14685df8878037365f07012d'
          '73a38607608950967845c810c7c587f9f5b20b65'
          'eb4515aa40dcf5fdd33a9b86c22600c4dc741cf0'
          '04f1965e3dd7b3782acd2f074f39be32d1af9f46'
          '8539ca6d83ce104ba50fa38b8fedba4bc16e929b')

prepare() {
  cd "${srcdir}/lua-"*
  cp -r * ../KeraLua-*/external/lua/
  cd ../KeraLua-*
  cp -r * ../NLua-*/Core/KeraLua/
  cd ../KopiLua-*
  cp -r * ../NLua-*/Core/KopiLua/
  cd ../NLua-*
  find . -type f -exec dos2unix {} \;
  ./autogen.sh --prefix=/usr
  find . -name '*.csproj' -exec sed -i 's,<DebugType>none,<DebugType>pdbonly,g' {} \;
  cd Core/KopiLua
  ./autogen.sh --prefix=/usr
}
          
build() {
	cd "${srcdir}/"NLua-*
	xbuild NLua.Net45.sln /p:Configuration=Release
	cd Applications/LuaRunner
	xbuild NLua.csproj /p:Configuration=Release
	cd ../../Core/KopiLua/Lua
	xbuild Lua.csproj /p:Configuration=Release
	cd ../Luac
	xbuild Luac.csproj /p:Configuration=Release
	cd ../../KeraLua/external/lua
	mkdir build
	cd build
	cmake -DCMAKE_BUILD_TYPE=Release -DLIB_LUA_VER='' -DLIB_SUFFIX='' ..
	make
}

package() {
	cd "${srcdir}/"NLua-*/Run/Release/net45
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/lib/nlua/"{} \;
	cd ../../../Applications/LuaRunner
	install -Dm755 luarunner "$pkgdir/usr/bin/nlua"
	cd obj/Release
	find . -name 'NLua.exe*' -exec install -m644 {} "$pkgdir/usr/lib/nlua/" \;
	cd ../../../../Core
	install -Dm644 NLua/nlua.pc "$pkgdir/usr/lib/pkgconfig/nlua.pc"
	install -m644 KeraLua/src/keralua.pc "$pkgdir/usr/lib/pkgconfig/"
	cd KeraLua/external/lua/build/lib
	install -m644 liblua.so "$pkgdir/usr/lib/libnlua.so"
	cd ../../../../../
	cd KopiLua
	install -m644 KopiLua/kopilua.pc "$pkgdir/usr/lib/pkgconfig/"
	install -m755 Lua/lua "$pkgdir/usr/bin/kopilua"
	install -m644 Lua/obj/Release/Lua.exe "$pkgdir/usr/lib/nlua/"
	install -m755 Luac/luac "$pkgdir/usr/bin/kopiluac"
	install -m644 Luac/obj/Release/Luac.exe "$pkgdir/usr/lib/nlua/"
	install -m644 "$srcdir/KeraLua.dll.config" "$pkgdir/usr/lib/nlua/"
	find "$pkgdir/usr/lib/nlua" -name '*.dll' -exec gacutil -i {} -root "$pkgdir/usr/lib" \;
	find "$pkgdir/usr/bin" -type f -exec sed -i "s,kopilua,nlua," {} \;
	sed -i "s,kopilua,nlua," "$pkgdir/usr/lib/pkgconfig/kopilua.pc"
	sed -i "s,LuaRunner,NLua," "$pkgdir/usr/bin/nlua"
}
