_xpadcommit='55df811f2066fcaec2548248f0a1a6a0c12dc6b8'
_xpadsha512sum='d4120f1318902fa981c1c08810ce871b866e39ca6358b049c418de04ad0387545e92c4b29ab161f73d8b095961c141169fb925194026af7964069312929c5092'

pkgname='steamos-xpad-dkms'
pkgver='0.2'
pkgrel='1'
pkgdesc="xpad kernel module included with Valve's SteamOS"
arch=('any')
url='http://thread.gmane.org/gmane.linux.kernel.input/33971'
license=('GPL2')
install="${pkgname}.install"
depends=('dkms')
optdepends=('linux-headers: Build the module against Arch kernel'
            'linux-lts-headers: Build the module against LTS Arch kernel')
source=("https://raw.github.com/torvalds/linux/${_xpadcommit}/drivers/input/joystick/xpad.c"
        "Makefile"
        "0001-input_xpad_use-proper-endpoint-type.patch"
        "0002-input_xpad_set-the-leds-properly-on-xbox-wireless-controllers.patch"
        "0003-input_xpad_move-the-input-device-creation-to-a-new-function.patch"
        "0004-input_xpad_set-the-correct-led-number.patch"
        "0005-input_xpad_disconnect-all-wireless-controllers-at-init.patch"
        "0006-input_xpad_handle-present-and-gone-correctly.patch"
        "0007-input_xpad_properly-name-the-led-class-devices.patch"
        "steamos-xpad-dkms.dkms")
sha512sums=(${_xpadsha512sum}
            '3aef5f9530d2243a09d35ef110839a71665eea7ff268f839ec860f1dfb041287dc6615760d04056e32a5865a08cad73ef1247de684fb9ed8e65586a380ec7d41'
            'd9516d59df4e56fc3aa5a31082c36514863842b75c29c78d9a266eff6aaac41e1ef816c0c7bf2b3a26fcf5c1f39270d0f01a33ca0c8c6a734178dfee1cd9f749'
            '3c8f04f3451aa6f057e8513e104430f86e27b25787007ea44121c363ce5593e3fc2a893609a323fc8a614ea2d509ad506ec553df98d527e0e0c24da2dcb9afa1'
            '4538bd704f5d8da92de6479feb7afc79c7b2a49d16dca3c1054f64bd5660d3e812e514cf730427b7264f39baf7d4445901e28835a07089f47de71deb7328c0fa'
            'c24a1e63597cc1bef05c6ad87811ffbba06691caa7e5facaa57ec9f7616888541fcaed7d9b8685dd55ef7de0f2a5c4a5fd36ff88dc3b7f8d05488b2afdff26eb'
            'f1e4596f6ddf7adda01191e838e657c75f49021fd262da69ddbaa26af9a8b5ef9ffe0ba2fdc56ab492a66bf85c333ee04f115e2ebcc2cf48819829f97eb68d35'
            '973b5c2a0f080b30676312a06861d59ca85743bde7bc983ba4456197f2dc4d2aa53730a4637ac4a261b28ed751599eb1428b1d4f308c41aa64f95a3264d3e052'
            '6b3b075e0affe990676e56b3ef9e648a440660625c925fbcc7fd266b3c39895e5a87b360cd3ad370a7a7f475ebb138a35accd7503d382d0cd29d26592ec6909b'
            'b3352710ca76c50ffa94f7a1140a34490d4f75cd8840e473be60496ecd5d5cc8bfbc6bcca2eb8574c0740e73a1983426735aa0bf89e7f281babfecc82bede29d')

prepare() {
    local _tmpdir="${srcdir}/temp"
    mkdir "${_tmpdir}"
    cp -L "${srcdir}/xpad.c" "${_tmpdir}/xpad.c" # we will patch a copy of our source xpad.c
    
    patch -d "${_tmpdir}" -Np4 -i "${srcdir}/0001-input_xpad_use-proper-endpoint-type.patch"
    patch -d "${_tmpdir}" -Np4 -i "${srcdir}/0002-input_xpad_set-the-leds-properly-on-xbox-wireless-controllers.patch"
    patch -d "${_tmpdir}" -Np4 -i "${srcdir}/0003-input_xpad_move-the-input-device-creation-to-a-new-function.patch"
    patch -d "${_tmpdir}" -Np4 -i "${srcdir}/0004-input_xpad_set-the-correct-led-number.patch"
    patch -d "${_tmpdir}" -Np4 -i "${srcdir}/0005-input_xpad_disconnect-all-wireless-controllers-at-init.patch"
    patch -d "${_tmpdir}" -Np4 -i "${srcdir}/0006-input_xpad_handle-present-and-gone-correctly.patch"
    patch -d "${_tmpdir}" -Np4 -i "${srcdir}/0007-input_xpad_properly-name-the-led-class-devices.patch"
}

package() {
    local _tmpdir="${srcdir}/temp"
    install -d "${pkgdir}/usr/src/${pkgname}-${pkgver}"
    install -m 644 -T "${_tmpdir}/xpad.c" "${pkgdir}/usr/src/${pkgname}-${pkgver}/xpad.c"
    install -m 644 -T "${srcdir}/Makefile" "${pkgdir}/usr/src/${pkgname}-${pkgver}/Makefile"
    install -m 644 -T "${srcdir}/steamos-xpad-dkms.dkms" "${pkgdir}/usr/src/${pkgname}-${pkgver}/dkms.conf"
}