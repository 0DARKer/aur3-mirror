# Maintainer: Philipp Richter <richterphilipp.pops@gmail.com>

_xpadcommit='55df811f2066fcaec2548248f0a1a6a0c12dc6b8'
_xpadsha512sum='d4120f1318902fa981c1c08810ce871b866e39ca6358b049c418de04ad0387545e92c4b29ab161f73d8b095961c141169fb925194026af7964069312929c5092'

_pkgbase='steamos-xpad'
pkgname='steamos-xpad-dkms'
pkgver='0.2.1'
pkgrel='1'
pkgdesc="xpad kernel module included with Valve's SteamOS"
arch=('any')
url='https://github.com/ValveSoftware/steamos_kernel'
license=('GPL2')
install="${pkgname}.install"
depends=('dkms')
conflicts=("${_pkgbase}")
optdepends=('linux-headers: Build the module against Arch kernel'
            'linux-lts-headers: Build the module against LTS Arch kernel')
source=("https://raw.github.com/torvalds/linux/${_xpadcommit}/drivers/input/joystick/xpad.c"
        "Makefile"
        "steamos_kernel-d92cfaac03183c01382bde7e817d22e4ea9078d5-fix-xbox-controller-pad-support.patch"
        "steamos-xpad-dkms.dkms")
sha512sums=(${_xpadsha512sum}
            '3aef5f9530d2243a09d35ef110839a71665eea7ff268f839ec860f1dfb041287dc6615760d04056e32a5865a08cad73ef1247de684fb9ed8e65586a380ec7d41'
            'd059e549b1a5eaa6b6ae0fd653ed84cc7aa1afd60b21eec899653971ab8510a2cf1311694f203e8e48a6c9f419f45e59d85fcdbf81675137c41eeff80c81c62e'
            '11558d0a6198b7e1d9669dfba6863da126aa0bd94ec3f19851e83f1fb1bde2d195804d2ef4a4c20d4324767cf44f51e21582df38840c757437f390f9baa9adf5')

prepare() {
    local _tmpdir="${srcdir}/temp"
    mkdir "${_tmpdir}"
    cp -L "${srcdir}/xpad.c" "${_tmpdir}/xpad.c" # we will patch a copy of our source xpad.c
    
    patch -d "${_tmpdir}" -Np4 -i "${srcdir}/steamos_kernel-d92cfaac03183c01382bde7e817d22e4ea9078d5-fix-xbox-controller-pad-support.patch"
}

package() {
    local _tmpdir="${srcdir}/temp"
    install -d "${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
    install -m 644 -T "${_tmpdir}/xpad.c" "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/xpad.c"
    install -m 644 -T "${srcdir}/Makefile" "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/Makefile"
    install -m 644 -T "${srcdir}/steamos-xpad-dkms.dkms" "${pkgdir}/usr/src/${_pkgbase}-${pkgver}/dkms.conf"
}
