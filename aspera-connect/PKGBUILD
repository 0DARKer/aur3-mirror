# Maintainer: Oliver Mangold <o dot mangold at gmail dot com>
# Contributor: Alpinweis <alpinweis@gmail.com>
pkgname=aspera-connect
pkgver=3.4.2
pkgrel=1
pkgdesc="Aspera high-performance transfer browser plug-in (binary only)"
arch=('i686' 'x86_64')
url="http://www.asperasoft.com"
license=('custom')
depends=('gtk2')
optdepends=('libmysqlclient_r.so.15')

if [ "$CARCH" == "i686" ]; then
    source=("http://d3gcli72yxqn2z.cloudfront.net/connect/bin/aspera-connect-3.4.2.90351-linux-32.tar.gz")
    sha384sums=('5cad10d73111c9629fbc0d8c44840f8c9f5ed64884022ecd98ca50daefffc8ac15ddc67c6420dd90e9ce492997689925')
elif [ "$CARCH" == "x86_64" ]; then
    source=("http://d3gcli72yxqn2z.cloudfront.net/connect/bin/aspera-connect-3.4.2.90351-linux-64.tar.gz")
    sha384sums=('e3e4fecaa6c703f3b5a2effad23414edd5ed4cd2af1d7989e46d9a4bf8a550a8d99229eefb01cfebe80c65f6ae085942')
fi

package() {
    INSTALL_DIR=/opt/aspera
    PLUGIN_DIR=/usr/lib/mozilla/plugins
    install_script=$srcdir/$(basename "${source[0]}" .tar.gz).sh
    SKIP=`awk '/^__ARCHIVE_FOLLOWS__/ { print NR + 1; exit 0; }' "$install_script"`
    mkdir -p "$pkgdir$INSTALL_DIR" "$pkgdir$PLUGIN_DIR"
    tail -n +$SKIP "$install_script" | tar zxo -C "$pkgdir$INSTALL_DIR"
    ln -s "$INSTALL_DIR/lib/libnpasperaweb.so" "$pkgdir$PLUGIN_DIR/"
}
