# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Soeren Koerner <nonick at posteo dot de>
# Contributor: Benedikt 'linopolus' Mueller <benemue at googlemail dot com>

pkgname=mediathek
pkgver=3.0.0
pkgrel=1
pkgdesc="Offers access to the Mediathek of different tv stations (ARD, ZDF, Arte, etc.)"
arch=(any)
url="http://zdfmediathk.sourceforge.net"
license=('custom')
depends=('java-runtime' 'vlc')
optdepends=('mplayer: record streams' 'flvstreamer: stream flash')
options=(!strip !zipman)
source=(http://downloads.sourceforge.net/zdfmediathk/MediathekView_${pkgver}.zip
        $pkgname
        $pkgname.desktop)
md5sums=('ddff07a9f98febf3567091b33954cfe0'
         'e52a61eabb6a5931dc2a1fe261bc7d95'
         '7d4548884f5dc517ad30fd0584ec9d46')

package() {
  install -d $pkgdir/{opt/$pkgname/{lib,bin},usr/{bin,share/{{doc,licenses}/$pkgname,applications}}}
  install -m755 $pkgname $pkgdir/usr/bin/
  install -m644 MediathekView.jar $pkgdir/opt/$pkgname/
  install -m644 -t $pkgdir/opt/$pkgname/lib lib/*
  install -m755 bin/flv.sh $pkgdir/opt/$pkgname/bin/
  install -m644 $pkgname.desktop $pkgdir/usr/share/applications/
  install -m644 Anleitung.pdf $pkgdir/usr/share/doc/$pkgname/
  install -m644 -t $pkgdir/usr/share/licenses/$pkgname Copyright/*
  rm $pkgdir/usr/share/licenses/$pkgname/flvstreamer-LICENSE.txt
}