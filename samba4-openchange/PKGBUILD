# Maintainer: Adam Russell <adamlr6+arch@gmail.com>
# Contributor: ngoonee <n g    o o n    e e <at gmail.com, delete all spaces>>
pkgname=samba4-openchange
pkgver=4.0.0A13
# We use the 'A' to fake out pacman's version comparators.  Samba chooses
# to append 'a','b',etc to their subsequent releases, which pamcan
# misconstrues as alpha, beta, etc.  Bad samba!
_realver=4.0.0alpha13
pkgrel=2
pkgdesc="Alpha build of samba4. This package was SPECIFICALLY created to support evolution-mapi and may not work for any other purpose."
arch=('i686' 'x86_64')
url="http://www.samba.org"
license=('GPL3')
depends=('db>=4.7' 'popt' 'libcups' 'acl' 'libldap' 'libcap>=2.16' 'heimdal>=1.2-1' 'pam' 'fam' 'gnutls>=2.4.1')
makedepends=('python2')
provides=("samba4=${pkgver}")
options=(!makeflags)
source=(http://us1.samba.org/samba/ftp/samba4/samba-${_realver}.tar.gz)    
md5sums=('76cdb4f50f8b892eaa885fcf0f1aef4a')
_prefix="/opt/samba4"

build() {
	# The build process expects python to be python2,
	# so that's what it will be.
	mkdir -p ${srcdir}/bin
	if [[ ! -h ${srcdir}/bin/python ]]; then
		ln -s /usr/bin/python2 ${srcdir}/bin/python
	fi
	if [[ ! -h ${srcdir}/bin/python-config ]]; then
		ln -s /usr/bin/python2-config ${srcdir}/bin/python-config
	fi
	export PATH="${srcdir}/bin:${PATH}"

	cd ${srcdir}/samba-${_realver}/lib/tdb
	./autogen.sh
	./configure --prefix=${_prefix}/samba
	make || return 1
	make DESTDIR="$pkgdir/" install
	make realdistclean

	cd ${srcdir}/samba-${_realver}/lib/tevent
	./autogen.sh
	./configure --prefix=${_prefix}/samba
	make || return 1
	make DESTDIR="$pkgdir/" install
	make realdistclean

	cd ${srcdir}/samba-${_realver}/source4
	./autogen.sh
	./configure --prefix=${_prefix} --enable-fhs
	make || return 1
}

package() {
	_pyver=`python2 -c 'import sys; print(sys.version[:3])'`

	export PATH="${srcdir}/bin:${PATH}"

	cd ${srcdir}/samba-${_realver}/source4
	make DESTDIR="$pkgdir/" install

	install -d ${pkgdir}/etc/ld.so.conf.d
	echo "${_prefix}/samba/lib" > ${pkgdir}/etc/ld.so.conf.d/samba4.conf

	cd ${pkgdir}/${_prefix}/lib/
	for _i in libldb; do
		ln -s ${_i}-samba4.so.0 ${_i}.so
		ln -s ${_i}-samba4.so.0 ${_i}.so.0
	done

	find ${pkgdir}/${_prefix}/lib/python${_pyver}/site-packages/ -name '*.py' | \
		xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
	find ${pkgdir}/${_prefix}/bin ${pkgdir}/${_prefix}/sbin -type f -executable | \
		xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
}
