# Maintainer: Axper Jan <483ken at gmail dot com>
# Contributor: Daniel Kirchner <daniel at ekpyron dot rog>
# Contributor: Gustavo Alvarez <sl1pkn07 at gmail dot com>
# Contributor: Chase Geigle <sky at skystrife dot com>
pkgname=sdl2-hg
pkgver=7501
pkgrel=3
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2.0). (Mercurial Version)"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('MIT')
makedepends=('mercurial' 'cmake')
depends=('sh')
provides=('sdl2')
conflicts=('sdl2')
source=('sdl2::hg+http://hg.libsdl.org/SDL')
md5sums=('SKIP')
_hgrepo="sdl2"

pkgver() {
  cd "${srcdir}/${_hgrepo}"
  hg identify -n
}

prepare() {
  mkdir "${srcdir}/${_hgrepo}/build"
}

build() {
  cd "${srcdir}/${_hgrepo}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DSDL_STATIC=OFF
  make
}

package() {
  cd "${srcdir}/${_hgrepo}/build"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 ../COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 ../sdl2.m4 "${pkgdir}/usr/share/aclocal/sdl2.m4"
}
