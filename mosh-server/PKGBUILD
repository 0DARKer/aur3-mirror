# Maintainer: kreed <kreed@kreed.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Christian Neukirchen <chneukirchen@gmail.com>
# Contributor: Thomas Weißschuh <thomas_weissschuh lavabit.com>

pkgname=mosh-server
pkgver=1.2.3
pkgrel=1
pkgdesc='Mobile shell, surviving disconnects with local echo and line editing (server component only)'
arch=('x86_64' 'i686')
url='http://mosh.mit.edu/'
license=('GPL3')
depends=('protobuf' 'ncurses' 'zlib' 'libutempter')
source=("https://github.com/downloads/keithw/mosh/mosh-$pkgver.tar.gz")
sha256sums=('93f09fda77e57f05485a61f3ac679bf9f3f359a9b0b93c216ddd53cd124a768f')
options=('!emptydirs')
conflicts=('mosh')

build() {
  cd "$srcdir/mosh-$pkgver"

  ./autogen.sh
  ./configure --prefix=/usr --disable-client
  make
}

package() {
  cd "$srcdir/mosh-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 "conf/ufw/applications.d/mosh" \
    "$pkgdir/usr/share/mosh/ufw-mosh.example"
}

# vim:set ts=2 sw=2 et:
