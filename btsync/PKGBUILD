# Maintainer: Dongsheng Cai <http://dongsheng.org>
# Maintainer: ava1ar < mail(at)ava1ar(dot)info >
# Contributor: codekoala < aur(at)cloudlery.com >

pkgname=btsync
pkgver=1.0.134
pkgrel=2
pkgdesc="Automatically sync files via secure, distributed technology"
license=("custom:btsync")
arch=('i686' 'x86_64')
url="http://labs.bittorrent.com/experiments/sync.html"
_arch='x64'
[ "$CARCH" = 'i686' ] && _arch='i386'
install=btsync.install
source=(http://btsync.s3-website-us-east-1.amazonaws.com/${pkgname}_${_arch}.tar.gz
  license.html::http://www.bittorrent.com/legal/terms-of-use
  btsync.service
  btsync@.service
  btsync.json
  btsync@.json)
backup=('etc/btsync/btsync.json')
sha1sums=('fcaa7400e2af5d1285a672bc6ddd3e0945becf08'
	  'f2593c371fef2f3dd521306e66066a3019a8805c'
          '006b42c700d19a9cc72a95847b07b277d4f603f9'
          '5ba1d11536d4e05a34a5b9a12f4ffa4162d7d3fd'
          '3f479df53e0e74f87141c462bfddb12b58917a37'
          '4f7387b8f3428ad524ba90ca4d20882e3f94fe1f')
[ "$CARCH" = 'i686' ] && sha1sums[0]='273b066ab71678602445a72539f9fe031efd061e'
 
package() {
  install -dm755 "${pkgdir}/var/btsync"
 
  cd "${srcdir}"
  install -Dm644 license.html "${pkgdir}/usr/share/licenses/${pkgname}/license.html"
  install -Dm755 btsync "${pkgdir}/usr/bin/btsync"
  install -Dm644 btsync.json "${pkgdir}/etc/btsync/btsync.json"
  install -Dm644 btsync@.json "${pkgdir}/etc/btsync/btsync@.json"
 
  # install systemd script
  install -Dm644 btsync.service "$pkgdir/usr/lib/systemd/system/btsync.service"
  install -Dm644 btsync@.service "$pkgdir/usr/lib/systemd/system/btsync@.service"
}
