# Maintainer: Gu1 <gu1@aeroxteam.fr>
pkgname=python2-swiftclient
pkgver=2.3.1
pkgrel=2
pkgdesc="Client Library for OpenStack Object Storage API"
arch=('any')
url="https://github.com/openstack/python-swiftclient"
license=('Apache 2.0')
depends=('python2' 'python2-requests' 'python2-keystoneclient')
makedepends=('python2-setuptools')
options=(!emptydirs)
source=("https://github.com/openstack/python-swiftclient/archive/${pkgver}.tar.gz")
sha256sums=('b79bef337bb44bb98f9a9f9412ea96a109a3c0061eef4c70f5ea416f9c4ad59b')

build() {
  cd "python-swiftclient-$pkgver"
  echo -e "Name: python-swiftclient\nVersion: $pkgver" > PKG-INFO

  sed -e 's|bin/python|bin/python2|' -i bin/swift
}

package() {
  cd "python-swiftclient-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
