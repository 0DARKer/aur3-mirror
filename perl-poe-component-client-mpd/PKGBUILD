# Contributor: 3ED <krzysztof1987 at gmail dot com>
#

pkgname=perl-poe-component-client-mpd
_lastauthor=J/JQ/JQUELIN
_pkgname=POE-Component-Client-MPD
pkgver=1.121670
pkgrel=1
pkgdesc="full-blown poe-aware mpd client library"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-moosex-has-sugar' 'perl-audio-mpd-common' 'perl-poe' 'perl-moosex-semiaffordanceaccessor' 'perl-moosex-poe' 'perl-test-corpus-audio-mpd>=1.120990')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('023ba78d2855bbb3a3b7c32a055d6b24bb138660efae15f074c1bb5ad6b71805')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  perl Build.PL
  perl Build
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  perl Build test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  perl Build install
}
