# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=lzlib
pkgver=1.3
pkgrel=1
pkgdesc="A library providing in-memory LZMA compression and decompression functions"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/lzip/lzlib.html"
license=('GPL3')
depends=('gcc-libs')
makedepends=('make')
install=$pkgname.install
source=(http://download.savannah.gnu.org/releases/lzip/$pkgname-$pkgver.tar.gz)
sha256sums=('e8aba598db960b36fe75f8e135d6e506c99861deb50d3f92f925c0bda85208da')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  install -Dm755 minilzip ${pkgdir}/usr/bin/minilzip
}
