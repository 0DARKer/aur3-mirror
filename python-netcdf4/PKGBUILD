# Contributor: rememberthemer <rememberthemer@_GMAIL_DOT_COM_>
# vim: set et sw=4 ts=4 sts=4:

pkgname=python-netcdf4
pkgver=0.9.9
pkgrel=1
pkgdesc="A python3 library for reading, manpiulating and creating netcdf files"
arch=('x86_64' 'i686')
url="http://code.google.com/p/netcdf4-python"
license=('MIT')

depends=('python' 'netcdf>=4.1.2' 'hdf5>=1.8.7' 'python-numpy')
optdepends=('python2-netcdf4: python 2 version, includes docs and examples')
source=(http://netcdf4-python.googlecode.com/files/netCDF4-${pkgver}.tar.gz)

build() {
    cd "$srcdir"/netCDF4-$pkgver
    python setup.py clean --all
    USE_NCCONFIG=1 python setup.py build
}

package() {
    cd "$srcdir"/netCDF4-$pkgver
    python setup.py install --prefix=/usr --root="$pkgdir"
    
    mkdir -p "$pkgdir"/usr/share/doc/${pkgname}/{docs,test,examples}
    install -m644 test/* -t "$pkgdir"/usr/share/doc/$pkgname/test
    install -m644 docs/* -t "$pkgdir"/usr/share/doc/$pkgname/docs
    install -m644 examples/* "$pkgdir"/usr/bin/* -t "$pkgdir"/usr/share/doc/$pkgname/examples
    
    rm -r "$pkgdir"/usr/bin
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
md5sums=('3f671a529b57ebc9c286a75baf80089d')
