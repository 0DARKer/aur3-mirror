# Contributor: abenson <adrian.benson@yahoo.co.nz>

pkgname=python-netcdf4
pkgver=0.9.3
pkgrel=2
pkgdesc="A python library for reading, manpiulating and creating netcdf files"
arch=('x86_64' 'i686')
url="http://code.google.com/p/netcdf4-python"
license=('MIT')

builddepends=()
depends=('python2' 'netcdf>=4.1' 'hdf5>=1.8.4' 'python2-numpy')

source=(http://netcdf4-python.googlecode.com/files/netCDF4-${pkgver}.tar.gz
        'license.txt')
md5sums=('b3a82af0300d0e0b136824b9a0bb2f41'
         'd3b89c97de68de49328b557d21de8adc')
changelog=${pkgname}.ChangeLog

build() {
    cd "$srcdir"/netCDF4-$pkgver
    
    python2 setup.py build || return 1
    # comment following if you only want netcdf4 module 
    # included to allow backward compatibility with the deprecated pycdf
    NETCDF3_DIR=/usr python2 setup-nc3.py build || return 1
}

package() {
    cd "$srcdir"/netCDF4-$pkgver
    python2 setup.py install --prefix=/usr --root="$pkgdir" || return 1
    # comment following if you only want netcdf4 module 
    # included to allow backward compatibility with the deprecated pycdf
    NETCDF3_DIR=/usr python2 setup-nc3.py install \
      --prefix=/usr --root="$pkgdir" || return 1
    
    # install arbitrarily creates a bin dir with some netcdf conversion programs,
    # however these functions are better handle by netcdf itself. They are also 
    # installed under examples
    rm -r "$pkgdir"/usr/bin

    mkdir -p "$pkgdir"/usr/share/{${pkgname},doc/${pkgname}}
    
    cp -r docs examples "$pkgdir"/usr/share/doc/$pkgname
    cp -r test utils "$pkgdir"/usr/share/$pkgname
    
    find  "$pkgdir"/usr/share -type f -exec chmod 644 '{}' \;
    
    install -Dm644 "$srcdir"/license.txt \
            "$pkgdir"/usr/share/licenses/$pkgname/license.txt || return 1
    
    # fix crunchbangs fo python2
    sed -e 's/#!.*/#!\/usr\/bin\/env python2/' \
        -i "$pkgdir"/usr/share/python-netcdf4/utils/{grib2nc4,nc4tonc3,nc3tonc4}
}
# vim: set et sw=4 ts=4 sts=4:
