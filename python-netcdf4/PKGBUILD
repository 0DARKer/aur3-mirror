# Contributor: rememberthemer <rememberthemer@_GMAIL_DOT_COM_>
# vim: set et sw=4 ts=4 sts=4:

pkgname=python-netcdf4
pkgver=1.1.6
pkgrel=1
pkgdesc="A python3 library for reading, manpiulating and creating netcdf files"
arch=('x86_64' 'i686')
url="https://github.com/Unidata/netcdf4-python"
license=('MIT')

depends=('python' 'python-numpy' 'netcdf>=4.1.2' 'hdf5'
         'curl' 'zlib' 'libssh2' 'openssl' 'python-dateutil')
makedepends=('cython')
optdepends=('python2-netcdf4: python 2 version, includes docs and examples')
source=(https://github.com/Unidata/netcdf4-python/archive/v${pkgver}rel.tar.gz)
md5sums=('d54669d962c95a15b6d789adf65afd2c')

build() {
    cd "$srcdir"/netcdf4-python-${pkgver}rel
    2to3 -vnw setup.py
    USE_NCCONFIG=1 python setup.py clean --all
    USE_NCCONFIG=1 python setup.py build
}

package() {
    cd "$srcdir"/netcdf4-python-${pkgver}rel
     USE_NCCONFIG=1 python setup.py install --prefix=/usr --root="$pkgdir"

    mkdir -p "$pkgdir"/usr/share/doc/${pkgname}/{docs,test,examples}
    install -m644 test/* -t "$pkgdir"/usr/share/doc/$pkgname/test
    install -m644 docs/* -t "$pkgdir"/usr/share/doc/$pkgname/docs
    install -m644 examples/* "$pkgdir"/usr/bin/* -t "$pkgdir"/usr/share/doc/$pkgname/examples

    rm -r "$pkgdir"/usr/bin
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
