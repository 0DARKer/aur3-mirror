# Maintainer: birdflesh <antkoul at gmail dot com>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Leo von Klenze <aur@leo.von-klenze.de>

pkgname=w_scan
pkgver=20121111
pkgrel=2
pkgdesc="Universal ATSC and DVB blind scanner"
arch=('i686' 'x86_64')
url="http://wirbel.htpc-forum.de/w_scan/index_en.html"
license=('GPL')
depends=('glibc')
source=("http://wirbel.htpc-forum.de/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('30da05747fed9988e11ebc7745f5e71f')

build() {
  cd "$srcdir/$pkgname-$pkgver" 
  sed -i 's/DTV_DVBT2_PLP_ID/DTV_DVBT2_PLP_ID_LEGACY/' scan.c	# Temporary compilation fix. Thank you trishika!
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 README "$pkgdir/usr/share/doc/$pkgname"
}

