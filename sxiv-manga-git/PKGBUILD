# Maintainer: UnCO <undercooled _at_ lavabit DOT com>
# Contributor: Army
# Contributor: Bastien Dejean <baskerville _at_ lavabit DOT com>

pkgname=sxiv-manga-git
_gitname="sxiv"
pkgver=v1.1.1.46.gd5b2d37
pkgrel=1
pkgdesc="Simple (or small or suckless) X Image Viewer with manga view mode (-m)"
arch=('i686' 'x86_64')
url="https://github.com/muennich/sxiv"
license=('GPL2')
depends=('imlib2' 'desktop-file-utils')
makedepends=('git')
optdepends=(
  'imagemagick: for the example commands in config.def.h'
  'libjpeg-turbo: for the example commands in config.def.h'
  'gimp: for the example commands in config.def.h')
provides=('sxiv')
conflicts=('sxiv')
source=('git://github.com/muennich/sxiv.git'
        'manga.patch')
md5sums=('SKIP'
         'f37cebe48e1fd9e3387cbc5cb731b689')
install=sxiv.install

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_gitname"

  patch -p1 -i $startdir/manga.patch
  if [ -e $startdir/config.h ];then cp $startdir/config.h .;fi

  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$_gitname"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  install -Dm644 sxiv.desktop "$pkgdir/usr/share/applications/sxiv.desktop"
}

