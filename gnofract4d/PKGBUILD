# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: sausageandeggs <sausageandeggs@archlinux.us>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=gnofract4d
pkgver=3.14
pkgrel=2
pkgdesc="A fractal browser with PyGTK gui"
arch=(i686 x86_64)
url=http://$pkgname.sourceforge.net
license=(custom)
depends=(desktop-file-utils libjpeg pygtk)
options=(!strip)
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('bdd8e950a98221054d52883d5303a435cbaa3d5d4239c36aff91598a6433d20a')
sha512sums=('cf3614f60a43f7ab20208fb1a62d3823495c84060d177d2926bad20a475e661d7ec45a7bc14e826229656027fe7c0f68aac093b6b0dc7912e84d8d8ec8ff81e5')

build() {
    cd "$srcdir"/$pkgname-$pkgver/
    find -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    python2 setup.py build
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
