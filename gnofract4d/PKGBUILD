# Maintainer: willemw <willemw12@gmail.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: sausageandeggs <sausageandeggs@archlinux.us>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=gnofract4d
pkgver=3.14.1
pkgrel=2
pkgdesc="Create beautiful fractal images in PyGTK"
arch=('any')
url="http://gnofract4d.sourceforge.net"
license=('BSD')
makedepends=('docbook-xsl')
depends=('desktop-file-utils' 'libjpeg' 'libpng' 'pygtk')
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz
        gnofract4d-manual.html)
md5sums=('0bdfa514dcab3dd8ba126d640fd36717'
         '784e9aeb0abcf473d338481f37503ab0')

prepare() {
  cd $pkgname-$pkgver
  sed -i "s|/usr/share/sgml/docbook/stylesheet/xsl/nwalsh/xhtml/docbook.xsl|/usr/share/xml/docbook/xsl-stylesheets-"$(pacman -Q docbook-xsl | awk '{ print $2 }' | awk -F"-" '{ print $1 }')"/manpages/docbook.xsl|" \
            doc/gnofract4d-manual/C/gnofract4d.xsl
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
  python2 createdocs.py
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 doc/gnofract4d.1 "$pkgdir/usr/share/man/man1/$pkgname.1"

  # Patch for createdocs.py. Generating "Formula Reference Reference" entries fails:
  #     Erro:  no refentry: No refentry elements found in "Gnofract 4D".   Gnofract 4D
  install -m644 "$srcdir/gnofract4d-manual.html" "$pkgdir/usr/share/gnome/help/gnofract4d/C/gnofract4d-manual.html"
}

