# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=system-storage-manager-git
pkgver=0.3.1.g83f908a
pkgrel=1
pkgdesc="System Storage Manager (ssm) provides easy to use command line interface to manage your storage using various technologies like lvm, btrfs, encrypted volumes and possibly more - git checkout"
arch=('i686' 'x86_64')
url="http://sourceforge.net/p/storagemanager/home/Home/"
license=('GPL')
depends=('python2' 'lvm2' 'e2fsprogs' 'xfsprogs' 'cryptsetup' 'btrfs-progs' 'device-mapper' 'dmraid')
makedepends=('git')
provides=(storagemanager)
conflicts=(storagemanager)
source=('system-storage-manager::git://git.code.sf.net/p/storagemanager/code')

pkgver() {
	cd system-storage-manager/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd system-storage-manager/
	sed -i "s/'AUTHORS', //" setup.py
}

package() {
	cd system-storage-manager
	python2 setup.py install --root="${pkgdir}"
}

sha256sums=('SKIP')
