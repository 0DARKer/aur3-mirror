# Maintainer: Boohbah <boohbah at gmail.com>

pkgname=libsodium-git
_gitname=libsodium
pkgver=5cb2c0b
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl-based crypto library (git version)"
arch=('i686' 'x86_64')
url="https://github.com/jedisct1/libsodium"
license=('MIT')
makedepends=('git')
conflicts=('libsodium')
provides=('libsodium')
source=('git://github.com/jedisct1/libsodium.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's/-/./g'
}

build() {
  cd $_gitname
  ./autogen.sh
  ./configure --prefix="/usr"
  make
}

check() {
  cd $_gitname
  make check
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
