# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-json-any'
pkgver='1.36'
pkgrel='1'
pkgdesc="Wrapper Class for the various JSON classes."
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-json>=2.02' 'perl-json-xs>=2.3' 'perl-yaml-syck' 'perl-namespace-clean')
makedepends=('perl-test-requires' 'perl-test-without-module' 'perl-test-warnings' 'perl-test-fatal')
url='http://search.cpan.org/dist/JSON-Any'
source=("http://cpan.metacpan.org/authors/id/E/ET/ETHER/JSON-Any-${pkgver}.tar.gz")
md5sums=('378650e94616c3752acb6ec440a1de38')
sha512sums=('73061d457ae95b1613c524d260152aa0a3aec7fbf163bfe3e413f27d10fd047d25954a33e52a1652f1737bce33b25ddff3e4749825d73aea4fa1b68816719b76')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/JSON-Any-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
