# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-json-any'
pkgver='1.33'
pkgrel='1'
pkgdesc="Wrapper Class for the various JSON classes."
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-json>=2.02' 'perl-json-xs>=2.3' 'perl-yaml-syck')
makedepends=('perl-test-requires' 'perl-test-without-module' 'perl-test-warnings')
url='http://search.cpan.org/dist/JSON-Any'
source=("http://cpan.metacpan.org/authors/id/E/ET/ETHER/JSON-Any-${pkgver}.tar.gz")
md5sums=('c7940f091c106f9e2c7ff93f8ae8bfee')
sha512sums=('d019198cba76ccabb147884b03acce664ba9f93f75e936571319510ac1cf0e052c9f172d6a3a7cbe9fb8a28c22a73fdfc8416044101f0a0c4e4e5f86f12f311b')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/JSON-Any-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
