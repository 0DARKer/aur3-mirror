# Maintainer: Dino Morelli <dino@ui3.info>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Simo Leone <neotuli@gmail.com>

pkgname=darcs
pkgver=2.8.4
pkgrel=3
pkgdesc='Decentralized replacement for CVS with roots in quantum mechanics'
url='http://darcs.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('ghc' 'cabal-install')
depends=('curl')
options=('strip')
source=("http://darcs.net/releases/${pkgname}-${pkgver}.tar.gz")

sha256sums=('3fab4dbb2d5dd9f381d49f4730c12e1534b5c484b0dde9712614a7151f659f98')


# PKGBUILD functions

build() {
   cd ${srcdir}/${pkgname}-${pkgver}

   # This may have been causing problems in the past, took it out for now
   # But it's a good idea to maybe have your cabal updated periodically, eh?
   #cabal update

   cabal sandbox init
   cabal install --only-dependencies
   cabal configure -O --prefix=/usr
   cabal build
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}

   cabal copy --destdir=${pkgdir}

   # darcs is both a binary and a library. This packaging exists
   # solely to give the user the darcs binary. Unfortunately the only
   # way I found to prevent installation of these files was to blow
   # away this dir just prior to packaging:
   rm -r ${pkgdir}/usr/lib

   chmod 755 $pkgdir/usr/share/man/man1/$pkgname.1   
}
