# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=systemd
pkgname=lib32-${_pkgbase}
pkgver=195
pkgrel=2
pkgdesc="system and service manager (32bit)"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('lib32-pam' 'lib32-lzma' 'lib32-libgcrypt' 'lib32-glib2' 'lib32-libcap' "${_pkgbase}")
makedepends=('lib32-acl' 'lib32-dbus-core' 'hwids' 'lib32-util-linux' 'lib32-kmod' 'gobject-introspection' 'gperf' 'intltool' 'linux-api-headers' 'quota-tools' 'gcc-multilib') # 'cryptsetup' 'python'
provides=("lib32-libsystemd=$pkgver" "lib32-systemd-tools=$pkgver" "lib32-udev=$pkgver")
conflicts=('lib32-libsystemd' 'lib32-systemd-tools' 'lib32-udev')
options=('!libtool')
source=("http://www.freedesktop.org/software/$_pkgbase/$_pkgbase-$pkgver.tar.xz"
        '0001-Reinstate-TIMEOUT-handling.patch'
        'use-split-usr-path.patch')
md5sums=('38e8c8144e7e6e5bc3ce32eb4260e680'
         '5543be25f205f853a21fa5ee68e03f0d'
         'fd5b5f04ab0a847373d357555129d4c0')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}

  # still waiting on ipw2x00, et al to get fixed...
  patch -Np1 <"$srcdir/0001-Reinstate-TIMEOUT-handling.patch"

  # hang onto this until we do the /{,s}bin merge
  patch -Np1 <"$srcdir/use-split-usr-path.patch"

  ./configure \
      --libdir=/usr/lib32 \
      --libexecdir=/usr/lib32 \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-introspection \
      --disable-audit \
      --disable-gtk-doc \
      --disable-ima \
      --disable-libcryptsetup \
      --disable-manpages \
      --disable-microhttpd \
      --disable-nls \
      --disable-qrencode \
      --disable-selinux \
      --disable-tcpwrap \
      --with-distro=arch \
      --with-usb-ids-path=/usr/share/hwdata/usb.ids \
      --with-pci-ids-path=/usr/share/hwdata/pci.ids \
      --without-python

  make
}

package() {
  make -C $_pkgbase-$pkgver DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/{etc,var,usr/{bin,include,lib,share}}
}
