# Maintainer : n00b <drake_nxt(at)hotmail.com>
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: hekel <hekel(at)archlinux.info>

pkgname=mate-file-archiver-thunar
pkgver=1.6.0
pkgrel=1
pkgdesc="Archive manipulator for MATE without caja dependency and with thunar-archive-plugin support, not meant to be used with caja"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'bzip2' 'desktop-file-utils' 'gtk2' 'gzip' 'libarchive' 'tar')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
optdepends=('unrar: for RAR uncompression'
	    'zip: for ZIP archives' 'unzip: for ZIP archives'
	    'p7zip: 7zip compression utility' 'arj: for ARJ archives'
	    'unace: extraction tool for the proprietary ace archive format'
	    'thunar-archive-plugin: create and deflate archives in thunar')
conflicts=('mate-file-archiver')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname%-*}-${pkgver}.tar.xz"
        fr-rpm-bsdtar.patch
	caja_schema_fix.patch
	add_schema_fix.patch
	engrampa.tap)
sha1sums=('c7df115b532a14fb7be193113da32cda4a2218e8'
          '219b05a979bf6f249aaae27964f02345fd81168d'
	  'aa4ed66f955dbe5a5cc63f810e9f511a42a64d62'
	  'e761c20de8f99998390a99e0ede88ad6f85e22f0'
	  'b76f24a892ac3dc9275a01d4a9985cbae431adf6')
install=${pkgname%-*}.install

prepare() {
    cd "${srcdir}/${pkgname%-*}-${pkgver}"    
    # This patch 'depends' on libarchive
    patch -Np1 -i "${srcdir}/fr-rpm-bsdtar.patch"
    # Make caja depencenies optional
    patch -Np1 -i "${srcdir}/caja_schema_fix.patch"
    # Fix engrampa crashing when adding a directory to an existing archive
    patch -Np0 -i "${srcdir}/add_schema_fix.patch"
}

build() {
    cd "${srcdir}/${pkgname%-*}-${pkgver}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --disable-scrollkeeper \
        --disable-packagekit \
	--disable-caja-actions
    mate-doc-prepare --force
    make
}

package() {
    cd "${srcdir}/${pkgname%-*}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -Dm755 ${srcdir}/engrampa.tap ${pkgdir}/usr/lib/xfce4/thunar-archive-plugin/engrampa.tap
}
