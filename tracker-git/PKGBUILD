# Mantainer: Stefano Facchini <stefano.facchini at gmail.com>
# Contributor: Necronian <necronian at gmail dot com>
# Contributor: Emanuele Rossi <nuovodna at gmail dot com>
# Contributor: Frederic Bezies <fredbezies@gmail.com>

pkgname=tracker-git
_pkgname=tracker
pkgver=20111102
pkgrel=1
pkgdesc="All-in-one indexer, search tool and metadata database"
url="http://projects.gnome.org/tracker"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('libgee' 'libgnome-keyring' 'upower' 'libexif' 'exempi'
         'poppler-glib' 'libgsf' 'enca' 'networkmanager' 'gtk3'
         'desktop-file-utils' 'hicolor-icon-theme' 'sqlite3' 'icu'
         'glib2' 'libffi' 'pcre' 'util-linux')
makedepends=('libgee' 'libgnome-keyring' 'upower' 'libexif' 'exempi'
             'poppler-glib' 'libgsf' 'icu' 'enca' 'networkmanager' 'gtk3'
             'desktop-file-utils' 'hicolor-icon-theme' 'gobject-introspection'
             'intltool' 'giflib' 'gstreamer0.10-base' 'totem-plparser'
             'evolution' 'taglib' 'nautilus' 'gnome-panel' 'libvorbis' 'flac'
             'vala' 'gtk-doc')
options=('!libtool' '!emptydirs')
conflicts=('tracker' 'libtracker-sparql')
provides=('tracker' 'libtracker-sparql')
install=tracker.install
source=()
md5sums=()

_gitroot="git://git.gnome.org/tracker"
_gitname="tracker"
_buildir=${_gitname}-build

build() {
    cd ${srcdir}

    msg 'Connecting to GIT server...'

    if [ -d ${_gitname} ]; then
	cd ${_gitname} && git pull origin
	cd ..
    else
	git clone ${_gitroot}
    fi

    msg 'GIT checkout done or server timeout.'

    if [ -d ${_buildir} ]; then
	msg 'Cleaning previous build...'
	rm -rf ${_buildir}
    fi

    git clone ${_gitname} ${_buildir}
    cd ${_buildir}

    msg 'Building...'

    ./autogen.sh \
	--prefix=/usr \
       	--sysconfdir=/etc \
	--localstatedir=/var \
	--libexecdir=/usr/lib/tracker \
	--disable-unit-tests \
	--enable-libflac \
	--enable-libvorbis

    make
}

package() {
    cd ${srcdir}/${_buildir}
    make DESTDIR="$pkgdir" install
} 
