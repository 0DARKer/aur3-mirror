# Mantainer: Stefano Facchini <stefano.facchini at gmail.com>
# Contributor: Necronian <necronian at gmail dot com>
# Contributor: Emanuele Rossi <nuovodna at gmail dot com>
# Contributor: Frederic Bezies <fredbezies@gmail.com>

pkgname=tracker-git
_pkgname=tracker
pkgver=20110802
pkgrel=1
pkgdesc="A first class object database, extensible tag/metadata database, search tool and indexer. This package has introspection enabled."
url="http://projects.gnome.org/tracker"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('upower' 'gstreamer0.10-base' 'libgee' 'gtkhtml' 'libgsf'
         'taglib' 'icu' 'networkmanager' 'giflib' 'poppler')
makedepends=('intltool' 'nautilus' 'totem-plparser' 'gobject-introspection'
             'vala' 'gtk-doc' 'git' 'gnome-common' 'hicolor-icon-theme')
optdepends=('nautilus' 'enca' 'gtkhtml' 'gnome-panel' 'totem-plparser')
options=('!libtool' !emptydirs)
conflicts=(libtracker tracker)
provides=(libtracker tracker)
install=tracker.install
source=()
md5sums=()

_gitroot="git://git.gnome.org/tracker"
_gitname="tracker"
_buildir=${_gitname}-build

build() {
    cd ${srcdir}

    msg 'Connecting to GIT server...'

    if [ -d ${_gitname} ]; then
	cd ${_gitname} && git pull origin
	cd ..
    else
	git clone ${_gitroot}
    fi

    msg 'GIT checkout done or server timeout.'

    if [ -d ${_buildir} ]; then
	msg 'Cleaning previous build...'
	rm -rf ${_buildir}
    fi

    git clone ${_gitname} ${_buildir}
    cd ${_buildir}

    msg 'Building...'

    ./autogen.sh \
	--prefix=/usr \
       	--sysconfdir=/etc \
	--localstatedir=/var \
	--libexecdir=/usr/lib/tracker \
	--enable-video-extractor=gstreamer \
	--disable-hal \
	--enable-unit-tests=no \
	--disable-miner-evolution \
	--disable-nautilus-extension \
	--enable-introspection \
	--disable-schemas-compile
    make
}

package() {
    cd ${srcdir}/${_buildir}
    make DESTDIR="$pkgdir" install
} 
