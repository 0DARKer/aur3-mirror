# Maintainer: OmeGa <omega at mailoo dot org>
# Contributor: Vinycius Maia <suportevg@uol.com.br>

pkgname=firefox-extension-greasemonkey
_addons_file=178376
pkgver=1.5
pkgrel=1
pkgdesc="Customize the way a web page displays or behaves, by using small bits of JavaScript."
arch=('any')
url="http://www.greasespot.net/"
license=('BSD' 'MIT' 'MPL')
depends=('firefox>=14.0')
makedepends=('unzip')
source=(https://addons.mozilla.org/firefox/downloads/file/${_addons_file}/greasemonkey-${pkgver}.xpi)
noextract=("greasemonkey-${pkgver}.xpi")
sha1sums=('d0e6d329c80b3ad7744cea95df3db8e789ce4e5b')

build() {
  cd ${srcdir}
  [[ -d ${pkgname}.extract ]] || \
    mkdir ${pkgname}.extract
  cd ${pkgname}.extract
  unzip ../greasemonkey-${pkgver}.xpi
}

package() {
  cd ${srcdir}/${pkgname}.extract  
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf) || return 1
  local dstdir=${pkgdir}/usr/lib/firefox/extensions/${emid}
  install -d ${dstdir}
#  sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>20.*</em:maxVersion>#' install.rdf
  cp -R * ${dstdir}
  rm ${dstdir}/LICENSE*
  find ${pkgdir} -type d -exec chmod 0755 {} \;
  find ${pkgdir} -type f -exec chmod 0644 {} \;

  # Licenses
  install -Dm644 LICENSE.bsd ${pkgdir}/usr/share/licenses/${pkgname}/BSD
  install -Dm644 LICENSE.mit ${pkgdir}/usr/share/licenses/${pkgname}/MIT
}
