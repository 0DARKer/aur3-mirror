# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Artiom MOLCHANOV

_pkg=vloopback
pkgname=$_pkg-svn
pkgver=24
pkgrel=1
pkgdesc="The video4linux loopback device is a device driver that generates video pipes."
arch=('i686' 'x86_64')
url="http://www.lavrsen.dk/twiki/bin/view/Motion/VideoFourLinuxLoopbackDevice"
license=('GPL')
depends=('glibc')
makedepends=('subversion' 'kernel26-headers')
provides=("$_pkg")
conflicts=("$_pkg")
install="$pkgname.install"

_svntrunk=http://www.lavrsen.dk/svn/vloopback/trunk/
_svnmod=$_pkg

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  # examples makefile fix
  sed -i "s|/usr/sbin|$pkgdir/usr/bin|g" example/Makefile

  make
}

package() {
  cd "$srcdir/$_svnmod-build"

  # Install module
  install -Dm755 vloopback.ko \
    "$pkgdir/lib/modules/`uname -r`/kernel/drivers/misc/vloopback.ko"

  # Install docs and examples
  install -d "$pkgdir"/usr/{bin,share/doc/$_pkg}
  install -Dm644 FAQ README $_pkg.html "$pkgdir/usr/share/doc/$_pkg"
  cd example && make install
}
