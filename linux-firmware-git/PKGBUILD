# Based on linux-firmware by Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: xduugu
pkgname=linux-firmware-git
pkgver=20110519
pkgrel=1
pkgdesc="Firmware files for Linux"
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/dwmw2/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
makedepends=('git')
conflicts=('linux-firmware'
           'kernel26-firmware'
           'ar9170-fw'
           'carl9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw')
provides=("kernel26-firmware"{,-git}"=$pkgver" "linux-firmware=$pkgver")
options=(!strip)
source=(http://www.kernel.org/pub/linux/kernel/people/chr/carl9170/fw/1.9.2/carl9170-1.fw)
md5sums=('9e33ac39dcd610aa1ad80bbf4f1abccf')
sha256sums=('0009861c6ed9943b014dc45b6df5532616b41223f067d20b4df7aaa5a437f4f5')

_gitroot="git://git.kernel.org/pub/scm/linux/kernel/git/dwmw2/linux-firmware.git"
_gitname="linux-firmware"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git checkout master && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
}

package() {
  cd "$srcdir/$_gitname"
  install -dm755 "$pkgdir/lib/firmware" "$pkgdir/usr/share/licenses/$pkgname"
  cp -a * "$pkgdir/lib/firmware/"
  install -Dm644 "$srcdir/carl9170-1.fw" "$pkgdir/lib/firmware/carl9170-1.fw"

  # get missing firmware from kernel branch
  #git checkout -q origin/from-kernel
  #local i
  #for i in $(git diff origin/master origin/from-kernel --diff-filter=A --name-only); do
    #cp -a --parents "$i" "$pkgdir/lib/firmware/"
  #done

  rm -f "$pkgdir/lib/firmware/GPL"*
  mv "$pkgdir/lib/firmware"/{LICEN*,WHENCE} "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
