# Based on linux-firmware by Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: xduugu
pkgname=linux-firmware-git
pkgver=20101206
pkgrel=1
pkgdesc="Firmware files for Linux"
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/dwmw2/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
makedepends=('git')
conflicts=('linux-firmware'
           'kernel26-firmware'
           'ar9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw')
provides=("kernel26-firmware"{,-git}"=$pkgver" "linux-firmware=$pkgver")
options=(!strip)

_gitroot="git://git.kernel.org/pub/scm/linux/kernel/git/dwmw2/linux-firmware.git"
_gitname="linux-firmware"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git checkout master && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
}

package() {
  cd "$srcdir/$_gitname"
  install -dm755 "$pkgdir/lib/firmware" "$pkgdir/usr/share/licenses/linux-firmware/"
  cp -a * "$pkgdir/lib/firmware/"

  # get missing firmware from kernel branch
  git checkout -q origin/from-kernel
  local i
  for i in $(git diff origin/master origin/from-kernel --diff-filter=A --name-only); do
    cp -a --parents "$i" "$pkgdir/lib/firmware/"
  done

  rm -f "$pkgdir/lib/firmware/GPL"*
  mv "$pkgdir/lib/firmware"/{LICEN*,WHENCE} "$pkgdir/usr/share/licenses/linux-firmware/"
}

# vim:set ts=2 sw=2 et:
