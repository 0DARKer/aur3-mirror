# Based on linux-firmware by Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: xduugu
pkgname=linux-firmware-git
pkgver=20120717
pkgrel=1
pkgdesc="Firmware files for Linux"
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
makedepends=('git')
conflicts=('linux-firmware'
           'kernel26-firmware'
           'carl9170-fw')
provides=("linux-firmware=$pkgver")
options=(!strip)
source=('carl9170_1.9.6.fw::http://wireless.kernel.org/en/users/Drivers/carl9170/fw1.9.6?action=AttachFile&do=get&target=carl9170-1.fw')
md5sums=('2fa6ed98d53d0b5fbcc136d1cf5e9609')
sha256sums=('e1695dbfbc6aa7bb3182615bd47905e2df808317e4050878e50bb24285b37068')

_gitroot="git://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git"
_gitname="linux-firmware"

build() {
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname"
    git checkout master
    git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
}

package() {
  cd "$srcdir/$_gitname"
  install -dm755 "$pkgdir/usr/lib/firmware" "$pkgdir/usr/share/licenses/$pkgname"
  cp -a * "$pkgdir/usr/lib/firmware/"
  install -Dm644 "$srcdir/carl9170_1.9.6.fw" "$pkgdir/usr/lib/firmware/carl9170-1.fw"

  rm -f "$pkgdir/usr/lib/firmware/"{configure,GPL*}
  mv "$pkgdir/usr/lib/firmware"/{LICEN*,WHENCE} "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
