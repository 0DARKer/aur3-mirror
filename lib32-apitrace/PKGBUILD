# $Id: PKGBUILD 85781 2013-03-07 07:29:48Z lcarlier $
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Contributor: Glaucous <glakke1 at gmail dot com>

pkgname=lib32-apitrace
pkgver=3.0
pkgrel=1
pkgdesc="Graphics API Tracing (32-bit)"
arch=('x86_64')
url="https://github.com/apitrace/apitrace"
license=('custom')
makedepends=('cmake' 'lib32-mesa-libgl' 'mesa-libgl' 'python2' 'gcc-multilib')
source=("https://github.com/apitrace/apitrace/zipball/$pkgver"
	gcc-4.7-fix.patch)
md5sums=('d3c61c88684de9dc2641d974292b0c49'
         '27a9a7aa14355b56fb48e2e17ff9911e')

build() {
  cd ${srcdir}/apitrace-*

  # Merged upstream
  patch -Np1 -i ${srcdir}/gcc-4.7-fix.patch

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  sed -i -e 's#lib/${CMAKE_PROJECT_NAME}#lib32/${CMAKE_PROJECT_NAME}#g' CMakeLists.txt
  sed -i -e 's#/lib/apitrace/wrappers#/lib32/apitrace/wrappers#g' common/trace_tools_trace.cpp

  cmake . -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE='/usr/bin/python2' -DENABLE_GUI="no"
  make -C build
}

package() {
  depends=('python2' 'lib32-libgl' 'apitrace')

  cd ${srcdir}/apitrace-*

  make -C build DESTDIR="${pkgdir}/" install
  
  mv -v ${pkgdir}"/usr/bin/apitrace" ${pkgdir}"/usr/bin/apitrace32"

  rm -r ${pkgdir}/usr/share
  rm -r ${pkgdir}/usr/bin/*retrace
  
  install -m755 -d ${pkgdir}"/usr/share/licenses/apitrace"
  ln -s apitrace "$pkgdir/usr/share/licenses/apitrace/"${pkgname}
}
