# Maintainer: Max Roder <maxroder@web.de>

# To port this PKGBUILD to another language of tor-browser you 
# have to change $pkgname, $_language, $pkgdesc and $url in PKGBUILD
# AND (!) the first line in the .install file!

pkgname='tor-browser-en'
pkgver='2.2.36_rc1'
_realpkgver='2.2.36-1-dev'
_language='en-US'
pkgrel='1'
pkgdesc='Anonymous browsing using firefox and tor'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-runtime')
optdepends=('zenity: simple dialog boxes'
			'kdialog: KDE dialog boxes'
			'notify-send: Gnome dialog boxes')
install="${pkgname}.install"
source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz"
		"https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz.asc"
		"${pkgname}.desktop"
		"${pkgname}.png"
		"${pkgname}.sh")
if [[ "$CARCH" == 'x86_64' ]]; then
	sha256sums=('d0aeb8d361949302de739e1232d6214ccf94e838335926415a5922d5161bc096'
				'ef4976d3c7d1cc62c90c8ed1229380d6347a490d44106faf133fc7bfcfd0e55b')
else
	sha256sums=('89a3a1e8acc324de98d088bd67a22bf578010cef597c718657c73bea19e9eb6f'
				'ca99adef8875f2bb5454635fb1f89f1307e4ea075fc23d311184f8a871958ad2')
fi
sha256sums+=('2217f011197329019ae3d282d95623e0230f8f7a3a604290744280530cf1698a'
			'17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			'7fc947b8ae1483c5abb4545f6e26b315ff1fb16eb7ae8837f7afb10a9277383b')
noextract=("tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")

package() {
	cd "${srcdir}"

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.sh
	sed -i "s/REPL_VERSION/${_realpkgver}/g"	${pkgname}.sh
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.sh

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.desktop
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.desktop
	sed -i "s/REPL_COMMENT/${pkgdesc}/g"		${pkgname}.desktop

	install -Dm 644 ${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 ${pkgname}.png		${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 ${pkgname}.sh		${pkgdir}/usr/bin/${pkgname}

	install -Dm 644 tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz ${pkgdir}/opt/${pkgname}/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz
}

# vim:set ts=2 sw=2 et:
