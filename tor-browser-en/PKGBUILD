# Maintainer: Max Roder <maxroder@web.de>

pkgname='tor-browser-en'
pkgver='2.2.34_rc3'
_realpkgver='2.2.34-3-dev'
_language='en-US'
pkgrel='2'
pkgdesc='Lets you use Tor with Firefox (+ Torbutton, NoScript, HTTPS-Everywhere) without the need to configure anything. English version.'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-environment' 'xulrunner')
install='tor-browser-en.install'
if [[ "$CARCH" == 'i686' ]]; then
	source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")
	sha256sums=('6c8edcaccc5998bbab26f3fbb3588caa85d8c89b2301c980de5cecb5ee1095e9')
else
	source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")
	sha256sums=('1dc4d64cdba6535ec38da769d82d17259d3711f28f3c78b8a31c9e6e85828b60')
fi
source+=("${pkgname}.desktop"
		"${pkgname}.png")
sha256sums+=('61f7c6a656d7198192573bf84b6487bacdbe5d9bc97e84215e8009a005057545'
			'17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b')
options=('!strip' 'emptydirs')

package() {
	cd ${srcdir}/tor-browser_en-US

	install -d ${pkgdir}/opt/${pkgname}/ ${pkgdir}/usr/bin/

	cp -a * ${pkgdir}/opt/${pkgname}/

	# Desktop stuff
	install -Dm644 ../${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm644 ../${pkgname}.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png

	# Wrapper script
	echo "#!/bin/sh" >> ${pkgdir}/usr/bin/${pkgname}
	echo "cd /opt/${pkgname}/; ./start-tor-browser" >> ${pkgdir}/usr/bin/${pkgname}
	chmod +x ${pkgdir}/usr/bin/${pkgname}
}

# vim:set ts=2 sw=2 et:
