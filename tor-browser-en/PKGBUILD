# Maintainer: Max Roder <maxroder@web.de>

# To port this PKGBUILD to another language of tor-browser you 
# have to change $pkgname, $_language, $pkgdesc and $url in PKGBUILD
# AND (!) the first line in the .install file!

pkgname='tor-browser-en'
pkgver='2.2.35_rc12'
_realpkgver='2.2.35-12-dev'
_language='en-US'
pkgrel='1'
pkgdesc='Anonymous browsing using firefox and tor'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-runtime')
optdepends=('zenity: simple dialog boxes'
			'kdialog: KDE dialog boxes'
			'notify-send: Gnome dialog boxes')
install="${pkgname}.install"
source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz"
		"https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz.asc"
		"${pkgname}.desktop"
		"${pkgname}.png"
		"${pkgname}.sh")
if [[ "$CARCH" == 'x86_64' ]]; then
	sha256sums=('e01bc289b5cb776ebe6b71d0586a78e83d3425c6ed146bc6bd07b65f30675fad'
				'f93786ca8561480d5394241dfd733731a3e7ba40528e3b9c6e2af539ce5e95a7')
else
	sha256sums=('1afc4d8682da1753ba1557271cf23840b442efd6836ff228b43c3e1bde412cfa'
				'6def4d31c532f238b20c5b042e40b2b7d41ca46e4da3b251196ccbe39132a7a4')
fi
sha256sums+=('2217f011197329019ae3d282d95623e0230f8f7a3a604290744280530cf1698a'
			'17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			'7fc947b8ae1483c5abb4545f6e26b315ff1fb16eb7ae8837f7afb10a9277383b')
noextract=("tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")

package() {
	cd "${srcdir}"

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.sh
	sed -i "s/REPL_VERSION/${_realpkgver}/g"	${pkgname}.sh
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.sh

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.desktop
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.desktop
	sed -i "s/REPL_COMMENT/${pkgdesc}/g"		${pkgname}.desktop

	install -Dm 644 ${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 ${pkgname}.png		${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 ${pkgname}.sh		${pkgdir}/usr/bin/${pkgname}

	install -Dm 644 tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz ${pkgdir}/opt/${pkgname}/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz
}

# vim:set ts=2 sw=2 et:
