# Maintainer: Max Roder <maxroder@web.de>

pkgname='tor-browser-en'
pkgver='2.2.35_rc5'
_realpkgver='2.2.35-5-dev'
_language='en-US'
pkgrel='1'
pkgdesc='Lets you use Tor with Firefox (+ Torbutton, NoScript, HTTPS-Everywhere) without the need to configure anything. English version.'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-environment' 'xulrunner')
install='tor-browser-en.install'
if [[ "$CARCH" == 'i686' ]]; then
	source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")
	sha256sums=('bcbe140a0966e834fd26c634b71633705ceecde5aecd25c16f3afbb0b061823b')
else
	source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")
	sha256sums=('88201b882ead6ff7b80b3874f02f413b76d5b7572f14876075f1ea716efb37dc')
fi
source+=("${pkgname}.desktop"
		"${pkgname}.png"
		"${pkgname}.sh")
sha256sums+=('61f7c6a656d7198192573bf84b6487bacdbe5d9bc97e84215e8009a005057545'
			'17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			'9a8e2ee31f83ae6145122bc40fdce81441b298dc10eb2605cbda37f80eee7978')
options=('!strip' 'emptydirs')

package() {
	cd ${srcdir}/tor-browser_en-US

	install -d ${pkgdir}/opt/${pkgname}/ ${pkgdir}/usr/bin/

	# Any way to use install here?
	cp -a * ${pkgdir}/opt/${pkgname}/

	# Other files
	install -Dm644 ../${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm644 ../${pkgname}.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm755 ../${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
}

# vim:set ts=2 sw=2 et:
