# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Allan McRae <allan@archlinux.org>
pkgname=gpaint
pkgver=0.3.3
pkgrel=5
pkgdesc="Paintbrush clone for Gnome"
arch=('x86_64' 'i686')
license=('GPL')
url="http://www.gnu.org/software/gpaint/"
depends=('libgnomeprintui')
source=(ftp://alpha.gnu.org/gnu/$pkgname/${pkgname}-2-${pkgver}.tar.gz)
md5sums=('037cc283d2cdd806dbdc462839bb3b72')

build() {
  cd ${pkgname}-2-${pkgver}
  sed -i "s#GTK_RESPONSE_DISCARD#GTK_RESPONSE_NO#" src/drawing.c

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-2-${pkgver}

  make DESTDIR=${pkgdir} install
  mv "$pkgdir/usr/bin/gpaint-2" "$pkgdir/usr/bin/gpaint"
  mkdir -p ${pkgdir}/usr/share/applications
  install -m644 ${srcdir}/${pkgname}-2-${pkgver}/gpaint.desktop \
    ${pkgdir}/usr/share/applications
  echo "Categories=Graphics;" >> ${pkgdir}/usr/share/applications/gpaint.desktop
  sed -i 's:Terminal=0:Terminal=false:' \
    "$pkgdir/usr/share/applications/gpaint.desktop"
  sed -i 's:gpaint-2:gpaint:' \
    "$pkgdir/usr/share/applications/gpaint.desktop"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
