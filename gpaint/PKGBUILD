# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=gpaint
pkgver=0.3.3
pkgrel=8
pkgdesc="Paintbrush clone for Gnome"
arch=('x86_64' 'i686')
license=('GPL')
depends=('libgnomeprintui')
url="http://www.gnu.org/software/gpaint/"
source=("ftp://alpha.gnu.org/gnu/$pkgname/$pkgname-2-$pkgver.tar.gz"
        'fill.patch::https://savannah.gnu.org/patch/download.php?file_id=19341')
sha256sums=('541706460610c8bd917225a0370762b66e378e756afa780e67677eeb0a2dd0eb'
            'd8600339f2a53e21c46e150e29817f1f999d8963859712d2f988b38687ebb2a5')

build() {
  cd "$pkgname-2-$pkgver"
  
  sed -i 's#GTK_RESPONSE_DISCARD#GTK_RESPONSE_NO#' src/drawing.c
  patch -p1 -i ../fill.patch
  #autoreconf -i
  ./configure --prefix=/usr
  make LDFLAGS+="-lm"
  echo "Categories=Graphics;" >> gpaint.desktop
  sed -i 's:Terminal=0:Terminal=false:' "gpaint.desktop"
  sed -i 's:gpaint-2:gpaint:' "gpaint.desktop"
}

package() {
  cd "$pkgname-2-$pkgver"

  make DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/bin/gpaint-2" "$pkgdir/usr/bin/gpaint"
  mkdir -p "$pkgdir/usr/share/applications"
  install -m644 "$srcdir/$pkgname-2-$pkgver/gpaint.desktop" \
    "$pkgdir/usr/share/applications"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
