# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-gettext
pkgver=0.18.1.1
pkgrel=1
arch=(any)
pkgdesc="GNU internationalization library (mingw-w64)"
depends=(mingw-w64-crt mingw-w64-libiconv mingw-w64-termcap)
makedepends=(mingw32-gcc)
options=(!strip !buildflags !libtool)
license=(GPL LGPL)
url="http://www.gnu.org/software/gettext/"
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-$pkgver.tar.gz"
        "gettext-0.18.1.1-tml.patch")
md5sums=('3dd55b952826d2b32f51308f2f91aa89'
         'daa03e4e4549fbab89b84889e748b355')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "$srcdir/gettext-$pkgver"
  
  patch -Np0 -i ${srcdir}/gettext-0.18.1.1-tml.patch

  # The libtool bundled with this package is too old for win64 support
  autoreconf -i --force
  
  unset LDFLAGS
  export CFLAGS="$CFLAGS -mms-bitfields"

  # Some build workarounds
  export gl_cv_func_memchr_works="yes"
  export ac_cv_func_strnlen_working="yes"

  cd gettext-runtime

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --disable-java \
      --disable-native-java \
      --disable-csharp \
      --enable-static \
      --enable-threads=win32 \
      --without-emacs \
      --disable-openmp
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/gettext-$pkgver/gettext-runtime/build-${_arch}"
    make DESTDIR=$pkgdir install

    ${_arch}-strip $pkgdir/usr/${_arch}/bin/*.exe
    ${_arch}-strip -x -g $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a
    rm -rf "$pkgdir/usr/${_arch}/share"
  done
}
