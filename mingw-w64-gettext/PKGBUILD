# Contributor: Filip Brcic <brcha@gna.org>
pkgname=mingw-w64-gettext
pkgver=0.19.2
pkgrel=1
arch=(any)
pkgdesc="GNU internationalization library (mingw-w64)"
depends=(mingw-w64-crt mingw-w64-libiconv mingw-w64-termcap mingw-w64-libunistring)
makedepends=(mingw-w64-gcc)
options=(!strip !buildflags staticlibs)
license=("GPL")
url="http://www.gnu.org/software/gettext"
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-${pkgver}.tar.gz"{,.sig})
md5sums=('6898a2fc5d711b17278a59cfbfc10bc5'
         'SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
	cd "${srcdir}/gettext-${pkgver}"

	export CFLAGS="-O2 -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4 -mms-bitfields"
	export CXXFLAGS="-O2 -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
	unset LDFLAGS
	
	for _arch in ${_architectures}; do
		mkdir -p build-${_arch} && pushd build-${_arch}
		../configure \
			--prefix=/usr/${_arch} \
			--host=${_arch} \
			--target=${_arch} \
			--build=${CHOST} \
			--disable-java \
			--disable-native-java \
			--disable-csharp \
			--enable-static \
			--enable-threads=win32 \
			--without-emacs
		make
		popd
	done
}

package() {
	for _arch in ${_architectures}; do
		cd "${srcdir}/${pkgname#mingw-w64-}-$pkgver/build-${_arch}"
		make DESTDIR="$pkgdir" install
		find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
		find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
		find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
		rm "$pkgdir/usr/${_arch}/bin/"{autopoint,gettext.sh,gettextize}
		rm -r "$pkgdir/usr/${_arch}/lib/gettext"
		rm -r "$pkgdir/usr/${_arch}/share"
	done
}
