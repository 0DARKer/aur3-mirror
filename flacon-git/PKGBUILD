# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=flacon-git
pkgver=v1.0.1.1.gbede0aa
pkgrel=1
pkgdesc="Extracts individual tracks from one big audio file containing the entire album of music and saves them as separate audio files. (Git Version)"
arch=('x86_64' 'i686')
url="http://flacon.github.io/"
license=('LGPL2.1')
depends=('qt4' 'uchardet' 'shntool')
makedepends=('cmake' 'git')
optdepends=('faac: For AAC support'
            'flac: For FLAC support'
            'lame: For MP3 support'
            'mac: For APE support'
            'mp3gain: For MP3 Replay Gain support'
            'vorbis-tools: For OGG support'
            'ttaenc: For TrueAudio support'
            'vorbisgain: For OGG Replay Gain support'
            'wavpack: For WavPack support')
conflicts=('flacon')
provides=('flacon')
install=flacon.install
source=('git+https://github.com/flacon/flacon.git')
md5sums=('SKIP')
_gitname="flacon"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  cd "${_gitname}"
  # tmpdir not found patch
  sed '/setDefaultValue(Encoder_TmpDir,/c\    setDefaultValue(Encoder_TmpDir, "/tmp");' -i settings.cpp

  # capitalise Music folder patch
  sed '/setDefaultValue(OutFiles_Directory,/c\    setDefaultValue(OutFiles_Directory, "~/Music");' -i settings.cpp
}

build() {
  cd "${_gitname}"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_TESTS=Yes
  make
}

check() {
  cd "${_gitname}/tests"
 ./flacon_test
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
