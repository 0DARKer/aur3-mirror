# Maintainer: Jozef Riha <jose1711 at gmail dot com>

pkgname=brickv
pkgver=2.0.9
pkgrel=2
pkgdesc="brick viewer is a testing/flashing tool for tinkerforge brick(let)s"
url="http://www.tinkerforge.com/"
license=("GPL2")
arch=('i686' 'x86_64')
depends=('pyqwt' 'python2-pyserial' 'brickd' 'python-pyqt4')
source=(https://github.com/Tinkerforge/${pkgname}/archive/v${pkgver}.zip)
md5sums=('01d69b0b76923aee68b738ad8d2ae7bc')

build() {
  cd $srcdir/$pkgname-$pkgver/src/
  python2 setup.py config
  python2 setup.py build
  cd brickv/plugin_system/plugins
  for i in */build_ui.py; do cd $(dirname $i); ./build_ui.py; cd ..; done
  cd ../..
  python2 ./build_ui.py
}

package() {
  cd $srcdir/$pkgname-$pkgver/src/
  python2 setup.py install --root=$pkgdir --prefix=/usr
}
