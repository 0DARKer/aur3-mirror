# Maintainer: ant32 <antreimer@gmail.com>
# Contributor: Filip Brcic <brcha@gna.org>
_qt_module=qtwebkit
pkgname=mingw-w64-qt5-webkit
pkgver=5.3.2
pkgrel=1
arch=(any)
pkgdesc="Classes for a WebKit2 based implementation and a new QML API (mingw-w64)"
depends=(mingw-w64-qt5-base mingw-w64-qt5-declarative mingw-w64-qt5-sensors
         mingw-w64-qt5-location mingw-w64-qt5-multimedia mingw-w64-angleproject
         mingw-w64-fontconfig mingw-w64-libpng mingw-w64-libjpeg-turbo mingw-w64-libxslt
         mingw-w64-zlib mingw-w64-icu mingw-w64-sqlite3 mingw-w64-libwebp)
makedepends=(mingw-w64-gcc python gperf ruby mingw-w64-pkg-config)
options=(!strip !buildflags staticlibs)
license=("custom, FDL, GPL3, LGPL")
url="http://qt-project.org"
_pkgfqn="${_qt_module}-opensource-src-${pkgver}"
groups=(mingw-w64-qt mingw-w64-qt5)
source=("http://download.qt-project.org/official_releases/qt/5.3/${pkgver}/submodules/${_pkgfqn}.tar.xz"
        qt5-qtwebkit-use-correct-icu-libs.patch
        qt5-qtwebkit-enable-pkgconfig-support-for-win32-target.patch
        qtwebkit-dont-use-bundled-angle-libraries.patch
        qt5-qtwebkit-dont-depend-on-icu.patch
        revert-qt4-unicode-removal.patch
        qt5-qtwebkit-workaround-build-breakage-after-svn-commit-136242.patch
        changeset_150223.diff
        qtwebkit-opensource-src-5.0.1-debuginfo.patch
        webkit-commit-151422.patch
        qt5-angle-platform.patch)
md5sums=('0497b5b46540848e0db6a9e219d756fb'
         '9a46d0b85a6e86a33ac6bb79cb6a6fe8'
         'ac574de962545d6a9e975b4db63c3e09'
         '767c94b065a9b0dbbcb8e66641bf8fb1'
         'a6ac2864b58c03d417b573012c920dc5'
         '4e374836f26853b4d82be0e87aa584a5'
         '8b60ddebf01ee04a0e1180e98cdd3d52'
         '872847bbe7c053eb140a01961d337841'
         '6aba6468efafb64943887079e258b799'
         'c36fe581e0f3b61cef19415782b257ca'
         '928f6fd40dd2210bc20f2b8e343c73ff')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/${_pkgfqn}"

  # The ICU libraries used for cross-compilation are named exactly the same as their native Linux counterpart
  patch -p0 -i "${srcdir}/qt5-qtwebkit-use-correct-icu-libs.patch"

  # By default the build system assumes that pkg-config isn't used for the win32 target
  # However, we're using it in the Fedora MinGW toolchain so make sure it is used automatically
  patch -p0 -i "${srcdir}/qt5-qtwebkit-enable-pkgconfig-support-for-win32-target.patch"

  # Make sure the bundled copy of the ANGLE library isn't used
  patch -p1 -i "${srcdir}/qtwebkit-dont-use-bundled-angle-libraries.patch"

  # qtwebkit depends on qtbase with ICU support.
  # As ICU support in qtbase also introduces over 20MB of additional dependency
  # bloat (and the qtbase libraries itself are only 13MB) the decision was made
  # to build qtbase without ICU support.
  # Make sure qtwebkit doesn't depend on a qtbase with ICU support any more
  patch -p1 -i "${srcdir}/qt5-qtwebkit-dont-depend-on-icu.patch"

  # Undo the removal of the old Qt4 based unicode support
  # as we want to use it instead of the bloated ICU
  patch -p1 -R -i "${srcdir}/revert-qt4-unicode-removal.patch"

  # WebKit svn commit 136242 implemented a split into QtWebKit and QtWebKitWidgets
  # Due to this change a static library named WebKit1.a is created first.
  # After this a shared library is created named Qt5WebKit.dll which contains
  # the contents of this static library and some other object files.
  # However, various symbols in the static library are expected to be exported
  # in the Qt5WebKit.dll shared library. As static libraries normally don't care
  # about exported symbols (everything is exported after all) the decoration
  # attribute Q_DECL_EXPORT won't be set.
  # This results in undefined references when trying to link the QtWebKitWidgets
  # shared library (which depends on various symbols which aren't exported properly
  # in the Qt5WebKit.dll shared library)
  patch -p0 -i "${srcdir}/qt5-qtwebkit-workaround-build-breakage-after-svn-commit-136242.patch"

  # Fix detection of native tools which started to fail as of QtWebkit 5.1.0
  # Caused by upstream commit 150223, so revert it for now
  patch -p1 -R -i "${srcdir}/changeset_150223.diff"

  # smaller debuginfo s/-g/-g1/ (debian uses -gstabs) to avoid 4gb size limit
  patch -p1 -i "${srcdir}/qtwebkit-opensource-src-5.0.1-debuginfo.patch"

  # Revert commit 151422 to fix a build failure which happens because we're not using ICU
  patch -p1 -R -i "${srcdir}/webkit-commit-151422.patch"

  # platform patch thanks to luspi
  patch -p0 -i "${srcdir}/qt5-angle-platform.patch"

  # Make sure the bundled copy of the ANGLE libraries isn't used
  rm -rf Source/ThirdParty/ANGLE
}

build() {
  cd "${srcdir}/${_pkgfqn}"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-qmake-qt5 QMAKE_CXXFLAGS+=-Wno-c++0x-compat ../WebKit.pro
    make
    popd
  done
}

package() {
  cd "${srcdir}/${_pkgfqn}"
  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make INSTALL_ROOT="${pkgdir}" install

    # The .dll's are installed in both bindir and libdir
    # One copy of the .dll's is sufficient
    rm -f "${pkgdir}/usr/${_arch}/lib/"*.dll
		find "${pkgdir}/usr/${_arch}" -name "*.exe" -o -name "*.bat" -o -name "*.def" -o -name "*.exp" -o -name '*.prl' | xargs -rtl1 rm
		find "${pkgdir}/usr/${_arch}" -name "*.dll" -exec ${_arch}-strip --strip-unneeded {} \;
		find "${pkgdir}/usr/${_arch}" -name "*.a" -o -name "*.dll" | xargs -rtl1 ${_arch}-strip -g
    popd
  done
}
