# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: wido <widowild [at] myopera [dot] com>

pkgname=sat-xmpp
_realname=sat
pkgver=0.4.1
pkgrel=1
pkgdesc="Multi-frontends multi-purposes XMPP client"
url="http://salut-a-toi.org/"
arch=('any')
depends=('python2-dbus' 'python2-lxml' 'mutagen' 'python2-imaging' 'pyfeed' 'twisted' 'python2-wokkel' 'xe' 'python2-zope-interface')
optdepends=('python2-gobject2: for the jp frontend'
            'python2-progressbar: for the jp frontend'
            'python2-urwid-satext>=0.3.0: for the primitivus frontend'
            'wxpython: for the wix frontend'
            'sat_media: for the wix frontend')
makedepends=('python2')
license=('AGPL3')
source=("ftp://ftp.goffi.org/$_realname/$_realname-${pkgver}.tar.bz2")
md5sums=('fa82f67c9dba4144926e43e2c47b5e1d')
options=('!strip')

build() {
  cd "$srcdir/$_realname-$pkgver"
  python2 setup.py build
}

package(){
  cd "$srcdir/$_realname-$pkgver"
  SAT_INSTALL=nopreinstall python2 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=1

  # Patch for TAP_PATH
  sed -i '/^TAP_PATH/c TAP_PATH="/usr/lib/python2.7/site-packages/sat/"' "$pkgdir/usr/lib/python2.7/site-packages/sat/sat.sh"

  # Fix symlink
  rm "$pkgdir/usr/bin/sat"
  ln -s '/usr/lib/python2.7/site-packages/sat/sat.sh' "$pkgdir/usr/bin/sat"
}
