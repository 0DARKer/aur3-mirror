
# Maintainer : mdevlamynck <matthias.devlamynck@mailoo.org>

pkgname=my-little-investigations
_gitrepo=https://github.com/mdevlamynck/${pkgname}.git
pkgver=1.0.4
pkgrel=2
pkgdesc="An Ace Attorney like game based on My Little Pony : Friendship is Magic"
arch=('i686' 'x86_64')
url="http://www.equestriandreamers.com/"
license=('MIT')
groups=('')
replaces=('my-little-investigation')
depends=('sdl2' 'sdl2_image' 'sdl2_ttf' 'sdl2_mixer' 'ffmpeg' 'crypto++') # 'xdelta3')
makedepends=('cmake')
source=(
	"git+${_gitrepo}"
	"http://www.art-software.fr/files/MLI/common_data-${pkgver}.tar.xz"
)
md5sums=(
	'SKIP'
	'501917635959f3df6e0bb8f669f04dcd'
)

build () {
	cd "${srcdir}/${pkgname}"
	git checkout ${pkgver}
	mkdir -p build
	cd build
	cmake .. -DCMAKE_BUILD_TYPE=Release
	make MyLittleInvestigations
}

package() {
	cd "${srcdir}/${pkgname}/build"
	install -D -m 755 MyLittleInvestigations "${pkgdir}/usr/bin/MyLittleInvestigations"

	cd "${srcdir}/${pkgname}/linux"
	install -D -m 644 MyLittleInvestigations.desktop "${pkgdir}/usr/share/applications/MyLittleInvestigations.desktop"

	cd "${srcdir}/${pkgname}"
	install -D -m 644 mli.ico "${pkgdir}/usr/share/MyLittleInvestigations/mli.ico"

	for file in $(find "${srcdir}/common_data" -type f)
	do
		install -D -m 644 "${file}" "${pkgdir}/usr/share/MyLittleInvestigations/${file#"${srcdir}/common_data"}"
	done

	echo "${pkgver}" > "${pkgdir}/usr/share/MyLittleInvestigations/version"
}
