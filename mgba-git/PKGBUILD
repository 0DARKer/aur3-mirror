# Maintainer: Duck Hunt <vaporeon@tfwno.gf>

pkgbase=mgba-git
pkgname=('libmgba-git' 'mgba-qt-git' 'mgba-sdl-git')
pkgver=r1530.1e60a30
pkgrel=1
pkgdesc="A Nintendo Gameboy Advance Emulator focusing on both speed and accuracy"
arch=('i686' 'x86_64')
url="https://endrift.com/mgba/"
license=('MPL2')
makedepends=('git' 'cmake' 'qt5-base' 'sdl2' 'zlib' 'libpng' 'libzip' 'libedit' 'ffmpeg' 'imagemagick')
source=("git+https://github.com/mgba-emu/mgba.git"
        "mgba.desktop")
md5sums=('SKIP'
         'd78dff8622177747f0c80bf2847500db')

pkgver() {
  cd "mgba"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/mgba/res/
  convert mgba-1024.png -resize 256x256 mgba-256.png
}

build() {
  cd "${srcdir}"/mgba
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_libmgba-git() {
  depends=('zlib' 'libpng' 'libzip' 'libedit' 'ffmpeg' 'imagemagick')
  conflicts=('libmgba')
  cd "${srcdir}"/mgba/build
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/bin
}

package_mgba-qt-git() {
  depends=("libmgba-git=${pkgver}" 'qt5-base' 'sdl2')
  conflicts=('mgba-qt')
  cd "${srcdir}"/mgba/build
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/mgba/res/mgba-256.png "${pkgdir}"/usr/share/pixmaps/mgba.png
  desktop-file-install "${srcdir}"/mgba.desktop --dir "${pkgdir}"/usr/share/applications/
  rm "${pkgdir}"/usr/bin/mgba
  rm -rf "${pkgdir}"/usr/lib
}

package_mgba-sdl-git() {
  depends=("libmgba-git=${pkgver}" 'sdl2')
  conflicts=('mgba-sdl')
  cd "${srcdir}"/mgba/build
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}"/usr/bin/mgba-qt
  rm -rf "${pkgdir}"/usr/lib
}
