# Maintainer: Duck Hunt <vaporeon@tfwno.gf>

pkgname=mgba-git
pkgver=r1453.fa1b74a
pkgrel=1
pkgdesc="A Nintendo Gameboy Advance Emulator focusing on both speed and accuracy"
arch=('i686' 'x86_64')
url="https://endrift.com/mgba/"
license=('MPL2')
makedepends=('git' 'cmake' 'imagemagick')
depends=('qt5-base' 'qt5-multimedia' 'sdl2' 'zlib' 'libpng' 'libzip' 'libedit' 'ffmpeg' 'imagemagick')
conflicts=('mgba')
source=("git+https://github.com/mgba-emu/mgba.git"
        "mgba.desktop")
md5sums=('SKIP'
         'd78dff8622177747f0c80bf2847500db')

pkgver() {
  cd "mgba"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}"/mgba/res/
  convert mgba-1024.png -resize 256x256 mgba-256.png
}

build() {
  cd "${srcdir}"/mgba
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/mgba/build
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}"/mgba/res/mgba-256.png "${pkgdir}"/usr/share/pixmaps/mgba.png
  install -Dm 644 "${srcdir}"/mgba/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  desktop-file-install "${srcdir}"/mgba.desktop --dir "$pkgdir"/usr/share/applications/
}
