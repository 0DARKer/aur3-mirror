# Maintainer: Duck Hunt <vaporeon@tfwno.gf>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgbase=mgba-git
pkgname=('libmgba-git' 'mgba-sdl-git' 'mgba-qt-git' 'libretro-mgba-git')
pkgver=r1843.513b9fb
pkgrel=1
arch=('i686' 'x86_64')
url="https://endrift.com/mgba/"
license=('custom:MPL2')
makedepends=('git' 'cmake' 'qt5-base' 'qt5-multimedia' 'sdl2' 'zlib' 'libpng'
             'libzip' 'libedit' 'ffmpeg' 'imagemagick' 'desktop-file-utils')
source=("git+https://github.com/mgba-emu/mgba.git"
        "mgba.desktop"
        "https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mgba_libretro.info")
sha1sums=('SKIP'
          '5f852ec91eaaf146482d5b55d91489664d2d91c3'
          '1c0dfc2fe9b269ea567c0a5332a043eb3f5b5d17')
pkgver() {
  cd mgba
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build && mkdir build
  else
    mkdir build
  fi

  cd mgba/res
  convert mgba-1024.png -resize 256x256 mgba-256.png
}

build() {
  cd build
  sed -i 's/BUILD_LIBRETRO OFF/BUILD_LIBRETRO ON/' "$srcdir"/mgba/CMakeLists.txt
  cmake "$srcdir"/mgba -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_libmgba-git() {
  pkgdesc='Shared library of mGBA'
  depends=('zlib' 'libpng' 'libzip' 'libedit' 'ffmpeg' 'imagemagick')
  conflicts=('libmgba')
  provides=('libmgba')

  cmake -DCOMPONENT=libmgba mgba -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -P build/cmake_install.cmake

  install -Dm644 "$srcdir"/mgba/LICENSE "$pkgdir"/usr/share/licenses/libmgba/LICENSE
}

package_mgba-sdl-git() {
  pkgdesc='A Nintendo Gameboy Advance Emulator focusing on both speed and accuracy'
  depends=('libmgba-git' 'sdl2')
  conflicts=('mgba-sdl')
  provides=('mgba-sdl')

  cmake -DCOMPONENT=mgba-sdl mgba -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -P build/cmake_install.cmake

  install -d "$pkgdir"/usr/share/licenses/mgba-sdl
  ln -s /usr/share/licenses/libmgba/LICENSE "$pkgdir"/usr/share/licenses/mgba-sdl/LICENSE
}

package_mgba-qt-git() {
  pkgdesc='A Nintendo Gameboy Advance Emulator focusing on both speed and accuracy - Qt5 UI'
  depends=('libmgba-git' 'qt5-base' 'qt5-multimedia' 'sdl2')
  conflicts=('mgba-qt')
  provides=('mgba-qt')

  cmake -DCOMPONENT=mgba-qt mgba -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -P build/cmake_install.cmake

  install -Dm644 "$srcdir"/mgba/res/mgba-256.png "$pkgdir"/usr/share/pixmaps/mgba.png
  desktop-file-install "$srcdir"/mgba.desktop --dir "$pkgdir"/usr/share/applications/
  install -d "$pkgdir"/usr/share/licenses/mgba-qt
  ln -s /usr/share/licenses/libmgba/LICENSE "$pkgdir"/usr/share/licenses/mgba-qt/LICENSE
}

package_libretro-mgba-git() {
  pkgdesc='A Nintendo Gameboy Advance Emulator focusing on both speed and accuracy - Libretro Core'
  depends=('libmgba-git')
  conflicts=('libretro-mgba')
  provides=('libretro-mgba')
  install -Dm644 "$srcdir"/build/mgba_libretro.so "$pkgdir"/usr/lib/libretro/mgba_libretro.so
  install -Dm644 "$srcdir"/mgba_libretro.info "$pkgdir"/usr/lib/libretro/mgba_libretro.info
  install -d "$pkgdir"/usr/share/licenses/libretro-mgba
  ln -s /usr/share/licenses/libmgba/LICENSE "$pkgdir"/usr/share/licenses/libretro-mgba/LICENSE
}
