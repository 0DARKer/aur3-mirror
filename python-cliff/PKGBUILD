# Maintainer: Carl George < arch at cgtx.us >

_module="cliff"
pkgname=("python-${_module}" "python2-${_module}")
pkgver="1.12.0"
pkgrel="1"
pkgdesc="Command Line Interface Formulation Framework"
arch=("any")
url="https://github.com/openstack/cliff"
license=("Apache")
makedepends=("python-pbr>=0.6" "python-setuptools" "python2-pbr>=0.6" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('7609abc1789522fc4e228ea5567a9fd1db3963850396178adca3332cdd9c929a')

prepare() {
    sed -i '/argparse/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
    sed -i '/pbr/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-cliff() {
    depends=("python-cmd2>=0.6.7"
             "python-prettytable>=0.7"
             "python-pyparsing>=2.0.1"
             "python-six>=1.9.0"
             "python-stevedore>=1.3.0")
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-cliff() {
    depends=("python2-cmd2>=0.6.7"
             "python2-prettytable>=0.7"
             "python2-pyparsing>=2.0.1"
             "python2-six>=1.9.0"
             "python2-stevedore>=1.3.0")
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
