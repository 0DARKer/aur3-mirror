# Maintainer : Luca Bennati <lucak3 at gmail dot com>

pkgname=nvidia-pf
pkgver=275.09.07
_kernver='2.6.39-pf'
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26-pf."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-pf>=2.6.39' 'kernel26-pf<2.6.40' "nvidia-utils=${pkgver}")
makedepends=('kernel26-pf>=2.6.39' 'kernel26-pf<2.6.40')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('d779057af3a2f96a05ddda201606618b')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums=('f75df248f7bf32199ed88db2f080cbfa')
fi

build() {
	cd "$srcdir"
	sh "${_pkg}.run" --extract-only
	cd "${_pkg}/kernel"
 	make SYSSRC="/lib/modules/${_kernver}/build" module
}

package() {
	install -D -m644 "$srcdir/${_pkg}/kernel/nvidia.ko" \
		"$pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
	install -d -m755 "$pkgdir/etc/modprobe.d"
	echo "blacklist nouveau" >> "$pkgdir/etc/modprobe.d/nouveau_blacklist-pf.conf"
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "$startdir/nvidia.install"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
	install -D -m644 "${srcdir}/${_pkg}/LICENSE" \
		        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
