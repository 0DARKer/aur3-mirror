# maintainer: karabaja4 <karabaja4@archlinux.us>

pkgname=xfdesktop-standalone
pkgver=4.6.2
pkgrel=1
pkgdesc="A desktop manager for Xfce - standalone version to use with xfwm4-standalone"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
depends=('libxfce4menu>=4.6.2' 'hicolor-icon-theme' 'libxfcegui4>=4.6.4' 'libwnck>=2.30.0' 'xfconf>=4.6.2')
makedepends=('pkgconfig' 'intltool')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
options=('!libtool')
install=xfdesktop.install
source=(http://archive.xfce.org/src/xfce/xfdesktop/4.6/xfdesktop-${pkgver}.tar.bz2)
md5sums=('e800ea9fee7a5c5eaf2ae96e23a83e3a')

build() {
  cd "${srcdir}/xfdesktop-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static --disable-desktop-icons \
    --disable-file-icons --disable-thunarx --disable-exo \
    --disable-desktop-menu-dir-monitor --disable-panel-plugin || return 1
  make || return 1
}

package() {
  cd "${srcdir}/xfdesktop-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
