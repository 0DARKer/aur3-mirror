pkgname=capstone-git
pkgver=20141114.1818.02cafeb
pkgrel=1
pkgdesc="A lightweight multi-platform, multi-architecture disassembly framework."
arch=('i686' 'x86_64')
url="http://www.capstone-engine.org/"
license=('BSD')
makedepends=('git')
provides=('capstone')
conflicts=('capstone')

source=("$pkgname"::"git://github.com/aquynh/capstone.git")
md5sums=('SKIP')

pkgver () {
  cd "${srcdir}/${pkgname}"
  _date=`date +"%Y%m%d"`
  echo "$_date.$(git rev-list --count next).$(git rev-parse --short next)"
}


build() {
  #make yaourt happy. (Seriously, this needs to die)
  ABSBUILDDIR=${BUILDDIR}
  unset BUILDDIR

  cd ${srcdir}/${pkgname}
  make

  cd "./bindings/python"
  #mkdir ./build/{py2,py3}
  python2 setup.py build -b ./build/py2
  python setup.py build -b ./build/py3
}

check() {
  cd "${srcdir}/${pkgname}"
  make check
  cd "./bindings/python"
  make check
}

package() {
  cd ${srcdir}/${pkgname}
  make DESTDIR="$pkgdir/" install
  install -d -m755 ${pkgdir}/usr/share/licenses/capstone-git
  install LICENSE.TXT ${pkgdir}/usr/share/licenses/capstone-git/LICENSE.TXT

  cd ${srcdir}/${pkgname}/bindings/python
  python2 setup.py install --root ${pkgdir}
  python setup.py install --root ${pkgdir}
}
# vim:set ts=2 sw=2 et:
