# Maintainer: Justin Dray <justin at dray dot be>
# Contributor: Slash <demodevil5 at yahoo dot com>
# Contributor: blades <blades at gecko dot org dot uk>
# Contributor: jonkristian <hello at jonkristian dot no>

pkgname=headphones
pkgver=0.5.3
pkgrel=1
pkgdesc="Automatic music downloader for SABnzbd"
arch=('any')
url="http://github.com/rembo10/headphones"
license=('GPL3')
depends=('python2')
conflicts=('headphones-git')
backup=('etc/conf.d/headphones')
install='headphones.install'
source=("https://github.com/rembo10/headphones/archive/v${pkgver}.zip"
	'headphones.service'
	'headphones.conf')
sha256sums=('06c2e7411f50cfd7e60cddf649f81067c54ed0ac5985116e999357a4cb876902'
            '76d73e0bacd6accc76c9ee62a69aba221cdf7c4fb7731480acb3e46fa98ce83b'
            '7ff93306b8952b9626b96f3da05d3e8fff0fd5e1dfed71dd34b93e10d2107d7e')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	find ./ -iname '*.py' -exec sed -i 's/#!\/usr\/bin\/env python.*$/#!\/usr\/bin\/env python2/;s/#!\/usr\/bin\/python.*/#!\/usr\/bin\/env python2/' {} \;
	sed -i 's/#! \/usr\/bin\/env python/#! \/usr\/bin\/env python2/g' lib/cherrypy/cherryd
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -d ${pkgdir}/opt/
	cp -r "${srcdir}/headphones-${pkgver}" "${pkgdir}/opt/headphones"

	install -D -m644 "${srcdir}/headphones.conf" "${pkgdir}/etc/conf.d/headphones"
	install -D -m644 "${srcdir}/headphones.service" "${pkgdir}/usr/lib/systemd/system/headphones.service"
}

# vim:set ts=2 sw=2 et:
