# Maintainer: Chris Chapman <chris dot chapman at aggiemail dot usu dot edu>

pkgname=eclipse-pde-sdk
pkgver=3.5.2
pkgrel=1
pkgdesc="Eclipse Plugin Development Environment for Eclipse"
arch=('i686' 'x86_64')
url="http://www.eclipse.org/pde/"
license=('EPL')
depends=('eclipse>=${pkgver}')
source=(http://mirror.cc.columbia.edu/pub/software/eclipse/eclipse/downloads/drops/R-3.5.2-201002111343/eclipse-PDE-SDK-${pkgver}.zip)
md5sums=('7dcdbfa3cec43d5b22f23620ffd93ff9')

build() {
    _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

    cd ${srcdir}/eclipse

    # Features
    find features -type f | while read _feature ; do
        if [[ ${_feature} =~ (.*\.jar$) ]] ; then
            install -dm755 ${_dest}/${_feature%*.jar}
            cd ${_dest}/${_feature} || return 1
            jar xf ${srcdir}/${_feature} || return 1
        else
            install -Dm644 ${_feature} ${_dest}/${_feature}
        fi
    done

    # Plugins
    find plugins -type f | while read _plugin ; do
        install -Dm644 ${_plugin} ${_dest}/${_plugin}
    done
}
