# Maintainer: Bram Schoenmakers <me@bramschoenmakers.nl>
# Contributor: henning mueller <henning@orgizm.net>

# If you'd like to have documentation, please set the variable below to 1.
# This implies installing pandoc, which in turn depends on many Haskell packages.
_havedocs=0

pkgname=bup
pkgver=0.24
pkgrel=1
_pkgrev=234d02f
pkgdesc="Backup tool using git pack files."
arch=('i686' 'x86_64')
url="http://github.com/apenwarr/bup"
license=('GPL')
depends=('python2' 'python-fuse' 'par2cmdline')
[ $_havedocs = 1 ] && makedepends=('haskell-pandoc')
source=("https://nodeload.github.com/apenwarr/$pkgname/zipball/$pkgname-$pkgver")
md5sums=('1516f71c60c3a6bf567ebda8ff2c06db')

build() {
  cd $srcdir/apenwarr-$pkgname-$_pkgrev

  find . -name \*.py | xargs sed -i 's:env python:env python2:'
  sed -i 's:PYTHON=python:PYTHON=python2:' Makefile

  make
}

package() {
  cd $srcdir/apenwarr-$pkgname-$_pkgrev
  make DESTDIR=$pkgdir install
}
