# Maintainer: Bram Schoenmakers <me@bramschoenmakers.nl>
# Contributor: henning mueller <henning@orgizm.net>

# If you'd like to have documentation, please set the variable below to 1.
# This implies installing pandoc, which in turn depends on many Haskell packages.
_havedocs=0

pkgname=bup
pkgver=0.25rc1
_pkgver=0.25-rc1
pkgrel=2
pkgdesc="Backup tool using git pack files."
arch=('i686' 'x86_64')
url="http://github.com/apenwarr/bup"
license=('GPL')
depends=('python2-fuse' 'par2cmdline')
[ $_havedocs = 1 ] && makedepends=('haskell-pandoc')
source=("https://github.com/apenwarr/${pkgname}/archive/${pkgname}-${_pkgver}.tar.gz")

build() {
  cd "${srcdir}/${pkgname}-${pkgname}-${_pkgver}"

  find . -name \*.py | xargs sed -i 's:env python:env python2:'
  sed -i 's:PYTHON=python:PYTHON=python2:' Makefile

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgname}-${_pkgver}"
  make DESTDIR=$pkgdir install
}
md5sums=('9d9af10900d84e2951aa64631ff4860d')
