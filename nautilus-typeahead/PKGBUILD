# Maintainer: Joschka Thurner <mail@joschkathurner.de>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus-typeahead
pkgver=3.14.0
pkgrel=1
pkgdesc="GNOME file manager with interactive-search patch from Ubuntu"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils gnome-icon-theme dconf
         libtracker-sparql libnotify nautilus-sendto 'libnautilus-extension=3.14.0')
makedepends=(intltool gobject-introspection python python2 packagekit)
conflicts=('nautilus')
provides=('nautilus=3.14.0')
install=nautilus-typeahead.install
url="http://www.gnome.org"
source=(http://download.gnome.org/sources/nautilus/${pkgver%.*}/nautilus-$pkgver.tar.xz
        git-fixes.patch	interactive_search.patch)
sha256sums=('6f909ed9afa9abffe489b80b57888bd7bc7acea2cf48717c54362bd3b6aee0d4'
            '5b9303d8b2d779d5522c8a16f206233d46dd1fbc17409b8b9b0292be2177b843'
	    '9bce1512772a1fcd266a1b3e696d6189cb1137390842b06dd30809569a129557')

prepare() {
  cd nautilus-$pkgver
  patch -Np1 -i ../git-fixes.patch
  # bug: https://bugs.launchpad.net/ubuntu/+source/nautilus/+bug/1164016
  # source: https://bazaar.launchpad.net/~ubuntu-branches/ubuntu/trusty/nautilus/trusty/files/head:/debian/patches/
  patch -Np1 -i ../interactive_search.patch
}

build() {
  cd nautilus-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-update-mimedb \
      --disable-schemas-compile \
      --without-libnautilus-extension
  make
}


package() {
  cd nautilus-$pkgver
  make DESTDIR="$pkgdir" install
  # Remove libnautilus-extension
  rm -r "$pkgdir"/usr/include
  rm -r "$pkgdir"/usr/lib/{girepository-1.0,pkgconfig}
  rm -r "$pkgdir"/usr/lib/libnautilus-extension.so*
  rm -r "$pkgdir"/usr/share/{gir-1.0,gtk-doc}
}

