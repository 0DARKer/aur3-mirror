# Maintainer: Joschka Thurner <mail@joschkathurner.de>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus-typeahead
pkgver=3.12.0
pkgrel=1
pkgdesc="GNOME file manager with interactive-search patch from ubuntu"
arch=(i686 x86_64)
license=(GPL)
depends=(libexif gnome-desktop exempi gvfs desktop-file-utils gnome-icon-theme dconf
         libtracker-sparql libnotify nautilus-sendto 'libnautilus-extension=3.12.0')
makedepends=(intltool gobject-introspection python)
conflicts=('nautilus')
provides=('nautilus=3.12.0')
install=nautilus-typeahead.install
groups=(gnome)
url="http://www.gnome.org"
options=('!emptydirs')
source=(http://download.gnome.org/sources/nautilus/${pkgver%.*}/nautilus-$pkgver.tar.xz
        interactive_search.patch)
sha256sums=('c19af20ab1fadf04bebee7ec8d3375c5efbe27bb6e0b7d3a60369523baf5e4df'
            '9bce1512772a1fcd266a1b3e696d6189cb1137390842b06dd30809569a129557')

prepare() {
  cd nautilus-$pkgver
  # bug: https://bugs.launchpad.net/ubuntu/+source/nautilus/+bug/1164016
  # source: https://bazaar.launchpad.net/~ubuntu-branches/ubuntu/trusty/nautilus/trusty/files/head:/debian/patches/
  patch -Np1 -i ../interactive_search.patch
}

build() {
  cd nautilus-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-schemas-compile
  make
}

package() {
  cd nautilus-$pkgver
  make DESTDIR="$pkgdir" install

### Remove libnautilus-extension
  rm -r "$pkgdir"/usr/include
  rm -r "$pkgdir"/usr/lib/{girepository-1.0,pkgconfig}
  rm -r "$pkgdir"/usr/lib/libnautilus-extension.so*
  rm -r "$pkgdir"/usr/share/{gir-1.0,gtk-doc}
}
