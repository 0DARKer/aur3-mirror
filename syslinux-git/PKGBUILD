# Maintainer : Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_pkgname="syslinux"
pkgname="${_pkgname}-git"

pkgver=20111216
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Collection of boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE - GIT Development version"
url="http://syslinux.zytor.com/"
license=('GPL2')
makedepends=('git' 'nasm' 'curl')
depends=('perl' 'glibc')
optdepends=('perl-passwd-md5: For md5pass'
			'perl-digest-sha1: For sha1pass'
			'mtools: For mkdiskimage and syslinux'
			'dosfstools: For FAT filesystems support')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

backup=('boot/syslinux/syslinux.cfg')
install="${_pkgname}.install"

source=('Syslinux-Do-not-build-dos-and-windows-executables.patch'
		'syslinux-install_update'
		'syslinux.cfg')

sha256sums=('6d2e510e677ff8063df7400b80413ee4429130927734440a6dcef3843dc93c93'
			'49573a3b65362326c8b1e9bfa49e83225e0bf9d3710454de4070abc9481c614c'
			'b4ba8f9daa67ec35da6e05adb610469bc8c4facf488468c1ec3e5860ba75313d')

_gitroot="git://git.kernel.org/pub/scm/boot/syslinux/syslinux.git"
_gitname="${_pkgname}"

_update_git() {
	
	cd "${srcdir}"
	
	msg "Connecting to GIT server...."
	
	if [ -d "${srcdir}/${_gitname}/" ]
	then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout master
		git merge remotes/origin/master
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
	fi
	
	msg "GIT checkout done or server timeout"
	
	cd "${srcdir}/"
	
	## Download the installation and update script
	## This script is maintained at git://gist.github.com/772138.git
	# curl "https://gist.github.com/gists/772138/download" > "${srcdir}/syslinux_install_script.tar.gz"
	# bsdtar xf "${srcdir}/syslinux_install_script.tar.gz"
	
	# cd "${srcdir}"/gist*/
	# mv "${PWD}/syslinux-install_update" "${srcdir}/syslinux-install_update"
	# rm -rf "${srcdir}"/gist*/
	
}

build() {
	
	_update_git
	
	rm -rf "${srcdir}/${_gitname}_build" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build"
	
	# Do not try to build syslinux with our default LDFLAGS, it will fail
	unset LDFLAGS
	
	# Do not try to build the Windows or DOS installers
	patch -Np1 -i "${srcdir}/Syslinux-Do-not-build-dos-and-windows-executables.patch"
	
	# Fix FHS manpage path
	sed 's|/usr/man|$(DATADIR)/man|g' -i "${srcdir}/${_gitname}_build/mk/syslinux.mk" || true
	
	# Use python2 instead of python
	sed 's|/usr/bin/python|/usr/bin/env python2|g' -i "${srcdir}/${_gitname}_build/com32/cmenu"/*.py || true
	sed 's|/usr/bin/env python|/usr/bin/env python2|g' -i "${srcdir}/${_gitname}_build/com32/cmenu"/*.py || true
	sed 's|python |python2 |g' -i "${srcdir}/${_gitname}_build/com32/cmenu/Makefile" || true
	
	msg "Starting make..."
	make
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build"
	make INSTALLROOT="${pkgdir}" AUXDIR="/usr/lib/${_pkgname}" install
	
	# Install the default configuration
	install -D -m644 "${srcdir}/syslinux.cfg" "${pkgdir}/boot/syslinux/syslinux.cfg"
	
	# Install the installation and update script
	install -D -m755 "${srcdir}/syslinux-install_update" "${pkgdir}/usr/sbin/syslinux-install_update"
	
}
