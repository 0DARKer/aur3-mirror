# Maintainer: Keshav P R <skodabenz at rocketmail dot com>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=syslinux-git
_pkgname=syslinux
pkgver=20110122
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Collection of flexible boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE"
url="http://syslinux.zytor.com/"
license=('GPL2')
depends=('perl' 'glibc')
optdepends=('perl-passwd-md5:  For md5pass'
            'perl-digest-sha1: For sha1pass'
            'mtools:           For mkdiskimage and syslinux'
            'dosfstools:       For FAT filesystems support'
           )
makedepends=('git' 'nasm')
provides=(${_pkgname})
conflicts=(${_pkgname})

source=(0001-i915resolution-add-id-for-GM45-chipset.patch
        0002-mandir-is-at-usr-share.patch
        0003-Do-not-build-dos-and-windows-executables.patch
        0004-Force-python2-to-be-used.patch
        0005-Add-VERY_QUIET-option.patch
        0006-Hide-cursor-early.patch
       )

sha256sums=('1d061f3714150114e685d1b91a4750be6d696415e22bc0abc5322b7161e62d3b'
            '228ca175cb27eb2c22764b580b41ef78d48f546296738a677cf2abe0c52e6b32'
            '72d5781387717fa323623a995a259f9d6f6a133a71017ee28f575c4a7ba26e3d'
            '4c42dbcb9aa561ce92c5925024e552393349c90f7ece7d5fccb3aec3041b2b29'
            '821ccb173e2b92089923f8f2d9b2d5368376938d88d734191a6b0c4122f7ddd3'
            'e858f36bf93f0dba5542477c32352a3031c858c41b24a0e2b46268661a8935f7')
            
_gitroot="git://git.kernel.org/pub/scm/boot/syslinux/syslinux.git"
_gitname="${_pkgname}"

update_git() {
  
  cd "${srcdir}"
  
  msg "Connecting to GIT server...."
  
  if [ -d ${srcdir}/${_gitname}/ ]
  then
      cd ${srcdir}/${_gitname}/
      git reset --hard
      git fetch
      git checkout master
      git merge remotes/origin/master
      msg "The local GIT repo has been updated."
  else
      git clone ${_gitroot} ${_gitname}
  fi
  
  msg "GIT checkout done or server timeout"
  
}

build() {
  
  update_git
  
  cd "${srcdir}"
  
  rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  patch -p1 -i ${srcdir}/0001-i915resolution-add-id-for-GM45-chipset.patch
  patch -p1 -i ${srcdir}/0002-mandir-is-at-usr-share.patch
  patch -p1 -i ${srcdir}/0003-Do-not-build-dos-and-windows-executables.patch
  patch -p1 -i ${srcdir}/0004-Force-python2-to-be-used.patch
  # patch -p1 -i ${srcdir}/0005-Add-VERY_QUIET-option.patch
  # patch -p1 -i ${srcdir}/0006-Hide-cursor-early.patch

  # Do not try to build syslinux with our default LDFLAGS, it will fail
  unset LDFLAGS

  msg "Starting make..."
  make VERY_QUIET=1

}

package() {
  
  cd "${srcdir}/${_gitname}-build"
  make INSTALLROOT="${pkgdir}" AUXDIR=/usr/lib/syslinux install

}
