pkgname=arm-wince-binutils
pkgver=2.23.1
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (Windows CE)"
arch=(i686 x86_64)
url="http://www.gnu.org/software/binutils"
license=("GPL")
depends=(glibc)
source=("ftp://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.gz")
md5sums=('7a519f12859baa0282866b8e8a4d04f0')

_optimal_make_jobs() {
  if [ -r /proc/cpuinfo ]; then
    local core_count=$(grep -Fc processor /proc/cpuinfo)
  else
    local core_count=0
  fi
  if [ $core_count -gt 1 ]; then
    echo -n $[$core_count-1]
  else
    echo -n 1
  fi
}

build() {
  cd "${srcdir}/binutils-$pkgver"
  ./configure \
    --prefix=/usr \
    --target=arm-wince \
    --host=$CHOST \
    --build=$CHOST \
    --disable-nls
  make -j$(_optimal_make_jobs)
}

package()
{
  cd "${srcdir}/binutils-$pkgver"
  make DESTDIR=$pkgdir install
  rm -rf "$pkgdir/usr/lib"
  rm -rf "$pkgdir/usr/share/"{info,man}
}
