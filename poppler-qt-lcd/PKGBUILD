# $Id: PKGBUILD 36062 2009-04-19 20:15:39Z jgc $
# Maintainer: Paul Gideon Dann <pdgiddie_at_gmail.com>

pkgname=poppler-qt-lcd
pkgver=0.14.5
pkgrel=1
provides=('poppler-qt=$pkgver')
conflicts=('poppler-qt')
pkgdesc="Poppler Qt bindings, with patches to allow subpixel rendering via Cairo"
arch=(i686 x86_64)
license=('GPL')
depends=('qt>=4.7.0' 'cairo>=1.10.0' "poppler>=${pkgver}")
makedepends=('cmake' 'pkgconfig')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/poppler-${pkgver}.tar.gz
        0001-Cairo-backend-added-to-Qt4-wrapper.patch
        0002-Setting-default-Qt4-backend-to-Cairo.patch
        0003-Forcing-subpixel-rendering-in-Cairo-backend.patch
        0004-Don-t-build-the-main-poppler-library.patch)
md5sums=('f60038f22284876f021f29f1b1a0fe1c'
         'c41ba4a9ed27103e3057465f0309b82c'
         'dc05211551917d8f17568e0e08b43598'
         '9f3655aeb08f9748c9908c7b0cb53ae0'
         'a38a58573d8a1f4a5b79faa756c7a577')

build() {
  cd "${srcdir}/poppler-${pkgver}"

  for patch in ${srcdir}/*.patch; do
    patch -Np1 -i $patch || return 1
  done;

  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_QT4_TESTS=OFF -DENABLE_UTILS=OFF \
        -DENABLE_ABIWORD=OFF -DENABLE_CPP=OFF \
        -DWITH_Cairo=ON -DWITH_GLIB=OFF -DWITH_Qt3=OFF -DWITH_Qt4=ON ..
  make poppler-qt4 || return 1
  make DESTDIR="${pkgdir}" install || return 1
  msg "Removing unwanted pkg-config files"
  rm -f "${pkgdir}"/usr/lib/pkgconfig/poppler{,-splash,-cairo}.pc || return 1
}
