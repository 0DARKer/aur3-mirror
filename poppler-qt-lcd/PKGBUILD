# $Id: PKGBUILD 36062 2009-04-19 20:15:39Z jgc $
# Maintainer: Paul Gideon Dann <pdgiddie_at_gmail.com>

pkgname=poppler-qt-lcd
pkgver=0.16.3
pkgrel=1
provides=('poppler-qt=$pkgver')
conflicts=('poppler-qt')
pkgdesc="Poppler Qt bindings, with patches to allow subpixel rendering via Cairo"
arch=(i686 x86_64)
license=('GPL')
depends=('qt>=4.7.0' 'cairo>=1.10.0' "poppler>=${pkgver}")
makedepends=('cmake' 'pkgconfig')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/poppler-${pkgver}.tar.gz
        0001-Cairo-backend-added-to-Qt4-wrapper.patch
        0002-Setting-default-Qt4-backend-to-Cairo.patch
        0003-Forcing-subpixel-rendering-in-Cairo-backend.patch
        0004-Don-t-build-the-main-poppler-library.patch)
md5sums=('42227f1a1498089213a07533596b22f4'
         '07dd58455349337ed6a3c7ba5d6ac618'
         '31c57616cb9cac5040d12e5d0aefe279'
         '3a98149f3f4e53629a6a9a89d36816e0'
         'c066ce8d13e9b6afc05beeb1d4888119')

build() {
  cd "${srcdir}/poppler-${pkgver}"

  for patch in ${srcdir}/*.patch; do
    patch -Np1 -i $patch
  done;

  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_QT4_TESTS=OFF -DENABLE_UTILS=OFF \
        -DENABLE_ABIWORD=OFF -DENABLE_CPP=OFF \
        -DWITH_Cairo=ON -DWITH_GLIB=OFF -DWITH_Qt3=OFF -DWITH_Qt4=ON ..
  make poppler-qt4
  make DESTDIR="${pkgdir}" install
  msg "Removing unwanted pkg-config files"
  rm -f "${pkgdir}"/usr/lib/pkgconfig/poppler{,-splash,-cairo}.pc
}
