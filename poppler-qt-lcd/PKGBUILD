# $Id: PKGBUILD 36062 2009-04-19 20:15:39Z jgc $
# Maintainer: Paul Gideon Dann <pdgiddie_at_gmail.com>

pkgname=poppler-qt-lcd
pkgver=0.18.4
pkgrel=1
provides=('poppler-qt=$pkgver')
conflicts=('poppler-qt')
pkgdesc="Poppler Qt bindings, with patches to allow subpixel rendering via Cairo"
arch=(i686 x86_64)
license=('GPL')
depends=('qt>=4.7.0' 'cairo>=1.10.0' "poppler>=${pkgver}")
makedepends=('cmake' 'pkgconfig')
url="https://github.com/giddie/poppler-qt4-cairo-backend"
source=(http://poppler.freedesktop.org/poppler-${pkgver}.tar.gz
        0001-Cairo-backend-added-to-Qt4-wrapper.patch
        0002-Setting-default-Qt4-backend-to-Cairo.patch
        0003-Forcing-subpixel-rendering-in-Cairo-backend.patch
        0004-Don-t-build-the-main-poppler-library.patch)
md5sums=('12658f3308597e57f3faff538cc73baf'
         'db48d2e289a1c38904698c2810998d50'
         'a2c6403987cc813f490eefc5d6d4616c'
         '6ea76d66d53c71e83e75d4fa34933cbf'
         'a6c21fffaa22c111175c869e62c8ad0b')

build() {
  cd "${srcdir}/poppler-${pkgver}"

  for patch in ${srcdir}/*.patch; do
    patch -Np1 -i $patch
  done;

  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_QT4_TESTS=OFF -DENABLE_UTILS=OFF -DENABLE_CPP=OFF \
        -DWITH_Cairo=ON -DWITH_GLIB=OFF -DWITH_Qt4=ON ..
  make poppler-qt4
  make DESTDIR="${pkgdir}" install
  msg "Removing unwanted pkg-config files"
  rm -f "${pkgdir}"/usr/lib/pkgconfig/poppler{,-splash,-cairo}.pc
}
