# Maintainer : Paula Breton <paula@parashep.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=ucon64
pkgver=2.0.0
pkgrel=3
pkgdesc="A rom backup and management tool for nearly any console including support for flash and memory card writers."
arch=('i686' 'x86_64')
url="http://ucon64.sourceforge.net/index.php"
license="GPL"
depends=('zlib')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-src.tar.gz \
        ucon64.sh
        patch-backup-lynxit.c \
        patch-libdiscimage-misc_z.c \
        patch-libdiscimage-unzip.h \
        patch-misc-archive.c \
        patch-misc-unzip.h \
        patch-ucon64.c \
        patch-ucon64_misc.c)
md5sums=('33804256edb265d43bc3dbb9ada4441a'
         '070be30ccf9439d9428a7f0d4d03f86b'
         '0c315bce4b590749b7451b33f86d5b2d'
         '3a405c6696069b8799dce85c21c6c6e9'
         '249aaa950fba7bc9983b4dafdeaf37ab'
         '42057845212bf93dee24584912316494'
         'fbd555b1fa8b0d6a1547fcca5110e8a9'
         'a991ce0999b9724dec816fb1747b1237'
         'edac4423575708755e7dc1a603538c01')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}-src/src
  for i in ../../patch*; do patch -p0 < $i; echo $i; done
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}-src/src
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}-src/src

  # Do the things ucon64's install.sh file does manually

  # Install the script that will check for ucon64 in the user folder
  # and create one if it is not found or run ucon64.
  install -Dm755 ${srcdir}/ucon64.sh ${pkgdir}/usr/bin/ucon64

  # Install ucon64 and the discmage library in /usr/share
  install -Dm775 ${pkgname} ${pkgdir}/usr/share/${pkgname}/${pkgname}
  if [ -f libdiscmage/discmage.so ]; then
    cp libdiscmage/discmage.so ${pkgdir}/usr/share/${pkgname}/
  elif [ -f discmage.so ]; then
    cp discmage.so ${pkgdir}/usr/share/${pkgname}/
  fi
}
