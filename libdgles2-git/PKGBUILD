# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libdgles2-git
pkgver=20110304
pkgrel=1
pkgdesc="Desktop OpenGL ES 2.0 converter library"
arch=('i686' 'x86_64')
url="http://meego.gitorious.org/qemu-maemo/gles-libs"
license=('GPL')
depends=('libgles')
makedepends=('git')
provides=('libdgles2')
conflicts=('libdgles2')

_gitroot="git://gitorious.org/qemu-maemo/gles-libs.git"
_gitname="gles-libs"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"/dgles2

  ./configure --prefix=/usr \
    --enable-osmesa \
    --enable-offscreen \
    --enable-glx \
    --enable-x11
  make
}

package() {
  cd "$srcdir/$_gitname-build"/dgles2
  
  # Install libraries
  cd objs-x86_64
  install -d ${pkgdir}/usr/lib
  install -Dm755 libEGL.so.1.3.0 \
    libGLES_CM.so.1.3.0 \
    libGLESv2.so.1.3.0 \
    ${pkgdir}/usr/lib
  ln -s libEGL.so.1.3.0 ${pkgdir}/usr/lib/libEGL.so.1
  ln -s libEGL.so.1 ${pkgdir}/usr/lib/libEGL.so
  ln -s libGLES_CM.so.1.3.0 ${pkgdir}/usr/lib/libGLES_CM.so.1
  ln -s libGLES_CM.so.1 ${pkgdir}/usr/lib/libGLES_CM.so
  ln -s libGLESv2.so.1.3.0 ${pkgdir}/usr/lib/libGLESv2.so.1
  ln -s libGLESv2.so.1 ${pkgdir}/usr/lib/libGLESv2.so

  # Install headers
  cd ../include
  for d in EGL GLES GLES2 KHR; do
    install -d ${pkgdir}/usr/include/$d
    install -Dm644 $d/*.h ${pkgdir}/usr/include/$d
  done
}
