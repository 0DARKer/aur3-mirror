# Maintainer: Det <nimetonmaili g-mail>
# Contributors: Achilleas Pipinellis, speed145a, Schnouki

pkgname=firefox-beta-bin
pkgver=36.0b7
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org - Beta"
arch=('i686' 'x86_64')
url="https://www.mozilla.org/en-US/firefox/channel/#beta"
license=('MPL' 'GPL' 'LGPL')
depends=('alsa-lib' 'dbus-glib' 'desktop-file-utils' 'gtk2' 'hicolor-icon-theme'
         'icu' 'libevent' 'libvpx' 'libxt' 'mime-types' 'nss' 'sqlite')
optdepends=('gst-plugins-base: vorbis decoding, ogg demuxing'
            'gst-plugins-good: webm and mp4 demuxing'
            'gst-plugins-bad: aac, vp8 and opus decoding'
            'gst-plugins-ugly: h.264 and mp3 decoding'
            'gst-libav: more decoders'
            'libpulse: PulseAudio driver'
            'networkmanager: Location detection via available WiFi networks')
makedepends=('pacman>=4.2.0')
install=$pkgname.install
source=("$pkgname.desktop"
        "$pkgname-safe.desktop")
## RC
#source_i686=("https://ftp.mozilla.org/pub/mozilla.org/firefox/candidates/${pkgver/rc*}-candidates/build${pkgver/*rc}/linux-i686/en-US/firefox-${pkgver/rc*}.tar.bz2")
#source_x86_64=("https://ftp.mozilla.org/pub/mozilla.org/firefox/candidates/${pkgver/rc*}-candidates/build${pkgver/*rc}/linux-x86_64/en-US/firefox-${pkgver/rc*}.tar.bz2")
## Beta/Stable
source_i686=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/linux-i686/en-US/firefox-$pkgver.tar.bz2")
source_x86_64=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/linux-x86_64/en-US/firefox-$pkgver.tar.bz2")
sha512sums=('80c74b084034de91bc9ec24aa8f858660428b3f33cf991c9a28728217fa7aedc856fe9e44bd0dc683edba3f3c960908998adb5498cc17441f15eb9c2c339fcdd'
            '7c3ed4ad4b75337aba4a6ba775bdf0f776356531a56ec783d44d055426eb75292f5e52f505ea9658cde2e15e3955709cf17834f41b4423f97558aaa1ef7ae8d1')
sha512sums_i686=('54896b7aab2a515dbec748a2503017699aaf18bd5e08fa630d0534aa7bb6b58da31a0e6b6714d6e376975208401576fc24ca7e496e9135d84ea233564c551053')
sha512sums_x86_64=('c7c82dd1c925f2b41cb22dc56bd2dd21f92faf346abcb74649192c454add3cf58094a2806206ace2e446ae7eddc14b4f2427635977577cfe15b69fda826bf155')

package() {
  # Create directories
  msg2 "Creating directory structure..."
  mkdir -p "$pkgdir"/usr/bin
  mkdir -p "$pkgdir"/usr/share/applications
  mkdir -p "$pkgdir"/usr/share/icons/hicolor/128x128/apps
  mkdir -p "$pkgdir"/opt

  msg2 "Moving stuff in place..."
  # Install
  cp -r firefox/ "$pkgdir"/opt/$pkgname-$pkgver

  # /usr/bin link
  ln -s /opt/$pkgname-$pkgver/firefox "$pkgdir"/usr/bin/$pkgname

  # Desktops
  install -m644 *.desktop "$pkgdir"/usr/share/applications/

  # Icons
  for i in 16 32 48; do
    install -d "$pkgdir"/usr/share/icons/hicolor/${i}x$i/apps/
    ln -s /opt/$pkgname-$pkgver/browser/chrome/icons/default/default$i.png \
          "$pkgdir"/usr/share/icons/hicolor/${i}x$i/apps/$pkgname.png
  done

  ln -s /opt/$pkgname-$pkgver/browser/icons/mozicon128.png \
        "$pkgdir"/usr/share/icons/hicolor/128x128/apps/$pkgname.png
}
