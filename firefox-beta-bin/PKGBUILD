# Maintainer : Achilleas Pipinellis <axilleas@archlinux.info>
# Contributor: speed145a <speed145a(at)hotmail(dot)com>
# Contributor: Schnouki

pkgname=firefox-beta-bin
pkgver=32.0b1
pkgrel=1
pkgdesc='Standalone web browser from mozilla.org, beta build binaries.'
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url='http://www.mozilla.org/en-US/firefox'
depends=('gtk2' 'dbus-glib' 'desktop-file-utils' 'libxt' 'mime-types' 'nss' 'shared-mime-info')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'gstreamer0.10-base-plugins: vorbis decoding, ogg demuxing'
            'gstreamer0.10-good-plugins: webm and mp4 demuxing'
            'gstreamer0.10-bad-plugins: aac, vp8 and opus decoding'
            'gstreamer0.10-ugly-plugins: h.264 and mp3 decoding'
            'gstreamer0.10-ffmpeg: more decoders'
            'libpulse: PulseAudio audio driver')
source=("http://ftp.mozilla.org/pub/firefox/releases/${pkgver}/linux-${CARCH}/en-US/firefox-${pkgver}.tar.bz2"
        "$pkgname.desktop"
        "$pkgname-safe.desktop")

sha1sums=('e67630d27d54e09a41097b9a09ef8346bc11c22d'
'dd2afa6a6369b9c6ca549152d2df46794b3b385d'
'30990dafa5bea211ea2b84dadb56ac68324f48a3')

[[ "$CARCH" == "i686" ]] && sha1sums[0]="9a44d98bd852bb195b0874023cb1ea8a1bed991c"


package() {
	cd "${srcdir}"

	mkdir -p "${pkgdir}"/{usr/{bin,share/{applications,pixmaps}},opt}
	cp -r firefox "${pkgdir}"/opt/${pkgname}-${pkgver}

	ln -s /opt/${pkgname}-${pkgver}/firefox "${pkgdir}"/usr/bin/${pkgname}
	install -m644 "${srcdir}"/{${pkgname}.desktop,${pkgname}-safe.desktop} "${pkgdir}"/usr/share/applications/
	install -m644 "${srcdir}"/firefox/browser/icons/mozicon128.png "${pkgdir}"/usr/share/pixmaps/${pkgname}-icon.png
}
