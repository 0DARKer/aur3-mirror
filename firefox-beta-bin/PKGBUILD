# Maintainer: Det <nimetonmaili g-mail>
# Contributors: Achilleas Pipinellis, speed145a, Schnouki

pkgname=firefox-beta-bin
pkgver=36.0b4
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org - Beta"
arch=('i686' 'x86_64')
url="https://www.mozilla.org/en-US/firefox/channel/#beta"
license=('MPL' 'GPL' 'LGPL')
depends=('alsa-lib' 'dbus-glib' 'desktop-file-utils' 'gtk2' 'hicolor-icon-theme'
         'icu' 'libevent' 'libvpx' 'libxt' 'mime-types' 'nss' 'sqlite')
optdepends=('gst-plugins-base: vorbis decoding, ogg demuxing'
            'gst-plugins-good: webm and mp4 demuxing'
            'gst-plugins-bad: aac, vp8 and opus decoding'
            'gst-plugins-ugly: h.264 and mp3 decoding'
            'gst-libav: more decoders'
            'libpulse: PulseAudio driver'
            'networkmanager: Location detection via available WiFi networks')
makedepends=('pacman>=4.2.0')
install=$pkgname.install

# RC
#source_x86_64=("https://ftp.mozilla.org/pub/mozilla.org/firefox/candidates/${pkgver/rc*}-candidates/build${pkgver/*rc}/linux-x86_64/en-US/firefox-${pkgver/rc*}.tar.bz2")
#source_i686=("https://ftp.mozilla.org/pub/mozilla.org/firefox/candidates/${pkgver/rc*}-candidates/build${pkgver/*rc}/linux-i686/en-US/firefox-${pkgver/rc*}.tar.bz2")

# Beta/Stable
source_x86_64=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/linux-x86_64/en-US/firefox-$pkgver.tar.bz2")
source_i686=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/linux-i686/en-US/firefox-$pkgver.tar.bz2")

# MD5
md5sums_x86_64=('4d9c2fd940b82da913912a381f86b92c')
md5sums_i686=('6ee9b3824ec9fb2278b12a7a5f815529')

# Any
source=("$pkgname.desktop"
        "$pkgname-safe.desktop")
md5sums=('13bd50c37f55a83539a680ce83162468'
         'a85c53ccb2b78514f37833d288a20ba2')

package() {
  # Create directories
  msg2 "Creating directory structure..."
  mkdir -p "$pkgdir"/usr/bin
  mkdir -p "$pkgdir"/usr/share/applications
  mkdir -p "$pkgdir"/usr/share/icons/hicolor/128x128/apps
  mkdir -p "$pkgdir"/opt

  msg2 "Moving stuff in place..."
  # Install
  cp -r firefox/ "$pkgdir"/opt/$pkgname-$pkgver

  # /usr/bin link
  ln -s /opt/$pkgname-$pkgver/firefox "$pkgdir"/usr/bin/$pkgname

  # Desktops
  install -m644 *.desktop "$pkgdir"/usr/share/applications/

  # Icons
  for i in 16 32 48; do
    install -d "$pkgdir"/usr/share/icons/hicolor/${i}x$i/apps/
    ln -s /opt/$pkgname-$pkgver/browser/chrome/icons/default/default$i.png \
          "$pkgdir"/usr/share/icons/hicolor/${i}x$i/apps/$pkgname.png
  done

  ln -s /opt/$pkgname-$pkgver/browser/icons/mozicon128.png \
        "$pkgdir"/usr/share/icons/hicolor/128x128/apps/$pkgname.png
}
