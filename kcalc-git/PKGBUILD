#maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kcalc-git
pkgver=r1142.e123b16
pkgrel=1
pkgdesc="Scientific Calculator. (GIT version)"
url="https://www.kde.org/applications/utilities/kcalc"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kxmlgui' 'knotifications')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('kcalc' 'kdeutils-kcalc')
provides=('kcalc')
source=("git://anongit.kde.org/kcalc.git")
sha1sums=('SKIP')
install="kcalc-git.install"

pkgver() {
  cd kcalc
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kcalc \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
