pkgname=mingw32-cairo
pkgver=1.10.2
pkgrel=2
arch=(any)
pkgdesc="cairo (mingw32)"
depends=(mingw32-libpng mingw32-pixman mingw32-fontconfig mingw32-freetype)
makedepends=(mingw32-gcc)
options=(!strip !buildflags)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
source=(http://cairographics.org/releases/cairo-${pkgver}.tar.gz)
sha1sums=('ccce5ae03f99c505db97c286a0c9a90a926d3c6e')

build()
{
  cd "$srcdir/cairo-$pkgver"

  export CFLAGS="$CFLAGS -mms-bitfields"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS PKG_CONFIG_PATH

  # ffs() is undefined
  sed -i '231i \
#ifdef __MINGW32__\
#define ffs __builtin_ffs\
#endif\
' src/cairo-compiler-private.h

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --enable-xlib=no \
    --enable-xlib-xrender=no \
    --enable-win32=yes \
    --enable-win32-font=yes \
    --enable-png=yes \
    --enable-static

  sed -i "s/deplibs_check_method=.*/deplibs_check_method=pass_all/g" libtool

  make
}

package () {
  cd "$srcdir/cairo-$pkgver"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/share
}
