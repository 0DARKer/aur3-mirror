# Maintainer: xduugu
# Contributor: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: David Fuhr <david.fuhr@web.de>

pkgname=oracle-sqldeveloper
pkgver=3.0.04.34
pkgrel=2
pkgdesc="A graphical tool for database development"
arch=('any')
url="http://www.oracle.com/technology/products/database/sql_developer/index.html"
license=('custom:OTN')
depends=('java-environment')
install=oracle-sqldeveloper.install
source=(http://download/file/from/oracle/page/sqldeveloper-$pkgver-no-jre.zip
        oracle-sqldeveloper.desktop
        oracle-sqldeveloper.sh
        LICENSE
        java_home.patch)
md5sums=('f3339bd9943c3352e635d7e3a043df9f'
         'd7bbcd2d0af1e35934e65abbe2dc3fa1'
         'a73ee0987b0faa7d789d03b72ce4f3d0'
         '71a4092467209c160d0f34abbc08e049'
         '827ede82380ed1bf245ff417fec9fd52')
sha256sums=('fa31b31ead6120fe83ca27610345ad20124ff223ae27f4a8a47b5ef0f305f55e'
            '0b34737f9ea9376f55ca075588ba1a16c0f9112069b502b0da94dd992b39336c'
            '8ae9df42b3ac1fbdf0e0ed6f7e9de5df38019c35054558fa389419e2e02aa4fa'
            '7b3a6fd8a1ade4427382ee36dc28432655902a0a68547b29c5ce089bd85fe3de'
            'b220dd1b784cf984b72b367b4784b2dcebf83227c2e419d3d102b1ab4c282c14')

package() {
  cd "$srcdir/sqldeveloper"

  patch -Np1 < "$srcdir/java_home.patch"
  find . \( -iname "*.exe" -o -iname "*.dll" \) -exec rm -f "{}" +
  find . -type f -exec install -Dm644 "{}" "$pkgdir/opt/oracle-sqldeveloper/{}" \;
  chmod +x "$pkgdir/opt/$pkgname/sqldeveloper.sh"

  install -Dm755 "$srcdir/$pkgname.sh"      "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/LICENSE"          "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
