# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: David Fuhr <david.fuhr@web.de>

pkgname=oracle-sqldeveloper
pkgver=2.1.1
pkgrel=2
pkgdesc="A graphical tool for database development"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technology/products/database/sql_developer/index.html"
license=('custom:OTN')
depends=('java-environment')
makedepends=('unzip')
install=oracle-sqldeveloper.install
source=(sqldeveloper-2.1.1.64.45-no-jre.zip
        oracle-sqldeveloper.desktop
        oracle-sqldeveloper.sh
        oracle-sqldeveloper
        LICENSE-OTN
        java_home.patch)
md5sums=('71851ee924ef45e75be9e8bce2f5ec0a'
         'bd9f9780e02f7fa807d1433e99e0b777'
         '3ade4d954df2b4c5c6a1e586eb1ab726'
         'ba2823487588fd4d8e6b7adae07b06d3'
         '8eea9f79adcb6b53e8c35622c41aeb8d'
         'ea0cb0253554e0d034ab651efe4b5d90')

build() {
  cd "$srcdir/sqldeveloper"

  find \( -iname "*.exe" -o -iname "*.dll" \) -exec rm {} +
  patch -p1 -i "$srcdir/java_home.patch" || return 1

  find . -type f | while read f; do
    target=${f:2}
    install -Dm644 "$f" "$pkgdir/opt/oracle-sqldeveloper/$target"
  done

  install -Dm755 "$srcdir/$pkgname" "$pkgdir/opt/$pkgname/bin/$pkgname"
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/etc/profile.d/$pkgname.sh"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/LICENSE-OTN" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
