# Maintainer: XZS <d dot f dot fischer at web dot de>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgname=waf
pkgver=1.8.2
pkgrel=1
pkgdesc='General-purpose build system modelled after Scons'
url='http://code.google.com/p/waf/'
arch=('any')
license=('BSD')
depends=('python')
provides=('python-waf')
source=("http://ftp.waf.io/pub/release/$pkgname-$pkgver.tar.bz2")
sha256sums=('a9e01e3ad1ce8121508d157cefedaf99dc6120aa4edfab1095a7f4f8871acfbb')

prepare() {
  cd "$pkgname-$pkgver"

  # Extract license
  head -n 30 waf | tail -n 25 > LICENSE

  # Python 3 fix
  sed -i '0,/env python/s//python3/' waf
}

build() {
  cd "$pkgname-$pkgver"
  ./waf-light \
    configure --prefix=/usr \
    build --make-waf --tools='compat,compat15,ocaml,go,cython,scala,erlang,cuda,gcj,boost,pep8,eclipse'
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 waf "$pkgdir/usr/bin/waf"

  local waflib="$pkgdir/usr/lib/waf3-$pkgver-$(grep -Pom1 '(?<=^REVISION=")[[:xdigit:]]*(?="$)' "$pkgdir/usr/bin/waf")"
  install -dm755 "$waflib"
  unzip -d "$waflib" zip/waflib.zip

  # compile all python sources for once to be used after installation.
  python -m compileall "$waflib"
}

# vim:set ts=2 sw=2 et:
