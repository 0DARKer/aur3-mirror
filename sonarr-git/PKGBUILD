_gitname="Sonarr"
_gitver="2.0.0"
_gitbranch="develop"
_gitpatch=
pkgname="sonarr-git"
pkgver=2.0.0.r6020
pkgrel=1
pkgdesc="Smart PVR for newsgroup and bittorrent users"
arch=(any)
url="http://www.sonarr.tv"
license=('GPL3')
depends=('mono' 'libmediainfo' 'sqlite')
makedepends=('git' 'nodejs')
install='sonarr.install'
provides=('sonarr')
conflicts=('sonarr' 'sonarr-develop')
options=('!strip')
source=("git://github.com/Sonarr/Sonarr.git#branch=${_gitbranch}"
        "sonarr.sh"
        "sonarr.service"
        "sonarr.install")
md5sums=(SKIP
         '4db62d0f6c5f53ed6bc4780f81a9c087'
         '7823be12518bce8979f1a64529504c93'
         '129201f2c68a6cb66cb5c1e7b04cd437')

pkgver() {
  cd "${_gitname}"

  # FIXME May deviate from actual version
  printf "${_gitver}.r%s" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${_gitname}"

  # Git for some reason complains if identity is not already set
  git config user.email "you@example.com"
  git config user.name "Your Name"

  msg2 "Rebasing using pull request (https://github.com/Sonarr/Sonarr/pull/202)"
  git fetch origin pull/202/head:XBuild-support
  git rebase --onto XBuild-support origin/${_gitbranch} --force-rebase --ignore-whitespace
}

build() {
  cd "${_gitname}"

  npm install
  fakeroot mozroots --import --machine --sync
  MONO_IOMAP=case xbuild src/NzbDrone.sln /t:Configuration=Release /t:Build
  node node_modules/gulp/bin/gulp.js build
}

package() {
  msg2 "Remove native Windows binaries"
  find ${_gitname}/_output/ \( \
	-name "ServiceUninstall.*" \
     -o -name "ServiceInstall.*" \
     -o -name "sqlite3.*" \
     -o -name "MediaInfo.*" \
     -o -name "*.pdb" \) \
  -type f -delete


  install -d -m 755 "${pkgdir}/var/lib/sonarr"

  msg2 "Install Sonarr in /usr/lib"
  install -d -m 755 "${pkgdir}/usr/lib/sonarr"
  cp -dpr --no-preserve=ownership "${_gitname}/_output/"* "${pkgdir}/usr/lib/sonarr"
  
  msg2 "Install executable into /usr/bin"
  install -D -m755 "${srcdir}/sonarr.sh" "${pkgdir}/usr/bin/sonarr"
  
  msg2 "Install sonarr.service"
  install -D -m 644 "${srcdir}/sonarr.service" "${pkgdir}/usr/lib/systemd/system/sonarr.service"
}
