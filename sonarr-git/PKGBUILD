pkgname="sonarr-git"
pkgver=2.0.0.r6043
pkgrel=1
pkgdesc="Smart PVR for newsgroup and bittorrent users"
arch=(any)

_gitname="Sonarr"
_gitver="2.0.0"
_gitbranch="develop"

url="https://sonarr.tv"
license=('GPL3')
groups=()
depends=('mono'
         'libmediainfo'
         'sqlite')
makedepends=('git'
             'nodejs')
optdepends=()
provides=('sonarr')
conflicts=('sonarr'
           'sonarr-develop')
replaces=()
backup=()
options=('!strip')
install='sonarr.install'
changelog=
source=("git://github.com/Sonarr/Sonarr.git#branch=${_gitbranch}"
        "sonarr.sh"
        "sonarr.service"
        "sonarr.install")
sha256sums=('SKIP'
            '5f137ff0bce04309e7ea2392f5eebda0460823e17dd58b476ca03eecd661ea27'
            'c3d4a95737a4715a84e51aaaafaae993782d01c7bfb6507b620d071b46a5015b'
            '31bd0086b49305b8d2a36ddec55be9cc17c555e14c55b2f896de7f4e0ddf0a38')

pkgver() {
  cd "${_gitname}"

  # FIXME May deviate from actual version
  printf "${_gitver}.r%s" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "${_gitname}"

  # FIXME Git complains if user is not already set
  git config user.email "you@example.com"
  git config user.name "Your Name"

  msg2 "Rebasing using pull requests (https://github.com/Sonarr/Sonarr/pull/{210,211})"
  git fetch origin pull/210/head:xbuild-support --force
  git fetch origin pull/211/head:process-name --force
  git rebase process-name --ignore-whitespace
  git rebase xbuild-support --ignore-whitespace
}

build() {
  cd "${_gitname}"

  npm install
  fakeroot mozroots --import --machine --sync
  MONO_IOMAP=case xbuild src/NzbDrone.sln /t:Configuration=Release /t:Build
  node node_modules/gulp/bin/gulp.js build
}

package() {
  find "${_gitname}/_output/" \( \
        -name "ServiceUninstall.*" \
     -o -name "ServiceInstall.*" \
     -o -name "sqlite3.*" \
     -o -name "MediaInfo.*" \
     -o -name "NzbDrone.Windows.*" \
   \) -type f -delete

  install -d "${pkgdir}/var/lib/sonarr"

  install -Dm755 "sonarr.sh" "${pkgdir}/usr/bin/sonarr"
  install -Dm644 "sonarr.service" "${pkgdir}/usr/lib/systemd/system/sonarr.service"

  install -d "${pkgdir}/opt/sonarr"
  cp -a "${_gitname}/_output/"* "${pkgdir}/opt/sonarr"
  find "${pkgdir}/opt/sonarr" -type d -exec chmod 755 {} \;
  find "${pkgdir}/opt/sonarr" -type f -exec chmod 644 {} \;
  find "${pkgdir}/opt/sonarr" -name \*.exe -type f -exec chmod 755 {} \;
}
