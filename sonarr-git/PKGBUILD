_gitname="Sonarr"
_gitver="2.0.0"
_gitbranch="develop"

# NB: Only for pull requests in origin
_git_patches=""
_git_patches+="210 " # Compiler: Support for XBuild
_git_patches+="211 " # Mono: Set process name
_git_patches+="222 " # Download clients: New client rTorrent

pkgname="sonarr-git"
pkgver=2.0.0.r6120
pkgrel=1
pkgdesc="Automated TV series manager and downloader (PVR) - git branch ${_gitbranch}"
arch=(any)
url="https://sonarr.tv"
license=('GPL3')
groups=()
depends=('mono'
         'libmediainfo'
         'sqlite')
makedepends=('git'
             'nodejs')
optdepends=()
provides=('sonarr')
conflicts=('sonarr'
           'sonarr-develop')
replaces=()
backup=()
options=('!strip')
install='sonarr.install'
changelog=
source=("git://github.com/Sonarr/Sonarr.git#branch=${_gitbranch}"
        "sonarr.sh"
        "sonarr.service"
        "sonarr.install")
sha256sums=('SKIP'
            '5f137ff0bce04309e7ea2392f5eebda0460823e17dd58b476ca03eecd661ea27'
            'c3d4a95737a4715a84e51aaaafaae993782d01c7bfb6507b620d071b46a5015b'
            '31bd0086b49305b8d2a36ddec55be9cc17c555e14c55b2f896de7f4e0ddf0a38')

pkgver() {
  cd "${_gitname}"

  # FIXME May deviate from actual version
  printf "${_gitver}.r%s" "$(git rev-list --count ${_gitbranch})"
}

prepare() {
  cd "${_gitname}"

  # Nuget requires some certificates in order to download libraries
  # FIXME Find a way to avoid requsting root
  sudo mozroots --import --machine --sync

  # Install necessary nodejs libraries
  npm install

  # Git complains if user is not already set
  git config user.email "you@example.com"
  git config user.name "Your Name"

  for patch in ${_git_patches}; do
      msg2 "Rebasing using patch (https://github.com/Sonarr/Sonarr/pull/${patch}"
      git fetch origin pull/${patch}/head:${patch} --force
      git rebase ${patch} --force-rebase --ignore-whitespace --quiet
  done
}

build() {
  cd "${_gitname}"

  xbuild ./src/NzbDrone.sln /t:Configuration=Release /t:Build
  node ./node_modules/gulp/bin/gulp.js build
}

package() {
  find "${_gitname}/_output/" \( \
        -name "ServiceUninstall.*" \
     -o -name "ServiceInstall.*" \
     -o -name "sqlite3.*" \
     -o -name "MediaInfo.*" \
     -o -name "NzbDrone.Windows.*" \
   \) -type f -delete

  install -d "${pkgdir}/var/lib/sonarr"

  install -Dm755 "sonarr.sh" "${pkgdir}/usr/bin/sonarr"
  install -Dm644 "sonarr.service" "${pkgdir}/usr/lib/systemd/system/sonarr.service"

  install -d "${pkgdir}/opt/sonarr"
  cp -a "${_gitname}/_output/"* "${pkgdir}/opt/sonarr"
  find "${pkgdir}/opt/sonarr" -type d -exec chmod 755 {} \;
  find "${pkgdir}/opt/sonarr" -type f -exec chmod 644 {} \;
  find "${pkgdir}/opt/sonarr" -name \*.exe -type f -exec chmod 755 {} \;
}
