# Maintainer: Lars Johnsen <lars.johnsen@gmail.com>
pkgname="sonarr-git"
pkgver=2.0.0.r6018.1837ba9
pkgrel=1
pkgdesc="Smart PVR for newsgroup and bittorrent users"
arch=(any)
url="http://www.sonarr.tv"
license=('GPL3')
depends=('mono' 'libmediainfo' 'sqlite')
makedepends=('git' 'nodejs')
install='sonarr.install'
provides=('sonarr')
conflicts=('sonarr' 'sonarr-develop')
source=("git://github.com/Sonarr/Sonarr.git#branch=develop"
        "sonarr.sh"
        "sonarr.service"
        "sonarr.install")
md5sums=(SKIP
         '4db62d0f6c5f53ed6bc4780f81a9c087'
         '7823be12518bce8979f1a64529504c93'
         '6413a3db424de8d85a320c9e60ecac14')
_gitname="Sonarr"
_developver="2.0.0"

pkgver() {
  cd "$_gitname"

  # FIXME May deviate from actual version
  printf "$_developver.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_gitname"

  msg2 "Patching"
  git fetch origin pull/202/head:XBuild-support
  git checkout XBuild-support
  git rebase origin/develop
}

build() {
  cd "$_gitname"

  npm install
  fakeroot mozroots --import --machine --sync
  MONO_IOMAP=case xbuild src/NzbDrone.sln /t:Configuration=Release /t:Build
  node node_modules/gulp/bin/gulp.js build
}

package() {
  msg2 "Remove native Windows binaries"
  find ${_gitname}/_output/ \( \
	-name "ServiceUninstall.*" \
     -o -name "ServiceInstall.*" \
     -o -name "sqlite3.*" \
     -o -name "MediaInfo.*" \
     -o -name "*.pdb" \) \
  -type f -delete


  install -d -m 755 "${pkgdir}/var/lib/sonarr"

  msg2 "Install Sonarr in /usr/lib"
  install -d -m 755 "${pkgdir}/usr/lib/sonarr"
  cp -dpr --no-preserve=ownership "${_gitname}/_output/"* "${pkgdir}/usr/lib/sonarr"
  
  msg2 "Install executable into /usr/bin"
  install -D -m755 "${srcdir}/sonarr.sh" "${pkgdir}/usr/bin/sonarr"
  
  msg2 "Install sonarr.service"
  install -D -m 644 "${srcdir}/sonarr.service" "${pkgdir}/usr/lib/systemd/system/sonarr.service"
}
