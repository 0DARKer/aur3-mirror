# Maintainer: Lars Johnsen <lars.johnsen@gmail.com>
pkgname="sonarr-git"
pkgver=6019.36e9180
pkgrel=1
_gitname="Sonarr"
pkgdesc="Smart PVR for newsgroup and bittorrent users"
arch=(any)
url="http://www.sonarr.tv"
license=('GPL3')
depends=('mono' 'libmediainfo' 'sqlite')
makedepends=('git' 'nodejs')
install='sonarr.install'
provides=('sonarr')
conflicts=('sonarr' 'sonarr-develop')
source=("Sonarr::git+https://github.com/larsjohnsen/Sonarr.git#branch=compile-linux"
        "sonarr.sh"
        "sonarr.service"
        "sonarr.install")
md5sums=(SKIP
         '4db62d0f6c5f53ed6bc4780f81a9c087'
         '7823be12518bce8979f1a64529504c93'
         '6413a3db424de8d85a320c9e60ecac14')

pkgver() {
  cd "$_gitname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_gitname"

  msg2 "Installing certifications for NuGet"
  fakeroot mozroots --import --machine --sync

  npm install
}

build() {
  cd "$_gitname"

  xbuild src/NzbDrone.sln
  node node_modules/gulp/bin/gulp.js build
}

package() {
  install -d -m 755 "${pkgdir}/var/lib/sonarr"
  
  msg2 "Install Sonarr in /usr/lib"
  install -d -m 755 "${pkgdir}/usr/lib/sonarr"
  cp -dpr --no-preserve=ownership "${_gitname}/_output/"* "${pkgdir}/usr/lib/sonarr"
  
  msg2 "Install executable into /usr/bin"
  install -D -m755 "${srcdir}/sonarr.sh" "${pkgdir}/usr/bin/sonarr"
  
  msg2 "Install sonarr.service"
  install -D -m 644 "${srcdir}/sonarr.service" "${pkgdir}/usr/lib/systemd/system/sonarr.service"
}
