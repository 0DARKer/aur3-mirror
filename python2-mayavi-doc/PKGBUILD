# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-mayavi-doc
pkgver=3.4.1
_githubtag=d113d31
pkgrel=1
pkgdesc="Documentation for Mayavi"
arch=('any')
url="https://github.com/enthought/mayavi"
license=('BSD')
makedepends=('python2-distribute' 'python2-setupdocs')
conflicts=('python2-mayavi-doc-git' 'python-ets-mayavi-doc-svn')
options=(!emptydirs)

source=("https://github.com/enthought/mayavi/tarball/${pkgver}"
        "mayavi-doc-fix.patch")
md5sums=('f9a01581b745dcbde747072394b189db'
         '8e3c99241ff2d7c2384419cd500cca14')

build() {
  cd "$srcdir/enthought-mayavi-${_githubtag}"

  patch -p0 < ../mayavi-doc-fix.patch

  python2 setup.py build_docs || return 1
}

package() {
  cd "$srcdir/enthought-mayavi-${_githubtag}"

  install -d "${pkgdir}"/usr/share/doc/enthought
  cp -r docs/build/mayavi/html "${pkgdir}"/usr/share/doc/enthought/mayavi
  cp -r docs/build/tvtk/html   "${pkgdir}"/usr/share/doc/enthought/tvtk

  _target="$pkgdir`python2 -c 'from distutils.sysconfig import get_python_lib; print get_python_lib()'`"
  install -d "${_target}"/enthought/{mayavi,tvtk}
  ln -s /usr/share/doc/enthought/mayavi "${_target}"/enthought/mayavi/html
  ln -s /usr/share/doc/enthought/tvtk   "${_target}"/enthought/tvtk/html
}

