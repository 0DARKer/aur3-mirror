# Maintainer: Mael Kerbiriou <mael.kerbiriouATfreeDOTfr>

pkgname=swc-git
pkgver=r355.78ae79d
pkgrel=1
pkgdesc='A library for making a simple Wayland compositor'
arch=('i686' 'x86_64')
license=('MIT')
url='https://github.com/michaelforney/swc'
depends=('libevdev' 'wld-git' 'systemd' 'xcb-util-wm' 'libxkbcommon')
makedepends=('git')
provides=('swc')
conflicts=('swc')
source=('git+https://github.com/michaelforney/swc.git')
md5sums=('SKIP')

_gitname="swc"

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"
  make
  
}

package() {
  cd "$srcdir/$_gitname"
  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
}

# vim: set noet ff=unix: