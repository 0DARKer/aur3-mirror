# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: nem <nem@ikitten.co.uk>
# Contributor: agvares <agvares13@gmail.com>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>

pkgname=codelite
pkgver=6.0.1
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages"
arch=('i686' 'x86_64')
url="http://www.codelite.org/"
license=('GPL')
depends=('wxgtk' 'curl' 'webkitgtk2' 'libssh' 'xterm' 'python2' 'libedit' 'libtinfo')
makedepends=('pkgconfig' 'cmake')
optdepends=('graphviz: callgraph visualization'
            'lldb-svn: debugger used by the lldb plugin')
#source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-gtk.src.tar.gz
source=(https://github.com/eranif/${pkgname}/archive/${pkgver}.tar.gz
	http://repos.codelite.org/wxCrafterLibs/wxgui.zip)
md5sums=('5bbc3cc83638975903e74a69a9ad62ea'
         'b0e3967b2ad25d79990cc192d8bb4ccd')
if [[ "$CARCH" == 'i686' ]]; then
  source+=(http://repos.codelite.org/wxCrafterLibs/ArchLinux/32/wxCrafter.so)
  md5sums+=('6b17beb72b4287fcc7372f04574a439a')
elif [[ "$CARCH" == 'x86_64' ]]; then
  source+=(http://repos.codelite.org/wxCrafterLibs/ArchLinux/64/wxCrafter.so)
  md5sums+=('5fd6bfc711ae98fe0fb1249a7566e7d6')
fi
noextract=('wxgui.zip')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    CXXFLAGS="${CXXFLAGS} -fno-devirtualize"

    mkdir -p build
    cd build
    cmake -G "Unix Makefiles" -DCMAKE_BUILD_TYPE=Release -DENABLE_CLANG=1 -DCMAKE_INSTALL_LIBDIR=lib ..
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make -j1 DESTDIR="${pkgdir}" install
    install -m 755 -D "${srcdir}/wxCrafter.so" "${pkgdir}/usr/lib/codelite/wxCrafter.so"
    install -m 644 -D "${srcdir}/wxgui.zip" "${pkgdir}/usr/share/codelite/wxgui.zip"
    install -m 644 -D "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
