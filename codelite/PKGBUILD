# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: nem <nem@ikitten.co.uk>
# Contributor: agvares <agvares13@gmail.com>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>

pkgname=codelite
pkgver=5.2
pkgrel=2
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages"
arch=('i686' 'x86_64')
url="http://www.codelite.org/"
license=('GPL')
depends=('wxgtk2.9>=2.9.4' 'curl' 'webkitgtk2')
makedepends=('pkgconfig' 'cmake')
optdepends=('graphviz: callgraph visualization')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-gtk.src.tar.gz
	http://repos.codelite.org/wxCrafterLibs/wxgui.zip
        codelite.patch)
md5sums=('297892da483e4f51a4b7dd6d3e4e5af0'
         'dc7e5f6a6905a844a0c171684390cf04'
         'd0d3831d383cdfe793daf04497c689c6')
if [[ "$CARCH" == 'i686' ]]; then
  source+=(http://repos.codelite.org/wxCrafterLibs/fedora/32/wxCrafter.so)
  md5sums+=('ec79732bdb4acd50124c48b625d7ee96')
elif [[ "$CARCH" == 'x86_64' ]]; then
  source+=(http://repos.codelite.org/wxCrafterLibs/fedora/64/wxCrafter.so)
  md5sums+=('f3df02e116a4d5a4f2695eab753f2156')
fi
noextract=('wxgui.zip')


build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    patch -p1 -i ../codelite.patch
    mkdir -p build
    cd build
    sed -i 's/wx-config/wx-config-2.9/g' ../CMakeLists.txt
    cmake -G "Unix Makefiles" -DCMAKE_BUILD_TYPE=Release -DENABLE_CLANG=1 -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.9 -DwxWidgets_wxrc_EXECUTABLE=/usr/bin/wxrc-2.9 ..
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}/build
    make -j1 DESTDIR=${pkgdir} install
    install -m 755 -D ${srcdir}/wxCrafter.so ${pkgdir}/usr/lib/codelite/wxCrafter.so
    install -m 644 -D ${srcdir}/wxgui.zip ${pkgdir}/usr/share/codelite/wxgui.zip
    install -m 644 -D ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
