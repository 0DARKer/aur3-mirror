# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: nem <nem@ikitten.co.uk>
# Contributor: agvares <agvares13@gmail.com>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>

pkgname=codelite
pkgver=6.0
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages"
arch=('i686' 'x86_64')
url="http://www.codelite.org/"
license=('GPL')
depends=('wxgtk' 'curl' 'webkitgtk2' 'libssh' 'xterm' 'python2' 'libedit' 'libtinfo')
makedepends=('pkgconfig' 'cmake')
optdepends=('graphviz: callgraph visualization')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-gtk.src.tar.gz
	http://repos.codelite.org/wxCrafterLibs/wxgui.zip)
md5sums=('13b5f9add95ef3d1e68b724f5912619c'
         'b0e3967b2ad25d79990cc192d8bb4ccd')
if [[ "$CARCH" == 'i686' ]]; then
  source+=(http://repos.codelite.org/wxCrafterLibs/fedora/32/wxCrafter.so)
  md5sums+=('e19531043e407522343b24c4900fa3f0')
elif [[ "$CARCH" == 'x86_64' ]]; then
  source+=(http://repos.codelite.org/wxCrafterLibs/fedora/64/wxCrafter.so)
  md5sums+=('7f4622fdfb6361e27e0dd87a6d1f16a9')
fi
noextract=('wxgui.zip')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    CXXFLAGS="${CXXFLAGS} -fno-devirtualize"

    mkdir -p build
    cd build
    cmake -G "Unix Makefiles" -DCMAKE_BUILD_TYPE=Release -DENABLE_CLANG=1 ..
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make -j1 DESTDIR="${pkgdir}" install
    install -m 755 -D "${srcdir}/wxCrafter.so" "${pkgdir}/usr/lib/codelite/wxCrafter.so"
    install -m 644 -D "${srcdir}/wxgui.zip" "${pkgdir}/usr/share/codelite/wxgui.zip"
    install -m 644 -D "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
