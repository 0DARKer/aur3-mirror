# Maintainer: William Giokas <1007380@gmail.com>

pkgname=minimetro
_pkgname=MiniMetro
pkgver=alpha7a
pkgrel=1
pkgdesc='minimalistic subway layout game'
url='http://dinopoloclub.com/minimetro/'
license=('custom:None')
arch=('i686' 'x86_64')
depends=('libgl' 'libx11' 'glu')
source=("http://static.dinopoloclub.com/minimetro/builds/alpha7/MiniMetro-${pkgver}-linux.tar.gz")
md5sums=('fb4d02a8e75e3dec2aebcfb6562a9075')

case $CARCH in
  i686)
    _notarch=x86_64
    _arch=x86
    ;;
  x86_64)
    _notarch=x86
    _arch=x86_64
    ;;
esac


package() {
  install -dm 755 "$pkgdir"/usr/bin
  install -dm 755 "$pkgdir"/opt/minimetro
  _inst_loc=opt/minimetro/MiniMetro-${pkgver}-linux_Data
  cp -r "$srcdir"/MiniMetro-${pkgver}-linux_Data/ \
        "$srcdir"/MiniMetro-${pkgver}-linux.$_arch \
        "$pkgdir"/opt/minimetro/

  find "$pkgdir"/opt/minimetro \
    -name "$_notarch" \
    -type d \
    -exec rm -r {} +

  cat << EOF > "$pkgdir"/usr/bin/minimetro
#!/usr/bin/env bash
/opt/minimetro/MiniMetro-${pkgver}-linux.$_arch
EOF

  chmod +x "$pkgdir"/usr/bin/minimetro
}
