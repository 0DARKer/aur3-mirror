# Maintainer: XZS <d dot f dot fischer at web dot de>
# Contributor: Samuel Littley <samuel.littley@toastwaffle.com>
# Contributor: William Giokas <1007380@gmail.com>

pkgname=minimetro
_pkgname=MiniMetro
pkgver=beta17
pkgrel=2
pkgdesc='minimalistic subway layout game'
url="http://dinopoloclub.com/${pkgname}/"
license=('custom:None')
arch=('i686' 'x86_64')
depends=('libgl' 'libx11' 'glu' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('imagemagick')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')
install=desktop.install
source=("hib://${_pkgname}-${pkgver}-linux.tar.gz"
        "minimetro.desktop"
        "minimetro.svg")
md5sums=('3631d83c72b9bef5f87cd5f555803a0a'
         '0fe19f989f9606a825b67b9b379c9d6c'
         '6e7915bc03ced67cffb81174f260a742')

prepare() {
  case $CARCH in
    i686) _notarch=x86_64 ;;
    x86_64) _notarch=x86 ;;
  esac
  find -name "*$_notarch" -exec rm -r {} +
}

package() {
  # First, install the game itself.
  destdir="$pkgdir/opt/$pkgname"
  install -dm 755 "$destdir"
  cp -r ${_pkgname}{_,.}* "$destdir"

  # Make the log file writable by the 'games' group. This is needed to run.
  log="$destdir"/${_pkgname}_Data/log.txt
  touch "$log"
  chmod 664 "$log"
  chown :games "$log"

  # Now, care for supplementary files.
  install -dm 755 "$pkgdir"/usr/{bin,share/{applications,icons/hicolor/{scalable,256x256}/apps}} "$destdir"

  echo "#!/opt/$pkgname/${_pkgname}.$CARCH" > "$pkgdir"/usr/bin/${pkgname}
  chmod +x "$pkgdir"/usr/bin/$pkgname

  cp ${pkgname}.desktop "$pkgdir"/usr/share/applications
  cp ${pkgname}.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps
  convert ${pkgname}.svg "$pkgdir"/usr/share/icons/hicolor/256x256/apps/minimetro.png
}
