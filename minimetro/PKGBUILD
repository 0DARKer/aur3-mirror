# Maintainer: William Giokas <1007380@gmail.com>

pkgname=minimetro
_pkgname=MiniMetro
pkgver=alpha12a
_build=alpha12
pkgrel=1
pkgdesc='minimalistic subway layout game'
url='http://dinopoloclub.com/minimetro/'
license=('custom:None')
arch=('i686' 'x86_64')
depends=('libgl' 'libx11' 'glu')
source=("http://static.dinopoloclub.com/minimetro/builds/${_build}/MiniMetro-${pkgver}-linux.tar.gz")
md5sums=('0afe3c6a25716a0af22b3c7fe10f3927')

case $CARCH in
  i686)
    _notarch=x86_64
    _arch=x86
    ;;
  x86_64)
    _notarch=x86
    _arch=x86_64
    ;;
esac

package() {
  install -dm 755 "$pkgdir"/usr/bin
  install -dm 755 "$pkgdir"/opt/minimetro
  cp -r "$srcdir"/MiniMetro-${pkgver}-linux_Data/ \
        "$srcdir"/MiniMetro-${pkgver}-linux.$_arch \
        "$pkgdir"/opt/minimetro/

  # Make the log file writable by the 'games' group. This is needed to run.
  touch "$pkgdir"/opt/minimetro/MiniMetro-${pkgver}-linux_Data/log.txt
  chmod 664 "$pkgdir"/opt/minimetro/MiniMetro-${pkgver}-linux_Data/log.txt
  chown :games "$pkgdir"/opt/minimetro/MiniMetro-${pkgver}-linux_Data/log.txt

  find "$pkgdir"/opt/minimetro \
    -name "$_notarch" \
    -type d \
    -exec rm -r {} +

  cat << EOF > "$pkgdir"/usr/bin/minimetro
#!/opt/minimetro/MiniMetro-${pkgver}-linux.$_arch
EOF

  chmod +x "$pkgdir"/usr/bin/minimetro
}
