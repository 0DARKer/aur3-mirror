# Maintainer:  fsckd <fsckdaemon@gmail.com>
# Contributor: cruznick <cruznick@archlinux.us>

# Tunables: change y to n to disable
_rm_bld_dirs=${_rm_bld_dirs:-n}        # remove build directories
_mk_burg_emu=${_mk_burg_emu:-y}        # enable burg emu

pkgname="burg-bios-bzr"
pkgver=1844
pkgrel=1
pkgdesc="Brand-new Universal loadeR from GRUB2 - Built for i386 BIOS"
url="http://code.google.com/p/burg/"
license="GPL3"
arch=('i686' 'x86_64')

makedepends=('bzr' 'ruby' 'python2' 'ncurses' 'sdl')
depends=('gettext' 'freetype2')
optdepends=('os-prober' 'memtest86+')

conflicts=('burg-bios')
replaces=('burg' 'burg-bzr' 'burg-emu')
provides=('burg' 'burg-bios' 'burg-bzr')

[[ "${_mk_burg_emu}" == 'y' ]] && conflicts+=('burg-emu')
[[ "${_mk_burg_emu}" == 'y' ]] && provides+=('burg-emu')

#options=('!makeflags')
changelog='burg.changelog'
backup=('etc/default/burg' 'etc/burg.d/40_custom')

source=('burg.default' 'arch-burg.patch' '20_memtest86+' 'update-burg')

sha256sums=('e94ee55a1fa9cadb5d752c40df6060c0b5c6b42f6f69440d24642b483255b05a'
            '57fa4d1ab439a3e716cf60f5eda533969f8d4a46b6425e85f0529d1897897446'
            '31edd8578c337be2f02dae8a292b5c53d34b107ab255634698794f999a293506'
            '646d55a233706329ecc9b4b6d0eb0460e6d37b84ebbe3e15c3176cbd23bf075b')

install='burg.install'

_bzrmod="burg"
_bzrtrunk="lp:${_bzrmod}"

_common_configure_opts="--prefix=/usr --bindir=/bin \
                    --sbindir=/sbin --mandir=/usr/share/man \
                    --infodir=/usr/share/info --sysconfdir=/etc \
                    --disable-werror"

_update_bzr() {
  
  cd "${srcdir}/"
  
  msg "Connecting to the server...."
  
  if [[ ! -d "${srcdir}/${_bzrmod}" ]] ; then
    bzr branch "${_bzrtrunk}"
  else
    cd "${srcdir}/${_bzrmod}" && bzr pull
  fi
  
  msg "BZR checkout done or server timeout"
  
}

_build_dir() {
  
  local rm_bdir='0'
  local bdir="${_bzrmod-build}"
  
  while (( "$#" ))
  do
    [[ "$1" == '-r' ]] && rm_bdir=1 \
                       || bdir="${_bzrmod}-$1-build"
    shift
  done
  
  rm -rf "${srcdir}/${bdir}"
  
  if [[ "${rm_bdir}" == '0' ]] ; then
    cp -rip "${srcdir}/${_bzrmod}" "${srcdir}/${bdir}"
    cd "${srcdir}/${bdir}/"
  fi
  
}

_build_common() {
  
  cd "${srcdir}/${bdir}/"
  
  # Archlinux changed default python to python3, need to use /usr/bin/python2 instead
  sed 's|python|python2|g' -i "${srcdir}/${bdir}/autogen.sh" || true
  
  ./autogen.sh
  
}

_build_bios() {
  
  msg "Building burg bios...."
  
  _build_dir
  
  # some random patches to facilitate automatic creation of burg.cfg
  patch -Np1 -i  "${srcdir}/arch-burg.patch"
  
  ## fix unifont.bdf location so burg-mkfont can create *.pf2 files
  sed 's|/usr/share/fonts/unifont|/usr/share/fonts/misc|g' -i "${srcdir}/${bdir}/configure"
  
  _build_common
  
  _bios_configure_opts="${_common_configure_opts} --with-platform=pc --disable-grub-emu-usb"
  ./configure ${_bios_configure_opts}
  
  CFLAGS="" make
  
}

_build_emu() {
  
  msg "Building burg-emu...."
  
  _build_dir emu
  
  _build_common
  
  _emu_configure_opts="${_common_configure_opts} --with-platform=emu --disable-grub-emu-usb"
  ./configure ${_emu_configure_opts}
  
  CFLAGS="" make
  
}

build() {
  
  _update_bzr
  
  _build_bios
  
  [[ "${_mk_burg_emu}" == 'y' ]] && _build_emu
  
}

package() {
  
  cd "${srcdir}/${bdir}/"
  
  make DESTDIR="${pkgdir}/" install
  
  [[ "${_mk_burg_emu}" == 'y' ]] && install -D -m0755 "${srcdir}/${bdir}/grub-emu" "${pkgdir}/bin/burg-emu"
  
  # install /etc/default/burg(needed config file)
  install -D -m0644 "${srcdir}/burg.default" "${pkgdir}/etc/default/burg"
  
  # install update-burg script
  install -D -m0755 "${srcdir}/update-burg" "${pkgdir}/sbin/update-burg"
  
  # install memtest config detection
  install -D -m0755 "${srcdir}/20_memtest86+" "${pkgdir}/etc/burg.d/20_memtest86+"
  
  [[ "${_rm_bld_dirs}" == 'y' ]] && _build_dir -r
  
  if [[ "${_mk_burg_emu}" == 'y' ]]; then
    [[ "${_rm_bld_dirs}" == 'y' ]] && _build_dir -r emu
  fi
  
}
