# Contributor: Andre Klitzing <aklitzing () online () de>
pkgname=mingw32-wxmsw
pkgver=2.8.12
pkgrel=3
pkgdesc="Win32 implementation of wxWidgets API for GUI (mingw32)"
arch=(any)
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('mingw32-runtime')
makedepends=('mingw32-w32api' 'mingw32-gcc' 'mingw32-binutils' 'hd2u')
conflicts=('mingw32-wxmsw-static')
options=(!strip !buildflags)
install=wxmsw.install
source=("http://downloads.sourceforge.net/wxwindows/wxMSW-$pkgver.tar.bz2")
md5sums=('4bda849b9f7c66b58c4bc92e505ea714')

build() {
  cd "$srcdir/wxMSW-$pkgver"
  unset LDFLAGS
  mkdir -p build2 && cd build2
  ../configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --with-msw \
    --enable-unicode \
    --enable-shared
  make
}

package() {
  cd "$srcdir/wxMSW-$pkgver/build2"
  make DESTDIR="$pkgdir/" install

  msg "Get rid of stupid carriage return (^M problem) in all text files!"
  cd "$pkgdir/"
  find ./ -type f -exec dos2unix --d2u --skipbin {} \;
}

