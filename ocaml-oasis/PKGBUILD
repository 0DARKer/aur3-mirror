# Maintainer: Magnus Therning <magnus@therning.org>

pkgname=ocaml-oasis
pkgver=0.2.0
pkgrel=3
pkgdesc="OCaml oasis"
arch=('i686' 'x86_64')
url=("http://oasis.forge.ocamlcore.org/index.php")
# LGPL + linking exception
license=('LGPL')
depends=('ocaml')
makedepends=('ocaml-extlib' 'ocaml-fileutils' 'ocaml-findlib' 'ocaml-ocamlgraph' 'ocaml-ocamlify' 'ocaml-odn' 'ocaml-ounit' 'pcre-ocaml' 'ocaml-expect')
source=(
    https://forge.ocamlcore.org/frs/download.php/501/oasis-${pkgver}.tar.gz
    fix-setup
    )
options=(!strip)
changelog=$pkgname.changelog

build() {
    cd ${srcdir}/${pkgname/ocaml-/}-${pkgver}

    patch -p3 < ../fix-setup
    touch doc/MANUAL.mkd

    ocaml setup.ml -configure --prefix /usr --destdir ${pkgdir}
    ocaml setup.ml -build
}

package() {
    cd ${srcdir}/${pkgname/ocaml-/}-${pkgver}

    # ocamlfind is broken, IMHO
    mkdir -p ${pkgdir}$(ocamlfind printconf path|head -n 1)
    OCAMLFIND_DESTDIR=${pkgdir}$(ocamlfind printconf path|head -n 1) ocaml setup.ml -install
}

sha256sums=('085067782ad944e8650a4a5b70e7ef097687987cacd757f973de09daf4431345'
            '7df01a03672e958d0459590cfc80e610767831219db8d26454ae6702588bf7be')
