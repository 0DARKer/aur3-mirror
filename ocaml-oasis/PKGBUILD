# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Magnus Therning <magnus@therning.org>

pkgname=ocaml-oasis
pkgver=0.2.0
pkgrel=6
pkgdesc='Architecture for building OCaml libraries and applications.'
arch=('i686' 'x86_64')
url=("http://oasis.forge.ocamlcore.org")
license=('LGPL')
depends=('glibc' 'pcre')
makedepends=('ocaml'
             'ocaml-extlib'
             'ocaml-fileutils>=0.4.2'
             'ocaml-findlib'
             'ocaml-ocamlgraph'
             'ocaml-ocamlify'
             'ocaml-odn>=0.0.3'
             'ocaml-ounit'
             'ocaml-pcre'
             'ocaml-expect')
source=("https://forge.ocamlcore.org/frs/download.php/501/oasis-$pkgver.tar.gz")
md5sums=('e5d04bfe41eacd4f58a156784700a2ba')
options=(!strip)

build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  make CONFIGUREFLAGS="--prefix /usr --destdir $pkgdir" build
}

package() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -m755 -d $OCAMLFIND_DESTDIR
  make install
}
