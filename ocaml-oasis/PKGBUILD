# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Magnus Therning <magnus@therning.org>

pkgname=ocaml-oasis
pkgver=0.2.0
pkgrel=7
pkgdesc='Architecture for building OCaml libraries and applications.'
arch=('i686' 'x86_64')
url=("http://oasis.forge.ocamlcore.org")
license=('LGPL')
depends=('glibc' 'pcre')
makedepends=('ocaml'
             'ocaml-extlib'
             'ocaml-fileutils>=0.4.2'
             'ocaml-findlib'
             'ocaml-ocamlgraph'
             'ocaml-ocamlify'
             'ocaml-odn>=0.0.3'
             'ocaml-ounit'
             'ocaml-pcre'
             'ocaml-expect')
source=("http://forge.ocamlcore.org/frs/download.php/501/oasis-$pkgver.tar.gz"
        "ounit.patch")
md5sums=('e5d04bfe41eacd4f58a156784700a2ba'
         'ebf53334976b1050dfc9ed8a89bcd417')
options=(!strip)

build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"
  patch -p1 -i "$startdir/ounit.patch"
  make CONFIGUREFLAGS="--prefix /usr --destdir $pkgdir" build
}

package() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm755 "$OCAMLFIND_DESTDIR"
  make install
}
