# Maintainer: Joris Steyn <jorissteyn@gmail.com>
#
# This package conflicts with PEAR's phploc.
pkgname=phploc
pkgver=1.7.4
pkgrel=1
pkgdesc='A tool for quickly measuring the size of a PHP project'
arch=('any')
url='https://github.com/sebastianbergmann/phploc'
license=('BSD')
depends=('php')
makedepends=('php-composer')
source=()
md5sums=()

prepare() {
  cd "$srcdir"

  if [ -x composer-dir ]; then
    composercmd=update
  else
    mkdir composer-dir
    composercmd=install
  fi

  cd composer-dir

  # download everything with composer, we could also get a 
  # phploc tarball but then we would still have to fetch all
  # dependencies.
  echo '{
    "require-dev": {
        "phploc/phploc": "'$pkgver'"
    },
    "minimum-stability": "dev"
  }'> composer.json

  # composer install (or update if build directory exists)
  composer $composercmd --prefer-dist -vv --optimize-autoloader \
    --no-scripts --no-custom-installers --no-interaction

  # this seems only applicable when installing through PEAR
  #sed -i 's#@php_bin@#'`which php`'#g' vendor/phploc/phploc/phploc.php
}

package() {
  cd "$srcdir/composer-dir"

  # /usr/share/phploc contains all source files and dependencies 
  install -d "$pkgdir"/usr/{share/phploc,bin}
  cp -dr --no-preserve=ownership vendor/* "$pkgdir"/usr/share/phploc/

  # complicated path, read it like this:
  #   /usr/share/[phploc-composer-dir]/[actual vendor]/[package]/executable.php
  ln -s ../share/phploc/phploc/phploc/phploc.php "$pkgdir"/usr/bin/phploc
}

# vim:set ts=2 sw=2 et:
