# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
#
# (Added from ibus package)
# Contributor: Rainy <rainylau@gmail.com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Brad Fanella <bradfanella@archlinux.us>

_pkgname=ibus
pkgname=lib32-$_pkgname
pkgver=1.4.99.20121109
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux (32-bit libraries)"
arch=(x86_64)
url=http://ibus.googlecode.com
license=(LGPL)
depends=($_pkgname lib32-gtk{2,3})
makedepends=(dconf gcc-multilib gobject-introspection intltool iso-codes python2-{gobject,xdg})
options=(!emptydirs !libtool)
install=$_pkgname.install
source=($url/files/$_pkgname-$pkgver.tar.gz)
sha256sums=('8d84e586f62ed07843cb9c3294f912994b257fd2ca9457c0a2c2eea7bc9e8111')
sha512sums=('3e6e785ab38fdce4da68d780c48e08bcff247cf55df8c08a5fc0ed4754079d27bea43fd9ab1e4b456bebfdc6efb01923c3dfa12090367486c629ff0ec88b44c5')

build() {
    export PYTHON=python2
    export CC='gcc -m32'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

    cd "$srcdir"/$_pkgname-$pkgver/
    ./configure \
        --prefix=/usr \
        --libdir=/usr/lib32 \
        --libexecdir=/usr/lib32/$_pkgname \
        --sysconfdir=/etc \
        --disable-gconf \
        --enable-dconf \
        --disable-memconf \
        --enable-ui \
        --disable-nls
    make
}

package() {
    cd "$srcdir"/$_pkgname-$pkgver/
    make DESTDIR="$pkgdir" install
    rm -rf "$pkgdir"/{etc,usr/{bin,include,lib,lib32/ibus,share}}
}
