# Contributions by: Timoth√©e Ravier <tim@siosm.fr>, Jonas Heinrich <onny@project-insanity.org>
# Maintainer: Ruben Kelevra <ruben@freifunk-nrw.de>

pkgname=etherpad-lite
pkgver=1.4.1
pkgrel=2
pkgdesc="Lightweight fork of etherpad based on javascript"
arch=(any)
url="http://etherpad.org"
_watch=('http://etherpad.org','Documentation <small>v([\d.]*)</small>')
license=('GPL2')
depends=('curl' 'python2' 'openssl' 'nodejs')
optdepends=('sqlite: to use sqlite as databse'
            'mariadb: to use mysql/mariadb as database'
            'postgresql: to use postgresql as database')
conflicts=('etherpad-lite-git')
backup=(usr/share/webapps/etherpad-lite/settings.json)
install='etherpad-lite.install'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ether/${pkgname}/archive/${pkgver}.tar.gz"
	"etherpad-lite.service"
	"etherpad-lite.tmpfile.d"
	"etherpad-lite-1.4.1-npm2.patch::https://github.com/ether/etherpad-lite/commit/94968e69be03a78f6202b83fc958be292244f677.patch")
sha512sums=('f315d6ffe4d0bc7fd113121634ff8730ce1203bb7de431910d011dde9d86c971691daa30b77cdbd89a60bc94260b32af2fc72522f5fbe4baf4cd3511ae1c96d5'
	    '7b775171da97a3e7ad44a2b1b319970fd307a88e90a171c49bf70d2382767175e98bb21c7054e38ee6c066bd2dfadf94d28a9ff31d4f21145ec4441caa13c4d7'
	    '8172f4fc2d1f394046cb9215b50917cc5f42559d030b408c5c1919e3b62e53776d56ec00674260a332533545aa08160993e8c61663ce2786c9d97a2dafd560e4'
	    '2921eccb02589495c7d2a8c99bdee2d1eefad739ac1bd5248898b05c65898321836833712d7cf9cbf3fab7ed9a7f98b8d4152b7d95551dcd9c48581e401d80e5')

prepare() {
    cd "$pkgname-$pkgver"
    sed -i "s/9001/19001/" settings.json.template
    echo "applying patch to support npm 2.x..."
    patch -p1 -i "${srcdir}/etherpad-lite-1.4.1-npm2.patch"
}

build() {
    cd "$pkgname-$pkgver"
    export PYTHON=python2
    ./bin/installDeps.sh
}

package() {
    cd "$pkgname-$pkgver"
    echo 'cleaning up unneeded files...'
    rm -r start.bat var
    echo 'move files...'
    install -dm 755 "${pkgdir}"/usr/share/webapps/${pkgname}
    cp -a . "${pkgdir}"/usr/share/webapps/${pkgname}
    install -Dm644 "${srcdir}"/etherpad-lite.service "${pkgdir}"/usr/lib/systemd/system/etherpad-lite.service
    install -Dm644 "${srcdir}"/etherpad-lite.tmpfile.d "${pkgdir}"/usr/lib/tmpfiles.d/etherpad-lite.conf
    install -D LICENSE 	"${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
