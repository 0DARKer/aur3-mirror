# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

_pkgname=libsigc++2.0
pkgname=mingw32-${_pkgname}
pkgver=2.2.10
pkgrel=1
arch=(any)
pkgdesc="Full callback system for use in widget libraries (mingw32)"
depends=('mingw32-runtime')
makedepends=(mingw32-gcc)
license=(LGPL)
options=(!strip)
url="http://libsigc.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/libsigc++/2.2/libsigc++-${pkgver}.tar.xz)
sha256sums=('8ceb6f2732f5399ef50d5b70f433d49945a12e0900b8f9f43c135866a2e5bf47')

build()
{
  cd ${srcdir}/libsigc++-$pkgver

  export CFLAGS="-O2 -pipe -march=i686"
  export CXXFLAGS="$CFLAGS"
  export CPPFLAGS="$CPPFLAGS -D_REENTRANT"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig"
  unset LDFLAGS PKG_CONFIG_PATH

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-documentation --enable-static

  make
}

package() {
  cd ${srcdir}/libsigc++-$pkgver
  make install DESTDIR=${pkgdir}

  cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
}
