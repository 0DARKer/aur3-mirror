# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

_pkgname=libsigc++2.0
pkgname=mingw32-${_pkgname}
pkgver=2.2.8
pkgrel=1
arch=(i686 x86_64)
pkgdesc="Full callback system for use in widget libraries (mingw32)"
depends=('mingw32-runtime')
makedepends=(mingw32-gcc)
license=(LGPL)
options=(!strip)
url="http://libsigc.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/libsigc++/2.2/libsigc++-${pkgver}.tar.bz2)
sha256sums=('0fd75fd8249e09ae2fd3ffd20c3fbaea11b47d71ba5ba2340ed98b6bdca40ea8')

build()
{
  cd ${srcdir}/libsigc++-$pkgver

  export CPPFLAGS="$CPPFLAGS -D_REENTRANT"
  export PKG_CONFIG_PATH="/usr/i486-mingw32/lib/pkgconfig"
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-documentation --enable-static

  make
}

package() {
  cd ${srcdir}/libsigc++-$pkgver
  make install DESTDIR=${pkgdir}

  cd $startdir/pkg
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
}
