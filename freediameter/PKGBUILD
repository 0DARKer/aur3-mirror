# Maintainer: Robert Knauer <robert@privatdemail.net>

pkgname=freediameter
pkgver=1.1.3
pkgrel=2
pkgdesc="An open source implementation of the diameter protocol"
arch=('i686' 'x86_64')
url="http://www.freediameter.net/"
license=('BSD')
depends=('gnutls' 'libidn')
makedepends=('cmake' 'bison' 'flex')
install="${pkgname}.install"
source=(
  'freediameter'
  "http://www.freediameter.net/hg/freeDiameter/archive/${pkgver}.tar.gz"
  'config.c.patch'
)
sha256sums=(
  'c3fd1ba01a21fb12f24355469dc923e7faec50f469fb85509daabda10ce18500'
  '293cee70c7d2ab959396329c7e7e0a30841aba19c99f2a26fcf9600dd191cda4'
  'b73433f3e810b7edd0d79b3e1645f33dad0173c736b8c90173557e59d9c5a95b'
)
conflicts=('freediameter-hg')

build() {
  cd "${srcdir}/freeDiameter-${pkgver}"
  # patch config.c to support newer gnutls versions (check of the own certificate is disabled now, so you should be sure that it's valid)
  patch "libfdcore/config.c" "${srcdir}/config.c.patch"
  # prepare build dir
  rm -rf build
  mkdir build
  cd build
  # build
  cmake -DCMAKE_INSTALL_PREFIX:STRING=/usr -DDEFAULT_CONF_PATH:STRING=/etc/freeDiameter -DDISABLE_SCTP:BOOL=ON ../
  make
}

package() {
  cd "${srcdir}/freeDiameter-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  # install configuration files
  install -D -m 0644 "${srcdir}/freeDiameter-${pkgver}/doc/freediameter.conf.sample" "${pkgdir}/etc/freeDiameter/freeDiameter.conf.sample"
  rm -f "${srcdir}/freeDiameter-${pkgver}/doc/freediameter.conf.sample"
  for _f in ${srcdir}/freeDiameter-${pkgver}/doc/*.conf.sample; do
    install -D -m 0644 "${_f}" "${pkgdir}/etc/freeDiameter/"
  done
  install -D -m 0755 "${srcdir}/freediameter" "${pkgdir}/etc/rc.d/freediameter"
}
