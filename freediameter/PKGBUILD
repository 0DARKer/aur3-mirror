# Maintainer: Robert Knauer <robert@privatdemail.net>

pkgname=freediameter
pkgver=1.1.5
pkgrel=1
pkgdesc="An open source implementation of the diameter protocol"
arch=('i686' 'x86_64')
url="http://www.freediameter.net/"
license=('BSD')
depends=('gnutls' 'libidn')
makedepends=('cmake' 'bison' 'flex')
install="${pkgname}.install"
source=(
  "http://www.freediameter.net/hg/freeDiameter/archive/${pkgver}.tar.gz"
  'freediameter.service'
)
sha256sums=(
  'c1ea8302fbcf8c6c0ef0cfa2023ee84ff43478842fb8c838c04ecdb8657b2009'
  'd6bea4c108526222d1096a031eb8d1ff168eb73b0a22747a6efe85098759d52f'
)
conflicts=('freediameter-hg')

build() {
  cd "${srcdir}/freeDiameter-${pkgver}"
  # prepare build dir
  rm -rf 'build'
  mkdir 'build'
  cd 'build'
  # build
  cmake -DCMAKE_INSTALL_PREFIX:STRING=/usr -DDEFAULT_CONF_PATH:STRING=/etc/freediameter -DDISABLE_SCTP:BOOL=ON ..
  make
}

package() {
  cd "${srcdir}/freeDiameter-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  # install systemd service file
  install -Dm 0644 "${srcdir}/freediameter.service" "${pkgdir}/usr/lib/systemd/system/freediameter.service"
  # install configuration files
  install -dm 0755 "${pkgdir}/etc/freediameter/samples/single_host/"
  install -m 0644 "../doc/"*".conf.sample" "${pkgdir}/etc/freediameter/samples/"
  install -m 0644 "../doc/"*".py.sample" "${pkgdir}/etc/freediameter/samples/"
  install -m 0644 "../doc/"*".sql" "${pkgdir}/etc/freediameter/samples/"
  install -m 0644 "../doc/single_host/"* "${pkgdir}/etc/freediameter/samples/single_host/"
}
