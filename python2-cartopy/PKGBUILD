# Maintainer: David Verelst <david dott verelst hat gmail com>
# Contributor: richli
pkgname=python2-cartopy
pkgver=0.10.0
pkgrel=2
pkgdesc="A cartographic Python library with matplotlib support"
arch=('i686' 'x86_64')
url="http://scitools.org.uk/cartopy"
license=('LGPL3')
depends=('python2' 'proj' 'geos' 'python2-shapely' 
         'python2-pyshp' 'python2-scipy' 'python2-matplotlib')
makedepends=('cython2')
#checkdepends=('pep8-python2' 'python2-nose' 'python2-mock')
source=("https://github.com/SciTools/cartopy/archive/v${pkgver}.tar.gz")
md5sums=('5f9f2bdea1fad2969482332c9701bb23')
sha1sums=('4ca0f742a12d2d5ccb95ee724dc0fdac3d014ff1')
sha256sums=('607c95ed3e2d62a7dd43db81d07e2280d003fb31868051549e1871b4083b2a59')

build() {
    cd "$srcdir"/cartopy-$pkgver
    python2 setup.py build_ext --inplace
    python2 setup.py build
}

# As of 2014-02-05, I had 2/194 tests fail, due to some text rendering. I don't know if it's an issue with my font setup.
# confirmed, 1 failure and 1 error, but I didn't look into it any further ...
#check() {
#    cd "$srcdir"/cartopy-$pkgver
#    nosetests2 --with-doctest -sv
#}

package() {
    cd "$srcdir"/cartopy-$pkgver
    python2 setup.py install --root="$pkgdir"/ --optimize=1

    sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
        $(find "${pkgdir}" -name '*.py')

    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:

