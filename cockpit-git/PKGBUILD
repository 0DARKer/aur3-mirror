# Maintainer: Mark Constable <markc@renta.net>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Iwan Timmer <irtimmer@gmail.com>

pkgname=cockpit-git
pkgver=r151.d6e6fba
pkgrel=1
pkgdesc='A systemd web based user interface for Linux servers'
arch=(i686 x86_64)
url='http://www.cockpit-project.org/'
license=(GPL21)
depends=(libssh krb5 sshpass accountsservice perl-json udisks2 json-glib)
makedepends=(git intltool python2-pyscss gtk-doc perl-javascript-minifier-xs gobject-introspection networkmanager storaged)
source=(git://github.com/cockpit-project/cockpit
        cockpit.pam)
sha1sums=('SKIP'
          '9d1268e4e9ad6aca14d2afd5e64176f497cc027d')

pkgver() {
  cd cockpit
  echo r$(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd cockpit

  # Build failed with --as-needed
  export LDFLAGS="${LDFLAGS//,--as-needed}"

  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

check() {
  cd cockpit
#  make check
}

package() {
  cd cockpit
  make DESTDIR="$pkgdir" install

  # Overwrite cockpit pam file with ArchLinux supported pam file
  cp ../cockpit.pam $pkgdir/etc/pam
}
