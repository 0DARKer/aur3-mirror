# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Iwan Timmer <irtimmer@gmail.com>

pkgname=cockpit-git
pkgver=r129.0f43e52
pkgrel=1
pkgdesc='a user interface for Linux servers'
arch=(i686 x86_64)
url='http://www.cockpit-project.org/'
license=(GPL21)
depends=(libssh krb5 sshpass accountsservice perl-json udisks2 json-glib)
makedepends=(git intltool python2-pyscss gtk-doc perl-javascript-minifier-xs gobject-introspection networkmanager storaged)
optdepends=('networkmanager: network support'
            'storaged: lvm/raid support')
source=(git://github.com/cockpit-project/cockpit
        cockpit.pam)
sha1sums=('SKIP'
          'ddade6e01627e42dabaf0f4e905d13904fca6d3b')

pkgver() {
  cd cockpit
  echo r$(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd cockpit

  # Use python2 instead of python3
  sed 's/python/python2/g' -i src/web/sassify-jqm
}

build() {
  cd cockpit

  # Build failed with --as-needed
  export LDFLAGS="${LDFLAGS//,--as-needed}"

  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

check() {
  cd cockpit
#  make check
}

package() {
  cd cockpit
  make DESTDIR="$pkgdir" install

  # Overwrite cockpit pam file with ArchLinux supported pam file
  cp ../cockpit.pam $pkgdir/etc/pam
}
