# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lauri Niskanen <ape@ape3000.com>
# Contributor: Sebastian.Salich@gmx.de
# Contributor: Doc Angelo

pkgname=lib32-libmumble
pkgver=1.2.3
pkgrel=2
arch=(x86_64)
pkgdesc="A voice chat application similar to TeamSpeak (32-bit overlay library)"
license=(GPL)
depends=("mumble=$pkgver" lib32-libgl)
makedepends=(mesa gcc-multilib)
options=('!libtool') 
url="http://mumble.sourceforge.net/"
source=("http://downloads.sourceforge.net/mumble/mumble-$pkgver.tar.gz")
md5sums=('55804e3dfe98b16eeb40abca6c254625')

build() {
  cd $srcdir/mumble-$pkgver/overlay_gl

  qmake overlay_gl.pro QMAKE_CFLAGS+="-m32" QMAKE_LFLAGS+="-m32"
  make
}

package() {
  cd $srcdir/mumble-$pkgver

  # lib stuff
  install -m755 -D ./release/libmumble.so.$pkgver $pkgdir/usr/lib32/mumble/libmumble.so.$pkgver
  ln -s libmumble.so.$pkgver $pkgdir/usr/lib32/mumble/libmumble.so
  ln -s libmumble.so.$pkgver $pkgdir/usr/lib32/mumble/libmumble.so.1
  ln -s libmumble.so.$pkgver $pkgdir/usr/lib32/mumble/libmumble.so.1.2
}

# vim: sw=2:ts=2 et:
