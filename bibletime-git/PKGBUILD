# Maintainer: SanskritFritz (gmail)
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias T. <OldShatterhand at gmx-topmail dot de>

pkgname=bibletime-git
pkgver=20130122
pkgrel=1
pkgdesc="Bible study tool for KDE4 - sword-svn compatible branch"
arch=('x86_64' 'i686')
url="http://www.bibletime.info/"
license=('GPL2')
depends=('qt' 'sword' 'openssl' 'clucene' 'qtwebkit')
makedepends=('cmake' 'git')
provides=('bibletime')
conflicts=('bibletime')
install=$pkgname.install

_gitroot="git://gitorious.org/bibletime/bibletime.git"
_gitname="bibletime"

build() {
	cd "$srcdir"

	if [ -d "$srcdir/$_gitname" ] ; then
		cd $_gitname && git pull origin
	else
		git clone $_gitroot
	fi
	cd "$srcdir/$_gitname"
	git checkout sword-svn-compat

	CXXFLAGS+=" -fpermissive"
	cmake \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DCMAKE_BUILD_TYPE=Release \
	  "$srcdir/$_gitname"

	make
}

package() {
	cd "$srcdir/$_gitname"

	make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
