# Maintainer: Piotr Rogo≈ºa <rogoza dot piotr at gmail dot com>
# Contributor: Jonathas Rodrigues
# vim:set ts=2 sw=2 et ft=sh tw=100: expandtab

_author='A/AD/ADAMK'
_perlmod=ORLite
pkgname=perl-orlite
pkgver=1.96
pkgrel=1
pkgdesc='ORLite - Extremely light weight SQLite-specific ORM'
arch=('any')
url="http://search.cpan.org/~adamk/ORLite"
license=('GPL' 'PerlArtistic')
depends=(
perl-file-remove 
perl-dbd-sqlite 
perl-params-util 
)
makedepends=(
perl-test-script
)
options=(!emptydirs)
source=("http://cpan.perl.org/modules/by-authors/id/$_author/$_perlmod-$pkgver.tar.gz")

build(){
  cd "$srcdir/$_perlmod-$pkgver"

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}
check(){
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}
package(){
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR=$pkgdir/

  # remove perllocal.pod and .packlist
  find $pkgdir -name perllocal.pod -delete
  find $pkgdir -name .packlist -delete
}

md5sums=('0d2ce1679e7da739780dc4a22f47317b')
