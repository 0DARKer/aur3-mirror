# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=qtspim
pkgver=9.0.4
pkgrel=1
pkgdesc="New user interface for spim, a MIPS simulator"
arch=('i686' 'x86_64')
url="http://spimsimulator.sourceforge.net/"
license=('BSD')
depends=('qt')
makedepends=('subversion')
source=()
md5sums=()

__svntrunk="https://spimsimulator.svn.sourceforge.net/svnroot/spimsimulator"
__svnmod="spimsimulator"
__svnrev=526

build() {
  cd "$srcdir"
  msg "Connecting to SVN server......."
  if [ -d $__svnmod/.svn ]; then
    cd $__svnmod && svn up -r $__svnrev
  else
    svn co $__svntrunk --config-dir ./ -r $__svnrev $__svnmod
  fi
  msg "SVN checkout done or server timeout"

  if [ -e "$srcdir"/$__svnmod-build ]; then
    rm -rf "$srcdir"/$__svnmod-build
  fi
  cp -a "$srcdir"/$__svnmod "$srcdir"/$__svnmod-build

  msg "Starting make..."
  cd "$srcdir"/$__svnmod-build/QtSpim

  unset MAKEFLAGS
  qmake
  make
}

package() {
  cd "$srcdir"/$__svnmod-build/QtSpim
  install -d -m755 "$pkgdir"/usr/share/qtspim
  cp -r help "$pkgdir"/usr/share/qtspim/
  rm -rf "$pkgdir"/usr/share/qtspim/help/.svn
  install -D -m755 QtSpim "$pkgdir"/usr/bin/QtSpim

  cd "$srcdir"/$__svnmod-build
  install -D -m644 Documentation/spim.man "$pkgdir"/usr/share/man/man1/qtspim.1
  install -D -m644 Setup/qtspim_debian_deployment/qtspim.desktop "$pkgdir"/usr/share/applications/qtspim.desktop
  install -D -m644 Setup/qtspim_debian_deployment/copyright "$pkgdir"/usr/share/licenses/$pkgname/copyright
  install -D -m644 Setup/NewIcon48x48.png "$pkgdir"/usr/share/qtspim/qtspim.png
}

# vim:set ts=2 sw=2 et:
