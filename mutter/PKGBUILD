# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>
# Contributor: lh <jason52lh AT gmail.com>
# Based on the mutter package trunk

pkgname=mutter
pkgver=2.91.91
pkgrel=1
pkgdesc="A window manager for GNOME3"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
depends=('startup-notification' 'gconf>=2.28.0' 'zenity>=2.28.0' 'libcanberra-gtk3' 'clutter' 'gobject-introspection>=0.9.8' 'gtk3')
makedepends=('gnome-doc-utils>=0.16.0' 'glproto' 'intltool')
options=(!libtool !emptydirs)
install=mutter.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.91/${pkgname}-${pkgver}.tar.bz2")
sha256sums=(`wget ${source/.t*}.sha256sum -qO - | grep tar.bz2 | cut -d " " -f1`)

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/mutter --localstatedir=/var --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain mutter "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}