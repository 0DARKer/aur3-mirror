# Maintainer: ponsfoot <cabezon dot hashimoto at gmail dot com>

pkgname=libkkc
pkgver=0.2.3
pkgrel=4
pkgdesc="Japanese Kana Kanji conversion library"
arch=('i686' 'x86_64')
url="https://bitbucket.org/libkkc"
license=('GPL')
options=(!libtool)
depends=('libkkc-data' 'skk-jisyo' 'marisa' 'json-glib' 'libgee06')
makedepends=('vala0.16' 'pkg-config' 'gobject-introspection' 'marisa-python2')
changelog=ChangeLog
source=(https://bitbucket.org/libkkc/libkkc/downloads/${pkgname}-${pkgver}.tar.gz)
sha1sums=('65545d2d64324087fbe14472de7e2bb6dc233e91')

build() {
  cd "${pkgname}-${pkgver}"

  VALAC=/usr/bin/valac-0.16 \
  PYTHON=/usr/bin/python2 \
  ./configure --prefix=/usr --disable-static --disable-silent-rules
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m 644 README data/rules/README.rules COPYING \
    "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/"  
}
