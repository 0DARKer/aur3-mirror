# Maintainer: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca-git
pkgver=20101023
pkgrel=1
pkgdesc="A scriptable screen reader and magnifier"
arch=(i686 x86_64)
license=('LGPL')
url="http://www.gnome.org/projects/orca"
install=orca-git.install
options=('!libtool')
depends=('pygtk>=2.22.0' 'gnome-python>=2.28.1' 'dbus-python>=0.83.0' 'at-spi>=1.32.0' 'python-wnck' 'gnome-speech>=0.4.25' 'gnome-mag>=0.16.0' 'brltty' 'hicolor-icon-theme' 'pyxdg')
makedepends=('pkgconfig' 'intltool' 'gnome-common')
replaces=('gnopernicus')
conflicts=('orca')

_gitroot="git://git.gnome.org/orca"
_gitname="orca"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."
  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi
  msg "GIT checkout done or server timeout"

  msg "Starting make..."
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # BUILD HERE

  PYTHON=python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
  make DESTDIR=${pkgdir} install
}
