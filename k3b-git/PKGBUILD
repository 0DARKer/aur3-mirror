#Maintainer: Dan Vratil <vratil@progdansoft.com>

pkgname=k3b-git
pkgver=20110116
pkgrel=1
pkgdesc='Feature-rich and easy to handle CD burning application (Git version)'
arch=('i686' 'x86_64')
license=('GPL')
url='http://k3b.sourceforge.net'
depends=('kdebase-runtime' 'kdemultimedia-kioslave' 'libsamplerate' 'libmad'
         'ffmpeg' 'taglib' 'libmpcdec' 'libdvdread' 'cdrdao' 'cdrkit'
         'shared-mime-info' 'libxft' 'flac')
makedepends=('git' 'cmake' 'automoc4' )
install="k3b.install"
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager:    for vcd burning support'
            'transcode:    for advanced mpeg conversion support'
            'emovix:       for bootable multimedia cd/dvd support')
options=('!libtool')
conflicts=('k3b')

_gitroot="git://git.kde.org/k3b"
_gitname="k3b"

build() {
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  if [[ -d "$srcdir/$_gitname-build" ]]; then
       msg "Cleaning the previous build directory..."
       rm -rf "$srcdir/$_gitname-build"
  fi
 
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
 
  cd "$srcdir/$_gitname-build"

  sed -i 's/^add_subdirectory(\ doc\ )/#add_subdirectory(\ doc\ )/' CMakeLists.txt

  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr \
	   -DK3B_ENABLE_HAL_SUPPORT=OFF
  make
}

package() {
  cd $_gitname-build

  make DESTDIR="$pkgdir" install
}	
