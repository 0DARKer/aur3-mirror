#Maintainer: Dan Vratil <vratil@progdansoft.com>

pkgname=k3b-git
pkgver=2.0.80.20140107.34c7d5e
pkgrel=1
pkgdesc='Feature-rich and easy to handle CD burning application (Git version)'
arch=('i686' 'x86_64')
license=('GPL')
url='http://k3b.sourceforge.net'
depends=('kdebase-runtime' 'libkcddb' 'libsamplerate' 'libmad'
         'ffmpeg' 'taglib' 'libmpcdec' 'libdvdread' 'cdrdao' 'cdrkit'
         'shared-mime-info' 'libxft' 'flac' 'musicbrainz')
makedepends=('git' 'cmake' 'automoc4' )
install="k3b.install"
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager: for vcd burning support'
            'transcode: for advanced mpeg conversion support'
            'emovix: for bootable multimedia cd/dvd support')
options=('!libtool')
provides=('k3b')
conflicts=('k3b')

source=('git://anongit.kde.org/k3b.git'
        'http://downloads.sourceforge.net/k3b/k3b-2.0.2.tar.bz2')
sha1sums=('SKIP'
          '8b30a4d07942e82559b01bc07dea6bcf2defd532')
_gitname="k3b"
noextract=('k3b-2.0.2.tar.bz2')

pkgver() {
  cd "${_gitname}"
  _major_v="$(cat CMakeLists.txt | grep -num1 K3B_VERSION_MAJOR | tr -d ')' | cut -d ' ' -f4)"
  _minor_v="$(cat CMakeLists.txt | grep -num1 K3B_VERSION_MINOR | tr -d ')' | cut -d ' ' -f4)"
  _patch_v="$(cat CMakeLists.txt | grep -num1 K3B_VERSION_RELEASE | tr -d ')' | cut -d ' ' -f2)"
  echo "$(echo -n ${_major_v}.${_minor_v}.${_patch_v}).$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"

  # extract PO (i18n) files from the K3B 2.0.2 source packages and add in CMakeFiles
  rm -fr k3b-2.0.2
  rm -fr po
  bsdtar -xf "${srcdir}/k3b-2.0.2.tar.bz2" k3b-2.0.2/po
  mv k3b-2.0.2/po .
  rm -fr k3b-2.0.2

  echo '
include(MacroOptionalAddSubdirectory)
macro_optional_add_subdirectory( po )
' >> CMakeLists.txt

  sed '1icmake_policy(SET CMP0002 OLD)' -i po/CMakeLists.txt

}

build() {
  cd "${_gitname}"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DQT_QMAKE_EXECUTABLE=qmake-qt4 -DK3B_ENABLE_HAL_SUPPORT=OFF -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
