# Contributor: fila pruda.com
# Contributor: tuxce <tuxce.net@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: BlueRaven <blue@ravenconsulting.it>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=pure-ftpd
pkgver=1.0.36
pkgrel=6
pkgdesc="A fast, production quality, standards-conformant FTP server"
arch=('i686' 'x86_64')
url="http://www.pureftpd.org/"
license=('custom')
depends=('openssl' 'libmariadbclient')
backup=('etc/pure-ftpd.conf')
source=("ftp://pureftpd.org/pub/${pkgname}/releases/${pkgname}-${pkgver}.tar.bz2"
	'pure-ftpd'
	'pure-ftpd.service')



build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr \
	   --bindir=/usr/bin \
	   --sbindir=/usr/bin \
	   --with-puredb \
	   --with-ftpwho \
	   --with-altlog \
	   --with-tls \
	   --with-mysql \
	   --with-quotas
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -Dm755 ${srcdir}/pure-ftpd ${pkgdir}/etc/rc.d/pure-ftpd
	install -Dm644 ${srcdir}/pure-ftpd.service ${pkgdir}/usr/lib/systemd/system/pure-ftpd.service
	
	install -Dm644 configuration-file/pure-ftpd.conf ${pkgdir}/etc/pure-ftpd.conf
	sed -i 's|#PIDFile                     /var/run/pure-ftpd.pid|PIDFile                     /run/pure-ftpd.pid|' ${pkgdir}/etc/pure-ftpd.conf
	sed -i 's|# /usr/sbin/pure-config.pl /usr/etc/pure-ftpd.conf|# /usr/bin/pure-config.pl /etc/pure-ftpd.conf|' ${pkgdir}/etc/pure-ftpd.conf
	
	install -Dm755 configuration-file/pure-config.pl ${pkgdir}/usr/bin/pure-config.pl
	
	#install license
	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	
	
}
md5sums=('7899c75c1fed7dbad0352eb31080e066'
         'c485e022536c2468854a37f0339b72aa'
	 '4221c92095097546d6ebc8483f1ef86a')

