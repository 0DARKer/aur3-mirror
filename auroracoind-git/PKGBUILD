# Maintainer: Daniel Spies <daniel dot spies at fuceekay dot com>

pkgname=auroracoind-git
_gitname=auroracoin-project
pkgver=0.0.1
pkgrel=1
pkgdesc="Cryptocurrency for Iceland (daemon)"
arch=('x86_64' 'i686')
url="http://www.auroracoin.org/"
license=('MIT')
provides=('auroracoind')
conflicts=('auroracoind')
depends=('miniupnpc' 'boost-libs' 'openssl')
makedepends=('git' 'boost' 'gcc' 'make')
source=("git+https://github.com/baldurodinsson/${_gitname}.git")
sha256sums=(SKIP)

_makefile_unix=makefile.unix

pkgver() {
	cd ${_gitname}
	git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd ${_gitname}/src
	make USE_UPNP=1 USE_SSL=1 $MAKEFLAGS -f ${_makefile_unix} AuroraCoind
}

package() {
	cd ${_gitname}
	install -Dm755 src/AuroraCoind ${pkgdir}/usr/bin/auroracoind
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

