# Maintainer: SaultDon <sault.don gmail>
# Contributor: Lantald < lantald at gmx.com >
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Eric Forgeot < http://esclinux.tk >
pkgname=qgis
pkgver=2.2.0
pkgrel=1
pkgdesc='Quantum GIS is a Geographic Information System (GIS) that supports vector, raster & database formats'
url='http://qgis.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libmysqlclient'
         'postgresql-libs'
         'sqlite3'
         'jasper'
         'curl'
         'qwt>=6.1.0'
         'qwtpolar>=1.1.0'
         'pyqt'
         'python2'
         'python2-pyqt>=4.10.4dev'
         'python2-qscintilla'
         'python2-psycopg2'
         'giflib'
         'cfitsio'
         'proj'
         'gdal'
         'geos'
         'flex'
         'bison'
         'libspatialite'
         'spatialindex')
makedepends=('cmake'
             'grass'
             'gsl'
             'postgis'
             'netcdf'
             'fcgi'
             'python2-sip>=4.15.3'
             'txt2tags')
optdepends=('osgearth: OpenSceneGraph'
            'taudem: TauDEM for processing toolbox integration')
install=${pkgname}.install
source=("http://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2"
        "qgis.sh")
md5sums=('0390e50c8a38c6cbef1720fc11aeabe6'
         '488a57456d9f3ca5965e0aed617cd8fa')

build() {
  # Fix insecure RPATH is weird, but just works ;)
  # echo "os.system(\"sed -i '/^LFLAGS/s|-Wl,-rpath,.\+ ||g' gui/Makefile core/Makefile\")" >> python/configure.py.in

  cd "${srcdir}/${pkgname}-${pkgver}"

  if [ -d "build" ]; then
    rm -rf build
  fi
  mkdir build
  cd build

  cmake ../ \
    -DENABLE_TESTS=OFF \
    -DQWT_INCLUDE_DIR=/usr/include/qwt \
    -DQWT_LIBRARY=/usr/lib/libqwt.so \
    -DQWTPOLAR_INCLUDE_DIR=/usr/include/qwt \
    -DQWTPOLAR_LIBRARY=/usr/lib/libqwtpolar.so \
    -DWITH_INTERNAL_QWTPOLAR=FALSE \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGRASS_PREFIX=/opt/grass \
    -DQGIS_MANUAL_SUBDIR=share/man \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DPYTHON_SITE_PACKAGES_DIR=/usr/lib/python2.7/site-packages \
    -DPYTHON_INCLUDE_PATH=/usr/include/python2.7 \
    -DQT_QMAKE_EXECUTABLE=qmake4 \
    -DWITH_MAPSERVER=TRUE

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR=${pkgdir} install
  
  # create a more user-friendly application name link
  ln -s /usr/bin/qgis "${pkgdir}/usr/bin/quantum-gis"
  
  # install some freedesktop.org compatibility
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/debian/qgis.desktop" \
                      "${pkgdir}/usr/share/applications/qgis.desktop"
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/debian/qbrowser.desktop" \
                      "${pkgdir}/usr/share/applications/qbrowser.desktop"
  install -dm755 "${pkgdir}/usr/share/pixmaps" \
                      "${pkgdir}/usr/share/mimelnk/application"
  for mime in "${srcdir}/${pkgname}-${pkgver}/debian/mime/application/"*.desktop
    do install -m644 "${mime}" \
                      "${pkgdir}/usr/share/mimelnk/application"
  done
  ln -s /usr/share/qgis/images/icons/qgis-icon.png "${pkgdir}/usr/share/pixmaps/qgis-icon.png"
  ln -s /usr/share/qgis/images/icons/qgis-mime-icon.png "${pkgdir}/usr/share/pixmaps/qgis-mime-icon.png"
  
  # enable qgis for python by setting PYTHONPATH
  install -Dm644 "${srcdir}/qgis.sh" \
                      "${pkgdir}/etc/profile.d/qgis.sh"
}