# Maintainer: Mitch Bigelow <ipha00@gmail.com>

pkgname=xfwm4-tiling
_realname=xfwm4
pkgver=4.10.0
pkgrel=2
pkgdesc="Xfce window manager with corner tiling patch"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=("libxfce4ui" "libwnck" "hicolor-icon-theme")
makedepends=('pkgconfig' 'intltool')
provides=("${_realname}=${pkgver}")
conflicts=("${_realname}")
replaces=("${_realname}")
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${_realname}/4.10/${_realname}-${pkgver}.tar.bz2
		${pkgname}-${pkgver}-${pkgrel}.patch
		xfwm4-4.10.0-implement-NET_WM_MOVERESIZE_CANCEL.patch)
sha1sums=(	'1549954949b5e1f38a2279a39a477b52bb5339f2'
			'1619ea1a33500048fa7b7498980d2fe9afdb3e8f'
			'32dd678f8477a5b30d17641a0d75c5a54c4dca52')

build() {

	cd ${srcdir}/${_realname}-${pkgver}

	# Fix https://bugzilla.xfce.org/show_bug.cgi?id=8949 (FS#29970)
	patch -Np1 -i "$srcdir/xfwm4-4.10.0-implement-NET_WM_MOVERESIZE_CANCEL.patch"

	# Corner tiling patch
	patch -p1 < ${srcdir}/${pkgname}-${pkgver}-${pkgrel}.patch

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-static \
		--enable-startup-notification \
		--enable-randr \
		--enable-compositor \
		--enable-xsync \
		--disable-debug

	make
}

package() {
	cd ${srcdir}/${_realname}-${pkgver}
	make DESTDIR=${pkgdir} install
}
