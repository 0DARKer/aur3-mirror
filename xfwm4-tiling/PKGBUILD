# Maintainer: Simon Schneider <schneida@tridex.net>
pkgname=xfwm4-tiling
_realname=xfwm4
pkgver=4.8.2
pkgrel=1
pkgdesc="Xfce window manager"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=("libxfce4ui" "libwnck" "hicolor-icon-theme")
makedepends=('pkgconfig' 'intltool')
provides=("${_realname}=${pkgver}")
conflicts=("${_realname}")
replaces=("${_realname}")
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${_realname}/4.8/${_realname}-${pkgver}.tar.bz2 ${_realname}-${pkgver}-tiling-${pkgrel}.patch)
md5sums=('41b730d5abf39a69fc3b221728ddaa75'
         'ba35bfcfbe59a1c72b5e35afec34aff8')
build() {

  cd ${srcdir}/${_realname}-${pkgver}
  patch -p1 < ${srcdir}/${_realname}-${pkgver}-tiling-${pkgrel}.patch
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-startup-notification \
	--enable-randr \
	--enable-compositor \
	--enable-xsync \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
