# Maintainer: Techlive Zheng <techlivezheng@gmail.com>
# Contributor: Matt Parnell/ilikenwf

# Note: the maintainer/contributor of this package is also the
# de-facto head developer of the nightingale project...
# this script thus gets some extra TLC since I use arch

# If you're reading this, WE NEED MORE DEVELOPERS!
# If you know C++, XUL, and are familiar with the mozilla SDK,
# contact parwok@gmail.com or join our irc on MozNet #nightingale
# or the forums at getnightingale.com/forum

pkgname=nightingale
pkgdesc="Community port of Songbird to be more Linux native, up to date, and opensource."
pkgver=1.11.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://getnightingale.com/"
license=('GPL2' 'MPL' 'BSD')
makedepends=('git' 'subversion' 'python2' 'unzip' 'libidl2' 'zip')
depends=('gtk2'
		 'libxt'
		 'sqlite3'
		 'gstreamer0.10'
		 'gstreamer0.10-base'
		 'gstreamer0.10-ffmpeg'
		 'gstreamer0.10-bad-plugins'
		 'gstreamer0.10-good-plugins'
		 'gstreamer0.10-ugly-plugins'
		)
optdepends=()
install=nightingale.install
provides=()
conflicts=('nightingale-git')

if [ "$CARCH" = x86_64 ]; then
	source=('nightingale.desktop'
			'nscore.h.patch'
			'http://downloads.sourceforge.net/project/ngale/1.11-Build-Deps/x86_64/linux-x86_64-1.11.tar.lzma'
			'http://downloads.sourceforge.net/project/ngale/1.11-Build-Deps/vendor-1.11.zip'
			'nightingale-1.11.0.tar.gz::http://github.com/nightingale-media-player/nightingale-hacking/tarball/nightingale-1.11.0')
	md5sums=('7741cc247648e95dd9dad8c953616757'
			'56fd5fce0969fa02fd6db76ace61f0e5'
			'c64fb572b8476607a0c3a0d8dd918416'
			'208c81bfd972ae4cb1f350688da304e6'
            'd5d8f96b067a09d327fcf781878edf18')
else
	source=('nightingale.desktop'
			'nscore.h.patch'
			'http://downloads.sourceforge.net/project/ngale/1.11-Build-Deps/i686/linux-i686-1.11.tar.lzma'
			'http://downloads.sourceforge.net/project/ngale/1.11-Build-Deps/vendor-1.11.zip'
			'nightingale-1.11.0.tar.gz::http://github.com/nightingale-media-player/nightingale-hacking/tarball/nightingale-1.11.0')
    md5sums=('7741cc247648e95dd9dad8c953616757'
            '56fd5fce0969fa02fd6db76ace61f0e5'
            'be1027548e15378f4136003261a74656'
            '208c81bfd972ae4cb1f350688da304e6'
            'd5d8f96b067a09d327fcf781878edf18')
fi

build() {
	cd ${srcdir}

	[ -d nightingale ] && rm -rf nightingale

	mv nightingale-media-player-nightingale-hacking-* nightingale

	cd ${srcdir}/$pkgname/dependencies

	# just in case we already have the symlinks there...to avoid errors
	rm -rf vendor &> /dev/null
	rm -rf linux-$CARCH &> /dev/null

	ln -s ${srcdir}/vendor ./
	ln -s ${srcdir}/linux-$CARCH ./

	patch ${srcdir}/linux-$CARCH/mozilla-1.9.2/release/include/nscore.h ${srcdir}/nscore.h.patch

    sed -i 's:#include <glib/gutils.h>:#include <glib.h>:g' ${srcdir}/$pkgname/components/mediacore/gstreamer/src/sbGStreamerService.cpp

	cd ${srcdir}/$pkgname

	# our gstreamer path, since we use system libs :)
	export GST_PLUGIN_PATH="/usr/lib/gstreamer-0.10"

	# use our own gstreamer libs
	echo 'ac_add_options --with-media-core=gstreamer-system' >> nightingale.config

	# force the buildscripts to use python2 to avoid breakage
	export PYTHON="/usr/bin/python2"

	# gcc 4.7 fix - be permissive, for now...I guess we should fix
	# the actual code sometime...
	export CXXFLAGS="-fpermissive"

	# clobber just in case we're rebuilding, as we've probably pulled new code down
	make -f nightingale.mk clobber

	# build it
	make -f nightingale.mk
}

package() {
	install -d --group=users ${pkgdir}/opt/nightingale

	cp -a ${srcdir}/$pkgname/compiled/dist/* ${pkgdir}/opt/nightingale/

	# copy the extensions first
	[ -d ${srcdir}/xpi-stage ] && rm -rf ${srcdir}/xpi-stage

	cp -a ${srcdir}/$pkgname/compiled/xpi-stage ${srcdir}

	chmod 755 ${pkgdir}/opt/nightingale/nightingale
	chmod 755 ${pkgdir}/opt/nightingale/nightingale-bin
	chmod 755 ${pkgdir}/opt/nightingale/xulrunner/xulrunner
	chmod 755 ${pkgdir}/opt/nightingale/xulrunner/xulrunner-bin
	chmod -R a+r ${pkgdir}/opt/nightingale

	install -D ${srcdir}/$pkgname/compiled/dist/chrome/icons/default/default.xpm \
		${pkgdir}/usr/share/pixmaps/nightingale.xpm
	install -D -m644 ${srcdir}/nightingale.desktop \
		${pkgdir}/usr/share/applications/nightingale.desktop

	msg "Note that the last.FM addon is in $srcdir/xpi-stage/audioscrobbler."
	msg "If you want to use it, you'll need to install it manually after running Nightingale."
	msg "There are more addons at http://addons.getnightingale.com"
	msg "--------------------------------------------------------------"
	msg "If you have issues, drop by http://forum.getnightingale.com or"
	msg "#nightingale on MozNet IRC"
}
