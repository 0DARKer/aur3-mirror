# Maintainer: Rich Li <rich@dranek.com>
# Patch for IPython completer provided by luismiguelgcg
pkgname=python-h5py
pkgver=2.2.1
pkgrel=3
pkgdesc="General-purpose Python bindings for the HDF5 library"
url="http://www.h5py.org"
arch=('i686' 'x86_64')
license=('BSD')
depends=('hdf5' 'python-numpy')
makedepends=('cython')
source=(https://github.com/h5py/h5py/archive/"$pkgver".tar.gz
        ipython-completion-fix.patch)
md5sums=('5743ea033b2042bbda0c0e12f8f76ecd'
         'e6bd5c325e1b5ac19d9cbf9034b8c9f4')
sha1sums=('001277268035146ded8a6c6d1c30a4a355b9d952'
          'c2c3cfc9ccf607805313e4c3035dbad57c6d5445')
sha256sums=('eed1cff0ceb551ddd463b0e978c01fc7dfaf5ffc217ea0deece6d802f3a61816'
            '93f11f1eac5ccdc229a770c0da3a8183f0d08ea1dacdfda3f5c322c166bf6e6e')
 
build() {
    cd "$srcdir/h5py-$pkgver"
    # Fix for IPython completer
    patch -Np1 -i "$srcdir/ipython-completion-fix.patch"
    python setup.py build
}

check() {
    cd "$srcdir/h5py-$pkgver"
    python setup.py test
}
 
package() {
    cd "$srcdir/h5py-$pkgver"
    python setup.py install --prefix=/usr --root="$pkgdir/" --optimize=1
    install -D -m644 licenses/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
