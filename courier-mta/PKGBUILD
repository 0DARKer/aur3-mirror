# $Id: PKGBUILD 73307 2012-07-06 03:48:24Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

# -----------  NOTE TO ALL USERS ------------
# Go read http://www.courier-mta.org/install.html b4 running or building courier

pkgname=courier-mta
pkgver=0.69.0
pkgrel=1
pkgdesc="IMAP(s)/POP3(s) and SMTP Server with ML-manager, webmail and webconfig"
arch=(i686 x86_64)
license=('GPL2')
backup=('etc/courier/imapd.cnf' 'etc/courier/pop3d.cnf' \
        'etc/courier/imapd' 'etc/courier/imapd-ssl' \
        'etc/courier/pop3d' 'etc/courier/pop3d-ssl' \
        'etc/courier/courierd' 'etc/courier/sqwebmaild' \
        'etc/courier/esmtpd' 'etc/courier/esmtpd-ssl' \
        'etc/courier/esmtpd.cnf' 'etc/courier/esmtpd-msa' \
        'etc/courier/webadmin/password' 'etc/courier/esmtpauthclient' \
        'etc/conf.d/courier-mta')
url="http://courier-mta.org"
depends=('courier-authlib>=0.63.0' 'gamin' 'gcc-libs' 'gdbm' 'pcre' 'mime-types' 'ca-certificates')
optdepends=('libldap')
makedepends=('apache' 'pam' 'expect' 'gnupg' 'libldap' 'gamin')
provides=('smtp-server' 'smtp-forwarder' 'imap-server' 'pop3-server' 'courier-imap' 'courier-maildrop')
conflicts=('courier-imap' 'smtp-forwarder' 'smtp-server' 'imap-server' 'courier-maildrop' 'ucspi-tcp')
options=('!libtool')
install=courier-mta.install
source=(http://downloads.sourceforge.net/project/courier/courier/${pkgver}/courier-${pkgver}.tar.bz2
        courier.rc.d
        esmtpd.rc.d
        esmtpd-ssl.rc.d
        esmtpd-msa.rc.d
        imapd.rc.d
        imapd-ssl.rc.d
        pop3d.rc.d
        pop3d-ssl.rc.d
        webmaild.rc.d)
sha512sums=('96e048015392f5673986fc086a8e7a7e8ecece1d6e08b096a05ae3f8f7feabd96e8dcf951fcb265efae773b909479a95f6aa2de88204677658c458079dec5b39'
	    '08c09778728693fda7aabedb4d4206a075b985ca108bdd3e8ea62c0eaa354264296c4d1f16886c628dfa01cfa206a83cc7edf59cdaa9984007adf46988c63ef4'
	    'af95abda27b108d24b40989ad7ed3dcad6579a2e678057fb620f6782533a3b5e13815be29ebba72459017b08c399c7a714a94c893298d0c7dc1e778e371a0e91'
	    '29c1725634b76412d83ce775e23b3069b47ad767261faf3eadb71198a6e2502e58314178956f3daa46605881b22964eff8f908dd1babfeb3cefa6c06a0af5717'
	    'e557281304aa14ae6e9bb9f0b2b9bcaf6d7ce8e41291d2f512ec4a7cb05e6b760d3c0b7a4813f3ae7964bc634bbf2d15a1a166fb1147b76778ad98a845927a39'
	    '1bf7e881dea83978d125e33a8de9e06f9d329eb7a4a487fc023c129017584fb998a7195f3102fc750cb8af32a7f115c0062d0e2339740d8d819f558adbeb4815'
	    '454eb1956a36fc36f275142c7898c00bc0dbabd9c821b0ff6d80b606087d94c4c9e194a1549bd8be70d229dd10d3a2e57b1eb13a362649152c45e878bcc16903'
	    '46e37585c6135cf47d7ac82ed5950d49f985f4f5119eca1fa465a1a4a6cdabf087b3fa81a30f8b10d70c9f1eb714d087faea1a7fe2be91bcffd44cd3582bafb1'
	    '00476ea41e39743d03713508200373680c0c0ca8f8a348d79ad6b23b4aa48fa6b5a938f4cde5c79b1598c4fedaf2eeb1541cf9347b7d1b569ba9cd11612b0f0f'
	    'c7490b63ef2a229109d2686ba7fcfe58ff6768ab7b31f11c69aad28a79adfd87c67a9f3e1a5d1b230cd95e36d4f1bbd2465f05a1bc73bf84810d6a599d8bfa00')

build() {
  cd ${srcdir}/courier-${pkgver}

  # fix a tiny bug
  sed -i -e \
    's|--with-authchangepwdir=/var/tmp/dev/null|--with-authchangepwdir=$libexecdir/authlib|' \
    configure && chmod 755 configure

  LDFLAGS+=",-L /usr/lib/courier-authlib -lcourierauth"
  echo $LDFLAGS
  # courier is more about configuring than compiling :-), lets start the mess
  ./configure --prefix=/usr \
    --sysconfdir=/etc/courier \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --localstatedir=/var/spool/courier \
    --disable-root-check \
    --enable-unicode \
    --enable-workarounds-for-imap-client-bugs \
    --enable-mimetypes=/etc/mime.types \
    --with-piddir=/var/run/courier \
    --with-locking-method=lockf \
    --with-trashquota \
    --with-db=gdbm \
    --with-trashquota \
    --with-random=/dev/urandom --without-ispell \
    --with-mailuser=courier --with-mailgroup=courier \
    --with-certdb=/etc/ssl/certs/
  make
}

package() {
  cd ${srcdir}/courier-${pkgver}

  #chown mail.mail ${pkgdir}/var/spool/courier
  make DESTDIR=${pkgdir} install
  # docs say we can get rid of those after make
  find ${pkgdir} -name '*\.a' -exec -rm -f {} \;
  # install the perftest-script for testings
  install -Dm755 courier/perftest1 ${pkgdir}/usr/lib/courier/perftest1
  ###############################################################################
  # this is what usually "make install-configure" does
  # *.dist files get rid of "dist"
  for distfile in ${pkgdir}/etc/courier/*.dist; do
    mv ${distfile} ${pkgdir}/etc/courier/$(basename ${distfile} .dist)
  done
  # install pam files according to the layout used in Archlinux
  for pamfile in ${pkgdir}/etc/courier/*.authpam; do
    sed -i 's|/lib/security/pam_pwdb\.so|pam_unix.so|' ${pamfile}
    #echo "password  required  pam_unix.so" >> $pamfile
    install -Dm 644 ${pamfile} \
      ${pkgdir}/etc/pam.d/$(basename ${pamfile} .authpam | sed "s/d$//")
    rm -f ${pamfile}
  done

  ###############################################################################
  # Arch Linux specific tweaks to make things easier for the user
  # create passwordfile for webadmin -> standard archwebadmin
  sed -i 's|/etc/courier/webadmin/password|$(DESTDIR)/etc/courier/webadmin/password|g' Makefile
  yes "archwebadmin" | make DESTDIR=${pkgdir} install-webadmin-password
  # arch specific scripts
  install -D -m 755 ${srcdir}/courier.rc.d ${pkgdir}/etc/rc.d/courier
  install -D -m 755 ${srcdir}/imapd.rc.d ${pkgdir}/etc/rc.d/imapd
  install -D -m 755 ${srcdir}/imapd-ssl.rc.d ${pkgdir}/etc/rc.d/imapd-ssl
  install -D -m 755 ${srcdir}/pop3d.rc.d ${pkgdir}/etc/rc.d/pop3d
  install -D -m 755 ${srcdir}/pop3d-ssl.rc.d ${pkgdir}/etc/rc.d/pop3d-ssl
  install -D -m 755 ${srcdir}/esmtpd.rc.d ${pkgdir}/etc/rc.d/esmtpd
  install -D -m 755 ${srcdir}/esmtpd-ssl.rc.d ${pkgdir}/etc/rc.d/esmtpd-ssl
  install -D -m 755 ${srcdir}/esmtpd-msa.rc.d ${pkgdir}/etc/rc.d/esmtpd-msa
  install -D -m 755 ${srcdir}/webmaild.rc.d ${pkgdir}/etc/rc.d/webmaild
   #install -Dm 655 ${srcdir}/courier-webmail-cleancache.cron.hourly \
   # ${pkgdir}/etc/cron.hourly/courier-webmail-cleancache
  # bug http://bugs.archlinux.org/task/5154
   find ${pkgdir}/usr/lib -name '*\.a' -exec rm -f {} \;
  # fixing some permissions
   chown -R courier:courier ${pkgdir}/usr/lib/courier/modules
   rm -r ${pkgdir}/var/run
   #chown -R courier:courier ${pkgdir}/var/run/courier
   chown root:root ${pkgdir}/usr/{.,bin,lib,sbin,share}
  # make a link to /usr/sbin/sendmail
   install -dm 755 ${pkgdir}/usr/sbin
   cd ${pkgdir}/usr/sbin
   ln -s ../bin/sendmail ./sendmail
}
