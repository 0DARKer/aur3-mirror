# Maintainer: Benoit Favre <benoit.favre@gmail.com>
# Contributor: Sam Bliss <m13253@hotmail.com> -- new VCS guidelines for pacman 4.1 
pkgname=emscripten-git
pkgver=20140107
pkgrel=1
pkgdesc="an LLVM-to-JavaScript compiler"
arch=(i686 x86_64)
url="http://emscripten.org"
license=('MIT')
depends=('clang' 'llvm' 'nodejs>=0.5.5' 'python2>=2.7')
makedepends=('git')
conflicts=('emscripten')
provides=('emscripten')
source=('git://github.com/kripken/emscripten.git#branch=llvm-3.4'
        emscripten.sh)
md5sums=('SKIP'
         'e188f26bb5a765b16d9eb23948e1ef3d')

pkgver() {
  cd "$srcdir/emscripten"
  git log -1 --format="%cd" --date=short | tr -d -
}

package() {
  cd "$srcdir/emscripten"

  sed -i 's/\<python\>/python2/g' $(find . -name \*.py) em++ emar emcc em-config emconfigure emmake emranlib emrun emscons

  mkdir -p "$pkgdir/usr/lib/emscripten"
  mkdir -p "$pkgdir/etc/profile.d"
  mkdir -p "$pkgdir/usr/bin" 

  cp -R em* cmake/ scons-tools/ src/ system/ third_party/ tools/ "$pkgdir/usr/lib/emscripten"
  
  install -m 0755 $srcdir/emscripten.sh "$pkgdir/etc/profile.d/"
  
  for i in em++ emar emcc em-config emconfigure emmake emranlib emrun emscons
  do
    install -m 0755 $i "$pkgdir/usr/lib/emscripten"
    ln -s /usr/lib/emscripten/$i "$pkgdir/usr/bin/$i"
  done
}

# vim:set ts=2 sw=2 et:
