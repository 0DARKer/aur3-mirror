# Maintainer: Benoit Favre <benoit.favre@gmail.com>
pkgname=emscripten-git
pkgver=20121001
pkgrel=1
pkgdesc="an LLVM-to-JavaScript compiler"
arch=(i686 x86_64)
url="http://emscripten.org"
license=('MIT')
depends=('clang' 'llvm' 'nodejs>=0.5.5' 'python2>=2.7')
makedepends=('git')
source=(emscripten-wrapper.sh emscripten-config)
md5sums=('ac5c217faf52c6ec1c4a9c6f44e8d95d'
         '1ba5ed180447443da6d0df7a4fe1fa60')
install=$pkgname.install

_gitroot=git://github.com/kripken/emscripten.git
_gitname=emscripten

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #
  sed -i 's/\<python\>/python2/g' $(find . -name \*.py) emcc em++ emar em-config emlibtool emconfigure emmake
}

package() {
  cd "$srcdir/$_gitname-build"
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/lib/emscripten"
  mkdir -p "$pkgdir/usr/lib/emscripten/tests"
  cp -r -L emcc em++ emar em-config emlibtool emconfigure emmake emranlib emscripten.py src/ system/ tools/ third_party/ tools/ "$pkgdir/usr/lib/emscripten"
  cp -r -L tests/hello* "$pkgdir/usr/lib/emscripten/tests"
  install -m 0755 $srcdir/emscripten-wrapper.sh "$pkgdir/usr/bin"
  install -m 0644 $srcdir/emscripten-config "$pkgdir/usr/lib/emscripten"
  cd "$pkgdir/usr/bin"
  for i in emcc em++ emar em-config emlibtool emconfigure emmake emranlib
  do
    ln -s emscripten-wrapper.sh $i
  done
}

# vim:set ts=2 sw=2 et:
