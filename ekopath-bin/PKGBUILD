# Maintainer: kfgz <kfgz at interia dot pl>
# Contributor: 6xx
# Contributor: csslayer

pkgname=ekopath-bin
_real_name=ekopath
_pkgver=2011-12-04
pkgver=20111204
pkgrel=1
pkgdesc="A high-performance Intel 64 / AMD64 compiler for C99, C++ 2003, Fortran 90/95 and partial 2003"
url="http://pathscale.com/ekopath-compiler-suite"
arch=('x86_64')
license=('unknown')
makedepends=('xdelta')
options=(!strip)
source=(http://kfgz.mydevil.net/${_real_name}-${_pkgver}.tar.xz
       ekopath.sh
       pathcc.patch)
md5sums=('3d9f37aa3715343c996ba5e8845b24c1'
         '00c72b71ed4b2f03d1dd84264b405a7a'
         '4816fb9b2d75a847cc922701d05a3321')

package() {
	
  install -dm755 "${pkgdir}"/{etc/profile.d,opt/ekopath}
  
  cd "${srcdir}/src"
  cp -axr * "${pkgdir}"/opt/ekopath
  
  mv "${pkgdir}"/opt/ekopath/bin/pathcc "${pkgdir}"/opt/ekopath/bin/pathcc.bak
  xdelta patch "${srcdir}"/pathcc.patch "${pkgdir}"/opt/ekopath/bin/pathcc.bak "${pkgdir}"/opt/ekopath/bin/pathcc
  chmod +x "${pkgdir}"/opt/ekopath/bin/pathcc
  rm -f "${pkgdir}"/opt/ekopath/bin/pathcc.bak
  install -m755 "${srcdir}"/ekopath.sh "${pkgdir}"/etc/profile.d/
}
