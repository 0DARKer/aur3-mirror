# Maintainer: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=apk-tools
pkgver=2.3.3
pkgrel=1
pkgdesc='Alpine Package Keeper - package manager for Alpine Linux'
arch=('i686' 'x86_64')
url='http://git.alpinelinux.org/cgit/apk-tools/'
license=('GPL')
depends=('glibc' 'lua' 'openssl' 'zlib')
source=(http://git.alpinelinux.org/cgit/apk-tools/snapshot/$pkgname-$pkgver.tar.bz2)
md5sums=('659c04201019781c3ea092f946369fed')

build() {
  cd $srcdir/$pkgname-$pkgver

  # fix (or rather bypass) compilation issues
  sed '/-nopie/d' -i src/Makefile
  sed 's/-Werror//' -i Make.rules

  # no need to keep apk in /bin
  sed -e 's|/usr/sbin:||' \
      -e 's|/sbin:||' -i src/package.c
  sed 's|/sbin|/usr/bin|' -i Makefile

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
