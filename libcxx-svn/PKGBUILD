# Maintainer: Steve Anton <anxuiz.nx@gmail.com>

pkgname=libcxx-svn
pkgver=122057
pkgrel=1
pkgdesc="a new implementation of the C++ standard library targeting C++0X"
arch=('i686' 'x86_64')
url="http://libcxx.llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('gcc-libs')
makedepends=('clang' 'cmake' 'subversion')
source=()
md5sums=()

_svntrunk=http://llvm.org/svn/llvm-project/libcxx/trunk
_svnmod=libcxx

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    cd $_svnmod
    svn up -r $pkgver
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    cd $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  # do an out of source build
  [[ ! -d build ]] && mkdir build
  cd build

  msg $(which clang)

  # libcxx appears to only build correctly with clang at this time.
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_COMPILER=$(which clang) \
    -DCMAKE_CXX_COMPILER=$(which clang++)

  # BUILD
  make
}

package() {
  cd "$srcdir/$_svnmod/build"
  make DESTDIR="$pkgdir/" install

  cd "$srcdir/$_svnmod"
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
