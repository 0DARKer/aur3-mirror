# Maintainer: Louis des Landes <aur@psykar.com>
# Contributor: dlech

pkgname=keebuntu-git
pkgver=0.0.0
pkgrel=1
pkgdesc="Adds an app indicator icon for keepass (for unity & cinnamon)"
arch=(any)
url="https://github.com/dlech/Keebuntu"
license=('GPL2')
md5sums=('SKIP')
depends=(
    keepass
    libappindicator
    dbus-sharp-glib
    gtk-sharp-2
)
makedepends=('git' 'libappindicator')
source=('repo::git+https://github.com/dlech/Keebuntu.git')
install=$pkgname.install

_keepassdir=usr/share/keepass/appindicator
_icondir=icons/hicolor/256x256/apps

pkgver() {
  cd "$srcdir/repo"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/repo"
  sed -i 's|/usr/lib/keepass2|/usr/share/keepass|' KeePassExe.proj
  sed -i 's|libMagickWand.so.5|libMagickWand-6.Q16HDRI.so|' ImageMagick/ImageMagick.dll.config
  xbuild /property:Configuration=Release ApplicationIndicator/AppIndicatorPlugin.csproj
}

package() {
  cd "$srcdir/repo"
  # Copy stuff across manually
  mkdir -p $pkgdir/$_keepassdir
  mkdir -p $pkgdir/usr/share/$_icondir
  cp ApplicationIndicator/bin/Release/* $pkgdir/$_keepassdir
  cp ApplicationIndicator/Resources/$_icondir/* $pkgdir/usr/share/$_icondir/
  rm $pkgdir/$_keepassdir/KeePass.*
}

# vim:set ts=2 sw=2 et:
