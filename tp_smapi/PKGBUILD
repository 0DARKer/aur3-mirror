# Maintainer: xduugu
# Contributor: nh2
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>

_pkgname=tp_smapi
_kernext=
_kernver=${_kernver:-$(uname -r)}
_extramodules=extramodules-${_kernver%.*}-ARCH
[[ ! -d /lib/modules/$_extramodules ]] && _extramodules=$_kernver
pkgname=$_pkgname$_kernext
pkgver=0.41
pkgrel=2
pkgdesc="Modules for ThinkPad's SMAPI functionality"
[[ -n $_kernext ]] && pkgdesc="$pkgdesc (for linux$_kernext)"
arch=('i686' 'x86_64')
url="http://tpctl.sourceforge.net/"
license=('GPL')
depends=("linux$_kernext")
makedepends=("linux$_kernext-headers")
install=$_pkgname.install
source=(https://cloud.github.com/downloads/evgeni/$_pkgname/$_pkgname-$pkgver.tar.gz) # https://github.com/downloads/evgeni/$_pkgname/$_pkgname-$pkgver.tar.gz
md5sums=('63c683415c764568f6bf17c7eabe4752')
sha256sums=('6aef02b92d10360ac9be0db29ae390636be55017990063a092a285c70b54e666')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -ri "s/^(KVER\s*:=).*$/\1$_kernver/" Makefile
  make HDAPS=1
}

package() {
  make -C "/lib/modules/$_kernver/build" INSTALL_MOD_PATH="$pkgdir" M="$srcdir/$_pkgname-$pkgver" modules_install

  # move hdaps.ko into updates to override in-tree kernel module
  if [[ $_extramodules != $_kernver ]]; then
    cd "$pkgdir/lib/modules/"
    mv "$_kernver/extra" "$_extramodules"
    rmdir "$_kernver"
  fi

  # compress kernel modules
  find "$pkgdir" -name "*.ko" -exec gzip -9 {} +

  # update kernel version in install file
  sed -ri "s/^(kernver=).*$/\1$_kernver/" "$startdir/$_pkgname.install"
}
