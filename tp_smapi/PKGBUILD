# Maintainer: xduugu
# Contributor: nh2
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>

_pkgname=tp_smapi
pkgname=$_pkgname
#pkgname=$_pkgname$(uname -r | sed -r 's/^[^-]+//')
pkgver=0.40
pkgrel=7
pkgdesc="Modules for ThinkPad's SMAPI functionality"
arch=('i686' 'x86_64')
url="http://tpctl.sourceforge.net/"
license=('GPL')
depends=('mkinitcpio>=0.6.8' 'module-init-tools>=3.12-2')
makedepends=('kernel26-headers')
install=tp_smapi.install
source=(http://downloads.sourceforge.net/sourceforge/tpctl/$_pkgname-$pkgver.tgz
        2.6.37-compatibility.patch
        add_newer_models.patch
        adjust_makefile.patch
        chargefix.patch
        hdaps-orientation-update.patch
        include-thinkpad_ec.h-directly.patch)
md5sums=('f4eb8bb4d4413a5ae65aa7d77f4112c0'
         '5159e081e309285a2dd8b2ab94b071eb'
         'f80fd09292bf8d01e6a78b3c762ecbb5'
         '90894542082f4b27da979aea36111cb4'
         '96cb89b22ed2661b902fe456b0624107'
         'fde19e6a4fca81bd66151bbcd59f8ef1'
         '8d235aa21aca8310ee36caaab75f0319')
sha256sums=('ccb0965684cad076160750f2ac380f99373fa73f0bcc1091ffce2492f56349ab'
            'e91a95b0834bea428f5bbac97aced098b6d90e3cbfc5ee3b576bf18141167b83'
            'a7e03a8890e1d06f37e7d0a83f7e7f34fdfb8b5345d3d8df6acb4adfaf7a03ee'
            '07597268e6850558feeec72c0fec98e1cefb260b5518b0a65435fa071cc9662c'
            'd27f5db68e1fe44a3ca8c2314a39410df92438eaa44acbb3263805f68ba7cea3'
            '09ab2f8ac0243dec17d53b74800d07ae751dfefdf101823af4bfa7b9336ee5ce'
            'cabb2e0fa45784b5cfc2ce2ae92a434afe6ad56fa3e042e88e1a979f1f981293')

build() {
  # update kernel version in install file
  sed -ri "s/^(kernver=).*$/\1$(uname -r)/" "$startdir/$_pkgname.install"

  cd "$srcdir/$_pkgname-$pkgver"
  local patch
  for patch in "${source[@]}"; do
    [[ $patch =~ .*\.patch$ ]] && patch -Np1 < "$srcdir/${patch##*/}"
  done
  make HDAPS=1
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C "/lib/modules/$(uname -r)/build" INSTALL_MOD_PATH="$pkgdir" M="$PWD" modules_install

  # move hdaps.ko into updates to override in-tree kernel module
  cd "$pkgdir/lib/modules/$(uname -r)/"
  mkdir updates
  mv extra/hdaps.ko updates

  # compress kernel modules
  find "$pkgdir" -name "*.ko" -exec gzip -9 {} +
}
