# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Kovivchak Evgen<oneonfire@gmail.com>
pkgname=zopfli-git
pkgver=1.0.0.18.ga29e46b
pkgrel=1
epoch=1
pkgdesc="A new zlib (gzip, deflate) compatible compressor "
arch=('i686' 'x86_64')
url="https://code.google.com/p/zopfli/"
license=('Apache')
depends=('gcc-libs')
makedepends=('git')
options=('!makeflags')
source=('git+https://github.com/google/zopfli.git')
sha512sums=('SKIP')

pkgver() {
  cd zopfli
  git describe | sed -e 's/^zopfli-//' -e 'y/-/./'
}

build() {
  cd zopfli
  make CFLAGS+='-fPIC -lm' CXXFLAGS+=-lm zopfli libzopfli zopflipng
}

package() {
  cd zopfli

  install -d "$pkgdir"/usr/{bin,lib}

  install -m755 zopfli{,png} "$pkgdir"/usr/bin/
  install -m644 libzopfli.so.1.0.1 "$pkgdir"/usr/lib/
  ln -s libzopfli.so.1.* "$pkgdir"/usr/lib/libzopfli.so.1
}
