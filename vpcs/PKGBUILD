# Maintainer: Brian Van S. <brianvans at gmail dot com>
# vim:set ts=2 sw=2 et:

pkgname=vpcs
pkgver=0.6
pkgrel=1
pkgdesc="Simulates up to 9 computers for ping/traceroute purposes."
arch=('i686' 'x86_64')
url="http://vpcs.sourceforge.net/"
license=('BSD')
install='vpcs.install'
source=("http://sourceforge.net/code-snapshots/svn/v/vp/vpcs/code/vpcs-code-102-trunk.zip")
md5sums=('3595a6d0fbb390a00dea98062e5f0b93')

build() {
  # for some reason 0.6 has no src tarball
  # remove this hack below when/if they finally do upload one
  ln -sf "${srcdir}/vpcs-code-102-trunk/" "${srcdir}/${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  # fix the makefile hardcoding i386
  if test "$CARCH" == x86_64; then
    sed -i "s/i386/x86_64/" Makefile.linux
  fi
  # fix the makefile forcing a static binary
  sed -i "s/-s -static//" Makefile.linux
  make -f ./Makefile.linux
}
package() {
  install -D -m 0755 "${srcdir}/${pkgname}-${pkgver}/src/vpcs" "${pkgdir}/usr/bin/vpcs"
  install -D -m 0644 "${srcdir}/${pkgname}-${pkgver}/readme.txt" "${pkgdir}/usr/share/${pkgname}/readme.txt"
  install -D -m 0644 "${srcdir}/${pkgname}-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
