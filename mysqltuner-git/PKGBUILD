# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=mysqltuner-git
pkgver=1.2.0.12.g71f14ea
pkgrel=1
pkgdesc="MySQLTuner - review configuration quickly and make adjustments to increase performance and stability - git checkout"
arch=('any')
url="http://www.mysqltuner.com"
license=('GPL')
makedepends=('git')
depends=('perl')
provides=(mysqltuner)
conflicts=(mysqltuner)
source=('mysqltuner::git://github.com/rackerhacker/MySQLTuner-perl.git')

pkgver() {
	cd mysqltuner/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

package() {
	cd mysqltuner/

	install -D -m755 mysqltuner.pl ${pkgdir}/usr/sbin/mysqltuner
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/mysqltuner/LICENSE
	install -D -m644 README ${pkgdir}/usr/share/doc/mysqltuner/README
}

sha256sums=('SKIP')
