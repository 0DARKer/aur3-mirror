# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Paolo Herms

pkgname=frama-c
pkgver=20130601
_codename=Fluorine
pkgrel=1
pkgdesc='Extensible platform dedicated to source-code analysis of C software'
arch=('i686' 'x86_64')
license=('LGPL2.1')
url='http://frama-c.com'
depends=('gtksourceview2' 'libgnomecanvas' 'ocaml')
makedepends=('lablgtk2' 'coq' 'graphviz' 'ltl2ba')
optdepends=('lablgtk2' 'coq' 'graphviz' 'ltl2ba')
source=(http://frama-c.com/download/frama-c-$_codename-$pkgver.tar.gz
        frama-c-ocaml401.patch
        make40.patch)
md5sums=('69eed6b3f649725bf03d8500aaeb20a5'
         '2209d20dbb1953174b15306914182362'
         '88198f0ac765c505a1ee3efa57f9a877')

prepare() {
  cd frama-c-$_codename-$pkgver 
  patch -p0 -i ../frama-c-ocaml401.patch
  patch -p1 -i ../make40.patch
}

build() {
  cd frama-c-$_codename-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd frama-c-$_codename-$pkgver
  make DESTDIR="$pkgdir" install
}
