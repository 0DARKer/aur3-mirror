# Maintainer: megadriver <megadriver at gmx dot com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Renchi Raju <renchi@green.tam.uiuc.edu>
# Contributor: jpkotta
# Based on emacs from [extra] and emacs-bzr from the AUR

pkgname=emacs-lucid
pkgver=24.3
pkgrel=4
pkgdesc="The extensible, customizable, self-documenting real-time display editor (Lucid toolkit version)"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('librsvg' 'gpm' 'giflib' 'libxpm' 'libotf' 'm17n-lib' 'libxaw' 'hicolor-icon-theme' 'desktop-file-utils' 'alsa-lib' 'imagemagick')
install=emacs.install
conflicts=('emacs')
provides=('emacs')
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-$pkgver.tar.xz magickwand.patch emacs-24.3-giflib5.patch)
md5sums=('ea9ed000ca165280265aabb55b9afbd7'
         '7cfbe424932da6ff79aee329de5f47a4'
         '560cdf463299e059c5fadf474bcba218')

prepare() {
  cd "$srcdir"/emacs-$pkgver
  patch -Np1 -i ../magickwand.patch
  patch -Np0 -i ../emacs-24.3-giflib5.patch
}

build() {
  cd "$srcdir"/emacs-$pkgver
  ./autogen.sh
  ac_cv_lib_gif_EGifPutExtensionLast=yes ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --with-x-toolkit=lucid --with-xft --without-gconf --without-gsettings
  make
}

package() {
  cd "$srcdir"/emacs-$pkgver
  make DESTDIR="$pkgdir" install

  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}
  # fix user/root permissions on usr/share files
  find "$pkgdir"/usr/share/emacs/$pkgver -exec chown root:root {} \;
  # fix perms on /var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
  chown -R root:games "$pkgdir"/var/games
  # remove conflict with texinfo package
  rm "$pkgdir"/usr/share/info/info.info.gz
  rm "$pkgdir"/usr/share/info/dir
}
