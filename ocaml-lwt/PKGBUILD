# Maintainer: Sergei Lebedev <superbobry@gmail.com
# Contributor: serp <serp256 at gmail dot com>
srcname=lwt
pkgname="ocaml-$srcname"
pkgver=2.3.1
pkgrel=2
pkgdesc="A library for cooperative threads in OCaml"
arch=('i686' 'x86_64')
url="http://ocsigen.org/lwt/"
license=('LGPL')
depends=('ocaml>=3.12.0' 'ocaml-react' 'ocaml-text' 'ocaml-ssl' 'libev' 'glibc')
makedepends=('ocaml-findlib')
source=(http://ocsigen.org/download/$srcname-$pkgver.tar.gz)
md5sums=('fb7f3600a075c94a94eacf2464c09feb')
conflicts=('lwt')

build() {
  cd $srcdir/$srcname-$pkgver

  # What requires what?
  # --enable-glib      glibc
  # --enable-react     ocaml-react
  # --enable-ssl       ocaml-ssl
  # --enable-text      ocaml-text

  ./configure --enable-react \
              --enable-glib \
              --enable-ssl \
              --enable-text
  make || return 1
}


package() {
  cd $srcdir/$srcname-$pkgver

  export OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)"
  export OCAMLFIND_LDCONF="ignore"

  mkdir -p $OCAMLFIND_DESTDIR
  env DESTDIR=$pkgdir make install
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
