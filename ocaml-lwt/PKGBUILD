# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com
# Contributor: serp <serp256 at gmail dot com>

pkgname="ocaml-lwt"
pkgver=2.4.2
pkgrel=2
pkgdesc="A library for cooperative threads in OCaml"
arch=('i686' 'x86_64')
url="http://ocsigen.org/lwt/"
license=('LGPL')
depends=('ocaml>=3.12.0' 'ocaml-compiler-libs'
         'ocaml-react' 'ocaml-text' 'ocaml-ssl' 'libev' 'glibc')
makedepends=('ocaml-findlib')
source=(http://ocsigen.org/download/lwt-$pkgver.tar.gz)
md5sums=('0d6eeaa295d62aefaf39e034460bc7b4')
options=(!strip !makeflags)

build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  echo '<src/top/*.*>: use_compiler_libs' >> _tags
  # What requires what?
  # --enable-glib      glibc
  # --enable-react     ocaml-react
  # --enable-ssl       ocaml-ssl
  # --enable-text      ocaml-text
  # --enable-toplevel  ocaml-compiler-libs

  ./configure --enable-react \
              --enable-glib \
              --enable-ssl \
              --enable-text \
              --enable-toplevel \
              --disable-debug \
              --prefix /usr \
              --destdir $pkgdir
  make || return 1
}


package() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)"

  mkdir -p "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
