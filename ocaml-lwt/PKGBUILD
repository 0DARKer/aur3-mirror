# Contributor: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com
# Contributor: serp <serp256 at gmail dot com>

pkgname="ocaml-lwt"
pkgver=2.4.4
pkgrel=1
pkgdesc="A library for cooperative threads in OCaml"
arch=('i686' 'x86_64')
url="http://ocsigen.org/lwt/"
license=('LGPL')
depends=('ocaml>=3.12.0' 'ocaml-compiler-libs'
         'ocaml-react' 'ocaml-text' 'ocaml-ssl' 'libev' 'glibc')
makedepends=('ocaml-findlib')
source=(https://github.com/ocsigen/lwt/archive/$pkgver.tar.gz)
#source=(http://ocsigen.org/download/lwt-$pkgver.tar.gz)
sha256sums=('e06b8870860ecdd6fb2e991489d4ac00497bcbf8186b441e98fcb0602de1e919')
options=(!strip !makeflags staticlibs)
build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  echo '<src/top/*.*>: use_compiler_libs' >> _tags
  # What requires what?
  # --enable-glib      glibc
  # --enable-react     ocaml-react
  # --enable-ssl       ocaml-ssl
  # --enable-text      ocaml-text
  # --enable-toplevel  ocaml-compiler-libs

  ./configure --enable-react \
              --enable-glib \
              --enable-ssl \
              --enable-text \
              --enable-toplevel \
              --disable-debug \
              --prefix /usr \
              --destdir $pkgdir
  make || return 1
}


package() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)"

  mkdir -p "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
