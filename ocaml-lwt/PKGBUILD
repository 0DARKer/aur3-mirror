# Maintainer: Sergei Lebedev <superbobry@gmail.com
# Contributor: serp <serp256 at gmail dot com>
pkgname="ocaml-lwt"
pkgver=2.3.1
pkgrel=3
pkgdesc="A library for cooperative threads in OCaml"
arch=('i686' 'x86_64')
url="http://ocsigen.org/lwt/"
license=('LGPL')
depends=('ocaml-with-complibs>=3.12.0'
         'ocaml-react' 'ocaml-text' 'ocaml-ssl' 'libev' 'glibc')
makedepends=('ocaml-findlib')
source=(http://ocsigen.org/download/lwt-$pkgver.tar.gz)
md5sums=('fb7f3600a075c94a94eacf2464c09feb')
conflicts=('lwt')

build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  # What requires what?
  # --enable-glib      glibc
  # --enable-react     ocaml-react
  # --enable-ssl       ocaml-ssl
  # --enable-text      ocaml-text
  # --enable-toplevel  ocaml-with-complibs (should be replaced by ocaml
  #                                         in community/ soon).

  ./configure --enable-react \
              --enable-glib \
              --enable-ssl \
              --enable-text \
              --enable-toplevel \
              --prefix $pkgdir/usr
  make || return 1
}


package() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)"
  export OCAMLFIND_LDCONF="ignore"

  mkdir -p $OCAMLFIND_DESTDIR
  make install
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
