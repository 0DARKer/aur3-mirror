# Maintainer: Max Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=perl-locale-maketext-fuzzy
pkgver=0.11
pkgrel=1
pkgdesc="Maketext from already interpolated strings"
arch=('i686' 'x86_64')
url="http://search.cpan.org/~audreyt/Locale-Maketext-Fuzzy-$pkgver/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/A/AU/AUDREYT/Locale-Maketext-Fuzzy-$pkgver.tar.gz")
md5sums=('623d791f1b57c076e18e5090d0cb2aca')

# Function to change to working directory and set
# environment variables to override undesired options.
prepareEnvironment() {
	cd "$srcdir/Locale-Maketext-Fuzzy-$pkgver"

	export \
		PERL_MM_USE_DEFAULT=1 \
		PERL_AUTOINSTALL=--skipdeps \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
		PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
		MODULEBUILDRC=/dev/null
}

build() {
	prepareEnvironment

	/usr/bin/perl Makefile.PL
	make
}

check() {
	prepareEnvironment

	make test
}

package() {
	prepareEnvironment

	make install

	# Remove "perllocal.pod" and ".packlist".
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
