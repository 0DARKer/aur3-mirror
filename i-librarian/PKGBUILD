#Maintainer:Kwrazi<kwrazi@gmail.com>

# Bug: can't use comma in pkgname
pkgname="i-librarian"
pkgver=2.10
pkgrel=1
pkgdesc="Academic software for managing research papers (web client)"
arch=('any')
url="http://www.bioinformatics.org/librarian/index.php"
license=('GPL3')
depends=('apache' 
         'php' 
         'php-sqlite' 
         'php-gd' 'poppler' 
         'bibutils' 
         'ghostscript' 
         'pdftk')
backup=("etc/webapps/${pkgname}/.htaccess")
install=${pkgname}.install
source=("http://bioinformatics.org/librarian/I,-Librarian-$pkgver-Linux.tar.gz")
md5sums=('e7ac14ffefe2221fd9c00b921c44bac7')

package() {
	_instdir=/usr/share/webapps/${pkgname}
	_etcdir=/etc/webapps/${pkgname}
	
	mkdir -p ${pkgdir}/${_instdir} ${pkgdir}/${_etcdir}
	
	cd ${pkgdir}/${_instdir}
	cp -ra ${srcdir}/* .
	rm ${pkgdir}/${_instdir}/*-Linux.tar.gz
	mv library/.htaccess ${pkgdir}/${_etcdir}
	ln -s /etc/webapps/${pkgname}/.htaccess library/.htaccess
	
	cat > ${pkgdir}/${_etcdir}/apache.example.conf <<EOF
# Configuration for I,-Librarian
#
# Instructions:
#   - Edit this file if necessary
#   - Copy or link this file to /etc/httpd/conf/extra/i-librarian.conf
#   - Edit httpd.conf to include this file, i.e.
#        Include conf/extra/i-librarian.conf
#
# Check that php.ini has extension pdo_sqlite.so enabled.
#

Alias /librarian ${_instdir}

<Directory "${_instdir}">
   AllowOverride None
   Order deny,allow
   Deny from all
   Allow from 127.0.0.1
   <IfModule mod_php5.c>
      php_value upload_max_filesize 200M
      php_value post_max_size 800M
      php_value max_input_time 120
   </IfModule>
</Directory>
<Directory "${_instdir}/library">
   IndexIgnore *
   AddType text/plain .html .htm .shtml .php
   <IfModule mod_php5.c>
      php_admin_flag engine off
   </IfModule>
   <FilesMatch "\.(sq3|pdf)$">
      Order allow,deny
   </FilesMatch>
</Directory>
EOF
}
