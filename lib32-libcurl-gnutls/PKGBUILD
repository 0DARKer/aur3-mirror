# based on Jose Riha <jose1711 gmail com> native package
# Maintainer: Larry Li <lzlarryli gmail com>

_pkgbasename=libcurl-gnutls
pkgname=lib32-$_pkgbasename
_pkgname=curl
_curlbuild=curlbuild-32.h
pkgver=7.32.0
pkgrel=1
pkgdesc="An URL retrieval utility and library"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=(custom)
makedepends=('gcc-multilib')
depends=('lib32-libssh2' 'lib32-gnutls')
options=('!libtool' '!strip')
source=("http://curl.haxx.se/download/$_pkgname-$pkgver.tar.gz"{,.asc}
        curlbuild.h
        01_runtests_gdb.patch
        02_art_http_scripting.patch
        03_keep_symbols_compat.patch
        04_workaround_as_needed_bug.patch
        06_always-disable-valgrind.patch
        07_do-not-disable-debug-symbols.patch
        90_gnutls.patch
        99_nss.patch)

md5sums=('f77cf3cb59cf2bfe686b80338323fd40'
         'SKIP'
         '751bd433ede935c8fae727377625a8ae'
         '7ad5ff89674f5f08b232064c515767ce'
         '5cadcf82367cef12738fc3b0ef27483f'
         '955b12e575215735b6bd563ee2e3af2a'
         'fd110c854e055d0375798c2857bd260e'
         '798ec0e687d6193f1fa537f82d2b132f'
         'eb393f4dcd524916372c6bdd66c78c0b'
         '85ca09852fb72376c444ac8dea0da3d2'
         '2e71a1e566212e28f0a9756b3366900d')

prepare() {
  cd "$_pkgname-$pkgver"
  # apply debian patches
  for i in ../*patch
	do
	patch -p1 < "$i"
	done
}

build() {
  cd "$_pkgname-$pkgver"
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  ./configure \
      --libdir=/usr/lib32 \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-dependency-tracking \
      --enable-versioned-symbols \
      --disable-ldap \
      --disable-ldaps \
      --enable-ipv6 \
      --enable-manual \
      --enable-threaded-resolver \
      --without-libidn \
      --with-random=/dev/urandom \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
      --without-ssl \
      --with-gnutls=/usr
  make
}

package() {
  install -Dm644 $srcdir/$_pkgname-$pkgver/lib/.libs/libcurl.so.4.3.0 $pkgdir/usr/lib32/libcurl-gnutls.so.4.3.0
  ln -s libcurl-gnutls.so.4.3.0 $pkgdir/usr/lib32/libcurl-gnutls.so.4

  install -D -m644 $srcdir/$_pkgname-$pkgver/docs/LICENSE-MIXING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
