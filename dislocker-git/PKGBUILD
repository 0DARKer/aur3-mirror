# Maintainer: boppbo <boris.bopp at gmail dot com>

pkgname=dislocker-git
pkgver=23
pkgrel=1
pkgdesc="Read BitLocker encrypted volumes under Linux"
arch=('i686' 'x86_64')
url="http://www.hsc.fr/ressources/outils/dislocker"
license=('GPL2')
source=('dislocker::git+https://github.com/Aorimn/dislocker.git')
depends=('fuse')
makedepends=('git')
conflicts=('dislocker')
provides=('dislocker')
md5sums=('SKIP')

build() {
  cd dislocker/src
  make fuse
}

package() {
  cd dislocker/src
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man1"
  make INSTALL_PATH="$pkgdir/usr/bin/" MAN_PATH="$pkgdir/usr/share/man/man1" install
}

pkgver() {
  cd dislocker
  echo $(git rev-list --count master)
}
