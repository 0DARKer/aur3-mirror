# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-devel-repl'
pkgver='1.003025'
pkgrel="2"
pkgdesc="a modern perl interactive shell"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-file-homedir' 'perl-module-runtime' 'perl-moose>=0.93' 'perl-moosex-getopt>=0.18' 'perl-moosex-object-pluggable>=0.0009' 'perl-namespace-autoclean' 'perl-task-weaken')
makedepends=('perl-test-checkdeps>=0.007' 'perl-test-fatal')
url='http://search.cpan.org/dist/Devel-REPL/'
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/Devel-REPL-${pkgver}.tar.gz")
md5sums=('c610a5bfe436adec8b84f1d500e3a22f')
sha512sums=('6ba0f3dfc5dad2d9af51fcbd0cd93a96cc615c2ac0b03781f3b19d69ed0bab0d446380e62caeec317c4f94093e9af19380716f821c6c8847d3bdd53cdc5b35ba')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Devel-REPL-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Build.PL
  ./Build
}

check() {
  prepare_environment
  ./Build test
}

package() {
  prepare_environment
  ./Build install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
