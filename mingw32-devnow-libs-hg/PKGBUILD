# Maintainer: Eduardo Sánchez Muñoz <eduardosanchezmunoz@gmail.com>

pkgname=mingw32-devnow-libs-hg
_hgname=devnow-libs
pkgver=42
pkgrel=1
pkgdesc="Collection of general purpose C libraries."
arch=('i686' 'x86_64')
url="https://redmine.edsm.es/projects/devnow-libs/"
license=('custom:Simple Public License (SimPL) 2.0')
depends=('mingw32-runtime')
makedepends=('mingw32-gcc' 'mercurial' 'cmake' 'make')
options=()
conflicts=('mingw32-devnow-libs')
provides=('mingw32-devnow-libs')
source=("hg+https://hg.edsm.es/$_hgname"
	"Toolchain-cross-mingw32-linux.cmake")
md5sums=('SKIP'
	 7eb918d90a215b652f3557aa7026ac8f)

pkgver() {
	cd "$srcdir/$_hgname"
	echo $(hg identify -n)
}

build() {
	cd "$srcdir"
	
	rm -rf "$srcdir/$_hgname-build"
	mkdir -p "$srcdir/$_hgname-build"
	cd "$srcdir/$_hgname-build"
	
	# Unset these variables or cmake will fail.
	unset CFLAGS
	unset CXXFLAGS
	unset CPPFLAGS
	unset LDFLAGS
	
	cmake -DCMAKE_TOOLCHAIN_FILE="$srcdir/Toolchain-cross-mingw32-linux.cmake" \
	      -DCMAKE_INSTALL_PREFIX=/usr/i486-mingw32 \
	      -DCMAKE_SHARED_LINKER_FLAGS="-L/usr/i486-mingw32/bin" \
	      -DCMAKE_EXE_LINKER_FLAGS="-L/usr/i486-mingw32/bin" \
	      "$srcdir/$_hgname"
	make
}

package() {
	cd "$srcdir/$_hgname-build"
	make DESTDIR="$pkgdir" install
	
	rm -rf "$pkgdir/usr/share"
	#install -Dm644 "$srcdir/$_hgname/COPYING.SimPL" "$pkgdir/usr/share/licenses/$pkgname/COPYING.SimPL"
}
