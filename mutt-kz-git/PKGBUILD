# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: boyska <piuttosto@logorroici.org>

pkgroot=mutt-kz
pkgname=mutt-kz-git
pkgver=latest
pkgrel=2
pkgdesc='Small but powerful text-based mail client (experimental fork) (development version)'
url='https://github.com/karelzak/mutt-kz'
license=('GPL')
source=('git+https://github.com/karelzak/mutt-kz.git')
sha1sums=('SKIP')
arch=('i686' 'x86_64')
depends=('gdbm' 'gpgme' 'openssl>=0.9.8e' 'libidn' 'libsasl' 'mime-types' 'ncurses' 'notmuch' 'zlib')
makedepends=('git' 'gnupg' 'libxslt')
conflicts=('mutt')
provides=('mutt')

pkgver() {
  cd "${srcdir}/${pkgroot}"

  # Get the version number.
  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgroot}"

  # Configure the build.
 ./prepare \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-debug \
    --enable-gpgme \
    --enable-hcache \
    --enable-imap \
    --enable-notmuch \
    --enable-pgp \
    --enable-pop \
    --enable-smtp \
    --with-idn \
    --with-sasl \
    --with-ssl=/usr

  # Build it!
  make
}

package() {
  cd "${srcdir}/${pkgroot}"

  # Install the program.
  make DESTDIR=${pkgdir} install

  # Provided by the mime-types package.
  rm "${pkgdir}/etc/mime.types"
}

# vim: ft=sh ts=2 sw=2 et
