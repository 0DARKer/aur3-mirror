# Maintainer: neersighted <n@neersighted.com>
# Contributor: boyska <piuttosto@logorroici.org>

pkgname=mutt-kz-git
pkgver=latest
pkgrel=1
pkgdesc='Small but powerful text-based mail client (experimental fork) (development version)'
url='https://github.com/karelzak/mutt-kz'
license=('GPL')
source=('git://github.com/karelzak/mutt-kz.git')
md5sums=('SKIP')
sha1sums=('SKIP')
sha256sums=('SKIP')
arch=('i686' 'x86_64')
depends=('gdbm' 'gpgme' 'openssl>=0.9.8e' 'libsasl' 'mime-types' 'ncurses' 'notmuch' 'zlib')
makedepends=('git' 'gnupg' 'libxslt')
conflicts=('mutt')
provides=('mutt')

pkgver() {
  cd ${srcdir}/mutt-kz

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd ${srcdir}/mutt-kz

  # Configure the build.
 ./prepare \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-debug \
    --enable-gpgme \
    --enable-hcache \
    --enable-imap \
    --enable-notmuch \
    --enable-pgp \
    --enable-pop \
    --enable-smtp \
    --with-idn \
    --with-sasl \
    --with-ssl=/usr

  # Build it!
  make
}

package() {
  cd ${srcdir}/mutt-kz

  # Install the program.
  make DESTDIR=${pkgdir} install

  # Provided by the mime-types package.
  rm ${pkgdir}/etc/mime.types
}

# vim: set ts=2 sw=2 et:
