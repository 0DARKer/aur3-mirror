# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Partha Chowdhury <kira.laucas@gmail.com>

pkgname=conky-audacious
pkgver=1.8.1
pkgrel=1
pkgdesc="An advanced, highly configurable system monitor for X with audacious support."
arch=('i686' 'x86_64')
url="http://conky.sourceforge.net/"
license=('custom')
depends=('alsa-lib' 'curl' 'wireless_tools' 'imlib2' 'audacious')
makedepends=('pkgconfig')
provides=('conky')
conflicts=('conky')
replaces=('torsmo')
backup=(etc/conky/conky.conf etc/conky/conky_no_x11.conf)
source=(http://downloads.sourceforge.net/project/conky/conky/${pkgver}/conky-${pkgver}.tar.bz2)
md5sums=('366dc6a5c2ebebfbe6f53da25061b5d6')

build() {
  cd "${srcdir}/conky-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-wlan \
              --enable-rss \
              --enable-ibm \
              --enable-imlib2 \
              --enable-audacious \
              --disable-lua
  make
}

package() {
  cd "${srcdir}/conky-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
