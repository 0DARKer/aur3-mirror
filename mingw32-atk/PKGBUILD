# Maintainer: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>

pkgname=mingw32-atk
pkgver=1.30.0
pkgrel=1
arch=(any)
pkgdesc="A library providing a set of interfaces for accessibility (mingw32)"
depends=(mingw32-glib2)
makedepends=(mingw32-gcc)
options=(!strip)
license=('LGPL')
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/atk/1.30/atk-${pkgver}.tar.bz2)
sha256sums=('92b9b1213cafc68fe9c3806273b968c26423237d7b1f631dd83dc5270b8c268c')

build()
{
  cd "$srcdir/atk-$pkgver"

  export CFLAGS="-O2 -mms-bitfields"
  export PKG_CONFIG_PATH="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST
  make
}

package() {
  cd "$srcdir/atk-$pkgver"
  mkdir -p "$pkgdir/usr/i486-mingw32/lib"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/share/gtk-doc
}
