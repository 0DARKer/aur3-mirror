# Maintainer: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>

pkgname=mingw32-atk
pkgver=1.32.0
pkgrel=1
arch=(any)
pkgdesc="A library providing a set of interfaces for accessibility (mingw32)"
depends=(mingw32-glib2)
makedepends=(mingw32-gcc)
options=(!strip)
license=('LGPL')
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/atk/1.32/atk-${pkgver}.tar.bz2)
sha256sums=('e9a3e598f75c4db1af914f8b052dd9f7e89e920a96cc187c18eb06b8339cb16e')

build()
{
  cd "$srcdir/atk-$pkgver"

  export CFLAGS="-O2 -mms-bitfields"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS PKG_CONFIG_PATH

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST
  make
}

package() {
  cd "$srcdir/atk-$pkgver"
  mkdir -p "$pkgdir/usr/i486-mingw32/lib"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/share/gtk-doc
}
