# Maintainer: EpocSquadron <crimsonmage a gmail>
pkgname=libreoffice-extension-writer2latex
_javaname=writer2latex
_exts=( writer2latex writer2xhtml writer4latex )
pkgver=1.1.6
pkgrel=1
pkgdesc="A java program and collection of extensions for converting to and working with latex in LibreOffice"
arch=('any')
url="http://writer2latex.sourceforge.net"
license=('LGPL')
groups=('libreoffice-extensions')
depends=('libreoffice' 'java-runtime')
makedepends=('unzip')
source=('http://sourceforge.net/projects/writer2latex/files/writer2latex/Writer2LaTeX%201.1/writer2latex116alpha.zip/download' 
'writer2latex.sh' )
md5sums=('24aaec4f262f69d6a5291aea14c122b7'
         'da6e71bf4bf9c4f3e5b3969291dab6a0')

package() {
  #Install libreoffice extensions
  for _ext in ${_exts[@]}; do
    mkdir -p $srcdir/$_ext
    unzip -d $srcdir/$_ext $srcdir/writer2latex11/$_ext.oxt
    mkdir -p "$pkgdir/usr/lib/libreoffice/share/extensions/$_ext"
    cp -R "$srcdir/$_ext"/* "$pkgdir/usr/lib/libreoffice/share/extensions/$_ext/"
  done

  #Make sure extension executables are.
  chmod 0755 "$pkgdir/usr/lib/libreoffice/share/extensions/writer2latex/writer2latex-filter.jar"
  chmod 0755 "$pkgdir/usr/lib/libreoffice/share/extensions/writer2xhtml/writer2xhtml-filter.jar"
  chmod 0755 "$pkgdir/usr/lib/libreoffice/share/extensions/writer4latex/writer4latex.jar"

  #Install java program
  install -d "$pkgdir/usr/share/java/$_javaname"
  install -m755 "$srcdir/writer2latex11/$_javaname.jar" "$pkgdir/usr/share/java/$_javaname"

  #Install shell script to execute java program
  mkdir -p "$pkgdir/usr/bin"
  install -m755 "$srcdir/$_javaname.sh" "$pkgdir/usr/bin/"
  mv "$pkgdir/usr/bin/$_javaname.sh" "$pkgdir/usr/bin/$_javaname"

  #Install latex styles.
  install -d "$pkgdir/usr/share/texmf/$_javaname/"
  install -m644 "$srcdir/writer2latex11/latex/ooomath.sty" "$pkgdir/usr/share/texmf/$_javaname/"

  #Install files needed by java program
  mkdir -p "$pkgdir/usr/share/$_javaname/"
  cp -R "$srcdir/writer2latex11/config" "$pkgdir/usr/share/$_javaname"
  cp -R "$srcdir/writer2latex11/xslt" "$pkgdir/usr/share/$_javaname"
  cp -R "$srcdir/writer2latex11/zotero" "$pkgdir/usr/share/$_javaname"

  #Install documentation
  mkdir -p "$pkgdir/usr/share/doc/$_javaname"
  cp "$srcdir/writer2latex11/doc"/* "$pkgdir/usr/share/doc/$_javaname"
}
