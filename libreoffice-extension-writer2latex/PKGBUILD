# Maintainer: Uwe Koloska <kolewu@koloro.de>
# Contributor: EpocSquadron <crimsonmage a gmail>
pkgname=libreoffice-extension-writer2latex
_javaname=writer2latex
_exts=( writer2latex writer2xhtml writer4latex )
pkgver=1.2.1
pkgrel=6
pkgdesc="A java program and collection of LibreOffice extensions for converting to and working with LaTeX in LibreOffice"
arch=('any')
url="http://writer2latex.sourceforge.net"
license=('LGPL')
groups=('libreoffice-extensions')
depends=('libreoffice-common' 'java-runtime-headless')
makedepends=('git' 'apache-ant' 'java-environment')
conflicts=('libreoffice-extension-writer2latex-svn')

_nodotver=$(echo "$pkgver" | tr -d .)
_srcname=${_javaname}${_nodotver}
# "$_srcname"::git+"https://github.com/kolewu/writer2latex.git#tag=$pkgver"
source=("http://downloads.sourceforge.net/project/writer2latex/writer2latex/Writer2LaTeX%201.2/writer2latex${_nodotver}.zip"
        'build.xml.patch'
        'writer2latex.sh')
md5sums=('643906496a5ec519cd4ee044305675bc'
         '5fd5b0f73fc7d1e5213f61225ca1726c'
         'f2d20081ef6d5c905d54385697856642')

#prepare() {
#  cd "$srcdir/$_srcname"
#  patch < "$srcdir/build.xml.patch"
#}

build() {
  cd "$srcdir/$_srcname/doc"
  #ant compile jar oxt
  libreoffice --headless --convert-to pdf user-manual.odt
}

package() {
  #Install the extensions
  for _ext in ${_exts[@]}; do
    install -dm755  "$pkgdir/usr/lib/libreoffice/share/extensions"
    unzip -q "$srcdir/$_srcname/$_ext.oxt" -d "$pkgdir/usr/lib/libreoffice/share/extensions/$_ext"
    chmod 0644 "$pkgdir/usr/lib/libreoffice/share/extensions/$_ext/"*.jar
  done

  #Install the java converter
  install -D -m644 "$srcdir/$_srcname/$_javaname.jar" "$pkgdir/usr/share/java/$_javaname/$_javaname.jar"

  #Install shell script to execute java program
  install -D -m755 "$srcdir/$_javaname.sh" "$pkgdir/usr/bin/$_javaname"

  #Install documentation
  install -D -m644 "$srcdir/$_srcname/doc/user-manual.odt" "$pkgdir/usr/share/doc/$pkgname/user-manual.odt"
  install -D -m644 "$srcdir/$_srcname/doc/user-manual.pdf" "$pkgdir/usr/share/doc/$pkgname/user-manual.pdf"
  install -D -m644 "$srcdir/$_srcname/Readme.txt" "$pkgdir/usr/share/doc/$pkgname/Readme.txt"
  install -D -m644 "$srcdir/$_srcname/changelog.txt" "$pkgdir/usr/share/doc/$pkgname/changelog.txt"
  install -D -m644 "$srcdir/$_srcname/History.txt" "$pkgdir/usr/share/doc/$pkgname/History.txt"
}

