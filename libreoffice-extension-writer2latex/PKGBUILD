# Maintainer: Uwe Koloska <kolewu@koloro.de>
# Contributor: EpocSquadron <crimsonmage a gmail>
pkgname=libreoffice-extension-writer2latex
_srcname=writer2latex
_javaname=writer2latex
_exts=( writer2latex writer2xhtml writer4latex )
pkgver=1.2.1
pkgrel=2
pkgdesc="A java program and collection of LibreOffice extensions for converting to and working with LaTeX in LibreOffice"
arch=('any')
url="http://writer2latex.sourceforge.net"
license=('LGPL')
groups=('libreoffice-extensions')
depends=('libreoffice-common' 'java-runtime' 'java-environment')
makedepends=('git' 'apache-ant')
conflicts=('libreoffice-extension-writer2latex-svn')
source=("$_srcname"::git+"https://github.com/kolewu/writer2latex.git#tag=$pkgver"
        'build.xml.patch'
        'writer2latex.sh')
md5sums=('SKIP'
         '5fd5b0f73fc7d1e5213f61225ca1726c'
         '38fd6ef1aaf9ba7872dc5871e303942e')

prepare() {
  cd "$srcdir/$_srcname"
  patch < "$srcdir/build.xml.patch"
}

build() {
  cd "$srcdir/$_srcname"
  ant compile jar oxt
}

package() {
  #Install the extensions
  for _ext in ${_exts[@]}; do
    install -dm755  "$pkgdir/usr/lib/libreoffice/share/extensions"
    unzip -q "$srcdir/$_srcname/target/lib/$_ext.oxt" -d "$pkgdir/usr/lib/libreoffice/share/extensions/$_ext"
    chmod 0755 "$pkgdir/usr/lib/libreoffice/share/extensions/$_ext/"*.jar
  done

  #Install the java converter
  install -D -m755 "$srcdir/$_srcname/target/lib/$_javaname.jar" "$pkgdir/usr/share/java/$_javaname.jar"

  #Install shell script to execute java program
  install -D -m755 "$srcdir/$_javaname.sh" "$pkgdir/usr/bin/$_javaname"
}

