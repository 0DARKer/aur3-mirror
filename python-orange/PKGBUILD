# Contributor: Blaž Tomažič <blaz.tomazic@gmail.com>
pkgname=python-orange
pkgver=20110506 #$(date +"%Y%m%d")
pkgver_download=2011-05-06 #$(date +"%Y-%m-%d")
pkgrel=1
pkgdesc="Open source data visualization and analysis for novice and experts. Data mining through visual programming or Python scripting."
arch=('i686' 'x86_64')
url="http://www.ailab.si/orange"
license=('GPL')
depends=('python2' 'python2-numpy' 'python-numarray' 'python-numeric' 'python2-matplotlib')
optdepends=('pyqwt: GUI support' 'python-networkx')
source=("http://www.ailab.si/orange/download/orange-source-snapshot-$pkgver_download.zip"
        "pyprops.py.patch"
        "pyxtract.py.patch")
md5sums=('97b8c5dc3fc48668a9b67ca33e9242c1'
         'fd16a973d254917a58e2fa4a21ca24e9'
         '72899c7b4b422bf8211d84176900ff37')

_extract_dir="Orange-2.0.0b"

build() {
  cd "${srcdir}"
  
  #patch some files
  patch "${_extract_dir}/source/pyxtract/pyprops.py" pyprops.py.patch
  patch "${_extract_dir}/source/pyxtract/pyxtract.py" pyxtract.py.patch

  cd "${_extract_dir}"
  python2 setup.py build || return 1
}

package() {
  cd "${srcdir}/${_extract_dir}"
  python2 setup.py install --root="${pkgdir}" || return 1
  
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
             $(find ${pkgdir} -name '*.py')
}

# vim:set ts=2 sw=2 et:
