pkgbase=python-orange
pkgname=('python-orange' 'python2-orange')
pkgver=3.1
pkgrel=1
pkgdesc="Open source data visualization and analysis for novice and experts. Data mining through visual programming or Python scripting."
arch=('i686' 'x86_64')
url="http://orange.biolab.si/"
license=('GPL3')

makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('python-pyqt4: GUI support')
source=("https://github.com/biolab/orange3/archive/${pkgver}.tar.gz"
        "http://orange.biolab.si/download/files/archive/Orange-2.7.2.tar.gz")
md5sums=('bb6011d0db23f3ba2720431342614a65'
'SKIP')

build() {
  cd "${srcdir}/orange3-$pkgver"
  python setup.py build

  cd "${srcdir}/Orange-2.7.2"
  python2 setup.py build
}

package_python-orange() {
  depends=('python-matplotlib' 'python-scikit-learn' 'python-pyqt4')

  cd "${srcdir}/orange3-$pkgver"
  python setup.py install --root="${pkgdir}"
}

package_python2-orange() {
  depends=('python2-matplotlib' 'python2-scikit-learn' 'python2-pyqt4')

  cd "${srcdir}/Orange-2.7.2"
  python2 setup.py install --root="${pkgdir}"
}

