# Maintainer: moostik <mooostik_at_gmail.com>
# Contributor: Blaž Tomažič <blaz.tomazic@gmail.com>
 
pkgname=python-orange
pkgver=2.6
pkgrel=1
pkgdesc="Open source data visualization and analysis for novice and experts. Data mining through visual programming or Python scripting."
arch=('any')
url="http://www.ailab.si/orange"
license=('GPL3')
depends=('python2' 'python2-numpy' 'python2-numarray' 'python-numeric' 'python2-matplotlib')
optdepends=('pyqwt: GUI support' 'python-networkx: Network module')
conflicts=('python-orange-hg' 'python-orange-svn')
source=("http://pypi.python.org/packages/source/O/Orange/Orange-${pkgver}.tar.gz")
md5sums=('7c8e4636cc02984a1bb58c3cd45a5d45')
 
build() {
  cd "${srcdir}/Orange-$pkgver"
  python2 setup.py build || return 1
}
 
package() {
  cd "${srcdir}/Orange-$pkgver"
  python2 setup.py install --root="${pkgdir}" || return 1
 
  find ${pkgdir} -name '*.py' | while read FILE; do
    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
           -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
        "$FILE"
  done
}
 
# vim:set ts=2 sw=2 et: