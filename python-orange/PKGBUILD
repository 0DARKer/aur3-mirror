# Contributor: Blaž Tomažič <blaz.tomazic@gmail.com>
pkgname=python-orange
pkgname_download=orange
pkgver=20101130
pkgver_download=2010-11-30
pkgrel=1
pkgdesc="Open source data visualization and analysis for novice and experts. Data mining through visual programming or Python scripting."
arch=('i686' 'x86_64')
url="http://www.ailab.si/orange"
license=('GPL')
depends=('python2' 'python-numpy' 'python-numarray' 'python-numeric' 'python-matplotlib')
makedepends=('make')
optdepends=('pyqwt')
install=('python-orange.install')
source=("http://www.ailab.si/orange/download/orange-source-snapshot-$pkgver_download.zip"
        "orange-canvas.sh")
md5sums=('2b5ebb4172c18c5d3a4f1e57abd10b3b'
         'e0eea2e9eb4c2d090c88423f768ae6d0')

build() {
  # fix to use python2
  pyfiles=$(find "$srcdir" -name '*.py' | grep -v '__init__.py')
  for pyfile in $pyfiles; do
    FIRSTLINE=$(head -1 "$pyfile")
    if [ "$FIRSTLINE" = '#!/usr/bin/env python' ]; then
      sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "$pyfile"
    elif [ "$FIRSTLINE" != '#!/usr/bin/env python2' ]; then
      sed -i 1i'#!/usr/bin/env python2' "$pyfile"
    fi
    unset FIRSTLINE
  done
  makefiles=$(find "$srcdir" -regex '.*Makefile\|.*makefile.defs\|.*makedep.py')
  for makefile in $makefiles; do
    sed -i 's|python |python2 |' "$makefile"
  done

  cd "$srcdir/source"
  make || return 1
}

package() {
  cd "$srcdir"
  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/orange"
  tar -cf - --exclude='.svn' * | tar --no-same-owner -xf - -C "$pkgdir/usr/lib/python2.7/site-packages/orange"
  sh -c "echo '/usr/lib/python2.7/site-packages/orange' > $pkgdir/usr/lib/python2.7/site-packages/orange.pth"
  ln -s /usr/lib/python2.7/site-packages/orange/liborange.so "$pkgdir/usr/lib/liborange.so"
  
  install -Dm755 orange-canvas.sh "$pkgdir/usr/bin/orange-canvas"
}

# vim:set ts=2 sw=2 et:
