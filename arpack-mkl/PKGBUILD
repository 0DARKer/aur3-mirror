# Maintainer: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=arpack-mkl
_pkgrealname=arpack
pkgver=3.1.5
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Fortran77 subroutines designed to solve large scale eigenvalue problems, BLAS&LAPACK from Intel MKL'
url='http://forge.scilab.org/index.php/p/arpack-ng/'
license=('BSD')
depends=('intel-mkl' 'openmpi')
provides=('arpack-ng')
provides=('arpack')
conflicts=('arpack')
options=('libtool' 'staticlibs')

source=("http://forge.scilab.org/index.php/p/arpack-ng/downloads/get/${_pkgrealname}-ng_${pkgver}.tar.gz")
md5sums=('f773f34079a9c24807da6bc2e72fe6df')
sha512sums=('4876b0babb89bbcc39e92b63d4537b860e9cd0b8a188937d3ba815041b7261623cdac25c91c8bef20ebce4271f822307c492cf6c9f22ec3d740218405337a515')

if [ "$CARCH" == "x86_64" ]; then
    _intel_arch=intel64
    _intel_lib=mkl_gf_lp64
elif [ "$CARCH" == "i686" ]; then
    _intel_arch=ia32
    _intel_lib=mkl_gf
fi

build() {
  # Set up the environment for MKL
  source /opt/intel/mkl/bin/mklvars.sh ${_intel_arch}
  _mkllibs=" -fopenmp -Wl,--no-as-needed -L${MKLROOT}/lib/intel64 -l${_intel_lib} -lmkl_core -lmkl_sequential -lpthread -lm"

  cd "$srcdir/${_pkgrealname}-ng-${pkgver}"

  ./configure --prefix=/usr --enable-mpi \
  --with-blas="${_mkllibs}" \
  --with-lapack="${_mkllibs}"

  make \
    F77="mpif77" \
    CFLAGS+=" `pkg-config --cflags ompi` " \
    LIBS+=" `pkg-config --libs ompi` "
}

package() {
  cd "$srcdir/${_pkgrealname}-ng-${pkgver}"

  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
