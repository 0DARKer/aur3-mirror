# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>
# Contributor: Schtroumpfette <fpeterschmitt@voila.fr>

pkgname=dofus
pkgver=2.22
pkgrel=3
pkgdesc='A manga inspired, Massively Multiplayer Online Role-playing Game (MMORPG) for Adobe AIR .'
arch=('any')
url='http://www.dofus.com/'
license=('custom:Dofus License')
install='dofus.install'
backup=('opt/ankama/dofus/transition.conf')
depends=('ankama-transition>=3.9.2-1' 'adobe-air-sdk')
if [ "$CARCH" = 'x86_64' ]; then
  depends+=('lib32-gtk2' 'lib32-alsa-lib')
else
  depends+=('gtk2' 'alsa-lib')
fi

source=('dofus-release-package.tar.gz'
        'air-generic-launcher.sh'
        'transition.conf.patch'
        'Dofus.patch')
md5sums=('615f532fa7b565038a7979a9fd116f1b'
         'f179eaa5e6e6674b1853cf826fc33c3a'
         'ea9fb95c6027be5f0efb64fc7038f369'
         '96695668b3d538d294bfbb7372aef166')

# Skip this block if PKGBUILD is sourced by updpkgsums (pstree is used for this check,
# and if not available, this check is skipped)
if ! ( command -v pstree >/dev/null 2>&1 && \
  pstree --show-parents $$ | grep -q updpkgsums ) ; then
  # Remove source and md5sum for the wrong arch
  if [ "$CARCH" == "x86_64" ];then
    unset source[1] md5sums[1]
  elif [ "$CARCH" == 'i686' ]; then
    unset source[0] md5sums[0]
  fi
fi

prepare() {
  cd "$srcdir"
  msg2 "Modifying transition configuration to use adl-based launchers"
  patch -p0 < transition.conf.patch

  msg2 "Modifying launcher to avoid crash on start up"
  patch -p0 < Dofus.patch
}

package() {
  cd "$srcdir"
  msg2 'Installing main applications...'
  mv usr opt "$pkgdir/"

  msg2 'Setting up game file permissions...'
  _installdir="$pkgdir/opt/ankama/dofus"
  chgrp -R games $_installdir
  chmod -R g+w $_installdir

  msg2 'Installing adl based launchers...'
  install -Dm755 'air-generic-launcher.sh' "$_installdir/bin/air-generic-launcher.sh"
  install -Dm755 'air-generic-launcher.sh' "$_installdir/share/reg/bin/air-generic-launcher.sh"

  msg2 'Installing transition update file...'
  ln -s '/opt/ankama/transition/' "$_installdir/transition"
}

# vim:set ts=2 sw=2 et:
