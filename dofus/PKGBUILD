# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: p2k <Patrick.Schneider@uni-ulm.de>
# Contributor: Schtroumpfette <fpeterschmitt@voila.fr>

# To make AUR accept the package
pkgname=dofus
pkgver=2.18
pkgrel=10
pkgdesc='A manga inspired, Massively Multiplayer Online Role-playing Game (MMORPG) for Adobe AIR .'
arch=('i686' 'x86_64')
url='http://www.dofus.com/'
license=('custom:Dofus License')
install='dofus.install'
backup=('opt/ankama/dofus/transition.conf')
depends=('ankama-transition>=3.8.1-3' 'adobe-air-sdk')
if [ "$CARCH" == 'x86_64' ]; then
  depends+=('lib32-gtk2' 'lib32-webkitgtk2' 'lib32-libxml2' 'lib32-nss' 'lib32-alsa-lib')
else
  depends+=('gtk2' 'webkitgtk2' 'libxml2' 'nss' 'alsa-lib')
fi

source=('DofusInstall-x86.tar.gz::http://download.dofus.com/full/linux/'
        'dofus.sh'
        'dofus.desktop'
        'air-generic-launcher.sh'
        'transition.conf.append')
if [ "$CARCH" == 'x86_64' ]; then
  source[0]='DofusInstall-amd64.tar.gz::http://download.dofus.com/full/linux/x64'
fi

md5sums=('95f61eb7264a3b7d99c41aa0f084e7ed'
         '87a6d34df0e1484cf3e2a5c4ec937277'
         '98e5aed1b3450afda1f4f2188dab0731'
         'f179eaa5e6e6674b1853cf826fc33c3a'
         '0241708575069715d56b88b96dcbf7e8')

prepare() {
  cd "$srcdir"
  msg2 "Modifying transition configuration to use adl-based launchers"
  cat 'transition.conf.append' >> 'Dofus/transition.conf'
}

package() {
  _installdir="$pkgdir/opt/ankama/dofus"
  install -d "$_installdir"

  cd "$srcdir"
  msg2 'Installing menu entry...'
  install -Dm755 'dofus.sh' "$pkgdir/usr/bin/dofus"
  install -Dm644 'dofus.desktop' "$pkgdir/usr/share/applications/dofus.desktop"
  install -Dm644 'Dofus/share/icon/dofus-icon-128.png' "$pkgdir/usr/share/pixmaps/dofus.png"

  msg2 'Installing main applications...'
  mv "Dofus/"{bin,share,transition.conf} "$_installdir"

  msg2 'Setting up game file permissions...'
  chgrp -R games $_installdir
  chmod -R g+w $_installdir
  chmod 666 "$_installdir/share/META-INF/AIR/application.xml"

  msg2 'Installing adl based launchers...'
  install -Dm755 'air-generic-launcher.sh' "$_installdir/bin"
  install -Dm755 'air-generic-launcher.sh' "$_installdir/share/reg/bin"

  msg2 'Installing transition update file...'
  ln -s '/opt/ankama/transition/' "$_installdir/transition"
}

# vim:set ts=2 sw=2 et:
