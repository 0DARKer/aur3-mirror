# Maintainer: p2k <Patrick.Schneider@uni-ulm.de>
# Original Maintainer: Schtroumpfette <fpeterschmitt@voila.fr>
pkgname=dofus
pkgver=2.0
pkgrel=10
pkgdesc="A free, manga inspired, Massively Multiplayer Online Role-playing Game (MMORPG) for Adobe AIR."
url="http://www.dofus.com/"
license="custom:Dofus License"
arch=('i686' 'x86_64')
source=(http://dl.ak.ankama.com/games/dofus2/setup/DofusInstall.run air-app-installer UpLauncher.arch.sh)
if [ "$CARCH" == "x86_64" ];then
  depends=('bin32-adobe-air' 'lib32-libidn' 'lib32-gtk2' 'lib32-freetype2' 'lib32-libxt'
           'lib32-libjpeg6' 'lib32-libpng12' 'lib32-openssl' 'lib32-openssl-compatibility'
           'lib32-libldap' 'lib32-gvfs' 'lib32-nss' 'lib32-libkrb53' 'lib32-libxslt'
           'lib32-dbus-glib' 'lib32-alsa-lib' 'lib32-util-linux-ng')
else
  depends=('adobe-air-sdk' 'libidn' 'gtk2' 'freetype2' 'libxt'
           'libjpeg6' 'libpng12' 'openssl' 'openssl-compatibility'
           'libldap' 'gvfs' 'nss' 'krb5' 'libxslt'
           'alsa-lib' 'util-linux-ng')
fi
makedepends=('unzip')
install=(dofus.install)
md5sums=('50f1c0db6d8d10592e832056dbd1615e'
         'fb20eba1cd6ccab978d75c5b75d836ab'
         'ae460da240d653d796e5c8f703d572c0')

build() {
  cd "$srcdir"

  msg2 "Extracting installer..."
  sh ./DofusInstall.run --noexec --keep --target dofus_install
}

package() {
  cd "$srcdir/dofus_install"
  install_dir="$pkgdir/opt/dofus"

  msg2 "Installing AIR applications..."
  sh "$srcdir/air-app-installer" Dofus.air "$install_dir"
  sh "$srcdir/air-app-installer" Reg.air "$install_dir/share/reg"

  msg2 "Installing additional content..."
  mv "$install_dir/share/reg/share/Reg.swf" "$install_dir/share/reg/Reg.swf"
  ln -s ../Reg.swf "$install_dir/share/reg/share/Reg.swf"
  ln -s ../content "$install_dir/share/reg/share/content"
  install UpLauncher "$install_dir/share/UpLauncher"
  install -m 644 games.xml "$install_dir/share/games.xml"

  msg2 "Installing launcher script..."
  install "$srcdir/UpLauncher.arch.sh" "$install_dir/share/UpLauncher.arch.sh"
  mkdir -p "$pkgdir/usr/bin"
  ln -s /opt/dofus/share/UpLauncher.arch.sh "$pkgdir/usr/bin/dofus"

  msg2 "Installing menu icon..."
  mkdir -p "$pkgdir/usr/share/applications"
  cat > "$pkgdir/usr/share/applications/Dofus.desktop" <<EOF
[Desktop Entry]
Encoding=UTF-8
Type=Application
Name=Dofus
GenericName=Dofus
Comment=Dofus
Icon=/opt/dofus/share/icon/dofus-icon-48.png
Exec=/opt/dofus/share/UpLauncher.arch.sh
Path=/opt/dofus/share/
Categories=Game;RolePlaying;Ankama-Games
EOF

  msg2 "Installing licenses..."
  mkdir -p "$pkgdir/usr/share/licenses/dofus"
  cp -R licenses/*.txt "$pkgdir/usr/share/licenses/dofus"
}
# vim:set ts=2 sw=2 et:
