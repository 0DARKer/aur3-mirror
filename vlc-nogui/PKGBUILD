# Maintainer: Alexsandr Pavlov <kidoz at mail dot ru>

pkgname=vlc-nogui
pkgver=1.1.11
pkgrel=2
pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'faad2' 'fluidsynth' 'libmtp' 'libdvbpsi' 'lirc-utils'
         'libdca' 'libproxy' 'sdl_image' 'libdvdnav' 
         'lua' 'libv4l' 'libcddb' 'smbclient' 'libmatroska' 'zvbi' 
         'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg'
         'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug'
         'ttf-freefont' 'libxv' 'libass' 'xdg-utils' 'libxpm')
makedepends=('alsa-lib' 'avahi' 'jack' 'live-media' 'pkgconfig' 'udev')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin')
provides=('vlc')
conflicts=('vlc' 'vlc-plugin')
replaces=('vlc' 'vlc-plugin')
options=('!libtool')
source=(http://downloads.videolan.org/pub/videolan/vlc/${pkgver}/vlc-${pkgver}.tar.bz2)
md5sums=('a64846d6f21ea179ae8e8bfb6f9447fe')

build() {
  cd "${srcdir}/vlc-${pkgver}"

  sed -i -e 's:truetype/freefont:TTF:g' modules/misc/freetype.c
  ./configure --prefix=/usr \
              --enable-faad \
              --enable-v4l \
              --enable-theora \
              --enable-flac \
              --enable-snapshot \
              --enable-ogg \
              --enable-shout \
              --enable-lirc \
              --enable-pvr \
              --enable-realrtsp \
              --disable-rpath \
              --disable-qt4 \
              --disable-skins2 \
              --disable-glx \
              --with-live555-tree=/usr/lib/live
  make
}

package() {
  cd "${srcdir}/vlc-${pkgver}"
  
  make DESTDIR=${pkgdir}/ install

  rm -rf ${pkgdir}/usr/share/applications
  rm -rf ${pkgdir}/usr/share/kde4
  rm -rf ${pkgdir}/usr/share/icons
  rm ${pkgdir}/usr/share/vlc/vlc.ico
}
