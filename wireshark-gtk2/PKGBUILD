# Maintainer: Mitch Bigelow <ipha00@gmail.com>

pkgname=wireshark-gtk2
pkgbase=wireshark
pkgver=1.10.0
pkgrel=1
pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK2 frontend'
arch=('i686' 'x86_64')
url='http://www.wireshark.org/'
license=('GPL2')

depends=('krb5' 'libcap' 'libpcap' 'bash' 'gnutls' 'libgcrypt' 'glib2' 'lua51' 'gtk2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('bison' 'flex' 'python')

provides=('wireshark-cli' 'wireshark-gtk')
options=(!libtool)
install=wireshark-gtk2.install
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2
		01_fix_encoding_asn2deb_pod.patch
		02_fix_encoding_idl2deb_pod.patch
		03_force_utf8_for_pod2man.patch)
sha256sums=('1f4b377ba6284a51797bcc437aa2918bfaeb5d30908cd6194bd09f7054c65add'
			'0dfe86b1e4bbf4090035fc1fcd36c2790d418a5749e23a2a7e7285699a6c147d'
			'a46f6e87477fcbf8289bfde5e525e6c0b4c3268bd1a2e189e3120bd3ad4f7c27'
			'902f72132b9e0dd95f89ca0bcb216ef9c866ac11c842866781d239b6b7873daf')

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

	# https://bugs.wireshark.org/bugzilla/show_bug.cgi?id=8774
	for p in \
		01_fix_encoding_asn2deb_pod.patch \
		02_fix_encoding_idl2deb_pod.patch \
		03_force_utf8_for_pod2man.patch;
	do
		patch -p0 <${srcdir}/${p}
	done

	./autogen.sh
	./configure \
		--prefix=/usr \
		--with-ssl \
		--with-zlib=yes \
		--with-lua \
		--with-gtk3=no
	make all
}

package() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	#wireshark uid group is 150
	chgrp 150 "${pkgdir}/usr/bin/dumpcap"
	chmod 754 "${pkgdir}/usr/bin/dumpcap"

	# Headers
	install -dm755 ${pkgdir}/usr/include/${pkgbase}/{epan/{crypt,dfilter,dissectors,ftypes},wiretap,wsutil}

	install -m644 color.h config.h register.h "${pkgdir}/usr/include/${pkgbase}"
	for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes wiretap wsutil; do
		install -m644 ${d}/*.h ${pkgdir}/usr/include/${pkgbase}/${d}
	done

	# Icons
	for d in 16 32 48; do
		install -Dm644 image/hi${d}-app-wireshark.png "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png"
	done

	for d in 16 24 32 48 64 128 256 ; do
		install -Dm644 image/WiresharkDoc-${d}.png "${pkgdir}/usr/share/icons/hicolor/${d}x${d}/mimetypes/application-vnd.tcpdump.pcap.png"
	done

	install -Dm644 wireshark.desktop "${pkgdir}/usr/share/applications/wireshark.desktop"
}
