# Maintainer: ponsfoot <cabezon dot hashimoto at gmail dot com>

## You can choose the input method framework to use either ibus, scim, uim or both.
## If you will be not using ibus, comment out below.
_ibus_mozc="yes"
## If you will be using scim, uncomment below.
_scim_mozc="yes"
## If you will be using uim, uncomment below.
_uim_mozc="yes"

## If you will be using mozc.el on Emacs, uncomment below.
_emacs_mozc="yes"

#*************************************************************
# Upstreams:
#
# mozc
# http://code.google.com/p/mozc/
#
# Alternative dictionary by UTSUMI
# http://www.geocities.jp/ep3797/mozc_01.html"
#
# uim-mozc by macuim
# http://code.google.com/p/macuim/
#
#*************************************************************

pkgbase=mozc-ut
pkgname=mozc-ut
true && pkgname=('mozc-server-ut' 'mozc-utils-gui-ut')
_origname=mozc
_mozcver=1.1.690.102
_utver=20110409
_uimmozcrev=222
_protobuf_ver=2.3.0
pkgver=${_mozcver}.${_utver}
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.geocities.jp/ep3797/mozc_01.html"
license=('BSD' 'GPL' 'custom')
makedepends=('pkg-config' 'python2' 'curl' 'qt' 'gtest' 'zinnia' 'protobuf')
changelog=${pkgbase}.ChangeLog
_copt=Release
#_copt=Debug
source=(http://mozc.googlecode.com/files/mozc-${_mozcver}.tar.bz2
        http://downloads.sourceforge.net/mdk-ut/mozcdic-ut-advanced-${_utver}.tar.bz2
        mozc_tool_config.desktop
        mozc_tool_dic.desktop
        ArchLinux-utdic.patch)
sha1sums=('2305e57b63bb5fc631f3e64eab1f1bfffffd3bf0'
          'd3a06fa796feaa57e2c98a7d38f3a56daed44539'
          '66e0141e93129e8ded499cff67cc8828cce5efae'
          '53ff0c5d492665046bc3224b354ca57911634d7b'
          'b8f3170da6715f1feabfcc316cfedbe3f708815f')

if [[ "$_ibus_mozc" = "yes" ]]; then
    true && pkgname=(${pkgname[@]} 'ibus-mozc-ut')
    makedepends=(${makedepends[@]} 'ibus')
fi
if [[ "$_scim_mozc" = "yes" ]]; then
    true && pkgname=(${pkgname[@]} 'scim-mozc-ut')
    makedepends=(${makedepends[@]} 'scim')
fi
if [[ "$_emacs_mozc" = "yes" ]]; then
    true && pkgname=(${pkgname[@]} 'emacs-mozc-ut' 'emacs-mozc-bin-ut')
fi

if [[ "$_uim_mozc"  = "yes" ]]; then
    true && pkgname=(${pkgname[@]} 'uim-mozc-ut')
    makedepends=(${makedepends[@]} 'uim' 'subversion')
    source=(${source[@]}
            http://protobuf.googlecode.com/files/protobuf-${_protobuf_ver}.tar.bz2)
    sha1sums=(${sha1sums[@]}
              'db0fbdc58be22a676335a37787178a4dfddf93c6')
else
    makedepends=(${makedepends[@]} 'protobuf')
fi

build() {
  if [[ "$_ibus_mozc" != "yes" && "$_uim_mozc" != "yes" && "$_scim_mozc" != "yes" ]]; then
      msg "***********************************************************"
      msg " Please choose either one of ibus, scim or uim at least."
      msg " See PKGBUILD."
      msg "***********************************************************"
      return 1
  fi

  cd "$srcdir"

  # Checkout macuim-mozc (for uim-mozc)
  if [[ "$_uim_mozc" = "yes" ]]; then
      msg "Connecting to macuim SVN server..."
      if [[ -d uim-mozc/.svn ]]; then
          (cd uim-mozc ; svn update)
      else
          svn co http://macuim.googlecode.com/svn/trunk/Mozc uim-mozc -r $_uimmozcrev
      fi
      msg "SVN checkout done or server timeout"
  fi

  cd "${srcdir}/${_origname}-${_mozcver}"

  # uim-mozc
  if [[ "$_uim_mozc" = "yes" ]]; then
      # patch to link protobuf statically
      cp -rf "${srcdir}/protobuf-${_protobuf_ver}" protobuf/files
      patch -p0 -i "${srcdir}/uim-mozc/mozc-linux-protobuf.diff"
      cp -rf "${srcdir}/uim-mozc/uim" unix/
      patch -p0 -i "${srcdir}/uim-mozc/mozc-kill-line.diff"
      #patch -p0 -i "${srcdir}/temp-mozc-kill-line.patch"
  fi

  # Copy dictionary files
  cp -f ${srcdir}/mozcdic-ut-advanced-${_utver}/dictionaries/{dictionary*.txt,zip_code_seed.tsv} \
        data/dictionary/

  # Adjust to use python2
  sed -i '1s|python.*$|python2|'  `find . -name \*.py -type f -print`
  sed -i "s|'python'|'python2'|g" `grep -lR \'python\' * --include=*.gyp{,i}`

  # Modify branding
  patch -Np0 -i "${srcdir}/ArchLinux-utdic.patch"

  _target="server/server.gyp:mozc_server gui/gui.gyp:mozc_tool"
  [[ "$_emacs_mozc" = "yes" ]] && _target="${_target} unix/emacs/emacs.gyp:mozc_emacs_helper"
  [[ "$_ibus_mozc"  = "yes" ]] && _target="${_target} unix/ibus/ibus.gyp:ibus_mozc"
  [[ "$_scim_mozc"  = "yes" ]] && _target="${_target} unix/scim/scim.gyp:scim_mozc unix/scim/scim.gyp:scim_mozc_setup"
  [[ "$_uim_mozc"   = "yes" ]] && _target="${_target} unix/uim/uim.gyp:uim-mozc"

  python2 build_mozc.py gyp
  python2 build_mozc.py build_tools -c $_copt
  python2 build_mozc.py build -c $_copt $_target

  # modify path for ibus-mozc and update icon path in .desktop files and 
  if [[ "$_ibus_mozc" = "yes" ]]; then
      sed -i 's|/usr/libexec/|/usr/lib/ibus-mozc/|g' out_linux/${_copt}/obj/gen/unix/ibus/mozc.xml
      _icon_prop="/usr/share/ibus-mozc/properties.png"
      _icon_dic="/usr/share/ibus-mozc/dictionary.png"
  elif [[ "$_scim_mozc" = "yes" ]]; then
      _icondir=`pkg-config scim --variable=icondir`
      _icon_prop="${_icondir}/scim-mozc-properties.png"
      _icon_dic="${_icondir}/scim-mozc-dictionary.png"
  else
      _icon_prop="/usr/share/uim/pixmaps/mozc-properties.png"
      _icon_dic="/usr/share/uim/pixmaps/mozc-dictionary.png"
  fi
  sed "s|^Icon=|Icon=${_icon_prop}|" "${srcdir}/mozc_tool_config.desktop" > mozc_tool_config.desktop
  sed "s|^Icon=|Icon=${_icon_dic}|"  "${srcdir}/mozc_tool_dic.desktop"    > mozc_tool_dic.desktop
}

package_mozc-server-ut() {
  pkgdesc="Server part of the Mozc input method"
  arch=('i686' 'x86_64')
  depends=('curl')
  [[ "$_uim_mozc" != "yes" ]] && depends=(${depends[@]} 'protobuf')
  replaces=('mozc')
  provides=('mozc' 'mozc-server')
  conflicts=('mozc' 'mozc-server')

  cd "${srcdir}/${_origname}-${_mozcver}"
  install -D -m 755 out_linux/${_copt}/mozc_server "${pkgdir}/usr/lib/mozc/mozc_server"
  install -d "${pkgdir}/usr/lib/mozc/documents/"
  install    -m 644 data/installer/*.html "${pkgdir}/usr/lib/mozc/documents/"
}

package_mozc-utils-gui-ut() {
  pkgdesc="Mozc GUI uitilities, such as the configuration dialog, user dictionary tool and about dialog"
  arch=('i686' 'x86_64')
  depends=('mozc-server' 'qt')
  provides=('mozc-utils-gui')
  conflicts=('mozc-utils-gui')

  cd "${srcdir}/${_origname}-${_mozcver}"
  install -D -m 755 out_linux/${_copt}/mozc_tool "${pkgdir}/usr/lib/mozc/mozc_tool"

  # install .desktop files
  install -d "${pkgdir}/usr/share/applications"
  install -m 644 *.desktop "${pkgdir}/usr/share/applications/"
}

package_emacs-mozc-ut() {
  pkgdesc="Mozc for Emacs"
  arch=('any')
  depends=('emacs-mozc-bin' 'emacs')
  install=emacs-mozc.install
  provides=('emacs-mozc')
  conflicts=('emacs-mozc')

  cd "${srcdir}/${_origname}-${_mozcver}"
  install -D -m 644 unix/emacs/mozc.el "${pkgdir}/usr/share/emacs/site-lisp/emacs-mozc/mozc.el"
}

package_emacs-mozc-bin-ut() {
  pkgdesc="Helper module for emacs-mozc"
  arch=('i686' 'x86_64')
  depends=('mozc-server')
  provides=('emacs-mozc-bin')
  conflicts=('emacs-mozc-bin')

  cd "${srcdir}/${_origname}-${_mozcver}"
  install -D -m 755 out_linux/${_copt}/mozc_emacs_helper "${pkgdir}/usr/bin/mozc_emacs_helper"
}

package_ibus-mozc-ut() {
  pkgdesc="IBus engine module for Mozc"
  arch=('i686' 'x86_64')
  depends=('mozc-server' 'ibus')
  provides=('ibus-mozc')
  conflicts=('ibus-mozc')

  cd "${srcdir}/${_origname}-${_mozcver}"
  install -D -m 755 out_linux/${_copt}/ibus_mozc          "${pkgdir}/usr/lib/ibus-mozc/ibus-engine-mozc"
  install -D -m 644 out_linux/${_copt}/obj/gen/unix/ibus/mozc.xml "${pkgdir}/usr/share/ibus/component/mozc.xml"
  install -D -m 644 data/images/unix/ime_product_icon_opensource-32.png "${pkgdir}/usr/share/ibus-mozc/product_icon.png"
  install    -m 644 data/images/unix/ui-tool.png          "${pkgdir}/usr/share/ibus-mozc/tool.png"
  install    -m 644 data/images/unix/ui-properties.png    "${pkgdir}/usr/share/ibus-mozc/properties.png"
  install    -m 644 data/images/unix/ui-dictionary.png    "${pkgdir}/usr/share/ibus-mozc/dictionary.png"
  install    -m 644 data/images/unix/ui-direct.png        "${pkgdir}/usr/share/ibus-mozc/direct.png"
  install    -m 644 data/images/unix/ui-hiragana.png      "${pkgdir}/usr/share/ibus-mozc/hiragana.png"
  install    -m 644 data/images/unix/ui-katakana_half.png "${pkgdir}/usr/share/ibus-mozc/katakana_half.png"
  install    -m 644 data/images/unix/ui-katakana_full.png "${pkgdir}/usr/share/ibus-mozc/katakana_full.png"
  install    -m 644 data/images/unix/ui-alpha_half.png    "${pkgdir}/usr/share/ibus-mozc/alpha_half.png"
  install    -m 644 data/images/unix/ui-alpha_full.png    "${pkgdir}/usr/share/ibus-mozc/alpha_full.png"
}

package_scim-mozc-ut() {
  pkgdesc="SCIM IMEngine module for Mozc"
  arch=('i686' 'x86_64')
  depends=('mozc-server' 'scim')
  provides=('scim-mozc')
  conflicts=('scim-mozc')

  cd "${srcdir}/${_origname}-${_mozcver}"
  _moduledir=`pkg-config scim --variable=moduledir`
  install -D -m 755 out_linux/${_copt}/lib.target/libscim_mozc.so       "${pkgdir}${_moduledir}/IMEngine/mozc.so"
  install -D -m 755 out_linux/${_copt}/lib.target/libscim_mozc_setup.so "${pkgdir}${_moduledir}/SetupUI/mozc-setup.so"
  _icondir=`pkg-config scim --variable=icondir`
  install -D -m 644 data/images/unix/ime_product_icon_opensource-32.png "${pkgdir}${_icondir}/scim-mozc.png"
  install    -m 644 data/images/unix/ui-tool.png          "${pkgdir}${_icondir}/scim-mozc-tool.png"
  install    -m 644 data/images/unix/ui-properties.png    "${pkgdir}${_icondir}/scim-mozc-properties.png"
  install    -m 644 data/images/unix/ui-dictionary.png    "${pkgdir}${_icondir}/scim-mozc-dictionary.png"
  install    -m 644 data/images/unix/ui-direct.png        "${pkgdir}${_icondir}/scim-mozc-direct.png"
  install    -m 644 data/images/unix/ui-hiragana.png      "${pkgdir}${_icondir}/scim-mozc-hiragana.png"
  install    -m 644 data/images/unix/ui-katakana_half.png "${pkgdir}${_icondir}/scim-mozc-katakana_half.png"
  install    -m 644 data/images/unix/ui-katakana_full.png "${pkgdir}${_icondir}/scim-mozc-katakana_full.png"
  install    -m 644 data/images/unix/ui-alpha_half.png    "${pkgdir}${_icondir}/scim-mozc-alpha_half.png"
  install    -m 644 data/images/unix/ui-alpha_full.png    "${pkgdir}${_icondir}/scim-mozc-alpha_full.png"
}

package_uim-mozc-ut() {
  pkgdesc="uim IMEngine module for Mozc"
  arch=('i686' 'x86_64')
  depends=('mozc-server' 'uim')
  install=uim-mozc.install
  provides=('uim-mozc')
  conflicts=('uim-mozc')

  cd "${srcdir}/${_origname}-${_mozcver}"
  install -D -m 755 out_linux/${_copt}/libuim-mozc.so "${pkgdir}/usr/lib/uim/plugin/libuim-mozc.so"
  install -d "${pkgdir}/usr/share/uim"
  install    -m 644 ${srcdir}/uim-mozc/scm/*.scm      "${pkgdir}/usr/share/uim/"
  install -D -m 644 data/images/unix/ime_product_icon_opensource-32.png "${pkgdir}/usr/share/uim/pixmaps/mozc.png"
  install    -m 644 data/images/unix/ui-properties.png "${pkgdir}/usr/share/uim/pixmaps/mozc-properties.png"
  install    -m 644 data/images/unix/ui-dictionary.png "${pkgdir}/usr/share/uim/pixmaps/mozc-dictionary.png"
}

# Global pkgdesc and depends are here so that they will be picked up by AUR
pkgdesc="Mozc the Japanese input method using alternative dictionary, with uim-mozc (optional)"
depends=('curl' 'protobuf' 'qt')
