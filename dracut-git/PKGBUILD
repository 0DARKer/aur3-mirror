# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>
_pkgname=dracut
pkgname=dracut-git
pkgver=036.r3.gc8a9a6b
pkgrel=2
pkgdesc="Generic, modular, cross-distribution initramfs generation tool"
arch=('i686' 'x86_64')
url="https://dracut.wiki.kernel.org/"
license=('GPL')
provides=($_pkgname)
conflicts=($_pkgname)
depends=('cpio' 'asciidoc' 'bash')
optdepends=('cryptsetup' 'device-mapper' 'rpcbind' 'mdadm' 'net-tools' 'iproute2' 'lvm2')
makedepends=('git' 'docbook-xsl')
backup=('etc/dracut.conf')
changelog=Changelog
source=("git://git.kernel.org/pub/scm/boot/dracut/dracut.git")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_pkgname"
    # Use the tag of the last commit
    git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/$_pkgname"
    make sysconfdir=/etc prefix=/usr sbindir=/usr/bin
}

package() {
    cd "$srcdir/$_pkgname"
    make DESTDIR="${pkgdir}" sysconfdir=/etc prefix=/usr sbindir=/usr/bin install
} 

