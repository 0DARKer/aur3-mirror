# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=giada
pkgver=0.6.2
pkgrel=5
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=('i686' 'x86_64')
url=http://www.giadamusic.com/
license=(GPL3)
depends=('libxpm' 'fltk' 'rtaudio' 'rtmidi')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/download-action.php?os=source"
    "$pkgname-logo.png"
    "$pkgname.desktop"
    "$pkgname.install"
    'samplerate.patch')
    
# doesn't extract to a distinct directory
noextract=("$pkgname-$pkgver.tar.gz")

prepare() {
	mkdir -p "$srcdir/$pkgname-$pkgver"
	# extract to distinct directory 
    bsdtar xf "${srcdir}/$pkgname-$pkgver.tar.gz" \
		-C "$srcdir/$pkgname-$pkgver"
    cd "$srcdir/$pkgname-$pkgver"
    # BUG: giada defaults on 44100 sampling rate becoming
    #      not jack server, run at 48000 (default), capable.
    # FIX: forcing giada working at 48000.
    # WIP: auto detect sampling rate safely (have little issues ATM)
    # FAQ: comment next line if you do not intend using it with jack@48000
	patch -p0 -i "$srcdir/samplerate.patch"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --target=linux 
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm644 "$srcdir/$pkgname-logo.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

md5sums=('70a87311e87e79d6a96eda4e849a4260'
         'e712157099a1be39f2adcfc4630d961e'
         '135bef9e2df07b7550a2f4e4358b6933'
         '0b6211b511da48346cecdc1d2f963c76'
         '1a5b38cab9f7ddfffbaae04c6b771ef8')
