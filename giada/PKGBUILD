# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=giada
pkgver=0.4.8
pkgrel=3
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=(i686 x86_64)
url="http://www.monocasual.com/giada/"
license=('GPL3')
depends=('libxpm' 'fltk' 'rtaudio')
install="$pkgname.install"
#options=('!strip') #debug
source=("${url}download.php?dist=source&file=bin/${pkgname}_${pkgver}_src.tar.gz"
        "$pkgname.png::http://www.monocasual.com/img/logoindex_giada.png"
        "$pkgname.desktop" "$pkgname-cosmetic.patch")
md5sums=('87bc25c7b1edf234f3d5db0bd86e501f'
         '91ce67f32b923dcba6fad82ca60ca80c'
         '135bef9e2df07b7550a2f4e4358b6933'
         'eb8ef7fcc8798bacfe2f3313488f71f7')

build() {
  cd "$srcdir"

  # find rtaudio headers
  export CXXFLAGS="$CXXFLAGS -I/usr/include/rtaudio"

  # debug
#  export CXXFLAGS+=" -g"

  # x86_64 fix
  sed -i "s/\((void\*)\)\([ic]\)/\1(intptr_t)\2/" src/gg_keyboard.cpp

  # cosmetic patch (menus appearing elsewhere)
  patch -p1 -i ${source[3]}

  # BUG: giada defaults on 44100 sampling rate becoming
  #      not jack server, run at 48000 (default), capable.
  # FIX: forcing giada working at 48000.
  # WIP: auto detect sampling rate safelly (have little issues ATM)
  # FAQ: comment next line if you do not intend using it with jack@48000
  sed -i 's/44100/48000/' `grep -rl 44100 .`

  ./configure --prefix=/usr \
              --target=linux
  make
}

package() {
  cd "$srcdir"
  make DESTDIR="$pkgdir/" install

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 $pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
