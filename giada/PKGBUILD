# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=giada
pkgver=0.9.0
pkgrel=1
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=('i686' 'x86_64')
url=http://www.giadamusic.com/
license=(GPL3)
depends=('libxpm' 'fltk' 'libpulse' 'jack' 'rtmidi>=2.1.0')
install="$pkgname.install"
##
## upstream removed embedded source and requires a system installed 'hacked' rtaudio lib
## I don't like it 
##
_rtaudio=rtaudio-4.1.1
_giada="$pkgname-$pkgver-src"
source=("http://sourceforge.net/projects/giada-looper/files/giada-${pkgver}-src.tar.gz"
	"http://www.music.mcgill.ca/~gary/rtaudio/release/$_rtaudio.tar.gz"
    'giada-logo.png'
    'giada.desktop'
    'include-fixes.patch'
    'rtaudio-makefile.patch'
    'steinberg-vst24.patch')

#"$pkgname-$pkgver.tar.gz::$url/download-action.php?os=source&version=$pkgver"    
# http://www.giadamusic.com/download/grab/source

provides=('giada')
conflicts=('giada-git')
    
prepare() {
	## prepare new rtaudio src
	rm -fr $_rtaudio/{doc,autom4te.cache,contrib}
	patch -p1 -i $_giada/src/rtaudio/rtaudio-4.1.1-giada.patch
	patch -p1 -i "$srcdir"/rtaudio-makefile.patch
	(cd $_rtaudio; aclocal; autoconf)
    cd $_giada;   
    patch -p1 -i "$srcdir"/include-fixes.patch
	## patch to use steinberg-vst24 from AUR
	## if you have it, add --enable-vst further down
#	patch -p1 -i "$srcdir/steinberg-vst24.patch"
    ##
	## Alternativly, leave out the patch, google for 'vst/aeffectx.h' 
	## and put aeffect.h, aeffectx.h, vstfxstore.h in src/vst/ ;)
	##
	## force use of system CFLAGS/CXXFLAGS
	#sed -i 's:CFLAGS   = @CXXFLAGS@:CFLAGS   += @CXXFLAGS@:' src/rtaudio/Makefile.in
}


build() {
	cd $_rtaudio
	  ./configure --with-jack --with-alsa --with-pulse
	  make
	  cp RtAudio.h librtaudio.a ../$_giada/src/rtaudio
    cd ../$_giada
    # link static lib
    sed 's@-lrtaudio@rtaudio/librtaudio.a@g' -i src/Makefile.in 
    # rtaudio pkg-config flags - adopt according to configure flags: --with-jack --with-alsa --with-pulse
      ./configure CPPFLAGS="$CPPFLAGS -pthread -DHAVE_GETTIMEOFDAY -D__UNIX_JACK__ -D__LINUX_ALSA__ -D__LINUX_PULSE__ -I./rtaudio -I./src/rtaudio" \
		--prefix=/usr --target=linux #--enable-vst 
    make
}

package() {
    cd $_giada
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm644 "$srcdir/$pkgname-logo.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

md5sums=('ee60b8470aefc911586e41cb744585d7'
         '99763d3187fbe24ca959a1b711c17984'
         'e712157099a1be39f2adcfc4630d961e'
         '135bef9e2df07b7550a2f4e4358b6933'
         '432a6c6aca5906478cc81923022a8b7b'
         'b97ae96f519daf90f82020ae18f1e4e9'
         'ad3b4feac7bb5ad8dbd831e9fd7c0ac7')


