# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rtfreedman
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=giada
pkgver=0.6.1
pkgrel=1
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=(i686 x86_64)
url=http://www.giadamusic.com/
license=(GPL3)
depends=(libxpm fltk rtaudio)
install=$pkgname.install
#options=(!strip) #debug
source=($pkgname-$pkgver.tar.gz::$url/download-action.php?os=source
    $url/img/$pkgname-logo.png
    $pkgname.desktop)
sha256sums=('f62fe24f38adda2bd03035b75f503e096170748c0331a6af9d40ef1cbd8c0706'
    'a0319be4b7f25ac52beee2e5d14b60bdb253ad574c36b49be63ac05328823512'
    'ab645cad52cf908f4d1957a0d166ee9ad3ec9f24b2593310fd634d648e2ce915')
sha512sums=('ce5ff04b2d8695cc02425fd4bc4780094209d8ab5875d580f2a5177b329b2e1071af20fa20f9ee27c18bd0c2cb94a9847b0caa650b96232cfb66640d867b3f03'
    'eb59a4f4a844e0c19939e5776d7fdc5fc8c737287930ad7250bd434a158b00bb536799e559c067715db567e4dd13baebfc01ee4cafb4000ec4f45148f1a6fe79'
    'f97dbcc0c76a08f456a205d10bf46d1cab1e61f203c7e693727c678ccf8f1b853faacf261da6b331f54452c379a6e0ad65800f8bcac47e3fbcd5b5ea0f4c9c22')

build() {
    export CXXFLAGS+=" -I/usr/include/rtaudio" # -g
    export CPPFLAGS+=" -I/usr/include/rtaudio"

    sed -i "s:\((void\*)\)\([ic]\):\1(intptr_t)\2:" src/gg_keyboard.cpp

    # BUG: giada defaults on 44100 sampling rate becoming
    #      not jack server, run at 48000 (default), capable.
    # FIX: forcing giada working at 48000.
    # WIP: auto detect sampling rate safely (have little issues ATM)
    # FAQ: comment next line if you do not intend using it with jack@48000
    find -type f -exec sed -i 's:44100:48000:' '{}' \;

    ./configure --prefix=/usr --target=linux
    make -C src
}

package() {
    make DESTDIR="$pkgdir" install
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 $pkgname-logo.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
