# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=giada
pkgver=0.4.5
pkgrel=2
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=(i686 x86_64)
url="http://www.monocasual.com/giada/"
license=('GPL3')
depends=('libxpm' 'fltk' 'rtaudio')
install="$pkgname.install"
#options=('!strip') #debug
source=("${url}download.php?dist=source&file=bin/${pkgname}_${pkgver}_src.tar.gz"
        "$pkgname.png::http://www.monocasual.com/img/logoindex_giada.png"
        "$pkgname.desktop")
md5sums=('1220b443f0b3662d737bb95d8455f63f'
         '91ce67f32b923dcba6fad82ca60ca80c'
         '135bef9e2df07b7550a2f4e4358b6933')

build() {
  cd "$srcdir"

  # find rtaudio headers
  export CXXFLAGS="$CXXFLAGS -I/usr/include/rtaudio"

  # debug
  # export CXXFLAGS="$CXXFLAGS -g"

  # gcc 4.6 and x86_64 fixes
  sed -i "s/(void\*) *\([[:alnum:]_]\+\)/reinterpret_cast<void*>(\1)/" src/gg_keyboard.cpp
  sed -i "s/(int)p/(long)p/" src/{ge_mixed.cpp,gd_mainWindow.cpp,gg_keyboard.cpp}

  # BUG: giada defaults on 44100 sampling rate becoming
  #      not jack server, run at 48000 (default), capable.
  # FIX: forcing giada working at 48000.
  # WIP: auto detect sampling rate safelly (have little issues ATM)
  # FAQ: comment next line if you do not intend using it with jack@48000
  sed -i 's/44100/48000/' `grep -rl 44100 .`

  ./configure --prefix=/usr \
              --target=linux
  make
}

package() {
  cd "$srcdir"
  make DESTDIR="$pkgdir/" install

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 $pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
