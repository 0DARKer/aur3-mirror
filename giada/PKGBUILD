# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=giada
pkgver=0.7.1
pkgrel=2
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=('i686' 'x86_64')
url=http://www.giadamusic.com/
license=(GPL3)
#depends=('libxpm' 'fltk' 'rtaudio' 'rtmidi')
depends=('libxpm' 'fltk' 'rtmidi')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/download-action.php?os=source&version=$pkgver"
    "$pkgname-logo.png"
    "$pkgname.desktop"
    "samplerate.patch"
    "main.cpp.patch"
    "steinberg-vst24.patch")
    
_srcdir="$pkgname-$pkgver-src"

prepare() {
    cd "$_srcdir"
    # BUG: giada defaults on 44100 sampling rate becoming
    #      not jack server, run at 48000 (default), capable.
    # FIX: forcing giada working at 48000.
    # WIP: auto detect sampling rate safely (have little issues ATM)
    # FAQ: comment next line if you do not intend using it with jack@48000
	patch -p0 -i "$srcdir/samplerate.patch"
	# patch to use steinberg-vst24 from AUR
	# if you have it, add --enable-vst further down
	patch -p1 -i "$srcdir/steinberg-vst24.patch"
	# Alternativly, leave out the patch, google for 'vst/aeffectx.h' 
	# and put aeffect.h, aeffectx.h, vstfxstore.h in src/vst/ ;)
	#
	# include usleep from unistd.h
	# fixed in git
	patch -p0 -i "$srcdir/main.cpp.patch"
}


build() {
    cd "$_srcdir"
    ./configure --prefix=/usr --target=linux #--enable-vst 
    (cd src; make libs)
    sed -i 's:-lpulse-simple:-lpulse-simple  -lpulse:' src/Makefile
    make
}

package() {
    cd "$_srcdir"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm644 "$srcdir/$pkgname-logo.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

md5sums=('1d256f68540b46ee8f2aaf9e8c220cc2'
         'e712157099a1be39f2adcfc4630d961e'
         '135bef9e2df07b7550a2f4e4358b6933'
         '1a5b38cab9f7ddfffbaae04c6b771ef8'
         '1d3b318ee3f67eda7d09d815edb8498a'
         'ad3b4feac7bb5ad8dbd831e9fd7c0ac7')
