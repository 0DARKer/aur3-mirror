# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rtfreedman
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=giada
pkgver=0.6.2
pkgrel=3
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=(i686 x86_64)
url=http://www.giadamusic.com/
license=(GPL3)
depends=(fltk libxpm rtaudio)
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::$url/download-action.php?os=source
    http://dl.dropbox.com/u/3395784/aur/$pkgname/$pkgname.png
    $pkgname.desktop)
sha256sums=('0525e40da02e251e8f09b360452924e6c4a8ba7cfede4a1bbda4f8c24a33be0b'
    'a0319be4b7f25ac52beee2e5d14b60bdb253ad574c36b49be63ac05328823512'
    'ab645cad52cf908f4d1957a0d166ee9ad3ec9f24b2593310fd634d648e2ce915')
sha512sums=('b9a7b2456d36a49e17b507056a0a0f6ab7919120fdf5eded89b3433e00ca00cd780fb62ed0c17900d0126dac6ef21962a0a990217fb67f01556925e2dfc1ba40'
    'eb59a4f4a844e0c19939e5776d7fdc5fc8c737287930ad7250bd434a158b00bb536799e559c067715db567e4dd13baebfc01ee4cafb4000ec4f45148f1a6fe79'
    'f97dbcc0c76a08f456a205d10bf46d1cab1e61f203c7e693727c678ccf8f1b853faacf261da6b331f54452c379a6e0ad65800f8bcac47e3fbcd5b5ea0f4c9c22')

prepare() {
    sed -i '/AM_INIT_AUTOMAKE/ s:(.*::' configure.ac
    sed -i '/#define DEFAULT_SAMPLERATE/ s:44100:48000:' src/const.h
}

build() {
    export CXXFLAGS+=" -I/usr/include/rtaudio"
    export CPPFLAGS+=" -I/usr/include/rtaudio"
    autoreconf -fi
    ./configure --prefix=/usr --target=linux
    make
}

package() {
    make DESTDIR="$pkgdir" install
    desktop-file-install $pkgname.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
