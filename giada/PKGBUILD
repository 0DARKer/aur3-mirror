# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=giada
pkgver=0.8.4
pkgrel=2
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=('i686' 'x86_64')
url=http://www.giadamusic.com/
license=(GPL3)
depends=('libxpm' 'fltk' 'libpulse' 'jack' 'rtmidi>=2.1.0')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/download-action.php?os=source&version=$pkgver"
    "$pkgname-logo.png"
    "$pkgname.desktop"
    "steinberg-vst24.patch"
    'gd_config.patch')

provides=('giada')
conflicts=('giada-git')
    
_srcdir="$pkgname-$pkgver-src"

prepare() {
    cd "$_srcdir"
	## patch to use steinberg-vst24 from AUR
	## if you have it, add --enable-vst further down
#	patch -p1 -i "$srcdir/steinberg-vst24.patch"
	## Alternativly, leave out the patch, google for 'vst/aeffectx.h' 
	## and put aeffect.h, aeffectx.h, vstfxstore.h in src/vst/ ;)
	## force use of system CFLAGS/CXXFLAGS
	sed -i 's:CFLAGS   = @CXXFLAGS@:CFLAGS   += @CXXFLAGS@:' src/rtaudio/Makefile.in
	## API change in RtMidi 2.1.0
	sed -i 's/RtError/RtMidiError/g' src/kernelMidi.cpp
	patch -p1 -i "$srcdir/gd_config.patch"
}


build() {
    cd "$_srcdir"
    ./configure --prefix=/usr --target=linux #--enable-vst 
    (cd src; make libs)
    make
}

package() {
    cd "$_srcdir"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm644 "$srcdir/$pkgname-logo.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

md5sums=('2e6e685f34e461ed79dbd859a98bc2dd'
         'e712157099a1be39f2adcfc4630d961e'
         '135bef9e2df07b7550a2f4e4358b6933'
         'ad3b4feac7bb5ad8dbd831e9fd7c0ac7'
         '64147d0ac79197135362204773989d95')


