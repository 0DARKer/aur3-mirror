# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=giada
pkgver=0.5.6
pkgrel=2
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=(i686 x86_64)
url=http://www.monocasual.com/$pkgname
license=(GPL3)
depends=(libxpm fltk rtaudio)
install=$pkgname.install
#options=(!strip) #debug
source=($url/download.php?dist=source\&file=bin/${pkgname}_${pkgver}_src.tar.gz
    $pkgname.png::${url%/*}/img/logoindex_giada.png
    $pkgname.desktop)
sha256sums=('6be363d47fea1d2b168a3ed9fc5182b8bdda5891fc495a577957fec26db7baba'
    '196c3933266b9c7d1201c5e32ffa91f0e438c15ce832ad53597801ac9705b2ee'
    'ab645cad52cf908f4d1957a0d166ee9ad3ec9f24b2593310fd634d648e2ce915')
sha512sums=('9c1a0d40df4489fb07fef97e8a4b902b3cdac5fca7711315b21a1036e4b9e0e60e2d6d04a5c0b3f77890750c06043197a77a038c16ebf320cc989eb643534aba'
    '99ad459792b4035189aa3bd8a84386ff585542b5c9c7fda127fd36017f24080b1f37a8c890054e55f399e3bc5cbdc1df0cd8f8995ca10aa0a7567a1332371fe9'
    'f97dbcc0c76a08f456a205d10bf46d1cab1e61f203c7e693727c678ccf8f1b853faacf261da6b331f54452c379a6e0ad65800f8bcac47e3fbcd5b5ea0f4c9c22')

build() {
    cd "$srcdir"
    export CXXFLAGS+=" -I/usr/include/rtaudio" # -g
    export CPPFLAGS+=" -I/usr/include/rtaudio"

    sed -i "s:\((void\*)\)\([ic]\):\1(intptr_t)\2:" src/gg_keyboard.cpp

    # BUG: giada defaults on 44100 sampling rate becoming
    #      not jack server, run at 48000 (default), capable.
    # FIX: forcing giada working at 48000.
    # WIP: auto detect sampling rate safelly (have little issues ATM)
    # FAQ: comment next line if you do not intend using it with jack@48000
    sed -i 's/44100/48000/' $(find -type f)

    ./configure --prefix=/usr --target=linux
    cd src/
    make
}

package() {
    cd "$srcdir"
    make DESTDIR="$pkgdir" install

    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
