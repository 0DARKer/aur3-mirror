# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rtfreedman
# Contributor: SpepS <dreamspepser@yahoo.it>

pkgname=giada
pkgver=0.6.0
pkgrel=1
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=(i686 x86_64)
url=http://www.giadamusic.com/
license=(GPL3)
depends=(libxpm fltk rtaudio)
install=$pkgname.install
#options=(!strip) #debug
source=($pkgname-$pkgver.tar.gz::$url/download-action.php?os=source
    $url/img/$pkgname-logo.png
    $pkgname.desktop)
sha256sums=('5f4f05f44ffc178d1527d44ac8d6eb5934e41ac7cc5ef69562d7a512886f877b'
    'a0319be4b7f25ac52beee2e5d14b60bdb253ad574c36b49be63ac05328823512'
    'ab645cad52cf908f4d1957a0d166ee9ad3ec9f24b2593310fd634d648e2ce915')
sha512sums=('d3ac4d6a30f9b8db46faddbc20aeeeeee20946d9a15600055060761e525eaa7ac388e77bd0e3a158c97336d7d7577f5abed7f42ade93f9db32aaca839e61ffb9'
    'eb59a4f4a844e0c19939e5776d7fdc5fc8c737287930ad7250bd434a158b00bb536799e559c067715db567e4dd13baebfc01ee4cafb4000ec4f45148f1a6fe79'
    'f97dbcc0c76a08f456a205d10bf46d1cab1e61f203c7e693727c678ccf8f1b853faacf261da6b331f54452c379a6e0ad65800f8bcac47e3fbcd5b5ea0f4c9c22')

build() {
    cd "$srcdir"
    export CXXFLAGS+=" -I/usr/include/rtaudio" # -g
    export CPPFLAGS+=" -I/usr/include/rtaudio"

    sed -i "s:\((void\*)\)\([ic]\):\1(intptr_t)\2:" src/gg_keyboard.cpp

    # BUG: giada defaults on 44100 sampling rate becoming
    #      not jack server, run at 48000 (default), capable.
    # FIX: forcing giada working at 48000.
    # WIP: auto detect sampling rate safely (have little issues ATM)
    # FAQ: comment next line if you do not intend using it with jack@48000
    sed -i 's/44100/48000/' $(find -type f)

    ./configure --prefix=/usr --target=linux
    cd src/
    make
}

package() {
    cd "$srcdir"
    make DESTDIR="$pkgdir" install

    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 $pkgname-logo.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
