# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=giada
pkgver=0.4.4
pkgrel=2
pkgdesc="A minimal, hardcore audio tool and loop machine for DJs and live performers"
arch=(i686 x86_64)
url="http://www.monocasual.com/giada/"
license=('GPL3')
depends=('libxpm' 'fltk' 'rtaudio')
install="$pkgname.install"
#options=('!strip')
source=("${url}download.php?dist=source&file=bin/${pkgname}_${pkgver}_src.tar.gz"
        "$pkgname.png::http://www.monocasual.com/img/logoindex_giada.png"
        "$pkgname.desktop")
md5sums=('5ccf5ba0be4d76f31689ee6592061c3d'
         '91ce67f32b923dcba6fad82ca60ca80c'
         '135bef9e2df07b7550a2f4e4358b6933')

build() {
  cd "$srcdir"

  # find rtaudio headers
  export CXXFLAGS="$CXXFLAGS -I/usr/include/rtaudio"

  # gcc 4.6 fixes
  sed -i "s/(void\*)i/reinterpret_cast<void*>(i)/" src/gg_keyboard.cpp
  sed -i "s/(int)p/(long)p/" src/{ge_mixed.cpp,gd_mainWindow.cpp,gg_keyboard.cpp}

  ./configure --prefix=/usr \
              --target=linux
  make
}

package() {
  cd "$srcdir"
  make DESTDIR="$pkgdir/" install

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 $pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
