# Maintainer: kfgz <kfgz at interia pl>
# Contributor: reMiND <detanator at gmail dot com>

pkgname=aqemu-git
pkgver=20140408
pkgrel=1
pkgdesc="QEMU GUI written in Qt4"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/aqemu/"
license=('GPL2')
depends=('qt4' 'openssl' 'libvncserver')
#cmake check libvncserver automatically; for building without libvncserver
#uncomment -DWITHOUT_EMBEDDED_DISPLAY=on
options=(!makeflags)
makedepends=('git' 'cmake')
provides=('aqemu')
conflicts=('aqemu')
source=('git://aqemu.git.sourceforge.net/gitroot/aqemu/aqemu')
md5sums=('SKIP')
install=aqemu.install

pkgver() {
  cd "${srcdir}"/aqemu
  git describe --long --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}"/aqemu

  cmake \
	-DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
	-DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr/
#	-DWITHOUT_EMBEDDED_DISPLAY=on
  make
}

package() {  
  cd "${srcdir}"/aqemu
  make DESTDIR="${pkgdir}" install
}
