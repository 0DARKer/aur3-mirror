# Maintainer: fenuks

_pkgname=qllauncher
pkgname=$_pkgname-git
pkgver=2a5c13d
pkgrel=1
pkgdesc="Native standalone application to launch Quake Live on Linux"
arch=("i686" "x86_64")
url="https://bitbucket.org/fx_/quakelivelauncher/overview"
license=("GPL3")
depends=("python" "python-pyqt4" "python-requests" "python-sleekxmpp" "wine" "xorg-server-utils")
makedepends=("git")
provides=("$_pkgname")
conflicts=("$_pkgname")
install=$pkgname.install
source=("$_pkgname::git+https://bitbucket.org/fx_/quakelivelauncher.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

package() {
    cd "$srcdir/$_pkgname"
    install -dm755 "$pkgdir/opt/$_pkgname/"
    install -d "$pkgdir/usr/bin/"
    cp -r core "$pkgdir/opt/$_pkgname/"
    cp -r ui "$pkgdir/opt/$_pkgname/"
    install -Dm755 main.py "$pkgdir/opt/$_pkgname/main.py"
    ln -s "/opt/$_pkgname/main.py" "$pkgdir/usr/bin/qllauncher"
    install -Dm644 "$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
    install -Dm644 ui/images/quakelivelogo.png "$pkgdir/usr/share/pixmaps/quakelivelogo.png"
}
