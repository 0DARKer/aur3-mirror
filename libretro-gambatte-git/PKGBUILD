# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-gambatte-git
pkgver=556.8b342c2
pkgrel=1
pkgdesc="libretro implementation of Gambatte. (Game Boy/Game Boy Color)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/gambatte-libretro"
license=('GPL')
makedepends=('git')

_libname=gambatte_libretro
_gitname=gambatte-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/${_libname}.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}/libgambatte"
  make -f Makefile.libretro
}

package() {
  install -Dm644 "${_gitname}/libgambatte/${_libname}.so" "${pkgdir}/usr/lib/libretro/${_libname}.so"
  install -Dm644 "${_libname}.info" "${pkgdir}/usr/lib/libretro/${_libname}.info"
}
