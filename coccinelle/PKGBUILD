# Maintainer: Sylvain Henry <hsyl20@gmail.com>
# Contributor: Sylvain Henry <hsyl20@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Dan McGee <dpmcgee@gmail.com>
# Contributor: LeCrayonVert <sunrider@laposte.net>
# Contributor: Lukas Fleischer <archlinux@cryptocrack.de>
# Contributor: Vladimir Kirillov <proger@wilab.org.ua>


pkgname=coccinelle
_pkgver=1.0.0-rc17
pkgver=1.0.0rc17
pkgrel=1
pkgdesc="Provides spatch program used to apply semantic patches"
arch=('i686' 'x86_64')
url="http://coccinelle.lip6.fr"
license=('GPL2')
makedepends=('ocaml')
depends=('ocaml-findlib' 'python2')
options=(!strip)
source=(
  http://coccinelle.lip6.fr/distrib/${pkgname}-${_pkgver}.tgz
)

build() {
   cd "$srcdir/$pkgname-$_pkgver"

   ./configure --prefix=/usr --with-python=/usr/bin/python2
   make depend 
   make 
}

check() {
   cd "$srcdir/$pkgname-$_pkgver"
   make check
}

package() {
   cd "$srcdir/$pkgname-$_pkgver"
   make DESTDIR=$pkgdir MANDIR="/usr/share/man" install 
}

sha256sums=('628d90650d504aa742e12eb371455bad8468eeed59533d6388366f173a498b61')
