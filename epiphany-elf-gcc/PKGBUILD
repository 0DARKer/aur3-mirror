# Maintainer: Jonathan Scott Tinkham <sctincman@gmail.com>
# Adapted from arm-none-eabi- by Anatol Pomozov

_target=epiphany-elf
pkgname=$_target-gcc
_pkgname=epiphany-gcc
pkgver=4.8.2
_pkgver=4.8
pkgrel=1
_newlibver=1.20
pkgdesc='The GNU Compiler Collection - cross compiler for the Adapteva Epiphany target'
arch=(i686 x86_64)
url='http://www.adapteva.com/'
license=(GPL LGPL FDL)
depends=($_target-binutils cloog libmpc)
makedepends=(gmp mpfr)
#checkdepends=(dejagnu)
options=(!emptydirs !strip staticlibs)
source=("https://github.com/adapteva/$_pkgname/archive/$_pkgname-$_pkgver.tar.gz"
	"https://github.com/adapteva/epiphany-newlib/archive/epiphany-newlib-$_newlibver.tar.gz")
md5sums=('a42c5a8eab54d823d0bea9cd26bc876c'
         '8991da5691eb67e5b2b9fb3d9d8dede1')

if [ -n "$_snapshot" ]; then
  _basedir=gcc-$_snapshot
else
  _basedir=$_pkgname-$_pkgname-$_pkgver
fi

prepare() {
  # Move newlib and libgloss to gcc source directory
  mv "$srcdir"/epiphany-newlib-epiphany-newlib-$_newlibver/newlib "$srcdir/$_basedir"
  mv "$srcdir"/epiphany-newlib-epiphany-newlib-$_newlibver/libgloss "$srcdir/$_basedir"

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" "$srcdir"/$_basedir/{libiberty,gcc}/configure
}

build() {
  cd $_basedir
  mkdir -p build
  cd build

  export CFLAGS_FOR_TARGET='-O2 -g -pipe'
  export CXXFLAGS_FOR_TARGET='-O2 -g -pipe'

  ../configure --target=$_target \
               --prefix=/usr \
               --libexecdir=/usr/lib \
               --with-pkgversion='Arch User Repository' \
               --with-bugurl='https://github.com/adapteva/epiphany-gcc/issues' \
               --enable-multilib \
               --enable-interwork \
               --enable-languages=c,c++ \
               --with-newlib \
               --with-gnu-as \
               --with-gnu-ld \
               --disable-nls \
               --with-headers=newlib/libc/include \
               --disable-werror

  make
}

check() {
  cd $_basedir/build
}

package() {
  cd $_basedir/build

  make DESTDIR="$pkgdir" install -j1

  # Strip host objects but do not touch target one (strip corrupts access.o in libepiphany.a)
  find "$pkgdir/usr/lib" -executable -type f -exec /usr/bin/strip --strip-debug '{}' \;
  find "$pkgdir/usr/bin" -executable -type f -exec /usr/bin/strip --strip-debug '{}' \;

  # Remove files that conflict with host gcc package
  rm -r "$pkgdir"/usr/share/man/man7
  rm -r "$pkgdir"/usr/share/info
  rm -r "$pkgdir"/usr/share/gcc-$pkgver
  rm "$pkgdir"/usr/lib/libiberty.a
}
