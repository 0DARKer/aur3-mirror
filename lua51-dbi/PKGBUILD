pkgname=lua51-dbi
_pkgname=luadbi
pkgver=0.5
pkgrel=1
pkgdesc="Database interface library for Lua (5.1)"
arch=('i686' 'x86_64')
url="http://code.google.com/p/luadbi/"
license=('MIT')
depends=('lua51' 'sqlite3' 'libmysqlclient' 'postgresql-libs')
source=("http://luadbi.googlecode.com/files/${_pkgname}.${pkgver}.tar.gz"
        'mycommit.patch::https://luadbi.googlecode.com/issues/attachment?aid=200002000&name=mycommit.patch&token=EQvH1qpmlXTi65oZyAhOcg14RMg%3A1381956990984')
md5sums=('ede2b003aadddc151aac87050c3d926e'
         'e5d6a5304671e478abaa27873a9c13df')
noextract=("${_pkgname}.${pkgver}.tar.gz")

prepare() {
  cd "${srcdir}"
  rm -fr "${pkgname}"
  mkdir "${pkgname}" && cd "${pkgname}"
  cp "../${_pkgname}.${pkgver}.tar.gz" .
  bsdtar -xf "${_pkgname}.${pkgver}.tar.gz"
  rm "${_pkgname}.${pkgver}.tar.gz"

  patch -p1 -i "${srcdir}/mycommit.patch"

  sed 's|#!/usr/bin/lua|#!/usr/bin/lua5.1|g' -i DBI.lua
}

build() {
  cd "${pkgname}"
  make PSQL_INC="-I$(pg_config --includedir-server)" free
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install_free
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

