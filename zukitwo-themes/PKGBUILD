# Contributor: grimi <grimi at poczta dot fm>
# Maintainer: grimi <grimi at poczta dot fm>

pkgname=zukitwo-themes
pkgver=20111110
pkgrel=2
pkgdesc="Themes for gtk3, gtk2, metacity, gnome-shell and emerald..."
arch=('any')
url="http://gnome-look.org/content/show.php/Zukitwo?content=140562"
license=('GPL')
depends=('gtk-engines' 'gtk-engine-murrine>=0.98.1.1' 'gtk-engine-unico>=1.0.1')
source=(http://gnome-look.org/CONTENT/content-files/140562-Zukitwo.zip)
md5sums=('9b03ca87116cf368de5a94b9573a3fd1')


package() {
  # fix font definitions
  sed 's|font:|font-size:|g' -i Zukitwo-Shell/gnome-shell/gnome-shell.css

  # install themes
  find Zukitwo*/ -not -name *Thumbs.db -type f \
      -exec install -Dm644 "{}" "$pkgdir/usr/share/themes/{}" \;

  # emerald part
  mkdir -p zuki
  tar xf Zukitwo.emerald -C zuki
  install -d "$pkgdir/usr/share/emerald/themes/Zukitwo"
  install -m644 zuki/*.{ini,png} "$pkgdir/usr/share/emerald/themes/Zukitwo"
}

# vim:set ts=2 sw=2 et:
