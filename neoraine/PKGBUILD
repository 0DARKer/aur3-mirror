# Maintainer: TryA <tryagainprod@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=neoraine
pkgver=1.2.12
_rainever=0.51.11
pkgrel=1
pkgdesc="A NeoGeo CD emulator based on Raine"
url="http://rainemu.swishparty.co.uk"
license=('unknown')
arch=('i686' 'x86_64')
if [ "${CARCH}" == "x86_64" ]; then
  depends=('lib32-sdl_ttf' 'lib32-sdl_image' 'lib32-sdl_sound' 'lib32-muparser')
  makedepends=('nasm' 'gcc-multilib')
else
  depends=('sdl_ttf' 'sdl_image' 'sdl_sound' 'muparser')
  makedepends=('nasm')
fi
source=("http://trya.alwaysdata.net/linux/raines-${_rainever}.tar.bz2")
md5sums=('a6305bfc5c699d3e75f7150c408882fd')

build() {
  cd "${srcdir}/raine-${_rainever}"

  # fix system directories in Makefile for the install target
  sed -e 's|\$(prefix)\/games|\$(prefix)\/bin|' \
      -e 's|INSTALL = @install|INSTALL = install|' \
      -e 's|rainedata = $(sharedir)/raine|rainedata = $(sharedir)/neoraine|' \
      -i makefile

  # -O3 optimizations cause segfaults, use -O2 instead
  sed -e 's|CFLAGS = -O3|CFLAGS = -O2|' -i makefile

  # NeoRaine share path conflicts with Raine, define another directory
  sed -e 's|"/usr/share/games/raine/"|"/usr/share/games/neoraine/"|' \
      -i source/raine.c

  make NEO=1
}

package() {
  cd "${srcdir}/raine-${_rainever}"
  make NEO=1 DESTDIR="${pkgdir}" install

  # install fonts and bitmaps
  install -m644 fonts/{Vera.ttf,VeraMono.ttf,font6x8.bin} "${pkgdir}/usr/share/games/${pkgname}/fonts"
  install -m644 bitmaps/{cursor.png,raine_logo.png} "${pkgdir}/usr/share/games/${pkgname}/bitmaps"
}
