# Maintainer: trya <tryagainprod@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=neoraine
pkgver=1.3.5
_gitver=386a314
pkgrel=1
pkgdesc="A NeoGeo CD emulator based on Raine"
url="http://rainemu.swishparty.co.uk"
license=('unknown')
arch=('i686' 'x86_64')
if [ "$CARCH" == "x86_64" ]; then
  depends=('lib32-sdl_ttf' 'lib32-sdl_image' 'lib32-sdl_sound' 'lib32-muparser')
  makedepends=('nasm' 'gcc-multilib')
else
  depends=('sdl_ttf' 'sdl_image' 'sdl_sound' 'muparser')
  makedepends=('nasm')
fi
source=("neoraine-$pkgver.tar.gz"::"http://rainemu.swishparty.co.uk/cgi-bin/gitweb.cgi?p=raine;a=snapshot;h=386a314a18fd5459df46420cd97d5d012f0590fc;sf=tgz"
        "http://trya.alwaysdata.net/linux/raines-0.51.11.tar.bz2")
md5sums=('a6a1c1471638a6f94a460d2efba87f21'
         'a6305bfc5c699d3e75f7150c408882fd')

build() {
  cd "$srcdir/raine-$_gitver"
  
  # copy required version.h from Raine source tarball
  cp ../raine-0.51.11/source/version.h source/

  # fix system directories in Makefile for the install target
  sed -e 's|\$(prefix)\/games|\$(prefix)\/bin|' \
      -e 's|INSTALL = @install|INSTALL = install|' \
      -e 's|rainedata = $(sharedir)/raine|rainedata = $(sharedir)/neoraine|' \
      -i makefile

  # -O3 optimizations cause segfaults, use -O2 instead
  sed -e 's|CFLAGS = -O3|CFLAGS = -O2|' -i makefile

  # NeoRaine share path conflicts with Raine, define another directory
  sed -e 's|"/usr/share/games/raine/"|"/usr/share/games/neoraine/"|' \
      -i source/raine.c
  
  # replace a libpng deprecated function
  sed -e 's|png_ptr->jmpbuf|png_jmpbuf(png_ptr)|' \
      -i source/savepng.c

  make NEO=1
}

package() {
  cd "$srcdir/raine-$_gitver"
  make NEO=1 DESTDIR="$pkgdir" install

  # install fonts and bitmaps
  install -m644 ../raine-0.51.11/fonts/{Vera.ttf,VeraMono.ttf,font6x8.bin} "$pkgdir/usr/share/games/$pkgname/fonts"
  install -m644 ../raine-0.51.11/bitmaps/{cursor.png,raine_logo.png} "$pkgdir/usr/share/games/$pkgname/bitmaps"
}
