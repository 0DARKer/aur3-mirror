# Maintainer: almostalive <almostalive2003 at lavabit dot com>

pkgname=libretro-mednafen-vb-git
_gitname=mednafen-libretro
pkgver=2013.05.01_79e7422
pkgrel=1
pkgdesc="libretro implementation of mednafen's Virtual Boy."
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/mednafen-libretro"
license=('custom')
makedepends=('git')
conflicts=('libretro-super-git')
source=('git://github.com/libretro/mednafen-libretro.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-|.|g')_$(git describe --always | sed 's/-/_/g')"
}

build() {
  cd $_gitname
  make core=vb
}

package()
{
  mkdir -p $pkgdir/usr/lib/libretro
  install -v -m644 $srcdir/$_gitname/mednafen_vb_libretro.so $pkgdir/usr/lib/libretro/libretro-mednafen-vb.so
}

