# Contributor: Richard Kaka≈° <richard.kakas@gmail.com>
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=printrun-git
_gitname='Printrun'
pkgver=20130604
pkgrel=1
pkgdesc='GUI interface for 3D printing on RepRap and other printers'
arch=('any')
url='https://github.com/kliment/Printrun'
license=('GPL')
provides=('printrun')
conflicts=('printrun')
depends=('python2-pyserial' 'wxpython' 'pyglet')
makedepends=('git')
source=('git://github.com/kliment/Printrun.git'
        'pronterface.desktop' 'pronterface.png')
md5sums=('SKIP'
         '3dfd9fca6b36d914d9b60a69dc9d0d14'
         '9010a039260f0ea34f02671025138ab2')

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --tags --always | sed 's/printrun-//' | sed 's|-|.|g'
}

build() {
  cd "${srcdir}/${_gitname}"

  python2 setup.py build

  sed 's/^\(#\!.*\)python$/\1python2/' -i \
    build/lib/printrun/calibrateextruder.py \
    build/lib/printrun/gcoder.py \
    build/lib/printrun/gcview.py \
    build/lib/printrun/graph.py \
    build/lib/printrun/SkeinforgeQuickEditDialog.py \
    build/lib/printrun/stlview.py
}

package() {
  cd "${srcdir}/${_gitname}"
  python2 setup.py install --root="${pkgdir}" --prefix=/usr

  install -D -m644 "${srcdir}/pronterface.desktop" \
    "$pkgdir/usr/share/applications/pronterface.desktop"
  install -D -m644 "${srcdir}/pronterface.png" \
    "$pkgdir/usr/share/pixmaps/pronterface.png"
}
