# Contributor: Nathan Owe.
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=star
pkgver=1.5.2
pkgrel=1
pkgdesc="Most POSIX compliant and fastest known implementation of a tar archiver."
arch=('i686' 'x86_64')
url="http://cdrecord.berlios.de/old/private/star.html"
license=('cddl')
source=(ftp://ftp.berlios.de/pub/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('5c15cedb52e26b02b95c82de08c5fcc1')
depends=('sh' 'acl')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make RUNPATH=-R/usr/lib DEFINSGRP=root
}
package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d $pkgdir/usr/man
  make INS_BASE="$pkgdir/usr" INS_RBASE="$pkgdir" DEFINSGRP="root"  install
  install -Dm644 COPYING \
    "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  rm $pkgdir/usr/bin/{gnutar,tar,mt}
  cp -r $pkgdir/usr/man $pkgdir/usr/share
  rm -r $pkgdir/usr/man
  cd $pkgdir
  chmod g-w usr usr/{share,include,lib,bin,sbin,share/doc}
  # to avoid conflict with package 'dump' ...
  install -Dm755 $pkgdir/usr/sbin/rmt $pkgdir/usr/sbin/srmt
  install -Dm644  $pkgdir/usr/share/man/man1/rmt.1 \
    $pkgdir/usr/share/man/man1/srmt.1
  rm $pkgdir/usr/share/man/man1/rmt.1
  sed -i 's+/opt/schily/sbin/rmt+/usr/sbin/srmt+' \
    $pkgdir/usr/share/man/man1/srmt.1
  # and package 'cdrtools'
  rm $pkgdir/usr/sbin/rmt $pkgdir/usr/share/man/man5/makefiles.5 \
    $pkgdir/usr/share/man/man5/makerules.5
}
