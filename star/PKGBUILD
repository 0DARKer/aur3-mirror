# Contributor: Nathan Owe.
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=star
pkgver=1.5.1
pkgrel=1
pkgdesc="Most POSIX compliant and fastest known implementation of a tar archiver."
arch=('i686' 'x86_64')
url="http://cdrecord.berlios.de/old/private/star.html"
license=('cddl')
source=(ftp://ftp.berlios.de/pub/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('f9a28f83702624c4c08ef1a343014c7a')
depends=('sh' 'acl')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make RUNPATH=-R/usr/lib DEFINSGRP=root
}
package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INS_BASE="$pkgdir/usr" DEFINSGRP="root"  install
  install -Dm644 COPYING \
    "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  rm $pkgdir/usr/bin/{gnutar,tar,mt}
  cp -r $pkgdir/usr/man $pkgdir/usr/share
  rm -r $pkgdir/usr/man
  # to avoid conflict with package 'dump' ...
  cp $pkgdir/usr/sbin/rmt $pkgdir/usr/sbin/srmt
  # and package 'cdrtools'
  rm $pkgdir/usr/sbin/rmt $pkgdir/usr/share/man/man5/makefiles.5 \
    $pkgdir/usr/share/man/man5/makerules.5
}
