# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mathieu.clabaut <mathieu.clabaut@gmail.com>

pkgname=etckeeper-git
pkgver=0.64.9.ge5577a7
pkgrel=1
pkgdesc="collection of tools to let /etc be stored in a git, hg or bzr repository - git checkout"
arch=('i686' 'x86_64')
url="http://kitenet.net/~joey/code/etckeeper/"
license=('GPL2')
provides=('etckeeper')
conflicts=('etckeeper')
depends=('git')
makedepends=('mercurial' 'bzr')
optdepends=('mercurial: use mercurial for version control'
	'bzr: use bazaar for version control')
source=('git://git.kitenet.net/etckeeper')

pkgver() {
	cd etckeeper/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd etckeeper/

	# This does not exist, but we do not want the apt stuff installed.
	sed -i -e "/^LOWLEVEL_PACKAGE_MANAGER/c LOWLEVEL_PACKAGE_MANAGER=pacman" \
		-e "/^HIGHLEVEL_PACKAGE_MANAGER/c HIGHLEVEL_PACKAGE_MANAGER=pacman" etckeeper.conf
}

package() {
	cd etckeeper/

	make DESTDIR=${pkgdir} install
	install -D -m0755 debian/cron.daily ${pkgdir}/etc/cron.daily/etckeeper
}

sha256sums=('SKIP')
