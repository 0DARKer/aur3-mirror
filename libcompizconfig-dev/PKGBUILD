# WTF release naming scheme FTW
pkgname=libcompizconfig-dev
pkgseries=0.9.5
pkgver=${pkgseries}.92
pkgrel=1
pkgdesc="Compiz configuration system library"
arch=('i686' 'x86_64')
url="http://compiz.org"
license=('GPL')
provides=('libcompizconfig')
conflicts=('libcompizconfig')
depends=('compiz-core-dev' 'libxml2' 'protobuf')
makedepends=('intltool' 'pkgconfig' 'cmake')
source=("https://launchpad.net/compiz-libcompizconfig/${pkgseries}/${pkgver}/+download/libcompizconfig-${pkgver}.tar.bz2")
md5sums=('0dd03011cac8cc4bc0e0877e38bcca90')

build() {
    cd "libcompizconfig-${pkgver}"
    [[ -d build ]] || mkdir build
    cd build
    cmake .. \
        -DCMAKE_BUILD_TYPE="Release" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCOMPIZ_PACKAGING_ENABLED=TRUE \
        -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
        -DUSE_GSETTINGS=Off \
        -DCOMPIZ_DESTDIR="${pkgdir}"
    make
}

package() {
    cd "libcompizconfig-${pkgver}/build"
    make install
	make findcompizconfig_install
}
