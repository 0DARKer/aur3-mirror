# Maintainer: J0k3r <moebius282 e4at gmail D0_T com>

pkgname=kairo
pkgver=1.4
pkgrel=1
pkgdesc="A game about exploring the lost world of Kairo"
url="http://kairo.lockeddoorpuzzle.com/"
license=('unknown')
# On their website: 
# > Legal Notice: Kairo is a commercial game and by purchasing it or downloading the demo you are legally bound not to 
# > redistribute the game without our consent. However you are more than welcome to use screenshots and videos from Kairo
# > for whatever purpose you see fit. Including, but not limited to, YouTube videos or TwitchTV streaming.
arch=('i686' 'x86_64')
groups=("games")
if [ "$CARCH" = "x86_64" ]; then
	depends=('lib32-glu' 'lib32-gcc-libs' 'lib32-libxcursor')
else
	depends=('glu' 'gcc-libs' 'libxcursor')
fi
makedepends=('imagemagick')
_archivename="${pkgname}-linux-${pkgver}.tar.gz"
source=("file://${_archivename}"
		"${pkgname}.desktop")
sha256sums=('b0ee3448bfbb41251811bf319639215708e77cf8e9c5a379186abddca7839a14'
            'b69715022f7b44c828899f7812f7d1658d27147d38de95bc3a452ffb86f89289')
noextract=("${_archivename}")
PKGEXT=".pkg.tar"

package()
{
	mkdir -p "${pkgdir}/opt/${pkgname}/"
	mkdir -p "${pkgdir}/usr/share/pixmaps/"
	mkdir -p "${pkgdir}/usr/bin/"
	
	tar -xzf "${srcdir}/${_archivename}" -C "${pkgdir}/opt/${pkgname}/"

	# strange defualt perms
	chown -R root:root "${pkgdir}/opt/${pkgname}/"
	chmod -R 0755 "${pkgdir}/opt/${pkgname}/"

	ln -s "/opt/${pkgname}/Kairo" "${pkgdir}/usr/bin/${pkgname}"

	# getting the press icon from the official site and converting it to a reasonabile size. If someone has a proper icon source, write me.
	msg "Getting the icon"
	curl -L "http://kairo.lockeddoorpuzzle.com/files/KairoLargeIcon.png" | convert - -resize 64x64 "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	
	install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

}