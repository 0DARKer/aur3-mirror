# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: vicky91 <vickypaiers@gmail.com>

_pkgname=qterminal
pkgname=$_pkgname-git
pkgver=126.f568dcb
pkgrel=1
pkgdesc="Lightweight Qt-based terminal emulator"
arch=('i686' 'x86_64')
url="https://github.com/qterminal/qterminal"
license=('GPL2')
provides=($_pkgname)
conflicts=($_pkgname)
depends=('qtermwidget-git' 'libqxt')
makedepends=('git' 'cmake')
source=("git://github.com/qterminal/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	echo "$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
