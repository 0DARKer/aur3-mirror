# Contributor: Graziano Giuliani <graziano.giuliani@poste.it>
pkgname=emos
pkgver=000392
pkgrel=8
pkgdesc="ECMWF Interpolation Library"
arch=(i686 x86_64)
url="https://software.ecmwf.int/wiki/display/EMOS/Emoslib"
license=('Apache')
groups=(science)
depends=()
makedepends=(rsync grib_api gcc-fortran)
options=('staticlibs')
source=(https://software.ecmwf.int/wiki/download/attachments/3473472/${pkgname}_${pkgver}.tar.gz)
md5sums=('165b9d7918fd67fd849c59287fc54bb6')

build() {
  cd ${srcdir}/${pkgname}_${pkgver}
  sed -i -e "/CFLAGS/s|$| -fPIC|" config/config.linux_gfortran*.in
  sed -i -e "/FFLAGS/s|$| -fPIC|" config/config.linux_gfortran*.in
  # gcc/fortran: y; ifort: i
  # NB: ifort could be enabled here instead of gfortran
  _response="y\n"

  # 64-bit reals on x86_64, else 32-bit reals
  if [[ "$CARCH" == "x86_64" ]]; then
    _response="${_response}y\n"
  else
    _response="${_response}n\n"
  fi

  _response="${_response}y\n/usr\ny\n/usr\n"
  # The installation directory (/usr/local/lib) appears to be 
  # hard-coded during compilation
  _response="${_response}/usr/share/emos\n"
  echo -e $_response | ./build_library
}

package() {
  cd ${srcdir}/${pkgname}_${pkgver}
  ls bufrtables/*000* > .list/bufrtables
  mkdir -p ${pkgdir}/usr/lib
  mkdir -p ${pkgdir}/usr/share/emos

  if [[ "$CARCH" == "x86_64" ]]; then
    install -m 644 libemosR64.a ${pkgdir}/usr/lib/libemosR64.a
    ln -sf /usr/lib/libemosR64.a ${pkgdir}/usr/lib/libemos.a
  else
    install -m 644 libemos.a ${pkgdir}/usr/lib/libemos.a
  fi
  for d in gribtables bufrtables crextables land_sea_mask gribtemplates
  do
    mkdir -p ${pkgdir}/usr/share/emos/$d
    find $d -type d -exec mkdir -p ${pkgdir}/usr/share/emos/{} \;
    for i in `cat .list/$d`
    do
      rsync -a $i ${pkgdir}/usr/share/emos/$i
    done
  done
}

# vim:set ts=2 sw=2 et:
