# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname='perl-module-build-tiny'
_pkgname='Module-Build-Tiny'
pkgver='0.035'
pkgrel='1'
pkgdesc="A tiny replacement for Module::Build"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.1' 'perl-extutils-config>=0.003' 'perl-extutils-helpers>=0.020' 'perl-extutils-installpaths>=0.002')
makedepends=()
provides=("module-build-tiny=${pkgver}" "Module::Build::Tiny=${pkgver}" "perl-module-build-tiny=${pkgver}")
url="http://search.cpan.org/dist/Module-Build-Tiny"
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEONT/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('88496b24fb5ff0a3c329b7832d62cabf6511e8a8485206398c9fbeacfa3d017ca9812f0b717430c38f2134a8e77ae0236fd101a5248b731ab6dea5327edc08f1')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_pkgname}-${pkgver}"
    /usr/bin/perl Build.PL
    ./Build
  )
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    echo "Since v0.029 it is require Test-Harness-3.29"
    echo "But in core Perl module perl-test-harness=3.26 :("
    echo "Skip tests"
    #./Build test
  )
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ( export PERL_AUTOINSTALL=--skipdeps                       \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'"
    ./Build install
  )
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
