# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname='perl-module-build-tiny'
_pkgname='Module-Build-Tiny'
pkgver='0.019'
pkgrel='2'
pkgdesc="A tiny replacement for Module::Build"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.1')
makedepends=('perl-extutils-config>=0.003' 'perl-extutils-helpers>=0.020' 'perl-extutils-installpaths>=0.002')
provides=("module-build-tiny=${pkgver}" "Module::Build::Tiny=${pkgver}" "perl-module-build-tiny=${pkgver}")
url="http://search.cpan.org/dist/Module-Build-Tiny"
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEONT/${_pkgname}-${pkgver}.tar.gz")
md5sums=('5c7ee09377ac34d60cd8616b35782c63')
sha512sums=('e92e9b7a740f837b7a06c74ef50b8e823743948e2435e11792476f4939071c11315249bd08e6157ba5498adfbe7b5231e3a8817cef29e1439a36664334faf502')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_pkgname}-${pkgver}"
    /usr/bin/perl Build.PL
    ./Build
  )
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    ./Build test
  )
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ( export PERL_AUTOINSTALL=--skipdeps                       \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'"
    ./Build install
  )
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
