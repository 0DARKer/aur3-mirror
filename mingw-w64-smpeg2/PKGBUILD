pkgname=mingw-w64-smpeg2
pkgver=2.0.0.svn408
pkgrel=1
pkgdesc="SDL MPEG Player Library (mingw-w64)"
arch=(any)
url="http://icculus.org/smpeg"
license=("LGPL")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config subversion)
depends=(mingw-w64-crt mingw-w64-sdl2)
provides=("mingw-w64-smpeg-svn=408")
options=(!libtool !strip !buildflags)

_svntrunk="svn://svn.icculus.org/smpeg/trunk"
_svnmod=smpeg
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r 408)
  else
    svn co $_svntrunk --config-dir ./ -r 408 $_svnmod
  fi
  
  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  
  cd "smpeg"
  ./autogen.sh
}

build() {
	for _arch in ${_architectures}; do
		unset LDFLAGS
		mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		${srcdir}/smpeg/configure \
			--prefix=/usr/${_arch} \
			--build=$CHOST \
			--host=${_arch} \
			--disable-sdltest \
			--disable-gtk-player \
			--disable-gtktest \
			--disable-opengl-player \
			--with-sdl-prefix=/usr/${_arch} \
			--without-x
		make
	done
}

package() {
	for _arch in ${_architectures}; do
		cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		make DESTDIR="$pkgdir" install
		find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
		find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
		find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
		rm -r "$pkgdir/usr/${_arch}/share"
	done
}
