# Maintainer: Thomas Krug <phragment@htu.tugraz.at>
# Contributor: veox <veox at wemakethings dot net>

pkgname=libsigrok-git
_pkgname=libsigrok
pkgver=2056.48d3238
pkgrel=1
pkgdesc="Client software that supports various hardware logic analyzers, core library (git version)"
arch=('i686' 'x86_64')
url="http://www.sigrok.org/wiki/Libsigrok"
license=('GPL3')
depends=('libzip' 'libftdi-compat' 'alsa-lib' 'libserialport-git')
makedepends=('git')
provides=('libsigrok')
conflicts=('libsigrok')
source=("git://sigrok.org/$_pkgname")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"

  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" PREFIX=/usr install
}

# vim:set ts=2 sw=2 et:
