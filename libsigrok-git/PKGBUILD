# Maintainer: Noel Maersk <veox at wemakethings dot net>

pkgname=libsigrok-git
_gitname=libsigrok
pkgver=1943.854434d
pkgrel=1
pkgdesc="Client software that supports various hardware logic analyzers, core library (git version)"
arch=('i686' 'x86_64')
url="http://sigrok.org/wiki/Main_Page"
license=('GPL3')
depends=('glib2' 'libzip' 'zlib' 'libftdi')
optdepends=('libusbx: several devices'
            'libudev: several devices')
makedepends=('git')
provides=('libsigrok')
conflicts=('libsigrok')
source=("git://sigrok.org/${_gitname}")
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/${_gitname}"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/${_gitname}"
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
