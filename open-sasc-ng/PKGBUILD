# Maintainer:  Wessel Dirksen "p-we" <wdirksen at gmail dot com>
# Contributor: Juha-Matti "Suolx" Heikkala (previous maintainer)
# Contributor: bas-t (providing patches for kernels > 3.7 and hosting GIT repo)
# Contributor: Petr Vacek (providing cardslot.conf for serial port SC readers)

# There are 2 choices for pkgver, define as needed:
# stable - version 570: most universally stable version for all systems 
# final -  version 620: most recent version at development stop and stable on most systems

pkgver=stable

pkgname=open-sasc-ng
pkgrel=1
pkgdesc="A SoftCAM creating virtual DVB interface"
url="https://github.com/bas-t/sasc.git"
arch=('i686' 'x86_64')
license=('GPL')
depends=('linux-headers')
#NOTE: if you use another type of kernel, you must change depends or comment it out
makedepends=('git')
conflicts=(sasc-ng)
provides=(sasc-ng)
backup=('etc/camdir/cardclient.conf' 'etc/conf.d/sasc-ng' 'etc/camdir/cardslot.conf')
install='sasc-ng.install'
source=('cardclient.conf' 'sasc-ng.rc' 'sasc-ng.conf' \
	'open-sasc-ng.lr' 'cardslot.conf' 'sasc-ng.service' \
	'sasc-ng.install' 'rev-620.patch' 'rev-570.patch')

md5sums=('3b547abcba9c0dda70b0d0a4b6e7311a'
         'bf4a6ef101aa5c8871381d336235ac0f'
         'c2e83b638cbbe839f11a2daf11b124fe'
         '99be6670bc66d12640c9d82cc9bde3b5'
	 '90baa862eb4da0c1e579ad003e51f2b0'
	 '79651ebbab0587cfbefe6b2e4ccdfd68'
	 '6a418f045e86e7f406fa529908b2557e'
	 '479548908f8d773d05d0cddc65f63c12'
	 'd7d13bdbee08211f1f1556189d08cca6')

_gitroot="git://github.com/bas-t/sasc.git"
_gitdir="sc"
_gitbuild="sc-build"

  if [ "$pkgver" == "stable" ]; then
  	_gitbranch="rev-570"  
  else
  	_gitbranch="rev-620"
  fi

prepare() {

 	  if [ ! -d "$srcdir/$_gitbranch" ]; then
		  git clone -b "$_gitbranch" "$_gitroot" "$_gitbranch/$_gitdir"
	  fi

	rm -rf "$srcdir/$_gitbuild"
	cp -r "$srcdir/$_gitbranch" "$_gitbuild"
	cd "$srcdir/$_gitbuild"

	msg "Applying patches..."
	patch -p1 < "$srcdir/$_gitbranch.patch"

	chmod a+x "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/configure"
	chmod a+x "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/makelinks.sh"
	chmod a+x "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/dvbloopback/module/config_dvb.pl"
}

build() {

	cd "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng"      
	./configure 
      
	make module || return 1
	make || return 1
}

package() {

	mkdir -p "$pkgdir/usr/bin"
	mkdir -p "$pkgdir/usr/lib/modules/`uname -r`/extra"
	mkdir -p "$pkgdir/etc/conf.d"
	mkdir -p "$pkgdir/etc/rc.d"
	mkdir -p "$pkgdir/etc/camdir"
	mkdir -p "$pkgdir/etc/logrotate.d/"
	mkdir -p "$pkgdir/usr/lib/systemd/system/"
      
	install -m0755 "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/sasc-ng" "$pkgdir/usr/bin/"
	install -m0644 "$srcdir/$_gitbuild/$_gitdir/contrib/sasc-ng/dvbloopback.ko" "$pkgdir/usr/lib/modules/`uname -r`/extra"
	install -m0755 "$srcdir/sasc-ng.rc" "$pkgdir/etc/rc.d/sasc-ng"
	install -m0644 "$srcdir/sasc-ng.conf" "$pkgdir/etc/conf.d/sasc-ng"
	install -m0644 "$srcdir/cardclient.conf" "$pkgdir/etc/camdir/cardclient.conf"
	install -m0644 "$srcdir/cardslot.conf" "$pkgdir/etc/camdir/cardslot.conf"
	install -m0644 "$srcdir/open-sasc-ng.lr" "$pkgdir/etc/logrotate.d/open-sasc-ng.lr"
	install -m0644 "$srcdir/sasc-ng.service" "$pkgdir/usr/lib/systemd/system/sasc-ng.service"
}
