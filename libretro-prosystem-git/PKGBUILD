# Maintainer: prettyvanilla <prettyvanilla@posteo.at>

pkgname=libretro-prosystem-git
pkgver=9.4ab1785
pkgrel=1
pkgdesc="libretro implementation of ProSystem. (Atari 7800)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/prosystem-libretro"
license=('GPL')
depends=('zlib')
makedepends=('git')

_gitname=prosystem-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/prosystem_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm644 "${_gitname}/prosystem_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-prosystem.so"
  install -Dm644 "prosystem_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-prosystem.info"
}
