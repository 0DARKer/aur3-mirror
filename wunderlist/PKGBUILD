# Maintainer: Christian Autermann <christian@autermann.org>

pkgname=wunderlist
_pkgname=Wunderlist
pkgver=1.2.4
pkgrel=4
pkgdesc="free and easy-to-use task manager"
arch=("i686" "x86_64")
url="http://www.6wunderkinder.com/wunderlist/"
license=('APACHE')
depends=(libffi libwebkit libsoup-gnome curl libnotify hspell libxss)
options=(!strip !zipman !purge)

if [ "$CARCH" = "i686" ]; then _arch="32"
  md5sums=(6f16a7a724360090dee9de597c6870b3 0c6f49762bc0043394d5e440f2ff1af1)
elif [ "$CARCH" = "x86_64" ]; then _arch="64"
  md5sums=(6f16a7a724360090dee9de597c6870b3 5dbf6df8a161325bb7ff28a883f2fb76)
fi

source=("$pkgname.desktop"
        "http://www.6wunderkinder.com/downloads/${pkgname}-${pkgver}-linux-${_arch}.tgz")

build() {
  true
}

package() {
  _srcdir="$srcdir/$_pkgname-$pkgver"
  _pkgdir="$pkgdir/opt/$pkgname"
  _tlibdir="$_pkgdir/runtime/1.2.0.RC3"
  _slibdir="$_srcdir/runtime/1.2.0.RC3"

  cd "$srcdir"

  # install desktop file
  mkdir -p "$pkgdir/usr/share/applications"
  install -m 644 $pkgname.desktop "$pkgdir/usr/share/applications/"

  cd "$_srcdir"

  # install licence file
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  cp LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname"

  mkdir -p "$_pkgdir"

  # install app
  cp -R Resources {ti,}manifest tiapp.xml modules "$_pkgdir"

  # install binary
  install -T -m 755 "$_pkgname" "$_pkgdir/$pkgname"

  # install libraries
  mkdir -p "$pkgdir/usr/lib/$pkgname"
  mkdir -p "$_tlibdir"

  cd "$_slibdir"
  for lib in libPoco*.so.9 libicu*.so.42 libkhost.so libmodman.so.1; do
    install -m 755 "$_slibdir/$lib" "$pkgdir/usr/lib/$pkgname"
    ln -s "/usr/lib/$pkgname/$lib" "$_tlibdir"
  done

  # install certificate
  install -m 644 "$_slibdir/rootcert.pem" "$_tlibdir"

  # link system libraries
  for lib in gio libcurl.so.4 libhspell.so.0 libsoup-{gnome-,}2.4.so.1 \
    libgcrypt.so libg{lib,io,object,module,thread}-2.0.so.0 \
    libwebkitgtk-1.0.so.0; do
    ln -s "/usr/lib/${lib}" "$_tlibdir"
  done
  ln -s "/usr/share/webkitgtk-1.0/webinspector" "$_tlibdir"
  ln -s "/lib/libgcrypt.so.11" "$_tlibdir"

  # and now... some dirty, dirty hacks.........
  ln -s "/usr/lib/libgnutls.so.28" "$_tlibdir/libgnutls.so.26"
  ln -s "/usr/lib/libproxy.so.1.0.0" "$_tlibdir/libproxy.so.0"
  ln -s "/usr/lib/libnotify.so.4" "$_tlibdir/libnotify.so.1"
  ln -s "/usr/lib/libcrypto.so.1.0.0" "$_tlibdir/libcrypto.so.0.9.8"
  ln -s "/usr/lib/libssl.so.1.0.0" "$_tlibdir/libssl.so.0.9.8"

  # install 'docs'
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  cd "$_pkgdir/Resources/"
  mv CHANGELOG{,.txt} README "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
