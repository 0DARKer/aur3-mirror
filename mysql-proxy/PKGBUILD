# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Sebastian Vandekerckhove <sebastianvdk(at)foxatwork(dot)be>
# Contributor: Ivan Abdulin <elbahek@gmail.com>

pkgname=mysql-proxy
pkgver=0.8.5
pkgrel=1
pkgdesc='Monitors, analyzes and transformations communication between the MySQL client and server'
arch=('i686' 'x86_64')
url='http://dev.mysql.com/downloads/mysql-proxy/'
license=('GPL')
depends=('lua51' 'glib2' 'libevent')
makedepends=('libmysqlclient')
validpgpkeys=('A4A9406876FCBD3C456770C88C718D3B5072E1F5')
source=("http://dev.mysql.com/get/Downloads/MySQL-Proxy/${pkgname}-${pkgver}.tar.gz"
	"${pkgname}-${pkgver}.tar.gz.asc")
sha256sums=('e5bac3df8bca2eed41cdbf09439264b34c77339cfe1eda506b0d76eaa2d2b2d5'
            'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--datadir=/usr/share/${pkgname} \
		--with-lua=lua5.1
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}

