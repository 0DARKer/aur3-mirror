# Maintainer: Tianjiao Yin <ytj000@gmail.com>

pkgname=waf-docs
pkgver=1.6.11
pkgrel=1
pkgdesc="Documentation for Waf, a general-purpose build system modelled after Scons"
url="http://code.google.com/p/waf/"
arch=('any')
license=("BSD")
makedepends=("python2-sphinx" "texlive-latexextra")
source=("http://waf.googlecode.com/files/waf-$pkgver.tar.bz2")
sha256sums=('80e14813288707cb668fb7e3f715103f0eb07faa459981d2185894042e48b832')

build() {
  cd "$srcdir/waf-$pkgver/docs/sphinx"

  sed -i "s/sphinx-build/sphinx-build2/g" Makefile

  make html
  make singlehtml

  make latex
  make -C build/latex
  mv build/latex/waf.pdf build
  rm build/latex -rf
}

package() {
  cd "$srcdir/waf-$pkgver/docs/sphinx"

  rm build/doctrees -rf
  mkdir -p $pkgdir/usr/share/doc/
  mv build $pkgdir/usr/share/doc/waf
}

# vim:set ts=2 sw=2 et:
