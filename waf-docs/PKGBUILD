# Maintainer: Tianjiao Yin <ytj000@gmail.com>

pkgname=waf-docs
pkgver=1.7.0
pkgrel=1
pkgdesc="Documentation for Waf, a general-purpose build system modelled after Scons"
url="http://code.google.com/p/waf/"
arch=('any')
license=("BSD")
makedepends=("python2-sphinx" "texlive-latexextra" "asciidoc" 
             "dia" "imagemagick" "source-highlight"
             "dblatex" "waf" "fop")
source=("http://waf.googlecode.com/files/waf-$pkgver.tar.bz2")
sha256sums=('9b4fda9060d30260b323ae64f11bf77371596295ca5810c2c709a7c66b184048')

build() {
  cd "$srcdir/waf-$pkgver/docs/sphinx"

  sed -i "s/sphinx-build/sphinx-build2/g" Makefile

  make html

  make latex
  make -C build/latex
  mv build/latex/waf.pdf build
  rm build/latex -rf

  cd "$srcdir/waf-$pkgver/docs/book"
  waf configure
  waf --targets=single.html
  cd build
	a2x -L -a toc --icons-dir=. --icons -D . -d book -f pdf ../waf.txt --fop
}

package() {
  cd "$srcdir/waf-$pkgver/docs/sphinx"

  rm build/doctrees -rf
  mkdir -p $pkgdir/usr/share/doc/
  mv build $pkgdir/usr/share/doc/waf
  mv "$srcdir/waf-$pkgver/docs/book/build" $pkgdir/usr/share/doc/waf/book
  mv "$pkgdir/usr/share/doc/waf/book/waf.pdf" $pkgdir/usr/share/doc/waf/wafbook_FIXME.pdf
}

# vim:set ts=2 sw=2 et:
