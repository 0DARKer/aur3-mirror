# Maintainer: Tianjiao Yin <ytj000@gmail.com>

pkgname=waf-docs
pkgver=1.7.2
pkgrel=2
pkgdesc="Documentation for Waf, a general-purpose build system modelled after Scons"
url="http://code.google.com/p/waf/"
arch=('any')
license=("BSD")
makedepends=("python2-sphinx" "texlive-latexextra" "asciidoc" 
             "dia" "imagemagick" "source-highlight"
             "dblatex" "waf") # fop
source=("http://waf.googlecode.com/files/waf-$pkgver.tar.bz2"
        "book.patch"
        "asciidoc-dblatex.sty"
        "asciidoc-dblatex.xsl"
        )
sha256sums=('6ccbc601f4e96985af1c4e7b2512bb7c660a4bac4b4a40ffeb7bd339edcf6e71'
            '27cc8b990b7604e305845e5c30f2775c50b787f78983262e7c9c5cc257119703'
            '60fff09895cb6c3f419524ed20bf602dfa74c12c0e0f28e1adf56f58d2d14210'
            'e97b462320d0f90b9091422ba24bfb5863db6366e64b3a083f141f583b3e334a')

build() {

  # Part 1, build waf documentation
  ###########################################################################
  cd "$srcdir/waf-$pkgver/docs/sphinx"

  sed -i "s/sphinx-build/sphinx-build2/g" Makefile

  make html

  make latex
  make -C build/latex
  mv build/latex/waf.pdf build
  rm build/latex -rf

  # Part 2, build waf book
  ###########################################################################

  cd "$srcdir/waf-$pkgver/docs/book"

  # -----------------------------------------------
  # https://groups.google.com/forum/?fromgroups=#!topic/waf-users/H6Ikm6bRUjQ
  patch wscript $srcdir/book.patch
  cp $srcdir/asciidoc-dblatex.* .
  # -----------------------------------------------

  waf configure
  waf
}

package() {
  cd "$srcdir/waf-$pkgver/docs/sphinx"

  rm build/doctrees -rf
  mkdir -p $pkgdir/usr/share/doc/
  mv build $pkgdir/usr/share/doc/waf
  mv $srcdir/waf-$pkgver/docs/book/build $pkgdir/usr/share/doc/waf/book

  mkdir -p $pkgdir/usr/share/doc/waf/bookpdf
  mv $pkgdir/usr/share/doc/waf/book/*.pdf $pkgdir/usr/share/doc/waf/bookpdf
}

# vim:set ts=2 sw=2 et:
