# Contributor: Fernando
# Maintainer: Hoàng Đức Hiếu <arch@zahe.me>

pkgname=diffpdf
pkgver=2.1.1
pkgrel=1
pkgdesc="Diffing pdf files visually or textually"
url="http://www.qtrac.eu/diffpdf.html"
license=('GPL')
arch=(i686 x86_64)
depends=(poppler-qt)
changelog=CHANGES
source=("http://www.qtrac.eu/$pkgname-$pkgver.tar.gz")

md5sums=('eca981ad8de6e9c1969bb0491c22db14')

build () {
    cd "$srcdir"/$pkgname-$pkgver
    lrelease diffpdf.pro
    qmake
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    install -m 755 -D diffpdf "$pkgdir"/usr/bin/diffpdf
    install -m 644 -D diffpdf.1 "$pkgdir"/usr/share/man/man1/diffpdf.1
}
