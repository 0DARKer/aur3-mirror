# Contributor: perlawk
# Maintainer: DevRandom (jruffin at gmail dot com)
pkgname=gsl-shell
pkgver=2.1.0
pkgrel=5
pkgdesc="Offers an interactive Lua scripting interface to the GSL collection of mathematical functions." 
url="http://www.nongnu.org/gsl-shell/"
arch=('i686' 'x86_64')
license=('GPLv3')
depends=('lua' 'gsl' 'agg')

# source=("http://git.savannah.gnu.org/cgit/gsl-shell.git/snapshot/gsl-shell-2.1.0.tar.gz")
# The above is the original source. Problematically, this URL returns a file that is
# apparently packed on-the-fly by git and that always has a different checksum.
# We cannot use it directly because of the integrity checks.
# The URL below works because it is a static copy of the snapshot.
source=("http://fossies.org/unix/misc/gsl-shell-2.1.0.tar.gz")
sha1sums=('287b928ab72775061ef8d956b38ae531b9bd83c5')
build() {
    cd "$srcdir"/"$pkgname-$pkgver"

    sed -i -e "s|PREFIX=.*$|PREFIX=/usr|" makeconfig
    sed -i -e "s|DESTDIR=.*$|DESTDIR=$pkgdir|" makeconfig

    # The library dependencies are not handled too well in the Makefile.
    # Building the package with -j8 in the make options tends failing
    # because LuaJIT2 and libagg get linked before they are built.
    # We work around this by building them explicitly before the rest.
    make luajit2 && make agg-plot && make 
}
package() {
    cd "$srcdir"/"$pkgname-$pkgver"
    make install
}
