# Maintainer: Alad Wenter <alad@linuxbbq.org>
pkgname=systemd-shim
pkgver=6.4bzr1
_pkgver=6
pkgrel=3
pkgdesc="shim for systemd"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntu/+source/systemd-shim"
license=('GPL')
depends=('cgmanager')
optdepends=('pm-utils: utilities and scripts for power management'
	'libsystemd-standalone: systemd libraries'
	'eudev: udev forked by Gentoo')
provides=('systemd=215')
conflicts=('systemd')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/systemd-shim_6.orig.tar.xz"
	"https://launchpad.net/ubuntu/+archive/primary/+files/systemd-shim_6-4bzr1.debian.tar.xz")
sha256sums=('f1ae09c460b73f12d65cf31831e6561550776b9db60e6368f71a17e60a01dc30'
            '9f9c7a88c7d57d67af54a848ddb956d53702abffd9d5ac785673cfefb2ba9958')

prepare() {
	_patchdir="$srcdir/debian/patches"
	cd "$srcdir/$pkgname-$_pkgver"
	patch -p1 < "$_patchdir/systemd-policy-sync.patch"
	patch -p1 < "$_patchdir/0001-support-logind-cgroup-requests.patch"
	patch -p1 < "$_patchdir/0002-main-move-ourselves-into-cgroup-at-startup.patch"
	patch -p1 < "$_patchdir/0003-shim-send-the-scope-name-and-done-in-JobRemoved-sign.patch"
}
	
build() {
	cd "$srcdir/$pkgname-$_pkgver"
	aclocal
	automake --add-missing
	./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/${pkgname}
	make
}

package() {
	cd "$srcdir/$pkgname-$_pkgver"
	make DESTDIR="$pkgdir/" install
	mv "$pkgdir/etc/dbus-1/system.d/org.freedesktop.systemd1.conf" \
		"$pkgdir/etc/dbus-1/system.d/org.freedesktop.systemd-shim.conf"
}
