# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=systemsettings-git
pkgver=r1381.089fd80
pkgrel=1
pkgdesc='KDE system settings'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('kcmutils-git' 'khtml-git' 'kdelibs4support-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
conflicts=('systemsettings' 'kde-workspace<0')
provides=('systemsettings')
source=('git://anongit.kde.org/systemsettings.git')
md5sums=('SKIP')

pkgver() {
  cd systemsettings
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../systemsettings \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
