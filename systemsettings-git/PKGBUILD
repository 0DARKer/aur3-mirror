# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=systemsettings-git
pkgver=r1362.5e8d763
pkgrel=1
pkgdesc='KDE system settings'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('kcmutils-git' 'khtml-git' 'kdelibs4support-git')
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git')
conflicts=('systemsettings' 'kde-workspace<0')
provides=('systemsettings')
source=('git://anongit.kde.org/systemsettings.git')
md5sums=('SKIP')

pkgver() {
  cd systemsettings
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../systemsettings/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
