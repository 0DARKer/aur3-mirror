# Maintainer: Charles Vejnar <ce@vejnar.org>

pkgname=star-cshl
pkgver=2.4.1d
pkgrel=1
pkgdesc="STAR aligns RNA-seq reads to a reference genome using uncompressed suffix arrays"
arch=("i686" "x86_64")
url="https://github.com/alexdobin/STAR"
license=("GPL3")
source=("https://github.com/alexdobin/STAR/archive/STAR_${pkgver}.tar.gz")
sha1sums=("9a14f2a3ca73ea3bce20296bcdb1bb9ea50647d3")

build() {
    cd "$srcdir/STAR-STAR_${pkgver}/source"

    make STAR
    mv STAR STARshort
    make clean
    make STARlong
    mv STAR STARlong
}

package() {
    cd "$srcdir/STAR-STAR_${pkgver}/source"

    install -Dm755 STARshort "$pkgdir/usr/bin/STAR"
    install -Dm755 STARlong "$pkgdir/usr/bin/STARlong"
    install -Dm644 parametersDefault "$pkgdir/usr/share/doc/star-cshl/parameters"

    cd "$srcdir/STAR-STAR_${pkgver}/doc"

    install -Dm644 STARmanual.pdf "$pkgdir/usr/share/doc/star-cshl/STARmanual.pdf"
}
