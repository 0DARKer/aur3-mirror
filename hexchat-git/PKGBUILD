#Maintainer: TingPing tingping@tingping.se
pkgname=hexchat-git
pkgver=2.9.1
pkgrel=1
pkgdesc="A GTK+ based IRC client"
arch=('i686' 'x86_64')
url="http://www.hexchat.org/"
license=('GPL')
options=('!libtool')
depends=('gtk2' 'openssl' 'dbus-glib' 'libnotify' 'libproxy' 'hicolor-icon-theme')
makedepends=('git' 'tcl' 'perl' 'python2')
optdepends=('enchant: for spell check'
            'perl: for perl plugin'
            'tcl: for tcl plugin'
            'python2: for python plugin')
install='hexchat.install'
provides=('hexchat')
conflicts=('hexchat' 'hexchat-sasl')
replaces=('xchat-wdk-git')

build() {
  if [[ -d "$pkgname" ]]; then
    cd "$pkgname"
    msg 'Cleaning...'
    git clean -dfx
    msg 'Updating...'
    git pull origin
  else
    msg 'Cloning...'
    git clone 'git://github.com/hexchat/hexchat.git' "$pkgname" --depth=1 --single-branch --branch master
  fi

  cd "$srcdir/$pkgname"
  chmod +x autogen.sh
  ./autogen.sh
  LIBS+="-lgmodule-2.0" ./configure --prefix=/usr --sysconfdir=/etc --datadir=/usr/share \
    --enable-openssl --enable-dbus --enable-nls --enable-ipv6 --enable-shm --enable-spell=static
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
  install -D -m644 hexchat.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/hexchat.svg"
}