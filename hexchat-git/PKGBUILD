#Maintainer: TingPing tingping@tingping.se
pkgname=hexchat-git
pkgver=20130421
pkgrel=1
pkgdesc='A GTK+ based IRC client'
arch=('i686' 'x86_64' 'armv6h')
url='http://hexchat.org/'
license=('GPL')
options=('!libtool')
depends=('gtk2' 'openssl' 'dbus-glib' 'sox'
         'libsexy' 'libnotify' 'libproxy' 
         'hicolor-icon-theme' 'desktop-file-utils' 'sound-theme-freedesktop')
makedepends=('git' 'perl' 'python2')
optdepends=('enchant: for spell check'
            'perl: for perl plugin'
            'python2: for python plugin')
install='hexchat.install'
provides=('hexchat')
conflicts=('hexchat')
source=('git://github.com/hexchat/hexchat.git')
md5sums=('SKIP')
_gitname='hexchat'

build() {
  cd "$srcdir/$_gitname"

  # Use freedesktop sound theme
  sed -i s/hexchat_beep.wav/message-new-instant.oga/ src/fe-gtk/fe-gtk.c

  ./autogen.sh
  ./configure --prefix=/usr --enable-spell=libsexy --enable-textfe
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install

  # Remove gconf junk
  rm -rf "$pkgdir/usr/etc"

  # Remove now unused sound theme
  rm -rf "$pkgdir/usr/share/sounds"

  # Fix opening irc:// links
  desktop-file-edit \
    --add-mime-type='x-scheme-handler/irc;x-scheme-handler/ircs' \
    --set-key=Exec --set-value='sh -c "hexchat --existing --url %u || exec hexchat"' \
    "$pkgdir/usr/share/applications/hexchat.desktop"
}
