#Maintainer: TingPing tingping@tingping.se
pkgname=hexchat-git
pkgver=2.9.5.200.g65e3845
pkgrel=1
pkgdesc='A GTK+ based IRC client'
arch=('i686' 'x86_64' 'armv6h')
url='http://hexchat.org/'
license=('GPL')
options=('!libtool')
depends=('gtk2' 'openssl' 'dbus-glib' 'libcanberra'
         'libsexy' 'libnotify' 'libproxy' 
         'hicolor-icon-theme' 'desktop-file-utils' 'sound-theme-freedesktop')
makedepends=('git' 'perl' 'python')
optdepends=('enchant: for spell check'
            'perl: for perl plugin'
            'python: for python plugin')
install='hexchat.install'
provides=('hexchat')
conflicts=('hexchat')
source=('git://github.com/hexchat/hexchat.git')
md5sums=('SKIP')
_gitname='hexchat'

pkgver() {
  cd "$_gitname"
  _ver=`git describe --always | sed 's/-/./g'`
  echo ${_ver:1}
}

build() {
  cd "$srcdir/$_gitname"

  ./autogen.sh
  ./configure --prefix=/usr --enable-spell=libsexy --enable-textfe --enable-python=python3
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install

  # Remove gconf junk
  rm -rf "$pkgdir/usr/etc"

  # Fix opening irc:// links
  desktop-file-edit \
    --add-mime-type='x-scheme-handler/irc;x-scheme-handler/ircs' \
    --set-key=Exec --set-value='sh -c "hexchat --existing --url %u || exec hexchat"' \
    "$pkgdir/usr/share/applications/hexchat.desktop"
}
