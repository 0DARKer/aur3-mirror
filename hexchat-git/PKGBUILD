#Maintainer: TingPing tingping@tingping.se
pkgname=hexchat-git
pkgver=20130401
pkgrel=1
pkgdesc="A GTK+ based IRC client"
arch=('i686' 'x86_64')
url="http://hexchat.org/"
license=('GPL')
options=('!libtool')
depends=('gtk2' 'openssl' 'dbus-glib'
         'libsexy' 'libnotify' 'libproxy' 
         'hicolor-icon-theme')
makedepends=('git' 'perl' 'python2'
             'desktop-file-utils')
optdepends=('enchant: for spell check'
            'perl: for perl plugin'
            'python2: for python plugin')
install='hexchat.install'
provides=('hexchat')
conflicts=('hexchat')
source=('git://github.com/hexchat/hexchat.git')
md5sums=('SKIP')
_gitname='hexchat'

build() {
  cd "$srcdir/$_gitname"
  ./autogen.sh
  ./configure --prefix=/usr --enable-spell=libsexy --enable-textfe
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install

  # Remove gconf junk
  rm -rf "$pkgdir/usr/etc/"

  # Fix opening irc:// links
  desktop-file-edit \
    --add-mime-type='x-scheme-handler/irc;x-scheme-handler/ircs' \
    --set-key=Exec --set-value='sh -c "hexchat --existing --url %u || exec hexchat"' \
    "$pkgdir/usr/share/applications/hexchat.desktop"
}

