#Maintainer: TingPing tingping@tingping.se
pkgname=hexchat-git
pkgver=20130103
pkgrel=1
pkgdesc="A GTK+ based IRC client"
arch=('i686' 'x86_64')
url="http://www.hexchat.org/"
license=('GPL')
options=('!libtool')
depends=('gtk2' 'openssl' 'dbus-glib' 'libnotify' 'libproxy' 'hicolor-icon-theme')
makedepends=('git' 'tcl' 'perl' 'python2')
optdepends=('enchant: for spell check'
            'perl: for perl plugin'
            'tcl: for tcl plugin'
            'python2: for python plugin')
install='hexchat.install'
provides=('hexchat')
conflicts=('hexchat' 'hexchat-sasl' 'hexchat-xsys')
replaces=('xchat-wdk-git')

_gitroot='git://github.com/hexchat/hexchat.git'
_gitname='hexchat'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname" --depth=1 --single-branch --branch master
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  mkdir m4
  chmod +x autogen.sh
  ./autogen.sh
  ./configure --prefix=/usr --datadir=/usr/share \
    --enable-openssl --enable-dbus --enable-nls --enable-ipv6 --enable-shm --enable-spell=static
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
  install -D -m644 "share/icons/hexchat.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/hexchat.svg"
}

