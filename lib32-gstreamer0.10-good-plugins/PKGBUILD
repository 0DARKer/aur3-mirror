# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Maintainer: GordonGR <gordongr@freemail.gr>

_pkgname=gstreamer0.10-good-plugins
pkgname=lib32-$_pkgname
pkgver=0.10.31
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good plugin libraries, lib32"
arch=('x86_64')
url=http://gstreamer.freedesktop.org/
license=('LGPL')
depends=("${_pkgbasename}" "lib32-gstreamer0.10-good" "lib32-bzip2")
makedepends=("gcc-multilib" "intltool" "libavc1394" "libiec61883" "aalib" "lib32-libshout" "lib32-libdv" "lib32-flac" "lib32-gconf" "lib32-wavpack" "lib32-taglib" "lib32-libsoup" "lib32-v4l-utils" "lib32-libcaca" "lib32-gdk-pixbuf2" "lib32-libdv" "lib32-libpulse" "lib32-jack" "lib32-libpng" "lib32-libjpeg" "lib32-systemd")
options=(!emptydirs !libtool)
source=("$url/src/gst-plugins-good/gst-plugins-good-$pkgver.tar.xz"
		"v4l2-buffer-input.patch")
md5sums=('555845ceab722e517040bab57f9ace95'
         'decaeaab1dbd839216ec0db1126a3b26')


prepare() {
    cd gst-plugins-good-$pkgver/
    sed -i -e 's:AC_PATH_XTRA::g' -e 's:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:g' configure.ac
    patch -Np1 -i ../v4l2-buffer-input.patch
}

build() {
    export CC="gcc -m32 -march=i686"
    export CXX="g++ -m32 -march=i686"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd gst-plugins-good-$pkgver/
    autoreconf -fi
    ./configure --prefix=/usr \
        --libdir=/usr/lib32/ \
        --build=i686-unknown-linux-gnu \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --enable-experimental \
        --disable-schemas-install \
        --disable-hal \
        --disable-esd \
        --with-package-name="GStreamer Good Plugins (Archlinux)" \
        --with-package-origin="http://www.archlinux.org/"
    make
    sed 's:gst sys ext:gst:' -i Makefile
}


package() {
 
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make -C sys DESTDIR="${pkgdir}" install
  make -C ext GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  }
