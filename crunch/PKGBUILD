# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Jinx <Jinxware[at]gmail[dot]com>

pkgname=crunch
pkgver=3.2
pkgrel=1
pkgdesc="A wordlist generator for all combinations/permutations of a given character set"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/crunch-wordlist/"
license=('GPL')
depends=('glibc')
source=("http://downloads.sourceforge.net/crunch-wordlist/$pkgname-$pkgver.tgz"
        "http://downloads.sourceforge.net/crunch-wordlist/readme.txt")
md5sums=('4b6e74e5c50575544a7c66d389471039'
         'e6242ce04dbb84e9c9ebea49c6c91e8e')

build() {
  cd "$srcdir/$pkgname$pkgver"

  make
}

package() {
  cd "$srcdir/$pkgname$pkgver"

  # bin
  install -Dm755  $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # data
  install -Dm644 charset.lst \
    "$pkgdir/usr/share/$pkgname/charset.lst"

  # docs
  install -Dm644 ../readme.txt \
    "$pkgdir/usr/share/doc/$pkgname/readme.txt"

  # man
  install -Dm644  $pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1"
}
