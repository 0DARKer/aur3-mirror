# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Jinx <Jinxware[at]gmail[dot]com>

pkgname=crunch
pkgver=2.8
pkgrel=1
pkgdesc="A wordlist generator for all combinations/permutations of a given character set"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/crunch-wordlist/"
license=('GPL')
depends=('glibc')
source=("http://downloads.sourceforge.net/crunch-wordlist/$pkgname-$pkgver.tgz"
        "http://downloads.sourceforge.net/crunch-wordlist/readme.txt")
md5sums=('563e2ab01d31684ecbadd9beaba08d13'
         '48802606a58d845adf6ad6d49b1a1561')

build() {
  cd "$srcdir/$pkgname$pkgver"

  make
}

package() {
  cd "$srcdir/$pkgname$pkgver"

  install -d "$pkgdir"/usr/{bin,share/{$pkgname,man/man1,doc/$pkgname}}

  install -Dm755  $pkgname "$pkgdir/usr/bin"

  # Data, docs and man file
  install -Dm644 charset.lst "$pkgdir/usr/share/$pkgname"
  install -Dm644 ../readme.txt "$pkgdir/usr/share/doc/$pkgname"
  gzip -9 $pkgname.1 -c > "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
}
