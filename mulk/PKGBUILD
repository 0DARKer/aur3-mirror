# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Nathan Owe <ndowens04 at gmail>
# Contributor: Florian Besser <fbesser@gmail.com>

pkgname=mulk
pkgver=0.6.0
pkgrel=1
pkgdesc="Multi-connection command line tool to download Internet sites."
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/mulk"
license=('GPL3')
depends=('curl' 'uriparser' 'tidyhtml' 'libmetalink')
options=('!libtool')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('4e3311985b56d66548dd99dc2133ba00')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # add missing header
  sed -i "51a#include <inttypes.h>" lib/defines.h

  # remove hardcoded rpath
  sed -i "s|\${wl}-rpath[^']*||g" configure

  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}



