# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Paolo Stivanin <garrett16@hotmail.it>

pkgname=bashstyle
pkgver=7.12
pkgrel=1
pkgdesc="A PyGTK client for managing bash, readline, vim, and nano settings"
arch=('any')
url="http://freshmeat.net/projects/bsng/"
license=('GPL3' 'LGPL3')
depends=('bc' 'gnome-python' 'hicolor-icon-theme' 'newt' 'vte')
optdepends=('dmidecode:   sysinfo --bios support'
            'less:        colorized man pages'
            'ghostscript: man2pdf support'
            'pciutils:    sysinfo --pci support'
            'psyco:       optimized program speed'
            'usbutils:    sysinfo --usb support')
install=bsng.install
source=(http://download.tuxfamily.org/bashstyleng/$pkgname-ng-$pkgver.tar.xz)
md5sums=('b93db33a0201b5e08301d32d9ee49150')

build() {
  cd "$srcdir/$pkgname-ng-$pkgver"
  sed -i 's,python,&2,g' .configure/checks
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-ng-$pkgver"
  make DESTDIR="$pkgdir" DISABLE_POSTINSTALL=1 install
  install -dm755 "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/bashstyle.schemas" \
    data/bashstyle.schemas
}
