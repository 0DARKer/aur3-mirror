# Maintainer: Armin K. <krejzi at email dot com>

pkgname=imsettings
pkgver=1.6.3
pkgrel=3
pkgdesc="framework that delivers Input Method settings and applies the changes immediately"
arch=('i686' 'x86_64')
url="http://tagoh.bitbucket.org/imsettings/"
license=('LGPL2')
depends=('gtk2' 'ibus' 'libgxim' 'libnotify')
makedepends=('check' 'gconf' 'gobject-introspection' 'gtk3' 'xfconf')
optdepends=('gconf: GConf backend'
            'ibus-qt: IBus support in Qt4 apps'
            'xfconf: Xfce backend')
options=('!libtool')
source=("https://bitbucket.org/tagoh/${pkgname}/downloads/${pkgname}-${pkgver}.tar.bz2"
        'ibus.conf')
md5sums=('1a2253f548f7da6ff1211d01e48a8b98'
         'bb6cc6103e9d298fcbf0376bfbac746e')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/${pkgname} --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR="${pkgdir}"

  install -Dm644 ../ibus.conf "${pkgdir}/etc/X11/xinit/xinput.d/ibus.conf"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
