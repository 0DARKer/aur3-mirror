# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=treesheets
pkgname=${_pkgname}-git
pkgver=r74.f7254e6
pkgrel=2
pkgdesc='The ultimate replacement for spreadsheets, mind mappers, outliners, PIMs, text editors and small databases'
url='http://treesheets.com/'
license=('zlib')
depends=('wxgtk' 'webkitgtk2')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')

source=("git://github.com/aardappel/${_pkgname}.git#branch=master"
        "${_pkgname}.desktop"
        "bookcase.png")
sha512sums=('SKIP'
            '1bebd3ad4263c40f4aaddd5f8eee97e779ede3d4b2fd37fa300a45e5ade14a51da8b596a9bfe970f02258dfdb9205f554c8af6740870ece6af498be62d7d8d52'
            '55ebdb53f1897db6828b56aa5ff7feaaea468b971304e1a5db1f3822aef3bd613fe3b2a3de91011168540bbfb00179dfe4e6f4be533f56b5f0ac9faf4ae2d7e3')

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cp bookcase.png ${_pkgname}/TS/images/nuvola/dropdown/bookcase.png
    cd ${_pkgname}
    sed -i 's|L"/|"/usr/share/treesheets/|g;s|exepath\ *+\ *||g;s|^.*exepath.*$||;s|frame->"/usr|"/usr|g;s|sys->"/usr|"/usr|g' src/*
    find TS/examples -type f -exec chmod 644 '{}' \;
    find TS/images -type f ! -iname "*.png" -delete
}

build() {
    cd ${_pkgname}/src
    ./build.sh
}

package() {
    install -Dm644 ${_pkgname}.desktop "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
    install -Dm644 ${_pkgname}/ZLIB_LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm755 ${_pkgname}/TS/${_pkgname} "${pkgdir}/usr/bin/${_pkgname}"
    install -d "${pkgdir}/usr/share/${_pkgname}"
    cp -R ${_pkgname}/TS/examples "${pkgdir}/usr/share/${_pkgname}/examples"
    cp -R ${_pkgname}/TS/images "${pkgdir}/usr/share/${_pkgname}/images"
}
