# Maintainer: Prurigro
# Contributor: Prurigro

_pkgname=treesheets
pkgname=${_pkgname}-git
pkgver=r72.777a70d
pkgrel=1
pkgdesc='The ultimate replacement for spreadsheets, mind mappers, outliners, PIMs, text editors and small databases'
url='http://treesheets.com/'
license=('zlib')
depends=('wxgtk')
arch=('i686' 'x86_64')

source=("git://github.com/aardappel/${_pkgname}.git#branch=master"
        "${_pkgname}.desktop")
sha512sums=('SKIP'
            '1bebd3ad4263c40f4aaddd5f8eee97e779ede3d4b2fd37fa300a45e5ade14a51da8b596a9bfe970f02258dfdb9205f554c8af6740870ece6af498be62d7d8d52')

pkgver() {
    cd ${_pkgname}
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${_pkgname}/src
    ./build.sh
}

package() {
    # The binary needs to be located in and executed from the treesheets shared files directory
    install -d "${pkgdir}/usr/bin"
    echo -e '#!/usr/bin/env bash\n\ncd /opt/'${_pkgname}'\n'./${_pkgname}'' > "${pkgdir}/usr/bin/${_pkgname}"
    chmod 755 "${pkgdir}/usr/bin/${_pkgname}"

    # Install the shared files directory to opt (/usr/share can't have executables in it)
    install -d "${pkgdir}/opt"
    cp -R "${srcdir}/${_pkgname}/TS" "${pkgdir}/opt/${_pkgname}"

    # Install the xdg .desktop file
    install -Dm644 "${srcdir}/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"

    # Install the license file
    install -Dm644 "${srcdir}/${_pkgname}/ZLIB_LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
