# Maintainer: Stefan Haller <haliner@googlemail.com>
#
# The original PKGBUILD for socat in the official repository was written by:
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=socat2
pkgver=2.0.0beta4
_realname=socat
_realver=2.0.0-b4
pkgrel=2
pkgdesc='Multipurpose relay (development version)'
url='http://www.dest-unreach.org/socat/socat-version2.html'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('readline' 'openssl')
source=("http://www.dest-unreach.org/socat/download/${_realname}-${_realver}.tar.bz2")
sha1sums=('3903e0b1d8bef59e05fbbfa45f25d06a80a49eee')

build() {
	cd "${srcdir}/${_realname}-${_realver}"

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \

	make
}

package() {
	cd "${srcdir}/${_realname}-${_realver}"

	make DESTDIR="${pkgdir}" install

	mv "${pkgdir}/usr/bin/socat"{,2}
	mv "${pkgdir}/usr/bin/filan"{,2}
	mv "${pkgdir}/usr/bin/procan"{,2}
	mv "${pkgdir}/usr/share/man/man1/socat"{,2}".1"
	sed -i -e 's/socat/socat2/g;s/filan/filan2/g;s/procan/procan2/g' \
		"${pkgdir}/usr/share/man/man1/socat2.1"
}
