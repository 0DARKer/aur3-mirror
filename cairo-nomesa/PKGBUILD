# Maintainer: Mathijs van de Nes <mvdnes@mvdnes.nl>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=cairo-nomesa
pkgver=1.12.16
pkgrel=1
pkgdesc="A vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'sh' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'lzo2' 'gcc-libs')
makedepends=('gtk-doc')
checkdepends=('ttf-dejavu' 'gsfonts' 'xorg-server-xvfb' 'valgrind')
provides=('cairo-xcb' 'cairo')
replaces=('cairo-xcb' 'cairo')
source=(http://cairographics.org/releases/${pkgname:0:5}-$pkgver.tar.xz)

sha1sums=('4f6e337d5d3edd7ea79d1426f575331552b003ec')

build() {
  cd ${pkgname:0:5}-$pkgver

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-tee \
	--disable-gl \
	--disable-egl \
	--enable-svg \
	--enable-ps \
	--enable-pdf \
	--enable-gobject
  make
}

check() {
  cd ${pkgname:0:5}-$pkgver
  make -k test || /bin/true
  
  # results:
  # 1.12.16-1:	249 Passed, 251 Failed [0 crashed, 9 expected], 33 Skipped
}

package() {
  cd ${pkgname:0:5}-$pkgver
  make DESTDIR="$pkgdir" install
}
