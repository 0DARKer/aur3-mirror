# Maintainer: Mathijs van de Nes <mvdnes@mvdnes.nl>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgname=cairo-nomesa
pkgver=1.14.0
pkgrel=1
pkgdesc="A vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'sh' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.30.0' 'glib2' 'lzo2' 'gcc-libs')
makedepends=('gtk-doc')
provides=('cairo-xcb' 'cairo')
replaces=('cairo-xcb' 'cairo')
source=(http://cairographics.org/releases/${pkgname:0:5}-$pkgver.tar.xz)

sha1sums=('53cf589b983412ea7f78feee2e1ba9cea6e3ebae')

build() {
  cd ${pkgname:0:5}-$pkgver

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-tee \
	--disable-gl \
	--disable-egl \
	--enable-svg \
	--enable-ps \
	--enable-pdf \
	--enable-gobject
  make
}

package() {
  cd ${pkgname:0:5}-$pkgver
  make DESTDIR="$pkgdir" install
}
