# Contributor: Vitaliy Berdinskikh <skipper13@archlinux.org.ua>
# Contributor: Andrea Agosti <cifvts@gmail.com>
# Maintainer: xduugu
pkgname=instantclient-sdk
pkgver=11.2.0.1
pkgrel=3
pkgdesc="Additional header files for developing Oracle applications with Instant Client"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technology/tech/oci/instantclient/"
license=('custom:OTN')
depends=(instantclient-basic=$pkgver)
#
# grab the source from
# i686  : http://www.oracle.com/technology/software/tech/oci/instantclient/htdocs/linuxsoft.html
# x86_64: http://www.oracle.com/technology/software/tech/oci/instantclient/htdocs/linuxx86_64soft.html
#
source=($pkgname-linux32-$pkgver.zip LICENSE)
md5sums=('374e1986621cb83ec90d4714c5430473'
         '2d62e22e4f2d7e6d220fafde0f692a7d')
sha256sums=('e3c5bca1f3068eb6f57655fe6ebcbead48e6acf3d920bb9c932777deeeecafdf'
            'f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')

if [[ $CARCH = x86_64 ]]; then
	source[0]=oracle-${pkgname%%-*}$(echo $pkgver | sed -r 's/^([0-9]+\.[0-9]+).*$/\1/')-${pkgname#*-}-${pkgver}.0-1.$CARCH.zip
	md5sums=('ee46ae0ec92397cb9b0cef4f48e0eda7'
           '2d62e22e4f2d7e6d220fafde0f692a7d')
	sha256sums=('3fa56a3b9a39415d69855c3b8e61b2e9952c426ddc419dfd3bdb9e3cc9b17dc3'
              'f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')
fi

build() {
	local dest=$pkgdir/opt/${pkgname%%-*}

	# copy files
	local pkgver=${pkgver/./_}
	install -d "${dest%/*}" &&
	cp -a "$srcdir/${pkgname%%-*}_${pkgver%%.*}" "$dest" || return 1

	# fix permissions
	cd "$dest/sdk" &&
	chmod -R a=r,u+w,a+X * || return 1

	install -Dm644 "$srcdir/LICENSE" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
}

# vim:set ts=2 sw=2 noet:
