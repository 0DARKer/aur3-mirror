pkgname="radare2-git"
pkgver=20121201
pkgrel=1
pkgdesc="A collection of tools with the aim to create a complete, portable, multi-architecture, unix-like toolchain for reverse engineering."
arch=('i686' 'x86_64')
url="http://radare.org"
license=('GPL3')
depends=('valabind-git' 'python2' 'libewf')
makedepends=('mercurial' 'python2' 'lua')
provides=('radare2' 'r2-bindings')
conflicts=('radare2' 'r2-bindings' 'radare2-hg')

_gitroot="git://github.com/radare/radare2.git"
_gitname="radare2"

build() {
	export CFLAGS="${CFLAGS//-fPIE -pie}"
	export CXXFLAGS="${CXXFLAGS//-fPIE -pie}"
	export PKG_CONFIG_PATH="${srcdir}/${_gitname}-build/pkgcfg:$PKG_CONFIG_PATH"

	cd ${srcdir}

	if [ -d ${_gitname}-build ] ; then
		msg "Removing leftovers..."
		rm -rf ${_gitname}-build
	fi

	if [ -d ${_gitname} ]; then
		cd ${_gitname}
		git pull --rebase
	else
		git clone ${_gitroot} ${_gitname}
	fi

	cd ${srcdir}
	git clone ${_gitname} ${_gitname}-build
	cd ${_gitname}-build

	msg "Building radare2..."
	./sys/build.sh

	# hack to enable compiling the language bindings
	mkdir _tmp
	make DESTDIR="${pkgdir}"/_tmp install
	_pfx="${pkgdir}"/_tmp

	export LIBR_PLUGINS=${_pfx}/lib/radare2
	export LD_LIBRARY_PATH=${_pfx}/lib:$LD_LIBRARY_PATH
	export DYLD_LIBRARY_PATH=${_pfx}/lib:$LD_LIBRARY_PATH

	msg "Building language bindings..."
	cd r2-bindings
	make clean
	./configure --prefix=/usr --enable=python --enable=lua
	cd python
	make
	cd ../lua
	make

	rm -rf "${pkgdir}"/_tmp
}

package() {
	cd ${srcdir}/${_gitname}-build

	make DESTDIR=${pkgdir} install

	# temp hack for html graph
	install -m755 mk/htmlgraph/htmlgraph.sh ${pkgdir}/usr/lib/radare2/0.9.3git/www/ 
	ln -s /usr/lib/radare2/0.9.3git/www/htmlgraph.sh ${pkgdir}/usr/bin/htmlgraph.sh

	cd r2-bindings
	make DESTDIR=${pkgdir} install-vapi
	make DESTDIR=${pkgdir} PYTHON=python2.7 install-python
	make DESTDIR=${pkgdir} install-lua

	cd ${srcdir}
	rm -rf ${_gitname}-build
}
