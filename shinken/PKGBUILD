# Maintainer: Julien "Adyxax" Dessaux <judessaux@gmail.com>
pkgname='shinken'
pkgver='0.6'
pkgrel='1'
pkgdesc='Shinken is a new powerful monitoring tool compatible with NagiosÂ® and written in python that enables organizations to identify and resolve IT problems before they impact the business.'
arch=('any')
url='http://www.shinken-monitoring.org/'
license=('GPL3')
groups=()
depends=('python' 'python2' 'python-pyro')
makedepends=('setuptools')
optdepends=('nagios-plugins: various monitoring checks'
            'nagcon: a simple yet great curses interface over nagios style status.dat file')
provides=('shinken')
conflicts=()
replaces=()
backup=()
options=()
install=('shinken.install')
changelog=
source=("http://shinken-monitoring.org/pub/$pkgname-$pkgver.tar.gz" 'shinken.install' 'shinken.setup_parameters')
noextract=()
md5sums=('296b37db6799915ae9822e48670c5c20'
         '564726b4d34f9522ba31e152fe2c228e'
         '85d8cb4b28662f83df26a3da05e2f4ff')

build() {
  cp -f shinken.setup_parameters "$srcdir/$pkgname-$pkgver/setup_parameters"
  cd "$srcdir/$pkgname-$pkgver"

  find ./ -name '*.py' -exec sed -r 's/^#!\/usr\/bin\/env python2.6$/#!\/usr\/bin\/env python2.7/' -i {} \;
  find ./ -type f -exec sed -r 's/\/etc\/init.d/\/etc\/rc.d/' -i {} \;
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/
  mv $pkgdir/etc/init.d $pkgdir/etc/rc.d
  rm $pkgdir/etc/shinken/{brokerd-windows.ini,nagios-windows.cfg,pollerd-windows.ini,schedulerd-windows.ini}
  find $pkgdir/etc/ -type f -exec sed -i 's!'$pkgdir'!!g' {} \;
  sed -i 's!/usr/lib/nagios/plugins!/usr/share/nagios/libexec!g' $pkgdir/etc/shinken/resource.cfg
  sed -i 's!modules.*Livestatus, Simple-log!modules Status-Dat, Simple-log!' $pkgdir/etc/shinken/shinken-specific.cfg
  sed -i 's!log_end_msg [01]!echo !' $pkgdir/etc/rc.d/shinken
  rm $pkgdir/var/lib/shinken/void_for_git
  mkdir $pkgdir/var/lib/shinken/rw
}

# vim:set ts=2 sw=2 et:
