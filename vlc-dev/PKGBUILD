# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: bxs <bxsbxs@gmail.com>
# Contributor: uwinkelvos <uwinkelvos@gmx.de> (for being such a genius)
# Contributor: <you know who you are>
# Based by ~80% on [extra]'s vlc

pkgbase=vlc
pkgname=('vlc-dev')
true && pkgname=('vlc-dev' 'vlc-plugin-dev')
pkgver=1.1.7
_dlver=1.1.7
pkgrel=1
pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player - Bleeding edge version"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
makedepends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'lirc-utils' 'libdca' 'qt' 'libproxy' 'sdl_image' 'libdvdnav' 'mesa' 'lua' 'v4l-utils' 'libcddb' 'smbclient' 'libmatroska' 'zvbi' 'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2' 'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug' 'avahi' 'ttf-freefont' 'libxv' 'libass' 'xdg-utils' 'libxpm' 'xulrunner' 'live-media' 'libnotify' 'libcdio' 'flac' 'libtheora' 'alsa-lib' 'jack' 'kdelibs' 'udev' 'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'libpulse')
options=('!libtool')
source=("http://downloads.videolan.org/pub/videolan/${pkgbase}/${_dlver}/${pkgbase}-${_dlver}.tar.bz2"
        vlc-1.1.7-libmatroska.patch
        vlc-libnotify-0.7.patch)
md5sums=(`wget ${source}.md5 -qO - | cut -d " " -f1`
         'd03b0a4f3d2a0ece5c0a9e1a2919dec0'
         '122ae89daed6ebfe6e95455506ee0a1e')
# source[0]=("http://downloads.videolan.org/pub/videolan/testing/${pkgbase}-${_dlver}/${pkgbase}-${_dlver}.tar.bz2")   ## The 'bleeding edge' line
# sha1sums[0]=(`wget ${source}.sha1 -qO - | cut -d " " -f1`)					    ## The 'bleeding edge' line

build() {
  cd ${pkgbase}-${_dlver}

  patch -Np1 -i ../vlc-1.1.7-libmatroska.patch
  [[ `pacman -Q libnotify|cut -d " " -f2` = 0.7.* ]] && patch -Np1 -i ../vlc-libnotify-0.7.patch
  sed -i -e 's:truetype/freefont:TTF:g' modules/misc/freetype.c
  # Most configure options are enabled by default and thus not included here.
  # A full list can be seen with: 'src/vlc-*/configure --help'
  ./configure --prefix=/usr \
              --disable-rpath \
              --enable-faad \
              --enable-v4l \
              --enable-snapshot \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --enable-mozilla \
              --with-live555-tree=/usr/lib/live \
              --enable-realrtsp \
              --enable-merge-ffmpeg
  make
}

package_vlc-plugin-dev() {
  pkgdesc="VLC mozilla browser plugin - Bleeding edge version"
  depends=("${pkgbase}=${pkgver}" 'nspr' 'xulrunner' 'libxpm')
  provides=("vlc-plugin=${pkgver}")
  conflicts=('vlc-plugin')

  cd ${pkgbase}-${pkgver}

  #for linking against libvlc
  make -C src DESTDIR="${pkgdir}" install-libLTLIBRARIES

  make -C projects/mozilla DESTDIR="${pkgdir}/" install

  #clean up libvlc
  make -C src DESTDIR="${pkgdir}" uninstall-libLTLIBRARIES
}

package_vlc-dev() {
  depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libcdio' 'libdca' 'qt' 'libproxy' 'sdl_image' 'libdvdnav' 'mesa' 'lua' 'v4l-utils' 'libcddb' 'smbclient' 'libmatroska' 'zvbi' 'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2' 'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug' 'ttf-freefont' 'libxv' 'libass' 'xdg-utils' 'desktop-file-utils')
  optdepends=('avahi: for service discovery using bonjour protocol'
              'libnotify: for notification plugin'
              'ncurses: for ncurses interface support'
              'libdvdcss: for decoding encrypted DVDs'
              'lirc-utils: for lirc plugin'
              'libavc1394: for devices using the 1394ta AV/C'
              'libdc1394: for IEEE 1394 plugin'
              'kdelibs: KDE Solid hardware integration'
              'vdpau-video: VDPAU backend for VA API (for GPU acceleration on Nvidia cards)'
              'xvba-video: XvBA backend for VA API (for GPU acceleration on ATI cards)'
              'libpulse: PulseAudio support')
  options=('!libtool')
  provides=("vlc=${pkgver}")
  conflicts=('vlc' 'vlc-pulse-plugin')
  replaces=('vlc-pulse-plugin')
  install=vlc-dev.install

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}/" install

  for res in 16 32 48 128; do
    install -D -m644 share/icons/${res}x${res}/vlc.png \
        "${pkgdir}"/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png
  done

  rm -rf "${pkgdir}/usr/lib/mozilla"
}