# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: bxs <bxsbxs@gmail.com>
# Contributor: uwinkelvos <uwinkelvos@gmx.de> (for being such a genius)
# Based on [extra]'s vlc

pkgname=vlc-dev
pkgver=1.2.0pre1
_dlver=1.2.0-pre1
pkgrel=1
pkgdesc="A multi-platform media player - Bleeding edge version"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libcdio' 'libdca' 'qt' 'libproxy' 'sdl_image' 'libdvdnav' 'mesa' 'lua' 'libcddb' 'smbclient' 'libmatroska' 'zvbi' 'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp' 'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug' 'ttf-freefont' 'libxv' 'libass' 'xdg-utils' 'desktop-file-utils' 'libxpm')
makedepends=('avahi' 'live-media' 'libnotify' 'flac' 'libtheora' 'alsa-lib' 'jack' 'kdelibs' 'udev' 'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'libpulse' 'lirc-utils' 'gnutls' 'libcaca')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin'
            'ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'kdelibs: KDE Solid hardware integration'
            'libpulse: PulseAudio support'
            'vdpau-video: VDPAU back-end for Nvidia'
            'xvba-video: XvBA backend for ATI/AMD'
            'libva-driver-intel: back-end for intel cards')
provides=("vlc=${pkgver}")
conflicts=('vlc' 'vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool')
install=vlc-dev.install
# source=("http://download.videolan.org/pub/videolan/vlc/${_dlver}/vlc-${_dlver}.tar.bz2")
# md5sums=(`wget ${source}.md5 -qO - | cut -d " " -f1`)
source=(http://downloads.videolan.org/pub/videolan/testing/vlc-${_dlver}/vlc-${_dlver}.tar.xz)   ## The 'bleeding edge' line
# sha1sums=(`wget ${source}.sha1 -qO - | cut -d " " -f1`)					    ## The 'bleeding edge' line
sha1sums=('72b38f2c97cad4f143b16a2891f083bfab5b5174')

build() {
  cd vlc-${_dlver}

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c

  # A full list can be seen with: 'src/vlc-*/configure --help'
  ./configure --prefix=/usr \
              --disable-rpath \
              --enable-faad \
              --enable-dbus-control \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --with-live555-tree=/usr/lib/live \
              --enable-upnp \
              --enable-realrtsp
  make
}

package() {
  cd vlc-${_dlver}

  make DESTDIR="${pkgdir}/" install

  for res in 16 32 48 128; do
    install -D -m644 share/icons/${res}x${res}/vlc.png "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done
}