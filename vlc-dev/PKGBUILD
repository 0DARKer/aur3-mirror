# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: bxs <bxsbxs@gmail.com>
# Contributor: uwinkelvos <uwinkelvos@gmx.de> (for being such a genius)
# Based on [extra]'s vlc

pkgname=vlc-dev
pkgver=1.2.0pre4
_dlver=1.2.0-pre4
pkgrel=1
pkgdesc="A multi-platform media player - Bleeding edge version"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'desktop-file-utils' 'faad2' 'ffmpeg' 'fluidsynth' 'fribidi' 'libass' 'libcddb' 'libcdio' 'libdca' 'libdvbpsi' 'libdvdnav' 'libmad' 'libmatroska' 'libmodplug' 'libmpcdec' 'libmpeg2' 'libmtp' 'libproxy' 'libshout' 'libupnp' 'libxpm' 'libxv' 'lua' 'mesa' 'qt' 'sdl_image' 'smbclient' 'sysfsutils' 'taglib' 'ttf-freefont' 'xcb-util-keysyms' 'xdg-utils' 'zvbi')
makedepends=('alsa-lib' 'avahi' 'flac' 'gnutls' 'jack' 'kdelibs' 'libavc1394' 'libcaca' 'libdc1394' 'libnotify' 'libpulse' 'libraw1394' 'libtheora' 'libva' 'lirc-utils' 'live-media' 'udev')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'libdvdcss: for decoding encrypted DVDs'
            'libnotify: for notification plugin'
            'libpulse: PulseAudio support'
            'lirc-utils: for lirc plugin'
            'kdelibs: KDE Solid hardware integration'
            'ncurses: for ncurses interface support'
            'libva-driver-intel: back-end for intel cards'
            'vdpau-video: VDPAU back-end for Nvidia'
            'xvba-video: XvBA backend for ATI/AMD')
provides=("vlc=$pkgver")
conflicts=('vlc' 'vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool')
install=vlc-dev.install
# source=("http://download.videolan.org/pub/videolan/vlc/$_dlver/vlc-$_dlver.tar.bz2")
# md5sums=(`curl -s $source.md5 | cut -d " " -f1`)
source=(http://downloads.videolan.org/pub/videolan/testing/vlc-$_dlver/vlc-$_dlver.tar.xz)   ## The 'bleeding edge' line
sha512sums=(`curl -s $source.sha512 | cut -d " " -f1`)                                      ## The 'bleeding edge' line

build() {
  cd vlc-$_dlver

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c

  ./configure --prefix=/usr \
              --disable-rpath \
              --enable-faad \
              --enable-dbus-control \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --enable-upnp \
              --enable-realrtsp
  make
}

package() {
  cd vlc-$_dlver

  make DESTDIR="$pkgdir" install

  for res in 16 32 48 128; do
    install -D -m644 share/icons/${res}x$res/vlc.png "$pkgdir/usr/share/icons/hicolor/${res}x$res/apps/vlc.png"
  done
}