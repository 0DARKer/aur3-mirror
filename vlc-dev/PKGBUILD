# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: bxs <bxsbxs@gmail.com>
# Contributor: uwinkelvos <uwinkelvos@gmx.de> (for being such a genius)
# Based on [extra]'s vlc

pkgname=vlc-dev
pkgver=2.0.0
_dlver=2.0.0
pkgrel=2
pkgdesc="A multi-platform media player - Bleeding edge version"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'desktop-file-utils' 'faad2' 'ffmpeg' 'fluidsynth' 'fribidi' 'libass' 'libcddb' 'libcdio' 'libdca' 'libdvbpsi' 'libdvdnav' 'libmad' 'libmatroska' 'libmodplug' 'libmpcdec' 'libmpeg2' 'libmtp' 'libproxy' 'libshout' 'libupnp' 'libxpm' 'libxv' 'lua' 'mesa' 'qt' 'sdl_image' 'smbclient' 'sysfsutils' 'taglib' 'ttf-freefont' 'xcb-util-keysyms' 'xdg-utils' 'zvbi')
makedepends=('alsa-lib' 'avahi' 'flac' 'gnutls' 'jack' 'kdelibs' 'libavc1394' 'libbluray' 'libcaca' 'libdc1394' 'libnotify' 'libpulse' 'libraw1394' 'libtheora' 'libva' 'lirc-utils' 'live-media' 'udev')
optdepends=('avahi: for service discovery using bonjour protocol'
            'kdelibs: KDE Solid hardware integration'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'libdvdcss: for decoding encrypted DVDs'
            'libnotify: for notification plugin'
            'libpulse: PulseAudio support'
            'lirc-utils: for lirc plugin'
            'libbluray: for Blu-Ray disks'
            'libva-driver-intel: back-end for intel cards'
            'ncurses: for ncurses interface support'
            'vdpau-video: VDPAU back-end for Nvidia'
            'xvba-video: XvBA backend for ATI/AMD')
provides=("vlc=$pkgver")
conflicts=('vlc' 'vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool')
install=vlc-dev.install
# source=("http://download.videolan.org/pub/videolan/vlc/$_dlver/vlc-$_dlver.tar.bz2")
source=("http://sourceforge.net/projects/vlc/files/$_dlver/vlc-$_dlver.tar.xz")
# source=("http://downloads.videolan.org/pub/videolan/testing/vlc-$_dlver/vlc-$_dlver~unix.tar.xz")
md5sums=(`curl -Ls $source.md5 | cut -d " " -f1`)
# sha256sums=(`curl -s $source.sha256 | cut -d " " -f1`)

build() {
  cd vlc-$_dlver

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c

  ./configure --prefix=/usr \
              --disable-rpath \
              --enable-faad \
              --enable-dbus-control \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --enable-upnp \
              --enable-bluray \
              --enable-live555 \
              --enable-realrtsp
  make
}

package() {
  cd vlc-$_dlver

  make DESTDIR="$pkgdir" install

  for i in 16x16 32x32 48x48 128x128; do
    install -D -m644 share/icons/$i/vlc.png "$pkgdir/usr/share/icons/hicolor/$i/apps/vlc.png"
  done
}