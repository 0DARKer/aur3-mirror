# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: bxs <bxsbxs@gmail.com>
# Contributor: uwinkelvos <uwinkelvos@gmx.de> (for being such a genius)
# Based on [extra]'s vlc

pkgname=vlc-dev
pkgver=1.1.10
_dlver=1.1.10
pkgrel=3
pkgdesc="VLC mozilla browser plugin - Bleeding edge version"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('GPL')
depends=('a52dec' 'fluidsynth' 'libmtp' 'libdvbpsi' 'libcdio' 'libdca' 'qt' 'libproxy' 'sdl_image' 'libdvdnav' 'mesa' 'lua' 'v4l-utils' 'libcddb' 'smbclient' 'libmatroska' 'zvbi' 'taglib' 'sysfsutils' 'libmpcdec' 'ffmpeg' 'faad2' 'libupnp' 'libshout' 'libmad' 'fribidi' 'libmpeg2' 'libmodplug' 'ttf-freefont' 'libxv' 'libass' 'xdg-utils' 'desktop-file-utils')
makedepends=('avahi' 'pkgconfig' 'libxpm' 'live-media' 'libnotify' 'flac' 'libtheora' 'alsa-lib' 'jack' 'kdelibs' 'udev' 'libraw1394' 'libdc1394' 'libavc1394' 'libva' 'libpulse' 'lirc-utils' 'gnutls' 'libcaca')
optdepends=('avahi: for service discovery using bonjour protocol'
            'libnotify: for notification plugin'
            'ncurses: for ncurses interface support'
            'libdvdcss: for decoding encrypted DVDs'
            'lirc-utils: for lirc plugin'
            'libavc1394: for devices using the 1394ta AV/C'
            'libdc1394: for IEEE 1394 plugin'
            'kdelibs: KDE Solid hardware integration'
            'vdpau-video: VDPAU backend for VA API (for GPU acceleration on Nvidia cards)'
            'xvba-video: XvBA backend for VA API (for GPU acceleration on ATI cards)'
            'libpulse: PulseAudio support')
provides=("vlc=${pkgver}")
conflicts=('vlc' 'vlc-plugin')
replaces=('vlc-plugin')
options=('!libtool')
install=vlc-dev.install
source=("http://download.videolan.org/pub/videolan/vlc/${_dlver}/vlc-${_dlver}.tar.bz2"
        vlc-1.1.10-libavformat.patch)
md5sums=(`wget ${source}.md5 -qO - | cut -d " " -f1`
         '9b82571a594e08e67fbae9c96c3f3ed7')
# source[0]=("http://downloads.videolan.org/pub/videolan/testing/vlc-${_dlver}/vlc-${_dlver}.tar.bz2")   ## The 'bleeding edge' line
# sha1sums[0]=(`wget ${source}.sha1 -qO - | cut -d " " -f1`)					    ## The 'bleeding edge' line

build() {
  cd vlc-${_dlver}

  sed -i -e 's:truetype/freefont:TTF:g' modules/misc/freetype.c

  # FS#24872
  patch -Np1 -i ../vlc-1.1.10-libavformat.patch

  # A full list can be seen with: 'src/vlc-*/configure --help'
  ./configure --prefix=/usr \
              --disable-rpath \
              --enable-faad \
              --enable-v4l \
              --enable-snapshot \
              --enable-dbus-control \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --with-live555-tree=/usr/lib/live \
              --enable-upnp \
              --enable-realrtsp
  make
}

package() {
  cd vlc-${pkgver}

  make DESTDIR="${pkgdir}/" install

  for res in 16 32 48 128; do
    install -D -m644 share/icons/${res}x${res}/vlc.png "${pkgdir}/usr/share/icons/hicolor/${res}x${res}/apps/vlc.png"
  done
}