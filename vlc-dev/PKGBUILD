# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: bxs <bxsbxs@gmail.com>
# Contributor: uwinkelvos <uwinkelvos@gmx.de> (for being such a genius)
# Based on [extra]'s vlc

pkgname=vlc-dev
pkgver=2.0.0
_dlver=2.0.0
pkgrel=2
pkgdesc="A multi-platform media player - Bleeding edge version"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('LGPL2.1' 'GPL2')
depends=('a52dec' 'aalib' 'faad2' 'ffmpeg' 'fluidsynth' 'libass' 'libcddb' 'libcdio' 'libdca' 'libdvbpsi' 'libdvdnav' 'libmad' 'libmatroska' 'libmodplug' 'libmpcdec' 'libmpeg2' 'libmtp' 'libproxy' 'libshout' 'libtar' 'libtiger' 'libupnp' 'libxinerama' 'libxpm' 'lua' 'mesa' 'qt' 'sdl_image' 'smbclient' 'taglib' 'ttf-freefont' 'xcb-util-keysyms' 'zvbi')
makedepends=('alsa-lib' 'avahi' 'flac' 'gnome-vfs' 'gnutls' 'jack' 'kdelibs' 'libavc1394' 'libbluray' 'libcaca' 'libdc1394' 'libdvdcss' 'libgcrypt' 'libgme' 'libgoom2' 'libnotify' 'libpulse' 'libraw1394' 'libtheora' 'libva' 'lirc-utils' 'live-media' 'ncurses' 'portaudio' 'projectm' 'schroedinger' 'udev' 'twolame' 'vcdimager' 'xosd')
optdepends=('avahi: for service discovery using bonjour protocol'
            'gnome-vfs: for GNOME Virtual File System support'
            'kdelibs: KDE Solid hardware integration'
            'libavc1394: for devices using the 1394ta AV/C'
            'libbluray: for Blu-Ray disks'
            'libcaca: for colored ASCII art video output'
            'libdc1394: for IEEE 1394 plugin'
            'libdvdcss: for decoding encrypted DVDs'
            'libgcrypt: support for some plugins'
            'libgme: for libgme plugin'
            'libgoom2: for libgoom plugin'
            'libnotify: for notification plugin'
            'libpulse: PulseAudio support'
            'librsvg: for SVG plugin'
            'libva-driver-intel: back-end for intel cards'
            'lirc-utils: for lirc plugin'
            'ncurses: for ncurses interface support'
            'portaudio: for portaudio support'
            'projectm: for ProjectM visualisation plugin'
            'twolame: for TwoLAME mpeg2 encoder plugin'
            'udev: for service discovery using udev'
            'vcdimager: navigate VCD with libvcdinfo'
            'vdpau-video: VDPAU back-end for Nvidia'
            'xosd: for xosd support'
            'xvba-video: XvBA backend for ATI/AMD')
provides=("vlc=$pkgver")
conflicts=('vlc' 'vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool')
install=vlc-dev.install
# source=("http://download.videolan.org/pub/videolan/vlc/$_dlver/vlc-$_dlver.tar.bz2")
source=("http://sourceforge.net/projects/vlc/files/$_dlver/vlc-$_dlver.tar.xz"
        'access_smb_link_to_smbclient.patch')
# source=("http://downloads.videolan.org/pub/videolan/testing/vlc-$_dlver/vlc-$_dlver~unix.tar.xz")
md5sums=(`curl -Ls $source.md5 | cut -d " " -f1`
         '975f8f9fe9b187512ed8dabb0b8aa6b8')
# sha256sums=(`curl -s $source.sha256 | cut -d " " -f1`)

build() {
  cd vlc-$_dlver

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c

  patch -Np1 -i ../access_smb_link_to_smbclient.patch
  autoreconf -fi

  ./configure --prefix=/usr \
              --disable-rpath \
              --enable-faad \
              --enable-nls \
              --enable-lirc \
              --enable-pvr \
              --enable-ncurses \
              --enable-real \
              --enable-realrtsp \
              --enable-xosd \
              --enable-aa \
              --enable-vcdx
  make
}

package() {
  cd vlc-$_dlver

  make DESTDIR="$pkgdir" install

  for i in 16x16 32x32 48x48 128x128; do
    install -D -m644 share/icons/$i/vlc.png "$pkgdir/usr/share/icons/hicolor/$i/apps/vlc.png"
  done
}