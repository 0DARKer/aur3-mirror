# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: bxs <bxsbxs@gmail.com>
# Contributor: uwinkelvos <uwinkelvos@gmx.de> (for being such a genius)
# Based on [extra]'s vlc

pkgname=vlc-dev
pkgver=2.0.3
_dlver=$pkgver
pkgrel=1
pkgdesc="A multi-platform media player - Bleeding edge version"
arch=('i686' 'x86_64')
url="http://www.videolan.org/vlc/"
license=('LGPL2.1' 'GPL2')
depends=('a52dec' 'faad2' 'ffmpeg' 'libass' 'libdca' 'libdvbpsi' 'libdvdnav' 'libmad' 'libmatroska' 'libmodplug' 'libmpcdec' 'libmpeg2' 'libproxy' 'libshout' 'libtiger' 'libupnp' 'libxpm' 'lua' 'qt' 'sdl_image' 'taglib' 'ttf-freefont' 'xcb-util-keysyms' 'zvbi')
makedepends=('aalib' 'flac' 'fluidsynth' 'gnome-vfs' 'kdelibs' 'libavc1394' 'libbluray' 'libcaca' 'libdc1394' 'libdvdcss' 'libgme' 'libgoom2' 'libmtp' 'libnotify' 'librsvg' 'libtar' 'lirc-utils' 'live-media' 'portaudio' 'projectm' 'twolame' 'vcdimager' 'xosd')
optdepends=('aalib: for ASCII art plugin'
            'avahi: for service discovery using bonjour protocol'
            'flac: for Free Lossless Audio Codec plugin'
            'fluidsynth: for synthesizer MIDI FluidSynth'
            'gnome-vfs: for GNOME Virtual File System support'
            'kdelibs: KDE Solid hardware integration'
            'libavc1394: for devices using the 1394ta AV/C'
            'libbluray: for Blu-Ray disks'
            'libcaca: for colored ASCII art video output'
            'libcdio: for audio CD playback support'
            'libdc1394: for IEEE 1394 plugin'
            'libdvdcss: for decoding encrypted DVDs'
            'libgme: for libgme plugin'
            'libgoom2: for libgoom plugin'
            'libmtp: for MTP devices support'
            'libnotify: for notification plugin'
            'librsvg: for SVG plugin'
            'libva-driver-intel: back-end for intel cards'
            'lirc-utils: for lirc plugin'
            'ncurses: for ncurses interface support'
            'portaudio: for portaudio support'
            'projectm: for ProjectM visualisation plugin'
            'smbclient: for SMB access plugin'
            'twolame: for TwoLAME mpeg2 encoder plugin'
            'vcdimager: navigate VCD with libvcdinfo'
            'vdpau-video: VDPAU back-end for Nvidia'
            'xosd: for xosd support'
            'xvba-video: XvBA backend for ATI/AMD')
provides=("vlc=$pkgver")
conflicts=('vlc' 'vlc-plugin')
replaces=('vlc-plugin')
backup=('usr/share/vlc/lua/http/.hosts'
        'usr/share/vlc/lua/http/dialogs/.hosts')
options=('!libtool' '!emptydirs')
install=vlc-dev.install
source=("http://sourceforge.net/projects/vlc/files/$_dlver/vlc-$_dlver.tar.xz")
# source=("http://downloads.videolan.org/pub/videolan/testing/vlc-$_dlver/vlc-$_dlver~unix.tar.xz")
md5sums=(`curl -Ls $source.md5 | cut -d " " -f1`)
# sha256sums=(`curl -s $source.sha256 | cut -d " " -f1`)

build() {
  cd vlc-$_dlver

  sed -i -e 's:truetype/freefont:TTF:g' modules/text_renderer/freetype.c

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-rpath \
              --enable-aa \
              --enable-faad \
              --enable-lirc \
              --enable-ncurses \
              --enable-nls \
              --enable-pvr \
              --enable-realrtsp \
              --enable-upnp \
              --enable-vcdx \
              --enable-xosd
  make
}

package() {
  cd vlc-$_dlver

  make DESTDIR="$pkgdir" install

  for i in 16x16 32x32 48x48 128x128; do
    install -Dm644 share/icons/$i/vlc.png "$pkgdir/usr/share/icons/hicolor/$i/apps/vlc.png"
  done
}