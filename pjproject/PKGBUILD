# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Marti Raudsepp <marti@juffo.org>
#Contributor: Travis Hegner <travis.hegner@gmail.com>

pkgname=pjproject
pkgver=2.0.1
pkgrel=1
pkgdesc="Open source SIP stack and media stack"
arch=('i686' 'x86_64')
url="http://www.pjsip.org/"
license=('GPL')
depends=('openssl' 'portaudio' 'speex' 'alsa-lib' 'libsamplerate' 'util-linux-ng' 'ffmpeg')
makedepends=('e2fsprogs' 'python')
install=pjproject.install
source=(http://www.pjsip.org/release/${pkgver}/pjproject-${pkgver}.tar.bz2)
md5sums=('69e68612deaa3c760ec6aac46968c95a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p third_party/libsamplerate/src
  cp /usr/include/samplerate.h third_party/libsamplerate/src
  echo "#define PJMEDIA_RESAMPLE_IMP PJMEDIA_RESAMPLE_LIBSAMPLERATE" >> pjlib/include/pj/config_site.h
  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr --with-external-speex --with-external-pa --enable-epoll --disable-oss --enable-libsamplerate
  make -j1 dep
  make -j1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR=${pkgdir} install
  install -D -m755 pjsip-apps/bin/pjsua-*gnu ${pkgdir}/usr/bin/pjsua
}
