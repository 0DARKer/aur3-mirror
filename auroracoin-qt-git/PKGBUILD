# Maintainer: Daniel Spies <daniel dot spies at fuceekay dot com>

pkgname=auroracoin-qt-git
_gitname=auroracoin-project
pkgver=0.0.1
pkgrel=2
pkgdesc="Cryptocurrency for Iceland (GUI)"
arch=('x86_64' 'i686')
url="http://www.auroracoin.org/"
license=('MIT')
provides=('auroracoin-qt')
conflicts=('auroracoin-qt')
depends=('qt4' 'miniupnpc' 'boost-libs' 'auroracoind-git')
makedepends=('git' 'boost' 'gcc' 'make')
source=("git+https://github.com/baldurodinsson/${_gitname}.git"
        "auroracoin.desktop")
install=auroracoin.install
sha256sums=(SKIP 
            'e5268351a185aa91b7cb696bca7a375428a368f8657c922556f89e1c907ece8f')

## files & commands for building
_qmake=qmake-qt4

pkgver() {
	cd ${_gitname}
	git describe --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd ${_gitname}
	${_qmake}
	make $MAKEFLAGS
}

package() {
	install -Dm644 auroracoin.desktop ${pkgdir}/usr/share/applications/auroracoin.desktop

	cd ${_gitname}
	install -Dm755 auroracoin-qt ${pkgdir}/usr/bin/auroracoin-qt
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
	install -Dm644 src/qt/res/icons/bitcoin.png ${pkgdir}/usr/share/pixmaps/auroracoin256.png
}

