# Maintainer: Your Name <stef204 AT(remove_this) yandex DOT(remove_this) com>
pkgbase=masshash
pkgname=('masshash' 'masshash-launcher')
pkgver=1.0.0
pkgrel=1
pkgdesc="Set of file hashing tools"
arch=('any')
url="https://github.com/jdleicher/MassHash"
license=('GPL3')
# https://github.com/jdleicher/MassHash/archive/1.0.0.tar.gz
source=("https://github.com/jdleicher/MassHash/archive/$pkgver.tar.gz"
        "masshash-launcher.desktop")
md5sums=('9e9ccf3228841416045c8d599f68239b'
         '5dd1c93f1e636c24fe1d9d42eeaf0568')

package_masshash() {
	depends=('python')

	cd "$srcdir/MassHash-$pkgver"
	install -m755 -d "$pkgdir/usr/bin"
	install -m755 -d "$pkgdir/usr/share/doc/mashhash"
	install -m755 python3/masshash "$pkgdir/usr/bin"
	install -m644 README.md "$pkgdir/usr/share/doc/mashhash/"
	install -m644 AUTHORS "$pkgdir/usr/share/doc/mashhash/"
}

package_masshash-launcher() {
	depends=('masshash' 'python-gobject' 'gtk3' 'hicolor-icon-theme')
	install=masshash-launcher.install

	cd "$srcdir/MassHash-$pkgver"
	install -m755 -d "$pkgdir/usr/bin"
	install -m755 -d "$pkgdir/usr/share/applications"
	install -m755 -d "$pkgdir/usr/share/masshash-launcher"
	install -m755 -d "$pkgdir/usr/share/icons/hicolor/scalable/apps"
	install -m755 python3/masshash-launcher "$pkgdir/usr/bin/"
	install -m644 data/masshash-launcher.glade "$pkgdir/usr/share/masshash-launcher/"
	install -m644 data/masshash-launcher.svg "$pkgdir/usr/share/masshash-launcher/"
	install -m644 data/masshash-launcher.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/"
	install -m644 "$srcdir/masshash-launcher.desktop" "$pkgdir/usr/share/applications/"
}
