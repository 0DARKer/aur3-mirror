# Maintainer:  Johannes Schlatow <johannes.schlatow@googlemail.com> 
# Contributor: igel

pkgname=ziboptsuite
pkgver=2.1.0
pkgrel=1
pkgdesc="The ZIB Optimization Suite is a tool for generating and solving mixed integer programs. Consists of ZIMPL, SoPlex and SCIP."
arch=('i686' 'x86_64')
url="http://zibopt.zib.de/"
license=('LGPL3' 'custom:ZIB Academic License')
depends=('zlib' 'gmp' 'readline')
provides=('scip=2.1.0' 'soplex=1.6.0' 'zimpl=3.2.0')
source=(http://zibopt.zib.de/download/$pkgname-$pkgver.tgz)
sha256sums=('af8282d8cfca2860225c6b8d64da061ef942bd5a9e9f58b09505e6dcb462cafd')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	make || return 1
	make test || return 1
}

package() {
	# Path names of the package components
	local _scip='scip-2.1.0'
	local _soplex='soplex-1.6.0'
	local _zimpl='zimpl-3.2.0'

	cd $srcdir/$pkgname-$pkgver

	# Some files have permission 640
	# @FIXME: Future versions might not require this line
	chmod -R a+r .

	# Binaries
	install -D -m755 $_scip/bin/scip $pkgdir/usr/bin/scip
	install -D -m755 $_soplex/bin/soplex $pkgdir/usr/bin/soplex
	install -D -m755 $_zimpl/bin/zimpl $pkgdir/usr/bin/zimpl

	# Libraries
	mkdir -p $pkgdir/usr/lib
	cp -d $_scip/lib/libscip* $pkgdir/usr/lib/
	cp -d $_soplex/lib/* $pkgdir/usr/lib/
	cp -d $_zimpl/lib/* $pkgdir/usr/lib/

	# Includes
	for dir in blockmemshell dijkstra nlpi objscip scip tclique xml; do
		mkdir -p $pkgdir/usr/include/scip/$dir
		install -D -m644 $_scip/src/$dir/*.h $pkgdir/usr/include/scip/$dir/
	done

	mkdir -p $pkgdir/usr/include/{soplex,zimpl}
	cp $_soplex/src/*.h $pkgdir/usr/include/soplex/
	cp $_zimpl/src/*.h $pkgdir/usr/include/zimpl/

	# Documentation
	mkdir -p $pkgdir/usr/share/doc/ziboptsuite/{scip,soplex,zimpl}
	cp COPYING $pkgdir/usr/share/doc/ziboptsuite/
	cp -r $_scip/{CHANGELOG,COPYING,release-notes,doc} $pkgdir/usr/share/doc/ziboptsuite/scip/
	cp -r $_soplex/{CHANGELOG,COPYING,doc} $pkgdir/usr/share/doc/ziboptsuite/soplex/
	cp -r $_zimpl/{CHANGELOG,LICENSE,README,doc,example} $pkgdir/usr/share/doc/ziboptsuite/zimpl/

	# License
	mkdir -p $pkgdir/usr/share/licenses/$pkgname
	cp COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
