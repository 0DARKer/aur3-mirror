# Maintainer: Sial <NAME at cpan dot org>

_perlmod=Smart-Comments
pkgname=perl-smart-comments
pkgver=1.0.4
pkgrel=2
pkgdesc="Comments that do more than just sit there"
arch=(any)
url="http://search.cpan.org/~chorny/${_perlmod}-${pkgver}/"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.8.1')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/C/CH/CHORNY/${_perlmod}-${pkgver}.tar.gz)
md5sums=('4f7a229e54ee067f3aaffb3338bfa4eb')

build() {
  cd "${srcdir}/${_perlmod}-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd ${_perlmod}-${pkgver}
  make test
}

package() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  make install DESTDIR="${pkgdir}/"
}

# vim:set ts=2 sw=2 et:
