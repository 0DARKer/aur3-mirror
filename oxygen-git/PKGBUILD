# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=oxygen-git
pkgver=r3652.f58aa1d
pkgrel=1
pkgdesc='KDE Oxygen style'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('frameworkintegration-git' 'kwin-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
optdepends=("oxygen-kde4-git: Oxygen widget style for KDE4 applications")
conflicts=('oxygen' 'kde-workspace<0')
provides=('oxygen')
source=('git://anongit.kde.org/oxygen.git')
md5sums=('SKIP')

pkgver() {
  cd oxygen
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../oxygen \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
