# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=oxygen-git
pkgver=r3469.cf1bdd6
pkgrel=1
pkgdesc='KDE Oxygen style'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('frameworkintegration-git' 'kwin-git')
makedepends=('extra-cmake-modules' 'git' 'kf5umbrella-git')
conflicts=('oxygen' 'kde-workspace<0')
provides=('oxygen')
source=('git://anongit.kde.org/oxygen.git')
md5sums=('SKIP')

pkgver() {
  cd oxygen
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../oxygen/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
