# Contributor: mosra <mosra@centrum.cz>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=kdevelop-pg-qt-git
pkgver=r591.83be916
pkgrel=1
pkgdesc="KDevelop parser generator - Git build"
arch=(i686 x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
groups=(kde)
depends=(kdelibs)
makedepends=(cmake automoc4 git)
provides=(kdevelop-pg-qt)
conflicts=(kdevelop-pg-qt)
source=('git://anongit.kde.org/kdevelop-pg-qt')
sha512sums=('SKIP')

pkgver() {
    cd kdevelop-pg-qt
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    mkdir -p build
    cd build

    cmake ../kdevelop-pg-qt \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib

    make
}

package() {
    cd build

    make DESTDIR="${pkgdir}" install
}
