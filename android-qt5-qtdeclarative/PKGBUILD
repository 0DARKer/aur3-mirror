# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=android-qt5-qtdeclarative
pkgver=5.1.0_rc1
pkgrel=1
pkgdesc="Qt 5 for Android: qtdeclarative module (QtQuick 2, QML)"
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/Qt5ForAndroid"
license=('LGPL')
groups=('qt-android' 'qt5-android')
depends=('android-qt5-qtbase' 'android-qt5-qtxmlpatterns' 'android-qt5-qtjsbackend')
makedepends=('gcc')
options=('!libtool' '!strip')
_pkgver="${pkgver/_/-}"
_pkgfqn="qtdeclarative-opensource-src-${_pkgver}"
source=("http://download.qt-project.org/development_releases/qt/5.1/${_pkgver}/submodules/${_pkgfqn}.tar.xz")
md5sums=('bb352ea7730b0c4b38f7c677ba0ba6bc')

build() {
  cd ${srcdir}/${_pkgfqn}
  
  /opt/android-qt5/${_pkgver}/bin/qmake
  make
}

package() {
  cd ${srcdir}/${_pkgfqn}
  
  make INSTALL_ROOT="${pkgdir}" install
    
  # Strip by hand, system strip doesn't work on arm binaries

  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/${_pkgver}/lib/*.so* \
    ${pkgdir}/opt/android-qt5/${_pkgver}/plugins/qmltooling/*.so \
    ${pkgdir}/opt/android-qt5/${_pkgver}/qml/*/*.so \
    ${pkgdir}/opt/android-qt5/${_pkgver}/qml/*/*/*.so \
    ${pkgdir}/opt/android-qt5/${_pkgver}/qml/*/*/*/*.so
  # linux static libs in lib
  strip ${STRIP_STATIC} ${pkgdir}/opt/android-qt5/${_pkgver}/lib/*.a
}
