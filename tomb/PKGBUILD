# Maintainer: Rich Li <rich at dranek com>
# Contributor: nignux <nignux@freaknet.org>
# Contributor: fauno <fauno@parabola.nu>
# Contributor: BoySka

pkgbase=tomb
pkgname=tomb
true && pkgname=(tomb tomb-kdf)
pkgver=1.5.2
pkgrel=1
pkgdesc="Crypto Undertaker, a simple tool to manage encrypted storage"
arch=('i686' 'x86_64')
url="http://www.dyne.org/software/tomb/"
license=('GPL3')
install=${pkgname}.install
source=(https://files.dyne.org/.xsend.php?file=tomb/Tomb-${pkgver}.tar.gz
        https://files.dyne.org/.xsend.php?file=tomb/Tomb-${pkgver}.tar.gz.sha
        https://files.dyne.org/.xsend.php?file=tomb/Tomb-${pkgver}.tar.gz.sha.asc
        )
# Alternate source for the tarball:
# https://github.com/dyne/Tomb/archive/v1.5.2.tar.gz

# The first hash comes from the .sha file
sha256sums=('6ae28aaec7b533127534070b25ccba08eb1416dc07e7e74bf433f5c5d675f7e2'
            'SKIP'
            'SKIP')
# The public key is found at http://jaromil.dyne.org/contact
# gpg --recv-keys 0x73b35da54acb7d10

build() {
  cd ${srcdir}/Tomb-${pkgver}/extras/kdf-keys
  make
}

# The checks require root access
#check() {
#  cd ${srcdir}/Tomb-${pkgver}
#  make test
#}

package_tomb() {
  pkgdesc="Crypto Undertaker, simple tool to manage encrypted storage, from the hashes of the dyne:bolic nesting mechanism."
  depends=('bc' 'cryptsetup' 'gnupg' 'sudo' 'zsh' 'wipe')
  optdepends=(
  'steghide: steganography'
  'dcfldd: show nice progress during massive I/O'
  )
  arch=('any')

  cd "${srcdir}/Tomb-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}

package_tomb-kdf() {
  pkgdesc="Crypto Undertaker extensions to improve password security"
  depends=('libgcrypt')

  cd "${srcdir}/Tomb-${pkgver}/extras/kdf-keys"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}
