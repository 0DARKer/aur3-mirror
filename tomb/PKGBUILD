# Author jaromil <jaromil@dyne.org
# Maintainer: nignux <nignux@freaknet.org>

pkgname=tomb
pkgver=20110427
pkgrel=1
pkgdesc="Crypto Undertaker, simple tool to manage encrypted storage, from the hashes of the dyne:bolic nesting mechanism."
arch=('i686' 'x86_64')
url="http://tomb.dyne.org/"
license=('GPL')
depends=('bc' 'cryptsetup' 
         'gnupg' 'gtk2>=2.16' 
         'libnotify' 'pinentry' 
         'sudo' 'zsh')
makedepends=('git')
optdepends=('steghide: steganography support' 
            'dcfldd: dd hashing support'
            'wipe: secure remove support')
install=${pkgname}.install
provides=('tomb')
 
_gitroot="git://code.dyne.org/tomb.git"
_gitname="tomb"
 
build() {
  cd ${srcdir}
  msg "Connecting to GIT server...."
 
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done"
  msg "Starting make..."
  
  cd ${srcdir}/${_gitname}
 
  autoreconf -i
  ./configure --prefix=/usr
  make
}
 
package() {
  cd ${srcdir}/${_gitname}
  make DESTDIR=${pkgdir} install
}
