# Maintainer: Rich Li <rich at dranek com>
# Contributor: nignux <nignux@freaknet.org>
# Contributor: fauno <fauno@parabola.nu>
# Contributor: BoySka

pkgbase=tomb
pkgname=(tomb tomb-kdf)
pkgver=1.5.2
pkgrel=2
pkgdesc="Crypto Undertaker, a simple tool to manage encrypted storage"
arch=('i686' 'x86_64')
url="http://www.dyne.org/software/tomb/"
license=('GPL3')
install=${pkgname}.install
source=(
        https://releases.dyne.org/tomb/Tomb-${pkgver}.tar.gz
        https://releases.dyne.org/tomb/Tomb-${pkgver}.tar.gz.sha
        https://releases.dyne.org/tomb/Tomb-${pkgver}.tar.gz.sha.asc
        )

# The first hash comes from the .sha file
sha256sums=('6ae28aaec7b533127534070b25ccba08eb1416dc07e7e74bf433f5c5d675f7e2'
            'd0dba7e833dbbcd30662c5c3ae173ad96800b038ee7dc9b23a73a5488883382f'
            'SKIP')
# The public key is found at http://jaromil.dyne.org/contact
# gpg --recv-keys 0x73b35da54acb7d10

build() {
  cd ${srcdir}/Tomb-${pkgver}/extras/kdf-keys
  make
}

# The checks require root access
#check() {
#  cd ${srcdir}/Tomb-${pkgver}
#  make test
#}

package_tomb() {
  depends=('bc' 'cryptsetup' 'gnupg' 'sudo' 'zsh' 'e2fsprogs')
  optdepends=(
  'steghide: steganography'
  'dcfldd: show nice progress during massive I/O'
  'wipe: secure file deletion'
  'qrencode: for paper backups of keys'
  'swish-e: file content indexer'
  )
  arch=('any')

  cd "${srcdir}/Tomb-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}

package_tomb-kdf() {
  pkgdesc="Crypto Undertaker extensions to improve password security"
  depends=('libgcrypt')

  cd "${srcdir}/Tomb-${pkgver}/extras/kdf-keys"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}
