## PKGBUILD [bash]
# Maintainer: nignux <nignux@freaknet.org>

pkgbase=tomb
pkgname=tomb
true && pkgname=(tomb python2-tomb tomb-gui)
pkgver=1.4
pkgrel=1
pkgdesc="Crypto Undertaker, simple tool to manage encrypted storage"
arch=('i686' 'x86_64' 'mips64el')
url="http://www.dyne.org/software/tomb/"
license=('GPL3')
depends=('bc' 'cryptsetup' 'gnupg' 'sudo' 'zsh' 'steghide'
         'dcfldd' 'wipe')
makedepends=('python2-pyqt' 'python2-distribute')
install=${pkgname}.install
source=(https://files.dyne.org/tomb/releases/Tomb-${pkgver}.tar.gz{,.sha{,.asc}})
# The first hash comes from the .sha file
sha256sums=('2621ac6b9180321e69743dc899645449b2b958c6aa46e4b2601c2e89131bbf29'
            'fc8b168f991d5b9c1135ceec6e55ac94f494ac4bed7e104e14c7f9ee787591e5'
            'SKIP')

build() {
  cd ${srcdir}/Tomb-${pkgver}/extras/qt

  python2 setup.py build_ui
}

# The checks require root access
#check() {
#  cd ${srcdir}/Tomb-${pkgver}
#  make test
#}

package_tomb() {
  pkgdesc="Crypto Undertaker, simple tool to manage encrypted storage, from the hashes of the dyne:bolic nesting mechanism."
  cd ${srcdir}/Tomb-${pkgver}
  make DESTDIR=${pkgdir} install
}

package_python2-tomb() {
  pkgdesc="Crypto Undertaker library"

  cd ${srcdir}/Tomb-${pkgver}/extras/pytomb
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

package_tomb-gui() {
  pkgdesc="Crypto Undertaker GUI for open and create tombs"
  depends=('python2-tomb' 'python2-pyqt')

  cd ${srcdir}/Tomb-${pkgver}/extras/qt
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}
pkgdesc="Crypto Undertaker, simple tool to manage encrypted storage"
depends=('bc' 'cryptsetup' 'gnupg' 'sudo' 'zsh' 'steghide'
         'dcfldd' 'wipe')
