# Contributor: Berkus <berkus@madfire.net>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Gimmeapill <gimmeapill at gmail com>

pkgname=armstrong-hg
pkgver=376
pkgrel=1
pkgdesc="A Buzz-like tracker library"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://code.google.com/p/armstrong"
makedepends=('boost' 'mercurial' 'scons')
depends=('fftw' 'jack' 'liblo' 'python2')
provides=('armstrong')
conflicts=('libneil')

_hgroot="http://bitbucket.org/paniq"
_hgrepo=armstrong

build() {

  cd "$srcdir"
  msg "Connecting to Mercurial server...."
  if [ -d $_hgrepo ]; then
    cd $_hgrepo
    hg pull -u
    msg "The local files are updated."
  else
    hg clone $_hgroot/$_hgrepo $_hgrepo
  fi

  msg "Mercurial clone done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  # Python2 fixes
  export PYTHON="python2"
  sed \
	-e "s_\(env python\).*_\12_" \
	-e "s_\(bin/python\).*_\12_" \
	-i `grep -rlE "(env python|bin/python)" .`
  sed -i "s_'python_&2_" SConstruct

  # Build

  scons configure PREFIX=/usr DESTDIR="$pkgdir/"
  scons
}

package() {

  cd "$srcdir/$_hgrepo-build"

  scons install
}
