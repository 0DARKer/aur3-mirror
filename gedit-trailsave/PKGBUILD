# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=gedit-trailsave
pkgver=3.1.2
pkgrel=2
pkgdesc="A plugin for gedit 3 to strip trailing whitespace on save"
arch=('any')
url="https://github.com/jonleighton/gedit-trailsave"
license=('GPL')
depends=('gedit')
source=($pkgname-$pkgver.tar.gz::https://github.com/jonleighton/$pkgname/tarball/v$pkgver
        gedit-plugin-3.1.2-use-python3-loader.patch)
sha256sums=('7517c3a5da4e5adc2bba6d39c7c867b2ef379a386eafc58de67ed4173ca2cbd4'
            'f74049c50cc7985e17e857f33fcc9daeb1dd939e341de2c4ec49851cb6cf6d1e')

prepare() {
  cd "$srcdir/jonleighton-$pkgname"*

  patch -Np1 -i "$srcdir/gedit-plugin-3.1.2-use-python3-loader.patch"
}

package() {
  cd "$srcdir/jonleighton-$pkgname"*

  install -d "$pkgdir/usr/lib/gedit/plugins"
  install -Dm644 -t "$pkgdir/usr/lib/gedit/plugins/" trailsave.*

  # Compile Python scripts
  python -m compileall "$pkgdir"
  python -O -m compileall "$pkgdir"
}

# vim:set ts=2 sw=2 et:
