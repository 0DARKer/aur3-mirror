# Maintainer: Emanuel Couto <emanuel.amaral.couto@gmail.com>
# Contributor: Richard Jackson <rdjack21@gmail.com>
pkgname='openjfx'
pkgver='8u40_b14'
pkgrel=1
pkgdesc='An open source, next generation client application platform for desktop and embedded systems based on JavaSE'
arch=('x86_64')
url='https://wiki.openjdk.java.net/display/OpenJFX/Main'
license=('GPL')
depends=('jdk8-openjdk')
makedepends=('mercurial' 'gradle-1.8')
source=('openjfx::hg+http://hg.openjdk.java.net/openjfx/8u-dev/rt#tag=8u40-b14'
        'gradle.properties'
        'java_options.patch')
md5sums=('SKIP'
         '938e69f3a9ba05c357096ede947c56bd'
         '4f15de8762e79c2d7413f622c29e738c')

prepare() {
  # Apply patches
  cd "${srcdir}/${pkgname}"
  patch -p1 < "${srcdir}/java_options.patch"
}

build() {
  # Links the properties file
  ln -s "${srcdir}/gradle.properties" "${srcdir}/${pkgname}"

  # Build openjfx
  cd "$srcdir/$pkgname"
  gradle-1.8 sdkLinux
}

package() {
  local openjdk8dir="usr/lib/jvm/java-8-openjdk"
  local builddir="${srcdir}/${pkgname}/build"
  local sdkdir="${builddir}/sdk"

  mkdir -p "${pkgdir}/${openjdk8dir}/jre/lib/amd64"
  mkdir -p "${pkgdir}/${openjdk8dir}/jre/lib/ext"
  mkdir -p "${pkgdir}/${openjdk8dir}/jre/lib"
  mkdir -p "${pkgdir}/${openjdk8dir}/lib"
  mkdir -p "${pkgdir}/${openjdk8dir}/bin"
  mkdir -p "${pkgdir}/${openjdk8dir}/man/man1"

  install -m755 "${sdkdir}/rt/lib/amd64"/*.* "${pkgdir}/${openjdk8dir}/jre/lib/amd64"
  install -m644 "${sdkdir}/rt/lib/ext"/*.* "${pkgdir}/${openjdk8dir}/jre/lib/ext"
  install -m644 "${sdkdir}/rt/lib"/*.* "${pkgdir}/${openjdk8dir}/jre/lib"
  install -m644 "${sdkdir}/lib"/*.* "${pkgdir}/${openjdk8dir}/lib"
  install -m755 "${sdkdir}/bin"/* "${pkgdir}/${openjdk8dir}/bin"

  install -m644 "${sdkdir}/man/man1/javapackager.1" "${pkgdir}/${openjdk8dir}/man/man1"
  gzip "${pkgdir}/${openjdk8dir}/man/man1/javapackager.1"
}
