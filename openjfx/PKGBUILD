# Maintainer: Emanuel Couto <emanuel dot amaral dot couto at gmail dot com>
# Contributor: Richard Jackson <rdjack21 at gmail dot com>
# Contributor: Tinx <arch at tinx dot eu>
pkgname=('openjfx' 'openjfx-doc' 'openjfx-src')
pkgver='8u40_b18'
pkgrel=1
pkgdesc='OpenJFX 8 libraries'
arch=('x86_64')
url='https://wiki.openjdk.java.net/display/OpenJFX/Main'
license=('GPL')
depends=('jdk8-openjdk')
makedepends=('mercurial' 'gradle-1.8' 'bison' 'gperf' 'qtchooser')
source=('openjfx::hg+http://hg.openjdk.java.net/openjfx/8u-dev/rt#tag=8u40-b18'
        'gradle.properties'
        'java_options.patch')
md5sums=('SKIP'
         '5aefb7fde3f33f809731af76b37c58b4'
         '4f15de8762e79c2d7413f622c29e738c')

prepare() {
  # Apply patches
  cd "${srcdir}/${pkgname}"
  patch -p1 < "${srcdir}/java_options.patch"
}

build() {
  # Links the properties file
  ln -s "${srcdir}/gradle.properties" "${srcdir}/${pkgname}"

  # Build openjfx
  cd "$srcdir/$pkgname"
  gradle-1.8
}

package_openjfx() {
  local openjdk8dir="usr/lib/jvm/java-8-openjdk"
  local builddir="${srcdir}/${pkgname}/build"
  local sdkdir="${builddir}/sdk"

  install -d "${pkgdir}/${openjdk8dir}/jre/lib/amd64"
  install -d "${pkgdir}/${openjdk8dir}/jre/lib/ext"
  install -d "${pkgdir}/${openjdk8dir}/lib"
  install -d "${pkgdir}/${openjdk8dir}/bin"
  install -d "${pkgdir}/${openjdk8dir}/man/man1"
  
  install -m755 "${sdkdir}/rt/lib/amd64"/*.* "${pkgdir}/${openjdk8dir}/jre/lib/amd64"
  install -m644 "${sdkdir}/rt/lib/ext"/*.* "${pkgdir}/${openjdk8dir}/jre/lib/ext"
  install -m644 "${sdkdir}/rt/lib"/*.* "${pkgdir}/${openjdk8dir}/jre/lib"
  install -m644 "${sdkdir}/lib"/*.* "${pkgdir}/${openjdk8dir}/lib"
  install -m755 "${sdkdir}/bin"/* "${pkgdir}/${openjdk8dir}/bin"
  install -m644 "${sdkdir}/man/man1/javapackager.1" "${pkgdir}/${openjdk8dir}/man/man1"
}

package_openjfx-doc() {
  local docdir="usr/share/doc"
  local builddir="${srcdir}/openjfx/build"

  pkgdesc="OpenJFX 8 documentation"

  install -d "${pkgdir}/${docdir}/openjfx"
  cp -dr --no-preserve=ownership "${builddir}/javadoc"/* "${pkgdir}/${docdir}/openjfx"
}

package_openjfx-src() {
  local openjdk8dir="usr/lib/jvm/java-8-openjdk"
  local builddir="${srcdir}/openjfx/build"

  pkgdesc="OpenJFX 8 sources"

  install -d "${pkgdir}/${openjdk8dir}"
  install -m644 "${builddir}/javafx-src.zip" "${pkgdir}/${openjdk8dir}"
}
