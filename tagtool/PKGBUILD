# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmannt-online.de>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Filip Wojciechowski <filip at loka dot pl>

pkgname=tagtool
pkgver=0.12.3
pkgrel=7
pkgdesc='Tool to edit tags in MP3 and Ogg Vorbis files'
url='http://tagtool.sourceforge.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('intltool')
depends=('hicolor-icon-theme' 'libglade' 'id3lib' 'libvorbis' 'gconf')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2"
        'libvorbis-libm.patch')
sha1sums=('e7ed4ebf8f9881cf38b2340b18e95947f658308e'
          '9e0dfb4c1563ca1d8822f1dfa161915b0f157380')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../libvorbis-libm.patch
	mkdir m4; autoreconf -fis
	./configure --prefix=/usr --datadir=/usr/share 
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install 
	install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
	gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
		--domain tagtool "${pkgdir}"/etc/gconf/schemas/*.schemas 
	rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas 
}
