# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

_pkgname=xattr
pkgname=python2-$_pkgname
pkgver=0.6.4
pkgrel=2
pkgdesc="Module for manipulating filesystem extended attributes"
arch=(i686 x86_64)
url=http://undefined.org/python/#xattr
license=(MIT)
depends=(python2)
makedepends=(python2-distribute)
source=(https://github.com/xattr/$_pkgname/archive/v$pkgver.zip)
sha256sums=('0f30bab20e3cbfc3971c51a53ec2cd767cf8a8b0b12c15fc6b4094fa4127fffa')
sha512sums=('438fe7f5ca3e38756ec2b3c53c472f409f0a690bab14a2f24466d00b8093159cdf1bf918556a7351267ced00b87009f8a1b8d17b9102067e4c03e47600be6586')

prepare() {
    sed -ri 's:^#!/usr/bin/(env )?python$:&2:' $_pkgname-$pkgver/$_pkgname/tool.py
}

build() {
    cd $_pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $_pkgname-$pkgver/
    python2 setup.py install --prefix=/usr --root="$pkgdir"
    install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
