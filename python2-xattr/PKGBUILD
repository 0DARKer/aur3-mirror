# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=python2-xattr
pkgver=0.7.4
pkgrel=1
pkgdesc="Module for manipulating filesystem extended attributes"
arch=(i686 x86_64)
url=https://pypi.python.org/pypi/xattr/
license=(MIT)
depends=(python2)
makedepends=(python2-setuptools)
source=(http://pypi.python.org/packages/source/x/${pkgname#*-}/${pkgname#*-}-$pkgver.tar.gz)
sha256sums=('16c612ac01b9b5a31967e231c7f7d32b7263f151d5e3c617c69378170054805f')
sha512sums=('c86963be781f3c690aac31491101e954cf743939c89e0dc32e8aa36aee8e750aa02200e2c816fd5163273ace9ba57780103741d1e47c3edc5ff6828da4328f7a')

prepare() {
    sed -ri 's:^#!/usr/bin/(env )?python$:&2:' ${pkgname#*-}-$pkgver/${pkgname#*-}/tool.py
}

build() {
    cd ${pkgname#*-}-$pkgver/
    python2 setup.py build
}

package() {
    cd ${pkgname#*-}-$pkgver/
    python2 setup.py install --prefix=/usr --root="$pkgdir"
    install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
