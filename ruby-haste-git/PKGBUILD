# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgroot=ruby-haste
pkgname=ruby-haste-git
pkgver=latest
pkgrel=2
pkgdesc='Simple client for uploading text to a haste-server (hastebin) (development version)'
url='https://github.com/seejohnrun/haste-client'
license=('MIT')
source=("${pkgroot}::git+https://github.com/seejohnrun/haste-client.git")
sha1sums=('SKIP')
arch=('any')
depends=('ruby' 'ruby-json')
makedepends=('git')
conflicts=('ruby-haste')
provides=('ruby-haste')

pkgver() {
  cd "${srcdir}/${pkgroot}"

  # Get the version number.
  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgroot}"

  # Build it!
  gem build haste.gemspec
}

package() {
  cd "${srcdir}/${pkgroot}"

  # Install the program.
  gem install --no-user-install --ignore-dependencies \
    -i "${pkgdir}$(ruby -e 'puts Gem.default_dir')" \
    -n "${pkgdir}/usr/bin" \
    haste-*.gem
}

# vim: ft=sh ts=2 sw=2 et
