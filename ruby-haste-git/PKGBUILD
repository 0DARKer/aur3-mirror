# Maintainer: neersighted <n@neersighted.com>
# Contributor: Matt Harrison <matt at mistbyte dot com>

pkgname=ruby-haste-git
pkgver=latest
pkgrel=1
pkgdesc='Simple client for uploading text to a haste-server (hastebin) (development version)'
url='https://github.com/seejohnrun/haste-client'
license=('MIT')
source=('git://github.com/seejohnrun/haste-client.git')
md5sums=('SKIP')
sha1sums=('SKIP')
sha256sums=('SKIP')
arch=('any')
depends=('ruby' 'ruby-json')
makedepends=('git' 'ruby')
conflicts=('ruby-haste')
provides=('ruby-haste')

pkgver() {
  cd ${srcdir}/haste-client

  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd ${srcdir}/haste-client

  # Build it!
  gem build haste.gemspec
}

package() {
  cd ${srcdir}/haste-client

  # Install the program.
  gem install --no-user-install --ignore-dependencies \
    -i ${pkgdir}$(ruby -e 'puts Gem.default_dir') \
    -n ${pkgdir}/usr/bin haste
}

# vim:set ts=2 sw=2 et:
