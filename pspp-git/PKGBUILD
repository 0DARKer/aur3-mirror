# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Edited for git by: ilikenwf <parwok@gmail.com>
# Contributor: ilikenwf/Matt Parnell <parwok@gmail.com>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>

pkgname=pspp-git
pkgver=20130321
pkgrel=1
pkgdesc="Statistical analysis program"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/pspp/"
license=('GPL3')
depends=('gsl' 'gtksourceview2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('perl' 'gperf' 'git')
install=pspp.install
options=('!libtool')

_gitroot="git://git.sv.gnu.org/pspp.git"
_gitname="pspp"
_gitroot2="git://git.sv.gnu.org/gnulib.git"
_gitname2="gnulib"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

# Please check README.git to see which commit to checkout for gnulib
  cd "$srcdir"
  if [[ -d "$_gitname2" ]]; then
    cd "$_gitname2" && git checkout master && git pull origin
    git checkout f022473fdaf724d84817c4003120b9a38fbf884b
    msg "Gnulib updated."
  else
    git clone "$_gitroot2" "$_gitname2"
    cd "$_gitname2" && git checkout f022473fdaf724d84817c4003120b9a38fbf884b
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"                                                             
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make -f Smake

  ./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--without-libreadline-prefix

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  
  make DESTDIR="$pkgdir" install

  rm -f "$pkgdir/usr/share/info/dir"

  if [[ -x $(which emacs) ]]; then
    emacs -Q --batch --eval '(byte-compile-file "pspp-mode.el")'
    install -d ${pkgdir}/usr/share/emacs/site-lisp/pspp
    install -m 644 -t ${pkgdir}/usr/share/emacs/site-lisp/pspp \
               pspp-mode.el{,c}
  fi
}
