# Maintainer: Maciej Sieczka <msieczka at sieczka dot org>
# Contributor: Maciej Sieczka <msieczka at sieczka dot org>

# Cosmetics: override 2 funtions to get rid of "Retrieving Sources..." and
# "Extracting Sources..." messages - irrelevant when building source fetched
# from SVN:
download_sources() { true;}; extract_sources() { true;}

pkgname='grass64-svn'
# During the build makepkg sets $pkgver to the latest modified revision of
# $_svntrunk.
pkgver=54790
pkgrel=1
pkgdesc='GRASS GIS (stable 6.4.x SVN branch): geospatial data management and analysis, image processing, graphics/maps production, spatial modeling and visualization.'
arch=('i686' 'x86_64')
url='http://grass.osgeo.org/'
license=('GPL')
provides=("$pkgname")

# More about GRASS build and runtime deps on http://grasswiki.osgeo.org/wiki/Compile_and_Install.
depends=('zlib' 'freetype2' 'cfitsio' 'fftw' 'gdal' 'geos' 'glu' 'libjpeg' 'libpng' 'libtiff' 'libxmu' 'mesa' 'python2' 'python2-numpy' 'postgresql' 'proj' 'tcl' 'tk' 'wxpython' 'xorg-server' 'cairo' 'unixodbc')
makedepends=('subversion' 'doxygen')
optdepends=('r: R language interface; see http://grasswiki.osgeo.org/wiki/R_statistics'
            'mysql: mysql database interface'
            'ffmpeg: ffmpeg support'
            'lapack: required for GMATH library'
            'blas: required for GMATH library'
            'lesstif: motif support')

_svntrunk='http://svn.osgeo.org/grass/grass/branches/releasebranch_6_4'
_svnmod="$pkgname"

build() {
  if [ -d "$pkgname" ]; then
    msg 'Reverting local changes in the SVN working copy, if there are any...'
    # Revert modifications of the source code present in version control (e.g.
    # those done during the previous build):
    svn revert --recursive "$pkgname" > /dev/null || (error 'SVN revert failed.'; exit 1)
    # Un-hide everything, including previous build products, to be able to
    # remove them in the next step:
    svn propdel svn:ignore --recursive "$pkgname" > /dev/null
    # Remove what `make distclean' would have, plus anything else not under
    # version control to keep the build dir pristine:
    svn status "$pkgname" | grep '^\?' | sed 's,^.\{8\},,' | xargs rm -rf
    # Bring the original svn:ignore props back, for the sake of it:
    svn revert --recursive "$pkgname" > /dev/null
    msg "Updating from ${_svntrunk}..."
    svn up --revision $pkgver "$pkgname" || (error 'SVN update failed.'; exit 1)
  else
    msg "Checking out from ${_svntrunk}..."
    svn co --revision $pkgver "$_svntrunk" "$pkgname" || (error 'SVN checkout failed.'; exit 1)
  fi

  cd "$pkgname"

  msg 'Patching source...'
  # Tweak the initial PATH so that it advertised a python2->python link as
  # python. No patching to deal with the python/python2/3 issue needed:
  ln -s "`which python2`" "${srcdir}/${pkgname}/python"
  PATH="${srcdir}/${pkgname}:$PATH"
  export PATH
  
  # A patch no longer needed:
  # sed -i '1 s/python/python2/' lib/python/ctypes/ctypesgen.py
  
  # INSTDIR is partly hardcoded in `configure'. Fix it, so that INST_DIR, which
  # is derived from it, is set as needed:
  sed -i "s,INSTDIR='\${prefix}'\"/grass-\${GRASS_VERSION_MAJOR}\.\${GRASS_VERSION_MINOR}\.\${GRASS_VERSION_RELEASE}\",INSTDIR='\${prefix}/${pkgname}'," configure
  # This e.g. prevents ./configure from not telling true about the "Installation directory:".

  msg 'Configuring build...'
  # Enabling only those features which are not enabled by default. Out of the
  # usefull ones, only DWG, MySQL, FFMPEG and Motif are left disabled. LAPACK
  # and BLAS are not used for anything in GRASS anyway:
  CFLAGS="$CFLAGS -Wall" CXXFLAGS="$CXXFLAGS -Wall" ./configure \
    --prefix=/opt \
    --exec-prefix="/opt/$pkgname" \
    --with-cxx \
    --with-cairo \
    --with-freetype \
    --with-freetype-includes=/usr/include/freetype2 \
    --with-geos \
    --with-nls \
    --with-odbc \
    --with-postgres \
    --with-python=/usr/bin/python2-config \
    --with-readline \
    --with-proj-includes=/usr/include \
    --with-proj-libs=/usr/lib \
    --with-proj-share=/usr/share/proj \
    --with-sqlite \
    --with-wxwidgets=/usr/bin/wx-config \
    --with-tcltk=no

  # To provide a usefull stacktrace:
  #
  # CFLAGS="-O0 -ggdb -Wall -Werror-implicit-function-declaration -fexceptions"
  # CXXFLAGS="-O0 -ggdb -Wall -Werror-implicit-function-declaration -fexceptions"
  # options=(!strip)
  # Not sure if -Werror-implicit-function-declaration -fexceptions should really go to CXXFLAGS.
  # Let me know if you know.

  # TK 8.6 is not compatible with GRASS 6.4 and 6.5, until the issue
  # http://trac.osgeo.org/grass/ticket/1843#comment:8 is fixed.

  # According to Glynn Clements of GRASS dev team, --enable-64bit has effect
  # only on AIX, HP-UX, IRIX and Solaris. It's *always* enabled on GNU/Linux
  # if the build platform supports it, no matter what "64bit support:" on the
  # configure output reads.

  msg 'Building...'
  make
  # That used to be `make PYTHON=python2'
}

package() {
  cd "$pkgname"
  # Install GRASS in $pkgir of makepkg's fakeroot env:
  make prefix="${pkgdir}/opt" exec_prefix="${pkgdir}/opt/${pkgname}" INST_DIR="${pkgdir}/opt/${pkgname}" install

  msg 'Patching the build results...'
  # During `make install' several files get a content based on `INST_DIR' and
  # `UNIX_BIN' make vars. It's not possible to avoid this without changing
  # GRASS build system. Some post-install tweaks are needed:
  sed -i "s,${pkgdir},,g" "${pkgdir}/opt/${pkgname}/include/Make/Platform.make" \
                          "${pkgdir}/opt/${pkgname}/include/Make/Grass.make" \
                          "${pkgdir}/opt/${pkgname}/etc/fontcap" \
                          "${pkgdir}/opt/${pkgname}/bin/grass64"

  # Link GRASS exec script and gem binary in /usr/bin under a custom name.
  # This allows e.g. grass64 and grass64-svn be co-installed:
  mkdir -p "${pkgdir}/usr/bin"
  ln -s "/opt/${pkgname}/bin/grass64" "${pkgdir}/usr/bin/grass64-svn"
  ln -s "/opt/${pkgname}/bin/gem64" "${pkgdir}/usr/bin/gem64-svn"

  # Instead of patching GRASS Python scripts to use `python2' and messing with
  # GRASS_PYTHON, link to a correct interpreter from $GISBASE:
  ln -s "`which python2`" "${pkgdir}/opt/${pkgname}/bin/python"

  # Install an ldconfig conf for GRASS libs in /opt to be visible on the
  # system. Arch runs `ldconfig' after install automatically:
  echo "/opt/${pkgname}/lib" > "${srcdir}/${pkgname}.conf"
  install -D -m644 "${srcdir}/${pkgname}.conf" "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"

  # Desktop integration:
  install -D -m644 "${srcdir}/${pkgname}/gui/icons/grass-64x64.png" "${pkgdir}/usr/share/icons/${pkgname}-64x64.png"
  sed -i -e "s,^Name=GRASS GIS\$,Name=GRASS GIS 6.4.x SVN r${pkgver}," \
         -e "s,^Icon=/usr/share/icons/grass-48x48.png\$,Icon=/usr/share/icons/${pkgname}-64x64.png," \
         -e "s,^Exec=grass64\$,Exec=grass64-svn," \
         "${srcdir}/${pkgname}/gui/icons/grass.desktop"
  install -D -m644 "${srcdir}/${pkgname}/gui/icons/grass.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
