# Mantainer: Simone Sclavi <darkhado@gmail.com>
pkgname=lexmark-inkjet-legacy
pkgver=1.0_1
pkgrel=4
pkgdesc='Inkjet drivers for Lexmark printers & multifunction'
arch=('x86_64' 'i686')
url="http://www.lexmark.com"
license=('custom')
depends=('cups' 'java-environment=6')
case "$CARCH" in
    i686) __arch='i386'; md5sums=('d201e48f643d8759d131b4567d340264');;
    x86_64) __arch='amd64'; md5sums=('90d67439b0d8570659a5df1692702525');;
esac
source=("http://downloads.lexmark.com/downloads/cpd/$pkgname-${pkgver/_/-}.${__arch}.deb.sh.tar.gz")
install=lx.install
package() {
    ./$pkgname-${pkgver/_/-}.${__arch}.deb.sh --noexec --target lexmark-printer-drivers
    cd lexmark-printer-drivers

    mkdir -p $pkgdir/usr/share/licenses/$pkgname
    cp files_extra/license* $pkgdir/usr/share/licenses/$pkgname

    tar xf instarchive_all --lzma

    mkdir lexmark-inkjet-legacy
    ar x $pkgname-${pkgver/_/-}.${__arch}.deb
    tar xf data.tar.gz -C $pkgname
    ar x lexmark-legacy-wsu-${pkgver/_/-}.${__arch}.deb
    tar xf data.tar.gz -C $pkgname

    if [ "$CARCH" == i686 ]; then
        rm -rf ${pkgname}/usr/local/lexmark/legacy/lib64
        rm -rf ${pkgname}/usr/local/lexmark/wsu_legacy/lib64
    fi

    cd ${pkgname}/usr/local/lexmark/legacy/etc/
    mkdir -p $pkgdir/usr/share/cups/model/Lexmark
    cp *.ppd $pkgdir/usr/share/cups/model/Lexmark
    cd ../../..
    mkdir -p $pkgdir/usr/share/lexmark
    cp -a lexmark $pkgdir/usr/share
    cd lexmark/legacy/bin
    install -Dm755 lxhcp $pkgdir/usr/lib/cups/backend/lxhcp
    
    mkdir -p $pkgdir/usr/bin
    mkdir -p $pkgdir/etc/udev/rules.d
    mkdir -p $pkgdir/etc/dbus-1/session.d
    ln -s /usr/share/lexmark/legacy/bin/.scripts/lxtoolbox $pkgdir/usr/bin/lxtoolbox
    ln -s /usr/share/lexmark/legacy/bin/.scripts/postinstall.sh $pkgdir/usr/bin/postinstall.sh  
    ln -s /usr/share/lexmark/legacy/etc/99-lexmarklegacy-10.rules $pkgdir/etc/udev/rules.d/99-lexmarklegacy-10.rules
    ln -s /usr/share/lexmark/wsu_legacy/etc/99-lexmarklegacy-util.rules $pkgdir/etc/udev/rules.d/99-lexmarklegacy-util.rules
    ln -s /usr/share/lexmark/legacy/etc/umf.conf $pkgdir/etc/dbus-1/session.d/umf-legacy.conf
    ln -s /usr/share/lexmark/legacy/lib/liblxknf09hpec.so $pkgdir/usr/share/lexmark/legacy/lib/liblxknf09hpec

    chown -R root:root $pkgdir/usr
    find $pkgdir -type f -exec sed -i 's#/usr/local/#/usr/share/#g' '{}' ';'
}



