# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=lxqt-policykit
pkgname=$_pkgname-git
pkgver=0.6.99
pkgrel=1
pkgdesc="The LXQt policykit authentication agent"
arch=("i686" "x86_64" "armv6h")
url="http://www.lxde.org"
license=("GPL2")
depends=("liblxqt-git" "libqtxdg-git" "polkit-qt")
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git://git.lxde.org/git/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
