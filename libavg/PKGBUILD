# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libavg
pkgver=1.5.4
pkgrel=1
pkgdesc='High-level multimedia platform with a focus on interactive art installations.'
arch=('i686' 'x86_64')
url='http://www.libavg.de'
depends=('graphicsmagick' 'imagemagick' 'ffmpeg' 'boost' 'python2' 'pango')
license=('LGPL')
source=("http://www.libavg.de/raw-attachment/wiki/DownLoad/libavg-${pkgver}.tar.gz")
md5sums=('2eda46f06553cd71d02561415ea4fd2a')

build() {
  cd ${pkgname}-${pkgver}

  # workaround for linking errors, caused by "-Wl,--as-needed" in /etc/makepkg.conf
  unset LDFLAGS

  # fix python2
  find -type f -exec sed -e 's_#!/usr/bin/python_&2_' -e 's_#!/usr/bin/env python_&2_' -i {} \;

  ./bootstrap

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/src/avgrc \
    ${pkgdir}/etc/avgrc
}
