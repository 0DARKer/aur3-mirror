# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libavg
pkgver=1.7.0
pkgrel=1
pkgdesc='High-level multimedia platform with a focus on interactive art installations.'
arch=('i686' 'x86_64')
url='http://www.libavg.de'
depends=('librsvg' 'gdk-pixbuf2' 'libxml2' 'ffmpeg' 'boost-libs' 'python2' 'pango')
optdepends=('libxi' 'libvdpau' 'libdc1394' )
makedepends=(python2)
license=('LGPL')
source=("http://www.libavg.de/raw-attachment/wiki/DownLoad/libavg-${pkgver}.tar.gz")
md5sums=('f88a7978748bb07502c6c050b444be04')

build() {
  cd ${pkgname}-${pkgver}

  # workaround for linking errors, caused by "-Wl,--as-needed" in /etc/makepkg.conf
  unset LDFLAGS

  # fix python2
  find -type f -exec sed -e 's_#!/usr/bin/python_&2_' -e 's_#!/usr/bin/env python_&2_' -i {} \;

  ./bootstrap

  PYTHON=/usr/bin/python2 ./configure \
    --prefix=/usr

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/src/avgrc \
    ${pkgdir}/etc/avgrc
}
