# Maintainer: Thomas Dziedzic < gostrc at gmail >

pkgname=urbi-sdk
pkgver=2.7.1
pkgrel=1
pkgdesc='Urbi is an open-source software platform to control robots or complex systems in general.'
arch=('i686' 'x86_64')
url='http://www.gostai.com/products/urbi/'
license=('AGPL3')
depends=('boost' 'swig1')
#makedepends=('texlive-bin' 'gnuplot' 'texlive-core')
makedepends=('gnuplot')
source=("http://www.gostai.com/downloads/urbi-sdk/${pkgver}/urbi-sdk-${pkgver}.tar.bz2")
md5sums=('4a557dcd0048e42d1fa8283b514a41dd')

build() {
  cd ${pkgname}-${pkgver}

  # update for python2 switch
  find -type f -exec sed -i 's_/usr/bin/python_/usr/bin/python2_' {} \;
  find -type f -exec sed -i 's_/usr/bin/env python_/usr/bin/env python2_' {} \;
  find -type f -exec touch -t 01010101 {} \;

  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2 -Wno-error"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2 -Wno-error"

  ./configure \
    --prefix=/usr \
    --enable-doc=no

  # fix java error http://forum.gostai.com/viewtopic.php?f=26&t=4772&sid=9c3e00b18566dac0cdfe9fc74b22ea8c
  sed \
    -e 's_urbi/SoundUtilities.java__' \
    -e 's_urbi/MedianFilter.java__' \
    -e 's_urbi/SoundSampler.java__' \
    -e 's_urbi/ImageUtilities.java__' \
    -e 's_urbi/UMain.java__' \
    -i sdk-remote/src/swig/java/Makefile.in

  make
} 

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
