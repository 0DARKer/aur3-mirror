# Maintainer: Tom Kuther <tom@kuther.net>

_cfgdir=/etc/nginx
_tmpdir=/var/lib/nginx

pkgname=nginx-naxsi-dav
pkgver=1.2.7
_naxsi_ver=0.50
pkgrel=1
pkgdesc="lightweight HTTP server and IMAP/POP3 proxy server, with Naxsi WAF and dav-ext-module"
arch=('i686' 'x86_64')
depends=('pcre' 'zlib' 'openssl')
makedepends=('passenger' 'git')
optdepends=('passenger' 'nx_util: Naxsi whitelist and reports generator')
url="http://nginx.org"
license=('custom')
backup=(${_cfgdir:1}/fastcgi.conf
	${_cfgdir:1}/fastcgi_params
	${_cfgdir:1}/koi-win
	${_cfgdir:1}/koi-utf
	${_cfgdir:1}/mime.types
	${_cfgdir:1}/nginx.conf
	${_cfgdir:1}/scgi_params
	${_cfgdir:1}/uwsgi_params
	${_cfgdir:1}/win-utf
	etc/logrotate.d/nginx)
source=("http://nginx.org/download/nginx-$pkgver.tar.gz"
        "https://naxsi.googlecode.com/files/naxsi-core-$_naxsi_ver.tgz"
        rc.d
	service
	nginx.logrotate)
md5sums=('d252f5c689a14a668e241c744ccf5f06'
         '4693f8fae5abb2112c842c6dc4eb3316'
         'f62c7c9b5a53471d4666a4c49ad363fb'
         '62d494d23aef31d0b867161f9fffa6eb'
         'b38744739022876554a0444d92e6603b')

provides=('nginx')
conflicts=('nginx')
replaces=('nginx')

build() {
	cd "$srcdir"
	if [ -d "nginx-dav-ext-module" ]; then
		cd nginx-dav-ext-module && git pull
	else
		git clone https://github.com/arut/nginx-dav-ext-module.git
	fi

	cd "$srcdir/nginx-${pkgver}"

        ./configure \
	--add-module=$srcdir/naxsi-core-$_naxsi_ver/naxsi_src \
        --prefix=$_cfgdir \
        --conf-path=$_cfgdir/nginx.conf \
        --sbin-path=/usr/sbin/nginx \
        --pid-path=/var/run/nginx.pid \
        --lock-path=/var/lock/nginx.lock \
        --user=http --group=http \
        --http-log-path=/var/log/nginx/access.log \
        --error-log-path=/var/log/nginx/error.log \
        --http-client-body-temp-path=$_tmpdir/client-body \
        --http-proxy-temp-path=$_tmpdir/proxy \
        --http-fastcgi-temp-path=$_tmpdir/fastcgi \
        --http-scgi-temp-path=$_tmpdir/scgi \
        --http-uwsgi-temp-path=$_tmpdir/uwsgi \
        --with-imap --with-imap_ssl_module \
        --with-ipv6 --with-pcre-jit \
        --with-file-aio \
        --with-http_dav_module \
        --with-http_gzip_static_module \
        --with-http_realip_module \
        --with-http_ssl_module \
        --with-http_stub_status_module \
        --add-module=/usr/lib/passenger/ext/nginx \
	--add-module=$srcdir/nginx-dav-ext-module
	
        #--with-http_mp4_module \
        #--with-http_realip_module \
        #--with-http_addition_module \
        #--with-http_xslt_module \
        #--with-http_image_filter_module \
        #--with-http_geoip_module \
        #--with-http_sub_module \
        #--with-http_flv_module \
        #--with-http_random_index_module \
        #--with-http_secure_link_module \
        #--with-http_degradation_module \
        #--with-http_perl_module \

	make
}

package() {
	cd "$srcdir/nginx-${pkgver}"
	make DESTDIR="$pkgdir" install

	install -d "$pkgdir"/etc/logrotate.d/
	install -m644 "$srcdir"/nginx.logrotate "$pkgdir"/etc/logrotate.d/nginx

        sed -e 's|\<user\s\+\w\+;|user html;|g' \
	    -e '44s|html|/usr/share/nginx/html|' \
	    -e '54s|html|/usr/share/nginx/html|' \
	    -i "$pkgdir"/etc/nginx/nginx.conf
	rm "$pkgdir"/etc/nginx/*.default

	install -d "$pkgdir"/$_tmpdir

	install -d "$pkgdir"/usr/share/nginx
	mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

	install -D -m755 "$srcdir"/rc.d "$pkgdir"/etc/rc.d/nginx
	install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/nginx.service
	install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/nginx/LICENSE
	rm -rf "$pkgdir"/var/run

	install -Dm644 "$srcdir/naxsi-core-$_naxsi_ver/naxsi_config/naxsi_core.rules" "$pkgdir/etc/nginx/naxsi_core.rules"
	install -Dm644 "$srcdir/naxsi-core-$_naxsi_ver/naxsi_config/default_location_config.example" "$pkgdir/etc/nginx/default_location_config.example"
}
