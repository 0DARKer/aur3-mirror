# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
pkgname=raknet
pkgver=4.033
pkgrel=1
pkgdesc="Cross platform C++ network library"
arch=('i686' 'x86_64')
url="http://www.jenkinssoftware.com/"
license=('custom')
makedepends=('cmake')
source=(http://www.jenkinssoftware.com/raknet/downloads/RakNet_PC-${pkgver}.zip
        FastDelegate.h.fixed
        license.txt)
md5sums=('fbf57c494efc7be641f83d905c0b743d'
         '733b8f5beb7a5eb451bdbb69fafff382'
         '6bd636fe028aac184c20b3a50d83cd79')

build()
{
  cd ${srcdir}
  mkdir -p build
  cd build

  # enable LIBCAT_SECURITY features
  sed -i -e s@//#define@#define@g ../Source/NativeFeatureIncludesOverrides.h
  
  # fix compilation problem with included FastDelegate.h
  mv ../Source/cat/port/FastDelegate.h ../Source/cat/port/FastDelegate.h.no
  cp ../FastDelegate.h.fixed ../Source/cat/port/FastDelegate.h
 
  cmake -DCMAKE_BUILD_TYPE=Release  \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DDISABLE_EXAMPLES=True     ..

  make
}

package()
{
  cd ${pkgdir}
  
  # DESTDIR is not used correctly so copy library and its headers manually
  mkdir -p usr/share/licenses/${pkgname}
  cp ${srcdir}/license.txt usr/share/licenses/${pkgname}/license.txt
  
  mkdir -p usr/lib/
  cp ${srcdir}/build/Lib/DLL/libRakNetDynamic.so      usr/lib/
  cp ${srcdir}/build/Lib/LibStatic/libRakNetStatic.a  usr/lib/
  
  mkdir -p usr/include/raknet/
  install -m644 ${srcdir}/Source/*.h usr/include/raknet/
}
