# Maintainer: Muhammad Tauqir Ahmad <mtahmed@uwaterloo.ca>

pkgname=lldb-svn
pkgver=178769
_gcc_ver=4.6.2
pkgrel=1
pkgdesc="The LLDB Debugger"
arch=('i686' 'x86_64')
url="http://llvm.org/"
license=('custom:University of Illinois/NCSA')
depends=('gcc-libs' 'libffi' 'python2' "gcc>=$_gcc_ver" 'libedit' 'llvm' 'clang')
makedepends=('svn' 'cmake' 'swig')
provides=('lldb')
conflicts=('lldb')
source=()

_svntrunk="http://llvm.org/svn/llvm-project"
_svnmod="llvm"
_cfemod="cfe"
_lldbmod="lldb"

build() {
  cd "$srcdir"

  msg2 "Connecting to llvm.org SVN server ..."

  msg2 "Fetching llvm libs ..."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn update -r $pkgver) || warning "Update failed!"
  else
    svn co $_svntrunk/$_svnmod/trunk $_svnmod -r $pkgver
  fi

  msg2 "Fetching clang ..."
  if [ -d $_cfemod/.svn ]; then
    (cd $_cfemod && svn update -r $pkgver) || warning "Update failed!"
  else
    svn co $_svntrunk/$_cfemod/trunk $_cfemod -r $pkgver
  fi

  msg2 "Fetching lldb ..."
  if [ -d $_lldbmod/.svn ]; then
    (cd $_lldbmod && svn update -r $pkgver) || warning "Update failed!"
  else
    svn co $_svntrunk/$_lldbmod/trunk $_lldbmod -r $pkgver #--config-dir ./
  fi

  msg2 "SVN checkout done or server timed out."

  rm -rf $_svnmod-build
  svn export $_svnmod $_svnmod-build
  svn export $_cfemod $_svnmod-build/tools/clang
  svn export $_lldbmod $_svnmod-build/tools/lldb
  cd $_svnmod-build

  msg2 "Starting build ..."

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  export CFLAGS="$CFLAGS -fno-tree-pre"
  export CXXFLAGS="$CFLAGS -fno-tree-pre"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -LLVM_ENABLE_ASSERTIONS=OFF \
    -LLVM_ENABLE_FFI=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    ..

  # Change to liking if more cores available.
  make -j4
}

package() {
  cd "$srcdir/$_svnmod-build/"


  # Install the license
  install -Dm644 tools/lldb/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd build/bin/

  # Install the lldb binary
  install -Dm755 lldb "$pkgdir/usr/bin/lldb"

  cd "$srcdir/$_svnmod-build/build/lib/"

  # Install the lldb library
  install -Dm755 liblldb.so "$pkgdir/usr/lib/llvm/liblldb.so"
  # Install the lldb python libraries.
  cp --recursive --preserve=mode --no-preserve=ownership,timestamps python2.7/ "$pkgdir/usr/lib/"
  # Relink the _lldb.so
  ln -sf '../../../llvm/liblldb.so' "$pkgdir/usr/lib/python2.7/site-packages/lldb/_lldb.so"
}

# vim:set ts=2 sw=2 et:
