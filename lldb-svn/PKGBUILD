# Maintainer: Muhammad Tauqir Ahmad <mtahmed@uwaterloo.ca>
# Contributor: Nicolas Hureau <aur@kalenz.fr>

pkgname=lldb-svn
_gcc_ver=4.6.2
pkgver=186408
pkgrel=1
pkgdesc="The LLDB Debugger"
arch=('i686' 'x86_64')
url="http://llvm.org/"
license=('custom:University of Illinois/NCSA')
depends=('gcc-libs' 'libffi' 'python2' "gcc>=$_gcc_ver" 'libedit' 'llvm' 'clang')
makedepends=('svn' 'cmake' 'swig')
provides=('lldb')
conflicts=('lldb')
source=(
  'llvm::svn+http://llvm.org/svn/llvm-project/llvm/trunk'
  'cfe::svn+http://llvm.org/svn/llvm-project/cfe/trunk'
  'lldb::svn+http://llvm.org/svn/llvm-project/lldb/trunk'
  'lldb-archlinux-python2-config.patch'
)
sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  '1bdb279dbbc68d00b99bae5c3e9e85ac05a65f9506a951966555fa1c9d938819'
)

pkgver() {
  cd "$SRCDEST/llvm"
  echo $(svnversion)
}

build() {
  cd "$srcdir"
  msg2 "Exporting Clang in the LLVM tree"
  svn export cfe llvm/tools/clang &> /dev/null
  msg2 "Exporting LLDB in the LLVM tree"
  svn export lldb llvm/tools/lldb &> /dev/null

  cd "$srcdir/llvm/tools/lldb"
  msg2 "Applying Archlinux specific patch"
  patch -p1 < $srcdir/lldb-archlinux-python2-config.patch

  cd "$srcdir/llvm"
  msg2 "Starting build ..."

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  export CFLAGS="$CFLAGS -fno-tree-pre"
  export CXXFLAGS="$CXXFLAGS -fno-tree-pre"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -LLVM_ENABLE_ASSERTIONS=OFF \
    -LLVM_ENABLE_FFI=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    ..

  make
}

package() {
  cd "$srcdir/llvm"

  # Install the license
  install -Dm644 tools/lldb/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$srcdir/llvm/build"

  # Install the lldb binary
  install -Dm755 bin/lldb "$pkgdir/usr/bin/lldb"

  # Install the lldb library
  install -Dm755 lib/liblldb.so "$pkgdir/usr/lib/liblldb.so"

  # Install the lldb python libraries.
  install -Dm644 tools/lldb/scripts/lldb.py "$pkgdir/usr/lib/python2.7/lldb.py"
  python2 -m compileall "$pkgdir/usr/lib/python2.7/"
  python2 -O -m compileall "$pkgdir/usr/lib/python2.7/"

  # Relink the _lldb.so for python
  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages"
  ln -sf /usr/lib/liblldb.so "$pkgdir/usr/lib/python2.7/site-packages/_lldb.so"
}

# vim:set ts=2 sw=2 et:
