# Maintainer: Muhammad Tauqir Ahmad <mtahmed@uwaterloo.ca>
# Contributor: Nicolas Hureau <aur@kalenz.fr>

pkgname=lldb-svn
pkgver=178769
_gcc_ver=4.6.2
pkgrel=1
pkgdesc="The LLDB Debugger"
arch=('i686' 'x86_64')
url="http://llvm.org/"
license=('custom:University of Illinois/NCSA')
depends=('gcc-libs' 'libffi' 'python2' "gcc>=$_gcc_ver" 'libedit' 'llvm' 'clang')
makedepends=('svn' 'cmake' 'swig')
provides=('lldb')
conflicts=('lldb')
source=('lldb-archlinux-python2-config.patch')
sha1sums=('a9b90f269390601891086093e4c44396f02b72ea')

_svntrunk="http://llvm.org/svn/llvm-project"
_svnmod="llvm"
_cfemod="cfe"
_lldbmod="lldb"

build() {
  cd "$srcdir"

  msg2 "Connecting to llvm.org SVN server ..."

  msg2 "Fetching llvm libs ..."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn update -r $pkgver) || warning "Update failed!"
  else
    svn co $_svntrunk/$_svnmod/trunk $_svnmod -r $pkgver
  fi

  msg2 "Fetching clang ..."
  if [ -d $_cfemod/.svn ]; then
    (cd $_cfemod && svn update -r $pkgver) || warning "Update failed!"
  else
    svn co $_svntrunk/$_cfemod/trunk $_cfemod -r $pkgver
  fi

  msg2 "Fetching lldb ..."
  if [ -d $_lldbmod/.svn ]; then
    (cd $_lldbmod && svn update -r $pkgver) || warning "Update failed!"
  else
    svn co $_svntrunk/$_lldbmod/trunk $_lldbmod -r $pkgver #--config-dir ./
  fi

  msg2 "SVN checkout done or server timed out."

  rm -rf $_svnmod-build
  svn export $_svnmod $_svnmod-build
  svn export $_cfemod $_svnmod-build/tools/clang
  svn export $_lldbmod $_svnmod-build/tools/lldb

  cd "$_svnmod-build/tools/lldb"
  msg2 "Applying Archlinux specific patch"
  patch -p1 < ../../../lldb-archlinux-python2-config.patch
  cd "../.."

  msg2 "Starting build ..."

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  export CFLAGS="$CFLAGS -fno-tree-pre"
  export CXXFLAGS="$CFLAGS -fno-tree-pre"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -LLVM_ENABLE_ASSERTIONS=OFF \
    -LLVM_ENABLE_FFI=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    ..

  # Change to liking if more cores available.
  make -j4
}

package() {
  cd "$srcdir/$_svnmod-build/"


  # Install the license
  install -Dm644 tools/lldb/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$srcdir/$_svnmod-build/build/"

  # Install the lldb binary
  install -Dm755 bin/lldb "$pkgdir/usr/bin/lldb"

  # Install the lldb library
  install -Dm755 lib/liblldb.so "$pkgdir/usr/lib/liblldb.so"
  # Install the lldb python libraries.
  install -Dm644 tools/lldb/scripts/lldb.py "$pkgdir/usr/lib/python2.7/lldb.py"
  python2 -m compileall "$pkgdir/usr/lib/python2.7/"
  python2 -O -m compileall "$pkgdir/usr/lib/python2.7/"
  # Relink the _lldb.so
  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/lldb"
  ln -sf /usr/lib/liblldb.so "$pkgdir/usr/lib/python2.7/site-packages/_lldb.so"
}

# vim:set ts=2 sw=2 et:
