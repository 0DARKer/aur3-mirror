# Maintainer:  Ainola <opp310@alh.rqh> (ROT13 this)
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Xyne
# Contributor: Rick Chen (stuffcorpse)
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Camille Moncelier <pix@devlife.org>

pkgname=nexuiz
pkgver=2.5.2
_zipver=252
pkgrel=3
arch=('i686' 'x86_64')
pkgdesc="Fast-paced multiplayer first-person shooter."
url="http://www.alientrap.org/nexuiz/"
license=("GPL")
depends=('alsa-lib' 'curl' 'libjpeg>=8' 'libmodplug' 'libvorbis' 'libxpm' 'libxxf86dga' 'libxxf86vm' 'sdl' 'libpng>=1.4.0')
makedepends=('mesa')
conflicts=('nexuiz-data')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_zipver}.zip \
        bin-links.tar.gz nexuiz-glx.desktop nexuiz-sdl.desktop\
        libpng14.patch nex-icons.tar.gz nexuiz.install)
sha256sums=('a5e27ebcc9775c4a490d0d3536c32e4a8f8f96b038c0b6a78d1823c37a962000'
            '9105680007b25156ae4fea977a220c98af761e7f48bad0e29315ebab5eb9fc01'
            '277ace303189dc25403a1a7c03b66d033e6aed52c98adb4ecfb5f627b101fff3'
            '50ad2bf51a291635d7d3195745f9536b7182c0ed548259c0a8627ea951e3da7f'
            '327c45a56faa1963fa08a590694fd0c23bed448282f9473c2d483447df9a6e1f'
            '25e478b783bb7f8cff04b7c3e538a82e8ab852d72930277e83d00777d2a7186e'
            '8ca411bb490f60296868ed7831f34aae7b57738d13f3e3c4e843488912ea3869')
install=nexuiz.install
build(){
    cd $srcdir/Nexuiz
    bsdtar -x -f "sources/enginesource20091001.zip"
    cd darkplaces
    patch -Np1 -i "${srcdir}/libpng14.patch"
    
    #build the binaries separately instead to avoid truncated files
    make CPUOPTIMIZATIONS="${CFLAGS}" DP_LINK_TO_LIBJPEG=1 cl-nexuiz
    make CPUOPTIMIZATIONS="${CFLAGS}" DP_LINK_TO_LIBJPEG=1 sdl-nexuiz
    make CPUOPTIMIZATIONS="${CFLAGS}" DP_LINK_TO_LIBJPEG=1 sv-nexuiz
}

package(){
    mkdir -p "$pkgdir/opt/nexuiz"
    
    #install the compiled binaries, /usr/bin links, and .desktop files
    cd $srcdir/Nexuiz/darkplaces
    install -Dm755 nexuiz-glx nexuiz-sdl nexuiz-dedicated -t "$pkgdir/opt/nexuiz"
    cd -
    install -dm755 $pkgdir/usr/{bin,share/applications}
    install -Dm755 $srcdir/bin-links/nexuiz-{glx,sdl,dedicated} -t $pkgdir/usr/bin
    install -Dm644 $srcdir/*.desktop -t $pkgdir/usr/share/applications
    
    #install game data, then set proper permissions
    for dir in Docs data havoc server; do
        mv "$srcdir/Nexuiz/$dir" "$pkgdir/opt/nexuiz/"
        find "$pkgdir/opt/nexuiz/$dir" -type d -exec chmod 755 {} \;
        find "$pkgdir/opt/nexuiz/$dir" -type f -exec chmod 644 {} \;
    done
    #remove unneeded cruft
    find "$pkgdir/opt/nexuiz/server" -name "*_windows.bat" -exec rm {} \;
    find "$pkgdir/opt/nexuiz/server" -name "*_mac.sh" -exec rm {} \;
    
    #install icons
    for size in 16 22 24 32 48 64 128 256 512; do
        install -Dm644 "$srcdir/nex${size}.png" \
            "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/nexuiz.png"
    done
}
