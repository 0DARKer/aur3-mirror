# Maintainer: Heine <zannvip at gmail dot com>
# Contributor: Tucos <baspape@gmail.com>
# Contributor: Jelle van der Waa <jellevdwaa  @ [the google mail .com]>

pkgname=techne
pkgver=0.2.1
pkgrel=2
pkgdesc="A general purpose, programmable physical simulator and renderer"
arch=('x86_64' 'i686')
url="http://savannah.nongnu.org/projects/techne/"
license=('GPL3')
depends=('lua' 'gtkglext' 'libwebkit' 'libmicrohttpd' 'openal' 'imagemagick' 'ode-compat')
makedepends=('gcc-objc')
source=(http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('aada599e3e46f05c3131d718ccefd320')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  LUA_LIBS="`pkg-config --libs lua`" LUA_CFLAGS=" " LUAC=/usr/bin/luac ./configure  --prefix=/usr 
  make 
}
package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
