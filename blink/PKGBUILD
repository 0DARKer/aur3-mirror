# Maintainer: Ingo Gottwald <in dot gottwald at gmail dot com>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=blink
pkgver=0.9.1
pkgrel=3
pkgdesc="A state of the art easy to use Qt SIP client"
arch=('any')
url="http://www.icanblink.com"
license=('custom:MIT')
depends=('python2-cjson' 'python2-pyqt' 'python2-sipsimple' 'libvncserver' 'cython2')
source=(
        "http://download.ag-projects.com/BlinkQt/$pkgname-$pkgver.tar.gz"
        "blink.desktop"
        )
sha256sums=(
        'f578e5186893c3488e7773fbb775028ae54540433a0c51aefa5af983ca2bfdae'
        '3d9bfe4e54a5e36747b6ffcca5d26d0e19d2292855d10a94543e0891ddf7dd0f'
        )

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/"

  # license
  install -Dm644 doc/license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # desktop file
  install -Dm644 "$startdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
