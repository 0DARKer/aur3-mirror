# Maintainer : SpepS <dreamspepser at yahoo dot it>

pkgname=blink
pkgver=20110629
pkgrel=1
pkgdesc="A state of the art, easy to use SIP client"
arch=('any')
url="http://www.icanblink.com"
license=('GPL')
depends=('python2-pyqt' 'python-application' 'python-cjson' 'python2-sipsimple' 'python2-zope-interface'
         'python2-distribute' 'python-dnspython' 'python-lxml' 'python-imaging' 'python-dateutil')
conflicts=("$pkgname-darcs")
makedepends=('darcs')
install="$pkgname.install"

_darcstrunk="http://devel.ag-projects.com/repositories"
_darcsmod="blink-qt"

build() {
  cd "$srcdir"

  msg "Checking for previous build"

  if [[ -d $_darcsmod/_darcs ]]
  then
    msg "Retrieving missing patches"
    cd $_darcsmod
    darcs pull -a $_darcstrunk/$_darcsmod
  else
    msg "Retrieving complete sources"
    darcs get --partial --set-scripts-executable $_darcstrunk/$_darcsmod
    cd $_darcsmod
  fi

  rm -rf "$srcdir/$_darcsmod-build"
  cp -r "$srcdir/$_darcsmod" "$srcdir/$_darcsmod-build"
  cd "$srcdir/$_darcsmod-build"

  msg "Starting build"

  #
  # BUILD
  #

  # solves wrong imports for application.python
  sed -i "s|\(application\.python\)\( import Null\)|\1\.util\2|g" `grep -rl "application\.python import Null" .`
  sed -i "s|, limit|\nfrom application.python import limit|" `grep -rl ", limit" .`

  python2 setup.py build
}

package() {
  cd "$srcdir/$_darcsmod-build"

  python2 setup.py install --root="$pkgdir/"

  # desktop file, man and pixmap
  cd debian
  install -Dm 644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm 644 $pkgname.xpm "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm 644 $pkgname.1 "$pkgdir/usr/share/man/man1/$pkgname.1"

  # set pkgver
  pkgver=3.9.2
}
