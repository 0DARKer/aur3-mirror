# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=android-qt5-qt3d-git
pkgver=5.2.0_735.30879a6
pkgrel=1
pkgdesc="Qt 5 for Android: qt3d module"
groups=('qt-android' 'qt5-android')
arch=('i686' 'x86_64')
url="https://qt.gitorious.org/qt/qt3d"
license=('LGPL')
depends=('android-qt5-qtdeclarative-git')
makedepends=('git')
options=('!libtool' '!strip')
source=("${pkgname}::git://gitorious.org/qt/qt3d.git")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${pkgname}
  _basever=$(grep MODULE_VERSION .qmake.conf | awk -- '{print $3}')
  echo ${_basever}_$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  #git describe --always | sed 's|-|_|g' | sed 's|^v||'
}


build() {
  cd ${srcdir}/${pkgname}

  /opt/android-qt5/bin/qmake
  make
}

package() {
  cd ${srcdir}/${pkgname}

  make INSTALL_ROOT="${pkgdir}" install

  # Strip by hand, system strip doesn't work on arm binaries
  
  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/lib/*.so* \
    ${pkgdir}/opt/android-qt5/qml/*/*.so
}
