# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=opensc-opendnie-git
pkgver=0.13.0.119.gf77a629
pkgrel=1
pkgdesc="Access smart cards that support cryptographic operations. with OpenDNIe driver (Spanish ID card). (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/OpenSC/OpenSC/wiki"
license=("LGPL")
depends=('pinentry' 'ccid')
makedepends=('git' 'docbook-xsl' 'openssl' 'libltdl')
provides=('opensc' 'opensc-opendnie')
conflicts=('opensc')
backup=('etc/opensc.conf')
source=('opensc-opendnie::git+https://github.com/OpenDNIe/OpenSC.git#branch=opendnie'
        'opensc.conf.in-dnie.diff')
sha1sums=('SKIP'
          '55ea882b19cf20ec43095b61d94851ddabac56d3')
_gitroot="opensc-opendnie"

pkgver() {
  cd "${_gitroot}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare () {
  cd "${_gitroot}"
  patch -p0 -i "${srcdir}/opensc.conf.in-dnie.diff"
  sed 's|        |\t|g' -i etc/opensc.conf.in
}

build() {
  cd "${_gitroot}"
  ./bootstrap
  ./configure --prefix=/usr --sysconfdir=/etc --enable-man --enable-doc --enable-openssl \
              --enable-zlib --enable-readline --enable-pcsc --enable-sm --enable-dnie-ui \
              --with-xsl-stylesheetsdir="/usr/share/xml/docbook/xsl-stylesheets-1.78.1"
  make
}

package() {
  cd "${_gitroot}"
  make DESTDIR="${pkgdir}" install
}
