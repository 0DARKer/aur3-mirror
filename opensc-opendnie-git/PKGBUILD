# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=opensc-opendnie-git
pkgver=0.13.0.102.g2c39f16
pkgrel=1
pkgdesc="Access smart cards that support cryptographic operations. with OpenDNIe driver. (GIT version)"
arch=('i686' 'x86_64')
url="http://www.opensc-project.org/opensc"
license=("LGPL")
backup=(etc/opensc.conf)
depends=('pinentry' 'ccid')
makedepends=('git' 'docbook-xsl')
provides=('opensc')
conflicts=('opensc')
options=('!emptydirs' '!libtool')

source=("opensc-opendnie::git://github.com/OpenDNIe/OpenSC.git#branch=opendnie")
md5sums=('SKIP')
_gitroot="opensc-opendnie"

pkgver() {
  cd "${_gitroot}"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${_gitroot}"
  ./bootstrap
  _sheetdir=(/usr/share/xml/docbook/xsl-stylesheets-*)
  ./configure --prefix=/usr --enable-openssl --enable-readline --enable-man --enable-pcsc --enable-zlib \
              --enable-sm --enable-dnie-ui --enable-doc --sysconfdir=/etc --with-xsl-stylesheetsdir="${_sheetdir}"
  make
}

package() {
  cd "${_gitroot}"
  install -d "${pkgdir}"/{etc,usr/lib/pkcs11}
  make DESTDIR="${pkgdir}" install
}
