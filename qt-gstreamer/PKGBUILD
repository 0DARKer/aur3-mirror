# Maintainer: laloch <laloch@atlas.cz>
pkgname=qt-gstreamer
pkgver=0.10.1
pkgrel=2
pkgdesc="Qt bindings for GStreamer"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/gstreamer/qt-gstreamer"
license=('LGPL2.1')
depends=('qt' 'gstreamer0.10-base')
makedepends=('cmake' 'automoc4' 'boost' 'flex' 'bison')
source=("http://cgit.freedesktop.org/gstreamer/$pkgname/snapshot/RELEASE-$pkgver.tar.gz")
md5sums=('7548df96feb4620f7e211c2d935094ca')

build() {
  rm -rf "$srcdir/$pkgname-build"
  mkdir "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  cmake "$srcdir/RELEASE-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQTGSTREAMER_EXAMPLES=OFF \
    -DQTGSTREAMER_TESTS=OFF
  make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
