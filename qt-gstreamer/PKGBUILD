# Maintainer: laloch <laloch@atlas.cz>
pkgname=qt-gstreamer
pkgver=0.10.1
pkgrel=5
pkgdesc="Qt bindings for GStreamer"
arch=('i686' 'x86_64')
url="http://gstreamer.freedesktop.org/modules/qt-gstreamer.html"
license=('LGPL2.1')
depends=('qt' 'gstreamer0.10-base')
makedepends=('cmake' 'automoc4' 'boost' 'flex' 'bison')
source=("http://cgit.freedesktop.org/gstreamer/$pkgname/snapshot/RELEASE-$pkgver.tar.bz2"
        "fix-glib-2.29.patch::http://cgit.freedesktop.org/gstreamer/qt-gstreamer/patch/src/QGst/caps.cpp?id=a5487083433341783733858b5c153b759053f032"
        "fix-boost-1.48.patch"
	"fix-glib-2.30.patch::http://cgit.freedesktop.org/gstreamer/qt-gstreamer/patch/?id=087d7650d611bbda5e7604f4e98c247a9c104f34"
        )
md5sums=('ce11806d7b4bb38509988ae17d2a1be4'
         'dbbc9a6b6e30508b6279f62c66e8ce7f'
         '90db17dd3061df2596a1e472bae5d4b4'
	 '55c2610d24cabeb7c9aa701f39b47133')

build() {
  patch -d "$srcdir/RELEASE-$pkgver" -p1 < "$srcdir/fix-glib-2.29.patch"
  patch -d "$srcdir/RELEASE-$pkgver" -p1 < "$srcdir/fix-boost-1.48.patch"
  patch -d "$srcdir/RELEASE-$pkgver" -p1 < "$srcdir/fix-glib-2.30.patch"

  rm -rf "$srcdir/$pkgname-build"
  mkdir "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  cmake "$srcdir/RELEASE-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQTGSTREAMER_EXAMPLES=OFF \
    -DQTGSTREAMER_TESTS=OFF
  make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
