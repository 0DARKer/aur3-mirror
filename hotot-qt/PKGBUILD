# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=hotot
pkgname=hotot-qt
pkgver=0.9.7%7Egit_1374_g70df9a6
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca) in Qt."
arch=(i686 x86_64)
url="http://www.hotot.org/"
license=('LGPL3')
depends=('qtwebkit')
makedepends=('cmake' 'intltool')
install="$pkgname.install"
source=("https://launchpad.net/~hotot-team/+archive/ppa/+files/${_name}_${pkgver//_/-}-0ubuntu0ppa1%7Eprecise1.tar.gz")
md5sums=('fe7c2e4601380cb895b0594f11a08e1e')

build() {
  cd "$srcdir/$_name-precise"

  # remove google analytics tracking code (tnx to ianux)
  sed -i '/\/\/ 6. run track code/,+14d' data/js/hotot.js
  sed -i '/ga.js/d' data/index.html

  # build qt
  [ -d build-qt ] || mkdir build-qt && cd build-qt
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/$_name-precise/build-qt"
  make DESTDIR="$pkgdir/" install
}
