# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=hotot
pkgname=hotot-qt
pkgver=0.9.7%7Ehg1036
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca) in Qt."
arch=(i686 x86_64)
url="http://www.hotot.org/"
license=('LGPL3')
depends=('qt')
makedepends=('cmake' 'intltool')
conflicts=("$pkgname-hg")
install="$pkgname.install"
source=("https://launchpad.net/~hotot-team/+archive/ppa/+files/${_pkg}_${pkgver}-0ubuntu0ppa1%7Eoneiric1.tar.gz")
md5sums=('accde49a2c22e3b7f146c1babc0d611b')

build() {
  cd "$srcdir/$_pkg-"*

  # remove google analytics tracking code
  sed -i '/\/\/ 6. run track code/,+4d' data/index.html

  # build qt
  [ -d build-qt ] || mkdir build-qt && cd build-qt
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/$_pkg-"*/build-qt

  make DESTDIR="$pkgdir/" install
}
