# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=hotot
pkgname=hotot-qt
pkgver=0.9.7%7Egit_1298_g1d9a578
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca) in Qt."
arch=(i686 x86_64)
url="http://www.hotot.org/"
license=('LGPL3')
depends=('qtwebkit')
makedepends=('cmake' 'intltool')
install="$pkgname.install"
source=("https://launchpad.net/~hotot-team/+archive/ppa/+files/${_pkg}_${pkgver//_/-}-0ubuntu0ppa1%7Eoneiric1.tar.gz")
md5sums=('1a1d1b110d1eee9bae5a28001ad09cd4')

build() {
  cd "$srcdir/$_pkg-oneiric"

  # remove google analytics tracking code (tnx to ianux)
  sed -i '/\/\/ 6. run track code/,+14d' data/js/hotot.js
  sed -i '/ga.js/d' data/index.html

  # build qt
  [ -d build-qt ] || mkdir build-qt && cd build-qt
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/$_pkg-oneiric"/build-qt

  make DESTDIR="$pkgdir/" install
}
