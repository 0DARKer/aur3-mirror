# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=hotot
pkgname=hotot-qt
pkgver=0.9.7%7Egit_1138_g0f83b81
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca) in Qt."
arch=(i686 x86_64)
url="http://www.hotot.org/"
license=('LGPL3')
depends=('qt')
makedepends=('cmake' 'intltool')
install="$pkgname.install"
source=("https://launchpad.net/~hotot-team/+archive/ppa/+files/${_pkg}_${pkgver//_/-}-0ubuntu0ppa1%7Eoneiric1.tar.gz")
md5sums=('b58ed0384ea903385f82401a069af236')

build() {
  cd "$srcdir/$_pkg-oneiric"

  # remove google analytics tracking code
  sed -i '/\/\/ 6. run track code/,+4d' data/index.html

  # build qt
  [ -d build-qt ] || mkdir build-qt && cd build-qt
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "$srcdir/$_pkg-oneiric"/build-qt

  make DESTDIR="$pkgdir/" install

  # change icon names ..
  for _i in `find "$pkgdir" -name "$_pkg.png"`; do
    mv $_i `echo $_i | sed "s/$_pkg.png/$pkgname.png/"`
  done

  # .. and desktop file icon to not conflict with hotot
  sed -i "/Icon/s/$_pkg/$pkgname/" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
