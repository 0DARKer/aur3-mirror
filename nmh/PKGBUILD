# Maintainer: Chris Brannon <cmbrannon79@gmail.com>

pkgname=nmh
pkgver=1.5
pkgrel=1
pkgdesc="Powerful electronic mail handling system, based on a set of command-line utilities."
arch=('i686' 'x86_64')
url="http://www.nongnu.org/nmh/"
license=('custom')
changelog='ChangeLog'
source=("http://download.savannah.nongnu.org/releases/nmh/$pkgname-$pkgver.tar.gz")
md5sums=('5a7bc5a157f4428f7660bb882e842375')
backup=(etc/nmh/MailAliases etc/nmh/components etc/nmh/digestcomps
        etc/nmh/distcomps etc/nmh/forwcomps etc/nmh/mhl.body etc/nmh/mhl.digest
        etc/nmh/mhl.format etc/nmh/mhl.forward etc/nmh/mhl.headers
        etc/nmh/mhl.reply etc/nmh/mhn.defaults etc/nmh/mts.conf
        etc/nmh/rcvdistcomps etc/nmh/rcvdistcomps.outbox etc/nmh/replcomps
        etc/nmh/replgroupcomps etc/nmh/scan.MMDDYY etc/nmh/scan.YYYYMMDD
        etc/nmh/scan.default etc/nmh/scan.mailx etc/nmh/scan.nomime
        etc/nmh/scan.size etc/nmh/scan.time etc/nmh/scan.timely
        etc/nmh/scan.unseen)
depends=(db sh)
options=('!makeflags')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc/nmh --libdir="/usr/lib/nmh"
    make || return 1
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    install -d -m755 "$pkgdir/usr/share/licenses/nmh"
    install -m644 COPYRIGHT "$pkgdir/usr/share/licenses/nmh/"
}
