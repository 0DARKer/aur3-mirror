# Maintainer:  Lucky <aur.archlinux.org [at] lucky.take0ver [dot] net>
# Contributor: Ashren <edgar [at] archlinux [dot] us>
# Contributor: Daenyth <Daenyth [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

pkgname=rtorrent-vi-color
_pkgname="rtorrent"
#_pkgname="${pkgname%-*}" not supported by AUR ;)
pkgver=0.8.9
pkgrel=1
pkgdesc="Ncurses BitTorrent client based on libTorrent with color patch."
url="http://libtorrent.rakshasa.no"
arch=("i686" "x86_64")
license=("GPL")
depends=("curl" "libtorrent=0.12.9" "xmlrpc-c")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
install="${pkgname}.install"
source=("${url}/downloads/${_pkgname}-${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}_vi_keybinding_tjwoosta.patch"
        "${_pkgname}-${pkgver}_canvas_color.patch")
sha1sums=("0ac51c185e98b5a386e5f1a07bca9a9963e2d6ce"
          "4115b19e5f50cb57dea7aab4f931270651cc7321"
          "a4c2247a62b60c3ffb07dc688f2371cccfa3664d")

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -uNp1 -i "${srcdir}/${_pkgname}-${pkgver}_vi_keybinding_tjwoosta.patch"
  patch -uNp1 -i "${srcdir}/${_pkgname}-${pkgver}_canvas_color.patch"

  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
  ./configure \
    --prefix=/usr \
    --enable-debug \
    --with-xmlrpc-c \

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 doc/faq.xml "${pkgdir}/usr/share/doc/rtorrent/faq.xml"
  install -Dm644 doc/rtorrent.1 "${pkgdir}/usr/share/man/man1/rtorrent.1"
  install -Dm644 doc/rtorrent.rc "${pkgdir}/usr/share/doc/rtorrent/rtorrent.rc"
}
