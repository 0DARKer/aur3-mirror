# Maintainer: Tim Wooster <tjwoosta@gmail.com>

pkgname=rtorrent-vi-color
pkgver=0.8.6
pkgrel=2
pkgdesc="Ncurses BitTorrent client based on libTorrent patched for vi style scroll keys and colors."
arch=('i686' 'x86_64')
url="http://libtorrent.rakshasa.no"
install=$pkgname.install
license=('GPL')
depends=('libtorrent=0.12.6' 'curl>=7.14.3' 'xmlrpc-c')
conflicts=('rtorrent')
provides=('rtorrent')
source=(http://libtorrent.rakshasa.no/downloads/rtorrent-$pkgver.tar.gz
        rtorrent-canvas-fix.patch
        rtorrent-vi.patch
        fix_ncurses_5.8.patch)

md5sums=('b804c45c01c40312926bcea6b55bb084'
         '03cbbf61ac0c5a128880a5a453677a87'
         'ceb3a0cbdb38f348462f3e5d763bbc62'
         '5215cb68a17434aa99cdf380f18ba89f')

build() {
  cd "$srcdir/rtorrent-$pkgver"
  patch -p3 -i "$srcdir/rtorrent-vi.patch"
  patch -p7 -i "$srcdir/rtorrent-canvas-fix.patch"
  patch -uNp1 -i "$srcdir/fix_ncurses_5.8.patch"
  CXXFLAGS="$CXXFLAGS -fno-strict-aliasing" \
    ./configure --prefix=/usr --disable-debug --with-xmlrpc-c
  make
}

package() {
  cd "$srcdir/rtorrent-$pkgver"
  make DESTDIR="$pkgdir/" install
}


# vim:set ts=2 sw=2 et:
