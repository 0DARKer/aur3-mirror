# Maintainer: Zubkov Alexander <green@msu.ru>
pkgname=zfs-linux-git
pkgver=20110305
pkgrel=1
pkgdesc="Native ZFS for Linux"
arch=(x86_64)
url="https://github.com/zfs-linux"
license=('GPL' 'CDDL')
depends=('glibc' 'util-linux' 'e2fsprogs' 'nfs-utils')
makedepends=('git' 'kernel26-headers')
provides=('zfs-linux')
options=(!libtool)
source=(lzfs-install-loc.patch)
md5sums=('b50b77601d3e723ae2bc60a6eed6be14')

_modules=('spl' 'zfs' 'lzfs')
_gitroot="https://github.com/zfs-linux"
_gitname=stub

build() {
    cd "$srcdir"

    for _gitname in ${_modules[*]}
    do
        (
        msg "Connecting to GIT server ($_gitname)...."
        if [ -d $_gitname ] ; then
            cd $_gitname && git pull origin
            msg "The local files are updated."
        else
            git clone "$_gitroot/$_gitname.git" $_gitname
        fi

        msg "GIT checkout done or server timeout"
        )
    done

    for _gitname in ${_modules[*]}
    do
        (
        msg "Starting make ($_gitname)..."

        rm -rf "$srcdir/$_gitname-build"
        git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
        cd "$srcdir/$_gitname-build"

        _conf_opt=
        case $_gitname in
        zfs)
            _conf_opt="--with-spl=$srcdir/spl-build/"
            ;;
        lzfs)
            _conf_opt="--with-spl=$srcdir/spl-build/
                       --with-zfs=$srcdir/zfs-build/"
            patch -p1 < "$srcdir/lzfs-install-loc.patch"
            sh ./autogen.sh
            ;;
        esac

        #sh ./autogen.sh
        ./configure --prefix=/usr --libexecdir=/usr/lib $_conf_opt
        make
        )
    done
}

package() {
    for _gitname in ${_modules[*]}
    do
        (
        msg "Installing ($_gitname)..."
        cd "$srcdir/$_gitname-build"
        make DESTDIR="$pkgdir/" install
        )
    done
} 
