# Maintainer: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Michele Gastaldo <pikiweb@gmail.com>
# Contributor: twa022 <twa022 at gmail dot com>

pkgname=empower-svn
pkgver=76628
pkgrel=2
pkgdesc="Polkit Authentication Agent for e17"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('custom')
depends=('elementary')
makedepends=('subversion')

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/empower"
_svnmod="empower"

build() {
  cd "$srcdir"

  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 "$srcdir/$_svnmod-build/COPYING-PLAIN" \
  	"$pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN"
  install -Dm644 "$srcdir/$_svnmod-build/COPYING" \
  	"$pkgdir/usr/share/licenses/$pkgname/COPYING"
  	
  rm -r "$srcdir/$_svnmod-build"
}
