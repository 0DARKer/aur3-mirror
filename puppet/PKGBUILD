# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>
# Contributor: Greg Sutcliffe <puppet-aur (at) emeraldreverie (dot) org>
# Contributor: Hyacinthe Cartiaux <hyacinthe (dot) cartiaux (at) free (dot) fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

pkgname=puppet
pkgver=3.5.0
pkgrel=1
pkgdesc="A system for automating system administration tasks."
arch=('any')
url="https://puppetlabs.com/puppet/puppet-open-source"
license=('APACHE')
depends=('facter' 'logrotate' 'ruby-shadow')
backup=('etc/puppet/puppet.conf')
install=$pkgname.install
source=(https://github.com/puppetlabs/$pkgname/archive/$pkgver.tar.gz)
md5sums=('69c31e17d5afcb1991d26ccc1c27052b')

package() {
  cd "$srcdir"/$pkgname-$pkgver

  ruby install.rb --destdir="$pkgdir"/

  # Set up vim and emacs
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/vim/ftdetect/puppet.vim \
    "$pkgdir"/usr/share/vim/vimfiles/ftdetect/puppet.vim
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/vim/syntax/puppet.vim \
    "$pkgdir"/usr/share/vim/vimfiles/syntax/puppet.vim
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/emacs/puppet-mode.el \
    "$pkgdir"/usr/share/emacs/site-lisp/puppet-mode.el
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/emacs/puppet-mode-init.el \
    "$pkgdir"/usr/share/emacs/site-lisp/site-start.d/puppet-mode-init.el

  # Configuration
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/redhat/puppet.conf \
    "$pkgdir"/etc/puppet/puppet.conf
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/redhat/logrotate \
    "$pkgdir"/etc/logrotate.d/puppet
  install -Dm644 "$srcdir"/$pkgname-$pkgver/conf/fileserver.conf \
    "$pkgdir"/etc/puppet/fileserver.conf
  install -Dm644 "$srcdir"/$pkgname-$pkgver/conf/tagmail.conf \
    "$pkgdir"/etc/puppet/tagmail.conf

  # Setup tmpfiles.d config
  install -d "$pkgdir"/usr/lib/tmpfiles.d/
  echo "D /var/run/puppet 0755 puppet puppet -" > "$pkgdir"/usr/lib/tmpfiles.d/puppet.conf

  # systemd
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/systemd/puppet.service \
    "$pkgdir"/usr/lib/systemd/system/puppet.service
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/systemd/puppetmaster.service \
    "$pkgdir"/usr/lib/systemd/system/puppetmaster.service

  # Create puppet homedir
  install -d "$pkgdir"/var/lib/puppet/
}
