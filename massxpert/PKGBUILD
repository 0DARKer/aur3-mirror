# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=massxpert
pkgver=3.2.0
pkgrel=1
pkgdesc="A mass spectromic software suite based on Qt 4"
arch=('i686' 'x86_64')
url="http://massxpert.org/wiki/"
license=('GPL3')
depends=('qt')
makedepends=('cmake')
source=("http://download.tuxfamily.org/${pkgname}/source/${pkgname}-${pkgver}.tar.gz")
md5sums=('612141639fc4db5a53764ed80db0e0f8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix path to icon in the .desktop file
  sed -i "s/massxpert-icon-32.xpm/massxpert/" ${pkgname}.desktop

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_CXX_FLAGS=-Wno-error=unused-but-set-variable
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 gui/images/massxpert-icon-64.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
