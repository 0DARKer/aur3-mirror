# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Nokeyboardgrab patch by Procyon

pkgname=sdl-nokeyboardgrab
pkgver=1.2.15
pkgrel=1
pkgdesc="The sdl library, patched to prevent keyboard grab"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('LGPL')
depends=('glibc' 'libxext' 'libxrender' 'libx11')
makedepends=('alsa-lib' 'mesa' 'libpulse')
provides=('sdl=1.2.15')
conflicts=('sdl')
options=('!libtool')
source=(http://www.libsdl.org/release/SDL-${pkgver}.tar.gz
        sdl-1.2.14-fix-mouse-clicking.patch
        sdl-1.2.14-disable-mmx.patch)
md5sums=('9d96df8417572a2afb781a7c4c811a85'
         '04d8c179f125e04bcd4c9d60e013c2d7'
         'e5c16b7611f62c7cb25f0534eff68852')

build() {
  cd ${srcdir}/SDL-${pkgver}

  #THIS IS THE KEYBOARD UNGRAB PATCH
    sed -i '/XGrabKeyboard/{s_^_//_;n;s_^_//_}' src/video/x11/SDL_x11wm.c
  #It comments out the function call to XGrabKeyboard that spans two lines

  patch -Np1 -i $srcdir/sdl-1.2.14-fix-mouse-clicking.patch
  patch -Np1 -i $srcdir/sdl-1.2.14-disable-mmx.patch
  ./configure --prefix=/usr --disable-nasm --enable-alsa --enable-esd \
              --with-x --disable-rpath --disable-static
  make
}

package() {
  cd ${srcdir}/SDL-${pkgver}
  make DESTDIR=${pkgdir} install
}
