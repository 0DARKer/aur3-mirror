# Contributor: Procyon <procyon._HIDDEN_@gmail.com>
# Original Maintainer: Allan McRae

pkgname=sdl-nokeyboardgrab
pkgver=1.2.14
pkgrel=4
pkgdesc="The sdl library, libsdl, patched to prevent keyboard grab"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('LGPL')
depends=('glibc' 'libxext' 'libxrender' 'libx11')
provides=('sdl=1.2.14')
conflicts=('sdl')
makedepends=('alsa-lib' 'esound' 'mesa')
options=('!libtool')
source=(http://www.libsdl.org/release/SDL-${pkgver}.tar.gz
	sdl-1.2.14-fix-mouse-clicking.patch
	sdl-1.2.14-joystick-crash.diff)
md5sums=('e52086d1b508fa0b76c52ee30b55bec4'
	 '04d8c179f125e04bcd4c9d60e013c2d7'
	 '9d8890b3817736a5d365f7497f096634')

build() {
  cd ${srcdir}/SDL-${pkgver}
  #THIS IS THE KEYBOARD UNGRAB PATCH
  sed -i '/XGrabKeyboard/{s_^_//_;n;s_^_//_}' src/video/x11/SDL_x11wm.c
  patch -Np1 -i $srcdir/sdl-1.2.14-fix-mouse-clicking.patch
  patch -Np1 -i $srcdir/sdl-1.2.14-joystick-crash.diff
  ./configure --prefix=/usr --disable-nasm --enable-alsa --enable-esd \
	      --with-x --disable-rpath
  make || return 1
}

package() {
  cd ${srcdir}/SDL-${pkgver}
  make DESTDIR=${pkgdir} install
}
