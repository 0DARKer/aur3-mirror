# Maintainer: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>

pkgname=newsoul
pkgver=0.2
pkgrel=2
pkgdesc="Museek+, a daemon/server based Soulseek client, resurrected."
arch=('i686' 'x86_64')
url="https://github.com/KenjiTakahashi/newsoul"
license=('GPL3')
depends=(
    'json-c'
    'libevent'
    'taglib'
    'nettle'
    'sqlite'
)
optdepends=('python2-crypto: some python utils')
makedepends=('python2' 'premake')
provides=('newsoul')
source=("https://github.com/KenjiTakahashi/${pkgname}/archive/${pkgver}.tar.gz")
md5sums=('627a928173a01e63330be0a99a6895c4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"

    premake4 gmake
    make newsoul
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"

    premake4 --prefix="${pkgdir}" install

    cd ../python-bindings
    python2 setup.py install --root=${pkgdir}/ --optimize=1

    cd ../python-utils
    python2 setup.py install --root=${pkgdir}/ --optimize=1
}

# vim:set ts=4 sw=4 et:
