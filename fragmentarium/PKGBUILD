# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: <nixtrian@gmail.com>

_git=9a4fc26b59
_name=Fragmentarium
pkgname=fragmentarium
pkgver=0.9_git_$_git
pkgrel=1
pkgdesc="An IDE for exploring pixel based graphics on the GPU"
url="http://syntopia.github.com/Fragmentarium/"
license=('GPL3' 'LGPL')
arch=('i686' 'x86_64')
depends=('qt')
install="$pkgname.install"
source=("https://github.com/Syntopia/Fragmentarium/tarball/$_git")
md5sums=('ffbcc3d6f5866f4ade8ab1965467c0e5')

build() {
  cd "$srcdir/Syntopia-$_name-${_git::7}/$_name-Source"

  # set right path for examples and misc
  sed -e "/<</s/..\/../\/usr\/share\/$pkgname/" \
      -e "s/Examples\/Include/\/usr\/share\/$pkgname\/&/" \
      -i $_name/GUI/MainWindow.cpp

  qmake -project -after "CONFIG+=opengl" \
                 -after "QT+=xml opengl script"
  qmake && make
}

package() {
 cd "$srcdir/Syntopia-$_name-${_git::7}/$_name-Source"     

  # bin
  install -Dm755 $_name-Source "$pkgdir/usr/bin/$pkgname"

  # extra
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a Examples Misc "$pkgdir/usr/share/$pkgname"

  # desktop file
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 Icons/fileicons/$_name-256.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
