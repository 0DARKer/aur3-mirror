# Maintainer: kfgz <kfgz at interia dot pl>
# Contributor: Alexander Fehr <pizzapunk at gmail dot com>
# Contributor: Jose Valecillos <valecillosjg at gmail dot com>
# package based on acroread

pkgname=acroread-bin
pkgver=9.4.2
pkgrel=1
pkgdesc="A PDF file viewer."
arch=('i686' 'x86_64')
url="http://www.adobe.com/products/reader/"
license=('custom')
depends=('desktop-file-utils')
conflicts=('acroread'
           'acroread-chs'
           'acroread-cht'
           'acroread-de'
           'acroread-es'
           'acroread-fi'
           'acroread-fr'
           'acroread-it'
           'acroread-ja'
           'acroread-nl'
           'acroread-ptb'
           'bin32-acroread-chs'
           'bin32-acroread-es'
           'bin32-acroread-fr'
           'bin32-acroread-it')

if [[ "$CARCH" == 'i686' ]]; then
  depends+=('gtk2' 'mesa')
  optdepends=('libcanberra: XDG sound support')
else
  depends+=('lib32'-{'gtk2','mesa','libxml2','xcb-util'})
  optdepends=('lib32-libcanberra: XDG sound support')
fi

options=('!strip')
install=acroread.install
source=(ftp://ftp.adobe.com/pub/adobe/reader/unix/9.x/${pkgver}/enu/AdbeRdr${pkgver}-1_i486linux_enu.tar.bz2
        acroread.sh)
md5sums=('307aaaf22e8c08958927dfabb0f4d745'
         'ff77175ad4650e8ffaf05f4eae36893d')

build() {
  cd "$srcdir/AdobeReader"
  bsdtar -xf COMMON.TAR
  bsdtar -xf ILINXR.TAR
  cd Adobe/Reader9

  msg2 "Installing Main Files..."
  mkdir -p "$pkgdir/opt/Adobe/Reader9"
  cp -a * "$pkgdir/opt/Adobe/Reader9"
  echo done

  msg2 "Installing Bin Files..."
  mkdir -p "$pkgdir/usr/bin"
  ln -s /opt/Adobe/Reader9/bin/acroread "$pkgdir/usr/bin"
  install -Dm644 Resource/Shell/acroread.1.gz \
          ${pkgdir}/usr/share/man/man1/acroread.1.gz
  install -Dm644 Resource/Shell/acroread_tab \
          ${pkgdir}/etc/bash_completion.d/acroread
  install -Dm755 ${srcdir}/acroread.sh \
          ${pkgdir}/etc/profile.d/acroread.sh
  sed -i "s/_filedir/_filedir_acroread/" \
          "$pkgdir/etc/bash_completion.d/acroread" 
  echo done

  msg2 "Installing Broswer Plugin..."
  mkdir -p "$pkgdir/usr/lib/mozilla/plugins"
  ln -s /opt/Adobe/Reader9/Browser/intellinux/nppdf.so \
          "$pkgdir/usr/lib/mozilla/plugins"
  echo done
	
  msg2 "Installing Icon Resource..."
  install -D -m644 Resource/Support/AdobeReader.desktop \
          "$pkgdir/usr/share/applications/acroread.desktop"
  sed -i '/^Icon/s|AdobeReader9|acroread|' \
          "$pkgdir/usr/share/applications/acroread.desktop"
  install -D -m644 Resource/Icons/64x64/AdobeReader9.png \
          "$pkgdir/usr/share/pixmaps/acroread.png"
  echo done

  msg2 "Installing License..."
  install -D -m644 Reader/Legal/en_US/License.txt \
          "$pkgdir/usr/share/licenses/acroread/License.txt"
  echo done
}
