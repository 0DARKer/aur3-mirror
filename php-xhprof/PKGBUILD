# Contributor: Sebastien Duquette <ekse.0x@gmail.com>
pkgname=php-xhprof
pkgver=0.9.2
pkgrel=1
pkgdesc="A Hierarchical Profiler for PHP"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/xhprof"
license=('Apache')
depends=('php')
makedepends=()
optdepends=('graphviz: to generate callgraphs in reports')
backup=('etc/php/conf.d/xhprof.ini')
options=()
install=php-xhprof.install
source=("http://pecl.php.net/get/xhprof-$pkgver.tgz")
md5sums=('ae40b153d157e6369a32e2c1a59a61ec')

build() {
  cd "$srcdir/xhprof-$pkgver/extension/"

  phpize || return 1 
  ./configure --prefix=/usr --with-php-config=$(which php-config)
  make || return 1
  make INSTALL_ROOT="$pkgdir/" install
  echo ';extension=xhprof.so' > xhprof.ini
  install -D -m644 xhprof.ini "$pkgdir/etc/php/conf.d/xhprof.ini"

  # HTML Report interface
  cd ".."
  mkdir -p $pkgdir/usr/share/webapps/xhprof/
  cp -ra xhprof_html $pkgdir/usr/share/webapps/xhprof/
  cp -ra xhprof_lib $pkgdir/usr/share/webapps/xhprof/

  # apache
  mkdir -p $pkgdir/etc/webapps/xhprof/
  cat >$pkgdir/etc/webapps/xhprof/xhprof.example.conf <<EOF
        Alias /xhprof "/usr/share/webapps/xhprof/xhprof_html"
        <Directory "/usr/share/webapps/xhprof/xhprof_html">
                AllowOverride All
                Options FollowSymlinks
                Order allow,deny
                Allow from all
        </Directory>
EOF

}

#Y vim:set ts=2 sw=2 et:
