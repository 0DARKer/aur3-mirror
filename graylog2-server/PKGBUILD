# Maintainer: Spider.007 <aur@spider007.net>

pkgname=graylog2-server
pkgver=0.20.6
pkgrel=1
pkgdesc="Graylog2 is an open source syslog implementation that stores your logs in ElasticSearch and MongoDB"
arch=(any)
url="http://graylog2.org/"
license=('GPL')
depends=(java-runtime elasticsearch=0.90.10 mongodb)
install=graylog2-server.install
backup=(
	etc/graylog2.conf
)
source=(
	https://github.com/Graylog2/graylog2-server/releases/download/${pkgver}/${pkgname}-${pkgver}.tgz
	graylog2-tmpfiles.conf
	graylog2.service
)
sha256sums=('0a863856a96d709cb76bc64e23950a6b706e30a05e0248315c8b1a1fb17f8c80'
            '28f7fbcc3b5d431c64c983c39714f6f925caac4bb8d4e1ef4dc99c4ba1979cfd'
            '669168ac09b77077fc229c7c5ef1ddca3e6d23b77cd08df08323d409b99c6f3a')

package() {
	cd "$srcdir/$pkgname-${pkgver}"

	install -Dm644 graylog2-server.jar "$pkgdir/usr/lib/graylog2-server.jar"
	install -Dm644 "$srcdir/graylog2-tmpfiles.conf" "$pkgdir//usr/lib/tmpfiles.d/graylog2-server.conf"
	install -Dm644 "$srcdir/graylog2.service" "$pkgdir/usr/lib/systemd/system/graylog2.service"

	install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	cp README.markdown COPYING "$pkgdir/usr/share/doc/$pkgname"

	install -Dm644 graylog2.conf.example $pkgdir/etc/graylog2.conf
}
