# Maintainer: Spider.007 <aur@spider007.net>

pkgname=graylog2-server
pkgver=0.20.0
pkgrel=2
pkgdesc="Graylog2 is an open source syslog implementation that stores your logs in ElasticSearch and MongoDB"
arch=(any)
url="http://graylog2.org/"
license=('GPL')
depends=(java-runtime elasticsearch=0.90.10)
optdepends=(
	"mongodb"
	"zeromq"
	"apache-activemq"
	"rabbitmq"
)
backup=(
	etc/graylog2.conf
)
source=(
	https://github.com/Graylog2/graylog2-server/releases/download/${pkgver}/${pkgname}-${pkgver}.tgz
	graylog2.service
)
sha256sums=('137bbe93aa63aac7778c36ef158e754141b0ff6e3c12028928a46a433eab75ee'
            '16710a0e524309b9bd403b368c714969e1a46434f7c6f8a6ee8709ee908b88d4')

package() {
	cd "$srcdir/$pkgname-${pkgver}"

	install -Dm644 graylog2-server.jar "$pkgdir/usr/lib/graylog2-server.jar"
	install -Dm644 "$srcdir/graylog2.service" "$pkgdir/usr/lib/systemd/system/graylog2.service"

	install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	cp README.markdown COPYING "$pkgdir/usr/share/doc/$pkgname"

	install -Dm644 graylog2.conf.example $pkgdir/etc/graylog2.conf
}
