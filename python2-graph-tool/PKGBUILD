# Maintainer: Daniel MÃ¼llner <muellner@math.stanford.edu>

# Remove the --enable-openmp flag below if you do not want parallelization.

pkgname=python2-graph-tool
pkgver=2.2.22
pkgrel=1
pkgdesc='A Python module for manipulation and statistical analysis of graphs'
arch=('i686' 'x86_64')
url='http://projects.skewed.de/graph-tool/'
license=(GPL3)
depends=(boost python2 expat python2-scipy python2-numpy cgal graphviz)
makedepends=(sparsehash)
optdepends=('cairomm: graph drawing'
'python2-cairo: graph drawing'
'python2-matplotlib: graph drawing')
options=(!libtool)
source=("http://downloads.skewed.de/graph-tool/graph-tool-$pkgver.tar.bz2")
sha256sums=(c889466c82e972688bb2b477a9ab2af9900b8c80451630a275ed0eca34180715)

build() {
  cd "$srcdir/graph-tool-$pkgver"
  ./configure --enable-openmp --prefix='/usr' --docdir="/usr/share/doc/$pkgname" PYTHON=python2
  make
}

package() {
  cd "$srcdir/graph-tool-$pkgver"
  # Remove information about the source directory, which is temporary.
  sed -i 's/#define PACKAGE_SOURCE_DIR ".*"/#define PACKAGE_SOURCE_DIR ""/' config.h
  make DESTDIR="$pkgdir/" install
}
