# Maintainer: Daniel MÃ¼llner <muellner@math.stanford.edu>

# Remove the --enable-openmp flag below if you do not want parallelization.

pkgname=python2-graph-tool
pkgver=2.2.18
pkgrel=3
pkgdesc='A Python module for manipulation and statistical analysis of graphs'
arch=('any')
url='http://projects.skewed.de/graph-tool/'
license=(GPL3)
depends=(gcc boost python2 expat python2-scipy python2-numpy cgal graphviz)
optdepends=('cairomm: graph drawing'
'python2-cairo: graph drawing'
'python2-matplotlib: graph drawing')
source=("http://downloads.skewed.de/graph-tool/graph-tool-$pkgver.tar.bz2")
sha256sums=(939ce610143c4cd03988f1e021eba02c2e30aac0451d036c0a2872d5b7f94dbd)

build() {
  cd "$srcdir/graph-tool-$pkgver"
  ./configure --enable-openmp --prefix='/usr' PYTHON=python2
  make
}

package() {
  cd "$srcdir/graph-tool-$pkgver"
  # Remove information about the source directory, which is temporary.
  sed -i 's/#define PACKAGE_SOURCE_DIR ".*"/#define PACKAGE_SOURCE_DIR ""/' config.h
  sed -i "s/#define PACKAGE_DOC_DIR \"\/usr\/doc\/graph-tool\"/#define PACKAGE_DOC_DIR \"\/usr\/share\/doc\/$pkgname\"/" config.h
  make DESTDIR="$pkgdir/" PACKAGE_DOC_DIR="/usr/share/doc/$pkgname" install
}
