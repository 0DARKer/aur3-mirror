#~~~~~~~~~#
#NOTE: before building please download FirePro_8.911.3.1_Linux_X32X64_132092.zip using this site:
#http://support.amd.com/us/gpudownload/fire/Pages/fire_linux.aspx
# and move it next to this PKGBUILD


# Maintainer: Vi0L0 <vi0l093@gmail.com>
# Great Contributor: Eduardo "kensai" Romero (previous maintainer)
# Great Contributor: Shen Miren <dickeny@gmail.com> (build_module/hook idea and code)

PKGEXT='.pkg.tar.gz'

pkgname=catalyst-firepro
pkgver=8.911.3
_reldate=12.2
pkgrel=1
pkgdesc="AMD/ATI drivers for FirePro/GL/MV brand cards. catalyst-hook + catalyst-utils + lib32-catalyst-utils"
arch=('i686' 'x86_64')
url="http://www.ati.amd.com"
license=('custom')
depends=('xorg-server>=1.7.0' 'xorg-server<1.12.0' 'linux>=3.0' 'linux<3.3' 'linux-headers' 'netkit-bsd-finger' 'libxrandr' 'libsm' 'fontconfig' 'libxcursor' 'libxi' 'gcc-libs' 'gcc>4.0.0' 'make')
optdepends=('qt: to run ATi Catalyst Control Center (amdcccle)')
replaces=('catalyst' 'catalyst-nostock' 'libgl')
conflicts=('catalyst' 'catalyst-test' 'catalyst-utils' 'nvidia' 'libgl' 'xf86-video-ati' 'xf86-video-radeonhd' 'ati-dri')
provides=("catalyst-utils=${_reldate}" 'libgl' "libatical=${_reldate}" "catalyst=${_reldate}")

if [ "${CARCH}" = "x86_64" ]; then
 warning "x86_64 system detected"
 warning "[multilib] repository must be uncommented in /etc/pacman.conf to add lib32-catalyst-utils to the package"
  if [[ `cat /etc/pacman.conf | grep -c "#\[multilib]"` = 0 ]]; then
    warning "OK, lib32-catalyst-utils will be added to the package"   
    depends+=('lib32-libxext' 'lib32-libdrm')
    conflicts+=('lib32-libgl' 'lib32-nvidia-utils' 'lib32-catalyst-utils')
    replaces+=('lib32-ati-fglrx-utils' 'lib32-fglrx-utils' 'lib32-libgl' 'lib32-catalyst-utils')
    provides+=('lib32-libgl' "lib32-catalyst-utils=${pkgver}")
      else
	warning "lib32-catalyst-utils will NOT be added to the package"
  fi
fi

install=${pkgname}.install

source=(
    http://www2.ati.com/drivers/firepro/FirePro_8.911.3.1_Linux_X32X64_132092.zip
    catalyst.sh
    lib32-catalyst.sh
    amdcccle.desktop
    atieventsd.sh
    hook-fglrx
    catalyst_build_module
    ati_make.sh
    makefile_compat.patch)

md5sums=('b60f7c76cd2e7a55ffed19ac7dc55f56'
         'bdafe749e046bfddee2d1c5e90eabd83'
	 'af7fb8ee4fc96fd54c5b483e33dc71c4'
         '4efa8414a8fe9eeb50da38b5522ef81d'
         'f729bf913613f49b0b9759c246058a87'
         '9126e1ef0c724f8b57d3ac0fe77efe2f'
         'cc5ecffd2e815c3ed05f39753ce6dc53'
	 '210fd25311893117fa6bd609eda086e1'
	 '3e1b82bd69774ea808da69c983d6a43b')

build() {
  ## Unpack archive
    msg "Unpacking archive ..."
    /bin/sh ./fglrx-8.911.3.1/amd-driver-installer-8.911.3.1-x86.x86_64.run --extract archive_files
}

package() {
##catalyst-utils section
  ## Install userspace tools and libraries
    # Create directories
      install -m755 -d "${pkgdir}/etc/ati"
      install -m755 -d "${pkgdir}/etc/rc.d"
      install -m755 -d "${pkgdir}/etc/profile.d"
      install -m755 -d "${pkgdir}/etc/acpi/events"
      install -m755 -d "${pkgdir}/etc/security/console.apps"
      install -m755 -d "${pkgdir}/etc/OpenCL/vendors"  # since 11.11

      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/dri"
      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/drivers"
      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions/fglrx" # since 11.4
      install -m755 -d "${pkgdir}/usr/lib/xorg/modules/linux"
      install -m755 -d "${pkgdir}/usr/lib/dri"
      install -m755 -d "${pkgdir}/usr/lib/fglrx" # since 11.4

      install -m755 -d "${pkgdir}/usr/bin"
      install -m755 -d "${pkgdir}/usr/sbin"

      install -m755 -d "${pkgdir}/usr/include/X11/extensions"
      install -m755 -d "${pkgdir}/usr/include/GL"

      install -m755 -d "${pkgdir}/usr/share/applications"
      install -m755 -d "${pkgdir}/usr/share/ati/amdcccle"
      install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
      install -m755 -d "${pkgdir}/usr/share/man/man8"
      install -m755 -d "${pkgdir}/usr/share/pixmaps"

    # X.org driver
      if [ "${CARCH}" = "i686" ]; then
	cd "${srcdir}/archive_files/xpic/usr/X11R6/lib/modules" || return 1
      elif [ "${CARCH}" = "x86_64" ]; then
	cd "${srcdir}/archive_files/xpic_64a/usr/X11R6/lib64/modules" || return 1
      fi

     # *.a added in 11.2, and removed in 11.3...
      #install -m644 *.a "${pkgdir}/usr/lib/xorg/modules/" || return 1
      install -m755 *.so "${pkgdir}/usr/lib/xorg/modules/" || return 1
      install -m755 drivers/*.so "${pkgdir}/usr/lib/xorg/modules/drivers/" || return 1
      install -m755 linux/*.so "${pkgdir}/usr/lib/xorg/modules/linux/" || return 1
      #install -m755 extensions/libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/" || return 1 #before 11.4
      install -m755 extensions/fglrx/fglrx-libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/fglrx/fglrx-libglx.so" || return 1 # since 11.5
      ln -snf /usr/lib/xorg/modules/extensions/fglrx/fglrx-libglx.so "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" # since 11.4
      #install -m755 extensions/libdri.so "${pkgdir}/usr/lib/xorg/modules/extensions/libdri.ati" || return 1

    # Controlcenter / libraries
      if [ "${CARCH}" = "i686" ]; then
	cd "${srcdir}/archive_files/arch/x86/usr" || return 1
	_lib=lib
      elif [ "${CARCH}" = "x86_64" ]; then
	cd "${srcdir}/archive_files/arch/x86_64/usr" || return 1
	_lib=lib64
      fi

      install -m755 X11R6/bin/* "${pkgdir}/usr/bin/" || return 1
      install -m755 sbin/* "${pkgdir}/usr/sbin/" || return 1
      #install -m755 X11R6/${_lib}/*.so* "${pkgdir}/usr/lib/" || return #before 11.4
      install -m755 X11R6/${_lib}/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/fglrx" || return 1 # since 11.5
      ln -snf /usr/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/fglrx/libGL.so.1.2" # since 11.4
      ln -snf /usr/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/fglrx-libGL.so.1.2" # since 11.4
      ln -snf /usr/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/libGL.so.1.2" # since 11.4
      ln -snf /usr/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/libGL.so.1" # since 11.4
      ln -snf /usr/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib/libGL.so" # since 11.4
      install -m755 X11R6/${_lib}/libAMDXvBA.so.1.0 "${pkgdir}/usr/lib/" || return 1 # since 11.4
      ln -snf libAMDXvBA.so.1.0 "${pkgdir}/usr/lib/libAMDXvBA.so.1" # since 11.4
      ln -snf libAMDXvBA.so.1.0 "${pkgdir}/usr/lib/libAMDXvBA.so" # since 11.4
      install -m755 X11R6/${_lib}/libatiadlxx.so "${pkgdir}/usr/lib/" || return 1 # since 11.4
      install -m755 X11R6/${_lib}/libfglrx_dm.so.1.0 "${pkgdir}/usr/lib/" || return 1 # since 11.4
      install -m755 X11R6/${_lib}/libXvBAW.so.1.0 "${pkgdir}/usr/lib/" || return 1 # since 11.4
      ln -snf libXvBAW.so.1.0 "${pkgdir}/usr/lib/libXvBAW.so.1" # since 11.4
      ln -snf libXvBAW.so.1.0 "${pkgdir}/usr/lib/libXvBAW.so" # since 11.4
      install -m644 X11R6/${_lib}/*.a "${pkgdir}/usr/lib/" || return 1 # really needed?
      install -m644 X11R6/${_lib}/*.cap "${pkgdir}/usr/lib/" || return 1
      install -m755 X11R6/${_lib}/modules/dri/*.so "${pkgdir}/usr/lib/xorg/modules/dri/" || return 1
      install -m755 ${_lib}/*.so* "${pkgdir}/usr/lib/" || return 1

    ## QT libs (only 2 files) - un-comment 2 lines below if you don't want to install qt package
#      install -m755 -d "${pkgdir}/usr/share/ati/${_lib}"
#      install -m755 share/ati/${_lib}/*.so* "${pkgdir}/usr/share/ati/${_lib}/" || return 1

      ln -snf /usr/lib/xorg/modules/dri/fglrx_dri.so ${pkgdir}/usr/lib/dri/fglrx_dri.so
      ln -snf libfglrx_dm.so.1.0 "${pkgdir}/usr/lib/libfglrx_dm.so.1"
      ln -snf libfglrx_dm.so.1.0 "${pkgdir}/usr/lib/libfglrx_dm.so"
      ln -snf libatiuki.so.1.0 "${pkgdir}/usr/lib/libatiuki.so.1"
      ln -snf libatiuki.so.1.0 "${pkgdir}/usr/lib/libatiuki.so"
      ln -snf libOpenCL.so.1 "${pkgdir}/usr/lib/libOpenCL.so" # since 11.11


      cd "${srcdir}"/archive_files/common
      install -m644 etc/ati/* "${pkgdir}/etc/ati/" || return 1
      chmod 755 "${pkgdir}/etc/ati/authatieventsd.sh" || return 1

      #security provided with 10.9, is it working fine?
      install -m644 etc/security/console.apps/amdcccle-su "${pkgdir}/etc/security/console.apps/" || return 1

     # *.h removed in 11.3...
      #install -m644 usr/X11R6/include/X11/extensions/*.h "${pkgdir}/usr/include/X11/extensions/" || return 1
      install -m755 usr/X11R6/bin/amdupdaterandrconfig "${pkgdir}/usr/bin/" || return 1
      install -m644 usr/include/GL/*.h "${pkgdir}/usr/include/GL/" || return 1
      install -m755 usr/sbin/*.sh "${pkgdir}/usr/sbin/" || return 1
      install -m644 usr/share/ati/amdcccle/* "${pkgdir}/usr/share/ati/amdcccle/" || return 1
      install -m644 usr/share/icons/*.xpm "${pkgdir}/usr/share/pixmaps/" || return 1
      install -m644 usr/share/man/man8/*.8 "${pkgdir}/usr/share/man/man8/" || return 1
      install -m644 "${srcdir}/amdcccle.desktop" "${pkgdir}/usr/share/applications/" || return 1

    # ACPI example files
      install -m755 usr/share/doc/fglrx/examples/etc/acpi/*.sh "${pkgdir}/etc/acpi/" || return 1
      sed -i -e 's/usr\/X11R6/usr/g' "${pkgdir}/etc/acpi/ati-powermode.sh" || return 1
      install -m644 usr/share/doc/fglrx/examples/etc/acpi/events/* "${pkgdir}/etc/acpi/events/" || return 1

    # Add ATI Events Daemon launcher
      install -m755 "${srcdir}/atieventsd.sh" "${pkgdir}/etc/rc.d/atieventsd" || return 1

    # thanks to cerebral, we dont need that damned symlink
      install -m755 "${srcdir}/catalyst.sh" "${pkgdir}/etc/profile.d/" || return 1

    # License
      install -m644 "${srcdir}/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1


    # since 11.11 : opencl files
      if [ "${CARCH}" = "i686" ]; then
	cd "${srcdir}/archive_files/arch/x86" || return 1
	_arc=32
      elif [ "${CARCH}" = "x86_64" ]; then
	cd "${srcdir}/archive_files/arch/x86_64" || return 1
	_arc=64
      fi

      # since 11.11: amd's vendor file for it's opencl library
      install -m644 etc/OpenCL/vendors/amdocl${_arc}.icd "${pkgdir}/etc/OpenCL/vendors/" || return 1

      # since 11.11: clinfo binary
      install -m755 usr/bin/clinfo "${pkgdir}/usr/bin/" || return 1


##catalyst-hook section
    # Prepare modules source files
      _archdir=x86_64
      test "${CARCH}" = "i686" && _archdir=x86
      cd "${srcdir}/archive_files/"
      install -m755 -d "${pkgdir}/usr/share/ati/build_mod" || return 1
      install -m644 common/lib/modules/fglrx/build_mod/*.c \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m644 common/lib/modules/fglrx/build_mod/*.h \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m644 common/lib/modules/fglrx/build_mod/2.6.x/Makefile \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m644 arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a \
                "${pkgdir}/usr/share/ati/build_mod/" || return 1
      install -m755 -d "${pkgdir}/usr/bin" || return 1
      install -m755 "${srcdir}/catalyst_build_module" "${pkgdir}/usr/bin" || return 1

    # modified ati's make.sh script
      install -m755 "${srcdir}/ati_make.sh" "${pkgdir}/usr/share/ati/build_mod/" || return 1

    # makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
    # works only in combination with ati_make.sh script
      install -m644 "${srcdir}/makefile_compat.patch" "${pkgdir}/usr/share/ati/build_mod/" || return 1


    # optional patch
#      for ver in ${patch_ver}; do
#      install -m644 "${srcdir}/${patch_file}" \
#                "${pkgdir}/usr/share/ati/build_mod/${ver}.patch" || return 1
#      done

    # hook fglrx
      install -m755 -d "${pkgdir}/lib/initcpio/install" || return 1
      install -m644 "${srcdir}/hook-fglrx" "${pkgdir}/lib/initcpio/install/fglrx" || return 1



##lib32-catalyst-utils section
      if [ "${CARCH}" = "x86_64" ] && [[ `cat /etc/pacman.conf | grep -c "#\[multilib]"` = 0 ]]; then
	cd ${srcdir}
	install -D -m755 lib32-catalyst.sh ${pkgdir}/etc/profile.d/lib32-catalyst.sh || return 1

	# Install lib32 libraries
	cd "${srcdir}/archive_files/arch/x86/usr" || return 1
	install -dm755 "${pkgdir}/usr/lib32" || return 1
	install -dm755 "${pkgdir}/usr/lib32/fglrx" || return 1 # since 11.4
	install -dm755 "${pkgdir}/usr/lib32/xorg/modules/dri" || return 1
	install -m755 lib/*.so* "${pkgdir}/usr/lib32/" || return 1
	#install -m755 X11R6/lib/*.so* "${pkgdir}/usr/lib32/" || return 1 #before 11.4

	install -m755 X11R6/lib/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/fglrx" || return 1 # since 11.5
	ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/fglrx/libGL.so.1.2" # since 11.4
	ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/fglrx-libGL.so.1.2" # since 11.4
	ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/libGL.so.1.2" # since 11.4
	ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/libGL.so.1" # since 11.4
	ln -sf /usr/lib32/fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/libGL.so" # since 11.4
	install -m755 X11R6/lib/libAMDXvBA.so.1.0 "${pkgdir}/usr/lib32/" || return 1 # since 11.4
	install -m755 X11R6/lib/libatiadlxx.so "${pkgdir}/usr/lib32/" || return 1 # since 11.4
	install -m755 X11R6/lib/libfglrx_dm.so.1.0 "${pkgdir}/usr/lib32/" || return 1 # since 11.4
	install -m755 X11R6/lib/libXvBAW.so.1.0 "${pkgdir}/usr/lib32/" || return 1 # since 11.4

	install -m755 X11R6/lib/modules/dri/*.so "${pkgdir}/usr/lib32/xorg/modules/dri/" || return 1
	ln -snf /usr/lib32/xorg/modules/dri "${pkgdir}/usr/lib32/dri" || return 1

	cd "$pkgdir/usr/lib32/" || return 1
	ln -sf libfglrx_dm.so.1.0 libfglrx_dm.so.1 || return 1
	ln -sf libAMDXvBA.so.1.0 libAMDXvBA.so.1 || return 1
	ln -sf libXvBAW.so.1.0 libXvBAW.so.1 || return 1
	ln -sf libatiuki.so.1.0 libatiuki.so.1 || return 1
	ln -sf libatiuki.so.1.0 libatiuki.so || return 1
	ln -sf libOpenCL.so.1 libOpenCL.so  || return 1 # since 11.11
      fi
}
