# Maintainer: Viech
# Contributor: Martin F. Schumann <mfs@mfs.name>

pkgname=unvanquished
pkgver=0.9.0
pkgrel=1
pkgdesc="A team-based fps/rts hybrid game which pits aliens against humans. Monthly release that can be played on official servers."
arch=('x86_64' 'i686')
url="http://www.unvanquished.net"
license=('GPL3')
depends=('curl' 'freetype2' 'glew' 'gmp' 'libjpeg-turbo' 'ncurses' 'libogg' 'libpng' 'sdl' 'libvorbis' 'zlib' 'libwebp>=0.2.0' 'libtheora' 'nettle' 'speex' 'xvidcore' 'openal' 'xdg-utils' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme')
makedepends=('git' 'cmake')
provides=('unvanquished')
conflicts=('unvanquished-maps' 'unvanquished-git')
options=('emptydirs')
backup=('srv/unvanquished/main/server.cfg' 'srv/unvanquished/main/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=('unvanquished.install' 'unvanquished.sh' 'unvanquished-server.sh' 'unvanquished-update-paks.sh' 'unvanquished-rc.sh' 'unvanquished.desktop' 'ChangeLog')

md5sums=('ce5398923fa900f2f18ff58aca7bbe35'
         '09ed4b00045453b68a0a6fda07299b0e'
         'b47705a01575c2c1c2ef7bb4a5bcf8d7'
         '1da9acdc7e8f83610dd6398ba2b38e72'
         '759173428e2d8686d2bb840a9948fb8b'
         'ac69d49b3c665d274d0ab58870220522'
         '4c90913e0723f0cc81ad2304b5764c32')

# I'm using __git* instead of _git* so makepkg leaves pkgver where I want it
__gitroot="https://github.com/Unvanquished/Unvanquished.git"
__gitname=$pkgname
__gitver="v$pkgver"

build() {
	cd $srcdir
	
	msg "Connecting to GIT server ..."

	if [[ -d $__gitname ]]; then
		msg "Updating local files ..."
		cd $__gitname
		git fetch
		git checkout $__gitver
	else
		msg "Cloning repository ..."
		git clone $__gitroot $__gitname
		
		msg "Checking out $__gitver ..."
		cd $__gitname
		git checkout $__gitver
	fi

	msg "The local files are up to date."
	msg "Starting build ..."

	cd $srcdir/$pkgname
	
	cmake -D CMAKE_INSTALL_PREFIX="" . && make
}

package() {
	# create installation dirs
	cd $pkgdir

	install -d opt/$pkgname usr/bin etc/rc.d usr/share/applications var/cache/$pkgname/update-paks
	install -dm 775 srv/$pkgname/main

	# install starters
	cd $srcdir

	install -m 755 $pkgname.sh $pkgdir/usr/bin/$pkgname
	install -m 755 $pkgname-server.sh $pkgdir/usr/bin/$pkgname-server
	install -m 755 $pkgname-update-paks.sh $pkgdir/usr/bin/$pkgname-update-paks
	install -m 755 $pkgname-rc.sh $pkgdir/etc/rc.d/$pkgname
	install -m 644 $pkgname.desktop $pkgdir/usr/share/applications/
	
	# install base files
	cd $srcdir/$pkgname
	
	install -m 755 daemon daemonded *.so download-pk3.sh $pkgdir/opt/$pkgname/
	install -m 644 COPYING.txt GPL.txt README.txt debian/$pkgname.png $pkgdir/opt/$pkgname/
	
	cp -pr main $pkgdir/opt/$pkgname/
	chown root:root $pkgdir/opt/$pkgname/main
	
	# setup server directory 
	install -m 660 debian/config/* $pkgdir/srv/$pkgname/main/
	
	cd $pkgdir/srv/$pkgname/

	ln -s . .Unvanquished

	# link game binarys under suitable name
	cd $pkgdir/opt/$pkgname/
	
	ln -s daemon unvanquished
	ln -s daemonded unvanquishedded
}
