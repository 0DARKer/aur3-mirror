# Maintainer: Viech
# Contributor: Martin F. Schumann <mfs@mfs.name>

pkgname=unvanquished
pkgver=0.10.0
pkgrel=1
pkgdesc="A team-based fps/rts hybrid game which pits aliens against humans. Monthly release that can be played on official servers."
arch=('x86_64' 'i686')
url="http://www.unvanquished.net"
license=('GPL3')
depends=('curl' 'freetype2' 'glew' 'gmp' 'libjpeg-turbo' 'ncurses' 'libogg' 'libpng' 'sdl' 'libvorbis' 'zlib' 'libwebp>=0.2.0' 'libtheora' 'nettle' 'speex' 'xvidcore' 'openal' 'xdg-utils' 'desktop-file-utils' 'shared-mime-info' 'hicolor-icon-theme')
makedepends=('git' 'cmake')
provides=('unvanquished')
conflicts=('unvanquished-maps' 'unvanquished-git')
options=('emptydirs')
backup=('srv/unvanquished/main/server.cfg' 'srv/unvanquished/main/maprotation.cfg')
changelog='ChangeLog'
install='unvanquished.install'
source=('unvanquished.install' 'unvanquished.sh' 'unvanquished-server.sh' 'unvanquished-update-paks.sh' 'unvanquished-rc.sh' 'unvanquished.desktop' 'ChangeLog')

md5sums=('ce5398923fa900f2f18ff58aca7bbe35'
         '09ed4b00045453b68a0a6fda07299b0e'
         'b47705a01575c2c1c2ef7bb4a5bcf8d7'
         '1da9acdc7e8f83610dd6398ba2b38e72'
         '759173428e2d8686d2bb840a9948fb8b'
         'ac69d49b3c665d274d0ab58870220522'
         'a289d20e3e2c3b9f0c4bce10b2b84345')

# I'm using _name for compliance with the *-git version of this package
_name=$pkgname

# I'm using __git* instead of _git* so makepkg leaves pkgver alone
__gitroot="https://github.com/Unvanquished/Unvanquished.git"
__gitver="v$pkgver"

build() {
	cd $srcdir
	
	msg "Connecting to GIT server ..."

	if [[ -d $_name ]]; then
		msg "Updating local files ..."
		cd $_name
		git fetch
		git checkout $__gitver
	else
		msg "Cloning repository ..."
		git clone $__gitroot $_name
		
		msg "Checking out $__gitver ..."
		cd $_name
		git checkout $__gitver
	fi

	msg "The local files are up to date."
	
	msg "Starting build ..."
	cd $srcdir/$_name
	cmake -D CMAKE_INSTALL_PREFIX="" . && make
}

package() {
	# create installation dirs
	cd $pkgdir

	install -d opt/$_name usr/bin etc/rc.d usr/share/applications var/cache/$_name/update-paks
	install -dm 775 srv/$_name/main

	# install starters
	cd $srcdir

	install -m 755 $_name.sh $pkgdir/usr/bin/$_name
	install -m 755 $_name-server.sh $pkgdir/usr/bin/$_name-server
	install -m 755 $_name-update-paks.sh $pkgdir/usr/bin/$_name-update-paks
	install -m 755 $_name-rc.sh $pkgdir/etc/rc.d/$_name
	install -m 644 $_name.desktop $pkgdir/usr/share/applications/
	
	# install base files
	cd $srcdir/$_name
	
	install -m 755 daemon daemonded *.so download-pk3.sh $pkgdir/opt/$_name/
	install -m 644 COPYING.txt GPL.txt README.txt debian/$_name.png $pkgdir/opt/$_name/
	
	cp -pr main $pkgdir/opt/$_name/
	chown -R root:root $pkgdir/opt/$_name/main
	
	# setup server directory 
	install -m 660 debian/config/* $pkgdir/srv/$_name/main/
	
	cd $pkgdir/srv/$_name/

	ln -s . .Unvanquished

	# link game binarys under suitable name
	cd $pkgdir/opt/$_name/
	
	ln -s daemon $_name
	ln -s daemonded ${_name}ded
}
