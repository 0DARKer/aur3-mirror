pkgname=sympa
pkgver=6.1.11
pkgrel=1
pkgdesc="Sympa is a mailing list management software"
url="http://www.sympa.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('perl-html-stripscripts-parser' 'perl-mailtools' 'perl-mime-encwords' 'perl-file-copy-recursive'
         'perl-template-toolkit' 'perl-io-stringy' 'perl-mime-tools' 'perl-term-progressbar'
         'perl-xml-libxml' 'perl-dbi' 'perl-dbd-mysql')
source=(http://www.sympa.org/distribution/$pkgname-$pkgver.tar.gz
        sympa.rc)
md5sums=('297f31cd74d5e4f5b2dd5d1e35e81fd0'
         '33862041b6f8bbda9a2ebeddcbdafe91')
install=sympa.install

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PATH=$PATH:/usr/lib/perl5/core_perl/bin
  ./configure --prefix=/srv/sympa \
              --sysconfdir=/srv/sympa/etc \
              --with-confdir=/srv/sympa/etc \
              --enable-secure \
              --with-sendmail_aliases=/srv/sympa/etc/mail/aliases \
              --with-virtual_aliases=/srv/sympa/etc/mail/virtual
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir/etc/rc.d/init.d"
  rm -rf "$pkgdir/sample"
  install -D -m755 "$srcdir/sympa.rc" "$pkgdir/etc/rc.d/sympa"
  install -d "$pkgdir/srv/sympa/var/lock/subsys"

  chown -R 52:52 "$pkgdir/srv/sympa"
}
