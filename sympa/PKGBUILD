# Maintainer: Stefan Melmuk <stefan at diebin dot at>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# Contributor: Dimitri Pekarovsky <dimitri dot pekarovsky where gmail dot com>

pkgname=sympa
pkgver=6.1.20
pkgrel=8
pkgdesc="A mailing list management software"
url="http://www.sympa.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=(
  # Available in standard repos
  'perl-archive-zip' 'perl-dbd-mysql' 'perl-crypt-openssl-bignum'
  'perl-crypt-openssl-rsa' 'perl-fcgi' 'perl-file-copy-recursive'
  'perl-html-formattext' 'perl-mail-dkim' 'perl-soap-lite'
  'perl-term-progressbar' 'perl-template-toolkit' 'perl-xml-libxml'

  # Available in AUR
  'perl-html-stripscripts-parser' 'perl-digest-sha' 'perl-encode'
  'perl-crypt-ciphersaber' 'perl-file-temp' 'perl-mime-base64'
  'perl-mime-encwords' 'perl-net-netmask'
  'perl-regexp-common' 'perl-test-harness'
)
install="sympa.install"
source=("http://www.sympa.org/distribution/$pkgname-$pkgver.tar.gz"
        'sympa.install'
        'sympa.service'
        'sympa-archived.service'
        'sympa-bounced.service'
        'sympa-bulk.service'
        'sympa-task_manager.service')
md5sums=('e5504be2206e49e2af80ad23984a44ed'
         'd9ec10d34aff3c79726873181accce19'
         '2057cde16da0a634a4dbc8f6ba04f07e'
         '25ce77f419ebfca129d795fa072a4770'
         '5ac4ccd934bb4c2d48aad1440b12d31b'
         '82ada4224f2410481f2d84d72b2a23e6'
         '0244aa3c498da64ef52d77c2de493acd')

_etc="/etc/${pkgname}"
_usr="/opt/${pkgname}"
_var="/srv/${pkgname}"

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  export PATH=$PATH:/usr/bin/core_perl

  ./configure \
    --prefix=${_var} \
    --sysconfdir=${_etc} \
    --with-confdir=${_etc} \
    --with-bindir=${_usr}/bin \
    --with-sbindir=${_usr}/bin \
    --with-cgidir=${_usr}/bin \
    --with-libexecdir=${_usr}/bin \
    --with-libdir=${_usr}/bin \
    --with-spooldir=${_usr}/spool \
    --with-expldir=${_var}/list_data \
    --with-piddir=${_var} \
    --with-etcdir=${_var}/etc \
    --with-aliases_file=${_etc}/aliases \
    --with-perl=`/usr/bin/which perl`

  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" install

  # we don't need init.d files
  rm -rf "$pkgdir/etc/rc.d"

  # we need systemd unit files
  install -d "$pkgdir/usr/lib/systemd/system"

  install -D -m644 "$srcdir/sympa.service" "$pkgdir/usr/lib/systemd/system/"
  install -D -m644 "$srcdir/sympa-archived.service" "$pkgdir/usr/lib/systemd/system/"
  install -D -m644 "$srcdir/sympa-bounced.service" "$pkgdir/usr/lib/systemd/system/"
  install -D -m644 "$srcdir/sympa-bulk.service" "$pkgdir/usr/lib/systemd/system/"
  install -D -m644 "$srcdir/sympa-task_manager.service" "$pkgdir/usr/lib/systemd/system/"

  chown -R 52:52 "$pkgdir/${_prefix}"
}

# vim:set ts=2 sw=2 et:
