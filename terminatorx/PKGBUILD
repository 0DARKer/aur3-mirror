# Maintainer : Krzysztof Wloch <wloszekk@gmail.com>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_name=terminatorX
pkgname=terminatorx
pkgver=3.84
pkgrel=3
pkgdesc="A realtime audio synthesizer that lets you scratch on sampled audio data"
arch=('i686' 'x86_64')
url="http://terminatorx.org"
license=('GPL')
depends=('gtk2' 'jack' 'liblrdf' 'audiofile' 'mpg321'
         'libxxf86dga' 'sox' 'libmad' 'vorbis-tools' 'rarian')
install="$pkgname.install"
source=("http://terminatorx.org/dist/$_name-$pkgver.tar.bz2")
md5sums=('c5e47758ed784bce6c049d3d9dd245f4')

build() {
  cd "$srcdir/$_name-$pkgver"

  # zlib fix
  sed -i '/gzFile/s/\*//' src/tX_midiin.cc

  # DSO link fix
  LDFLAGS+=" -ldl" \
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"
  make DESTDIR="$pkgdir" install

  cd gnome-support

  # desktop file
  install -Dm644 $_name.desktop \
    "$pkgdir/usr/share/applications/$_name.desktop"

  # icons
  install -d "$pkgdir/usr/share/pixmaps"
  install -Dm644 *.png "$pkgdir/usr/share/pixmaps"

  # mime
  install -Dm644 $_name.mime \
    "$pkgdir/usr/share/mime/packages/$_name.mime"
  install -Dm644 $_name.keys \
    "$pkgdir/usr/share/mime-info/$_name.keys"
}