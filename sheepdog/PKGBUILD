# Maintainer: Steven Armstrong <steven-aur at armstrong.cc>
pkgname=sheepdog
pkgver=0.3.0
pkgrel=2
pkgdesc="Distributed storage system for QEMU/KVM"
arch=('x86_64')
url="http://www.osrg.net/sheepdog/"
license=('GPLv2')
depends=('glibc>=2.9' 'corosync' 'qemu-kvm>=0.13')
makedepends=('autoconf' 'automake' 'libtool' 'pkg-config')
_package_folder='collie-sheepdog-bb41896'
source=("${_package_folder}.tar.gz::https://github.com/collie/sheepdog/tarball/v0.3.0"
	"sheepdog.conf.d"
	"sheepdog.rc.d"
	"sheepdog.uidgid")
install="${pkgname}.install"
md5sums=('9b28dad11926ee52cda15a961d9b54fe'
         'b9e34c4f3471c402f1138066f81fa59b'
         'bbbf12b375d22c9fadb5251f8d1edb7c'
         '1f74a7977e24af6e09f7ecca61384f40')
sha1sums=('c8d9991bdc7c3d3995d81f09d4077d583ddded8e'
          '215a93bb293089c8afbd16cb0a77d43f0366df02'
          'eb94264d90813b096aac1ee86fac5ba1c0b7bcf9'
          '36a8fa2a388553bf8c280fe71c2775bd7893da7e')

build() {
  cd "$srcdir/$_package_folder"

  ./autogen.sh
  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --localstatedir=/var
  make
}

package() {
  cd "$srcdir"
  # use custom init script
  install -D sheepdog.conf.d "${pkgdir}/etc/conf.d/sheepdog"
  install -D sheepdog.rc.d "${pkgdir}/etc/rc.d/sheepdog"
  # configure corosync for running sheepdog as non-root user
  install -D sheepdog.uidgid "${pkgdir}/etc/corosync/uidgid.d/sheepdog"

  cd "$_package_folder"
  make DESTDIR="$pkgdir/" install
  rm -r "${pkgdir}/etc/init.d"
}
