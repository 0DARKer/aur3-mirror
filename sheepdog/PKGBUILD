# Maintainer: Steven Armstrong <steven-aur at armstrong.cc>
pkgname=sheepdog
pkgver=0.3.0
pkgrel=1
pkgdesc="Distributed storage system for QEMU/KVM"
arch=('x86_64')
url="http://www.osrg.net/sheepdog/"
license=('GPLv2')
depends=('glibc>=2.9' 'corosync' 'qemu-kvm>=0.13')
makedepends=('autoconf' 'automake' 'libtool' 'pkg-config')
_package_folder='collie-sheepdog-bb41896'
source=("${_package_folder}.tar.gz::https://github.com/collie/sheepdog/tarball/v0.3.0"
	"sheepdog.uidgid"
	"sheepdog.config")
install="${pkgname}.install"
md5sums=('9b28dad11926ee52cda15a961d9b54fe'
         '1f74a7977e24af6e09f7ecca61384f40'
         'b8c3fb7d33b050f2ed33a1a2e99cb111')
sha1sums=('c8d9991bdc7c3d3995d81f09d4077d583ddded8e'
          '36a8fa2a388553bf8c280fe71c2775bd7893da7e'
          '8b125a39ed43924e9f92fb5b936084d652bdfd29')

build() {
  cd "$srcdir/$_package_folder"

  ./autogen.sh
  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --localstatedir=/var
  make
}

package() {
  cd "$srcdir"
  mkdir -p "${pkgdir}/etc/corosync/"
  install -D sheepdog.config "${pkgdir}/etc/corosync/corosync.conf"
  mkdir -p "${pkgdir}/etc/corosync/uidgid.d"
  install -D sheepdog.uidgid "${pkgdir}/etc/corosync/uidgid.d/sheepdog"

  cd "$_package_folder"
  make DESTDIR="$pkgdir/" install
  mkdir "${pkgdir}/etc/rc.d"
  mv "${pkgdir}/etc/init.d/sheepdog" "${pkgdir}/etc/rc.d/sheepdog"
  rm -r "${pkgdir}/etc/init.d"
}
