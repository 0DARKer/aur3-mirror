# Maintainer: Patrick McCarty <pnorcks at gmail dot com>
# Previous maintainer: Fernando Munoz <f[at]beford.net>
# Previous maintainer: Alexandre Bique <bique.alexandre[at]gmail.com>

pkgname=isomd5sum
pkgver=1.0.7
pkgrel=2
pkgdesc="isomd5sum provides a way of making use of the ISO9660 application data area to store md5sum data about the iso."
arch=(i686 x86_64)
license=('GPL')
depends=('popt' 'python2')
url="https://fedorahosted.org/releases/i/s/isomd5sum"
source=(${url}/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
sha256sums=('2aee6c354a7d78298f72e55c6cd7b317414ab334e355d37badf636ecacfb7ee8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  msg 'Patching Makefile'
  sed -i -e 's/shell python /shell python2 /' Makefile
  sed -i -e 's/-Werror/ /' Makefile
  sed -i -e 's/^\(LIBDIR = \)lib64/\1lib/' Makefile
  msg 'Starting make'
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
