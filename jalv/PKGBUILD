# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=jalv
pkgver=1.0.0
pkgrel=2
pkgdesc="Run an LV2 plugin as a JACK application"
arch=(i686 x86_64)
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('lilv>=0.14.0' 'suil>=0.6.0')
makedepends=('python2' 'qt' 'gtk2' 'gtkmm')
optdepends=('qt: for Qt GUI'
            'gtk2: for Gtk2 GUI'
            'gtkmm: for Gtkmm GUI')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('f20f81dbb437f4e4ea10e00694f6cc4d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr \
                        --mandir=/usr/share/man
  python2 waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir/" python2 waf install

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
