# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=jalv
pkgver=1.2.0
pkgrel=1
pkgdesc="Run an LV2 plugin as a JACK application"
arch=(i686 x86_64)
url="http://drobilla.net/software/$pkgname/"
license=('custom:ISC')
depends=('lilv' 'suil')
makedepends=('python2' 'qt' 'gtk2' 'gtk3' 'gtkmm')
optdepends=('qt: for Qt GUI'
            'gtk2: for Gtk2 GUI'
            'gtk3: for Gtk3 GUI'
            'gtkmm: for Gtkmm GUI')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('babb0ffb0f5083d87b93def98880579d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr \
                        --mandir=/usr/share/man
  python2 waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  DESTDIR="$pkgdir/" python2 waf install

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
