# Maintainer: Jameson Pugh <imntreal@gmail.com>

pkgname=lib32-libappindicator
_pkgbasename=libappindicator
pkgver=12.10.0
pkgrel=1
pkgdesc="Library to allow applications to export a menu into the Unity Menu bar (GTK+ 2 library)"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
depends=('lib32-libindicator' 'lib32-libdbusmenu-gtk2')
makedepends=('gobject-introspection' 'vala' 'gtk-sharp-2' 'pygtk' 'perl-xml-libxml' 'dbus-glib' 'gcc-multilib')
options=('!libtool')
source=(http://launchpad.net/libappindicator/${pkgver%.*}/$pkgver/+download/$_pkgbasename-$pkgver.tar.gz)
md5sums=('41910f2005edee9240da1e53fffcdc12')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "$srcdir/$_pkgbasename-$pkgver"

  # Fix mono bindings location
  sed -i 's|/cli/|/mono/|' bindings/mono/{appindicator-sharp-0.1.pc.in,Makefile.in}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --libdir=/usr/lib32 --disable-static
  make -j1
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir/" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
