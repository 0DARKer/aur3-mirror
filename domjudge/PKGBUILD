# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=domjudge
pkgver=3.3.5
pkgrel=1
pkgdesc="programming contest jury system"
arch=('i686' 'x86_64')
url="http://domjudge.sourceforge.net/"
license=('GPL' 'LGPL' 'MIT' 'BSD')
depends=('sudo' 'apache' 'php' 'php-apache' 'mysql' 'curl' 'libxslt')
makedepends=('boost' 'sharutils')
install='domjudge.install'

source=(http://prdownloads.sourceforge.net/domjudge/domjudge-$pkgver.tar.gz
        domjudge-submitd.service domjudge-judgehostd.service)
md5sums=('c577d79187b4d4bdcd35ece782883048'
         '669e8f1b19875fae04cb0d6077d4fba2'
         '93cd5b10fc2636b664f2c17b4f34436f')

build() {
  cd "$srcdir"/domjudge-$pkgver
  ./configure --enable-fhs --prefix=/usr \
    --sysconfdir=/etc --localstatedir=/var \
    --enable-submitclient=http,dolstra \
    --with-domjudge-user=root --with-webserver-group=http
  make domserver judgehost
}

package() {
  cd "$srcdir"/domjudge-$pkgver
  make install-domserver install-judgehost DESTDIR="$pkgdir"/
  rm -rf "$pkgdir"/tmp
  install -D -m 750 "$pkgdir"/etc/domjudge/sudoers-domjudge "$pkgdir"/etc/sudoers.d/domjudge
  sed -s "s/^root/domjudge/g" -i "$pkgdir"/etc/sudoers.d/domjudge
  rm "$pkgdir"/etc/domjudge/{htpasswd-jury,sudoers-domjudge,dbpasswords.secret}
  install -D "$srcdir"/domjudge-submitd.service "$pkgdir"/etc/systemd/system/domjudge-submitd.service
  install -D "$srcdir"/domjudge-judgehostd.service "$pkgdir"/etc/systemd/system/domjudge-judgehostd.service
}

