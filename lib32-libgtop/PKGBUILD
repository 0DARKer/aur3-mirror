# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: TryA <tryagainprod {at} gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbase=libgtop
pkgname=lib32-libgtop
pkgver=2.28.4
pkgrel=1
pkgdesc="A library that read information about processes and the running system (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2' 'lib32-libxau' "${_pkgbase}")
makedepends=('intltool' 'gcc-multilib' 'texinfo')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/2.28/${_pkgbase}-${pkgver}.tar.xz)
options=('!libtool')
url="http://www.gnome.org/"
sha256sums=('b6619cfd1540267ee81d456d1a26c72c793261586f1b00a7b9a7a229d0abe1d8')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-libgtop-smp --libdir=/usr/lib32 \
      --enable-introspection=no
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,share}
}
