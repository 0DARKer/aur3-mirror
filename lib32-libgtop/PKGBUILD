# Maintainer: TryA <tryagainprod {at} gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libgtop
pkgname=lib32-libgtop
pkgver=2.28.2
pkgrel=1
pkgdesc="A library that read information about processes and the running system (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2>=2.24' 'lib32-libxau' 'texinfo')
makedepends=('intltool' 'gcc-multilib')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.28/${_pkgbasename}-${pkgver}.tar.bz2)
options=('!libtool')
url="http://www.gnome.org/"
sha256sums=('49958d7da1f76b257bfd0d557d8ed2b218a5ab0d31b59fed1c32ddf2a1529f5d')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-libgtop-smp --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,share}
}
