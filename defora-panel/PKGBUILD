# Contributor: Calimero <calimeroteknik@free.fr>
pkgname=defora-panel
pkgver=0.2.17
pkgrel=1
pkgdesc='The Defora lightweight panel'
arch=('i686' 'x86_64')
url='http://defora.org/os'
license=('GPL3')
depends=('defora-libdesktop' 'gtk2')
source=("http://www.defora.org/os/download/download/4009/Panel-${pkgver}.tar.gz")
md5sums=('c98ffded91688904e0ac56cf4d3a9f72')

build() {
  cd "$srcdir/Panel-$pkgver"
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/Panel-$pkgver"
  make DESTDIR="$pkgdir" PREFIX="/usr" install

  #avoid a conflict with libavc1394.
  mv "$pkgdir"/usr/bin/{panelctl,panelcontrol}
  sed -i 's/panelctl/panelcontrol/g' \
   "$pkgdir/usr/share/applications/panel-settings.desktop" \
   "$pkgdir/usr/share/man/man1/panelctl.1"
  mv "$pkgdir"/usr/share/man/man1/{panelctl,panelcontrol}.1
}
