# Maintainer: Jens Adam <jra@byte.cx>

pkgname=zmap
pkgver=1.1.1
pkgrel=1
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
license=('Apache')
arch=('i686' 'x86_64')
depends=('gmp' 'libpcap')
makedepends=('byacc-noconflict' 'cmake' 'gengetopt')
backup=('etc/zmap/blacklist.conf' 'etc/zmap/zmap.conf')
source=("https://github.com/zmap/zmap/archive/v${pkgver}.tar.gz")
sha256sums=('e3a740a97f217840b6afa81e2c84e11d44f91346cb9f2b21d0f5eda11fca5128')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  cmake -DENABLE_HARDENING=ON
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/src
  install -D zmap "${pkgdir}"/usr/bin/zmap
  install -Dm644 zmap.1 "${pkgdir}"/usr/share/man/man1/zmap.1
  install -d "${pkgdir}"/etc/zmap
  install -m644 ../conf/* "${pkgdir}"/etc/zmap
  cd ..
  for F in AUTHORS CHANGELOG INSTALL README; do
    install -Dm644 ${F} "${pkgdir}"/usr/share/doc/zmap/${F}
  done
  cp -a examples "${pkgdir}"/usr/share/doc/zmap
}
