# Co-maintainer: Gitus < git at shroomrider dot com >
# Co-maintainer: Enjolras < 0enjolras0 at laposte dot net >

pkgname=seeks
pkgver=0.3.4
pkgrel=4
pkgdesc="An open decentralized platform for collaborative search content"
arch=('i686' 'x86_64')
url="http://www.seeks-project.info"
license=('custom:"AGPL3"')
backup=('etc/logrotate.d/seeks'
    'etc/conf.d/seeks'
    'etc/seeks/config')
depends=('pcre' 'curl' 'libxml2' 'libevent'  'protobuf' 'tokyocabinet' 'opencv')
makedepends=('docbook2x' 'pkg-config' )
conflicts=('seeks-git')
source=("http://downloads.sourceforge.net/project/seeks/solo/seeks-$pkgver.tar.gz"
    'seeks'
    'seeks.conf.d'
    'seeks.logrotate'
    'logfile.patch'
)
install='install'
sha256sums=('0bcdf63fe3f5a709fa7612563b5299a28e2008be071a25f99f7d1c36d7059f91'
            '5933968805bdaea64b075cfbe6915fbca41e39788edf8dbdb742b8aea4fbf910'
            'c78945d6ec3917aba0f860bebb4cc83af87ae04c5641ee6c605bd6fbc77cc384'
            'f2bd002e666c6a7cc9429f9c54da55e31e03aafab7f666c53c8a5fd6747653bf'
            '60e102a5a110fa3e7532e1d28f3ac0cb603baabc1e9b4dda5965b8fbeb69ca60')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    
    ./configure \
	--prefix=/usr --sysconfdir=/etc \
	--enable-httpserv-plugin --enable-image-websearch-plugin=yes
    
    sed -i s/-Wl,--as-needed// config.status
    sed -i s/cxflann.h/cv.h/ src/plugins/img_websearch/ocvsurf.cpp
    make
    
}

package() {
    cd  "$srcdir/$pkgname-$pkgver/src/"

    #Changing logdir and logfile options
    #disable plugins cf and query-capture in config
    patch -p0 < "$srcdir/logfile.patch"
    
    cd "$srcdir/$pkgname-$pkgver"

    make DESTDIR="$pkgdir" install
   
    install -D -m644 Licenses  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -m644 AGPL-3.txt  "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -m644 BSD-yui.txt  "${pkgdir}/usr/share/licenses/${pkgname}/"
    install -D -m755 ../seeks  "${pkgdir}"/etc/rc.d/seeks
    install -D -m644 ../seeks.conf.d "${pkgdir}"/etc/conf.d/seeks
    install -D -m600 ../seeks.logrotate  "${pkgdir}"/etc/logrotate.d/seeks

    cd "$pkgdir/usr/share/seeks/plugins" && ln -s websearch img_websearch
}
