# Maintainer: ksj <podhorsky.ksj@gmail.com>
# Contributor: kasa <biuta.jr@gmail.com>
# Contributor: L42y <423300@gmail.com>

_gitname=network-manager-applet
pkgname="$_gitname"-git
pkgver=20130701
pkgrel=1
pkgdesc="GNOME frontends to Networkmanager"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('networkmanager-git' 'gtk3' 'iso-codes' 'libsecret'
         'notification-daemon' 'libnotify' 'gnome-icon-theme'
         'mobile-broadband-provider-info' )
         
makedepends=('intltool' 'gobject-introspection')
provides=('network-manager-applet')
conflicts=('network-manager-applet-svn' 'network-manager-applet')
options=('!libtool')
install=${pkgname}.install
source=(git://git.gnome.org/network-manager-applet)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $srcdir/$_gitname
    git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {
	cd $srcdir/$_gitname
	
	./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/networkmanager \
		--disable-static \
		--disable-maintainer-mode \
		--without-bluetooth \
		--disable-migration \
		--without-modem-manager-1       
	make
}

package() {
	make -C $srcdir/$_gitname DESTDIR="$pkgdir" install
}
