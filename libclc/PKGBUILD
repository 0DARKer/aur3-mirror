# Maintainer: Armin K. <krejzi at email dot com>

pkgname=libclc
pkgver=20130430
pkgrel=1
pkgdesc="Library requirements of the OpenCL C programming language"
arch=('i686' 'x86_64')
url="http://libclc.llvm.org/"
license=('MIT')
depends=('llvm-r600')
makedepends=('git' 'python2')

_gitname=libclc
_gitroot=git://people.freedesktop.org/~tstellar/${_gitname}
_rev=acb59f99cb831dbcf5ab4e2d26584192c8f5655e

prepare() {
  if [ -d "${_gitname}" ] ; then
    cd "${_gitname}" && git pull && cd ..
  else
    git clone "${_gitroot}"
  fi

  cd "${_gitname}"
  git checkout "${_rev}"
}

build() {
  cd "${_gitname}"
  python2 ./configure.py --prefix=/usr
  make
}

package() {
  cd "${_gitname}"
  make install DESTDIR="$pkgdir"
  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
