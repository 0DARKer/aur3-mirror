# Maintainer: Daniel Escoz <darkhogg+aur@gmail.com>
pkgname=steam-idle-master-git
pkgver=r35.4ec0a0d
pkgrel=2
pkgdesc="Get your Steam Trading Cards the Easy Way"
arch=(any)
url="https://github.com/jshackles/idle_master"
license=('GPL')
depends=(python2-beautifulsoup4 python2-requests python2-pillow tk)
makedepends=('git')
source=(
  repo::git+https://github.com/jshackles/idle_master.git
  steam-idle
)
backup=(usr/lib/steam-idle-master/settings.txt)
md5sums=('SKIP'
         '43b8d7e347b72939b4aa6a6b0a90132f')

pkgver() {
  cd "$srcdir/repo"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {

  mkdir -p "$pkgdir/usr/lib/steam-idle-master"
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/etc/steam-idle-master"

  install -m0755 "$srcdir/steam-idle" "$pkgdir/usr/bin/steam-idle"

  install -m0644 "$srcdir/repo/settings.txt" "$pkgdir/usr/lib/steam-idle-master/settings.txt"
  ln -s /usr/lib/steam-idle-master/settings.txt "$pkgdir/etc/steam-idle-master/"

  cd "$srcdir/repo/Releases/Linux"
  for f in *; do
    install -m0644 "$f" "$pkgdir/usr/lib/steam-idle-master/"
  done

  touch "$pkgdir/usr/lib/steam-idle-master/idlemaster.log"
  chmod a+rw "$pkgdir/usr/lib/steam-idle-master/idlemaster.log"
}

# vim:set ts=2 sw=2 et:
