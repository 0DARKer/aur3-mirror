# Maintainer: Aaron Chen <nextAaron at gmail.com>
# Contributor: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>
# Contributor: Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>

_pkgname=bin_prot
pkgname=ocaml-$_pkgname
pkgver=109.15.00
pkgrel=1
pkgdesc="Support for reading and writing OCaml-values in a type-safe binary protocol"
arch=(i686 x86_64)
license=(LGPL)
depends=(ocaml 'ocaml-type_conv>=109.15.00')
makedepends=(ocaml-findlib)
url="http://janestreet.github.com"
source=("https://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=(05df96e2a9afd349324f86543184ab98)
options=(!strip !makeflags)

build () {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package () {
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
