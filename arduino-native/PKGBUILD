# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: PyroPeter <googlemail.com@abi1789>
# Contributor: darkapex <me@jailuthra.in>
# Contributor: tty0 <vt.tty0[d0t]gmail.com>

pkgname=arduino-native
pkgver=1.0.6
pkgrel=2
pkgdesc="Arduino prototyping platform SDK (Arch compiler toolchain)"
arch=('i686' 'x86_64')
url="http://arduino.cc/en/Main/Software"
options=(!strip staticlibs)
license=('GPL' 'LGPL')
depends=('gtk2' 'avr-gcc' 'avr-libc' 'avrdude' 'libusb-compat' 'java-runtime'
         'java-rxtx' 'desktop-file-utils')
makedepends=('java-environment' 'apache-ant' 'icoutils')
provides=('arduino')
conflicts=('arduino' 'arduino-toolchain')
install="arduino.install"
source=("https://github.com/arduino/Arduino/archive/${pkgver}.tar.gz"
        'arduino-fix-arguments.patch'
        'arduino-ide-add-board-options.patch'
        'arduino-gcc-4.8-no-use-cxa-atexit.patch'
        'arduino.desktop'
        'arduino.xml')
sha256sums=('4020fde5605156a99a2d19f37c14dcf2b9a66a7e2b11c93eb5353a862deb9f5d'
            'fd88d7e5e93c1da0680051e1c7a57ecccb289cde1db3b6834700d7d92aeed1a8'
            'e0a32a46a0cc798b3d019a675c97136817a10d5c3fe88b38df7bdfcbebb96824'
            '356f87aca8b33b97c299b21ca8dc876d446de33944409f7f8b6748b5e7772fe7'
            'd817829bb2830cb690ed63f14d8a990bb513bef4a4ebc6227a82abdfc8bcd35d'
            '473b82156505e9bd903e4d8484e8d183f2e3bf3c1f7e29940b815929ae597b68')

prepare() {
  cd "Arduino-${pkgver}"

  patch -Np1 <"${srcdir}/arduino-fix-arguments.patch"
  patch -Np1 <"${srcdir}/arduino-gcc-4.8-no-use-cxa-atexit.patch"
  patch -Np1 <"${srcdir}/arduino-ide-add-board-options.patch"

  icotool -x -o .. build/shared/lib/arduino_icon.ico
}

build() {
  cd "Arduino-${pkgver}/build"

  ant -Dversion=1.0.6 -Dplatform=linux dist
}

package() {
  cd "Arduino-${pkgver}/build/linux/work"

  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/"{doc,applications,mime/packages}

  # use Arch's avr toolchain
  rm -rf hardware/tools/*

  # copy the whole SDK to /usr/share/arduino/
  cp -a . "${pkgdir}/usr/share/arduino"

  # use system's RXTX library
  ln -sf /usr/lib/librxtxSerial.so "${pkgdir}/usr/share/arduino/lib/librxtxSerial.so"
  ln -sf /usr/lib/librxtxSerial.so "${pkgdir}/usr/share/arduino/lib/librxtxSerial64.so"
  ln -sf /usr/share/java/rxtx/RXTXcomm.jar "${pkgdir}/usr/share/arduino/lib/RXTXcomm.jar"

  # at least support the FHS a little bit
  ln -s /usr/share/arduino/arduino "${pkgdir}/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "${pkgdir}/usr/share/doc/arduino"

  # desktop icon
  for size in 16 32 48 256; do
    install -Dm644 "${srcdir}/arduino_icon_"*"_${size}x${size}x32.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/arduino.png"
  done

  # desktop and mimetype files
  install -m644 "${srcdir}/arduino.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/arduino.xml" "${pkgdir}/usr/share/mime/packages/"
}
