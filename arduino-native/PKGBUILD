# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: PyroPeter <googlemail.com@abi1789>
# Contributor: darkapex <me@jailuthra.in>
# Contributor: tty0 <vt.tty0[d0t]gmail.com>

pkgname=arduino-native
pkgver=1.6.0
pkgrel=1
pkgdesc="Arduino prototyping platform SDK (with Arch toolchains)"
arch=('i686' 'x86_64')
url="http://arduino.cc/en/Main/Software"
options=(!strip staticlibs)
license=('GPL' 'LGPL')
depends=('gtk2' 'avr-gcc' 'avr-libc' 'avrdude' 'libusb-compat' 'java-runtime'
         'desktop-file-utils' 'arm-none-eabi-gcc' 'arm-none-eabi-newlib')
makedepends=('icoutils')
provides=('arduino')
conflicts=('arduino' 'arduino-toolchain')
install="arduino.install"
source=('arduino.desktop'
        'arduino.xml'
        'newlib-2.2-support.patch'
        'use-arch-toolchains.patch')
source_i686+=("http://arduino.cc/download_handler.php?f=/arduino-${pkgver}-linux32.tar.xz")
source_x86_64+=("http://arduino.cc/download_handler.php?f=/arduino-${pkgver}-linux64.tar.xz")
sha256sums=('d817829bb2830cb690ed63f14d8a990bb513bef4a4ebc6227a82abdfc8bcd35d'
            '473b82156505e9bd903e4d8484e8d183f2e3bf3c1f7e29940b815929ae597b68'
            'ea406aec3fb7ff44a7a6cecb6ff17ca4a92315eb48ab922a5476cb88b6fd035d'
            '811dd668c15ca3a09eedf74fa29774e3cfaf4de7d0f245ba44b70f34bd1d88e7')
sha256sums_i686=('6ec36165811918290bc4fde7b071a69c4c83fa887d25961470ddb300cb14f2df')
sha256sums_x86_64=('a5c499fabd90761a675026fbcdf3f9bfb4051d072e7a2b3de3296de632dfd290')

prepare() {
  cd "arduino-${pkgver}"

  patch -ENp1 <"${srcdir}/newlib-2.2-support.patch"
  patch -Np1 <"${srcdir}/use-arch-toolchains.patch"
}

build() {
  cd "arduino-${pkgver}"

  icotool -x -o .. lib/arduino_icon.ico
}

package() {
  cd "arduino-${pkgver}"

  mkdir -p "${pkgdir}/usr/bin"
  mkdir -p "${pkgdir}/usr/share/"{doc,applications,mime/packages}

  # remove unused toolchains
  rm -rf hardware/tools/{avr,gcc-arm-none-eabi-4.8.3-2014q1}
  # rm -rf hardware/tools/{avr,gcc-arm-none-eabi-4.8.3-2014q1}/*
  # ln -s /usr/bin hardware/tools/avr/bin
  # ln -s /usr/bin hardware/tools/gcc-arm-none-eabi-4.8.3-2014q1/bin
  # mkdir hardware/tools/avr/etc
  # ln -s /etc/avrdude.conf hardware/tools/avr/etc/avrdude.conf

  # copy the whole SDK to /usr/share/arduino/
  cp -a . "${pkgdir}/usr/share/arduino"

  # at least support the FHS a little bit
  ln -s /usr/share/arduino/arduino "${pkgdir}/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "${pkgdir}/usr/share/doc/arduino"

  # desktop icon
  for size in 16 32 48 256; do
    install -Dm644 ../arduino_icon_*_${size}x${size}x32.png \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/arduino.png"
  done

  # desktop and mimetype files
  install -m644 "${srcdir}/arduino.desktop" "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/arduino.xml" "${pkgdir}/usr/share/mime/packages/"
}
