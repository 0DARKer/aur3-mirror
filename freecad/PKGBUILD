# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jordi De Groof <jordi (dot) degroof (at) gmail (dot) com>
# Contributor: mickele
# Contributor: manwithgrenade
# Contributor: bricem13
# Contributor: gborzi

pkgname=freecad
pkgver=0.14.3692
pkgrel=1
pkgdesc='A general purpose 3D CAD modeler'
arch=('i686' 'x86_64')
url='http://www.freecadweb.org/'
license=('LGPL')
depends=('boost-libs' 'curl' 'opencascade>=6.6.0' 'pivy-hg' 'xerces-c' 'libspnav' 'shared-mime-info' 'hicolor-icon-theme' 'python2-matplotlib' 'shiboken' 'python2-pyside')
makedepends=('boost' 'eigen3' 'gcc-fortran' 'swig' 'xerces-c' 'desktop-file-utils' 'cmake' 'coin>=3.1.3-9')
optdepends=('python2-matplotlib' 'pycollada-git' 'python2-pyqt4') 
options=(!libtool !makeflags)
install=freecad.install
source=("http://downloads.sourceforge.net/sourceforge/free-cad/freecad-${pkgver}.tar.gz"
	"freecad-0.13.diff"
	"${pkgname}.desktop"
	"${pkgname}.xml"
	"BRepOffsetAPI_MakePipeShellPyImp.patch")

sha512sums=('2d687fe334106c47035696641e832ead2a385b5503e24af3e15fcf5d94240be23daac6b7f51d4fdf61575dc0426eb65935ce428cf95f806e5c5e725882e93096'
	    'f03c75239bd6d5429ecd9690b779a95c08ef791424f31bfe989594990750088300ae5fb75f50051c0028ba218eac3cace2b6c7f2666c1368a259f728c4098625'
	    '02098793ce4e2e4744ffeb4fcacd364ce680786a1eb0b1cece3c1d8d5033733d9b4bd15ac0a842b9dceeb006b2cd292e8a332fdbad9b187e943d934acf8ce9bf'
	    'ac0dfe1b2c58335867e94d4ce86696763b6354dcdca68a653bf115b6ea562f0f8ff7d243d42d0c6fd6e758bd488d626e30cd917bb1b5c676c1f0cafdece005d6'
	    '5f0cc54e6436a68ec93831021a123a39a32447b4f35e03f45cb6ffae61daca104c2554b6cec85d58b2010714dd50682434e5b6091b4c1dd8fca345903dea7ae5')

_installdir=/opt/$pkgname
# I prefer installing in opt because installing
# in /usr there are non standard dir.
# However if you prefer /usr uncomment the following line
# _installdir=/usr

#prepare(){
  #cd "${srcdir}/freecad-${pkgver}/"
  # compatibility issues with OCC-6.6
  #for _FILE in $( grep -Rl "BRepTools::OuterShell" * )
  #do
  #  sed -e "s|BRepTools|BRepClass3d|g" -i "$_FILE"
  #done

  # these patch contain some code taken from upstream
  # thanks to cbuehler 
  #patch -Np1 -i ../freecad-0.13.diff
  #patch -p0 -i ../BRepOffsetAPI_MakePipeShellPyImp.patch
#}

build() {
  cd "${srcdir}/freecad-${pkgver}/"

  mkdir -p build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX:PATH=${_installdir} .. \
  	-DOCC_INCLUDE_DIR:PATH=/opt/opencascade/inc/ \
	-DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2 \
	-DFREECAD_USE_EXTERNAL_PIVY:BOOL=ON

  # add these options if you don't want to update coin 
  #	-DCMAKE_C_FLAGS:STRING="$CFLAGS -fpermissive" \
  #	-DCMAKE_CXX_FLAGS:STRING="$CXXFLAGS -fpermissive"
	
  make

  # Builds Qt plugin
  #cd ../src/Tools/plugins/widget/
  #qmake-qt4 plugin.pro
  #make
}

package() {
  cd "${srcdir}/freecad-${pkgver}/build"

  # Install main program
  make DESTDIR="${pkgdir}" install

  # Symlink to /usr/bin
  mkdir -p "${pkgdir}/usr/bin/"
  ln -sf "${_installdir}/bin/FreeCAD" "${pkgdir}/usr/bin/freecad"
  ln -sf "${_installdir}/bin/FreeCADCmd" "${pkgdir}/usr/bin/freecadcmd"

  # Installs Qt plugin
  #install -Dm755 ../src/Tools/plugins/widget/libFreeCAD_widgets.so "${pkgdir}/usr/lib/qt4/plugins/designer/libFreeCAD_widgets.so"

  # Install pixmaps and desktop shortcut
  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/${pkgname}.desktop"

   # Mime info
   install -D -m644 "${srcdir}/${pkgname}.xml" "${pkgdir}/usr/share/mime/packages/${pkgname}.xml"
}
