# Maintainer: Jordi De Groof <jordi (dot) degroof (at) gmail (dot) com>
# Contributor: Michele Mocciola <mickele>
# Contributor: manwithgrenade
# Contributor: bricem13

pkgname=freecad
pkgver=0.11.3729
pkgrel=2
pkgdesc="A general purpose 3D CAD modeler, aimed directly at mechanical engineering \
  and product design but also architecture or other engineering specialties"
arch=('i686' 'x86_64')
url="http://free-cad.sourceforge.net/"
license=('GPL' )
depends=('python2' 'pivy-hg' 'boost-libs' 'eigen' 'xerces-c' 'opencascade' 'gcc-fortran' 'ode')
makedepends=('desktop-file-utils' 'boost')
options=('!libtool')
install=('freecad.install')
source=(http://downloads.sourceforge.net/sourceforge/free-cad/freecad-${pkgver}.tar.gz ${pkgname}.desktop ${pkgname}.xml)

build() {
  cd "${srcdir}/FreeCAD-${pkgver}/"

  # Builds main program
  ./configure --prefix=/opt/freecad \
    --with-qt4-dir=/usr \
    --with-qt4-lib=/usr/lib/qt \
    --with-xercesc-include=/usr/include/xercesc \
    PYTHON=/usr/bin/python2 \
    --with-python-include=/usr/include/python2.7/ \
    --with-python-lib=/usr/lib/python2.7/ \
    --with-occ-include=/opt/opencascade/inc/ \
    --with-occ-lib=/opt/opencascade/lib/ \
    --disable-debug
  make

  # Builds Qt plugin
  cd src/Tools/plugins/widget/ || return 1
  qmake plugin.pro || return 1
  make || return 1
}

package() {
  # install main progam
  cd "${srcdir}/FreeCAD-${pkgver}/" || return 1
  make DESTDIR="${pkgdir}" install || return 1

  # Symlink to /usr/bin
  mkdir -p ${pkgdir}/usr/bin/
  ln -s /opt/freecad/bin/FreeCAD ${pkgdir}/usr/bin/freecad
  ln -s /opt/freecad/bin/FreeCADCmd ${pkgdir}/usr/bin/freecadcmd

  # Installs Qt plugin
  cd src/Tools/plugins/widget/ || return 1
  install -Dm644 ./libFreeCAD_widgets.so "${pkgdir}/usr/lib/qt/plugins/designer/libFreeCAD_widgets.so" || return 1

  # Install pixmaps and desktop shortcut
  cd "${srcdir}/FreeCAD-${pkgver}/" || return 1
  install -D -m 755 src/Gui/Icons/freecad.xpm "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm" || return 1
  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/${pkgname}.desktop"

   # Mime info
   install -D -m644 ${srcdir}/${pkgname}.xml ${pkgdir}/usr/share/mime/packages/${pkgname}.xml
   mkdir -p ${pkgdir}/usr/share/icons/hicolor/48x48/mimetypes/
   ln -s /usr/share/pixmaps/freecad.xpm ${pkgdir}/usr/share/icons/hicolor/48x48/mimetypes/application-x-extension-fcstd.xpm
}

md5sums=('cd68f52ee201c49a83a400b87686b9b9'
         '19644cc6deab8600fe28c72b45b035ad'
         'c2f4154c8e4678825411de8e7fa54c6b')
