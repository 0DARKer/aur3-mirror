# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libgphoto2
pkgname=lib32-$_pkgbase
pkgver=2.4.11
pkgrel=2
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs. (32 bit)"
arch=('x86_64')
url="http://www.gphoto.org"
license=(LGPL)
depends=('lib32-libexif' 'lib32-libjpeg-turbo' 'lib32-gd' 'lib32-libltdl' 'lib32-libusb-compat')
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://downloads.sourceforge.net/gphoto/${_pkgbase}-${pkgver}.tar.gz)
md5sums=('a2230c31e320acb7ececd90bc02fe515')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  udevscriptdir=/lib/udev ./configure --prefix=/usr --with-drivers=all \
  	--disable-static --disable-rpath --libdir=/usr/lib32 --without-hal
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{lib,usr/{bin,include,share}}
}
