# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libgphoto2
pkgname=lib32-${_pkgbase}
pkgver=2.5.3.1
pkgrel=1
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs. (32 bit)"
arch=('x86_64')
url="http://www.gphoto.org"
license=(LGPL)
depends=('lib32-libexif' 'lib32-libjpeg-turbo'  'lib32-libltdl' 'lib32-libusb-compat' 'lib32-libxml2' 'lib32-fontconfig' "${_pkgbase}")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://downloads.sourceforge.net/gphoto/${_pkgbase}-${pkgver}.tar.gz")
sha1sums=('4e04713a0a32ba79ce1b5eed11aa7c3335200f9f')

prepare() {
  rm -fr build
  cp -R  "${_pkgbase}-${pkgver}" build
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'  
  cd build
  ./configure --prefix=/usr --disable-rpath --libdir=/usr/lib32 --without-hal
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share,lib32/udev}
}
