# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Maintainer: Le Duc Hieu <alaviss0 at gmail dot com>

_pkgbase=libgphoto2
pkgname=lib32-${_pkgbase}
pkgver=2.5.4
pkgrel=1
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs. (32 bit)"
arch=('x86_64')
url="http://www.gphoto.org"
license=(LGPL)
depends=('lib32-libexif' 'lib32-libjpeg'  'lib32-libltdl' 'lib32-libusb-compat'
         'lib32-libxml2' 'lib32-gd' "${_pkgbase}=$pkgver") # out-of-date if libgphoto2 ver != lib32-libgphoto2 ver
makedepends=('gcc-multilib')
source=("http://downloads.sourceforge.net/gphoto/${_pkgbase}-${pkgver}.tar.gz")
md5sums=('db6c7ca1bdeb80f3470036fb400efec5')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  cd "${srcdir}/$_pkgbase-$pkgver"
  ./configure --prefix=/usr --disable-rpath --libdir=/usr/lib32
  make
}

package() {
  make -C "${srcdir}/$_pkgbase-$pkgver" DESTDIR="${pkgdir}" install
  # Remove unwanted dirs
  rm -rf "${pkgdir}"/usr/{bin,include,share,lib32/udev}
}
