# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=pysmf
pkgname=python-$_name
pkgver=20101120
pkgrel=1
pkgdesc="A Python module for Standard MIDI Files, based on libsmf."
arch=(i686 x86_64)
url="http://das.nasophon.de/download/"
license=('BSD')
depends=('python' 'libsmf')
makedepends=('python-distribute' 'cython')
source=("http://das.nasophon.de/download/$_name-$pkgver.tar.gz")
md5sums=('edf71a76912bfb99464a2a030e46098a')

build() {
  cd "$srcdir/$_name-$pkgver"

  # python 3.x build fix
  sed -i 's/commands/subprocess/' setup.py

  python setup.py build
}

package() {
  cd "$srcdir/$_name-$pkgver"

  python setup.py install --root="$pkgdir/"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
