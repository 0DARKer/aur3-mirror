# Maintainer: t3ddy  <t3ddy1988 "at" gmail {dot} com>
# Contributor: Lex Rivera aka x-demon <aur@x-demon.org>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: ruario

pkgname=google-chrome
pkgver=16.0.912.77   # Check for new Linux releases in: http://googlechromereleases.blogspot.com/search/label/Stable%20updates
pkgrel=1
pkgdesc="An attempt at creating a safer, faster, and more stable browser (Stable Channel)"
arch=('i686' 'x86_64')
url="http://www.google.com/chrome"
license=('custom:chrome')
depends=('alsa-lib' 'gconf' 'gtk2' 'hicolor-icon-theme' 'libpng12' 'libxslt' 'libxss' 'nss' 'xdg-utils')
optdepends=('gecko-mediaplayer' 'kdebase-kdialog')
provides=("google-chrome=$pkgver")
conflicts=('google-chrome')
install=${pkgname}.install
_channel='stable'
_verbld=16.0.912.77-118311

if [ "$CARCH" = "i686" ]; then
    _arch='i386'
    md5sums=('f22fd9f2ae82e8fe0c33585e0e7c6c5a')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    optdepends+=('flashplugin')
    md5sums=('23fc88c72a02e801223813723ed012f6')
fi


source=("http://dl.google.com/linux/chrome/rpm/stable/${_arch}/google-chrome-${_channel}-${_verbld}.${_arch}.rpm")


package() {
    msg "Preparing install"
    install -d "$pkgdir"/{opt,usr/{bin,share/applications}}
    mv -v opt/google "$pkgdir"/opt
    msg2 "Done preparing!"

    msg "Actual installation"
    ln -sv /opt/google/chrome/google-chrome "$pkgdir/usr/bin/"
    ln -sv /usr/lib/mozilla/plugins "$pkgdir/opt/google/chrome"
    mv -v "$pkgdir/opt/google/chrome/google-chrome.desktop" "$pkgdir/usr/share/applications"

    # Adding man page
    if [ ! -e "$srcdir/usr/share/man/man1/google-chrome.1.gz" ]; then
      gzip -9 "$srcdir/usr/share/man/man1/google-chrome.1"
    fi
    install -Dm644 "$srcdir/usr/share/man/man1/google-chrome.1.gz" "$pkgdir/usr/share/man/man1/google-chrome.1.gz"

    mkdir "$pkgdir/opt/google/chrome/lib"
    cd "$pkgdir/opt/google/chrome/lib"
    ln -sv /usr/lib/libnspr4.so libnspr4.so.0d
    ln -sv /usr/lib/libnss3.so libnss3.so.1d
    ln -sv /usr/lib/libnssutil3.so libnssutil3.so.1d
    ln -sv /usr/lib/libplc4.so libplc4.so.0d
    ln -sv /usr/lib/libplds4.so libplds4.so.0d
    ln -sv /usr/lib/libsmime3.so libsmime3.so.1d
    ln -sv /usr/lib/libssl3.so libssl3.so.1d

    # Symlinking icons to /usr/share/icons/hicolor/
    for png in "$pkgdir/opt/google/chrome/product_logo_"*.png; do
      pngsize="${png##*/product_logo_}"
      mkdir -p "$pkgdir/usr/share/icons/hicolor/"${pngsize%.png}x${pngsize%.png}/apps
      (cd "$pkgdir/usr/share/icons/hicolor/${pngsize%.png}x${pngsize%.png}/apps/"; ln -s ../../../../../../opt/google/chrome/product_logo_${pngsize} google-chrome.png)
    done

    # Fixing permissions of chrome-sandbox
    chmod 4755 "$pkgdir/opt/google/chrome/chrome-sandbox"

    msg2 "Installation finished!"
}
