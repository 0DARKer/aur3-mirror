# Maintainer: cars10t <carsten.1 gmx net>

_pkgbase=opendune
pkgname=$_pkgbase-git
_gitname=OpenDUNE
pkgver=0.8pre
pkgrel=1
pkgdesc="Open source re-creation of the popular game Dune II"
arch=('i686' 'x86_64')
url="https://github.com/OpenDUNE/OpenDUNE"
license=('GPL2')
depends=('sdl')
makedepends=('git')
#optdepends=('timidity++: midi music support')
conflicts=('opendune')
provides=('opendune')
install="$_pkgbase.install"
source=("git://github.com/OpenDUNE/OpenDUNE.git"
	"$_pkgbase.sh"
	"$_pkgbase.desktop"
	"$_pkgbase.png")
# Because the sources are not static, skip Git checksum:
md5sums=('SKIP'
	'af0a69bb636dee2d1fe24606853ee0ec'
	'2ff96b1efe864b1426f950005625af35'
	'4a6fef6e5bdde8ee7f038554bc5ed93b')

pkgver() {
  cd $_gitname

  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd $_gitname
  
  # circumventing alsa-lib 1.0.26 bug
  ./configure --prefix-dir=/opt/opendune --without-asound

  make
}

package() {
  cd $_gitname

  make INSTALL_ICON_DIR="$pkgdir/opt/$_pkgbase" INSTALL_BINARY_DIR="$pkgdir/opt/$_pkgbase" INSTALL_DOC_DIR="$pkgdir/opt/$_pkgbase/doc" install
  
  # install binary, icon and desktop files
  install -d "$pkgdir/usr/bin"
  install -m755 "$srcdir/$_pkgbase.sh" "$pkgdir/usr/bin/$_pkgbase"
  install -d "$pkgdir/opt/$_pkgbase/data"
  install -m644 "$srcdir/$_gitname/bin/data/dune.cfg" "$pkgdir/opt/$_pkgbase/data"
  install -m644 "$srcdir/$_gitname/bin/data/put_dune2_here.txt" "$pkgdir/opt/$_pkgbase/data"
  install -Dm644 "$srcdir/$_pkgbase.png" "$pkgdir/usr/share/pixmaps/$_pkgbase.png"
  install -Dm644 "$srcdir/$_pkgbase.desktop" "$pkgdir/usr/share/applications/$_pkgbase.desktop"
}
