# Maintainer: Suhaimi Ghazali <serdotlinecho@gmail.com>

pkgname=gnome-mpv-git
_gitname=gnome-mpv
pkgver=2014.12.14.gfe00b20
pkgrel=1
pkgdesc="GNOME frontend for MPV"
arch=('i686' 'x86_64')
url="https://github.com/gnome-mpv/gnome-mpv"
license=('GPL')
depends=('mpv' 'glib2' 'gtk3')
makedepends=('git')
install=gnome-mpv.install
source=('git+https://github.com/gnome-mpv/gnome-mpv.git')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
}

build() {
    cd "$srcdir/$_gitname"
    autoreconf -sfi
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/$_gitname"
    make DESTDIR="$pkgdir" install
}
