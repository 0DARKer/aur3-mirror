# Maintainer: Suhaimi Ghazali <serdotlinecho@gmail.com>

pkgname=gnome-mpv-git
pkgver=v0.1.r53.gb1a769f
pkgrel=1
pkgdesc="GNOME frontend for MPV"
arch=('i686' 'x86_64')
url="https://github.com/gnome-mpv/gnome-mpv"
license=('GPL')
depends=('mpv' 'glib2' 'gtk3')
makedepends=('git')
install=gnome-mpv.install
source=("${pkgname}::git+https://github.com/gnome-mpv/gnome-mpv.git")
md5sums=('SKIP')

pkgver() {
    cd "${pkgname}"
    git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd "${pkgname}"
    autoreconf -sfi
    ./configure --prefix=/usr
    make
}

package() {
    cd "${pkgname}"
    make DESTDIR="${pkgdir}" install
}
