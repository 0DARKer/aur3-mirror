# Maintainer: speps <speps at aur dot archlinux dot org>

_name=rezound
pkgname=$_name-svn
pkgver=1998
pkgrel=1
pkgdesc="A graphical audio file editor"
arch=(i686 x86_64)
url="http://rezound.sourceforge.net/"
license=('GPL')
depends=('libpulse' 'portaudio' 'fftw' 'fox'
         'jack' 'audiofile' 'desktop-file-utils')
makedepends=('subversion')
provides=("$_name")
conflicts=("$_name")
install="$pkgname.install"
source=("$_name-boost.patch" "$_name.png")
md5sums=('64f3977a537859d193e0bb718849887c'
         '509e87ae40f74fbd69aa59abf431e81a')

_svntrunk=https://rezound.svn.sourceforge.net/svnroot/$_name/trunk
_svnmod=$_name

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build/$_name"

  #
  # BUILD HERE
  #

  # boost patch
  patch -p0 -i "$srcdir/$_name-boost.patch"

  # doc dir fix
  sed -i "/pkgdocdir/s/prefix/datarootdir/" config/am_include.mk

  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_svnmod-build/$_name"
  make DESTDIR="$pkgdir/" install

  # desktop files
  install -Dm644 packaging/generic_rpm/kde/x-rez.desktop \
    "$pkgdir/usr/share/applications/x-rez.desktop"
  sed "s|\(Icon=/usr/share/\).*|\1pixmaps/$_name.png|" \
    packaging/generic_rpm/kde/$_name.desktop > \
    "$pkgdir/usr/share/applications/$_name.desktop"

  # icon
  install -Dm644 "$srcdir/$_name.png" \
    "$pkgdir/usr/share/pixmaps/$_name.png"

  # man
  install -Dm644 packaging/debian/$_name.1 \
    "$pkgdir/usr/share/man/man1/$_name.1"
}

# vim:set ts=2 sw=2 et:
