# Maintainer Jekyll Wu<adaptee [at] gmail [dot] com>
# Basically, stolen shamelessly from http://chakra-probject.org/ccr

pkgname=vim-qt-git
pkgver=20110623
pkgrel=1
pkgdesc="A Qt powered vim GUI"
arch=("i686" "x86_64")
url="https://gitorious.org/vim-qt"
license=('custom:vim')
depends=('gpm' 'perl' 'python>=2.7.1' 'ruby' 'libxt'
         'desktop-file-utils' 'qt')
makedepends=('gpm' 'perl' 'python2>=2.7.1' 'ruby' 'libxt' 'desktop-file-utils' 'qt'
             'gettext' 'pkgconfig' 'rsync' 'sed' 'git')
source=(pythoncomplete.vim::http://www.vim.org/scripts/download_script.php\?src_id=10872
        vimrc archlinux.vim qvim.desktop)
backup=(etc/vimrc)
provides=('vim=7.3' 'gvim=7.3')
conflicts=('vim' 'gvim' 'vim-runtime')
options=(!strip)
install="${pkgname}.install"

_gitroot="git://gitorious.org/vim-qt/vim-qt.git"
_gitname="vim-qt"

_build_dir=vim-qt-build
_versiondir=vim73

build() {
  cd ${srcdir}
  msg "Connecting to GIT server...."

  if [ -d ${_gitname}/.git ] ; then
    cd ${_gitname}

    # Change remote url to anongit
    if [ -z $( git branch -v | grep anongit ) ] ; then
        git remote set-url origin ${_gitroot}
    fi

    git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd ${srcdir}
  rm -rf vim-qt-build
  rsync -a --exclude='.git/' ${_gitname}/ vim-qt-build

  msg2 'Patching...'

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    vim-qt-build/src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    vim-qt-build/src/feature.h
  (cd vim-qt-build/src && autoconf)

  msg2 'Building...'

  cd ${srcdir}/${_build_dir}

  export CFLAGS="-g -O0"
  export CXXFLAGS="-g -O0"

  ./configure \
  --prefix=/usr \
  --localstatedir=/var/lib/vim \
  --mandir=/usr/share/man \
  --with-compiledby=Archlinux \
  --with-features=big \
  --enable-gpm \
  --enable-acl \
  --with-x=yes \
  --enable-gui=qt \
  --with-qt-dir=/usr/lib/qt \
  --enable-multibyte \
  --enable-cscope \
  --enable-netbeans \
  --enable-perlinterp \
  --enable-pythoninterp \
  --enable-rubyinterp

  # temporary workaound of moc
  sed -i '/^MOC/s/MOC\s*=.*/MOC = moc/' src/Makefile

  make
}

package(){
  cd ${srcdir}/vim-qt-build
  make -j1 VIMRCLOC=/etc DESTDIR=${pkgdir} install

  # provided by (n)vi in core
  rm ${pkgdir}/usr/bin/{ex,view}

  # delete some manpages
  find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # freedesktop links
  install -Dm644 ${srcdir}/qvim.desktop \
      ${pkgdir}/usr/share/applications/qvim.desktop
  install -Dm644 runtime/vim48x48.png ${pkgdir}/usr/share/pixmaps/qvim.png

  # Don't forget logtalk.dict
  install -Dm644 ${srcdir}/vim-qt-build/runtime/ftplugin/logtalk.dict \
    ${pkgdir}/usr/share/vim/${_versiondir}/ftplugin/logtalk.dict

  # fix FS#17216
  sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
    ${pkgdir}/usr/share/vim/${_versiondir}/filetype.vim

  # patch filetype.vim for better handling of pacman related files
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
    ${pkgdir}/usr/share/vim/${_versiondir}/filetype.vim
  sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
    ${pkgdir}/usr/share/vim/${_versiondir}/ftplugin/changelog.vim

  ln -s /usr/bin/vim ${pkgdir}/usr/bin/qvim

  # make Aaron happy
  install -Dm644 ${srcdir}/pythoncomplete.vim \
    ${pkgdir}/usr/share/vim/${_versiondir}/autoload/pythoncomplete.vim

  # rc files
  install -Dm644 ${srcdir}/vimrc ${pkgdir}/etc/vimrc
  install -Dm644 ${srcdir}/archlinux.vim \
      ${pkgdir}/usr/share/vim/vimfiles/archlinux.vim

  # license
  install -dm755 ${pkgdir}/usr/share/licenses/vim
  ln -s /usr/share/vim/${_versiondir}/doc/uganda.txt \
      ${pkgdir}/usr/share/licenses/vim/license.txt
}

# vim: set sw=2:

md5sums=('6e7adfbd5d26c1d161030ec203a7f243'
         'e57777374891063b9ca48a1fe392ac05'
         '1fc9d4930ab312fd8654e72165d53ca3'
         '48d76291214e5ef81bc0812d6346f391')

