# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=rhythmcat
_pkgname=RhythmCat
pkgver=1.0.0
_pkgver=110402
pkgrel=2
pkgdesc="Karaoke, Converter and Audio Player in GTK2 (this is alpha version)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/rhythmcat/"
license=('GPL')
depends=('gtksourceview2' 'gstreamer0.10-base-plugins' 'sox' 'dbus-glib')
source=(http://rhythmcat.googlecode.com/files/${_pkgname}-${pkgver}-alpha1%7E${_pkgver}.tar.bz2
        RhythmCat.desktop)
install=info.install
sha256sums=('31aee88042eaa6835feb031a4f249ca6d1c221ffad87ec6a9fc348b9a46fb1ca'
            '5ef2edea0d7d713d15785832dd399cd4287622591b2748aba876ac4560703d48')
build() {
  cd "$srcdir/RhythmCat-1.0.0-alpha1~110402/"

  [ "$CARCH" = "i686" ] && _arch=i386 || _arch=amd64
  cat <<EOF > RhythmCat_install_plugins
#!/bin/bash

echo -n "==> getting plugins.. "

(
  (
    mkdir -p ~/.RhythmCat/Plugins && cd ~/.RhythmCat/Plugins && wget 'http://rhythmcat.googlecode.com/files/RhythmCat_Plugins_${_pkgver}_${_arch}.tar.bz2' -O - | tar -xj
  ) &> /dev/null && echo "done"
) || (
  echo "fail"
  exit 1
)
EOF

  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/RhythmCat-1.0.0-alpha1~110402/"

  make check
}

package() {
  cd "$srcdir/RhythmCat-1.0.0-alpha1~110402/"

  make DESTDIR="$pkgdir/" install

  install -m 755 "RhythmCat_install_plugins" "$pkgdir/usr/bin/"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m 644 "$srcdir/RhythmCat.desktop" "$pkgdir/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
