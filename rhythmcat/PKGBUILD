# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=rhythmcat
pkgver=1.0.0
pkgrel=11
_pkgname=RhythmCat
_pkgver=111008
_rc=RC1
pkgdesc="A Audio Player in GTK3 with plug-in support (this is 1st RC)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/rhythmcat/"
license=('GPL')
makedepends=('gtk-doc')
depends=('gtk3' 'gstreamer0.10-base' 'dbus-glib')
optdepends=('gtksourceview3: for lyrics plugin')
source=(https://rhythmcat.googlecode.com/files/${_pkgname}-${pkgver}-${_rc}%7E${_pkgver}_Source.tar.bz2
        RhythmCat.desktop)
install=info.install
sha256sums=('6691fc5661cbb9c0a3f28784b1b94b03e8349f452bb951d85a4702a62796af39'
            '8e08af473e76b03abcec4d39c61e6af30c6d6c4dfe956ef1db95d3eea0554862')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}"

  ./configure \
    --prefix=/usr \
    --enable-gtk3

  make

# *** Simply plugin-fetcher ***
# I make multipackage for program and plugins if it will be possible.. ;)
  [ "$CARCH" = "i686" ] && _arch=i386 || _arch=amd64
  cat <<EOF > RhythmCat_install_plugins
#!/bin/bash

echo -n "==> getting plugins.. "

(
  (
    mkdir -p ~/.RhythmCat/Plugins && cd ~/.RhythmCat/Plugins && wget 'http://rhythmcat.googlecode.com/files/RhythmCat_Plugins_Gtk3_${_pkgver}_${_arch}.tar.bz2' -O - | tar -xj --overwrite
  ) &> /dev/null && echo "done"
) || (
  echo "fail"
  exit 1
)
EOF

}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}"

  make check
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}"

  make DESTDIR="$pkgdir/" install

  install -m 755 "RhythmCat_install_plugins" "$pkgdir/usr/bin/"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m 644 "$srcdir/RhythmCat.desktop" "$pkgdir/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
