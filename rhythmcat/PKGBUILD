# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=rhythmcat
_pkgname=RhythmCat
pkgver=0.6.1
_pkgver=101024
pkgrel=1
pkgdesc="Karaoke, Converter and Audio Player in GTK2 (this is alpha version)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/rhythmcat/"
license=('GPL')
depends=('gtksourceview2' 'gstreamer0.10-base-plugins' 'sox' 'dbus-glib')
source=(http://rhythmcat.googlecode.com/files/${_pkgname}-Source-${_pkgver}.tar.bz2
        RhythmCat.desktop
        pi.patch)
sha256sums=('912a700e354ac57a8c61a7decc143c6385024670f64fc0b8304e3dd7d5fffc3d'
            '5ef2edea0d7d713d15785832dd399cd4287622591b2748aba876ac4560703d48'
            '135c9c287e92013e6abc2061f591708ff27652cfa2f0f96743404ec79f5ff6ba')

build() {
  cd "$srcdir/${_pkgname}-Source-${_pkgver}"

  patch -Np0 < ../pi.patch

  ./configure --prefix=/usr

  make
  make check
}

package() {
  cd "$srcdir/${_pkgname}-Source-${_pkgver}"

  make DESTDIR="$pkgdir/" install

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m 644 "../RhythmCat.desktop" "$pkgdir/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
