# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

#PREFER_GTK3=1 # Uncomment or set as environment variable to enable

pkgname=rhythmcat
_pkgname=RhythmCat
pkgver=1.0.0
_pkgver=110802
pkgrel=6
pkgdesc="Karaoke, Converter and Audio Player in GTK2 (this is 2nd beta)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/rhythmcat/"
license=('GPL')
depends=('gtksourceview2' 'gstreamer0.10-base-plugins' 'sox' 'dbus-glib')
source=(https://rhythmcat.googlecode.com/files/RhythmCat-1.0.0-beta2%7E110802_Source.tar.bz2
        RhythmCat.desktop)
install=info.install
sha256sums=('ce8b0f1303d51a64a4df9386b3c69d0be5911b9536e3983aaf0b9b061f3c0b7a'
            '8e08af473e76b03abcec4d39c61e6af30c6d6c4dfe956ef1db95d3eea0554862')


[ -z "$PREFER_GTK3" ] && PREFER_GTK3=0

if [ "$PREFER_GTK3" != "0" ]; then
  depends=('gtksourceview3' 'gstreamer0.10-base-plugins' 'sox' 'dbus-glib')
fi


build() {
  cd "$srcdir/RhythmCat-1.0.0-beta2~110802/"

  [ "$CARCH" = "i686" ] && _arch=i386 || _arch=amd64
  cat <<EOF > RhythmCat_install_plugins
#!/bin/bash

echo -n "==> getting plugins.. "

(
  (
    mkdir -p ~/.RhythmCat/Plugins && cd ~/.RhythmCat/Plugins && wget 'http://rhythmcat.googlecode.com/files/RhythmCat_Plugins_${_pkgver}_${_arch}.tar.bz2' -O - | tar -xj --overwrite
  ) &> /dev/null && echo "done"
) || (
  echo "fail"
  exit 1
)
EOF

  if [ "$PREFER_GTK3" = "0" ]; then
    ./configure --prefix=/usr
  else
    ./configure --enable-gtk3 --prefix=/usr
  fi

  make
}

check() {
  cd "$srcdir/RhythmCat-1.0.0-beta2~110802/"

  make check
}

package() {
  cd "$srcdir/RhythmCat-1.0.0-beta2~110802/"

  make DESTDIR="$pkgdir/" install

  install -m 755 "RhythmCat_install_plugins" "$pkgdir/usr/bin/"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m 644 "$srcdir/RhythmCat.desktop" "$pkgdir/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
