# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgbase=rhythmcat
# AUR not support multipackaging:
pkgname=rhythmcat
true && pkgname=(rhythmcat rhythmcat-plugins)
pkgver=1.0.0
pkgrel=12
_pkgname=RhythmCat
_pkgver=111008
_rc=RC1
pkgdesc="A Audio Player in GTK3 with plug-in support (this is 1st RC)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/rhythmcat/"
license=('GPL')
makedepends=('gtk-doc')
depends=('gtk3' 'gstreamer0.10-base' 'dbus-glib')
source=(https://rhythmcat.googlecode.com/files/${_pkgname}-${pkgver}-${_rc}%7E${_pkgver}_Source.tar.bz2
        RhythmCat.desktop
        RhythmCat.sh)
sha256sums=('6691fc5661cbb9c0a3f28784b1b94b03e8349f452bb951d85a4702a62796af39'
            '8e08af473e76b03abcec4d39c61e6af30c6d6c4dfe956ef1db95d3eea0554862'
            '2c0551a3b33f74f76845f6865e7898568a0000b40670f4a04b34268c111d9d46')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}"

  ./configure \
    --prefix=/usr \
    --bindir=/usr/lib/RhythmCat \
    --enable-gtk3

  make

  for i in plugins/*; do (
    cd $i || continue
    test -f "Makefile3" || continue
    make --makefile=Makefile3
  ); done
}

check_rhythmcat() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}"

  make check
}

check_rhythmcat-plugins() {
  for i in *; do
    ls "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}/plugins/$i"/*.so &> /dev/null
  done
}

package_rhythmcat() {
  pkgdesc="A Audio Player in GTK3 (this is ${_rc})"

  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}"

  install -dm755 "$pkgdir/usr/bin/"

  make DESTDIR="$pkgdir/" install

  # this is beta code
  install -m 755 "$srcdir/RhythmCat.sh" "$pkgdir/usr/bin/RhythmCat"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m 644 "$srcdir/RhythmCat.desktop" "$pkgdir/usr/share/applications/"
}

package_rhythmcat-plugins() {
  pkgdesc="Plugins for RhythmCat (this is ${_rc})"
  depends=(rhythmcat gtksourceview3)

  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}/plugins/"

  install -dm755 "$pkgdir"/usr/share/RhythmCat/plugins/
  for i in *; do
    test -d "$i" || continue
    test -f "$i/Makefile3" || continue
    install -dm755 "$pkgdir"/usr/share/RhythmCat/plugins/"$i"
    install -m 644 "$i"/*.{so,conf} "$pkgdir"/usr/share/RhythmCat/plugins/"$i"/
  done
}


# AUR:
pkgdesc="A Audio Player in GTK3 with plug-in support (this is 1st RC)"
depends=('gtk3' 'gstreamer0.10-base' 'dbus-glib')

# vim:set ts=2 sw=2 et:
