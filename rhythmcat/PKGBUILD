# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=rhythmcat
_pkgname=RhythmCat
pkgver=1.0.0
_pkgver=110515
pkgrel=4
pkgdesc="Karaoke, Converter and Audio Player in GTK2 (this is 3rd alpha)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/rhythmcat/"
license=('GPL')
depends=('gtksourceview2' 'gstreamer0.10-base-plugins' 'sox' 'dbus-glib')
source=(http://rhythmcat.googlecode.com/files/RhythmCat-1.0.0-alpha3%7E110515_Source.tar.bz2
        RhythmCat.desktop)
install=info.install
sha256sums=('636ea918e2652309eb1836441c1eaeda0110c8e990848d60dc1308be71dd1d64'
            '8e08af473e76b03abcec4d39c61e6af30c6d6c4dfe956ef1db95d3eea0554862')

build() {
  cd "$srcdir/RhythmCat-1.0.0-alpha3~110515/"

  [ "$CARCH" = "i686" ] && _arch=i386 || _arch=amd64
  cat <<EOF > RhythmCat_install_plugins
#!/bin/bash

echo -n "==> getting plugins.. "

(
  (
    mkdir -p ~/.RhythmCat/Plugins && cd ~/.RhythmCat/Plugins && wget 'http://rhythmcat.googlecode.com/files/RhythmCat_Plugins_${_pkgver}_${_arch}.tar.bz2' -O - | tar -xj --overwrite
  ) &> /dev/null && echo "done"
) || (
  echo "fail"
  exit 1
)
EOF

  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/RhythmCat-1.0.0-alpha3~110515/"

  make check
}

package() {
  cd "$srcdir/RhythmCat-1.0.0-alpha3~110515/"

  make DESTDIR="$pkgdir/" install

  install -m 755 "RhythmCat_install_plugins" "$pkgdir/usr/bin/"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m 644 "$srcdir/RhythmCat.desktop" "$pkgdir/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
