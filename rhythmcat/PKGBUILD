# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgbase=rhythmcat
pkgname=rhythmcat
true && pkgname=(rhythmcat rhythmcat-plugins rhythmcat-plugins-extra)
pkgver=1.0.0
pkgrel=14
_pkgname=RhythmCat
_pkgver=1 
pkgdesc="A Audio Player in GTK3 with plug-in support (stable release)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/rhythmcat/"
license=('GPL')
makedepends=('gtk-doc')
depends=('gtk3' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'dbus-glib')
source=(https://rhythmcat.googlecode.com/files/${_pkgname}-${pkgver}-${_pkgver}_Source.tar.bz2
        RhythmCat.desktop)
sha256sums=('463ec413f4266fb13a1779dfc2a95b9d16eeed5c5053c38e78b26fba9d851c50'
            '8e08af473e76b03abcec4d39c61e6af30c6d6c4dfe956ef1db95d3eea0554862')

_plugins_base=(desktop-lyric lyric-show music-locator notify)
_plugins_extra=(lyric-crawler lyric-editor tageditor)

build() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_pkgver}"

  ./configure \
    --prefix=/usr \
    --enable-gtk3 

  make

# ------------

  cd "$srcdir/${_pkgname}-${pkgver}-${_pkgver}/plugins/base/"

  for i in "${_plugins_base[@]}"; do
    test -f "$i/Makefile3" || continue
    (cd "$i" && make --makefile=Makefile3)
  done

# ------------

  cd "$srcdir/${_pkgname}-${pkgver}-${_pkgver}/plugins/extra/"

  for i in "${_plugins_extra[@]}"; do
    test -f "$i/Makefile3" || continue
    (cd "$i" && make --makefile=Makefile3)
  done
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_pkgver}"

  make check
}

package_rhythmcat() {
  pkgdesc="A Audio Player in GTK3"
  optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
              'gstreamer0.10-bad-plugins: Extra media codecs'
              'gstreamer0.10-ffmpeg: Extra media codecs'
              'rhythmcat-plugins: Base plugins'
              'rhythmcat-plugins-extra: Extra plugins')

  cd "$srcdir/${_pkgname}-${pkgver}-${_pkgver}"

  make DESTDIR="$pkgdir/" install

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m 644 "$srcdir/RhythmCat.desktop" "$pkgdir/usr/share/applications/"
}

package_rhythmcat-plugins() {
  pkgdesc="Base plugins for RhythmCat"
  depends=(rhythmcat gtksourceview3)

  cd "$srcdir/${_pkgname}-${pkgver}-${_pkgver}/plugins/base/"

  install -dm755 "$pkgdir"/usr/share/RhythmCat/plugins/
  for i in "${_plugins_base[@]}"; do
    test -f "$i/Makefile3" || continue
    (cd "$i" && make INSTALL_DIR="$pkgdir/usr/share/RhythmCat/plugins/$i" install)
  done
}
package_rhythmcat-plugins-extra() {
  pkgdesc="Extra plugins for RhythmCat"
  depends=(rhythmcat)

  cd "$srcdir/${_pkgname}-${pkgver}-${_pkgver}/plugins/extra/"

  install -dm755 "$pkgdir"/usr/share/RhythmCat/plugins/
  for i in "${_plugins_extra[@]}"; do
    test -f "$i/Makefile3" || continue
    (cd "$i" && make INSTALL_DIR="$pkgdir/usr/share/RhythmCat/plugins/$i" install)
  done
}


# AUR:
pkgdesc="A Audio Player in GTK3 with plug-in support (stable release)"
depends=('gtk3' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'dbus-glib')

# vim:set ts=2 sw=2 et:
