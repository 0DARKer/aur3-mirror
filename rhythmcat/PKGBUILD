# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

#==> To enable gtk3: uncomment next line or set as environment variable <==#
# PREFER_GTK3=1

pkgname=rhythmcat
pkgver=1.0.0
pkgrel=10
_pkgname=RhythmCat
_pkgver=111008
_rc=RC1
pkgdesc="Karaoke, Converter and Audio Player in GTK2 (this is 1st RC)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/rhythmcat/"
license=('GPL')
makedepends=('gtk-doc')
depends=('gtk2' 'gstreamer0.10-base' 'dbus-glib')
optdepends=('gtksourceview2: for lyrics plugin')
source=(https://rhythmcat.googlecode.com/files/${_pkgname}-${pkgver}-${_rc}%7E${_pkgver}_Source.tar.bz2
        RhythmCat.desktop)
install=info.install
sha256sums=('6691fc5661cbb9c0a3f28784b1b94b03e8349f452bb951d85a4702a62796af39'
            '8e08af473e76b03abcec4d39c61e6af30c6d6c4dfe956ef1db95d3eea0554862')

# *** PREFER_GTK3 defaults here ***
if [ -z "$PREFER_GTK3" ]; then
  PREFER_GTK3=0
fi

# *** Rewrite dependency, if PREFER_GTK3 is true (boolean perl-like) ***
if [ "$PREFER_GTK3" != "0" ]; then
  # "true" is a hack for AUR parser
  true && depends=('gtk3' 'gstreamer0.10-base' 'dbus-glib')
  true && optdepends=('gtksourceview3: for lyrics plugin')
  _PREFER_GTK3__ENABLE_STRING="--enable-gtk3"
  _PREFER_GTK3__PLUGIN_INFIX="Gtk3_"
fi


build() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}"

# *** Configure and make here ***
  [ -f "Makefile" ] && make clean

  ./configure --prefix=/usr ${_PREFER_GTK3__ENABLE_STRING}

  make

# *** Simply plugin-fetcher ***
  [ "$CARCH" = "i686" ] && _arch=i386 || _arch=amd64
  cat <<EOF > RhythmCat_install_plugins
#!/bin/bash

echo -n "==> getting plugins.. "

(
  (
    mkdir -p ~/.RhythmCat/Plugins && cd ~/.RhythmCat/Plugins && wget 'http://rhythmcat.googlecode.com/files/RhythmCat_Plugins_${_PREFER_GTK3__PLUGIN_INFIX}${_pkgver}_${_arch}.tar.bz2' -O - | tar -xj --overwrite
  ) &> /dev/null && echo "done"
) || (
  echo "fail"
  exit 1
)
EOF

}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}"

  make check
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_rc}~${_pkgver}"

  make DESTDIR="$pkgdir/" install

  install -m 755 "RhythmCat_install_plugins" "$pkgdir/usr/bin/"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m 644 "$srcdir/RhythmCat.desktop" "$pkgdir/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
