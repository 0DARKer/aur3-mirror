# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

#==> To enable gtk3: uncomment next line or set as environment variable <==#
# PREFER_GTK3=1

pkgname=rhythmcat
pkgver=1.0.0
pkgrel=9
_pkgname=RhythmCat
_pkgver=110914
_beta=beta3
pkgdesc="Karaoke, Converter and Audio Player in GTK2 (this is 3rd beta)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/rhythmcat/"
license=('GPL')
makedepends=('gtk-doc')
depends=('gtk2' 'gstreamer0.10-base' 'dbus-glib')
optdepends=('gtksourceview2: for lyrics plugin')
source=(https://rhythmcat.googlecode.com/files/${_pkgname}-${pkgver}-${_beta}%7E${_pkgver}_Source.tar.bz2
        RhythmCat.desktop)
install=info.install
sha256sums=('82161812dd494a2d4033c88122d3dfba3e0611d1b02a219ef63f1f669c689379'
            '8e08af473e76b03abcec4d39c61e6af30c6d6c4dfe956ef1db95d3eea0554862')

# *** PREFER_GTK3 defaults here ***
if [ -z "$PREFER_GTK3" ]; then
  PREFER_GTK3=0
fi

# *** Rewrite dependency, if PREFER_GTK3 is true (boolean perl-like) ***
if [ "$PREFER_GTK3" != "0" ]; then
  # "true" is a hack for AUR parser
  true && depends=('gtk3' 'gstreamer0.10-base' 'dbus-glib')
  true && optdepends=('gtksourceview3: for lyrics plugin')
  _PREFER_GTK3__ENABLE_STRING="--enable-gtk3"
fi


build() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_beta}~${_pkgver}"

# *** Configure and make here ***
  [ -f "Makefile" ] && make clean

  ./configure --prefix=/usr ${_PREFER_GTK3__ENABLE_STRING}

  make

# *** Simply plugin-fetcher ***
  [ "$CARCH" = "i686" ] && _arch=i386 || _arch=amd64
  cat <<EOF > RhythmCat_install_plugins
#!/bin/bash

echo -n "==> getting plugins.. "

(
  (
    mkdir -p ~/.RhythmCat/Plugins && cd ~/.RhythmCat/Plugins && wget 'http://rhythmcat.googlecode.com/files/RhythmCat_Plugins_${_pkgver}_${_arch}.tar.bz2' -O - | tar -xj --overwrite
  ) &> /dev/null && echo "done"
) || (
  echo "fail"
  exit 1
)
EOF

}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_beta}~${_pkgver}"

  make check
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}-${_beta}~${_pkgver}"

  make DESTDIR="$pkgdir/" install

  install -m 755 "RhythmCat_install_plugins" "$pkgdir/usr/bin/"

  install -dm755 "$pkgdir/usr/share/applications/"
  install -m 644 "$srcdir/RhythmCat.desktop" "$pkgdir/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
