# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=luxrender-hg
pkgver=3290
_blender=2.59
pkgrel=1
pkgdesc="Rendering system for physically correct, unbiased image synthesis"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('boost-libs' 'freeimage' 'openexr' 'libcl' 'libgl')
makedepends=('cmake' 'boost' 'qt' 'luxrays-hg' 'python' 'opencl-headers' 'mercurial')
optdepends=('blender: Blender exporter' 'qt: Qt GUI' \
            'nvidia-utils: OpenCL support for nVidia GPUs' \
            'amdstream: OpenCL support for AMD GPUs' \
            'intel-opencl-sdk: OpenCL support for Intel CPUs')
provides=('luxrender')
conflicts=('luxrender')
source=()
md5sums=()

_hgroot="http://bitbucket.org/luxrender"
_hgrepo="lux"
_hgrootexport="http://src.luxrender.net/luxblend25"
_hgrepoexport="luxblend25"

build() {
  cd "$srcdir"
  msg "Connecting to $_hgrepo Mercurial server......."
  if [ -d $_hgrepo ]; then
    cd $_hgrepo
    hg pull -u
    cd "$srcdir"
  else
    hg clone $_hgroot/$_hgrepo $_hgrepo
  fi
  # exporter
  if [ -d $_hgrepoexport ]; then
    cd $_hgrepoexport
    hg pull -u
    cd "$srcdir"
  else
    hg clone $_hgrootexport $_hgrepoexport
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir"/$_hgrepo-build
  mkdir "$srcdir"/$_hgrepo-build
  cd "$srcdir"/$_hgrepo-build

  export CXXFLAGS="$CXXFLAGS -lpthread"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLUX_DOCUMENTATION=OFF \
    -DPYTHON_CUSTOM=ON \
    -DPYTHON_LIBRARIES=/usr/lib/libpython3.2mu.so \
    -DPYTHON_INCLUDE_PATH=/usr/include/python3.2mu/ \
    ../$_hgrepo
  make
}

package() {
  cd "$srcdir"/$_hgrepo-build
  make DESTDIR="$pkgdir" install

  # fix library path on x86_64
  [ "$CARCH" = "x86_64" ] && mv "$pkgdir"/usr/lib64 "$pkgdir"/usr/lib

  # install the blender exporter
  install -d -m755 "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/$_hgrepoexport/src/{luxrender,presets} \
    "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/$_hgrepo-build/pylux.so "$pkgdir"/usr/share/blender/$_blender/scripts/addons/luxrender/
}

# vim:set ts=2 sw=2 et:
