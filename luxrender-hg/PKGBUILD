# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=luxrender-hg
pkgver=3041
pkgrel=1
pkgdesc="Rendering system for physically correct, unbiased image synthesis"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('libgl' 'boost-libs' 'luxrays-hg' 'openexr')
makedepends=('mercurial' 'cmake' 'boost')
optdepends=('qt: GUI')
provides=('luxrender')
conflicts=('luxrender')
source=()
md5sums=()

_hgroot="http://bitbucket.org/luxrender"
_hgrepo="lux"

build() {
  cd "$srcdir"
  msg "Connecting to $_hgrepo Mercurial server......."
  if [ -d $_hgrepo ]; then
    cd "$srcdir"/$_hgrepo
    hg pull -u
    cd "$srcdir"
  else
    hg clone -b default $_hgroot/$_hgrepo $_hgrepo
  fi
  # exporter
  if [ -d luxblend ]; then
    cd "$srcdir"/luxblend
    hg pull -u
    cd "$srcdir"
  else
    hg clone http://src.luxrender.net/luxblend
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir"/$_hgrepo-build ]; then
    rm -rf "$srcdir"/$_hgrepo-build
  fi
  cp -r "$srcdir"/lux "$srcdir"/$_hgrepo-build
  cd "$srcdir"/$_hgrepo-build

  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir"/$_hgrepo-build
  make DESTDIR="$pkgdir" install

  # fix library path on x86_64
  [ "$CARCH" = "x86_64" ] && mv "$pkgdir"/usr/lib64 "$pkgdir"/usr/lib

  # install blender exporter
  install -d -m755 "$pkgdir"/usr/share/blender/scripts
  install -m644 "$srcdir"/luxblend/*.py "$pkgdir"/usr/share/blender/scripts
}

# vim:set ts=2 sw=2 et:
