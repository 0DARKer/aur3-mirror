pkgname=cockatrice-git
pkgver=20120622
pkgrel=1
pkgdesc="A multiplatform application for playing card games such as Magic: The Gathering over a network."
arch=('i686' 'x86_64')
url="http://www.cockatrice.de/index.php"
license=('GPL2')
depends=('qt>=4.5.1')
source=('cockatrice.sh' 'arch_build.patch')
md5sums=('c3158c4b53526148faa0a036dc521860'
         '56cb01ee8f0e64728b1c75c405867fb0')
makedepends=('git')
install='cockatrice.install'

_gitroot="git://github.com/mbruker/Cockatrice.git"
_gitname="Cockatrice"

build() {
  cd "$srcdir"

  msg "Connecting to the GIT server...."
  if [ -d "${srcdir}/${_gitname}" ] ; then
    cd ${_gitname}
    git clean -f
    git reset --hard HEAD
    git pull --rebase || return 1
    msg2 "Local files updated"
  else
    git clone ${_gitroot} || return 1
    cd ${_gitname}
  fi
  msg2 "GIT checkout done or server timeout"

  cd cockatrice

  patch -Np2 -i "$srcdir/arch_build.patch"

  lrelease cockatrice.pro
  qmake
  make

  cd ../oracle

  lrelease oracle.pro
  qmake
  make

  cd ../servatrice

  lrelease servatrice.pro
  qmake
  make
}

package() {
  cd "${srcdir}/${_gitname}"

  install -dm775 -g games $pkgdir/opt/cockatrice/{bin,oracle,pic}

  install -Dm755 $srcdir/cockatrice.sh $pkgdir/usr/bin/cockatrice
  install -Dm644 cockatrice/cockatrice.desktop $pkgdir/usr/share/applications/cockatrice.desktop
  install -Dm644 cockatrice/resources/appicon.svg $pkgdir/usr/share/icons/cockatrice.svg
  install -Dm755 -g games cockatrice/cockatrice $pkgdir/opt/cockatrice/bin
  install -Dm755 -g games servatrice/servatrice $pkgdir/opt/cockatrice/bin
  install -Dm755 -g games oracle/oracle $pkgdir/opt/cockatrice/bin
  install -dm775 -g games zonebg/ $pkgdir/opt/cockatrice/zonebg
  install -m644 -g games zonebg/* $pkgdir/opt/cockatrice/zonebg/

  install -Dm664 -g games oracle/sets.xml $pkgdir/opt/cockatrice/oracle
  install -Dm644 -g games servatrice/servatrice.ini.example $pkgdir/opt/cockatrice/bin
}
