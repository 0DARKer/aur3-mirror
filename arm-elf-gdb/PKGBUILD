# $Id:$
# Adapted from standard gdb package 
# Maintainer: Andreas Messer <andi@surveycorner.de>

pkgname=arm-elf-gdb
pkgver=7.4
pkgrel=1
pkgdesc="The GNU Debugger for ARM target"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python2')
makedepends=('texinfo')
optdepends=('openocd: for debugging jtag targets')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.bz2)
md5sums=('95a9a8305fed4d30a30a6dc28ff9d060')

build() {
  cd ${srcdir}/gdb-${pkgver}
  
  ./configure --prefix=/usr \
	--target=arm-elf \
	--host=$CHOST \
	--build=$CHOST \
	--disable-nls --with-system-readline \
	--with-local-prefix=/usr/lib/arm-elf \
        --with-sysroot=/usr/$CHOST/arm-elf

  make || return 1
}

package() {
  cd ${srcdir}/gdb-${pkgver}
  make DESTDIR=${pkgdir} install || return 1

  # resolve conflicts with binutils / remove unneeded files
  rm -rf  ${pkgdir}/usr/include
  rm -f  ${pkgdir}/usr/lib/{libiberty,libbfd,libopcodes}.a
  rm -rf ${pkgdir}/usr/share/info
  rm -rf ${pkgdir}/usr/share/gdb
}
