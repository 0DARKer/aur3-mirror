# Maintainer: Alessio Sergi <asergi [at] archlinux [dot] us>

pkgname=oxygen-transparent-git
_realname=oxygen-transparent
pkgver=20110306
pkgrel=1
pkgdesc="Oxygen style and decoration with support for transparency"
arch=('i686' 'x86_64')
url="http://kde-look.org/content/show.php/oxygen-transparent?content=127752"
license=('GPL' 'LGPL')
depends=('kdebase-workspace')
makedepends=('automoc4' 'cmake' 'git')
provides=(${_realname})
conflicts=(${_realname})

_gitroot="git://anongit.kde.org/${_realname}"
_gitname=${pkgname}

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ]; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/build"
  cd "${srcdir}/build"

  cmake -DCMAKE_BUILD_TYPE=RELEASE \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install
}
