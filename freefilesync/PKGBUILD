# Contributor: chenxing <cxcxcxcx AT gmail DOT com>
# Contributor: Michael Burkhard <Michael DOT Burkhard AT web DOT de>
# Maintainer: alexmo82 <25396682 AT live DOT it>

pkgname=freefilesync
pkgver=6.7
pkgrel=0
pkgdesc="Visual folder comparison and synchronization"
arch=('any')
url="http://sourceforge.net/projects/freefilesync/"
license=('GPLv3')
depends=(wxgtk webkitgtk2 boost)
conflicts=(freefilesync-ubuntu)
source=("http://downloads.sourceforge.net/project/freefilesync/freefilesync/$pkgver/FreeFileSync_${pkgver}_Source.zip" 
	"http://downloads.sourceforge.net/project/zenxml/zenXml_2.0.zip"
	FreeFileSync.desktop 
	ffsicon.png 
	RealtimeSync.desktop 
	rtsicon.png
	)
md5sums=('7f726d401865b1e2085fc5bc1da900a9'
         '714e654e974c8f61e7565e66695d66a4'
         'a7be7841f47f29d8e6210c8ab592a1e3'
         '1f452dff6f970d95839411008d86250b'
         '90152f1021f2d6fb6ab4fa511dc60fa9'
         'ee5587fa0a8d906ad416564e4daf5a06')

build() {
  cd ${srcdir}/FreeFileSync/Source
  sed -i 's/WX_CONFIG_LIB =$(HOME)/#WX_CONFIG_LIB =$(HOME)/' Makefile
  sed -i 's/BOOST_LIB_DIR =$(HOME)/#BOOST_LIB_DIR =$(HOME)/' Makefile
  sed -i 's/CXXFLAGS  += -I$(HOME)/#CXXFLAGS  += -I$(HOME)/' Makefile
  sed -i 's/zenXml/zenxml/' Makefile
  sed -i 's/CXXFLAGS[ \t]*=/CXXFLAGS = -Wno-unused-local-typedefs -Wno-deprecated-declarations -Wno-literal-suffix /' Makefile
  make launchpad

  cd RealtimeSync
  sed -i 's/WX_CONFIG_LIB =$(HOME)/#WX_CONFIG_LIB =$(HOME)/' Makefile
  sed -i 's/BOOST_LIB_DIR =$(HOME)/#BOOST_LIB_DIR =$(HOME)/' Makefile
  sed -i 's/CXXFLAGS  += -I$(HOME)/#CXXFLAGS  += -I$(HOME)/' Makefile
  sed -i 's/zenXml/zenxml/' Makefile
  sed -i 's/CXXFLAGS[ \t]*=/CXXFLAGS = -Wno-unused-local-typedefs -Wno-deprecated-declarations -Wno-literal-suffix /' Makefile
  make launchpad
}

package() {
  cd ${srcdir}/FreeFileSync/Source
  make DESTDIR=${pkgdir} install

  cd RealtimeSync
  make DESTDIR=${pkgdir} install

  cd ${srcdir}
  install -Dm644 FreeFileSync.desktop $pkgdir/usr/share/applications/FreeFileSync.desktop
  install -Dm644 ffsicon.png $pkgdir/usr/share/pixmaps/ffsicon.png
  install -Dm644 RealtimeSync.desktop $pkgdir/usr/share/applications/RealtimeSync.desktop
  install -Dm644 rtsicon.png $pkgdir/usr/share/pixmaps/rtsicon.png
}

