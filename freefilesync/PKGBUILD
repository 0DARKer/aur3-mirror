# Contributor: chenxing <cxcxcxcx at gmail.com>
# Maintainer: alexmo82 <25396682 at live.it>

pkgname=freefilesync
pkgver=5.18
pkgrel=2
pkgdesc="Visual folder comparison and synchronization"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/freefilesync/"
license=('GPLv3')
depends=(wxgtk boost)
conflicts=(freefilesync-ubuntu)
source=("http://downloads.sourceforge.net/project/freefilesync/freefilesync/v$pkgver/FreeFileSync_${pkgver}_Source.zip" FreeFileSync.desktop ffsicon.png RealtimeSync.desktop rtsicon.png)
md5sums=('9f2cf1b35e1c451963321b5bda86eb56'
         'a7be7841f47f29d8e6210c8ab592a1e3'
         '929a26a4e7c3cc18ff72f0113d5f6dcf'
         '90152f1021f2d6fb6ab4fa511dc60fa9'
         '92e47b1d27f5ef12c60e7eb069d4ffe8')

build() {
  cd ${srcdir}
  sed -i 's/LINKFLAGS =$/LINKFLAGS = -lz/' Makefile
  make BUILD=Launchpad

  cd RealtimeSync
  make BUILD=Launchpad
}

package() {
  cd ${srcdir}
  make DESTDIR=${pkgdir} install

  cd RealtimeSync
  make DESTDIR=${pkgdir} install

  cd ${srcdir}
  install -Dm644 FreeFileSync.desktop $pkgdir/usr/share/applications/FreeFileSync.desktop
  install -Dm644 ffsicon.png $pkgdir/usr/share/pixmaps/ffsicon.png
    install -Dm644 RealtimeSync.desktop $pkgdir/usr/share/applications/RealtimeSync.desktop
  install -Dm644 rtsicon.png $pkgdir/usr/share/pixmaps/rtsicon.png
}
