# Maintainer: Padfoot <padfoot at exemail dot com dot au>

pkgname=lightdm-gtk-greeter-osk
_srcname=lightdm-gtk-greeter
pkgver=1.3.0
pkgrel=1
pkgdesc="GTK+ greeter for LightDM with on-screen keyboard support"
arch=('i686'
      'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3'
         'LGPL3')
depends=('lightdm'
         'gtk3')
makedepends=('gtk-doc'
             'gnome-common'
             'gnome-doc-utils'
             'gobject-introspection'
             'intltool'
             'patch')
provides=('lightdm-gtk-greeter')
conflicts=('lightdm-gtk-greeter'
           'lightdm-gtk-greeter-bzr')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
install='lightdm-gtk-greeter.install'
source=("https://launchpad.net/$_srcname/1.4/$pkgver/+download/$_srcname-$pkgver.tar.gz"
        "lightdm-gtk-greeter.conf"
        "lightdm-gtk-greeter.patch")
md5sums=('b49cc62d25a89c2ff115fb0abc740165'
         'a3b36ca70dd4fc03f089bfacaa0ab963'
         '7e6395e9e056545898a6dafa1d81061b')

build() {
  cd "$srcdir/$_srcname-$pkgver"

  patch -p1 -i $srcdir/lightdm-gtk-greeter.patch
  
  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm \
    --disable-static
    
  make
}

package() {
  cd "$srcdir/$_srcname-$pkgver"
  
  make DESTDIR="${pkgdir}" install

  cp ../lightdm-gtk-greeter.conf $pkgdir/etc/lightdm
}
