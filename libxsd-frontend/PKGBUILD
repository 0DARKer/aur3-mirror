# Maintainer: kevku <kevku@msn.com>
pkgname=libxsd-frontend
pkgver=1.17.0
pkgrel=1
pkgdesc="A compiler frontend for the W3C XML Schema definition language"
arch=('x86_64' 'i686')
depends=('xerces-c' 'boost' 'libcult' 'libfrontend-elements')
makedepends=('build')
url="http://www.codesynthesis.com/projects/libxsd-frontend"
license=('GPL2')
source=("http://www.codesynthesis.com/download/$pkgname/1.17/$pkgname-$pkgver.tar.bz2")
md5sums=('a7e8914f6b8913688f130af43d3c555d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  #configuration

  mkdir -p \
	build/{ld,cxx/gnu} \
	build/import/lib{boost,cult,frontend-elements,xerces-c}

  cat >> build/cxx/configuration-dynamic.make <<- EOF
	cxx_id       := gnu
	cxx_optimize := n
	cxx_debug    := n
	cxx_rpath    := n
	cxx_pp_extra_options :=
	cxx_extra_options    := ${CXXFLAGS} -I/usr/include/boost
	cxx_ld_extra_options := ${LDFLAGS}
	cxx_extra_libs       :=
	cxx_extra_lib_paths  :=
		EOF

  cat >> build/cxx/gnu/configuration-dynamic.make <<- EOF
	cxx_gnu := g++
	cxx_gnu_libraries :=
	cxx_gnu_optimization_options :=
		EOF
  cat >> build/import/libboost/configuration-dynamic.make <<- EOF
	libboost_installed := y
	libboost_suffix := -mt
		EOF
  cat >> build/import/libcult/configuration-dynamic.make <<- EOF
	libcult_installed := y
		EOF
  cat >> build/ld/configuration-lib-dynamic.make <<- EOF
	ld_lib_type   := shared
		EOF
  cat >> build/import/libfrontend-elements/configuration-dynamic.make <<- EOF
	libfrontend_elements_installed := y
		EOF

  cat >> build/import/libxerces-c/configuration-dynamic.make <<- EOF
	libxerces_c_installed := y
		EOF

  make
}
package() {
 cd "$srcdir/$pkgname-$pkgver"
 install -D xsd-frontend/libxsd-frontend.so $pkgdir/usr/lib/libxsd-frontend.so
  find xsd-frontend -iname "*.cxx" \
		-o -iname "makefile" \
		-o -iname ".git*" \
		-o -iname "*.o" -o -iname "*.d" \
		-o -iname "*.m4" -o -iname "*.l" \
		-o -iname "*.cpp-options" -o -iname "*.so" | xargs rm -f
  
  mkdir $pkgdir/usr/include
  cp -r xsd-frontend $pkgdir/usr/include
}
