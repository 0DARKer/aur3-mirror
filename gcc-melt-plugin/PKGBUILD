# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc-melt-plugin
pkgver=0.8
_gccver=4.6
pkgrel=1
pkgdesc="Provides a lispy domain specific language to easily code GCC extensions in"
arch=('i686' 'x86_64')
url="http://gcc-melt.org/"
license=('GPL3')
depends=('gcc=4.6.1')
options=('!makeflags')
source=(http://gcc-melt.org/melt-${pkgver}-plugin-for-gcc-${_gccver}.tgz)
md5sums=('c6247a8d1f30b142e329dd8ec4f85c93')

build() {
  cd $srcdir/melt-${pkgver}-plugin-for-gcc-${_gccver}
  
  # fix typos in Makefile
  sed -i "s#install-melt-source:#install-melt-sources:#" Makefile
  sed -i "s#/melt-source#/melt-sources#" Makefile
  
  make
}

package() {
  cd $srcdir/melt-${pkgver}-plugin-for-gcc-${_gccver}
  make DESTDIR=$pkgdir install
}

