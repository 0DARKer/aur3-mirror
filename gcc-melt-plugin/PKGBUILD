# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc-melt-plugin
pkgver=0.9.1
_gccver=4.6
pkgrel=1
pkgdesc="Provides a lispy domain specific language to easily code GCC extensions in"
arch=('i686' 'x86_64')
url="http://gcc-melt.org/"
license=('GPL3')
depends=('gcc=4.6.1')
options=('!makeflags')
source=(http://gcc-melt.org/melt-${pkgver}-plugin-for-gcc-${_gccver}.tgz
        melt-0.9.1-gcc46-header.patch)
md5sums=('5d342073af875296d9aad1bd25aa59f9'
         '9cbec05c596b73b75fe1ec9fb55650a7')

build() {
  cd $srcdir/melt-${pkgver}-plugin-for-gcc-${_gccver}
  
  patch -Np1 -i ${srcdir}/melt-0.9.1-gcc46-header.patch
  
  # do not build documentation  (adds heavy makedepends...)
  sed -i "/all:/s# doc##" Makefile
  
  make
}

package() {
  cd $srcdir/melt-${pkgver}-plugin-for-gcc-${_gccver}
  make DESTDIR=$pkgdir install
}

