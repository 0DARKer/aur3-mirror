# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=bitfighter
pkgver=017a
pkgrel=1
pkgdesc="A free multiplayer 2-D space combat game with Robotron-like controls"
arch=('i686' 'x86_64')
url="http://bitfighter.org/"
license=('GPL')
depends=('libgl' 'libpng' 'libvorbis' 'openal' 'sdl' 'speex')
makedepends=('lua')
optdepends=('lua: plugins for the level editor')
install="${pkgname}.install"
source=("http://${pkgname}.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.png"
        "${pkgname}.desktop"
        "${pkgname}.sh")
md5sums=('4ceb2bd59c31b285e194ee9534b700bd'
         '8b28490af9deadd0a1a104ae372f5b08'
         'db0e0f7adc8b10e0f7622574eeecb61e'
         'dfb225149c92739d08d186e6ea0de747')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "36i#include \"zlib.h\"" zap/ScreenShooter.cpp
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install game files
  mkdir -p "${pkgdir}/opt/${pkgname}"
  chgrp games "${pkgdir}/opt/${pkgname}"
  chmod 775 "${pkgdir}/opt/${pkgname}"
  cp -r resource/* exe/*.{cfg,ini} "${pkgdir}/opt/${pkgname}"
  install -Dm755 "exe/${pkgname}" "${pkgdir}/opt/${pkgname}"

  # Delete music files to fix a game crash (v017a)
  rm -r "${pkgdir}/opt/${pkgname}/music"

  # Install launcher
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Install pixmap and .desktop file
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install docs
  install -Dm644 doc/README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 End-User*.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
