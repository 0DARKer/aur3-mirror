# Maintainer: cars10t <carsten.1 gmx net>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=bitfighter
pkgver=0.18a
pkgrel=1
pkgdesc="A fast-paced team-based outer-space multi-player combat game"
arch=('i686' 'x86_64')
url="http://bitfighter.org/"
license=('GPL')
depends=('libgl' 'libpng' 'libvorbis' 'libmodplug' 'openal' 'sdl' 'speex' 'desktop-file-utils')
makedepends=('lua' 'cmake')
optdepends=('lua: plugins for the level editor')
install="${pkgname}.install"
source=("http://${pkgname}.googlecode.com/files/${pkgname}-${pkgver//./}.tar.gz"
        "${pkgname}.png"
        "${pkgname}.desktop")
md5sums=('7400a23f461103920cda523237fd9196'
         '8b28490af9deadd0a1a104ae372f5b08'
         '657e0ff146dd3dac682e15be5a74fbf0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver//./}/build"
  cmake ..
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver//./}"

  # Install game files
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  mkdir -p "${pkgdir}/usr/bin"
  cp -r resource/* exe/*.{cfg,ini} "${pkgdir}/usr/share/${pkgname}"
  install -m755 exe/${pkgname} "${pkgdir}/usr/bin/${pkgname}"

  # Install a desktop entry
  install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install documentation
  install -Dm644 doc/README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 End-User*.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
