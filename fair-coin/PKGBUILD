# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: shahid <helllamer@gmail.com>

pkgbase=fair-coin
pkgname=('faircoin-daemon' 'faircoin-qt')
pkgver=1.2.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://fair-coin.org/"
makedepends=('boost' 'automoc4' 'qrencode' 'miniupnpc' 'protobuf')
license=('custom:MIT')
source=(https://github.com/FairCoinTeam/fair-coin/archive/v${pkgver}.tar.gz
	https://github.com/FairCoinTeam/fair-coin/raw/v1.1.1/contrib/debian/novacoin-qt.desktop
	https://raw.githubusercontent.com/bitcoin/bitcoin/master/share/pixmaps/bitcoin128.png
	https://raw.github.com/FairCoinTeam/fair-coin/v$pkgver/contrib/debian/examples/novacoin.conf
	https://raw.github.com/FairCoinTeam/fair-coin/v$pkgver/contrib/debian/manpages/novacoind.1
	https://raw.github.com/FairCoinTeam/fair-coin/v$pkgver/contrib/debian/manpages/novacoin.conf.5)

prepare() {
  sed -i -e "s/NoVa/Fair/g" -e "s/nova/fair/g" \
      ${srcdir}/novacoin-qt.desktop \
      ${srcdir}/novacoin.conf \
      ${srcdir}/novacoind.1 \
      ${srcdir}/novacoin.conf.5

  cd "$srcdir/$pkgbase-$pkgver"
  ./autogen.sh
}

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr --disable-maintainer-mode --disable-tests --with-incompatible-bdb --with-gui=qt4

  # fix error with missing DSO by adding -lrt
  # https://github.com/bitcoin/bitcoin/issues/879
  sed -i -e "s/^LIBS = .*$/& -lrt/" src/Makefile
  make
}

package_faircoin-qt() {
  pkgdesc="faircoin is a peer-to-peer network based digital currency - Qt"
  depends=(boost-libs qt4 miniupnpc qrencode protobuf)
  install=faircoin-qt.install

  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm755 src/qt/FairCoin-qt "$pkgdir"/usr/bin/faircoin-qt
  install -Dm644 "$srcdir"/novacoin-qt.desktop \
    "$pkgdir"/usr/share/applications/faircoin.desktop
  install -Dm644 "$srcdir"/bitcoin128.png \
    "$pkgdir"/usr/share/pixmaps/faircoin128.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_faircoin-daemon() {
  pkgdesc="faircoin is a peer-to-peer network based digital currency - daemon"
  depends=(boost-libs miniupnpc openssl)

  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm755 src/FairCoind "$pkgdir"/usr/bin/faircoind
  install -Dm644 "$srcdir"/novacoin.conf \
    "$pkgdir/usr/share/doc/$pkgname/examples/faircoin.conf"
  install -Dm644 "$srcdir"/novacoind.1 \
    "$pkgdir"/usr/share/man/man1/faircoind.1
  install -Dm644 "$srcdir"/novacoin.conf.5 \
    "$pkgdir"/usr/share/man/man5/faircoin.conf.5
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_faircoin-cli() {
  pkgdesc="faircoin is a peer-to-peer network based digital currency - RPC client"
  depends=(boost-libs openssl)

  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm755 src/faircoin-cli "$pkgdir"/usr/bin/faircoin-cli
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
md5sums=('ba30b5f35354bac5d4e14280356b5544'
         'd28e6e9094215dcca33555e3b0876356'
         '0aae5f0110179e8e1d588530bcce5d2a'
         '9723b6420548b651316cacf36d760074'
         'aae79bab068decaa0288c3c7859404b9'
         'a40ed2c3a72a6adfef312e255a50d85d')
