# Contributor: Nathan Hulse <nat.hulse@gmail.com>

_getthis=gvfs
pkgname=gvfs-smb-lite
pkgver=1.10.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="SMB/CIFS (Windows client) backend for gvfs"
provides=('gvfs-smb')
conflicts=('gvfs-smb')
depends=("gvfs-lite=${pkgver}" 'smbclient>=3.5.5' 'libgnome-keyring>=2.32.0')
install=gvfs-module.install
license=('LGPL')
makedepends=('fuse>=2.8.4' 'bluez>=4.77' 'smbclient>=3.5.5' 'pkgconfig' 'intltool')
url="http://www.gnome.org"
options=(!libtool)
source=(http://ftp.gnome.org/pub/gnome/sources/${_getthis}/1.10/${_getthis}-${pkgver}.tar.bz2)
md5sums=('7ca2acfa4dffd94b95f4b8713a346607')
sha256sums=('417a15e53fc20c2e1a1de5ea5903430b0ac2e98178d2fcbcfc8fba3082e801ea')

build() {
  cd "${srcdir}/${_getthis}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gvfs \
      --with-bash-completion-dir=/etc/bash_completion.d
  make
}

package() {
  cd "${srcdir}/${_getthis}-${pkgver}/daemon"
  install -m755 -d "${pkgdir}/usr/lib/gvfs"
  install -m755 -d "${pkgdir}/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-smb{,-browse} "${pkgdir}/usr/lib/gvfs/"
  install -m644 smb{,-browse}.mount "${pkgdir}/usr/share/gvfs/mounts/"
}


