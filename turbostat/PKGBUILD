# Contributor: graysky <graysky AT archlinux dot us>

pkgname=turbostat
pkgver=3.9.6
pkgrel=1
_srcname=linux-3.9
pkgdesc="Newer version of turbostat-shows CPU frequency and C-state residency on modern Intel turbo-capable processors."
arch=('x86_64' 'i686')
url="http://acpi.sourceforge.net/dsdt/index.php"
license=('GPL')
conflicts=('pmtools')
source=("http://www.kernel.org/pub/linux/kernel/v3.x/${_srcname}.tar.xz"
"http://www.kernel.org/pub/linux/kernel/v3.x/patch-${pkgver}.xz")

sha256sums=('60bc3e64ee5dc778de2cd7cd7640abf518a4c9d4f31b8ed624e16fad53f54541'
            '13296dad939ef4e05adba87b9d0476aa8e2ccf92866f14835327dae8a1402fc3')

prepare() {
  cd "$_srcname"
  patch -p1 -i "${srcdir}/patch-${pkgver}"
}

build() {
	cd "$_srcname/tools/power/x86/$pkgname"
	make
}

package() {
	cd "$_srcname/tools/power/x86/$pkgname"
	make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
