# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Prashant Vaibhav <mercurysquad@googlemail.com>

pkgname=juce
pkgver=1.52
pkgrel=1
pkgdesc="Jules Utility Class Extensions is an all-encompassing C++ class library for developing cross-platform applications."
arch=('i686' 'x86_64')
url="http://www.rawmaterialsoftware.com/juce/"
license=('GPL')
depends=('mesa' 'libxinerama' 'freetype2' 'pth' 'alsa-lib')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}_${pkgver/./_}.zip")
md5sums=('6cd492c77773f09ac4f5ab296abc011f')
#md5sums=('f6744484288a852b0cee75a177e6f4cd')

build() {
  cd "$srcdir/$pkgname/Builds/Linux"

  make CONFIG=Release

  # demo

  cd "../../extras/juce demo/Builds/Linux"

  make CONFIG=Release
}

package() {
  cd "$srcdir/$pkgname"

  # prepare dirs
  install -d "$pkgdir"/usr/{lib,bin,{share/doc,include}/$pkgname}

  # static lib
  install -Dm644 Builds/Linux/build/libjuce.a "$pkgdir/usr/lib/libjuce.a"

  # headers
  install -Dm644 *.{h,cpp} "$pkgdir/usr/include/$pkgname"

  # demo
  install -Dm755 "extras/juce demo/Builds/Linux/build/JuceDemo" "$pkgdir/usr/bin"

  # docs
  cp -a docs/* "$pkgdir/usr/share/doc/$pkgname"
}
