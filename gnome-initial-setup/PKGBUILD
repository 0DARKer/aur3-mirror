# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-initial-setup
pkgver=3.10.1.1
pkgrel=2
pkgdesc="Simple, easy, and safe way to prepare a new system"
arch=('i686' 'x86_64')
url="http://www.gnome.org/"
license=('GPL')
depends=('gnome-online-accounts' 'gdm' 'libpwquality')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*.*}/$pkgname-$pkgver.tar.xz
        goa-add.patch
        0001-goa-Prevent-a-use-after-free.patch
        0001-Disable-GOA-page-in-new-user-mode.patch)
sha256sums=('e99dfaed37037fe26b1e648bfac55785e9706fdb79b4856fbb2afb860618272b'
            '5278ece335c2f2078eac63cb6652ec3c3b4ba221080e5a1c8b38cf701ca92687'
            '6ae97fb266334caaa375ba026269f5f96c9cbc6c29a652b985b75e442f0d084e'
            '6be2cfc6b468425d5be900f08d8639a1aab9e1a2702b0977f2d38c24aad72a75')

prepare() {
  cd $pkgname-$pkgver

  # Apply patches from Fedora
  patch -Np1 -i ../goa-add.patch
  patch -Np1 -i ../0001-goa-Prevent-a-use-after-free.patch
  patch -Np1 -i ../0001-Disable-GOA-page-in-new-user-mode.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
