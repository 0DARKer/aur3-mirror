# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Matt Harrison <matt at mistbyte dot com>
# Contributor: Renzo Carbonara <gnuk0001@gmail.com>

pkgname=ruby-yard
_gemname=${pkgname#ruby-}
pkgver=0.8.6
pkgrel=1
pkgdesc="Documentation tool for consistent and usable documentation in Ruby."
arch=('any')
url="http://yardoc.org"
license=('MIT')
depends=('ruby')
makedepends=('rubygems')
source=("http://gems.rubyforge.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha256sums=('9b5f4ee7c84dad60bca59638fb25d87edac2084e6d654ac1d613efd7b4476ade')

package() {
	cd "$srcdir"
	local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
	gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
	install -D "$pkgdir$_gemdir/gems/${_gemname}-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
