# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>
# Contributor: Matt Harrison <matt at mistbyte dot com>
# Contributor: Renzo Carbonara <gnuk0001@gmail.com>

_gemname=yard
pkgname=ruby-$_gemname
pkgver=0.8.7.3
pkgrel=1
pkgdesc="Documentation tool for consistent and usable documentation in Ruby."
arch=('any')
url="http://yardoc.org"
license=('MIT')
depends=('ruby')
makedepends=('rubygems')
source=("http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha256sums=('a371992abc81801c4010fdd2aa91b3f9ad47e5179f41d888c8faa86048d74855')

package() {
	cd "$srcdir"
	local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
	gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
	install -D "$pkgdir$_gemdir/gems/${_gemname}-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
