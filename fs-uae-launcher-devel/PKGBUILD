# Maintainer: grimi <grimi at poczta dot fm>

_pkgname=fs-uae-launcher
pkgname=fs-uae-launcher-devel
pkgver=2.5.22dev
pkgrel=2
pkgdesc="Launcher and configuration program for FS-UAE (development version)."
arch=("any")
url="http://fs-uae.net/download-devel"
license=("GPL2")
depends=("fs-uae-devel" "python-pyqt4" "python-setuptools"
        "python-lhafile" "hicolor-icon-theme" "xdg-utils")
optdepends=("p7zip: for .7z zip support")
install="${pkgname}.install"
source=("http://fs-uae.net/devel/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
#source=("http://ppa.launchpad.net/fengestad/devel/ubuntu/pool/main/f/${_pkgname}/${_pkgname}_${pkgver}.orig.tar.gz")
provides=("fs-uae-launcher")
conflicts=("fs-uae-launcher")
md5sums=('2a83faf6de1c051c923d4d56c3377d3b')



build() {
   cd ${_pkgname}-${pkgver}

   python setup.py build
   make
}


package() {
   cd ${_pkgname}-${pkgver}

   python setup.py install --skip-build --prefix=/usr \
      --install-scripts=/usr/share/${_pkgname} \
      --install-lib=/usr/share/${_pkgname} --root="${pkgdir}"
   make install DESTDIR="${pkgdir}" prefix=/usr

   #install -d "${pkgdir}"/usr/bin
   #ln -s /usr/share/${_pkgname}/${_pkgname} "${pkgdir}"/usr/bin/${_pkgname}
}


# vim:set ts=3 sw=3 et:

