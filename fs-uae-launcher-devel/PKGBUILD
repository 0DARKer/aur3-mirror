# Maintainer: grimi <grimi at poczta dot fm>

_pkgname=fs-uae-launcher
pkgname=fs-uae-launcher-devel
pkgver=2.3.99beta2
pkgrel=1
pkgdesc="Launcher and configuration program for FS-UAE (development version)."
arch=("any")
url="http://fs-uae.net/download-devel"
license=("GPL2")
depends=("fs-uae-devel" "xdg-utils" "python2-pyqt4" "python2-setuptools" "python2-lhafile" "hicolor-icon-theme")
optdepends=("p7zip: for .7z zip support")
install="${pkgname}.install"
source=("http://fs-uae.net/stable/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
provides=("fs-uae-launcher")
conflicts=("fs-uae-launcher")
md5sums=('f53182f07388bb17112af69933313a52')



build() {
   cd ${_pkgname}-${pkgver}

   python2 setup.py build
   make
}



package() {
   cd ${_pkgname}-${pkgver}

   python2 setup.py install --skip-build --prefix=/usr \
      --install-scripts=/usr/share/${_pkgname} \
      --install-lib=/usr/share/${_pkgname} --root="${pkgdir}"
   make install DESTDIR="${pkgdir}"

   install -d "${pkgdir}"/usr/bin
   ln -s /usr/share/${_pkgname}/${_pkgname} "${pkgdir}"/usr/bin/${_pkgname}
}


# vim:set ts=3 sw=3 et:

