# Maintainer: Army <uli.armbruster @t gmail>

pkgname=guitarix-git
pkgver=20120413
pkgrel=1
pkgdesc="a virtual guitar amplifier for Linux"
arch=(i686 x86_64)
license=('GPL')
url="http://sourceforge.net/apps/wordpress/guitarix/"
depends=('gtkmm' 'jack' 'fftw' 'boost-libs')
makedepends=('python2' 'boost' 'intltool')
optdepends=('meterbridge: sound meters')
conflicts=('guitarix' 'guitarix2' 'gx_head')
provides=('guitarix' 'guitarix2' 'gx_head')
install=$pkgname.install

_gitroot="git://git.code.sf.net/p/guitarix/git"
_gitname="guitarix"

build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."
	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi
	msg "GIT checkout done or server timeout"

	rm -rf "${srcdir}/${_gitname}-build"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
	cd "${srcdir}/${_gitname}-build/trunk"

	python2 waf configure --prefix=/usr \
		--includeresampler \
		--includeconvolver \
		--no-faust \
		--optimization \
		--shared-lib
	python2 waf build
}

package() {
	cd "${srcdir}/${_gitname}-build/trunk"
	python2 waf install --destdir="$pkgdir"
} 
