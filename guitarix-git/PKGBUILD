# Maintainer: Army
_pkgname=guitarix
pkgname=$_pkgname-git
pkgver=20130427.2007
pkgrel=1
pkgdesc="a virtual guitar amplifier for Linux"
arch=('i686' 'x86_64')
url="http://guitarix.sourceforge.net"
license=('GPL')
depends=('gtkmm' 'jack' 'fftw' 'boost-libs' 'liblrdf')
makedepends=('git' 'python2' 'boost' 'intltool')
optdepends=('meterbridge: sound meters')
provides=("$_pkgname" 'guitarix2' 'gx_head')
conflicts=("$_pkgname" 'guitarix2' 'gx_head')
install=$pkgname.install
source=("$_pkgname::git+git://git.code.sf.net/p/guitarix/git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-list --count master)"
}

build() {
  cd "$srcdir/$_pkgname/trunk"
  python2 waf configure --prefix=/usr \
	  --includeresampler \
	  --includeconvolver \
	  --no-faust \
	  --optimization \
	  --build-lv2 \
	  --shared-lib
  python2 waf build
}

package() {
  cd "$srcdir/$_pkgname"
  cd trunk
  python2 waf install --destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
