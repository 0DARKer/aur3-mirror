# Maintainer: sekret
# Contributor: Army
_pkgname=guitarix
pkgname=$_pkgname-git
pkgver=20140117.2393
pkgrel=1
pkgdesc="a virtual guitar amplifier for Linux"
arch=('i686' 'x86_64')
url="http://guitarix.sourceforge.net"
license=('GPL')
depends=('gtkmm' 'jack' 'liblrdf' 'boost-libs' 'zita-convolver' 'zita-resampler' 'eigen3' 'bluez-libs')
makedepends=('git' 'python2' 'boost' 'intltool' 'lv2' 'gperf')
optdepends=('meterbridge: sound meters')
provides=("$_pkgname" 'guitarix2' 'gx_head')
conflicts=("$_pkgname" 'guitarix2' 'gx_head')
install=$pkgname.install
source=("$_pkgname::git+git://git.code.sf.net/p/guitarix/git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-list --count master)"
}

build() {
  cd "$srcdir/$_pkgname/trunk"
  python2 waf configure --prefix=/usr \
                        --no-desktop-update \
                        --no-ldconfig \
                        --no-faust \
                        --optimization \
                        --shared-lib \
                        --lib-dev
  python2 waf build
}

package() {
  cd "$srcdir/$_pkgname"
  cd trunk
  python2 waf install --destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
