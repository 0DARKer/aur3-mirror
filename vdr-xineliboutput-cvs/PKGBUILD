# Contributor: David Spicer <azleifel at gmail dot com>

pkgname=vdr-xineliboutput-cvs
_pluginname=xineliboutput
pkgver=20121226
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin that allows VDR to be used with budget cards"
url="http://sourceforge.net/projects/xineliboutput/"
license=('GPL2')
depends=('dbus-glib' 'libextractor' 'libbluray' 'vdr' 'xine-lib')
makedepends=('cvs' 'pkgconfig')
optdepends=('xine-ui: An alternative frontend')
provides=('vdr-xineliboutput')
conflicts=('vdr-xineliboutput')
backup=('etc/vdr/plugins/xineliboutput/allowed_hosts.conf')
install=${pkgname}.install
source=(remote.conf.LIRC.devinput
        remote.conf.LIRC.mceusb
        vdr-sxfe.desktop
        vdr-xine-ui.desktop)
md5sums=('ecb0f2ba5e1b230b683909097ff72108'
         '8a1d026743620a3f5fd2e9c31155c69d'
         'de4b514c2a5e6d84dc4b87ea3b9fde9e'
         '2d5cef1e40a5c7b920ea75c3d129c61f')

_cvsroot=":pserver:anonymous@xineliboutput.cvs.sourceforge.net:/cvsroot/xineliboutput"
_cvsmod="vdr-${_pluginname}"

build() {
  cd ${srcdir}
  msg "Connecting to $_cvsmod.sourceforge.net CVS server...."
  if [ -d $_cvsmod/CVS ]; then
    cd $_cvsmod
    cvs -z3 update -d
  else
    cvs -z3 -d $_cvsroot co -D $pkgver -f $_cvsmod
    cd $_cvsmod
  fi

  msg "CVS checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_cvsmod}-build"
  cp -r "${srcdir}/${_cvsmod}" "${srcdir}/${_cvsmod}-build"
  cd "${srcdir}/${_cvsmod}-build"

  # Assumed VDR directory environment (VDRDIR/Make.config):
  # MANDIR       = /usr/share/man
  # BINDIR       = /usr/bin
  # LOCDIR       = /usr/share/locale
  # PLUGINLIBDIR = /usr/lib/vdr
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr

  install -d -m755 "${pkgdir}/etc/vdr/plugins/${_pluginname}"
  install -d -m755 "${pkgdir}/usr/lib/vdr"
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -d -m755 "${pkgdir}/usr/share/locale"

  make VDRDIR="/usr/include/vdr" \
       LIBDIR="/usr/lib/vdr" \
       LOCALEDIR="${pkgdir}/usr/share/locale" \
       DESTDIR="${pkgdir}" install
  
  # Install configuration etc. files
  install -m644 examples/allowed_hosts.conf "${pkgdir}/etc/vdr/plugins/${_pluginname}"
  
  # Install documents and example remote.conf
  for _file in HISTORY README examples/remote.conf.example "${srcdir}/remote.conf.LIRC.devinput" "${srcdir}/remote.conf.LIRC.mceusb"; do
    install -m644 "${_file}" "${pkgdir}/usr/share/doc/${pkgname}/${_file##*/}"
  done
  
  # Install homebrew desktop files
  install -D -m644 "${srcdir}/vdr-sxfe.desktop" "${pkgdir}/usr/share/applications/vdr-sxfe.desktop"
  install -m644 "${srcdir}/vdr-xine-ui.desktop" "${pkgdir}/usr/share/applications/vdr-xine-ui.desktop"
}
