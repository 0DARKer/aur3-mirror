# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-git
pkgver=r195.e7441fc
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/base/baloo'
license=('LGPL')
depends=('kdepimlibs' 'xapian-core')
makedepends=('cmake' 'automoc4' 'git')
source=('git://anongit.kde.org/baloo')
md5sums=('SKIP')

pkgver() {
  cd baloo
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../baloo \
    -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
