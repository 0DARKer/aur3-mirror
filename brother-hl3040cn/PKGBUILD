# Contributor: Libernux <dutchman55@gmx.com>
pkgname="brother-hl3040cn"
pkgver="1.1.2"
pkgrel=1
pkgdesc="LPR and CUPS driver for the Brother HL3040cn"
url="http://solutions.brother.com/linux/en_us/"
arch=('i686' 'x86_64')
license=('custom:brother commercial license')
depends=('cups')
install='brother-hl3040cn.install'
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/hl3040cnlpr-$pkgver-1.i386.rpm"
	"http://www.brother.com/pub/bsc/linux/dlf/hl3040cncupswrapper-$pkgver-2.i386.rpm"
	'cupswrapper-license.txt'
	'lpr-license.txt'
)
md5sums=(
	'66a568f725a4fe70636061306fcb1a9f'
	'90d1e077284553730dd178c80fc502f3'
	'97ad0cffd216059e9d1d3121899d8646'
	'5e87a3dc0f3e3438c088eda0f3565f0d'
)
build() {
	cd "$srcdir/opt/brother/Printers/hl3040cn/cupswrapper"
#  generate the wrapper
	echo '#! /bin/sh' > genwrapper
	sed -n '/^printer_model/,/^device_model/p' cupswrapperhl3040cn >>genwrapper
	echo 'tmp_filter=brlpdwrapperhl3040cn' >>genwrapper
	echo 'touch $tmp_filter' >>genwrapper
	sed -n '/cat <<!ENDOFWFILTER!/,/!ENDOFWFILTER!/p' cupswrapperhl3040cn >>genwrapper
	sed -i  's|/usr/share/cups/model/Brother|/usr/share/cups/model|g' genwrapper
	chmod +x genwrapper
	./genwrapper
	rm "$srcdir/opt/brother/Printers/hl3040cn/cupswrapper/cupswrapperhl3040cn"
	rm "$srcdir/opt/brother/Printers/hl3040cn/cupswrapper/genwrapper"
	rm "$srcdir/opt/brother/Printers/hl3040cn/inf/setupPrintcapij"
}
package() {
	cd "$pkgdir"
	cp -R "$srcdir/opt" "$pkgdir"
	cp -R "$srcdir/usr" "$pkgdir"
	install -m 644 -D "$srcdir/opt/brother/Printers/hl3040cn/cupswrapper/brother_hl3040cn_printer_en.ppd" "$pkgdir/usr/share/cups/model/brother_hl3040cn_printer_en.ppd"
	install -m 755 -D "$srcdir/opt/brother/Printers/hl3040cn/cupswrapper/brlpdwrapperhl3040cn" "$pkgdir/usr/lib/cups/filter/brlpdwrapperhl3040cn"
	install -m 644 -D "$startdir"/cupswrapper-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/cupswrapper-licence.txt"
	install -m 644 -D "$startdir"/lpr-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/lpr-licence.txt"
}