pkgname=ganglia
pkgver=3.3.1
pkgrel=1
pkgdesc="A scalable distributed monitoring system for high-performance computing systems such as clusters and Grids."
arch=('i686' 'x86_64')
url="http://ganglia.sourceforge.net/"
license=('GPL')
depends=('apr' 'confuse' 'expat' 'pcre' 'python2' 'rrdtool')
options=('!libtool')
install='ganglia.install'
backup=('etc/ganglia/gmond.conf'
        'etc/ganglia/gmetad.conf')
source=("http://downloads.sourceforge.net/ganglia/$pkgname-$pkgver.tar.gz" 
        'gmond.rc'
        'gmetad.rc'
        'ganglia.install'
        'uid.patch')
md5sums=('93b46f84e554def5efc5c05ad61e9a1c'
         '12b2f85361754eaf6fc5129cccec7034'
         '292b74a5a09a0bd9a4a658823adc034b'
         'e10f1b3d721e568fa134c907a2c33ae0'
         '7859c71b6506459cfd78167edfc27f29')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  for patch in $srcdir/*.patch; do
    msg2 "Applying $(basename $patch)"
    patch -Np1 -i $patch
  done

  msg2 "Starting build"
  ./configure --prefix=/usr --sysconfdir=/etc/ganglia --with-gmetad \
              --enable-gexec --enable-status --with-python=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  msg2 "Generating default gmond.conf"
  ./gmond/gmond --default_config > "$pkgdir/etc/ganglia/gmond.conf"
  
  # Install the rc files
  install -m 755 -D "$srcdir/gmond.rc" "$pkgdir/etc/rc.d/gmond"
  install -m 755 -D "$srcdir/gmetad.rc" "$pkgdir/etc/rc.d/gmetad"

  msg2 "Installing web frontend"
  mkdir -p "$pkgdir/usr/share/webapps"
  cd web
  sed -e "s|@varstatedir@|/var/lib|" conf_default.php.in > conf_default.php
  cp -a . "$pkgdir/usr/share/webapps/ganglia"
}
