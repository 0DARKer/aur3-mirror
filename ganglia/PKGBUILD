# Contributor: Lee.MaRS <leemars@gmail.com>
pkgname=ganglia
pkgver=3.1.2
pkgrel=1
pkgdesc="A scalable distributed monitoring system for high-performance computing systems such as clusters and Grids."
arch=('i686' 'x86_64')
url="http://ganglia.sourceforge.net/"
license=('GPL')
depends=('rrdtool' 'python' 'expat' 'confuse' 'apr')
options=('!libtool')
install='ganglia.install'
source=("http://downloads.sourceforge.net/ganglia/$pkgname-$pkgver.tar.gz" 
        "gmond.conf"
        "gmetad.conf"
        "gmond.service"
        "gmetad.service"
        "ganglia.install")        
md5sums=('aaba09a6fa897ed581789f679f7ca62d'
         '63f56c2cccf0092d6bff23db2f48b8ce'
         'da4034ac64c9c7f99a1437bf07d91ad1'
         'b94ecc823d4969cb66dc1731663b6003'
         '1dae4eb8b1cd408c03c333e0dec850d6'
         '3c5d0736fca25d19e8367b838bea69aa')

build() {
  src="$startdir/src"
  srcpkg="$src/$pkgname-$pkgver"
  dstpkg="$startdir/pkg"
  
  cd "$srcpkg"

  ./configure --prefix=/usr --with-gmetad --enable-gexec --enable-status
  make || return 1
  make DESTDIR="$dstpkg" install
  
  # Copy the man pages
  mkdir -p "$dstpkg/usr/share/man/man1"
  cp -f "$srcpkg/mans"/* "$dstpkg/usr/share/man/man1"
  
  # Copy the example configuration files
  install -m 644 -D "$src/gmond.conf" "$dstpkg/etc/ganglia/gmond.conf.example"
  install -m 644 -D "$src/gmetad.conf" "$dstpkg/etc/ganglia/gmetad.conf.example"

  # Copy the web frontend
  mkdir -p "$dstpkg/usr/share/ganglia"
  cp -rf "$srcpkg/web" "$dstpkg/usr/share/ganglia/web"
  
  # Install the services
  install -m 755 -D "$src/gmond.service" "$dstpkg/etc/rc.d/gmond"
  install -m 755 -D "$src/gmetad.service" "$dstpkg/etc/rc.d/gmetad"
}

# vim:set ts=2 sw=2 et:
