# Maintainer: Facundo Tuesca <facutuesca at gmail dot com>

pkgname=k2pdfopt
pkgver=2.16
pkgrel=1
pkgdesc="A tool that optimizes PDF files for viewing on mobile readers"
arch=('i686' 'x86_64')
url="http://www.willus.com/k2pdfopt/"
license=('GPL3')
depends=('jbig2dec>=0.11'
	'openjpeg2>=2.0.0'
	'freetype2>=2.5.0.1'
	'zlib>=1.2.8'
	'libpng>=1.6.3'
	'jasper>=1.900.1'
	'djvulibre>=3.5.25.3'
	'libjpeg-turbo>=1.3.0'
	'netpbm>=10.61.02'
	'gsl>=1.16'
	'leptonica>=1.69')
source=("http://www.willus.com/${pkgname}/src/${pkgname}_v${pkgver}_src.zip"
	"http://mupdf.googlecode.com/files/mupdf-1.3-source.tar.gz"
	"http://tesseract-ocr.googlecode.com/files/tesseract-ocr-3.02.02.tar.gz"
	"http://www-e.uni-magdeburg.de/jschulen/ocr/gocr-0.49.tar.gz"
	"k2pdfopt.patch"
	"tesseract.patch")
md5sums=('616226f04d21df758d970f1c5bb986d7'
         'fe53c2a56ebd7759f5f965bc4ff66359'
         '26adc8154f0e815053816825dde246e6'
         '4e527bc4bdd97c2be15fdd818857507f'
         '350eb0e949b0056e778bd8939528d0ee'
         '0e85e48aed62771dfc090787c079359d')

prepare() {
	cd "${srcdir}/${pkgname}_v${pkgver}"
	rm -f "include_mod/gocr.h"
	cp mupdf_mod/font.c mupdf_mod/string.c "${srcdir}/mupdf-1.3-source/source/fitz/"
	cp mupdf_mod/pdf-* "${srcdir}/mupdf-1.3-source/source/pdf/"
	rm -rf ${srcdir}/mupdf-1.3-source/thirdparty/{curl,freetype,jpeg,zlib,jbig2dec}
	cp tesseract_mod/dawg.cpp "${srcdir}/tesseract-ocr/dict/"
	cp tesseract_mod/tessdatamanager.cpp "${srcdir}/tesseract-ocr/ccutil/"
	cp tesseract_mod/tessedit.cpp "${srcdir}/tesseract-ocr/ccmain/"
	cp tesseract_mod/tesscapi.cpp "${srcdir}/tesseract-ocr/api"
	cp include_mod/tesseract.h include_mod/leptonica.h "${srcdir}/tesseract-ocr/api/"
	cd "${srcdir}"
	patch -p0 -i "${srcdir}/tesseract.patch"
	patch -p0 -i "${srcdir}/k2pdfopt.patch"
	mkdir -p "patched_libraries"
}

build() {
	cd "${srcdir}/mupdf-1.3-source/"
	make prefix="${srcdir}/patched_libraries" install
	cd "${srcdir}/tesseract-ocr/"
	./autogen.sh
	./configure --prefix="${srcdir}/patched_libraries" --disable-shared
	make install
	cd "${srcdir}/gocr-0.49/"
	./configure 
	cp src/{gocr.h,pnm.h,unicode.h,list.h} "${srcdir}/patched_libraries/include"
	cp include/config.h "${srcdir}/patched_libraries/include"
	make libs
	cp src/libPgm2asc.a "${srcdir}/patched_libraries/lib"

	cd "${srcdir}/${pkgname}_v${pkgver}/k2pdfoptlib"
	gcc  -Wall -c *.c -I ../include_mod/ -I ${srcdir}/patched_libraries/include \
		-I . -I ../willuslib
	ar rcs libk2pdfopt.a *.o
	cd "${srcdir}/${pkgname}_v${pkgver}/willuslib"
	gcc -Wall -c *.c -I ../include_mod/ -I ${srcdir}/patched_libraries/include
	ar rcs libwillus.a *.o
	cd "${srcdir}/${pkgname}_v${pkgver}"
	gcc  -Wall -o k2pdfopt.o -c k2pdfopt.c -I k2pdfoptlib/ -I willuslib/ \
		-I include_mod/ -I ${srcdir}/patched_libraries/include
	g++ -Ofast k2pdfopt.o -o k2pdfopt -I willuslib/ -I k2pdfoptlib/ -I include_mod/ \
		 -I ${srcdir}/patched_libraries/include -L ${srcdir}/patched_libraries/lib/ \
		-L willuslib/ -L k2pdfoptlib/ -lk2pdfopt -lwillus -ldjvulibre -lz -lmupdf  \
		-ljbig2dec -ljpeg -lopenjp2 -lpng -lfreetype -lpthread -lmupdf-js-none \
		-ljasper -lPgm2asc -lgsl -lgslcblas -llept -ltesseract
	
}

package() {
	cd "${srcdir}/${pkgname}_v${pkgver}"
	mkdir -p "${pkgdir}/usr/bin/"
	cp ${pkgname} "${pkgdir}/usr/bin/"
}
