# Maintainer: Facundo Tuesca <facutuesca at gmail dot com>

pkgname=k2pdfopt
pkgver=2.14
pkgrel=2
pkgdesc="A tool that optimizes PDF files for viewing on mobile readers"
arch=('i686' 'x86_64')
url="http://www.willus.com/k2pdfopt/"
license=('GPL3')
depends=('jbig2dec>=0.11'
	'openjpeg2>=2.0.0'
	'freetype2>=2.5.0.1'
	'zlib>=1.2.8'
	'libpng>=1.6.3'
	'jasper>=1.900.1'
	'libjpeg-turbo>=1.3.0-4')
makedepends=('cmake>=2.6')
optdepends=('djvulibre>=3.5.25.3: djvu support')
source=("http://www.willus.com/${pkgname}/src/${pkgname}_v${pkgver}_src.zip"
	"http://mupdf.googlecode.com/files/mupdf-1.3-source.tar.gz"
	"mupdf.patch"
	"${pkgname}.patch")
md5sums=('2ecd5ded8c184bf2b008acd6e300c870'
         'fe53c2a56ebd7759f5f965bc4ff66359'
         'ca17eeee8b619da1e9969c4f5da95b68'
         '2a4d9aefd06c66f7613fe59b45d50468')

prepare() {
	cd "${srcdir}/${pkgname}_v${pkgver}"
	mkdir -p "mupdf"
	cp "${srcdir}/mupdf-1.3-source/platform/debian/mupdf.pc" "mupdf/" 
	patch -p0 -i "${srcdir}/mupdf.patch"
	cp "mupdf_mod/font.c" "mupdf_mod/string.c" "${srcdir}/mupdf-1.3-source/source/fitz/"
	cp mupdf_mod/pdf-* "${srcdir}/mupdf-1.3-source/source/pdf/"
	patch -p1 -i "${srcdir}/${pkgname}.patch"
}

build() {
	cd "${srcdir}/mupdf-1.3-source"
	make prefix="${srcdir}/${pkgname}_v${pkgver}/mupdf" install
	cd "${srcdir}/${pkgname}_v${pkgver}"
	cmake CMakeLists.txt
	make
}

package() {
	cd "${srcdir}/${pkgname}_v${pkgver}"
	mkdir -p "${pkgdir}/usr/bin/"
	cp ${pkgname} "${pkgdir}/usr/bin/"
}
