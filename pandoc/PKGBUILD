# Maintainer: Nicolas Reynolds <fauno@kiwwwi.com.ar>
# Based on haskell-pandoc
# custom variables
_hkgname=pandoc
_licensefile=COPYING

# PKGBUILD options/directives
pkgname=pandoc
pkgver=1.8.2.1
pkgrel=2
pkgdesc='Conversion between markup formats (no Haskell libs)'
url='http://johnmacfarlane.net/pandoc'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('ghc' 'sh' 'haskell-http' 'haskell-base64-bytestring'
'haskell-bytestring' 'haskell-citeproc-hs' 'haskell-containers'
'haskell-directory' 'haskell-dlist' 'haskell-extensible-exceptions'
'haskell-filepath' 'haskell-json' 'haskell-mtl' 'haskell-network'
'haskell-old-time' 'haskell-pandoc-types' 'haskell-parsec' 'haskell-process'
'haskell-random' 'haskell-syb' 'haskell-tagsoup' 'haskell-texmath'
'haskell-utf8-string' 'haskell-xhtml' 'haskell-xml' 'haskell-zip-archive')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('f1025377b2c78043b06d7963eb61ce66c2290d2580f2488a8186c82c24f09433')
conflicts=('haskell-pandoc')

# PKGBUILD functions
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}

    msg2 "Removing Haskell libs"
    rm -rvf ${pkgdir}/usr/lib
}
