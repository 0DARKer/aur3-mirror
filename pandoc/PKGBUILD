# Maintainer: Nicolas Reynolds <fauno@kiwwwi.com.ar>
# Based on haskell-pandoc

# Run `makepkg -sp SRCBUILD` if you want to update the source tarball

pkgname=pandoc
pkgver=1.9.4.5
pkgrel=5
pkgdesc='Conversion between markup formats (no Haskell libs)'
url='http://johnmacfarlane.net/pandoc/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('ghc' 'sh' 'cabal-install')
options=(strip !makeflags !distcc)
source=(https://repo.parabolagnulinux.org/other/${pkgname}-${pkgver}-$pkgrel-any.src.tar.xz{,.sig} SRCBUILD)
conflicts=('haskell-pandoc')
optdepends=('texlive-most: for pdf creation')

# PKGBUILD functions
build() {
    mkdir -p ${srcdir}/{build,${pkgname}-${pkgver}}
    cd ${srcdir}/${pkgname}-${pkgver}

   while read _hkpkg; do
       pushd ${srcdir}/${pkgname}-${pkgver}/${_hkpkg} >/dev/null

       msg2 "Building $_hkpkg"

       case $_hkpkg in
        $pkgname-$pkgver)
         HOME=${srcdir}/${pkgname}-${pkgver} \
         cabal configure --flags='-library' \
                         --prefix=/usr \
                         --libdir=${srcdir}/build/usr/lib -v

         HOME=${srcdir}/${pkgname}-${pkgver} \
         cabal build
         ;;

        citeproc-hs-*)
         HOME=${srcdir}/${pkgname}-${pkgver} \
         cabal install --flags='embed_data_files' \
                       --prefix=${srcdir}/build/usr -v
         ;;

        *)
         HOME=${srcdir}/${pkgname}-${pkgver} \
         cabal install --prefix=${srcdir}/build/usr
         ;;
       esac

       popd >/dev/null
   done <BUILDORDER

}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/${pkgname}-${pkgver}

  runghc Setup.hs copy --destdir=${pkgdir}/

  msg2 "Removing lib files..."
  rm -rfv ${pkgdir}/build

# EC is unfree and makes Parabola TeXLive cry
# besides, it's unneeded
  sed "/fontenc/d" -i ${pkgdir}/usr/share/${pkgname}-${pkgver}/templates/default.latex

  find ${pkgdir}/usr/share -type f -exec chmod 644 {} \;
  find ${pkgdir}/usr/share -type d -exec chmod 755 {} \;
}
md5sums=('af01e55daded42fa54e8a2bb142c0a78'
         '96d0ba0d9060574e56e3f23c996d0e8e'
         '54b521d5c3ed5e2b53c4ca3206f6c941')
