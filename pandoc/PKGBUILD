# Maintainer: Jim Pryor <profjim@jimpryor.net>
# Contributor: Alex Griffin <griffin.aj@gmail.com>
pkgname=pandoc
pkgver=1.6
pkgrel=3
pkgdesc="Convert between markup formats (no haskell libs)"
url="http://johnmacfarlane.net/pandoc/"
# url="http://hackage.haskell.org/package/pandoc"
license=('GPL2' 'custom')
arch=('i686' 'x86_64')
depends=('gmp' 'zlib')
makedepends=('ghc' 'haskell-cabal' 'haskell-random' 'haskell-http>=4000.0.5' 'haskell-bytestring>=0.9' 'haskell-containers>=0.1' 'haskell-directory>=1' 'haskell-extensible-exceptions' 'haskell-filepath>=1.1' 'haskell-mtl>=1.1' 'haskell-network>=2' 'haskell-old-time>=1' 'haskell-parsec>=2.1' 'haskell-pretty>=1' 'haskell-process>=1' 'haskell-syb' 'haskell-texmath>=0.3' 'haskell-utf8-string>=0.3' 'haskell-xhtml>=3000.0' 'haskell-xml>=1.3.5' 'haskell-xml<1.4' 'haskell-zip-archive>=0.1.1.4')
options=('strip')
conflicts=('haskell-pandoc')
source=(http://pandoc.googlecode.com/files/$pkgname-$pkgver.tar.gz)
# source=(http://hackage.haskell.org/packages/archive/pandoc/$pkgver/pandoc-$pkgver.tar.gz)
md5sums=('d7682167664e0e8f60268e491f1330ce')

build() {
    cd $srcdir/$pkgname-$pkgver

    # For citeproc support, add -fciteproc as an option (requires
    #   haskell-citeproc-hs at compile time, and curl at run time)
    # For syntax highlighting support, add -fhighlighting (requires
    #   haskell-highlighting-kate at compile time, and pcre at run time)
	# -f-library flag was removed, see http://github.com/jgm/pandoc/issues/issue/2/
    runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname} || return 1
    runhaskell Setup build || return 1
    runhaskell Setup copy --destdir=$pkgdir || return 1

	# Man pages are nice! default install gives bad permissions
	install -D -m644 man/man1/pandoc.1        $pkgdir/usr/share/man/man1/pandoc.1
	install -D -m644 man/man1/markdown2pdf.1  $pkgdir/usr/share/man/man1/markdown2pdf.1

	cd $pkgdir

	# delete unneeded libraries
	rm -rf usr/lib

    # Shell scripts providing useful wrappers around pandoc, no longer updated
	rm -f usr/share/$pkgname-$pkgver/markdown2pdf

    # Other documents
    install -D -m644 usr/share/$pkgname-$pkgver/COPYRIGHT     usr/share/licenses/$pkgname/COPYRIGHT || return 1
    install -D -m644 usr/share/$pkgname-$pkgver/README        usr/share/doc/$pkgname/README || return 1
    install -D -m644 usr/share/$pkgname-$pkgver/changelog     usr/share/doc/$pkgname/changelog || return 1
	rm -rf usr/share/$pkgname-$pkgver/{BUGS,INSTALL,COPYRIGHT,README,changelog} usr/share/doc/$pkgname/COPYING

}

