# Maintainer: Sergey Omelkov <omelkovs at gmail dot com>>

pkgname=gnome-settings-daemon-120dpi
_pkgname=gnome-settings-daemon
pkgver=3.8.1
pkgrel=1
pkgdesc="The GNOME Settings daemon with changed hardcoded DPI value to 120"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'gnome-desktop' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'libcanberra-pulse' 'libnotify'
         'libsystemd' 'libwacom' 'nss' 'pulseaudio' 'pulseaudio-alsa' 'upower')
makedepends=('intltool' 'xf86-input-wacom' 'libxslt' 'docbook-xsl')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
provides=('gnome-settings-daemon')
conflicts=('gnome-settings-daemon')
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz
        0002-set-dpi-to-120.patch)
sha256sums=('367a770ec7affff3c85a43bc39ced5a02a1b1761780d954329f6b21c0f2efcee'
            '56dde24e4ae567a90aa8b2250f81965eba77c60dff670de7c60a95f445e02f28')
         
build() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../../0002-set-dpi-to-120.patch

  aclocal
  autoconf
  automake --add-missing

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-settings-daemon --disable-static \
      --enable-systemd --disable-ibus

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Plugins that aren't installed still have schema references
  # and cause gsettings errors - remove the references we're not using
  sed -i '/org\.gnome\.settings-daemon\.plugins\.updates/d' \
    "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.settings-daemon.plugins.gschema.xml"
}


