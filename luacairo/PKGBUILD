# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=luacairo
pkgver=1.10.0.1
pkgrel=1
pkgdesc="Lua binding of Cairo"
arch=(i686 x86_64)
url="http://www.dynaset.org/dogusanh/pgluacairo.html"
license=('GPL')
depends=('cairo' 'gtk2' 'lua')
source=("http://www.dynaset.org/dogusanh/download/$pkgname-$pkgver-src.zip")
md5sums=('4912325172c3fbf6478da50178d55179')

build() {

  cd "$srcdir/$pkgname-$pkgver-src/src"

  # FC_FONT Fix
  sed -i "64,86d;94,97d" lcairo-ft-font.c
  # CFLAGS Fix
  sed -i "s_\(lua\)5.1_\1_g" Makefile

  make
}

package() {

  cd "$srcdir/$pkgname-$pkgver-src"

  mkdir -p "$pkgdir"/usr/{lib/lua/5.1,share/{doc/$pkgname,$pkgname/examples}}

  install -Dm 755 src/lcairo.so "$pkgdir/usr/lib/lua/5.1"
  install -Dm 644 doc/* "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 examples/* "$pkgdir/usr/share/$pkgname/examples"
}

# vim:set ts=2 sw=2 et:
