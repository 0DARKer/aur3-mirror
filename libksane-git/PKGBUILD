pkgname=libksane-git
pkgver=r345.a37669b
pkgrel=1
pkgdesc='An image scanning library'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'sane')
makedepends=('git' 'automoc4' 'cmake')
conflicts=('libksane')
provides=('libksane')
groups=('digikam-git')
source=('git://anongit.kde.org/libksane')
md5sums=('SKIP')

pkgver() {
  cd libksane
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake  ../libksane -DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}