# Maintainer: mutantmonkey <aur@mutantmonkey.in>
pkgname=qgis-git
pkgver=20120429
pkgrel=1
pkgdesc="A Geographic Information System (GIS) that supports vector, raster & database formats; dev version"
arch=('i686' 'x86_64')
url="http://qgis.org/"
license=('GPL')
depends=('libmysqlclient' 'postgresql-libs' 'sqlite3' 'jasper' 'curl' 'qt' 'python2' 'python2-qt' 'giflib'  'cfitsio' 'qwt5' 'gdal' 'flex' 'bison' 'libspatialite' 'libspatialindex')
makedepends=('cmake' 'gsl' 'postgis' 'netcdf' 'python2-sip')
optdepends=('postgis: postgis support and SPIT plugin'
            'fcgi: qgis mapserver'
            'python2-sip: python-support'
            'grass: grass plugin'
            'gsl: georeferencer  ')
provides=('qgis')
replaces=('qgis-svn')
conflicts=('qgis')
options=('!makeflags')
source=(qgis.desktop)
sha256sums=('1d487c6d4ce207593e687c6f6873b4463b1f5bda2a3edb1383735f9f3e27ff20')

_gitroot=https://github.com/qgis/Quantum-GIS.git
_gitname=qgis

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DGRASS_PREFIX=/opt/grass \
		-DQGIS_MANUAL_SUBDIR=share/man \
    -DQWT_INCLUDE_DIR=/usr/include/qwt5 \
    -DQWT_LIBRARY=/usr/lib/libqwt5.so \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DPYTHON_SITE_PACKAGES_DIR=/usr/lib/python2.7/site-packages \
    -DPYTHON_INCLUDE_PATH=/usr/include/python2.7

	make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

  # create a more user-friendly application name link
  ln -s /usr/bin/qgis $pkgdir/usr/bin/quantum-gis

	# install some freedesktop.org compatibility
	install -D -m644 ${srcdir}/qgis.desktop \
		${pkgdir}/usr/share/applications/qgis.desktop
}

# vim:set ts=2 sw=2 et:
