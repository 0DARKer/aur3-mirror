# Contributor: Twa022 <twa022@gmail.com>

pkgname=vlc-legacy
_pkgname=vlc
pkgver=0.8.6i
_x264ver=20080625
_xvidver=1.1.3
pkgrel=6
pkgdesc="A multi-platform MPEG, VCD/DVD, and DivX player"
arch=(i686 x86_64)
url="http://www.videolan.org/vlc/"
license=('GPL')
depends=('libmad>=0.15.1b-2' 'libmpeg2>=0.4.0b-3'
         'wxgtk' 'hal>=0.5.9.1' 'libid3tag>=0.15.1b-2' 'bash>=3.1'
         'libdvbpsi>=0.1.5' 'fribidi>=0.10.7' 'sysfsutils>=2.0.0'
         'libdvdnav>=0.1.10-2' 'libdvdread>=0.9.4-3'
         'libmatroska>=0.8.0' 'libxv>=1.0.1' 'libcdio>=0.78.2' 'desktop-file-utils'
         'lame' 'sdl' 'libvorbis' 'a52dec' 'faad2>=2.6.1' 'faac' 'zlib' 'imlib2'
         'libtheora' 'libdca' 'gnutls>=2.4.1')
#Note: These build plugins, so I guess they'd be runtime depends
makedepends=('live-media' 'gnome-vfs' 'mesa' 'sdl_image' 'libmpcdec' 'speex'
             'libnotify' 'libmodplug' 'lirc-utils' 'subversion' 'yasm')
options=(force)
source=(http://download.videolan.org/pub/videolan/vlc/${pkgver}/${_pkgname}-${pkgver}.tar.bz2
		vlc-legacy.desktop
		vlc-legacy
		ftp://ftp.videolan.org/pub/videolan/x264/snapshots/x264-snapshot-$_x264ver-2245.tar.bz2
		http://downloads.xvid.org/downloads/xvidcore-${_xvidver}.tar.bz2)

build() {
## Build internal x264
    echo "Building internal x264" && sleep 2
    cd "$srcdir/x264-snapshot-$_x264ver-2245" || return 1

    ./configure \
      --enable-pthread \
      --enable-visualize \
      --enable-shared \
      --enable-pic \
	  --prefix=/opt/vlc-legacy || return 1

    make  || return 1
	make DESTDIR=$pkgdir install || return 1
	
## Build internal xvidcore
    echo "Building internal xvidcore" && sleep 2
	cd $srcdir/xvidcore-${_xvidver}/build/generic
	./configure --prefix=/opt/vlc-legacy
	make  || return 1
	make DESTDIR=$pkgdir install || return 1
	#Fix dynamic libraries
	cd $pkgdir/opt/vlc-legacy/lib
	mylib=$(basename libxvidcore.so.*)
	ln -sf ${mylib} libxvidcore.so.4
	ln -sf ${mylib} libxvidcore.so

## Build internal ffmpeg
  echo "Building internal ffmpeg" && sleep 2
  cd $srcdir
  svn export svn://svn.mplayerhq.hu/ffmpeg/trunk@14236 ffmpeg
  [ -d $srcdir/ffmpeg-build ] && rm -r $srcdir/ffmpeg-build
  cp -r $srcdir/ffmpeg $srcdir/ffmpeg-build
  cd $srcdir/ffmpeg-build
  ## ffmpeg retrieves libswscale from trunk, but we need a slightly older
  ## version to make this revision of ffmpeg compile.
  rm -r libswscale
  svn export svn://svn.mplayerhq.hu/mplayer/trunk/libswscale@28314 libswscale
   # configure
    LDFLAGS="-L${pkgdir}/opt/vlc-legacy/lib ${LDFLAGS} -lbz2" \
    CFLAGS="-I${pkgdir}/opt/vlc-legacy/include ${CFLAGS}" \
    CPPFLAGS="-I${pkgdir}/opt/vlc-legacy/include ${CPPFLAGS}" \
    PKG_CONFIG_PATH="$pkgdir/opt/vlc-legacy/lib/pkgconfig:${PKG_CONFIG_PATH}" \
   ./configure \
        --enable-gpl \
        --enable-postproc \
        --enable-libmp3lame \
        --enable-libvorbis \
        --enable-libfaac \
        --enable-libfaad \
		--enable-liba52 \
        --enable-libxvid \
        --enable-libtheora \
        --disable-libamr-nb \
        --enable-shared \
        --enable-pthreads \
        --enable-x11grab \
		--enable-libx264 \
	    --prefix=/opt/vlc-legacy \
     || return 1

   # build
   make || return 1
   make DESTDIR=$pkgdir install || return 1
   
## Build vlc-legacy
  echo "Building vlc-legacy" && sleep 2
  cd ${srcdir}/${_pkgname}-${pkgver}
  sed -i -e 's:#include <vlc\/vlc.h>:#include <vlc/vlc.h> \n #include <locale.h>: ' src/misc/charset.c
  sed -i -e 's:/truetype/freefont/FreeSerifBold.ttf:/TTF/VeraBd.ttf:' modules/misc/freetype.c

  # features that won't build on x86_64 and others
  [ "${CARCH}" = "i686" ] && EXTRAFEATURES="--enable-loader"

  # export linker flags for the ffmpeg tree
  # this has to be in sync with how ffmpeg was configured above
  export LDFLAGS_ffmpeg="-lfaad -la52 -lxvidcore -lmp3lame -lx264"

  LDFLAGS="-L${pkgdir}/opt/vlc-legacy/lib ${LDFLAGS} -lbz2" \
  CFLAGS="-I${pkgdir}/opt/vlc-legacy/include ${CFLAGS}" \
  CPPFLAGS="-I${pkgdir}/opt/vlc-legacy/include ${CPPFLAGS}" \
  PKG_CONFIG_PATH=$pkgdir/opt/vlc-legacy/lib/pkgconfig:${PKG_CONFIG_PATH} \
  ./configure \
    --prefix=/opt/$pkgname --enable-dvdread --enable-dvdnav --enable-madi \
    --enable-ffmpeg --disable-rpath --enable-wxwidgets --enable-faad \
    --enable-alsa --enable-skins2 --enable-live555 --enable-dvb --enable-theora \
    --enable-dmo --with-live555-tree=/usr/lib/live-media \
    --with-ffmpeg-faac --with-ffmpeg-vorbis --with-ffmpeg-dts \
    --with-ffmpeg-ogg --with-ffmpeg-theora --with-ffmpeg-tree=$startdir/src/ffmpeg-build/ \
    --with-ffmpeg-config-path=$startdir/src/ffmpeg-build/ \
    --enable-v4l --enable-lirc --program-suffix=-legacy \
    --with-wx-config=wx-config ${EXTRAFEATURES} \
    || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  
  rm -r ${pkgdir}/opt/vlc-legacy/share/applications
  mkdir -p ${pkgdir}/usr/share/applications
  install -D -m 644 ${srcdir}/vlc-legacy.desktop ${pkgdir}/usr/share/applications/vlc-legacy.desktop
  mkdir -p ${pkgdir}/usr/bin
  install -D -m 755 ${srcdir}/vlc-legacy ${pkgdir}/usr/bin/vlc-legacy
}

md5sums=('3c90520c9f22a68d287458d5a8af989e'
         'eb2e6fa695070d53ef27b54951ccab29'
         '7b806f9081d49c6ea3489591111d3867'
         '6b4da463cf4c800b69fbf8518d6461e5'
         '29c60d4d991ac18f687a8fd13cfe64b7')
