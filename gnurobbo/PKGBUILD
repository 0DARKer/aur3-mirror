# Contributor: Christoph Zeiler <moonblade.dot.org: arch>
# Maintainer:  Jesse Jaara      <gmail.com: jesse.jaara>

pkgname=gnurobbo
pkgver=0.66
pkgrel=3
pkgdesc='A free port of QXLands logic game "Robbo" for the Atari XE/XL'
arch=('i686' 'x86_64')
url="http://$pkgname.sourceforge.net/"
license=('GPL')
depends=('sdl_ttf' 'sdl_mixer' 'sdl_image')
optdepends=('gnurobbo-data-nonfree')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-source.tar.gz"
	'dummy.dat'
	"${pkgname}.desktop")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -e 's|#MUSIC=-DHAVE_MUSIC|MUSIC=-DHAVE_MUSIC|g' \
      -e 's|=-O3 -pipe -Wall -fomit-frame-pointer|+=|g' \
      -e 's|-lSDL_image|-lm -lSDL_image|g' \
	-i Makefile

  sed '/#define DEBUG_DUMP_VM_USAGE/d' -i game.c

  # remove non-free data
  sed 's|original.dat|dummy.dat|g' -i levels.h

  make PACKAGE_DATA_DIR="/usr/share/${pkgname}"

}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PACKAGE_DATA_DIR="${pkgdir}/usr/share/${pkgname}" BINDIR="${pkgdir}/usr/bin" DOCDIR="${pkgdir}/usr/share/doc/${pkgname}" install

  mkdir "${pkgdir}/usr/share/gnurobbo/music"
  rm -r "${pkgdir}"/usr/share/gnurobbo/{levels/original.dat,skins/original}

  install -m644 ChangeLog COPYING README "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dm644 icon32.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  install -Dm644 ../dummy.dat "${pkgdir}/usr/share/${pkgname}/levels/dummy.dat"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
md5sums=('77fdf9a186a08c1f95b94bd35ebbc21c'
         '904dd16b523e97c867d9a31aeacef1b6'
         'e520a3c136854383627d1d960a41f8b2')
