# Maintainer: b00rt00s ( bomby dot zrzuc at gmail dot com )
# Contributor: Allan McRae <allan@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=tcl85
pkgver=8.5.15
pkgrel=2
pkgdesc="The Tcl scripting language, 8.5 series"
arch=('i686' 'x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
conflicts=('tcl' 'tcl84')
provides=("tcl" "tcl=${pkgver}")
source=(http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
md5sums=('f3df162f92c69b254079c4d0af7a690f')
options=('staticlibs')

build() {
	cd ${srcdir}/tcl${pkgver}/unix
	
	# we build the tcl sqlite interface in sqlite-tcl package
	rm -rf ${srcdir}/tcl${pkgver}/pkgs/sqlite3*
	
	[[ $CARCH == "x86_64" ]] && BIT="--enable-64bit"
	./configure --prefix=/usr --mandir=/usr/share/man --enable-threads $BIT
	
	make
}

check() {
	cd ${srcdir}/tcl${pkgver}/unix
	make test
}

package()
{
	cd ${srcdir}/tcl${pkgver}/unix
	
	make INSTALL_ROOT=${pkgdir} install install-private-headers
	ln -sf tclsh8.5 ${pkgdir}/usr/bin/tclsh
	ln -s libtcl8.5.so "${pkgdir}/usr/lib/libtcl8.5.so.0"
	
	# install license
	install -Dm644 ../license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	
	# remove buildroot traces
	sed -i "s#${srcdir}#/usr/src#" ${pkgdir}/usr/lib/tclConfig.sh
}
