# Maintainer: Taylor Venable <taylor@metasyntax.net>

srcname='bolt'
pkgname="ocaml-${srcname}"
pkgver=1.2
pkgrel=1
pkgdesc='A logging tool for the OCaml language that is modeled after log4j'
arch=('i686' 'x86_64')
url='http://bolt.x9c.fr/'
license=('LGPL3')
depends=('ocaml>=3.12.1')
makedepends=('ocaml-findlib')
source=('http://bolt.x9c.fr/distrib/bolt-1.2.tar.gz')
options=('!strip')
md5sums=('3cf14ece1f1b60be7e25dfe9613dc0dd')

build() {
    cd "$srcdir/${srcname}-${pkgver}"
    chmod +x configure
    ./configure

    env DESTDIR="$pkgdir" \
        OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
        make
}


package() {
    mkdir -p "$pkgdir/$(ocamlfind printconf destdir)"

    cd "$srcdir/${srcname}-${pkgver}"

    env DESTDIR="$pkgdir" \
        OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)" \
        make install
}
