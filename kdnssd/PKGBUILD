# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdnssd
pkgver=4.98.0
pkgrel=1
pkgdesc='KDNSSD Framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdnssd-framework'
license=('LGPL')
depends=('qt5-base' 'avahi')
makedepends=('extra-cmake-modules')
groups=('kf5')
replaces=('kdnssd-framework')
conflicts=('kdnssd-framework')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('dd336e053f1fb5f6f9ca5a911fd4a1b9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
