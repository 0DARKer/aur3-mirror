# $Id: PKGBUILD 167418 2012-10-02 02:04:08Z bisson $
# Contributor: tobias [tobias [at] archlinux.org]
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

_pkgname=mutt
pkgname=${_pkgname}-rfc2047
pkgver=1.5.21
pkgrel=8
pkgdesc='Small but very powerful text-based mail client, with create_rfc2047_parameters patch'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('i686' 'x86_64')
optdepends=('smtp-forwarder: to send mail')
depends=('gpgme' 'ncurses' 'openssl' 'libsasl' 'gdbm' 'libidn' 'mime-types' 'krb5')
conflicts=('mutt')
provides=('mutt')
source=("ftp://ftp.mutt.org/mutt/devel/${_pkgname}-${pkgver}.tar.gz"
        'pop-cachedir.patch'
        'crypt-gpgme.patch'
        "http://www.emaillab.org/mutt/${pkgver}/patch-${pkgver}.tt.create_rfc2047_params.1.gz")
sha1sums=('a8475f2618ce5d5d33bff85c0affdf21ab1d76b9'
          '32dbcafe05e4a919fd00c6a1064c6ecd205e6eb1'
          '8cf0d4278aab1d162ab7f847f1b917327d630d98'
          'd85a7a1e5590af9e6b54260bffd10d1f0ee82243')

install=install

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	patch -i ../pop-cachedir.patch # FS#31536
	patch -p1 -i ../crypt-gpgme.patch # FS#31735
    patch -Np1 -i ../patch-${pkgver}.tt.create_rfc2047_params.1
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-gpgme \
		--enable-pop \
		--enable-imap \
		--enable-smtp \
		--enable-hcache \
		--with-curses=/usr \
		--with-regex \
		--with-gss=/usr \
		--with-ssl=/usr \
		--with-sasl \
		--with-idn \

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/bin/{flea,muttbug}
	rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
	rm "${pkgdir}"/etc/mime.types{,.dist}
	install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
