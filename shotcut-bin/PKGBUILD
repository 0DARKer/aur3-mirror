# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=shotcut-bin
pkgver=140116
pkgrel=1
pkgdesc="A free, open source, cross-platform video editor."
arch=(i686 x86_64)
url="http://www.mltframework.org/bin/view/Shotcut/WebHome"
license=('GPL3')
depends=('jack' 'libexif' 'desktop-file-utils')
provides=('shotcut')
install="$pkgname.install"
source=("http://d1av856udzjaks.cloudfront.net/shotcut/shotcut-debian7-x86${CARCH/*86}-$pkgver.tar.bz2")
[[ $CARCH = 'x86_64' ]] && \
md5sums=('74c2a04e16b6e019a805ae5e9df188ad') || \
md5sums=('2228955305a499b5bc1853fb31ca638e')

prepare() {
  cd Shotcut
  # fix desktop file
  sed -i '1d;/Exec/s/sh.*/shotcut-bin/' Shotcut.desktop
}

package() {
  cd Shotcut

  # bundle
  install -d "$pkgdir/opt/shotcut"
  cp -a Shotcut.app/* "$pkgdir/opt/shotcut"

  # desktop file
  install -Dm644 Shotcut.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # launcher
  install -d "$pkgdir/usr/bin"
  ln -s /opt/shotcut/shotcut "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
