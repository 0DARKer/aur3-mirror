# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=shotcut-bin
pkgver=140801
pkgrel=1
pkgdesc="A free, open source, cross-platform video editor."
arch=(i686 x86_64)
url="http://www.mltframework.org/bin/view/Shotcut/WebHome"
license=('GPL3')
depends=('jack' 'libexif' 'desktop-file-utils')
provides=('shotcut')
install="$pkgname.install"
#source=("http://d1av856udzjaks.cloudfront.net/shotcut/shotcut-debian7-x86${CARCH/*86}-$pkgver.tar.bz2")
source=("https://github.com/mltframework/shotcut/releases/download/v${pkgver::2}.${pkgver:2:2}/shotcut-debian7-x86${CARCH/*86}-$pkgver.tar.bz2")
[[ $CARCH = 'x86_64' ]] && \
md5sums=('b9c7c3fda748d764638b4c68aab8050e') || \
md5sums=('107119ed5e4e4adac81681faffd267f1')

prepare() {
  cd Shotcut
  # fix desktop file
  sed -i '1d;/Exec/s/sh.*/shotcut-bin/' Shotcut.desktop
}

package() {
  cd Shotcut

  # bundle
  install -d "$pkgdir/opt/shotcut"
  cp -a Shotcut.app/* "$pkgdir/opt/shotcut"

  # desktop file
  install -Dm644 Shotcut.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # launcher
  install -d "$pkgdir/usr/bin"
  ln -s /opt/shotcut/shotcut "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
