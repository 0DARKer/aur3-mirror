# Maintainer: jernick <jeromyreimer at gmail dot com>

PKGEXT=.pkg.tar
pkgname=shotcut-bin
pkgver=141203
pkgrel=1
pkgdesc="A free, open source, cross-platform video editor."
arch=(i686 x86_64)
url="http://www.mltframework.org/bin/view/Shotcut/WebHome"
license=('GPL3')
depends=('jack' 'libexif' 'desktop-file-utils')
provides=('shotcut')
install="$pkgname.install"
#source=("http://d1av856udzjaks.cloudfront.net/shotcut/shotcut-debian7-x86${CARCH/*86}-$pkgver.tar.bz2")
source=("https://github.com/mltframework/shotcut/releases/download/v${pkgver::2}.${pkgver:2:2}/shotcut-debian7-x86${CARCH/*86}-$pkgver.tar.bz2")
[[ $CARCH = 'x86_64' ]] && \
md5sums=('b25436753944827ac38e96b922465e05') || \
md5sums=('f26680cbc6619a9bdb9c85d9a8ce4d05')

prepare() {
  cd Shotcut
  # fix desktop file
  sed -i '1d;/Exec/s/sh.*/shotcut-bin/' Shotcut.desktop
}

package() {
  cd Shotcut

  # bundle
  install -d "$pkgdir/opt/shotcut"
  cp -a Shotcut.app/* "$pkgdir/opt/shotcut"

  # desktop file
  install -Dm644 Shotcut.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # launcher
  install -d "$pkgdir/usr/bin"
  ln -s /opt/shotcut/shotcut "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
