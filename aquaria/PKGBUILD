# Maintainer: AlexanderR <alexander r at gmx com>
pkgname=aquaria
# format: official.version_changeset
pkgver=1.1.3_245
pkgrel=1
pkgdesc="2D Fantasy Action-Adventure by Bit Blot. Open Source version. Package uses data files from Humble Indie Bundle if present, otherwise falls back to downloading beta."
arch=('i686' 'x86_64')
url='http://www.bit-blot.com/'
license=('custom: commercial' 'GPL')
install=aquaria.install
depends=('openal' 'sdl' 'lua' 'freetype2' 'libvorbis' 'gcc-libs' 'libpng')
makedepends=('unzip' 'cmake' 'mercurial')
conflicts=('aquaria-hib-hg' 'aquaria-hb_version')
changelog=ChangeLog
# TODO: uncomment after removal of other packages
#replaces=('aquaria-hib-hg' '	aquaria-hb_version')

_pkg_hib='aquaria-lnx-humble-bundle.mojo.run'
_pkg_beta='aquaria-beta3-installer.run'

source=('https://bitbucket.org/AlexanderR/aquaria/get/37d19fdd3fc4.tar.bz2'
        'aquaria.desktop'
        'aquaria.sh')
md5sums=('a90fbbd45b995bc5faa23a5da5ed62a8'
         '71c79de74f4971cce34a172f44c9fc1d'
         '92768cd8560dc04ec0ad95fc06074361')

if [ -f $_pkg_hib ]; then
  source+=('aquaria-lnx-humble-bundle.mojo.run::file://aquaria-lnx-humble-bundle.mojo.run')
  md5sums+=('8b24ddeeb553e028bbd501102f891cc2')
else
  # SSL on icculus.org does not work for some reason
  source+=('http://icculus.org/aquaria/aquaria-beta3-installer.run')
  md5sums+=('4771128b2d05d33b24304fe369e488c3')
fi

build() {
  cd "$srcdir"

  if [ -f $_pkg_hib ]; then
    unzip -uo $_pkg_hib || true
  else
    unzip -uo $_pkg_beta || true
  fi

  cd data

  mv README-linux.txt aquaria.png docs/ ..

  # libs are to be replaced by system ones, new executable should be created during compilation,
  # xdg-open is currently used only in obsolete "onBuy()" function
  rm -rf libSDL-1.2.so.0 libgcc_s.so.1 libopenal.so.1 libstdc++.so.6 \
    aquaria xdg-open aquaria.png

  cd "$srcdir/AlexanderR-aquaria-37d19fdd3fc4"

  cmake -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd "$srcdir"

  mkdir -p "$pkgdir/usr/share/$pkgname"
  install -D -m 755 ${pkgname}.sh "$pkgdir/usr/bin/$pkgname"
  install -D -m 644 ${pkgname}.desktop "$pkgdir/usr/share/applications/${pkgname}.desktop"
  install -D -m 644 ${pkgname}.png "$pkgdir/usr/share/pixmaps/${pkgname}.png"
  install -D -m 644 README-linux.txt "$pkgdir/usr/share/doc/$pkgname/README"
  cp -r data/* "$pkgdir/usr/share/$pkgname"
  cp -r docs/* "$pkgdir/usr/share/doc/$pkgname"

  cd AlexanderR-$pkgname-37d19fdd3fc4

  cp -r game_scripts/* "$pkgdir/usr/share/$pkgname"
  install -D -m 644 CONTENT-LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m 755 $pkgname "$pkgdir/usr/share/$pkgname"
}
