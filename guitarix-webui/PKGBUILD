# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=guitarix-webui
pkgver=0.28.0_0.6
pkgrel=1
pkgdesc="A web frontend for Guitarix."
arch=('any')
url="http://guitarix.sourceforge.net/"
license=('GPL')
depends=('guitarix' 'python2-numpy')
install="$pkgname.install"
source=("http://download.sourceforge.net/project/guitarix/guitarix/$pkgname-${pkgver/_/-}.tar.bz2"
        start_guitarix_{engine,webui})
md5sums=('4b2ed5b50687f7018bfca9266fb69d76'
         '305fa4029ad7d263705ad377ce09cb60'
         'debeb08a272a6953f3e644acdd1aa059')

prepare() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  # python2 shebang
  sed -i '1s/env python/&2/' `find . -name "*.py"`
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  # web server
  install -d "$pkgdir/usr/share/$pkgname/websockify"
  install -Dm644 websockify/*.py "$pkgdir/usr/share/$pkgname/websockify"

  # web ui
  cp -a webui "$pkgdir/usr/share/$pkgname"

  # doc
  install -Dm644 README-dist.txt "$pkgdir/usr/share/doc/$pkgname/README"

  # start scripts
  install -d "$pkgdir/usr/bin"
  install -Dm755 ../${source[1]} "$pkgdir/usr/bin"
  install -Dm755 ../${source[2]} "$pkgdir/usr/bin"
}
