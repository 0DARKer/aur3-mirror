# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

pkgname=i3bar
pkgver=0.6
pkgrel=2
pkgdesc="An xcb-based status- and ws-bar for i3"
arch=('i686' 'x86_64')
url="http://git.merovius.de/i3bar/"
license=('BSD')
groups=('i3')
depends=('libx11' 'yajl' 'libev')
# If you don't use a chroot and use i3-git
# replace the i3-wm makedependency with that.
makedepends=('pkgconfig' 'asciidoc' 'i3-wm')
conflicts=('i3bar-git')
source=("http://git.merovius.de/i3bar/snapshot/$pkgname-$pkgver.tar.bz2"
        "i3bar-yajl2-pnutzh4x0r.patch")
md5sums=('3d4c462d3367183a7f5240edb6569640'
         '7e3d58a76a659c89fbf08a4060f0ace3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # yajl2 brought some API changes
  patch -p0 < "$srcdir/i3bar-yajl2-pnutzh4x0r.patch"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/i3bar/LICENSE"
}

# vim:set ts=2 sw=2 et:
