# Original maintainer: David Campbell <davekong@.archlinux.us>
# Current maintainer: Matti Eiden <matti.eiden@env.tamk.fi>
pkgname=sup-git
pkgver=20110321
pkgrel=1
pkgdesc="CLI mail client in the style of Gmail (tagging, threading, search)."
arch=('i686' 'x86_64')
url="http://sup.rubyforge.org/"
license=('GPL')
depends=('ruby-xapian-full' 'gdbm' 'ruby-ncursesw' 'ruby-rmail>=0.17' \
	 'ruby-highline' 'ruby-net-ssh' 'ruby-trollop>=0.12' 'ruby-lockfile' \
	 'ruby-mime-types>1' 'ruby-locale' 'ruby-gettext' 'ruby-fastthread' )
makedepends=('ruby' 'git')
source=(rakefile.patch)
md5sums=('df66367edde5ced15450efecfc7f8bf9')

_gitroot="git://gitorious.org/sup/mainline.git"
_gitname="sup"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build"
  msg  "Patching the rakefile"
  patch -p1 -i ../rakefile.patch

  msg "Building the gem"
  rake gem

  cd pkg
  local _gemdir="$(ruby --enable-gems -e'puts Gem.default_dir')"

  local target="${pkgdir}${_gemdir}"
  local bintarget="$pkgdir/usr/bin"
  install -d "${bintarget}"

  gem install -li "${target}" sup -n "${bintarget}" --ignore-dependencies
}
