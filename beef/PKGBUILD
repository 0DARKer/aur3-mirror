# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=beef
pkgver=1.0.0
pkgrel=1
pkgdesc='Flexible interpreter for the Brainfuck programming language'
arch=('x86_64' 'i686')
url='http://kiyuko.org/software/beef'
license=('GPL')
depends=('cattle')
makedepends=('addinclude')
source=("$pkgname-$pkgver.tgz::http://kiyuko.org/software/$pkgname/releases/1.0.0/source")
sha256sums=('cd730b01173e615bfe04569df9aef4a39cf2d1295df0417c1d787b86e6fb4004')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  addinclude -t src/main.c stdio
  addinclude -t src/io.c stdio
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
