# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-daemon-generic
pkgver=0.83
pkgrel=1
pkgdesc="framework to provide start/stop/reload for a daemon"
arch=('any')
url="http://search.cpan.org/dist/Daemon-Generic"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-file-slurp' 'perl-file-flock')
makedepends=('perl' 'perl-anyevent' 'perl-eval-linenumbers' 'perl-event' 'perl-file-flock')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MU/MUIR/modules/Daemon-Generic-$pkgver.tar.gz")
md5sums=('3ee5da95ffc7e14cdefa1c443a6160af')
sha512sums=('94dccbd8552992fb85a54b5d6c087df6f2c98219bf48e881b40646c078628a92dbae3d883c56a2adaf252b1cea74e177c80753c04de2c6e98bb064a5469c657a')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Daemon-Generic-${pkgver}"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/Daemon-Generic-${pkgver}"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "${srcdir}/Daemon-Generic-${pkgver}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
