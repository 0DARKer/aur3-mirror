# Maintainer: Andras Biro <bbandi86@gmail.com>
# Contributor: Andrea Fagiani <andfagiani_at_gmail_dot_com>
# PKGBUILD based on the avr-libc PKGBUILD by 
#	Corrado Primier <bardo@aur.archlinux.org> and danst0 <danst0@west.de>

pkgname=avr-libc-tinyos
pkgver=1.6.7
pkgrel=2
pkgdesc="The C runtime library for the AVR family of microcontrollers, with TinyOS patches"
arch=('any')
url="http://savannah.nongnu.org/projects/avr-libc/"
license=('custom')
depends=('gcc-avr-tinyos')
provides=('avr-libc')
options=('staticlibs' '!strip') 
source=(http://download.savannah.gnu.org/releases/avr-libc/avr-libc-${pkgver}.tar.bz2)
md5sums=('1ccf8078e583d527392f079082f816b0')

build() {
  cd ${srcdir}/avr-libc-${pkgver}
  ./configure --build=$(./config.guess) --host=avr --prefix=/usr
  make
}

package() {
  cd ${srcdir}/avr-libc-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
