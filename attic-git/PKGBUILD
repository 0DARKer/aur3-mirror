# Maintainer: Beneldr <beneldr (dot) aur (at) gmail (dot) com>
# Contributor: willemw <willemw12 (at) gmail (dot) com>

pkgname=attic-git
pkgver=0.13.r3.g1b0e21b
pkgrel=4
pkgdesc="A deduplicating backup program for efficient and secure backups."
arch=('i686' 'x86_64')
url="https://attic-backup.org/"
license=('BSD')
depends=('openssl' 'python-msgpack')
optdepends=('python-llfuse: Mounting backups as a FUSE filesystem')
makedepends=('git' 'cython' 'python-sphinx')
provides=('attic')
conflicts=('attic')
source=("$pkgname"::'git://github.com/jborg/attic.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname/docs"
  PYTHONPATH=.. make man
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m644 "docs/_build/man/attic-deduplicatingarchiver.1" "$pkgdir/usr/share/man/man1/attic.1"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  python setup.py -q install --root="$pkgdir" --optimize=1
}
