# Maintainer: Beneldr <beneldr (dot) aur (at) gmail (dot) com>

pkgname=attic-git
pkgver=0.9.r1.g9c80089
pkgrel=1
pkgdesc="A deduplicating backup program for efficient and secure backups."
arch=('i686' 'x86_64')
url="https://github.com/jborg/attic"
license=('BSD')
depends=('python' 'openssl' 'python-msgpack')
optdepends=('python-llfuse: Mounting backups as a FUSE filesystem')
makedepends=('git' 'cython')
conflicts=('attic')
source=("$pkgname"::'git://github.com/jborg/attic.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  python setup.py -q install --root="$pkgdir" --optimize=1
}
