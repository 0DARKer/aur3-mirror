# Maintainer: Andrea Fagiani <andfagiani {at} gmail {dot} com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>

# Installation order:  freetype2-ubuntu fontconfig-ubuntu libxft-ubuntu cairo-ubuntu
_pkgbasename=freetype2-ubuntu
pkgname=lib32-$_pkgbasename
pkgver=2.4.4
_ubver=2.4.2-2.1
pkgrel=1
pkgdesc="TrueType font rendering library, with Ubuntu's LCD rendering patches (32-bit)"
arch=(x86_64)
url="https://launchpad.net/ubuntu/natty/+source/freetype"
license=('GPL')
depends=('lib32-zlib' 'freetype2-ubuntu')
makedepends=(gcc-multilib)
provides=("lib32-freetype2=$pkgver")
conflicts=('lib32-freetype2')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        https://launchpad.net/ubuntu/natty/+source/freetype/$_ubver/+files/freetype_$_ubver.diff.gz
        apple-offset.patch enable-valid.patch)

md5sums=('b3e2b6e2f1c3e0dffa1fd2a0f848b671'
         '68a5fe548b573eb994d212b0928f249b'
         '1c964f90ae90aa391af574ceb0814cda'
         '214119610444c9b02766ccee5e220680')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/freetype-$pkgver

  # Patch from ubuntu
  patch -Np1 -i $srcdir/freetype_$_ubver.diff

  sed -i -e "s#\ssrc# freetype-$pkgver/src#g" debian/patches-freetype/freetype-2.1.7-backwards.compat.patch
  sed -i -e "s/-p[0-9]\|CVE-.*\.patch//g" debian/patches-freetype/series

  for _f in $(cat debian/patches-freetype/series) ; do    
    patch -Np1 -i debian/patches-freetype/$_f    
  done

  # Patches from arch trunk
  patch -Np1 -i $srcdir/enable-valid.patch

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd $srcdir/freetype-$pkgver

  make DESTDIR=$pkgdir install

  rm -rf $pkgdir/usr/{include,share,bin}
}
