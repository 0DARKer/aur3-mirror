# Maintainer: Andrea Fagiani <andfagiani {at} gmail {dot} com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Contributor: Biru Ionut <biru.ionut at gmail.com>

# Installation order:  freetype2-ubuntu fontconfig-ubuntu libxft-ubuntu cairo-ubuntu
_pkgbasename=freetype2-ubuntu
pkgname=lib32-$_pkgbasename
pkgver=2.4.6
_ubver=2.4.6-2
pkgrel=2
pkgdesc="TrueType font rendering library, with Ubuntu's LCD rendering patches (32-bit)"
arch=(x86_64)
url="https://launchpad.net/ubuntu/precise/+source/freetype"
license=('GPL')
depends=('lib32-zlib' 'freetype2-ubuntu')
makedepends=(gcc-multilib)
provides=("lib32-freetype2=$pkgver")
conflicts=('lib32-freetype2')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        https://launchpad.net/ubuntu/precise/+source/freetype/$_ubver/+files/freetype_$_ubver.diff.gz
        apple-offset.patch enable-valid.patch)

md5sums=('5e6510613f612809d2d7862592b92ab7'
         '3c738acba25b3de4635b8da8d39a71ad'
         '1c964f90ae90aa391af574ceb0814cda'
         '214119610444c9b02766ccee5e220680')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/freetype-$pkgver

  # Patch from ubuntu
  patch -Np1 -i $srcdir/freetype_$_ubver.diff

  sed -e "s/-p[0-9]\|.*otvalid\.patch//g" \
      -i debian/patches-freetype/series

  sed -e 's/ src/ a\/src/g' \
      -e '/^Index.*fbase.c/,/EOF/d' \
      -i debian/patches-freetype/freetype-2.1.7-backwards.compat.patch

  for _f in $(cat debian/patches-freetype/series) ; do    
    patch -Np1 -i debian/patches-freetype/$_f    
  done

  # Patches from arch trunk
  patch -Np1 -i $srcdir/enable-valid.patch

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd $srcdir/freetype-$pkgver

  make DESTDIR=$pkgdir install

  rm -rf $pkgdir/usr/{include,share,bin}
}
