# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

_p=audacious
pkgname=${_p}2
pkgver=3.0.2
pkgrel=1
pkgdesc='Lightweight, GTK-based advanced audio player focused on audio quality. Gtk2 version.'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL3')
depends=('gtk2' 'libmcs' 'dbus-glib' 'libguess' 'libsm'
         'desktop-file-utils' 'hicolor-icon-theme')
optdepends=("$pkgname-plugins: required for all"
            'unzip: zipped skins support')
install="$pkgname.install"
source=("http://distfiles.atheme.org/$_p-$pkgver.tar.gz")
sha1sums=('68799076acafb151668285e8d9498048633d7b60')

build() {
  cd "$srcdir/$_p-$pkgver"

  [[ "$CARCH" == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'

  # Patch to let this coexist with audacious
  for _i in acious client core gui tag tool; do
    msg "Processing aud$_i .."
    sed -i "s|\([aA]\)\(ud${_i}2\{0,1\}\)|\1ud${_i}2|g" `grep -rl "[aA]ud$_i" .`
    for _d in `find . -depth -type d -name "*[aA]ud$_i*"`; do
      _n="$(echo $_d | sed "s|\([aA]\)\(ud${_i}2\{0,1\}\)|\1ud${_i}2|g")"
      msg "Moving $_d -> $_n" && mv "$_d" "$_n"
    done
    for _f in `find . -depth -type f -name "*[aA]ud$_i*"`; do
      _n="$(echo $_f | sed "s|\([aA]\)\(ud${_i}2\{0,1\}\)|\1ud${_i}2|g")"
      msg "Renaming $_f -> $_n" && mv "$_f" "$_n"
    done
  done

  ./configure --prefix=/usr \
              --disable-gtk3 \
              "$extraopts"
  make
}

package() {
  cd "$srcdir/$_p-$pkgver"

  make DESTDIR="$pkgdir/" install
}
