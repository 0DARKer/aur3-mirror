# Maintainer: xhochy <uwelk@xhochy.org>

# need to choose anonther name since heimdal is still blacklisted
pkgname=heimdal-aur
# Do not install the config as this will conflict with krb5
pkgver=1.5pre2 
pkgrel=1
pkgdesc="Implementation of Kerberos V5 libraries"
arch=('i686' 'x86_64')
url="http://www.h5l.org/"
license=('custom')
depends=('db' 'openssl' 'sqlite3' 'libldap')
# Do not install the config as this will conflict with krb5
# backup=(etc/krb5/krb5.conf)
options=('!libtool' '!emptydirs')
source=(http://www.h5l.org/dist/src/heimdal-1.5pre2.tar.gz
	heimdal
	kadmind
	kpasswd
	base-version-script.map
)
md5sums=('2b3e7a5d3e8262e7bea87c98790c97ae'  # heimdal-1.5pre2.tar.gz
         '9caf92f1ac2ab3c847cb0f7013973c0f'  # heimdal
         '7d4307ef3a31bc44e1a932f6921bcb12'  # kadmind
         '3a3ae1efedf23cfa7877cc3a8def6f85'  # kpasswd
         '8dfdaa919df2be48fa181db646cd8a9b'  # base-version-script.map
)

build() {
  cp base-version-script.map ${srcdir}/heimdal-${pkgver}/base/version-script.map
  cd ${srcdir}/heimdal-${pkgver}
	
  sed -i 's|$(LIB_NDBM)|$(LIB_NDBM) $(LIB_db_create)|' lib/otp/Makefile.am

  # TODO Include man pages in /usr/share/man but add a heimdal suffix
  ./configure --prefix=/opt/heimdal --enable-shared=yes --without-x \
	--sysconfdir=/etc/krb5 \
	--includedir=/usr/heimdal/include \
	--mandir=/opt/heimdal/share/man \
	--datadir=/var/lib/heimdal \
	--localstatedir=/var/lib/heimdal \
	--with-openssl=/usr \
	--with-readline-lib=/usr/lib \
	--with-readline-include=/usr/include/readline \
	--with-sqlite3-lib=/usr/lib \
	--with-sqlite3-include=/usr/include \
	--with-openldap=/usr \
	--libexecdir=/opt/heimdal/sbin
  make
}

package() {
  cd ${srcdir}/heimdal-${pkgver}
  make DESTDIR=${pkgdir} install

  # Rename daemons and their manpages
  for i in telnetd ftpd rshd; do
    mv ${pkgdir}/opt/heimdal/share/man/man8/{,k}${i}.8
    mv ${pkgdir}/opt/heimdal/sbin/{,k}${i}
  done
  
  # Rename clients and their manpages
  for i in rcp rsh telnet ftp su login; do
    if [ -f ${pkgdir}/opt/heimdal/share/man/man1/${i}.1 ]; then
      mv ${pkgdir}/opt/heimdal/share/man/man1/{,k}${i}.1
    fi
    mv ${pkgdir}/opt/heimdal/bin/{,k}${i}
  done
  rm -rf ${pkgdir}/opt/heimdal/share/man/cat{1,3,5,8}
  
  # install config
  # Do not install the config as this will conflict with krb5
  # install -D -m644 ${srcdir}/heimdal-${pkgver}/krb5.conf ${pkgdir}/etc/krb5/krb5.conf

  # install init scripts
  mkdir -p $pkgdir/etc/rc.d/init.d
  for i in heimdal kadmind kpasswd; do
    install -m755 ${srcdir}/${i} ${pkgdir}/etc/rc.d/init.d/${i}
  done

  # Remove conflicts
  rm ${pkgdir}/opt/heimdal/share/man/man5/ftpusers.5*		          # man-pages
  rm ${pkgdir}/opt/heimdal/share/man/man3/{DES,DH,EVP,OpenSSL,RAND,RSA}*  # openssl
  rm ${pkgdir}/opt/heimdal/share/man/man3/os.3*                           # erlang

  # Install the license
  install -Dm644 ${srcdir}/heimdal-${pkgver}/LICENSE \
  	${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
