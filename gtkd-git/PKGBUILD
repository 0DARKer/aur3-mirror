#Maintainer: TingPing <tingping@tingping.se>
pkgname=gtkd-git
pkgver=2.1.0.41.g7ea7b80
pkgrel=1
pkgdesc='D binding and OO wrapper of Gtk3 and related libs'
url='http://dsource.org/projects/gtkd'
license=('LGPL')
arch=('i686' 'x86_64')
provides=('gtkd')
conflicts=('gtkd' 'gtkd-svn')
depends=('gtk3' 'dmd')
makedepends=('gtksourceview3' 'gstreamer'  'libgda')
optdepends=('gtksourceview3' 'gstreamer' 'libgda')
source=('git://github.com/gtkd-developers/GtkD.git')
md5sums=('SKIP')
_gitname='GtkD'

pkgver() {
	cd "$_gitname"
	_ver=`git describe --always | sed 's/-/./g'`
	echo ${_ver:5}
}

build() {
	cd "$_gitname"
	make prefix='/usr' gtkd gtkdgl sv gstreamer gda
}

package() {
	cd "$_gitname"
	make DESTDIR="$pkgdir" prefix='/usr' install-{gtkd,gtkdgl,gtkdsv,gstreamer,gda}
}

