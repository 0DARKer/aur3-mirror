# Maintainer: Marcelo Garlet Millani <marcelogmillani at gmail dot com>
_hkgname=simtreelo
pkgname=haskell-simtreelo
pkgver=0.1.0.2
pkgrel=1
pkgdesc='Loads data organized in a tree'
url="http://hackage.haskell.org/package/${_hkgname}"
license=('GPL3')
arch=('i686' 'x86_64') # should work for any arch which haskell may be compiled to
# static linkage
#makedepends=('ghc')
# dynamic linkage
depends=('ghc')
source=( "http://hackage.haskell.org/package/${_hkgname}-${pkgver}/${_hkgname}-${pkgver}.tar.gz" )
sha256sums=('171bddaa2dcaab28a4755656ef3b6021f5d18ddddd8517cef816a612c82e2865')
install=${pkgname}.install

build() {
	cd ${srcdir}/${_hkgname}-${pkgver}

	# static linkage
	# runhaskell Setup configure -O --enable-split-objs \
  # 	--prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
	# dynamic linkage
	runhaskell Setup configure -O --enable-split-objs --enable-shared \
		--prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
	runhaskell Setup build
	runhaskell Setup haddock
	runhaskell Setup register   --gen-script
	runhaskell Setup unregister --gen-script
	sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
	cd ${srcdir}/${_hkgname}-${pkgver}
	runhaskell Setup copy --destdir=${pkgdir}
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
	install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
	rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}

