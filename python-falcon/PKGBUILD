# Maintainer: Carl George < arch at cgtx.us >

_module="falcon"
pkgname=("python-${_module}" "python2-${_module}")
pkgver="0.2"
pkgrel="2"
pkgdesc="An unladen web framework for building APIs and app backends."
arch=("any")
url="https://github.com/falconry/falcon"
license=("Apache")
makedepends=(
	"cython"
	"python-setuptools"
	"cython2"
	"python2-setuptools"
)
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('92bb899bf6e58e2299e3b1de1e628b90c38544ba3354a0141d108318b50c3402')

prepare() {
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

package_python-falcon() {
	depends=(
		"python-six"
		"python-mimeparse"
	)
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-falcon() {
	depends=(
		"python2-six"
		"python2-mimeparse"
	)
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
