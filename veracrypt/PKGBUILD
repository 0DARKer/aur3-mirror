# Maintainer: Eric Ozwigh <ozwigh at gmail dot com>

pkgname=veracrypt
pkgver=1.0e
pkgrel=1
pkgdesc="Free disk encryption software, TrueCrypt fork."
arch=('i686' 'x86_64')
url="http://veracrypt.codeplex.com/"
license=('custom')
depends=('fuse>=2.8.0' 'wxgtk2.8>=2.8.9' 'libsm' 'device-mapper')
makedepends=('git' 'nasm')
install='veracrypt.install'
source=('old_wx.patch'
        'no_makeself.patch'
        'veracrypt.desktop'
        'veracrypt.install')
md5sums=('cb1a53e6e4765211c55244b58a934040'
         'bb879def187ae29f8081167916374a32'
         'd39f184e2195c7e2865b971633fec2a1'
         '7bfbb1a2acbb2b890925e3f6aa244467')

build() {
  _repo='https://git01.codeplex.com/veracrypt'
  _tag="VeraCrypt_Linux_$pkgver"
  # clone & reset to tagged ver
  cd "$srcdir"
  git clone "$_repo"
  cd "veracrypt/src"
  git reset --hard "$_tag"
  # apply patches
  patch -Np1 -i ../../old_wx.patch  # wxStandardPaths workaround
  patch -Np1 -i ../../no_makeself.patch  # disable sfx archive
  # build
  export WX_CONFIG=/usr/bin/wx-config-2.8
  make LFLAGS+="-ldl"
}

package() {
  cd "$srcdir/veracrypt/src"
  install -D -m755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 Resources/Icons/VeraCrypt-48x48.xpm "${pkgdir}/usr/share/pixmaps/veracrypt.xpm"
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/License.txt"
}
