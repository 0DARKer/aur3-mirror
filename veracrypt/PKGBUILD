# Maintainer: Eric Ozwigh <ozwigh at gmail dot com>

pkgname=veracrypt
pkgver=1.0f_1
pkgrel=1
pkgdesc="Free disk encryption software, TrueCrypt fork."
arch=('i686' 'x86_64')
url="http://veracrypt.codeplex.com/"
license=('custom')
depends=('fuse>=2.8.0' 'wxgtk>=3.0.0' 'libsm' 'device-mapper')
makedepends=('git' 'nasm')
install='veracrypt.install'
source=('no_makeself.patch'
        'veracrypt.desktop'
        'veracrypt.install')
sha1sums=('0cbe9f7875ab52be125cc575533f76fab60314a5'
          '87c8dbecbc6a741d61cb56f36c2f8596e6cef509'
          '49dd09b177d4b9ee1517e5bc898b2581a86e419e')

build() {
  _repo='https://git01.codeplex.com/veracrypt'
  _sha1='3a0fb8273014efbdcb17ef3be2e98f136ad4e19b'
  # clone & reset to tagged ver
  cd "$srcdir"
  git clone "$_repo"
  cd "veracrypt/src"
  git reset --hard "$_sha1"
  # apply patch(es)
  patch -Np1 -i ../../no_makeself.patch  # disable sfx archive
  # build
  export WX_CONFIG=/usr/bin/wx-config
  make LFLAGS+="-ldl"
}

package() {
  cd "$srcdir/veracrypt/src"
  install -D -m755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 Resources/Icons/VeraCrypt-48x48.xpm "${pkgdir}/usr/share/pixmaps/veracrypt.xpm"
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/License.txt"
}
