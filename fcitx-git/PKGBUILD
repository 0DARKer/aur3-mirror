# Maintainer: Yichao Yu <yyc1992 AT gmail.com>
# Contributor: Weng XueTian <wengxt AT gmail com>
# Contributor: lh <jarryson AT gmail com>

pkgname=fcitx-git
pkgver=20120606
pkgrel=1
pkgdesc="A Flexible Input Method Framework"
arch=("i686" "x86_64")
license=('GPL')
url="http://fcitx-im.org/wiki/Fcitx"
depends=('pango' 'gtk2' 'qt' 'gtk3' 'dbus-core' 'libxinerama'
  'gtk-update-icon-cache' 'shared-mime-info' 'hicolor-icon-theme'
  'desktop-file-utils' 'icu' 'enchant' 'libxml2' 'libxkbfile' 'iso-codes'
  'gobject-introspection')
makedepends=('git' 'intltool' 'cmake')
conflicts=('fcitx' 'fcitx-qt' 'fcitx-gtk2' 'fcitx-gtk3' 'fcitx-keyboard')
provides=("fcitx=4.2.5" 'fcitx-qt' 'fcitx-gtk2' 'fcitx-gtk3' 'fcitx-keyboard')
install="$pkgname.install"

_gitname="fcitx"
_gitroot="https://github.com/fcitx/fcitx.git"

build() {
  cd "$srcdir"
  msg "Connecting to the GIT server...."

  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  cd "$srcdir/"

  msg "Creating make environment..."

  rm -rf "$srcdir/$_gitname-build"
  mkdir -p "$srcdir/$_gitname-build"

  msg "Starting make..."
  cd "$srcdir/$_gitname-build"
  cmake ../$_gitname -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Debug \
    -DENABLE_QT_IM_MODULE=On \
    -DENABLE_GTK2_IM_MODULE=On \
    -DENABLE_GTK3_IM_MODULE=On
  make
}

package(){
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
