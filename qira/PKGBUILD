# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=qira
pkgver=1.0
pkgrel=1
pkgdesc='QEMU Interactive Runtime Analyser'
url='https://code.google.com/p/qira/'
arch=('x86_64' 'i686')
license=('GPL2')
depends=('qemu' 'python2-ipaddr' 'python2-flask-socketio' 'python2-pillow' 'python2-capstone' 'python2-pydot' 'python2-pyelftools')
makedepends=('python2-setuptools')
source=(https://qira.googlecode.com/git/releases/${pkgname}-${pkgver}.tar.xz)
sha512sums=('9bde101e8c4aadd61e288978dde3074efb6845f1699d60fb9376c58f1ae2358c509cba657b70b79191ec00dcd71cfd6ac030d72d6f6ec91ea0c646543661158e')

prepare() {
  cd ${pkgname}
  find . -type d -print0 | xargs -0 chmod +rx
  find . -type f -print0 | xargs -0 chmod +r
  find . -type f -executable -print0 | xargs -0 chmod +x
}

build() {
  cd ${pkgname}/qiradb
  python2 setup.py build
}

package() {
  cd ${pkgname}

  pushd qiradb
  python2 setup.py install -O1 --single-version-externally-managed --root="${pkgdir}"
  popd

  mv qiradb/VERSION VERSION
  rm -r qiradb

  install -d "${pkgdir}/opt/${pkgname}"
  cp -r . "${pkgdir}/opt/${pkgname}"

  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
