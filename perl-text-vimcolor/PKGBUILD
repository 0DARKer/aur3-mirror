# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Pete Morris <pete dot morris at gmail>

_perlmod=Text-VimColor
_modnamespace=Text
pkgname=perl-text-vimcolor
pkgver=0.24
pkgrel=2
pkgdesc="Text::VimColor - Syntax highlight text using Vim"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('GPL' 'PerlArtistic')
depends=('perl-path-class>=0.04' 'perl-file-sharedir')
makedepends=('perl-file-sharedir-install>=0.03')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('34923c3124a8dca1599929926f1047ad3340babef83d8fcf2a8199ee74ab9e9cd9edaecab1b05a411f6ebb199ce5cf37945f3d746f9ed7861578cce8fd9ee15b')

build() {
	cd "${_perlmod}-${pkgver}"

	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}"
}
