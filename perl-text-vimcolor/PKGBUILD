# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Pete Morris <pete dot morris at gmail>

_perlmod=Text-VimColor
_modnamespace=Text
pkgname=perl-text-vimcolor
pkgver=0.22
pkgrel=1
pkgdesc="Text::VimColor - Syntax highlight text using Vim"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-path-class>=0.04' 'perl-file-sharedir')
makedepends=('perl-file-sharedir-install>=0.03')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('5f68cdab2f6dea443be7e1fb7ea1d2c77ee6351b6ba7797b170c8f6b3a8cfd9931c19f8f099bcc6fc09d514aebbc647a7f9f0ee6e1ac08239ed5a6c1eb60f151')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
