# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Pete Morris <pete dot morris at gmail>

_perlmod=Text-VimColor
_modnamespace=Text
pkgname=perl-text-vimcolor
pkgver=0.21
pkgrel=1
pkgdesc="Text::VimColor - Syntax highlight text using Vim"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-path-class>=0.04' 'perl-file-sharedir')
makedepends=('perl-file-sharedir-install>=0.03')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('c043257b80ebac8a7f1e21852e9eb6f778041f1741d9f34d28d0f98fff66774a5600657c7f921781ef10552c496db9c5cf9eb6086609301fca5f614f6b295094')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
