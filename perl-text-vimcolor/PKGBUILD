# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Pete Morris <pete dot morris at gmail>

pkgname=perl-text-vimcolor
_pkgname=Text-VimColor
pkgver=0.14
pkgrel=2
pkgdesc="Text::VimColor - Syntax highlight text using Vim"
arch=('any')
url="http://search.cpan.org/dist/$_pkgname"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-path-class')
makedepends=('perl-file-sharedir-install')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/R/RW/RWSTAUNER/$_pkgname-$pkgver.tar.gz)
sha512sums=('6d1faf22a362318d53e56563bd6ff97315889626a90dc0e7d621a6de4bb23fb645393888aec0aa52d9c50e84cfa85118e2e57d2bc8b764a6bdbc7cba354d7331')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
