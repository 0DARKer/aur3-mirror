# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Almost all of this PKGBUILD is taken from the vinagre one on abs from:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=vinagre-spice
_pkgname=vinagre
pkgver=3.2.1
pkgrel=1
pkgdesc="A Spice/VNC/RDP Client for the GNOME Desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/vinagre/"
install=vinagre.install
depends=('libgnome-keyring' 'gtk-vnc' 'avahi' 'desktop-file-utils' 'hicolor-icon-theme' 'telepathy-glib' 'dconf' 'libpeas' 'shared-mime-info' 'gnome-icon-theme' 'spice-gtk3')
optdepends=('rdesktop: RDP plugin')
makedepends=('gnome-doc-utils' 'intltool' 'rdesktop')
provides=('vinagre')
conflicts=('vinagre')
groups=('gnome-extra')
options=('!emptydirs' '!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz)
md5sums=('6c1f984ef9a9237f1308ad2707926f3f')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --disable-scrollkeeper \
        --libexecdir=/usr/lib/vinagre \
        --enable-rdp \
        --enable-spice
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

