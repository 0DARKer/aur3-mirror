# Maintainer: Jonathan Lestrelin <zanko@daemontux.org>
# Almost all of this PKGBUILD is taken from the vinagre one on abs from:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=vinagre-spice
_pkgname=vinagre
pkgver=3.0.1
pkgrel=1
pkgdesc="A Spice/VNC/RDP Client for the GNOME Desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/vinagre/"
install=vinagre.install
depends=('libgnome-keyring' 'gtk-vnc' 'avahi' 'desktop-file-utils' 'hicolor-icon-theme' 'telepathy-glib' 'dconf' 'libpeas' 'shared-mime-info' 'gnome-icon-theme' 'spice-gtk3')
optdepends=('rdesktop: RDP plugin')
makedepends=('gtk-doc' 'intltool' 'rdesktop')
groups=('gnome-extra')
options=('!emptydirs' '!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/${pkgver%.*}/${_pkgname}-${pkgver}.tar.bz2)
sha256sums=('a4c2ec5935c0cea0da1958e7f27a925a03867854cdcaf904ce8c7e405ebe2e5b')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-scrollkeeper --libexecdir=/usr/lib/vinagre --enable-rdp --enable-spice
  sed -i -e "s/SUBDIRS = help po//g" Makefile.* #help fail to build
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
