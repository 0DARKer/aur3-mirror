# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Almost all of this PKGBUILD is taken from the vinagre one on abs from:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=vinagre-spice
_pkgname=vinagre
pkgver=3.2.1
pkgrel=2
pkgdesc="A VNC Client for the GNOME Desktop (with Spice support enable)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/vinagre/"
install=vinagre.install
depends=('libgnome-keyring' 'gtk-vnc' 'vte3' 'avahi' 'desktop-file-utils' 'hicolor-icon-theme' 'telepathy-glib' 'dconf' 'shared-mime-info' 'gnome-icon-theme' 'spice-gtk3')
optdepends=('rdesktop: RDP plugin')
makedepends=('gnome-doc-utils' 'intltool' 'rdesktop' 'openssh')
provides=('vinagre')
conflicts=('vinagre')
options=('!emptydirs' '!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz)
sha256sums=('077dc011d048e9ed7a683ed74dc027c5c7e47d0b95b99acb493bf99fff22369c')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  #building with as-needed breaks rdp
  #https://bugzilla.gnome.org/show_bug.cgi?id=653558
  export LDFLAGS="$LDFLAGS,--no-as-needed"
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --disable-scrollkeeper \
        --libexecdir=/usr/lib/vinagre \
        --enable-rdp \
        --enable-spice
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

