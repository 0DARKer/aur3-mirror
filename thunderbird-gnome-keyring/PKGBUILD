# Maintainer: lepokle <devel@leo.von-klenze.de>
pkgname='thunderbird-gnome-keyring'
pkgver=24.3.0
pkgrel=4
pkgdesc="Gnome-keyring integration for Thunderbird"
arch=('i686' 'x86_64')
url='http://github.com/lepokle/firefox-gnome-keyring'
license=('GPL')
#depends=('thunderbird>=24.2.0' 'gnome-keyring')
makedepends=('zip' 'unzip' 'nspr>=4.10.2' 'libgnome-keyring' 'yasm')
provides=('thunderbird-gnome-keyring')
source=("${pkgname}-1.0.tar.gz::$url/tarball/v1.0"
        "ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/source/thunderbird-$pkgver.source.tar.bz2"
        "mozconfig")
md5sums=('5ce5b61fd1545819d63dd3102a6ec931'
         '3093e14a3635123c0c70c8c11c27c7b1'
         'ecdbcc9c8065d61f3bf3c7b395acb4d2')

_xulsrcdir="comm-esr24"
_xuldir="xulrunner"
_subdir="lepokle-firefox-gnome-keyring-359a970"

prepare()
{
    msg "Prepare XUL Runner..."
    cp mozconfig "$srcdir/$_xulsrcdir/mozilla/.mozconfig" 
    cd "$srcdir/$_xulsrcdir/mozilla"
    echo "ac_add_options --prefix=\"$srcdir/$_xuldir\"" >> .mozconfig

    # WebRTC build tries to execute "python" and expects Python 2
    # Workaround taken from chromium PKGBUILD
    mkdir -p "$srcdir/$_xulsrcdir/mozilla/python2-path"
    ln -sf /usr/bin/python2 "$srcdir/$_xulsrcdir/mozilla/python2-path/python"

    # configure script misdetects the preprocessor without an optimization level
    # https://bugs.archlinux.org/task/34644
    sed -i '/ac_cpp=/s/$CPPFLAGS/& -O2/' configure
}


build()
{
        msg "Build XUL Runner..."
        
        export PATH="$srcdir/python2-path:$PATH"
        export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/xulrunner-$pkgver"
        export PYTHON="/usr/bin/python2"
        
        cd "$srcdir/$_xulsrcdir/mozilla"
        make -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
        mkdir -p "$srcdir/$_xuldir"
        make -f client.mk install
    
	msg "Building extension..."
	cd "${srcdir}/${_subdir}"
        export PKG_CONFIG_PATH="$srcdir/$_xuldir/lib/pkgconfig"
	make clean && make
}

package()
{
	cd ${srcdir}

	mkdir -p ${pkgdir}/usr/lib/thunderbird/extensions
	unzip ${srcdir}/${_subdir}/mozilla-gnome-keyring-*.xpi -d ${pkgdir}/usr/lib/thunderbird/extensions/{6f9d85e0-794d-11dd-ad8b-0800200c9a66}
}

