# Maintainer: drakkan <nicola.murino@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=pg_repack
pkgver=1.3.0
pkgrel=2
pkgdesc="Reorganize tables in PostgreSQL databases with minimal locks (fork of pg_reorg)"
arch=(i686 x86_64)
url="http://reorg.github.com/pg_repack/"
license=('BSD')
depends=('postgresql-libs')
makedepends=('postgresql')
source=("https://github.com/reorg/pg_repack/archive/ver_${pkgver}.tar.gz"
	"34.patch"
	"0001-Revert-Add-a-Makefile-check-to-avoid-building-agains.patch")
	

md5sums=('88eb20f0e1640afb26249dabeb0a5405'
         '71077aac8c399bed373b42a2dd44785c'
	 '68061aa8b53d2a5d637affba141b55b6')

prepare() {
  cd "$srcdir/$pkgname-ver_$pkgver"
  patch -Np1 < "../34.patch"
  patch -Np1 < "../0001-Revert-Add-a-Makefile-check-to-avoid-building-agains.patch"
}

build() {
  cd "$srcdir/$pkgname-ver_$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-ver_$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/pg_repack/COPYRIGHT
}
