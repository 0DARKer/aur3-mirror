# Maintainer: drakkan <nicola.murino@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=pg_repack
pkgver=1.2.0_beta1
_pkgver=1.2.0-beta1
pkgrel=2
pkgdesc="Reorganize tables in PostgreSQL databases with minimal locks (fork of pg_reorg)"
arch=(i686 x86_64)
url="http://reorg.github.com/pg_repack/"
license=('BSD')
depends=('postgresql-libs')
makedepends=('postgresql')
source=("https://github.com/reorg/pg_repack/archive/ver_${_pkgver}.tar.gz"
	"0001-Fix-a-nasty-data-loss-corruption-bug-from-the-sql_po.patch")
md5sums=('3d74ab0c3dc9aadaba5bb42032d56e44'
	'0e7e56cb8505d17cc8dd12b31665f288')

prepare() {
  cd "$srcdir/$pkgname-ver_$_pkgver"
  patch -p1 -i "${srcdir}/0001-Fix-a-nasty-data-loss-corruption-bug-from-the-sql_po.patch"
}

build() {
  cd "$srcdir/$pkgname-ver_$_pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-ver_$_pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/pg_repack/COPYRIGHT
}
