# Maintainer:  Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Nathan Owe. <ndowens04 at gmail>

pkgname=newsfeed
pkgver=2.16
pkgrel=4
pkgdesc="An RSS/RDF/Atom reader and aggregator"
arch=('any')
url="http://home.arcor.de/mdoege/newsfeed/"
license=('GPL')
depends=('python2' 'tk' 'python2-feedparser')
optdepends=('python2-pyaudio: for audio notifications (recommended)'
            'snack: for audio notifications (legacy)')
source=("http://home.arcor.de/mdoege/newsfeed/NewsFeed-$pkgver.tar.gz"
        "newsfeed.desktop"
        "newsfeed.png")
md5sums=('2343cd287f8612a5d3cedfad1a7edee4'
         'bf9de50dadb523f9dd4599eb1d852bf5'
         '934bc285373e953a21cbf1a4b17888ca')

build() {
  cd "$srcdir/NewsFeed-$pkgver"
  sed -i 's/"feedparser",//g' setup.py
  rm feedparser.py
  python2 setup.py build
}

package() {
  cd "$srcdir/NewsFeed-$pkgver"
  python2 setup.py install \
    --prefix=/usr \
    --root="$pkgdir" \
    --install-data=/usr/share/$pkgname
  mkdir -p $pkgdir/usr/share/icons
  cp $srcdir/../newsfeed.png $pkgdir/usr/share/icons
  mkdir -p $pkgdir/usr/share/applications
  cp $srcdir/../newsfeed.desktop $pkgdir/usr/share/applications
}
