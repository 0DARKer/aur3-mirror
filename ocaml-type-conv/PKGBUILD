# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor : Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>
pkgname=ocaml-type-conv
pkgver=3.0.4
pkgrel=2
pkgdesc="mini library with common code-generation helpers for OCaml"
arch=('i686' 'x86_64')
url="https://forge.ocamlcore.org/projects/type-conv"
license=('LGPL')
depends=('ocaml')
makedepends=('ocaml-findlib')
source=("http://forge.ocamlcore.org/frs/download.php/697/${pkgname/ocaml-/}-$pkgver.tar.gz")
md5sums=('3dbc1b4d339911fcc07880fefc91617e')
options=(!strip !makeflags)

build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"
  make CONFIGUREFLAGS="--prefix /usr --destdir $pkgdir" build
}

package(){
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"
  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR"
  make install
}
