# Maintainer: jsteel <mail at jsteel dor org>
# Contributor: Gadget3000 <gadget3000 at msn dot com>
# Contributor: Eric Forgeot < http://ifiction.free.fr >
# Contributor: Diablo150 < www.djl-linux.org >

pkgname=djl
pkgver=1.2.20
pkgrel=3
pkgdesc="A game manager inspired by Valve's Steam software for Windows"
arch=('any')
url="http://djl-linux.org"
license=('GPL')
depends=('python2')
source=($url/maj_djl/Paquets-Arch/$pkgname-$pkgver-Arch.tar.gz)
md5sums=('51ae181e22f09bf91b20b0a1bde06b69')

build() {
  # fix a read permission
  chmod +r "$srcdir"/$pkgname/$pkgname/libs/test_ws.py

  # prepare the launcher
  sed "s|\`dirname\ \$0\`|/usr/share/djl|g" -i "$srcdir"/$pkgname/$pkgname.sh

  # patch djl to work with python2
  for i in $(find "$srcdir" | grep "\.py"); do
    sed "s/\#\!\/usr\/bin\/python/\#\!\/usr\/bin\/python2/" -i "$i"
    sed "s/\#\! \/usr\/bin\/env python/\#\! \/usr\/bin\/env python2/g" -i "$i"
    sed "s/\#\!\/usr\/bin\/env python/\#\!\/usr\/bin\/env python2/g" -i "$i"
    sed "s/\/usr\/bin\/python2.6/\/usr\/bin\/python2.7/g" -i "$i"
    sed "s/commande_python = \"python2.6\"/commande_python = \"python2.7\"/g" -i "$i"
    sed "s/os.system('python djl.py')/os.system('python2 djl.py')/g" -i "$i"
  done
  sed "s/python/python2/g" -i "$srcdir"/$pkgname/$pkgname.sh
}

package() {
  install -dm755 "$pkgdir"/usr/share/$pkgname
  install -Dm755 "$srcdir"/$pkgname/$pkgname.sh "$pkgdir"/usr/bin/$pkgname

  cp -r "$srcdir"/$pkgname/* "$pkgdir"/usr/share/$pkgname/
}
