# $Id: PKGBUILD 80964 2010-05-24 06:39:23Z allan $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: benetnash <benetnash@mail.icpnet.pl>

# Contributor: Thomas Haider <t.haider@vcnc.org>

pkgname=openssh-hpn
_pkgname=openssh
pkgver=6.1p1
pkgrel=1
pkgdesc='A Secure SHell server/client with High Performance patch'
arch=('i686' 'x86_64')
license=('custom:BSD')
url="http://www.psc.edu/networking/projects/hpn-ssh/"
backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config' 'etc/pam.d/sshd' 'etc/conf.d/sshd')
depends=('krb5' 'openssl' 'libedit')
optdepends=('x11-ssh-askpass: input passphrase in X without a terminal')
provides=('openssh')
conflicts=('openssh')

_hpn_ver='hpn13v14.diff'
_hpn_patch="${_pkgname}-${pkgver}-${_hpn_ver}"

source=("ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${_pkgname}-${pkgver}.tar.gz"
        'sshd'
        'sshd.confd'
        'sshd.pam'
	'openssh-6.1p1-hpn13v14.diff')

md5sums=('3345cbf4efe90ffb06a78670ab2d05d5'
         'e44715d16fa16b586994fc1ffb6719f7'
         'e2cea70ac13af7e63d40eb04415eacd5'
         '0f494f12464e844654c53ac5da2323a5'
         '30ff8b264c87715df8e680fb1f1156e8')

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    patch -p1 < ../${_hpn_patch}

    ./configure \
            --prefix=/usr \
            --libexecdir=/usr/lib/ssh \
            --sysconfdir=/etc/ssh \
            --with-privsep-user=nobody \
            --with-md5-passwords \
            --with-pam \
            --with-mantype=man \
            --with-xauth=/usr/bin/xauth \
            --with-kerberos5=/usr \
            --with-ssl-engine \
            --with-libedit=/usr/lib \
            --disable-strip
    make 
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    install -Dm755 ../sshd "${pkgdir}"/etc/rc.d/sshd
    install -Dm644 ../sshd.pam "${pkgdir}"/etc/pam.d/sshd
    install -Dm644 ../sshd.confd "${pkgdir}"/etc/conf.d/sshd
    install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"

    rm "${pkgdir}/usr/share/man/man1/slogin.1"
    ln -sf ssh.1.gz "${pkgdir}"/usr/share/man/man1/slogin.1.gz

    #additional contrib scripts that we like
    install -Dm755 contrib/findssl.sh "${pkgdir}"/usr/bin/findssl.sh
    install -Dm755 contrib/ssh-copy-id "${pkgdir}"/usr/bin/ssh-copy-id
    install -Dm644 contrib/ssh-copy-id.1  "${pkgdir}"/usr/share/man/man1/ssh-copy-id.1

    # PAM is a common, standard feature to have
    sed \
            -e '/^#ChallengeResponseAuthentication yes$/c ChallengeResponseAuthentication no' \
            -e '/^#UsePAM no$/c UsePAM yes' \
            -i "${pkgdir}"/etc/ssh/sshd_config
}
