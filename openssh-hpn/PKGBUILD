# $Id: PKGBUILD 80964 2010-05-24 06:39:23Z allan $
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: benetnash <benetnash@mail.icpnet.pl>

# Contributor: Thomas Haider <t.haider@vcnc.org>

pkgname=openssh-hpn
_pkgname=openssh
pkgver=5.6p1
pkgrel=1
pkgdesc='A Secure SHell server/client with High Performance patch'
arch=('i686' 'x86_64')
license=('custom')
url="http://www.psc.edu/networking/projects/hpn-ssh/"
backup=('etc/ssh/ssh_config' 'etc/ssh/sshd_config' 'etc/pam.d/sshd')
depends=('openssl' 'zlib' 'pam' 'tcp_wrappers' 'heimdal')
provides=('openssh')
conflicts=('openssh')

_hpn_ver='hpn13v10.diff'
_hpn_patch="${_pkgname}-${pkgver}-${_hpn_ver}"
source=("ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${_pkgname}-${pkgver}.tar.gz"
        'sshd' 'sshd.confd' 'sshd.pam'
		http://www.psc.edu/networking/projects/hpn-ssh/${_hpn_patch}.gz)

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  patch -p1 < ../${_hpn_patch}

  #NOTE we disable-strip so that makepkg can decide whether to strip or not
  ./configure --prefix=/usr --libexecdir=/usr/lib/ssh \
    --sysconfdir=/etc/ssh --with-tcp-wrappers --with-privsep-user=nobody \
    --with-md5-passwords --with-pam --with-mantype=man --mandir=/usr/share/man \
    --with-xauth=/usr/bin/xauth --with-kerberos5=/usr --with-ssl-engine \
    --disable-strip
  make || return 1
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm755 ${srcdir}/sshd ${pkgdir}/etc/rc.d/sshd

  install -Dm644 LICENCE ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE
  install -Dm644 ${srcdir}/sshd.pam ${pkgdir}/etc/pam.d/sshd
  install -Dm644 ${srcdir}/sshd.confd ${pkgdir}/etc/conf.d/sshd

  rm ${pkgdir}/usr/share/man/man1/slogin.1
  ln -sf ssh.1.gz ${pkgdir}/usr/share/man/man1/slogin.1.gz

  #additional contrib scripts that we like
  install -Dm755 contrib/findssl.sh ${pkgdir}/usr/bin/findssl.sh
  install -Dm755 contrib/ssh-copy-id ${pkgdir}/usr/bin/ssh-copy-id
  install -Dm644 contrib/ssh-copy-id.1  ${pkgdir}/usr/share/man/man1/ssh-copy-id.1

  # sshd_config
  sed -i \
    -e 's|^#ListenAddress 0.0.0.0|ListenAddress 0.0.0.0|g' \
    -e 's|^#UsePAM no|UsePAM yes|g' \
    -e 's|^#ChallengeResponseAuthentication yes|ChallengeResponseAuthentication no|g' \
	-e 's|^#TcpRcvBufPoll no|TcpRcvBufPoll yes|g' \
    ${pkgdir}/etc/ssh/sshd_config
  echo "HashKnownHosts yes" >>  ${pkgdir}/etc/ssh/ssh_config
  echo "StrictHostKeyChecking ask" >>  ${pkgdir}/etc/ssh/ssh_config

  #ssh_config
  sed -i \
    -e 's|^# Host \*|Host *|g' \
    ${pkgdir}/etc/ssh/ssh_config
}
sha256sums=('538af53b2b8162c21a293bb004ae2bdb141abd250f61b4cea55244749f3c6c2b'
            '4b1ac716f280c8ccf52d29aa86e85898b7a69e1632acbe1355aefbc3bfe836af'
            '4eab4e844567c5bd3b6102658f2a2592164c783f9657cdd689f48e546f6b17f1'
            'fa82d17743a712623d3f2aa1e0a6aeb3dc6d3bbcac75ba19560b4c1405a7a709'
            '6a9ee815e8ffcc9068c3dce4ad4f2898fc0db6b768a3152280aceb8c06c8b450')
