# Maintainer: kusakata <shohei atmark kusakata period com>

pkgname=openspades-git
pkgver=0.0.10.190.g670f41f
pkgrel=1
pkgdesc="A clone of Voxlap Ace of Spades 0.75"
arch=('i686' 'x86_64')
url="https://github.com/yvt/openspades"
license=('GPL3')
depends=('curl' 'fltk' 'glew' 'mesa' 'openal' 'openspades-data-git' 'sdl2' 'sdl2_image')
makedepends=('cmake' 'git')
provides=('openspades')
source=("$pkgname"::'git://github.com/yvt/openspades.git' "openspades.desktop")
md5sums=('SKIP'
         '397bf995c84d5ae8d7014eae7d4a1c2d')

pkgver() {
	cd "$pkgname"
	git describe --tags | sed 's/^v//' | sed 's/-/./g'
}

build() {
	cd "$pkgname"
	cmake -D CMAKE_INSTALL_PREFIX=/usr -D OPENSPADES_RESDIR=/usr/share/openspades-data-git/Resources .
	make OpenSpades
}

package() {
	cd "$pkgname/bin"
	install -Dm755 OpenSpades "${pkgdir}/usr/bin/openspades"
	cd "${srcdir}"
	install -Dm644 "openspades.desktop" "${pkgdir}/usr/share/applications/openspades.desktop"
}
