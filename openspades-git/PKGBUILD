# Maintainer: kusakata <shohei atmark kusakata period com>

pkgname=openspades-git
pkgver=0.0.11.42.gd8cf944
pkgrel=1
pkgdesc="A clone of Voxlap Ace of Spades 0.75"
arch=('i686' 'x86_64')
url="https://sites.google.com/a/yvt.jp/openspades/"
license=('GPL3')
depends=('curl' 'glew' 'mesa' 'openal' 'sdl2' 'sdl2_image')
makedepends=('cmake' 'git' 'imagemagick' 'unzip' 'wget')
provides=('openspades')
source=("$pkgname"::'git://github.com/yvt/openspades.git' "openspades.desktop")
md5sums=('SKIP'
         '397bf995c84d5ae8d7014eae7d4a1c2d')

pkgver() {
	cd "$pkgname"
	git describe --tags | sed 's/^v//' | sed 's/-/./g'
}

prepare() {
	cd "$pkgname/Resources"
	cat PakLocation.txt | grep "http://" | wget -i -
	unzip *.zip
}

build() {
	cd "$pkgname"
	cmake -D CMAKE_INSTALL_PREFIX=/usr -D OPENSPADES_RESDIR=/usr/share/openspades-git/Resources .
	make OpenSpades
}

package() {
	cd "$pkgname/bin"
	install -Dm755 OpenSpades "${pkgdir}/usr/bin/openspades"
	cd "${srcdir}"
	install -Dm644 "openspades.desktop" "${pkgdir}/usr/share/applications/openspades.desktop"
	
	mkdir "${pkgdir}/usr/share/${pkgname}"
	mkdir "${pkgdir}/usr/share/pixmaps"

	cd "$pkgname"
	cp -r Resources "${pkgdir}/usr/share/${pkgname}"
	
	cd "Resources/Icons"
	convert OpenSpades.ico openspades.png
	cp openspades-7.png "${pkgdir}/usr/share/pixmaps/openspades.png"

	cd "${pkgdir}/usr/share/${pkgname}/Resources"
	rm Makefile cmake_install.cmake
	rm -rf CMakeFiles
}
