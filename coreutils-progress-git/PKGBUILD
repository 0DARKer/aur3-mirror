# Maintainer: Felix Wiegand <koffeinflummi@gmail.com>

pkgname=coreutils-progress-git
pkgver=v8.23.211.645b5e15
pkgrel=1
pkgdesc="fork of the basic GNU utils, now with progress bars"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('base')
depends=('glibc' 'pam' 'acl' 'gmp' 'libcap')
makedepends=('git' 'gperf' 'rsync')
conflicts=('coreutils')
provides=('coreutils')
install=${pkgname/-git}.install
source=('git+https://github.com/KoffeinFlummi/coreutils-progress')
md5sums=('SKIP')

pkgver() {
    cd "${srcdir}/${pkgname/-git}"
    git describe --always | sed 's|-|.|g'
}

build() {
    cd "${srcdir}/${pkgname/-git}"

    ./bootstrap
    ./configure --prefix=/usr --libexecdir=/usr/lib \
        --enable-no-install-program=groups,hostname,kill,uptime

    make
}

package() {
    cd "${srcdir}/${pkgname/-git}"
    make DESTDIR="${pkgdir}" install
}
