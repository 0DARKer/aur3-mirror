# Maintainer: Ner0

pkgname=gala-bzr
pkgver=323
pkgrel=1
pkgdesc="Pantheon's Window Manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/gala"
license=('GPL3')
groups=('pantheon')
depends=('clutter-gtk' 'granite-bzr' 'mutter' 'plank-bzr')
makedepends=('bzr' 'vala' 'cmake')
install=gala.install
source=('bzr+lp:gala')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/gala"
  bzr revno
}

build() {
  cd "$srcdir/gala"

  rm -rf build/ 2>/dev/null
  mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DCMAKE_EXE_LINKER_FLAGS='-lm'
  make
}

package() {
  cd "$srcdir/gala/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
