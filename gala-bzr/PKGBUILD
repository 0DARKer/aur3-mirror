# Maintainer: Ner0

pkgname=gala-bzr
pkgver=322
pkgrel=1
pkgdesc="A window & compositing manager based on libmutter and designed by elementary"
arch=('i686' 'x86_64')
url="https://launchpad.net/gala"
license=('GPL3')
groups=('pantheon')
depends=('granite-bzr' 'mutter' 'clutter' 'clutter-gtk' 'bamf' 'libxcb' 'libxfixes' 'gtk3' 'gobject-introspection' 'gsettings-desktop-schemas' 'plank-bzr>=798')
makedepends=('bzr' 'vala' 'cmake')
install=gala.install
source=('gala::bzr+http://bazaar.launchpad.net/~gala-dev/gala/trunk/')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/gala"
  bzr revno
}

build() {
  cd "$srcdir/gala"

  sed -i 's|${DEPS_LIBRARIES}|${DEPS_LIBRARIES} -lm|g' CMakeLists.txt

  rm -rf build/
  mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DGSETTINGS_LOCALINSTALL=OFF
  make
}

package() {
  cd "$srcdir/gala/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
