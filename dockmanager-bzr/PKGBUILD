# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=dockmanager-bzr
pkgver=83
pkgrel=2
pkgdesc="Dock-independent helper scripts for compatible docks"
url="https://launchpad.net/dockmanager"
arch=(i686 x86_64)
license=(GPL)
depends=('libdesktop-agnostic>=0.3.90-2' gconf dbus-glib python2)
makedepends=(bzr 'vala>=0.10' pkgconfig intltool autoconf gnome-common)
conflicts=(dockmanager)
provides=("dockmanager=0.1.0")
options=('!libtool' '!emptydirs')
install=dockmanager.install
source=(vala.patch)
md5sums=('f6db8c3a432b41b620f6fcc8bfccfd81')

_bzrtrunk=lp:dockmanager
_bzrmod=dockmanager

build() {
  cd "$srcdir"

  msg2 "Connecting to Launchpad...."

  if [ -d $_bzrmod ] ; then
    ( cd $_bzrmod && bzr up )
  else
    bzr checkout $_bzrtrunk $_bzrmod
  fi

  msg2 "BZR checkout done or server timeout"

  rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build

  msg2 "Starting make..."

  patch -Np0 -i "$srcdir/vala.patch"

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc

  make
}

package() {
  cd "$srcdir/$_bzrmod-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  # Fix for python2
  find $pkgdir -type f -exec sed -i '1s|#!/usr/bin/env python$|&2|' {} +

  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/dockmanager.schemas" "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -rf "$pkgdir/etc/gconf"
}
