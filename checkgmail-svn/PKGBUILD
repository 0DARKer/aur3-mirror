# Contributor: Chris Baker <baker.chris.3@gmail.com>
# Contributor: Reventlov <contact+aur@volcanis.me>
#Â Maintainer: Martin Herndl <martin.herndl@gmail.com>
pkgname=checkgmail-svn
pkgver=r47
pkgrel=6
pkgdesc="An alternative Gmail Notifier for Linux and other *nix systems - patched to work"
arch=('any')
url="http://checkgmail.sourceforge.net"
license=('GPL')
depends=('perl-gtk2-trayicon' 'perl-lwp-protocol-https' 'perl-xml-simple' 'perl-freezethaw' 'perl-crypt-simple' 'perl-crypt-blowfish')
optdepends=('perl-gtk2-sexy')
makedepends=('gendesk')
conflicts=('checkgmail')
provides=('checkgmail')
install="readme.install"
source=("$pkgname"::'svn://svn.code.sf.net/p/checkgmail/code/'
        'checkgmail.png'
        'use-lwp-protocol-https.patch'
        'checkgmail-2factor-support-with-csrf.patch')
md5sums=('SKIP'
         'f551f6cefb06201b03f8cb41fc85edb3'
         '690dc9f74d0acc775f2b794b3031ab17'
         '5548be0b1bd35549ef8fe119809a08e1')

pkgver() {
    cd "$pkgname"
    local ver="$(svnversion)"
    printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
    cd "$srcdir/$pkgname"

    for p in use-lwp-protocol-https checkgmail-2factor-support-with-csrf
    do
        msg2 "Applying $p.patch..."
        patch -p0 < "../$p.patch"
    done

    # fix broken account urls
    sed -i 's|www.google.com/accounts|accounts.google.com|' checkgmail

    # fix broken action urls
    sed -i 's|mail.google.com/mail|mail.google.com/mail/u/0|' checkgmail

    cd "$srcdir"

    gendesk -f -n --pkgname="checkgmail" --pkgdesc='Checks a Gmail account for new mail' \
        --categories='Network;Email' --name="CheckGmail"
}

package() {
    cd "$srcdir/$pkgname"

    install -D -m 755 checkgmail ${pkgdir}/usr/bin/checkgmail
    install -D -m 644 man/checkgmail.1.gz \
        ${pkgdir}/usr/share/man/man1/checkgmail.1.gz

    # freedesktop
    install -D -m 644 ${srcdir}/checkgmail.desktop \
        ${pkgdir}/usr/share/applications/checkgmail.desktop
    install -D -m 644 ${srcdir}/checkgmail.png \
        ${pkgdir}/usr/share/pixmaps/checkgmail.png
}
