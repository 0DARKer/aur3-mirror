# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=python2-kombu
pkgver=3.0.20
pkgrel=1
pkgdesc="AMQP Messaging Framework for Python 2.x"
arch=('any')
url="http://pypi.python.org/pypi/kombu/"
license=('custom:BSD')
depends=('python2' 'python2-anyjson')
optdepends=("python2-librabbitmq: High performance AMQP transport written in C")
makedepends=('python2-setuptools')
options=(!emptydirs)
source=("http://pypi.python.org/packages/source/k/kombu/kombu-$pkgver.tar.gz")
sha512sums=('e91c3d5e556432103cf19f403163dc1da2baccf4504c1343e07bff756261316011be23620a0b59aa4450839f856562525d985ed1eccc88f4f84677c5695659d3')

build() {
  cd kombu-$pkgver
  python2 setup.py build
}

package() {
  cd kombu-$pkgver
  python2 setup.py install -O1 --root="$pkgdir/"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

