# Maintainer: Hinrich Harms <arch@hinrich.de>

pkgname=enigmail-cvs
pkgver=20100626
pkgrel=1

# Version of Thunderbird Enigmail will be build against:
_tb_ver=3.1

# Thunderbird lib directory for XPI installation:
_tb_libdir=thunderbird-3.1

pkgdesc="OpenPGP security extension for Mozilla Thunderbird 3"
arch=('i686' 'x86_64')
url="http://enigmail.mozdev.org"
license=('MPL' 'GPL')
makedepends=('cvs' 'zip')
depends=('gnupg' 'thunderbird>=3.1')
provides=('enigmail')
conflicts=('enigmail')
options=('!ccache' '!distcc')
source=("http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/${_tb_ver}/source/thunderbird-${_tb_ver}.source.tar.bz2"
        "mozconfig")
md5sums=('feb4d737d568066076879a09bd0d506e'
         'ec786d343c0f951dfd71df0438c1f0ce')

_cvsmod="enigmail/src"
_cvsroot=":pserver:guest:guest@mozdev.org:/cvs"
build() {
	# Checkout Enigmail
	cd $startdir/src
	msg "Connecting to CVS server...."
	cvs -z3 -d $_cvsroot co $_cvsmod
	msg "CVS checkout done or server timeout"

	# Thunderbird
	msg "Compiling needed parts of Thunderbird..."
	cd $srcdir
	_comm_dir=$(ls -1d comm-* |sort |tail -1)
	cd $_comm_dir
	export MOZCONFIG="$startdir/mozconfig"
	make -f client.mk export || return 1
	cd mozilla/modules/libreg
	make || return 1
	cd ../../xpcom/string
	make || return 1
	cd ..
	make || return 1
	cd obsolete
	make || return 1

	# Enigmail
	msg "Compiling Enigmail..."
	mv $srcdir/enigmail/src $srcdir/$_comm_dir/mailnews/extensions/enigmail
	cd $srcdir/$_comm_dir/mailnews/extensions/enigmail
	./makemake -r || return 1
	make || return 1
	
	# Create xpi file
	make xpi || return 1
	msg "XPI file created."

	# Install to the thunderbird lib directory
	_emid=`grep em:id package/install.rdf | head -n1 | sed 's/.*>\(.*\)<.*/\1/'`
	mkdir -p $pkgdir/usr/lib/$_tb_libdir/extensions/$_emid
	cd $pkgdir/usr/lib/$_tb_libdir/extensions/$_emid
	# Look for the current (highest) version number of the xpi
	_xpidir=$srcdir/$_comm_dir/mozilla/dist/bin
	_xpifullpath=`ls $_xpidir/enigmail-*-linux-$CARCH.xpi | sort | sed -n '$p'`
	bsdtar -x -f $_xpifullpath
}
