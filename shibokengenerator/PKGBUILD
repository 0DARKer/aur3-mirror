# Maintainer: Matthias Maennich <arch@maennich.net>
pkgname=shibokengenerator
pkgver=1.1.0
pkgrel=1
_pyver=2.7
pkgdesc="Support library for Python bindings"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.pyside.org"
depends=("python2>=$_pyver" 'qt>=4.7' 'openssl' 'generatorrunner>=0.6.16')
makedepends=('cmake' 'automoc4')
conflicts=('shiboken')
source=("http://www.pyside.org/files/shiboken-$pkgver.tar.bz2")
md5sums=('9c9d696c8c426fb5abf28a6bd3759558')

build(){
    cd $srcdir/shiboken-$pkgver
    mkdir -p build && cd build
    cmake ../ -DCMAKE_INSTALL_PREFIX=/usr  \
              -DCMAKE_BUILD_TYPE=Release   \
              -DBUILD_TESTS=OFF            \
              -DPYTHON_EXECUTABLE=/usr/bin/python$_pyver
    make shiboken
}

package(){
    cd $srcdir/shiboken-$pkgver/build/generator
    make DESTDIR=$pkgdir install

    mkdir -m 755 -p $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -m 755 ../data/ShibokenConfig.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -m 755 ../data/ShibokenConfigVersion.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/
    install -m 755 ../data/ShibokenConfig-python$_pyver.cmake $pkgdir/usr/lib/cmake/Shiboken-$pkgver/

    mkdir -m 755 -p $pkgdir/usr/lib/pkgconfig/
    install -m 755 ../data/shiboken.pc $pkgdir/usr/lib/pkgconfig/
}
