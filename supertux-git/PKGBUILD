# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Hakim <acrox999 at gmail dot com>
# Contributor: Patrick Bartels <p4ddy.b@gmail.com>

pkgname='supertux-git'
pkgver=5207.31f8084
pkgrel=1
pkgdesc="A classic 2D jump'n run sidescroller game in a style similar to the original SuperMario game"
url='http://super-tux.sourceforge.net'
license=('GPL')
arch=('i686' 'x86_64')
depends=('sdl_image' 'physfs' 'openal' 'libvorbis' 'curl' 'boost' 'glew')
makedepends=('git' 'cmake')
conflicts=('supertux')
provides=('supertux')
source=('git+https://code.google.com/p/supertux/')
sha512sums=('SKIP')

pkgver() {
  cd supertux
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "${srcdir}/supertux"

  sed -i '/curl\/types.h/d' src/addon/addon_manager.cpp
  sed -i '1i#include <cstddef>' src/supertux/screen_manager.hpp  
}

build() {
  cd "${srcdir}/supertux"

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINSTALL_SUBDIR_BIN=bin \
    .

  make
}

package() {
  cd "${srcdir}/supertux"

  make DESTDIR="${pkgdir}/" install
}
