# Contributor: Daniele Paolella <danielepaolella@email.it>
pkgname=ladish
pkgver=1
pkgrel=2
pkgdesc="Session management system for JACK applications"
arch=('i686' 'x86_64')
url="http://ladish.org"
license=('GPL2')
makedepends=('boost' 'intltool')
depends=('jackdbus' 'e2fsprogs' 'python2' 'flowcanvas')
optdepends=('laditools: LADI Tools')
changelog=$pkgname.changelog
source=(http://ladish.org/download/ladish-$pkgver.tar.bz2
        loader.patch)
md5sums=('ef77c90c68b2b79c1330cada618bf5d0'
         '0913746c27c3fb71380c26e1f093948d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 < "$srcdir/loader.patch"
  python2 ./waf configure --prefix=/usr
  python2 ./waf
  sed -i 's/#!\/usr\/bin\/env python/#!\/usr\/bin\/env python2/' ladish_control
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 ./waf install --destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
