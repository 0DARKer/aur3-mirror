# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Daniele Paolella <danielepaolella@email.it>

pkgname=ladish
pkgver=1
pkgrel=3
pkgdesc="Session management system for JACK applications"
arch=('i686' 'x86_64')
url="http://ladish.org"
license=('GPL2')
makedepends=('boost' 'intltool' 'desktop-file-utils')
depends=('jackdbus' 'python2' 'dbus-glib' 'flowcanvas' 'hicolor-icon-theme')
optdepends=('laditools: LADI Tools')
install="$pkgname.install"
source=("http://ladish.org/download/ladish-$pkgver.tar.bz2")
md5sums=('ef77c90c68b2b79c1330cada618bf5d0')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # missing include
  sed -i "36i#include <sys/resource.h>" daemon/loader.c

  # use python2
  sed -i "1s/python/&2/" ladish_control
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON=/usr/bin/python2

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 waf install --destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
