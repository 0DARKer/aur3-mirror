# Maintainer: Alexej Magura <lspci.sh*gmail*>
# Contributor: Jeremy Whiting <jpwhiting@kde.org>
# Contributor: Lisa Denia <eiffel56@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=automake-1.12
_realname=automake
pkgver=1.12.6
pkgrel=6
pkgdesc="A GNU tool for automatically creating Makefiles"
arch=('any')
license=('GPL')
url="http://www.gnu.org/software/automake"
groups=('base-devel')
depends=('perl' 'bash')
provides="automake=1.12"
install=automake.install
source=(ftp://ftp.gnu.org/gnu/$_realname/$_realname-$pkgver.tar.gz)
md5sums=('199d39ece2e6070d64ac20d45ac86026')

prepare () {
  cd "$srcdir/$_realname-$pkgver"
  ./configure --prefix=/usr
}

build () {
  cd "$srcdir/$_realname-$pkgver"
  make
}

package () {
  cd "$srcdir/$_realname-$pkgver"
  make DESTDIR="$pkgdir" install

  for file in $(find "$pkgdir"/usr/share/info/* -iname "*\.info*"); do
    mv "$file" "$(sed -r 's/(\.info)/-1.12\1/' <<<"$file")"
  done

  rm "$pkgdir/usr/share/man/man1/"{aclocal,automake}.1
}
