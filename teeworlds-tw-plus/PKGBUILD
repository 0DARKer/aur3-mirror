# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=teeworlds-tw-plus
pkgver=0.7.4
pkgrel=1
pkgdesc="A customized server, named TW+, providing iCTF+, iFreeze and more"
arch=('i686' 'x86_64')
url="https://www.teeworlds.com/forum/viewtopic.php?id=9431"
license=('custom')
if [[ $CARCH == i686 ]]; then
    depends=(teeworlds)
elif [[ $CARCH == x86_64 ]]; then
    depends=(teeworlds lib32-gcc-libs)
fi
makedepends=('unzip')
source=(https://github.com/downloads/Teetime/teeworlds/tw+-$pkgver-Linux.zip
        server.sh.in README
        ctf4_darkgreen.map ifreeze2.1.map ifreeze2.2.map)
md5sums=('f15f47b90fe69fb1002c66764f36d3d1'
         '529146a3b13993770fa414f66c67fa26'
         '3969f0dd1ef418fc04d5eae9c8603a3d'
         'f9fcd5f437c6e5906d3a99b6af0851e0'
         'fbf8ec3246900f3fe53e1cca7d4f89c8'
         'a4c0a0e3f7bc455d5c7486f184a68232')

package() {
   # create dirs
  install -dm755 "$pkgdir"/usr/bin \
                 "$pkgdir"/usr/share/$pkgname \
                 "$pkgdir"/usr/share/doc/$pkgname \
                 "$pkgdir"/usr/share/teeworlds/data/maps/
   # install files from source=()
  install -m755 tw+_srv "$pkgdir"/usr/share/$pkgname/teeworlds_srv
  install -m644 banmasters.cfg "$pkgdir"/usr/share/$pkgname/
  install -m644 sample-ictf.cfg "$pkgdir"/usr/share/$pkgname/
  install -m644 sample-ifreeze.cfg "$pkgdir"/usr/share/$pkgname/
  install -m644 README "$pkgdir"/usr/share/doc/$pkgname/
  for map in ctf4_darkgreen.map ifreeze2.1.map ifreeze2.2.map; do
    install -m644 $map "$pkgdir"/usr/share/teeworlds/data/maps/
  done
  
   # set iCTF+ script and cfg file
  install -m755 server.sh.in "$pkgdir"/usr/bin/teeworlds-ictf+_srv
  sed -i "$pkgdir"/usr/bin/teeworlds-ictf+_srv \
      -e "s/@MODNAME@/$pkgname/" \
      -e "s/server.cfg/sample-ictf.cfg/"
  sed -i "$pkgdir"/usr/share/$pkgname/sample-ictf.cfg \
      -e 's/sv_map.*/sv_map ctf4_darkgreen/'
  
   # set iFreeze script and cfg file
  install -m755 server.sh.in "$pkgdir"/usr/bin/teeworlds-ifreeze_srv
  sed -i "$pkgdir"/usr/bin/teeworlds-ifreeze_srv \
      -e "s/@MODNAME@/$pkgname/" \
      -e "s/server.cfg/sample-ifreeze.cfg/"
  sed -i "$pkgdir"/usr/share/$pkgname/sample-ifreeze.cfg \
      -e 's/sv_map.*/sv_map ifreeze2.1/'
}
