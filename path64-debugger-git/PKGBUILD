# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=path64-debugger-git
pkgver=20110617
pkgrel=1
pkgdesc="PathScale debugger"
arch=('x86_64')
url="http://www.path64.org/"
license=('CDDL')
depends=('gcc-libs' 'ncurses')
makedepends=('git')
source=()
md5sums=()

_gitroot="git://github.com/path64/debugger.git"
_gitname="debugger"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir/$_gitname-build" ]; then
        rm -rf "$srcdir/$_gitname-build"
  fi  

  mkdir -p "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr "$srcdir/$_gitname"
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
} 
