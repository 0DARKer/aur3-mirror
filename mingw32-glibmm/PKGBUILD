# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

_pkgname=glibmm
pkgname=mingw32-${_pkgname}
pkgver=2.24.2
pkgrel=3
arch=(any)
pkgdesc="C++ interface for glib (mingw32)"
depends=('mingw32-glib2>=2.24' 'mingw32-libsigc++2.0>=2.2.5')
makedepends=(mingw32-gcc)
license=(LGPL)
options=(!strip)
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgname}/2.24/${_pkgname}-$pkgver.tar.bz2)
sha256sums=('d4687c0b27f9c9f955a5306649008a1f8d8935edff916278fb7b65b44292ff7a')

build()
{
  cd ${srcdir}/${_pkgname}-$pkgver

  export CPPFLAGS="$CPPFLAGS -D_REENTRANT"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig"
  unset LDFLAGS PKG_CONFIG_PATH

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-documentation

  make
}

package() {
  cd ${srcdir}/${_pkgname}-$pkgver
  make install DESTDIR=$startdir/pkg

  cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
}
