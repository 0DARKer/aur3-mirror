# Maintainer : Marco A Rojas <marquicus at gmail.com>
# Maintainer : Netanel Shine <netanel at archlinux.org.il >
# Maintainer: ngoonee <ngoonee.talk@gmail.com>
# Contributor: Adam Russell <adamlr6+arch@gmail.com>
pkgname=samba4
pkgver=4.0.0A15
# We use the 'A' to fake out pacman's version comparators.  Samba chooses
# to append 'a','b',etc to their subsequent releases, which pamcan
# misconstrues as alpha, beta, etc.  Bad samba!
_realver=4.0.0alpha15
pkgrel=1
pkgdesc="Alpha build of samba4."
arch=('i686' 'x86_64')
url="http://www.samba.org"
license=('GPL3')
depends=('db>=4.7' 'popt' 'libcups' 'acl' 'libldap' 'libcap>=2.16' 'pam' 'fam' 'gnutls>=2.4.1' 'talloc' 'tdb' 'tevent' 'ldb')
makedepends=('python2' 'docbook-xsl')
options=(!makeflags)
source=(http://us1.samba.org/samba/ftp/samba4/samba-${_realver}.tar.gz)    
md5sums=('19a464072f6685ef0988ce850a96bd6a')
_prefix="/opt/samba4"

build() {
  # change to use python2
  sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
         -e "s|python-config|python2-config|" \
         -e "s|bin/python|bin/python2|" \
    $(find ${srcdir}/samba-${_realver} -name '*.py')
  sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
    $(find ${srcdir}/samba-${_realver} -name 'wscript*')
  sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
         -e "s|python-config|python2-config|" \
         -e "s|bin/python|bin/python2|" \
    $(find ${srcdir}/samba-${_realver} -name 'configure.ac')
  sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
    $(find ${srcdir}/samba-${_realver} -name 'upgrade_from_s3')
#  sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
#    $(find ${srcdir}/samba-${_realver} -name 'provision')
  sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
         -e "s|python-config|python2-config|" \
    $(find ${srcdir}/samba-${_realver}/buildtools -type f)
  sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
         -e "s|python-config|python2-config|" \
    $(find ${srcdir}/samba-${_realver}/source4/scripting -type f)

  # change prefix for pkgconfig
#  sed -i -e "s|prefix=/usr$|prefix=${_prefix}|" \
#    $(find ${srcdir}/samba-${_realver} -name '*.pc')

  export PYTHON=/usr/bin/python2
	cd ${srcdir}/samba-${_realver}
	./configure --prefix=${_prefix} --enable-fhs
	make
}

package() {
	_pyver=`python2 -c 'import sys; print(sys.version[:3])'`

	cd ${srcdir}/samba-${_realver}
	make DESTDIR="$pkgdir/" install

	install -d ${pkgdir}/etc/ld.so.conf.d
	echo "${_prefix}/samba/lib" > ${pkgdir}/etc/ld.so.conf.d/samba4.conf

	cd ${pkgdir}/${_prefix}/lib/
	for _i in libldb; do
		ln -s ${_i}-samba4.so.0 ${_i}.so
		ln -s ${_i}-samba4.so.0 ${_i}.so.0
	done

	# remove conflict files of tevent and ldb
	rm -f ${pkgdir}/${_prefix}/lib/libldb.so*
	rm -f ${pkgdir}/${_prefix}/bin/{ldbadd,ldbdel,ldbedit,ldbmodify,ldbrename,ldbsearch}
	rm -f ${pkgdir}/${_prefix}/share/man/man?/{ldbadd,ldbdel,ldbedit,ldbmodify,ldbrename,ldbsearch}.1
	rm -f ${pkgdir}/${_prefix}/share/man/man3/ldb.3
	rm -f ${pkgdir}/${_prefix}/lib/python${_pyver}/site-packages/ldb.so*
	rm -f ${pkgdir}/${_prefix}/lib/python${_pyver}/site-packages/_tevent.so*

	find ${pkgdir}/${_prefix}/lib/python${_pyver}/site-packages/ -name '*.py' | \
		xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
	find ${pkgdir}/${_prefix}/bin ${pkgdir}/${_prefix}/sbin -type f -executable | \
		xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
}

backup=(etc/ld.so.conf.d/samba4.conf)

