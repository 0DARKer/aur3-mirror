# Maintainer: Michael Goehler <somebody.here@gmx.de>
# Contributor: Nikolay Amiantov <nikoamia@gmail.com>

pkgname=libfprint-vfs5011-git
_pkgname=libfprint
pkgver=r261.35e356f
pkgrel=1
pkgdesc="libfprint is an open source software library designed to make it easy for application developers to add support for consumer fingerprint readers to their software."
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/fprint/libfprint/"
license=(LGPL)
depends=(libusb nss gdk-pixbuf2)
makedepends=(git)
provides=(libfprint)
conflicts=(libfprint)
options=(!libtool !emptydirs)
install=libfprint-vfs5011-git.install
source=('libfprint::git+git://anongit.freedesktop.org/libfprint/libfprint'
        '50-fprint.rules'
        '0001-lib-Add-VFS5011-driver-v2.patch'
        'validity-0017-support.patch')
md5sums=('SKIP'
         'f88ba1a84ea6f9c0bc98f713feb19dfb'
         '4c5b7008e55bf2947f6e8590636ed6e7'
         'c41d6b4f96fc4d34200c18c73b4b680a')

pkgver() {
    cd "$srcdir/${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_pkgname}"

    patch -p1 < ../0001-lib-Add-VFS5011-driver-v2.patch

    ./autogen.sh --prefix=/usr \
                 --sysconfdir=/etc \
                 --disable-static \
                 --disable-examples-build \
                 --disable-x11-examples-build \
                 --disable-debug-log

    make

    patch -p1 < ../validity-0017-support.patch
}

package() {
    cd "${srcdir}/${_pkgname}"

    make DESTDIR="${pkgdir}/" install

    mkdir -p "${pkgdir}/lib/udev/rules.d"
    mv "${pkgdir}"/usr/lib/udev/rules.d/* "${pkgdir}/lib/udev/rules.d/"

    #install -m644 "${srcdir}/50-fprint.rules" "${pkgdir}/lib/udev/rules.d"
}

# vim:set ts=4 sw=4 et:
