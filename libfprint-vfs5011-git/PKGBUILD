# Maintainer: Michael Goehler <somebody.here@gmx.de>
# Contributor: Nikolay Amiantov <nikoamia@gmail.com>

pkgname=libfprint-vfs5011-git
_pkgname=fprint_vfs5011
pkgver=r26.0a401cf
pkgrel=1
pkgdesc="libfprint is an open source software library designed to make it easy for application developers to add support for consumer fingerprint readers to their software."
arch=(i686 x86_64)
url="https://github.com/ars3niy/fprint_vfs5011.git"
license=(LGPL)
depends=(libusb nss gdk-pixbuf2)
makedepends=(git)
provides=(libfprint)
options=(!libtool !emptydirs)
install=fprint.install
# waiting for pull request (https://github.com/ars3niy/fprint_vfs5011/pull/8)
#source=('fprint_vfs5011::git+https://github.com/ars3niy/fprint_vfs5011.git' '50-fprint.rules')
source=('fprint_vfs5011::git+https://github.com/abbradar/fprint_vfs5011.git' '50-fprint.rules')
md5sums=('SKIP'
         '07dacb59c9a88a773ac9978a9e8b2118')

pkgver() {
    cd "$srcdir/${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}/${_pkgname}"

    ./autogen.sh --prefix=/usr \
                 --sysconfdir=/etc \
                 --disable-static \
                 --disable-examples-build \
                 --disable-x11-examples-build \
                 --disable-debug-log \
                 --with-drivers=vfs5011

    make
}

package() {
    cd "${srcdir}/${_pkgname}"

    make DESTDIR="${pkgdir}/" install

    mkdir -p "${pkgdir}/lib/udev/rules.d"
    mv "${pkgdir}"/usr/lib/udev/rules.d/* "${pkgdir}/lib/udev/rules.d/"

    install -m644 "${srcdir}/50-fprint.rules" "${pkgdir}/lib/udev/rules.d"
}

# vim:set ts=4 sw=4 et:
