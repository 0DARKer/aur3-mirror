# Maintainer: Michael Goehler <somebody.here@gmx.de>
# Contributor: Nikolay Amiantov <nikoamia@gmail.com>

pkgname=libfprint-vfs5011-git
_pkgname=libfprint
pkgver=r261.35e356f
pkgrel=1
pkgdesc="libfprint is an open source software library designed to make it easy for application developers to add support for consumer fingerprint readers to their software."
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/fprint/libfprint/"
license=(LGPL)
depends=(libusb nss gdk-pixbuf2)
makedepends=(git)
provides=(libfprint)
conflicts=(libfprint)
options=(!libtool !emptydirs)
install=libfprint-vfs5011-git.install
source=('libfprint::git+git://anongit.freedesktop.org/libfprint/libfprint'
        '0001-lib-Add-VFS5011-driver-v2.patch'
        'validity-0017-support.patch')
md5sums=('SKIP'
         '4c5b7008e55bf2947f6e8590636ed6e7'
         'fd0bd43c795e497e16ebbf6f224726a7')

pkgver() {
    cd "$srcdir/${_pkgname}"

    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "${srcdir}/${_pkgname}"

    patch -p1 < ../0001-lib-Add-VFS5011-driver-v2.patch
    patch -p1 < ../validity-0017-support.patch
}

build() {
    cd "${srcdir}/${_pkgname}"

    ./autogen.sh --prefix=/usr \
                 --sysconfdir=/etc \
                 --disable-static \
                 --disable-examples-build \
                 --disable-x11-examples-build \
                 --disable-debug-log

    make

    sed -i 's/\(^SUBSYSTEM.*\)/\1, MODE="0664", GROUP="scanner"/' libfprint/60-fprint-autosuspend.rules
}

package() {
    cd "${srcdir}/${_pkgname}"

    make DESTDIR="${pkgdir}/" install
}

# vim:set ts=4 sw=4 et:
