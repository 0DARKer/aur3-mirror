# Maintainer: korjjj <korjjj+aur@gmail.com>
# Contributors:
#  xeross <contact@xeross.me>
#  codekoala <codekoala@gmail.com>

pkgname=etcd
pkgver=0.4.3
pkgrel=1
pkgdesc='A highly-available key value store for shared configuration and service discovery.'
arch=('x86_64' 'i686')
url='https://github.com/coreos/etcd'
license=('Apache License 2')
makedepends=('go')
install='etcd.install'
backup=('etc/conf.d/etcd')

source=("https://github.com/coreos/${pkgname}/archive/v${pkgver}.tar.gz"
        'etcd.service'
        'etcd.conf')

sha256sums=('db2b597d752993bfdf73c82a5f7837f4231d653858995d9068e8f3f7a9d5e4e2'
            'dd0e76e58da1fe99c4ad3739f06e70fb0acfad8d4725d581b7e76acd57e8fef8'
            '514e6ab46330a376499e78f45eb65897ac44cf87c5ae62269f159939a74662ac')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./build
}

package() {
  install -Dm644 "${srcdir}/etcd.conf" "${pkgdir}/etc/conf.d/etcd"
  install -Dm644 "${srcdir}/etcd.service" "${pkgdir}/usr/lib/systemd/system/etcd.service"
  install -dm755 "${pkgdir}/var/lib/${pkgname}"
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
