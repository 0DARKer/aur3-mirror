# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>
# Contributors:
#  xeross <contact@xeross.me>
#  codekoala <codekoala@gmail.com>

pkgname=etcd
pkgver=2.0.5
pkgrel=1
pkgdesc='A highly-available key value store for shared configuration and service discovery.'
arch=('x86_64' 'i686')
url='https://github.com/coreos/etcd'
license=('Apache License 2')
makedepends=('go')
install='etcd.install'
backup=('etc/conf.d/etcd')
provides=('etcdctl')
replaces=('etcdctl')
conflicts=('etcdctl')

source=("https://github.com/coreos/${pkgname}/archive/v${pkgver}.tar.gz"
        'etcd.service'
        'etcd.conf')

sha256sums=('ded57e8aac39c8ea7badb9785fbca564b3d768c2482c2d9c40bff83a3dc32ad8'
            'cdc3faed99fb6e31740b574e9c977a26d1d5a112515a8f899fc1b6a8a889c0ca'
            '3c5452af6bfc7a5c5a3ea203221d2181677997c7dd999807a91578c3df4835de')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./build
}

package() {
  install -Dm644 "${srcdir}/etcd.conf" "${pkgdir}/etc/conf.d/etcd"
  install -Dm644 "${srcdir}/etcd.service" "${pkgdir}/usr/lib/systemd/system/etcd.service"
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm755 "bin/etcd" "${pkgdir}/usr/bin/etcd"
  install -Dm755 "bin/etcdctl" "${pkgdir}/usr/bin/etcdctl"
  install -Dm755 "bin/etcd-dump-logs" "${pkgdir}/usr/binetcd-dump-logs"
  install -Dm755 "bin/etcd-migrate" "${pkgdir}/usr/bin/etcd-migrate"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 Documentation/*.md -t ${pkgdir}/usr/share/doc/${pkgname}
}

# vim:set ts=2 sw=2 et:
