pkgname=eclipse-subversive
pkgver=1.1.3.I20140206_1700
pkgrel=1
pkgdesc="Eclipse plug-in that provides Subversion support."
arch=('any')
url="http://www.eclipse.org/subversive/"
license=('EPL')
depends=('eclipse')
source=("http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/technology/subversive/1.1/builds/Subversive-${pkgver//_/-}.zip")
md5sums=('38a79fc512f3fa77804098b76be337b3')

package()
{
	_dest="${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse"
	cd "$srcdir"

	# Features
	find features -type f -name "*.jar" | while read feature ; do
		install -dm755 ${_dest}/${feature/.jar}
		cd ${_dest}/${feature/.jar}
		jar xf ${srcdir}/${feature} || return 1
	done

	# Plugins
	find plugins -type f -name "*.jar" -exec install -Dm644 {} ${_dest}/{} \;
}

