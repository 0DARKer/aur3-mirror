# Contributor: Gerhard Brauer <gerbra@archlinux.de>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Markus Opitz <mastero23 at gmail dot com>
# Maintainer: Milan Knížek <knizek@volny.cz>
pkgname=nxproxy
pkgver=3.5.0.9
pkgrel=2
pkgdesc="NX compression proxy"
arch=(i686 x86_64)
url="http://www.x2go.org/"
license=('GPL')
depends=("nxcomp>=${pkgver}" 'gcc-libs')
conflicts=("${pkgname}-x2go-git")
groups=('x2go' 'alts')
source=('nxcomp_Png.cpp.patch'
        "http://code.x2go.org/releases/source/nx-libs/nx-libs_${pkgver}-full.tar.gz")
md5sums=('4271a8a665b13a16d5999f743e147dd6'
         'be272307cf477f756aef702a6d4ed2ae')

build() {
  cd "$srcdir/nx-libs_${pkgver}"
  patch -p1 < ../nxcomp_Png.cpp.patch
  make || return 1
}

package() {
  cd "$srcdir/nx-libs_${pkgver}/${pkgname}"

  install -m 755 -d "$pkgdir/usr/bin/"
  install -m 755 nxproxy "$pkgdir/usr/bin/"

  install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 "../doc/changelog" "${pkgdir}/usr/share/doc/${pkgname}/changelog.DEBIAN"
  install -m 644 "../doc/README.NX-redistributed" "${pkgdir}/usr/share/doc/${pkgname}/README.NX-redistributed"
}

