# Maintainer: crazyelf5

pkgname=mundus-bzr
pkgver=84
pkgrel=1
pkgdesc="Clean your home folder from old configuration files once created by uninstalled software"
arch=('any')
url="https://launchpad.net/mundus"
license=('GPL3')
depends=('gambas3-runtime' 'gambas3-gb-image' 'gambas3-gb-qt4' 'gambas3-gb-form' 'gambas3-gb-desktop' 'gambas3-gb-settings' 'gambas3-devel' 'gambas3-gb-form-stock' 'gambas3-gb-net' 'gambas3-gb-net-curl' 'gambas3-gb-web')
makedepends=('bzr')
source=('mundus.desktop')
md5sums=('50a68f32912901c6f3715b9a0b483331')

_bzrtrunk=lp:mundus
_bzrmod=mundus

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd "$_bzrmod-build"

  gbc3 -eagtpmv
  gba3
}

package() {
  cd "$srcdir/$_bzrmod-build/"

  install -dm755 "$pkgdir"/usr/{bin,share/{applications,pixmaps}}
  install -m755 mundus-build.gambas "$pkgdir/usr/bin/mundus"
  install -m644 icon.png "$pkgdir/usr/share/pixmaps/mundus.png"
  install -m644 "$srcdir/mundus.desktop" "$pkgdir/usr/share/applications"
}

# vim:set ts=2 sw=2 et:
