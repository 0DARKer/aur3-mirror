# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168-lqx
_realname=r8168
pkgver=8.035.00
pkgrel=3
pkgdesc="A kernel module for Realtek 8168 network cards (for linux-lqx kernel)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux-lqx>=3.9' 'linux-lqx<3.10')
makedepends=('linux-lqx-headers')
source=(http://r8168.googlecode.com/files/$_realname-$pkgver.tar.bz2 linux-3.8.patch)
install=$_realname.install

_extramodules=extramodules-3.9-lqx
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
   cd "$srcdir"
   patch -p0 -i "linux-3.8.patch"
   cd "$_realname-$pkgver"
   make -C /usr/lib/modules/$_kernver/build \
      SUBDIRS="$srcdir/$_realname-$pkgver/src" \
      EXTRA_CFAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
   cd "$srcdir/$_realname-$pkgver/src"
   install -D -m644 $_realname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_realname.ko"
   find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

sha256sums=('b904d31fc9bd6c97cc34479a0eba0e3a9bba5351a43b56dd5fff6b7dc1a5df73'
            '96dd497a787ef5f5e2d56e540ed7ab85d1658a0d54af8238745623817e6e1624')
