# $Id: PKGBUILD 84041 2013-02-09 01:31:50Z foutrelis $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168-lqx
_realname=r8168
pkgver=8.035.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lqx"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux-lqx')
makedepends=('linux-headers')
source=(http://r8168.googlecode.com/files/$_realname-$pkgver.tar.bz2)
install=$_realname.install

_extramodules=extramodules-3.7-lqx
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
   cd "$srcdir/$_realname-$pkgver"
   #patch -p0 -i "$srcdir/linux33.patch"
   make -C /usr/lib/modules/$_kernver/build \
      SUBDIRS="$srcdir/$_realname-$pkgver/src" \
      EXTRA_CFAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
   cd "$srcdir/$_realname-$pkgver/src"
   install -D -m644 $_realname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_realname.ko"
   find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

sha256sums=('b904d31fc9bd6c97cc34479a0eba0e3a9bba5351a43b56dd5fff6b7dc1a5df73')
