# Maintainer: NeoRaider <neoraider@universe-factory.net>

_pkgname=libxcb
pkgname=lib32-${_pkgname}-git
pkgver=20120105
pkgrel=1
pkgdesc="X11 client-side library (32-bit, git version)"
arch=(x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto-git' 'lib32-libxdmcp' 'lib32-libxau' 'libxcb-git')
makedepends=('pkgconfig' 'libxslt' 'python' 'autoconf')
conflicts=('libx11<1.1.99.2' 'lib32-libxcb')
provides=('lib32-libxcb=1.7')
options=('!libtool')
license=('custom')
source=(libxcb-1.1-no-pthread-stubs.patch)
sha1sums=('3455e84642283bc91c8313af319002a20bbcbdf4')

_gitroot="git://git.freedesktop.org/git/xcb/libxcb"
_gitname="libxcb"

build() {
  cd "${srcdir}"

  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/build"
  cd "${srcdir}/build"

  patch -Np1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  ./autogen.sh --prefix=/usr --enable-xinput --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s "${_pkgname}-git" "$pkgdir/usr/share/licenses/$pkgname" 
}
