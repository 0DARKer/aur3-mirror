# Maintainer: NeoRaider <neoraider@universe-factory.net>

_pkgname=libxcb
pkgname=lib32-${_pkgname}-git
pkgver=1.9.r38.ge8663a9
pkgrel=1
pkgdesc="X11 client-side library (32-bit, git version)"
arch=(x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto-git' 'lib32-libxdmcp' 'lib32-libxau' 'libxcb-git')
makedepends=('pkgconfig' 'libxslt' 'python' 'autoconf')
conflicts=('libx11<1.1.99.2' 'lib32-libxcb')
provides=('lib32-libxcb=1.7')
options=('!libtool')
license=('custom')
source=('git+http://anongit.freedesktop.org/git/xcb/libxcb.git'
		'libxcb-1.1-no-pthread-stubs.patch')
sha512sums=('SKIP'
            '1181ac37d3de565321485001cd991fe94f0c92540c17940deaa5a6e3891292cdcbb510588b36aca7ceb5cdcdedd26e0ccc53797e773680514027faae2cd4b19f')
_gitname="libxcb"

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/$_gitname"
  patch -Np1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  ./autogen.sh --prefix=/usr --enable-xinput --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/$_gitname"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s "${_pkgname}-git" "$pkgdir/usr/share/licenses/$pkgname" 
}
# vim:set ts=2 sw=2 bs=2:
