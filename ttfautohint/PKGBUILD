# Maintainer: Arthur Titeica <arthur.titeica [at] gmail.com>

pkgname=ttfautohint
pkgver=0.95
pkgrel=1
pkgdesc="Provides automated hinting process for web fonts"
arch=('i686' 'x86_64')
url="http://www.freetype.org/ttfautohint/"
license=('GPL')
groups=()
depends=('freetype2')
makedepends=('qt4')
optdepends=('texlive-bin: generate docs'
            'pandoc: generate docs'
            'ghc: pandoc filter'
            'qt4: ttfautohintGUI')
source=(https://downloads.sourceforge.net/project/freetype/ttfautohint/$pkgver/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('b6c38df621bf66ea2838a4ec3dc14a3b'
         'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export QMAKE='/usr/bin/qmake-qt4'
  export MOC='/usr/bin/moc-qt4'
  export UIC='/usr/bin/uic-qt4'
  export RCC='/usr/bin/rcc-qt4'
  # export QTDIR='/usr/lib/qt4'
  CFLAGS="-I/usr/include/qt4" ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
