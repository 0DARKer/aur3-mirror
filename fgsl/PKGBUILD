# Maintainer: aphirst <adam@aphirst.karoo.co.uk>

pkgname=fgsl
pkgver=0.9.4
pkgrel=1
pkgdesc="A Fortran interface to the GNU Scientific Library"
arch=('i686' 'x86_64')
url="http://www.lrz.de/services/software/mathematik/gsl/fortran/"
license=('GPL')
depends=("gsl>=1.15")
source=("http://www.lrz.de/services/software/mathematik/gsl/fortran/$pkgname-$pkgver.tar.gz")
md5sums=('cee6760809bc1ddbb6172fc8818a76b2')

case $CARCH in
    i686)
        _bits=32;;
    x86_64)
        _bits=64;;
esac

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --bits $_bits --prefix /usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir"/usr install
}

# vim:set ts=2 sw=2 et:
