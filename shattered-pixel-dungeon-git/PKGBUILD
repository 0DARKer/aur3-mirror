# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=pixel-dungeon
pkgname=shattered-${_pkgname}-git
_pkgver=0.2.1c
pkgver=0.2.1c.116
pkgrel=1
pkgdesc="Shattered fork of the popular rogue-like game"
url="http://shatteredpixel.tumblr.com/"
license=('GPL3')
depends=('java-environment' 'bash')
makedepends=('git')
arch=('any')
source=("${_pkgname}::git+https://github.com/prurigro/${_pkgname}-gdx.git#branch=shattered-pixel-${_pkgver}"
        "shattered-${_pkgname}.sh"
        "shattered-${_pkgname}.desktop"
        "no-encryption.patch")
sha512sums=('SKIP'
            '88814d1f33eea6bd5656d3ca731ed5a6cfce10ecdae24012252c5b32c4b194ec75fb0e22cac70897802679086c6a32e210d52933ec45ca94ff350ac4ad7c266e'
            '204a7bcedbbc14bdad6586e4b759b326191a7fd2c344dadc7032495d4caa5fe32edac4118d7294229a6fe24f6684416fff37e260bbc9dde9e50846a03ba77db8'
            '93293f1051cdb5860f22c4c952019f04b0ec41e873f9313bd1d0a15a90defce360779ff75d239d0ceb07ce1a2320b2234473ba9cd95f6269e1568bf173b2b0f9')

pkgver() {
    cd $_pkgname
    echo -n "${_pkgver}.$(git rev-list --count HEAD)"
}

prepare() {
    cd $_pkgname
    patch -p1 < ../no-encryption.patch
}

build() {
    cd $_pkgname
    unset _JAVA_OPTIONS
    GRADLE_USER_HOME="$srcdir" ./gradlew desktop:dist
}

package() {
    install -Dm755 shattered-${_pkgname}.sh "$pkgdir"/usr/bin/shattered-${_pkgname}
    install -Dm644 shattered-${_pkgname}.desktop "$pkgdir"/usr/share/applications/shattered-${_pkgname}.desktop
    install -Dm644 ${_pkgname}/android/res/drawable-xxxhdpi/ic_launcher.png "$pkgdir"/usr/share/pixmaps/shattered-${_pkgname}.png
    install -Dm644 ${_pkgname}/desktop/build/libs/desktop-*.jar "$pkgdir"/usr/share/shattered-${_pkgname}/shattered-${_pkgname}.jar
}
