# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Anonymo <meowdib at gmail dot com>

pkgname=clarity-icon-theme
pkgver=0.3.1
pkgrel=1
pkgdesc="Vector icon theme for GTK. It can be used with XFCE or GNOME"
arch=('any')
makedepends=('imagemagick')
url="http://gnome-look.org/content/show.php/Clarity?content=135654"
license=('CCPL:cc-by-3.0')
options=(!strip)
source=(http://www.fileden.com/files/2011/2/28/3088999/${pkgname}_${pkgver}.tar.gz)
md5sums=('0f4ed424de425c1fd96192dccfe72eea')

  #################################################################
  #  Put the themes you want to build in _buildtheme array below  #
  #                                                               #
  # Available themes:                                             #
  #  violaceus      - violet/pink gradient                        #
  #  lux_violaceus  - glossy variant                              #
  #  canus          - grey gradient                               #
  #  dark_canus     - dark grey gradient                          #
  #  caeruleus      - blue gradient                               #
  #  lux_caeruleus  - glossy variant                              #
  #  viridis        - green gradient                              #
  #  luteus         - orange gradient                             #
  #################################################################

_buildtheme=(violaceus lux_violaceus canus dark_canus caeruleus lux_caeruleus viridis luteus)

build() {
for _theme in ${_buildtheme[*]}; do
  cd ${srcdir}/${pkgname}_${pkgver}
  make ${_theme}
  install -d themes/${_theme}
  cp -r {scalable,16x16,index.theme} themes/${_theme}
  sed -i s/Name=Clarity/Name=Clarity-${_theme}/ themes/${_theme}/index.theme
done
}

package() {
for _theme in ${_buildtheme[*]}; do
  
  cd ${srcdir}/${pkgname}_${pkgver}/themes/${_theme}
  install -d ${pkgdir}/usr/share/icons/Clarity-${_theme}
  cp -r {scalable,16x16,index.theme} ${pkgdir}/usr/share/icons/Clarity-${_theme}

  cd ${pkgdir}/usr/share/icons/Clarity-${_theme}

  #install archlinux icons
  ln -sf ../distributor-logos/arch.svg scalable/places/start-here.svg
  ln -sf ../distributor-logos/arch.svg scalable/places/gnome-main-menu.svg
  ln -sf ../distributor-logos/arch.svg scalable/places/distributor-logo.svg
  ln -sf ../distributor-logos/arch.png 16x16/places/start-here.png
  ln -sf ../distributor-logos/arch.png 16x16/places/gnome-main-menu.png
  ln -sf ../distributor-logos/arch.png 16x16/places/distributor-logo.png

  #symlink adobe-flash to the right place
  ln -s adobe-flashplayer.svg scalable/apps/flash-player-properties.svg
  ln -s adobe-flashplayer.png 16x16/apps/flash-player-properties.png

  #symlink email-icon to the right place
  ln -s email.svg scalable/apps/internet-mail.svg
  ln -s email.png 16x16/apps/internet-mail.png

  #create icon for qlandkartegt
  ln -s google-earth.svg scalable/apps/qlandkartegt.svg
  ln -s google-earth.png 16x16/apps/qlandkartegt.png

  #create icon for wicd
  ln -s ../devices/network-wireless.svg scalable/apps/wicd-gtk.svg
  ln -s ../devices/network-wireless.png 16x16/apps/wicd-gtk.png

  #create icon for camorama
  ln -s ../devices/camera-web.svg scalable/apps/camorama.svg
  ln -s ../devices/camera-web.png 16x16/apps/camorama.png

  #create icon for galculator
  ln -s calc.svg scalable/apps/galculator.svg
  ln -s calc.png 16x16/apps/galculator.png

  #create icon for dconf-editor
  ln -s ../categories/package_settings.svg scalable/apps/dconf-editor.svg
  ln -s ../categories/package_settings.png 16x16/apps/dconf-editor.png

done
}