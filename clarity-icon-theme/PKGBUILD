# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Anonymo <meowdib at gmail dot com>

pkgname=clarity-icon-theme
pkgver=0.3.2
pkgrel=1
pkgdesc="Vector icons in 7 colourthemes for GTK."
arch=('any')
makedepends=('imagemagick')
url="http://gnome-look.org/content/show.php/Clarity?content=135654"
license=('GPL3')
options=(!strip)
source=(http://www.fileden.com/files/2011/2/28/3088999/${pkgname}_${pkgver}.tar.gz)
md5sums=('e0f25fe13e1108a73bb2b6605d0749e3')

  #################################################################
  #  Put the themes you want to build in _buildtheme array below  #
  #                                                               #
  # Available themes:                                             #
  #  violaceus      - violet/pink gradient                        #
  #  lux_violaceus  - glossy variant                              #
  #  canus          - grey gradient                               #
  #  dark_canus     - dark grey gradient                          #
  #  caeruleus      - blue gradient                               #
  #  lux_caeruleus  - glossy variant                              #
  #  viridis        - green gradient                              #
  #  luteus         - orange gradient                             #
  #################################################################
  #
  #
#####
 ###
  #
_buildtheme=(violaceus lux_violaceus canus dark_canus caeruleus lux_caeruleus viridis luteus)

build() {
for _theme in ${_buildtheme[*]}; do
  cd "${srcdir}/${pkgname}_${pkgver}"
  make ${_theme}
  install -d themes/${_theme}
  cp -r {scalable,16x16,index.theme} themes/${_theme}
  sed -i s/Name=Clarity/Name=Clarity-${_theme}/ themes/${_theme}/index.theme
done
}

package() {
for _theme in ${_buildtheme[*]}; do
  cd "${srcdir}/${pkgname}_${pkgver}/themes/${_theme}"
  install -d ${pkgdir}/usr/share/icons/clarity-${_theme}
  cp -r {scalable,16x16,index.theme} ${pkgdir}/usr/share/icons/clarity-${_theme}

  #install archlinux icons
  cd "${pkgdir}/usr/share/icons/clarity-${_theme}"
  ln -sf ../distributor-logos/arch.svg scalable/places/start-here.svg
  ln -sf ../distributor-logos/arch.svg scalable/places/gnome-main-menu.svg
  ln -sf ../distributor-logos/arch.svg scalable/places/distributor-logo.svg
  ln -sf ../distributor-logos/arch.png 16x16/places/start-here.png
  ln -sf ../distributor-logos/arch.png 16x16/places/gnome-main-menu.png
  ln -sf ../distributor-logos/arch.png 16x16/places/distributor-logo.png

  #set permissions
  cd "${srcdir}/${pkgname}_${pkgver}/themes/${_theme}"
  find -type f -or -type l -exec chmod 644 "{}" \;
done
}