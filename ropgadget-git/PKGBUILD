# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=ropgadget-git
_pkgname=ROPgadget
pkgver=5.3.331.41539ce
pkgrel=2
pkgdesc="Search gadgets in binaries to facilitate ROP exploitation for several file formats and architectures"
url="http://www.shell-storm.org/project/ROPgadget"
arch=('any')
license=('GPL2')
depends=('python2-capstone')
makedepends=('git' 'python2-setuptools')
provides=('ropgadget')
conflicts=('ropgadget')
source=(git+https://github.com/JonathanSalwan/ROPgadget)
sha512sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  _ropgadgetver=$(grep ^version setup.py|cut -d\" -f2)
  printf "%s.%s.%s" "${_ropgadgetver}" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

check() {
  cd ${_pkgname}
  ./ROPgadget.py --binary ./test-suite-binaries/elf-Linux-x86 --string "main"|grep main
}

package() {
  cd ${_pkgname}
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
}

# vim:set ts=2 sw=2 et:
