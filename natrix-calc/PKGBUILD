# Maintainer: Mijo Medvedec <mijo dot medvedec at gmail dot com>

pkgname=natrix-calc
pkgver=2.1
pkgrel=3
pkgdesc="Generic IP calculator with ability to calculate parameters of IPv4/IPv6 networks"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/natrix/"
license=('GPL3')
depends=('qt4')
makedepends=('qt4' 'gendesk')
provides=('natrix-calc')
source=("http://sourceforge.net/projects/natrix/files/${pkgver}/${pkgname}_${pkgver}-${pkgrel}.zip")
md5sums=('926051195e0da96d5d044720e1949ef8')
sha256sums=('8b592bb0f102962d207b1b9bb9380c610e8a1bc7c7275a9ec072bdee084ed30c')

prepare() {
	gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc"
}

build() {
	cd "$srcdir/$pkgname"
	qmake-qt4 Natrix.pro
	make
}

package() {
	cd "$srcdir/$pkgname"
	install -Dm 755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

	install -dm 755 "$pkgdir/usr/share/$pkgname/locale"
	install -Dm 644 *.qm "$pkgdir/usr/share/$pkgname/locale"

	install -Dm 644 "$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
	install -Dm 644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

	sed -i 's/Natrix-calc/Natrix/g' "$pkgdir/usr/share/applications/$pkgname.desktop"
	sed -i 's/Office;/Network;Internet;Utility;KDE;/g' "$pkgdir/usr/share/applications/$pkgname.desktop"
}
