# Contributor: Sergio Correia <sergio@correia.cc>
# Contributor: Joe Davison <joedavison.davison@gmail.com>

pkgname='qemu-rpi-git'
pkgver=20130318
pkgrel=1
pkgdesc='Processor emulator and virtual machine with with expansive multi-arch support and fast operation (with an accelerator), git version with Raspberry Pi emulation.'
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2')
url='http://wiki.qemu.org/Index.html'
makedepends=('git' 'texi2html' 'perl')
depends=('brltty' 'gnutls' 'libpulse' 'bluez' 'vde2' 'curl' 'sdl' 'spice' 'libcap-ng')
conflicts=('qemu' 'qemu-linaro' 'kvm' 'kvm-git' 'seabios' 'libcacard')
provides=('qemu')
options=('!strip')

source=('65-kvm.rules'
        'qemu.install'
        'baum_h_build_fix.patch')

md5sums=('b316a066d2f1bb57d8f5b7ea1d0d1caf'
         '01c35861fe8ca651b4e202280cbac9cf'
         'f3393228cda2d83e2246c782c31c5d77')

install='qemu.install'


_gitroot='git://github.com/Torlus/qemu.git'
_gitname='qemu-rpi'
_gitbranch='rpi'

build() {
    if [ ! -d "${_gitname}" ]; then
        git clone -b "${_gitbranch}" --depth 1 "${_gitroot}" "${_gitname}" && cd "${_gitname}"
    else
        cd "${_gitname}" && git reset --hard && git pull origin && git clean -dfx
    fi

    msg "GIT checkout done."

    patch -uNp1 -i ../baum_h_build_fix.patch

    ./configure --prefix=/usr --sysconfdir=/etc --audio-drv-list=alsa,sdl,pa \
              --python=/usr/bin/python2 \
              --audio-card-list=ac97,sb16,es1370,hda \
              --enable-docs --enable-mixemu --libexecdir=/usr/lib/qemu --disable-werror
    make
}

package() {
    cd "${_gitname}"

    make DESTDIR="${pkgdir}" libexecdir="/usr/lib/qemu" install

    install -D -m644 "${srcdir}/65-kvm.rules" "${pkgdir}/usr/lib/udev/rules.d/65-kvm.rules"

    # bridge_helper needs suid
    # https://bugs.archlinux.org/task/32565
    chmod u+s "${pkgdir}/usr/lib/qemu/qemu-bridge-helper"

    # strip scripts directory
    find "${pkgdir}/usr/src/linux-${_kernver}/scripts"  -type f -perm -u+w 2>/dev/null | while read binary ; do
        case "$(file -bi "$binary")" in
            *application/x-executable*) # Binaries
            /usr/bin/strip $STRIP_BINARIES "$binary";;
        esac
    done
}
