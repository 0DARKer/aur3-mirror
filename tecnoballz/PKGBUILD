#Maintainer: Simone Sclavi 'Ito' darkhado@gmail.com

pkgname=tecnoballz
pkgver=0.92
pkgrel=3
_patch=3
arch=('i686' 'x86_64')
pkgdesc="An exciting brick breaker game with Debian's patches"
url="http://linux.tlk.fr/games/TecnoballZ/"
if [ $CARCH = "i686" ]; then
    depends=('sdl_image' 'sdl_mixer' )
else
    depends=('lib32-sdl_image' 'lib32-sdl_mixer')
    makedepends=('gcc-multilib');fi
license=('GPL3')
backup=('var/games/tecnoballz/tecnoballz.hi')
source=(http://ftp.de.debian.org/debian/pool/main/t/tecnoballz/tecnoballz_${pkgver}.orig.tar.gz
http://ftp.de.debian.org/debian/pool/main/t/tecnoballz/tecnoballz_${pkgver}-${_patch}.diff.gz)

md5sums=('111022212bc77b7dfcb453eaa5eac751'
         'b88348a8c80e20ba9c06cc70b9aa2916')
install=tecnoballz.install
build() {
    if [ $CARCH = "x86_64" ]; then
        export CC='gcc -m32'
        export CXX="g++ -m32"
        export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
    fi          
    patch -Np1 -i ${pkgname}_${pkgver}-${_patch}.diff
    cd $pkgname-$pkgver
    patch -Np1 -i ../debian/patches/010_scorefile_path.diff 
    patch -Np1 -i ../debian/patches/011_gcc-4.3-fixes.diff 
    patch -Np1 -i ../debian/patches/020_level_data.diff 
    patch -Np1 -i ../debian/patches/030_texts_dir.diff  
    ## Using -O3 is not recommended for gcc 4.x !! 
    sed -i 's/CXXFLAGS=" -O3 -Wall"/CXXFLAGS=" -O2 -Wall"/' configure
    ./configure --prefix=/usr
    sed -i 's/supervisor.c/supervisor.cc/' src/Makefile.in
    make 
}
package(){
    cd $pkgname-$pkgver
    make DESTDIR=$pkgdir install
    mkdir -p $pkgdir/usr/bin && mv $pkgdir/usr/games/$pkgname $pkgdir/usr/bin
    chown root:root $pkgdir/usr/bin/$pkgname
    chmod 755 $pkgdir/usr/bin/$pkgname
    rm -rf $pkgdir/usr/games
    mkdir $pkgdir/usr/share/$pkgname/texts
    cd src/TecnoballZ
    install -m644 texts/* $pkgdir/usr/share/$pkgname/texts
    install -m644 levels-data.xml $pkgdir/usr/share/$pkgname/
    chown root:games $pkgdir/var/games/$pkgname
    chmod -R 775 $pkgdir/var/games/

    cd $srcdir/debian
    mkdir -p $pkgdir/usr/share/{applications,pixmaps} 
    install -m644 tecnoballz.xpm $pkgdir/usr/share/pixmaps 
    install -m644 tecnoballz.desktop $pkgdir/usr/share/applications 
}

