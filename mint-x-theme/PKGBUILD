# Maintainer: Carl George <carl@carlgeorge.us>
# Special thanks to the Linux Mint project, without them this would not exist.

pkgname=mint-x-theme
_name=mint-themes
pkgver=1.2.8
_gtk3ver=3.8-9
pkgrel=2
pkgdesc='A Linux Mint theme, based on Clearlooks Revamp, Elementary, and Faenza.  Includes GTK2, GTK3, Metacity, and Xfce components.'
arch=('any')
url='http://packages.linuxmint.com'
license=('GPL3')
conflicts=('mint-themes')
source=("${url}/pool/main/m/${_name}/${_name}_${pkgver}.tar.gz"
        "${url}/pool/main/m/${_name}-gtk3/${_name}-gtk3_${_gtk3ver}.tar.gz")
sha256sums=('a47e3db4ee460e29c4e3c52748d0a265ae6986794ca6b9c03578d7f79dcc916f'
            '889bfe6ca9d0c46cddafe453b7f063f32572ead43daabb6e67147c20101d352c')

prepare() {
    find ${srcdir} -name .gitkeep -type f -delete
    find ${srcdir} -type d ! -perm 755 -exec chmod 755 {} +
    find ${srcdir} -type f ! -perm 644 -exec chmod 644 {} +
}

package() {
    cp -dr --no-preserve=ownership ${srcdir}/${_name}/usr ${pkgdir}/
    cp -dr --no-preserve=ownership ${srcdir}/${_name}-gtk3/usr ${pkgdir}/
}
