# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jon Sturm <Jasturm002@aol.com>

_pkgname=gfm
pkgname=$_pkgname-svn
pkgver=1.07.4505
pkgrel=1
pkgdesc="Group File Manager for TI graphing calculator Group Files - .8*g/.tig (svn version)"
arch=(i686 x86_64)
url=http://tilp.info/
license=(GPL2)
depends=(libglade libticalcs)
makedepends=(subversion)
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
options=(!libtool)
install=$pkgname.install
source=($pkgname::svn+http://svn.tilp.info/repos/tilp/$_pkgname/trunk)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    echo $(grep -o 'version [0-9.]\+' -m1 $pkgname/ChangeLog | tr -d '[a-zA-Z ]').$(svnversion "$SRCDEST"/$pkgname/)
}

prepare() {
    cd $pkgname/
    sed -i 's:.*serial.*::g' acinclude.m4
    echo '# serial 1' > acinclude.m4.new
    cat acinclude.m4 >> acinclude.m4.new
    mv acinclude.m4{.new,}
}

build() {
    cd $pkgname/
    autoreconf -fi
    #KDE users can remove the --without-kde option to enable kde file dialogs
    ./configure --prefix=/usr --without-kde
    make
}

package() {
    make -C $pkgname DESTDIR="$pkgdir" install
}
