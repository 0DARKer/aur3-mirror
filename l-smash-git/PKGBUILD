# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=l-smash-git
pkgver=rev799.5d62cb9
pkgrel=1
pkgdesc="MP4 muxer and other tools (GIT Version)"
arch=('i686' 'x86_64')
license=('custom')
depends=('glibc')
makedepends=('git')
provides=('lsmash')
conflicts=('lsmash')
url="http://code.google.com/p/l-smash/"
source=('git+https://code.google.com/p/l-smash/')
md5sums=('SKIP')
_gitname="l-smash"

pkgver() {
  cd "${_gitname}"
  echo "rev$(git rev-list HEAD 2> /dev/null | wc -l).$(git describe --always 2> /dev/null)"
}

build() {
  cd "${_gitname}"
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
