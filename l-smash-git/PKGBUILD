# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=l-smash-git
pkgver=v2.3.0.109.gc41f1ff
pkgrel=1
pkgdesc="MP4 muxer and other tools (GIT Version)"
arch=('i686' 'x86_64')
license=('custom:l-smash')
depends=('glibc')
makedepends=('git')
provides=('lsmash')
conflicts=('lsmash')
url="http://l-smash.github.io/l-smash/"
source=('git+https://github.com/l-smash/l-smash')
md5sums=('SKIP')
_gitname="l-smash"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
