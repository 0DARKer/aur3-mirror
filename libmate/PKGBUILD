pkgname=libmate
pkgver=2011.12.16
pkgrel=1
pkgdesc="Common libraries for MATE"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ttf-freefont' 'mate-conf')
makedepends=('mate-doc-utils' 'mate-common' 'mate-corba' 'mate-conf'
             'libmatecomponent' 'mate-mime-data' 'gvfs' 'mate-vfs') #  'libcanberra'
options=('!emptydirs' '!libtool')
url="http://matsusoft.com.ar/projects/mate/"
groups=('mate')
install=libmate.install

# mirrors
# http://cptl.org/mate/${pkgname}.tar.gz
# http://hax0r.se/mate/${pkgname}.tar.gz
# http://matsusoft.com.ar/uploads/gnu-linux/mate/${pkgname}.tar.gz
# http://matsusoft.com.ar/projects/mate/source/${pkgname}.tar.gz

source=(http://matsusoft.com.ar/projects/mate/files/${pkgname}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('41e7bcfdc0af4bcb30d8d1508211a0f8f7c0c763a95a57d622047adf3ee618e4')

build() {
    cd "${srcdir}"

    # Optional Features:
    #    --enable-esd            Enable ESounD (default=no)
    #    --enable-canberra       Enable canberra (default=yes)
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/libmate \
        --disable-canberra \
        --disable-schemas-install || return 1

    make || return 1
}

package() {
    cd "${srcdir}"

    make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1  DESTDIR="${pkgdir}" install || return 1

    install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
    mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
