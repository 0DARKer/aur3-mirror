# Maintainer: Mitsu <archlinux AT suumitsu DOT eu>
# Based on PKGBUILD from darkcoin-git maintained by Viliam Kubis <viliam dot kubis at gmail dot com>

pkgname='darkcoin'
pkgver=0.9.12.14
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.darkcoin.io/"
depends=('qt4>=4.6' 'boost-libs>=1.46' 'miniupnpc>=1.6' 'qrencode>=3.4.3')
makedepends=('boost' 'automoc4' 'qrencode' 'miniupnpc')
license=('MIT')
pkgdesc="Privacy-Centric Cryptographic Currency with fully encrypted transactions and anonymous block transactions. (includes both the qtclient and the headless daemon)"
provides=(darkcoin)
conflicts=(darkcoin-git)
source=("https://github.com/darkcoinproject/darkcoin/archive/v$pkgver.tar.gz")
sha256sums=('be374f3b9b06f53c692c2f127e1aead2394eafbc3bad53b43fe87b63a52666d2')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	# make qt gui
	qmake-qt4 USE_QRCODE=1 USE_UPNP=1
	make -j $(nproc)

	# make darkcoin daemon
	cd "$srcdir/$pkgname-$pkgver/src"
	make $MAKEFLAGS -j $(nproc) -f makefile.unix
}


package() {
	install -D -m755 "$srcdir/$pkgname-$pkgver/darkcoin-qt" "$pkgdir/usr/bin/darkcoin-qt"
	install -D -m755 "$srcdir/$pkgname-$pkgver/src/darkcoind" "$pkgdir/usr/bin/darkcoind"
	install -D -m644 "$srcdir/$pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# Prepared for when "master" gets merged
	#install -D -m644 "$srcdir/$pkgname-$pkgver/share/pixmaps/darkcoin128.png" "$pkgdir/usr/share/pixmaps/darkcoin128.png"
	#install -D -m644 "$srcdir/$pkgname-$pkgver/contrib/debian/darkcoin-qt.desktop" "$pkgdir/usr/share/applications/darkcoin.desktop"
}

