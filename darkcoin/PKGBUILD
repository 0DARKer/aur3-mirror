# Maintainer: Mitsu <archlinux AT suumitsu DOT eu>
# Based on PKGBUILD from darkcoin-git maintained by Viliam Kubis <viliam dot kubis at gmail dot com>

pkgname='darkcoin'
pkgver=0.9.12.0
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.darkcoin.io/"
depends=('qt4>=4.6' 'boost-libs>=1.46' 'miniupnpc>=1.6' 'qrencode>=3.4.3')
makedepends=('boost' 'automoc4' 'qrencode' 'miniupnpc')
license=('MIT')
pkgdesc="Privacy-Centric Cryptographic Currency with fully encrypted transactions and anonymous block transactions. (includes both the qtclient and the headless daemon)"
provides=(darkcoin)
conflicts=(darkcoin-git)
source=("https://github.com/darkcoinproject/darkcoin/archive/v$pkgver.tar.gz")
sha256sums=('3ea54eb1f0eab6bfac842ca8a22e68682447e20adf93a5024d12d20dfdd6f010')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	# make qt gui
	qmake-qt4 USE_QRCODE=1 USE_UPNP=1
	make

	# make darkcoin daemon
	cd "$srcdir/$pkgname-$pkgver/src"
	make $MAKEFLAGS -f makefile.unix
}


package() {
	# install darkcoin-qt client
	install -D -m755 "$srcdir/$pkgname-$pkgver/darkcoin-qt" "$pkgdir/usr/bin/darkcoin-qt"

	# install darkcoin daemon
	install -D -m755 "$srcdir/$pkgname-$pkgver/src/darkcoind" "$pkgdir/usr/bin/darkcoind"

	# install license
	install -D -m644 "$srcdir/$pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

