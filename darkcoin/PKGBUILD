# Maintainer: Mitsu <archlinux AT suumitsu DOT eu>
# Based on PKGBUILD from darkcoin-git maintained by Viliam Kubis <viliam dot kubis at gmail dot com>

pkgname='darkcoin'
pkgver=0.10.16.16
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.darkcoin.io/"
depends=('qt4>=4.6' 'boost-libs>=1.46' 'miniupnpc>=1.6' 'qrencode>=3.4.3')
makedepends=('boost' 'automoc4' 'qrencode' 'miniupnpc')
license=('MIT')
pkgdesc="Privacy-Centric Cryptographic Currency with fully encrypted transactions and anonymous block transactions. (includes both the qtclient and the headless daemon)"
provides=(darkcoin)
conflicts=('darkcoin-git' 'darkcoin-bin-rc')
source=("https://github.com/darkcoinproject/darkcoin/archive/v$pkgver.tar.gz")
sha512sums=('81e64363490abea71eb39d6545abe38fc182df39cc329a935e033fab315242261eacba46cd4dc30dbfa02e53b92846a3c97b25314b359648d3faf5cf7c09fba2')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	# make qt gui
	qmake-qt4 USE_QRCODE=1 USE_UPNP=1
	make -j $(nproc)

	# make darkcoin daemon
	cd "$srcdir/$pkgname-$pkgver/src"
	make $MAKEFLAGS -j $(nproc) -f makefile.unix
}


package() {
	install -D -m755 "$srcdir/$pkgname-$pkgver/darkcoin-qt" "$pkgdir/usr/bin/darkcoin-qt"
	install -D -m755 "$srcdir/$pkgname-$pkgver/src/darkcoind" "$pkgdir/usr/bin/darkcoind"
	install -D -m644 "$srcdir/$pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D -m644 "$srcdir/$pkgname-$pkgver/share/pixmaps/darkcoin128.png" "$pkgdir/usr/share/pixmaps/darkcoin128.png"
	install -D -m644 "$srcdir/$pkgname-$pkgver/contrib/debian/darkcoin-qt.desktop" "$pkgdir/usr/share/applications/darkcoin.desktop"
}

