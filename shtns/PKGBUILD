pkgname=shtns
pkgver=491.3b0dc0859241
pkgrel=2
pkgdesc="high performance library for Spherical Harmonic Transform"
arch=(i686 x86_64)
url="https://bitbucket.org/nschaeff/shtns"
license=('CeCILL')
depends=('python-numpy' 'fftw')
makedepends=('mercurial')
source=("$pkgname::hg+https://bitbucket.org/nschaeff/shtns")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd "$srcdir"
  cp -a ${pkgname}{,-py}

  cd "$srcdir"/$pkgname
  ./configure --prefix=/usr --enable-openmp

  sed -i "s/CFLAGS=/CFLAGS=$CFLAGS -fPIC /g" Makefile

  make
  gcc -shared -o libshtns_omp.so  -Wl,--whole-archive libshtns_omp.a -Wl,--no-whole-archive

  cd "$srcdir"/${pkgname}-py
  ./configure --prefix=/usr --enable-openmp --enable-python

  sed -i "s/CFLAGS=/CFLAGS=$CFLAGS -fPIC /g" Makefile
  sed -i "s|setup.py install|setup.py install --root ${pkgdir} --optimize 1|g" Makefile

  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="${pkgdir}" PREFIX="${pkgdir}"/usr install
  install "$srcdir/$pkgname"/libshtns_omp.so "${pkgdir}"/usr/lib

  cd "$srcdir/$pkgname-py"
  make DESTDIR="${pkgdir}" install
}
