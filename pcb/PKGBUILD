# Contributor: Jared Casper <jaredcasper@gmail.com>
pkgname=pcb
pkgver=20100929
pkgrel=1
pkgdesc="An interactive printed circuit board editor for the X11 window system."
url="http://pcb.gpleda.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('m4' 'gtk2' 'flex' 'bison' 'gd' 'perlxml')
optdepends=('tk: for the graphical QFP footprint builder')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/pcb/$pkgname-$pkgver.tar.gz)
md5sums=('4c71f5d1c40ad65539957748b88eb863')

build() {
  cd $srcdir/$pkgname-$pkgver/
  if [ ! -x /usr/bin/wish ]; then
     config="env WISH=/usr/bin/true ./configure --enable-dbus"
  else
     config="./configure --enable-dbus"
  fi
  $config --prefix=/usr \
          --disable-update-mime-database \
          --disable-update-desktop-database
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/
  make -j1 prefix=$pkgdir/usr install
  rm $pkgdir/usr/share/info/dir
}

