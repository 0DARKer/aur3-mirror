pkgname=('i3ipc-glib-git')
_srcname='i3ipc-glib'
pkgver=r132.dda10b3
pkgrel=1
pkgdesc='A C interface library to i3wm'
arch=('i686' 'x86_64')
url='https://github.com/acrisci/i3ipc-glib'
license=('GPL3')

depends=('glib2' 'json-glib')
optdepends=('i3ipc-lua-git: Lua language bindings'
            'i3ipc-python-git: Python language bindings'
            'i3ipc-gjs-git: JavaScript language bindings')
makedepends=('libxcb' 'xcb-proto' 'gobject-introspection' 'gtk-doc')
provides=('i3ipc-glib')
conflicts=('i3ipc-glib')

source=("$_srcname::git+https://github.com/acrisci/i3ipc-glib.git")
sha256sums=('SKIP')

pkgver() {
	cd "$_srcname"
	printf 'r%s.%s\n' "$(git rev-list HEAD | wc -l)" "$(git describe --always)"
}

build() {
	cd "$_srcname"
	./autogen.sh --prefix=/usr
	make
}

package() {
	cd "$_srcname"
	make DESTDIR="$pkgdir" install
}
