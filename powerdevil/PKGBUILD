# Contributor: Antonio Rojas

pkgname=powerdevil
pkgver=5.0.1
pkgrel=1
pkgdesc='KDE power management tools'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/base/powerdevil'
license=('LGPL')
depends=('plasma-workspace')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('1757857cb6ebe6d96ebd1f5c35e2f743')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# move conflicting policy file to /opt
  mv "$pkgdir"/usr/share/polkit-1 "$pkgdir"/opt/kf5/share
  rm -fr "$pkgdir"/usr
}
