# Maintainer: Yngve Inntjore Levinsen <yngve.inntjore.levinsen@cern.ch>

pkgname=placet-svn
pkgver=2436
pkgrel=1
pkgdesc="Accelerator Optics simulation code, svn development version"
url="http://project-placet.web.cern.ch/project-placet/"
license=("gpl2")
depends=('gcc-libs' 'tcl' 'octave' 'python' 'openmpi' 'gsl')
#conflicts=("htgen")
provides=("placet")
makedepends=('subversion')
arch=('x86_64')

source=('svn+http://svnweb.cern.ch/guest/clicsw/trunk/placet')
md5sums=('SKIP')

pkgver() {
  cd "$SRCDEST/placet" &&  svnversion
}
build() {
    cd placet
    ./configure \
        --enable-octave \
        --enable-python \
        --enable-mpi \
        --with-python=python2 \
        --prefix=/usr

    make
}

package() {
    cd placet
    make DESTDIR=${pkgdir} install

    install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/license.txt

    # Prefer the binary to have different name:
    cd ${pkgdir}/usr/bin/
    mv placet placet-r${pkgver}
    ln -s placet-r${pkgver} placet
}
