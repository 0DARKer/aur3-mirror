# Maintainer: Armin K. <krejzi at email dot com>

pkgname=broadcom-sta-dkms
pkgver=5.100.82.112
_pkgdlver=5_100_82_112
pkgrel=6
pkgdesc="Broadcom hybrid wireless device driver for Linux"
arch=(i686 x86_64)
url="http://www.broadcom.com/support/802.11/linux_sta.php"
license=('custom')
depends=('linux-headers' 'dkms')
provides=('broadcom-wl' 'dkms-broadcom-wl')
conflicts=('broadcom-wl' 'dkms-broadcom-wl')
source=(http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_x86_32-v${_pkgdlver}.tar.gz
        http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_x86_64-v${_pkgdlver}.tar.gz
        distro.patch
        fixes.patch
        broadcom-sta.conf
        dkms.conf)
install=broadcom-sta.install
noextract=("hybrid-portsrc_x86_32-v$_pkgdlver.tar.gz"
           "hybrid-portsrc_x86_64-v$_pkgdlver.tar.gz")
md5sums=('62d04d148b99f993ef575a71332593a9'
         '310d7ce233a9a352fbe62c451b2ea309'
         '5ea63d8c69e891c8fff56a25a898c3ab'
         '246b2a67a5592293104615f5e2bacbb7'
         'e907423f49ff68bdfcba236c03585d6b'
         'b158a6b734e885015200fc1fc8a121c4')

prepare() {
  if [[ ${CARCH} = "i686" ]]; then
     _arch=x86_32
  else
     _arch=x86_64
  fi

  mkdir -p "$srcdir/broadcom-sta"
  cd "$srcdir/broadcom-sta"

  tar -xf "$srcdir/hybrid-portsrc_${_arch}-v${_pkgdlver}.tar.gz"

  patch -Np1 -i "$srcdir/distro.patch"
  patch -Np1 -i "$srcdir/fixes.patch"
}

package() {
  cd "$srcdir/broadcom-sta"

  install -dm755 "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}"
  install -dm755 "$pkgdir/usr/share/licenses/broadcom-sta-dkms"

  cp -dpr "$srcdir/broadcom-sta/lib" "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}"
  cp -dpr "$srcdir/broadcom-sta/src" "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}"
  cp -dpr "$srcdir/broadcom-sta/Makefile" "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}"

  install -Dm644 "$srcdir/broadcom-sta.conf" "$pkgdir/usr/lib/modprobe.d/broadcom-sta.conf"
  sed -e "s/@VERSION@/${pkgver}-${pkgrel}/" "$srcdir/dkms.conf" > "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}/dkms.conf"

  ln -sf /usr/src/broadcom-sta-${pkgver}-${pkgrel}/lib/LICENSE.txt "$pkgdir/usr/share/licenses/broadcom-sta-dkms/LICENSE"
}
