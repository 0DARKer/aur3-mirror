# Maintainer: Armin K. <krejzi at email dot com>

pkgname=broadcom-sta-dkms
pkgver=5.100.82.112
_pkgdlver=5_100_82_112
pkgrel=3
pkgdesc="Broadcom hybrid wireless device driver for Linux"
arch=('i686' 'x86_64')
url="http://www.broadcom.com/support/802.11/linux_sta.php"
license=('custom')
depends=('linux-headers' 'dkms')
provides=('broadcom-wl' 'dkms-broadcom-wl')
conflicts=('broadcom-wl' 'dkms-broadcom-wl')
source=("http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_x86_32-v${_pkgdlver}.tar.gz"
        "http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_x86_64-v${_pkgdlver}.tar.gz"
        "rename-to-wlan0.patch"
        "license.patch"
        "build-fixes.patch"
        "3.8.patch"
        "broadcom-sta.conf"
        "dkms.conf")
install=broadcom-sta.install
noextract=("hybrid-portsrc_x86_32-v$_pkgdlver.tar.gz"
           "hybrid-portsrc_x86_64-v$_pkgdlver.tar.gz")
md5sums=('62d04d148b99f993ef575a71332593a9'
         '310d7ce233a9a352fbe62c451b2ea309'
         'e128f4461073518210f86f77d0eca13e'
         'ae43c9952e6cbc55d79c955b2a776648'
         '5e87c60d1aabcb50bb6a113c4200840f'
         '0880553bd97a4a7fc2eb3f2fb47f059c'
         'e907423f49ff68bdfcba236c03585d6b'
         '3c896e915ee7e3b3e1d4536d06d296df')

build() {
  if [[ ${CARCH} = "i686" ]]; then
     _arch=x86_32
  else
     _arch=x86_64
  fi

  mkdir -p "$srcdir/broadcom-sta"
  cd "$srcdir/broadcom-sta"

  tar -xf "$srcdir/hybrid-portsrc_${_arch}-v${_pkgdlver}.tar.gz"

  patch -Np1 -i "$srcdir/rename-to-wlan0.patch"
  patch -Np1 -i "$srcdir/license.patch"
  patch -Np1 -i "$srcdir/build-fixes.patch"
  patch -Np1 -i "$srcdir/3.8.patch"
}

package() {
  cd "$srcdir/broadcom-sta"

  install -dm755 "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}"
  install -dm755 "$pkgdir/usr/share/licenses/broadcom-sta-dkms"

  cp -dpr "$srcdir/broadcom-sta/lib" "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}"
  cp -dpr "$srcdir/broadcom-sta/src" "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}"
  cp -dpr "$srcdir/broadcom-sta/Makefile" "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}"

  install -Dm644 "$srcdir/broadcom-sta.conf" "$pkgdir/usr/lib/modprobe.d/broadcom-sta.conf"
  sed -e "s/@VERSION@/${pkgver}-${pkgrel}/" "$srcdir/dkms.conf" > "$pkgdir/usr/src/broadcom-sta-${pkgver}-${pkgrel}/dkms.conf"

  ln -sf /usr/src/broadcom-sta-${pkgver}-${pkgrel}/lib/LICENSE.txt "$pkgdir/usr/share/licenses/broadcom-sta-dkms/LICENSE"
}
