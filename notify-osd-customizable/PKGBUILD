# Maintainer: 4javier <4javiereg4 at gmail dot com>
pkgname=notify-osd-customizable
_realname=notify-osd
pkgver=0.9.30
pkgrel=3
pkgdesc="daemon that displays passive pop-up notifications, with leolik patch added"
arch=(i686 x86_64)
url="https://launchpad.net/notify-osd"
license=('GPL')
groups=()
depends=('libwnck' 'gconf')
optdepends=('notifyconf:gui to customize notifies appearence')
makedepends=('pkgconfig' 'libnotify')
provides=('notification-daemon' 'notify-osd')
conflicts=('notification-daemon' 'notify-osd')
install=conf.install
source=(https://launchpad.net/~leolik/+archive/leolik/+files/${_realname}_${pkgver}-0ubuntu4-leolik~ppa1-1.tar.gz notify-osd conf.install notify-osd-0.9.30-libnotify-0.7.patch)
md5sums=('21f8b0fe77e8185415093c1c716bd348'
         '1b1c942c8caebbdaad8402dbd0a8160a'
         '3ae1449accba75c595b29730afc2add6'
         'e52fed9d71a1d2892ae1d7b11a20aa46')


build() {
  cd "$srcdir/$_realname-$pkgver"
  patch -Np0 -i ${srcdir}/notify-osd-0.9.30-libnotify-0.7.patch
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make || return 1
 }

package() {
	cd "$srcdir/$_realname-$pkgver"
	make DESTDIR="$pkgdir/" install
	install -D -m644 ${srcdir}/notify-osd ${pkgdir}/etc/skel/.notify-osd
 }



