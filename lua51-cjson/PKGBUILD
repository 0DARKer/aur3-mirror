# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=lua51-cjson
pkgver=2.1.0
pkgrel=1
pkgdesc="A fast JSON parsing and encoding support for Lua."
arch=(i686 x86_64)
url="http://www.kyne.com.au/~mark/software/lua-cjson.php"
license=('custom:MIT')
depends=('lua51')
optdepends=('perl: UTF8 implementation test')
source=("http://www.kyne.com.au/~mark/software/download/lua-cjson-$pkgver.tar.gz")
md5sums=('24f270663e9f6ca8ba2a02cef19f7963')

build() {
  cd "$srcdir/lua-cjson-$pkgver"
  make PREFIX=/usr LUA_INCLUDE_DIR=/usr/include/lua5.1
}

package() {
  cd "$srcdir/lua-cjson-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" LUA_INCLUDE_DIR=/usr/include/lua5.1 install{,-extra}

  # license
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # use lua5.1
  sed -i "1s|env lua|&5.1|" `grep -rl "env lua" "$pkgdir"`

  # do not conflict with lua-cjson
  cd "$pkgdir/usr/bin"
  mv lua2json lua512json
  mv json2lua json2lua51
}

# vim:set ts=2 sw=2 et:
