# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based on: Anton Shestakov <engored*ya.ru>

pkgname=sdlmess-wout-toolkits
pkgver="0.158.u0"
_pkgver="$(echo ${pkgver} | tr -d . | cut -d "u" -f1)"
pkgrel=1
pkgdesc="Multiple Emulator Super System, computer and game console emulator using SDL with OpenGL support. Without toolkits (gtk and qt)"
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
conflicts=('sdlmess')
depends=('sdl2_ttf' 'flac' 'portmidi' 'libjpeg-turbo')
makedepends=('nasm' 'mesa' 'glu' 'wget' 'python2')
optdepends=('sdlmamefamily-tools: Tools for manage MAME/MESS/UME roms')
source=("https://github.com/mamedev/mame/archive/mame${_pkgver}.tar.gz"
        "sdlmess.sh")
sha1sums=('c9f880e619c23290bc655542b5eebbc127596663'
          '3aa6469df36684b9d9e57ae9828c426d6d051bc9')
install="sdlmess.install"

prepare() {
  cd "mame-mame${_pkgver}"

  find . -type f -not -name "*.png" -exec perl -pi -e 's|\r\n?|\n|g' "{}" \;
}

build() {
  cd "mame-mame${_pkgver}"

  [ "${CARCH}" = "x86_64" ] && _ptr64=1 && _sse2=1
  [ "${CARCH}" = "i686" ] && _ptr64=0 && _sse2=0

  make PYTHON=python2 PTR64="${_ptr64}" SSE2="${_sse2}" OPTIMIZE=2 NOWERROR=1 BUILD_EXPAT=0 BUILD_ZLIB=1 BUILD_FLAC=0 BUILD_JPEGLIB=0 BUILD_MIDILIB=0 BUILD_SQLITE3=0 NO_USE_QTDEBUG=1 FULLNAME=sdlmess TARGET=mess
}

package() {
  cd "mame-mame${_pkgver}"
  # Installing the wrapper script
  install -Dm755 ../sdlmess.sh "${pkgdir}/usr/bin/sdlmess"

  # Installing binaries
  install -Dm755 sdlmess "${pkgdir}/usr/share/sdlmess/sdlmess"

  # Installing extra bits
  install -d "${pkgdir}/usr/share/sdlmess/"{artwork,hash}
  install -m644 artwork/* "${pkgdir}/usr/share/sdlmess/artwork/"
  install -m644 hash/* "${pkgdir}/usr/share/sdlmess/hash/"

  # Install man
  install -Dm644 src/osd/sdl/man/mess.6 "${pkgdir}/usr/share/man/man6/sdlmess.6"

  # The license
  install -Dm644 docs/license.txt "${pkgdir}/usr/share/licenses/custom/${pkgname}/license.txt"

  # documentation
  cd docs
  for i in $(find . -type f); do install -Dm644 "${i}" "${pkgdir}/usr/share/doc/${pkgname}/${i}"; done
}
