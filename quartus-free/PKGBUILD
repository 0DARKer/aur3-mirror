# Maintainer: Matthias Blaicher <matthias at blaicher dot com>
#
# NOTE: To build this package you need to place the Altera Quartus installer into your startdir.
#       If you plan on using the usbblaster make sure you are member of the plugdev group.
pkgname=quartus-free
pkgver=12.1_177
pkgrel=1
pkgdesc="Quartus II Web Edition design software for Altera FPGA's"
arch=('i686' 'x86_64')
url="http://www.altera.com/products/software/quartus-ii/web-edition"
license=('custom')

# According to the installer script, these dependencies are needed for the installer
[[ $CARCH = i686 ]] && depends=('glibc' 'libxext' 'libx11' 'libxau' 'libxdmcp' 'freetype2' 
				'fontconfig' 'expat')
[[ $CARCH = x86_64 ]] && depends=('lib32-glibc' 'lib32-libxext' 'lib32-libx11' 
				  'lib32-libxau' 'lib32-libxdmcp' 'lib32-freetype2'
				  'lib32-fontconfig' 'lib32-expat')
makedepends=('bash')
				  
source=("${pkgver}_quartus_free_linux.tar.gz" "quartus.sh" "quartus.desktop" "51-usbblaster.rules")
md5sums=('a206a7585d23d433c26ada2a916b24d1' 'e7fdf2f918cd1d7c78d7019d713a1da3' 
	 'a1984f06442d98b0c433ea923a11725b' 'f5744dc4820725b93917e3a24df13da9')
options=('!strip')

PKGEXT=".pkg.tar"

build() {
    cd "${srcdir}/${pkgver}_quartus_free_linux"
    ./setup --all -w --auto --target=${pkgdir}/opt/altera
}

package() {
    cd "${srcdir}/${pkgver}_quartus_free_linux"
    install -D -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    
    
    cd "${srcdir}"
    install -D -m644 quartus.sh "${pkgdir}/etc/profile.d/quartus.sh"
    install -D -m644 51-usbblaster.rules "${pkgdir}/etc/udev/rules.d/51-usbblaster.rules"
    install -D -m644 quartus.desktop "${pkgdir}/usr/share/applications/quartus.desktop"
}

# vim:set ts=2 sw=2 et: