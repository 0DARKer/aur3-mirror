# Maintainer: Matthias Blaicher <matthias at blaicher dot com>
#
# NOTE: If you plan on using the usbblaster make sure you are member of the plugdev group.
# NOTE: Altera has dramatically changed their packing in regards to version 12. This
#       PKGBUILD will install the full Altera suite now. Be aware that the space requirement
#       is around 13GB.
#
pkgname=quartus-free
pkgver=14.0.0.209
pkgrel=1
pkgdesc="Quartus II Web Edition design software for Altera FPGA's"
arch=('i686' 'x86_64')
url="http://www.altera.com/products/software/quartus-ii/web-edition"
license=('custom')

_alteradir="/opt/altera"

# According to the installer script, these dependencies are needed for the installer
if [[ $CARCH = i686 ]]
then
   depends=('desktop-file-utils' 'glibc' 'libxext' 'libx11' 'libxau' 'libxdmcp' 'freetype2' 
		        'fontconfig' 'expat' 'libpng12')
   optdepends=('gtk: For eclipse-nios2 support'
               'libxtst: For eclipse-nios2 support')
fi

if [[ $CARCH = x86_64 ]]
then
   depends=('desktop-file-utils' 'lib32-glibc' 'lib32-libxext' 'lib32-libx11' 
	    'lib32-libxau' 'lib32-libxdmcp' 'lib32-freetype2'
	    'lib32-fontconfig' 'lib32-expat' 'lib32-libpng12' 
	    'lib32-libxrender' 'lib32-libsm')
   optdepends=('lib32-gtk: For eclipse-nios2 support'
               'lib32-libxtst: For eclipse-nios2 support')
fi

makedepends=('bash')
				  
source=("Quartus-web-14.0.0.200-linux.tar::http://download.altera.com/akdlm/software/acdsinst/14.0/200/ib_tar/Quartus-web-14.0.0.200-linux.tar"
	"Quartus-web-14.0.2.209-linux-update.run::http://download.altera.com/akdlm/software/acdsinst/14.0.2/209/update/QuartusSetup-14.0.2.209-linux.run"
	"quartus.sh" "quartus.desktop" "51-usbblaster.rules" "quartus.install")
md5sums=('6403fa259e959af6e5cae9080527dbd8'
	 'B3AA1ED71CF4317B251A9D341C0DBDA8 '
         '067c444cae7fe31d3608245712b43ce8'
         '32b17cb8b992fc2dccd33d87f0dcd8ce'
         'f5744dc4820725b93917e3a24df13da9'
         'a331a81c44aed062a7af6d28542c3d82')

options=('!strip')
install='quartus.install'
PKGEXT=".pkg.tar"

package() {
    cd "${srcdir}"
    
    # Install version 14.0.0.200
    DISPLAY="" bash ./setup.sh --mode unattended --unattendedmodeui none --installdir "${pkgdir}${_alteradir}"
    
    # Update to version to 14.0.2.209
    chmod +x Quartus-web-14.0.2.209-linux-update.run
    DISPLAY="" ./Quartus-web-14.0.2.209-linux-update.run --mode unattended --unattendedmodeui none --installdir "${pkgdir}${_alteradir}"
    
    # Remove uninstaller and install logs since we have a working package management
    rm -r "${pkgdir}${_alteradir}/uninstall"
    rm -r "${pkgdir}${_alteradir}/logs"
    
    # Replace altera directory in integration files
    sed -i.bak "s,_alteradir,$_alteradir,g" quartus.sh
    sed -i.bak "s,_alteradir,$_alteradir,g" quartus.desktop
    
    # Copy license file
    install -D -m644 "${pkgdir}${_alteradir}/licenses/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    
    # Install integration files
    install -D -m755 quartus.sh "${pkgdir}/etc/profile.d/quartus.sh"
    install -D -m644 51-usbblaster.rules "${pkgdir}/etc/udev/rules.d/51-usbblaster.rules"
    install -D -m644 quartus.desktop "${pkgdir}/usr/share/applications/quartus.desktop"
}

# vim:set ts=2 sw=2 et:
