# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: anubis2591 <anubis2591 at gmail dot com>

pkgname=kid3-qt
pkgver=2.0.1
pkgrel=3
pkgdesc="An efficient ID3 tag editor - non KDE version"
arch=('i686' 'x86_64')
url="http://kid3.sourceforge.net"
license=('GPL')
depends=('id3lib' 'qt' 'tunepimp')
makedepends=('cmake' 'docbook-xsl')
install=$pkgname.install
source=("http://downloads.sourceforge.net/sourceforge/kid3/kid3-$pkgver.tar.gz"
        "gcc47.patch")
sha1sums=('951864e665b3b40c7020dcce38de6186cf4a7e12'
          'bca76084ddb20086cb1ec5e55ded8a2ac8d446bb')

build() {
  cd "$srcdir"

  # gcc 4.7 fix
  patch -Np0 -i "$srcdir"/gcc47.patch

  rm -rf build
  mkdir build && cd build

  cmake ../kid3-"$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_KDE=OFF \
    -DWITH_PHONON=OFF
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
