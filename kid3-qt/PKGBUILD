# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: anubis2591 <anubis2591 at gmail dot com>

pkgname=kid3-qt
pkgver=2.1
pkgrel=3
pkgdesc="An efficient ID3 tag editor - Non KDE version"
arch=('i686' 'x86_64')
url="http://kid3.sourceforge.net"
license=('GPL2')
depends=('chromaprint' 'id3lib' 'qt' 'taglib')
makedepends=('cmake' 'docbook-xsl')
install=$pkgname.install
source=("http://downloads.sourceforge.net/sourceforge/kid3/kid3-$pkgver.tar.gz"
        "ffmpeg-0.11.diff")
sha1sums=('bdfad021123d41cdbe38dc02f92869786d9cdff6'
          '80855ee0099964fa52b816797a9fe2eceeec8b5c')

build() {
  cd "$srcdir/kid3-$pkgver"

  # fix building against ffmpeg 0.11
  patch -Np1 -i "$srcdir"/ffmpeg-0.11.diff

  cd "$srcdir"

  rm -rf build
  mkdir build && cd build

  cmake ../kid3-"$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_KDE=OFF \
    -DWITH_PHONON=OFF
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
