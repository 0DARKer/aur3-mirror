# Maintainer: josephgbr <rafael.f.f1 at gmail.com>

_pkgbase=libcap-ng
pkgname=lib32-$_pkgbase
pkgver=0.6.6
pkgrel=1
pkgdesc="A library intended to make programming with POSIX capabilities much easier than the traditional libcap (32 bit)"
arch=('x86_64')
url="http://people.redhat.com/sgrubb/libcap-ng/"
license=('GPL2' 'LGPL2.1')
depends=('lib32-glibc' "$_pkgbase")
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://people.redhat.com/sgrubb/$_pkgbase/$_pkgbase-$pkgver.tar.gz)
md5sums=('eb71f967cecb44b4342baac98ef8cb0f')

build() {
  export CC='gcc -m32'
  
  cd "$srcdir/$_pkgbase-$pkgver"

  ./configure --prefix=/usr --enable-static=no --with-python=no --libdir=/usr/lib32
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
