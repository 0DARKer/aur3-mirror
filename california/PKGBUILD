# Maintainer: Martin Poehlmann <mpdeimos@outlook.com>

pkgname=california
pkgver=0.1.2
pkgrel=1
pkgdesc='A new calendar application for GNOME 3. It is currently under heavy development.'
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/California"
license=('LGPL2.1')
depends=('evolution-data-server' 'libgee')
makedepends=('automake' 'make' 'vala' 'git' 'intltool' 'gnome-common')
install="${pkgname}.install"
source=('https://download.gnome.org/sources/california/0.1/california-0.1.2.tar.xz')
sha256sums=('5b67abf6187d99a861ed1eac9654b0a3bade1fb810f10452dc3021fce1e18d2c')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr
	sed -i 's#californiadocdir = ${prefix}#californiadocdir = ${datadir}#g' Makefile
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make install DESTDIR="$pkgdir"
	install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
