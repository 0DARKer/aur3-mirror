# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>

pkgname=b43-firmware
realname=broadcom-wl
pkgver=5.10.144.3
pkgrel=1
pkgdesc="Firmware for Broadcom B43 wireless networking chips"
arch=("i686" "x86_64")
url="http://linuxwireless.org/en/users/Drivers/b43"
license=("GPL")
depends=("linux<3.2")
makedepends=("b43-fwcutter>=014")
options=(!emptydirs)
source=(http://www.lwfinger.com/${pkgname}/${realname}-${pkgver}.tar.bz2)

package() {
	cd "${srcdir}"/${realname}-${pkgver}

	# Directories
	install -d "${pkgdir}"/lib/firmware/

	# Application
	b43-fwcutter -w ""${pkgdir}"/lib/firmware/" linux/wl_apsta.o

	# Messages
	msg "You should also add 'b43' into the 'modules' section of your '/etc/rc.conf' file."
}

sha1sums=('cdb8d3fe2cb2737b6206bbb0cfa72a53dfe11208')
