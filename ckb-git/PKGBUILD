# Maintainer: Penetal

pkgname=ckb-git
pkgver="0.0.46"
pkgrel=2
pkgdesc="Corsair K65/K70/K95 Driver for Linux and OSX"
arch=('any')
url="https://github.com/ccMSC/ckb"
license=('GPL2')
depends=('qt5-base')
makedepends=('git' 'gcc')
install=ckb.install
source=('git://github.com/ccMSC/ckb.git'
        'ckb.sh'
        'ckb.desktop')
md5sums=('SKIP' 'SKIP' 'SKIP')
_gitname="ckb"

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git describe --always | sed 's/-/./g')
}

build() {
  cd "$srcdir/$_gitname"
  ./qmake-auto
  make
}

package() {
  cd "$srcdir/$_gitname/"

  install -d "$pkgdir/opt"
  cp -R "$srcdir/$_gitname/bin" "$pkgdir/opt/ckb"

  install -Dm755 "$srcdir/ckb.sh" "$pkgdir/usr/bin/ckb"
  ln -s "/opt/ckb/ckb-daemon" "$pkgdir/usr/bin/ckb-daemon"
  chmod 755 "$pkgdir/opt/ckb/ckb-daemon"

  install -Dm644 "$srcdir/$_gitname/service/systemd/ckb-daemon.service" "$pkgdir/usr/lib/systemd/system/ckb-daemon.service"

  install -Dm644 "$srcdir/ckb.desktop" "$pkgdir/usr/share/applications/ckb.desktop"

  install -Dm644 "$srcdir/$_gitname/src/ckb/ckb-logo.png" "$pkgdir/usr/share/ckb/ckb.png"
}

