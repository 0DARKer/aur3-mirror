# Maintainer: Max Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=blink-darcs
pkgver=20110513
pkgrel=3
pkgdesc="A state of the art, easy to use SIP client"
arch=('i686' 'x86_64')
url="http://www.icanblink.com"
license=('GPL')
depends=('python26' 'python26-eventlet08' 'python26-gnutls114' 'python26-pyqt'
         'python26-zope-interface' 'python26-sipsimple-darcs' 'python26-distribute'
         'python26-lxml' 'python26-cjson' 'python26-cython0113' 'python26-dnspython'
         'python26-twisted' 'python26-imaging' 'python26-dateutil' 'openssl'
         'gnutls' 'libxml2' 'libxslt' 'python26-xcaplib' 'python26-msrplib')
makedepends=('subversion' 'darcs')

_darcstrunk="http://devel.ag-projects.com/repositories"
_darcsmod="blink-qt"

build() {
 if [[ -d $_darcsmod/_darcs ]]; then
  msg "Retrieving missing patches"
  cd $_darcsmod
  darcs pull -a $_darcstrunk/$_darcsmod
 else
  msg "Retrieving complete sources"
  darcs get --lazy $_darcstrunk/$_darcsmod
  cd $_darcsmod
 fi

 python26 setup.py build
}

package() {
 cd "$srcdir/$_darcsmod"
 python26 setup.py install --root="$pkgdir"

 cd debian
 install -Dm 644 blink.desktop "$pkgdir/usr/share/applications/blink.desktop"
 install -Dm 644 blink.xpm "$pkgdir/usr/share/pixmaps/blink.xpm"
 install -Dm 644 blink.1 "$pkgdir/usr/share/man/man1/blink.1"
}
