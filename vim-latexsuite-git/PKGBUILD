# Maintainer: Army
_pkgname=vim-latexsuite
pkgname=$_pkgname-git
pkgver=0.789.eaa765e
pkgrel=1
pkgdesc="Provides a comprehensive set of tools to view, edit, and compile LaTeX documents within Vim. Also provides macros to speed up the creation of LaTeX documents."
arch=('any')
url="http://vim-latex.sourceforge.net"
license=('custom:vim')
groups=('vim-plugins')
depends=('vim')
makedepends=('git')
optdepends=('python2' 'texlive-bin')
provides=("$_pkgname" "$_pkgname-svn")
conflicts=("$_pkgname" "$_pkgname-svn")
install=$pkgname.install
source=("$_pkgname::git+git://vim-latex.git.sourceforge.net/gitroot/vim-latex/vim-latex")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
  cd "$srcdir/$_pkgname"
  find . -type f -name *.py | xargs sed -i 's/\/usr\/bin\/python/\/usr\/bin\/env python2/g'
}

package() {
  cd "$srcdir/$_pkgname"
  make PREFIX=${pkgdir}/usr VIMDIR=${pkgdir}/usr/share/vim/vimfiles install
}

# vim:set ts=2 sw=2 et:
