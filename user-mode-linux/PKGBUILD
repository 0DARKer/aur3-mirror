pkgname=user-mode-linux
_kernelname=-uml
pkgver=3.3.4
pkgrel=1
pkgdesc="Run multiple virtual Linux systems as applications within a normal Linux system"
arch=('i686' 'x86_64')
license=('GPL2')
source=(ftp://ftp.kernel.org/pub/linux/kernel/v3.0/linux-$pkgver.tar.bz2
        config)
md5sums=('113baeccd2a3341ecc62ae5e73c2ed1d'
         'dfe620c75044847f43e17c3f72de026e')
depends=('glibc' 'uml_utilities')
makedepends=('libpcap')
url="http://user-mode-linux.sourceforge.net/"

build() {
  cd "$srcdir/linux-$pkgver"
  cp "$srcdir/config" ./.config

  if [ "${_kernelname}" != "" ]; then
    sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"${_kernelname}\"|g" ./.config
  fi

  LDFLAGS="" make oldconfig ARCH=um
  LDFLAGS="" make vmlinux modules ARCH=um
}

package() {
  cd "$srcdir/linux-$pkgver"

  mkdir -p "$pkgdir/lib/modules" "$pkgdir/usr/lib/uml"

  make INSTALL_MOD_PATH="$pkgdir" modules_install ARCH=um

  mv "$pkgdir/lib/modules" "$pkgdir/usr/lib/uml/"
  rm -rf "$pkgdir/lib/"

  mkdir -p "$pkgdir/usr/bin"
  cp vmlinux "$pkgdir/usr/bin"
}
