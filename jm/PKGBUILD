# Maintainer : Gergely Imreh <imrehgATgmailDOTcom>
pkgname=jm
pkgver=18.3
pkgrel=1
pkgdesc="H.264/AVC reference software"
arch=('i686' 'x86_64')
url="http://www.w6rz.net/"
license=('Custom')
depends=('gcc-libs')
makedepends=('gcc')
source=("http://iphome.hhi.de/suehring/tml/download/jm${pkgver}.zip"
        "http://iphome.hhi.de/suehring/tml/download/COPYRIGHT_ITU.txt")

build() {
  cd "${srcdir}/JM"
  chmod +x unixprep.sh && ./unixprep.sh

  tomake=( ldecod lencod rtpdump rtp_loss )
  for _prog in ${tomake[@]}
  do
     cd "${srcdir}/JM/${_prog}"
     sed -i s/-march=pentium4//g Makefile
     make 
  done
}

package() {
  cd "${srcdir}/JM/bin"

  runtimes=( ldecod lencod rtpdump rtp_loss )
  for _prog in ${runtimes[@]}
  do
     install -D -m 755 "${_prog}.exe" "${pkgdir}/usr/bin/${_prog}"
  done

  install -D -m 644 "${srcdir}/COPYRIGHT_ITU.txt" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT_ITU.txt"
}
md5sums=('8b13c1e523eff46a9823a4ac131c4577'
         'eefe0252fdc3fc63e363aa2fb96e66da')
