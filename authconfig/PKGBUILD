# Maintainer: Fran√ßois Steinmetz <francois.steinmetz@gmail.com>
# Contributor: Zdenek Janecek <jan.zdenek@gmail.com>
pkgname=authconfig
pkgver=6.1.17
pkgrel=2
pkgdesc="Graphical and command line utility which can configure a system to be a client for certain networked user information and authentication schemes. This utility can configure authentication to LDAP, Kerberos or Winbind."
arch=('i686' 'x86_64')
url="https://fedorahosted.org/authconfig/"
license=('GPL2')
# TODO: depends?
depends=('python2')
makedepends=('intltool')
# TODO: backup?
backup=()
source=("https://fedorahosted.org/releases/a/u/authconfig/$pkgname-$pkgver.tar.bz2"
        "configure.patch"
        "system-config-authentication"
        "org.archlinux.pkexec.system-config-authentication.policy")
sha1sums=('177c9d471c83eb2a2a9a98e2a5a0792054238eff'
          'fa7200780a62b1edd8959046b06f561938581b20'
          'af2f3aa28eaf229c899546978a00a504a848a2fa'
          '734d1b95835b5a8aaed9fa62a85aa710ca626f67')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  sed -i '1s|\<python\>|python2|' authconfig.py authconfig-gtk.py
  patch -p0 -i "$srcdir/configure.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # /usr/bin only contains symlinks to "consolehelper", which does not
  # exist on Arch (seems to be a Fedora-specific wrapper)...
  rm -rf "$pkgdir/usr/bin"
  rm -rf "$pkgdir/etc/security/console.apps"

  # ...so use an Arch-specific wrapper instead. Yay.
  # TODO: Are wrappers for authconfig, authconfig-tui, authconfig-gtk needed?
  install -Dm0755 "$srcdir/system-config-authentication" \
    "$pkgdir/usr/bin/system-config-authentication"
  install -Dm0644 "$srcdir/org.archlinux.pkexec.system-config-authentication.policy" \
    "$pkgdir/usr/share/polkit-1/actions/org.archlinux.pkexec.system-config-authentication.policy"

  # Kill empty directories
  rm -rf "$pkgdir/etc/X11"

  # TODO: Is this really needed? Most DEs use the icons/ folder just fine.
  install -Dm644 "icons/48x48/system-config-authentication.png" \
    "$pkgdir/usr/share/pixmaps/system-config-authentication.png"
}

# vim:set ts=2 sw=2 et:
