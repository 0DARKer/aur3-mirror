# Maintainer: Michael Schubert <mschu.dev at google mail>

pkgname=copasi
pkgver=4.7
pkgrel=1
pkgdesc="COPASI is used for simulation and analysis of biochemical networks and their dynamics"
arch=('i686' 'x86_64')
url="http://www.copasi.org/"
license=('Artistic 2.0')
depends=('glibc')
source=('copasi-desktop.tar.gz')
md5sums=('9582e4e3d0d1d5433dea4c48f1f5f396')
install=copasi.install

if [ "$CARCH" = "i686" ]; then
  source=("${source[@]}" 'http://www.comp-sys-bio.org/mirror.copasi.org/Copasi-34-Linux-Dynamic.tar.gz')
  md5sums=("${md5sums[@]}" '8a31617f2bf68114607cdc74c2b6d2ae')
fi
if [ "$CARCH" = "x86_64" ]; then
  source=("${source[@]}" 'http://www.comp-sys-bio.org/mirror.copasi.org/Copasi-34-Linux64-Dynamic.tar.gz')
  md5sums=("${md5sums[@]}" '9455aa2949d0631b79626561fdb78c48')  
fi

package() {
  # install program files
  cd $srcdir/$pkgname
  install -d $pkgdir/usr/{bin,share}
  install -m755 bin/* $pkgdir/usr/bin
  cp -R share/$pkgname $pkgdir/usr/share/
  
  # desktop integration...
  cd $srcdir/$pkgname-desktop
  # copasi.sh sets systems-wide COPASIDIR environment variable, used by CopasiUI
  install -Dm755 copasi.sh $pkgdir/etc/profile.d/copasi.sh
  # copasi.desktop
  install -Dm644 copasi.desktop $pkgdir/usr/share/applications/copasi.desktop
  # MIME information
  install -Dm644 copasi.xml $pkgdir/usr/share/mime/packages/copasi.xml
  # icons
  install -d $pkgdir/usr/share/icons/hicolor
  cp -R icons/* $pkgdir/usr/share/icons/hicolor/
  # copasi starts CopasiUI after setting COPASIDIR environment variable
  # useful for launching COPASI using the menu entry
  install -Dm755 copasi $pkgdir/usr/bin/copasi
}
