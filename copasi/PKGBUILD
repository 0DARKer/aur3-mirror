# Maintainer: Michael Schubert <mschu.dev at google mail>

pkgname=copasi
pkgver=4.8
_pkgver=35
pkgrel=1
pkgdesc="COmplex PAthway SImulator for analysis of biochemical networks and their dynamics"
arch=('i686' 'x86_64')
url="http://www.copasi.org/"
license=('Artistic 2.0')
depends=('glibc')
source=('copasi-desktop.tar.gz')
md5sums=('9582e4e3d0d1d5433dea4c48f1f5f396')
install=copasi.install

if [ "$CARCH" = "i686" ]; then
  source=("${source[@]}" "http://www.comp-sys-bio.org/mirror.copasi.org/Copasi-$_pkgver-Linux-Dynamic.tar.gz")
  md5sums=("${md5sums[@]}" '77446a83c0ce08fb62f0f735857bdd82')
fi
if [ "$CARCH" = "x86_64" ]; then
  source=("${source[@]}" "http://www.comp-sys-bio.org/mirror.copasi.org/Copasi-$_pkgver-Linux64-Dynamic.tar.gz")
  md5sums=("${md5sums[@]}" '671adba5851f76851499ba7ddb2253b5')  
fi

package() {
  # install program files
  cd $srcdir/$pkgname
  install -d $pkgdir/usr/{bin,share}
  install -m755 bin/* $pkgdir/usr/bin
  cp -R share/$pkgname $pkgdir/usr/share/
  
  # desktop integration...
  cd $srcdir/$pkgname-desktop
  install -Dm755 copasi.sh $pkgdir/etc/profile.d/copasi.sh
  install -Dm644 copasi.desktop $pkgdir/usr/share/applications/copasi.desktop
  install -Dm644 copasi.xml $pkgdir/usr/share/mime/packages/copasi.xml
  install -d $pkgdir/usr/share/icons/hicolor
  cp -R icons/* $pkgdir/usr/share/icons/hicolor/
  # copasi starts CopasiUI after setting COPASIDIR environment variable
  # useful for launching COPASI using the menu entry
  install -Dm755 copasi $pkgdir/usr/bin/copasi
}
