# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=kkedit
pkgver=0.0.19
pkgrel=2
pkgdesc="Source code text editor."
arch=('i686' 'x86_64')
url="http://keithhedger.hostingsiteforfree.com/pages/kkedit/help.html"
license=('GPL3')
depends=('ctags' 'gtksourceview2' 'libunique' 'xdg-utils')
optdepends=('webkitgtk2: for Gtk-Doc viewer'
            'xterm: for opening terminal within editor')
source=(http://keithhedger.hostingsiteforfree.com/zips/kkedit/KKEdit-$pkgver.tar.gz)
md5sums=('74a1d3237cd95dd2eebdd721d789d5e5')


prepare() {
  cd $srcdir/KKEdit-$pkgver

  # Comment next line and uncomment the line that follows if you want Gtk-Doc viewer(needs webkitgtk2)
  sh autogen.sh --prefix=/usr --disable-docviewer 
  #sh autogen.sh --prefix=/usr
}

build() {
  cd $srcdir/KKEdit-$pkgver
  make
}

package() {
  cd $srcdir/KKEdit-$pkgver/KKEdit

  # Binary
  install -Dm755 app/kkedit $pkgdir/usr/bin/kkedit
  
  # Desktop icon
  cd resources/
  install -Dm644 pixmaps/KKEdit.png $pkgdir/usr/share/pixmaps/KKEdit.png
  install -Dm644 pixmaps/KKEdit.desktop $pkgdir/usr/share/applications/KKEdit.desktop
  sed -i '6iIcon=/usr/share/KKEdit/pixmaps/KKEdit.png' $pkgdir/usr/share/applications/KKEdit.desktop 
  sed -i '6iExec=/usr/bin/kkedit' $pkgdir/usr/share/applications/KKEdit.desktop
  
  # Help & Tools
  mkdir -p $pkgdir/usr/share/KKEdit
  cp -r help/ tools/ $pkgdir/usr/share/KKEdit  
}