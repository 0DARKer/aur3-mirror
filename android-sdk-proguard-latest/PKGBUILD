# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>
_pkgname=android-sdk
pkgname=android-sdk-proguard-latest
pkgver=r21.1
pkgrel=1
_proguardver=4.9
pkgdesc='Google Android SDK'
arch=('i686' 'x86_64')
url="http://developer.android.com/sdk/index.html"
license=('custom')
conflicts=($_pkgname)
provides=($_pkgname=$pkgver)
depends=('java-runtime' 'alsa-lib' 'openal' 'libstdc++5' 'libxv'
         'ncurses' 'sdl' 'swt' 'zlib')
if [[ $CARCH = x86_64 ]]; then
  depends=('java-runtime' 'lib32-alsa-lib' 'lib32-openal' 'lib32-libstdc++5'
           'lib32-libxv' 'lib32-ncurses' 'lib32-sdl' 'lib32-zlib' 'swt')
fi
install=$_pkgname.install
optdepends=('android-udev: udev rules for Android devices'
            'android-sdk-platform-tools: adb, aapt, aidl, dexdump and dx')
source=(http://dl.google.com/android/${_pkgname}_$pkgver-linux.tgz
        http://downloads.sourceforge.net/proguard/proguard${_proguardver}.tar.gz
        $_pkgname.png
        $_pkgname.desktop
        $_pkgname.sh
        $_pkgname.csh
        $_pkgname.conf
        license.html)
sha1sums=('2631e4bd81d502767f57178764a50a5f91964abc'
          '125891aca13b692df9ed890888fabe8541c21535'
          'c249134310ae91dccce1ad839b3985211674ab71'
          'e834f53e13d5926ec7be3fd775fa80aa6f328eb6'
          '78f8574e651c9bf8b7515ecb30c7ef93edbc4a96'
          '08c85aab7523e22b298891c7047bc0e7adbf3437'
          '145bdf3eb41a56574b289c1577a24bc47097ec83'
          'bfb91be7e0b602d765b7a1fcaf0ce1b7e1a93faa')

package() {
  cd "$pkgdir"
  install -Dm755 "$srcdir/$_pkgname.sh" etc/profile.d/$_pkgname.sh
  install -Dm755 "$srcdir/$_pkgname.csh" etc/profile.d/$_pkgname.csh
  install -Dm644 "$srcdir/$_pkgname.conf" etc/ld.so.conf.d/$_pkgname.conf
  mkdir -p opt
  cp -a "$srcdir/$_pkgname-linux" opt/$_pkgname
  install -Dm655 "$srcdir/$_pkgname.png" usr/share/pixmaps/$_pkgname.png
  install -Dm655 "$srcdir/$_pkgname.desktop" \
    usr/share/applications/$_pkgname.desktop
  install -Dm644 "$srcdir/license.html" \
    usr/share/licenses/$_pkgname/license.html
  
  rm -rf opt/$_pkgname/tools/proguard
  cp -a "$srcdir/proguard${_proguardver}" opt/$_pkgname/tools/proguard
  cp -t opt/$_pkgname/tools/proguard $srcdir/$_pkgname-linux/tools/proguard/*.txt

  if [[ $CARCH = i686 ]]; then
    rm -rf ${pkgdir}/opt/android-sdk/tools/lib/{monitor-,}x86_64 \
      ${pkgdir}/opt/android-sdk/tools/lib/lib64* \
      ${pkgdir}/opt/android-sdk/tools/emulator64-*
  fi

  # Fix broken permissions
  chmod -R o=g opt/$_pkgname
}
