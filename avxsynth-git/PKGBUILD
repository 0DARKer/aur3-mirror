# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=avxsynth-git
pkgver=20120426
pkgrel=1
pkgdesc="Linux Port of AviSynth"
arch=('i686' 'x86_64')
url="http://www.avxsynth.org"
license=("GPL2")
depends=('qt' 'mplayer' 'log4cpp' 'cairo' 'pango' 'ffmpegsource')
makedepends=('git')
replace=('avxsynth-svn' 'avxsynth')
provides=('avxsynth')
conflicts=( 'avxsynth-svn' 'avxsynth')
options=('!makeflags')
source=(avxsynth.profile
        source-fix.diff)
md5sums=('0b715231c8e4841b5a70cfe4c22bdc25'
         '416dd62a3aaee517292518c3e7093a6d')

_gitroot="git://github.com/avxsynth/avxsynth.git"
_gitname="avxsynth"

build() {
  cd "${srcdir}"

    msg "Connecting to GIT server..."

    if [ -d "${srcdir}"/"${_gitname}" ] ; then
      cd "${_gitname}" && git pull
    else
      git clone --depth=1 "${_gitroot}"
    fi

    [ -d "${srcdir}"/"${_gitname}"-build ] && rm -fr "${srcdir}"/"${_gitname}"-build
    cp -R "${srcdir}"/"${_gitname}" "${srcdir}"/"${_gitname}"-build

  msg "GIT checkout done or server timeout"

  msg "Starting build..."
  cd "${srcdir}"/"${_gitname}"-build/

  # Fix build libavxbtinfncs.so
  patch --silent -p0 < ../source-fix.diff
 
  # Fix Qt & general paths
  for i in $(find . -name Makefile); do sed 's|local/||g' -i "${i}"; done
  sed -e 's|qt4|qt|g' -e 's|qt/Q|Q|g' -e 's|moc-qt|moc|g' -i apps/AVXEdit/Makefile
  sed -e "s|.AVXSynth|${_gitname}|g" -e "s|\$(HOME)|${pkgdir}/usr/lib|g" -i common.mk
  
  # Fix RPATH
  for i in $(find . -name Makefile); do 
    sed -e "s|-R\$(AVXSYNTH_DEPLOY_PLUGINS)|-R/usr/lib/\$(AVXSYNTH_DEPLOY_REL_PATH)|g" \
        -e "s|-R\$(AVXSYNTH_DEPLOY_ROOT)|-R/usr/lib/\$(AVXSYNTH_DEPLOY_ROOT_REL_PATH)|g" -i "${i}"
  done

  # Fix GCC 4.7?
  sed "s|-Wl \$(AVXCOMMON_LIB) \$(FRAMESERVER_LIB) \$(SYSLIBRARIES)|-Wl,\$(AVXCOMMON_LIB) -Wl,\$(FRAMESERVER_LIB) -Wl,\$(SYSLIBRARIES)|g" -i apps/avxframeserver/frameserverapp/Makefile

  SUBLIBS+="-ldl" make
}

package() {

  cd "${srcdir}"/"${_gitname}"-build/

  install -Dm775 apps/AVXEdit/AVXEdit "${pkgdir}"/usr/bin/AVXEdit
  install -Dm775 apps/avxframeserver/frameserverapp/avxSynthFrameServer "${pkgdir}"/usr/bin/avxSynthFrameServer

  ln -s /usr/lib/"${_gitname}"/libavx{btinfncs,common,synth}.so "${pkgdir}"/usr/lib/

  install -Dm755 "${srcdir}"/../avxsynth.profile "${pkgdir}"/etc/profile.d/avxsynth.sh
}