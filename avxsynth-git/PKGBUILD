# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=avxsynth-git
pkgver=20130210
pkgrel=1
pkgdesc="Linux Port of AviSynth. (Git version)"
arch=('i686' 'x86_64')
url="http://www.avxsynth.org"
license=('GPLv2')
depends=('qt' 'mplayer' 'log4cpp' 'zlib' 'bzip2' 'ffmpegsource' 'ffmpeg')
makedepends=('git' 'svn' 'yasm' 'subversion')
provides=('avxsynth')
conflicts=('avxsynth')
options=('!libtool')
source=("https://github.com/qyot27/mpv-dep-patches/raw/master/AvxSynth-0001-Fix-compilation-error-in-avxffms2-because-FFMS2-itse.patch"
        "https://github.com/qyot27/mpv-dep-patches/raw/master/AvxSynth-0001-avxsynth_c.h-use-quoted-include-for-windowsPorts.patch")
md5sums=('89472520adf93429ecb021fe7facb0e3'
         '8bcc6ec08a4b0d62c6ada90043fe36e6')

_gitroot="git://github.com/avxsynth/avxsynth.git"
_gitname="avxsynth"

build() {
  cd "${srcdir}"

  msg "Connecting to avxsynth GIT server..."
  if [ -d "${_gitname}" ] ; then
      cd "${_gitname}" && git pull
  else
      git clone --depth=1 "${_gitroot}"
  fi

  msg "GIT checkout done or server timeout"

  rm -fr "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  patch --silent -Np1 -i "${srcdir}/AvxSynth-0001-Fix-compilation-error-in-avxffms2-because-FFMS2-itse.patch"
  patch --silent -Np1 -i "${srcdir}/AvxSynth-0001-avxsynth_c.h-use-quoted-include-for-windowsPorts.patch"

  autoreconf -if
  ./configure --prefix=/usr --enable-all --enable-silent-rules

  make
  msg2 "Done"
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
}
