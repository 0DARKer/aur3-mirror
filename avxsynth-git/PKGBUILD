# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=avxsynth-git
pkgver=20120623
pkgrel=1
pkgdesc="Linux Port of AviSynth. with include build with libav/libpostproc from GIT"
arch=('i686' 'x86_64')
url="http://www.avxsynth.org"
license=('GPL2' 'GPL3' 'MIT')
depends=('qt' 'mplayer' 'log4cpp' 'ffmpegsource-libav-svn' 'zlib' 'bzip2')
makedepends=('git' 'yasm' 'subversion')
provides=('avxsynth')
conflicts=('avxsynth')
options=('!libtool')

_gitavroot="git://git.libav.org/libav.git"
_gitavname="libav"

_gitpostprocroot='git://git.videolan.org/libpostproc.git'
_gitpostprocname='libpostproc'

_gitroot="git://github.com/avxsynth/avxsynth.git"
_gitname="avxsynth"

build() {
    cd "${srcdir}"
    rm -fr build
    msg "Connecting to the libav Git repository..."
    if [ -d "${_gitavname}" ] ; then
        cd "${_gitavname}" && git pull --depth=1 && cd "${srcdir}"
        msg "The local files are updated."
    else
        git clone --depth=1 "${_gitavroot}" && cd "${srcdir}"
    fi
    msg "GIT checkout done or server timeout"
    rm -rf "${_gitavname}"-build
    cp -R "${_gitavname}" "${_gitavname}"-build

    msg "Connecting to libpostproc GIT server..."
    if [ -d "${_gitpostprocname}" ] ; then
        cd "${_gitpostprocname}" && git pull --depth=1 && cd "${srcdir}"
        msg "The local files are updated."
    else
        git clone --depth=1 "${_gitpostprocroot}" && cd "${srcdir}"
    fi
    msg "GIT checkout done or server timeout"
    rm -fr "${_gitpostprocname}"-build
    cp -R "${_gitpostprocname}" "${_gitpostprocname}"-build

    msg "Connecting to avxsynth GIT server..."
    if [ -d "${_gitname}" ] ; then
        cd "${_gitname}" && git pull && cd "${srcdir}"
        msg "The local files are updated."
    else
        git clone --depth=1 "${_gitroot}" && cd "${srcdir}"
    fi
    msg "GIT checkout done or server timeout"
    rm -fr "${_gitname}"-build
    cp -R "${_gitname}" "${_gitname}"-build

    msg "Starting build..."
    msg2 "Build libav"
    cd "${srcdir}"/"${_gitavname}"-build/
    ./configure --prefix="${srcdir}"/build --disable-{network,{encod,mux}ers,hwaccels,{in,out}devs,debug,av{conv,play,probe,server},doc,vdpau} --enable-pic
    make
    make install

    msg2 "Build libpostproc"
    cd "${srcdir}"/"${_gitpostprocname}"-build/
    CFLAGS="-fPIC" PKG_CONFIG_LIBDIR=""${srcdir}"/build/lib/pkgconfig" ./configure --prefix="${srcdir}"/build --disable-debug
    make
    make install

    msg2 "Build avxsynth"
    cd "${srcdir}"/"${_gitname}"-build/

    ln -s /usr/lib/pkgconfig/{glib-2.0,gobject-2.0,gmodule-no-export-2.0,libpcre,cairo,pango,pangocairo,log4cpp,Qt{Core,Gui},pixman-1,fontconfig,freetype2,libpng,x{cb{,-shm},au,proto,dmcp,cb-render,render,11},{kb,render}proto,ffms2}.pc "${srcdir}"/build/lib/pkgconfig

    autoreconf -i

    PKG_CONFIG_LIBDIR=""${srcdir}"/build/lib/pkgconfig" ./configure --prefix=/usr --enable-all

    make
    msg2 "Done"
}

package() {
    cd "${srcdir}"/"${_gitname}"-build/
    make DESTDIR="${pkgdir}" install

    # Install examples
    install -d "${pkgdir}"/usr/share/"${_gitname}"/examples
    cp -R scripts/*.avs "${pkgdir}"/usr/share/"${_gitname}"/examples
}