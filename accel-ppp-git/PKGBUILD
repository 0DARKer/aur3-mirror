# Maintainer: Alexandr Boiko <brdcom@ya.ru>
pkgname=accel-ppp-git
pkgver=20150326
pkgrel=2
pkgdesc="High performance PPTP/L2TP/PPPoE/IPoE server"
arch=('i686' 'x86_64')
url="http://sourceforge.net/apps/trac/accel-ppp/"
license=('GPL')
depends=('openssl>=1.0.0' 'pcre>=8.30' 'libnl')
makedepends=('git' 'cmake>=2.6' 'libnl1' 'net-snmp>=5.x' 'lua51')
conflicts=('accel-ppp')
install='accel-ppp.install'
options=('docs')
backup=('etc/accel-ppp.conf' 'etc/accel-ppp.lua' 'etc/snmp/accel-ppp.conf')
source=(accel-ppp-default
	accel-ppp.lua
	accel-ppp.tmpfiles
	accel-pppd.service)
md5sums=('bb3e20c8808000f4efe52230c13a13f3'
         '23e32b0d758be2adf0f7b6987857afc2'
         '5be7d42b434b74d7e692b19a9e3c4297'
         'e5dec17bd405052fc647a805db5dd449')

_gitroot=git://accel-ppp.git.sourceforge.net/gitroot/accel-ppp/accel-ppp
_gitname=accel-ppp

prepare() {
	cd $srcdir
	msg "Connecting to GIT server...."
	
	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
		cd $srcdir
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"

	sed -i 's|RUNTIME DESTINATION sbin|RUNTIME DESTINATION bin|' \
	"$_gitname/accel-pppd/CMakeLists.txt"
	
	if [ -d "$_gitname-build" ]; then
		rm -fr "$_gitname-build"
		mkdir "$_gitname-build"
	else
		mkdir "$_gitname-build"
	fi
}
		
build() {
	msg "Starting build..."
	cd "$_gitname-build"
	cmake \
		-DCMAKE_SYSTEM_NAME=Linux \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Debug \
		-DLOG_FILE=TRUE \
		-DLOG_PGSQL=FALSE \
		-DSHAPER=TRUE \
		-DRADIUS=TRUE \
		-DNETSNMP=TRUE \
		-DLUA=TRUE \
		-DLUA_INCLUDE_DIR="/usr/include/lua5.1" \
		"../$_gitname/"
	make || return 1
}
package() {
	cd $srcdir/$_gitname-build
	make DESTDIR="$pkgdir/" install || return 1

	mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib"
	install -dm0755 "$pkgdir/etc/snmp"
	install -dm0755 "$pkgdir/var/lib/accel-ppp"
	touch "$pkgdir/etc/snmp/$_gitname.conf"
	install -Dm0644 "$srcdir/$_gitname-default" "$pkgdir/etc/conf.d/accel-pppd"
	install -Dm0644 "$srcdir/$_gitname/README" "$pkgdir/usr/share/doc/$_gitname/README"
	install -Dm0644 "$srcdir/accel-pppd.service" "$pkgdir/usr/lib/systemd/system/accel-pppd.service"
	install -Dm0644 "$srcdir/$_gitname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$_gitname.conf"
	install -Dm0644 "$srcdir/$_gitname.lua" "$pkgdir/etc/$_gitname.lua"
	install -Dm0644 "$pkgdir/etc/$_gitname.conf.dist" "$pkgdir/etc/$_gitname.conf"
	install -Dm0644 "$srcdir/$_gitname/accel-pppd/extra/net-snmp/ACCEL-PPP-MIB.txt" "$pkgdir/usr/share/snmp/mibs/ACCEL-PPP-MIB.txt"
	install -Dm0644 "$srcdir/$_gitname/COPYING" "$pkgdir/usr/share/licenses/$_gitname/LICENSE"
}

