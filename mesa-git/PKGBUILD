# Maintainer: Kristian Klausen <klausenbusk@hotmail.com>
# Contributor: Egon Ashrafinia <e.ashrafinia@gmail.com>
# Contributor: Tavian Barnes <tavianator@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>
# Contributor: Diego Jose <diegoxter1006@gmail.com>

# Credits to maintainers of mesa in extra!
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa-git
pkgname=mesa-git
# Dirty hack as AUR dont support split-packages
true && pkgname=('libglapi-git' 'libgl-git' 'mesa-git' 'osmesa-git' 'libgbm-git' 'libgles-git' 'libegl-git' 'khrplatform-devel-git' 'ati-dri-git' 'intel-dri-git' 'svga-dri-git' 'nouveau-dri-git')

pkgver=20130219
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('git' 'glproto>=1.4.16' 'libdrm>=2.4.39' 'libxxf86vm>=1.1.2' 'libxdamage>=1.1.3' 'expat>=2.1.0' 'libx11>=1.5.0' 'libxt>=1.1.3' 
             'gcc-libs>=4.7.1-6' 'dri2proto>=2.8' 'python2' 'libxml2' 'imake' 'llvm-amdgpu-git' 'systemd' 'libvdpau>=0.5')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=(LICENSE)
sha512sums=('25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2')

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

build () {
    cd "${srcdir}"

    msg 'Connecting to git.freedesktop.org GIT server....'
    if [ -d ${_gitname} ] ; then
        cd ${_gitname} && git pull origin
    else
        git clone ${_gitroot} ${_gitname} --depth 1
    fi
    msg 'GIT checkout done or server timeout'
    msg 'Starting make...'

    cd "${srcdir}/${_gitname}"

    # TODO: --enable-r600-llvm-compiler and --with-llvm-shared-libs maybe
    COMMONOPTS="--prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --with-egl-platforms=x11,drm \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau "

    ./autogen.sh \
      $COMMONOPTS

    make
}

package_libglapi-git () {
  depends=('glibc')
  pkgdesc="free implementation of the GL API -- shared library. The Mesa GL API module is responsible for dispatching all the gl* functions"
  provides=("libglapi")

  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/libglapi-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libglapi-git/"
}

package_libgl-git () {
  depends=('libdrm>=2.4.39' 'libxxf86vm>=1.1.2' 'libxdamage>=1.1.3' 'expat>=2.1.0' 'libglapi' 'gcc-libs')
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"
  provides=("libgl")

  # fix linking because of splitted package
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" install

  # libGL & libdricore
  make -C "${srcdir}/${_gitname}/src/glx" DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_gitname}/src/mesa/libdricore" DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" uninstall


  make -C "${srcdir}/${_gitname}/src/gallium/targets/dri-swrast" DESTDIR="${pkgdir}" install

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  install -m755 -d "${pkgdir}/usr/share/licenses/libgl-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgl-git/"
}

package_mesa-git () {
  # check also gl.pc
  depends=('libgl' 'libx11>=1.5.0' 'libxext>=1.3.1' 'libxdamage' 'libxfixes' 'libxcb' 'libxxf86vm')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  pkgdesc="Mesa 3-D graphics libraries and include files"
  provides=("mesa")

  make -C "${srcdir}/${_gitname}/src/mesa" DESTDIR="${pkgdir}" install-glHEADERS
  make -C "${srcdir}/${_gitname}/src/mesa/drivers/dri" DESTDIR="${pkgdir}" install-driincludeHEADERS  
  make -C "${srcdir}/${_gitname}/src/mesa" DESTDIR="${pkgdir}" install-pkgconfigDATA
  make -C "${srcdir}/${_gitname}/src/mesa/drivers/dri" DESTDIR="${pkgdir}" install-pkgconfigDATA  
  make -C "${srcdir}/${_gitname}/src/mesa/drivers/dri/common" DESTDIR="${pkgdir}" install-sysconfDATA

  make -C "${srcdir}/${_gitname}/src/gallium/targets/xa-vmwgfx" DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-git/"
}

package_osmesa-git () {
  depends=('libglapi' 'gcc-libs')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  pkgdesc="Mesa 3D off-screen rendering library"
  provides=("osmesa")

  # fix linking because of splitted package
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" install

  make -C "${srcdir}/${_gitname}/src/mesa/drivers/osmesa" DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/osmesa-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/osmesa-git/"
}

package_libgbm-git () {
  depends=('systemd' 'libglapi' 'libdrm')
  pkgdesc="Mesa gbm library"
  provides=("libgbm")

  # fix linking because of splitted package
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" install

  make -C "${srcdir}/${_gitname}/src/gbm" DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/libgbm-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgbm-git/"
}

package_libgles-git () {
  depends=('libglapi' 'libdrm' 'khrplatform-devel')
  pkgdesc="Mesa GLES libraries and headers"
  provides=("libgles")

  # fix linking because of splitted package
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" install

  make -C "${srcdir}/${_gitname}/src/mapi/es1api" DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_gitname}/src/mapi/es2api" DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/libgles-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgles-git/"
}

package_libegl-git () {
  # check also egl.pc
  depends=('libx11' 'libxext' 'libxdamage' 'libxfixes' 'libxxf86vm' 'libxcb' 'libgbm' 'khrplatform-devel')
  pkgdesc="Mesa EGL libraries and headers"
  provides=("libegl")

  # fix link with libllvmradeon9.2.0
  make -C ${srcdir}/${_gitname}/src/gallium/drivers/radeon DESTDIR="${pkgdir}" install

  make -C "${srcdir}/${_gitname}/src/gallium/targets/egl-static" DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/doc/libegl"
  install -m644 "${srcdir}/${_gitname}/docs/egl.html" "${pkgdir}/usr/share/doc/libegl/"
  
  # fix linking because of splitted package
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_gitname}/src/gbm" DESTDIR="${pkgdir}" install
  
  make -C ${srcdir}/${_gitname}/src/egl DESTDIR="${pkgdir}" install

  # fix link with libllvmradeon9.1
  make -C ${srcdir}/${_gitname}/src/gallium/drivers/radeon DESTDIR="${pkgdir}" uninstall

  # fix linking because of splitted package - cleanup
  make -C "${srcdir}/${_gitname}/src/gbm" DESTDIR="${pkgdir}" uninstall
  make -C "${srcdir}/${_gitname}/src/mapi/shared-glapi" DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/libegl-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libegl-git/"
  
  # fix file conflicts
  rm -rf "${pkgdir}/usr/include/KHR"
}

package_khrplatform-devel-git () {
  pkgdesc="Khronos platform development package"
  provides=("khrplatform-devel")

  install -m755 -d "${pkgdir}/usr/include/KHR"
  install -m644 "${srcdir}/${_gitname}/include/KHR/khrplatform.h" "${pkgdir}/usr/include/KHR/" 

  install -m755 -d "${pkgdir}/usr/share/licenses/khrplatform-devel-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/khrplatform-devel-git/"
}

package_ati-dri-git () {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI radeon/r200 + Gallium3D r300,r600 drivers for AMD/ATI Radeon"
  conflicts=('xf86-video-ati<6.9.0-6')
  provides=("ati-dri")

  # fix linking because of splitted package
  make -C "${srcdir}/${_gitname}/src/mesa/libdricore" DESTDIR="${pkgdir}" install
  
  # classic mesa drivers for radeon,r200
  make -C "${srcdir}/${_gitname}/src/mesa/drivers/dri/radeon" DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_gitname}/src/mesa/drivers/dri/r200" DESTDIR="${pkgdir}" install
  # gallium3D driver for r300,r600,radeonsi
  make -C "${srcdir}/${_gitname}/src/gallium/targets/dri-r300" DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_gitname}/src/gallium/targets/dri-r600" DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_gitname}/src/gallium/targets/dri-radeonsi" DESTDIR="${pkgdir}" install
  # vdpau driver
  make -C "${srcdir}/${_gitname}/src/gallium/targets/vdpau-r300" DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_gitname}/src/gallium/targets/vdpau-r600" DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_gitname}/src/gallium/targets/vdpau-radeonsi" DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C "${srcdir}/${_gitname}/src/mesa/libdricore" DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri-git/"
}

package_intel-dri-git () {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for Intel"
  provides=("intel-dri")

  # fix linking because of splitted package
  make -C "${srcdir}/${_gitname}/src/mesa/libdricore" DESTDIR="${pkgdir}" install

  make -C "${srcdir}/${_gitname}/src/mesa/drivers/dri/i915" DESTDIR="${pkgdir}" install
  make -C "${srcdir}/${_gitname}/src/mesa/drivers/dri/i965" DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C "${srcdir}/${_gitname}/src/mesa/libdricore" DESTDIR="${pkgdir}" uninstall
  
  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/intel-dri-git/"
}

package_nouveau-dri-git () {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa classic DRI + Gallium3D drivers for Nouveau"
  provides=("nouveau-dri")

  # fix linking because of splitted package
  make -C "${srcdir}/${_gitname}/src/mesa/libdricore" DESTDIR="${pkgdir}" install

  # classic mesa driver for nv10 , nv20 nouveau_vieux_dri.so
  make -C "${srcdir}/${_gitname}/src/mesa/drivers/dri/nouveau" DESTDIR="${pkgdir}" install
  # gallium3D driver for nv30 - nv40 - nv50 nouveau_dri.so
  make -C "${srcdir}/${_gitname}/src/gallium/targets/dri-nouveau" DESTDIR="${pkgdir}" install
  # vdpau driver
  make -C "${srcdir}/${_gitname}/src/gallium/targets/vdpau-nouveau" DESTDIR="${pkgdir}" install
  
  # fix linking because of splitted package - cleanup
  make -C "${srcdir}/${_gitname}/src/mesa/libdricore" DESTDIR="${pkgdir}" uninstall
  
  install -m755 -d "${pkgdir}/usr/share/licenses/nouveau-dri-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/nouveau-dri-git/"
}

package_svga-dri-git () {
  depends=('gcc-libs' 'libdrm' 'expat')
  pkgdesc="Gallium3D VMware guest GL driver"
  provides=("svga-dri")

  make -C "${srcdir}/${_gitname}/src/gallium/targets/dri-vmwgfx" DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/svga-dri-git"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/svga-dri-git/"
}
