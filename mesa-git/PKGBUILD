# Maintainer: Tavian Barnes <tavianator@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>

pkgbase=mesa-git
pkgname=mesa-git
#pkgname=('mesa-git' 'libgl-git' 'libgles-git' 'libegl-git' 'ati-dri-git'
#         'intel-dri-git' 'unichrome-dri-git' 'mach64-dri-git' 'mga-dri-git'
#         'r128-dri-git' 'savage-dri-git' 'sis-dri-git' 'tdfx-dri-git'
#         'nouveau-dri-git')
pkgver=20110528
pkgrel=1
_mesaver=7.10.2
arch=(i686 x86_64)
makedepends=('git' 'pkgconfig' 'glproto-git' 'libdrm-git' 'libxxf86vm'
             'libxdamage' 'expat' 'libx11' 'libxt' 'gcc-libs' 'dri2proto-git'
             'python2' 'talloc' 'libxml2' 'imake' 'llvm')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=(LICENSE)
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a')

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

build() {
  msg 'Connecting to git.freedesktop.org GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
  else
    git clone ${_gitroot}
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd "${srcdir}"

  # Cleanup and prepare the build dir
  [ -d build ] && rm -rf build
  cp -r ${_gitname} build
  cd build

  # python2 build fixes
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" $(find -name '*.py')
  sed -i -e "s|PYTHON2 = python|PYTHON2 = python2|" "${srcdir}"/build/configs/{default,autoconf.in}
  sed -i -e "s|python|python2|" "${srcdir}"/build/src/gallium/auxiliary/Makefile

  ./autogen.sh --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --enable-gallium-radeon \
    --enable-gallium-r600 \
    --enable-gallium-nouveau \
    --enable-gallium-swrast \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --with-state-trackers=dri,glx,egl \
    --disable-glut \
    --enable-gles1 \
    --enable-gles2 \
    --enable-egl \
    --enable-shared-dricore \
    --enable-texture-float
  make
}

package_libgl-git() {
  depends=('libdrm-git' 'libxxf86vm' 'libxdamage' 'expat')
  conflicts=('libgl')
  provides=("libgl=${_mesaver}")
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"

  cd "${srcdir}"/build
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"

  bin/minstall lib/libGL.so* "${pkgdir}/usr/lib/"

  cd src/mesa/drivers/dri
  #make -C swrast DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/gallium/targets/dri-swrast DESTDIR="${pkgdir}" install
  ln -s swrastg_dri.so "${pkgdir}/usr/lib/xorg/modules/dri/swrast_dri.so"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  install -m755 -d "${pkgdir}/usr/share/licenses/libgl-git"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgl-git/"
}

package_libgles-git() {
  depends=('libgl-git')
  conflicts=('libgles')
  provides=("libgles=${_mesaver}")
  pkgdesc="Mesa GLES libraries and headers"

  cd "${srcdir}"/build
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m755 -d "${pkgdir}/usr/include"
  install -m755 -d "${pkgdir}/usr/include/GLES"
  install -m755 -d "${pkgdir}/usr/include/GLES2"
  bin/minstall lib/libGLESv* "${pkgdir}/usr/lib/"
  bin/minstall src/mapi/es1api/glesv1_cm.pc "${pkgdir}/usr/lib/pkgconfig/"
  bin/minstall src/mapi/es2api/glesv2.pc "${pkgdir}/usr/lib/pkgconfig/"
  bin/minstall include/GLES/* "${pkgdir}/usr/include/GLES/"
  bin/minstall include/GLES2/* "${pkgdir}/usr/include/GLES2/"
  bin/minstall include/GLES2/* "${pkgdir}/usr/include/GLES2/"

  install -m755 -d "${pkgdir}/usr/share/licenses/libgles-git"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgles-git/"
}

package_libegl-git() {
  depends=('libgl-git')
  conflicts=('libegl')
  provides=("libegl=${_mesaver}")
  pkgdesc="Mesa libEGL libraries and headers"

  cd "${srcdir}"/build
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 -d "${pkgdir}/usr/lib/egl"
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m755 -d "${pkgdir}/usr/include"
  install -m755 -d "${pkgdir}/usr/include/"
  install -m755 -d "${pkgdir}/usr/include/EGL"
  install -m755 -d "${pkgdir}/usr/include/KHR"
  install -m755 -d "${pkgdir}/usr/share"
  install -m755 -d "${pkgdir}/usr/share/doc"
  install -m755 -d "${pkgdir}/usr/share/doc/libegl"
  bin/minstall lib/libEGL.so* "${pkgdir}/usr/lib/"
  bin/minstall lib/egl/* "${pkgdir}/usr/lib/egl/"
  bin/minstall src/egl/main/egl.pc "${pkgdir}/usr/lib/pkgconfig/"
  bin/minstall include/EGL/* "${pkgdir}/usr/include/EGL/"
  bin/minstall include/KHR/khrplatform.h "${pkgdir}/usr/include/KHR/"
  bin/minstall docs/egl.html "${pkgdir}/usr/share/doc/libegl/"

  install -m755 -d "${pkgdir}/usr/share/licenses/libegl-git"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libegl-git/"
}

package_ati-dri-git() {
  depends=('libgl-git')
  conflicts=('ati-dri')
  provides=("ati-dri=${_mesaver}")
  pkgdesc="Mesa DRI + Gallium3D r300 drivers for AMD/ATI Radeon"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C radeon DESTDIR="${pkgdir}" install
  make -C r200 DESTDIR="${pkgdir}" install
  # classic mesa driver for R300 r300_dri.so
  #make -C r300 DESTDIR="${pkgdir}" install  <------- depricated
  # gallium3D driver for R300 r300_dri.so
  make -C "${srcdir}"/build/src/gallium/targets/dri-r300 DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/gallium/targets/dri-r600 DESTDIR="${pkgdir}" install
  #make -C r600 DESTDIR="${pkgdir}" install
}

package_intel-dri-git() {
  depends=('libgl-git')
  conflicts=('intel-dri')
  provides=("intel-dri=${_mesaver}")
  pkgdesc="Mesa DRI drivers for Intel"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C i810 DESTDIR="${pkgdir}" install
  make -C i915 DESTDIR="${pkgdir}" install
  make -C i965 DESTDIR="${pkgdir}" install
}

package_unichrome-dri-git() {
  depends=('libgl-git')
  conflicts=('unichrome-dri')
  provides=("unichrome-dri=${_mesaver}")
  pkgdesc="Mesa DRI drivers for S3 Graphics/VIA Unichrome"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C unichrome DESTDIR="${pkgdir}" install
}

package_mach64-dri-git() {
  depends=('libgl-git')
  conflicts=('mach64-dri')
  provides=("mach64-dri=${_mesaver}")
  pkgdesc="Mesa DRI drivers for ATI Mach64"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C mach64 DESTDIR="${pkgdir}" install
}

package_mga-dri-git() {
  depends=('libgl-git')
  conflicts=('mga-dri')
  provides=("mga-dri=${_mesaver}")
  pkgdesc="Mesa DRI drivers for Matrox"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C mga DESTDIR="${pkgdir}" install
}

package_r128-dri-git() {
  depends=('libgl-git')
  conflicts=('r128-dri')
  provides=("r128-dri=${_mesaver}")
  pkgdesc="Mesa DRI drivers for ATI Rage128"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C r128 DESTDIR="${pkgdir}" install
}

package_savage-dri-git() {
  depends=('libgl-git')
  conflicts=('savage-dri')
  provides=("savage-dri=${_mesaver}")
  pkgdesc="Mesa DRI drivers for S3 Sraphics/VIA Savage"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C savage DESTDIR="${pkgdir}" install
}

package_sis-dri-git() {
  depends=('libgl-git')
  conflicts=('sis-dri')
  provides=("sis-dri=${_mesaver}")
  pkgdesc="Mesa DRI drivers for SiS"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C sis DESTDIR="${pkgdir}" install
}

package_tdfx-dri-git() {
  depends=('libgl-git')
  conflicts=('tdfx-dri')
  provides=("tdfx-dri=${_mesaver}")
  pkgdesc="Mesa DRI drivers for 3dfx"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  make -C tdfx DESTDIR="${pkgdir}" install
}

package_nouveau-dri-git() {
  depends=('libgl-git')
  conflicts=('nouveau-dri')
  provides=("nouveau-dri=${_mesaver}")
  pkgdesc="Mesa classic DRI + Gallium3D drivers for Nouveau"

  cd "${srcdir}/build/src/mesa/drivers/dri"
  # classic mesa driver for nv10 , nv20 nouveau_vieux_dri.so
  # Ugly hack to work around buildsystem bug
  make -C nouveau DESTDIR="${pkgdir}" INCLUDES="-I. -I../../../../../src/mesa/drivers/dri/common -Iserver -I../../../../../include -I../../../../../src/mapi -I../../../../../src/mesa -I../../../../../src/egl/main -I../../../../../src/egl/drivers/dri -I/usr/include/libdrm -I/usr/include/nouveau" install
  # gallium3D driver for nv30 - nv40 - nv50 nouveau_dri.so
  make -C "${srcdir}"/build/src/gallium/targets/dri-nouveau DESTDIR="${pkgdir}" install
}

package_mesa-git() {
  depends=('libgl-git' 'libx11' 'libxt' 'gcc-libs' 'dri2proto-git' 'libdrm-git' 'glproto-git')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  conflicts=('mesa')
  provides=("mesa=${_mesaver}")
  pkgdesc="Mesa 3-D graphics libraries and include files"

  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/lib/libGL.so"*
  rm -f "${pkgdir}/usr/lib/libGLESv"*
  rm -f "${pkgdir}/usr/lib/libEGL"*
  rm -rf "${pkgdir}/usr/lib/egl"
  rm -f ${pkgdir}/usr/lib/pkgconfig/{glesv1_cm.pc,glesv2.pc,egl.pc}
  rm -rf "${pkgdir}/usr/lib/xorg"
  rm -f "${pkgdir}/usr/include/GL/glew.h"
  rm -f "${pkgdir}/usr/include/GL/glxew.h"
  rm -f "${pkgdir}/usr/include/GL/wglew.h"
  rm -f "${pkgdir}/usr/include/GL/glut.h"
  rm -rf ${pkgdir}/usr/include/{GLES,GLES2,EGL,KHR}

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-git"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-git/"
}
