# Maintainer: Armin K. <krejzi at email dot com>
# Contributor: Kristian Klausen <klausenbusk@hotmail.com>
# Contributor: Egon Ashrafinia <e.ashrafinia@gmail.com>
# Contributor: Tavian Barnes <tavianator@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>
# Contributor: Diego Jose <diegoxter1006@gmail.com>

pkgbase=mesa-git
pkgname=mesa-git
true && pkgname=('ati-dri-git' 'intel-dri-git' 'nouveau-dri-git' 'svga-dri-git' # 'mesa-libcl-git' # clover state tracker
                 'mesa-git' 'mesa-libgl-git')

pkgver=10.1.0_devel.60267
pkgrel=1
pkgdesc="Mesa 3-D graphics library"
arch=('i686' 'x86_64')
makedepends=('python2' 'libxml2' 'libx11' 'glproto' 'libdrm' 'dri2proto' 'libxxf86vm' 'libxdamage'
             'libvdpau' 'wayland' 'elfutils' 'llvm' 'systemd' 'git' 'dri3proto' 'presentproto'
             'libxshmfence'
             # 'libclc-svn' # clover state tracker
             )

url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=('mesa::git://anongit.freedesktop.org/mesa/mesa#branch=master'
        'LICENSE')
md5sums=('SKIP'
         '5c65a0fe315dd347e09b1f2826a1df5a')

pkgver() {
    cd "${srcdir}/mesa"
    echo $(cat VERSION | tr "-" "_").$(git rev-list --count HEAD)
}

_mesaver() {
    path="${srcdir}/mesa/VERSION"
    [ -f $path ] && cat "$path"
}

build () {
    cd "${srcdir}/mesa"

    ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --with-egl-platforms=x11,drm,wayland \
    --with-llvm-shared-libs \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau \
    --disable-xvmc \
    # --enable-opencl \

    make
    # fake installation
    mkdir -p "${srcdir}/fakeinstall"
    make DESTDIR="${srcdir}/fakeinstall" install
}

package_ati-dri-git () {
  pkgdesc="Mesa drivers for AMD/ATI Radeon"
  depends=("mesa-libgl=$(_mesaver)")
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  conflicts=('ati-dri')
  provides=('ati-dri')

  install -m755 -d "${pkgdir}/usr/lib/vdpau/"
  mv -v "${srcdir}/fakeinstall/usr/lib/vdpau"/libvdpau_{r600,radeonsi}.so* "${pkgdir}/usr/lib/vdpau/"

  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/dri"
  mv -v "${srcdir}/fakeinstall/usr/lib/xorg/modules/dri"/{r200,r300,r600,radeon,radeonsi}_dri.so "${pkgdir}/usr/lib/xorg/modules/dri/"

  install -m755 -d "${pkgdir}/usr/lib/gallium-pipe"
  mv -v "${srcdir}/fakeinstall/usr/lib/gallium-pipe"/pipe_{r300,r600,radeonsi}.so "${pkgdir}/usr/lib/gallium-pipe/"

  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/ati-dri/"

  ln -s ati-dri "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_intel-dri-git () {
  pkgdesc="Mesa drivers for Intel"
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  depends=("mesa-libgl=$(_mesaver)")
  conflicts=('intel-dri')
  provides=('intel-dri')

  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/dri"
  mv -v "${srcdir}/fakeinstall/usr/lib/xorg/modules/dri"/{i915,i965}_dri.so "${pkgdir}/usr/lib/xorg/modules/dri/"

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/intel-dri/"

  ln -s intel-dri "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_nouveau-dri-git () {
  pkgdesc="Mesa drivers for Nouveau"
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  depends=("mesa-libgl=$(_mesaver)")
  conflicts=('nouveau-dri')
  provides=('nouveau-dri')

  install -m755 -d "${pkgdir}/usr/lib/vdpau/"
  mv -v "${srcdir}/fakeinstall/usr/lib/vdpau/"libvdpau_nouveau.so* "${pkgdir}/usr/lib/vdpau/"

  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/dri"
  mv -v "${srcdir}/fakeinstall/usr/lib/xorg/modules/dri"/nouveau_{dri,vieux_dri}.so "${pkgdir}/usr/lib/xorg/modules/dri/"

  install -m755 -d "${pkgdir}/usr/lib/gallium-pipe"
  mv -v "${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_nouveau.so" "${pkgdir}/usr/lib/gallium-pipe/"

  install -m755 -d "${pkgdir}/usr/share/licenses/nouveau-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/nouveau-dri/"

  ln -s nouveau-dri "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_svga-dri-git () {
  pkgdesc="Gallium3D VMware guest GL driver"
  optdepends=('libtxc_dxtn: S3 Texture Compressed support')
  depends=("mesa-libgl=$(_mesaver)")
  conflicts=('svga-dri')
  provides=('svga-dri')

  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/dri"
  mv -v "${srcdir}/fakeinstall/usr/lib/xorg/modules/dri/vmwgfx_dri.so" "${pkgdir}/usr/lib/xorg/modules/dri/"

  install -m755 -d "${pkgdir}/usr/lib/gallium-pipe"
  mv -v "${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_vmwgfx.so" "${pkgdir}/usr/lib/gallium-pipe/"

  install -m755 -d "${pkgdir}/usr/share/licenses/svga-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/svga-dri/"

  ln -s svga-dri "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_mesa-libcl-git () {
  pkgdesc="R600/Radeonsi OpenCL library"
  depends=("ati-dri-git=$(_mesaver)" 'libclc-svn')
  provides=('libcl' 'opencl-headers')
  replaces=('libcl' 'opencl-headers')
  conflicts=('libcl' 'opencl-headers')

  install -m755 -d "${pkgdir}/usr/include"
  mv -v "${srcdir}/fakeinstall/usr/include/CL" "${pkgdir}/usr/include/"

  install -m755 -d "${pkgdir}/usr/lib"
  mv -v "${srcdir}/fakeinstall/usr/lib"/libOpenCL.so* "${pkgdir}/usr/lib/"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-git () {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libvdpau' 'wayland' 'libxxf86vm' 'libxdamage' 'systemd' 'elfutils' 'llvm-libs')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=("mesa=$(_mesaver)" 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  conflicts=('mesa' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  replaces=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')

  mv -v "${srcdir}/fakeinstall"/* "${pkgdir}"

  mv "${pkgdir}/usr/lib/libGL.so.1.2.0" "${pkgdir}/usr/lib/mesa-libGL.so.1.2.0"
  rm "${pkgdir}/usr/lib"/libGL.so{,.1}

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"

  ln -s mesa "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_mesa-libgl-git () {
  pkgdesc="Mesa 3-D graphics library"
  depends=("mesa=$(_mesaver)")
  provides=("mesa-libgl=$(_mesaver)" "libgl=$(_mesaver)")
  conflicts=('mesa-libgl')
  replaces=('libgl')

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s mesa-libGL.so.1.2.0 "${pkgdir}/usr/lib/libGL.so"
  ln -s mesa-libGL.so.1.2.0 "${pkgdir}/usr/lib/libGL.so.1"
  ln -s mesa-libGL.so.1.2.0  "${pkgdir}/usr/lib/libGL.so.1.2.0"

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-libgl/"

  ln -s mesa-libgl "${pkgdir}/usr/share/licenses/${pkgname}"
}
