# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbase=libsoup
pkgname=lib32-${_pkgbase}
(( 42 )) && pkgname=('lib32-libsoup' 'lib32-libsoup-gnome')
pkgver=2.36.1
pkgrel=1
arch=('x86_64')
license=('LGPL')
makedepends=('lib32-glib2' 'lib32-libxml2' 'lib32-sqlite3' 'lib32-libgnome-keyring' 'intltool' 'gobject-introspection' 'lib32-glib-networking' 'gcc-multilib' "$_pkgbase" "$_pkgbase-gnome")
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/${pkgver%.*}/${_pkgbase}-${pkgver}.tar.xz)
options=('!libtool' '!emptydirs')
sha256sums=('73722407ff5fa7fcff2dae384aa04d85e7900c0e3fed46389f1289a1409e1f65')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libdir=/usr/lib32 --enable-introspection=no
      
  make
}

package_lib32-libsoup() {
  pkgdesc="GNOME HTTP Library - base library (32 bit)"
  depends=('lib32-glib2' 'lib32-libxml2' 'lib32-glib-networking' "$_pkgbase")

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib32/libsoup-gnome-2.4.*
  rm -f "${pkgdir}/usr/lib32/pkgconfig/libsoup-gnome-2.4.pc"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/share"
  #rm -f "${pkgdir}/usr/lib32/girepository-1.0/SoupGNOME-2.4.typelib"
}

package_lib32-libsoup-gnome() {
  pkgdesc="GNOME HTTP Library - GNOME libraries (32 bit)"
  depends=("lib32-libsoup=${pkgver}" 'lib32-libgnome-keyring' 'lib32-sqlite3' "$_pkgbase-gnome")

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib32/libsoup-2.4.*
  rm -f "${pkgdir}/usr/lib32/pkgconfig/libsoup-2.4.pc"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/share"
  #rm -f "${pkgdir}/usr/lib32/girepository-1.0/Soup-2.4.typelib"
}
