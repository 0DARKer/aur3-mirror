# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libsoup
pkgname=lib32-${_pkgbase}
(( 42 )) && pkgname=('lib32-libsoup' 'lib32-libsoup-gnome')
pkgver=2.38.1
pkgrel=1
arch=('x86_64')
license=('LGPL')
url="http://www.gnome.org"
makedepends=('lib32-glib2' 'lib32-libxml2' 'lib32-sqlite3' 'lib32-libgnome-keyring' 'intltool' 'lib32-glib-networking' 'gcc-multilib' "$_pkgbase" "$_pkgbase-gnome") # 'gobject-introspection'
_ver=${pkgver%.*}
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/${_ver}/${_pkgbase}-${pkgver}.tar.xz)
options=('!libtool' '!emptydirs')
sha256sums=('71b8923fc7a5fef9abc5420f7f3d666fdb589f43a8c50892d584d58b3c513f9a')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${_pkgbase}-${pkgver}"  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libdir=/usr/lib32 --enable-introspection=no      
  make
}

package_lib32-libsoup() {
  pkgdesc="GNOME HTTP Library - base library (32 bit)"
  depends=('lib32-glib2' 'lib32-libxml2' "${_pkgbase}")

  cd "${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib32/libsoup-gnome-2.4.*
  rm -f "${pkgdir}"/usr/lib32/pkgconfig/libsoup-gnome-2.4.pc
  rm -f "${pkgdir}"/usr/lib32/girepository-1.0/SoupGNOME-2.4.typelib
  rm -rf "${pkgdir}"/usr/include
  rm -rf "${pkgdir}"/usr/share
}

package_lib32-libsoup-gnome() {
  pkgdesc="GNOME HTTP Library - GNOME libraries (32 bit)"
  depends=("lib32-libxml2" 'lib32-libgnome-keyring' 'lib32-sqlite3' "$_pkgbase}-gnome")

  cd "${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib32/libsoup-2.4.*
  rm -f "${pkgdir}"/usr/lib32/pkgconfig/libsoup-2.4.pc
  rm -f "${pkgdir}"/usr/lib32/girepository-1.0/Soup-2.4.typelib
  rm -rf "${pkgdir}"/usr/include
  rm -rf "${pkgdir}"/usr/share
}
