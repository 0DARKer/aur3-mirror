# Maintainer: Army
pkgname=dunst-git
pkgver=20130226
pkgrel=1
pkgdesc="a lightweight replacement for the notification-daemons provided by most desktop environments"
arch=('i686' 'x86_64')
url="http://www.knopwob.org/dunst"
license=('MIT')
depends=('libxinerama' 'libxss' 'pango')
makedepends=('git' 'perl')
if [ -e $startdir/config.h ]
then
  makedepends+=('libxdg-basedir')
else
  depends+=('libxdg-basedir')
fi
optdepends=('libnotify: dunst is able to listen to notifications from libnotify')
provides=('dunst' 'notification-daemon')
conflicts=('dunst')

_gitroot="https://github.com/knopwob/dunst.git"
_gitname="dunst"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  
  if [ -e $startdir/config.h ]
  then
	  msg "use custom config.h"
	  cp ${startdir}/config.h .
	  CFLAGS=-DSTATIC_CONFIG make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
  else
	  msg "use default config.h"
	  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
  fi
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" PREFIX=/usr install
  install -Dm644 "${srcdir}/dunst-build/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
