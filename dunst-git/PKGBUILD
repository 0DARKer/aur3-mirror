# Maintainer: Army
_pkgname=dunst
pkgname=$_pkgname-git
pkgver=0.5.0.143.gdb27662
pkgrel=1
pkgdesc="a lightweight replacement for the notification-daemons provided by most desktop environments"
arch=('i686' 'x86_64')
url="http://www.knopwob.org/dunst"
license=('MIT')
depends=('libxinerama' 'libxss' 'pango')
makedepends=('git' 'perl')
optdepends=('libnotify: dunst is able to listen to notifications from libnotify')
if [ -e $SRCDEST/config.h ];then optdepends+=('libxdg-basedir: if you want to use dunstrc for configuration');else depends+=('libxdg-basedir');fi
provides=('dunst' 'notification-daemon')
conflicts=('dunst')
source=("$_pkgname::git+https://github.com/knopwob/dunst.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed -e 's|-|.|g' -e '1s|^.||'
}

prepare() {
  cd "$srcdir/$_pkgname"
  if [ -e $SRCDEST/config.h ];then msg "using custom config.h";cp $SRCDEST/config.h .;fi
}

build() {
  cd "$srcdir/$_pkgname"
  if [ -e $SRCDEST/config.h ];then CFLAGS=-DSTATIC_CONFIG make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11;else make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11;fi
}

package() {
  cd "$srcdir/$_pkgname"
  if [ -e $SRCDEST/config.h ];then CFLAGS=-DSTATIC_CONFIG make DESTDIR="$pkgdir/" PREFIX=/usr install;else make DESTDIR="$pkgdir/" PREFIX=/usr install;fi
  install -Dm644 "$srcdir/$_pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
