# Maintainer: Army

_pkgname=dunst
pkgname=${_pkgname}-git
pkgver=20120622
pkgrel=1
pkgdesc="dmenu-ish notification system "
arch=(i686 x86_64)
url="http://knopwob.github.com/dunst"
license=(MIT)
depends=(dbus-core libxinerama libxft libxss)
makedepends=('git' 'perl')
optdepends=('libnotify: dunst is able to listen to notifications from libnotify')
provides=(${_pkgname} notification-daemon)
conflicts=(${_pkgname})

_gitroot="https://github.com/knopwob/dunst.git"
_gitname="${_pkgname}"

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."
	
	if [ -d ${_gitname} ] ; then
		cd ${_gitname}
		git pull
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi
	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "${srcdir}/${_gitname}-build"
	cp -a "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
	cd "${srcdir}/${_gitname}-build"
	
	sed -i \
		-e 's/^CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/^CFLAGS   =/CFLAGS   +=/g' \
		-e 's/^LDFLAGS  =/LDFLAGS  +=/g' \
		config.mk
	
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${_gitname}-build"
	make PREFIX=${pkgdir}/usr install
	install -Dm644 "${srcdir}/dunst-build/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
