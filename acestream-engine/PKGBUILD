# Contributor: MacCyber <jonas.enge at gmail dot com>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Antti Hautaniemi <an7oine at me com>

pkgname=acestream-engine
pkgver=3.0.3
pkgrel=2
_ubuntuver=14.04
pkgdesc="ACE Stream engine"
arch=('i686' 'x86_64')
url="http://acestream.org/"
license=('unknown')
depends=('python2-setuptools' 'python2-xlib' 'python2-m2crypto' 'python2-apsw')
install='acestream-engine.install'
source=('acestream-engine.service'
        "http://dl.acestream.org/ubuntu/${_ubuntuver%.*}/acestream_${pkgver}_ubuntu_${_ubuntuver}_$CARCH.tar.gz")
sha256sums=('b9863a9dd3ee6d41d18475f5f539107fe81a573f45ca1cb98013441f955f1af0')
if [[ "$CARCH" == "x86_64" ]]; then
  sha256sums+=('1970babe53f54d0587746e906db02485702cbeb1a97af52e118a8fc892eb6f74')
elif [[ "$CARCH" == "i686" ]]; then
  sha256sums+=('06f329915273c3adbc2d7c2c23bbeddd3d244c837d0817acaa8c90bd648b6753')
fi

package() {
  cd "$srcdir/acestream_${pkgver}_ubuntu_${_ubuntuver}_$CARCH"
  install -Dm755 acestreamengine "$pkgdir/usr/bin/acestreamengine"
  install -d "$pkgdir/usr/share/acestream/"
  cp -a data lib "$pkgdir/usr/share/acestream/"
  install -Dm644 "$srcdir/acestream-engine.service" "$pkgdir/usr/lib/systemd/system/acestream-engine.service"
}
