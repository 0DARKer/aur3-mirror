# Maintainer: oi_wtf <brainpower at gulli dot com>

pkgname=sfgui-git
pkgver=0.2.3.r1.g5a17f40
pkgrel=2
pkgdesc="SFGUI (Simple and Fast Graphical User Interface), a C++ GUI library for SFML."
arch=('i686' 'x86_64')
url="http://sfgui.sfml-dev.org/"
license=('zlib')
depends=('sfml-git')
makedepends=('cmake' 'git')
provides=('sfgui')
conflicts=('sfgui')
replaces=('sfgui')

source=("git+https://github.com/TankOs/SFGUI.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/SFGUI"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/SFGUI"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir/SFGUI"

  make DESTDIR="$pkgdir/" install

  install -Dm644 "${srcdir}/SFGUI/cmake/FindSFGUI.cmake" "$pkgdir/usr/share/cmake-3.1/Modules/FindSFGUI.cmake"

  install -Dm644 ./LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 ./FONT.LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/FONT.LICENSE

}

# vim:set ts=2 sw=2 et:
