# Maintainer: graysky <graysky AT archlinux DOT org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Austin ( doorknob60 [at] gmail [dot] com )

pkgname=broadcom-wl-ck
pkgver=5.100.82.112
pkgrel=15
_pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver for linux-ck.'
pkgdesc="${_pkgdesc}"
arch=('i686' 'x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux-ck>=3.4' 'linux-ck<3.5')
makedepends=('linux-ck-headers>=3.4' 'linux-ck-headers<3.5')
#replaces=()
#conflicts=()
#groups=('ck-generic')

[[ $CARCH = x86_64 ]] && ARCH=x86_64 || ARCH=x86_32
source=("http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz"
'bc_wl_abiupdate.patch'
'modprobe.d'
'linux3.patch'
'license.patch'
'semaphore.patch'
'linux3.4.0.patch')
sha1sums=('5bd78c20324e6a4aa9f3fafdc6f0155e884d5131'
          '1725d3352e294388a1fbe78696a147265525a30f'
          '89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
          '3e18f905bbe5e8b99a53d6ecb3b80a919f3531f2'
          'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
          '105f8e1d48ebe1f25d53859e5ab9326a27435c66'
          'b00fa2f9acf5bcce923f9ee20f9a22740cb0ef3a')
[[ $CARCH = x86_64 ]] && sha1sums[0]='5bd78c20324e6a4aa9f3fafdc6f0155e884d5131'
backup=('etc/modprobe.d/broadcom-wl_ck.conf')
install=broadcom-wl-ck.install

_extramodules=extramodules-3.4-ck
_kernver="$(cat /lib/modules/${_extramodules}/version)"

build() {
	cd "${srcdir}"
	patch -p1 -i linux3.patch
	patch -p1 -i license.patch
	patch -p1 -i semaphore.patch
	patch -p1 -i bc_wl_abiupdate.patch
	patch -p2 -i linux3.4.0.patch
	make -C /lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"
	install -D -m 644 wl.ko "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	gzip "${pkgdir}/lib/modules/${_extramodules}/wl.ko"
	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl_ck.conf
}
