# Maintainer: graysky <graysky AT archlinux DOT org>
# Maintainer: Armin K. <krejzi at email dot com>
# Contributor: Austin ( doorknob60 [at] gmail [dot] com )
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=broadcom-wl-ck
pkgver=6.30.223.141
pkgrel=20
_pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver for linux-ck.'
_extramodules="extramodules-3.14-ck"
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgdesc="${_pkgdesc}"
arch=('i686' 'x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux-ck>=3.14' 'linux-ck<3.15')
makedepends=('linux-ck-headers>=3.14' 'linux-ck-headers<3.15')
conflicts=('broadcom-wl-ck-atom' 'broadcom-wl-ck-barcelona' 'broadcom-wl-ck-bulldozer' 'broadcom-wl-ck-corex' 'broadcom-wl-ck-core2' 'broadcom-wl-ck-haswell' 'broadcom-wl-ck-ivybridge' 'broadcom-wl-ck-kx' 'broadcom-wl-ck-k10' 'broadcom-wl-ck-nehalem' 'broadcom-wl-ck-p4' 'broadcom-wl-ck-piledriver' 'broadcom-wl-ck-pentm' 'broadcom-wl-ck-sandybridge')
#replaces=()
#groups=('ck-generic')

[[ $CARCH = x86_64 ]] && _arch=_64 || _arch=
source=("http://www.broadcom.com/docs/linux_sta/hybrid-v35${_arch}-nodebug-pcoem-${pkgver//./_}.tar.gz"
'modprobe.d'
'license.patch'
'linux-recent.patch'
'gcc.patch')
sha256sums=('5f37b2b879e29b220dc64ce2e93d922dc231d4241da03bcbab15ced10e649b4a'
            'b4aca51ac5ed20cb79057437be7baf3650563b7a9d5efc515f0b9b34fbb9dc32'
            '24d02018e4e5b90516ba3e98bd957ad91e758944c8a21e22d56a516f2361c6f9'
            'a20091e9bae625ee34aecb3900a402506229781bb709124208322ef9601edb4f'
            'fac1bdde7b837aaf96a46feb28a16ae8f5744ed0c6270ed959a26089d8bda963')
[[ $CARCH = x86_64 ]] && sha256sums[0]='5f37b2b879e29b220dc64ce2e93d922dc231d4241da03bcbab15ced10e649b4a'
install=broadcom-wl-ck.install

prepare() {
  patch -p1 -i linux-recent.patch
  patch -p1 -i license.patch
	patch -p1 -i gcc.patch

	sed -e "/BRCM_WLAN_IFNAME/s:eth:wlan:" \
		-i src/wl/sys/wl_linux.c
}


build() {
	make -C /usr/lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	install -Dm644 wl.ko "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"

	# makepkg does not do this automatically for this pkg so do it here
	gzip -9 "${pkgdir}/usr/lib/modules/${_extramodules}/wl.ko"
	install -Dm644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 modprobe.d "${pkgdir}/usr/lib/modprobe.d/broadcom-wl_ck.conf"
}
