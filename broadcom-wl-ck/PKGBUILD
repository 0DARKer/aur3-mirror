# Maintainer: graysky <graysky AT archlinux DOT org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Austin ( doorknob60 [at] gmail [dot] com )

pkgname=broadcom-wl-ck
pkgver=5.100.82.111
pkgrel=1
_kernver_ck='3.0-ck'
_pkgdesc='Broadcom 802.11abgn hybrid Linux networking device driver for linux-ck.'
pkgdesc="${_pkgdesc}"
arch=('i686' 'x86_64')
url='http://www.broadcom.com/support/802.11/linux_sta.php'
license=('custom')
depends=('linux-ck>=3.0' 'linux-ck<3.1')
makedepends=('linux-ck-headers>=3.0' 'linux-ck-headers<3.1')
_kernver="3.0-ck"
#conflicts=()
#groups=('ck-generic')

[[ $CARCH = x86_64 ]] && ARCH=x86_64 || ARCH=x86_32
source=("http://www.broadcom.com/docs/linux_sta/hybrid-portsrc_${ARCH}-v${pkgver//./_}.tar.gz"
'modprobe.d'
'license.patch'
'semaphore.patch')

sha1sums=('af23f31679973a1bcacea79b65d10dbe231f8862'
'89bf92286ede30dd85304c6c4e42e89cfdc0f60a'
'ea7b67982ddc0f56fd3becb9914fd4458fe7d373'
'105f8e1d48ebe1f25d53859e5ab9326a27435c66')
[[ $CARCH = x86_64 ]] && sha1sums[0]='8028950609631106c7844bb039a29f78e92baae7'

install=install.install

build() {
	cd "${srcdir}"
	patch -p1 -i license.patch
	patch -p1 -i semaphore.patch
	make -C /lib/modules/"${_kernver}"/build M=`pwd`
}

package() {
	cd "${srcdir}"
	install -D -m 755 wl.ko "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/net/wireless/wl.ko"
	install -D -m 644 lib/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -D -m 644 modprobe.d "${pkgdir}"/etc/modprobe.d/broadcom-wl.conf
}
