# Maintainer: Dennis Fink <the_metalgamer@hackerspace.lu>

pkgname=python2-acrylamid-git
pkgver=0.7.47.g78b46a1
pkgrel=1
pkgdesc="static blog generator in python with incremental rendering. Python2 version "
arch=('any')
url="http://posativ.org/acrylamid/"
license=('BSD')
depends=('python2-jinja>=2.4' 'python2-markdown>=2.0.1' 'python2-translitcodec>=0.2')
makedepends=('python2-distribute' 'git')
optdepends=('python2-pygments: Syntax Highlighting'
'python2-docutils: reStructuredText'
'python2-smartypants: Typography enhancements'
'python2-asciimathml: AsciiMathML to MathML'
'python2-unidecode: cyrillic/chinese ascii slugs'
'python2-textile: Textile'
'python2-yaml: exact YAML parser'
'python2-twitter-tools: Twitter'
'discount: Discount'
'python2-mako: Mako Templating'
)
source=('git://github.com/posativ/acrylamid.git')
provides=('python2-acrylamid')
conflicts=('python2-acrylamid')
md5sums=('SKIP')

_gitname=acrylamid

pkgver() {
  cd "$srcdir"/$_gitname
  git describe --always | sed 's|-|.|g'
}

build() {

  cd "$srcdir"/$_gitname
  python2 setup.py build

}

package() {

  cd "$srcdir"/$_gitname

  python2 setup.py install --root="$pkgdir" --optimize=1

  cd "$pkgdir"/usr/bin
  mv acrylamid acrylamid-python2
  install -D -m644 "$srcdir"/$_gitname/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

}

# vim:set ts=2 sw=2 et:
