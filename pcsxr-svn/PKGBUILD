# Maintainer: Eugene Yudin aka Infy <Eugene dot Yudin at gmail dot com>
# Thanks to lucak3, armitage, kazuo

pkgname=pcsxr-svn
pkgver=73194
pkgrel=1
pkgdesc="A Sony PlayStation emulator based on the PCSX-df Project"
arch=('i686' 'x86_64')
url="http://pcsxr.codeplex.com/"
license=('GPL')
depends=('libcdio' 'libgl' 'libglade' 'libxtst' 'libxv' 'sdl')
makedepends=('subversion' 'nasm' 'intltool' 'mesa')
provides=('pcsx')
conflicts=('pcsx' 'pcsx-df' 'pcsxr' 'pcsxr-alsa')
replaces=('pcsxr-alsa-svn')
options=(!libtool)
install=pcsxr.install

if [ "$CARCH" == "x86_64" ]
then
  depends=('libcdio' 'libglade' 'libxtst' 'libxv' 'sdl')
  makedepends=('subversion' 'nasm' 'intltool')
fi

_svntrunk=https://pcsxr.svn.codeplex.com/svn/pcsxr
_svnmod=pcsxr

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r ${pkgver})
  else
    svn co $_svntrunk --config-dir ./ -r ${pkgver} $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ./autogen.sh

  if [ "$CARCH" == "x86_64" ]
  then
    ./configure --prefix=/usr --enable-libcdio
  else
    ./configure --prefix=/usr --enable-libcdio --enable-opengl
  fi

  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}
