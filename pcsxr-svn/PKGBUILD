# Maintainer: Eugene Yudin aka Infy <Eugene dot Yudin at gmail dot com>
# Thanks to lucak3

pkgname=pcsxr-svn
pkgver=62027
pkgrel=1
pkgdesc="A Sony PlayStation emulator based on the PCSX-df Project"
arch=('i686' 'x86_64')
url="http://pcsxr.codeplex.com/"
license=('GPL')
depends=('libcdio' 'libgl' 'libglade' 'libxtst' 'libxv' 'libxxf86vm' 'sdl')
makedepends=('subversion' 'nasm')
provides=('pcsx')
conflicts=('pcsx' 'pcsx-df' 'pcsxr' 'pcsxr-alsa')
replaces=('pcsxr-alsa-svn')
options=(force !libtool)
install=pcsxr.install

_svntrunk=https://pcsxr.svn.codeplex.com/svn/pcsxr
_svnmod=pcsxr

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up)
  else
    svn co $_svntrunk --config-dir ./ $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ./autogen.sh
  ./configure --prefix=/usr --enable-libcdio --enable-opengl
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}