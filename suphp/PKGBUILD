pkgname=suphp
pkgver=0.7.1
pkgrel=3
pkgdesc="A tool for executing PHP scripts with the permissions of their owners."
arch=('i686' 'x86_64')
depends=("apr-util" "apache")
url="http://www.suphp.org/"
license=('GPL')
backup=('etc/suphp/suphp.conf')
source=(http://www.suphp.org/download/$pkgname-$pkgver.tar.gz
        suphp.conf)
md5sums=('c172dd4f15a75f4dcb08ea97d4202bb8'
         'a5a2279d41630ca2de1ee559dab00467')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --with-apr=/usr/bin/apr-1-config --sysconfdir=/etc/suphp --with-apache-user=http --with-setid-mode=owner --with-min-uid=33 --with-min-gid=33
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -D -m644 "$srcdir/suphp.conf" "$pkgdir/etc/suphp/suphp.conf"
  install -D -m644 "$srcdir/$pkgname-$pkgver/doc/suphp.conf-example" "$pkgdir/etc/suphp/suphp.conf.example"
}

# vim:set ts=2 sw=2 et:
