# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Bob Finch <w9ya@qrparci.net>

pkgname=linsmith
pkgver=0.99.21
pkgrel=1
pkgdesc="A Smith Charting program, mainly designed for educational use"
arch=('i686' 'x86_64')
url="http://jcoppens.com/soft/linsmith/index.en.php"
license=('GPL2')
depends=('tk' 'libgnomeui')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('70451da01bc18b28904be79aefdbdcbd'
         'a5633df9cfceaf7d515a4a3e3e10153d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # desktop and pixmaps
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 linsmith_icon.xpm "$pkgdir/usr/share/pixmaps/linsmith.xpm"
}
