# Maintainer: Olaf Bauer <hydro@freenet.de>

pkgname=makemkv
pkgver=1.8.10
pkgrel=1
pkgdesc="DVD and Blu-ray to MKV converter and network streamer"
arch=('i686' 'x86_64')
url="http://www.makemkv.com"
license=('LGPL' 'MPL' 'custom')
depends=('qt4' 'icu' 'ffmpeg')
if [ "$CARCH" = "x86_64" ]; then
  optdepends=('lib32-glibc: dts support')
fi
install=makemkv.install
source=(${url}/download/${pkgname}-bin-${pkgver}.tar.gz
        ${url}/download/${pkgname}-oss-${pkgver}.tar.gz)
md5sums=('ffd6a19151329822f9e15aeb158389c1'
         'da4114455f7342f567fa68aaa80e70d1')

build() {
  cd $srcdir/${pkgname}-oss-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/${pkgname}-oss-${pkgver}
  make DESTDIR="$pkgdir" install

  cd $srcdir/${pkgname}-bin-${pkgver}
  install -d $pkgdir/usr/bin/
  install -t $pkgdir/usr/bin/ bin/i386/mmdtsdec
  if [ "$CARCH" = "x86_64" ]; then
    install -t $pkgdir/usr/bin/ bin/amd64/makemkvcon
  else
    install -t $pkgdir/usr/bin/ bin/i386/makemkvcon
  fi
  install -d $pkgdir/usr/share/MakeMKV
  install -t $pkgdir/usr/share/MakeMKV src/share/makemkv_*.mo.gz src/share/*.mmcp.xml
  
  install -Dm 644 src/eula_en_linux.txt $pkgdir/usr/share/licenses/$pkgname/eula_en_linux.txt
}
