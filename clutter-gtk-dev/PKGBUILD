# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=clutter-gtk-dev
_pkgname=clutter-gtk
pkgver=0.11.4
pkgrel=1
pkgdesc="GTK clutter widget"
arch=('i686' 'x86_64')
url="http://clutter-project.org/"
options=('!libtool')
license=('LGPL')
depends=('clutter' 'gtk2')
makedepends=('gobject-introspection' 'gtk-doc')
provides=("clutter-gtk=${pkgver}")
conflicts=('clutter-gtk')
source=(http://www.clutter-project.org/sources/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.bz2)
sha256sums=('58d5b027c4589ff442d820f624d7991e25cbeaab795b05f2df2e4c7bc1af2cf6')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
