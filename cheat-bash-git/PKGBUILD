# Maintainer: Prurigro

_pkgname=cheat
pkgname=${_pkgname}-bash-git
pkgver=20140522.r26.f72e797
pkgrel=1
pkgdesc="A Bash reimplementation of a command line tool to create and view interactive cheatsheets"
url="https://github.com/jahendrie/${_pkgname}"
license=('GPL3')
arch=('any')
depends=('bash')
makedepends=('make')
source=("git://github.com/jahendrie/${_pkgname}.git#branch=master")
sha512sums=('SKIP')

prepare() {
    sed -i 's|PREFIX=/usr||' cheat/Makefile
}

pkgver() {
    cd $_pkgname
    printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd $_pkgname
    PREFIX="${pkgdir}/usr" make install
}
