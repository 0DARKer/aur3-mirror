# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname='profile-sync-daemon-git'
_pkgname='profile-sync-daemon'
pkgver=329.24711ff
pkgrel=1
pkgdesc='Syncs browser profiles to tmpfs reducing SSD/HDD calls and speeding-up browsers. Git version.'
arch=('any')
url='https://github.com/graysky2/profile-sync-daemon'
license=('MIT')
depends=('bash' 'binutils' 'coreutils' 'gawk' 'glibc' 'grep' 'procps-ng'
'ncurses' 'rsync' 'sed')
conflicts=('firefox-sync' 'goanysync' 'go-anysync-git' 'iceweasel-sync'
'tmpfs-store' 'tmpfs-sync' 'user-profile-sync-daemon' 'profile-sync-daemon')
provides=('profile-sync-daemon')
source=("git://github.com/graysky2/profile-sync-daemon#branch=unstable")
backup=('etc/psd.conf')
install=psd.install
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
	cd "$_pkgname"
	make
}

package() {
	cd "$_pkgname"
	make DESTDIR="$pkgdir" install-systemd-all
	install -Dm644 MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
