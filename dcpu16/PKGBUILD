# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=dcpu16
pkgver=998bb5f
pkgrel=2
pkgdesc='DCPU-16 emulator, written in Go'
arch=('x86_64' 'i686')
url='https://github.com/dcpu16/dcpu16-go'
license=('unknown')
makedepends=('go' 'go-termbox')
options=('!strip')
source=("$pkgname.zip::https://nodeload.github.com/dcpu16/dcpu16-go/zipball/master")
sha256sums=('298b65741c51722f425abba6a64919675f48c113e0b2019f69f672bc5bb13b5c')

build() {
  cd "$srcdir/$pkgname-$pkgname-go-$pkgver"

  msg2 "Building emulator..."
  go build -o "$pkgname" dcpu16.go video.go main.go

  msg2 "Building assembler..."
  go build -o "${pkgname}asm" dcpu16.go assembler.go

  msg2 "Building disassembler..."
  go build -o "${pkgname}disasm" dcpu16.go disassembler.go
}

package() {
  cd "$srcdir/$pkgname-$pkgname-go-$pkgver"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm755 "${pkgname}asm" "$pkgdir/usr/bin/${pkgname}asm"
  install -Dm755 "${pkgname}disasm" "$pkgdir/usr/bin/${pkgname}disasm"

  # No license yet
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
