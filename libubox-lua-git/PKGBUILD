# Maintainer: Luka Perkov <luka.perkov@sartura.hr>

pkgname=libubox-lua-git
_gitname=libubox
pkgver=r248.e58fa88
# commit e58fa88e1f69557203ddc616bed95b9817c4f7d0
pkgrel=1
pkgdesc='A small utility library developed for OpenWrt containing a event loop, linked lists, blobmsg, stream buffer management, [...]'
url='http://nbd.name/gitweb.cgi?p=luci2/libubox.git'
arch=('i686' 'x86_64')
license=('ISC BSD-3c')
depends=('json-c' 'lua51')
makedepends=('git' 'cmake' 'gcc' 'make')
conflicts=('libubox' 'libubox-git')
provides=('libubox')
source=('git://nbd.name/luci2/libubox.git' '001-lua-version.patch')
md5sums=('SKIP' '34e0767e6d1df56d188b1e18d91ef46f')

pkgver() {
	cd "$srcdir/$_gitname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_gitname"

	patch -p1 -i "$srcdir/001-lua-version.patch"
}

build() {
	cd "$srcdir/$_gitname"

	cmake CMakeLists.txt \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_LUA=ON

	make
}

package() {
	cd "$srcdir/$_gitname"

	make DESTDIR="$pkgdir" install
}
