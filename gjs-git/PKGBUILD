# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=gjs
pkgname=$_pkgname-git
pkgver=1.39.3.r24.g4ee2126
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gjs"
license=('GPL')
depends=('cairo' 'dbus-glib' "gobject-introspection>=1.38.0" "js24")
options=('!libtool')
replaces=('gjs')
provides=("gjs=1.39.3")
conflicts=('gjs')
source=('git://git.gnome.org/gjs')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed -E 's/^GJS_//;s/_/./g;s/([^-]*-g)/r\1/;s|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}
