#Maintainer: Alex Ferrando <alferpal@gmail.com>

pkgname=eclipse-ptp
pkgver=8.0.0
pkgrel=1
pkgdesc="Parallel developer tools for Eclipse"
arch=('i686' 'x86_64')
url="http://www.eclipse.org/ptp/"
license=('EPL')
depends=('eclipse')
source=("http://download.eclipse.org/tools/ptp/updates/luna/ptp-master-${pkgver}.201406102209.zip")

md5sums=('1e182a55c78c253d6a7e6bca4296c3c5')

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
