# Maintainer: Jason St. John <jstjohn .. purdue . edu>

_perlmod=Statistics-Descriptive
_modnamespace=Statistics
pkgname=perl-statistics-descriptive
pkgver=3.0605
pkgrel=1
pkgdesc="Statistics::Descriptive - Module of basic descriptive statistical functions."
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0')
makedepends=('perl-test-pod-coverage')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('c22decf0ac550d6b9d757aac5260d5dc9e446654ab11647d8f39f586274f4a73417eb2eb0c7cf916f986982c887dc44f2758f4158e9cec33dcc50e2ad4cb4f1d')

build() {
	cd "$srcdir/${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	perl Build.PL installdirs=vendor destdir="$pkgdir/"
	perl Build
}

check() {
	cd "$srcdir/${_perlmod}-${pkgver}"
	perl Build test
}

package() {
	cd "$srcdir/${_perlmod}-${pkgver}"
	perl Build install
}
