# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=perl-statistics-descriptive
_pkgname=Statistics-Descriptive
pkgver=3.0603
pkgrel=1
pkgdesc="Statistics::Descriptive - Module of basic descriptive statistical functions."
arch=('any')
url="http://search.cpan.org/dist/$_pkgname"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0')
makedepends=('perl-test-pod-coverage')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/SH/SHLOMIF/$_pkgname-$pkgver.tar.gz)
sha512sums=('29b740e543fa6d2804a2bd69aeac2f24edad4c36ce6fabfb2b35f8bdac436bb9372014084d4a59b0790e5f2391ff0483c1b3d11af4423303727142102f3153ba')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  perl Build.PL installdirs=vendor destdir="$pkgdir/"
  perl Build
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  perl Build test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  perl Build install
}

# vim:set ts=2 sw=2 et:
