# Maintainer : Jason St. John <jstjohn .. purdue . edu>

pkgname=perl-statistics-descriptive
_pkgname=Statistics-Descriptive
pkgver=3.0300
pkgrel=2
pkgdesc="Statistics::Descriptive - Module of basic descriptive statistical functions."
arch=('any')
url="http://search.cpan.org/dist/$_pkgname"
license=('PerlArtistic' 'GPL')
depends=('perl')
makedepends=('perl-test-pod-coverage')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/SH/SHLOMIF/$_pkgname-$pkgver.tar.gz)
sha512sums=('38264571b870e3546b565d7064c55e2da1cef7bc5a2162fcdc9d6113ad8e17f7107691d59d8e9874ebcacdb0908c5c695665cf8734c54f537cd2ff79413fb84a')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/$_pkgname-$pkgver"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
