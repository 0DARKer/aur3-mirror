# Maintainer: Jason St. John <jstjohn .. purdue . edu>

_perlmod=Statistics-Descriptive
_modnamespace=Statistics
pkgname=perl-statistics-descriptive
pkgver=3.0607
pkgrel=2
pkgdesc="Statistics::Descriptive - Module of basic descriptive statistical functions."
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('GPL' 'PerlArtistic')
depends=('perl-list-moreutils')
makedepends=('perl-test-pod-coverage')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('6a0ec5e0d5b6a0c6566155b72ce89d024612a7eb1a5ad84ef1bafd89698bf78ca4edfce45c00e798179b6a3df83490d0d823fbcefb47f8e41d5c9e35866a84d1')

build() {
	cd "${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	perl Build.PL installdirs=vendor destdir="${pkgdir}"
	perl Build
}

check() {
	cd "${_perlmod}-${pkgver}"
	perl Build test
}

package() {
	cd "${_perlmod}-${pkgver}"
	perl Build install
}
