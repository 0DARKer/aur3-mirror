# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=ropgadget
_pkgname=ROPgadget
pkgver=5.3
pkgrel=1
pkgdesc="Search gadgets in binaries to facilitate ROP exploitation for several file formats and architectures"
arch=('any')
url="http://www.shell-storm.org/project/ROPgadget/"
license=('GPL2')
depends=('python2-capstone')
source=(https://github.com/JonathanSalwan/${_pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('809090fe07705b222923b64585c2447a2d7fd3b15468be614e3d0b776966c143')

check() {
  cd ${_pkgname}-${pkgver}
  ./ROPgadget.py --binary ./test-suite-binaries/elf-Linux-x86 --string "main"|grep main
}

package() {
  cd ${_pkgname}-${pkgver}
  install -Dm 755 ROPgadget.py "${pkgdir}/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
