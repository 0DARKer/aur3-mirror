# Contributor: David Spicer <azleifel at googlemail dot com>

pkgname=vdr-rssreader
_pluginname=rssreader
pkgver=1.7.3
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="An RSS Reader plugin for the Video Disk Recorder (VDR)"
url="http://www.saunalahti.fi/rahrenbe/vdr/rssreader/"
license=('GPL2')
depends=('vdr=1.7.42' 'curl' 'expat')
backup=('var/lib/vdr/plugins/rssreader/rssreader.conf')
install=${pkgname}.install
source=(http://www.saunalahti.fi/rahrenbe/vdr/rssreader/files/${pkgname}-${pkgver}.tgz)
md5sums=('ba4a40ccdf4b7dd28243d68f8937ce0a')

build() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  # VDR directory environment
  _vdrCONFDIR="/var/lib/vdr"
  _vdrINCDIR="/usr/include"
  _vdrLIBDIR="/usr/lib/vdr"
  _vdrLOCDIR="/usr/share/locale"

  make DESTDIR="${pkgdir}" install

  # Install configuration file
  install -d -m755 "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"
  install -m644 rssreader/rssreader.conf "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}"

  # Install documents
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  for _file in README HISTORY; do
    install -m644 ${_file} "${pkgdir}/usr/share/doc/${pkgname}/${_file##*/}"
  done
}
