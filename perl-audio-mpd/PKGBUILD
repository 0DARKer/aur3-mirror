# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-audio-mpd
_pkgname=Audio-MPD
pkgver=1.120610
pkgrel=1
pkgdesc="Audio::MPD - class to talk to MPD (Music Player Daemon) servers"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.010' 'perl-audio-mpd-common' 'perl-getopt-euclid' 'perl-io-socket-ip' 'perl-moose' 'perl-moosex-has-sugar' 'perl-moosex-semiaffordanceaccessor' 'perl-proc-daemon')
makedepends=('perl-test-corpus-audio-mpd>=1.113282')
url="http://search.cpan.org/dist/$_pkgname"
source=(http://search.cpan.org/CPAN/authors/id/J/JQ/JQUELIN/$_pkgname-$pkgver.tar.gz)
sha512sums=('835ddef3ba75aae1cc183ff427c6163bbb5696f43bc2875abd0c46388f15869d51dda4d2093534567c7401e0a1208d901428290f86e6de1bcaa5357999c11811')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/$_pkgname-$pkgver"
  $PERL Build.PL
  $PERL Build
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  $PERL Build test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  $PERL Build install
}

# vim:set ts=2 sw=2 et:
