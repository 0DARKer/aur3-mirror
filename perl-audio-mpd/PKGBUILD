# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Justin Davis <jrcd83@gmail.com>

_perlmod=Audio-MPD
_modnamespace=Audio
pkgname=perl-audio-mpd
pkgver=1.120610
pkgrel=6
pkgdesc="Audio::MPD - class to talk to MPD (Music Player Daemon) servers"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl-audio-mpd-common' 'perl-getopt-euclid' 'perl-io-socket-ip' 'perl-moose' 'perl-moosex-has-sugar' 'perl-moosex-semiaffordanceaccessor' 'perl-proc-daemon')
makedepends=('perl-module-build>=0.3601' 'perl-test-corpus-audio-mpd>=1.113282')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('835ddef3ba75aae1cc183ff427c6163bbb5696f43bc2875abd0c46388f15869d51dda4d2093534567c7401e0a1208d901428290f86e6de1bcaa5357999c11811')

build() {
	cd "${_perlmod}-${pkgver}"

	perl Build.PL installdirs=vendor destdir="${pkgdir}/"
	perl Build
}

check() {
	cd "${_perlmod}-${pkgver}"
	perl Build test
}

package() {
	cd "${_perlmod}-${pkgver}"
	perl Build install
}
