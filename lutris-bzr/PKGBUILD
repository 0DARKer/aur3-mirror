# Maintainer: FoolEcho <foolecho@gmail.com>
pkgname=lutris-bzr
pkgver=101
pkgrel=1
pkgdesc="Lutris is a gaming platform aiming to support as many games as possible for the GNU/Linux operating system."
arch=('any')
url="http://lutris.net/"
license=('GPL3')
#wxpython or wxglade ?
depends=('python2' 'pygtk' 'wxpython' 'pyxdg' 'python2-yaml')
optdepends=(
  'python2-gconf: recommended'
  'joy2key: for programs that otherwise lack native joystick support'
  'pyglet: for fullscreen coverflow'
)
makedepends=('bzr' 'python-distutils-extra')
install=lutris.install

_bzrtrunk=lp:lutris
_bzrmod=lutris

build() {
  cd ${srcdir}

  msg "Connecting to the server...."

  if [ ! -d ./${_bzrmod} ]; then
    bzr co ${_bzrtrunk} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"

  # patch: use python2
  find ${srcdir}/${_bzrmod}-build -name "*py" -exec sed 's/python/python2/' -i {} \;
}

package() {
  cd "$srcdir/$_bzrmod-build"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}