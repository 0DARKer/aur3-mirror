# Maintainer: afettouhi <A.Fettouhi@gmail.com>

pkgname=vessel
pkgver=20121216
_pkgver=12082012
pkgrel=1
pkgdesc="Vessel is a physics-based, steampunk puzzle-platform video game developed by Strange Loop Games."
url="http://www.strangeloopgames.com/"
license=('custom')
arch=('i686' 'x86_64')
groups=("humble-indie-bundle6" "games")
if [ "$CARCH" = "x86_64" ]; then
    depends=('lib32-sdl' 'lib32-openal' 'bash' 'lib32-gcc-libs')
    optdepends=('lib32-libtxc_dxtn: texture decoding for open source ATI and Intel drivers')
else
    depends=('sdl' 'openal' 'bash' 'gcc-libs')
    optdepends=('libtxc_dxtn: texture decoding for open source ATI and Intel drivers')
fi
source=("${pkgname}-${_pkgver}-bin"
        "http://www.flibitijibibo.com/patch/vessel/linux-12162012.tar.gz"
        "${pkgname}.install"
        "${pkgname}.desktop"
        "${pkgname}.sh")
md5sums=("cdb4e42ac3dede4478f54bfe15ebfbeb"
         "6900202d2148b9172862c27af1627d82"
         "b25345577b44fbeeba96c6cc87f11c1e"
         "6057700248d5796056b1096962d53877"
         "174d23fe24b496e2cdda1d136327174e")
makedepends=('unzip')
options=('!strip')
# if [[ -f "${source[0]%%:*}" ]]; then
#   makedepends=('unzip')
# else
#   makedepends=('unzip' 'humblepie')
# fi
PKGEXT=".pkg.tar"

package() {
  mkdir -p "${pkgdir}/opt/games/${pkgname}"
  [[ -f "${source[0]}" ]] ||
    error "Place the game or a symlink to the game in ${startdir}"
  unzip -u "${source[0]}" -d "${pkgdir}/opt/games/${pkgname}" || true
  rm -rf "${pkgdir}/opt/games/${pkgname}/data/x86"
  rm "${pkgdir}/opt/games/${pkgname}/data/Linux.README"
  rm "${pkgdir}/opt/games/${pkgname}/data/Vessel"
  rm "${pkgdir}/opt/games/${pkgname}/data/Vessel.bmp"
  tar -zxvf linux-12162012.tar.gz -C "${pkgdir}/opt/games/${pkgname}/data/"
  find "${pkgdir}" -type f -exec chmod 644 "{}" +
  install -Dm644 "${pkgname}".desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm755 "${pkgname}".sh "${pkgdir}/usr/bin/${pkgname}"
  chmod 755 "${pkgdir}/opt/games/${pkgname}/data/Vessel" \
    "${pkgdir}/opt/games/${pkgname}/data/x86/vessel.x86"
}

# vim: ft=PKGBUILD sts=2 ts=2 sw=2 et
