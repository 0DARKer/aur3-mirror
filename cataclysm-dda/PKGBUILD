# Maintainer: Nick Hu <nickhu00@gmail.com>
pkgname=cataclysm-dda
pkgver=0.A
pkgrel=1
pkgdesc="Cataclysm: Dark Days Ahead is an actively maintained roguelike set in a post-apocalyptic world, forked from the original."
arch=('i686' 'x86_64')
url="http://www.cataclysmdda.com/"
license=('CCPL:by-sa')

depends=('lua51' 'sdl' 'sdl_ttf' 'sdl_image' 'gcc-libs' 'glibc' 'freetype2'
         'zlib' 'bzip2' 'libpng')
if test "$CARCH" == x86_64; then
  depends=('lib32-lua51' 'lib32-sdl' 'lib32-sdl_ttf' 'lib32-sdl_image'
          'lib32-gcc-libs' 'lib32-glibc' 'lib32-freetype2' 'lib32-zlib'
          'lib32-bzip2' 'lib32-libpng')
fi

install='cataclysm-dda.install'
source=('http://assets.cataclysmdda.com/downloads/linux_binaries/cddasdl-current.tar.gz'
        'keymap.txt')
md5sums=('56cb0557bcb2161856dc0e91b6e24e94'
         'eeadf652b437a2a4f4cf0b44f908c68b')

prepare() {
  cd "$srcdir/cataclysmdda-${pkgver}"
  sed -i 's/\x6C\x69\x62\x6C\x75\x61\x35\x2E\x31\x2E\x73\x6F\x2E\x30/\x6C\x69\x62\x6C\x75\x61\x35\x2E\x31\x2E\x73\x6F\x00\x00/' cataclysm-tiles

  sed -i 's|DIR=.*|DIR=/usr/share/cataclysm-dda\n|' cataclysm-launcher
}

package() {
  cd "$srcdir/cataclysmdda-${pkgver}"
  install -Dm755 cataclysm-launcher "$pkgdir/usr/bin/cataclysm-dda"
  install -Dm755 cataclysm-tiles "$pkgdir/usr/share/cataclysm-dda/cataclysm-tiles"
  cp -r data gfx lang lua "$pkgdir/usr/share/cataclysm-dda"
  install -dm775 -g games "$pkgdir/var/games"
  install -dm775 -g games "$pkgdir/var/games/cataclysm-dda"
  install -dm775 -g games "$pkgdir/var/games/cataclysm-dda/save"
  ln -s "/var/games/cataclysm-dda/save" "$pkgdir/usr/share/cataclysm-dda/save"
  install -Dm764 -g games "$srcdir/keymap.txt" "$pkgdir/usr/share/cataclysm-dda/data"
}
