# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=emacs-ess-git
pkgver=14.09.29.gd6ae126
pkgrel=1
pkgdesc="Emacs Speaks Statistics: A Universal Interface for \
 Statistical Analysis - git-version"
arch=('any')
url="http://ess.r-project.org"
license=('GPL')
depends=('emacs' 'r')
makedepends=('git')
provides=('ess' 'emacs-ess')
conflicts=('emacs-ess' 'emacs-ess-svn')
options=('docs')
install=ess.install
source=("emacs-ess::git://github.com/emacs-ess/ESS.git")
md5sums=('SKIP')
_gitname="emacs-ess"

prepare() {
  # problems with generating dvi-format-output, so disabling it
  sed -i '38s+dvi ++' "$srcdir/$_gitname"/doc/Makefile
  sed -i '105s+ess.dvi ess.pdf readme.dvi+ess.pdf+' "$srcdir/$_gitname"/doc/Makefile
}

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --tags | sed 's|-|.|g' |cut -c2-
}

build() {
  cd "$srcdir/$_gitname"
  make prefix=/usr 
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR=$pkgdir/usr LISPDIR=$pkgdir/usr/share/emacs/site-lisp/ess \
    INFODIR=$pkgdir/usr/share/info/ install 
}
