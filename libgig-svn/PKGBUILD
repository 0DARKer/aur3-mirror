# Maintainer: Moritz Kiefer <moritz.kiefer at gmail dot com>
# Contributor: SpotlightKid
pkgname=libgig-svn
_pkgname="${pkgname%-svn}"
pkgver=r2580    
pkgrel=3
pkgdesc="Gigasampler file access library"
arch=("i686" "x86_64")
url="http://www.linuxsampler.org/"
license=("GPL")
depends=("libsndfile"
         "util-linux")
makedepends=("subversion")
optdepends=()
provides=("libgig")
conflicts=("libgig")
source=("$pkgname"::"svn+https://svn.linuxsampler.org/svn/$_pkgname/trunk"
        "akai-open-mode.patch"
        "libdir.patch")
md5sums=('SKIP'
         '4adb0bcb53b280050b31eae15cdf0498'
         '1b1243dd8925d5e54e7817a7c61e02bd')

pkgver() {
  cd "$srcdir/$pkgname"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "$srcdir/$pkgname"
  patch -Np0 -i "$srcdir/akai-open-mode.patch"
  patch -Np0 -i "$srcdir/libdir.patch"
  }
build() {
  cd "$srcdir/$pkgname"
  make -f Makefile.cvs
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR=$pkgdir install

  # add libgig library path to ld.so.conf.d
  install -m0755 -d "$pkgdir/etc/ld.so.conf.d"
  echo "/usr/lib/$_pkgname" > "$pkgdir/etc/ld.so.conf.d/$_pkgname.conf"
}
