# Maintainer: Moritz Kiefer <moritz.kiefer at gmail dot com>
# Maintainer: Jiri Prochazka <ojirio@gmail.com>
pkgname=libgig-svn
_pkgname="${pkgname%-svn}"
pkgver=r2486    
pkgrel=2
pkgdesc="Gigasampler file access library"
arch=("i686" "x86_64")
url="http://www.linuxsampler.org/"
license=("GPL")
depends=("libsndfile"
         "util-linux")
makedepends=("subversion")
optdepends=()
provides=("libgig")
conflicts=("libgig")
source=("$pkgname"::"svn+https://svn.linuxsampler.org/svn/$_pkgname/trunk")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
  cd "$srcdir/$pkgname"
  make -f Makefile.cvs
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR=$pkgdir install
}
