# Maintainer: Jiri Prochazka <ojirio@gmail.com>
pkgname=libgig-svn
_pkg="${pkgname%-svn}"
pkgver=2334
pkgrel=1
pkgdesc="Gigasampler file access library"
arch=("i686" "x86_64")
url="http://www.linuxsampler.org/"
license=("GPL")
depends=("libsndfile")
makedepends=("subversion")
optdepends=()
options=("!libtool")

_svntrunk="https://svn.linuxsampler.org/svn/${_pkg}/trunk"
_svnmod="$_pkg"

build() {
  cd "$srcdir"

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_svnmod}-build"
  cp -a "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  #
  # BUILD
  #

  #aclocal && autoheader && autoconf && automake
  make -f Makefile.cvs
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  local _svndir="${srcdir}/${_svnmod}-build"
  . <(sed -n "s|^\s*\(${_pkg^^}_RELEASE_[^[:blank:]]\{1,\}\)\s*=\s*\([[:digit:]]\{1,\}\).*$|_\1=\2|p" "${_svndir}/configure.in")
  provides=("${_pkg}=${_LIBGIG_RELEASE_MAJOR}.${_LIBGIG_RELEASE_MINOR}.${_LIBGIG_RELEASE_BUILD}" "${_pkg}-cvs")
  conflicts=("${_pkg}" "${_pkg}-cvs")

  cd "$_svndir"
  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
