# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=3.8.1
pkgrel=2
pkgdesc="Small applications for the GNOME panel"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cpupower' 'gucharmap' 'gstreamer0.10-base-plugins' 'gnome-panel' 'libgtop' 'upower')
makedepends=('gnome-common' 'intltool' 'networkmanager' 'rarian' 'yelp-tools')
url="https://wiki.gnome.org/Projects/GnomeFlashback"
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        gnome-applets-cpupower.patch)
sha256sums=('c861a0be754024ea2ce6a3474fd367c0f3d69c2ea46f43c382fa7a69f2c40a7d'
            '88c3258fdb9e72690122e95a7e3a776a0626dd028e4335130d0fb944d7666d02')

prepare() {
  cd "$pkgname-$pkgver"

  # Use cpupower instead of cpufreq
  patch -Np1 -i "$srcdir/gnome-applets-cpupower.patch"

  # Turn off unneeded warnings
  CFLAGS="$CFLAGS -w"

  autoreconf -fi
}

build() {

  cd "$pkgname-$pkgver"

  # Turn off unneeded warnings
  CFLAGS="$CFLAGS -w"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-install \
              --enable-mixer-applet \
              PYTHON=/usr/bin/python2

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  mkdir -m755 -p "$pkgdir/usr/share/gconf/schemas"

  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" "$pkgdir"/etc/gconf/schemas/*.schemas 

  rm -r "$pkgdir"/etc/gconf
}

