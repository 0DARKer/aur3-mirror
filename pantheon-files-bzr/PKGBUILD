# Maintainer: Ner0

pkgname=pantheon-files-bzr
pkgver=1244
pkgrel=1
pkgdesc="A simple, powerful and sexy file manager from elementary (Marlin fork)"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-files"
license=('GPL3')
groups=('pantheon')
depends=('granite-bzr>=589' 'glib2' 'pango' 'gtk3' 'libgee' 'sqlite3' 'dbus-glib' 'gtk-update-icon-cache' 'libnotify' 'gnome-icon-theme' 'dconf' 'cairo' 'atk' 'desktop-file-utils' 'gconf')
makedepends=('bzr' 'vala' 'cmake')
optdepends=('tumbler: for generating thumbnails'
            'contractor-bzr: plugins integration'
            'pantheon-files-plugin-dropbox-bzr: Dropbox integration')
provides=('pantheon-files')
conflicts=('marlin-bin' 'marlin-bzr' 'files-bzr' 'pantheon-files')
install=pantheon-files.install
source=('bzr+lp:pantheon-files')
md5sums=('SKIP')

_UNITY=OFF
[ -f /usr/lib/libunity.so.[0-9] ] && _UNITY=ON

pkgver() {
  cd "$srcdir/pantheon-files"
  bzr revno
}

build() {
  cd "$srcdir/pantheon-files"
  rm -rf build/
  mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF -DICON_UPDATE=OFF -DWITH_UNITY=${_UNITY}
  make
}

package() {
  cd "$srcdir/pantheon-files/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
