# Maintainer: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

pkgname=velocity-xbox360-git
pkgver=0.1.0.0.f7eb638
pkgrel=1
pkgdesc="A cross-platform application built using the Qt framework which allows you to browse and edit Xbox 360 files."
arch=('i686' 'x86_64')
url="https://github.com/hetelek/Velocity.git"
license=('GPL3')
depends=('qt4' 'botan')
makedepends=('git')
source=("$pkgname"::'git://github.com/hetelek/Velocity.git'
        'project.patch'
        'velocity.desktop')
sha512sums=('SKIP'
            '6c04f32abd6719419d0f9dfeb38741c404351f1fb41304c0e2946c6746a0d014849e65df1abbf6fdea0b4a668fab8363fc13ff2e325203b85a24baf454da1ce9'
            'c4f7b60bee4822c2052853b054e0330fab03fdf7dece90f4631b91ea4a4d3571913592a038f9dbc74b9efecc6fddf280dec7aee95a84a7fd334baeb0ce8e8839')

pkgver() {
    cd "$srcdir/$pkgname"
    echo "$(grep 'VERSION =' Velocity/Velocity.pro | cut -d ' ' -f 3).$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$pkgname"
    patch -p0 < ../project.patch
}

build() {
    cd "$srcdir/$pkgname"

    cd XboxInternals
    qmake-qt4 XboxInternals.pro
    make

    cd ..

    mkdir -p XboxInternals-Linux/release
    cp XboxInternals/libXboxInternals.a XboxInternals-Linux/release

    cd Velocity
    qmake-qt4 Velocity.pro
    make
}

package() {
    mkdir -p "$pkgdir/opt/velocity/plugins"
    install -Dm755 "$srcdir/$pkgname/Velocity/Velocity" "$pkgdir/opt/velocity/velocity"
    install -Dm644 "$srcdir/$pkgname/Velocity/Images/Leaner.png" "$pkgdir/usr/share/pixmaps/velocity.png"
    mkdir -p "$pkgdir/usr/bin"
    ln -s /opt/velocity/velocity "$pkgdir/usr/bin/velocity"
    install -Dm644 "$srcdir/velocity.desktop" "$pkgdir/usr/share/applications/velocity.desktop"
}
