# Contributor: noonov <noonov@gmail.com>
# Contributor: gasuketsu02 <gasuketsu02@gmail.com>

pkgname=ttf-migu
pkgver=20121030
pkgrel=1
pkgdesc="Good looking Japanese TrueType font by mixing M+ and IPA."
arch=('any')
url="http://mix-mplus-ipa.sourceforge.jp/migu/"
license=('custom')
depends=('fontconfig' 'xorg-font-utils')
install=ttf.install
_mirror="iij" # keihanna, jaist, iij, osdn
source=("http://${_mirror}.dl.sourceforge.jp/mix-mplus-ipa/57240/migu-1c-${pkgver}.zip"
        "http://${_mirror}.dl.sourceforge.jp/mix-mplus-ipa/57240/migu-1m-${pkgver}.zip"
        "http://${_mirror}.dl.sourceforge.jp/mix-mplus-ipa/57240/migu-1p-${pkgver}.zip"
        "http://${_mirror}.dl.sourceforge.jp/mix-mplus-ipa/57240/migu-2m-${pkgver}.zip")
md5sums=('1b3826fcd2e1955eada74bf861ba8367'
         '43af4f483ab809befed5a1b233cf6a1e'
         '50d51190efbde5df686b55b091b2e2f8'
         '3cc531b52ea4ce40a8c272ffc5faab27')

package() {
  cd ${srcdir}

  install -d ${pkgdir}/usr/share/fonts/TTF
  install -m644 */*.ttf ${pkgdir}/usr/share/fonts/TTF

  install -D -m644 migu-1c-*/migu-README.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/COPYING.txt
  install -D -m644 migu-1c-*/ipag00303/IPA_Font_License_Agreement_v1.0.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/COPYING_IPA.txt
}
