# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=opendkim
pkgver=2.3.0
pkgrel=1
pkgdesc="An open source implementation of the DKIM sender authentication system. Based on a fork of dkim-milter."
arch=(i686 x86_64)
url="http://www.opendkim.org/"
license=('BSD, Sendmail')
depends=("openssl")
makedepends=("libmilter")
options=(!libtool)
backup=(etc/conf.d/opendkim)
install=opendkim.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz
        opendkim.conf
        opendkim.rc)
md5sums=('f254e91f62d352840c401e33dd4e19dc'
         '3e2bb1058ac0662f01e675aa6ac7ee8f'
         '093f5c098a45cc68753f97e45a5b374c')
sha1sums=('99fd2afcd67d92aeca95615c9a4ede2d8bea8ab0'
          '48dc76dce4df2cde9f6f88abf75fe562ad191ee0'
          '7d2158574ddd083c7c1cc6d353928f759984b2f7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Sample configuration
  mkdir -p "$pkgdir/etc/opendkim"
  mv "$pkgdir/usr/share/doc/opendkim/opendkim.conf.sample" "$pkgdir/etc/opendkim/opendkim.conf.sample"
  chmod 0700 "$pkgdir/etc/opendkim"
  chmod 0600 "$pkgdir/etc/opendkim/opendkim.conf.sample"

  # Init script
  install -Dm644 "$srcdir/opendkim.conf" "$pkgdir/etc/conf.d/opendkim"
  install -Dm755 "$srcdir/opendkim.rc" "$pkgdir/etc/rc.d/opendkim"

  # Remove empty dirs
  rmdir "$pkgdir/usr/share/man/man1" "$pkgdir/usr/share/man/man3"

  # License
  mkdir -p "$pkgdir/usr/share/licenses/opendkim"
  for f in LICENSE LICENSE.Sendmail; do
    ln -s ../../doc/opendkim/$f "$pkgdir/usr/share/licenses/opendkim/$f"
  done
}

# vim:set ts=2 sw=2 et:
