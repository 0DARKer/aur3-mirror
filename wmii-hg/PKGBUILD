# Contributor: Anthony Martin <ality@pbrane.org>
# Contributer: N30N <archlinux@alunamation.com>
# Contributer: thomasd <thdahms@gmx.de>
# Contributer: karnath <karnath@pop3.ru>
# Contributer: MrBougo <bougospam@gmail.com>

pkgname="wmii-hg"
pkgver=2809
pkgrel=1
pkgdesc="The latest hg pull of wmii, a lightweight, dynamic window manager for X11"
url="http://wmii.suckless.org"
license=("MIT")
arch=("i686" "x86_64")
depends=("libx11" "libxinerama" "libxrandr")
makedepends=("mercurial" "txt2tags" "libixp-hg" "libxft")
optdepends=("libxft: for anti-aliased font support"
	"plan9port: for use of the alternative plan9port wmiirc"
	"python2: for use of the alternative Python wmiirc"
	"ruby-rumai: for use of the alternative Ruby wmiirc"
	"xorg-xmessage: for use of the default wmiirc configs")
provides=("wmii")
conflicts=("wmii")
source=("wmii.desktop")
md5sums=("35b14c7c6c658b668d29eab4bd1f621c")

_hgroot="http://code.suckless.org/hg"
_hgrepo="wmii"

build() {
	LDFLAGS="$(echo ${LDFLAGS} | sed -r "s/(^|,)--as-needed(,|$)//")"
	_flags=("PREFIX=/usr" \
		"ETC=/etc" \
		"DESTDIR=${pkgdir}" \
		"PYTHON=python2")

	rm -rf build
	cp -rf "${_hgrepo}" build
	cd build
	hg checkout "${pkgver}"

	make ${_flags[@]} all
	make ${_flags[@]} install

	install -D -m644 img/wmii.png \
		"${pkgdir}/usr/share/pixmaps/wmii.png"
	install -D -m644 "${srcdir}/wmii.desktop" \
		"${pkgdir}/usr/share/xsessions/wmii.desktop"
}

# vim: set noet ff=unix
