# Maintainer: celmir <scribecelmir@tuxfamily.org>
pkgname=landeseternelleslinux
pkgver=1.6.3.0
pkgrel=1
pkgdesc="Fork of Eternal Lands's mmorpg for french speaking players"
arch=('i686' 'x86_64')
url="http://www.landes-eternelles.com"
license=(custom:'eternal_lands_license')
depends=('sdl_net' 'sdl_image' 'cal3d' 'mesa' 'openal' 'libxslt' 'libvorbis' 'gtk2')
makedepends=('make')
install='landeseternelles.install'
source=(http://landes-eternelles.com/client/Client_Sources-1.6.3.0.tar.bz2
	http://www.landes-eternelles.com/client/1630/Editeur_Sources-1.6.3.0.tar.bz2
        http://landes-eternelles.com/client/LandesEternellesLinux-1.6.3.0.tar.bz2
	landeseternelles.desktop
        landeseternelles.png
	http://landes-eternelles.com/client/1630/updates/updates1630_1.tar.bz2
)
md5sums=('0f575872af33eb0c0415b7caa6893c23'
'8a85d6f944d4c77f5130d581d254cff6'
'60187749a39af66c977469e49cf50884'
'34b87dba614b242297375cec98255acf'
'410fed06fca941a95b0427c30cb697c5'
'c2f207b61a449723d6d3c113961c6f63'
)

build() {
  cd "$srcdir/Client_Sources"
  for fic in *.c *.h io/*.c io/*.h
  do
  sed -e 's#editeur_sources#Editeur_Sources#g' -i $fic
  done 
  make -f Makefile.linux || return 1
  cd "$srcdir/Editeur_Sources"
  for fic in *
  do
  echo $fic
  sed -e 's#client_sources#Client_Sources#g' -i $fic
  done 
  make -f Makefile.linux || return 1
}

package() {
  mkdir -p $pkgdir/usr/{bin,share/{$pkgname,licenses/$pkgname,pixmaps,applications}}
  install -m644 $srcdir/landeseternelles.desktop $pkgdir/usr/share/applications/landeseternelles.desktop
  install -m644 $srcdir/landeseternelles.png $pkgdir/usr/share/pixmaps/landeseternelles.png
  cd "$srcdir/Client_Sources"
  install -m755 le.$CARCH.linux.bin $pkgdir/usr/bin/le.$CARCH.linux.bin
  cd "$srcdir/Editeur_Sources"
  install -m755 mapedit.$CARCH.linux.bin $pkgdir/usr/bin/mapedit.$CARCH.linux.bin
  cd "$srcdir/maps"
  mv -f *.gz $srcdir/LandesEternellesLinux/maps/
  chmod 0644 $srcdir/LandesEternellesLinux/maps/*.gz
  cd "$srcdir/meshes"
  mv -f *.cmf $srcdir/LandesEternellesLinux/meshes/
  chmod 0644 $srcdir/LandesEternellesLinux/meshes/*.cmf
  cd "$srcdir/LandesEternellesLinux"
  rm -f *.bin icon.bmp
  mv -f Licence.txt Accord_general_de_licence_LE.txt $pkgdir/usr/share/licenses/$pkgname/
  mv -f * $pkgdir/usr/share/$pkgname/
}

# vim:set ts=2 sw=2 et:
