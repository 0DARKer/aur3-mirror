# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kdeconnect-frameworks-git
pkgver=r469.d8ae828
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/base/kdeconnect-kde'
license=('GPL2')
depends=('kcmutils-git' 'kdelibs4support-git' 'qca-qt5-git' 'qjson-qt5')
makedepends=('extra-cmake-modules' 'git')
provides=('kdeconnect-frameworks')
conflicts=('kdeconnect-frameworks')
source=("git://anongit.kde.org/kdeconnect-kde.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
  cd kdeconnect-kde
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export CMAKE_PREFIX_PATH=/opt/kf5

  cd build
  cmake ../kdeconnect-kde \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
