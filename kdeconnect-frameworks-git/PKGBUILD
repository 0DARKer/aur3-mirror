# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kdeconnect-frameworks-git
pkgver=r529.42269a9
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/base/kdeconnect-kde'
license=('GPL2')
depends=('kcmutils' 'kdelibs4support' 'qca-qt5')
makedepends=('extra-cmake-modules' 'git')
provides=('kdeconnect-frameworks')
conflicts=('kdeconnect-frameworks' 'kdeconnect')
source=("git://anongit.kde.org/kdeconnect-kde.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
  cd kdeconnect-kde
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdeconnect-kde \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DQML_INSTALL_DIR=lib/qt/qml
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
