# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=ssrc
pkgver=1.30
pkgrel=4
pkgdesc="A fast and high quality sampling rate converter"
arch=('i686' 'x86_64')
url="http://shibatch.sourceforge.net"
license=('LGPL')
depends=('glibc')
options=(!zipman)
source=(${url}/download/${pkgname}-${pkgver}.tgz)
md5sums=('a14b3c4f97c52e37e9cfeb65fc61f2bf')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i '5a#include <string.h>' ssrc.c
  sed -i s/$\(CFLAGS\)/$\(CFLAGS\)\ -lm\ $\{LDFLAGS\}/g Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make clean
  make -e
}

package() {
  install -d "$pkgdir/usr/bin"
  install -m755 "$pkgname-$pkgver"/{ssrc,ssrc_hp} "$pkgdir/usr/bin/"
}
