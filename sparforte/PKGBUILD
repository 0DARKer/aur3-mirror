# Maintainer: Julian Leyh <julian@vgai.de>

pkgname=sparforte
pkgver=1.2
pkgrel=1
pkgdesc="The Business Shell with Sound Design"
arch=('i686' 'x86_64')
url="http://www.pegasoft.ca/sparforte.html"
license=('GPL')
depends=('sdl_image' 'postgresql-libs' 'libmysqlclient')
makedepends=('gcc-ada')
source=(http://www.pegasoft.ca/downloads/${pkgname}-${pkgver}-src.tgz
        apq-configure.patch
        configure.patch
        makefile.patch
        parser.patch
        src-makefile.patch)
md5sums=('0dbf06e186447c2a9c3c427772080be9'
         '4db36b8407526c149d569be2f73338f8'
         '6c75252cbea521e3b354271bcb85d3bb'
         '046d88b8b504114e02c3fd6e3c1a73e9'
         '74895e1a6e15de0b58df8a2fa95273be'
         '8199750924011830e74fe831f0e538dc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  for PATCH in ../*.patch; do
    patch -Np0 -i "$PATCH"
  done
  chmod -R u+w src
  ./configure --prefix=/usr \
              --arch=$CARCH \
              --make=make
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
