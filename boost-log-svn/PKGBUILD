# Maintainer: Dmitriy Morozov <archlinux@foxcub.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# TU: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>

# This PKGBUILD is modified from Arch's official Boost PKGBUILD to build the
# Boost.Logging library

pkgname=boost-log-svn
pkgver=718
_boostbase=boost
_boostver=1_50_0
pkgrel=1
arch=('i686' 'x86_64')
url="http://boost-log.sf.net"
makedepends=('icu' 'python' 'python2' 'bzip2' 'zlib' 'openmpi' 'subversion')
source=(http://downloads.sourceforge.net/sourceforge/${_boostbase}/${_boostbase}_${_boostver}.tar.gz)
license=('custom')
options=('!ccache')
replaces=('boost-log')
conflicts=('boost-log')

_stagedir="${srcdir}/stagedir"

_svntrunk=https://boost-log.svn.sourceforge.net/svnroot/boost-log/trunk
_svnmod=boost-log

build() {
  cd $srcdir
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  cp -a ${srcdir}/$_svnmod/$_svnmod/boost ${srcdir}/${_boostbase}_${_boostver}
  cp -a ${srcdir}/$_svnmod/$_svnmod/libs  ${srcdir}/${_boostbase}_${_boostver}

  # set python path for bjam
  cd "${srcdir}/${_boostbase}_${_boostver}/tools"
  echo "using python : 2.7 : /usr/bin/python2 ;" >> build/v2/user-config.jam
  echo "using python : 3.2 : /usr/bin/python : /usr/include/python3.2mu : /usr/lib ;" >> build/v2/user-config.jam
  echo "using mpi ;" >> build/v2/user-config.jam

  # build bjam
  cd "${srcdir}/${_boostbase}_${_boostver}/tools/build/v2/engine"
  ./build.sh cc

  _bindir="bin.linuxx86"
  [ "${CARCH}" = "x86_64" ] && _bindir="bin.linuxx86_64"

  install -d "${_stagedir}"/usr/bin
  install ${_bindir}/bjam "${_stagedir}"/usr/bin/bjam

  # build tools
  cd "${srcdir}/${_boostbase}_${_boostver}/tools/"
  "${_stagedir}"/usr/bin/bjam --toolset=gcc

  # copy the tools
  cd "${srcdir}/${_boostbase}_${_boostver}/dist/bin"
  for i in *;do
      install -m755 "${i}" "${_stagedir}/usr/bin/${i}"
  done

  #boostbook needed by quickbook
  cd "${srcdir}/${_boostbase}_${_boostver}/dist/"
  cp -r share "${_stagedir}"

  # build libs
  cd "${srcdir}/${_boostbase}_${_boostver}"

  # default "minimal" install: "release link=shared,static
  # runtime-link=shared threading=single,multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  # install to ${_stagedir} in preparation for split packaging

  "${_stagedir}"/usr/bin/bjam \
      release debug-symbols=off threading=multi \
      runtime-link=shared link=shared,static \
      cflags=-fno-strict-aliasing \
      toolset=gcc \
      --prefix="${_stagedir}" \
      -sTOOLS=gcc \
      --layout=system \
      --with-log \
      ${MAKEFLAGS} \
      install
}

package_boost() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
    depends=("boost-libs=${pkgver}")
    optdepends=('python: for python bindings'
                'python2: for python2 bindings'
                'boost-build: to use boost jam for building your project.')

    install -d "${pkgdir}"/usr/{include,lib,share}
    # headers/source files
    cp -r "${_stagedir}"/include/ "${pkgdir}"/usr/

    # static libs
    cp -r "${_stagedir}"/lib/*.a "${pkgdir}"/usr/lib/

    # utilities (bjam, bcp, pyste)
    cp -r "${_stagedir}"/usr/* "${pkgdir}"/usr/

    #boostbook
    cp -r "${_stagedir}"/share/* "${pkgdir}"/usr/share

    # license
    install -D -m644 "${srcdir}/${_boostbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt
}

package_boost-libs() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
    depends=('gcc-libs' 'bzip2' 'zlib' 'icu')
    optdepends=('openmpi: for mpi support')

    install -d "${pkgdir}/usr/lib"
    #shared libs
    cp -r "${_stagedir}"/lib/*.so{,.*} "${pkgdir}/usr/lib/"

    # license
    install -D -m644 "${srcdir}/${_boostbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}

package_boost-log-svn() {
    pkgdesc="Logging library provisionally accepted to Boost"
    depends=('boost')

    install -dm 755 "${pkgdir}"/usr/include/boost
    install -dm 755 "${pkgdir}"/usr/lib
    # headers/source files
    cp -r "${_stagedir}"/include/boost/log "${pkgdir}"/usr/include/boost

    # static libs
    cp -r "${_stagedir}"/lib/*log*.a "${pkgdir}"/usr/lib/
    cp -r "${_stagedir}"/lib/*log*.so{,.*} "${pkgdir}/usr/lib/"
}
md5sums=('dbc07ab0254df3dda6300fd737b3f264')
