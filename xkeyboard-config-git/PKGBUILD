# $Id$
# Maintainer: Alexander Baldeck <kth5@archlinux.org>
# Contributor: SÃ©bastien Mazy <melyadon@gmail.com>
pkgname=xkeyboard-config-git
pkgver=20081115
pkgrel=3
pkgdesc="X keyboard configuration files (git version)"
arch=(i686 x86_64)
license=('custom')
url="http://www.freedesktop.org/wiki/Software/XKeyboardConfig"
depends=('xorg-xkb-utils')
makedepends=('git' 'perlxml' 'pkgconfig' 'intltool')
provides=('xkbdata' 'xkeyboard-config=1.3')
replaces=('xkbdata')
conflicts=('xkbdata' 'xkeyboard-config')
source=()
md5sums=()

_gitroot="git://anongit.freedesktop.org/git/xkeyboard-config"
_gitname="xkeyboard-config"

build() {
  cd ${startdir}/src/
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d $startdir/src/$_gitname ] ; then
  cd $_gitname && git pull origin
  msg "The local files are updated."
  else
  git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd $startdir/src/
  cp -r ${startdir}/src/$_gitname ${startdir}/src/$_gitname-build || return 1
  cd $startdir/src/$_gitname-build/

  ./autogen.sh || return 1
  ./configure --prefix=/usr \
              --with-xkb-base=/usr/share/X11/xkb \
              --with-xkb-rules-symlink=xorg \
              --enable-compat-rules=yes || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
  rm -f ${startdir}/pkg/usr/share/X11/xkb/compiled || return 1
  install -m755 -d ${startdir}/pkg/var/lib/xkb
  install -m755 -d ${startdir}/pkg/usr/share/licenses/${pkgname}
  install -m644 COPYING ${startdir}/pkg/usr/share/licenses/${pkgname}/ || return 1
}
