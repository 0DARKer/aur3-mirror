#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=python3-aur
pkgver=2014.5.31
pkgrel=1
pkgdesc='AUR-related modules and helper utilities (aurploader, aurquery, aurtomatic).'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/python3-aur"
depends=(python3 python-xdg)
optdepends=('curl: Aurploader support')
replaces=(aurploader)
conflicts=(aurploader)
provides=(aurploader)
source=(
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2014.5.31.tar.xz
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2014.5.31.tar.xz.sig
)
sha512sums=(
  0732b27e702ee0959550e91363c0440dee435b7d75234052bde17b8ff845612aba784cd04553255d7e223d08af65c982753ca83574725da0c8fe913cbada7137
  513f608d12b2c72a9338eef9793c48f198a8c7c8f65d2491485dd5d85b2f085f77272d087034f44ebfe1092b638ca2d139e933b7663fdfc689f644b4db55956b
)
md5sums=(
  6b3d3eac88060cffac346cf9509e0850
  a08dc6a783e43c0d52e102fe8c1b384c
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  for aurfoo in aur*
  do
    if [[ -x "$aurfoo" ]]
    then
      install -Dm755 "$aurfoo" "$pkgdir/usr/bin/$aurfoo"
    fi
  done
}


# vim: set ts=2 sw=2 et:
