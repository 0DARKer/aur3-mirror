#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=python3-aur
pkgver=2014.6.5
pkgrel=1
pkgdesc='AUR-related modules and helper utilities (aurploader, aurquery, aurtomatic).'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/python3-aur"
depends=(python3 python-xdg)
optdepends=('curl: Aurploader support')
replaces=(aurploader)
conflicts=(aurploader)
provides=(aurploader)
source=(
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2014.6.5.tar.xz
  http://xyne.archlinux.ca/projects/python3-aur/src/python3-aur-2014.6.5.tar.xz.sig
)
sha512sums=(
  c3945ccddee3f40f71f25caa49aa9ef2aa0ef3499844ad1d340eefd5045ac704b8b928e636e25f3c1275ad667da5c6ce8d3c918c0f5e186e1af6da08361fb3f4
  ab2ac5dba92588dbdc46d140a0bae2bd40579e0b055e66ce183c1bd6a540707c979ce51c3ffe87d35086e7bf435c88e142b729b2579c0db784996c870c33c8cd
)
md5sums=(
  6712d2902c17116dd22d14ef9e3a2499
  96dce2f789b3fdc94c5202ebe884b47e
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  for aurfoo in aur*
  do
    if [[ -x "$aurfoo" ]]
    then
      install -Dm755 "$aurfoo" "$pkgdir/usr/bin/$aurfoo"
    fi
  done
}


# vim: set ts=2 sw=2 et:
