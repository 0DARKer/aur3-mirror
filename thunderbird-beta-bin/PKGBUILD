# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

pkgname=thunderbird-beta-bin
pkgver=3.3a3
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge binary version"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'gcc-libs' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell' 'sqlite3>=3.7.4')
optdepends=('libcanberra: for sound support'
	    'yasm: for WebM support')
provides=("thunderbird=${pkgver}")
conflicts=('thunderbird-beta')
install=${pkgname}.install
# In case releases.mozilla.org is not responding or the file(s) aren't yet uploaded there:
# source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/linux-${CARCH}/en-US/miramar-${pkgver}.tar.bz2
source=(http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/linux-${CARCH}/en-US/miramar-${pkgver}.tar.bz2
        thunderbird-beta.desktop)
md5sums=(`wget ${source/li*}/MD5SUMS -qO - | grep mi | grep "${CARCH}" | cut -d " " -f1`
         '0b6225c718e251ec0ea9a21be610b00a')

package() {
  sed -i 's|/usr/local/lib/thunderbird-|/opt/thunderbird-|' thunderbird/thunderbird

  install -dm755 "${pkgdir}"/{usr/{bin,share/{applications,pixmaps}},opt}
  cp -a thunderbird "${pkgdir}/opt/thunderbird-${pkgver}"

  ln -s /opt/thunderbird-${pkgver}/thunderbird "${pkgdir}/usr/bin/thunderbird-beta"
  install -m644 thunderbird/chrome/icons/default/default48.png "${pkgdir}/usr/share/pixmaps/thunderbird-beta.png"
  install -m644 thunderbird-beta.desktop "${pkgdir}/usr/share/applications"
}