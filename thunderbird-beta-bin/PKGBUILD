# Maintainer: Det <nimetonmaili g-mail>
# Based on [extra]'s thunderbird

pkgname=thunderbird-beta-bin
pkgver=34.0b1
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge binary version"
arch=('i686' 'x86_64')
url="https://www.mozilla.org/thunderbird"
license=('GPL' 'LGPL' 'MPL')
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2' 'gtk-update-icon-cache'
         'gtk2' 'hicolor-icon-theme' 'libnotify' 'libpng' 'libvpx' 'libxt' 'mime-types' 'nss' 'pixman'
         'sqlite' 'startup-notification')
makedepends=('pacman>=4.2.0')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
conflicts=('thunderbird-beta')
install=$pkgname.install
# RC
#source_x86_64=("https://ftp.mozilla.org/pub/mozilla.org/thunderbird/candidates/${pkgver/rc*}-candidates/build${pkgver/*rc}/linux-x86_64/en-US/thunderbird-${pkgver/rc*}.tar.bz2")
#source_i686=("https://ftp.mozilla.org/pub/mozilla.org/thunderbird/candidates/${pkgver/rc*}-candidates/build${pkgver/*rc}/linux-i686/en-US/thunderbird-${pkgver/rc*}.tar.bz2")
#md5sums_x86_64=('8129ec3e341f980c4914d06633d4f94a')
#md5sums_i686=('84536b725fd93b9beb1aaed07405e8c7')
# Beta/Stable
source_x86_64=("https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-x86_64/en-US/thunderbird-$pkgver.tar.bz2")
source_i686=("https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/en-US/thunderbird-$pkgver.tar.bz2")
md5sums_x86_64=('f37a507e4c3042164a9fd11f6f4299ad')
md5sums_i686=('1c020ad1bd60280e8556bb299d3f98f4')
# Any
source=("$pkgname.desktop"
        "$pkgname-safe.desktop"
        'vendor.js')
md5sums=('6c94c2e7f041c5c947af1ed958c0d227'
         '00fef0a11f3643454a1af66bb7e4af35'
         '5a53179d14ae9631b7afe5e4d0fc0b25')

package() {
  # Create directories
  msg2 "Creating directory structure..."
  install -d "$pkgdir"/usr/bin
  install -d "$pkgdir"/usr/share/applications
  install -d "$pkgdir"/opt

  msg2 "Moving stuff in place..."
  # Install
  cp -r thunderbird/ "$pkgdir"/opt/$pkgname-$pkgver
  install -m644 vendor.js "$pkgdir"/opt/$pkgname-$pkgver/defaults/pref/

  # /usr/bin symlink
  ln -s /opt/$pkgname-$pkgver/thunderbird "$pkgdir"/usr/bin/thunderbird-beta

  # Desktop
  install -m644 *.desktop "$pkgdir"/usr/share/applications/

  # Icons
  for i in 16 22 24 32 48 256; do
    install -d "$pkgdir"/usr/share/icons/hicolor/${i}x$i/apps/
    ln -s /opt/$pkgname-$pkgver/chrome/icons/default/default$i.png \
          "$pkgdir"/usr/share/icons/hicolor/${i}x$i/apps/$pkgname.png
  done
}