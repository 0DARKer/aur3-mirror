# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

pkgname=thunderbird-beta-bin
pkgver=8.0b4
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge binary version"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="http://www.mozilla.org/thunderbird"
depends=('gtk2' 'mozilla-common' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'fontconfig' 'freetype2' 'cairo' 'desktop-file-utils' 'libpng' 'hicolor-icon-theme')
optdepends=('libcanberra: for sound support'
            'yasm: for WebM support')
provides=("thunderbird=$pkgver")
conflicts=('thunderbird-beta')
install=$pkgname.install
# In case releases.mozilla.org is not responding or the file(s) aren't yet uploaded there:
source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-$CARCH/en-US/thunderbird-$pkgver.tar.bz2
# source=(http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-$CARCH/en-US/thunderbird-$pkgver.tar.bz2
        $pkgname.desktop)
md5sums=(`wget ${source/li*}/MD5SUMS -qO - | grep en-US | grep "$CARCH" | grep bz | cut -d " " -f1`
         'f8a9b91202e8c85ad7b8f23fb82a4d7b')

package() {
  sed -i 's|/usr/local/lib/thunderbird-|/opt/thunderbird-|' thunderbird/thunderbird
  install -dm755 "$pkgdir"/{usr/bin,opt}
  cp -a thunderbird "$pkgdir/opt/thunderbird-$pkgver"
  ln -s /opt/thunderbird-$pkgver/thunderbird "$pkgdir/usr/bin/thunderbird-beta"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -D thunderbird/chrome/icons/default/default${i/x*/}.png "$pkgdir/usr/share/icons/hicolor/$i/apps/thunderbird-beta.png"
  done

  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  rm -rf "$pkgdir/opt/thunderbird-$pkgver/"{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir/opt/thunderbird-$pkgver/dictionaries"
  ln -sf /usr/share/hyphen "$pkgdir/opt/thunderbird-$pkgver/hyphenation"
}