# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

pkgname=thunderbird-beta-bin
pkgver=5.0b1
pkgrel=1
pkgdesc="Standalone Mail/News reader - Bleeding edge binary version"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'mozilla-common' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'fontconfig' 'freetype2' 'cairo' 'desktop-file-utils' 'libpng')
optdepends=('libcanberra: for sound support'
            'yasm: for WebM support')
provides=("thunderbird=${pkgver}")
conflicts=("${pkgname/-bin}")
install=${pkgname}.install
# In case releases.mozilla.org is not responding or the file(s) aren't yet uploaded there:
# source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/linux-${CARCH}/en-US/miramar-${pkgver}.tar.bz2
source=(http://releases.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/linux-${CARCH}/en-US/thunderbird-${pkgver}.tar.bz2
        ${pkgname/-bin}.desktop)
md5sums=(`wget ${source/li*}/MD5SUMS -qO - | grep en-US | grep "${CARCH}" | grep bz | cut -d " " -f1`
         '13f694aab7fab4ead5c2135ad82dd4f8')

package() {
  sed -i 's|/usr/local/lib/thunderbird-|/opt/thunderbird-|' thunderbird/thunderbird

  install -dm755 "${pkgdir}"/{usr/{bin,share/{applications,pixmaps}},opt}
  cp -a thunderbird "${pkgdir}/opt/thunderbird-${pkgver}"

  ln -s /opt/thunderbird-${pkgver}/thunderbird "${pkgdir}/usr/bin/${pkgname/-bin}"
  install -m644 thunderbird/chrome/icons/default/default48.png "${pkgdir}/usr/share/pixmaps/${pkgname/-bin}.png"
  install -m644 ${pkgname/-bin}.desktop "${pkgdir}/usr/share/applications"
}