# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=gmp-ecm
pkgver=6.4.1
pkgrel=1
pkgdesc='Elliptic Curve Method for Integer Factorization'
url='http://ecm.gforge.inria.fr/'
license=('GPL' 'LGPL')
depends=('gmp')
options=('!libtool')
arch=('i686' 'x86_64')
source=("http://gforge.inria.fr/frs/download.php/30410/ecm-${pkgver}.tar.gz")
sha1sums=('9735e7d75b441d4f20447b1df3010cc314ddf768')

build() {
	cd "${srcdir}/ecm-${pkgver}"
	export CFLAGS="$CFLAGS -msse"
	./configure --prefix=/usr --enable-shared --enable-openmp
	# make ecm-params
	make
}

check() {
	cd "${srcdir}/ecm-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/ecm-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
