# Maintainer: sausageandeggs <sausageandeggs@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# $Id: PKGBUILD 155541 2012-04-04 14:13:00Z andrea $
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=('kdebase-dolphin-klook')
pkgver=4.9.2
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Dolphin with klook enabled"
url='https://projects.kde.org/projects/playground/base/klook'
license=('GPL2' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
depends=('klook-git')
makedepends=('cmake' 'automoc4' 'tidyhtml')
source=("http://download.kde.org/stable/${pkgver}/src/kde-baseapps-${pkgver}.tar.xz" 
	    "${pkgname}-${pkgver}.patch")
provides=(kdebase-dolphin=${pkgver})
conflicts=(kdebase-dolphin)
install=kdebase-dolphin.install

build() {
	cd ${srcdir}/kde-baseapps-${pkgver}

	patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}.patch

	cd ..
	[[ -d build ]] && rm -rf build
	mkdir build
	cd build
	cmake ../kde-baseapps-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd $srcdir/build/dolphin
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/dolphin
	make DESTDIR=$pkgdir install
}
sha1sums=('8b5a45cfd77e7e67a57c06cdbf387692e8547b66'
          'f0b23488a41af93e416b0b0babfb6e5673b17e8a')
