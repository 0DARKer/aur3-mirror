# Maintainer: sausageandeggs <sausageandeggs@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# $Id: PKGBUILD 155541 2012-04-04 14:13:00Z andrea $
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

_realname=kdebase-dolphin
pkgname=kdebase-dolphin-klook
pkgver=4.10.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Dolphin File Manager with KLook enabled"
url="http://kde.org/applications/system/dolphin/"
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
depends=('kdebase-runtime' 'kdebase-lib' 'nepomuk-widgets' 'klook-git')
makedepends=('kdelibs' 'cmake' 'automoc4' 'tidyhtml')
optdepends=('kdegraphics-svgpart: thumbailers for SVG files'
            'kdegraphics-thumbnailers: thumbnailers for graphics file'
            'ruby: servicemenu installation')
source=("http://download.kde.org/stable/${pkgver}/src/kde-baseapps-${pkgver}.tar.xz" 
        "${pkgname}-${pkgver}.patch")
provides=($_realname=${pkgver})
conflicts=($_realname)
install=$_realname.install

build() {
  cd ${srcdir}/kde-baseapps-${pkgver}

  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}.patch

  cd ..
  [[ -d build ]] && rm -rf build
  mkdir build
  cd build

  cmake ../kde-baseapps-${pkgver} \
          -DCMAKE_BUILD_TYPE=Release \
          -DKDE4_BUILD_TESTS=OFF \
          -DCMAKE_SKIP_RPATH=ON \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DQT_QMAKE_EXECUTABLE=qmake4

  make
}

package() {
  cd $srcdir/build/dolphin
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc/dolphin
  make DESTDIR=$pkgdir install
}

sha1sums=('5b0127bd946286f52d98a1779eabf65e416a66c3'
          '94c0ff6af0100a23fea78c0ee60624f32b6406ae')
