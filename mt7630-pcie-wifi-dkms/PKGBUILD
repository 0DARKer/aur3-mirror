# Maintainer: Benjamin ROBIN <dev@benjarobin.fr>

pkgname=mt7630-pcie-wifi-dkms
pkgver=20140625
pkgrel=1
pkgdesc="MT7630 PCIe Wifi (DKMS)"
arch=('i686' 'x86_64')
url="http://www.mediatek.com/en/downloads/mt7630-pcie/"
license=('GPL')
depends=('dkms')
optdepends=('linux-headers: If running standard kernel, otherwise find matching headers for your kernel')
install=MT7630e-wifi.install
source=('http://www.mediatek.com/AmazonS3/Downloads/linux/MT7630E_Wi-Fi_BT_Source_V3.14_20140625_v2.tar.gz'
        'dkms.conf'
        'MT7630E-3.15.patch') # http://sprunge.us/eWSS
md5sums=('d8583926d6c8ba8c3a1a8dd0b44a066e'
         '9be7eb03a24153a2708fa168bcb5a5ab'
         '561dc16b3df8f48aa15c2ce1ce1e6d3b')

prepare() {
    cd "$srcdir/MT7630E_Wi-Fi_BT_Source_V3.14_20140625_v2"
    patch -p1 -i "$srcdir/MT7630E-3.15.patch"
}

package() {
    cd "$srcdir/MT7630E_Wi-Fi_BT_Source_V3.14_20140625_v2"
    
    mkdir -p "${pkgdir}/usr/src/mt7630-pcie-wifi-${pkgver}"
    cp -RL --no-preserve='ownership' rt2x00/* "${pkgdir}/usr/src/mt7630-pcie-wifi-${pkgver}/"
    install -m644 "$srcdir/dkms.conf" "${pkgdir}/usr/src/mt7630-pcie-wifi-${pkgver}/"
    
    install -D -m644 "firmware/Wi-FI/MT7650E234.bin" "$pkgdir/usr/lib/firmware/MT7650E234.bin"
}
