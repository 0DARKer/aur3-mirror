# Maintainer: Tetsumaki <http://goo.gl/YMBdA>

pkgname=fwtool-git
pkgver=20110116
pkgrel=1
pkgdesc="PS3 Firmware PKG tool"
arch=('i686' 'x86_64')
url="http://www.teknoconsolas.es/index.php?q=blogs/waninkoko"
license=('GPLv3')
makedepends=('git')
optdepends=('geohot-ps3publictools-git' 'gpup' 'ps3-sfo-editor' 'fail0verflow-git')

_gitroot="git://github.com/evilsperm/fwtool.git"
_gitname="fwtool"

build() {
  cd "${srcdir}"

  if [ -d ${_gitname} ]; then
    cd "${_gitname}" && git pull origin
  else
    git clone $_gitroot
  fi

  rm -rf "${srcdir}/${_gitname}-build"
  git clone --depth 1 "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  for src in `ls`; do
    case `file -b --mime-type "${src}"` in
      "application/x-executable")
        install -D -m755 "./${src}" "${pkgdir}/usr/bin/${src}";;
    esac
  done
}
