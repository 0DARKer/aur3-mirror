# Maintainer: Elvis Stansvik <elvstone@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
pkgname=apache-activemq
pkgver=5.8.0
pkgrel=2
pkgdesc="Popular and powerful open source messaging and Integration Patterns provider"
arch=(any)
url="http://activemq.apache.org"
license=('Apache 2.0')
depends=('java-runtime')
backup=(etc/default/activemq)
source=(
	"http://archive.apache.org/dist/activemq/$pkgname/$pkgver/$pkgname-$pkgver-bin.tar.gz"
	"http://archive.apache.org/dist/activemq/$pkgname/$pkgver/$pkgname-$pkgver-bin.tar.gz.asc"
	activemq.service activemq.defaults activemq.patch) 
options=(!strip)

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    patch -p0 -i "${srcdir}"/activemq.patch
}

package() {
	mkdir -p "$pkgdir/usr/bin" "$pkgdir/opt/"
	install -D -m0644 "$srcdir/activemq.service" "$pkgdir/usr/lib/systemd/system/activemq.service"
	install -D -m0644 "$srcdir/activemq.defaults" "$pkgdir/etc/default/activemq"

	cd "$srcdir/$pkgname-$pkgver"
	mv "$srcdir/$pkgname-$pkgver" "$pkgdir/opt/activemq"

	ln -s /opt/activemq/bin/activemq "$pkgdir/usr/bin/activemq"
	ln -s /opt/activemq/bin/activemq-admin "$pkgdir/usr/bin/activemq-admin"

	rm -rf "$pkgdir/opt/activemq/bin/macosx"
}

sha256sums=('73657935c796275ed05df39a9247c1e507be0fa8f280c15fc3715f856ba54189'
            'SKIP'
            '97238c62dd416f519c2e591a9a20f7d1cd18c7fca3d15005676f42ed02d41489'
            '010cc6806a7438c2c6fbc6a184542d595b5cf64a00794239f9c6ecaab248e5db'
            '01842dc98cf0f3a0d5f648150c7d69aa74136490e8f3be0ec22fb011f59b2675')
