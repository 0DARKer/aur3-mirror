# Maintainer: Rodrigo Bezerra <rodrigobezerra21 at gmail dot com>
# Contributor: delor <bartekpiech@gmail com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: heinz from #qt-creator

pkgname=qtcreator-git
pkgver=2.7.0.1082.g6344e2a
pkgrel=1
pkgdesc="Lightweight, cross-platform integrated development environment"
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/Category:Tools::QtCreator"
license=('LGPL')
depends=('qt5-quick1' 'qt5-tools' 'qt5-svg')
makedepends=('git')
options=('docs')
optdepends=('qt5-doc: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project support'
            'openssh-askpass: for ssh support'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support'
            'valgrind: for analyze support')
provides=('qtcreator')
conflicts=('qtcreator')
install='qtcreator-git.install'
source=('git://gitorious.org/qt-creator/qt-creator.git'
        'qtcreator.desktop')
md5sums=('SKIP'
         '82888d4be900e7833d768050a135cd37')
_gitname="qt-creator"

pkgver() {
  cd "$srcdir/$_gitname"
  
  git describe --tags | sed 's/v/''/;s/-/./g'
}

build() {
  [[ -d build ]] && rm -r build
  mkdir build && cd build
  
  qmake ../qt-creator/qtcreator.pro
  make
  make docs -j1
}

package() {
  cd build

  make INSTALL_ROOT="${pkgdir}/usr/" install
  make INSTALL_ROOT="${pkgdir}/usr/" install_docs

  install -Dm644 "${srcdir}/qtcreator.desktop" \
    "${pkgdir}/usr/share/applications/qtcreator.desktop"
  install -Dm644 "${srcdir}/qt-creator/LGPL_EXCEPTION.TXT" \
    "${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT"
}
