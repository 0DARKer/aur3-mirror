pkgname=gwclient
pkgver=8.0.2
pkgrel=4
pkgdesc="Novell Groupwise 8 Client for Linux"
url="http://gwclient.provo.novell.com/"
license=(Novell-GW-8)
install=$pkgname.install
arch=('i686' 'x86_64')
case ${CARCH} in
    i686 )
        depends=('glibc' 'libstdc++5' 'jre' 'libxrender' 'hicolor-icon-theme' 'desktop-file-utils')
        makedepends=('unzip' 'gcc' 'rpmextract')
        ;;
    x86_64 )
        depends=('lib32-glibc' 'lib32-libstdc++5' 'bin32-jre' 'lib32-libxrender' 'hicolor-icon-theme' 'desktop-file-utils')
        makedepends=('unzip' 'gcc-multilib' 'rpmextract')
        ;;
esac
source=(http://gwclient.provo.novell.com/client/gw802hp4linuxclientm.zip)
md5sums=('66774e23221be818cc9fd68dcb31e00f')

package() {
      # extract blob package
    cd "$srcdir"
    unzip -o gw802hp4linuxclientm.zip "*client*"
    cd "$pkgdir"
    ls "$srcdir"/novell-groupwise-client*.rpm | xargs rpmextract.sh
    chmod +rx opt

      # Remove/comment this line to NOT remove all Help files
      # (it will increase the package size in ~90MB)
    rm -rf opt/novell/groupwise/client/lib/help

      # use distribution's JavaRE package
    rm -rf opt/novell/groupwise/client/{jre,java}
    #exit 1
    case ${CARCH} in
        i686 )
            sed -i 's:/opt/novell/groupwise/client/java/lib/i386:/opt/java/jre/lib/i386/client/:' "$pkgdir"/opt/novell/groupwise/client/bin/groupwise
            ;;
        x86_64 )
            sed -i 's:/opt/novell/groupwise/client/java/lib/i386:/opt/bin32-jre/lib/i386/client/:' "$pkgdir"/opt/novell/groupwise/client/bin/groupwise
            ;;
    esac

      # prepare system files
    rm -rf "$pkgdir"/usr
    install -d "$pkgdir"/usr/bin \
               "$pkgdir"/usr/share/applications/ \
               "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
               
    ln -s /opt/novell/groupwise/client/bin/groupwise.sh \
           "$pkgdir"/usr/bin/groupwise
    ln -s /opt/novell/groupwise/client/gwclient.desktop \
           "$pkgdir"/usr/share/applications/
    ln -s /opt/novell/groupwise/client/gwclient.png \
           "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
    
      # fix Name and Exec and Icon paths in the desktop file
    sed -i -e 's/Name=.*/Name=Novell GroupWise/;s/Exec=.*/Exec=groupwise/;s/Icon=.*/Icon=gwclient.png/' \
           "$pkgdir"/opt/novell/groupwise/client/gwclient.desktop
}
