# Contributor: Vryali <vryali at gmail dot com>
# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=gwclient
pkgver=8.0.2
pkgrel=6
pkgdesc="Novell Groupwise 8 Client for Linux"
url="http://gwclient.provo.novell.com/"
license=(Novell-GW-8)
install=$pkgname.install

arch=('i686' 'x86_64')
if [ "${CARCH}"  == "i686" ]; then
    depends=('glibc' 'libstdc++5' 'java-runtime' 'libxrender' 'libgnome' 'hicolor-icon-theme' 'desktop-file-utils')
    makedepends=('unzip' 'gcc' 'rpmextract' 'prelink')
elif [ "${CARCH}"  == "x86_64" ]; then
    depends=('lib32-glibc' 'lib32-libstdc++5' 'java-runtime-32' 'lib32-libxrender' 'libgnome' 'hicolor-icon-theme' 'desktop-file-utils')
    makedepends=('unzip' 'gcc-multilib' 'rpmextract' 'prelink')
fi
source=(http://gwclient.provo.novell.com/client/gw802hp4linuxclientm.zip)
md5sums=('66774e23221be818cc9fd68dcb31e00f')

package() {
      # extract blob package to $pkgdir
    cd "$srcdir"
    unzip -o gw802hp4linuxclientm.zip "*client*"
    cd "$pkgdir"
    ls "$srcdir"/novell-groupwise-client*.rpm | xargs rpmextract.sh
    chmod +rx opt

      # Remove/comment this line to NOT remove all Help files
      # (it will increase the package size in ~90MB)
    rm -rf opt/novell/groupwise/client/lib/help
    
      # use distribution's JavaRE package
    rm -rf opt/novell/groupwise/client/java
    if [ "${CARCH}"  == "i686" ]; then
        sed -i "$pkgdir"/opt/novell/groupwise/client/bin/groupwise \
            -e 's:/opt/novell/groupwise/client/java/:/opt/jre/:'
    elif [ "${CARCH}"  == "x86_64" ]; then
        sed -i "$pkgdir"/opt/novell/groupwise/client/bin/groupwise \
            -e 's:/opt/novell/groupwise/client/java/:/opt/java32/jre/:'
    fi
    
      # fix Name and Exec and Icon paths in the desktop file
    sed -i "$pkgdir"/opt/novell/groupwise/client/gwclient.desktop \
        -e 's/Name=.*/Name=Novell GroupWise/' \
        -e 's/Exec=.*/Exec=groupwise/' \
        -e 's/Icon=.*/Icon=gwclient.png'
           
      # avoid crash (https://bbs.archlinux.org/viewtopic.php?pid=1399352)
    sed -i 's/groupwise /groupwise -wr=off /' \
           "$pkgdir"/opt/novell/groupwise/client/bin/groupwise.sh
           
      # fix this lib's stack guard (eliminate warning in the console)
      execstack -c "$pkgdir"/opt/novell/groupwise/client/lib/libgwapijni.so.1

      # prepare system folders and symlinks
    rm -rf "$pkgdir"/usr
    install -d "$pkgdir"/usr/bin/ \
               "$pkgdir"/usr/share/applications/ \
               "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
               
    ln -s /opt/novell/groupwise/client/bin/groupwise.sh \
           "$pkgdir"/usr/bin/groupwise
    ln -s /opt/novell/groupwise/client/gwclient.desktop \
           "$pkgdir"/usr/share/applications/
    ln -s /opt/novell/groupwise/client/gwclient.png \
           "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
}
