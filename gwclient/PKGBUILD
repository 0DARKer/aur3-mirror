# Contributor: Vryali <vryali at gmail dot com>
# Maintainer: josephgbr <rafael.f.f1@gmail.com>

# NOTE: the source gw802linuxclientmulti is a ~200MB file, but the package
#       to be installed has only ~56MB.

pkgname=gwclient
_version=8.0.2
_build=90840
pkgver=${_version}.${_build}
pkgrel=7
pkgdesc="Novell Groupwise 8 Client for Linux"
url="http://gwclient.provo.novell.com/"
license=(custom)
install=$pkgname.install

arch=('i686' 'x86_64')
if [ "${CARCH}"  == "i686" ]; then
    depends=('glibc' 'libstdc++5' 'java-runtime' 'libxrender' 'libgnome'
             'hicolor-icon-theme' 'desktop-file-utils')
    makedepends=('gcc' 'rpmextract' 'prelink')
elif [ "${CARCH}"  == "x86_64" ]; then
    depends=('lib32-glibc' 'lib32-libstdc++5' 'java-runtime-32'
             'lib32-libxrender' 'libgnome' 'hicolor-icon-theme'
             'desktop-file-utils')
    makedepends=('gcc-multilib' 'rpmextract' 'prelink')
fi
source=(https://gwclient.innerweb.novell.com/client/gw802linuxclientmulti.tar.gz
        groupwise.sh.in)
md5sums=('16b1e563cc60b933ed2444e804ee7562'
         'a32d6960070819eec16332c929b8200d')

package() {
      # extract blob package to a better folder
    rm -rf "$pkgname-$pkgver"
    mkdir "$pkgname-$pkgver"
    cd "$pkgname-$pkgver"
    ls "$srcdir/gw${_version}-${_build}_client_linux_multi/novell-groupwise-client-${_version}-${_build}.i586.rpm" | xargs rpmextract.sh
    
      # reduce the number of char used in this script
    GWDIR=opt/novell/groupwise/client

      # remove/comment this line to NOT remove all Help files
      # (it will increase the package size in ~90MB)
    rm -rf $GWDIR/lib/help
    
      # remove JRE; use distribution's JavaRE package instead
    rm -rf $GWDIR/java
           
      # fix this lib's stack guard (eliminate warning in the console)
    execstack -c $GWDIR/lib/libgwapijni.so.1

      # define correct MOZ_PLUGIN_PATH and GWLIBDIR values
    if [ $CARCH == x86_64 ]; then
      MOZ_PLUGIN_PATH=/usr/lib32/mozilla
      GWLIBDIR=/usr/lib32/gwclient
    elif [ $CARCH == i686 ]; then
      MOZ_PLUGIN_PATH=/usr/lib/mozilla
      GWLIBDIR=/usr/lib/gwclient
    fi
    
      # prepare system folders and symlinks
    install -d "$pkgdir"/opt/novell/groupwise/bin \
               "$pkgdir"/usr/bin/ \
               "$pkgdir"/$GWLIBDIR \
               "$pkgdir"/usr/share/applications/ \
               "$pkgdir"/usr/share/icons/hicolor/48x48/apps/

      # install /opt stuff
    mv $GWDIR/bin/groupwise-bin "$pkgdir"/opt/novell/groupwise/bin
    ln -s $GWLIBDIR "$pkgdir"/opt/novell/groupwise/lib
    
      # install binaries
    install -m755 $srcdir/groupwise.sh.in "$pkgdir"/usr/bin/groupwise
    sed -i "$pkgdir"/usr/bin/groupwise \
        -e "s#@MOZ_PLUGIN_PATH@#$MOZ_PLUGIN_PATH#" \
        -e "s#@GWLIBDIR@#$GWLIBDIR#"
    
      # install libraries
    mv $GWDIR/lib/* "$pkgdir"/$GWLIBDIR
    
      # install icons and desktop files
    mv $GWDIR/gwclient.desktop "$pkgdir"/usr/share/applications/
    mv $GWDIR/gwclient.png "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
    
      # fix Name and Exec and Icon paths in the desktop file
    sed -i "$pkgdir"/usr/share/applications/gwclient.desktop \
        -e 's/Name=.*/Name=Novell GroupWise/' \
        -e 's/Exec=.*/Exec=groupwise/'        \
        -e 's/Icon=.*/Icon=gwclient.png/'
}
