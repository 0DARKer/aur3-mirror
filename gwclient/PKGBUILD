pkgname=gwclient
pkgver=8.0.2
pkgrel=4
pkgdesc="Novell Groupwise 8 Client for Linux"
url="http://gwclient.provo.novell.com/"
license=(Novell-GW-8)
arch=('i686' 'x86_64')
case ${CARCH} in
    i686 )
        depends=('glibc' 'libstdc++5' 'unzip' 'jre' 'libxrender')
        makedepends=('unzip' 'gcc' 'rpmextract')
        ;;
    x86_64 )
        depends=('lib32-glibc' 'lib32-libstdc++5' 'bin32-jre' 'hicolor-icon-theme' 'lib32-libxrender')
        makedepends=('unzip' 'gcc-multilib' 'rpmextract')
        ;;
esac
_srcname="gw802hp4linuxclientm.zip"
source=(http://gwclient.provo.novell.com/client/$_srcname)
md5sums=('66774e23221be818cc9fd68dcb31e00f')

package() {
    cd "$srcdir"
    unzip -o $_srcname "*client*"    
    cd "$pkgdir"
    ls "$srcdir"/novell-groupwise-client*.rpm | xargs rpmextract.sh
    chmod +rx opt

    rm -rf opt/novell/groupwise/client/{jre,java}
    sed -i 's%opt/novell/groupwise/client/jre%${JAVA_HOME}/jre%' "$pkgdir"/opt/novell/groupwise/client/bin/groupwise
    case ${CARCH} in
        i686 )
            sed -i '/LD_LIBRARY_PATH/s/:.*/:\/opt\/java\/jre\/lib\/i386:\/opt\/java\/jre\/lib\/i386\/client/' "$pkgdir"/opt/novell/groupwise/client/bin/groupwise
            ;;
        x86_64 )
            sed -i '/LD_LIBRARY_PATH/s/:.*/:\/opt\/bin32-jre\/jre\/lib\/i386:\/opt\/bin32-jre\/lib\/i386\/client/' "$pkgdir"/opt/novell/groupwise/client/bin/groupwise
            ;;
    esac

    rm -rf "$pkgdir"/usr
    install -d "$pkgdir"/usr/bin \
               "$pkgdir"/usr/share/applications/ \
               "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
               
    ln -rs "$pkgdir"/opt/novell/groupwise/client/bin/groupwise.sh \
           "$pkgdir"/usr/bin/groupwise
    ln -rs "$pkgdir"/opt/novell/groupwise/client/gwclient.desktop \
           "$pkgdir"/usr/share/applications/
    ln -rs "$pkgdir"/opt/novell/groupwise/client/gwclient.png \
           "$pkgdir"/usr/share/icons/hicolor/48x48/apps/
    
    # Fix Exec and Icone paths in the desktop file
    sed -i -e 's/Exec=.*/Exec=groupwise/;s/Icon=.*/Exec=gwclient.png/' \
           "$pkgdir"/opt/novell/groupwise/client/gwclient.desktop   
}
