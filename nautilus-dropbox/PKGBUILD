# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>
# Contributor: fazibear <fazibear@gmail.com>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>
# Contributor: Gordin <9ordin @t gmail.com>

pkgname=nautilus-dropbox
pkgver=0.7.1
pkgrel=2
pkgdesc="Dropbox for Linux - Nautilus extension"
arch=('i686' 'x86_64')
url="http://forums.dropbox.com/topic.php?id=21111"
license=('custom:CC-BY-ND-3' 'GPL')
depends=('libnotify' 'nautilus' 'dropbox' 'hicolor-icon-theme')
makedepends=('python2-docutils' 'python2' 'pygtk')
install=${pkgname}.install
options=('!libtool' '!emptydirs')
source=("https://www.dropbox.com/download?dl=packages/${pkgname}-${pkgver}.tar.bz2")
md5sums=('f08f6ffaecea55c2f1e615876dbf9479')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/"
  
  sed "s/python/python2/" \
  	-i configure \
  	-i Makefile.am \
  	-i Makefile.in \
  	-i dropbox.in \
  	-i rst2man.py
  
  # since python2-docutils, rst2man.py is named "rst2man2.py"
  sed "s#rst2man.py#/usr/bin/rst2man2.py#" \
  	-i configure
  
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/"
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/bin/dropbox"
  rm "${pkgdir}/usr/share/applications/dropbox.desktop"
  rmdir "${pkgdir}/usr/share/applications"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
