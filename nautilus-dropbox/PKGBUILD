# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>
# Contributor: fazibear <fazibear@gmail.com>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>
# Contributor: Gordin <9ordin @t gmail.com>

pkgname=nautilus-dropbox
pkgver=0.7.0
pkgrel=1
pkgdesc="Dropbox for Linux - Nautilus extension"
arch=('i686' 'x86_64')
url="http://www.dropbox.com"
license=('CC-BY-ND-3' 'GPL')
depends=('nautilus' 'dropbox')
makedepends=('docutils' 'pygtk' 'python2')
install=nautilus-dropbox.install
changelog=ChangeLog
options=('!libtool' '!emptydirs')
source=("https://www.dropbox.com/download?dl=packages/nautilus-dropbox-${pkgver}.tar.bz2")
md5sums=('0aabb7f09b21c26a739538de68ada3c5')

build() {
  cd "$srcdir/nautilus-dropbox-${pkgver}/"

  # fix python2 usage in Archlinux
  sed "s/python/python2/" \
	-i configure \
	-i configure.in \
	-i dropbox.in \
	-i rst2man.py \
	-i Makefile.am \
	-i Makefile.in

  # fix build failure of not finding .Tpo file
  sed 's|$(top_builddir)/libtool|libtool|' \
	-i aclocal.m4

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/nautilus-dropbox-$pkgver/"
  make install DESTDIR="$pkgdir"

  rm "$pkgdir/usr/bin/dropbox"
  rm "$pkgdir/usr/share/applications/dropbox.desktop"
}
