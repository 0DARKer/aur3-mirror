# Maintainer: josephgbr <rafael.f.f1@gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>
# Contributor: fazibear <fazibear@gmail.com>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>
# Contributor: Gordin <9ordin @t gmail.com>

pkgname=nautilus-dropbox
pkgver=0.6.9
pkgrel=2
pkgdesc="Dropbox for Linux - Nautilus extension"
arch=('i686' 'x86_64')
url="http://forums.dropbox.com/topic.php?id=21111"
license=('custom:CC-BY-ND-3' 'GPL')
depends=('libnotify' 'nautilus' 'dropbox')
makedepends=('docutils>=0.8' 'python2' 'pygtk')
install=$pkgname.install
changelog=$pkgname.changelog
options=('!libtool' '!emptydirs')
source=("https://www.dropbox.com/download?dl=packages/$pkgname-$pkgver.tar.bz2"
	'fix-rst2man.patch')
md5sums=('b79f8796aeea92979956f20973294321'
         'd89bc2746bfe6280ae2d051331ab79af')

build() {
  cd "$srcdir/$pkgname-$pkgver/"
  patch -p1 -i "$srcdir/fix-rst2man.patch"
  
  sed -i "s/| python/|python2/" configure.in
  sed -i "s/\[python\]/[python2]/" configure.in

  sed -i "s/| python/|python2/" configure
  sed -i "s/set dummy python/set dummy python2/" configure
  sed -i 's|$(top_builddir)/libtool|libtool|' configure

  sed -i 's|$(top_builddir)/libtool|libtool|' aclocal.m4
  sed -i "s|/usr/bin/python|/usr/bin/python2|" dropbox.in
  sed -i "s|env python|env python2|" rst2man.py
  sed -i 's|python |python2 |' Makefile.am
  sed -i 's|python |python2 |' Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/"
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/bin/dropbox"
  rm "$pkgdir/usr/share/applications/dropbox.desktop"
  rmdir "$pkgdir/usr/share/applications"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
