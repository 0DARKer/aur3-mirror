# Maintainer: lolilolicon <lolilolicon_at_gmail_dot_com>

pkgname=iat-git
pkgver=20111014
pkgrel=1
pkgdesc="Iso9660 Analyzer Tool detects many types of CD-ROM/DVD images, and converts them to UDF/ISO."
arch=('i686' 'x86_64')
url="http://iat.berlios.de"
license=('GPL')
makedepends=('git')
provides=('iat')
conflicts=('iat')

#_gitroot="git://git.berlios.de/iat"
_gitroot="git://github.com/lolilolicon/IAT.git"  # Temp. mirror till IAT finds a new home.
_gitname=master

build() {
  msg "Connecting to GIT server..."
  if [[ -d "$srcdir/$pkgname" ]]; then
    cd "$srcdir/$pkgname" && git pull origin $_gitname
  else
    git clone $_gitroot "$srcdir/$pkgname"
    cd "$srcdir/$pkgname"
    git checkout $_gitname
  fi
  msg "GIT checkout done or server timeout"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
}
