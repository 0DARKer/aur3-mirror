# Contributor: Ferrazzo Riccardo <f.riccardo87@gmail.com>
# Based on qtjambi PKGBUILD
# Category: lib

pkgname=qtjambi
pkgver=4.6.3
pkgrel=1

arch=( 'i686' 'x86_64')
[ $CARCH = i686 ] && ARCH=32
[ $CARCH = x86_64 ] && ARCH=64

pkgdesc="Java bindings for the Qt C++ toolkit."
url="http://qtjambi.sourceforge.net"
license=('GPL')

source=("http://sourceforge.net/projects/qtjambi/files/${pkgver}/qtjambi-linux${ARCH}-community-${pkgver}.tar.gz")
md5sums=()
[ $ARCH = 32 ] && md5sums=('ad7a3780fb02b44a93b4d42114bd9a38')
[ $ARCH = 64 ] && md5sums=('3a6528c256da8be7da00258059ee2745')

depends=( "qt>=4.5" "java-environment>=6" )
makedepends=("phonon")
conflicts=("qtjambi")

build() {

	# Set the location of your Qt directory here
	QTDIR=/usr

	# Set the location of your Qt plugin directory here
	PLUGINDIR=${QTDIR}/lib/qt/plugins

	# Make the necessary directories
	mkdir -p ${pkgdir}/{opt/qtjambi,${QTDIR}/{bin,lib,share/java},${PLUGINDIR}/{designer,qtjambi}}

	# Enter the unpacked archive
	cd ${srcdir}/qtjambi-linux${ARCH}-community-${pkgver}

	# Copy QtJambi to its /opt directory
	cp -a ./* ${pkgdir}/opt/qtjambi

	# Link the Java executables to their respective directory
	for jar in $( ls . | grep [.]jar ); do
		ln -s /opt/qtjambi/${jar} ${pkgdir}/usr/share/java
		ln -s /usr/share/java/${jar} ${pkgdir}/usr/share/java/${jar%-*}.jar
	done

	# Link the libraries to their respective directory
	for library in $( ls lib ); do
		if [[ ! -f ${QTDIR}/lib/${library} ]]; then
			ln -s /opt/qtjambi/lib/${library} ${pkgdir}/${QTDIR}/lib
		fi
	done

	# Link the binaries to their respective directory
	for binary in $( ls bin ); do
		if [[ ! -f /usr/bin/${binary} ]]; then
			ln -s /opt/qtjambi/bin/${binary} ${pkgdir}/${QTDIR}/bin
		fi
	done

	# Link the QtDesigner plugins to Qt's designer plugin directory
	for plugin in $( ls plugins/designer ); do
		ln -s /opt/qtjambi/plugins/designer/${plugin} ${pkgdir}/${PLUGINDIR}/designer
	done

	# Link the QtJambi plugins to their qtjambi plugin directory
	for plugin in $( ls plugins/qtjambi ); do
		ln -s /opt/qtjambi/plugins/qtjambi/${plugin} ${pkgdir}/${PLUGINDIR}/qtjambi
	done
} 
