# Maintainer: Kozec <kozec at kozec dot com>

pkgname='mcedit'
pkgver='0.1.6build299'
pkgrel=1
pkgdesc='Minecraft world editor'
arch=('i686' 'x86_64')
url='https://github.com/mcedit/mcedit'
license='BSD'

depends=('python2' 'python2-numpy' 'python2-opengl' 
	'python2-pygame' 'ttf-bitstream-vera' "pymclevel=$pkgver")
makedepends=('git')
conflicts=('mcedit-git' 'pymclevel-git')

source=(MCEdit MCEdit.desktop directories.patch
	disable-portable-mode-button.patch
	numpy1.7-compatibility.patch
	https://github.com/mcedit/mcedit/archive/${pkgver}.tar.gz
	)

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	# Provided setup.py depends on something weird, but I'm not even
	# going to bother.
	
	patch -Np0 -i ../directories.patch || return 1
	patch -Np0 -i ../disable-portable-mode-button.patch || return 1
	patch -Np1 -i ../numpy1.7-compatibility.patch || return 1
}


package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	mkdir -p "${pkgdir}/usr/bin"
	mkdir -p "${pkgdir}/usr/lib/mcedit"
	mkdir -p "${pkgdir}/usr/share/mcedit"
	mkdir -p "${pkgdir}/usr/share/applications"
	mkdir -p "${pkgdir}/usr/share/pixmaps"
	# Pys
	cp *.py "${pkgdir}/usr/lib/mcedit"
	for i in albow editortools filters ; do
		cp -R $i "${pkgdir}/usr/lib/mcedit/$i"
	done
	# Images and stuff
	cp *.png "${pkgdir}/usr/share/mcedit"
	for i in stock-schematics toolicons ; do
		cp -R $i "${pkgdir}/usr/share/mcedit/$i"
	done
	ln -s "/usr/share/mcedit/favicon.png" "${pkgdir}/usr/share/pixmaps/MCEdit.png"
	
	# Launcher
	install -D -m755 "${srcdir}/MCEdit" "${pkgdir}/usr/bin/"
	install -D "${srcdir}/MCEdit.desktop" "${pkgdir}/usr/share/applications"
}

md5sums=('b9de4195ddf84d85c0170e816ec0659e'
         'e598244d2770ee075d397d6c11dc140d'
         'd9a76225889bb8f775fed37ed5fc57e4'
         '098a4e6456b188e31594eafdde3b7ff4'
         '64b4617e54845b6d6e75486cdd1e2cd3'
         'b20a6479198f16c0a9bbcc5c591853ce')
