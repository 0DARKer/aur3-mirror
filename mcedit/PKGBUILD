# Maintainer: Kozec <kozec at kozec dot com>

pkgname='mcedit'
pkgver='0.1.6build320'
pkgrel=1
pkgdesc='Minecraft world editor'
arch=('i686' 'x86_64')
url='https://github.com/mcedit/mcedit'
license='BSD'

depends=('python2' 'python2-numpy' 'python2-opengl' 
	'python2-pygame' 'ttf-bitstream-vera' "pymclevel=$pkgver")
makedepends=('git')
conflicts=('mcedit-git' 'pymclevel-git')

source=(MCEdit MCEdit.desktop
	directories.patch
	disable-portable-mode.patch
	"don't-kill-my-opengl.patch"
	numpy1.7-compatibility.patch
	https://github.com/mcedit/mcedit/archive/${pkgver}.tar.gz
	)

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	# Provided setup.py depends on something weird, but I'm not even
	# going to bother.
	
	msg directories.patch
	patch -Np0 -i ../directories.patch || return 1
	msg disable-portable-mode.patch
	patch -Np0 -i ../disable-portable-mode.patch || return 1
	msg numpy1.7-compatibility.patch
	patch -Np1 -i ../numpy1.7-compatibility.patch || return 1
	msg "don't-kill-my-opengl.patch"
	patch -Np0 -i ../"don't-kill-my-opengl.patch" || return 1
}


package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	mkdir -p "${pkgdir}/usr/bin"
	mkdir -p "${pkgdir}/usr/lib/mcedit"
	mkdir -p "${pkgdir}/usr/share/mcedit"
	mkdir -p "${pkgdir}/usr/share/applications"
	mkdir -p "${pkgdir}/usr/share/pixmaps"
	# Pys
	cp *.py "${pkgdir}/usr/lib/mcedit"
	for i in albow editortools filters ; do
		cp -R $i "${pkgdir}/usr/lib/mcedit/$i"
	done
	# Images and stuff
	cp *.png "${pkgdir}/usr/share/mcedit"
	for i in stock-schematics toolicons ; do
		cp -R $i "${pkgdir}/usr/share/mcedit/$i"
	done
	ln -s "/usr/share/mcedit/favicon.png" "${pkgdir}/usr/share/pixmaps/MCEdit.png"
	
	# Launcher
	install -D -m755 "${srcdir}/MCEdit" "${pkgdir}/usr/bin/"
	install -D "${srcdir}/MCEdit.desktop" "${pkgdir}/usr/share/applications"
}

md5sums=('b9de4195ddf84d85c0170e816ec0659e'
         'e598244d2770ee075d397d6c11dc140d'
         'f506e084f18082603f1a38d49a8087c4'
         'b8b5e679ed9bbf9f6a0e489599d2c587'
         '2004adbd4b30a81f37c18e06713807d0'
         '17c62d6d70be435dd7a1e0a60a674152'
         '15560ae3521f627ef954f527333b1cf4')
