# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>
# Contributor: zoulnix <http://goo.gl/HQaP>

pkgname=opencity-svn
pkgver=462
pkgrel=1
pkgdesc="A full 3D city simulator game project (SVN-version)"
arch=('i686' 'x86_64')
url="http://opencity.info/"
license=('GPL')
depends=('mesa' 'sdl_image' 'sdl_net' 'sdl_mixer')
makedepends=('subversion')
conflicts=('opencity')
replaces=('opencity')

_svntrunk="https://opencity.svn.sourceforge.net/svnroot/opencity/trunk/opencity"
_svnmod=opencity

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD HERE
  #
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="${pkgdir}/" install

  # Removing unnecessary stuff
  find "${pkgdir}" -depth -type d -name ".svn" -exec rm -rf {} \;
}

# vim:set ts=2 sw=2 et:
