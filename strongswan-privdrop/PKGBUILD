# Maintainer: Noel Kuntze <noel [at] familie-kuntze dot de>

pkgname=strongswan-privdrop
pkgver=5.2.2
pkgrel=2
pkgdesc="open source IPsec implementation with privilege dropping enabled and set to run as non-root user"
url='http://www.strongswan.org'
license=("GPL")
arch=('i686' 'x86_64')
depends=('curl' 'gmp' 'iproute2' 'openssl' 'sqlite' 'gperf' 'sudo' 'libcap')
conflicts=('openswan' 'strongswan' 'strongswan-git')
options=(!libtool)
backup=(etc/ipsec.conf etc/strongswan.conf etc/strongswan.d/ etc/swanctl/swanctl.conf)
source=("http://download.strongswan.org/strongswan-${pkgver}.tar.bz2" 
	# needed because of #814
	"configure.patch::https://wiki.strongswan.org/attachments/download/586/configure.patch"
	# needed because of #819
	"invalid-proto-id.patch::https://wiki.strongswan.org/attachments/download/578/0001-ikev1-Set-protocol-ID-and-SPIs-in-INITIAL-CONTACT-no.patch"
	# needed for charon-systemd.user and charon-systemd.group support (see #887)    
        "charon-systemd.patch::https://wiki.strongswan.org/projects/strongswan/repository/revisions/f3c8332220f5be450199b909d4823cc1627bf47d/diff?format=diff"
	"missing-semicolon.patch::https://wiki.strongswan.org/projects/strongswan/repository/revisions/9c3c41f29bf5772626abde71f52c57c05e59fa94/diff/src/charon-systemd/charon-systemd.c?format=diff"
	"_updown" 
	"_updown_espmark" 
	"strongswan.conf"
	"strongswan-privdrop.install"
)

sha256sums=('cf2fbfdf200a5eced796f00dc11fea67ce477d38c54d5f073ac6c51618b172f4'
            '75f372ee1ed650100aad3e42871485710d00a764725849b1cd4b4d46946ad7bf'
            '50fc25bd151ecc9d617f699e5b7436c5aef57fdc92dc5bf2728b3d36173e8b27'
            '2e147333056bb0e22e18f3b3e59b8b923d06855f23d8f6c9125391069e164c6d'
            '5d4f3b4f6525a36159d983c428c647656ca34f49fa9a8433792a3ae3c1a221d7'
            '79d415109c8b884763ecb22207ec7a1ea13c4424e83ec99da801eb3efbb83781'
            '18ae3f8001a997c4211bfcfdca26181735d196287e7d53f299054a1dd08dea63'
            '9975a415f9d80502bebac71021bc62ab749ff0b57c9be2a14ab63ce6f489b490'
            '6d1f2b659bf4eff11403afbaa9976505080a874b264f54de8c3806d3ba792922')

# We don't build libipsec because it would get loaded before kernel-netlink and netkey, which
# would case processing to be handled in user space. Also, the plugin is experimental. If you need it,
# add --enable-libipsec and --enable-kernel-libipsec
prepare() {
        cd ${srcdir}/strongswan-${pkgver}
        patch -p1 < ${srcdir}/invalid-proto-id.patch
        patch -p1 < ${srcdir}/charon-systemd.patch
	patch -p1 < ${srcdir}/missing-semicolon.patch
        patch -p0 < ${srcdir}/configure.patch
}

build() {
   cd $srcdir/strongswan-$pkgver
  ./configure --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --with-ipsecdir=/usr/lib/strongswan \
        --enable-sqlite \
        --enable-openssl --enable-curl \
        --enable-sql --enable-attr-sql \
        --enable-farp --enable-dhcp \
        --enable-eap-sim --enable-eap-sim-file --enable-eap-simaka-pseudonym \
        --enable-eap-simaka-reauth --enable-eap-identity --enable-eap-md5 \
        --enable-eap-gtc --enable-eap-aka --enable-eap-aka-3gpp2 \
        --enable-eap-mschapv2 --enable-eap-radius --enable-xauth-eap \
        --enable-ha --enable-gcm --enable-ccm --enable-ctr --enable-unity \
	--enable-integrity-test --enable-load-tester --enable-test-vectors \
        --enable-af-alg --disable-ldap \
	--with-capabilities=libcap --enable-cmd --enable-ntru \
	--enable-vici --enable-swanctl --enable-systemd --enable-ext-auth
  make
}

package() {
  cd "$srcdir/strongswan-$pkgver"
  make DESTDIR=${pkgdir} install
  # The sudo_updown and sudo_updown_espmark scripts are simply the default updown scripts 
  # with "iptables" replaced by "sudo iptables" in order to make them work with privilege dropping
  # and running as non-root user.
  install -Dm750 -g strongswan $srcdir/_updown $pkgdir/usr/lib/strongswan/
  install -Dm750 -g strongswan $srcdir/_updown_espmark $pkgdir/usr/lib/strongswan/
  install -Dm644 -g strongswan $srcdir/strongswan.conf $pkgdir/etc/strongswan.conf
#  chgrp -R strongswan $pkgdir/etc/ipsec.d
#  chmod -R g+r $pkgdir/etc/ipsec.d
}

