# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=crunchyfrog
pkgver=0.4.1
pkgrel=4
pkgdesc="SQL editor and database schema browser for the GNOME desktop"
arch=(i686 x86_64)
url=http://code.google.com/p/crunchyfrog
license=(GPL3)
depends=(kiwi pygtksourceview2 python2-cairo python2-configobj python2-dbus python2-lxml python2-sexy python2-sphinx python2-xdg)
makedepends=(python2-distribute)
optdepends=('mysql-python: To handle MySQL Databases',
    'python-psycopg2: To handle PostgreSQL Databases',
    'python-pysqlite: To handle SQLite Databases',
    'pymssql: To handle Microsoft SQL Server Databases',
    'kinterbasdb: To handle Firebird Databases'
    'informixdb: To handle Informix Databases'
    'sapdb: To handle MaxDB'
    'ipython: To use built-in python shell')
[[ $CARCH == i686 ]] && optdepends+=('cx_oracle: To handle Oracle Databases')
source=(http://crunchyfrog.googlecode.com/files/$pkgname-$pkgver.tar.gz
    build-mo.patch)
sha256sums=('311ccc2c23952011594502cff6203ebd1388e5731d02153ae7a2376f0354fc93'
    'cea66de44986fcfa6afdaea72450444d0e11d7f5c2e0126958fbf7d89a135a99')
sha512sums=('e11e5d27a92bfacf3b73fef1ddcf9f292866005f7d0f5bbf898b9677f857eb0f82f561990d7ec3e074a4bc799757724c170c90e793a3f8e2c576f66797a6d1fd'
    'da2cecfeae15a0de553982e3a89cf7ddb93571f2fd1ecfacfcf55a23a9efb932e8dbe40081bd8c34a4d75ecbb30382d1282e0b646d8ec38fb9a9a6a7cbb0db3b')

prepare() {
    cd $pkgname-$pkgver/
    sed -i 's:^#!/usr/bin/env python$:&2:' $pkgname
    patch -Np1 -i ../build-mo.patch
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1
}
