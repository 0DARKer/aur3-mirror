_pkgbasename=libidn
pkgname=lib32-$_pkgbasename
pkgver=1.19
pkgrel=2
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications (32 bit, library only)"
url="http://www.gnu.org/software/libidn/"
arch=('x86_64')
license=('GPL3' 'LGPL')
depends=('lib32-glibc' 'texinfo' "$_pkgbasename>=$pkgver")
makedepends=('pkgconfig' 'gcc-multilib')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('a45142126d28162014c995f969bdb5a8')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{bin,include,share,lib32/*.a}
}
