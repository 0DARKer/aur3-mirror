# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>
# Maintainer:  Patrick McCarty <pnorcks at gmail dot com>

pkgname=csound6-git
_realname=csound6
pkgver=20130305
pkgrel=3
pkgdesc="A programming language for sound rendering and signal processing - Git snapshot of v6 repo"
arch=('i686' 'x86_64')
url="http://www.csounds.com/"
license=('LGPL')
depends=('fltk'
         'fluidsynth'
         'liblo'
         'libvorbis'
         'portaudio'
         'portmidi')
makedepends=('alsa-lib'
             'boost'
             'cmake'
             'eigen3'
             'git'
             'gmm'
             'jack'
             'ladspa'
             'libpng'
             'luajit'
             'pd'
             'stk'
             'swig'
             'wiiuse')
provides=('csound6')
conflicts=('csound')
source=(http://ccrma.stanford.edu/software/stk/release/stk-4.4.4.tar.gz
        $_realname.sh
        luajit-pkgconfig.patch)
sha256sums=('1276986481704f148933d4a2f8eaebb84827124acbd0243e5c8a004fa7c70710'
            '48cec48486cd87604f138fc5afcd3e88f047ffe4e453f54a23ba5b933eeaa2b0'
            '956e9a7fe93a8d0ef77f765fb8a7c25c9d65370806f700c70e87179a537f57e8')

_gitroot="git://git.code.sf.net/p/csound/csound6-git"
_gitname="csound6-git"

build() {
  cd "$srcdir"
  msg "Connecting to git.code.sf.net..."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -R "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -p1 -i "$srcdir/luajit-pkgconfig.patch"

  cp -a "$srcdir"/stk-4.4.4/{include,rawwaves,src} Opcodes/stk

  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_COREMIDI=no \
        -DUSE_AUDIOUNIT=no \
        -DBUILD_JAVA_INTERFACE=no \
        -DBUILD_CSOUND_AC=no \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        ..
  make
}

package() {
  cd "$srcdir/$_gitname-build/build"
  make DESTDIR="$pkgdir/" install

  # lua bindings
  install -Dm755 luaCsnd.so "$pkgdir/usr/lib/lua/5.1/luaCsnd.so"
  ln -sf /usr/lib/lua/5.1/luaCsnd.so "$pkgdir/usr/lib/luaCsnd.so"

  # python bindings
  install -d "$pkgdir/usr/lib/python2.7/site-packages"
  install -m755 _csnd.so csnd.py "$pkgdir/usr/lib/python2.7/site-packages/"
  ln -sf /usr/lib/python2.7/site-packages/_csnd.so "$pkgdir/usr/lib/_csnd.so"
  ln -sf /usr/lib/python2.7/site-packages/csnd.py "$pkgdir/usr/lib/csnd.py"

  # examples
  install -d "$pkgdir/usr/share/$_realname"
  cp -a ../examples "$pkgdir/usr/share/$_realname"
  cp -a ../samples "$pkgdir/usr/share/$_realname"

  # stk rawwave files
  cp -a ../Opcodes/stk/rawwaves "$pkgdir/usr/share/$_realname"

  # export vars in profile.d
  install -Dm755 "$srcdir/$_realname.sh" "$pkgdir/etc/profile.d/$_realname.sh"

  # python2 fix
  sed -i "s/^\#\!.*python/&2/" `grep -rl "\#\!.*python" "$pkgdir"`

  # install vim files
  install -d "$pkgdir/usr/share/vim/vimfiles/plugin"
  install -d "$pkgdir/usr/share/vim/vimfiles/syntax"
  install -m644 ../installer/misc/vim/csound.vim "$pkgdir/usr/share/vim/vimfiles/plugin/"
  install -m644 ../installer/misc/vim/csound_*.vim "$pkgdir/usr/share/vim/vimfiles/syntax/"

}

# vim:set ts=2 sw=2 et:
