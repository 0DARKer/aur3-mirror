# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: vogo <vogo(at)seznam(dot)cz>

pkgname=pacman-color-testing
pkgver=4.0.2
pkgrel=1
pkgdesc="A color patched command-line frontend for libalpm (Pacman) - prefers [testing]"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman>=4.0' 'pacman<4.1')
conflicts=('pacman-color')
provides=('pacman-color')
backup=('etc/pacman.d/color.conf')
source=(http://mirrors.kernel.org/archlinux/other/pacman/pacman-$pkgver.tar.gz
        $pkgname-$pkgver.patch
        color.conf)
# sha1sums=(`curl -s ${source/pacman-*}sha1sums | grep $pkgver.tar.gz | cut -d " " -f1`
sha1sums=('fdfedc1dff9bfc8270a3eacccc340f33928feb2d'
          '272a8c1edb45a4cc09e32c492964207a6d7d7bd8'
          '6e36b8720de0363f95b2ba7e06beb0770915c5c3')

build() {
  cd pacman-$pkgver
  patch -p1 -i ../$pkgname-$pkgver.patch
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
  make
}

package() {
  install -D pacman-$pkgver/src/pacman/.libs/pacman "$pkgdir/usr/bin/pacman-color"
  install -Dm644 color.conf "$pkgdir/etc/pacman.d/color.conf"
}
