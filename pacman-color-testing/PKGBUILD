# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color-testing
pkgver=3.5.3
pkgrel=1
pkgdesc="A color patched command-line frontend for libalpm (Pacman) - prefers [testing]"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman>=3.5' 'pacman<3.6')
conflicts=('pacman-color')
provides=('pacman-color')
backup=('etc/pacman.d/color.conf')
source=(ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz
        ${pkgname}-${pkgver}.patch
        color.conf)
md5sums=('c36c18ed4d8ec69c0ecb4f9684266901'
         'd25c520aea1921412ffed397e0a36657'
         '47665f5054196c20ba0dd280a8d4c5e1')

build() {
  cd pacman-${pkgver}
  patch -p1 -i ../${pkgname}-${pkgver}.patch
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
  make
}

package() {
  install -Dm755 pacman-${pkgver}/src/pacman/.libs/pacman "${pkgdir}/usr/bin/pacman-color"
  install -Dm644 color.conf "${pkgdir}/etc/pacman.d/color.conf"
}