# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color-testing
pkgver=3.5.4
pkgrel=2
pkgdesc="A color patched command-line frontend for libalpm (Pacman) - prefers [testing]"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman>=3.5' 'pacman<3.6')
conflicts=('pacman-color')
provides=('pacman-color')
backup=('etc/pacman.d/color.conf')
source=(ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz
        ${pkgname}-${pkgver}.patch
        color.conf
        0001-makepkg-fix-removing-symbolic-link.patch)
sha1sums=('880e9db07edfad8032262e593fd7dc22a6d798fe'
          '14202ebcc63f8890ee41b2bc3e8259adf58f3776'
          '6e36b8720de0363f95b2ba7e06beb0770915c5c3'
          'ded96541d187571c11009b77e822a33e344ba387')

build() {
  cd pacman-${pkgver}
  patch -p1 -i ../0001-makepkg-fix-removing-symbolic-link.patch
  patch -p1 -i ../${pkgname}-${pkgver}.patch
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
  make
}

package() {
  install -Dm755 pacman-${pkgver}/src/pacman/.libs/pacman "${pkgdir}/usr/bin/pacman-color"
  install -Dm644 color.conf "${pkgdir}/etc/pacman.d/color.conf"
}