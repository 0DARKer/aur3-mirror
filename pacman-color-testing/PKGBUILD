# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color-testing
pkgver=3.5.0
pkgrel=1
pkgdesc="Command-line frontend for libalpm aka pacman with color patch"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=("pacman=${pkgver}")
makedepends=('patch')
conflicts=('pacman-color')
backup=('etc/pacman.d/color.conf')
source=(ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz
        ${pkgname}-${pkgver}-${pkgrel}.patch
        color.conf)
md5sums=('6cd87230cf77b8a83d1c1f093ea5cc64'
         '7128fcc4a0225e1d7e12ff08dbe73895'
         '47665f5054196c20ba0dd280a8d4c5e1')

build() {
  cd pacman-${pkgver}
  patch -p1 -i ../${pkgname}-${pkgver}-${pkgrel}.patch
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
  make
}

package() {
  # install pacman-color
  install -Dm755 "pacman-${pkgver}/src/pacman/.libs/pacman" \
                 "${pkgdir}/usr/bin/pacman-color"

  # install color.conf
  install -Dm644 "color.conf" \
                 "${pkgdir}/etc/pacman.d/color.conf"
}

