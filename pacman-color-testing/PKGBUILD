# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color-testing
pkgver=3.5.2
pkgrel=1
pkgdesc="Command-line frontend for libalpm aka pacman with color patch"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman>=3.5' 'pacman<3.6')
conflicts=('pacman-color')
provides=('pacman-color')
backup=('etc/pacman.d/color.conf')
source=(ftp://ftp.archlinux.org/other/pacman/pacman-${pkgver}.tar.gz
        ${pkgname}-${pkgver}-${pkgrel}.patch
        color.conf)
md5sums=('b4f1fdbc17100923071ebe8fe9377be5'
         '470e4ced3356ebcc6f540aef9829a9d6'
         '47665f5054196c20ba0dd280a8d4c5e1')

build() {
  cd pacman-${pkgver}
  patch -p1 -i ../${pkgname}-${pkgver}-${pkgrel}.patch
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
  make
}

package() {
  # install pacman-color
  install -Dm755 pacman-${pkgver}/src/pacman/.libs/pacman ${pkgdir}/usr/bin/pacman-color

  # install color.conf
  install -Dm644 color.conf ${pkgdir}/etc/pacman.d/color.conf
}

