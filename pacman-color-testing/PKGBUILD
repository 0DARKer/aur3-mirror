# Contributor: vogo <vogo(at)seznam(dot)cz>
pkgname=pacman-color-testing
pkgver=3.5.4
pkgrel=2
pkgdesc="A color patched command-line frontend for libalpm (Pacman) - prefers [testing]"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman>=3.5' 'pacman<3.6')
conflicts=('pacman-color')
provides=('pacman-color')
backup=('etc/pacman.d/color.conf')
source=(http://mirrors.kernel.org/archlinux/other/pacman/pacman-${pkgver}.tar.gz
        ${pkgname}-${pkgver}.patch
        color.conf)
sha1sums=(`wget ${source/pacman-*}sha1sums -qO - | grep ${pkgver}.tar.gz | cut -d " " -f1`
          'ad9b6a3b385f9e4dc0be196723cc8e73609edf0c'
          '6e36b8720de0363f95b2ba7e06beb0770915c5c3')

build() {
  cd pacman-${pkgver}
  patch -p1 -i ../${pkgname}-${pkgver}.patch
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-doc
  make
}

package() {
  install -Dm755 pacman-${pkgver}/src/pacman/.libs/pacman "${pkgdir}/usr/bin/pacman-color"
  install -Dm644 color.conf "${pkgdir}/etc/pacman.d/color.conf"
}