# Maintainer: Kelly Prescott <kprescott@coolip.net>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=brltty-minimal
pkgver=4.5
pkgrel=2
pkgdesc="Braille display driver for Linux/Unix"
arch=(i686 x86_64)
url="http://mielke.cc/brltty"
license=(GPL LGPL)
depends=(bash icu)
conflicts=('brltty')
provides=('brltty')
backup=(etc/brltty.conf)
options=('!emptydirs')
install=brltty.install
source=(http://mielke.cc/brltty/releases/brltty-${pkgver}.tar.gz
        brltty.service)

build() {
  cd "${srcdir}/brltty-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --mandir=/usr/share/man \
    --with-tables-directory=/usr/share/brltty \
    --with-screen-driver=lx \
    --disable-gpm \
    --disable-speech-support \
    --disable-java-bindings \
    --disable-python-bindings \
    --disable-tcl-bindings \
    --disable-caml-bindings \
    --disable-static

  make
}

package() {
  cd "${srcdir}/brltty-${pkgver}"
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 Documents/brltty.conf "$pkgdir/etc/brltty.conf"
  install -Dm644 ../brltty.service "$pkgdir/usr/lib/systemd/system/brltty.service"
}
md5sums=('7b52fa7746fed41ed344a1f75ce55951'
         '0cad54bb5470122535f5e3a11d5ca123')
