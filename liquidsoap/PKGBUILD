# Maintainer: kfgz <kfgz at interia pl>
# Contributor: M0Rf30
# Contributor: Luciano A. Ferrer <laferrer at gmail dot com>

pkgname=liquidsoap
pkgver=1.0.0
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="Liquidsoap is the audio stream generator of the Savonet project, notably used for generating the stream of netradios"
url="http://savonet.sourceforge.net/"
install=liquidsoap.install
options=(!makeflags)
depends=('ocaml-pcre' 'perl-xml-dom' 'libxml-perl' 'ocaml-camomile'
         'ocaml-magic' 'ocaml-dtools' 'ocaml-duppy' 'ocaml-findlib' 'ocaml-aacplus'
         'ocaml-yojson' 'ocaml-xmlm' 'ocaml-xmlplaylist' 'ocaml-lastfm' 'vo-aacenc'
         'gavl' 'soundtouch' 'schroedinger' 'libtheora' 'flac' 'lame' 'portaudio'
         'libmad' 'speex' 'libvorbis' 'libpulse' 'libsamplerate' 'glib2' 'faad2' 
         'libogg' 'gstreamer0.10-base' 'libao' 'taglib' 'libxau')
         
#dssi, ocaml-dssi
conflicts=(liquidsoap-full)
source=(${pkgname}-${pkgver}-full.tar.bz2::http://sourceforge.net/projects/savonet/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}-full.tar.bz2/download
        PACKAGES)
md5sums=('e379caaf68b1141b0b34bdb3db14ab69'
         '703cc0c3c1e78484309ba3b40bc52545')

build() {
  cp "${srcdir}"/PACKAGES "${srcdir}"/${pkgname}-${pkgver}-full
  cd "${srcdir}"/${pkgname}-${pkgver}-full
  ./configure --prefix=/usr  --localstatedir=/var --sysconfdir=/etc --with-user=root --with-group=root
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}-full
  make DESTDIR="${pkgdir}" \
       datadir="${pkgdir}"/usr/share \
       mandir="${pkgdir}"/usr/share/man \
       localstatedir="${pkgdir}"/var \
       bindir="${pkgdir}"/usr/bin \
       libdir="${pkgdir}"/usr/lib \
       sysconfdir="${pkgdir}"/etc \
  install
}