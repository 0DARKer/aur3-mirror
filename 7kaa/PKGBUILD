# Contributor: xnitropl <xnitropl at gmail dot com>
# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>


pkgname=7kaa
pkgver=2.14.3
pkgrel=2
pkgdesc='Seven Kingdoms: Ancient Adversaries is a real-time strategy (RTS) computer game developed by Trevor Chan'
url='http://7kfans.com/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('openal' 'sdl_net' 'sh' 'gcc-libs' '7kaa-data' '7kaa-music')
# Dependency 'sdl' provides by 'sdl_net'

source=("http://downloads.sourceforge.net/project/skfans/7KAA%20${pkgver}/${pkgname}-source-${pkgver}.tar.bz2"
        "${pkgname}.sh"
        "${pkgname}.desktop"
        "${pkgname}.ico")

md5sums=('145090c391a4c843500bde399b19ba9b'
         '8d0634b0962ad621a7608378d7172e02'
         '3fd08eeb86036fa9b18b91a91c62cb99'
         '996b0cdc8e4448ee456603c6640bb19b')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure
  make
}

package() {
  # main file
  install -dm755 "${pkgdir}/opt/${pkgname}/"
  install -D -m755 "${srcdir}/${pkgname}-${pkgver}/src/client/${pkgname}" "${pkgdir}/opt/${pkgname}/${pkgname}"

  # bash script
  install -D -m755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # readme
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/README" "${pkgdir}/usr/share/doc/${pkgname}/README-SourceCode"

  # desktop entry
  install -D -m644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "${srcdir}/${pkgname}.ico" "${pkgdir}/usr/share/pixmaps/${pkgname}.ico"
}

# vim:set ts=2 sw=2 et:
