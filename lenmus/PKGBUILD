# Maintainer: Uwe Koloska <kolewu@koloro.de>
# Contributor: Chris Chapman <chris dot chapman at aggiemail dot usu dot edu>

pkgname=lenmus
pkgver=5.3.1
pkgrel=1
pkgdesc="A free program to learn music"
arch=('i686' 'x86_64')
url="http://lenmus.sourceforge.net/en/phonascus/intro"
license=('GPL')
depends=('portmidi' 'freetype2' 'wxgtk' 'sqlite3' 'boost-libs' 'liblomse>=0.14.0')
optdepends=('timidity++: for sound without external midi player')
makedepends=('bzr' 'boost' 'unittestpp')

source=("http://downloads.sourceforge.net/lenmus/${pkgname}_${pkgver}.tar.gz"
        'cmake-boost.patch'
	'cmake-find.patch'
        'lenmus-src.patch')
md5sums=('76e0e24c63246aa43f241f683e181973'
         '97a4bd5bcca0d85f2f5708c805a1c23e'
	 '4dfbbbf1a1a34ff8d53767a5f2744ee7'
         '6593c53701f071271bdb35410df58c06')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch < "${srcdir}/cmake-boost.patch" || return 1
  patch -p0 < "${srcdir}/cmake-find.patch" || return 1
  patch -p0 < "${srcdir}/lenmus-src.patch" || return 1

  cd "$srcdir"
  rm -rf build
  mkdir build

  cd build
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr ../${pkgname}-${pkgver}/

  make -j$(nproc) || return 1
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}/" install
}
