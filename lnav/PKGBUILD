# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=lnav
pkgver=0.5.0
pkgrel=1
pkgdesc="A curses-based tool for viewing and analyzing log files."
arch=(i686 x86_64)
url="http://tstack.github.io/lnav/"
license=('custom:BSD')
depends=('pcre' 'zlib' 'bzip2' 'sqlite3')
source=("https://dl.dropboxusercontent.com/u/70174949/lnav.org/$pkgname-$pkgver.tar.bz2")
md5sums=('86a64d4dde1389de946eb79b21c25deb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # readme
  install -Dm644 README "$pkgdir/usr/share/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
