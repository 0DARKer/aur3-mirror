# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=lnav
pkgver=0.6.2
pkgrel=1
pkgdesc="A curses-based tool for viewing and analyzing log files."
arch=(i686 x86_64)
url="http://tstack.github.io/lnav/"
license=('custom:BSD')
depends=('pcre' 'bzip2' 'sqlite3' 'openssl')
source=("https://github.com/tstack/lnav/archive/v$pkgver.tar.gz")
md5sums=('2a98ed26fab4072c96b0cdfe13cec235')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/lnav/LICENSE"

  # man
  install -Dm644 lnav.1 "$pkgdir/usr/share/man/man1/lnav.1"
}

# vim:set ts=2 sw=2 et:
