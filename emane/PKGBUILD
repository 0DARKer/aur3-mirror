# Maintainer: Tom Wambold <tom5760@gmail.com>
pkgname=emane
pkgver=0.7.4
pkgrel=1
pkgdesc="Extendable Mobile Ad-hoc Network Emulator - complete package"
arch=('i686' 'x86_64')
url="http://labs.cengen.com/emane/"
license=('BSD')
options=(!libtool)
depends=(ace libxml2 libpcap)
optdepends=('python2: Python bindings'
            'perl: Perl bindings')
makedepends=(python2 perl)
source=(http://labs.cengen.com/emane/download/source/$pkgver/emane-source-bundle.$pkgver.tgz
        emanecommeffect-include-headers.patch)
md5sums=('171db7731088c844f7fd05d678b0d7fc'
         '755c4a084548222d2ac492558f8a3ae8')

_modules=(
  emane-$pkgver
  libemaneeventservice-0.4.9
  emanecommeffect-0.4.7
  emanecommeffectcontroller-0.4.4
  emanegeneel-0.1.6
  emanelosscontroller-0.3.5
  emanetransinteractive-0.1.8
  emanetransplug-0.2.9
)

_perl_modules=(
  EMANE-Event-AntennaDirection-0.1.1
  EMANE-Event-CommEffect-0.4.6
  EMANE-Event-Location-0.3.6
  EMANE-Event-Pathloss-0.3.6
  EMANE-EventService-0.3.6
)

_python_modules=(
  python-emane-0.0.1
  python-emaneeventantennadirection-0.1.1
  python-emaneeventcommeffect-0.4.5
  python-emaneeventlocation-0.3.5
  python-emaneeventpathloss-0.3.5
  python-emaneeventservice-0.3.5
)
build() {
  cd "$srcdir"

  for m in "${_modules[@]}" "${_perl_modules[@]}" "${_python_modules}"
  do
    msg "Extracting $m"
    bsdtar -xf "$m.tar.gz"
  done

  # emanecommeffect doesn't install its headers by default for some reason
  pushd emanecommeffect-0.4.7
  patch -p1 -i "$srcdir/emanecommeffect-include-headers.patch"
  autoreconf
  popd

  export CPPFLAGS="$CPPFLAGS -I$pkgdir/usr/include"
  export PASTHRU_DEFINE="$CPPFLAGS"
  export LDFLAGS="$LDFLAGS -L$pkgdir/usr/lib"

  for m in "${_modules[@]}"
  do
    msg "Building $m"
    pushd "$m"
    ./configure --prefix=/usr
    make
    msg "Packaging $m"
    make DESTDIR="$pkgdir/" install
    popd
  done

  for m in "${_perl_modules[@]}"
  do
    msg "Building $m"
    bsdtar -xf "$m.tar.gz"
    pushd "$m"
    perl Makefile.PL
    make
    msg "Packaging $m"
    make DESTDIR="$pkgdir/" install
    popd
  done

  for m in "${_python_modules[@]}"
  do
    msg "Building $m"
    bsdtar -xf "$m.tar.gz"
    pushd "$m"
    python2 setup.py install --root="$pkgdir/" --optimize=1
    popd
  done
}

# vim:set ts=2 sw=2 et:
