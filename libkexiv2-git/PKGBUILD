pkgname=libkexiv2-git
_gitname=libkexiv2
pkgver=v4.10.2.11.g5fe7238
pkgrel=1
pkgdesc='Libkexiv2 is a wrapper around Exiv2 library'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('kdelibs' 'exiv2')
makedepends=('git' 'automoc4' 'cmake')
conflicts=('libkexiv2')
provides=('libkexiv2')
groups=('kde-git' 'digikam-git')
source=('git://anongit.kde.org/libkexiv2')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $srcdir
  
  msg "GIT checkout done"
  msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build
  fi
  mkdir $srcdir/build
  cd $srcdir/build
  cmake $srcdir/$_gitname -DCMAKE_BUILD_TYPE=debugfull \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
