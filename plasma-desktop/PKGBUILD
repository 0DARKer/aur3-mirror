# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=5.0.0
pkgrel=2
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-desktop'
license=('LGPL')
depends=('kemoticons' 'kitemmodels' 'qt5-graphicaleffects' 'libxkbfile' 'libusb-compat' 'libcanberra' 'systemsettings' 'ksysguard' 'powerdevil' 
'oxygen' 'kmenuedit' 'kinfocenter')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('plasma-nm: Network manager applet')
replaces=('kde-workspace')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('83a010a0f784dbd863299e3191d6169c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
