# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=4.96.0
pkgrel=2
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-desktop'
license=('LGPL')
depends=('qt5-graphicaleffects' 'libxkbfile' 'libusb-compat' 'libcanberra' 'systemsettings' 'ksysguard' 'powerdevil' 'oxygen' 'kmenuedit')
makedepends=('extra-cmake-modules' 'kdoctools')
replaces=('kde-workspace')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz" 'fix-bs-sr-translation.patch')
md5sums=('c1788217676a3fc400706808b164af73'
         '7334267831b0c0061231263738004f6d')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ${srcdir}/fix-bs-sr-translation.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
