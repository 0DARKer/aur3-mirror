# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=4.98.0
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-desktop'
license=('LGPL')
depends=('kemoticons' 'kitemmodels' 'qt5-graphicaleffects' 'libxkbfile' 'libusb-compat' 'libcanberra' 'systemsettings' 'ksysguard' 'powerdevil' 
'oxygen' 'kmenuedit')
makedepends=('extra-cmake-modules' 'kdoctools')
replaces=('kde-workspace')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('5d54957fb720a1d68e339571c7908124')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# remove conflicting policy file
  rm -fr "$pkgdir"/usr
}
