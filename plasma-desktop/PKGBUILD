# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=4.96.0
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-desktop'
license=('LGPL')
depends=('qt5-graphicaleffects' 'libxkbfile' 'libusb-compat' 'libcanberra' 'systemsettings' 'ksysguard' 'powerdevil' 'oxygen' 'kmenuedit')
makedepends=('extra-cmake-modules' 'kdoctools')
replaces=('kde-workspace')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('c1788217676a3fc400706808b164af73')

prepare() {
  mkdir -p build

# fix build
  sed -i '/(sr@ijekavianlatin)/d' $pkgname-$pkgver/po/CMakeLists.txt
  sed -i '/(sr@ijekavian)/d' $pkgname-$pkgver/po/CMakeLists.txt
  sed -i '/(sr@latin)/d' $pkgname-$pkgver/po/CMakeLists.txt
  sed -i '/(sr)/d' $pkgname-$pkgver/po/CMakeLists.txt
  sed -i '/(bs)/d' $pkgname-$pkgver/po/CMakeLists.txt
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
