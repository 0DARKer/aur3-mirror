# Maintainer: M0Rf30

pkgname=flixtor
pkgver=1.0.0
pkgrel=1
pkgdesc="Flixtor streams movies, series, videos and animes from Torrents."
arch=('x86_64' 'i686')
url="https://github.com/TorrentLookup/Flixtor/"
license=('GPL3')
makedepends=('nodejs' 'nodejs-grunt-cli' 'ruby-compass-alpha')
options=('!strip')
install="flixtor.install"
_gitname="Flixtor"
source=("https://github.com/TorrentLookup/Flixtor/archive/v${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/TorrentLookup/Flixtor/v${pkgver}/images/flixtor-ico.png"
        "flixtor.install"
        "flixtor.desktop"
	"flixtor.sh")

prepare() {
  test ${arch} == "i686" && _platform="linux32" || _platform="linux64"
}

build() {
  cd "${srcdir}/${_gitname}-${pkgver}"
  npm install
  # Get dependencies
  msg2 "Getting dependencies"
  npm install 
  npm install grunt grunt-node-webkit-builder grunt-contrib-copy jschardet iconv-lite


  # Build
  msg2 "Prepare for ${_platform}"
  msg2 "Building"
  mkdir src
  cp -r frames fonts images js node_modules styles package.json src/
  
  grunt --gruntfile Gruntfile.js

  # Thanks to Revelation60 for pointing it out
  # https://github.com/rogerwang/node-webkit/wiki/The-solution-of-lacking-libudev.so.0
  msg2 "Patching program to fix libudev.so.0 problem"
  cd "${srcdir}/${_gitname}-${pkgver}/builds/releases/Flixtor/${_platform}/Flixtor"
  sed -i 's/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x30/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x31/g' Flixtor
}

package() {
  test ${arch} == "i686" && _platform="linux32" || _platform="linux64"
  msg2 "Prepare for ${_platform}"
  cd "${srcdir}"

  _bpath="${srcdir}/${_gitname}-${pkgver}/builds/releases/Flixtor/${_platform}/Flixtor"

  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  install -dm755 "${pkgdir}/usr/bin"

  # Program
  mv "${_bpath}/Flixtor" "${_bpath}/flixtor"
  install -Dm755 "${_bpath}/flixtor" "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 "${_bpath}/nw.pak" "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 "${_bpath}/libffmpegsumo.so" "${pkgdir}/usr/share/${pkgname}/"

  # Link to program
  install -Dm755 "${srcdir}/flixtor.sh" "${pkgdir}/usr/bin/${pkgname}"
  

  # Desktop file
  install -Dm644 "${srcdir}/flixtor.desktop" "${pkgdir}/usr/share/applications/flixtor.desktop"

  # Icon
  install -Dm644 "${srcdir}/flixtor-ico.png" "${pkgdir}/usr/share/pixmaps/flixtor.png"
}

md5sums=('13ada6463fa9fdd923d96a57e893fbfa'
         '3b2c42be51b39aff65dc2c67bb94dc27'
         'ea6fed9bb4cb7d7e6848ae43d755f407'
         '2716817a9ce83bb89c75aa82d7f4dcf8'
	 'bbf5af1b4860de8c5673daa1587bbad6')
