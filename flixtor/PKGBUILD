# Maintainer: M0Rf30

pkgname=flixtor
pkgver=2.2.1
pkgrel=2
pkgdesc="Flixtor streams movies, series, videos and animes from Torrents."
arch=('x86_64' 'i686')
url="http://www.flixtor.me/"
license=('GPL3')
makedepends=('nodejs' 'nodejs-grunt-cli' 'ruby-compass-alpha')
options=('!strip')
install="flixtor.install"
_gitname="FlixtorMe"
source=("https://github.com/FlixtorMe/FlixtorMe/archive/v${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/FlixtorMe/FlixtorMe/v${pkgver}/images/flixtor-ico.png"
        "flixtor.install"
        "flixtor.desktop")
_DEST="/usr/share/flixtor"
provides=("flixtor")

prepare() {
  test ${arch} == "i686" && _platform="linux32" || _platform="linux64"
}

build() {
  cd "${srcdir}/${_gitname}-${pkgver}"
  npm install
  # Get dependencies
  msg2 "Getting dependencies"
  npm install 
  npm install grunt grunt-node-webkit-builder grunt-contrib-copy jschardet iconv-lite


  # Build
  msg2 "Prepare for ${_platform}"
  msg2 "Building"
  mkdir src
  cp -r fonts frames images js locales node_modules styles package.json src/
  
  grunt --gruntfile Gruntfile.js

  # Thanks to Revelation60 for pointing it out
  # https://github.com/rogerwang/node-webkit/wiki/The-solution-of-lacking-libudev.so.0
  msg2 "Patching program to fix libudev.so.0 problem"
  cd "${srcdir}/${_gitname}-${pkgver}/builds/Flixtor/${_platform}/"
  sed -i 's/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x30/\x75\x64\x65\x76\x2E\x73\x6F\x2E\x31/g' Flixtor
}

package() {
  test ${arch} == "i686" && _platform="linux32" || _platform="linux64"
  msg2 "Prepare for ${_platform}"
  cd "${srcdir}/${_gitname}-${pkgver}/builds/Flixtor/${_platform}/"

  # Program
  install -dm755 "${pkgdir}${_DEST}"

  install -Dm755 Flixtor "${pkgdir}${_DEST}"
  install -Dm644 nw.pak "${pkgdir}${_DEST}"
  install -Dm644 libffmpegsumo.so "${pkgdir}${_DEST}"

  # Link to program
  msg2 "Symlink /usr/bin/${provides[0]} -> ${_DEST}/"
  install -dm755 "${pkgdir}/usr/bin"
  ln -s "${_DEST}/Flixtor" "${pkgdir}/usr/bin/${provides[0]}"
  

  # Desktop file
  install -Dm644 "${srcdir}/flixtor.desktop" "${pkgdir}/usr/share/applications/flixtor.desktop"

  # Icon
  install -Dm644 "${srcdir}/flixtor-ico.png" "${pkgdir}/usr/share/pixmaps/flixtor.png"
}
md5sums=('4a15cb1907ffab1b80ec2d9d7419e37f'
         '3b2c42be51b39aff65dc2c67bb94dc27'
         'ea6fed9bb4cb7d7e6848ae43d755f407'
         '2716817a9ce83bb89c75aa82d7f4dcf8')
