# Maintainer: Justin Goguen <adekoba@hamiltonshells.ca>
# Contributor: Jaroslav Lichtblau <tu@dragonlord.cz>

pkgname=cmus-git
pkgver=20100209
pkgrel=1
pkgdesc="A very feature-rich ncurses-based music player"
url="http://gitorious.org/cmus/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libao' 'libmpcdec' 'ffmpeg' 'flac' 'libmad' 'faad2>=2.6.1')
makedepends=('git')
provides=('cmus')
conflicts=('cmus')

_gitroot="git://gitorious.org/cmus/cmus.git"
_gitname="cmus"

build() {
  cd "${srcdir}"

  msg "connecting to gitorious.org git server"
  if [ -d ${srcdir}/$_gitname ] ; then
    cd $_gitname && git pull origin || return 1
  else
    git clone $_gitroot $_gitname || return 1
  fi
  msg "git checkout done"
  msg "starting make ..."

  rm -rf ${srcdir}/$_gitname-build
  cp -r ${srcdir}/$_gitname ${srcdir}/$_gitname-build
  cd ${srcdir}/$_gitname-build

  ./configure prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
