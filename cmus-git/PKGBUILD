# Maintainer: Richard "Nothing4You" Schwab <mail NOSPAM w.tf-w.tf>

# Found typos, bugs or other problems? Feel free to create a pull-request:
# https://github.com/Nothing4You/PKGBUILDs

pkgname=cmus-git
pkgver=2.5.0.67.gde9e9bd
pkgrel=1
epoch=1
pkgdesc="A very feature-rich ncurses-based music player"
arch=('i686' 'x86_64')
url="http://cmus.sourceforge.net"
depends=('ncurses')
makedepends=('faad2' 'ffmpeg' 'flac' 'libao' 'libmad' 'libmodplug'
             'libmp4v2' 'libmpcdec' 'libpulse' 'libvorbis' 'wavpack')
#             'libmp4v2' 'libmpcdec' 'libpulse' 'libvorbis' 'opusfile' 'wavpack')
optdepends=('alsa-lib: for ALSA output plugin support'
            'libao: for AO output plugin support'
            'libpulse: for PulseAudio output plugin support'
            'faad2: for AAC input plugin support'
            'ffmpeg: for ffmpeg input plugin support'
            'flac: for flac input plugin support'
            'libmad: for mp3 input plugin support'
            'libmodplug: for modplug input plugin support'
            'libmp4v2: for mp4 input plugin support'
            'libmpcdec: for musepack input plugin support'
            'libvorbis: for vorbis input plugin support'
#            'opusfile: for opus input plugin support'
            'wavpack: for wavpack input plugin support')
conflicts=('cmus')
provides=('cmus')
license=('GPL')
source=(git://github.com/cmus/cmus)
sha512sums=(SKIP)

pkgver() {
  cd "$srcdir/${pkgname%-*}"
  git describe | sed "s/^v//; s/-/./g"
}

prepare() {
  cd "$srcdir/${pkgname%-*}"

  sed 's|^#!/usr/bin/python\s*$|#!/usr/bin/env python2|' -i contrib/*.py
}

build() {
  cd "$srcdir/${pkgname%-*}"

  ./configure prefix=/usr
  make
}

package() {
  cd "$srcdir/${pkgname%-*}"

  make DESTDIR="$pkgdir" install

  install -Dm 644 contrib/_cmus "$pkgdir/usr/share/zsh/site-functions/_cmus"
  install -Dm 644 contrib/cmus-updatepidgin.py "$pkgdir/usr/share/doc/cmus/examples/cmus-updatepidgin.py"
  install -Dm 644 contrib/cmus-updategaim.py "$pkgdir/usr/share/doc/cmus/examples/cmus-updategaim.py"
}
