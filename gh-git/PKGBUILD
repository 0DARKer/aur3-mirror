# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>

pkgname=gh-git
pkgver=latest
pkgrel=1
pkgdesc='Fast GitHub command line client (development version)'
url='http://owenou.com/gh/'
license=('MIT')
source=('git://github.com/jingweno/gh.git')
md5sums=('SKIP')
sha1sums=('SKIP')
sha256sums=('SKIP')
arch=('i686' 'x86_64')
depends=()
makedepends=('git' 'go' 'mercurial')
conflicts=('gh')
provides=('gh')

pkgver() {
  cd $srcdir/gh

  git describe --always | sed 's/^v//;s/-/./g'
}

prepare() {
  cd $srcdir/gh

  # Set up the environment.
  export GOPATH=$(pwd):$(pwd)/Godeps/_workspace
  GHSRC=`mktemp -d -t gh.XXX`
  GHDEP=src/github.com/jingweno

  # Install source to GOPATH.
  cp -r $(pwd)/. $GHSRC
  mkdir -p $GHDEP
  mv $GHSRC $GHDEP/gh
}

build() {
  cd $srcdir/gh

  # Build it!
  go build -o bin/gh
}

package() {
  cd $srcdir/gh

  # Install the program.
  install -Dm755 bin/gh  $pkgdir/usr/bin/gh
}

# vim:set ts=2 sw=2 et:
