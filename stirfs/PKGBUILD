# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=stirfs
pkgver=1.1
pkgrel=1
pkgdesc='The secure, transparent and irresistible filesystem'
arch=('i686' 'x86_64')
url="http://www.normalesup.org/~bisson/msc/stirfs-${pkgver}.c"
license=('BSD')
depends=('fuse' 'openssl')
source=("${url}")
sha1sums=('3961d8408a86219b7abadc27ae8e13f8c4d11d42')

build() {
	cd "${srcdir}"
	cc ${CFLAGS} -D_FILE_OFFSET_BITS=64 -lfuse -lcrypto -o stirfs "stirfs-${pkgver}.c" ${LDFLAGS}
}

package() {
	cd "${srcdir}"
	install -d "${pkgdir}"/usr/{bin,share/licenses/stirfs}
	install -m 755 "${srcdir}"/stirfs "${pkgdir}"/usr/bin/
	head -n 22 "stirfs-${pkgver}.c" > "${pkgdir}"/usr/share/licenses/stirfs/COPYING
}
