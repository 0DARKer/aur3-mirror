# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=stirfs
pkgver=1.2
pkgrel=1
pkgdesc='Secure, transparent and irresistible filesystem'
url="http://www.normalesup.org/~bisson/msc/stirfs-${pkgver}.c"
license=('BSD')
arch=('i686' 'x86_64')
depends=('fuse' 'openssl')
source=("${url}")
sha1sums=('f05679069bc72d1848658d876225c22951d02795')

build() {
	cd "${srcdir}"
	cc ${CFLAGS} -D_FILE_OFFSET_BITS=64 -lfuse -lcrypto ${LDFLAGS} -o stirfs "stirfs-${pkgver}.c"
}

package() {
	cd "${srcdir}"
	install -d "${pkgdir}"/usr/{bin,share/licenses/stirfs}
	install -m 755 "${srcdir}"/stirfs "${pkgdir}"/usr/bin/
	head -n 22 "stirfs-${pkgver}.c" > "${pkgdir}"/usr/share/licenses/stirfs/COPYING
}
