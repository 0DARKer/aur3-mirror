# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=stirfs
pkgver=1.4
pkgrel=1
pkgdesc='Secure, transparent and irresistible filesystem'
url='http://www.normalesup.org/~bisson/src/'
license=('ISC')
arch=('i686' 'x86_64' 'armv7h')
depends=('fuse' 'openssl')
source=("${url}/${pkgname}-${pkgver}.c")
sha1sums=('49cbc43ceeb2563d5b99b90c20c32f3477a00389')

build() {
	cd "${srcdir}"
	cc -lfuse -lcrypto \
		${CFLAGS} ${LDFLAGS} \
		-o ${pkgname} ${pkgname}-${pkgver}.c
}

package() {
	cd "${srcdir}"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	head -n 15 "${pkgname}-${pkgver}.c" > "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
