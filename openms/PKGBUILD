 # Maintainer: saxonbeta <saxonbeta at gmail.com>
pkgname=openms
pkgver=1.11.1
pkgrel=1
pkgdesc="C++ library for LC/MS data management and analyses"
arch=('i686' 'x86_64')
url="http://open-ms.sourceforge.net/"
license=('BSD')
depends=('boost' 'gsl' 'libsvm' 'glpk' 'xerces-c' 'qtwebkit')
makedepends=('cmake' 'seqan')
source=(http://download.sourceforge.net/project/open-ms/OpenMS/OpenMS-$pkgver/OpenMS-$pkgver.tar.gz
        OpenMS-TOPPView.desktop OpenMS-TOPPAS.desktop)
md5sums=('76f06e3295a7007360ee046f1f304250'
	 '7ec5d860140446b79ae682c9915e491d'
	 '792bb6c4001ffe01fb31a0f9ca6f576a')
sha256sums=('e2403df3e3e902dd92480b4137753da7af66f12c422b795d0daa93fd9b2cc185'
	    '70da7a0606938fcfbf22b86951d0c27c6b5e7556b34456e49fe472e111fbbd28'
	    '1b99e1838fe84f6fde8adb146115a6c19e30b5ae3952f24d509939ad26642d1f')


build() {
  cd "$srcdir/OpenMS-$pkgver"
  cmake -DBOOST_USE_STATIC=OFF -DINSTALL_PREFIX=/usr -DCMAKE_FIND_ROOT_PATH=/usr
  
  make
}

package() {
  cd "$srcdir/OpenMS-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 $srcdir/OpenMS-TOPPView.desktop ${pkgdir}/usr/share/applications/OpenMS-TOPPView.desktop
  install -D -m644 $srcdir/OpenMS-TOPPAS.desktop ${pkgdir}/usr/share/applications/OpenMS-TOPPAS.desktop
}