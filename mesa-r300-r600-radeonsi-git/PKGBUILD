# Maintainer: Lone_Wolf <lonewolf@xs4all.nl>
# Contributor : Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Adapted from offical archlinux mesa package

pkgname=mesa-r300-r600-radeonsi-git
pkgver=20130227
pkgrel=1
_realver=9.2
pkgdesc="Mesa for R300, R600 & radeon SI chipsets - git version."
arch=('x86_64')
depends=('libxt' 'libxxf86vm' 'libxdamage' 'xorg-server' 'libxvmc' 'libvdpau' 'wayland' 'llvm-amdgpu-git')
makedepends=('pkgconfig' 'python2' 'talloc' 'libxml2' 'imake' 'git' 'glproto' 'dri2proto>=2.6' 'xorg-server-devel' 'resourceproto')
optdepends=('libtxc_dxtn: S3TC support'
            'mesa-demos-git: glxinfo and glxgears')
provides=("mesa=${_realver}" "libgl=${_realver}" "ati-dri=${_realver}" "libglapi=${_realver}" "osmesa=${_realver}" "libgbm=${_realver}" "libgles=${_realver}" "libegl=${_realver}" "khrplatform-devel=${_realver}")
conflicts=('mesa' 'libgl' 'ati-dri' 'libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
url="http://mesa3d.sourceforge.net"
license=(custom)
source=(LICENSE)
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a')
options=(!libtool)
_gitroot='git://anongit.freedesktop.org./git/mesa/mesa'
_gitname='mesa'

build() {
  cd "$srcdir"
  msg "Connecting to git.freedesktop.org GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,swrast \
    --with-dri-drivers=swrast \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --with-egl-platforms=x11,drm,wayland \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau \
    --enable-gallium-gbm \
    --enable-xvmc \
    --enable-xorg \
    --enable-r600-llvm-compiler \
    --with-llvm-shared-libs

# additonal flags not in official mesa
#    --enable-gallium-gbm \
#    --enable-xvmc \
#    --enable-xorg \
#    --enable-r600-llvm-compiler \


# left out compile flags
# default = auto 
#  --enable-32-bit	       build 32-bit libraries [default=auto]
#  --enable-64-bit	       build 64-bit libraries [default=auto]
#  --enable-vdpau          enable vdpau library [default=auto]
#  --disable-driglx-direct enable direct rendering in GLX and EGL for DRI [default=auto]
#  --enable-va             enable va library [default=auto]
#  --with-egl-platforms[=DIRS...]
#                          comma delimited native platforms libEGL supports,
#                          e.g. "x11,drm" [default=auto]

#
# default = enabled/yes
#  --enable-shared[=PKGS]       build shared libraries [default=yes]
#  --enable-fast-install[=PKGS] optimize for fast installation [default=yes]
#  --disable-asm	              disable assembly usage [default=enabled on supported plaforms]
#  --disable-pic                compile PIC objects [default=enabled for shared builds on supported platforms]
#  --enable-dri                 enable DRI modules [default=enabled]
#  --enable-glx                 enable GLX library [default=auto]
#  --disable-egl                disable EGL library [default=enabled]
#  --enable-gallium-llvm        build gallium LLVM support [default=enabled on x86/x86_64]
#
#
# default = disabled/no
#  --enable-debug          use debug compiler flags and macros [default=disabled]
#  --enable-mangling       enable mangled symbols and library name [default=disabled]
#  --enable-selinux	       Build SELinux-aware Mesa [default=disabled]
#  --disable-opengl	       disable support for standard OpenGL API [default=no]
#  --enable-opencl         enable OpenCL library [default=no]
#  --enable-xlib-glx       make GLX library Xlib-based instead of DRI-based [default=disable]
#  --enable-gallium-egl    enable optional EGL state tracker (not required for
#                          EGL support in Gallium with OpenGL and OpenGL ES)
#                          (default=disable]
#  --enable-gallium-tests  Enable optional Gallium tests) [default=disable]
#  --enable-gallium-g3dvl  build gallium g3dvl [default=disabled]
#
  make
}

package() {

  cd "${srcdir}/${_gitname}-build" 
  make DESTDIR="${pkgdir}" install

  mv ${pkgdir}/usr/lib/libGL.so.1.2.0   ${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib/libGL.so{,.1}

# mesa-libgl
# See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1.2.0

#license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
