# Maintainer: Lone_Wolf <lonewolf@xs4all.nl>
# Contributor : Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Adapted from offical archlinux mesa package

pkgname=mesa-r300-r600-radeonsi-git
pkgver=9.2.57088
pkgrel=1
_realver=9.2
pkgdesc="Mesa for R300, R600 & radeon SI chipsets - git version."
arch=('x86_64')
depends=('libxxf86vm' 'libxvmc' 'libxdamage' 'xorg-server' 'libvdpau' 'wayland' 'elfutils' 'llvm>=3.3')
makedepends=('pkgconfig' 'python2' 'talloc' 'libxml2' 'imake' 'git' 'glproto' 'dri2proto>=2.6' 'xorg-server-devel' 'resourceproto' 'llvm>=3.3')
optdepends=('libtxc_dxtn: S3TC support'
            'mesa-demos: glxinfo and glxgears')
provides=("mesa=${_realver}" "ati-dri=${_realver}" "libgl=${_realver}" "mesa-libgl=${_realver}" "khrplatform-devel=${_realver}" "libegl=${_realver}" "libgbm=${_realver}" "libglapi=${_realver}" "libgles=${_realver}" "osmesa=${_realver}")
conflicts=('mesa' 'ati-dri' 'libgl' 'mesa-libgl' 'khrplatform-devel' 'libegl' 'libgbm' 'libglapi' 'libgles' 'osmesa')
url="http://mesa3d.sourceforge.net"
license=(custom)
source=("${pkgname}::git://anongit.freedesktop.org./git/mesa/mesa#branch=master"
        'LICENSE')
md5sums=('SKIP'
         '5c65a0fe315dd347e09b1f2826a1df5a')
install=mesa-r300-r600-radeonsi-git.install
options=(!libtool)
#_gitroot='git://anongit.freedesktop.org./git/mesa/mesa'
#_gitname='mesa'

pkgver() {
  cd "${pkgname}"
    echo ${_realver}.$(git rev-list --count HEAD)

}

build() {
  
  cd "${pkgname}"
  CPPFLAGS="$CPPFLAGS -O2" ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,swrast \
    --with-dri-drivers=swrast \
    --with-llvm-shared-libs \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --with-egl-platforms=x11,drm,wayland \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-vdpau \
    --enable-gallium-gbm \
    --enable-xvmc \
    --enable-xorg \
    --enable-r600-llvm-compiler




# additonal flags not in official mesa
#    --enable-gallium-gbm \
#    --enable-xvmc \
#    --enable-xorg \
#    --enable-r600-llvm-compiler \


# left out compile flags
# default = auto 
#  --enable-32-bit	       build 32-bit libraries [default=auto]
#  --enable-64-bit	       build 64-bit libraries [default=auto]
#  --disable-driglx-direct enable direct rendering in GLX and EGL for DRI [default=auto]
#  --enable-va             enable va library [default=auto]
#
#
# default = enabled/yes
#  --enable-shared[=PKGS]       build shared libraries [default=yes]
#  --enable-fast-install[=PKGS] optimize for fast installation [default=yes]
#  --disable-asm	              disable assembly usage [default=enabled on supported plaforms]
#  --disable-pic                compile PIC objects [default=enabled for shared builds on supported platforms]
#
#
# default = disabled/no
#  --enable-debug          use debug compiler flags and macros [default=disabled]
#  --enable-mangling       enable mangled symbols and library name [default=disabled]
#  --enable-selinux	       Build SELinux-aware Mesa [default=disabled]
#  --disable-opengl	       disable support for standard OpenGL API [default=no]
#  --enable-openvg         enable support for OpenVG API [default=no]
#  --enable-opencl         enable OpenCL library [default=no]
#  --enable-xlib-glx       make GLX library Xlib-based instead of DRI-based [default=disable]
#  --enable-gallium-tests  Enable optional Gallium tests) [default=disable]
#
  make
}

package() {

  cd "${pkgname}" 
  make DESTDIR="${pkgdir}" install

  # rename libgl.so to not conflict with blobs - may break gl.pc ?
  mv ${pkgdir}/usr/lib/libGL.so.1.2.0   ${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib/libGL.so{,.1}

# mesa-libgl
# See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1.2.0

#license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
  #removing git leftovers
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
