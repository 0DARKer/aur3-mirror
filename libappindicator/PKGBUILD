# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libappindicator
pkgver=0.4.92
pkgrel=1
pkgdesc="Library to allow applications to export a menu into the Unity Menu bar (GTK+ 2 library)"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
depends=('libindicator' 'libdbusmenu-gtk2')
makedepends=('gobject-introspection' 'vala' 'gtk-sharp-2' 'pygtk' 'perl-xml-libxml' 'dbus-glib')
options=('!libtool')
source=(http://launchpad.net/libappindicator/0.5/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('3e960dea059510061553ce0c24d22e75')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix mono bindings location
  sed -i 's|/cli/|/mono/|' bindings/mono/{appindicator-sharp-0.1.pc.in,Makefile.in}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-tests --with-gtk=2
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 DESTDIR="$pkgdir/" install
}
