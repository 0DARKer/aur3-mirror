# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: pumpkin <pumpkin at mailoo dot org>
# Contributor: Vsevolod Balashov <vsevolod at balashov dot name>

pkgname=gunicorn-python2
_pkgname=gunicorn
pkgver=18.0
pkgrel=1
pkgdesc='WSGI HTTP Server for UNIX'
arch=(any)
url='http://gunicorn.org/'
license=(MIT)
depends=(python2)
optdepends=(
  'python2-paste: For the deprecated gunicorn_paster-python2 command.'
  'python2-paste-deploy: For the deprecated gunicorn_paster-python2 command.'
)
makedepends=(python2-distribute)
source=("http://pypi.python.org/packages/source/g/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('b61f38786af6b8d644b530552012d669c7415fcf5852de320f290800db97983d')

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1
  mv "${pkgdir}/usr/bin/gunicorn"        "${pkgdir}/usr/bin/gunicorn-python2"
  mv "${pkgdir}/usr/bin/gunicorn_django" "${pkgdir}/usr/bin/gunicorn_django-python2"
  mv "${pkgdir}/usr/bin/gunicorn_paster" "${pkgdir}/usr/bin/gunicorn_paster-python2"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
