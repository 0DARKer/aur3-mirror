# Maintainer: Dražen Odobašić <dodobasic at gmail>

pkgname=gdal-curl
_pkgname=gdal
pkgver=1.11.0
pkgrel=5
pkgdesc="A translator library for raster geospatial data formats with curl and podofo (PDF) support"
arch=('i686' 'x86_64')
url="http://www.gdal.org/"
license=('custom')
depends=('curl' 'geos' 'giflib' 'hdf5' 'libgeotiff' 'libjpeg' 'libpng' 'libtiff' 'netcdf'  'python2' 'python2-numpy' 'cfitsio' 'sqlite3' 'libmariadbclient' 'postgresql-libs' 'podofo')
makedepends=('perl' 'swig' 'chrpath')
optdepends=('postgresql: postgresql database support'
            'mariadb: mariadb/mysql database support'
            'swig:  perl binding support'
	    'poppler: pdf conversion support'
	    'libspatialite: spatiallite support')
options=('!libtool' '!makeflags')
source=(http://download.osgeo.org/${_pkgname}/${pkgver}/${_pkgname}-${pkgver}.tar.xz \
	gdal-1.5.1-python-install.patch \
        0001-Fix-compilation-errors-with-json-c-0.12-patch-by-lan.patch \
	0002-GIF-add-compatibility-with-giflib-5.1-5519.patch
)

md5sums=('31f2c4a7230b40e5fdc3cf12a100f96b'
	 '81afc1c26d29cee84aadb6924fe33861'
	 'b3a118568e08a72758b743cd028cb9bd'
	 'c66a1aecb924b13a3c3cd47d20198f6b'
)

provides=('gdal==1.11.0')
conflicts=('gdal')

build() {

  cd ${srcdir}/$_pkgname-$pkgver
  patch -Np0 -i ${srcdir}/gdal-1.5.1-python-install.patch
  patch -Np2 -i ${srcdir}/0001-Fix-compilation-errors-with-json-c-0.12-patch-by-lan.patch
  patch -Np2 -i ${srcdir}/0002-GIF-add-compatibility-with-giflib-5.1-5519.patch

  # python2 fixes
  for file in swig/python/{,osgeo/,samples/,scripts/}*.py; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr --with-netcdf --with-libtiff --with-sqlite3 \
              --with-geotiff --with-mysql --with-python=/usr/bin/python2 \
	      --without-libtool --with-curl --with-hdf5 --with-perl --with-geos \
              --with-png --with-podofo --with-spatialite

  make
}

package () {
  cd ${srcdir}/$_pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  # install license
  install -D -m644 LICENSE.TXT ${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE

  #FS15477 clean up junks
  rm -f ${pkgdir}/usr/bin/*.dox

  # Remove RPATH
  chrpath --delete "${pkgdir}"/usr/lib/perl5/${CARCH}-linux-thread-multi/auto/Geo/OSR/OSR.so
  chrpath --delete "${pkgdir}"/usr/lib/perl5/${CARCH}-linux-thread-multi/auto/Geo/OGR/OGR.so
  chrpath --delete "${pkgdir}"/usr/lib/perl5/${CARCH}-linux-thread-multi/auto/Geo/GDAL/GDAL.so
  chrpath --delete "${pkgdir}"/usr/lib/perl5/${CARCH}-linux-thread-multi/auto/Geo/GDAL/Const/Const.so
}
