# Maintainer: Michael Ivko <ivko.mv@gmail.com>
# Contributor: Lieven Moors <lievenmoors@gmail.com>

pkgname=ntk-git
_gitname=fltk
epoch=1
pkgver=ed50ecc
pkgrel=1
pkgdesc="Fork of FLTK 1.3.0 with additional functionality."
arch=('i686' 'x86_64')
url="http://non.tuxfamily.org/"
license=('custom:LGPL')
groups=()
depends=('libxft' 'cairo' 'libjpeg')
makedepends=('git' 'python')
provides=('ntk')
conflicts=('ntk')
source=('git://git.tuxfamily.org/gitroot/non/fltk.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname

  ./waf configure --prefix=/usr
  ./waf
}

package() {
  cd $_gitname
  ./waf --destdir="$pkgdir" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
