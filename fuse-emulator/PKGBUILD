# Contributor: bender02 at archlinux dot us
# Maintainer: SanskritFritz (gmail)

pkgname=fuse-emulator
pkgver=1.1.1
pkgrel=2
pkgdesc="ZX Spectrum emulator"
arch=('i686' 'x86_64')
url="http://fuse-emulator.sourceforge.net/fuse.php"
license=("GPL2")
depends=('gtk2' 'libspectrum>=1.0.0' 'libxml2' 'libpng' 'alsa-lib')
source=(http://downloads.sourceforge.net/sourceforge/fuse-emulator/fuse-$pkgver.tar.gz
        fuse-emulator.png fuse-emulator.desktop)

build() {
  cd "$srcdir"/fuse-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/fuse-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m 644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -D -m 644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
md5sums=('858e530ffc04a2ed62dce76ac29b5762'
         '8c764eed97b5fc8d555bee1f0abf36f5'
         '883d4f908a2ca6601c5ec3f90c3b5596')
