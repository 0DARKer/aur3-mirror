# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Paulo Freire <paulofreire gmail com>

pkgname=autokernconf
pkgver=2011.02.22
pkgrel=1
pkgdesc="AUTOmagical KERNel CONFiguration via the Linux Kernel Driver Database (LKDDb)"
arch=('any')
url="http://cateee.net/autokernconf/"
license=('GPL')
depends=('coreutils' 'grep' 'sed' 'which')
optdepends=('pciutils: for pci.ids data'
            'util-linux-ng: for dmesg support')
install=$pkgname.install
source=(http://cateee.net/sources/$pkgname/$pkgname-${pkgver//./-}.tar.gz
        http://cateee.net/sources/lkddb/lkddb-${pkgver//./-}.tar.gz)
md5sums=('e99cb86099508b968405f493cba56382'
         '4364bf0a0c65d6b9dddfbc171a395a79')

build() {
  cd "$srcdir/$pkgname-${pkgver//./-}"
  sed -i 's,s\(bin/lsmod\),\1,' kdetect.sh
  sed -i "s,lkddb\.list,/usr/share/$pkgname/&," $pkgname.sh
  install -Dm755 kdetect.sh "$pkgdir/usr/bin/kdetect"
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm755 ../lkddb-${pkgver//./-}/lkddb.list \
    "$pkgdir/usr/share/$pkgname/lkddb.list"
}
