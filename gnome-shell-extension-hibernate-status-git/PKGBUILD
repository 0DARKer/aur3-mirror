# Maintainer: Michal Rus <m@michalrus.com>
pkgname="gnome-shell-extension-hibernate-status-git"
pkgver=r8.ce53feb
pkgrel=1
pkgdesc="GNOME Shell extension that adds a hibernate/hybrid suspend button in Status menu"
arch=('any')
url="https://github.com/arelange/gnome-shell-extension-hibernate-status"
license=('unknown')
depends=(gnome-shell-extensions systemd)
provides=('gnome-shell-extension-hibernate-status')
conflicts=('gnome-shell-extension-hibernate-status')
source=("$pkgname::git+https://github.com/Anthony25/gnome-shell-extension-hibernate-status#commit=ce53feb338160ae1a0642075c2f82c4af93c6c73")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  local extdir="$pkgdir/usr/share/gnome-shell/extensions/hibernate-status@dromi"
  mkdir -p "$extdir"
  find "$srcdir"/"$pkgname" -maxdepth 1 -mindepth 1 -not -name '.git' -exec mv -v '{}' "$extdir" ';'
  chmod -R a+rX "$extdir"
}
