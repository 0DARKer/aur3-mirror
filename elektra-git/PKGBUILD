
pkgname=elektra-git
pkgver=0.8.1.1321.g4f2b0c3
pkgrel=1
pkgdesc="A universal hierarchical configuration store. (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/ElektraInitiative/libelektra"
license=('BSD')
depends=('gcc-libs' 'yajl' 'libdbus' 'augeas' 'libsystemd')
makedepends=('git' 'cmake' 'boost' 'docbook-xsl' 'texlive-latexextra' 'texlive-fontsextra' 'tcl')
provides=('elektra')
conflicts=('elektra')
source=("elektra::git+https://github.com/ElektraInitiative/libelektra.git")
md5sums=('SKIP')
_gitname="elektra"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare(){
  cd "${_gitname}/doc"
  doxygen -u
}

build() {
  cd "${_gitname}"
  rm -fr build
  mkdir -p build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DENABLE_TESTING=OFF -DPLUGINS=ALL -Wno-dev  #-DBUILD_DOCUMENTATION=OFF
  make
}

# check() {
#   cd build
#   make test
# }

package() {
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../doc/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for i in $(find ${pkgdir}/usr/share/doc -type f); do sed "s|${srcdir}|/usr/share/doc|g" -i ${i} ;done
  rm -fr "${pkgdir}/usr/lib/elektra/tool_exec" "${srcdir}/usr/share/elektra"
}
