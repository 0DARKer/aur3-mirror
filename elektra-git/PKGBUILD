
pkgname=elektra-git
pkgver=0.8.8.443.g0e1d426
pkgrel=1
pkgdesc="A universal hierarchical configuration store. (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/ElektraInitiative/libelektra"
license=('BSD')
depends=('yajl' 'libdbus' 'augeas' 'libsystemd')
makedepends=('git' 'cmake' 'boost' 'docbook-xsl' 'texlive-latexextra' 'texlive-fontsextra' 'tcl')
provides=('elektra')
conflicts=('elektra')
source=("elektra::git+https://github.com/ElektraInitiative/libelektra.git")
md5sums=('SKIP')
_gitname="elektra"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare(){
  rm -fr build
  mkdir -p build
  cd "${_gitname}/doc"
  doxygen -u
}

build() {
  cd build
  cmake "../${_gitname}" -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 "${_gitname}/doc/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for i in $(find ${pkgdir}/usr/share/doc -type f); do sed "s|${srcdir}|/usr/share/doc|g" -i ${i} ;done
}
