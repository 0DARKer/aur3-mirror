
pkgname=elektra-git
pkgver=0.8.1.476.gd432310
pkgrel=1
pkgdesc="A universal hierarchical configuration store. (GIT version)"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Elektra/"
license=('BSD')
depends=('libxml2' 'gcc-libs')
makedepends=('git' 'cmake' 'docbook-xsl' 'texlive-latexextra')
provides=('elektra')
conflicts=('elektra')
source=("elektra::git://gitorious.org/elektra-initiative/libelektra.git")
md5sums=('SKIP')
_gitname="elektra"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_TESTING=OFF #-DBUILD_DOCUMENTATION=OFF
  make
}

check() {
  cd "${_gitname}/build"
  make test
}

package() {
  cd "${_gitname}/build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../doc/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  for i in $(find ${pkgdir}/usr/share/doc -type f); do sed "s|${srcdir}|/usr/share/doc|g" -i ${i} ;done
}
