# Maintainer: Padfoot <padfoot at exemail dot com dot au>

pkgname=lightdm-gtk2-greeter
_srcname=lightdm-gtk-greeter
pkgver=1.5.1
pkgrel=2
pkgdesc="GTK2+ greeter for LightDM"
arch=('i686'
      'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3'
         'LGPL3')
depends=('lightdm'
         'gtk2')
makedepends=('gtk-doc'
             'gnome-common'
             'gnome-doc-utils'
             'gobject-introspection'
             'intltool'
             'exo')
conflicts=('lightdm-gtk-greeter-osk'
           'lightdm-gtk-greeter-bzr'
           'lightdm-another-gtk-greeter'
           'lightdm-gtk-greeter')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
install='lightdm-gtk-greeter.install'
source=("https://launchpad.net/$_srcname/1.6/$pkgver/+download/$_srcname-$pkgver.tar.gz"
        "lightdm-gtk-greeter.conf"
        "lightdm-gtk-greeter.patch")
md5sums=('d40b61de2a1585fe6d17af70bc1a752f'
         'c5e90d9ab34136905c84d0d92204efca'
         '6e6b87d983a74f0606f3f5d0c39700a5')

build() {
  cd "$srcdir/$_srcname-$pkgver"
  export AUTOMAKE=automake

  patch -p1 -i $srcdir/lightdm-gtk-greeter.patch
  
  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm \
    --disable-static \
    --with-gtk2
    
  make
}

package() {
  cd "$srcdir/$_srcname-$pkgver"
  
  make DESTDIR="${pkgdir}" install

  cp ../lightdm-gtk-greeter.conf $pkgdir/etc/lightdm
}
