# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
pkgname=red
pkgver=0.3.2
pkgrel=1
pkgdesc="Red Programming Language - An open source, native code compiled, dialect of Rebol"
arch=('i686')
url="http://www.red-lang.org"
license=('custom:3-clause BSD' 'custom:BSL')
groups=('devel')
depends=('rebol=2.7.8')
makedepends=('wget' 'rebol=2.7.8')
source=("https://github.com/dockimbel/Red/archive/v${pkgver}.tar.gz")
md5sums=('1d9569f0e791276d2020fa540b19de1f')

build() {
  #
  # BUILD HERE
  #
  cd "$srcdir/Red-${pkgver}"
  rebol -qw red.r red/tests/hello.red
  msg2 "Generating docs..."
  cd docs
  rebol -qw makedoc2.r red-system-specs.txt
  rebol -qw makedoc2.r red-system-quick-test.txt
}

check() {
  cd "$srcdir/Red-$pkgver"
  rebol -qws run-all.r --batch
}

package() {
  cd "$srcdir"
  install -d "$pkgdir"/opt/red
  cp -R Red-$pkgver/* "$pkgdir"/opt/red/
  cd Red-$pkgver
  install -Dm644 BSD-3-License.txt "$pkgdir"/usr/share/licenses/$pkgname/BSD-3-License.txt
  install -Dm644 BSL-License.txt "$pkgdir"/usr/share/licenses/$pkgname/BSL-License.txt
  install -Dm644 docs/red-system-quick-test.html "$pkgdir"/usr/share/doc/$pkgname/red-system-quick-test.html
  install -Dm644 docs/red-system-specs.html "$pkgdir"/usr/share/doc/$pkgname/red-system-specs.html
  rm "$pkgdir"/opt/$pkgname/docs/red-system-{quick-test,specs}.html
}

# vim:set ts=2 sw=2 et:
