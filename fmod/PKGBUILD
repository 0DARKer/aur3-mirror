# Maintainer: Andrey Mivrenik <gim at fastmail dot fm>
pkgname=fmod
pkgver=1.05.11
pkgrel=1
pkgdesc="Interface for programmers to load FMOD Studio projects and play them back in realtime"
arch=('i686' 'x86_64')
url="http://www.fmod.org/download/"
license=('custom')
depends=()
changelog=$pkgname.changelog
source=("http://www.fmod.org/download/fmodstudio/api/Linux/fmodstudioapi${pkgver//[.]/}linux.tar.gz")
sha256sums=('673a5c8efc67a30c1d54ef6415db57efe02c52bed5e9b5a8a88564ce31ff72bf')

package() {
  _carch=''
  case "$CARCH" in
    x86_64) _carch='x86_64';;
    i686) _carch='x86';;
    arm*) _carch='arm';;
    armhf) _carch='armhf';;
  esac

  cd "fmodstudioapi${pkgver//[.]/}linux"
  install -d "$pkgdir/usr/"{lib,include,share/doc}

  # lowlevel
  install -m0644 api/lowlevel/lib/$_carch/* "$pkgdir/usr/lib/"
  install -m0644 api/lowlevel/inc/* "$pkgdir/usr/include/"

  # studio
  install -m0644 api/studio/lib/$_carch/* "$pkgdir/usr/lib/"
  install -m0644 api/studio/inc/* "$pkgdir/usr/include/"

  install -m0644 doc/* "$pkgdir/usr/share/doc/"
  install -Dm0644 doc/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
