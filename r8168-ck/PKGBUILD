# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168-ck
_realname=r8168
pkgver=8.030.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards (for linux-ck kernel)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux-ck>=3.3' 'linux-ck<3.4')
makedepends=('linux-ck-headers')
source=(http://r8168.googlecode.com/files/$_realname-$pkgver.tar.bz2)
install=$_realname.install

_extramodules=extramodules-3.3-ck
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
   cd "$srcdir/$_realname-$pkgver"
   #patch -p0 -i "$srcdir/linux33.patch"
   make clean BASEDIR=/lib/modules/$_kernver modules
}

package() {
   cd "$srcdir/$_realname-$pkgver/src"
   install -D -m644 $_realname.ko "$pkgdir/lib/modules/$_extramodules/$_realname.ko"
   find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

md5sums=('3205d895dbdc7564ba60b71acb308417')
sha256sums=('1b2267703c83f1709cad115cc39b11e8c4b64e7944452c684ef2bd86c626d6c4')
