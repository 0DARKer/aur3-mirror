# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168-ck
_realname=r8168
pkgver=8.038.00
pkgrel=4
pkgdesc="A kernel module for Realtek 8168 network cards (for linux-ck kernel)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux-ck>=3.16' 'linux-ck<3.17')
makedepends=('linux-ck-headers')
#source=(http://r8168.googlecode.com/files/$_realname-$pkgver.tar.bz2)
source=("http://r8168dl.appspot.com/files/r8168-8.038.00.tar.bz2"
        "delayed_work_ethtool_ops_fix2.patch")
install=$_realname.install

_extramodules=extramodules-3.16-ck
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
   cd "$srcdir/$_realname-$pkgver"
   patch -Np1 -i "$srcdir/delayed_work_ethtool_ops_fix2.patch"
   make -C /usr/lib/modules/$_kernver/build \
      SUBDIRS="$srcdir/$_realname-$pkgver/src" \
      EXTRA_CFAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
   cd "$srcdir/$_realname-$pkgver/src"
   install -D -m644 $_realname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_realname.ko"
   find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

sha256sums=('1384bdf4d33b219f937bf1a600c72b22962baf7796e562b08e4da3c000f49072'
            '2a03fa65e476cb7dd3ed5976037e0218260ff2660b6a4c495a824865eb10c303')
