# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168-ck
_realname=r8168
pkgver=8.036.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards (for linux-ck kernel)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux-ck>=3.9' 'linux-ck<3.10')
makedepends=('linux-ck-headers')
source=(http://r8168.googlecode.com/files/$_realname-$pkgver.tar.bz2)
install=$_realname.install

_extramodules=extramodules-3.9-ck
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
   cd "$srcdir"
   #patch -p0 -i "linux-3.8.patch"
   cd "$_realname-$pkgver"
   make -C /usr/lib/modules/$_kernver/build \
      SUBDIRS="$srcdir/$_realname-$pkgver/src" \
      EXTRA_CFAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
   cd "$srcdir/$_realname-$pkgver/src"
   install -D -m644 $_realname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_realname.ko"
   find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

sha256sums=('937fd5d7d4cbfc95cf2859d8c3beb15a8536dc4e41752f7a53f2a25f07cdcd33')
