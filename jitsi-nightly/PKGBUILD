# Contributor: Ananda Samaddar ananda@samaddar.co.uk
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: atommix aka Aleks Lifey <Aleks.Lifey@gmail.com>
# Contributor: Keshav P R <(the) (ddoott) (ridikulus) (ddoott) (rat) (aatt) (gemmmaeil) (ddoott) (ccoomm)>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

_pkgname="jitsi"
pkgname="${_pkgname}-nightly"

_relver="1.1"
_pseudo_buildver="4022"

## Taken from http://gitorious.org/arch-linux-greece/arch-linux-greece/blobs/master/list-of-sources/adslgr64/songbird-nightly/PKGBUILD
_build_number() {
	
	curl "http://download.jitsi.org/jitsi/nightly/src/" \
		| grep -m 1 "href=\"${_pkgname}-src-${_relver}-nightly.build" \
		| sed "s#.*${_pkgname}-src-${_relver}-nightly.build.##g" \
		| sed 's#.zip.*##g'
	
}

_buildver="$(_build_number)"

pkgver="${_relver}.${_pseudo_buildver}" ## For AUR interface
true && pkgver="${_relver}.${_buildver}" ## Actual pkgver
pkgrel="1"

pkgdesc="An audio/video SIP VoIP phone and instant messenger written in Java (formerly SIP-Communicator) - Nightly version"
arch=('i686' 'x86_64')
url="http://jitsi.org"
license=('LGPL')
depends=('java-runtime')
makedepends=("curl" 'apache-ant' 'java-environment')
options=(!strip !emptydirs zipman !libtool docs)

_actual_source_file="${_pkgname}-src-${_relver}-nightly.build.${_buildver}.zip"
# _actual_source_file_download="${_pkgname}-src-${_relver}-nightly.latest.zip"

# source=("${_actual_source_file}::http://download.jitsi.org/jitsi/nightly/src/${_actual_source_file}"
source=("${_pkgname}.desktop"
        "${_pkgname}.sh")

sha256sums=('61e3bec3470790fa067f87d978016ec4452a6fd3dfba2c9afa5245b58d3cb19d'
            '3b2145865bc2bfc19001937cc422491758136fe674a3ca8a6d1bd7275cbd403c')

_update_source() {
	
	cd "${srcdir}/"
	
	msg "Downloading..."
	curl -f -L -C - --ftp-pasv --retry 3 --retry-delay 3 -o "${srcdir}/${_actual_source_file}" "http://download.jitsi.org/jitsi/nightly/src/${_actual_source_file}"
	
	cd "${srcdir}/"
	
	msg "Extracting..."
	bsdtar -C "${srcdir}/" -xf "${srcdir}/${_actual_source_file}"
	
}

build() {
	
	_update_source
	
	cd "${srcdir}/${_pkgname}"
	
	## append the build revision to the jitsi version
	sed "s/0\.build\.by\.SVN/build.${pkgver##*.}/" -i "${srcdir}/${_pkgname}/src/net/java/sip/communicator/impl/version/NightlyBuildID.java"
	. /etc/profile.d/apache-ant.sh
	
	## Build
	/usr/share/apache-ant/bin/ant rebuild
	
}

package() {
	
	cd "${srcdir}/${_pkgname}"
	
	find lib/ lib/bundle/ -maxdepth 1 -type f \
		-exec install -D -m0644 {} "${pkgdir}/usr/lib/${pkgname}/"{} \;
	
	find lib/os-specific/linux/ -maxdepth 1 -type f \
		-execdir install -D -m0644 {} "${pkgdir}/usr/lib/${pkgname}/lib/"{} \;
	
	shopt -sq extglob
	
	find lib/native/linux$(sed 's/_/-/g' <<<${CARCH/#*(i?86|x86)/})/ -maxdepth 1 -type f \
		-execdir install -D -m0644 {} "${pkgdir}/usr/lib/${pkgname}/lib/native/"{} \;
	
	find sc-bundles/{,os-specific/linux/} -maxdepth 1 -type f \
		-execdir install -D -m0644 {} "${pkgdir}/usr/lib/${pkgname}/sc-bundles/"{} \;
	
	install -D -m0755 "$srcdir/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
	install -D -m0644 "$srcdir/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	
	local _file
	for _file in resources/install/debian/*.{svg,xpm}; do
		install -Dm644 "$_file" "${pkgdir}/usr/share/pixmaps/${pkgname}${_file/*sip-communicator/}"
	done
	
}
