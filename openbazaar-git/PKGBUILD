#Maintainer: M0Rf30

pkgname=openbazaar-git
pkgver=3546.b065181
pkgrel=1
pkgdesc="A decentralized marketplace proof of concept. It is based off of the POC code by the darkmarket team and protected by the GPL"
arch=(i686 x86_64)
url="http://openbazaar.org"
license=('MIT')
depends=(python2-bitcoin python2-gnupg python2-ipy python2-miniupnpc python2-obelisk python2-pillow python2-psutil python2-pycountry 
	 python2-pyelliptic python2-pysqlcipher python2-pystun python2-pyzmq python2-qrcode python2-requests python2-rfc3986 
	 python2-tornado curl)
optdepends=('pybitmessage: Send and receive messages to anyone on the bitmessage network')
makedepends=(git)
source=('openbazaar::git+https://github.com/OpenBazaar/OpenBazaar.git#branch=develop'
	openbazaar.service
	openbazaar.sh
	openbazaar-stop.sh)
install=openbazaar.install
options=('!strip')
conflicts=(openbazaar)

package(){
  cd $srcdir/openbazaar

  #Install systemd service 
  install -Dm644 $srcdir/openbazaar.service $pkgdir/usr/lib/systemd/system/openbazaar.service

  #Install openbazaar scripts
  install -Dm755 $srcdir/openbazaar.sh $pkgdir/usr/bin/openbazaar
  install -Dm755 $srcdir/openbazaar-stop.sh $pkgdir/usr/bin/openbazaar-stop

   #Create folder for user openbazaar
  cd installers/ubuntu
  cat build | head -n -2 > create_folders
  python2 create_folders
  cp -r output/usr/share $pkgdir/usr/
  install -dm755 $pkgdir/var/lib/
  mv $pkgdir/usr/share/openbazaar $pkgdir/var/lib/
  
  #Python2 bytecode generation
  cd $pkgdir/var/lib/openbazaar/ && python2 -m compileall .
}

pkgver() {
  cd openbazaar
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

md5sums=('SKIP'
         '3aa1c8dc7384d62ffdb404e973afc1f3'
         'bc0e6e1b624cb3af7d9c8a57f204f32a'
         'f79ebdbdf2cfc264c98b035391f4537c')
