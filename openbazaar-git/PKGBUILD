#Maintainer: M0Rf30

pkgname=openbazaar-git
pkgver=2407.1633098
pkgrel=1
pkgdesc="A decentralized marketplace proof of concept. It is based off of the POC code by the darkmarket team and protected by the GPL"
arch=(i686 x86_64)
url="http://openbazaar.org"
license=('GPL3')
depends=(python2-ecdsa python2-obelisk python2-pillow python2-certifi python2-coverage python2-flake8 python2-mock python2-nose 
	 python2-zope-interface python2-backports.ssl_match_hostname python2-websocket-client python2-pyelliptic python2-pyzmq 
	 python2-qrcode python2-requests python2-gnupg python2-pycountry python2-ipy python2-miniupnpc python2-tornado python2-six 
	 python2-pysqlcipher python2-argparse python2-pybitcointools python2-wsgiref wget)
optdepends=('pybitmessage: Send and receive messages to anyone on the bitmessage network')
makedepends=(git)
source=('openbazaar::git+https://github.com/OpenBazaar/OpenBazaar.git'
	openbazaar.service
	openbazaar.sh
	openbazaar-stop.sh)
install=openbazaar.install
options=('!strip')
conflicts=(openbazaar)

package(){
  cd $srcdir
  rm -r openbazaar/pybitmessage

  #Install systemd service 
  install -Dm644 $srcdir/openbazaar.service $pkgdir/usr/lib/systemd/system/openbazaar.service

  #Install openbazaar scripts
  install -Dm755 $srcdir/openbazaar.sh $pkgdir/usr/bin/openbazaar
  install -Dm755 $srcdir/openbazaar-stop.sh $pkgdir/usr/bin/openbazaar-stop

  #Create folder for user openbazaar
  install -dm755 $pkgdir/var/lib/openbazaar
  install -dm755 $pkgdir/var/lib/openbazaar/{logs,db}
  cp -r openbazaar/* $pkgdir/var/lib/openbazaar/
  
  #Python2 bytecode generation
  cd $pkgdir/var/lib/openbazaar/ && python2 -m compileall .
  
  chown 421:421 -R $pkgdir/var/lib/{openbazaar,openbazaar/*}
}

pkgver() {
  cd openbazaar
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

md5sums=('SKIP'
         '3aa1c8dc7384d62ffdb404e973afc1f3'
         'c16a3985b190a2f6ab3bb328a29392be'
         'fde396d7a13a78cf26d2f96d4780875a')
