#Maintainer: M0Rf30

_pkgname=openbazaar
pkgname=${_pkgname}-git
pkgver=3620.b1d63cd
pkgrel=1
pkgdesc="A decentralized marketplace proof of concept. It is based off of the POC code by the darkmarket team and protected by the GPL"
arch=(any)
url="http://openbazaar.org"
license=('MIT')
depends=(gnupg1 python2-bitcoin python2-dnschain python2-gnupg-hg python2-ipy python2-miniupnpc python2-obelisk python2-pillow python2-psutil 
         python2-pycountry python2-pyelliptic python2-pysqlcipher python2-pystun python2-pyzmq python2-qrcode python2-requests 
	 python2-rfc3986 python2-tornado curl jquery)
optdepends=('pybitmessage: Send and receive messages to anyone on the bitmessage network')
makedepends=(git)
source=("${_pkgname}::git+https://github.com/OpenBazaar/OpenBazaar.git#branch=develop"
	 ${_pkgname}.service
	 ${_pkgname}.sh
	 ${_pkgname}-stop.sh
)
install=${_pkgname}.install
options=('!strip')
conflicts=(${_pkgname})

package(){
  cd $srcdir/${_pkgname}

  sed -i "s/import node.network_util as network_util/from node import network_util, setup_db, util/g" node/openbazaar.py
  #Install systemd service 
  install -Dm644 $srcdir/${_pkgname}.service $pkgdir/usr/lib/systemd/system/${_pkgname}.service

  #Install ${_pkgname} scripts
  install -Dm755 $srcdir/${_pkgname}.sh $pkgdir/usr/bin/${_pkgname}
  install -Dm755 $srcdir/${_pkgname}-stop.sh $pkgdir/usr/bin/${_pkgname}-stop

   #Create folder for user ${_pkgname}
  cd installers/ubuntu
  cat build | head -n -2 > create_folders
  python2 create_folders
  cp -r output/usr/share $pkgdir/usr/
  install -dm755 $pkgdir/var/lib/
  mv $pkgdir/usr/share/${_pkgname} $pkgdir/var/lib/
  
  #jquery symlink
  ln -s -r /usr/share/jquery/jquery.min.js $pkgdir/var/lib/${_pkgname}/html/vendors/

  #Python2 bytecode generation
  cd $pkgdir/var/lib/${_pkgname}/ && python2 -m compileall .
}

pkgver() {
  cd ${_pkgname}
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}


md5sums=('SKIP'
         '3aa1c8dc7384d62ffdb404e973afc1f3'
         '85569b5ec72425e081d4bb35806daa00'
         'f79ebdbdf2cfc264c98b035391f4537c')
