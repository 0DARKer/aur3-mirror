# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gamgi
pkgver=0.15.1
pkgrel=1
pkgdesc="General Atomistic Modelling Graphic Interface is a free package to construct, view and analyse atomic structures."
arch=('i686' 'x86_64')
url="http://www.gamgi.org/"
license=('GPL')
depends=('gtkglext' 'hicolor-icon-theme')
install="$pkgname.install"
source=("${url}src/${pkgname}-all-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('fc96d6737c4d3428457b2b8e9a8c98f9'
         'b558e09451368776b70e168c8f8e6c13')

build() {
  cd "${srcdir}/${pkgname}${pkgver}/src"

  # Adding GDK-PIXBUF cflags
  sed -i "s|lib64|lib|g;31iPATH_GDK_H\ =\ \`pkg-config --cflags gdk-pixbuf-2.0\`" make_local
  sed -i "s|EXPAT_H)|& \\\$(PATH_GDK_H)|" make_rules

  make
}

package() {
  cd "${srcdir}/${pkgname}${pkgver}"

  install -d "${pkgdir}"/usr/{bin,share/{{doc/,}${pkgname},man/man1}}

  # Main App
  install -Dm755 src/${pkgname} "${pkgdir}/usr/bin"

  # Icons and desktop file
  for i in 16 22 32 48; do
    install -Dm644 doc/icon/${pkgname}${i}.png \
      "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png"
  done

  install -Dm644 ../${pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 doc/icon/${pkgname}316.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # Man page and docs
  gzip -c -9 doc/man/page > "${pkgdir}/usr/share/man/man1/$pkgname.1.gz"
  cp -a doc/*[^man] "${pkgdir}/usr/share/doc/${pkgname}"

  # Data files and examples
  cp -a dat/* "${pkgdir}/usr/share/${pkgname}"
}
