# Maintainer: Martin Stolpe <martinstolpe [at] gmail [dot] com>
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=go-openoffice-qt-native
_pkgname=go-openoffice
_GOver=3.2.1
pkgver=20100602
pkgrel=1
pkgdesc="OpenOffice.org - go-oo.org enhanced version of SUN's office suite - 3.2.1 branch - uses Qt native engine"
arch=('i686' 'x86_64')
_ootag=ooo320-m19
license=('LGPL3')
url="http://go-oo.org/"
install=${_pkgname}.install
depends=("curl>=7.19.6" "hunspell>=1.2.8" "python>=2.6.4" 'libwpd'
         'libxaw' "neon>=0.28.6" "icu>=4.2.1" 'hsqldb-java' 'libxslt' 'libxtst' 'lpsolve'
	  'beanshell' 'saxon' 'vigra' 'hyphen' 'libmspack' 'libldap' 'gtk2' 'lucene'
	 'hicolor-icon-theme' 'shared-mime-info' 'desktop-file-utils') #   'libmythes' 'libgraphite' 'redland>=1.0.10'
optdepends=('java-runtime:	adds java support'
            'libcups:		adds printing support'
            'gconf:		adds additional gnome support'
            'nss:		adds support for signed files/macros'
            'pstoedit:		translates PostScript and PDF graphics into other vector formats'
	    'poppler:		for the pdfimport extension'
	    'mesa:		for the OGLTrans extension'
	    'mono:		allows UNO automation with Mono'
	    'gstreamer0.10-base:	+ some gstr-plugins to support multimedia content, e.g. in impress'
            'kdelibs:		for kde integration')
makedepends=('automake' 'autoconf' 'wget' 'bison' 'findutils' 'flex' 'gawk' 'gcc-libs' 'libart-lgpl'
	'pam' 'sane' 'perl-archive-zip' 'pkgconfig' 'unzip' 'zip' "xulrunner>=1.9.2-4" 'apache-ant' 'cairo' 
	'gperf' 'libcups' 'pstoedit' 'gconf' "java-environment" 'unixodbc' 'mesa>=7.5' 'poppler>=0.10.7'
	'gstreamer0.10-base>=0.10.26'  'mono>=2.6.1' 'kdelibs>=4.4.0' 'libjpeg' 'boost' 'git' 'rsync')
backup=(usr/lib/go-openoffice/program/sofficerc)
provides=('openoffice-base')
conflicts=('openoffice-base')
source=(ArchLinux.patch
	buildfix_64bit_system_libjpeg.diff
	qt-use-native-backend.diff)
#options=('!distcc' '!ccache' '!makeflags')
options=('!makeflags')

_gitroot="git://anongit.freedesktop.org/git/ooo-build/ooo-build"
_gitname="go-openoffice"
_branch="ooo-build-3-2-1"

build() {
	cd "$srcdir"

	msg "Connecting to GIT server..."
	if [ -d $_gitname ] ; then
		cd $_gitname
		if [[ ! `git branch | grep ${_branch}` ]]; then
			git checkout -b ${_branch} origin/${_branch} --track
		fi
		git pull
		msg "The local files are updated."
	else
		git clone $_gitroot $_gitname
		cd $_gitname
		git checkout -b ${_branch} origin/${_branch} --track
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	if [[ `pacman -Qs openjdk6` ]]; then
		unset J2REDIR; unset J2SDKDIR; unset JAVA_HOME; unset CLASSPATH
		[ -z "${JAVA_HOME}" ] && . /etc/profile.d/openjdk6.sh
	fi

	[ -z "${MOZ_PLUGIN_PATH}" ] && . /etc/profile.d/mozilla-common.sh
	[ -z "${ANT_HOME}" ] && . /etc/profile.d/apache-ant.sh

	if [[ -d ${srcdir}/ooo-build-${_GOver} ]]; then
		msg "Cleaning up old build files"
		(cd ${srcdir}/ooo-build-${_GOver} && make clean)
	fi

	rsync -rlpgo "${srcdir}/$_gitname/" "${srcdir}/ooo-build-${_GOver}" || return 1

	cd ${srcdir}/ooo-build-${_GOver}

	# our ArchLinux distribution patch until we go upstream
	patch -Np0 -i ${srcdir}/ArchLinux.patch || return 1

	# hotfixes not yet upstream
# 	cp ${srcdir}/*.diff ${srcdir}/ooo-build-${_GOver}/patches/hotfixes/
	cp ${srcdir}/buildfix_64bit_system_libjpeg.diff ${srcdir}/ooo-build-${_GOver}/patches/hotfixes/
	cp ${srcdir}/qt-use-native-backend.diff ${srcdir}/ooo-build-${_GOver}/patches/hotfixes/

	# export C(XX)FLAGS
	# http://www.openoffice.org/issues/show_bug.cgi?id=103205
	unset CFLAGS
	unset CXXFLAGS
#	export ARCH_FLAGS="$CFLAGS"

	if [ "$CARCH" = "x86_64" ]; then
	      EXTRAOPTS="--without-stlport"
	 else EXTRAOPTS="--with-stlport"
	fi

#	autoreconf
	./autogen.sh --with-distro=ArchLinux \
		--with-build-version="${_GOver} ArchLinux build-${pkgrel} (${_ootag})"\
		--with-srcdir=${srcdir} \
		--with-max-jobs=${MAKEFLAGS/-j/} \
		--with-installed-ooo-dirname="${_pkgname}" \
		--prefix=/usr --exec-prefix=/usr --sysconfdir=/etc \
		--with-docdir=/usr/share/doc/packages/"${_pkgname}" \
		--mandir=/usr/share/man \
		--with-lang="" \
		--with-dict=ALL\
		--with-binsuffix=no \
		--disable-ldap \
		--enable-cairo\
		--disable-kde\
		--enable-kde4\
		--enable-lockdown\
		--with-system-boost\
		--with-system-cairo\
		--enable-crashdump\
		--without-gpc\
		--enable-opengl \
		--enable-minimizer \
		--enable-presenter-console \
		--enable-pdfimport \
		--enable-wiki-publisher \
		--enable-ogltrans \
		--with-ant-home="/usr/share/java/apache-ant"\
		--with-system-saxon\
		--with-saxon-jar=/usr/share/java/saxon/saxon9he.jar\
		--with-system-lucene\
		--with-lucene-core-jar=/usr/share/java/lucene-core.jar\
		--with-lucene-analyzers-jar=/usr/share/java/lucene-analyzers.jar\
		--with-system-beanshell\
		--with-system-vigra\
		--with-system-altlinuxhyph\
		--with-system-lpsolve\
		--without-system-redland \
		$EXTRAOPTS || return 1

#		--with-system-mythes\
#		--with-system-graphite\
#		--with-tag=${_ootag}
#		--enable-report-builder \
#		--with-additional-sections="OOXMLExport"

	./download

	unset MAKEFLAGS
	LD_PRELOAD="" make  || return 1
}

package() {
	cd ${srcdir}/ooo-build-${_GOver}
	LD_PRELOAD="" make DESTDIR=${pkgdir} install || return 1

       # install all built dictionaries from source tree
       pushd ${srcdir}/ooo-build-${_GOver}/build/${_ootag}/dictionaries/unxlng?6.pro/bin/
       for i in `ls -1 dict-??.oxt`; do
         install -D -m644 $i ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/$i || return 1
       done
       popd

	# install all other built extensions
	pushd ${srcdir}/ooo-build-${_GOver}/build/${_ootag}/solver/320/unxlng?6.pro/bin/
	install -m644 pdfimport/pdfimport.oxt ${pkgdir}/usr/lib/"${_pkgname}"/share/extension/install/pdfimport.oxt || return 1
	install -m644 swext/wiki-publisher.oxt ${pkgdir}/usr/lib/"${_pkgname}"/share/extension/install/wiki-publisher.oxt || return 1
	install -m644 minimizer/presentation-minimizer.oxt ${pkgdir}/usr/lib/"${_pkgname}"/share/extension/install/presentation-minimizer.oxt || return 1
	install -m644 presenter/presenter-screen.oxt ${pkgdir}/usr/lib/"${_pkgname}"/share/extension/install/presenter-screen.oxt || return 1
	popd
	
	# fix unopkg call for mktemp, #15410
	sed -i "s:\/bin\/mktemp:\/usr\/bin\/mktemp:" ${pkgdir}/usr/lib/go-openoffice/program/unopkg || return 1
	
	#fix http://bugs.archlinux.org/task/17656
	find ${pkgdir} -perm 444 -exec ls -lh {} \; 
	find ${pkgdir} -perm 444 -exec chmod 644 {} \;
	find ${pkgdir} -perm 555 -exec ls -lh {} \;
	find ${pkgdir} -perm 555 -exec chmod 755 {} \;
}
md5sums=('a1ff3d64d0ca95c62a15c07e7f19f193'
         'b005c4cf9f8e586539ca98c9cfe9bb77'
         '43efb9b6de0bba5186589ea37c50dc23')
