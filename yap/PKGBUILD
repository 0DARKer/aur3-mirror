# Submitter: tcx
# Maintainer: Leonidas <marek@xivilization.net>
# Repository: https://github.com/Leonidas-from-XIV/aur-yap
pkgname=yap
pkgver=6.2.2
pkgrel=3
pkgdesc="A high-performance Prolog compiler"
url="http://www.dcc.fc.up.pt/~vsc/Yap/"
license=("PerlArtistic" "LGPL")
arch=("i686" "x86_64")
makedepends=('texi2html' 'texinfo')
depends=('unixodbc' 'libmariadbclient')
# take care, lam conflicts with mpe2 through openmpi
optdepends=('lam: MPI LAM-based interface'
            'mpe2: MPE interface'
            'mpich: MPICH based MPI interface')
source=(http://www.dcc.fc.up.pt/~vsc/Yap/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f15f8382104443319a5883eafce5f52f4143b526c7f1cd88d19c1f63fc06d750')

build() {
  _builddir="${srcdir}"/${pkgname}-${pkgver}/${CARCH}
  mkdir -p "${_builddir}"
  cd "${_builddir}"

  ../configure \
    --prefix=/usr \
    --enable-clpqr \
    --enable-coroutining \
    --enable-depth-limit \
    --enable-threads \
    --enable-dynamic-loading \
    --enable-max-performance
  make
  make html
  make pdf
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/${CARCH}

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install_docs
}
