# Submitter: tcx
# Maintainer: Leonidas <marek@xivilization.net>
# Repository: https://github.com/Leonidas-from-XIV/aur-yap
pkgname=yap
pkgver=6.2.2
pkgrel=2
pkgdesc="A high-performance Prolog compiler"
url="http://www.dcc.fc.up.pt/~vsc/Yap/"
license=("PerlArtistic" "LGPL")
arch=("i686" "x86_64")
depends=('unixodbc' 'libmariadbclient')
# take care, lam conflicts with mpe2 through openmpi
optdepends=('lam: MPI LAM-based interface'
            'mpe2: MPE interface'
            'mpich: MPICH based MPI interface')
source=(http://www.dcc.fc.up.pt/~vsc/Yap/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f15f8382104443319a5883eafce5f52f4143b526c7f1cd88d19c1f63fc06d750')

build() {
  _builddir="${srcdir}"/${pkgname}-${pkgver}/${arch}
  mkdir -p "${_builddir}"
  cd "${_builddir}"

  ../configure \
    --prefix=/usr \
    --enable-clpqr \
    --enable-coroutining \
    --enable-depth-limit \
    --enable-threads \
    --enable-max-performance
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/${arch}

  make DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}"/usr/lib/libYap.a
}
