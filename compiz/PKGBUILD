# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Rob McCathie <archaur at rmcc dot com dot au
# Contributor: /dev/rs0 <rs0@secretco.de.com>
# Contributor: Iven Hsu <ivenvd AT gmail>
# Contributor: Nathan Hulse <nat.hulse@gmail.com>

pkgname=compiz
pkgver=0.9.12.0
_pkgseries=0.9.12
pkgrel=7
pkgdesc="Composite manager for Aiglx and Xgl, with plugins and CCSM (release version)"
arch=('i686' 'x86_64')
url="https://launchpad.net/compiz"
license=('GPL' 'LGPL' 'MIT')
depends=('boost' 'xorg-server' 'libxcomposite' 'startup-notification' 'librsvg' 'dbus' 'mesa' 'libxslt' 'fuse' 'glibmm' 'libxrender' 'libwnck3' 'pygtk' 'desktop-file-utils' 'pyrex' 'protobuf' 'metacity>=3.14.0' 'glu' 'libsm' 'dconf')
makedepends=('cmake' 'intltool')
optdepends=(
  'gnome-control-center: GNOME keybindings support (need to rebuild this package)'
  'kdebase-workspace: KDE window decoration support (need to rebuild this package)'
  'kdebase-lib: KDE window decoration support (need to rebuild this package)'
  'automoc4: KDE window decoration support (need to rebuild this package)'
  'xorg-xprop: grab various window properties for use in window matching rules'
  'gnome-themes-standard: provides the Adwaita theme for gtk-window-decorator'
  'gconf: GConf Configuration Backend support (need to rebuild this package)'
)
conflicts=('compiz-core')
replaces=('compiz-core-devel')
source=("https://launchpad.net/compiz/${_pkgseries}/${pkgver}/+download/compiz-${pkgver}.tar.bz2"
        "gwd-metacity-3_14-fix.patch"
        "set-gwd-default.patch"
        "focus-prevention-disable.patch"
        "gwd-system-font-fix.patch")
sha256sums=('b9d398c39ea49d63059eadf2652160d2c965832bb2be1a935f6cfab94620fed8'
            '5274c44b7bfeb6fe54f99ceeca20b2a59edc383a46d101a020f1bdb51d0e849c'
            '3aa6cb70f357b3d34d51735f4b5bcb0479086d7c7336de4bd8157569d6c52c08'
            'f4897590b0f677ba34767a29822f8f922a750daf66e8adf47be89f7c2550cf4b'
            'ff314a727d1e762875ea044a9a8583c564d06b26c0a9f48eb29ddd0f423e7755')
install='compiz.install'

prepare() {
  cd "compiz-${pkgver}"

  # Set gtk-window-decorator as default in the Window Decoration plugin
  patch -Np1 -i "${srcdir}/set-gwd-default.patch"

  # Set focus prevention level to off which means that new windows will always get focus
  patch -Np1 -i "${srcdir}/focus-prevention-disable.patch"

  # Patch gtk-window-decorator for Metacity 3.14 support
  patch -Np1 -i "${srcdir}/gwd-metacity-3_14-fix.patch"

  # Upstream fix for https://bugs.launchpad.net/compiz/+bug/1404054
  patch -Np1 -i "${srcdir}/gwd-system-font-fix.patch"

  find -type f \( -name 'CMakeLists.txt' -or -name '*.cmake' \) -exec sed -e 's/COMMAND python/COMMAND python2/g' -i {} \;
  find compizconfig/ccsm -type f -exec sed -e 's|^#!.*python|#!/usr/bin/env python2|g' -i {} \;
}

build() {
  cd "compiz-${pkgver}"

  export PYTHON="/usr/bin/python2"

  mkdir build; cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE="Release" \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DCMAKE_INSTALL_LIBDIR="/usr/lib" \
    -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCOMPIZ_DISABLE_SCHEMAS_INSTALL=On \
    -DCOMPIZ_BUILD_WITH_RPATH=Off \
    -DCOMPIZ_PACKAGING_ENABLED=On \
    -DBUILD_GTK=On \
    -DBUILD_METACITY=On \
    -DBUILD_KDE4=Off \
    -DUSE_GCONF=Off \
    -DUSE_GSETTINGS=On \
    -DCOMPIZ_BUILD_TESTING=Off \
    -DCOMPIZ_WERROR=Off \
    -DCOMPIZ_DEFAULT_PLUGINS="composite,opengl,decor,resize,place,move,ccp"

    make
}

package() {
  cd "compiz-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  # findcompiz_install needs COMPIZ_DESTDIR and install needs DESTDIR
  # make findcompiz_install
  CMAKE_DIR=$(cmake --system-information | grep '^CMAKE_ROOT' | awk -F\" '{print $2}')
  install -dm755 "${pkgdir}${CMAKE_DIR}/Modules/"
  install -m644 ../cmake/FindCompiz.cmake "${pkgdir}${CMAKE_DIR}/Modules/"

  # Add documentation
  install -dm755 "${pkgdir}/usr/share/doc/compiz/"
  install ../{AUTHORS,NEWS,README} "${pkgdir}/usr/share/doc/compiz/"

  # Add the gsettings schema files
  if ls generated/glib-2.0/schemas/ | grep -qm1 .gschema.xml; then
    install -dm755 "${pkgdir}/usr/share/glib-2.0/schemas/"
    install -m644 generated/glib-2.0/schemas/*.gschema.xml "${pkgdir}/usr/share/glib-2.0/schemas/"
  fi

  # Remove GConf schemas
  rm -rv "${pkgdir}/usr/share/gconf/" 
}
