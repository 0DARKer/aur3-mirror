# Maintainer: twa022 <twa022 at gmail dot com>

_pkgname=cinnamon-bluetooth
pkgname=${_pkgname}-git
pkgver=16.9934dad
pkgrel=1
pkgdesc="The Cinnamon Bluetooth Subsystem"
arch=(i686 x86_64)
url="http://live.gnome.org/GnomeBluetooth"
license=(GPL LGPL)
depends=(gtk3 hicolor-icon-theme libnotify bluez4)
makedepends=(intltool gobject-introspection itstool docbook-xsl)
options=(!libtool !emptydirs)
install=cinnamon-bluetooth.install
source=(git+https://github.com/linuxmint/cinnamon-bluetooth.git
        61-cinnamon-bluetooth-rfkill.rules)

pkgver() {
  cd ${srcdir}/${_pkgname}
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd ${srcdir}/${_pkgname}
  autoreconf -fi 
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile \
    --enable-compile-warnings=no 

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd ${srcdir}/${_pkgname}
  make DESTDIR="$pkgdir" install

  install -m644 -D ../61-cinnamon-bluetooth-rfkill.rules \
      "$pkgdir/usr/lib/udev/rules.d//61-cinnamon-bluetooth-rfkill.rules"
}
sha256sums=('SKIP'
            '8dd2d3637f380a89e72f65e6c8021e994ccbdc645f1a1b1dbffe74ac9f061f53')
