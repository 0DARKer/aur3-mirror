pkgname='lxqt-connman-applet-git'
pkgdesc='LxQt system-tray applet for connman'
url='https://github.com/surlykke/lxqt-connman-applet'
provides=('lxqt-connman-applet')
conflicts=('lxqt-connman-applet')

pkgver=r25.06d16d9
pkgrel=1
source=("$pkgname::git+https://github.com/surlykke/lxqt-connman-applet.git"
        'cmake-find-lxqt.patch')
md5sums=('SKIP'
         'd443bbbd5a4e7f42f96e3248c3b92341')

arch=('i686' 'x86_64')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"
  git apply '../cmake-find-lxqt.patch'
}

build() {
  cd "$pkgname"
  cmake . \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
}
