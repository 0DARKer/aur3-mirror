# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_pkgname=kamoso
pkgname=$_pkgname-git
pkgver=631.e9dab44
pkgrel=1
pkgdesc="Kamoso is an application to take pictures and videos out of your webcam."
url="https://projects.kde.org/projects/extragear/multimedia/kamoso"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kdebase-workspace' 'qt-gstreamer' 'libkipi')
makedepends=('cmake' 'git' 'automoc4')
conflicts=('kamoso')
provides=('kamoso')

source=("git://anongit.kde.org/kamoso/")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	echo "$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
	cd "$srcdir"
	mkdir -p build
	cd build

	cmake "../$_pkgname" -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` -DCMAKE_BUILD_TYPE=Release
	make
}

package() {
	cd "$srcdir/build"

	make DESTDIR="$pkgdir" install
}
