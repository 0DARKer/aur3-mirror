# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=air-video-server
pkgver=alpha5
pkgrel=1
pkgdesc="Air Video is a Client/Server-Mediaplayer, which allows you to stream videos directly from your PC to your iPhone"
arch=('i686' 'x86_64')
url="http://www.inmethod.com/air-video/index.html"
license=('custom')
depends=('zlib' 'faad2>=2.7' 'lame' 'x264>=20100107' 'faac' 'gpac' 'java-runtime')
backup=('etc/conf.d/air-video-server')
install=${pkgname}.install
source=(http://www.inmethod.com/air-video/download/ffmpeg-for-2.4.5.tar.bz2
        http://www.inmethod.com/air-video/download/linux/${pkgver}/AirVideoServerLinux.jar
        ${pkgname}.properties
        ${pkgname}.sh
        ${pkgname}.rc
        ${pkgname}.autostart
        mp4creator.sh
        LICENSE)
noextract=('AirVideoServerLinux.jar')
md5sums=('5190c22f663e4755cc9870e6a511b99c'
         '966e4326c16fd8a7c622ec563e1eaa2b'
         '45a6f1ec1d67dba8de940c8ddd327a56'
         '0dd2a26625ca5754380c346dc34a871b'
         '2b738be41e70fe1aac3259b4f9584f29'
         '702f82c153faf742ab451bf2f26500ea'
         '6e9403f1dd14654844ece68a3cc35f7e'
         '44dd1b2e8cf08cb372ba51217d267ad5')
                                    
build() {
  cd ${srcdir}/ffmpeg
  ./configure --prefix=/usr --enable-pthreads --disable-shared --enable-static \
    --enable-gpl --enable-libx264 --enable-libmp3lame --disable-decoder=aac
  make
}

package() {
  cd ${srcdir}
  
  install -m755 -d ${pkgdir}/usr/share/${pkgname}
  install -m644 AirVideoServerLinux.jar ${pkgdir}/usr/share/${pkgname}
  install -m644 ${pkgname}.properties ${pkgdir}/usr/share/${pkgname}
  install -m755 ffmpeg/ffmpeg ${pkgdir}/usr/share/${pkgname}
  install -Dm755 mp4creator.sh ${pkgdir}/usr/share/${pkgname}/mp4creator
  install -Dm755 ${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}

  install -Dm755 ${pkgname}.rc ${pkgdir}/etc/rc.d/${pkgname}
  install -Dm644 ${pkgname}.properties ${pkgdir}/etc/conf.d/${pkgname}
  install -Dm644 ${pkgname}.autostart ${pkgdir}/etc/xdg/autostart/${pkgname}.desktop

  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}/ffmpeg
  install -m644 ffmpeg/{LICENSE,COPYING.*} ${pkgdir}/usr/share/licenses/${pkgname}/ffmpeg
}
