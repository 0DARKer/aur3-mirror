# Maintainer: Dmitriy Morozov <archlinux@foxcub.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# TU: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>

# This PKGBUILD is modified from Arch's official Boost PKGBUILD to build the
# Boost.Logging library

pkgname=boost-log
pkgver=1.0
_boostbase=boost
_boostver=1_45_0
pkgrel=1
arch=('i686' 'x86_64')
url="http://boost-log.sf.net"
makedepends=('python2' 'bzip2' 'zlib')
source=(http://downloads.sourceforge.net/sourceforge/${_boostbase}/${_boostbase}_${_boostver}.tar.gz
        http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.zip)
license=('custom')
options=('!ccache')
md5sums=('739792c98fafb95e7a6b5da23a30062c'
         '2a2f078399fe158495bd2347af6bdbc5')

_stagedir="${srcdir}/stagedir"

build() {
  cp -a ${srcdir}/$pkgname-$pkgver/boost ${srcdir}/${_boostbase}_${_boostver}
  cp -a ${srcdir}/$pkgname-$pkgver/libs ${srcdir}/${_boostbase}_${_boostver}

  # set python path for bjam
  cd "${srcdir}/${_boostbase}_${_boostver}/tools"
  echo "using python : 2.7 : /usr/bin/python2 ;" >> build/v2/user-config.jam
  echo "using mpi ;" >> build/v2/user-config.jam

  # build bjam
  cd "${srcdir}/${_boostbase}_${_boostver}/tools/build/v2/engine/src"
  ./build.sh cc

  _bindir="bin.linuxx86"
  [ "${CARCH}" = "x86_64" ] && _bindir="bin.linuxx86_64"

  install -m755 -d "${_stagedir}"/usr/bin
  install -m755 ${_bindir}/bjam "${_stagedir}"/usr/bin/bjam

  # build bcp
  cd "${srcdir}/${_boostbase}_${_boostver}/tools/bcp"
  ../build/v2/engine/src/${_bindir}/bjam --toolset=gcc
  install -m755 "${srcdir}/${_boostbase}_${_boostver}/dist/bin/bcp" \
                ${_stagedir}/usr/bin/bcp

  # build libs
  cd "${srcdir}/${_boostbase}_${_boostver}"
  # default "minimal" install: "release link=shared,static
  # runtime-link=shared threading=multi"
  # --layout=tagged will add the "-mt" suffix for multithreaded libraries
  # and installs includes in /usr/include/boost.
  # --layout=system no longer adds the -mt suffix for multi-threaded libs.
  # install to ${_stagedir} in preparation for split packaging

  ./tools/build/v2/engine/src/${_bindir}/bjam \
      release debug-symbols=off threading=single,multi \
      runtime-link=shared link=shared,static \
      cflags=-fno-strict-aliasing \
      toolset=gcc \
      --prefix="${_stagedir}" \
      -sTOOLS=gcc \
      --layout=tagged \
      --with-log \
      ${MAKEFLAGS} \
      install

  # build pyste
  cd "${srcdir}/${_boostbase}_${_boostver}/libs/python/pyste/install"
  python2 setup.py install --root=${_stagedir}
}

package_boost() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Development"
    depends=("boost-libs=${pkgver}")
    optdepends=('python2: for python bindings')

    install -dm 755 "${pkgdir}"/usr/{include,lib}
    # headers/source files
    cp -r "${_stagedir}"/include/ "${pkgdir}"/usr/

    # static libs
    cp -r "${_stagedir}"/lib/*.a "${pkgdir}"/usr/lib/

    # utilities (bjam, bcp, pyste)
    cp -r "${_stagedir}"/usr/* "${pkgdir}"/usr/

    # license
    install -D -m644 "${srcdir}/${_boostbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost/LICENSE_1_0.txt
}

package_boost-libs() {
    pkgdesc="Free peer-reviewed portable C++ source libraries - Runtime"
    depends=('gcc-libs' 'bzip2' 'zlib')

    install -dm 755 "${pkgdir}/usr/lib"
    #shared libs
    cp -r "${_stagedir}"/lib/*.so{,.*} "${pkgdir}/usr/lib/"

    # license
    install -D -m644 "${srcdir}/${_boostbase}_${_boostver}/LICENSE_1_0.txt" \
        "${pkgdir}"/usr/share/licenses/boost-libs/LICENSE_1_0.txt
}

package_boost-log() {
    pkgdesc="Logging library provisionally accepted to Boost"
    depends=('boost')
    
    install -dm 755 "${pkgdir}"/usr/include/boost
    install -dm 755 "${pkgdir}"/usr/lib
    # headers/source files
    cp -r "${_stagedir}"/include/boost/log "${pkgdir}"/usr/include/boost

    # static libs
    cp -r "${_stagedir}"/lib/*log*.a "${pkgdir}"/usr/lib/
    cp -r "${_stagedir}"/lib/*log*.so{,.*} "${pkgdir}/usr/lib/"
}
