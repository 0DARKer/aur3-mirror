# Maintainer: Gilrain <gilrain+libre.arch A_T castelmo DOT_ re>

pkgname=pgl-git
pkgver=2.2.4.r57.ga40b84a
pkgrel=1
pkgdesc="A privacy oriented firewall application (GUI)."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/peerguardian/"
license=('GPL3')
depends=('logrotate' 'wget' 'libnetfilter_queue' 'iptables' 'qt4' 'polkit-qt')
optdepends=('unzip: for zipped blocklists'
	    'p7zip: for 7z blocklists'
	    'net-tools: to whitelist local IP addresses'
	    'networkmanager: to whitelist newly started network interfaces'
	    'smtp-forwarder: to send reports'
	    'tcptraceroute: to diagnose connection problems')
provides=('pgl')
conflicts=('blockcontrol' 'moblock')
backup=('etc/pgl/allow.p2p'
	'etc/pgl/blocklists.list'
	'etc/pgl/pglcmd.conf'
	'etc/logrotate.d/pglcmd'
	'etc/logrotate.d/pgld')
install=install
source=('git://git.code.sf.net/p/peerguardian/code/')
sha256sums=('SKIP')

pkgver() {
    cd code

    git describe --tags | sed -r 's/^pgl_//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd "$srcdir/code/pgl"

    ./autogen.sh
}

build() {
    # makes sure qt4 is used
    export QMAKE=qmake-qt4
    export MOC=moc-qt4
    export UIC=uic-qt4
    export RCC=rcc-qt4

    cd "$srcdir/code/pgl"

    ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
		--with-piddir=/run --with-systemd --disable-cron --without-initddir
    make
}

package() {
    cd "$srcdir/code/pgl"

    make DESTDIR=$pkgdir install

    # integrates daemon log in systemd journal
    sed -i '/^LOG_SYSLOG/s/0/1/g' $pkgdir/usr/lib/pgl/pglcmd.defaults
}
