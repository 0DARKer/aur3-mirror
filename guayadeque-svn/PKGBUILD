# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: sausageandeggs <sausageandeggs@archlinux.us>
# Contributor: Martin Herndl <martin.herndl@gmail.com>

_pkgname=guayadeque
pkgname=$_pkgname-svn
pkgver=1.0.1882
pkgrel=1
pkgdesc="Full Featured Linux media player for large collections"
arch=(i686 x86_64)
url=http://sourceforge.net/projects/$_pkgname
license=(GPL3)
depends=(desktop-file-utils flac taglib 'wxsqlite3>=3.0.1')
makedepends=(cmake subversion)
optdepends=('gstreamer0.10-plugins: all Gstreamer plugins to play all kind of music files')
provides=($_pkgname)
conflicts=($_pkgname)
source=($pkgname::svn+https://$_pkgname.svn.sourceforge.net/svnroot/$_pkgname/Trunk
    CMakeLists.patch)
sha256sums=('SKIP'
    '01d268c772ca762af79e5ccd6d5167b647425c6884ac38e689f8f8de1f5feb67')
sha512sums=('SKIP'
    'd225a19fe4203ccd70cd4606d29d3dfb60cd7761ea83abcd73cc20d63c9c55c0b685bf6c1e3373cf1efeb65e146373caf70cc9decce4430504d3de5a98bb0802')

pkgver() {
    echo 1.0.$(svnversion "$SRCDEST"/$pkgname/)
}

prepare() {
    cd $pkgname/
    patch -p1 -i ../CMakeLists.patch
}

build() {
    cd $pkgname/
    ./buildt
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DWXSQLITE3_LIB=wxcode_gtk2u_wxsqlite3-2.8 .
    make
}

package() {
    make -C $pkgname DESTDIR="$pkgdir" install
}
