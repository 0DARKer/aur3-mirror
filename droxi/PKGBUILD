# Maintainer: Brandon Mulcahy <brandon@jangler.info>

_gemname=droxi
pkgname=$_gemname
pkgver=0.3.1
pkgrel=1
pkgdesc='An ftp-like command-line interface to Dropbox'
arch=(any)
url='https://github.com/jangler/droxi'
license=(MIT)
depends=(ruby-dropbox-sdk)
options=(!emptydirs)
source=(https://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha1sums=('cdeac802a06eb4d66c6093a91e51a3bdbfc48a28')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/$_gemdir/gems/$_gemname-$pkgver"
  rake build
  install -D -m644 "build/${_gemname}.1" "$pkgdir/usr/share/man/man1/${_gemname}.1"
}
