# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Mantas MikulÄ—nas <grawity@gmail.com>

pkgname=eggdrop
_rpkgname=eggdrop
pkgver=1.6.21
_pkgver=${pkgver%.*}
pkgrel=3
pkgdesc="The world's most popular Open Source IRC bot."
arch=('i686' 'x86_64')
url="http://www.eggheads.org/"
license=('GPL2')
depends=('tcl>=8.3' 'zlib')
makedepends=('hardened-cc')
source=("http://ftp.eggheads.org/pub/$_rpkgname/source/$_pkgver/$_rpkgname$pkgver.tar.bz2"{,.asc})
backup=("etc/$pkgname.conf")
options=('!makeflags')

sha256sums=('75bd5573a609eac3940c0b6ca8251c3f38ea5d54b520e1cad93c650b4bc21754'
            '37729631794111496d0843e2f64f0dc2fc1744924fce41cdf1a55a3680399c21')

# Want *unique* multiple installations? Change the pkgname, it will just work.
_usrdir="/usr"
_sharedir="$_usrdir/share"
_modulesdir="$_usrdir/lib/$pkgname"
_scriptsdir="$_sharedir/$pkgname/scripts"
_helpdir="$_sharedir/$pkgname/help"
_bin="$_usrdir/bin/$pkgname"
_mandir="$_sharedir/man/man1"
readonly -a _usrdir _sharedir _modulesdir _scriptsdir _helpdir _bin _mandir

build() {
  cd "$srcdir/$_rpkgname$pkgver"

  export CC="/usr/bin/hgcc"

  sed -i "s|\"./language\"|\"/$_usrdir/$pkgname/language\"|g" src/eggdrop.h

  ./configure
  make config

  sed -i "s|-L/usr/lib|${LDFLAGS}|g" Makefile
  make
}

package() {
  eggtmp="$pkgdir/tmp"

  # This is ugly..

  cd "$srcdir/$_rpkgname$pkgver"
  make DEST="$eggtmp" install

  find "$eggtmp" -name 'CONTENTS' -exec rm -v {} +

  mkdir -p -m 0755 "$pkgdir/etc" \
    "$pkgdir/$_usrdir/"{bin,lib} \
    "$pkgdir/$_sharedir/"{$pkgname,doc,man/man1}

  mv "$eggtmp/modules-$pkgver" "$pkgdir/$_modulesdir"
  mv "$eggtmp/eggdrop-$pkgver" "$pkgdir/$_bin"
  mv "$eggtmp/doc/man1/$_rpkgname.1" "$pkgdir/$_mandir/$pkgname.1"
  mv "$eggtmp/doc" "$pkgdir/$_sharedir/doc/$pkgname-$pkgver"

  for d in language scripts help text; do
  mv "$eggtmp/${d}" "$pkgdir/$_sharedir/$pkgname"
  done

  sed -e '2d' \
    -e "1s@^.*@#!$_bin@" \
    -e "s@scripts/@$_scriptsdir/@g" \
    -e "s@help/@$_helpdir@g" \
    -e "s@modules/@$_modulesdir/@g" \
    eggdrop.conf > "$pkgdir/etc/$pkgname.conf"

  rm -fr "$eggtmp"
  
  find "$pkgdir/$_sharedir" -type f -exec chmod 0444 {} +

}

# vim:set ts=2 sw=2 et:
