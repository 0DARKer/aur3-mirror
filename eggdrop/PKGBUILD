# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Mantas MikulÄ—nas <grawity@gmail.com>

pkgname=eggdrop
pkgver=1.6.21
_pkgver=${pkgver%.*}
pkgrel=3
pkgdesc="The world's most popular Open Source IRC bot."
arch=('i686' 'x86_64')
url="http://www.eggheads.org/"
license=('GPL2')
depends=('tcl>=8.3' 'zlib')
makedepends=('hardened-cc')
source=("http://ftp.eggheads.org/pub/$pkgname/source/$_pkgver/$pkgname$pkgver.tar.bz2"{,.asc})
backup=('etc/eggdrop.conf')
options=('!makeflags')

sha256sums=('75bd5573a609eac3940c0b6ca8251c3f38ea5d54b520e1cad93c650b4bc21754'
            '37729631794111496d0843e2f64f0dc2fc1744924fce41cdf1a55a3680399c21')


build() {
  cd "$srcdir/$pkgname$pkgver"

  export CC="/usr/bin/hgcc"

  sed -i "s|\"./language\"|\"/usr/share/eggdrop/language\"|g" src/eggdrop.h

  ./configure
  make config

  sed -i "s|-L/usr/lib|${LDFLAGS}|g" Makefile
  make
}

package() {
  eggtmp="$pkgdir/tmp"

  # This is ugly..

  cd "$srcdir/$pkgname$pkgver"
  make DEST="$eggtmp" install

  find "$eggtmp" -name CONTENTS -exec rm {} +

  mkdir -p -m 0755 "$pkgdir/etc" \
    "$pkgdir/usr/lib" \
    "$pkgdir/usr/bin" \
    "$pkgdir/usr/share/doc" \
    "$pkgdir/usr/share/eggdrop/" \
    "$pkgdir/usr/share/man"

  mv "$eggtmp/modules-$pkgver" "$pkgdir/usr/lib/eggdrop"
  mv "$eggtmp/eggdrop-$pkgver" "$pkgdir/usr/bin/eggdrop"
  mv "$eggtmp/doc/man1" "$pkgdir/usr/share/man"
  mv "$eggtmp/doc" "$pkgdir/usr/share/doc/$pkgname-$pkgver"

  for d in language scripts help text; do
  mv "$eggtmp/${d}" "$pkgdir/usr/share/eggdrop"
  done

  _modulesdir="/usr/lib/eggdrop"
  _scriptsdir="/usr/share/eggdrop/scripts"
  _helpdir="/usr/share/eggdrop/help"
  _bin="/usr/bin/eggdrop"

  sed -e '2d' \
    -e "1s@^.*@#!$_bin@" \
    -e "s@scripts/@$_scriptsdir/@g" \
    -e "s@help/@$_helpdir@g" \
    -e "s@modules/@$_modulesdir/@g" \
    eggdrop.conf > "$pkgdir/etc/eggdrop.conf"

  rm -fr "$eggtmp"
  
  find "$pkgdir/usr/share" -type f -exec chmod 0444 {} +

}

# vim:set ts=2 sw=2 et:
