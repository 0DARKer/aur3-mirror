# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Mantas MikulÄ—nas <grawity@gmail.com>

pkgname=eggdrop
pkgver=1.6.20
_pkgver=${pkgver%.*}
pkgrel=2
pkgdesc="The world's most popular Open Source IRC bot."
arch=('i686' 'x86_64')
url="http://www.eggheads.org/"
license=('GPL2')
depends=('tcl>=8.3' 'zlib')
makedepends=('hardened-cc')
install="$pkgname.install"
source=("http://ftp.eggheads.org/pub/$pkgname/source/$_pkgver/$pkgname$pkgver.tar.bz2"{,.asc}
        'eggdrop-1.6.17-langdir.patch'
        'eggdrop-1.6.20-no_libdns.patch'
        'eggdrop-1.6.20-suzi_sp0010.patch'
        "$pkgname.install")
backup=('etc/eggdrop.conf')
options=('!makeflags')
sha256sums=('5d3b3f3c8988d054d6ba97b84b43e9620c062d57d2606eaa9f652c598aa44c6f'
            'b1de70fd8c91db9c5a69a5e16636452ee633e68ba90d1aeabe32ece93fc8ad9a'
            '06fa58a3fa78a418fecab8cfdbae38ced8b97df527593dbcd0785a23111f2dd7'
            'f254a992d3f8d7a6d17f9c41c6271275e7cb40b2a340beccfa82985a026302fc'
            'b3579aed32d855dd3323ca0cc2404092dd3725edcdeb5cb1572763353bbbaf3e'
            'f1f620d0c3d1107bc5553bbf9f977dc8533ba877e95a8e4e3b0954b8db9b9154')


build() {
  cd "$srcdir/$pkgname$pkgver"

  patch -Np1 -i "$srcdir/eggdrop-1.6.17-langdir.patch"
  patch -Np1 -i "$srcdir/eggdrop-1.6.20-no_libdns.patch"
  patch -Np1 -i "$srcdir/eggdrop-1.6.20-suzi_sp0010.patch"

  export CC="/usr/bin/hgcc"

  ./configure
  make config
  make
}

package() {
  eggtmp="$pkgdir/tmp"

  # This is ugly..

  cd "$srcdir/$pkgname$pkgver"
  make DEST="$eggtmp" install

  find "$eggtmp" -name CONTENTS -exec rm {} +

  mkdir -p -m 0755 "$pkgdir/etc" \
    "$pkgdir/usr/lib" \
    "$pkgdir/usr/bin" \
    "$pkgdir/usr/share/doc" \
    "$pkgdir/usr/share/eggdrop/" \
    "$pkgdir/usr/share/man/man1"

  mv "$eggtmp/modules-1.6.20" "$pkgdir/usr/lib/eggdrop"
  mv "$eggtmp/eggdrop-$pkgver" "$pkgdir/usr/bin/eggdrop"
  mv "$eggtmp/doc/man1" "$pkgdir/usr/share/man/man1"
  mv "$eggtmp/doc" "$pkgdir/usr/share/doc/$pkgname-$pkgver"

  for d in language scripts help text; do
  mv "$eggtmp/${d}" "$pkgdir/usr/share/eggdrop"
  done

  _modulesdir="/usr/lib/eggdrop"
  _scriptsdir="/usr/share/eggdrop/scripts"
  _helpdir="/usr/share/eggdrop/help"
  _bin="/usr/bin/eggdrop"

  sed -e '2d' \
    -e "1s@^.*@#!$_bin@" \
    -e "s@scripts/@$_scriptsdir/@g" \
    -e "s@help/@$_helpdir@g" \
    -e "s@modules/@$_modulesdir/@g" \
    eggdrop.conf > "$pkgdir/etc/eggdrop.conf"

  rm -fr "$eggtmp"
  
  find "$pkgdir/usr/share" -type f -exec chmod 0444 {} +

}

# vim:set ts=2 sw=2 et:
