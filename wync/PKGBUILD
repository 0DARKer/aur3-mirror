# Maintainer: Jameson Pugh <imntreal@gmail.com>
# Maintainer: stef312 <stef312_at_gmail_dot_com>
# Maintainer: Gaspar de El√≠as <caspercba_at_hotmail_dot_com>
# Maintainer: Artem Sheremet <dot_doom_at_gmail_dot_com>

pkgname=wync
pkgver=2.0.201
pkgrel=1
_arch=64
pkgdesc="Microsoft Lync client for Linux"
arch=('x86_64')
license=('custom:"Copyright (c) 2014 Fisil LLC"')
depends=('libxkbfile' 'curl' 'gcc-libs-multilib' 'ffmpeg-compat' 'gtk2' 'nss' 'qt5-multimedia' 'libxss' 'qt5-quick1' 'qt5-svg' 'postgresql-libs')
url="http://fisil.com/products.html"
options=('!strip' 'staticlibs')
source=("http://fisil.com/linux/wync_ubuntu${_arch}_v${pkgver}.deb"
				'wync.conf')
sha256sums=('a41f646d94c8f5ff6621572a709374053d62bc9bcee6fca4460018ef2bc72e76'
            'c66fc39d071e1595a6cf94a7db566555a40596a8db1eb2bb4d428f692c1d6a17')

package() {
	cd "${srcdir}"
	ar x "wync_ubuntu${_arch}_v${pkgver}.deb" >/dev/null
	tar -xzf data.tar.gz -C "${pkgdir}"

	# Remove autorun xdg file
	rm -rf "${pkgdir}/etc"

	# Remove group/world-writable bit
	chmod -R go-w "${pkgdir}/opt"
	chmod -R go-w "${pkgdir}/usr"

	install -dm 755 "${pkgdir}/usr/bin"
	ln -s /opt/wync_linux/wync.sh "${pkgdir}/usr/bin/wync"
	
	install -dm 755 "${pkgdir}/etc/ld.so.conf.d"
  install -m 755 wync.conf "${pkgdir}/etc/ld.so.conf.d/wync.conf"
}

# vim: set ts=2 sw=2 ft=sh noet:
