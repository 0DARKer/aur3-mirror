# Maintainer: Michael Lass <bevan@bi-co.net>
# Contributor: Tolga HOŞGÖR <fasdfasdas@gmail.com>
# Contributor: Henning Mueller <henning@orgizm.net>
 
pkgname=fritzing
pkgver=0.9.1b
pkgrel=2
pkgdesc='PCB layout prototyping application'
arch=('i686' 'x86_64')
url=http://fritzing.org
license=(GPL3)
makedepends=('boost')
# libsm and libxkbcommon-x11 are optdepends for qt5-base that are needed for the xcb plugin used by Fritzing
depends=('java-environment' 'libsm' 'libxkbcommon-x11' 'python2' 'qt5-serialport' 'qt5-svg')
source=(http://fritzing.org/download/$pkgver/source-tarball/fritzing-$pkgver.source.zip)
sha256sums=('6e29da311225fe668e2abdf3859e8784ab97e38c334e4f50eebdc283c6f744a7')

build() {
  cd "$srcdir"/fritzing-app-master
  qmake-qt5 && make
}

package() {
  cd "$srcdir"/fritzing-app-master

  # Unfortunately make install does not support DESTDIR or prefix. So we
  # have to install manually.

  # resources
  install -dm755 "$pkgdir"/usr/share/$pkgname
  cp -r parts pdb bins sketches translations "$pkgdir"/usr/share/$pkgname
  cp -rf resources/parts "$pkgdir"/usr/share/$pkgname

  # patch python scripts to use python2
  find "$pkgdir"/usr/share/$pkgname/parts/part-gen-scripts/ \
    -name \*.py -exec sed -i \
      -e 's#/usr/bin/env python$#/usr/bin/env python2#' \
      -e 's#/usr/bin/python$#/usr/bin/python2#' {} \;

  # binary
  install -Dm755 Fritzing "$pkgdir"/usr/bin/Fritzing

  # desktop file and icon
  install -Dm644 fritzing.desktop "$pkgdir"/usr/share/applications/fritzing.desktop
  install -Dm644 resources/images/fritzing_icon.png "$pkgdir"/usr/share/pixmaps/fritzing.png

  # man page
  install -Dm644 Fritzing.1 "$pkgdir"/usr/share/man/man1/Fritzing.1
}
