# Contributors:
#   henning mueller <henning@orgizm.net>

pkgname=fritzing
pkgver=0.8.2b
pkgrel=1
pkgdesc='PCB layout prototyping application'
arch=(i686 x86_64)
url=http://fritzing.org
license=(GPL3)
makedepends=(boost)
depends=(qt4)
source=(
  http://fritzing.org/download/$pkgver/source-tarball/fritzing-$pkgver.source.tar.bz2
  fritzing.desktop
)

build() {
  cd $srcdir/fritzing*source
  qmake-qt4 && make
}

package() {
  _destdir=usr/share/$pkgname

  cd $srcdir/fritzing*source

  mkdir -p $pkgdir/{$_destdir,usr/{bin,share/applications}}
  cp -r {parts,pdb,bins,sketches,translations,Fritzing,Fritzing.sh} $pkgdir/$_destdir
  cp -rf resources/parts $pkgdir/$_destdir

  cp $srcdir/fritzing.desktop $pkgdir/usr/share/applications
  ln -s /$_destdir/Fritzing $pkgdir/usr/bin/fritzing
}

sha256sums=('f77cc8b3c61a27d0c4ccc8c00ddecab6aca81c0bc2943645dbb30fa4b89610ac'
            'bb817f6ed55f079ddb0076bbb7ad96cf6ef254b825e711142bf62e50586510ed')
