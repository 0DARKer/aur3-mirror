# Contributor: Vitaliy Berdinskikh <skipper13@archlinux.org.ua>
# Contributor: Andrea Agosti <cifvts@gmail.com>
# Maintainer: xduugu
pkgname=instantclient-sqlplus
pkgver=11.2.0.1
pkgrel=3
pkgdesc="Additional libraries and executable for running SQL*Plus with Instant Client."
arch=('i686' 'x86_64')
url="http://www.oracle.com/technology/tech/oci/instantclient/"
license=('custom:OTN')
depends=("instantclient-basic=$pkgver")
#
# grab the source from
# i686  : http://www.oracle.com/technology/software/tech/oci/instantclient/htdocs/linuxsoft.html
# x86_64: http://www.oracle.com/technology/software/tech/oci/instantclient/htdocs/linuxx86_64soft.html
#
source=($pkgname-linux32-$pkgver.zip LICENSE)
md5sums=('94a004ee4f58149e62ed76107217d7c8'
         '2d62e22e4f2d7e6d220fafde0f692a7d')
sha256sums=('42ca0d215612c17bc4297369d2603adfe1d70aa051ba27381fe8389c5a2ef23a'
            'f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')

if [[ $CARCH = x86_64 ]]; then
	source[0]=oracle-${pkgname%%-*}$(echo $pkgver | sed -r 's/^([0-9]+\.[0-9]+).*$/\1/')-${pkgname#*-}-${pkgver}.0-1.$CARCH.zip
	md5sums=('1fdc0c3544194de35d2aabe9e6b3faf5'
           '2d62e22e4f2d7e6d220fafde0f692a7d')
	sha256sums=('8cc614a1038107d02d0ac2a4d946bd85cf5f6e0ffd9d212a4316188d916095f9'
              'f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')
fi

build() {
	local dest=$pkgdir/opt/${pkgname%%-*}

	# copy files
	local pkgver=${pkgver/./_}
	install -d "${dest%/*}" "$pkgdir/usr/bin" &&
	cp -a "$srcdir/${pkgname%%-*}_${pkgver%%.*}" "$dest" &&
	ln -s "/opt/${pkgname%%-*}/sqlplus" \
		"$pkgdir/usr/bin/" || return 1

	# fix permissions
	cd "$dest" &&
	chmod a=rx,u+w * &&
	chmod a-x *_README *.sql || return 1

	install -Dm644 "$srcdir/LICENSE" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
}

# vim:set ts=2 sw=2 noet:
