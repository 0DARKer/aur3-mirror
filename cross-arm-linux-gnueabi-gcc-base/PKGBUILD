# Maintainer: Chuan Ji <jichuan89@gmail.com>

_target="arm-linux-gnueabi"
pkgname=cross-${_target}-gcc-base
pkgver=4.5.2
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
depends=("cross-${_target}-binutils" 'libmpc' 'libelf' 'cloog-ppl')
options=(!libtool !emptydirs zipman docs !strip)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-core-${pkgver}.tar.bz2)
md5sums=('aa9e36bec080452372bfba793428ee82')

build() {
  cd $srcdir/gcc-$pkgver

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  [[ -d gcc-build ]] || mkdir gcc-build
  cd gcc-build

  ../configure --prefix=/usr \
	--program-prefix=${_target}- \
	--target=${_target} \
	--host=$CHOST \
	--build=$CHOST \
	--disable-shared --disable-nls \
        --disable-threads \
        --enable-languages=c --enable-multilib \
	--with-local-prefix=/usr/lib/${_target} \
	--with-as=/usr/bin/${_target}-as --with-ld=/usr/bin/${_target}-ld \
	--enable-softfloat \
	--with-float=soft \
        --with-newlib \
        --enable-interwork \
        --enable-addons

  make all-gcc all-target-libgcc
  make DESTDIR=$pkgdir install-gcc install-target-libgcc

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info

  cp -r $pkgdir/usr/libexec/* $pkgdir/usr/lib/
  rm -rf $pkgdir/usr/libexec

  # strip it manually
  /usr/bin/${_target}-strip $pkgdir/usr/bin/* 2>/dev/null || true
  find $pkgdir/usr/lib -type f -exec /usr/bin/${_target}-strip {} \; 2>/dev/null || true
}
