# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libbluray-git
pkgver=20130107
pkgrel=1
pkgdesc="Library to access Blu-Ray disks for video playback. (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="http://www.videolan.org/developers/libbluray.html"
depends=('libxml2' 'java-environment=7')
makedepends=('git' 'apache-ant' 'texlive-latexextra' 'doxygen')
optdepends=('libaacs: Enable AACS decryption')
provides=('libbluray')
conflicts=('libbluray')
options=('!libtool')

_gitroot="git://git.videolan.org/libbluray.git"
_gitname="libbluray"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server..."

  if [ -d "${_gitname}" ] ; then
      cd "${_gitname}" && git pull
  else
      git clone --depth=1 "${_gitroot}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -fr "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  source /etc/profile.d/jdk.sh
  sed 's|AM_CONFIG_HEADER(config.h)|AC_CONFIG_HEADERS([config.h])|g' -i configure.ac
  ./bootstrap
  ./configure --prefix=/usr --with-jdk="${JAVA_HOME}" --enable-bdjava --disable-static
  make
  make doxygen-run
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 src/.libs/libbluray.jar "${pkgdir}/usr/share/java/libbluray.jar"
  install -d "${pkgdir}/usr/share/doc/libbluray"
  cp -R doc/doxygen/html/* "${pkgdir}/usr/share/doc/libbluray/"
  chmod -R 664 "${pkgdir}/usr/share/doc/libbluray/"*
}
