# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libbluray-git
pkgver=20130410.491c7e8
pkgrel=1
pkgdesc="Library to access Blu-Ray disks for video playback. (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="http://www.videolan.org/developers/libbluray.html"
depends=('libxml2' 'java-environment=7')
makedepends=('git' 'apache-ant')
optdepends=('libaacs: Enable AACS decryption')
provides=('libbluray')
conflicts=('libbluray')
options=('!libtool')
source=('git://git.videolan.org/libbluray.git')
md5sums=('SKIP')
_gitname="libbluray"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  sed 's|target="1.4"|target="1.4" includeantruntime="false"|' -i src/libbluray/bdj/build.xml
}

build() {
  cd "${_gitname}"

  . /etc/profile.d/jdk.sh
  ./bootstrap
  JDK_HOME="${JAVA_HOME}" ./configure --prefix=/usr --enable-bdjava --disable-static
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
