# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libbluray-git
pkgver=0.5.0.182.gb03f875
pkgrel=1
pkgdesc="Library to access Blu-Ray disks for video playback. (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="http://www.videolan.org/developers/libbluray.html"
depends=('libxml2' "java-environment" 'freetype2')
makedepends=('git' 'apache-ant')
optdepends=('libaacs: Enable AACS decryption')
provides=('libbluray')
conflicts=('libbluray')
options=('!libtool')
source=('git://git.videolan.org/libbluray.git')
md5sums=('SKIP')
_gitname="libbluray"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"

  . /etc/profile.d/jdk.sh
  ./bootstrap
  JDK_HOME="${JAVA_HOME}" ./configure --prefix=/usr --disable-static --enable-bdjava
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
