# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libbluray-git
pkgver=20130220
pkgrel=2
pkgdesc="Library to access Blu-Ray disks for video playback. (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="http://www.videolan.org/developers/libbluray.html"
depends=('libxml2' 'java-environment=7')
makedepends=('git' 'apache-ant')
optdepends=('libaacs: Enable AACS decryption')
provides=('libbluray')
conflicts=('libbluray')
options=('!libtool')

_gitroot="git://git.videolan.org/libbluray.git"
_gitname="libbluray"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server..."

  if [ -d "${_gitname}" ] ; then
      cd "${_gitname}" && git pull
  else
      git clone --depth=1 "${_gitroot}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -fr "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  . /etc/profile.d/jdk.sh

  sed 's|target="1.4"|target="1.4" includeantruntime="false"|' -i src/libbluray/bdj/build.xml

  ./bootstrap
  JDK_HOME="${JAVA_HOME}" ./configure --prefix=/usr --enable-bdjava --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
}
