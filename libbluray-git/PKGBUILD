# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libbluray-git
pkgver=20130101
pkgrel=1
pkgdesc="Blu-Ray access library. (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.videolan.org/developers/libbluray.html"
depends=('libxml2' 'java-environment')
makedepends=('git' 'apache-ant' 'texlive-latexextra' 'doxygen')
optdepends=('libaacs-git: Enable AACS decryption')
provides=('libbluray')
conflicts=('libbluray')
options=('!libtool')

_gitroot="git://git.videolan.org/libbluray.git"
_gitname="libbluray"

build() {
  msg "Connecting to GIT server..."

  if [ -d "${srcdir}/${_gitname}" ] ; then
      cd "${_gitname}" && git pull
  else
      git clone --depth=1 "${_gitroot}"
  fi

  cd "${srcdir}"

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -fr "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  source /etc/profile.d/jdk.sh
  [ "$(LANG=C pacman -Qi automake |grep Version | cut -d ' ' -f10 | sed 's|.[0-9]-[0-9]||g')" = "1.13" ] && sed 's|AM_CONFIG_HEADER(config.h)|AC_CONFIG_HEADERS([config.h])|g' -i configure.ac
  ./bootstrap
  ./configure --prefix=/usr --with-jdk="${JAVA_HOME}" --enable-bdjava --disable-static
  make
  make doxygen-run
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
  install -Dm644 src/.libs/libbluray.jar "${pkgdir}/usr/share/java/libbluray.jar"
  install -d "${pkgdir}/usr/share/doc/libbluray"
  cp -R doc/doxygen/html/* "${pkgdir}/usr/share/doc/libbluray/"
  chmod 664 "${pkgdir}/usr/share/doc/libbluray/"*
}
