# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libbluray-git
pkgver=0.7.0.3.gf55ec39
pkgrel=1
pkgdesc="Library to access Blu-Ray disks for video playback. (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL2.1')
url="http://www.videolan.org/developers/libbluray.html"
depends=('libxml2' "java-environment" 'freetype2')
makedepends=('git' 'apache-ant' 'libaacs')
optdepends=('libaacs: Enable AACS decryption')
provides=('libbluray')
conflicts=('libbluray')
source=('git://git.videolan.org/libbluray.git')
sha1sums=('SKIP')

pkgver() {
  cd libbluray
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  cd libbluray
  ./bootstrap
}

build() {
  cd libbluray
  ./configure --prefix=/usr --disable-static --enable-bdjava
  make
}

package() {
  make -C libbluray DESTDIR="${pkgdir}" install
}
