# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Arthur Țițeică arthur.titeica/gmail/com
# Contributor: Thomas Laube <tomx3@tomtomtom.org>

_pkgname=vokoscreen
pkgname=${_pkgname}-git
pkgver=2.1.8.beta.r1.g12c27b2
pkgrel=1
pkgdesc="An easy to use screencast creator. Qt5 build. Development version"
arch=(
	"${CARCH}"
)
url="http://linuxecke.volkoh.de/vokoscreen/vokoscreen.html"
license=(
	"GPL"
)

depends=(
	"ffmpeg"
	"lame"
	"mkvtoolnix-cli"
	"opencv"
	"qt5-x11extras"
)
optdepends=(
	"pulseaudio-alsa: For PulseAudio support"
)
makedepends=(
	"git"
)
provides=(
	"${_pkgname}=${pkgver}"
)
conflicts=(
	"${_pkgname}"
)
replaces=(
	"${_pkgname}"
)

source=(
	"git+https://github.com/vkohaupt/${_pkgname}.git"
	"${_pkgname}.desktop.patch"
)
sha384sums=(
	"SKIP"
	"61eda118ba8891b41a8dbcff5c7358b28dea7c29158a7f246dbce7a03c2b3ed83d51236368e584eaa61a71aea674e257"
)
sha512sums=(
	"SKIP"
	"5fd3c3f486723848b40cb2a82d2c59680e673bcbfd76d175bff60959b8ba10ecbea2894d0c775d5d6818e22369c0894775977b13abdcfd18a779acb8bdb9cf9e"
)

pkgver() {
	# Updating package version
	cd ${srcdir}/${_pkgname}
	(
		set -o pipefail
		git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	# Patching *.desktop file
	patch ${srcdir}/${_pkgname}/applications/${_pkgname}.desktop < ${srcdir}/${_pkgname}.desktop.patch
	
	# Create build directory
	mkdir -p ${srcdir}/build
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ ${njobs} -ge 8 ]]; then
		njobs=$(( $njobs - 2 ))
	fi

	# Building package
	cd ${srcdir}/build
	qmake-qt5 ../${_pkgname}/${_pkgname}.pro \
		CONFIG+=release \
		CONFIG+=c++14 \
		CONFIG+=LINUX_INTEGRATED \
		
	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/build
	make INSTALL_ROOT=${pkgdir} install
}