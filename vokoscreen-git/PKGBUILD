# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Arthur Țițeică arthur.titeica/gmail/com
# Contributor: Thomas Laube <tomx3@tomtomtom.org>

_pkgname=vokoscreen
pkgname=${_pkgname}-git
pkgver=2.1.2.beta.r17.g3433be9
pkgrel=1
pkgdesc="An easy to use screencast creator. Qt5 build. Development version"
arch=(
	"$CARCH"
)
url="http://www.kohaupt-online.de/"
license=(
	"GPL"
)

depends=(
	"ffmpeg"
	"lame"
	"mkvtoolnix-cli"
	"opencv"
	"qt5-x11extras"
	"desktop-file-utils"
)
optdepends=(
	"pulseaudio: for audio recording"
)
makedepends=(
	"git"
)
provides=(
	"${_pkgname}=${pkgver}"
)
conflicts=(
	"${_pkgname}"
)
replaces=(
	"${_pkgname}"
)

source=(
	"git+https://github.com/vkohaupt/${_pkgname}.git"
	"${_pkgname}.desktop.patch"
)
sha384sums=(
	"SKIP"
	"b6c1915207fb5fee8b78a7a5aa304efa37326062db05fb1c9332a696970a162413ef4f415d2b9f197252bc9b68e30d2e"
)
sha512sums=(
	"SKIP"
	"74f3167a62dcc8fb81e3f15ca5db44918fcd1d9c2c28f8c139937f37d70ba2296176e6968b956d8392e529699f314a2089bcf8af593b1d843b087ca89b02663d"
)

pkgver() {
	# Updating package version
	cd ${srcdir}/${_pkgname}
	(
		set -o pipefail
		git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	# Patching *.desktop file
	patch ${srcdir}/${_pkgname}/applications/${_pkgname}.desktop < ${srcdir}/${_pkgname}.desktop.patch
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ $(nproc) -ge 8 ]]; then
		njobs=$(( $njobs - 2 ))
	fi

	# Building package
	cd ${srcdir}/${_pkgname}
	qmake-qt5 ${_pkgname}.pro \
		CONFIG+=release \
		CONFIG+=LINUX_INTEGRATED \
		INSTALL_ROOT_PATH=${pkgdir}/usr
		
	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/${_pkgname}
	make INSTALL_ROOT=${pkgdir} install
}