# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Arthur Țițeică arthur.titeica/gmail/com
# Contributor: Thomas Laube <tomx3@tomtomtom.org>

_pkgname=vokoscreen
pkgname=${_pkgname}-git
pkgver=2.1.9.beta.r6.gf62a423
pkgrel=2
pkgdesc="An easy to use screencast creator. Qt5 build. Development version"
arch=(
	"${CARCH}"
)
url="http://linuxecke.volkoh.de/vokoscreen/vokoscreen.html"
license=(
	"GPL"
)

depends=(
	"ffmpeg>=1.1.0"
	"ffmpeg<2.6.0"
	"lame"
	"mkvtoolnix-cli"
	"opencv"
	"qt5-x11extras"
	"qt5-tools"
)
optdepends=(
	"pulseaudio-alsa: For PulseAudio support"
)
makedepends=(
	"git"
)
provides=(
	"${_pkgname}=${pkgver}"
)
conflicts=(
	"${_pkgname}"
)
replaces=(
	"${_pkgname}"
)

source=(
	"git+https://github.com/vkohaupt/${_pkgname}.git"
	"${_pkgname}.desktop.patch"
)
sha512sums=(
	"SKIP"
	"109057c0ef1b30dee7324ea08b385e22e4cfc8cee5f176d4637b0d1e2edb6dfcf0e8d852c832c32432b06c0a6139d3f8ff105df8aec2a48dc29f4c5c57027d5a"
)

pkgver() {
	# Updating package version
	cd ${srcdir}/${_pkgname}
	(
		set -o pipefail
		git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	# Patching *.desktop file
	cd ${srcdir}/${_pkgname}
	patch ./applications/${_pkgname}.desktop < ${srcdir}/${_pkgname}.desktop.patch

	# Create build directory
	mkdir -p ${srcdir}/build
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)

	if [[ ${njobs} -ge 8 ]]; then
		njobs=$(( $njobs - 2 ))
	fi

	# Building package
	cd ${srcdir}/build
	qmake-qt5 ../${_pkgname} \
		CONFIG+=release \
		CONFIG+=c++14 \
		-spec linux-g++

	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/build
	make INSTALL_ROOT=${pkgdir} install
}