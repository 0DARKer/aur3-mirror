# Maintainer: Jan Dolinar <dolik.rce@gmail.com>

pkgname=theide
pkgver=4179
pkgrel=1
pkgdesc="Modern IDE designed for developping large U++/C++ applications"
arch=('i686' 'x86_64')
url="http://www.ultimatepp.org"
license=('BSD')
groups=()
depends=('libnotify' 'desktop-file-utils')
makedepends=()
provides=('theide')
conflicts=('theide')
replaces=()
backup=()
options=(!makeflags emptydirs)
install='theide.install'
source=('http://downloads.sourceforge.net/project/upp/upp/'$pkgver'/upp-x11-src-'$pkgver'.tar.gz'
        'http://upp-mirror.googlecode.com/svn-history/r'$pkgver'/trunk/uppbox/lpbuild/Makefile'
        'http://upp-mirror.googlecode.com/svn-history/r'$pkgver'/trunk/uppbox/lpbuild/theide.1'
        'license.txt')
noextract=()

build() {
  cd "$srcdir/upp-x11-src-$pkgver"
  #patch
  sed -i 's/ptrdiff_t/std::ptrdiff_t/' uppsrc/Core/Algo.h
  sed -i 's/ifdef __clang__/if 1/' uppsrc/Core/Convert.h
  sed -i '/include <libnotify/a\
		#ifdef NOTIFY_CHECK_VERSION\
			#if NOTIFY_CHECK_VERSION(0,7,0)\
				#define NOTIFY_VERSION_GT_0_7_0\
			#endif\
		#endif
    s/: "gtk-dialog-error", NULL);/: "gtk-dialog-error"\
	#ifndef NOTIFY_VERSION_GT_0_7_0\
						, NULL\
	#endif\
						);/' uppsrc/CtrlLib/TrayIconX11.cpp
  #build
  echo "#define IDE_VERSION \"$pkgver-Arch\"" > "uppsrc/ide/version.h"
  msg2 "Building $pkgname..."
  make -f "$srcdir/Makefile" PKG=ide FLAGS="GCC GUI MT" BINPREFIX="$srcdir/_out/bin/the" BINEXT="" NESTS="uppsrc" OBJDIR="$srcdir/_out" $_fast
}

package(){
  #install
  mkdir -p "$pkgdir/usr/bin"
  cp "$srcdir/_out/bin/theide" "$pkgdir/usr/bin/theide"
  #license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  cp "$srcdir/license.txt" "$pkgdir/usr/share/licenses/$pkgname"
  #man page
  mkdir -p "$pkgdir/usr/share/man/man1"
  cp "$srcdir/theide.1" "$pkgdir/usr/share/man/man1/theide.1"
  #desktop entry
  mkdir -p "$pkgdir/usr/share/applications"
  cp "$srcdir/upp-x11-src-$pkgver/uppsrc/ide/theide.desktop" "$pkgdir/usr/share/applications"
  #icon
  mkdir -p "$pkgdir/usr/share/pixmaps"
  cp "$srcdir/upp-x11-src-$pkgver/uppsrc/ide/theide-48.png" "$pkgdir/usr/share/pixmaps/theide.png"
  #fix permissions
  find "$pkgdir/usr/" -type f -exec chown root:root {} \; -exec chmod 644 {} \;
  chmod a+x "$pkgdir/usr/bin/theide"
}

md5sums=('d2ad0897393909d7d62e252090cda66a'
         '01f1e76986b7bb4a1f1b7109fe05c46e'
         'b29503ba2b262035c7e2e5ed28986b0d'
         'b214709f096e4f50d61f50988359241e')
