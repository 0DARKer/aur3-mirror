# $Id: PKGBUILD 62268 2012-01-18 22:02:35Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168-pf
_pkgname=r8168
pkgver=8.029.00
pkgrel=3
pkgdesc="A kernel module for Realtek 8168 network cards (linux-pf kernel)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux-pf>=3.3' 'linux-pf<3.4')
makedepends=('linux-headers')
source=(http://r8168.googlecode.com/files/$_pkgname-$pkgver.tar.bz2 linux33.patch)
install=$pkgname.install

_extramodules=extramodules-3.3-pf
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
	cd "$srcdir/$_pkgname-$pkgver"
	patch -p0 -i "$srcdir/linux33.patch"
	make clean SYSSRC=/usr/src/linux-$_kernver modules
}

package() {
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/lib/modules/$_extramodules/$_pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

md5sums=('5dc15a976950250b7b543876cc3350a8'
         '1a68f5d207ab5fe86d758e5de44891cc')
sha256sums=('211a2994c9caf57321dafdb3acdfaa877f0b3f447798dc2abd2e417db3b0e515'
	'8d7f10de397a2cfd87afa601f42e7c48b3b3f8ea8d5e2f826d1d7cd587ecdeaf')