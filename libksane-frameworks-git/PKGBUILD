# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libksane-frameworks-git
pkgver=r389.6601e5c
pkgrel=1
pkgdesc='A image scanning library that provides a QWidget that contains all the logic needed to interface a sacanner. KF5 Frameworks branch. (GIT version)'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdegraphics/libs/libksane'
license=('LGPL')
depends=('kwallet' 'sane' 'ktextwidgets')
makedepends=('git' 'cmake' 'extra-cmake-modules')
conflicts=('libksane')
provides=('libksane')
source=('git://anongit.kde.org/libksane#branch=frameworks')
md5sums=('SKIP')
_gitname=libksane

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  rm -fr build
  mkdir -p build
}

build() {
  cd build
  cmake "../${_gitname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}