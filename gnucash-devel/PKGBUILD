# Maintainer: Ng Oon-Ee <n g  o o n  e e AT gmail dot com>
# Contributor: Carl Flippin <carlf@photocarl.org>

pkgname=gnucash-devel
_pkgname=gnucash
pkgver=2.4.0
pkgrel=1
pkgdesc="A personal and small-business financial-accounting application"
arch=('i686' 'x86_64')
url="http://www.gnucash.org"
license=("GPL")
depends=('gtkhtml' 'slib' 'goffice' 'guile' 'libofx' 'libgnomeui' 'hicolor-icon-theme' 'libdbi-drivers')
makedepends=('intltool') 
optdepends=('evince: for print preview'
            'gnucash-docs: (AUR) for user documentation'
            'perl-finance-quote: for stock information lookups'
            'perl-date-manip: for stock information lookups')
options=('!libtool' 'makeflags')
provides=("gnucash=$pkgver")
conflicts=('gnucash')
install=${pkgname}.install 
source=(http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-${pkgver}.tar.gz)
md5sums=('fb7b4ebebf26e8504ea08787db123d5e')
# source=(http://www.gnucash.org/pub/gnucash/sources/unstable/2.3.x/gnucash-$pkgver.tar.bz2)

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man --sysconfdir=/etc \
    --libexecdir=/usr/lib --enable-ofx --enable-dbi
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  install -m755 -d ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${_pkgname}.schemas --domain gnucash ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
