# Maintainer: Moritz Lipp <mlq@pwmt.org>
pkgname=gnucash-devel
_pkgname=gnucash
pkgver=2.5.4
pkgrel=1
pkgdesc="A personal and small-business financial-accounting application"
arch=('i686' 'x86_64')
url="http://www.gnucash.org"
license=("GPL")
groups=()
depends=('gconf>=2.22' 'glib2>=2.20' 'gtk2>=2.16' 'guile>=2.0.0' 'libart-lgpl'
'goffice>=0.5.1' 'libgsf' 'libxml2>=2.5.10' 'pango' 'swig' 'webkitgtk>=1.0'
'libofx' 'aqbanking>4.0.0' 'sqlite2' 'libdbi-drivers')
makedepends=('subversion' 'intltool')
provides=('gnucash')
conflicts=('gnucash')
options=('!libtool' '!makeflags' '!emptydirs')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-${pkgver}.tar.gz)
md5sums=('37c8c9cf99ace068c33a0bc39c541a22')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure \
    --disable-error-on-warning \
    --disable--schemas-install \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --enable-ofx \
    --enable-aqbanking
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  cd src/doc/design
  make DESTDIR="${pkgdir}" install-info

  install -dm755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnucash "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
