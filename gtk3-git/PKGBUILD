# Maintainer: Andrew Crerar <andrew (dot) crerar at gmail>
# Contributor: RKA KriK <rka_krik@mail.ru>
# Contributor: Boohbah <boohbah at gmail.com>

_name=gtk+
pkgname=gtk3-git
pkgver=3.13.7.r152.gb547d57
pkgrel=2
pkgdesc="GObject-based multi-platform GUI toolkit (GIT Version)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('at-spi2-atk' 'atk' 'cairo' 'colord' 'glib2-git>=2.39.0'
         'gtk-update-icon-cache' 'libcups' 'libxcomposite' 'libxcursor' 'libxdamage'
         'libxi' 'libxinerama' 'libxkbcommon' 'libxrandr' 'pango'
         'shared-mime-info' 'wayland-git')
makedepends=('gobject-introspection-git')
optdepends=('gnome-icon-theme: Default icon theme'
            'gnome-themes-standard: Default widget theme')
conflicts=('gtk3')
provides=("gtk3=${pkgver}")
backup=('usr/share/gtk-3.0/settings.ini')
install="gtk3-git.install"
source=('git://git.gnome.org/gtk+' 'settings.ini')
md5sums=('SKIP'
         '52bca1105d029c5142909b9e349bb89c')

pkgver() {
  cd "${srcdir}/${_name}"

  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_name}"

  ./autogen.sh --disable-schemas-compile \
               --enable-broadway-backend \
               --enable-gtk2-dependency \
               --enable-wayland-backend \
               --enable-x11-backend \
               --localstatedir=/var \
               --prefix=/usr \
               --sysconfdir=/etc

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/${_name}"

  make DESTDIR="$pkgdir" install

  install -Dm644 "../settings.ini" "$pkgdir/usr/share/gtk-3.0/settings.ini"
}

