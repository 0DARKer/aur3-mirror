# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: napa3um <napa3um@gmail.com>

pkgname=wxsqlite3
pkgver=3.0.6.1
pkgrel=1
pkgdesc="wxWidgets wrapper for SQLite3."
arch=('i686' 'x86_64')
url="http://wxcode.sourceforge.net/components/wxsqlite3/"
license=('custom:wxWindows')
depends=('wxgtk' 'sqlite3')
source=("http://downloads.sourceforge.net/wxcode/$pkgname-$pkgver.tar.gz")
md5sums=('ef98360f585a37456ba032a159fae9bf')

build() {
  cd $pkgname-${pkgver%.*}
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-${pkgver%.*}
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 LICENCE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
