# Maintainer : SpepS <dreamspepser at yahoo it>
# Contributor: napa3um <napa3um@gmail.com>

pkgname=wxsqlite3
pkgver=2.1.1
pkgrel=1
pkgdesc="wxWidgets wrapper for SQLite3."
arch=('i686' 'x86_64')
url="http://wxcode.sourceforge.net/components/wxsqlite3/"
license=('custom:wxWindows')
depends=('wxgtk' 'sqlite3')
source=("http://downloads.sourceforge.net/wxcode/$pkgname-$pkgver.tar.gz")
md5sums=('70c4f335ef5242a39f3d323121064c72')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # add missing linking flags
  export LDFLAGS="$LDFLAGS `wx-config --libs` -lwx_gtk2u_stc-2.8"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Install license
  install -Dm 644 LICENCE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
