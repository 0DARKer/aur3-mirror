# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jonathan Conder <jonno dot conder at gmail dot com>

_name=gtk-engine-murrine
pkgname=lib32-$_name
pkgver=0.98.2
pkgrel=1
pkgdesc="A Gtk2 glassy look engine using cairo. Multilib."
arch=('x86_64')
url="http://cimitan.com/murrine/project/murrine"
license=('GPL')
depends=('lib32-gtk2' "$_name")
makedepends=('gcc-multilib' 'intltool' 'perl')
options=('!libtool')
source=("http://ftp.gnome.org/pub/GNOME/sources/murrine/0.98/murrine-$pkgver.tar.xz") 
md5sums=('bf01e0097b5f1e164dbcf807f4b9745e')

build() {
  cd "$srcdir/murrine-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --enable-animation \
              --enable-animationrtl
  make
}

package() {
  cd "$srcdir/murrine-$pkgver"
  make DESTDIR="$pkgdir" install

  # remove useless
  rm -rf "$pkgdir/usr/share"
}
