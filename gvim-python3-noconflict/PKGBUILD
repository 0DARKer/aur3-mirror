# Maintainer: David Davis <davisd@davisd.com>

pkgname=('gvim-python3-noconflict')
_topver=7.4
_patchlevel=307
__hgrev=54d96e3b2eec
_versiondir="vim${_topver//./}"
pkgver=${_topver}.${_patchlevel}
pkgrel=3
arch=('i686' 'x86_64')
license=('custom:vim')
url="http://www.vim.org"
makedepends=('gpm' 'python2' 'python' 'ruby' 'libxt' 'desktop-file-utils' 'gtk2' 'lua')
source=("ftp://ftp.archlinux.org/other/vim/vim-${pkgver}.tar.xz"
        "ftp://ftp.archlinux.org/other/vim/vim-${pkgver}.tar.xz.sig"
        'launch-python-vim.sh'
        'launch-python-gvim.sh')
md5sums=('836c8b5bab883658c07b67cba445b667'
         'SKIP'
         '082ad36094590b6000bde94d8d7b6ba1'
         'f86ac8da219561f63994fd10835bc6d4')

# source PKGBUILD && mksource
mksource() {
  [[ -x /usr/bin/hg ]] || (echo "hg not found. Install mercurial." && return 1)

  __hgroot='http://vim.googlecode.com/hg/'
  __hgrepo='vim'
  __hgbranch='default'

  hg clone -b ${__hgbranch} -u ${__hgrev} "${__hgroot}${__hgrepo}" ${__hgrepo}

  pushd ${__hgrepo}
  if (( $(hg id -n) < $(hg id -nr ${__hgbranch}) )); then
    printf 'You are not building the latest revision!\n'
    printf "Consider updating __hgrev to $(hg id -r ${__hgbranch}).\n"
  fi
  popd

  mv vim gvim-python3-noconflict-${pkgver}
  find gvim-python3-noconflict-${pkgver} -depth -type d -name .hg -exec rm -rf {} \;
  rm gvim-python3-noconflict-${pkgver}/{.hgignore,.hgtags}
  tar -cJf gvim-python3-noconflict-${pkgver}.tar.xz gvim-python3-noconflict-${pkgver}/*
  rm -r gvim-python3-noconflict-${pkgver}

  gpg --detach-sign gvim-python3-noconflict-${pkgver}.tar.xz

  scp gvim-python3-noconflict-${pkgver}.tar.xz nym:/srv/ftp/other/vim/
  scp gvim-python3-noconflict-${pkgver}.tar.xz.sig nym:/srv/ftp/other/vim/
}

build() {
  cp -a vim-${pkgver} gvim-python3-build

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    gvim-python3-build/src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    gvim-python3-build/src/feature.h

  (cd gvim-python3-build/src && autoconf)

  cd "${srcdir}"/gvim-python3-build

  ./configure \
    --prefix=/opt/gvim-python3-noconflict \
    --localstatedir=/var/lib/vim \
    --with-features=huge \
    --with-compiledby='Arch Linux' \
    --enable-gpm \
    --enable-acl \
    --with-x=yes \
    --enable-gui=gtk2 \
    --enable-multibyte \
    --enable-cscope \
    --enable-netbeans \
    --enable-perlinterp \
    --disable-pythoninterp \
    --enable-python3interp \
    --enable-rubyinterp \
    --enable-luainterp

  make
}

check() {
  # disable tests because they seem to freeze

  cd "${srcdir}"/gvim-python3-build

  #make test
}

package_gvim-python3-noconflict() {
  pkgdesc='Vi Improved, compiled with Python 3 support that can co-exist with the Arch maintained Python 2 compiled gvim package'
  depends=("vim-runtime>=${pkgver}-${pkgrel}" 'gpm' 'ruby' 'libxt'
           'desktop-file-utils' 'gtk2' 'lua' 'python' 'gvim')
  conflicts=('gvim-python3')
  install=gvim.install

  cd "${srcdir}"/gvim-python3-build
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # python vim detection script
  install -Dm755 "${srcdir}"/launch-python-vim.sh \
    "${pkgdir}"/usr/bin/launch-python-vim.sh

  # python gvim detection script
  install -Dm755 "${srcdir}"/launch-python-gvim.sh \
    "${pkgdir}"/usr/bin/launch-python-gvim.sh
}

