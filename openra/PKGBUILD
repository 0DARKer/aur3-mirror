# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Matthew Bowra-Dean <matthew@ijw.co.nz>
pkgname=openra
pkgver=20110511
pkgrel=1
pkgdesc="An open-source implementation of the Red Alert engine using .NET/mono and OpenGL"
arch=('any')
url="http://open-ra.org"
license=('GPL3')
install=openra.install
depends=('mono' 'openal' 'mesa' 'freetype2' 'sdl')
makedepends=('git' 'unzip')
optdepends=('nvidia-cg-toolkit: for using the alternative Cg renderer')
options=(!strip)
source=("https://github.com/OpenRA/OpenRA/zipball/release-$pkgver")
md5sums=('2aec16f1de5d34d4d19224cdea5edd94')

build() {
    unzip -f release-$pkgver
    cd OpenRA-OpenRA-*

    sed -i "s|prefix = .*|prefix = /usr|g" Makefile
    make
}

package() {
    cd OpenRA-OpenRA-*

    mkdir -p "$pkgdir/usr/share/applications"
    sed "s/{VERSION}/$pkgver/g" packaging/linux/openra.desktop > "$pkgdir/usr/share/applications/openra.desktop"

    mkdir -p "$pkgdir/usr/share/icons/"
    cp -r packaging/linux/hicolor "$pkgdir/usr/share/icons/"

    install -Dm755 packaging/linux/OpenRA.Utility.sh "$pkgdir/usr/share/openra/OpenRA.Utility.sh"

    make DESTDIR="$pkgdir" install

    find "$pkgdir" -iname \*.mdb -delete

    mv "$pkgdir/usr/share/openra/thirdparty/Tao/"* "$pkgdir/usr/share/openra/"
    rm -r "$pkgdir/usr/share/openra/thirdparty"
}
