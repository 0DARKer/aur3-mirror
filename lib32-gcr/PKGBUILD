# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gcr
pkgname=lib32-${_pkgbase}
pkgver=3.4.1
pkgrel=1
pkgdesc="A library for bits of crypto UI and parsing (32 bit)"
arch=(x86_64)
url="http://www.gnome.org"
license=('GPL2')
depends=('lib32-gtk3' 'lib32-libgcrypt' 'lib32-p11-kit' "${_pkgbase}")
makedepends=('intltool' 'gcc-multilib') # 'gobject-introspection'
options=(!libtool)
install=gcr.install
_ver=${pkgver%.*}
source=(http://download.gnome.org/sources/${_pkgbase}/${_ver}/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('a0812b6e9623465b0053f4b1b09f3b9316317b2c6ed69a73bc2c5b3b6b849609')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr \
    --disable-static \
    --disable-update-mime \
    --disable-schemas-compile \
    --libdir=/usr/lib32 \
    --libexec=/usr/lib32/gcr \
    --enable-introspection=no
  make
}

package() {
  cd "${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

