# Maintainer: Haskellfant <moritz.kiefer<at>purelyfunctional<dot>org>

pkgname=carla-git
pkgver=2308.f35431d
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=('i686' 'x86_64')
url="http://kxstudio.sf.net/carla"
license=('GPL2')
conflicts=('carla')
provides=('carla')
depends=("python-pyqt4"
         "fftw"
         "fluidsynth"
         "gtk2"
         "gtk3"
         "liblo"
         "linuxsampler-svn"
         "mxml"
         "ntk-git"
         "qt5-base")
makedepends=('git')
optdepends=("pygtk: NekoFilter UI"
            "zlib: extra native plugins"
            "zynaddsubfx-git: zynaddsubfx banks")
source=("$pkgname"::'git://github.com/falkTX/Carla.git')
md5sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir/" PREFIX=/usr install
}
