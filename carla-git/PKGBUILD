# Maintainer: Haskellfant <moritz.kiefer<at>purelyfunctional<dot>org>

pkgname=carla-git
pkgver=2280.64b8803
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=('i686' 'x86_64')
url="http://kxstudio.sf.net/carla"
license=('GPL2')
conflicts=('carla')
provides=('carla')
depends=("python-pyqt4"
         "python-pyliblo"
         "ntk-git"
         "libsmf"
         "fftw"
         "gtk3"
         "gtk2"
         "mxml"
         "python2"
         "fluidsynth"
         "linuxsampler-svn")
makedepends=('git')
optdepends=("glu: extra native plugins"
	        "zlib: extra native plugins"
            "pygtk: NekoFilter UI"
            "zynaddsubfx-git: zynaddsubfx banks"
            "ffmpeg: extra native plugins")
source=("$pkgname"::'git://github.com/falkTX/Carla.git')
md5sums=('SKIP')
install="$pkgname.install"

pkgver() {
  cd "$srcdir/$pkgname"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir/" PREFIX=/usr install
}
