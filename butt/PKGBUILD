# Maintainer: goll <adrian.goll+aur[at]gmail>
# Contributor: Kosava <kosava@gmail.com>

pkgname=butt
pkgver=0.1.13
pkgrel=1
pkgdesc="butt (broadcast using this tool) is an easy to use, multi OS streaming tool"
arch=('i686' 'x86_64')
license=("GPL2")
url="http://butt.sourceforge.net/"
depends=('fltk' 'libpng12')
optdepends=('icecast' 'shoutcast')
source=("http://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
sha1sums=('cb0581f6c76a3955ed19f332b58fcb83a360fc32')

build() {
	cd "$pkgname-$pkgver"
	sed -i 's|Fl/fl|FL/fl|' src/FLTK/fl_funcs.h
	./configure --prefix=/usr
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
