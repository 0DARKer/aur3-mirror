# Contributor: Thomas Dziedzic < gostrc at gmail >
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=sawfish-git
pkgver=20101127
pkgrel=1
pkgdesc='A window manager extensible using a Lisp-based scripting language.'
arch=('i686' 'x86_64')
url='http://sawfish.wikia.com'
license=('GPL')
depends=('gtk2' 'libsm')
makedepends=('git' 'rep-gtk-git' 'gettext')
provides=('sawfish')
conflicts=('sawfish')
install=sawfish.install
options=('libtool')
source=()
md5sums=()

_gitroot='git://git.gnome.org/sawfish'
_gitname='sawfish'

build() {
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    (cd $_gitname && git pull origin)
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf $_gitname-build
  cp -r $_gitname $_gitname-build
  cd $_gitname-build

  ./autogen.sh 

  ./configure --prefix=/usr --with-nls 
  make 
}
package() {
  cd $_gitname-build
  make DESTDIR="$pkgdir" install 
} 
