# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: William Di Luigi <williamdiluigi@gmail.com>

pkgname=javafx-scene-builder-devel
_major=2.0
_build=b13
_date=06_feb_2014
pkgver=$_major$_build
pkgrel=1
pkgdesc="A visual layout tool that lets users quickly design JavaFX application user interfaces, without coding."
arch=('i686' 'x86_64')
url="http://www.oracle.com/technetwork/java/javafx/tools/index.html"
license=('custom')
depends=('jdk-devel')
provides=('javafx-scene-builder')
conflicts=('javafx-scene-builder')

md5sums=('13a34a6259dc124ee7454bc3cb8be070'
         '2418cfea62849bee8db1c02962ae2705'
         'ea92247d9cb86358e2de880ef09f1428'
         '374fdcfa8037bccd2e189f78a2b2322d')

_arch="x64"
if [ "${CARCH}" != "x86_64" ]; then
  _arch="i586"
  md5sums[0]="2a1046f4729d43e1ffbe845e2998963e"
fi

source=("http://download.oracle.com/otn-pub/java/javafx_scenebuilder/${_major}-ea-${_build}/javafx_scenebuilder-${_major/./_}-ea-${_build}-linux-${_arch}-${_date}.tar.gz"
        'javafx-scene-builder.desktop'
        'javafx-scene-builder.png'
        'LICENSE')

DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -O --header "Cookie: gpw_e24=h"')

package() {
  install -d "${pkgdir}"/opt
  cp -r JavaFXSceneBuilder${_major} "${pkgdir}"/opt/JavaFXSceneBuilder

  install -D -m644 "${srcdir}"/javafx-scene-builder.desktop \
    "${pkgdir}"/usr/share/applications/javafx-scene-builder.desktop
  install -D -m644 "${srcdir}"/javafx-scene-builder.png \
    "${pkgdir}"/usr/share/pixmaps/javafx-scene-builder.png

  install -d "${pkgdir}"/usr/bin
  ln -s /opt/JavaFXSceneBuilder/JavaFXSceneBuilder${_major} \
    "${pkgdir}/usr/bin/JavaFXSceneBuilder"

  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install "${srcdir}"/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}
}
