# Maintainer: Harley Laue <losinggeneration@gmail.com>
pkgname=oolua
pkgver=1.3.1
pkgrel=3
pkgdesc="A C++ binding for Lua, which is intended to ease the embedding of Lua in C++ allowing easy access to tables, functions to be called and types to be pushed and pulled from the stack; also C++ in Lua by binding functions, types and class hierarchies."
arch=(i686 x86_64)
url="http://code.google.com/p/oolua/"
license=('MIT')
depends=(gcc-libs)
makedepends=(gmock premake4 tar)
source=(http://oolua.googlecode.com/files/$pkgname-release-$pkgver.tar.gz)
# it doesn't extract to a subdir
noextract=($pkgname-release-$pkgver.tar.gz)
md5sums=('9c2c1b2f9699344079bd5ab633cb55e6')

build() {
  # because it doesn't extract to a subdir, we must do it ourselves
  [ -d "$srcdir/$pkgname-$pkgver" ] && rm -fr $srcdir/$pkgname-$pkgver
  mkdir -p $srcdir/$pkgname-$pkgver

  cd "$srcdir/$pkgname-$pkgver"
  tar xf ../$basedir/$pkgname-release-$pkgver.tar.gz

  # and proceed normally from here
  cd "$srcdir/$pkgname-$pkgver"

  premake4 gmake
  # We only really want the library and file generator
  make -j1 config=release oolua file_generator
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install by hand creating the directories we need since there is no
  # make install
  mkdir -p $pkgdir/usr/{bin,lib,include/oolua,share/licenses/$pkgname}
  cp bin/Release/file_generator $pkgdir/usr/bin/${pkgname}_file_generator
  cp bin/Release/liboolua.a $pkgdir/usr/lib
  cp include/*.h $pkgdir/usr/include/oolua
  cp licence.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
