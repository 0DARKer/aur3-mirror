# Contributor: Frank Ickstadt (frank dot ickstadt at gmail dot com)
# for argyllcms 1.3.3 color management system

# Note for rebuilders:
# The name of the file to be downloaded is always the same!
# So if you are a bad boy (Santa knows everything!) and build as root, this file
# will be saved in /var/cache/pacman/src . Therefore, rebuilding a later version
# likely will fail due to an MD5 error. If so, delete the files from the cache
# to force a new download.

pkgname=argyllcms
_pkgname=Argyll
pkgver=1.3.3
_pkgver="V${pkgver}"
pkgrel=1
pkgdesc="An ICC compatible color management system with support for different colorimeter hardware"
arch=(i686 x86_64)
depends=('libxss' 'libx11' 'libxinerama' 'libxxf86vm' 'libxrandr')
makedepends=('ftjam' 'zip' 'unzip')
source=(http://www.argyllcms.com/${_pkgname}_${_pkgver}_src.zip)
url="http://www.argyllcms.com/"
license=("GPL" "AGPL")
install=${pkgname}.install
md5sums=('828d022e76bae7d7db5f521ce3910535')

build() {
  cd ${srcdir}/${_pkgname}_${_pkgver}
  sh makeall.sh
  sh makeinstall.sh
  sh makepackagebin.sh
  rm ${srcdir}/${_pkgname}_${_pkgver}/bin/License.txt
  rm ${srcdir}/${_pkgname}_${_pkgver}/ref/afiles
  rm ${srcdir}/${_pkgname}_${_pkgver}/doc/afiles
  mkdir -p ${pkgdir}/usr/bin
  install -m755 ${srcdir}/${_pkgname}_${_pkgver}/bin/* ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/share/${pkgname}/ref
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/ref/* ${pkgdir}/usr/share/${pkgname}/ref
  mkdir -p ${pkgdir}/usr/share/${pkgname}/profile
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/profile/*.sp ${pkgdir}/usr/share/${pkgname}/profile
  mkdir -p ${pkgdir}/usr/share/${pkgname}/scanin
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/scanin/*.cht ${pkgdir}/usr/share/${pkgname}/scanin
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/scanin/*.cie ${pkgdir}/usr/share/${pkgname}/scanin
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/scanin/*.ti2 ${pkgdir}/usr/share/${pkgname}/scanin
  mkdir -p ${pkgdir}/usr/share/${pkgname}/target
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/target/*.ti2 ${pkgdir}/usr/share/${pkgname}/target
  mkdir -p ${pkgdir}/usr/share/${pkgname}/spectro
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/spectro/*.sp ${pkgdir}/usr/share/${pkgname}/spectro
 # Because there is just html documentation we have to move it
  mkdir -p ${pkgdir}/usr/share/${pkgname}/doc/ccmxs
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/doc/ccmxs/*.ccmx ${pkgdir}/usr/share/${pkgname}/doc/ccmxs
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/doc/*.* ${pkgdir}/usr/share/${pkgname}/doc
  mkdir -p ${pkgdir}/etc/udev/rules.d
  install -m644 ${srcdir}/${_pkgname}_${_pkgver}/libusb/55-Argyll.rules ${pkgdir}/etc/udev/rules.d/55-Argyll.rules
}

