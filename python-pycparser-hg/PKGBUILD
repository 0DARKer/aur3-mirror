# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: lang2 <wenzhi.liang@gmail.com>

pkgname=python-pycparser-hg
pkgver=165.ca1c1e9dc67f
pkgrel=1
pkgdesc='C parser and AST generator written in Python'
url='https://bitbucket.org/eliben/pycparser'
makedepends=('mercurial')
depends=('python' 'python-ply')
conflicts=('pycparser' 'python-pycparser')
replaces=('pycparser' 'python-pycparser')
arch=('x86_64' 'i686')
license=('BSD')
source=('pycparser::hg+https://bitbucket.org/eliben/pycparser/')
sha256sums=('SKIP')

pkgver() {
  cd pycparser

  echo $(hg identify -n).$(hg identify -i)
}

prepare() {
  cd pycparser

  # generate lextab.py and yacctab.py
  python -c 'import pycparser; pycparser.CParser()'
  mv lextab.py yacctab.py pycparser
}

build() {
  cd pycparser

  python setup.py build
}

package() {
  cd pycparser

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
