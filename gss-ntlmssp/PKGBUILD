pkgname='gss-ntlmssp'
pkgver='0.3.1'
_pkgtag='commit=6ff0b3ee4049bdfefeca57f06625d93095fbf5a5'
pkgrel=1
pkgdesc="GSSAPI mechanism plugin for NTLMSSP, NTLM, NTLMv2"
url="https://fedorahosted.org/$pkgname/"
arch=(i686 x86_64)
license=(GPL3)
source=("git://git.fedorahosted.org/git/$pkgname.git#$_pkgtag")
sha1sums=('SKIP')
options=(!emptydirs)

prepare() {
  cd "$srcdir/$pkgname"
  mkdir -p m4 # optional, but quiets a warning
  autoreconf -f -i
}

build() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr
  make
  sed -i 's:\${exec_prefix}:/usr:' examples/mech.ntlmssp
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
  doc="$pkgdir/usr/share/doc/pkgname"
  install -Dm0644 README.txt \
                  "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm0644 examples/mech.ntlmssp \
                  "$pkgdir/usr/share/doc/$pkgname/mech.ntlmssp"
}

# vim: ts=2:sw=2:et:nowrap
