# Contributor: Hinrich Harms <arch@hinrich.de>
# Contributor: Flamelab <panosfilip@gmail.com>
# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=mutter
pkgname=$_pkgname-git
pkgver=3.11.2.39.g1db95bc
pkgrel=1
pkgdesc="The next-gen WM for gnome, with compositing by clutter"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=("gtk3>=3.9.11" "cogl>=1.15.6" 'clutter' 'dconf' 'gobject-introspection' 'gsettings-desktop-schemas'
         'libcanberra' 'startup-notification' 'zenity' 'libsm' "upower>=0.99.0")
makedepends=('git' 'intltool' 'gnome-doc-utils' 'gtk-doc')
provides=("mutter=3.11.2")
conflicts=('mutter')
groups=('gnome')
options=('!libtool' '!emptydirs')
install=$_pkgname.install
source=('git://git.gnome.org/mutter')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/mutter \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile --enable-compile-warnings=minimum \
      --enable-gtk-doc

  make

}

package()  {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir/" install
}
