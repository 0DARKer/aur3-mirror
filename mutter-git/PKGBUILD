# Contributor: Hinrich Harms <arch@hinrich.de>
# Contributor: Flamelab <panosfilip@gmail.com>
# Maintainer: Ner0

pkgname=mutter-git
pkgver=20120707
pkgrel=1
pkgdesc="The next-gen WM for gnome, with compositing by clutter"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('clutter' 'dconf' 'gobject-introspection' 'gsettings-desktop-schemas' 'libcanberra' 'startup-notification' 'zenity')
makedepends=('git' 'intltool' 'gnome-doc-utils')
provides=('mutter')
conflicts=('mutter')
groups=('gnome')
options=('!libtool' '!emptydirs')
install=$pkgname.install

_gitroot=git://git.gnome.org/mutter
_gitname=mutter


build() {
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin && cd ..
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  [ -d "$_gitname-build" ] && rm -rf "$_gitname-build"
  git clone "$_gitname" "$_gitname-build"
  cd "$_gitname-build"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/mutter \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile

  make ${MAKEFLAGS}

}

package()  {
  cd "$srcdir/$_gitname-build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

}
