# Maintainer: cars10t <carsten.1 gmx net>

_pkgbase=dunelegacy
pkgname=$_pkgbase-git
_gitname=dunelegacy
pkgver=v0.96.3.4.g68ca4b3
pkgrel=1
pkgdesc="Updated clone of Westood Studios' Dune2 (Git version)"
arch=('i686' 'x86_64')
url="http://dunelegacy.sf.net"
license=('GPL2')
depends=('sdl_mixer' 'desktop-file-utils')
makedepends=('git')
conflicts=('dunelegacy')
provides=('dunelegacy')
install="$_pkgbase.install"
source=("$_gitname::git://dunelegacy.git.sourceforge.net/gitroot/dunelegacy/dunelegacy")
# Because the sources are not static, skip Git checksum:
md5sums=('SKIP')

pkgver() {
  cd $_gitname

  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname

  autoreconf --install
  ./configure --prefix=/usr
  make
}

package() {
  cd $_gitname

  make DESTDIR=$pkgdir install
  
  # install icon and desktop files
  install -Dm644 "$_pkgbase.png" "$pkgdir/usr/share/pixmaps/$_pkgbase.png"
  install -Dm644 "$_pkgbase.desktop" "$pkgdir/usr/share/applications/$_pkgbase.desktop"
}
