# Maintainer: carstene1ns <carsten.1 gmx net>

_pkgbase=dunelegacy
pkgname=$_pkgbase-git
pkgver=v0.96.3.8.g319cb34
pkgrel=1
pkgdesc="Updated clone of Westood Studios' Dune2 (Git version)"
arch=('i686' 'x86_64')
url="http://dunelegacy.sf.net"
license=('GPL2')
depends=('sdl_mixer' 'desktop-file-utils')
makedepends=('git')
conflicts=('dunelegacy')
provides=('dunelegacy')
install=$_pkgbase.install
source=("$_pkgbase::git://dunelegacy.git.sourceforge.net/gitroot/dunelegacy/dunelegacy")
md5sums=('SKIP')

pkgver() {
  cd $_pkgbase

  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_pkgbase

  autoreconf --install
  ./configure --prefix=/usr
  make
}

package() {
  cd $_pkgbase

  make DESTDIR=$pkgdir install

  # install icon and desktop files
  install -Dm644 "$_pkgbase.png" "$pkgdir/usr/share/pixmaps/$_pkgbase.png"
  install -Dm644 "$_pkgbase.desktop" "$pkgdir/usr/share/applications/$_pkgbase.desktop"
}
