# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

#######################
#[ 3rd Party Plugins ]#
_pausewebui=1
#_ratiocolor=1
#_instantsearch=1
#_logoff=1
#_filemanager=1
#_fileshare=1
#_fileupload=1
#_mediastream=1
#_chat=1
#_titlebar=1
#_spectrogram=1
#######################

pkgname=('rutorrent-git')
pkgver=20150219
pkgrel=1
pkgdesc="Web frontend to rTorrent in PHP designed to resemble uTorrent"
url="https://github.com/Novik/ruTorrent"
license=('GPL')
arch=('any')

install=rutorrent.install
options=(!strip)

depends=('php' 'curl')
makedepends=('git')
optdepends=('mktorrent: create torrent files with mktorrent'
            'buildtorrent: create torrent files with buildtorrent'
            'transmission-cli: create torrent files with transmission'
            'php-geoip: enable geoip plugin'
            'geoip: enable geoip plugin'
            'unrar: enable unpack plugin'
            'unzip: enable unpack plugin')

conflicts=('rutorrent' 'rutorrent-plugins')
replaces=('rutorrent-svn' 'rutorrent-plugins-svn')
provides=('rutorrent' 'rutorrent-plugins')

_webdir="usr/share/webapps"
backup=("${_webdir}/rutorrent/conf/config.php"
        "${_webdir}/rutorrent/conf/access.ini"
        "${_webdir}/rutorrent/conf/plugins.ini")

source=(
        "plugins.ini"
        "https://raw.githubusercontent.com/weixiyen/jquery-filedrop/master/jquery.filedrop.js"
        "rutorrent::git+https://github.com/Novik/ruTorrent.git"

        "ratiocolor::git+https://github.com/Gyran/rutorrent-ratiocolor.git"
        "titlebar::git+https://github.com/SanKen/rutorrent-titlebar.git"
        "pausewebui::svn+https://rutorrent-pausewebui.googlecode.com/svn/trunk"
        "logoff::svn+https://rutorrent-logoff.googlecode.com/svn/trunk"
        "instantsearch::svn+https://rutorrent-instantsearch.googlecode.com/svn/trunk"
        "filemanager::svn+http://svn.rutorrent.org/svn/filemanager/trunk/filemanager"
        "fileshare::svn+http://svn.rutorrent.org/svn/filemanager/trunk/fileshare"
        "fileupload::svn+http://svn.rutorrent.org/svn/filemanager/trunk/fileupload"
        "mediastream::svn+http://svn.rutorrent.org/svn/filemanager/trunk/mediastream"
        "spectrogram::svn+http://svn.rutorrent.org/svn/spectrogram/trunk"
        "chat::svn+https://rutorrent-chat.googlecode.com/svn/trunk"
        )

md5sums=('82505fc57897d3b8dde162eb2a7856d7'
         '020b0c3fc1220d490992473a75485add'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

pkgver() {
    cd "$srcdir/rutorrent"
    git log -1 --format="%cd" --date=short |tr -d '-'
}

prepare() {
    cd "$srcdir/rutorrent"
    sed -i conf/config.php \
        -e "s:\$topDirectory .*:\$topDirectory = '/home';:" \
        -e "s:\$XMLRPCMountPoint .*:\$XMLRPCMountPoint = \"/rutorrent/RPC1\";:" \
        -e "s:\$tempDirectory .*:\$tempDirectory = '/${_webdir}/rutorrent/tmp';:"
    sed -i php/settings.php \
        -e "s:'/tmp:'/${_webdir}/rutorrent/tmp/:"
}

build() {
    cd "$srcdir/rutorrent/plugins"

    [[ $_pausewebui    ]] && cp -r "$srcdir/pausewebui" .
    [[ $_logoff        ]] && cp -r "$srcdir/logoff" .
    [[ $_instantsearch ]] && cp -r "$srcdir/instantsearch" .
    [[ $_filemanager   ]] && cp -r "$srcdir/filemanager" .
    [[ $_fileshare     ]] && cp -r "$srcdir/fileshare" .
    [[ $_fileupload    ]] && cp -r "$srcdir/fileupload" .
    [[ $_mediastream   ]] && cp -r "$srcdir/mediastream" .
    [[ $_spectrogram   ]] && cp -r "$srcdir/spectrogram" .
    [[ $_chat          ]] && cp -r "$srcdir/chat" .
    [[ $_ratiocolor    ]] && cp -r "$srcdir/ratiocolor" .
    [[ $_titlebar      ]] && cp -r "$srcdir/titlebar" .

    cp -f "$srcdir/jquery.filedrop.js" filedrop/jquery.filedrop.js

}

package() {
    install -dm755 "$pkgdir/$_webdir"
    cd "$pkgdir/$_webdir"

    cp -r "$srcdir/rutorrent" .
    rm -r rutorrent/{.git*,README*}

    install -Dm755 "$srcdir/plugins.ini" rutorrent/conf/plugins.ini
    install -dm777 rutorrent/tmp
}
