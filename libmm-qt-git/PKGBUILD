# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmm-qt-git
pkgver=40.d843013
pkgrel=1
pkgdesc='Qt-only wrapper for ModemManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libmm-qt'
license=('LGPL')
depends=('qt4')
makedepends=('cmake' 'git')
conflicts=('libmm-qt')
provides=('libmm-qt')
source=('git://anongit.kde.org/libmm-qt')
md5sums=('SKIP')

pkgver() {
  cd libmm-qt
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libmm-qt \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
