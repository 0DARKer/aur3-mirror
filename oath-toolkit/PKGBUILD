# Contributor: L42y <423300@gmail.com>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=oath-toolkit
pkgver=1.10.1
pkgrel=1
pkgdesc="The OATH Toolkit attempts to collect several tools that are useful when deploying technologies related to OATH"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/oath-toolkit/"
license=('GPL')
depends=('pam')
source=(http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # PAM doesn't look here anyway
  rm -rf $pkgdir/usr/lib/security
  install -D -m 755 pam_oath/.libs/pam_oath.so $pkgdir/lib/security/pam_oath.so

  # Install basic docs too
  install -D -m 644 pam_oath/README $pkgdir/usr/share/doc/oath-toolkit/PAM_README
  install -D -m 644 README $pkgdir/usr/share/doc/oath-toolkit/README
}
md5sums=('4ebea1bc5000f05df4335e720257c2d7')
