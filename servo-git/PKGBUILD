pkgname=servo-git
pkgver=6867.470d27a
pkgrel=1
pkgdesc='Parallel Browser Project: web browser written in Rust'
arch=('x86_64' 'i686')
url="https://github.com/mozilla/servo"
license=('MPL')
depends=('freetype2' 'mesa' 'libxrandr' 'libxi' 'libgl' 'glu' 'fontconfig')
makedepends=('git' 'autoconf2.13' 'curl' 'python2' 'gperf')
provides=('servo')
conflicts=('servo')
source=('git+https://github.com/servo/servo.git')

md5sums=('SKIP')


pkgver() {
	cd $srcdir/servo
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
	cd $srcdir/servo

	# fixes build error
	# possibly _FORTIFY_SOURCE? https://bugs.archlinux.org/task/34759
	unset CPPFLAGS

	./mach build
}

package() {
  install -Dm755 "$srcdir/servo/target/servo" "$pkgdir/usr/bin/servo"

  mkdir -p "$pkgdir/usr/lib"

  find $srcdir/servo/target/deps -name "*-*.so" -exec basename {} \; | sort | uniq | while read _f; do
	  _file=$(find $srcdir/servo/target/deps -name "$_f" -print | head -n 1)
	  if [ -z "$_file" ]; then
		  echo "Skipping: $_f"
		  continue
	  fi
	  install -Dm644 "$_file" "$pkgdir/usr/lib"
  done
}
