# Maintainer: Babets
# Contributor:  Leslie P. Polzer <polzer at stardawn dot org>
_pkgbasename=minizip
pkgname=lib32-minizip
pkgver=1.2.7
pkgrel=1
pkgdesc="ZIP file extraction library (32-bit)"
url="http://www.winimage.com/zLibDll/minizip.html"
license=('ZLIB' 'custom')
arch=('x86_64')
makedepends=('gcc-multilib' 'libtool-multilib')
options=(!libtool)
depends=('lib32-glibc' "$_pkgbasename" 'lib32-zlib')
source=("http://zlib.net/current/zlib-${pkgver}.tar.gz"
        LICENSE)
md5sums=('60df6a37c56e7c1366cca812414f7b85'
         '66b8119e37a80d8bc9d7b1bac59d7d8e')
sha1sums=('4aa358a95d1e5774603e6fa149c926a80df43559'
          'ad1e3fb070e1b63ae5350d9bb9607a79ecc0a5cd')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/zlib-$pkgver/contrib/$_pkgbasename
  rm Makefile
  autoreconf -i
  autoconf
  automake --add-missing
  automake
  ./configure --prefix=/usr --libdir=/usr/lib32

  make
}

package() {
  cd $srcdir/zlib-$pkgver/contrib/$_pkgbasename
  make install DESTDIR=${pkgdir}

  rm -rf $pkgdir/usr/{include,share}
  install -d $pkgdir/usr/share/licenses
  ln -s $_pkgbasename $pkgdir/usr/share/licenses/$pkgname
}
