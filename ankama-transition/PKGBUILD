# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
pkgname=ankama-transition
pkgver=3.8.1
pkgrel=3
pkgdesc="Updater for Ankama games Dofus and Wakfu"
arch=('i686' 'x86_64')
url="http://www.ankama.com"
license=('custom')
depends=('qt4' 'polkit')
source=('http://dl.ak.ankama.com/games/linux/ankama-transition.tar.gz'
        'transition.sh')
md5sums=('cff556afa09d994c63002a16e307038b'
         '45cf6884aba12f2a9a024ea62f57854c')
install='ankama-transition.install'

_archdir() {
  case "$CARCH" in
    'i686')
      echo "$srcdir/x86"
      ;;
    'x86_64')
      echo "$srcdir/amd64"
      ;;
  esac
}

prepare() {
  sed -i 's:$$${installation_path}:/opt/ankama:' \
      "$srcdir/usr/share/polkit-1/actions/com.ankama.transitionupdateservice.policy"
}

package() {
  cd "$srcdir/"
  cp -r 'etc' 'usr' "$(_archdir)/opt" "$pkgdir/"
  chgrp -R games "$pkgdir/opt/ankama/transition"
  chmod -R g+w "$pkgdir/opt/ankama/transition"
  install -Dm755 'transition.sh' "$pkgdir/usr/bin/transition"
  install -Dm644 'opt/ankama/transition/licences/README.txt' \
          "$pkgdir/usr/share/licenses/ankama-transition/licenses.txt"
}
