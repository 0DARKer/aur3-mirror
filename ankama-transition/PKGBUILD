# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
pkgname=ankama-transition
pkgver=3.9.2
pkgrel=1
pkgdesc="Updater for Ankama games Dofus and Wakfu"
arch=('i686' 'x86_64')
url="http://www.ankama.com"
license=('custom')
depends=('qt4' 'polkit' 'openssl')
# The 32-bit transition in ankama-transition will fail with error "mandatory plugin core",
# so we use the transition bundled in Dofus installers
source=('DofusInstall-amd64.tar.gz::http://download.dofus.com/full/linux/x64'
        'DofusInstall-x86.tar.gz::http://download.dofus.com/full/linux/'
        'http://dl.ak.ankama.com/games/linux/ankama-transition.tar.gz'
        'transition.sh')
md5sums=('9c34ea79de9b325af4b06ddf2d9c4c2a'
         '9d8262f579844d6b8f025c417a922ac1'
         '33683756fbe9ea7dd0b33b2df86d1125'
         '45cf6884aba12f2a9a024ea62f57854c')
# Skip this block if PKGBUILD is sourced by updpkgsums (pstree is used for this check,
# and if not available, this check is skipped)
if ! ( command -v pstree >/dev/null 2>&1 && \
  pstree --show-parents $$ | grep -q updpkgsums ) ; then
  # Remove source and md5sum for the wrong arch
  if [ "$CARCH" == "x86_64" ];then
    unset source[1] md5sums[1]
  elif [ "$CARCH" == 'i686' ]; then
    unset source[0] md5sums[0]
  fi
fi

install='ankama-transition.install'

prepare() {
  sed -i 's:$$${installation_path}:/opt/ankama:' \
      "$srcdir/usr/share/polkit-1/actions/com.ankama.transitionupdateservice.policy"
}

package() {
  cd "$srcdir/"
  cp -r 'etc' 'usr' "$pkgdir/"

  _installdir="$pkgdir/opt/ankama/transition"
  install -d "$_installdir"
  cp -r Dofus/transition/* "$_installdir"
  chgrp -R games "$_installdir"
  chmod -R g+w "$_installdir"

  install -Dm755 'transition.sh' "$pkgdir/usr/bin/transition"
  install -Dm644 'opt/ankama/transition/licences/README.txt' \
          "$pkgdir/usr/share/licenses/ankama-transition/licenses.txt"
}
