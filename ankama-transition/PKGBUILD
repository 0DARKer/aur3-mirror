# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
pkgname=ankama-transition
pkgver=3.9.2
pkgrel=1
pkgdesc="Updater for Ankama games Dofus and Wakfu"
arch=('i686' 'x86_64')
url="http://www.ankama.com"
license=('custom')
depends=('qt4' 'polkit' 'openssl')
# The 32-bit transition in ankama-transition will fail with error "mandatory plugin core",
# so we use the transition bundled in Dofus installers
source=('http://dl.ak.ankama.com/games/linux/ankama-transition-release.tar.gz'
        'transition.sh')
md5sums=('b8e089e6be7d379d9942f8c7d6f993b4'
         '5d2319dd2220195a0eb9371f09092f52')
install='ankama-transition.install'

prepare() {
  ln -sf "$srcdir/i386" i686
  ln -sf "$srcdir/amd64" x86_64
  sed -i 's:$$${installation_path}:/opt/ankama:' \
      "$srcdir/usr/share/polkit-1/actions/com.ankama.transitionupdateservice.policy"
}

package() {
  cd "$srcdir/"
  cp -r 'etc' 'usr' 'opt' "$pkgdir/"
  cp -r "$CARCH/opt"/* "$pkgdir/opt"

  _installdir="$pkgdir/opt/ankama/transition"
  chgrp -R games "$_installdir"
  chmod -R g+w "$_installdir"

  install -Dm755 'transition.sh' "$pkgdir/usr/bin/transition"
  install -Dm644 'opt/ankama/transition/licences/README.txt' \
          "$pkgdir/usr/share/licenses/ankama-transition/licenses.txt"
}
