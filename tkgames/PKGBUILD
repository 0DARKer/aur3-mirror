# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=tkgames
pkgver=1.1.1
pkgrel=2
pkgdesc="Games written in tcl/tk: Polypuzzle, Hearts, Spider, Yahtzee and others"
arch=('any')
url="http://tkgames.sourceforge.net/"
license=('GPL')
depends=('tk')
optdepends=('bash: for some scripts')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tgz)
md5sums=('7b0b8b1f448ef47248b28a62e6cda02b')

_games=('tkhearts-0.91' 'tkspider-1.06' 'tktk-0.97' 'tktp-0.82' 'tkyahtzee-1.2.4'
        'polypuzzle/polypuzzle' 'tkhangman/tkhangman' 'tksol/tksol' 'tksol/tksol_green')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  # some fixes
  chmod 755 tkhangman
  sed -i "s,/usr/local/lib,/usr/share/$pkgname,g" \
    polypuzzle/polypuzzle tkhangman/tkhangman tksol/tksol
  sed -i "s,\[pwd\],/usr/share/$pkgname/tksol,g" \
    tksol/tksol_green

  # install launchers
  for _source in ${_games[*]}; do
    _dest=${_source#*/} && _dest=${_dest%-*}
    install -Dm755 $_source "$pkgdir"/usr/bin/$_dest
  done

  # install data
  rm -f polypuzzle/polypuzzle
  mkdir -p "$pkgdir"/usr/share/$pkgname/{tkhangman,tksol}
  cp -rf -t "$pkgdir"/usr/share/$pkgname           polypuzzle
  cp -rf -t "$pkgdir"/usr/share/$pkgname/tkhangman tkhangman/{pictures,words}
  cp -rf -t "$pkgdir"/usr/share/$pkgname/tksol     tksol/{cardbacks,cards,pictures,*.xbm}

  # set permissions
  find "$pkgdir"/usr/share/$pkgname -type d -exec chmod 755 {} +
  find "$pkgdir"/usr/share/$pkgname -type f -exec chmod 644 {} +

  # isntall docs and manuals
  install -Dm644 tkhangman/README "$pkgdir"/usr/share/doc/$pkgname/tkhangman_README
  install -Dm644 tksol/readme     "$pkgdir"/usr/share/doc/$pkgname/tksol_README
  install -Dm644 tksol/tksol.6    "$pkgdir"/usr/share/man/man6/tksol.6
}
