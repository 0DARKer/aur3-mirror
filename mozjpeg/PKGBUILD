# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>
# Maintainer: N30N <archlinux@alunamation.com>

pkgname="mozjpeg"
pkgver=1.0
pkgrel=1
pkgdesc="JPEG image codec with accelerated baseline decoding and superior encoding"
url="https://github.com/mozilla/mozjpeg"
license=("BSD")
arch=("i686" "x86_64")
makedepends=("nasm")
source=("https://github.com/mozilla/${pkgname}/archive/v${pkgver}.tar.gz")
sha1sums=("13c127ba6387b6b96736c22e0166112f6f8959b3")

provides=("libjpeg=8.0.2" "turbojpeg" "libjpeg-turbo")
conflicts=("libjpeg" "turbojpeg" "libjpeg-turbo")
replaces=("libjpeg" "turbojpeg" "libjpeg-turbo")

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	autoreconf -fiv
	./configure --prefix=/usr --with-jpeg8 --mandir=/usr/share/man
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make \
		DESTDIR="${pkgdir}" \
		docdir="/usr/share/doc/${pkgname}" \
		exampledir="/usr/share/doc/${pkgname}" \
		install

	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	ln -t "${pkgdir}/usr/share/licenses/${pkgname}" -s "../../doc/${pkgname}/README"{,-turbo.txt,-mozilla.txt}
	install -m644 jpegint.h "${pkgdir}/usr/include" # required by other software
}
