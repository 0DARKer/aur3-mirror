# Maintainer: Jack Allnutt <jack@allnutt.eu>
pkgname=kiwiirc
pkgver=0.8.3
pkgrel=1
pkgdesc="A hand-crafted web-based IRC client that you can enjoy"
arch=('any')
url="https://kiwiirc.com/"
license=('AGPL3')
depends=('nodejs>=0.10.0')
options=('emptydirs' '!strip')
source=(https://github.com/prawnsalad/KiwiIRC/archive/v$pkgver.tar.gz
    'kiwi.patch'
    'kiwiirc.service')
md5sums=('42fe7bf75ffe64c160772470aef20c32'
         '2fe42b1b0d6c77fb03be1181f0533d9c'
         'd7251f4600e26f66513164fc75b2c1c0')

build() {
    cp kiwiirc.service "$srcdir/KiwiIRC-$pkgver/"
    cd "$srcdir/KiwiIRC-$pkgver"
    patch <../kiwi.patch
    npm -q install
}

package() {
    cd "$srcdir"
    install -D -m644 KiwiIRC-$pkgver/config.example.js $pkgdir/etc/kiwiirc/config.example.js
    install -D -m755 KiwiIRC-$pkgver/kiwi $pkgdir/usr/bin/kiwi
    install -D -m644 KiwiIRC-$pkgver/kiwiirc.service $pkgdir/usr/lib/systemd/system/kiwiirc.service
    install -D -m644 KiwiIRC-$pkgver/package.json $pkgdir/usr/lib/kiwiirc/package.json

    install -d -m755 $pkgdir/usr/lib/kiwiirc/server
    install -d -m755 $pkgdir/usr/lib/kiwiirc/server_modules
    install -d -m755 $pkgdir/usr/lib/kiwiirc/server/node_modules
    cp -dr --preserve=mode KiwiIRC-$pkgver/server $pkgdir/usr/lib/kiwiirc
    cp -dr --preserve=mode KiwiIRC-$pkgver/server_modules $pkgdir/usr/lib/kiwiirc
    cp -dr --preserve=mode KiwiIRC-$pkgver/node_modules $pkgdir/usr/lib/kiwiirc
    cp -dr --preserve=mode KiwiIRC-$pkgver/client $pkgdir/usr/lib/kiwiirc
}
