pkgname=hostname
pkgver=3.11
pkgrel=1
pkgdesc="Utility to set/show the host name or domain name"
arch=('i686' 'x86_64')
url="http://packages.qa.debian.org/h/hostname.html"
license=('GPL2')
depends=('glibc')
options=('!emptydirs')
source=("http://ftp.de.debian.org/debian/pool/main/h/$pkgname/hostname_$pkgver.tar.gz")
md5sums=('4abb765cb25e0315aa17dece62275e92')
sha256sums=('8492e822e25f7f8af225a00dfe9c4aa8113e927fe21194a39e2f81c17ce85ee1')

build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname"
  mkdir -m 0755 -p  "$pkgdir/usr/bin" "$pkgdir/usr/share/man/fr/man1"
  make BASEDIR="$pkgdir" install
  ln -sf /bin/hostname "$pkgdir/usr/bin/hostname"
}

# vim:set ts=2 sw=2 et:
