# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
pkgname=photivo-hg
pkgver=1005.24b0bc746dbb
pkgrel=1
pkgdesc="Free and open source photo processor"
arch=('i686' 'x86_64')
url="http://photivo.org/"
license=('GPL3')
depends=('exiv2' 'qt4' 'lcms2' 'fftw' 'graphicsmagick-qd16' 'lensfun' 'liblqr' 'shared-mime-info')
optdepends=('gimp: Gimp plugins' 'python2: Gimp to Photivo plugin')
makedepends=('mercurial' 'gimp')
provides=('photivo')
conflicts=('photivo')
install=photivo.install
source=('hg+https://code.google.com/p/photivo/' \
        'gcc_4.8-fix.diff')
md5sums=('SKIP'
         '3a43c3d6fc755215ff2b28a6f3bc2714')

pkgver() {
  cd "$srcdir/photivo"
  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd "$srcdir/photivo"

  # remove the need for ccache
  find . -type f -name "*.pro" -exec sed -i 's|ccache||' '{}' ';'
  # fix the build with gcc 4.8
  patch -Np1 < "$srcdir/gcc_4.8-fix.diff" || true

  qmake-qt4 PREFIX=/usr CONFIG+=WithGimp
  make

  # fix the "Gimp to Photivo" plugin
  sed -i -e 's|\x0D$||' -e 's|#!.*|#!/usr/bin/python2|' "mm extern photivo.py"
}

package() {
  cd "$srcdir/photivo"

  make INSTALL_ROOT="$pkgdir" install
  rm -rf "$pkgdir"/home

  # GIMP integration
  install -D -m755 ptGimp "$pkgdir/usr/lib/gimp/2.0/plug-ins/ptGimp"
  install -D -m755 "mm extern photivo.py" "$pkgdir/usr/lib/gimp/2.0/plug-ins/mm-extern-photivo.py"
}
