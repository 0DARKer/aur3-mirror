# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
pkgname=photivo-hg
pkgver=75
pkgrel=1
pkgdesc="Free and open source photo processor"
arch=('i686' 'x86_64')
url="http://photivo.org/"
license=('GPL3')
depends=('exiv2' 'qt' 'lcms2' 'fftw' 'graphicsmagick' 'lensfun')
optdepends=('gimp')
makedepends=('mercurial' 'gimp')
provides=('photivo')
conflicts=('photivo')
source=(gimp_image_add_layer_define.diff)
md5sums=('ad14c7e6c46d593b359cccd82a4c2327')

_hgroot="https://photivo.googlecode.com/hg/"
_hgrepo="photivo"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u || return 1
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo || return 1
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir/$_hgrepo-build" ]; then
    rm -rf "$srcdir/$_hgrepo-build"
  fi
  cp -a "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  # remove the need for ccache
  find . -type f -name "*.pro" -exec sed -i 's|ccache||' '{}' ';'
  # build fix
  patch -Np1 < "$srcdir"/gimp_image_add_layer_define.diff || true

  qmake PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_hgrepo-build"
  make INSTALL_ROOT="$pkgdir" install
  rm -rf "$pkgdir"/home
}
