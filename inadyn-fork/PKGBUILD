# Maintainer: Eric Engestrom <aur [at] engestrom [dot] ch>

pkgname=inadyn-fork
pkgver=1.99.6
pkgrel=1
pkgdesc="Simple dynamic DNS client - fork of the original INADYN implementation from Narcis Ilisei"
url=http://troglobit.com/inadyn.html
arch=(x86_64 i686 armv6h)
license=(GPL)
backup=(etc/inadyn.conf)
conflicts=(inadyn-opendns inadyn)
provides=(inadyn)
source=(https://github.com/troglobit/inadyn/archive/$pkgver.tar.gz
        inadyn.conf
        inadyn.service)
sha256sums=('3de3bdbc0e1922e6910f923c04ac67afaeae280e50b99029172fa5036e0d145e'
            '9196481a3c6875a0ead3a318608877ef49402c968f162f13a2f92d6882008c41'
            '4587f4ae2a4215f9ba67d5b3b7d0aa120a9ebd31677472fa68d8792f42d7135f')

prepare(){
  cd inadyn-$pkgver
  sed -i -e 's/sbin/bin/g' Makefile
}

build(){
  make -C inadyn-$pkgver
}

package(){
  make -C inadyn-$pkgver prefix=/usr DESTDIR=$pkgdir install
  install -Dm644 inadyn.service $pkgdir/usr/lib/systemd/system/inadyn.service
  install -Dm644 inadyn.conf $pkgdir/etc/inadyn.conf
}
