# Maintainer: reMiND <detanator_at_gmail_com>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: trile7 at gmail dot com

pkgname=inadyn-fork
_name=inadyn
pkgver=1.99.12
pkgrel=1
pkgdesc='Simple dynamic DNS client - fork of the original INADYN implementation from Narcis Ilisei'
url='http://troglobit.com/inadyn.html'
arch=('x86_64' 'i686' 'armv6h')
license=('GPL')
depends=('gnutls')
backup=('etc/inadyn.conf')
conflicts=('inadyn-opendns' 'inadyn' 'inadyn-mt' 'inadyn-fork-git')
provides=('inadyn')
source=(${_name}-$pkgver.tar.gz::https://github.com/troglobit/inadyn/archive/$pkgver.tar.gz
        inadyn.conf
        inadyn.service)
sha256sums=('a4909788cfa3fda47b274d2f8c64ccd746412ccaa1abbaecc54085f65de9225b'
            '919f87ea87b58e4ddfaf3346ffbcf899b8d9dcee5a0e9440e17e5db9552bba53'
            '4587f4ae2a4215f9ba67d5b3b7d0aa120a9ebd31677472fa68d8792f42d7135f')

build(){
  cd ${_name}-$pkgver
  ./configure --prefix=/usr
  make
}

package(){
  cd ${_name}-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm644 ../inadyn.service $pkgdir/usr/lib/systemd/system/inadyn.service
  install -Dm644 ../inadyn.conf $pkgdir/etc/inadyn.conf
}
