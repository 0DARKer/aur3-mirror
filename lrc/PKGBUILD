# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=lrc
pkgver=0.7
pkgrel=1
pkgdesc="A system for packing many files into a single file for installation and use in a program as its resources."
arch=(i686 x86_64)
url="https://bitbucket.org/StarFire/lrc/overview"
license=('GPL3')
depends=('sdl_image' 'tinyxml' 'bzip2')
options=('!libtool')
source=("https://bitbucket.org/StarFire/lrc/downloads/$pkgname-$pkgver.tar.bz2")
md5sums=('c6fdc3593126f315841de495edb7fa8f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # do not use inner tinyxml
  sed -i '/SUBDIRS.*tinyxml/d;s/ -[IL]tinyxml//' \
    src/compiler/Makefile.*

  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
