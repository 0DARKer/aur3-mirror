# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=lrc
pkgver=0.9
pkgrel=1
pkgdesc="A system for packing many files into a single file for installation and use in a program as its resources."
arch=(i686 x86_64)
url="https://bitbucket.org/StarFire/lrc/overview"
license=('GPL3')
depends=('sdl_image' 'tinyxml' 'bzip2')
makedepends=('libtool')
options=('!libtool')
source=("https://bitbucket.org/StarFire/lrc/downloads/$pkgname-$pkgver.tar.bz2" $pkgname-gcc4.7.patch)
md5sums=('08325985cdb498d5f899e213da5ab073'
         '4eb47076b614e00655ff0287e1ee441c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.7 patch
  patch -p2 -i ../${source[1]}

  # do not use inner tinyxml
  sed -i '/SUBDIRS.*tinyxml/d;s/ -[IL]tinyxml//' \
    src/compiler/Makefile.*

  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
