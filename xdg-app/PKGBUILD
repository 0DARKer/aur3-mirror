# Maintainer: Mirco Tischler <mt-ml at gmx dot de>

pkgname=xdg-app
pkgver=0.2
pkgrel=1
pkgdesc='An application deployment framework for desktop apps'
arch=('i686' 'x86_64')
url='https://github.com/alexlarsson/xdg-app'
license=('GPL2')
depends=('ostree')
install='xdg-app.install'
source=("https://github.com/alexlarsson/xdg-app/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('2c65f012e1823759bbd5244f30175b20b40faeac9e68e2693b4ed0d35d12a708')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # remove setuid flag, instead we set CAP_SYSADMIN and CAP_MKNOD in the install script
  chmod -s "$pkgdir/usr/bin/xdg-app-helper"
}

