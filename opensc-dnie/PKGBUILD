# Contributor: Tom < reztho at archlinux dot us >
pkgname=opensc-dnie
pkgver=1.4.7
pkgrel=2
pkgdesc='Support for the Spanish electronic National Identity Document (DNIe)'
arch=('i686' 'x86_64')
url='http://www.dnielectronico.es/descargas/index.html'
license=('custom')
depends=('glib2' 'pinentry' 'perl' 'zlib' 'openssl' 'openct' 'pcsclite' 'libassuan1')
optdepends=('firefox' 'ccid')
install=${pkgname}.install
conflicts=('opensc')
options=('!strip' '!libtool' '!emptydirs')
backup=('etc/opensc.conf')
_openscver=0.11.8
source=('http://www.dnielectronico.es/descargas/PKCS11_para_Sistemas_Unix/1.4.7.Ubuntu_Karmic_32/Ubuntu_Karmic_opensc-dnie_1.4.7-2_i386.tar' 'leeme.txt.gz' "http://www.opensc-project.org/files/opensc/opensc-${_openscver}.tar.gz")
[ "${CARCH}" == 'x86_64' ] && source=('http://www.dnielectronico.es/descargas/PKCS11_para_Sistemas_Unix/1.4.7.Ubuntu_Karmic_64/Ubuntu_Karmic_opensc-dnie_1.4.7-2_amd64.tar' 'leeme.txt.gz' "http://www.opensc-project.org/files/opensc/opensc-${_openscver}.tar.gz")

md5sums=('e632e52db0bb791f24c1c82a9d77fb51'
         '4162dce3ada5ec3198b145eba339ba45'
         'a269b478b18dddb648b9bd930206b5a8')

[ "${CARCH}" == 'x86_64' ] && md5sums=('32e14a20723af7b40689cd6ae2a38f6e'
                                       '4162dce3ada5ec3198b145eba339ba45'
                                       'a269b478b18dddb648b9bd930206b5a8')

build() {
# DNIe part ****************************************
  # Uncompressing the files...
  cd ${srcdir}
  _deb=opensc-dnie_${pkgver}-1_i386.deb
  [ "${CARCH}" == 'x86_64' ] && _deb=opensc-dnie_${pkgver}-1_amd64.deb
  bsdtar -xf ${_deb} || return 1
  bsdtar -xf data.tar.gz || return 1
  mv ${srcdir}/usr ${pkgdir}/

  # The license and statements of the author of this PKGBUILD...
  install -Dm644 ${srcdir}/leeme.txt \
  ${pkgdir}/usr/share/licenses/${pkgname}/leeme.txt || return 1
  install -m644 ${pkgdir}/usr/share/doc/${pkgname}/copyright \
  ${pkgdir}/usr/share/licenses/${pkgname} || return 1

  # Correcting some permissions
  find ${pkgdir}/usr/share/opensc-dnie/* -type f -exec chmod 644 {} \;
  
  # Removing unneeded files...
  rm -rf ${pkgdir}/usr/share/doc/opensc-dnie || return 1
  rm -rf ${pkgdir}/usr/share/applications || return 1

# OpenSC part *******************************
# Taken from the opensc PKGBUILD in AUR
  cd ${srcdir}/opensc-${_openscver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-openct --enable-nsplugin --enable-pcsc --with-plugin-dir=/usr/lib/mozilla/plugins --with-libassuan-prefix=/usr/share/libassuan1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -Dm644 ${srcdir}/opensc-${_openscver}/etc/opensc.conf ${pkgdir}/etc/opensc.conf || return 1
}

