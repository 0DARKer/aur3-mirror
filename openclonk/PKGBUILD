# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Wesley <rudirennsau at hotmail dot com>

pkgname=openclonk
pkgver=5.2.0
pkgrel=1
pkgdesc="Open Source successor of 'Clonk Rage', based on the source code from Redwolf Design"
arch=('i686' 'x86_64')
url="http://www.openclonk.org/"
license=('custom')
conflicts=(openclonk-hg)
depends=(gtk2 glew sdl_mixer libxpm)
makedepends=(cmake boost)
source=(http://hg.openclonk.org/openclonk/archive/openclonk-release-$pkgver-src.tar.gz
        clonk.png)
md5sums=('6dbad61f074f40e13ac519925a54093a'
         'bdced2f9cb6c732babf9fe800cf9dcc0')

build() {
	cd $srcdir/$pkgname-release-$pkgver-src

    [[ -d build ]] && rm -rf build
    mkdir build && cd build
	cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr
	make 
}

package() {
	cd $srcdir/$pkgname-release-$pkgver-src

    cd build
    make DESTDIR=$pkgdir install

    cd ..
    mkdir -p $pkgdir/usr/share/licenses/$pkgname
	install -D -m644 licenses/*.txt $pkgdir/usr/share/licenses/$pkgname

	install -D -m644 clonk.desktop $pkgdir/usr/share/applications/clonk.desktop
	install -D -m644 $srcdir/clonk.png $pkgdir/usr/share/pixmaps/clonk.png
}
