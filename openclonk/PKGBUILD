# Maintainer:
# Contributor: Jonathan Steel <mail at jsteel dot org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Wesley <rudirennsau at hotmail dot com>
# Contributor: Benedict Etzel <developer@beheh.de>

pkgname=openclonk
pkgver=5.4.0
pkgrel=1
_orig=$pkgname-release-$pkgver-src
pkgdesc='Multiplayer-action-tactic-skill game'
arch=('i686' 'x86_64')
url='http://openclonk.org'
license=('custom')
depends=('gtk2' 'glew' 'sdl_mixer' 'libxpm'  'hicolor-icon-theme' 'libupnp')
makedepends=('cmake' 'boost' 'imagemagick' 'mesa')
conflicts=('clonk_rage')
install=$pkgname.install
source=(http://openclonk.org/builds/release/$pkgver/$pkgname-$pkgver-src.tar.bz2)
md5sums=('2161f15bccee8882379308f5c01e1fb1')

build() {
  cd "${srcdir}/$pkgname-release-$pkgver-src"
  [[ -d build ]] && rm -rf build
  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFREETYPE_INCLUDE_DIRS=/usr/include/freetype2
  make
}

package() {
  cd "${srcdir}/${_orig}"
  cd build
  
  make DESTDIR="$pkgdir" install

  # move game binary to /usr/bin
  mv "$pkgdir"/usr/games/openclonk "$pkgdir"/usr/bin/$pkgname
  rm -r "$pkgdir"/usr/games

  # licenses
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 ../licenses/*.txt "$pkgdir"/usr/share/licenses/$pkgname
}

# vim: ts=2 sw=2 et:
