# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Wesley <rudirennsau at hotmail dot com>

pkgname=openclonk
pkgver=5.1.2
pkgrel=1
pkgdesc="Open Source successor of 'Clonk Rage', based on the source code from Redwolf Design"
arch=('i686' 'x86_64')
url="http://www.openclonk.org/"
license=('custom')
conflicts=(openclonk-hg)
depends=(gtk2 glew sdl_mixer)
makedepends=(cmake)
source=(http://hg.openclonk.org/openclonk/archive/openclonk-release-$pkgver-src.tar.gz
        $pkgname.sh
        $pkgname.desktop
        $pkgname.png)
md5sums=('e6cb6a90567850e9d24bd68fadbc87c9'
         '855f9567af89a24ebca9a196b5f5bf69'
         '5dcc82c9a576478b22e657d932d33c28'
         'bdced2f9cb6c732babf9fe800cf9dcc0')

build() {
	cd $srcdir/$pkgname-release-$pkgver-src

    [[ -d build ]] && rm -rf build
    mkdir build && cd build
	cmake ..
	make

    rm ../planet/*.c4g
    ./c4group -v ../planet/*.c4g/ -p
}

package() {
	cd $srcdir/$pkgname-release-$pkgver-src

	mkdir -p $pkgdir/usr/share/$pkgname

	cp clonk c4group planet/AUTHORS planet/COPYING planet/*.c4* $pkgdir/usr/share/$pkgname
	install -D -m644 licenses/*.txt $pkgdir/usr/share/licenses/$pkgname

	install -D -m755 $srcdir/$pkgname.sh $pkgdir/usr/bin/$pkgname
	install -D -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
	install -D -m644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
}
