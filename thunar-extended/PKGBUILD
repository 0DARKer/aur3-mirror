# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=thunar-extended
pkgver=1.4.0
pkgrel=1
pkgdesc="Thunar with cursor audio preview and extra options for trash, desktop files and user actions"
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://thunar.xfce.org"
groups=('xfce4')
conflicts=('thunar')
replaces=('thunar')
provides=('thunar=1.4.0')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify' 'udev' \
         'gtk2' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'xfce4-panel' 'xfce4-dev-tools')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
	    'polkit-gnome: for mounting internal partitions (needs root password)'
	    'gamin: for monitoring file changes without gvfs'
            'xfce4-panel: for trash applet'
	    'tumbler: for thumbnail previews'
	    'thunar-volman: manages removable devices'
	    'thunar-archive-plugin: create and deflate archives'
	    'thunar-media-tags-plugin: view/edit id3/ogg tags')
options=('!libtool')
install=thunar.install
source=(http://archive.xfce.org/src/xfce/thunar/1.4/Thunar-${pkgver}.tar.bz2 more_options.patch uca_num_files.patch preview.patch)

build() {
  cd "${srcdir}"/Thunar-${pkgver}
  
  patch -Np1 -i ../more_options.patch
  patch -Np1 -i ../uca_num_files.patch
  patch -Np1 -i ../preview.patch
  sed -i -e 's/Thunar_CFLAGS/thunar_CFLAGS/' -e 's/Thunar_LDADD/thunar_LDADD/' thunar/Makefile.am

  xdt-autogen
  
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static --disable-wallpaper-plugin \
    --enable-gstreamer --enable-gio-unix --enable-dbus --enable-exif \
    --enable-pcre --enable-gudev --disable-startup-notification \
    --enable-notifications --enable-pcre --enable-gtk-doc
  
  make
}

package() {
  cd "${srcdir}"/Thunar-${pkgver}
  make DESTDIR="${pkgdir}" install || return 1
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop
}


md5sums=('737ad2c36ed36b10fef82e860b9e990a' '9806ecf2b2795712380b740437ba86a4' '1aeaa6c7d6153818da9687a135a42427' '189a505e82851d0c045a20ad2817ffbe')
