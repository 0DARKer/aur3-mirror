# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=thunar-extended
pkgver=1.6.0
pkgrel=1
pkgdesc="Thunar with cursor audio preview and extra options for trash, desktop files and user actions"
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://thunar.xfce.org"
groups=('xfce4')
conflicts=('thunar')
replaces=('thunar')
provides=('thunar=1.4.0')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify' 'udev' \
         'gtk2' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng' 'gstreamer0.10')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'xfce4-panel' 'xfce4-dev-tools')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
	    'polkit-gnome: for mounting internal partitions (needs root password)'
	    'gamin: for monitoring file changes without gvfs'
            'xfce4-panel: for trash applet'
	    'tumbler: for thumbnail previews'
	    'thunar-volman: manages removable devices'
	    'thunar-archive-plugin: create and deflate archives'
	    'thunar-media-tags-plugin: view/edit id3/ogg tags')
options=('!libtool')
install=thunar.install
source=(http://archive.xfce.org/src/xfce/thunar/1.6/Thunar-${pkgver}.tar.bz2 notrash.patch uca_num_files.patch preview.patch thunar-1.6.0-show-nodisplay-true-applications.patch)

build() {
  cd "${srcdir}"/Thunar-${pkgver}
  
  patch -Np1 -i ../notrash.patch
  patch -Np1 -i ../uca_num_files.patch
  patch -Np1 -i ../preview.patch
  patch -Np1 -i ../thunar-1.6.0-show-nodisplay-true-applications.patch
  sed -i -e 's/Thunar_CFLAGS/thunar_CFLAGS/' -e 's/Thunar_LDADD/thunar_LDADD/' thunar/Makefile.am

  xdt-autogen
  
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static --disable-wallpaper-plugin \
    --enable-gstreamer --enable-gio-unix --enable-dbus --enable-exif \
    --enable-pcre --enable-gudev --disable-startup-notification \
    --enable-notifications --enable-gtk-doc
  
  make
}

package() {
  cd "${srcdir}"/Thunar-${pkgver}
  make DESTDIR="${pkgdir}" install || return 1
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop
}


md5sums=('33434fa215b1b858c819f7cb7aacde06' 'b6c0920eeefd3b3891a7f5c47b05b0d0' '1d62044729c8e34e7cc6c3fda0ea8df5' '1238a8aba4299576564b08c32c2c7d66' '6ea4f9aef0c8cbe141d55d6dec8c4799')
