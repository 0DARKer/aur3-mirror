# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=thunar-extended
pkgver=1.0.2
pkgrel=3
pkgdesc="Thunar with cursor audio preview and extra options for trash, desktop files and user actions"
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://thunar.xfce.org"
groups=('xfce4')
conflicts=('thunar')
replaces=('thunar')
provides=('thunar')
depends=('desktop-file-utils' 'libexif>=0.6.19' 'hicolor-icon-theme' 'fam' \
         'gtk2' 'exo>=0.3.107' 'libxfce4util>=4.6.2' 'libpng' 'libjpeg>=8.0.1')
makedepends=('intltool' 'pkgconfig' 'xfce4-panel>=4.6.4' 'xfce4-dev-tools')
optdepends=('gamin:   a fam replacement to make thunar monitor changes to files on the fly'
            'xfce4-panel: for trash applet')
options=('!libtool')
install=thunar.install
source=(http://archive.xfce.org/src/xfce/thunar/1.0/Thunar-${pkgver}.tar.bz2 more_options.patch uca_num_files.patch preview.patch)

build() {
  cd ${srcdir}/Thunar-${pkgver}
  patch -Np1 -i ../more_options.patch
  patch -Np1 -i ../uca_num_files.patch
  patch -Np1 -i ../preview.patch
  
  xdt-autogen || return 1
  
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static --disable-wallpaper-plugin \
    --enable-gstreamer --disable-gnome-thumbnailers --enable-exif \
    --enable-pcre --disable-startup-notification --with-volume-manager=none
  
  make || return 1
}

package() {
  cd ${srcdir}/Thunar-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop
}
md5sums=('c91073202d373a1f9951a1240083c36d' 'bedd88543da7d544da783cfbf4bc21c0' '2c6d1082c6ff8011c8cc7e42a246eb74' 'dc5c555da1506aef3767ca295e0e1aad')
