# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=fcitx-qt5
pkgver=0.1.0
pkgrel=2
pkgdesc="QT5 IM Module for Fcitx"
arch=('i686' 'x86_64')
url="http://code.google.com/p/fcitx"
license=('GPL')
depends=('fcitx>=4.2.6' 'qt5')
makedepends=('cmake')
source=("http://fcitx.googlecode.com/files/${pkgname}-${pkgver}.tar.xz")

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  rm -rf build
  mkdir build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/ -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}/build"
  install -Dm755 src/libfcitxplatforminputcontextplugin.so "$pkgdir/usr/lib/qt5/plugins/platforminputcontexts/libfcitxplatforminputcontextplugin.so"
}
md5sums=('87b0f0dac7f8990af7dde03970add7d7')
