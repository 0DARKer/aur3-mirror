# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=xarchiver-assume-name
pkgver=0.5.3
pkgrel=1
pkgdesc="Xarchiver with the ability to archive directories without a name change"
arch=('i686' 'x86_64')
url="http://xarchiver.sourceforge.net/"
license=('GPL')
groups=('xfce4-goodies')
depends=('gtk2' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('zip: ZIP support'
            'unzip: ZIP support'
            'p7zip: 7z support'
            'arj: ARJ support'
            'lzop: LZOP support'
            'cpio: RPM support'
            'unrar: RAR support')
install=xarchiver.install
source=(http://downloads.sourceforge.net/xarchiver/xarchiver-$pkgver.tar.bz2
        xarchiver-0.5.3-fix-rpm-support.patch
        xarchiver-0.5.3-fix-double-escaping.patch
        xarchiver-0.5.3-fix-password-protected.patch
        xarchiver-0.5.3-add-mime-types.patch
	xarchiver-0.5.3-explicit-extension.patch)

prepare() {
  cd xarchiver-$pkgver

  # Fix RPM support
  patch -Np1 -i ../xarchiver-0.5.3-fix-rpm-support.patch

  # Fix error when trying to create archives with spaces
  patch -Np1 -i ../xarchiver-0.5.3-fix-double-escaping.patch

  # Fix segfault when handling password protected files
  patch -Np1 -i ../xarchiver-0.5.3-fix-password-protected.patch

  # Add more MIME types in the desktop file
  patch -Np1 -i ../xarchiver-0.5.3-add-mime-types.patch

  # Stop GTK filechooser from cding to a dir you want to archive
  patch -Np1 -i ../xarchiver-0.5.3-explicit-extension.patch
}

build() {
  cd xarchiver-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4
  make
}

package() {
  cd xarchiver-$pkgver
  make DESTDIR="$pkgdir" install
}
md5sums=('fd390bbd2df76a5f8a007bdeae82d4aa' '812b93339f5e3332621f3c5abebfe277' '6178d7ab679b761469c880a8db991907' '35ab96d98521a0a36f3e9e9ec0969107' 'f9119f5290caa195a56b7d3c63d9137d' '5fb11bb9842f60dd29a0b3267c1ca804')
