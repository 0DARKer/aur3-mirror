pkgname=mingw-w64-freeglut
pkgver=2.8.0
pkgrel=1
pkgdesc="Provides functionality for small OpenGL programs. (mingw-w64)"
arch=(any)
url="http://freeglut.sourceforge.net/"
license=("MIT")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("http://downloads.sourceforge.net/freeglut/freeglut-$pkgver.tar.gz")
md5sums=('5db8651af306bc403fbfd36934a20e1d')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/freeglut-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --without-x
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip -x -g "$pkgdir/usr/${_arch}/bin/"*.dll
    ${_arch}-strip -g "$pkgdir/usr/${_arch}/lib/"*.a
  done
}
