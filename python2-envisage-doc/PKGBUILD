# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python2-envisage-doc
pkgver=4.2.0
_githubtag=a326011
pkgrel=1
pkgdesc="Documentation for Envisage"
arch=('any')
url="https://github.com/enthought/envisage"
license=('BSD')
makedepends=('python2-sphinx')
conflicts=('python2-envisage-doc-git')
options=(!emptydirs)

source=("$pkgname.tar.gz::https://github.com/enthought/envisage/tarball/${pkgver}")
md5sums=('65a2a31be206abc8ce8f6afaaf4b9234')

build() {
  cd "$srcdir/enthought-envisage-${_githubtag}/docs"
  make SPHINXBUILD=sphinx-build2 html
}

package() {
  cd "$srcdir/enthought-envisage-${_githubtag}/docs"
  install -d "${pkgdir}"/usr/share/doc/enthought/envisage
  cp -r build/html/* "${pkgdir}"/usr/share/doc/enthought/envisage

  install -D "../LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

