# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname='perl-safe-isa'
_pkgname='Safe-Isa'
pkgver='1.000003'
pkgrel='1'
pkgdesc="Safe::Isa - Call isa, can, does and DOES safely on things that may not be objects"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.1')
makedepends=('perl-extutils-makemaker>=6.59')
url='http://search.cpan.org/dist/Safe-Isa'
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/${_pkgname}-${pkgver}.tar.gz")
md5sums=('7e301faa2128b785203d413a6d6e5b09')
sha512sums=('aaf5574bf6496cba5d938cc172bcbb18a5f6330a3618796f61a641d8744f69065924d5932b5ef7181e03966cb780a612f2c29a2cf7b1772d6b63316a29d3c65d')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_pkgname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
