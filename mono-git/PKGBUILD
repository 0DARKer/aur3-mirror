pkgname=mono-git
pkgver=20120812
pkgrel=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"

url="http://www.mono-project.com/"
arch=('i686' 'x86_64')

license=('GPL' 'LGPL2' 'MPL' 'custom:MITX11')
depends=('zlib' 'libgdiplus>=2.10' 'sh')
makedepends=('git' 'pkgconfig' 'mono')
options=('!libtool' '!makeflags')
provides=('monodoc' 'mono=2.11.4')
conflicts=('monodoc')
source=(mono.rc.d)
md5sums=('8315e46c6a6e9625502521fc0ad1a322')
_gitroot="git://github.com/mono/mono.git"
_gitname="mono"

build() 
{
   cd ${srcdir}

   msg "Cloning Mono from GIT"
   if [ -d $_gitname ]; then
      cd $_gitname
      git pull || return 1
   else
      git clone $_gitroot $_gitname || return 1
      cd $_gitname
   fi

   #build mono
   ./autogen.sh --prefix=/usr --sysconfdir=/etc \
               --with-libgdiplus=installed
   make || return 1

   # build jay
   cd ${srcdir}/${_gitname}/mcs/jay
   make || return 1
}

package() {
  cd ${srcdir}/${_gitname}
  make DESTDIR=${pkgdir} install || return 1
  
  #install jay
  cd ${srcdir}/${_gitname}/mcs/jay
  make DESTDIR=${pkgdir} prefix=/usr install=../../install-sh install
  cd ${srcdir}/${_gitname}

  # install daemons
  mkdir -p ${pkgdir}/etc/rc.d
  install -m755 ${srcdir}/mono.rc.d ${pkgdir}/etc/rc.d/mono

  #install license
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 mcs/MIT.X11 ${pkgdir}/usr/share/licenses/${pkgname}/

  #fix the .pc file to be able to request mono on what it depends, fixes #go-oo build
  sed -i -e "s:#Requires:Requires:" ${pkgdir}/usr/lib/pkgconfig/mono.pc
}
