# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Ziemas <emil.j.n@bahnhof.se>
# Contributor: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Brice Carpentier <brice@dlfp.org>

_pkgname=mono
pkgname=mono-git
pkgver=3.2.3.r4112.g4b6a2c8
pkgrel=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL2' 'MPL' 'custom:MITX11')
url="http://www.mono-project.com/"
depends=('zlib' 'libgdiplus>=2.10' 'sh' 'python')
makedepends=('git')
options=('!libtool' '!makeflags')
provides=('monodoc' 'mono')
conflicts=('monodoc' 'mono')
source=(git://github.com/mono/mono.git
        mono.binfmt.d)
md5sums=('SKIP'
         'b9ef8a65fea497acf176cca16c1e2402')


pkgver() {
	cd ${srcdir}/${_pkgname}
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g;s/mono.//'
}

build() {
  cd ${srcdir}/${_pkgname}

   # build mono
   ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --disable-quiet-build \
    --disable-system-aot \
    --disable-static \
    --with-mcs-docs=no \
    --with-libgdiplus=installed
  make

  # build jay
  cd ${srcdir}/${_pkgname}/mcs/jay
  make
}

package() {
  cd ${srcdir}/${_pkgname}
  make DESTDIR=${pkgdir} install

  # install jay
  cd ${srcdir}/${_pkgname}/mcs/jay
  make DESTDIR=${pkgdir} prefix=/usr INSTALL=../../install-sh install
  cd ${srcdir}/${_pkgname}

  # install binfmt conf file and pathes
  install -D -m644 "${srcdir}"/mono.binfmt.d "${pkgdir}"/usr/lib/binfmt.d/mono.conf

  #install license
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 mcs/MIT.X11 ${pkgdir}/usr/share/licenses/${pkgname}/

  #fix .pc file to be able to request mono on what it depends, fixes #go-oo build
  sed -i -e "s:#Requires:Requires:" ${pkgdir}/usr/lib/pkgconfig/mono.pc
}
