# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

_perlmod=Digest-SHA
_modnamespace=Digest
pkgname=perl-digest-sha
pkgver=5.91
pkgrel=1
pkgdesc="Digest::SHA - Perl extension for SHA-1/224/256/384/512"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('GPL' 'PerlArtistic')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('8ee486f0206183e4ccc02199fdb79d48f707aa145f12ec6faae39c87eee7dc8df16c9568ae526ccdcf7eda2f272e983f1f9a6de174ca89d60a5011920790a4d7')

build() {
	cd "${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}"
}
