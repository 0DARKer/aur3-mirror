# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

_perlmod=Digest-SHA
_modnamespace=Digest
pkgname=perl-digest-sha
pkgver=5.83
pkgrel=1
pkgdesc="Digest::SHA - Perl extension for SHA-1/224/256/384/512"
arch=('any')
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl>=5.10.0')
url="http://search.cpan.org/dist/$_perlmod"
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('fba3112f6f353ea6993b75b3b2f098d92b819f95c5d283f4b4bb5c9841f2bbcd20f4de094876c04c2efd1a687c494fce20eb93f03fb25e428251cd3aba9f0aad')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	make test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	make install DESTDIR="$pkgdir/"
}
