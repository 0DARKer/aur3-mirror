# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

_perlmod=Digest-SHA
_modnamespace=Digest
pkgname=perl-digest-sha
pkgver=5.89
pkgrel=1
pkgdesc="Digest::SHA - Perl extension for SHA-1/224/256/384/512"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('d846020b60863caa54456391e0e54f8991120bd5ebc845ca74b5454f69f29a2f31dc81e916d9c71162479f6e8e09151fd92cffe08aac8f3efb08b5c78911d506')

build() {
	cd "${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}"
}
