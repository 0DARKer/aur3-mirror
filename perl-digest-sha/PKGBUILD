# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

_perlmod=Digest-SHA
_modnamespace=Digest
pkgname=perl-digest-sha
pkgver=5.73
pkgrel=1
pkgdesc="Digest::SHA - Perl extension for SHA-1/224/256/384/512"
arch=('any')
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl>=5.10.0')
url="http://search.cpan.org/dist/$_perlmod"
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('8bd2a4e8221ff70251f97a1e82bf9f7397edec52ae149c0400b2216412b2488198ae9fe0abfec36090d119227201460b21991caf8f0a78be26304b71c2c3e5e4')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
