# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

_perlmod=Digest-SHA
_modnamespace=Digest
pkgname=perl-digest-sha
pkgver=5.72
pkgrel=1
pkgdesc="Digest::SHA - Perl extension for SHA-1/224/256/384/512"
arch=('any')
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl>=5.10.0')
url="http://search.cpan.org/dist/$_perlmod"
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('e3776b191f63e07f929abda515936ec0a71e6af5b69d3cd66eba9754aaa7454238d278525ef58c18b4f9a027b21d7ea14724a62b6e1b70e51e5276ac26a8ce6d')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
