# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-digest-sha
_pkgname=Digest-SHA
pkgver=5.71
pkgrel=2
pkgdesc="Digest::SHA - Perl extension for SHA-1/224/256/384/512"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.10.0')
url="http://search.cpan.org/dist/$_pkgname"
source=(http://search.cpan.org/CPAN/authors/id/M/MS/MSHELOR/$_pkgname-$pkgver.tar.gz)
sha512sums=('182844006bcead07ec11061780dfe4c3543d30f533786f5d29ca647ce90489fd680ca5c865f959e21ac5f1ff5f586e7a86c7d35b28b06ae693838ac610d56e7f')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
