# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=wecase-git
pkgver=0
pkgrel=1
pkgdesc="The Linux Sina Weibo Client"
arch=('any')
url="https://github.com/WeCase/WeCase"
license=('GPL3')
depends=('python>=3' 'pyqt>=4' 'python-notify2')
makedepends=('git')
provides=('wecase')
conflicts=('wecase')
source=('WeCase::git://github.com/WeCase/WeCase.git' 'sinaweibopy::git://github.com/WeCase/sinaweibopy.git' 'launcher.sh')
md5sums=('SKIP' 'SKIP' 'b64664a8ebb1ae16e2ebc6aefa2f589f')

pkgver() {
  cd "$srcdir/WeCase"
  git describe --always | sed 's|-|.|g'
}

package() {
  mkdir -p "$pkgdir/usr/lib"
  cp -RP "$srcdir/WeCase" "$pkgdir/usr/lib/WeCase"
  rm -rf "$pkgdir/usr/lib/WeCase/.git"*
  cp "$srcdir/sinaweibopy/weibo.py" "$pkgdir/usr/lib/WeCase/src"
  install -Dm0755 "$srcdir/launcher.sh" "$pkgdir/usr/bin/wecase"
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s "/usr/lib/WeCase/res/img/WeCase 854.png" "$pkgdir/usr/share/pixmaps/WeCase.png"
  mkdir -p "$pkgdir/usr/share/applications"
  ln -s "/usr/lib/WeCase/res/wecase.desktop" "$pkgdir/usr/share/applications/wecase.desktop"
}
