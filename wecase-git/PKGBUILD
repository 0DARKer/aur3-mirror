# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=wecase-git
pkgver=0.04
pkgrel=1
pkgdesc="The Linux Sina Weibo Client"
arch=('any')
url="https://github.com/WeCase/WeCase"
license=('GPL3')
depends=('python>=3' 'pyqt>=4' 'python-dateutil' 'python-notify2')
makedepends=('git')
provides=('wecase')
conflicts=('wecase')
source=('WeCase::git://github.com/WeCase/WeCase.git' 'launcher.sh')
md5sums=('SKIP' 'b64664a8ebb1ae16e2ebc6aefa2f589f')

pkgver() {
  cd "$srcdir/WeCase"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/WeCase"
  git submodule init && git submodule update
}

package() {
  mkdir -p "$pkgdir/usr/lib"
  cp -RP "$srcdir/WeCase" "$pkgdir/usr/lib/WeCase"
  rm -rf "$pkgdir/usr/lib/WeCase/.git"*
  cp "$srcdir/WeCase/sdk/weibo.py" "$pkgdir/usr/lib/WeCase/src"
  install -Dm0755 "$srcdir/launcher.sh" "$pkgdir/usr/bin/wecase"
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s "/usr/lib/WeCase/res/img/WeCase 854.png" "$pkgdir/usr/share/pixmaps/WeCase.png"
  mkdir -p "$pkgdir/usr/share/applications"
  ln -s "/usr/lib/WeCase/res/wecase.desktop" "$pkgdir/usr/share/applications/wecase.desktop"
}
