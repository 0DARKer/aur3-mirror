# Maintainer: rtfreedman  <rob<d0t>til<d0t>freedman< T>googlemail<d0t>com>
#
# The code and the license of this repository is of unkown origin.
# Using it might be a problem in your country - in most others it isn't ;)
#
# If you're unsure, use it only on your own private systems
# and not inside your company or systems facing the public.
#
# This PKGBUILD will be available as long as the repository is public.
#
# For discussions about the git repo, see:
# http://phoronix.com/forums/showthread.php?81642-Native-Linux-Kernel-Module-Is-Out-For-Microsoft-exFAT
#

pkgname=exfat-git
pkgver=33.53c9015
pkgrel=1
url='https://github.com/rxrz/exfat-nofuse'
pkgdesc='Native kernel support for extended fat support'
license=('unknown')
#
# build for kernel major
_major=$(uname -r | cut -d '.' -f-2)
_extramodules=extramodules-${_major}-ARCH
#
arch=('i686' 'x86_64')
depends=('linux>=3.8' 'linux<3.11')
makedepends=('linux-headers' 'git')
options=('!strip')
#
install="${pkgname}.install"
source=("${pkgname}::git+https://github.com/rxrz/exfat-nofuse.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "${pkgname}"
  make
  gzip exfat_core.ko exfat_fs.ko
}

package() {
  cd "${pkgname}"
  
  install -d -m0755 \
	"${pkgdir}/usr/lib/modules/${_extramodules}/"
  install -m644 exfat_core.ko.gz exfat_fs.ko.gz \
	"${pkgdir}/usr/lib/modules/${_extramodules}/"
	
  # update exfat-git.install with kernel major
  sed -i "s/_extramodules='.*'/_extramodules='${_extramodules}'/" "${startdir}/${install}"

  # no license yet
# install -Dm755 LICENSE "${pkgdir}"/usr/share/licenses/exfat-git/LICENSE
}
