# Maintainer: Julian Labus <belzebub40k at gmail dot com>

pkgname=tvheadend
pkgver=3.2
pkgrel=1
pkgdesc="TV streaming server for Linux"
arch=('i686' 'x86_64')
url="http://www.lonelycoder.com/hts/tvheadend_overview.html"
license=('GPL3')
depends=('avahi' 'openssl' 'python2' 'systemd')
optdepends=('xmltv: For an alternative source of programme listings')
provides=('tvheadend')
conflicts=('tvheadend-git')
install=tvheadend.install
source=(https://github.com/downloads/tvheadend/tvheadend/${pkgname}-${pkgver}.tar.gz
        tvheadend.service)
md5sums=('4f7faa75c4ea24942c5a12a77ebdcdf0'
         '4af967f0c8afd89ef13092965a5b8c38')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --python=python2.7 --release
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Install binary
  make DESTDIR="$pkgdir/" install

  # Install systemd service
  install -D -m 644 "$srcdir/tvheadend.service" "$pkgdir/usr/lib/systemd/system/tvheadend.service"
}
