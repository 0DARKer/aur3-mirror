# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-gtk3
pkgver=3.2.3
pkgrel=1
pkgdesc="The GTK+ Toolkit (v3) (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gtk.org/"
depends=('lib32-atk' 'lib32-gdk-pixbuf2' 'lib32-libxcomposite'
	 'lib32-libcups' 'lib32-libxcursor' 'lib32-libxdamage'
	 'lib32-libxi' 'lib32-libxinerama' 'lib32-libxrandr'
	 'lib32-pango' 'lib32-cairo' 'lib32-colord' 'gtk3')
makedepends=('gcc-multilib')
options=('!libtool' '!docs')
install=gtk3.install
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/3.2/gtk+-${pkgver}.tar.xz")
sha256sums=('e2cf20f2510ebbc7be122a1a33dd1f472a7d06aaf16b4f2a63eb048cd9141d3d')

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "${srcdir}/gtk+-${pkgver}"
  CXX=/bin/false ./configure --prefix=/usr \
  	--sysconfdir=/etc \
  	--localstatedir=/var \
  	--enable-gtk2-dependency \
  	--disable-schemas-compile \
  	--libdir=/usr/lib32 \
  	--enable-introspection=no
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool  
  make
}

package() {
  cd "${srcdir}/gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}/usr/bin/gtk-query-immodules-3.0{,-32}
  rm -rf "${pkgdir}/usr"/{include,share,bin/gtk3-demo} "${pkgdir}/etc"
}
