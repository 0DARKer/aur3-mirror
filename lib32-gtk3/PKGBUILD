# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-gtk3
pkgver=3.4.1
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (v3) (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.gtk.org/"
depends=('lib32-atk' 'lib32-gdk-pixbuf2' 'lib32-libxcomposite'
	 'lib32-libcups' 'lib32-libxcursor' 'lib32-libxdamage'
	 'lib32-libxi' 'lib32-libxinerama' 'lib32-libxrandr'
	 'lib32-pango' 'lib32-cairo' 'lib32-colord' 'gtk3')
makedepends=('gcc-multilib')
options=('!libtool' '!docs')
install=gtk3.install
_ver=${pkgver%.*}
source=("http://ftp.gnome.org/pub/gnome/sources/gtk+/${_ver}/gtk+-${pkgver}.tar.xz")
sha256sums=('0716b533de0ff2ffdec1076bb85c7a95e2b7b6ac9ece71e84625043ae3eb25f7')

build() {  
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "gtk+-${pkgver}"
  CXX=/bin/false ./configure --prefix=/usr \
  	--sysconfdir=/etc --localstatedir=/var \
  	--enable-gtk2-dependency --disable-schemas-compile \
  	--libdir=/usr/lib32 --enable-introspection=no
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool  
  make
}

package() {
  cd "gtk+-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  rm "${pkgdir}"/usr/bin/gtk3-{demo-application,widget-factory}
  rm -rf "${pkgdir}"/usr/{include,share,bin/gtk3-demo} "${pkgdir}/etc"
}
