_pkgbasename=gtk3
pkgname=lib32-$_pkgbasename
pkgver=3.0.9
pkgrel=1
pkgdesc="The GTK+ Toolkit (v3) (32-bit)"
arch=('x86_64')
url="http://www.gtk.org/"
install=gtk3.install
depends=('lib32-atk' 'lib32-gnutls' 'lib32-heimdal' 'lib32-pango' 'lib32-libcups'
	 'lib32-libxcursor' 'lib32-libxinerama' 'lib32-libxrandr' 'lib32-libxi'
	 'lib32-libxcomposite' 'lib32-libxdamage' 'lib32-gdk-pixbuf2')
makedepends=('gobject-introspection' 'gcc-multilib')
options=('!libtool' '!docs')
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/3.0/gtk+-${pkgver}.tar.bz2)
sha256sums=('88a9dda6f2a23155ac3d7aca6b414ca3b55e2817b46bcc87733f1a407e16678c')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
    cd "${srcdir}/gtk+-${pkgver}"
    # CXX=/bin/false
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libdir=/usr/lib32 \
        --enable-gtk2-dependency \
        --disable-schemas-compile
    make
}

package() {
    cd "${srcdir}/gtk+-${pkgver}"
    make DESTDIR="${pkgdir}" install
    mv ${pkgdir}/usr/bin/gtk-query-immodules-3.0{,-32}
    rm -rf ${pkgdir}/usr/{include,share,bin/gtk3-demo} ${pkgdir}/etc
}
