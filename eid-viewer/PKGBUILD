# Maintainer: Vianney le Clément <vleclement AT gmail·com>
pkgname=eid-viewer
_version=4.0.0
_revision=82
pkgver=$_version.$_revision
pkgrel=1
pkgdesc="Viewer for Belgian Electronic Identity Card"
arch=(any)
url="https://code.google.com/p/eid-viewer/"
license=('LGPL3')
depends=('java-runtime' 'pcsclite')
makedepends=('subversion')
source=(https://$pkgname.googlecode.com/files/$pkgname-$_version-$_revision.zip
        eid-viewer.sh
        eid-viewer.png
        eid-viewer.desktop)
md5sums=('de30345cd62c2a6bc14f3951eceb8fde'
         '6c990e0061b26034f28da243d7c2d151'
         'f0b7ae4b674fbcea348135ea3cd3b4fb'
         '1a464128174ddf4e9496e34bf00f1906')
sha1sums=('ec98fcfdb9157423a16532ffaed4e2a18a77d97d'
          'f8e1c2f35ac8d349574f7a2f030db706f8986adb'
          '8970e9441fdc3f620ebc5071529943354c369cf9'
          '9394b2740ea2b190b7c6e4797528893fb9ca01a5')

package() {
  cd "$srcdir"

  for f in $(find -name '*.jar'); do
    install -Dm644 $f "$pkgdir/usr/share/java/eid-viewer/$(basename $f)"
  done

  install -Dm755 eid-viewer.sh "$pkgdir/usr/bin/eid-viewer"
  install -Dm644 eid-viewer.png "$pkgdir/usr/share/pixmaps/eid-viewer.png"
  install -Dm644 eid-viewer.desktop "$pkgdir/usr/share/applications/eid-viewer.desktop"
}

# vim:set ts=2 sw=2 et:
