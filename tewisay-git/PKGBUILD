pkgname=tewisay-git
_gitname='tewisay'
_gourl='github.com/neeee/tewisay'
pkgver=0.59.cd6e1b9
pkgrel=2
pkgdesc='Cowsay replacement with unicode and partial ansi escape support.'
url='https://github.com/neeee/tewisay'
license=('custom:CC0')
makedepends=('go' 'git' 'asciidoc')
optdepends=('cowsay: default cowfiles')
arch=('i686' 'x86_64' 'ARM')
source=('git://github.com/neeee/tewisay.git'
        'git://github.com/neeee/pflag.git'
        'git://github.com/neeee/rwidth.git')
sha256sums=('SKIP' 'SKIP' 'SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	echo "0.$(git rev-list --count HEAD).$(git describe --always )"
}

prepare() {
	cd "$srcdir/$_gitname"
	git submodule init
	git config submodule.pflag.url "$srcdir/pflag"
	git config submodule.rwidth.url "$srcdir/rwidth"
	git submodule update
}

build() {
	cd "$srcdir/$_gitname"
	go build
	a2x --doctype manpage --format manpage tewisay.1.txt
}

package() {
	cd "$srcdir/$_gitname"
	install -T -Dm755 tewisay "$pkgdir/usr/bin/tewisay"
	install -T -Dm644 _tewisay "$pkgdir/usr/share/zsh/site-functions/_tewisay"
	install -T -Dm644 tewisay.1 "$pkgdir/usr/share/man/man1/tewisay.1"
	install -T -Dm644 COPYING  "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	install -T -Dm644 cows/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING.tewis"
	install -d "$pkgdir/usr/share/cows"
	install -m644 cows/*.cow -t "$pkgdir/usr/share/cows"
	ln -s "tewisay" "$pkgdir/usr/bin/tewithink"
}
