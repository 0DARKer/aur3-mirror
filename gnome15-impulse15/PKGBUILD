# Maintainer: Nuno Araujo <nuno.araujo at russo79.com>
pkgname=gnome15-impulse15
pkgver=0.0.10
pkgrel=1
pkgdesc="Impulse plugin for gnome15"
arch=(any)
url="http://www.gnome15.org/"
license=('GPL')
depends=(gnome15 fftw libpulse)
source=(http://www.gnome15.org/downloads/Gnome15/Optional/$pkgname-$pkgver.tar.gz)
md5sums=('cf173feef24154946d77017801231b37')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  for file in $(find . -type f -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr PYTHON=python2 PYTHON_VERSION=2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
