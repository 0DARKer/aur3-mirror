# Maintainer: josephgbr <rafael.f.f1@gmail.com>

# NOTE: compatible with teeworlds version 0.6.2

pkgname=teeworlds-zcatch
pkgver=0.4.7.4affc155a
_commit=4affc155a906b6091aafc3f9635acc3b8b0aed8a
pkgrel=1
pkgdesc="A customized server where only one Tee will survive"
arch=('i686' 'x86_64')
url="http://www.teeworlds.com/forum/viewtopic.php?id=7958"
license=('custom')
depends=('gcc-libs')
makedepends=('python' 'bam' 'mesa' 'unzip')
provides=('teeworlds-zcatch-server')
conflicts=('teeworlds-zcatch-server')
source=("$pkgname-$pkgver.zip::https://codeload.github.com/Teetime/teeworlds/zip/$_commit"
        'README' 'server.sh.in' 'zcatch.cfg')
md5sums=('789835714dd6f35c3077dff062d61efa'
         '9760f9ca9c669c6a6a78a5824759c552'
         '529146a3b13993770fa414f66c67fa26'
         'faef06e99dac762e0d3d4391a5e92b41')
build() {
  cd teeworlds-$_commit
  bam server_release
}

package() {
  cd teeworlds-$_commit
  install -Dm755 zcatch_srv "$pkgdir"/usr/share/$pkgname/teeworlds_srv
  install -Dm644 ../zcatch.cfg "$pkgdir"/usr/share/$pkgname/server.cfg
  install -Dm755 ../server.sh.in "$pkgdir"/usr/bin/${pkgname}_srv
  sed -i "s/@MODNAME@/$pkgname/" "$pkgdir"/usr/bin/${pkgname}_srv
  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license
  install -Dm644 "$srcdir/README" "$pkgdir"/usr/share/doc/$pkgname/README
}

