# Maintainer: Daniel Kirchner <daniel AT ekpyron DOT org>
pkgname=mingw-w64-glfw
pkgver=2.7.6
pkgrel=2
pkgdesc="A free, open source, portable framework for OpenGL application development (mingw-w64)"
arch=('any')
url="http://www.glfw.org/"
license=('ZLIB')
makedepends=('mingw-w64-gcc')
depends=('mingw-w64-crt')
options=('!strip' '!buildflags')
source=(glfw-${pkgver}.tar.gz::"http://sourceforge.net/projects/glfw/files/glfw/${pkgver}/glfw-${pkgver}.tar.gz/download")
md5sums=('089687e240c23ecc9186cb9fdd8f6a90')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
  	rm -rf "${srcdir}/build-${_arch}"
  	cp -r "${srcdir}/glfw-${pkgver}" "${srcdir}/build-${_arch}"
  	cd "${srcdir}/build-${_arch}"
    make "TARGET=${_arch}-" "PREFIX=/usr/${_arch}" cross-mgw
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    make "TARGET=${_arch}-" "PREFIX=${pkgdir}/usr/${_arch}" cross-mgw-install
    sed -e "s,\@PREFIX\@,/usr/${_arch}," "${srcdir}/build-${_arch}/lib/win32/libglfw.pc.in" > "${pkgdir}/usr/${_arch}/lib/pkgconfig/libglfw.pc"
    ${_arch}-strip -x -g "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
  done

  #copy license file
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cp "${srcdir}/glfw-${pkgver}/COPYING.txt" "${pkgdir}/usr/share/licenses/${pkgname}"
}
