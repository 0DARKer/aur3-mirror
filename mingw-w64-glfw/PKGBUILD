# Maintainer: Daniel Kirchner <daniel AT ekpyron DOT org>
pkgname=mingw-w64-glfw
pkgver=2.7.3
pkgrel=1
pkgdesc="A free, open source, portable framework for OpenGL application development (mingw-w64)"
arch=('i686' 'x86_64')
url="http://www.glfw.org/"
license=('ZLIB')
makedepends=('mingw-w64-gcc')
depends=()
options=('!strip')
source=(glfw-2.7.3.tar.gz::"http://sourceforge.net/projects/glfw/files/glfw/2.7.3/glfw-2.7.3.tar.gz/download")
md5sums=('2e2dd9b5dd1a0d22db51c4cbbc5a3b81')

_targetarch32=i686-w64-mingw32
_targetarch64=x86_64-w64-mingw32

build() {
  cd "${srcdir}"
  cp -r "glfw-${pkgver}" "glfw-${pkgver}-32"
  
  cd "glfw-${pkgver}-32"
  make "TARGET=${_targetarch32}-" "PREFIX=/usr/${_targetarch32}" cross-mgw
  
  cd "../glfw-${pkgver}"
  # disable dynamic linking test as it's not working at the moment
  # (maybe dynamic linking isn't working in general at the moment?)
  sed -i -e 's/accuracy.exe defaults.exe dynamic.exe/accuracy.exe defaults.exe/' tests/Makefile.win32.cross-mgw
  make "TARGET=${_targetarch64}-" "PREFIX=/usr/${_targetarch64}" cross-mgw
}

package() {
  cd "${srcdir}/glfw-${pkgver}-32"
  make "TARGET=${_targetarch32}-" "PREFIX=${pkgdir}/usr/${_targetarch32}" cross-mgw-install
  
  cd "../glfw-${pkgver}"
  make "TARGET=${_targetarch64}-" "PREFIX=${pkgdir}/usr/${_targetarch64}" cross-mgw-install

  #copy license file
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  cp "${srcdir}/glfw-${pkgver}/COPYING.txt" "${pkgdir}/usr/share/licenses/${pkgname}"
}
