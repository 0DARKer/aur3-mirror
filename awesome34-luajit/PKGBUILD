# Maintainer: nasedo

pkgname=awesome34-luajit
pkgver=3.4.15
pkgrel=1
pkgdesc='A highly configurable, next generation framework window manager for X (uses luajit)'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=(
  'cairo'
  'dbus'
  'imlib2'
  'libev'
  'libxdg-basedir'
  'luajit'
  'pango'
  'startup-notification'
  'xcb-util'{,-image,-keysyms,-wm}''
  'xorg-xmessage'
)
makedepends=(
  'asciidoc'
  'cmake'
  'docbook-xsl'
  'doxygen'
  'gperf'
  'imagemagick'
  'xmlto'
)
optdepends=(
  'rlwrap: readline support for awesome-client'
  'feh: image background setter for awsetbg'
  'dex: autostart your desktop files'
  'vicious-git: widgets for the Awesome window manager'
)
conflicts=('awesome')
provides=('notification-daemon' 'awesome=3.4')
source=("http://awesome.naquadah.org/download/awesome-$pkgver.tar.xz"
        "luajit2.patch"
        "awesome.desktop"
        "awesomeksm.desktop")
md5sums=('4e3a14bed1e6a0d424fc79aba03d981e'
         '16f68521cfdcbb1d3818fbd78aafb011'
         '0fdbeec43d211c6750041d7e37611a6a'
         '2c3a0d91b7e12aa20a6bafc403a83cff')

build() {
  cd awesome-$pkgver
  patch -p1 < "$srcdir/luajit2.patch"
  make CMAKE_ARGS='-DCMAKE_INSTALL_PREFIX=/usr \
                   -DSYSCONFDIR=/etc \
                   -DLUA_LIBRARIES=/usr/lib/libluajit-5.1.so \
                   -DCMAKE_BUILD_TYPE=RELEASE'
}

package() {
  cd awesome-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/awesome.desktop" \
    "$pkgdir/usr/share/xsessions/awesome.desktop"
  # For KDE users
  install -Dm644 "$srcdir/awesomeksm.desktop" \
    "$pkgdir/usr/share/apps/ksmserver/windowmanagers/awesome.desktop"
}

