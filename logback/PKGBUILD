# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

pkgname=logback
pkgver=1.0.7
pkgrel=1
pkgdesc="Logback is java logging framework. Logback is intended as a successor to the popular log4j project."
arch=('any')
url="http://logback.qos.ch/"
license=('LGPL2.1')
depends=('java-environment' 'slf4j')
optdepends=('groovy')
source=(${url}dist/${pkgname}-${pkgver}.tar.gz setClasspath.sh)

package() {
	cd ${srcdir}

	install -D -m644 setClasspath.sh ${pkgdir}/usr/share/java/${pkgname}/setClasspath.sh

	cd ${pkgname}-${pkgver}

	for i in access classic core; do
		install -m644 ${pkgname}-${i}-${pkgver}.jar ${pkgdir}/usr/share/java/${pkgname}
	done
	for i in ${pkgdir}/usr/share/java/${pkgname}/*.jar; do
		_filename=${i##*\/}
		ln -s ${_filename} ${pkgdir}/usr/share/java/${pkgname}/${_filename/-${pkgver}/}
	done
}

md5sums=('383d95926e239b30240f22eafea4bef9'
         '57e5bfeb3167f070f94e9e00bc82140f')
sha256sums=('46d82d16c18d680df17fa2c95969b21bf5186883888a263f0798d6695665faf8'
            'a02ace4b6fa3c460a1c6a18758969ef3ce9f3d0a6567ab47c47ef19ed2afd793')
