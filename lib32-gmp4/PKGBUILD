#Contributor:Xemertix <arch88(at)katamail(dot)com>
#based on allan pkgbuild
# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

pkgname=lib32-gmp4
pkgver=4.3.2
pkgrel=2
pkgdesc="Legacy version of gmp. Provides libgmp.so.3 (32 bit)"
arch=('x86_64')
url="http://gmplib.org/"
depends=('lib32-gcc-libs' 'sh')
makedepends=('gcc-multilib')
license=('LGPL3')
options=(!libtool)
source=(ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.bz2)
md5sums=('dd60683d7057917e34630b4a787932e8')

build() {
  cd "${srcdir}"/gmp-${pkgver}
  
  ./configure --prefix=/usr --enable-cxx --libdir=/usr/lib32 \
  		--includedir=/usr/lib32/gmp \
  		ABI='32' CC='gcc -m32' CXX='g++ -m32' \
		PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  #Put gmp.h in the same folder as gmpxx.h
  sed -i 's/$(exec_prefix)\/include/$\(includedir\)/' Makefile
  
  make
}

check() {
  #step highly recommended by the upstream
  cd "${srcdir}"/gmp-${pkgver}
  make check

}

package() {
  cd "${srcdir}"/gmp-${pkgver}
  
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share
}
