# Author: Alexey Stukalov <astukalov@gmail.com>
pkgname=zorba-csv-module
pkgver=2.9.1
pkgrel=1
pkgdesc="Zorba XQuery processor CSV module."
url="http://www.zorba-xquery.com/html/modules/zorba/data-converters/csv"
arch=('i686' 'x86_64')
license=('APACHE')
depends=('zorba')
makedepends=('zorba>=2.9.1' 'cmake>=2.6.4')

source=(http://launchpad.net/zorba/trunk/2.9/+download/zorba_modules-$pkgver.tar.gz)
md5sums=('20805ce7985e883201bb2b6332418129')

prepare() {
  cd $srcdir

  mkdir build
}

build() {
  msg "Building..."
  cd $srcdir/build

  msg2 Configuring...
  cmake -Wno-dev \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} -std=gnu++11 -Wno-c++0x-compat" \
        -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
        $srcdir/zorba_modules/csv

  msg2 Compiling...
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}

