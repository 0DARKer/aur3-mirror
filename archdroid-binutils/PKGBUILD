# Maintainer: AlexanderR <alexander r at gmx com>

_target=arm-unknown-linux-gnueabi

pkgname=archdroid-binutils
pkgver=2.22
pkgrel=2
_date=20120323
pkgdesc="A set of programs to assemble and manipulate binary and object files, used by Droid Overlay project."
arch=('i686' 'x86_64')
license=('GPL')
options=('!libtool' '!distcc' '!ccache' '!buildflags')
url="https://www.gnu.org/s/binutils/"
depends=('glibc>=2.16' 'zlib')
groups=('archdroid')
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2)
md5sums=('ee0f10756c84979622b992a4a61ea3f5')

build() {
  cd "$srcdir"
  mkdir binutils-build && cd binutils-build

  "$srcdir"/binutils-${pkgver}/configure --prefix=/usr \
	--program-prefix=$_target- \
	--target=$_target \
	--enable-ld=default --enable-gold \
	--enable-shared --enable-threads \
	--enable-plugins --disable-nls \
	--with-sysroot=/fhs --disable-werror

  make configure-host
  make
}

package() {
  cd "$srcdir"/binutils-build

  DESTDIR="$pkgdir" make install

  rm "$pkgdir"/usr/lib/libiberty.a
  rm -r "$pkgdir"/usr/share

  # see #32101
  mkdir -p "$pkgdir"/usr/$CHOST/bin/
  for binutil in ar as ld ld.bfd nm objcopy objdump ranlib strip readelf; do
    ln -s ../../bin/$binutil "$pkgdir"/usr/$CHOST/bin/$binutil
  done
}
