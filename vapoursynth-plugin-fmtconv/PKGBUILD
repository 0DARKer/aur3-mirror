# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=fmtconv
pkgname=vapoursynth-plugin-${_plug}
pkgver=r13
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug}"
arch=('i686' 'x86_64')
url="http://forum.doom9.org/showthread.php?t=166504"
license=('custom:WTFPL')
depends=('vapoursynth')
source=("http://ldesoras.free.fr/src/vs/${_plug}-${pkgver}.zip")
sha1sums=('bfbf0a234a134c4406765c674e9fbc26b0818bba')

prepare() {
  rm -fr src/VapourSynth.h
  cd build/unix/
  chmod +x autogen.sh

  ./autogen.sh
}

build() {
  cd build/unix
  CPPFLAGS="${CPPFLAGS} -I. $(pkg-config --cflags vapoursynth)" ./configure --libdir=/usr/lib/vapoursynth
  make
}

package(){
  make -C build/unix DESTDIR=${pkgdir} install
  install -d "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/"
  install -m644 "${srcdir}/doc/"*.{png,html,css} "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/"
  install -Dm644 "${srcdir}/doc/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

