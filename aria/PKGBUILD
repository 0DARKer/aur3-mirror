# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=aria
pkgver=1.0.0
pkgrel=3
pkgdesc="Aria is one of the best download managers ever developed for Linux."
arch=('i686' 'x86_64')
url="http://aria-rpm.vookimedlo.cz/Home"
license=('GPL2')
depends=('gtk' 'openssl')
makedepends=('gettext' 'rpmextract')
source=(http://prdownloads.sourceforge.net/aria-rpm/$pkgname-$pkgver-1.fc12.src.rpm)
md5sums=('e4d8d20cfbce2f931fd889ca6eb31b96')

prepare() {

  # Extract source RPM
  rpmextract.sh aria-1.0.0-1.fc12.src.rpm
  cd $srcdir
  tar -jxvf aria-1.0.0.tar.bz2
  tar -jxvf icons.tar.bz2

  # Patches for building this oldtimer
  for pa in 04_fix_cxxflags 05_fix_token_pasting 06_specify_file_encoding 07_work_around_libstdc++ 08_bounds_checking_fix \
            09_gcc4 10_gcc41 11_sessions-size_type 100_export_url 101_translation 102_home_etc 103_gcc44
  do
    patch -p0 -i $pa
  done

  # zlib fix
  sed -i 's|gzread(infile|gzread((gzFile)infile|;s|gzclose(infile|gzclose((gzFile)infile|' $pkgname-$pkgver/src/RetrieveHTTP.cc

  # Configure patches
  cp config.guess config.sub install-sh missing mkinstalldirs $pkgname-$pkgver

  # Additional languages (Chinese and Dutch)
  cp nl.po zh_CN.po $pkgname-$pkgver/po
  cd $pkgname-$pkgver  
  sed -i 's|zh_TW|zh_TW zh_CN nl|' configure

  # Configure 
  ./configure

  # Patch infinite loopage
  sed -i 's|&& CONFIG_FILES=$@ CONFIG_HEADERS= $(SHELL) ./config.status|test|' Makefile
}

build() {
  cd $srcdir/$pkgname-$pkgver

 # Build at full speed
  make -j$(cat /proc/cpuinfo | grep processor | wc -l)
}

package() {
  cd $srcdir/$pkgname-$pkgver
 
  # Binary
  install -Dm755 src/aria $pkgdir/usr/bin/aria

  # Languages
  cd po/
  for lang in cs de es fr hu it pl ru ja zh_TW zh_CN nl
  do
    install -Dm644 $lang.gmo $pkgdir/usr/share/locale/$lang/LC_MESSAGES/aria.mo
  done

  # Resources
  cd ../resources
  mkdir $pkgdir/usr/share/aria
  cp * $pkgdir/usr/share/aria
  rm $pkgdir/usr/share/aria/Make*

  # Desktop icon
  cd $srcdir/icons
  install -Dm644 aria.desktop $pkgdir/usr/share/applications/aria.desktop
  sed -i 's_Icon=aria_Icon=/usr/share/pixmaps/aria.png_' $pkgdir/usr/share/applications/aria.desktop
  install -Dm644 aria_128.png $pkgdir/usr/share/pixmaps/aria.png
}