# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Jonny Gerold <jonny@fsk141.com>

pkgname=perl-mp3-tag
pkgver=1.13
pkgrel=1
pkgdesc='Module for reading tags of MP3 audio files.'
arch=('i686' 'x86_64')
url='http://search.cpan.org/~ilyaz/MP3-Tag/'
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/I/IL/ILYAZ/modules/MP3-Tag-${pkgver}.tar.gz")
md5sums=('1eea17c9c8a03433634eb37fc16311d3')

build() {
  cd "${srcdir}/MP3-Tag-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/MP3-Tag-${pkgver}"

  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
