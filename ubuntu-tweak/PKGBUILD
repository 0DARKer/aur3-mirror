# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-tweak
pkgver=0.8.4
pkgrel=1
pkgdesc="An easy configuration tool for system and desktop settings"
arch=('any')
url="http://ubuntu-tweak.com/"
license=('GPL')
groups=('unity-extra')
depends=('compiz-ubuntu' 'python2' 'gconf' 'libunique3' 'hicolor-icon-theme' 'python2-defer' 'python2-lxml')
# Also depends on polkit-gnome or polkit-qt
makedepends=('python2-distutils-extra' 'python2-distribute')
options=(!emptydirs)
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/${pkgver%.*}.x/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
        '01_arch_linux.patch')
sha512sums=('008a9dd2e63739dd7f9b3f182dbf0793805041207885c850d12994644f1ce2a6a36873d105559562910f3e832cc349fadf02261fc12019cbda69b6449924320e'
            '63db73252d31dd9316eae983a8fd4707be73a9edcdfdf84a093e132c0ac5f9e59224d48fc588b5014760218e4a05d58a1279639f07aa7feb04d7596784469a9d')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  rm -v data/ui/appcenter.ui                         \
        data/ui/appspage.ui                          \
        data/ui/loginsettings.ui                     \
        data/ui/sourcecenter.ui                      \
        data/ui/sourceeditor.ui                      \
        data/ui/updatemanager.ui                     \
        data/ui/uturl.ui                             \
        data/uturl                                   \
        data/uturl.schemas.in                        \
        ubuntutweak/admins/appcenter.py              \
        ubuntutweak/admins/sourcecenter.py           \
        ubuntutweak/admins/sourceeditor.py           \
        ubuntutweak/apps/__init__.py                 \
        ubuntutweak/clips/updateinfo.py              \
        ubuntutweak/common/sourcedata.py             \
        ubuntutweak/janitor/aptcache_plugin.py       \
        ubuntutweak/janitor/autoremoval_plugin.py    \
        ubuntutweak/janitor/oldkernel_plugin.py      \
        ubuntutweak/janitor/packageconfigs_plugin.py \
        ubuntutweak/janitor/softwarecenter_plugin.py \
        ubuntutweak/module/taskinstall.py            \
        ubuntutweak/module/updatemanager.py          \
        ubuntutweak/network/utdata.py                \
        ubuntutweak/network/xmlrpc.py                \
        ubuntutweak/policykit/widgets.py             \
        ubuntutweak/tweaks/loginsettings.py          \
        ubuntutweak/utils/package.py                 \
        ubuntutweak/utils/ppa.py
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # A patch created by me that
  #  - Removes any apt/dpkg/deb related functions
  #  - Removes any checks and references to other distros and replaces them
  #    with something appropriate for Arch Linux
  #  - Remove any features that require root (only Login manager right now)
  #    since they use a PolicyKit API exposed by aptdaemon.
  #  - Remove code that handles ut:// URL's
  patch -Np1 -i "${srcdir}/01_arch_linux.patch"

  python2 setup.py install --root="${pkgdir}/" --optimize=1

  # Python 2 fix
  sed -i 's|\(#!.*bin.*python$\)|\12|g' $(grep -rl '#!.*python[ ]*$' "${pkgdir}")
}

# vim:set ts=2 sw=2 et:
