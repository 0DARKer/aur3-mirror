# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-tweak
pkgver=0.8.3
pkgrel=100
pkgdesc="An easy configuration tool for system and desktop settings"
arch=('any')
url="http://ubuntu-tweak.com/"
license=('GPL')
groups=('unity-extra')
# python-defer is a split package. We actually need python2-defer.
depends=('compiz-ubuntu' 'python2' 'gconf' 'libunique3' 'hicolor-icon-theme' 'python-defer' 'python2-lxml')
true && depends=('compiz-ubuntu' 'python2' 'gconf' 'libunique3' 'hicolor-icon-theme' 'python2-defer' 'python2-lxml')
# Also depends on polkit-gnome or polkit-qt
makedepends=('python2-distutils-extra' 'python2-distribute')
options=(!emptydirs)
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/${pkgver%.*}.x/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
        '01_arch_linux.patch')
sha512sums=('7acac4eb77d871d0b1af79fef0fef0aa97c8e9c576ec81a705c6fe6a21f8a3ec72d33dc321bde26e9d8ae873c436c0c5d99bb21c99c37835d4d3369b3a2a373b'
            '6604f9877ddb0d46b0885713e6d942676341e07a7f07c165e9172f8cb54c54bca17d045fe90ecdaf7c39f6307b306d245425f30e694f683468b5d0b1184be59a')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # A patch created by me that
  #  - Removes any apt/dpkg/deb related functions
  #  - Removes any checks and references to other distros and replaces them
  #    with something appropriate for Arch Linux
  #  - Remove any features that require root (only Login manager right now)
  #    since they use a PolicyKit API exposed by aptdaemon.
  #  - Remove code that handles ut:// URL's
  patch -Np1 -i "${srcdir}/01_arch_linux.patch"

  python2 setup.py install --root="${pkgdir}/" --optimize=1

  # Python 2 fix
  sed -i 's|\(#!.*bin.*python$\)|\12|g' $(grep -rl '#!.*python[ ]*$' "${pkgdir}")
}

# vim:set ts=2 sw=2 et:
