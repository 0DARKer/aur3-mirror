# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=ubuntu-tweak
pkgver=0.8.1
pkgrel=100
pkgdesc="An easy configuration tool for system and desktop settings"
arch=('any')
url="http://ubuntu-tweak.com/"
license=('GPL')
groups=('unity-extra')
# python-defer is a split package. We actually need python2-defer.
depends=('compiz-ubuntu' 'python2' 'gconf' 'libunique3' 'hicolor-icon-theme' 'python-defer' 'python2-lxml')
true && depends=('compizconfig-python-ubuntu' 'python2' 'gconf' 'libunique3' 'hicolor-icon-theme' 'python2-defer' 'python2-lxml')
# Also depends on polkit-gnome or polkit-qt
makedepends=('python2-distutils-extra' 'python2-distribute')
options=(!emptydirs)
install=${pkgname}.install
source=("https://launchpad.net/${pkgname}/${pkgver%.*}.x/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
        '01_arch_linux.patch')
sha512sums=('0ddd75cf1fec0f6358f5f23fa4caf34427909e9dccab2d37c616cb947801c69126e1854c2e048732a1a37184b68dc7c3af3f1abb8c9e4cf2cbd171687d1e1706'
            '016fe3ba026f709292a19979f954d51521f33ca8ee34fd64defe07634bc9fecc00a5b05230b8355677bc564857a53e2a02670248c29077f6881fd407ae1b87d1')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # A patch created by me that
  #  - Removes any apt/dpkg/deb related functions
  #  - Removes any checks and references to other distros and replaces them
  #    with something appropriate for Arch Linux
  #  - Remove any features that require root (only Login manager right now)
  #    since they use a PolicyKit API exposed by aptdaemon.
  #  - Remove code that handles ut:// URL's
  patch -Np1 -i "${srcdir}/01_arch_linux.patch"

  python2 setup.py install --root="${pkgdir}/" --optimize=1

  # Python 2 fix
  sed -i 's|\(#!.*bin.*python$\)|\12|g' $(grep -rl '#!.*python[ ]*$' "${pkgdir}")
}

# vim:set ts=2 sw=2 et:
