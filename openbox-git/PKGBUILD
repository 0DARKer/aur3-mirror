# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Lauri JÃ¤ntti <janttilauri@gmail.com>

_pkgname=openbox
pkgname=$_pkgname-git
pkgver=20130115
pkgrel=1
pkgdesc="A window manager for the X11 windowing system"
arch=(i686 x86_64)
url=http://openbox.org/
license=('GPL')
depends=(libsm libxcursor libxinerama libxml2 libxrandr pango startup-notification)
makedepends=(git)
optdepends=('pyxdg: for the xdg-autostart script')
provides=($_pkgname)
conflicts=($_pkgname)
backup=(etc/xdg/openbox/menu.xml
    etc/xdg/openbox/rc.xml)
options=(!libtool)
install=$_pkgname.install

_gitroot=git://git.icculus.org/dana/$_pkgname.git
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone --depth 1 $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    autoreconf -fi
    ./configure \
        --prefix=/usr \
        --with-x \
        --enable-startup-notification \
        --sysconfdir=/etc
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
