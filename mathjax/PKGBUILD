# Maintainer: Kevin Dodd <jesin00@gmail.com>
# Contributor: Tianjiao Yin <ytj000@gmail.com>

pkgname=mathjax
pkgver=2.4
pkgrel=4
pkgdesc='An open source JavaScript display engine for mathematics that works in all modern browsers'
url='http://www.mathjax.org/'
arch=('any')
license=('apache')
provides=('otf-stix')
install='mathjax.install'
options=(!strip libtool staticlibs)
source=("https://github.com/mathjax/MathJax/archive/v${pkgver}-latest.tar.gz")
sha256sums=('f1450bafe63599729df7a879e40127f29cfc265abc4a0626bf9d274ed261cbeb')

package() {
	mkdir -pm755 "$pkgdir/usr/share/licenses/mathjax"
	cd "$pkgdir/usr/share"
	mkdir -m755 fonts javascript
	cp -a "$srcdir/MathJax-$pkgver-latest" javascript/mathjax
	mv javascript/mathjax/fonts fonts/mathjax
	ln -s '../../fonts/mathjax' javascript/mathjax/fonts
	ln -s '../../javascript/mathjax/LICENSE' licenses/mathjax/LICENSE

	# Certain AUR packages depend on the mathjax files existing at
	# /usr/share/mathjax so we have to make this symlink.
	# It might be removed after those packages are updated.
	ln -s 'javascript/mathjax' .
}
