# Maintainer: UnCO <undercooled _at_ lavabit DOT com>
# Contributor: Army
# Contributor: Bastien Dejean <baskerville _at_ lavabit DOT com>

_pkgname=sxiv
pkgname=sxiv-bskv-manga-git
pkgver=v1.1.1.55.g997c851
pkgrel=1
pkgdesc="Simple (or small or suckless) X Image Viewer with manga view mode (-m) (baskerville xft fork)"
arch=('i686' 'x86_64')
url="https://github.com/muennich/sxiv"
license=('GPL2')
depends=('imlib2' 'giflib' 'libxft')
makedepends=('git')
optdepends=(
  'imagemagick: for the example commands in config.def.h'
  'libjpeg-turbo: for the example commands in config.def.h'
  'gimp: for the example commands in config.def.h')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://github.com/muennich/${_pkgname}.git"
        'manga.patch'
        'xft.patch')
md5sums=('SKIP'
         '80cef1e1bd5af5b643ff1b6b5e87c0bc'
         '154b1b0544782297cfc96ec4c6cf2d5d')

pkgver() {
  cd $_pkgname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_pkgname

  patch -p1 -i "$srcdir"/manga.patch
  patch -p1 -i "$srcdir"/xft.patch
  if [ -e "$startdir"/config.h ]; then
    msg "Using custom config.h."
    cp "$startdir"/config.h .
  fi

  make PREFIX="/usr"
}

package() {
  cd $_pkgname
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  install -Dm644 sxiv.desktop "$pkgdir/usr/share/applications/sxiv.desktop"
}

# vim:set ts=2 sw=2 et:
