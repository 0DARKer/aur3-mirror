pkgname=mingw-w64-gmp-static
pkgver=5.0.5
pkgrel=3
pkgdesc="A free library for arbitrary precision arithmetic (mingw-w64, static)"
arch=(any)
url="http://gmplib.org/"
license=("LGPL3")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt)
conflicts=(mingw-w64-gmp)
provides=(mingw-w64-gmp)
options=(!libtool !strip !buildflags)
source=("ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.bz2")
md5sums=('041487d25e9c230b0c42b106361055fe')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/gmp-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-cxx \
      --enable-mpbsd
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.a' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}