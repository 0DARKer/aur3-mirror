# Maintainer: Graeme Gott <graeme@gottcode.org>

pkgname=kapow
pkgver=1.4.4.1
pkgrel=2
pkgdesc="Punch clock to track time spent on projects"
arch=('i686' 'x86_64')
url="http://gottcode.org/${pkgname}/"
license=('GPL3')
if [ -e /usr/bin/qmake ] || [ ! -e /usr/bin/qmake-qt4 ]; then
  depends=('qt5-base')
else
  depends=('qt4')
fi
install=${pkgname}.install
source=(http://gottcode.org/${pkgname}/${pkgname}-${pkgver}-src.tar.bz2)
md5sums=('7197a966710e7e304c6b8d6c3c069328')
sha256sums=('14cf9f3f21aa39e5eb387b944e0e45738874369ce8786b99cc85038f5a225991')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  if [ -e /usr/bin/qmake ]; then
    /usr/bin/qmake PREFIX=/usr
  else
    qmake-qt4 PREFIX=/usr
  fi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/" install
}
