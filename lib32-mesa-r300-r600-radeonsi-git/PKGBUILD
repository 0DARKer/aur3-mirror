# Contributor : Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Lone_Wolf <lonewolf@xs4all.nl>

pkgname=lib32-mesa-r300-r600-radeonsi-git
_gitname=mesa-r300-r600-radeonsi-git
pkgver=61886.7554539
pkgrel=1
_realver=10.2.0-devel
pkgdesc="LIB32 Mesa R300, R600 & Radeon SI - git version."
arch=('x86_64')
depends=('lib32-libxxf86vm' 'lib32-elfutils' 'lib32-libxvmc' 'lib32-libxdamage' 'gcc-multilib' 'lib32-libdrm' 'mesa-r300-r600-radeonsi-git' 'lib32-systemd' 'lib32-libvdpau' 'lib32-llvm-libs-svn' 'lib32-libxshmfence' 'lib32-wayland' 'lib32-libtxc_dxtn')
makedepends=('git' 'lib32-llvm-svn' 'mesa-r300-r600-radeonsi-git' 'lib32-libxshmfence' 'lib32-libvdpau' 'lib32-wayland')
optdepends=('lib32-mesa-demos: glxinfo')
provides=("lib32-mesa=${_realver}" "lib32-ati-dri=${_realver}" "lib32-libgl=${_realver}" "lib32-mesa-libgl=${_realver}" "lib32-libegl=${_realver}" "lib32-libgbm=${_realver}" "lib32-libglapi=${_realver}" "lib32-libgles=${_realver}" "lib32-osmesa=${_realver}" )
conflicts=('lib32-mesa' 'lib32-ati-dri' 'lib32-libgl' 'lib32-mesa-libgl' 'lib32-libegl' 'lib32-libgbm' 'lib32-libglapi' 'lib32-libgles' 'lib32-osmesa')
url="http://mesa3d.sourceforge.net"
license=(custom)
source=("${_gitname}::git://anongit.freedesktop.org./git/mesa/mesa#branch=master"
        'LICENSE')
install=lib32-mesa-r300-r600-radeonsi-git.install
options=(!libtool)
md5sums=('SKIP'
         '5c65a0fe315dd347e09b1f2826a1df5a')
# _gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
# _gitname='mesa'

pkgver() {
  cd "${_gitname}"
    echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "${_gitname}"
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  # for our llvm-config for 32 bit
  export LLVM_CONFIG=/usr/bin/llvm-config32
#  CPPFLAGS="$CPPFLAGS -O2" CFLAGS="-m32" CXXFLAGS="-m32" ./autogen.sh \
    ./autogen.sh \
    --enable-32-bit \
    --libdir=/usr/lib32 \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib32/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,swrast \
    --with-dri-drivers=swrast \
    --with-egl-platforms=x11,drm,wayland \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-dri3 \
    --enable-vdpau \
    --enable-gallium-gbm \
    --enable-xvmc \
    --enable-r600-llvm-compiler \


# Flags used in 10.1.0-3



#    --enable-32-bit \
#    --libdir=/usr/lib32 \
#    --prefix=/usr \
#    --sysconfdir=/etc \
#    --with-dri-driverdir=/usr/lib32/xorg/modules/dri \
#    --with-gallium-drivers=r300,r600,radeonsi,nouveau,swrast \
#    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
#    --with-llvm-shared-libs \
#    --enable-gallium-llvm \
#    --enable-egl \
#    --enable-gallium-egl \
#    --with-egl-platforms=x11,drm \
#    --enable-shared-glapi \
#    --enable-gbm \
#    --enable-glx-tls \
#    --enable-dri \
#    --enable-glx \
#    --enable-osmesa \
#    --enable-gles1 \
#    --enable-gles2 \
#    --enable-texture-float
#    --enable-vdpau
#    --enable-dri3

# additonal flags in this package
# --enable-gallium-gbm        enable optional gbm state tracker (not required for
#                             gbm support in Gallium) [default=auto]
# --enable-xvmc               enable xvmc library [default=auto]
# --enable-r600-llvm-compiler Enable experimental LLVM backend for graphics
#                             shaders [default=disable]

# left out compile flags
# default = auto 
#  --enable-32-bit	       build 32-bit libraries [default=auto]
#  --enable-64-bit	       build 64-bit libraries [default=auto]
#  --disable-driglx-direct     enable direct rendering in GLX and EGL for DRI [default=auto]
#  --enable-va                 enable va library [default=auto]
#
#
# default = enabled/yes
#  --enable-shared[=PKGS]       build shared libraries [default=yes]
#  --enable-fast-install[=PKGS] optimize for fast installation [default=yes]
#  --disable-asm	        disable assembly usage [default=enabled on supported plaforms]
#  --disable-pic                compile PIC objects [default=enabled for shared builds on supported platforms]
#  --enable-llvm-shared-libs   link with LLVM shared libraries [default=enabled]
#
# default = disabled/no
#  --enable-debug          use debug compiler flags and macros [default=disabled]
#  --enable-mangling       enable mangled symbols and library name [default=disabled]
#  --enable-selinux	       Build SELinux-aware Mesa [default=disabled]
#  --disable-opengl	       disable support for standard OpenGL API [default=no]
#  --enable-openvg         enable support for OpenVG API [default=no]
#  --enable-xlib-glx       make GLX library Xlib-based instead of DRI-based [default=disable]
#  --enable-gallium-tests  Enable optional Gallium tests) [default=disable]
#

  make
}

package() {

  cd "${_gitname}" 
  make DESTDIR="${pkgdir}" install

  install -m755 -d ${pkgdir}/usr/lib32/mesa
  # move libgl/EGL/glesv*.so to not conflict with blobs - may break .pc files ?
  mv -v ${pkgdir}/usr/lib32/libGL.so*    ${pkgdir}/usr/lib32/mesa/
  mv -v ${pkgdir}/usr/lib32/libEGL.so*   ${pkgdir}/usr/lib32/mesa/
  mv -v ${pkgdir}/usr/lib32/libGLES*.so* ${pkgdir}/usr/lib32/mesa/

# lib32-mesa-libgl
  ln -s /usr/lib32/mesa/libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so.1.2.0
  ln -s libGL.so.1.2.0	               ${pkgdir}/usr/lib32/libGL.so.1
  ln -s libGL.so.1.2.0                 ${pkgdir}/usr/lib32/libGL.so

  ln -s /usr/lib32/mesa/libEGL.so.1.0.0 ${pkgdir}/usr/lib32/libEGL.so.1.0.0
  ln -s libEGL.so.1.0.0	                ${pkgdir}/usr/lib32/libEGL.so.1
  ln -s libEGL.so.1.0.0                 ${pkgdir}/usr/lib32/libEGL.so

  ln -s /usr/lib32/mesa/libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib32/libGLESv1_CM.so.1.1.0
  ln -s libGLESv1_CM.so.1.1.0	              ${pkgdir}/usr/lib32/libGLESv1_CM.so.1
  ln -s libGLESv1_CM.so.1.1.0                 ${pkgdir}/usr/lib32/libGLESv1_CM.so

  ln -s /usr/lib32/mesa/libGLESv2.so.2.0.0 ${pkgdir}/usr/lib32/libGLESv2.so.2.0.0
  ln -s libGLESv2.so.2.0.0                 ${pkgdir}/usr/lib32/libGLESv2.so.2
  ln -s libGLESv2.so.2.0.0                 ${pkgdir}/usr/lib32/libGLESv2.so
  
  # removing folders/files that are already provided by mesa-r300-r600-radeonsi-git package
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/include

#license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
