# Maintainer: Markus Unterwaditzer <markus@unterwaditzer.net>
pkgname=chrontel-wetab-rpm
pkgver=1.0_11203.1.4.2
pkgrel=1
pkgdesc="HDMI-drivers for the WeTab and similar devices, extracted from the source-rpm files provided by 4tiitoo"
url="http://update.tiitoo.com/source/packages/"

depends=('qt')
arch=('x86_64' 'i686')
license=('BSD')
makedepends=('rpmextract')
install='chrontel.install'

_rpmname="chrontel-${pkgver/_/-}.wetab.src.rpm" # according to the aur guidelines, it's not allowed to use a hyphen in a version number. So, replace it afterwards with a underscore
_rpmextractname="chrontel-1.0"
source=(
  "daemon.sh"
  "daemon_config.sh"
  "Makefile"
  "http://update.tiitoo.com/source/packages/$_rpmname")
noextract=($_rpmname)
md5sums=('942b7c05e8535394312cf43504c2a991'
         '0c1a590b2202f0f41ae45e2b0c2b20c6'
         'f30a85eab8eb05853f5a7abee11afa39'
         '20c65d65452e8f4bab2c26a4de63fa38')

build() {
  cd $srcdir
  rpmextract.sh $_rpmname
  tar xvjf $_rpmextractname.tar.bz2
  cp Makefile $_rpmextractname
  cd $_rpmextractname
  make
}

package() {
  cd $srcdir/$_rpmextractname
  install -Dm755 tiitoo-hdmi-daemon $pkgdir/usr/bin/tiitoo-hdmi-daemon
  install -Dm755 resources/fw7036.bin $pkgdir/lib/firmware/chrontel/fw7036.bin
  install -Dm755 src/License.txt $pkgdir/usr/share/licenses/chrontel/LICENSE
  install -Dm755 ../daemon.sh $pkgdir/etc/rc.d/tiitoo-hdmi
  install -Dm755 ../daemon_config.sh $pkgdir/etc/conf.d/tiitoo-hdmi
}
