# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kf5-attica-git
pkgver=v0.4.2.r14.ga84850f
pkgrel=1
pkgdesc='A Qt library that implements the Open Collaboration Services API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/attica'
license=('LGPL')
depends=('qt5-base')
makedepends=('cmake' 'git')
source=('git://anongit.kde.org/attica')
md5sums=('SKIP')

pkgver() {
  cd attica
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../attica \
    -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DATTICA_ENABLE_TESTS=OFF \
    -DLIB_DESTINATION=/opt/kf5/lib \
    -DQT4_BUILD=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
