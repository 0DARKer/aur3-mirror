# Maintainer: Vin√≠cius dos Santos Oliveira <vini.ipsmaker@gmail.com>
pkgname=tufao
pkgver=1.0
pkgrel=1
pkgdesc="An asynchronous web framework for C++ built on top of Qt"
arch=('i686' 'x86_64')
url="https://github.com/vinipsmaker/tufao"
license=('LGPL2')
depends=('qt5-base')
makedepends=('cmake')
source=("https://github.com/vinipsmaker/tufao/archive/${pkgver}.tar.gz")
md5sums=('ad5d5efd0c2b170510045998a8038231')

build() {
  mkdir "${srcdir}/tufao-${pkgver}/build"
  cd "${srcdir}/tufao-${pkgver}/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd "${srcdir}/tufao-${pkgver}/build"
  make DESTDIR="$pkgdir" install
}
