# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: TheBenj88 <thebenj88 *AT* gmail *DOT* com>
# Maintainer: Ian Thompson <dalrik370 at gmail dot com>

pkgname=openxcom
pkgver=0.9
pkgrel=5
pkgdesc="An open-source reimplementation of the famous X-COM game"
arch=('i686' 'x86_64')
url="http://openxcom.org/"
license=('GPL3')
depends=('sdl_gfx' 'sdl_mixer' 'sdl_image' 'yaml-cpp0.3' 'mesa' 'glu' 'timidity++')
optdepends=('timidity-freepats: game music support')
install="${pkgname}.install"
source=("https://github.com/SupSuper/OpenXcom/tarball/v${pkgver}"
		"${pkgname}.install")
sha256sums=('340ff7292baa9a2b7ce4b244cc6dc0200c1d3b807a59087ef9827bba5c135634'
            '33a412d870d8c1399738b71f772aaa5954d0028a9c42373ca4a27124c154956d')


build() {
  cd "${srcdir}/SupSuper-OpenXcom-5c8b540/src"
  mv Makefile.simple Makefile
  # Fix a problem with yaml-cpp and pkg-config
  sed -i "s_--cflags sdl yaml-cpp\`_--cflags sdl\` -I/usr/include/yaml-cpp_" Makefile
  # Fix a compilation error
  sed -i "s_?=_+=_" Makefile

  make || return 1
}

package() {
  cd "${srcdir}/SupSuper-OpenXcom-5c8b540"

  # Install binary
  install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install data files
  mkdir -p "${pkgdir}/usr/share/${pkgname}/data/"{Language,SoldierName,Shaders,Ruleset}
  install -m644 bin/data/Language/* "${pkgdir}/usr/share/${pkgname}/data/Language"
  install -m644 bin/data/SoldierName/* "${pkgdir}/usr/share/${pkgname}/data/SoldierName"
  install -m644 bin/data/Shaders/* "${pkgdir}/usr/share/${pkgname}/data/Shaders"
  install -m644 bin/data/Ruleset/* "${pkgdir}/usr/share/${pkgname}/data/Ruleset"
  install -m644 bin/data/README.txt "${pkgdir}/usr/share/${pkgname}"

  # Install readme
  install -Dm644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"

  # Install desktop files
  install -Dm644 res/linux/openxcom.desktop "${pkgdir}/usr/share/applications/openxcom.desktop"
  install -Dm644 res/linux/icons/openxcom.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/openxcom.svg"
  install -Dm644 res/linux/icons/openxcom_48x48.png "${pkgdir}/usr/share/icons/hicolor/48x48/apps/openxcom.png"
  install -Dm644 res/linux/icons/openxcom_128x128.png "${pkgdir}/usr/share/icons/hicolor/128x128/apps/openxcom.png"
  install -Dm644 res/linux/icons/openxcom_48x48.png "${pkgdir}/usr/share/pixmaps/openxcom.png"
}
