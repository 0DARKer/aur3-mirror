# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=openxcom
pkgver=0.3
pkgrel=1
pkgdesc="An open-source reimplementation of the famous X-COM game"
arch=('i686' 'x86_64')
url="http://openxcom.org/"
license=('GPL3')
depends=('gcc-libs' 'sdl_gfx' 'sdl_mixer' 'timidity++' 'yaml-cpp')
install="${pkgname}.install"
source=("https://nodeload.github.com/SupSuper/OpenXcom/zipball/v${pkgver}")
md5sums=('2c536714467925f5e74e268cabd5097f')

build() {
  cd "${srcdir}/SupSuper-OpenXcom-1603231/src"

  # Fix a problem with yaml-cpp and pkg-config
  sed -i "s_--cflags sdl yaml-cpp\`_--cflags sdl\` -I/usr/include/yaml-cpp_" Makefile
  # Fix a compilation error
  sed -i "s_?=_+=_" Makefile

  make || return 1
}

package() {
  cd "${srcdir}/SupSuper-OpenXcom-1603231"

  # Install binary
  install -Dm755 "bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install data files
  mkdir -p "${pkgdir}/usr/share/${pkgname}/"{Language,SoldierName}
  install -m644 bin/DATA/Language/* "${pkgdir}/usr/share/${pkgname}/Language"
  install -m644 bin/DATA/SoldierName/* "${pkgdir}/usr/share/${pkgname}/SoldierName"
  install -m644 bin/DATA/README.txt "${pkgdir}/usr/share/${pkgname}"

  # Install readme
  install -Dm644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
}
