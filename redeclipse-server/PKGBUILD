# Maintainer: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=redeclipse-server
pkgver=1.3.1
pkgrel=1
pkgdesc="A single-player and multi-player first-person ego-shooter, built as a total conversion of Cube Engine 2 - server only"
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('custom' 'CCPL' 'ZLIB' 'MIT')
depends=('gcc-libs' 'zlib')
conflicts=('redeclipse')
source=(http://downloads.sourceforge.net/redeclipse/redeclipse_${pkgver}_nix_bsd.tar.bz2)

sha1sums=('4fcfc1dc19709c0e30d929a0d2b9b00f25fe60b5')

build() {
  cd "$srcdir/redeclipse/src"
  make server
}

package() {
  cd "$srcdir/redeclipse"

  make -C src DESTDIR="$pkgdir" prefix=/usr system-install-server

  install -Dm644 doc/license.txt   "$pkgdir/usr/share/licenses/redeclipse/LICENSE"
  install -Dm644 doc/trademark.txt "$pkgdir/usr/share/licenses/redeclipse/TRADEMARK"

  install -Dm644 doc/examples/servexec.cfg "$pkgdir/usr/share/doc/redeclipse/examples/servexec.cfg"
  install -Dm644 doc/examples/servinit.cfg "$pkgdir/usr/share/doc/redeclipse/examples/servinit.cfg"
}
