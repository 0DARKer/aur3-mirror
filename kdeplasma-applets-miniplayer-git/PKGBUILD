#Maintainer: Gustavo alvarez <sl1pkn07@gmail.com>

pkgname=kdeplasma-applets-miniplayer-git
pkgver=v2.5.18.g5ca780f
pkgrel=1
pkgdesc="A plasmoid for playing multimedia. (GIT version)"
arch=('i686' 'x86_64')
url="http://www.kde-look.org/content/show.php/Mini+Player?content=95501"
license=('GPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'git')
provides=('kdeplasma-applets-miniplayer')
conflicts=('kdeplasma-applets-miniplayer')

source=('mini-player::git://github.com/Emdek/plasmoid-mini-player.git')
md5sums=('SKIP')
_gitname="mini-player"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"

  cmake . -DCMAKE_INSTALL_PREFIX="$(kde4-config --prefix)"
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
