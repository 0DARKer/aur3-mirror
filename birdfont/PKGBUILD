# Maintainer: Matej Lach <matej.lach@gmail.com>

pkgname=birdfont
pkgver=0.34
pkgrel=1
pkgdesc='A free font editor that lets you create vector graphics and export TTF, EOT & SVG fonts'
arch=(i686 x86_64)
url='http://birdfont.org/'
license=('GPL3')
depends=(
	'vala' 'libgee' 'glib2' 'gtk2' 'libxml2' 'webkitgtk2' 'python2' 'gettext' 'desktop-file-utils'
	'hicolor-icon-theme')
makedepends=('vala' 'gettext' 'libgee')
source=(
	"http://birdfont.org/releases/${pkgname}-${pkgver}.tar.gz"
	'http://www.unicode.org/Public/UNIDATA/NamesList.txt')
sha512sums=(
	'dc11fe6efa0a73464711a680468da4bfa8c41d0b3f3d5f823de6e3957cfa354deab2e09a980c2abc59aea8244db8205296e1146fcba16ff83f10d4a7b0302d8b'
	'7138b96f446c72261dafa26f6eec446ac7134ba09bba75b0fa258465dc43b45cab1d9a4d90ff7b508b654469789f76329279e192fa6de6111b80d8140e75794e')

install=birdfont.install

prepare() {
	cd "${srcdir}/$pkgname-$pkgver"
	sed -i -e 's/gee-1\.0/gee-0.8/g' ./scripts/build.py	
}

build() {
	cd "${srcdir}/$pkgname-$pkgver"

	python2 ./configure -p /usr
	python2 ./scripts/linux_build.py -p /usr
}

package() {
	cd "${srcdir}/$pkgname-$pkgver"
	
	install -d -m 0755 "${pkgdir}/usr/share/unicode"
	python2 ./install.py -d "${pkgdir}" -n /share/man -l /lib
	install -m 0644 "${srcdir}/NamesList.txt" "${pkgdir}/usr/share/unicode"
}
