# Maintainer: Kevin <arc.mission at gmail>
pkgname=esteid-browser-plugin-hg-svn
_pkgname=esteid-browser-plugin
pkgver=3774
pkgrel=2
pkgdesc="Estonian ID-card Firefox plugin & extension (Community dev version)"
arch=('x86_64' 'i686')
url="http://code.google.com/p/esteid"
license=('LGPL')
depends=('boost' 'smartcardpp-svn' 'firefox>=3.6' 'gtkmm')
makedepends=('cmake' 'subversion' 'mercurial' 'unzip')
conflicts=('sk-esteidpkcs11loader-svn' 'sk-esteidfirefoxplugin-svn')

_svntrunk=https://esteid.googlecode.com/svn/$_pkgname/trunk
_svnmod=$_pkgname

_hgroot=https://firebreath.googlecode.com/hg/
_hgrepo=firebreath

build() {
  cd "$srcdir"
  
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo
  fi

  msg "Mercurial checkout done or server timeout"

  if [ -d $srcdir/$_hgrepo/projects/$_svnmod/.svn ]; then
    (cd $srcdir/$_hgrepo/projects/$_svnmod && svn up -r $pkgver)
  else
    mkdir $srcdir/$_hgrepo/projects
    cd $srcdir/$_hgrepo/projects
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build/"
  sed -i 's|(bits == 64) ? "/usr/lib64/" :||g' projects/esteid-browser-plugin/Mozilla/chrome/content/config.js
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DLIB_SUFFIX="" -DWITH_SYSTEM_BOOST=YES -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "$srcdir/$_hgrepo-build/"
  make DESTDIR="$pkgdir/" install
  mkdir -p $pkgdir/usr/lib/firefox-3.6/extensions/\{aa84ce40-4253-11da-8cd6-0800200c9a66\}/
  unzip *.xpi -d $pkgdir/usr/lib/firefox-3.6/extensions/\{aa84ce40-4253-11da-8cd6-0800200c9a66\}/
}
