 # Maintainer: Manuel Conzelmann <manuel.conzelmann@online.de>  
 
 # You have to register at unrealengine.com and in this profile you have to enter your github accountname, so that epicgames grants you access to the repository, otherwise makepkg will fail.
 
pkgname=ue4-git
pkgver=4.7.1.linux.r20.g4d6e621
pkgrel=1
pkgdesc="Unreal Engine 4 is a suite of integrated tools for game developers to design and build games, simulations, and visualizations."
arch=('x86_64')
url="https://www.unrealengine.com/"
license=('custom')
groups=()
depends=('qt5-base' 'sdl2' 'xorg-server' 'libpng' 'harfbuzz' 'graphite')
makedepends=('git' 'mono' 'clang' 'dos2unix' 'cmake' 'python' 'qt5-base' 'sdl2')
optdepends=( 'kdevelop')
provides=('ue4-git')
conflicts=('ue4')
#replaces=()
backup=()
options=()
install=
changelog=
source=("${pkgname}::git+https://github.com/3dluvr/UnrealEngine"
        'clean_method.txt'
        'CMakeLists.patch'
        'ue4.sh'
        'Setup.patch')
noextract=()
sha256sums=('SKIP'
            'e0b84b3fdd182a75d7a1c2c5ef65c4f9e941db71a204c0b3f6c1e14418e3a3c5'
            '0e3e989a208dff033daf178ef5170532037fc392fc513ab4987e3aad69bbe3e8'
            '93c664e50d92648303272d59590a9bb293b9c511662d5cda17665c6f1cdc3a32'
            '3f2bca4a215c3a40997fee077df342e512b56ceecbe6101eb6f41a6ffbe30bb8') #autofill using updpkgsums

pkgver() {
  cd "$srcdir/$pkgname"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  echo "You will need at least 20 GB of free disk space and a relatively powerful machine!"
  echo ""
  cd "$srcdir/$pkgname/Engine/Build/BatchFiles/Linux"
  patch < "$srcdir/Setup.patch"
  cd "$srcdir/$pkgname"
  ./Setup.sh
  cd "$srcdir/$pkgname"
  ./GenerateProjectFiles.sh
  sed -i "/GenerateProjectFiles.sh/r ${srcdir}/clean_method.txt" 'Makefile'
  echo ""
  echo "clang-bug, workaround: execute the following command:"
  echo ">$ mkdir ~/bin/ && cd ~/bin/ && ln -s /bin/ld.bfd ./ld.gold"
  echo ""
  echo "Add the following line to your .bashrc (or .zshrc if you use zsh) usually found in the home folder (hidden):"
  echo 'export PATH=$HOME/bin:$PATH'
  echo "press ENTER to continue"
  read
  echo ""
  make ShaderCompileWorker-Linux-Shipping UnrealLightmass-Linux-Shipping UnrealPak-Linux-Shipping UE4Editor-Linux-Shipping
  #make UE4Editor-Linux-Debug
}

package() {
  cd "$srcdir/$pkgname"
  install -d -m755 -o root -g root "$pkgdir/opt/$pkgname"
  mv Engine $pkgdir/opt/$pkgname/
  rm -rf $pkgdir/opt/$pkgname/Engine/Source
  mv FeaturePacks $pkgdir/opt/$pkgname/
  mv Templates $pkgdir/opt/$pkgname/
  chown -R root:root $pkgdir/opt/$pkgname
  install -D -m755 -o root -g root $srcdir/ue4.sh "${pkgdir}/usr/bin/ue4.sh"
  install -D -m644 LICENSE.pdf -o root -g root "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.pdf"
  install -d -m755 -o root -g users "$pkgdir/opt/$pkgname/Engine/Intermediate"
  chmod 775 $pkgdir/opt/$pkgname/Engine/Intermediate
  chown -R root:users $pkgdir/opt/$pkgname/Engine/DerivedDataCache
  chmod 775 $pkgdir/opt/$pkgname/Engine/DerivedDataCache/*
  chown -R root:users $pkgdir/opt/$pkgname/Engine/Saved
  chmod 775 $pkgdir/opt/$pkgname/Engine/Saved/*
  #make DESTDIR="$pkgdir/opt/$pkgname/" install
}