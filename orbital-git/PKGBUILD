# Maintainer: Joel Teichroeb <joel@teichroeb.net>

pkgname=orbital-git
pkgver=v0.1.r3.g10590cb
pkgrel=1
pkgdesc="A Wayland shell client"
arch=(i686 x86_64)
url="https://github.com/giucam/orbital"
license=('LGPL')
depends=('weston' 'nuclear-git' 'qt5-tools')
makedepends=('git' 'cmake')
source=(git://github.com/giucam/orbital.git)
sha1sums=('SKIP')

pkgver() {
	cd orbital
	git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
	cd orbital
	mkdir build
}

build() {
	cd orbital/build

	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd orbital/build

	make DESTDIR=${pkgdir} install
}
