# Maintainer: Gary DeLaney <gld1982ltd@gmail.com>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=orbital-git
pkgver=0.1.r291.g2fde33c
pkgrel=2
pkgdesc="A Wayland shell client"
arch=(i686 x86_64)
url="https://github.com/giucam/orbital"
license=('LGPL')
depends=('weston-orbital-git' 'qt5-tools' 'qt5-wayland' 'oxygen-icons')
optdepends=('solid')
makedepends=('git' 'cmake')
source=(git://github.com/giucam/orbital.git)
sha1sums=('SKIP')

pkgver() {
	cd orbital
	git describe --long | sed 's/v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd orbital
	mkdir -p build
}

build() {
	cd orbital/build
	cmake -DCMAKE_INSTALL_PREFIX=/usr ..
	make
}

package() {
	cd orbital/build

	make DESTDIR=${pkgdir} install
}
