# Submitter and contributer: mmm 
# Maintainer: jellysheep

pkgname=gpuocelot-svn
pkgver=2235
pkgrel=1
pkgdesc="Ocelot emulation allows CUDA programs to be executed on NVIDIA/AMD GPUs and x86-CPUs at full speed without recompilation"
arch=('i686' 'x86_64')
url="https://code.google.com/p/gpuocelot/"
license=('BSD')
depends=('cuda' 'boost' 'llvm' 'glew')
makedepends=('gcc>=4.5' 'svn' 'scons' 'flex' 'bison' 'mesa' 'python2' 'grep' 'coreutils')
provides=('gpuocelot')
source=('lib_mt_fix.patch'
	'ptxgrammar_fix.patch'
	"${pkgname}::svn+http://gpuocelot.googlecode.com/svn/trunk/ocelot"
	"${pkgname}/.release_build::svn+http://llvm.org/svn/llvm-project/llvm/tags/RELEASE_32/final/include")
md5sums=('21344ced287a146d5edd90fce5fc75cd'
	'a6f9854431258e50993d0d2a37bf8f85'
	'SKIP' 'SKIP')

pkgver() {
  cd $pkgname
  svnversion | tr -d [A-z]
}

build()
{
  cd $srcdir/$pkgname
  echo "ln -s $srcdir/$pkgname/.release_build/llvm $srcdir/$pkgname/.release_build/llvm/IR"
  ln -s "$srcdir/$pkgname/.release_build/llvm" "$srcdir/$pkgname/.release_build/llvm/IR"

  patch -p0 < $srcdir/lib_mt_fix.patch
  
  CORE_COUNT=$(nproc)
  msg "build using ${CORE_COUNT} cores..."
  
  python2 build.py -j ${CORE_COUNT} --install -p$srcdir/build || (  
    msg "resolving error in ptxgrammar.hpp and trying again..."
    patch -p0 < $srcdir/ptxgrammar_fix.patch
    python2 build.py -j ${CORE_COUNT} --install -p$srcdir/build
  )

}

package()
{
  mv $srcdir/build/* $pkgdir/
  rm -r $srcdir/build
}
