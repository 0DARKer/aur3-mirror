# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Andrea Zucchelli <zukka77@gmail.com>
# Contributor SanskritFritz (gmail)

pkgname=tinyproxy-git
pkgver=20110411
pkgrel=1
pkgdesc='A light-weight HTTP proxy. Transparent mode enabled.'
arch=('i686' 'x86_64')
url='https://banu.com/tinyproxy/'
license=('GPL')
makedepends=('asciidoc' 'git')
provides=('tinyproxy')
conflicts=('tinyproxy')
backup=('etc/tinyproxy/tinyproxy.conf')
source=('tinyproxy.sh')
md5sums=('d5b1915d5072f3b0d7abce09146d0b03')

_gitroot="git://banu.com/tinyproxy.git"
_gitname="tinyproxy"

build() {
        cd "$srcdir"
        msg "Connecting to the GIT repository..."

        if [ -d "$srcdir/$_gitname" ] ; then
                cd $_gitname && git pull origin
                msg "The local files are updated."
        else
                git clone $_gitroot
        fi

        msg "GIT checkout done (who invented that stupid 'server timeout' message? :)"
        msg "Starting make..."

        cd $srcdir/$_gitname

	./autogen.sh
        ./configure --prefix=/usr --sysconfdir=/etc/tinyproxy --localstatedir=/var --enable-transparent
        make
}

package() {
	cd $srcdir/$_gitname
	make DESTDIR="${pkgdir}" install
	install -Dm0755 "${srcdir}/tinyproxy.sh" "${pkgdir}/etc/rc.d/tinyproxy"

	install -dm0755 -o nobody -g nobody "${pkgdir}/var/log/${_gitname}"
	install -dm0755 -o nobody -g nobody "${pkgdir}/var/run/${_gitname}"

	# Provide sane defaults
	sed -i '/^#Listen/a\Listen 127.0.0.1' "${pkgdir}/etc/tinyproxy/tinyproxy.conf"
}
