# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=ruby-kramdown
pkgver=1.6.0
pkgrel=1
pkgdesc='A fast, pure-Ruby Markdown-superset converter'
arch=(any)
url='http://kramdown.gettalong.org'
license=('MIT')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/kramdown-${pkgver}.gem")
noextract=("kramdown-${pkgver}.gem")
sha256sums=('8bdb6aed0244d0fe77b2f5b49c44da0c8d597e7909b9f59052f7846bd1b85170')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install \
    -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "kramdown-${pkgver}.gem"

  install -D -m644 "${pkgdir}/${_gemdir}/gems/kramdown-${pkgver}/COPYING" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
