# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=ruby-kramdown
pkgver=1.5.0
pkgrel=2
pkgdesc='A fast, pure-Ruby Markdown-superset converter'
arch=(any)
url='http://kramdown.gettalong.org'
license=('MIT')
depends=('ruby')
options=(!emptydirs)
source=("https://rubygems.org/downloads/kramdown-${pkgver}.gem")
noextract=("kramdown-${pkgver}.gem")
sha256sums=('159a0651f043d3bb02e3eb108f43a716ac7aff7a2562a5b5899c8eab7c135dd5')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install \
    -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "kramdown-${pkgver}.gem"

  install -D -m644 "${pkgdir}/${_gemdir}/gems/kramdown-${pkgver}/COPYING" \
    "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
