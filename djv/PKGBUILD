# Maintainer: Jonathan Liu <net147@gmail.com>
# Contributer: N30N <archlinux@alunamation.com>
pkgname=djv
pkgver=0.9.0
pkgrel=3
pkgdesc="Movie playback and image processing software for the film and computer animation industries"
url="http://djv.sourceforge.net/"
license=('BSD')
arch=('i686' 'x86_64')
depends=("desktop-file-utils" "ffmpeg" "libjpeg" "libpng" "libtiff" "libquicktime" "openexr" "fltk" "glew" "portaudio")
makedepends=("cmake")
options=("!docs")
install="${pkgname}.install"
source=("${pkgname}_view.desktop"
        "http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}_src.tar.gz"
        "openexr.patch"
        "unistd.patch")
md5sums=('2668ad2635036f33ba615b5dc538b1cf'
         '9c4fbe1395202bb107cf663cc2e02760'
         '60d8e05c325054d591c9656888be3aec'
         'eb4f834fbc4dfb7d337a8d7aaed9ba2b')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/openexr.patch"
    patch -Np1 -i "${srcdir}/unistd.patch"
    sed -i '/enable_testing()/d' CMakeLists.txt
    sed -i '/add_subdirectory(tests)/d' CMakeLists.txt
}

build() {
	cd "${srcdir}"
    rm -fr build
	mkdir build
	cd build
	cmake -D CMAKE_INSTALL_PREFIX:PATH=/usr "../${pkgname}-${pkgver}"
    make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR=${pkgdir} install
	install -D -m644 {"${srcdir}","${pkgdir}/usr/share/applications"}/"${pkgname}_view.desktop"
    install -D -m644 "${srcdir}/${pkgname}-${pkgver}/LEGAL.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LEGAL.txt"
    install -D -m644 "${srcdir}/${pkgname}-${pkgver}/etc/X11/projector32x32.png" "${pkgdir}/usr/share/pixmaps/djv_view.png"
}

# vim: set noet ff=unix
