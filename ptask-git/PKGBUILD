# Maintainer: Robert Orzanna <orschiro@gmail.com>

_pkgname=ptask
pkgname=$_pkgname-git
pkgver=r224.08473de
pkgrel=2
pkgdesc='A GTK+ graphical user interface for managing tasks in taskwarrior'
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url='http://wpitchoune.net/ptask'
license=('GPL2')
depends=('task' 'json-c' 'gtk3' 'xdg-utils')
makedepends=('git' 'help2man' 'asciidoc')
install=$_pkgname.install
source=("git+http://git.wpitchoune.net/$_pkgname.git"
        'task-2.4.0-compat.patch')
sha512sums=('SKIP' '86f345419c5617f79542bd0bb123d2ee3f6546d99727f2cb1d0a2e14a681168d07830d7cdf5f5d41ca50bdffe33c661e4df17f9dcc4588659d595f4dcadf8af9')
conflicts=("$_pkgname")
provides=("$_pkgname")

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_pkgname
  patch -p1 < ../task-2.4.0-compat.patch
}

build() {
  cd $_pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/usr/share
  make ACLOCAL='aclocal-1.15' AUTOMAKE='automake-1.15' clean all
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
}
