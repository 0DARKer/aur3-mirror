# Maintainer: Robert Orzanna <orschiro@gmail.com>

pkgname=ptask-git
pkgver=r214.fdf70aa
pkgrel=1
pkgdesc="A GTK+ graphical user interface for managing tasks in taskwarrior"
arch=('i686' 'x86_64')
conflicts=('ptask')
url="http://wpitchoune.net/ptask"
license=('GPL2')
depends=('task' 'json-c' 'gtk3' 'hicolor-icon-theme')
makedepends=('git' 'help2man')
optdepends=('asciidoc: required to generate html version of NEWS and README'
	 'help2man: required to regenerate manpages automatically')
install=$pkgname.install
source=("$pkgname"::'git://wpitchoune.net/ptask.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "$srcdir/$pkgname"
	./configure --prefix=/usr  --sysconfdir=/usr/share
	make clean all
}

package() {
	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir" install
}
