# Maintainer: xduugu
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>
# Contributor: Mathias Burén <mathias . buren @ gmail . com>

_pkgname=tp_smapi
pkgname=$_pkgname-ck
#pkgname=$_pkgname$(uname -r | sed -r 's/^[^-]+//')
pkgver=0.41
pkgrel=5
pkgdesc="Modules for ThinkPad's SMAPI functionality, for -ck"
arch=('i686' 'x86_64')
url="http://tpctl.sourceforge.net/"
license=('GPL')
depends=('mkinitcpio>=0.6.8' 'module-init-tools>=3.12-2')
makedepends=('linux-ck-headers')
install=tp_smapi.install
source=(https://cloud.github.com/downloads/evgeni/tp_smapi/$_pkgname-$pkgver.tar.gz)
build() {
  # update kernel version in install file
  sed -ri "s/^(kernver=).*$/\1$(uname -r)/" "$startdir/$_pkgname.install"

  cd "$srcdir/$_pkgname-$pkgver"
  make HDAPS=1
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C "/lib/modules/$(uname -r)/build" INSTALL_MOD_PATH="$pkgdir" M="$PWD" modules_install

  # move hdaps.ko into updates to override in-tree kernel module
  cd "$pkgdir/lib/modules/$(uname -r)/"
  mkdir updates
  mv extra/hdaps.ko updates

  # compress kernel modules
  find "$pkgdir" -name "*.ko" -exec gzip -9 {} +
}
md5sums=('63c683415c764568f6bf17c7eabe4752')
sha256sums=('6aef02b92d10360ac9be0db29ae390636be55017990063a092a285c70b54e666')
