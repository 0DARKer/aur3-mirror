# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc-dragonegg-plugin
pkgver=3.1
_gccver=4.7.0
pkgrel=1
pkgdesc="integrates the LLVM optimizers and code generator with the GCC parsers"
arch=('i686' 'x86_64')
url="http://dragonegg.llvm.org/"
license=('GPL')
depends=("gcc=${_gccver}" 'llvm=3.1')
source=(http://llvm.org/releases/3.1/dragonegg-3.1.src.tar.gz{,.sig})
md5sums=('8553229b661412c4bf3ba0ede0d30f0b'
         '422e56c85d04fd7451dae752e47225dd')

_plugindir=/usr/lib/gcc/${CHOST}/${_gccver}/plugin

build() {
  cd $srcdir/dragonegg-${pkgver}.src/
  sed -i "s#\$(shell svnversion -n \$(TOP_DIR))#$pkgver#" Makefile
  make
}

package() {
  cd $srcdir/dragonegg-${pkgver}.src/
  install -Dm755 dragonegg.so $pkgdir/${_plugindir}/dragonegg.so
}
