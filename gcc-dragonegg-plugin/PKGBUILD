# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc-dragonegg-plugin
pkgver=3.0
_gccver=4.6.3
pkgrel=3
pkgdesc="integrates the LLVM optimizers and code generator with the GCC parsers"
arch=('i686' 'x86_64')
url="http://dragonegg.llvm.org/"
license=('GPL')
depends=("gcc=${_gccver}" 'llvm=3.0')
source=(http://llvm.org/releases/3.0/dragonegg-3.0.tar.gz{,.sig})
md5sums=('3704d215fb4343040eaff66a7a87c63a'
         'bcbbb60f48c651130503b8123fa360ca')

_plugindir=/usr/lib/gcc/${CHOST}/${_gccver}/plugin

build() {
  cd $srcdir/dragonegg-${pkgver}.src/
  sed -i "s#\$(shell svnversion -n \$(TOP_DIR))#$pkgver#" Makefile
  make
}

package() {
  cd $srcdir/dragonegg-${pkgver}.src/
  install -Dm755 dragonegg.so $pkgdir/${_plugindir}/dragonegg.so
}
