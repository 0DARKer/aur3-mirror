# Maintainer: masutu <masutu dot arch at gmail dot com>
# Contributor: Ivan Pulido <ijpulidos@riseup.net>
pkgname=python2-sunpy-git
_gitname=sunpy
pkgver=20140519
pkgrel=1
pkgdesc="Python library for solar physics"
arch=('i686' 'x86_64')
url="http://www.sunpy.org/"
license=('BSD')
depends=('python2-astropy'
          'python2-matplotlib'
          'python2-scipy')
optdepends=('python2-pandas: for lightcurve'
            'python2-sqlalchemy: for the database package'
            'python2-suds: for net'
            'python2-beautifulsoup4: for callisto spectrograms and net.helio'
            'python2-pytest: for running tests')
provides=(python2-sunpy)
conflicts=(python2-sunpy)
options=(!emptydirs)
source=(git+https://github.com/sunpy/sunpy)
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

package(){
  cd "$_gitname"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
