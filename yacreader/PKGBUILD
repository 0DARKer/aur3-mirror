# Maintainer: Felix Kauselmann <licorn at gmail dot com>
pkgname=yacreader
pkgver=7.1.0
pkgrel=1
pkgdesc=('A comic reader for cross-platform reading and managing your digital comic collection.')
arch=('i686' 'x86_64')
url="http://www.yacreader.com"
license=('GPL3')
depends=('qt5-base' 'qt5-multimedia' 'qt5-script' 'poppler-qt5' 'glu' 'p7zip' 'desktop-file-utils')
optdepends=(
    'qt5-imageformats: Support for TIFF images'
    'qrencode: YACReaderLibrary server info qr codes'
)
conflicts=('yacreader-bin')
install='yacreader.install'
source=(
  yacreader.patch
  "https://bitbucket.org/luisangelsm/yacreader/downloads/YACReader-$pkgver-src.tar.gz"
  "http://downloads.sourceforge.net/project/p7zip/p7zip/9.20.1/p7zip_9.20.1_src_all.tar.bz2"
)
md5sums=('2ab6df3ee2f7f551a3b0cdb91cf487fb'
         '74d66044fdda9417a38a764d1d77f07b'
         'bd6caaea567dc0d995c990c5cc883c89')

prepare() {
  cd $srcdir
  mv -n p7zip_9.20.1 YACReader-$pkgver-src/compressed_archive/libp7zip
  patch  -p0 -i yacreader.patch
}

build() {
  cd $srcdir/YACReader-$pkgver-src/
  qmake
  make
}

package() {
  cd $srcdir/YACReader-$pkgver-src/
  make INSTALL_ROOT="$pkgdir" install
}
