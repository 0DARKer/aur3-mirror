# Contributor: fnord0 < fnord0 AT riseup DOT net >
# Maintainer: Barry Keegan < barrykeegan AT gmail DOT com >

pkgname=('maltego' 'maltego-doc')
pkgver=3.5.3
pkgrel=2
arch=('any')
url='http://www.paterva.com/web6/products/maltego.php'
license=('custom')
source=("https://www.paterva.com/malv35/community/MaltegoCarbonCE.v$pkgver.zip"
        'maltego.png::https://www.paterva.com/web6/images/frontpage/maltegoPanelLogo.png'
        'maltego'
        'maltego.desktop'
        'http://www.paterva.com/web6/documentation/M3GuideGUI.pdf')
sha1sums=('773cc7f7325ae1b41301fb199526d67a06441710'
          '89fa4ea2904a61dbf5ad3f63b2a11c51e2bfccb6'
          '856c6e31801211bffbc797fbbc7f823e043a76cb'
          '72b7b34a3b60c191a102e993031c55b7bc233399'
          'b3d496acaa2fbd5a0fd0409aafc0af05280db736')

package_maltego() {
  pkgdesc='Maltego Radium Community Edition - a proprietary software for open source intelligence and forensics'
  depends=('java-runtime-common')
  install=$pkgname.install

  cd $srcdir/MaltegoCarbonCE
  install -d $pkgdir/usr/share/{$pkgname,applications,icons}
  install -d $pkgdir/usr/bin
  #install -d ${pkgdir}/usr/share/licenses/${pkgname} || return 1
  # the COMMERCIAL client license resides @ https://www.paterva.com/web5/server/MaltegoServerAgreement.pdf
  cp -pR bin etc groovy ide java maltego maltego-core-platform maltego-ui platform $pkgdir/usr/share/maltego

  # startup app
  cd $srcdir
  install -Dm755 maltego $pkgdir/usr/bin
  install -Dm644 maltego.png $pkgdir/usr/share/icons
  install -Dm644 maltego.desktop $pkgdir/usr/share/applications
}

package_maltego-doc() {
  pkgdesc='Maltego Radium Community Edition - a proprietary software for open source intelligence and forensics (documentation)'
  depends=('maltego')

  cd $srcdir
  install -d $pkgdir/usr/share/maltego/doc
  install -Dm644 M3GuideGUI.pdf $pkgdir/usr/share/maltego/doc
}
