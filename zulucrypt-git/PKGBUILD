# Maintainer: Salan54 <salan at fremenil dot com>
# Created: 2013-01-29

_basename=zulucrypt
pkgname=${_basename}-git
pkgver=20130317
pkgrel=1
pkgdesc="a cli and gui frontend to cryptsetup."
url="http://code.google.com/p/zulucrypt/"
arch=('x86_64' 'i686')
license=('GPL')
depends=('cryptsetup' 'qt4' 'libpwquality' 'libgnome-keyring')
makedepends=('cmake' 'git')
provides=(${_basename})
conflicts=(${_basename})
optdepends=('kdeutils-kwallet: retrieve volume keys from kde kwallet'
            'tcplay-git: trueCrypt implementation based on dm-crypt')
options=('!buildflags')

_gitroot='https://code.google.com/p/zulucrypt/'
_gitname='zulucrypt'
_branch='master'
_build='build'

changelog=${pkgname}.changelog
install=${pkgname}.install

build() {
    cd "${srcdir}"

    msg "Connecting to GIT server...."

    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
    fi

    msg "GIT checkout done or server timeout."
    msg "Starting build..."
  
    cd "${srcdir}/${_gitname}"
    if [[ -d "${_build}" ]]; then
        rm -Rf ${_build}
    fi
    mkdir -p ${_build}
    cd ${_build}

    cmake -DCMAKE_INSTALL_PREFIX=/usr/ -DNOKDE=true -DCMAKE_BUILD_TYPE=release . ..
    make
}
 
package() {
  cd "${srcdir}/${_gitname}/${_build}"
  make DESTDIR="$pkgdir" install
}
 
# vim:set ts=2 sw=2 et:
