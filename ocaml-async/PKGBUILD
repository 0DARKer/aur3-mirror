# Maintainer: Sergei Lebedev <superbobry@gmail.com>

pkgname=ocaml-async
pkgver=107.01
pkgrel=1
pkgdesc="Jane Street's asynchronous execution library for OCaml"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=3.12'
         'ocaml-sexplib'
         'ocaml-fieldslib'
         'ocaml-bin-prot'
         'ocaml-res'
         'ocaml-core')
makedepends=('ocaml-findlib')
options=(!strip !makeflags)
url="http://ocaml.janestcapital.com/?q=node/13"
source=(http://www.janestreet.com/ocaml/async-$pkgver.tar.gz)
md5sums=('7c98157433916789e96b88cc6f1f4a61')

build() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  ocaml setup.ml -configure --prefix /usr --destdir $pkgdir
  ocaml setup.ml -build || return 1
}

package() {
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"

  mkdir -p "$OCAMLFIND_DESTDIR"

  ocaml setup.ml -install
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
