# Maintainer: WPS Office <ksoprocn_bug@kingsoft.com>
# Maintainer: limser <echo bGltc2VyQHFxLmNvbQo=|base64 -d>

pkgbase=wpsforlinux
pkgname=wpsforlinux
true && pkgname=('ttf-fzfont' 'wpsoffice' 'wpsoffice-templates')
_pkgver=8.1.0.3721~b1p1
pkgver=8.1.0.3721b1p1
pkgrel=1
license=('custom:Kingsoft')
conflicts=('wps-office')
options=(!strip)
source=("http://wdl.cache.ijinshan.com/wps/download/Linux/unstable/wps-office_${_pkgver}_i386.deb")
sha1sums=('e496b93c2996053934b292c6dab7e2d7f8c5983a')
#PKGEXT='.pkg.tar' ##less time

_etMT="MimeType=application\/wps-office.et;application\/wps-office.ett;application\/vnd.ms-excel;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.template;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.sheet;"
_wppMT="MimeType=application\/wps-office.dps;application\/wps-office.dpt;application\/vnd.ms-powerpoint;\
application\/vnd.openxmlformats-officedocument.presentationml.presentation;\
application\/vnd.openxmlformats-officedocument.presentationml.slideshow;\
application\/vnd.openxmlformats-officedocument.presentationml.template;"
_wpsMT="MimeType=application\/wps-office.wps;application\/wps-office.wpt;\
application\/msword;application\/rtf;application\/msword-template;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.template;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.document;"

package_ttf-fzfont() {
    pkgdesc="FangZheng fonts separated from WPS Office."
    url="http://www.foundertype.com/"
    arch=('any')
    depends=('fontconfig')
    install=ttf-fzfont.install

    cd "${srcdir}"
    tar zxv -C "${pkgdir}" -f data.tar.gz ./usr/share/fonts
}

package_wpsoffice-templates () {
    pkgdesc='Templates for WPS Office.'
    url='http://linux.wps.cn/'
    arch=('any')
    depends=('wpsoffice')

    cd "${srcdir}"
    tar zxv -C "${pkgdir}" -f data.tar.gz ./opt/kingsoft/wps-office/templates
}

arch=('i686' 'x86_64')

package_wpsoffice () {
    pkgdesc='WPS Office is an office productivity suite.(Without templates and FangZheng fonts)'
    url='http://linux.wps.cn/'
    if [ "$CARCH" = "i686" ]; then
        depends=('fontconfig' 'glib2' 'libpng12' 'libsm' 'libxext' 'libxrender')
    elif [ "$CARCH" = "x86_64" ]; then
        depends=('lib32-fontconfig' 'lib32-glib2' 'lib32-libpng12' 'lib32-libsm' 'lib32-libxext' 'lib32-libxrender')
    fi
    depends+=('desktop-file-utils' 'shared-mime-info' 'xdg-utils')
    optdepends=('ttf-fzfont: FangZheng fonts'
                'wpsoffice-templates: Templates support')
    install=wps-office.install

    cd "${srcdir}"
    tar zxvf data.tar.gz -C "${pkgdir}" --exclude=usr/share/fonts\
        --exclude=opt/kingsoft/wps-office/templates

    ##mime wps-office-{et,wpp,wps}.xml
    sed -i '3,31d' $pkgdir/usr/share/mime/packages/wps-office-et.xml
    sed -i '3,36d' $pkgdir/usr/share/mime/packages/wps-office-wpp.xml
    sed -i '3,30d' $pkgdir/usr/share/mime/packages/wps-office-wps.xml

    ##desktop file with shared-mime-info wps-office-{et,wpp,wps}.xml
    #_et
    sed -i "s/^MimeType.*$/$_etMT/" $pkgdir/usr/share/applications/wps-office-et.desktop
    #_wpp
    sed -i "s/^MimeType.*$/$_wppMT/" $pkgdir/usr/share/applications/wps-office-wpp.desktop
    #_wps
    sed -i "s/^MimeType.*$/$_wpsMT/" $pkgdir/usr/share/applications/wps-office-wps.desktop
}
