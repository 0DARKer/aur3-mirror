# Maintainer: WPS Office <ksoprocn_bug@kingsoft.com>
# Maintainer: limser <echo bGltc2VyQHFxLmNvbQo=|base64 -d>

pkgbase=wpsforlinux
pkgname=wpsforlinux
true && pkgname=('wpsoffice-common' 'wpsoffice-fonts' 'wpsoffice-templates' 'wpsoffice')
_pkgver=8.1.0.3842~a9p1
groups=('wpsforlinux')
license=('custom:Kingsoft')
conflicts=('wps-office')
source=("http://wdl.cache.ijinshan.com/wps/download/Linux/unstable/wps-office_${_pkgver}_i386.deb" 'LICENSE')
sha1sums=('af619eb1f25e4682c2aef76c256505f026dd55b5' 'fffe33dc4b2bcd1398a96d6b2cd4e107cbef5fd8')
PKGEXT='.pkg.tar' ##reduce the time of compression

package_wpsoffice-fonts() {
    pkgver=5.20
    pkgrel=1
    arch=('any')
    pkgdesc="FangZheng fonts separated from WPS Office."
    url="http://www.foundertype.com/"
    depends=('fontconfig')
    install=wpsoffice-fonts.install

    cd "${srcdir}";
    tar zxv -C "${pkgdir}" -f data.tar.gz ./usr/share/fonts
}

package_wpsoffice-common() {
    pkgver=1.0
    pkgrel=1
    arch=('i686' 'x86_64')
    pkgdesc="Dependencies and license for wpsoffice."
    url="http://linux.wps.cn/"
    if [ "" = "i686" ]; then
        depends=('fontconfig' 'glib2' 'libpng12' 'libsm' 'libxext' 'libxrender' 'libcups')
        #optdepends=('libcups: CUPS printing support')
    elif [ "" = "x86_64" ]; then
        depends=('lib32-fontconfig' 'lib32-glib2' 'lib32-libpng12' 'lib32-libsm' 'lib32-libxext' 'lib32-libxrender' 'lib32-libcups')
        #optdepends=('lib32-libcups: CUPS printing support')
    fi

    cd "$srcdir";
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/wpsoffice/LICENSE
}

arch=('any')

package_wpsoffice-templates () {
    pkgver=1.0
    pkgrel=1
    pkgdesc="Templates for WPS Office."
    url="http://linux.wps.cn/"

    cd "${srcdir}";
    tar zxv -C "${pkgdir}" -f data.tar.gz ./opt/kingsoft/wps-office/templates
}

pkgver=${_pkgver}
pkgrel=1

_etMT="MimeType=application\/wps-office.et;application\/wps-office.ett;application\/vnd.ms-excel;application\/vnd.openxmlformats-officedocument.spreadsheetml.template;application\/vnd.openxmlformats-officedocument.spreadsheetml.sheet;"
_wppMT="MimeType=application\/wps-office.dps;application\/wps-office.dpt;application\/vnd.ms-powerpoint;application\/vnd.openxmlformats-officedocument.presentationml.presentation;application\/vnd.openxmlformats-officedocument.presentationml.slideshow;application\/vnd.openxmlformats-officedocument.presentationml.template;"
_wpsMT="MimeType=application\/wps-office.wps;application\/wps-office.wpt;application\/msword;application\/rtf;application\/msword-template;application\/vnd.openxmlformats-officedocument.wordprocessingml.template;application\/vnd.openxmlformats-officedocument.wordprocessingml.document;text\/plain;application\/x-mimearchive;text\/html"

package_wpsoffice() {
    pkgdesc="WPS Office is an office productivity suite, without templates and FangZheng fonts."
    url="http://linux.wps.cn/"
    options=(!strip)
    depends=('desktop-file-utils' 'shared-mime-info' 'xdg-utils' 'wpsoffice-common=1.0')
    optdepends=('wpsoffice-fonts: FangZheng fonts support'
                'wpsoffice-templates: Templates support')
    install=wpsoffice.install

    cd "${srcdir}";
    tar zxvf data.tar.gz -C "${pkgdir}" --exclude=usr/share/fonts --exclude=opt/kingsoft/wps-office/templates;
    sed -i '3,31d' $pkgdir/usr/share/mime/packages/wps-office-et.xml;
    sed -i '3,36d' $pkgdir/usr/share/mime/packages/wps-office-wpp.xml;
    sed -i '3,30d' $pkgdir/usr/share/mime/packages/wps-office-wps.xml;
    sed -i "s/^MimeType.*$/$_etMT/" $pkgdir/usr/share/applications/wps-office-et.desktop;
    sed -i "s/^MimeType.*$/$_wppMT/" $pkgdir/usr/share/applications/wps-office-wpp.desktop;
    sed -i "s/^MimeType.*$/$_wpsMT/" $pkgdir/usr/share/applications/wps-office-wps.desktop
}
