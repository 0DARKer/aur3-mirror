# Maintainer: WPS Office <ksoprocn_bug@kingsoft.com>
# Maintainer: limser <echo bGltc2VyQHFxLmNvbQo=|base64 -d>

pkgbase=wpsforlinux
pkgname=wpsforlinux
true && pkgname=('wpsoffice-depends' 'wpsoffice-fonts' 'wpsoffice-templates' 'wpsoffice')
_pkgver=8.1.0.3839~a9
license=('custom:Kingsoft')
conflicts=('wps-office')
options=(!strip)
source=("http://wdl.cache.ijinshan.com/wps/download/Linux/unstable/wps-office_${_pkgver}_i386.deb")
sha1sums=('ff27e524831a16b2fb87515587f69fafa5ef0218')
#PKGEXT='.pkg.tar' ##reduce the time of compression

package_wpsoffice-fonts() {
    pkgdesc="FangZheng fonts separated from WPS Office."
    url="http://www.foundertype.com/"
    pkgver=5.20
    pkgrel=1
    arch=('any')
    depends=('fontconfig')
    install=ttf-fzfont.install

    cd "${srcdir}"
    tar zxv -C "${pkgdir}" -f data.tar.gz ./usr/share/fonts
}

pkgver=${_pkgver}
pkgrel=1

package_wpsoffice-depends() {
    pkgdesc="Dependencies for wpsoffice."
    url='http://linux.wps.cn/'
    arch=('i686' 'x86_64')
    if [ "$CARCH" = "i686" ]; then
        depends=('fontconfig' 'glib2' 'libpng12' 'libsm' 'libxext' 'libxrender' 'libcups')
        #optdepends=('libcups: CUPS printing support')
    elif [ "$CARCH" = "x86_64" ]; then
        depends=('lib32-fontconfig' 'lib32-glib2' 'lib32-libpng12' 'lib32-libsm' 'lib32-libxext' 'lib32-libxrender' 'lib32-libcups')
        #optdepends=('lib32-libcups: CUPS printing support')
    fi
}

arch=('any')

package_wpsoffice-templates () {
    pkgdesc='Templates for WPS Office.'
    url='http://linux.wps.cn/'

    cd "${srcdir}"
    tar zxv -C "${pkgdir}" -f data.tar.gz ./opt/kingsoft/wps-office/templates
}

_etMT="MimeType=application\/wps-office.et;application\/wps-office.ett;application\/vnd.ms-excel;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.template;\
application\/vnd.openxmlformats-officedocument.spreadsheetml.sheet;"
_wppMT="MimeType=application\/wps-office.dps;application\/wps-office.dpt;application\/vnd.ms-powerpoint;\
application\/vnd.openxmlformats-officedocument.presentationml.presentation;\
application\/vnd.openxmlformats-officedocument.presentationml.slideshow;\
application\/vnd.openxmlformats-officedocument.presentationml.template;"
_wpsMT="MimeType=application\/wps-office.wps;application\/wps-office.wpt;\
application\/msword;application\/rtf;application\/msword-template;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.template;\
application\/vnd.openxmlformats-officedocument.wordprocessingml.document;\
text\/plain;application\/x-mimearchive;text\/html"

package_wpsoffice () {
    pkgdesc='WPS Office is an office productivity suite, without templates and FangZheng fonts.'
    url='http://linux.wps.cn/'
    depends=('desktop-file-utils' 'shared-mime-info' 'xdg-utils' 'wpsoffice-depends')
    optdepends=('wpsoffice-fonts: FangZheng fonts support'
                'wpsoffice-templates: Templates support')
    install=wps-office.install

    cd "${srcdir}"
    tar zxvf data.tar.gz -C "${pkgdir}" --exclude=usr/share/fonts\
        --exclude=opt/kingsoft/wps-office/templates

    ##mime wps-office-{et,wpp,wps}.xml
    sed -i '3,31d' $pkgdir/usr/share/mime/packages/wps-office-et.xml
    sed -i '3,36d' $pkgdir/usr/share/mime/packages/wps-office-wpp.xml
    sed -i '3,30d' $pkgdir/usr/share/mime/packages/wps-office-wps.xml

    ##desktop file with shared-mime-info wps-office-{et,wpp,wps}.xml
    #_et
    sed -i "s/^MimeType.*$/$_etMT/" $pkgdir/usr/share/applications/wps-office-et.desktop
    #_wpp
    sed -i "s/^MimeType.*$/$_wppMT/" $pkgdir/usr/share/applications/wps-office-wpp.desktop
    #_wps
    sed -i "s/^MimeType.*$/$_wpsMT/" $pkgdir/usr/share/applications/wps-office-wps.desktop
}
