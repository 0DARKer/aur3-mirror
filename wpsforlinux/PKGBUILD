# Maintainer: Kingsoft Office <ksoprocn_bug@kingsoft.com>
# Maintainer: limser <echo bGltc2VyQHFxLmNvbQo=|base64 -d>

pkgbase=wpsforlinux
pkgname=wpsforlinux
true && pkgname=('wpsoffice-fonts'
                 'wpsoffice-common'
                 'wpsoffice-en-US-templates'
                 'wpsoffice-zh-CN-templates'
                 'wpsoffice-en-US'
                 'wpsoffice-zh-CN'
                 'wpsoffice')
_pkgver=9.1.0.4093~a11
arch=('any')
groups=('wpsforlinux')
license=('custom:Kingsoft')
conflicts=('wps-office' 'kingsoft-office')
source=("http://wdl.cache.ijinshan.com/wps/download/Linux/unstable/kingsoft-office_${_pkgver}_i386.deb" 'LICENSE')
sha1sums=('70e46f3a4124f7e6ef7542d84632c40c48a579cc' 'fffe33dc4b2bcd1398a96d6b2cd4e107cbef5fd8')
#PKGEXT='.pkg.tar' ##reduce the time of compression

package_wpsoffice-fonts() {
    pkgver=5.20
    pkgrel=3
    pkgdesc="FangZheng fonts separated from Kingsoft Office(WPS Office)."
    url="http://www.foundertype.com/"
    depends=('fontconfig')
    install=wpsoffice-fonts.install

    cd "${srcdir}";
    tar xv -C "${pkgdir}" --lzma -f data.tar.lzma ./usr/share/fonts
}

package_wpsoffice-common() {
    pkgver=1.0
    pkgrel=1
    arch=('i686' 'x86_64')
    pkgdesc="Dependencies and license for Kingsoft Office(WPS office)."
    url="http://linux.wps.cn/"
    if [ "$CARCH" = "i686" ]; then
        depends=('fontconfig' 'glib2' 'libpng12' 'libsm' 'libxext' 'libxrender' 'libcups')
        #optdepends=('libcups: CUPS printing support')
    elif [ "$CARCH" = "x86_64" ]; then
        depends=('lib32-fontconfig' 'lib32-glib2' 'lib32-libpng12' 'lib32-libsm' 'lib32-libxext' 'lib32-libxrender' 'lib32-libcups')
        #optdepends=('lib32-libcups: CUPS printing support')
    fi

    cd "$srcdir";
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/wpsoffice/LICENSE
}

pkgver=${_pkgver}
pkgrel=1

package_wpsoffice-en-US-templates () {
    arch=('any')
    url="http://linux.wps.cn/"
    provides=('wpsoffice-templates')
    pkgdesc='English (United States) templates for Kingsoft Office(WPS Office).';
    depends=('wpsoffice-en-US');
    cd "${srcdir}";
    tar xv -C "${pkgdir}" --lzma -f data.tar.lzma ./opt/kingsoft/wps-office/office6/mui/en_US;
    rm "${pkgdir}"/opt/kingsoft/wps-office/office6/mui/en_US/{en_US.png,lang.conf}
}

package_wpsoffice-zh-CN-templates () {
    arch=('any')
    url="http://linux.wps.cn/"
    provides=('wpsoffice-templates')
    pkgdesc='Chinese (simplified) templates for Kingsoft Office(WPS Office).';
    depends=('wpsoffice-zh-CN');
    cd "${srcdir}";
    tar xv -C "${pkgdir}" --lzma -f data.tar.lzma ./opt/kingsoft/wps-office/office6/mui/zh_CN/templates
}

package_wpsoffice-en-US () {
    arch=('any')
    url="http://linux.wps.cn/"
    depends=('wpsoffice')
    provides=('wpsoffice-langpack')
    pkgdesc='English (United States) language pack for Kingsoft Office(WPS Office).';
    cd "${srcdir}";
    tar xv -C "${pkgdir}" --lzma -f data.tar.lzma ./opt/kingsoft/wps-office/office6/mui/en_US/{en_US.png,lang.conf}
}

package_wpsoffice-zh-CN () {
    arch=('any')
    url="http://linux.wps.cn/"
    depends=('wpsoffice')
    provides=('wpsoffice-langpack')
    pkgdesc='Chinese (simplified) language pack for Kingsoft Office(WPS Office).';
    cd "${srcdir}";
    tar xv -C "${pkgdir}" --lzma -f data.tar.lzma ./opt/kingsoft/wps-office/office6/mui/zh_CN --exclude=opt/kingsoft/wps-office/office6/mui/zh_CN/templates
}

_etMT="MimeType=application\/wps-office.et;application\/wps-office.ett;application\/vnd.ms-excel;application\/vnd.openxmlformats-officedocument.spreadsheetml.template;application\/vnd.openxmlformats-officedocument.spreadsheetml.sheet;audio\/webm;application\/xml;text\/html;text\/plain;text\/csv;"
_wppMT="MimeType=application\/wps-office.dps;application\/wps-office.dpt;application\/vnd.ms-powerpoint;application\/vnd.openxmlformats-officedocument.presentationml.presentation;application\/vnd.openxmlformats-officedocument.presentationml.slideshow;application\/vnd.openxmlformats-officedocument.presentationml.template;"
_wpsMT="MimeType=application\/wps-office.wps;application\/wps-office.wpt;application\/msword;application\/msword-template;application\/vnd.openxmlformats-officedocument.wordprocessingml.template;application\/vnd.openxmlformats-officedocument.wordprocessingml.document;audio\/webm;text\/plain;text\/html"

package_wpsoffice() {
    arch=('any')
    pkgdesc="Kingsoft Office (WPS Office), including Writer, Presentation and Spreadsheets, is a powerful office productivity suite, which is deeply compatible with all of the latest Microsoft Office file formats."
    url="http://linux.wps.cn/"
    options=(!strip)
    depends=('desktop-file-utils' 'shared-mime-info' 'xdg-utils' 'wpsoffice-common=1.0')
    optdepends=('wpsoffice-fonts: FangZheng fonts support'
                'wpsoffice-langpack: additional language support'
                'wpsoffice-templates: additional templates support')
    install=wpsoffice.install

    cd "${srcdir}";
    tar xv --lzma -f data.tar.lzma -C "${pkgdir}" --exclude=usr/share/fonts --exclude=opt/kingsoft/wps-office/office6/mui/en_US --exclude=opt/kingsoft/wps-office/office6/mui/zh_CN;
    msg2 "editing mime files: wps-office-{et,wpp,wps}.xml ...";
    sed -i '3,31d;s/application-wps-office./wps-office-/g' $pkgdir/usr/share/mime/packages/wps-office-et.xml;
    sed -i '3,36d;s/application-wps-office./wps-office-/g' $pkgdir/usr/share/mime/packages/wps-office-wpp.xml;
    sed -i '3,30d;s/application-wps-office./wps-office-/g' $pkgdir/usr/share/mime/packages/wps-office-wps.xml;
    msg2 "editing desktop files: wps-office-{et,wpp,wps}.desktop, with shared-mime-info ...";
    sed -i "s/^MimeType.*$/$_etMT/" $pkgdir/usr/share/applications/wps-office-et.desktop;
    sed -i "s/^MimeType.*$/$_wppMT/" $pkgdir/usr/share/applications/wps-office-wpp.desktop;
    sed -i "s/^MimeType.*$/$_wpsMT/" $pkgdir/usr/share/applications/wps-office-wps.desktop
}
