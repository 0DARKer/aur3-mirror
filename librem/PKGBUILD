# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=rem
pkgname=librem
pkgver=0.4.0
pkgrel=1
pkgdesc="A portable and generic library for real-time audio and video processing."
arch=(i686 x86_64)
url="http://www.creytiv.com/rem.html"
license=('BSD')
depends=('libre>=0.4.0')
source=(http://www.creytiv.com/pub/$_name-$pkgver.tar.gz)
md5sums=('0d5cf133a62d11a2f80875a8fa7adab6')

build() {
  cd "$srcdir/$_name-$pkgver"
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 docs/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # fix permission for static lib
  chmod 644 "$pkgdir/usr/lib/$pkgname.a"
}

# vim:set ts=2 sw=2 et:
