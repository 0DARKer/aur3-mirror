# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=4.98.0
pkgrel=1
pkgdesc='KWallet Framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kwallet'
license=('LGPL')
depends=('kwindowsystem' 'kconfig' 'knotifications' 'ki18n' 'kiconthemes')
makedepends=('extra-cmake-modules')
groups=('kf5')
conflicts=('kwalletframeworks')
replaces=('kwalletframeworks')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('ff1731688f7a7b2b8d0e3b97458fbed4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
