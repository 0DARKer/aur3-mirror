# Contributor: Ilya Galushko <helloy-ilya@yandex.ru>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com}
# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=linuxwacom-bamboo-cth-ctl
pkgver=0.10.11
_driverver=0.10.10-2
pkgrel=4
pkgdesc="Drivers for Wacom tablets with patches to enable second generation Bamboo tablets"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('xorg-server>=1.9.0' 'libxi' 'kernel26>=2.6.26')
makedepends=('xorg-server-devel' 'libxext' 'kernel26-headers')
options=('!libtool')
conflicts=('xf86-input-wacom' 'xf86-input-wacom-git' 'linuxwacom-cvs' 'input-wacom' 'inpput-wacom-cvs')
replaces=('linuxwacom' 'linuxwacom-dev' 'linuxwacom-cvs')
provides=("xf86-input-wacom=$pkgver" "input-wacom=$_driverver")
install=linuxwacom.install
source=(http://prdownloads.sourceforge.net/linuxwacom/input-wacom-${_driverver}.tar.bz2
        http://prdownloads.sourceforge.net/linuxwacom/xf86-input-wacom-${pkgver}.tar.bz2
        10-wacom.rules touch.patch tap.patch inlineDistance.patch)
md5sums=('3872f368c78cec43990a0b32c1768da8'
         '8dc40baa87115b011f8b514def65c0a7'
         'fae4f7104fdd90929a84b642acc1f54c'
         '8502804b7f727355a70f0ee4cc01fb24'
         'b0ab56bbcb488cdbf0d2b9f8ffd24d7a'
         '5f687ea29f3466ebac0cbc024d395cb8')

build() {
  msg "Building kernel module..."
  cd "${srcdir}"/input-wacom-${_driverver}
  ./configure --prefix=/usr
  make
  msg "Building X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  patch -p1 < ../touch.patch
  patch -p1 < ../tap.patch
  patch -p1 < ../inlineDistance.patch
  ./configure --prefix=/usr  --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  msg "Installing kernel module..."
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 36
  then
    _ver=2.6.36
  else
    _ver=2.6.30
  fi
  cd "${srcdir}"/input-wacom-${_driverver}/$_ver
  install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
  install -D -m 644 wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
  msg "Installing X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  make DESTDIR="${pkgdir}" install
  msg "Installing udev rules..."
  install -D -m644 "${srcdir}/10-wacom.rules" "${pkgdir}/etc/udev/rules.d/10-wacom.rules"
}

