# Contributor: Ilya Galushko <helloy-ilya@yandex.ru>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com}
# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=linuxwacom-bamboo-cth-ctl
pkgver=0.11.0
_driverver=0.10.11
pkgrel=1
pkgdesc="Drivers for Wacom tablets with patches to enable second generation Bamboo tablets"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('xorg-server>=1.9.0' 'libxi' 'kernel26>=2.6.26')
makedepends=('xorg-server-devel' 'libxext' 'kernel26-headers')
options=('!libtool')
conflicts=('xf86-input-wacom' 'xf86-input-wacom-git' 'linuxwacom-cvs' 'input-wacom' 'inpput-wacom-cvs')
replaces=('linuxwacom' 'linuxwacom-dev' 'linuxwacom-cvs')
provides=("xf86-input-wacom=$pkgver" "input-wacom=$_driverver")
install=linuxwacom.install
source=(http://prdownloads.sourceforge.net/linuxwacom/input-wacom-${_driverver}.tar.bz2
        http://prdownloads.sourceforge.net/linuxwacom/xf86-input-wacom-${pkgver}.tar.bz2
        10-wacom.rules)
md5sums=('471bbef564cca324c1ece6b739315407'
         'e0f6182cf7fba19c13f3db7e0da1baf3'
         'fae4f7104fdd90929a84b642acc1f54c')

build() {
  msg "Building kernel module..."
  cd "${srcdir}"/input-wacom-${_driverver}
  ./configure --prefix=/usr
  make
  msg "Building /dev/input/event support for serial devices..."
  cd inputattach
  gcc inputattach.c -o inputattach
  msg "Building X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  ./configure --prefix=/usr  --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  msg "Installing kernel modules..."
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 36
  then
    _ver=2.6.36
  else
    _ver=2.6.30
  fi
  cd "${srcdir}"/input-wacom-${_driverver}/$_ver
  install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
  install -D -m 644 wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
  msg "Installing /dev/input/event support for serial devices..."
  cd ../inputattach
  install -D -m 755 inputattach "${pkgdir}"/usr/bin/inputattach
  msg "Installing X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  make DESTDIR="${pkgdir}" install
  msg "Installing udev rules..."
  install -D -m644 "${srcdir}/10-wacom.rules" "${pkgdir}/etc/udev/rules.d/10-wacom.rules"
}

