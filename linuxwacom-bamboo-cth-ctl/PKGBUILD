# Contributor: Ilya Galushko <helloy-ilya@yandex.ru>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com}
# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=linuxwacom-bamboo-cth-ctl
pkgver=0.10.10
_driverver=0.10.10-2
pkgrel=18
pkgdesc="Drivers for Wacom tablets with patches to enable second generation Bamboo tablets"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('xorg-server>=1.9.0' 'libxi' 'kernel26>=2.6.26')
makedepends=('xorg-server-devel' 'libxext' 'kernel26-headers')
options=('!libtool')
conflicts=('xf86-input-wacom' 'xf86-input-wacom-git' 'linuxwacom-cvs' 'input-wacom' 'inpput-wacom-cvs')
replaces=('linuxwacom' 'linuxwacom-dev' 'linuxwacom-cvs')
provides=("xf86-input-wacom=$pkgver" "input-wacom=$_driverver")
install=linuxwacom.install
source=(http://prdownloads.sourceforge.net/linuxwacom/input-wacom-${_driverver}.tar.bz2
        http://prdownloads.sourceforge.net/linuxwacom/xf86-input-wacom-${pkgver}.tar.bz2
        10-wacom.rules notouch.patch buttons.patch axis.patch filtering.patch
        buttons2.patch buttons3.patch d6d7.patch super_hyper.patch)
md5sums=('3872f368c78cec43990a0b32c1768da8'
         '4070b27443758310dbcbf84158c8eeb1'
         'fae4f7104fdd90929a84b642acc1f54c'
         '8fd0b2613e8fbed6614116ad70d528c3'
         'a8982a0a9c396d5a0cccc7f6dece35ac'
         'dd596a221f0bc1baccf93a32626ff02f'
         'fb677678c35c34d91de70d33a9d6e7eb'
         'f098385fb5cae1c2b9b5628095c3e7af'
         'e245ec6aa79b28d7519097d3a026d08c'
         '186f7818056af2d2d6709e9dd7e48ecf'
         '8488b0b43c61b4cc66fb919438ea95a5')

build() {
  msg "Building kernel module..."
  cd "${srcdir}"/input-wacom-${_driverver}
  ./configure --prefix=/usr
  make
  msg "Building X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  patch -p1 < ../filtering.patch
  patch -p1 < ../axis.patch
  patch -p1 < ../buttons.patch
  patch -p1 < ../notouch.patch
  patch -p1 < ../buttons2.patch
  patch -p1 < ../buttons3.patch
  patch -p1 < ../d6d7.patch
  patch -p1 < ../super_hyper.patch
  ./configure --prefix=/usr  --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  msg "Installing kernel module..."
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 36
  then
    _ver=2.6.36
  else
    _ver=2.6.30
  fi
  cd "${srcdir}"/input-wacom-${_driverver}/$_ver
  install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
  msg "Installing X driver..."
  cd "${srcdir}"/xf86-input-wacom-${pkgver}
  make DESTDIR="${pkgdir}" install
  msg "Installing udev rules..."
  install -D -m644 "${srcdir}/10-wacom.rules" "${pkgdir}/etc/udev/rules.d/10-wacom.rules"
}

