# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor:  Eugen Zagorodniy <e dot zagorodniy at gmail dot com>
# Contributor: Olaf Leidinger <leidola@newcon.de>
# Contributer: Henning Garus <henning.garus@gmail.com>
# Contributor: Thomas Burdick <thomas.burdick@gmail.com>

pkgname=pyqwt
pkgver=5.2.0
pkgrel=9
pkgdesc="Python bindings for Qt Widgets for Technical Applications"
arch=("i686" "x86_64")
url="http://pyqwt.sourceforge.net/"
depends=('python2' 'python2-pyqt>=4.6.0' 'python2-numpy>=1.1.0' 'qwt5>=5.0.2' 'sip>=4.7.6')
options=('!makeflags')
license=("GPL")
source=("http://downloads.sourceforge.net/sourceforge/pyqwt/PyQwt-$pkgver.tar.gz"
        "qplt.py-r1.21.patch" "use-qwt5.patch" "qmake-qt4.patch"
)
md5sums=('fcd6c6029090d473dcc9df497516eae7'
         '30011f9139ad12ddbeac79c6a45d4b43'
         '8d811bcfaf1a952286b12b6e6152c68a'
         '747d439e97c09107c85ea37bcdd9a3cd')

build() {
  cd "$srcdir"/PyQwt-$pkgver

  patch -p1 < ../qplt.py-r1.21.patch
  patch -p1 < ../use-qwt5.patch
  patch -p1 < ../qmake-qt4.patch

  cd configure
  python2 configure.py -I/usr/include/qwt5 -lqwt5

  make
}

package() {
  cd "$srcdir"/PyQwt-$pkgver/configure

  make DESTDIR="$pkgdir" install

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" $(find "${pkgdir}" -name '*.py')
}

