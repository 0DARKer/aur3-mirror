# Maintainer: 謝致邦 <Yeking@Red54.com>

_cfgdir=/etc/tengine
_tmpdir=/var/lib/tengine

pkgname=tengine
pkgver=1.4.6
pkgrel=1
pkgdesc='Lightweight HTTP server and IMAP/POP3 proxy server'
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
depends=('pcre' 'zlib' 'openssl' 'geoip')
makedepends=('passenger')
url="http://tengine.taobao.org"
license=('custom')
install=tengine.install
backup=(${_cfgdir:1}/fastcgi.conf
		${_cfgdir:1}/fastcgi_params
		${_cfgdir:1}/koi-win
		${_cfgdir:1}/koi-utf
		${_cfgdir:1}/mime.types
		${_cfgdir:1}/tengine.conf
		${_cfgdir:1}/scgi_params
		${_cfgdir:1}/uwsgi_params
		${_cfgdir:1}/win-utf
		etc/logrotate.d/tengine)
source=(http://tengine.taobao.org/download/tengine-$pkgver.tar.gz
		service
		logrotate
		tengine.patch)
md5sums=('5ef85c8ce7a6d909c14e10cf7a8d4eec'
         '8db17b8c9d6109e25273dfddd8756c35'
         '2fdf8af91341eb38c026de2f53be2ac4'
         'dbcdf854ab3f86091b1c2ecae0a41b45')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -Np0 -i $srcdir/tengine.patch

	./configure \
        --prefix=$_cfgdir \
        --conf-path=$_cfgdir/tengine.conf \
        --sbin-path=/usr/bin/tengine \
        --pid-path=/var/run/tengine.pid \
        --lock-path=/var/lock/tengine.lock \
        --user=http --group=http \
        --http-log-path=/var/log/tengine/access.log \
        --error-log-path=/var/log/tengine/error.log \
        --http-client-body-temp-path=$_tmpdir/client-body \
        --http-proxy-temp-path=$_tmpdir/proxy \
        --http-fastcgi-temp-path=$_tmpdir/fastcgi \
        --http-scgi-temp-path=$_tmpdir/scgi \
        --http-uwsgi-temp-path=$_tmpdir/uwsgi \
        --with-imap --with-imap_ssl_module \
        --with-ipv6 --with-pcre-jit \
        --with-file-aio \
        --with-http_dav_module \
        --with-http_geoip_module \
        --with-http_gzip_static_module \
        --with-http_realip_module \
        --with-http_ssl_module \
        --with-http_stub_status_module \
        --add-module=/usr/lib/passenger/ext/nginx \
        --without-dso \
        #--with-http_mp4_module \
        #--with-http_addition_module \
        #--with-http_xslt_module \
        #--with-http_image_filter_module \
        #--with-http_sub_module \
        #--with-http_flv_module \
        #--with-http_random_index_module \
        #--with-http_secure_link_module \
        #--with-http_degradation_module \
        #--with-http_perl_module \

	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	sed -e 's|\<user\s\+\w\+;|user html;|g' \
		-e '44s|html|/usr/share/tengine/html|' \
		-e '54s|html|/usr/share/tengine/html|' \
		-i "$pkgdir"/etc/tengine/tengine.conf{,.default}
	#rm "$pkgdir"/etc/tengine/*.default

	install -d "$pkgdir"/$_tmpdir
	install -dm700 "$pkgdir"/$_tmpdir/proxy

	chmod 750 "$pkgdir"/var/log/tengine
	chown http:log "$pkgdir"/var/log/tengine

	install -d "$pkgdir"/usr/share/tengine
	mv "$pkgdir"/etc/tengine/html/ "$pkgdir"/usr/share/tengine

	install -Dm644 "$srcdir"/logrotate "$pkgdir"/etc/logrotate.d/tengine
	install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/tengine.service
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/tengine/LICENSE
	rm -rf "$pkgdir"/var/run
}
