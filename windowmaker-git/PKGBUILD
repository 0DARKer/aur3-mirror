# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

pkgname=windowmaker-git
pkgver=0.95.5.r231.gd30b942
pkgrel=1
pkgdesc="An X11 window manager with a NEXTSTEP look and feel"
arch=(i686 x86_64)
url="http://www.windowmaker.org"
license=('GPL' 'custom')
depends=('libxinerama' 'libxrandr' 'libxpm' 'libxft' 'libtiff>=3.9.1' 'giflib' 'libxmu')
makedepends=('git')
options=('!libtool')
provides=('windowmaker')
conflicts=('windowmaker')
source=("$pkgname::git://repo.or.cz/wmaker-crm.git"
	"wmaker.desktop")
md5sums=('SKIP'
         '2fba97bebfd691836b92b8f0db79ff13')

pkgver() {
  cd $pkgname
  git describe --long | sed -r 's/wmaker.//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
  [ -z "$LINGUAS" ] && export LINGUAS="`ls po/*.po | sed 's:po/\(.*\)\.po$:\1:'|tr '\n' ' '`"
  ./autogen.sh
}

build() {
  cd $pkgname
  [ -z "$LINGUAS" ] && export LINGUAS="`ls po/*.po | sed 's:po/\(.*\)\.po$:\1:'|tr '\n' ' '`"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-xinerama \
    --with-nlsdir=/usr/share/locale --with-gnustepdir=/usr/lib/GNUstep \
    --enable-usermenu --enable-modelock --enable-xrandr
  make V=0
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING.WTFPL "$pkgdir/usr/share/licenses/$pkgname/COPYING.WTFPL"
  install -D -m644 "$srcdir/wmaker.desktop" "$pkgdir/usr/share/xsessions/wmaker.desktop"
}
