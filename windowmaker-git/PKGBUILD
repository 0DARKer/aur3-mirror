# Contributor: Daniel Isenmann <daniel@archlinux.org>

pkgname=windowmaker-git
pkgver=20120222
pkgrel=1
pkgdesc="An X11 window manager with a NEXTSTEP look and feel"
arch=(i686 x86_64)
url="http://www.windowmaker.org"
license=('GPL' 'custom')
depends=('libxinerama' 'libxrandr' 'libpng' 'libxpm' 'libxft' 'libtiff>=3.9.1' 'giflib' 'libxmu')
makedepends=('git')
options=('!libtool')
provides=('windowmaker')
conflicts=('windowmaker')
source=(wmaker.desktop)
md5sums=('2fba97bebfd691836b92b8f0db79ff13')

_gitroot="git://repo.or.cz/wmaker-crm.git"
_gitname="WindowMaker"


build() {
  cd ${srcdir}
  msg "Connecting to the GIT server...."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi
  
  msg "GIT checkout done"
  msg "Starting make..."

  git clone $srcdir/$_gitname $srcdir/$_gitname-build
  cd $srcdir/$_gitname-build  

  [ -z "$LINGUAS" ] && export LINGUAS="`ls po/*.po | sed 's:po/\(.*\)\.po$:\1:'`"
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --enable-xinerama \
    --with-nlsdir=/usr/share/locale --with-gnustepdir=/usr/lib/GNUstep \
    --enable-usermenu --enable-modelock --enable-xrandr
  make
}

package() {
  cd $srcdir/$_gitname-build  

  make DESTDIR=$pkgdir install
  install -D -m644 COPYING.WTFPL $pkgdir/usr/share/licenses/$pkgname/COPYING.WTFPL
  install -D -m644 $srcdir/wmaker.desktop $pkgdir/usr/share/xsessions/wmaker.desktop

  rm -rf $srcdir/$_gitname-build
}
