# Contributor: Sickhate
# Contributor: KittyKatt <kittykatt AT silverirc.com>
# Contributor: UnCO <undercooled AT lavabit com>
pkgname='pekwm-git'
_pkgname='pekwm'
pkgver=20130522.819
pkgrel=1
pkgdesc="A small, fast, functional, and flexible window manager - GIT Version"
url="http://pekwm.org/"
arch=('i686' 'x86_64')
depends=('libjpeg' 'libpng' 'libxft' 'libxinerama' 'libxpm' 'libxrandr' 'gcc-libs')
makedepends=('git')
license=('GPL')
provides=("$_pkgname")
conflicts=("$_pkgname-svn" "$_pkgname" "$_pkgname-git")
source=("$_pkgname::git+http://www.pekwm.org/git/pekwm.git")
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-list --count master)"
}

build() {
  cd $_pkgname
  ./autogen.sh
   # Default settings:
  #    --enable-shape --disable-xinerama --enable-menus \
  #    --enable-harbour --disable-pcre --disable-debug  \
  #    --enable-xft --enable-image-xpm --enable-image-jpeg \
  #    --enable-image-png --enable-xrandr
  ./configure --prefix=/usr --sysconfdir=/etc --enable-xinerama \
    --enable-harbour
  make
}

package() {
  cd $_pkgname
  make DESTDIR=$pkgdir install
}
