# Maintainer: willemw <willemw12@gmail.com>

_pkgname=ngp
pkgname=$_pkgname-git
pkgver=r122.da9bf0a
pkgrel=1
pkgdesc="Ncurses grep tool"
arch=('i686' 'x86_64')
url="https://github.com/jonathanklee/ngp"
license=('GPL')
depends=('ncurses')
makedepends=('git')
provides=($_pkgname)
conflicts=($_pkgname)
source=(git://github.com/jonathanklee/ngp.git)
md5sums=(SKIP)

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir/" install
}

