# Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-config-any'
pkgver='0.23'
pkgrel=2
pkgdesc="Load configuration from different file formats, transparently"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-xml-libxml')
url='http://search.cpan.org/dist/Config-Any'
source=('http://search.cpan.org/CPAN/authors/id/B/BR/BRICAS/Config-Any-0.23.tar.gz')
md5sums=('97a303ef7426e5b6888bb7dd294a7e7a')
sha512sums=('f9ba62f66063d42b4d19648f8c3f7b92619d24bc1eb106e790909cc89cb183a62d3582f154401e1d8ef2e6eb4905964878230d1e087a668d37f71bc36171d64e')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null
  cd "${srcdir}/Config-Any-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
