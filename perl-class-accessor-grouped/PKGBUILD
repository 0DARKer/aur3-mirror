# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-class-accessor-grouped'
pkgver='0.10010'
pkgrel='1'
pkgdesc="Lets you build groups of accessors"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-module-runtime>=0.012')
makedepends=('perl-test-exception>=0.31')
url='http://search.cpan.org/dist/Class-Accessor-Grouped'
source=("http://search.cpan.org/CPAN/authors/id/R/RI/RIBASUSHI/Class-Accessor-Grouped-${pkgver}.tar.gz")
md5sums=('c2416c6d68b9bda3a8cbe9daff3a140c')
sha512sums=('eaa242cf5d247f3b0e457a0805d4706c237b508ed3fb43aa1fa2e09f6ad1f4d2ce09b0391d1da2147f5ae045dcf25e8cf69ebc863091a83662443c673a488785')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Class-Accessor-Grouped-${pkgver}"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/Class-Accessor-Grouped-${pkgver}"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "${srcdir}/Class-Accessor-Grouped-${pkgver}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
