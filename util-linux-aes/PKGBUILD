# Maintainer: xerc <aur[at]xerc.de>
# Contributor: milomouse <vincent[at]fea.st>
# Contributor: judd <jvinet[at]zeroflux.org>
# Contributor: falconindy <d[at]falconindy.com>

_basename=util-linux
pkgname=${_basename}-aes
_basever=2.22
pkgver=${_basever}
pkgrel=1
_loopaesdiff=${_basename}-${_basever}-20120927.diff

pkgdesc="Miscellaneous system utilities for Linux, with loop-AES support"
url="http://sourceforge.net/projects/loop-aes/"
license=('GPL2')
arch=('any')
groups=('base')

depends=('bash' 'ncurses>=5.7' 'zlib' 'filesystem' 'pam' 'shadow')
makedepends=('autoconf' 'automake')

provides=('eject' "util-linux=${pkgver}" "util-linux-ng=${pkgver}")
conflicts=('eject' 'util-linux' 'util-linux-ng' 'e2fsprogs<1.41.8-2')
replaces=('eject' 'util-linux' 'util-linux-ng', 'util-linux-ng-aes')
options=('!libtool')

install=${pkgname}.install
source=(http://ftp.kernel.org/pub/linux/utils/${_basename}/v${_basever}/${_basename}-${pkgver}.tar.bz2
        http://loop-aes.sourceforge.net/updates/${_loopaesdiff}.bz2
        su.1
        uuidd.tmpfiles
        pam-login
        pam-common
        pam-su
		hwclock-systz.patch)

build() {
  cd "${srcdir}/${_basename}-${pkgver}"

  #msg "Using losetup.8 from ${_basever}"
  #cp -f "../${_basename}-${_basever}/sys-utils/losetup.8" "sys-utils/losetup.8"

  msg "Patching with loop-AES"
  patch -Np1 -i "${srcdir}/${_loopaesdiff}"

  # https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/util-linux
  patch -p1 -i ../hwclock-systz.patch

  # unbreak --localstatedir
  # TODO(dreisner): find out what sami had in mind with these heuristics
  sed -i '71,75d' configure.ac
  
  msg "Starting autogen"
  ./autogen.sh

  msg "Starting configure"
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib \
    --localstatedir=/run \
    --enable-fs-paths-extra=/usr/bin:/usr/sbin \
	--enable-raw \
	--enable-vipw \
	--enable-newgrp \
	--enable-chfn-chsh \
	--enable-write \
	--enable-mesg \
    --enable-arch \
    --enable-partx \
	--enable-socket-activation

  msg "Starting make"
  make
}

check() {
  make -C "$_basename-$pkgver" check
}

package() {
  cd "${srcdir}/${_basename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"

  # setuid chfn and chsh
  chmod 4755 "$pkgdir"/usr/bin/{newgrp,ch{sh,fn}}

  # install PAM files for login-utils
  install -Dm644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chfn"
  install -m644 "$srcdir/pam-common" "$pkgdir/etc/pam.d/chsh"
  install -m644 "$srcdir/pam-login" "$pkgdir/etc/pam.d/login"
  install -m644 "$srcdir/pam-su" "${pkgdir}/etc/pam.d/su"
  install -m644 "$srcdir/pam-su" "${pkgdir}/etc/pam.d/su-l"

  # broken buildsys doesn't include su(1), which means it
  # isn't even in the dist tarball
  # TODO(dreisner): patched upstream
  install -m644 "$srcdir/su.1" "$pkgdir/usr/share/man/man1/su.1"

  # include tmpfiles fragment for uuidd
  # TODO(dreisner): offer this upstream?
  install -Dm644 "$srcdir/uuidd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/uuidd.conf"
}

md5sums=('dff0d1a9e54fdfe72ab0b1beb9972a77'
         '05ad9951a294677cccaa04ba9aee85dc'
         '7f524538dcf57284a86f03a98e624f04'
         'a39554bfd65cccfd8254bb46922f4a67'
         '4368b3f98abd8a32662e094c54e7f9b1'
         'a31374fef2cba0ca34dfc7078e2969e4'
         'fa85e5cce5d723275b14365ba71a8aad'
         '4085ab75e1006e167dfc57e9d8c2775a')
