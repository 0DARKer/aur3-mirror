# Maintainer: milomouse <vincent[at]fea.st>
# Contributor: judd <jvinet[at]zeroflux.org>
_basename=util-linux
pkgname=${_basename}-aes
pkgver=2.19
pkgrel=4
pkgdesc="Miscellaneous system utilities for Linux, with loop-AES support"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
license=('GPL2')
arch=('i686' 'x86_64')
groups=('base')
depends=('bash' 'ncurses>=5.7' 'zlib' 'filesystem')
optdepends=('perl: for chkdupexe support')
conflicts=('linux32' 'util-linux' 'util-linux-ng' 'e2fsprogs<1.41.8-2')
replaces=('linux32' 'util-linux' 'util-linux-ng')
provides=('linux32' "util-linux=${pkgver}" "util-linux-ng=${pkgver}")
options=('!libtool')
_loopaesdate=20110226
_loopaespatch=${_basename}-${pkgver}-aes-${_loopaesdate}.diff
source=(http://www.kernel.org/pub/linux/utils/${_basename}/v${pkgver}/${_basename}-${pkgver}.tar.bz2
        ${_loopaespatch})

build() {
  cd "${srcdir}/${_basename}-${pkgver}"
  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i hwclock/hwclock.c
  # provide loop-aes support
  patch -Np1 -i "${srcdir}/${_loopaespatch}"
  autoreconf
  automake
  ./configure --enable-arch --enable-write --enable-raw --disable-wall --enable-partx
  make
}

package() {
  cd "${srcdir}/${_basename}-${pkgver}"
  mkdir -p "${pkgdir}/var/lib/hwclock"
  make DESTDIR="${pkgdir}" install
}

md5sums=(
      '590ca71aad0b254e2631d84401f28255'  # util-linux-2.19.tar.bz2
      '81d7a92acb40e543718667057c75aa45') # util-linux-aes-20110226-2.19.diff
sha384sums=(
  '05e8e3a2685ff47c57d56bf9d5bfc9cbe5c1fa85200f403c5ccc08676b9b713fc935b3b040d5d5dcd2c5aa14b631f1bd'  # util-linux-2.19.tar.bz2
  'fc2fc459c95d3c4bae4df2628377068fad7c36c658c55e3859b60a2553a6bb8c9eb55637a8519f7390e25603648831da') # util-linux--aes-20110226-2.19.diff
