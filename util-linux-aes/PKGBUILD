# Maintainer: milomouse <vincent[at]fea.st>
# Contributor: judd <jvinet[at]zeroflux.org>

_basename=util-linux
pkgname=${_basename}-aes
_basever=2.19
pkgver=${_basever}.1
pkgrel=3
pkgdesc="Miscellaneous system utilities for Linux, with loop-AES support"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
license=('GPL2')
arch=('i686' 'x86_64')
groups=('base')
depends=('bash' 'ncurses>=5.7' 'zlib' 'filesystem')
makedepends=('autoconf' 'automake')
optdepends=('perl: for chkdupexe support')
provides=('linux32' "util-linux=${pkgver}" "util-linux-ng=${pkgver}")
conflicts=('linux32' 'util-linux' 'util-linux-ng' 'e2fsprogs<1.41.8-2')
replaces=('linux32' 'util-linux' 'util-linux-ng')
options=('!libtool')
_loopaesdate=20110510
_loopaesdiff=${_basename}-${pkgver}-${_loopaesdate}.diff
install=${pkgname}.install
source=(http://www.kernel.org/pub/linux/utils/${_basename}/v${_basever}/${_basename}-${pkgver}.tar.bz2
        http://loop-aes.sourceforge.net/updates/${_loopaesdiff}.bz2
        mount-segfault-${pkgver}.patch
        ${pkgname}.install)

build() {
  cd "${srcdir}/${_basename}-${pkgver}"
  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i hwclock/hwclock.c
  # fix https://bugs.archlinux.org/task/24261
  msg "Patching with: mount-segfault-${pkgver}.patch"
  patch -Np1 -i "${srcdir}/mount-segfault-${pkgver}.patch"
  # provide loop-AES support
  msg "Patching with: ${_loopaesdiff}"
  patch -Np1 -i "${srcdir}/${_loopaesdiff}"
  msg "Starting autoreconf.."
  autoreconf
  msg "Starting automake.."
  automake
  msg "Starting configure.."
  ./configure --enable-arch --enable-write --enable-raw --disable-wall --enable-partx
  msg "Starting make.."
  make
}

package() {
  cd "${srcdir}/${_basename}-${pkgver}"
  install -dm0755 "${pkgdir}/var/lib/hwclock"
  make DESTDIR="${pkgdir}" install
}

md5sums=('3eab06f05163dfa65479c44e5231932c'
         'd5d3786499faf63ef5632822fabcb62c'
         '3247b52f0e4b8044f23f2f7218e2fdea'
         '7bfa755b056a0c48a504153a3809cf1c')
sha384sums=('6cc4944ef0a7da3d16b66fde6a77ecece9440407f82d8e56024211b95a56e1e8e638f8d1efe79e10754dbe2faa41c9a9'
            '0ff1fad27ead9c054c1c42b335bb0551410a5abda1bad67df502bf5d32b369b479426682c13f7f6d80baf4c8467cab30'
            '05016b154e4282de66c430c104af4409e3c1cf5cd80edb72e098068ad812b9f76b39487e8645379243a6ffdf44ae360a'
            '86089fa37d025b399b1d53ff98781b553667b8651f666d207136e292de9e4df7fe2aa36c9dacdb9522febce971781b8f')
