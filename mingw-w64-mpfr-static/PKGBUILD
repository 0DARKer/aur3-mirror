pkgname=mingw-w64-mpfr-static
pkgver=3.1.1.p2
pkgrel=1
pkgdesc="Multiple-precision floating-point library (mingw-w64, static)"
arch=(any)
url="http://www.mpfr.org/"
license=("LGPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt "mingw-w64-gmp-static>=5.0")
conflicts=(mingw-w64-mpfr)
provides=(mingw-w64-mpfr)
options=(!libtool !strip !buildflags)
source=("https://gforge.inria.fr/frs/download.php/31162/mpfr-3.1.1.tar.xz"
"mpfr-3.1.1-p2.patch")
md5sums=('91d51c41fcf2799e4ee7a7126fc95c17'
         '16cb87dad37d9405953756b544943ce9')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

_optimal_make_jobs() {
  if [ -r /proc/cpuinfo ]; then
    local core_count=$(grep -Fc processor /proc/cpuinfo)
  else
    local core_count=0
  fi
  if [ $core_count -gt 1 ]; then
    echo -n $[$core_count-1]
  else
    echo -n 1
  fi
}

build() {
  cd "$srcdir/mpfr-3.1.1"
  patch -p1 -i $srcdir/mpfr-3.1.1-p2.patch
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver%.p2}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-thread-safe
    make -j$(_optimal_make_jobs)
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.a' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
