# Maintainer: FoolEcho <foolecho at gmail dot com>
pkgname=epson-inkjet-printer-201209w
_pkgname_filter=epson-inkjet-printer-filter
pkgver=1.0.1
pkgrel=1
pkgdesc="Epson Inkjet Printer Driver - XP-750 Series, XP-850 Series"
arch=('i686' 'x86_64')
url="http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX"
license=('LGPL' 'custom:EPSON LICENSE AGREEMENT')
depends=('cups')
source=(http://a1227.g.akamai.net/f/1227/40484/1d/download.ebz.epson.net/dsc/f/01/00/01/98/21/5a400e1e9ff60e9c66e3481710b7d606f76d5833/${pkgname}-${pkgver}-1lsb3.2.src.rpm)
sha256sums=('ec0471ed0733269f9e7a831542c7a59ce03c654d20ad7f59a7d56401573bbd9d')

build() {
  cd "$srcdir"
  tar xzf $pkgname-$pkgver.tar.gz
  FILTER_FILE=`ls ${_pkgname_filter}*.tar.gz`
  tar xzf $FILTER_FILE

  cd "${FILTER_FILE%.tar.gz}"
  aclocal
  libtoolize
  chmod +x configure
  # if you have runtime problems: add "--enable-debug" and look into /tmp/epson-inkjet-printer-filter.txt
  ./configure LDFLAGS="$LDFLAGS -Wl,--no-as-needed" --prefix=/opt/$pkgname
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "$pkgdir/opt/$pkgname/"
  if [ "$CARCH" = "x86_64" ]; then
     cp -r lib64 "$pkgdir/opt/$pkgname/"
  else
     cp -r lib "$pkgdir/opt/$pkgname/"
  fi
  cp -r resource "$pkgdir/opt/$pkgname/"

  if [ -e "watermark" ]; then
      cp -r watermark "$pkgdir/opt/$pkgname/"
  fi
  install -d "$pkgdir/usr/share/cups/model/$pkgname"
  install -m 644 ppds/* "$pkgdir/usr/share/cups/model/$pkgname"

  cd "$srcdir"
  FILTER_FILE=`ls $_pkgname_filter*.tar.gz`
  cd "${FILTER_FILE%.tar.gz}"
  install -d "$pkgdir/opt/$pkgname/cups/lib/filter/"
  install -m 755 src/epson_inkjet_printer_filter "$pkgdir/opt/$pkgname/cups/lib/filter/epson_inkjet_printer_filter"

  #license
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -D -m644 COPYING{,.EPSON,.LIB} "${pkgdir}/usr/share/licenses/${pkgname}/"
}
