# Maintainer: SpepS < dreamspepser at yahoo dot it>

_kver=`uname -r | sed 's/\..-.//'`
pkgname=avld
pkgver=0.1.4
pkgrel=6
pkgdesc="A V4L dummy video device built to simulate an input video device like a webcam or a video capture card."
arch=('i686' 'x86_64')
url="http://allonlinux.free.fr/Projets/AVLD/"
license=('GPL')
depends=('v4l-utils')
makedepends=('linux-headers')
source=("${url}src/${pkgname}_${pkgver}.tar.bz2"
	"${pkgname}_${pkgver}.patch" "bad.patch"
        "libv4l1-videodev.h")
install="${pkgname}.install"
md5sums=('dbe16be302293cd0b286f5bef8b33c5f'
         '926ab9d4e3c3ee2c7d0441ce546b2b9c'
         'beb282a1f21ba556cbd6436bf8de3d00'
         '7eda710e3032c0fe27d2063e2d0b51eb')

build() {
  cd "$srcdir/${pkgname}_${pkgver}"

  # Patching for newer kernels and memcpy issue (prevent high cpu usage)
  patch video_device.c ../${pkgname}_${pkgver}.patch
  patch video_device.c ../bad.patch

  # build using v4l-utils headers
  sed -i 's/<linux\/\(videodev.h\)>/"libv4l1-\1"/' video_device.c

  # remove old constants
  sed -i '347,350d;358,361d' video_device.c

  # use patched libv4l1-videodev.h header
  cp ../libv4l1-videodev.h .

  # kernel 3.2 patch
  sed -e "22a#if LINUX_VERSION_CODE >= KERNEL_VERSION(3,2,0)" \
      -e "22a#include <linux/module.h>\n#endif" \
      -i video_device.c

  make EXTRA_CFLAGS="-I."
}

package() {
  cd "$srcdir/${pkgname}_${pkgver}"

  # module
  install -Dm644 avld.ko \
    "$pkgdir/lib/modules/extramodules-$_kver/avld.ko"

  # readme
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
