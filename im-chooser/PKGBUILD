# Maintainer: Armin K. <krejzi at email dot com>

pkgname=im-chooser
pkgver=1.6.4
pkgrel=1
pkgdesc="GUI configuration tool for IMSettings"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/im-chooser/"
license=('GPL2')
depends=('gtk3' 'imsettings')
makedepends=('libxfce4util')
optdepends=('libxfce4util: for xfce4-im-chooser')
options=('!libtool')
source=("https://fedorahosted.org/releases/i/m/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('fbf6598df98c9992e91aa62b7d33bc1b')
install=im-chooser.install

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR="${pkgdir}"

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

