# Contributor: Henri HÃ¤kkinen  <henuxd@gmail.com>

pkgname=perl-search-xapian
pkgver=1.0.12.0
pkgrel=1
pkgdesc="Perl/CPAN Search::Xapian module - Perl frontend for the Xapian C++ search library"
arch=('i686' 'x86_64')
url="http://search.cpan.org/~olly/Search-Xapian-1.0.12.0/"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'xapian-core')
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/O/OL/OLLY/Search-Xapian-${pkgver}.tar.gz)
md5sums=('057e1fd845a207bd4fad4766e3cc63e2')

build() {
  cd "$srcdir/Search-Xapian-$pkgver"

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor || return 1
  make || return 1
  make install DESTDIR="$pkgdir/" || return 1

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}

# vim:set ts=2 sw=2 et:
