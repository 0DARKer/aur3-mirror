# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Alexander Mieland <dma147@linux-stats.org>
pkgname=metisse
pkgver=0.4.1
pkgrel=4
pkgdesc="X-based window system alternate to looking glass or other 3D"
arch=('i686' 'x86_64')
license=('GPL')
url="http://insitu.lri.fr/metisse/"
depends=('nucleo' 'x-server' 'libglade' 'gconf' 'mesa')
optdepends=('avahi')
options=(!libtool)
source=(http://insitu.lri.fr/metisse/download/$pkgname/$pkgname-$pkgver.tar.bz2 \
        metisse.desktop startmetisse gcc-4.4_fix.diff libpng1.4_fix.diff)
md5sums=('6bc947ea60612f6dbf0475a25327814b'
         'a6be7ea3d95133b76a2aaec8f94ccf69'
         'c75e0b205739f000574c485e7012db2e'
         '174182ffcdd5b8628a42ddd0527399b7'
         '965d5ece60ec4e5163d0301d26c8ce23')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -Np1 < "$srcdir"/gcc-4.4_fix.diff || true
  patch -Np1 < "$srcdir"/libpng1.4_fix.diff || true

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
   --libexecdir=/usr/lib/ --enable-mmx --with-x
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # install startup scripts
  install -D -m755 "$srcdir"/startmetisse "$pkgdir"/usr/bin/startmetisse
  install -D -m644 "$srcdir"/metisse.desktop "$pkgdir"/etc/X11/sessions/metisse.desktop
}
