# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sven Schneider <mr_xyz_unknown@web.de>
# Contributor: Dustin Mays <dork.fish.wat@gmail.com>
# Contributor: scj <scj archlinux us>

pkgname=sfml2-svn
pkgver=1616
pkgrel=1
pkgdesc="A simple, fast, cross-platform, and object-oriented multimedia API"
arch=('i686' 'x86_64')
url="http://sfml-dev.org"
license=('zlib')
depends=('libsndfile' 'libxrandr' 'libjpeg' 'libpng' 'openal' 'glew' 'freetype2' 'soil')
makedepends=('subversion' 'mesa' 'cmake')
conflicts=('sfml' 'sfml-svn')
provides=('sfml')
source=()
md5sums=()

_svntrunk=https://sfml.svn.sourceforge.net/svnroot/sfml/branches/sfml2
_svnmod=sfml2

build() {
	rm -rf ${srcdir}/$_svnmod-build

	if [ -d $_svnmod/.svn ]; then
		(cd $_svnmod && svn up -r $pkgver && cd ${srcdir})
	else
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
	fi

	msg "SVN checkout done or server timeout."

	svn export $_svnmod $_svnmod-build
	cd $_svnmod-build

	msg "Applying patches"
	sed -e 's/<SOIL\/SOIL.h>/<SOIL.h>/g' -i src/SFML/Graphics/ImageLoader.cpp

	msg "Starting make."
	cmake -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd $_svnmod-build

	make DESTDIR=${pkgdir} install

	mkdir -p ${pkgdir}/usr/share/sfml
  cp -r doc ${pkgdir}/usr/share/sfml

  install -Dm 644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Samples currently broken >.<
	#LDFLAGS="-L${pkgdir}/usr/lib" make samples || return 1
	#cp -r samples ${pkgdir}/usr/share/sfml
	#find "${pkgdir}/usr/share/sfml/samples" -name "*.o" -exec rm {} \;
}

# vim:ts=2:sw=2
