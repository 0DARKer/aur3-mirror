# Contributor: Andras Biro <bbandi86@gmail.com>
# PKGBUILD based on the gcc-avr PKGBUILD by 
#	Corrado Primier <bardo@aur.archlinux.org> and danst0 <danst0@west.de>

pkgname=gcc-avr-tinyos
pkgver=4.1.2
pkgrel=1
pkgdesc="The GNU avr Compiler Collection with TinyOS patches"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'custom')
url="http://gcc.gnu.org/"
depends=('binutils-avr-tinyos' 'cloog-ppl' 'gcc-libs' 'mpfr')
provides=('gcc-avr')
options=('!ccache' '!distcc' '!emptydirs' '!libtool' '!strip')
source=(http://www.tinyos.net/dist-2.1.0/tools/source/avr-gcc-${pkgver}.tgz)
md5sums=('17992734ca98c03cbbbcd8223d23509d')

build() {
  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  cd ${srcdir}/avr-gcc-${pkgver}

  mkdir build
  cd build
  ../configure --disable-libssp \
               --disable-nls \
               --enable-languages=c,c++ \
               --infodir=/usr/share/info \
               --libdir=/usr/lib \
               --libexecdir=/usr/lib \
               --mandir=/usr/share/man \
               --prefix=/usr \
               --target=avr \
               --with-gnu-as \
               --with-gnu-ld \
               --with-as=/usr/bin/avr-as \
               --with-ld=/usr/bin/avr-ld

  make || return 1
}

package() {
  cd ${srcdir}/avr-gcc-${pkgver}

  cd build
  make -j1 DESTDIR=${pkgdir} install || return 1

  rm -f ${pkgdir}/usr/lib/libiberty.a || return 1
  rm -rf ${pkgdir}/usr/share/man/man7 || return 1
  rm -rf ${pkgdir}/usr/share/info || return 1
}

# vim:set ts=2 sw=2 et:
