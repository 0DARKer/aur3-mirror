# Contributor: Vitaliy Berdinskikh ur6lad[at]archlinux.org.ua

[ $CARCH = "x86_64" ] && _arch=-x86_64
_eclipse_name=helios
_eclipse_release=SR2
_eclipse_timestamp=201102101200

pkgname=eclipse-jee
pkgver=3.6.2
pkgrel=1
pkgdesc="Eclipse IDE for Java EE Developers"
arch=('i686' 'x86_64')
url="http://www.eclipse.org"
license=('EPL')
depends=('java-environment>=6' 'gtk2>=2.16.1' 'xulrunner>=1.9.0.10')
makedepends=('unzip' 'patch')
provides=('eclipse')
conflicts=('eclipse')
install=${pkgname}.install
source=(eclipse.sh eclipse.desktop eclipse.ini.patch eclipse.svg #http://www.bearfruit.org/files/eclipse-icon-clean.svg
		http://ftp.ing.umu.se/mirror/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/${pkgname}-${_eclipse_name}-${_eclipse_release}-linux-gtk${_arch}.tar.gz)

package() {
	local _ant_path=eclipse/plugins/org.apache.ant_1.7.1.v20100518-1145
	local _icon_path=/usr/share/eclipse/plugins/org.eclipse.platform_${pkgver}.v${_eclipse_timestamp}

	cd $srcdir

	patch -p1 < eclipse.ini.patch
	# fix python 2.7 path
	sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' ${_ant_path}/bin/runant.py

	mkdir -p $pkgdir/usr/{bin,share/applications}
	mkdir -p $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48,scalable}/apps

	mv eclipse $pkgdir/usr/share
	install -D -m 755 eclipse.sh $pkgdir/usr/bin/eclipse
	install -D -m 644 eclipse.desktop $pkgdir/usr/share/applications
	install -D -m 644 eclipse.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/eclipse.svg
	ln -s ${_icon_path}/eclipse.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png
	ln -s ${_icon_path}/eclipse32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png
	ln -s ${_icon_path}/eclipse48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png
}

[ $CARCH = "x86_64" ] && {
md5sums=('00598e0866353f7a7b1a5ed65dc01610'
         '0537090ceeb11a2af66676481e8cf797'
         'cd86f9269a19a25e69168857d26050f0'
         '7e6f8ec0ba26d61146d4037c89d583dd'
         'ccdbc68cc20a76dc853c90eb89cd0617')
sha256sums=('4cca2873697a3af39a96449021d7fdc2fc2b01abc9f7883946081f6be7a5ed48'
            '4eb2189c96fcfa340886b049b34dc3636d7b2bfa865140dc72edb61455d900c3'
            'fc64b153dc26e52deda73f80ef1996a64feb42b0bcaaab92ba64ef65fac90a23'
            '4d7adc7a0f940d8601cae7afb8dd8be407a5c4fd5cdeca331a6c79bc288edee7'
            '886316427ca366a5b6238a16bb85cadd140de1165dc488b099b44d2cf541a7a8')
} || {
md5sums=('00598e0866353f7a7b1a5ed65dc01610'
         '0537090ceeb11a2af66676481e8cf797'
         'cd86f9269a19a25e69168857d26050f0'
         '7e6f8ec0ba26d61146d4037c89d583dd'
         '5952cd8b01e36dd4646ebd3faad28a6d')
sha256sums=('4cca2873697a3af39a96449021d7fdc2fc2b01abc9f7883946081f6be7a5ed48'
            '4eb2189c96fcfa340886b049b34dc3636d7b2bfa865140dc72edb61455d900c3'
            'fc64b153dc26e52deda73f80ef1996a64feb42b0bcaaab92ba64ef65fac90a23'
            '4d7adc7a0f940d8601cae7afb8dd8be407a5c4fd5cdeca331a6c79bc288edee7'
            'c0456a7b7b4d0fa05bcda8227def368c74459ea99c742b6dbc3f98f8e7e92466')
}
