# Contributor: Murtuza Akhtari <inxsible at gmail dot com>

pkgname=eclipse-jee
pkgver=3.6.0
_platform_ver=3.6.0
_eclipse_name=helios
_eclipse_release=R
pkgrel=1
_date=201006080911
pkgdesc="An IDE for Java EE and other languages"
arch=('i686' 'x86_64')
url="http://eclipse.org"
depends=('java-environment>=6' 'gtk2>=2.16.1' 'unzip' 'xulrunner>=1.9.0.10')
provides=('eclipse')
conflicts=('eclipse')
install=eclipse.install
makedepends=('zip' 'patch')
license=("EPL/1.1")
if [ "$CARCH" = "x86_64" ]; then
source=(http://mirror.cs.rit.edu/mirrors/eclipse/technology/epp/downloads/release/$_eclipse_name/$_eclipse_release/eclipse-jee-$_eclipse_name-linux-gtk-x86_64.tar.gz
            http://www.bearfruit.org/files/eclipse-icon-clean.svg
            eclipse.sh
            eclipse.desktop
	    eclipse.ini.patch)
md5sums=('652b78cd71c7b98985fdf993daad16f6'
         '77cff7543ccf7e177cb6667f5a20ce19'
         '7ea99a30fbaf06ec29261541b8eb1e23'
         'd88490571b49c8ebb221ed349815d040'
         'e61c61a282f97754dff09ce8de286798')
else
source=(http://mirror.cs.rit.edu/mirrors/eclipse/technology/epp/downloads/release/$_eclipse_name/$_eclipse_release/eclipse-jee-$_eclipse_name-linux-gtk.tar.gz
            http://www.bearfruit.org/files/eclipse-icon-clean.svg
            eclipse.sh
            eclipse.desktop
	    eclipse.ini.patch)
md5sums=('ff49d3b1040b429a0c18d0bcaa6cc4b8'
         '77cff7543ccf7e177cb6667f5a20ce19'
         '7ea99a30fbaf06ec29261541b8eb1e23'
         'd88490571b49c8ebb221ed349815d040'
         'e61c61a282f97754dff09ce8de286798')
fi

build() {
  cd "${srcdir}/eclipse" || return 1

  # patch to increase default memory limits and to allow for user configuration of plugins
  patch -Np0 -i "${srcdir}/eclipse.ini.patch" || return 1

  # install eclipse
  install -m755 -d "${pkgdir}/usr/share"
  mv "${srcdir}/eclipse" "${pkgdir}/usr/share/" || return 1

  # install misc
  mkdir -p ${pkgdir}/usr/bin ${pkgdir}/usr/share/applications \
    ${pkgdir}/usr/share/icons/hicolor/{16x16,32x32,48x48}/apps || return 1
  install -m755 "${srcdir}/eclipse.sh" "${pkgdir}/usr/bin/eclipse" || return 1
  install -m644 "${srcdir}/eclipse.desktop" "${pkgdir}/usr/share/applications/" || return 1
  ln -s /usr/share/eclipse/plugins/org.eclipse.platform_${_platform_ver}.v${_date}/eclipse.png \
    "${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png" || return 1
  ln -s /usr/share/eclipse/plugins/org.eclipse.platform_${_platform_ver}.v${_date}/eclipse32.png \
    "${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png" || return 1
  ln -s /usr/share/eclipse/plugins/org.eclipse.platform_${_platform_ver}.v${_date}/eclipse48.png \
    "${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png" || return 1


  # install icon
  install -D -m644 $startdir/src/eclipse-icon-clean.svg $startdir/pkg/usr/share/icons/hicolor/scalable/apps/eclipse.svg || return 1
}
