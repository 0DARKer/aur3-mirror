_pkgbasename=libffi
pkgname=lib32-$_pkgbasename
pkgver=3.0.9
pkgrel=3
pkgdesc="A portable, high level programming interface to various calling conventions"
arch=('x86_64')
license=('MIT')
url="http://sourceware.org/libffi"
depends=('lib32-glibc' "$_pkgbasename")
makedepends=('gcc-multilib')
options=('!libtool')
source=(ftp://sourceware.org/pub/$_pkgbasename/$_pkgbasename-${pkgver}.tar.gz)
md5sums=('1f300a7a7f975d4046f51c3022fa5ff1')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC="gcc -m32" CXX="g++ -m32"
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{share,lib32/libffi-?.?.?}
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
