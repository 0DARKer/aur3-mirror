pkgname=eihort
pkgver=0.3.10
pkgrel=4
pkgdesc='A OpenGL world renderer for minecraft with a really long view distance'
license=('BSD')
url="http://www.minecraftforum.net/topic/438878-/"
arch=('i686' 'x86_64')
depends=('lua' 'glew' 'sdl_image' 'zlib')
source=("https://bitbucket.org/lloigor/$pkgname/get/$pkgver.tar.gz"
        'icon.tar.gz' "$pkgname" "$pkgname.desktop" 'makefile.patch')
sha1sums=('e56407d07769ad09798af157854efabbfb13528c'
          'ba8443da8ff9ff45f3a29069726afb440aaabf1a'
          '54aca14c3362849a64e0a81bd66652408526095a'
          '5fd5dd65b7c2cbc728d384e482163d3a8bf25a80'
          'be70f01f5e33364d664eec3a65b8dd3db0e1ffaa')

build() {
	mv "$srcdir/lloigor-$pkgname-"* "$srcdir/$pkgname-$pkgver"
	cd "$srcdir/$pkgname-$pkgver"
	
	patch -p1 < ../makefile.patch
	export version=$pkgver
	unset CFLAGS
	unset CXXFLAGS
	make -f makefile.gnu CXXFLAGS.EXTRA='-lz -lGL -lX11' "$pkgname-$pkgver-linux-$(uname -m)"
}

package() {
	cd "$pkgdir"
	
	install -D "$srcdir/$pkgname-$pkgver/$pkgname-$pkgver-linux-$(uname -m)" "usr/share/$pkgname/$pkgname"
	install -m644 $(find "$srcdir/$pkgname-$pkgver/deploy/" \( ! -name "*.dll" \) -type f) "usr/share/$pkgname/"
	
	for f in "$srcdir/icon/"*.png; do
		s="$(basename $f .png)"
		install -Dm644 "$f" "usr/share/icons/hicolor/${s}x${s}/apps/$pkgname.png"
	done
	
	install -D "$srcdir/$pkgname" "usr/bin/$pkgname"
	install -D "$srcdir/$pkgname.desktop" "usr/share/applications/$pkgname.desktop"
}