pkgname=eihort
pkgver=0.3.12.1
pkgrel=1
pkgdesc='A OpenGL world renderer for minecraft with a really long view distance'
license=('BSD')
url="http://www.minecraftforum.net/topic/438878-/"
arch=('i686' 'x86_64')
depends=('lua' 'glew' 'sdl_image' 'zlib')
makedepends=('python')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/lloigor/$pkgname/get/$pkgver.tar.gz"
        'icon.tar.gz' "$pkgname.desktop")
sha1sums=('eaa652afe3f6443d571bbb538bd9f10f0b258089'
          'ba8443da8ff9ff45f3a29069726afb440aaabf1a'
          '5fd5dd65b7c2cbc728d384e482163d3a8bf25a80')

build() {
	cd "$srcdir"
	
	#the directory inside the tar archive has a solly name so letâ€™s move it to somewhere else
	dir="$(tar tf "$pkgname-$pkgver.tar.gz" | head -n 1 | sed "s|/.*||")"
	mv -f "$dir" "$pkgname-$pkgver"
	cd "$pkgname-$pkgver"
	
	#replaces hg for the makefile
	cat >hg <<-EOF
	#!/bin/sh
	echo "v$pkgver"
	EOF
	chmod +x hg
	
	LDLIBS="$(pkg-config --cflags --libs gl x11 xext)"
	PATH=".:$PATH" make LDFLAGS.EXTRA="$LDLIBS" "$pkgname-$pkgver-linux-$CARCH"
}

package() {
	cd "$pkgdir"
	
	install -D "$srcdir/$pkgname-$pkgver/$pkgname-$pkgver-linux-$CARCH" "usr/share/games/$pkgname/$pkgname"
	install -m644 $(find "$srcdir/$pkgname-$pkgver/deploy/" \( ! -name "*.dll" \) -type f) "usr/share/games/$pkgname/"
	
	for f in "$srcdir/icon/"*.png; do
		s="$(basename $f .png)"
		install -Dm644 "$f" "usr/share/icons/hicolor/${s}x${s}/apps/$pkgname.png"
	done
	
	install -D "$srcdir/$pkgname-$pkgver/debian/$pkgname" "usr/bin/$pkgname"
	install -D "$srcdir/$pkgname.desktop" "usr/share/applications/$pkgname.desktop"
}