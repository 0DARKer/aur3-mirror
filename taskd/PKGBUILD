# Maintainer: Rich Li <rich at dranek com>
# Contributor: Markus Holtermann <aur@markusholtermann.eu>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=taskd
pkgver=1.0.0
pkgrel=4
pkgdesc="A lightweight, secure server providing multi-user, multi-client access to task data"
url='http://tasktools.org/projects/taskd.html'
license=('MIT')
arch=('i686' 'x86_64')
depends=('task' 'gnutls')
makedepends=('cmake')
backup=('etc/conf.d/taskd')
install=taskd.install
source=('http://taskwarrior.org/download/taskd-1.0.0.tar.gz'
        'taskd.conf' 'taskd.service'
        '0001-fix-pki-generate-path.patch')
md5sums=('1cead23539e36d5623cb3ca1225072c0'
         'ab545ca3081efb251a76f48574c7a6e4'
         'fea451584f175beae776dc9f92281a22'
         'e4ef803c7b6682e5893ec442b205cea2')

prepare() {
  patch -p0 < "0001-fix-pki-generate-path.patch"
}

build() {
  cd "$srcdir/taskd-${pkgver}"
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

# Doesn't exist in 1.0.0 tarball but it does in git...
#check() {
#    cd "$srcdir/taskd-${pkgver}/test"
#    make
#    ./run_all
#}

package() {
  cd "$srcdir/taskd-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/taskd.conf \
    "${pkgdir}"/etc/conf.d/taskd

  install -Dm644 "${srcdir}"/taskd.service \
    "${pkgdir}"/usr/lib/systemd/system/taskd.service

  install -d "${pkgdir}"/var/lib/taskd

  install -Dm644 LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  install -d "${pkgdir}"/usr/share/taskd/pki

  install -D "${srcdir}"/taskd-${pkgver}/pki/generate{,.ca,.client,.crl,.server} \
    "${pkgdir}"/usr/share/taskd/pki/
}
