# See http://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines
# for more information on packaging from SVN sources.

# Maintainer: Patrice Peterson <runiq at archlinux dot us>
pkgname=kolmafia-svn
_pkgname="kolmafia"
pkgver=8998
pkgrel=1
pkgdesc="An alternate interface to the online adventure game Kingdom of Loathing."
arch=('i686' 'x86_64')
url="http://kolmafia.sourceforge.net/"
license=('BSD')
depends=('java-runtime')
makedepends=('apache-ant' 'subversion')
provides=(kolmafia)
conflicts=(kolmafia)
source=(kolmafia.sh)
md5sums=('30c78c3c2ba560099fd527bbd2e88ea5')

_svntrunk=https://kolmafia.svn.sourceforge.net/svnroot/kolmafia
_svnmod=kolmafia

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting ant..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ant -Djarfile=$_pkgname.jar jar desktop
}

package() {
  cd "$srcdir"/$_svnmod-build
  install -D -m644 $_pkgname.jar \
      "$pkgdir"/usr/share/java/$_pkgname/$_pkgname.jar
  install -D -m755 $srcdir/$_pkgname.sh "$pkgdir"/usr/bin/$_pkgname
  install -D -m644 build/$_pkgname.desktop \
      "$pkgdir"/usr/share/applications/$_pkgname.desktop
  install -D -m644 util/debian/kol.xpm \
      "$pkgdir"/usr/share/pixmaps/kol.xpm
}
