# Maintainer: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Brian Schubert <bewschubert@gmail.com>

_build_doc=ON
_build_apps=ON
pkgname=openmesh
pkgver=3.3
pkgrel=1
pkgdesc="A generic and efficient data structure for representing and manipulating polygonal meshes"
arch=('i686' 'x86_64')
url="http://www.openmesh.org"
license=('custom')
depends=('mesa')
optdepends=('qt4: for using included applications' )    
install=openmesh.install
source=("${pkgname}-${pkgver}.tar.bz2::http://www.openmesh.org/media/Releases/${pkgver}/OpenMesh-${pkgver}.tar.bz2"
    doc-install.patch)
md5sums=('7c4cb02e7218f847e2420e3d43a04981'
         '7598bde67d5cb8e868d4cf3b28d81545')

if [[ "${_build_doc}" == "ON" && "${_build_apps}" == "ON" ]]; then
    makedepends=('cmake' 'qt4' 'graphviz' 'doxygen')
elif [[ "${_build_doc}" == "ON" ]]; then
    makedepends=('cmake' 'graphviz' 'doxygen')
elif [[ "${_build_apps}" == "ON" ]]; then
    makedepends=('cmake' 'qt4')
else
    makedepends=('cmake')
fi

prepare() {
    if [[ "${_build_doc}" == "ON" ]]; then
	patch -Np0 -i doc-install.patch
    fi
}

build() {
    mkdir -p build
    cd build
    #cd "${srcdir}/OpenMesh-${pkgver}"
    cmake ../OpenMesh-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_APPS=${_build_apps}
    make

    if [[ "${_build_doc}" == "ON" ]]; then
	make doc
    fi
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    
    # install licenses
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/COPYING \
        "${pkgdir}"/usr/share/licenses/openmesh/COPYING
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/COPYING.EXCEPTIONS \
        "${pkgdir}"/usr/share/licenses/openmesh/COPYING.EXCEPTIONS
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/COPYING.LESSER \
        "${pkgdir}"/usr/share/licenses/openmesh/COPYING.LESSER
    install -D -m644 ../OpenMesh-${pkgver}/LICENSE/LICENSEHEADER \
        "${pkgdir}"/usr/share/licenses/openmesh/LICENSEHEADER

    # add openmesh library path to ld.so.conf
    install -d -m755 "$pkgdir/etc/ld.so.conf.d/"
    echo "/usr/lib/OpenMesh" > "$pkgdir/etc/ld.so.conf.d/openmesh.conf"
}
