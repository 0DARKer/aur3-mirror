#Contributor: MatToufoutu <mattoufootu[at]gmail[dot]com>
#Contributor: Jan Lieven jan[at]das<minus>labor(dot)org

pkgname=john-mpi
pkgver=1.8.0
pkgrel=1
_patchlevel=jumbo-1
arch=('i686' 'x86_64')
pkgdesc='JohnTheRipper password cracker with Jumbo patch and MPI support'
url='http://openwall.info'
license=('GPL')
provides=('john')
conflicts=('john' 'john-latest')
# For OpenCL support change swap the {make}depends arrays
depends=('mpich')
makedepends=('mpich')
# Remember to include one of the following packages into the depends
# array for OpenCL to work:
# amdstream
# opencl-nvidia
# intel-opencl-sdk
#depends=('mpich'
#         'libopencl'
#         'YOUR_OPENCL_VENDOR_HERE')
#makedepends=('opencl-headers'
#             'libopencl')
backup=(etc/john/john.conf
        etc/john/dumb32.conf
        etc/john/dumb16.conf
        etc/john/dynamic.conf)

source=("http://www.openwall.com/john/j/john-${pkgver}-${_patchlevel}.tar.gz"
        "params.h.patch")

prepare() {
  cd ${srcdir}/john-${pkgver}-${_patchlevel}/src
  patch -p0 < ${srcdir}/params.h.patch
}

build() {
  export PATH=/opt/mpich/bin:${PATH}

  cd ${srcdir}/john-${pkgver}-${_patchlevel}/src
	./configure --prefix=/usr --enable-mpi

	if [ $(which icc) -eq 0 ]; then
		case "${CARCH}" in
			"x86_64")
				make sse-intrinsics-64.S
				break
				;;
			"i686")
				make sse-intrinsics-32.S
				break
				;;
		esac
	fi

	make
}

package() {
	# config file
	sed -i 's|$JOHN|/usr/share/john|g' ${srcdir}/john-${pkgver}-${_patchlevel}/run/john.conf
  sed -i 's|/usr/share/john/dumb|/etc/john/dumb|g' ${srcdir}/john-${pkgver}-${_patchlevel}/run/john.conf
  sed -i 's|.include <dynamic.conf>|#.include <dynamic.conf>|g' ${srcdir}/john-${pkgver}-${_patchlevel}/run/john.conf
  sed -i 's|.include "/usr/share/john/john.local.conf"|#.include "/usr/share/john/john.local.conf"|g' ${srcdir}/john-${pkgver}-${_patchlevel}/run/john.conf
  install -dm755 ${pkgdir}/etc/john
	install -m644 ${srcdir}/john-${pkgver}-${_patchlevel}/run/*.conf ${pkgdir}/etc/john/
	
	# docs
	install -d -m755 ${pkgdir}/usr/share/doc/john
	install -m644 ${srcdir}/john-${pkgver}-${_patchlevel}/doc/* ${pkgdir}/usr/share/doc/john/
	install -d -m755 ${pkgdir}/usr/share/john/
	install -m644 ${srcdir}/john-${pkgver}-${_patchlevel}/run/*.chr ${pkgdir}/usr/share/john/
	install -m644 ${srcdir}/john-${pkgver}-${_patchlevel}/run/password.lst ${pkgdir}/usr/share/john/
	install -Dm644 ${srcdir}/john-${pkgver}-${_patchlevel}/doc/LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE

	# install binaries
  cd ${srcdir}/john-${pkgver}-${_patchlevel}/run/
  for i in $(find . -type f -perm 755); do
    install -Dm755 ${i} ${pkgdir}/usr/bin/${i}
  done
	cd ${pkgdir}/usr/bin
	ln -s john unafs
	ln -s john unique
	ln -s john unshadow
	ln -s john undrop
}

sha512sums=('a68ff3923842325e09359b39d343c0c43edb445365bc54175f2438f7ee2af8474ea66defb62fdf27897333e4ba7b5376b90698a9b18534cf88d2fae880c768cd'
            'b50e0961a75b57c487046ac6aca0a3149b81a733deb305bcb1e34169e0f37ddd8f11f7ef8dcb04a1d2bc5e689b32298f1b47ce87eef29942a958fb438772465d')
