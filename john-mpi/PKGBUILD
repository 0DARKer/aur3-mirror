#Contributor: MatToufoutu <mattoufootu[at]gmail[dot]com>
#Contributor: Jan Lieven jan[at]das<minus>labor(dot)org

pkgname=john-mpi
pkgver=1.7.7
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='JohnTheRipper password cracker with Jumbo patch and MPI support'
url='http://openwall.info'
license=('GPL')
provides=('john')
conflicts=('john' 'john-latest')
depends=('mpich2')

source=("http://www.openwall.com/john/g/john-${pkgver}.tar.gz"
        "http://openwall.info/wiki/_media/john/john-1.7.7/john-1.7.7-jumbo-5-RC6.diff.gz"
        "http://openwall.info/wiki/_media/john/john-1.7.7-jumbo-5-RC6-omp_fixes.diff.gz"
        "http://openwall.info/wiki/_media/john/john-1.7.7-jumbo-5-RC6-loader_mute_warning.diff.gz"
        "http://openwall.info/wiki/_media/john/john-1.7.7/john-1.7.7-jumbo-5-RC6-mssql_SSE2_patch-2.diff.gz"
        "http://openwall.info/wiki/_media/john/john-1.7.7-jumbo-5-RC6-various-patches.diff.gz"
        "http://openwall.info/wiki/_media/john/john-1.7.7/john-1.7.7-jumbo-5-RC6-more-mssql_fixes.diff.gz")

build() {
    export PATH=/opt/mpich2/bin:${PATH}

    cd ${srcdir}/john-${pkgver}/

    patch -p1 < ${srcdir}/john-${pkgver}-jumbo-5-RC6.diff
    patch -p1 < ${srcdir}/john-${pkgver}-jumbo-5-RC6-omp_fixes.diff
    patch -p1 < ${srcdir}/john-${pkgver}-jumbo-5-RC6-loader_mute_warning.diff
    patch -p1 < ${srcdir}/john-${pkgver}-jumbo-5-RC6-mssql_SSE2_patch-2.diff
    patch -p1 < ${srcdir}/john-${pkgver}-jumbo-5-RC6-various-patches.diff
    patch -p1 < ${srcdir}/john-${pkgver}-jumbo-5-RC6-more-mssql_fixes.diff

    cd src

    sed -i 's|#CC = mpicc|CC = mpicc|g' Makefile                                                                                                                    
    sed -i 's|#MPIOBJ = john-mpi.o|MPIOBJ = john-mpi.o|g' Makefile

    if [ "$CARCH" == "x86_64" ]; then
      if [ $(which icc) -eq 0 ]; then
        make linux-x86-64-icc
      else
        make linux-x86-64
      fi
    else
        make linux-x86-sse2
    fi
}

package() {
    cd ${srcdir}/john-${pkgver}/
    #config file
    install -d ${pkgdir}/etc
    install -m644 run/john.conf ${pkgdir}/etc/john.conf 

    #docs
    install -d ${pkgdir}/usr/share/doc/john/
    install -m644 doc/* ${pkgdir}/usr/share/doc/john/ 

    #wordlists, charset and stats files
    install -d ${pkgdir}/usr/share/john/
    install -m644 run/{{all,alnum,alpha,digits,lanman}.chr,password.lst,stats} ${pkgdir}/usr/share/john/ 

    #binaries
    install -d ${pkgdir}/usr/bin/
    install -m755 run/john ${pkgdir}/usr/bin/john 
    install -m755 run/mailer ${pkgdir}/usr/bin/john-mailer 
    install -m755 run/netntlm.pl ${pkgdir}/usr/bin/john-netntlm 
    install -m755 run/sap_prepare.pl ${pkgdir}/usr/bin/john-sap_prepare 
    install -m755 run/sha-dump.pl ${pkgdir}/usr/bin/john-ldap-dump 
    install -m755 run/netscreen.py ${pkgdir}/usr/bin/john-netscreen 
    install -m755 run/genincstats.rb ${pkgdir}/usr/bin/john-genincstats 

    #additional binaries
    install -d ${pkgdir}/usr/sbin/
    install -m755 run/genmkvpwd ${pkgdir}/usr/sbin/genmkvpwd 
    install -m755 run/calc_stat ${pkgdir}/usr/sbin/calc_stat 
    install -m755 run/mkvcalcproba ${pkgdir}/usr/sbin/mkvcalcproba 
    install -m755 run/tgtsnarf ${pkgdir}/usr/sbin/tgtsnarf 
    
    #symlinks
    cd ${pkgdir}/usr/bin/
    ln -s john unafs
    ln -s john unique
    ln -s john unshadow
    ln -s john undrop
}

sha256sums=('b821bac5059a3cdc8beb9a715691a9a412db4947345adb7f88eda2fa93293030'
            'b4c522019a20372fff8a8ef9f4275629a9f4b441c1811a0eede2ed6b8f0239b4'
            'bcc91d73a8277c0359c597d89d1bdbf8092b308cbf9e82fdb36ebacc85667b8d'
            '29ada2803f31a53d523144e32e09ceaadcce140ff5d157c513c1f7ac3f76efaf'
            '944fc62133ce97333446b2619d648b1c1c7038b772bd533395594961171d8fbe'
            '319818e0a943788185c876c4948dbd4d745f3ffa5e6b960194589dcea85a34c4'
            '57c6a4b5f1360ef3d6ef64e61e317cf3eafde541d3a6d41d071306a2df68f272')
