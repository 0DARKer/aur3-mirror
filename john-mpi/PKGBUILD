#Contributor: MatToufoutu <mattoufootu[at]gmail[dot]com>
#Contributor: Jan Lieven jan[at]das<minus>labor(dot)org

pkgname=john-mpi
pkgver=1.7.8
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='JohnTheRipper password cracker with Jumbo patch and MPI support'
url='http://openwall.info'
license=('GPL')
provides=('john')
conflicts=('john' 'john-latest')
depends=('mpich2')
backup=('etc/john/john.conf')

source=("http://www.openwall.com/john/g/john-${pkgver}.tar.gz"
        "john-1.7.8-jumbo-2-plugin.diff"
        "http://openwall.info/wiki/_media/john/john-1.7.8/john-1.7.8-jumbo-2-plugin-mscash2-2.diff.gz"
        "http://openwall.info/wiki/_media/john/john-1.7.8/john-1.7.8-jumbo-2-after-cash2_many_fixes-1.diff.gz"
        "params.h.patch")

build() {
  export PATH=/opt/mpich2/bin:${PATH}

  cd ${srcdir}/john-${pkgver}/

  patch -p1 < ${srcdir}/john-${pkgver}-jumbo-2-plugin.diff
  patch -p1 < ${srcdir}/john-${pkgver}-jumbo-2-plugin-mscash2-2.diff
  patch -p1 < ${srcdir}/john-${pkgver}-jumbo-2-after-cash2_many_fixes-1.diff

  cd src
  patch -p0 < ${srcdir}/params.h.patch

  sed -i 's|#CC = mpicc|CC = mpicc|g' Makefile                                                                                                                    
  sed -i 's|#MPIOBJ = john-mpi.o|MPIOBJ = john-mpi.o|g' Makefile

  if [ "$CARCH" == "x86_64" ]; then
   sed -i 's|CFLAGS = -c -Wall -O2|CFLAGS = -c -Wall -O2 -march=x86-64 -DJOHN_SYSTEMWIDE=1|' Makefile
   sed -i 's|^LDFLAGS =\(.*\)|LDFLAGS =\1 -lm|' Makefile
   sed -i -e 's|-m486||g' Makefile
    if [ $(which icc) -eq 0 ]; then
      make linux-x86-64-icc
    else
      make linux-x86-64
    fi
  else
    sed -i 's|CFLAGS = -c -Wall -O2|CFLAGS = -c -Wall -O2 -march=i686 -DJOHN_SYSTEMWIDE=1|' Makefile
    make linux-x86-sse2
  fi
}

package() {
	# config file
	sed -i 's|$JOHN|/usr/share/john|g' ${srcdir}/john-$pkgver/run/john.conf
	install -Dm644 ${srcdir}/john-$pkgver/run/john.conf ${pkgdir}/etc/john/john.conf
	
	# docs
	install -d ${pkgdir}/usr/share/doc/john
	install -m644 ${srcdir}/john-$pkgver/doc/* ${pkgdir}/usr/share/doc/john/
	install -d ${pkgdir}/usr/share/john/
	install -m644 ${srcdir}/john-$pkgver/run/*.chr ${pkgdir}/usr/share/john/
	install -m644 ${srcdir}/john-$pkgver/run/password.lst ${pkgdir}/usr/share/john/
	install -Dm644 ${srcdir}/john-$pkgver/doc/LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE

	# install password list and charset files
	install -m644 ${srcdir}/john-${pkgver}/run/{{all,alnum,alpha,digits,lanman}.chr,password.lst} \
          ${pkgdir}/usr/share/john/

	# install binaries
  cd ${srcdir}/john-${pkgver}/run/
  for i in $(find . -type f -perm 755); do
    install -Dm755 ${i} ${pkgdir}/usr/bin/${i}
  done
	cd ${pkgdir}/usr/bin
	ln -s john unafs
	ln -s john unique
	ln -s john unshadow
	ln -s john undrop
}

sha256sums=('12f4307602b9a8f0d3f82daf28e0f59de21aa82c9dcccd16819b288ec83ab559'
            'd6e9c171580b891f2f225ea66f1ee6c82fd31bce3b9be94887d93bac82cbfd03'
            'f67a6c162645a134b6d47c0ec00b3d011d9e254f9142e1cffc620b91e11375d7'
            '38b51320c65a784029e4fe7c142e6c1b85c4e20d87ef23929a2705143b98c5f9'
            '432466152dda1bfaae66095ac6d1db48e91c2557e412c799b8c01921b749414a')
