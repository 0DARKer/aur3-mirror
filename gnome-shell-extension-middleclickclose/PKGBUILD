# Maintainer: XZS <d.f.fischer at web dot de>

pkgname='gnome-shell-extension-middleclickclose'
_extname='middleclickclose@paolo.tranquilli.gmail.com'
pkgdesc="Close windows with a button click (the middle one by default) when in overview mode"
pkgver=6
pkgrel=2
arch=(any)
url='http://sourceforge.net/projects/buttons-to-panel/'
license=(unknown)
depends=('gnome-shell>=3.6')
source=("${pkgname}.zip::http://sourceforge.net/projects/mt-miscellaneous/files/gnome-extensions/3.16/${_extname}.v6.shell-extension.zip/download")
md5sums=('e7ea5800d8408ea33496cdac01a058f6')

# template start; name=modularize-package; version=1;
package() {
  for function in $(declare -F | grep -Po 'package_[[:digit:]]+[[:alpha:]_]*$')
  do
    $function
  done
}

# vim: filetype=sh
# template end;
# template start; name=install-code; version=1;
package_01_locate() {
  msg2 'Locating extension...'
  cd "$(dirname $(find -name 'metadata.json'))"
  extname=$(grep -Po '(?<="uuid": ")[^"]*' metadata.json)
  destdir=""$pkgdir/usr/share/gnome-shell/extensions/$extname""
}

package_02_install() {
  msg2 'Installing extension code...'
  find -maxdepth 1 \( -iname '*.js*' -or -iname '*.css' -or -iname '*.ui' \) -exec install -Dm644 -t "$destdir" '{}' +
}

# vim: filetype=sh
# template end;
# template start; name=install-schemas; version=1;
if [ -z "$install" ]
then
  install=gschemas.install
fi

package_10_schemas() {
  msg2 'Installing schemas...'
  find -name '*.xml' -exec install -Dm644 -t "$pkgdir/usr/share/glib-2.0/schemas" '{}' +
}

# vim: filetype=sh
# template end;
