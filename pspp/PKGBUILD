# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>
pkgname=pspp
pkgver=0.6.2
pkgrel=5
pkgdesc="Statistical analysis program"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/pspp/"
license=('GPL3')
depends=('gsl' 'plotutils' 'libglade')
optdepends=('postgresql-libs: PG database support'
            'zlib: GNUmeric support'
            'libxml2: GNUMERIC support')
changelog=ChangeLog
source=(ftp://ftp.gnu.org/gnu/pspp/pspp-0.6.2.tar.gz
        gtk-2.diff)
md5sums=('03080eb9e3ffeb03f18bc925fcd83fcb'
         '43880e65b579a9f07bc2e455df9e4195')
install=pspp.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    patch -Np1 -i ../../gtk-2.diff
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --without-libreadline-prefix
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=$pkgdir install
    rm -f ${pkgdir}/usr/share/info/dir
    install -d ${pkgdir}/usr/share/pspp/{contrib,examples}
    install -m 644 pspp-mode.el ${pkgdir}/usr/share/pspp/contrib/pspp-mode.el
    install -m 644 -t ${pkgdir}/usr/share/pspp/examples examples/*.stat
}

# End of file
