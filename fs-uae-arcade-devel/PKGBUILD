# Maintainer: grimi <grimi at poczta dot fm>

_pkgname=fs-uae-arcade
pkgname=fs-uae-arcade-devel
pkgver=2.5.0dev
pkgrel=1
pkgdesc="Full-screen game browser for FS-UAE (development version)."
arch=("any")
url="http://fs-uae.net/download-devel"
license=("GPL2")
depends=("fs-uae-devel" "python2-pygame" "python2-numpy" "python2-opengl" "python2-pillow" "python2-setuptools" "python2-lhafile" "hicolor-icon-theme" "xdg-utils")
install="${pkgname}.install"
source=("http://fs-uae.net/devel/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
provides=("fs-uae-game-center")
conflicts=("fs-uae-game-center" "fs-uae-game-center-devel")
replaces=("fs-uae-game-center-devel")
md5sums=('6dc61376ed794cca42aa97c391737e2d')



build() {
   cd ${_pkgname}-${pkgver}

   python2 setup.py build
   make
}



package() {
   cd ${_pkgname}-${pkgver}

   python2 setup.py install --skip-build --prefix=/usr \
      --install-scripts=/usr/share/${_pkgname} \
      --install-lib=/usr/share/${_pkgname} --root="${pkgdir}"
   make install DESTDIR="${pkgdir}"

   install -d "${pkgdir}"/usr/bin
   ln -s /usr/share/${_pkgname}/${_pkgname} "${pkgdir}"/usr/bin/${_pkgname}
}


# vim:set ts=3 sw=3 et:

