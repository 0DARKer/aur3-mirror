# Mantainer: Franco Tortoriello

#http://vogons.zetafleet.com/viewforum.php?f=41 patches
#http://vogons.zetafleet.com/viewforum.php?f=32 dev
#http://sourceforge.net/tracker/?words=tracker_browse&sort=open_date&sortdir=asc&offset=0&group_id=52551&atid=467234&assignee=&status=1&category=&artgroup=&keyword=&submitter=&artifact_id=

pkgname=dosbox-svn-patched
pkgver=3738
pkgrel=1
pkgdesc="An emulator with builtin DOS for running DOS Games, with several patches to increase functionality."
arch=('i686' 'x86_64')
url="http://dosbox.sourceforge.net/"
license=('GPL')
depends=('sdl_net' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'gcc-libs' 'openglide-cvs' 'freetype2')
makedepends=('mesa' 'subversion')
provides=('dosbox' 'dosbox-svn')
conflicts=('dosbox' 'dosbox-svn')
source=('dosbox.desktop' 'dosbox-16.png' 'dosbox-48.png' 'dosbox-128.png'
	'glide.patch' 'mt32.patch' 'ne2000.patch' 'beep.patch' 'swapstereo.patch' 'multiimage.patch')
install=dosbox.install

_svntrunk="https://dosbox.svn.sourceforge.net/svnroot/dosbox/dosbox/trunk"
_svnmod="dosbox"

build() {
  cd ${srcdir}

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r ${pkgver})
  else
    svn co $_svntrunk --config-dir ./ -r ${pkgver} $_svnmod
  fi

  msg "SVN checkout done or server timeout"

  rm -rf ${srcdir}/$_svnmod-build
  cp -r ${srcdir}/$_svnmod ${srcdir}/$_svnmod-build
  cd ${srcdir}/$_svnmod-build

  msg "Patching..."
  patch -p1 -i ../glide.patch
  patch -p1 -i ../mt32.patch
  patch -p1 -i ../ne2000.patch
  #patch -p1 -i ../parprinter.patch
  patch -p1 -i ../beep.patch
  #patch -p1 -i ../imgmake.patch
  #patch -p1 -i ../savestates.patch
  #patch -p1 -i ../ctrlbreak.patch
  patch -p1 -i ../swapstereo.patch
  patch -p1 -i ../multiimage.patch

  msg "Starting build..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/dosbox CPPFLAGS="$CPPFLAGS -I/usr/include/openglide/"
  make
}

package() {
  cd ${srcdir}/$_svnmod-build
  make DESTDIR=${pkgdir} install

 # Install desktop files and icon
  for i in 16 48 128; do
    install -Dm644 ${srcdir}/dosbox-$i.png ${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/dosbox.png
  done
  install -Dm644 ${srcdir}/dosbox.desktop ${pkgdir}/usr/share/applications/dosbox.desktop
}

md5sums=('f09753c6af7153327296d6618773d245'
         '4b26bcedfc56e4ef390ee4c66a774827'
         'b49ff452861ba1118f06cf5421878636'
         'c48983033ba1ad1f742577b5df5d8ae6'
         '8bbfe5812d95dd28aebf8108010882f3'
         '6445a9075d0e92829b7b4e59798ac099'
         '856100939c330010f6d49ca7ae42d9dd'
         'c1589044a2b9102e2a5ae72d372c1492'
         'ba65250c6164dc140c9df968679f03f7'
         '748577d1991e68428919bf2148c00069')
