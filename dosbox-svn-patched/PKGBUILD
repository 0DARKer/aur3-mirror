# Mantainer: Franco Tortoriello

pkgname=dosbox-svn-patched
pkgver=3873
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with patches with more features"
arch=(i686 x86_64)
url="http://dosbox.sourceforge.net"
license=(GPL)
depends=(sdl_net sdl_sound libpng mesa)
makedepends=(subversion glu openglide-cvs)
provides=(dosbox dosbox-svn)
conflicts=(dosbox dosbox-svn)
source=(dosbox::svn+http://svn.code.sf.net/p/dosbox/code-0/dosbox/trunk
	dosbox.png::http://www.dosbox.com/dosbox_wiki.png
	dosbox.desktop
	{01-beep,02-ctrlbreak,03-glide,04-swapstereo,05-gus-stereo}.patch
	{06-hd-order,07-time-stamp}.patch)

pkgver() {
  cd "$SRCDEST/dosbox"
  svnversion
}

prepare() {
  cd "$srcdir/dosbox"

  for _f in "$srcdir"/*.patch; do
    msg2 "Applying $_f"
    patch -p1 -i "$_f"
  done
}

build() {
  cd "$srcdir/dosbox"

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/dosbox
  make
}

package() {
  cd "$srcdir/dosbox"
  make DESTDIR="$pkgdir" install

  install -Dm644 README \
	"$pkgdir/usr/share/doc/dosbox/README"
  install -Dm644 docs/README.video \
	"$pkgdir/usr/share/doc/dosbox/README.video"
  install -Dm644 "$srcdir/dosbox.png" \
	"$pkgdir/usr/share/pixmaps/dosbox.png"
  install -Dm644 "$srcdir/dosbox.desktop" \
	"$pkgdir/usr/share/applications/dosbox.desktop"
}

md5sums=('SKIP'
         '3dcfe45c5ed0433316eaea51e3620b36'
         '77b693e82f9dd018d1ec763a3c60ec4f'
         '087422054d842afe8630e242bd6dfd25'
         'a2acfc1790f6a820b39e6480992b81f3'
         '5f496514b5037909569c212c2c9f0404'
         '99109804c522e206cd4ff50e83c4027a'
         '5b0ba01cd7ffe0e4529cdddbae306010'
         '34fe1e5973acf57acc3b738cece61aa2'
         'dbf10831a1ef62059ca9d0924f04e656')
