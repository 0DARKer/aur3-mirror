# Mantainer: Franco Tortoriello

pkgname=dosbox-svn-patched
pkgver=3827
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with patches with more features"
arch=(i686 x86_64)
url="http://dosbox.sourceforge.net"
license=(GPL)
depends=(sdl_net sdl_sound libpng alsa-lib libpcap mesa)
makedepends=(subversion mesa glu openglide-cvs munt-git)
provides=(dosbox dosbox-svn)
conflicts=(dosbox dosbox-svn)
source=(dosbox::svn+http://svn.code.sf.net/p/dosbox/code-0/dosbox/trunk
	dosbox.png::http://www.dosbox.com/dosbox_wiki.png
	dosbox.desktop
	beep.patch		# http://home.arcor.de/h-a-l-9000
	ctrlbreak.patch		# http://sourceforge.net/p/dosbox/patches/228 and 229
	ne2000.patch		# http://sourceforge.net/p/dosbox/patches/238
	glide.patch		# http://vogons.zetafleet.com/viewtopic.php?t=16462
	swapstereo.patch	# http://vogons.zetafleet.com/viewtopic.php?t=25827
	mt32.patch		# from munt; single thread version
	)
md5sums=(SKIP
	 3dcfe45c5ed0433316eaea51e3620b36
	 cd22b603f07f3ca821861d7f15b87087
	 a18266c0be2ec945eaa0b47ff1cb22b3
	 a2acfc1790f6a820b39e6480992b81f3
	 550efe35485dd159c0868eeb8ecf7e27
	 eb484941ec30e890d2bb98f5b96f67f2
	 cddfeba43bd032dc72ce92dfd1f3c2fb
	 f1564dfa7e03fbb4f446ab7db364f3c9)

pkgver() {
  cd "$SRCDEST/dosbox"
  svnversion
}

build() {
  cd dosbox

  msg2 "Patching ..."
  patch -p1 -i ../beep.patch
  patch -p1 -i ../ctrlbreak.patch
  patch -p1 -i ../ne2000.patch
  patch -p1 -i ../glide.patch
  patch -p1 -i ../swapstereo.patch
  patch -p1 -i ../mt32.patch

  msg2 "Starting build ..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/dosbox
  make
}

package() {
  cd dosbox
  make DESTDIR="$pkgdir" install

  install -Dm644 README "$pkgdir/usr/share/doc/dosbox/README"
  install -Dm644 docs/README.video "$pkgdir/usr/share/doc/dosbox/README.video"
  install -Dm644 ../dosbox.png "$pkgdir/usr/share/pixmaps/dosbox.png"
  install -Dm644 ../dosbox.desktop "$pkgdir/usr/share/applications/dosbox.desktop"
}

