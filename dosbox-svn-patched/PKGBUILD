# Mantainer: Franco Tortoriello

pkgname=dosbox-svn-patched
pkgver=3798
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with several patches to increase functionality"
arch=('i686' 'x86_64')
url="http://dosbox.sourceforge.net"
license=('GPL')
depends=('sdl_net' 'sdl_sound' 'libpng' 'alsa-lib' 'libpcap' 'openglide-cvs')
makedepends=('mesa' 'subversion')
provides=('dosbox' 'dosbox-svn')
conflicts=('dosbox' 'dosbox-svn')
source=('dosbox.desktop' dosbox.png::"http://www.dosbox.com/dosbox_wiki2.png"
	'beep.patch'		# http://home.arcor.de/h-a-l-9000
	'multiimage.patch'	# http://sourceforge.net/p/dosbox/patches/207
	'ctrlbreak.patch'	# http://sourceforge.net/p/dosbox/patches/228
				# http://sourceforge.net/p/dosbox/patches/229
	'ne2000.patch'		# http://sourceforge.net/p/dosbox/patches/238
	'glide.patch'		# http://vogons.zetafleet.com/viewtopic.php?t=16462
	'swapstereo.patch'	# http://vogons.zetafleet.com/viewtopic.php?t=25827
	)
md5sums=('7bd1353acaa9b3d50826595b054da292'
	 '71e0f4db01b89d227e1941bfe2716ac5'
	 '94b56993fc4d84a6150aac7726649310'
	 '7833f0c92a350c3ac8d3411f4bd9b3a2'
	 '2cc412fa6084db7e36d5be355aa23c86'
	 '08467feb8f6d712f10eb24cb7e549eaf'
	 '9fa2796aeeffcf2a6f2efc2e433cd462'
	 'be48ff2d296667d0cb4ac4938ac83fc5')

_svntrunk=https://dosbox.svn.sourceforge.net/svnroot/dosbox/dosbox/trunk
_svnmod=dosbox

build() {
  cd "$srcdir"

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  msg "Patching..."
  patch -p1 -i ../beep.patch
  patch -p1 -i ../multiimage.patch
  patch -p1 -i ../ctrlbreak.patch
  patch -p1 -i ../ne2000.patch
  patch -p1 -i ../glide.patch
  patch -p1 -i ../swapstereo.patch

  msg "Starting build..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/dosbox
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

  install -Dm644 README "$pkgdir/usr/share/doc/dosbox/README"
  install -Dm644 docs/README.video "$pkgdir/usr/share/doc/dosbox/README.video"
  install -Dm644 ../dosbox.png "$pkgdir/usr/share/pixmaps/dosbox.png"
  install -Dm644 ../dosbox.desktop "$pkgdir/usr/share/applications/dosbox.desktop"
}
