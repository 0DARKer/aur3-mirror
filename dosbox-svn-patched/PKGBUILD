# Mantainer: Franco Tortoriello

pkgname=dosbox-svn-patched
pkgver=3775
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with several patches to increase functionality"
arch=('i686' 'x86_64')
url="http://dosbox.sourceforge.net/"
license=('GPL')
depends=('sdl_net' 'zlib' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'openglide-cvs')
makedepends=('mesa' 'subversion')
provides=(dosbox dosbox-svn)
conflicts=(dosbox dosbox-svn)
source=(dosbox.desktop
	dosbox.png::"http://www.dosbox.com/dosbox_wiki2.png"
	'glide.patch' 'mt32.patch' 'ne2000.patch' 'beep.patch'
	'swapstereo.patch' 'multiimage.patch')
md5sums=('7bd1353acaa9b3d50826595b054da292'
	 '71e0f4db01b89d227e1941bfe2716ac5'
	 '77df1db36cfdb50ba257b8dbd8e18d62'
	 'd2f0eef1cc1c65aab7d612c6622b0547'
	 'd5a5226e87835530a844b31fe869b3f0'
	 '4729dcdeebdb22639facfbacb7a3d454'
	 'be48ff2d296667d0cb4ac4938ac83fc5'
	 '7833f0c92a350c3ac8d3411f4bd9b3a2')

_svntrunk=https://dosbox.svn.sourceforge.net/svnroot/dosbox/dosbox/trunk
_svnmod=dosbox

build() {
  cd "$srcdir"

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  msg "Patching..."
  patch -p1 -i ../glide.patch
  patch -p1 -i ../mt32.patch
  patch -p1 -i ../ne2000.patch
  patch -p1 -i ../beep.patch
  patch -p1 -i ../swapstereo.patch
  patch -p1 -i ../multiimage.patch

  msg "Starting build..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/$_svnmod \
	CXXFLAGS="$CXXFLAGS -I/usr/include/openglide"
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

  install -Dm644 README "$pkgdir/usr/share/doc/$_svnmod/README"
  install -Dm644 docs/README.video "$pkgdir/usr/share/doc/$_svnmod/README.video"
  install -Dm644 ../dosbox.png "$pkgdir/usr/share/pixmaps/$_pkgname.png"
  install -Dm644 ../dosbox.desktop "$pkgdir/usr/share/applications/$_svnmod.desktop"
}
