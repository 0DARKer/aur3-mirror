# Mantainer: Franco Tortoriello

_pkgname=dosbox
pkgname=dosbox-svn-patched
pkgver=3752
pkgrel=2
pkgdesc="x86 emulator with builtin DOS, with several patches to increase functionality"
arch=('i686' 'x86_64')
url="http://$_pkgname.sourceforge.net/"
license=('GPL')
depends=('sdl_net' 'zlib' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'openglide-cvs')
makedepends=('mesa' 'subversion')
provides=($_pkgname $_pkgname-svn)
conflicts=($_pkgname $_pkgname-svn)
install=$_pkgname.install
source=($_pkgname.desktop $_pkgname.png
	'glide.patch' 'mt32.patch' 'ne2000.patch' 'beep.patch'
	'swapstereo.patch' 'multiimage.patch')
md5sums=('f09753c6af7153327296d6618773d245'
         '2aac25fc06979e375953fcc36824dc5e'
         '8bbfe5812d95dd28aebf8108010882f3'
         'd54b1dd1bb65d2205e2e92d3f16e4761'
         '856100939c330010f6d49ca7ae42d9dd'
         'c1589044a2b9102e2a5ae72d372c1492'
         'ba65250c6164dc140c9df968679f03f7'
         '748577d1991e68428919bf2148c00069')

_svntrunk=https://$_pkgname.svn.sourceforge.net/svnroot/$_pkgname/$_pkgname/trunk
_svnmod=$_pkgname

build() {
  cd "$srcdir"

  if [ -d "$_svnmod/.svn" ]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  msg "Patching..."
  patch -p1 -i ../glide.patch
  patch -p1 -i ../mt32.patch
  patch -p1 -i ../ne2000.patch
  patch -p1 -i ../beep.patch
  patch -p1 -i ../swapstereo.patch
  patch -p1 -i ../multiimage.patch

  msg "Starting build..."
  ./autogen.sh
  ./configure --prefix="/usr" --sysconfdir="/etc/$_pkgname" \
	CXXFLAGS="$CXXFLAGS -I/usr/include/openglide"
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

  install -Dm644 README "$pkgdir/usr/share/doc/$_pkgname/README"
  install -Dm644 docs/README.video "$pkgdir/usr/share/doc/$_pkgname/README.video"
  install -Dm644 ../$_pkgname.png "$pkgdir/usr/share/pixmaps/$_pkgname.png"
  install -Dm644 ../$_pkgname.desktop "$pkgdir/usr/share/applications/$_pkgname.desktop"
}

