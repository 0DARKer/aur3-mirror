# Mantainer: Franco Tortoriello

pkgname=dosbox-svn-patched
pkgver=3833
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with patches with more features"
arch=(i686 x86_64)
url="http://dosbox.sourceforge.net"
license=(GPL)
depends=(sdl_net sdl_sound libpng alsa-lib libpcap mesa)
makedepends=(subversion mesa glu openglide-cvs munt-git)
provides=(dosbox dosbox-svn)
conflicts=(dosbox dosbox-svn)
source=(dosbox::svn+http://svn.code.sf.net/p/dosbox/code-0/dosbox/trunk
	dosbox.png::http://www.dosbox.com/dosbox_wiki.png
	dosbox.desktop
	01-beep.patch		# http://home.arcor.de/h-a-l-9000
	02-ctrlbreak.patch	# http://sourceforge.net/p/dosbox/patches/228 and 229
	03-ne2000.patch		# http://sourceforge.net/p/dosbox/patches/238
	04-glide.patch		# http://vogons.zetafleet.com/viewtopic.php?t=16462
	05-swapstereo.patch	# http://vogons.zetafleet.com/viewtopic.php?t=25827
	06-mt32.patch		# from munt; single thread version
	07-configure-fpu.patch
	)
md5sums=(SKIP
	 3dcfe45c5ed0433316eaea51e3620b36
	 cd22b603f07f3ca821861d7f15b87087
	 a18266c0be2ec945eaa0b47ff1cb22b3
	 a2acfc1790f6a820b39e6480992b81f3
	 550efe35485dd159c0868eeb8ecf7e27
	 305c287a5c9a331312e903591bcf32cb
	 cddfeba43bd032dc72ce92dfd1f3c2fb
	 20cdf5f69708af08bceabd7d688b3e36
	 ed3923d3e32937a1dde8b6e8e7ace7b2)

pkgver() {
  cd "$SRCDEST/dosbox"
  svnversion
}

prepare() {
  cd "$srcdir/dosbox"

  for _f in "$srcdir"/*.patch; do
    msg2 "Applying $_f"
    patch -p1 -i "$_f"
  done

  # 32->64-bit registers
  if [ $CARCH = x86_64 ]; then
    sed \
	-e 's|%eax|%rax|' \
	-e 's|andl|andq|' \
	-e 's|decl|decq|' \
	-e 's|incl|incq|' \
	-e 's|movl|movq|' \
	-e 's|shll|shlq|' \
	-i src/fpu/fpu_instructions_x86.h
  fi
}

build() {
  cd "$srcdir/dosbox"

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/dosbox
  make
}

package() {
  cd "$srcdir/dosbox"
  make DESTDIR="$pkgdir" install

  install -Dm644 README "$pkgdir/usr/share/doc/dosbox/README"
  install -Dm644 docs/README.video "$pkgdir/usr/share/doc/dosbox/README.video"
  install -Dm644 "$srcdir/dosbox.png" "$pkgdir/usr/share/pixmaps/dosbox.png"
  install -Dm644 "$srcdir/dosbox.desktop" "$pkgdir/usr/share/applications/dosbox.desktop"
}

