# Mantainer: Franco Tortoriello

pkgname=dosbox-svn-patched
pkgver=3809
pkgrel=1
pkgdesc="x86 emulator with builtin DOS, with patches with more features"
arch=(i686 x86_64)
url="http://dosbox.sourceforge.net"
license=(GPL)
depends=(sdl_net sdl_sound libpng alsa-lib libpcap openglide-cvs)
makedepends=(subversion mesa glu)
provides=('dosbox' 'dosbox-svn')
conflicts=('dosbox' 'dosbox-svn')
source=(dosbox.png::"http://www.dosbox.com/dosbox_wiki.png"
	dosbox.desktop
	beep.patch		# http://home.arcor.de/h-a-l-9000
	ctrlbreak.patch		# http://sourceforge.net/p/dosbox/patches/228 and 229
	ne2000.patch		# http://sourceforge.net/p/dosbox/patches/238
	glide.patch		# http://vogons.zetafleet.com/viewtopic.php?t=16462
	swapstereo.patch	# http://vogons.zetafleet.com/viewtopic.php?t=25827
	)
md5sums=('3dcfe45c5ed0433316eaea51e3620b36'
	 'cd22b603f07f3ca821861d7f15b87087'
	 '2635ca7a0daa14b31d13d0a85d3ca73a'
	 'a2acfc1790f6a820b39e6480992b81f3'
	 '08467feb8f6d712f10eb24cb7e549eaf'
	 '9fa2796aeeffcf2a6f2efc2e433cd462'
	 'be48ff2d296667d0cb4ac4938ac83fc5')

_svntrunk=http://svn.code.sf.net/p/dosbox/code-0/dosbox/trunk
_svnmod=dosbox

build() {
  cd "$srcdir"

  if [[ -d $_svnmod/.svn ]]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  msg "Patching..."
  patch -p1 -i ../beep.patch
  patch -p1 -i ../ctrlbreak.patch
  patch -p1 -i ../ne2000.patch
  patch -p1 -i ../glide.patch
  patch -p1 -i ../swapstereo.patch

  # Fix autoconf issues
  mv configure.in configure.ac
  sed -e 's|AM_CONFIG|AC_CONFIG|' -i configure.ac

  msg "Starting build..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/dosbox
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

  install -Dm644 README "$pkgdir/usr/share/doc/dosbox/README"
  install -Dm644 docs/README.video "$pkgdir/usr/share/doc/dosbox/README.video"
  install -Dm644 ../dosbox.png "$pkgdir/usr/share/pixmaps/dosbox.png"
  install -Dm644 ../dosbox.desktop "$pkgdir/usr/share/applications/dosbox.desktop"
}
