# Maintainer: Thibault Suzanne <thi [DOT] suzanne [AT] gmail [DOT] com>
# Category: science

pkgname='mlgmpidl-svn'
pkgver=r308
pkgrel=2
pkgdesc='Interface to the GMP and MPFR libraries for OCaml'
arch=('any')
url='http://www.inrialpes.fr/pop-art/people/bjeannet/mlxxxidl-forge/mlgmpidl/'
license=('LGPL')
depends=('gmp>=5' 'mpfr>=3' 'ocaml' 'camlidl' 'ocaml-findlib')
makedepends=('svn' 'sed')
source=("$pkgname::svn://scm.gforge.inria.fr/svn/mlxxxidl/mlgmpidl/trunk")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
    cd "$srcdir/$pkgname"

    sed -e 's|^OCAMLFIND *=.*$|OCAMLFIND=|' \
      -e 's|^CAMLIDL_PREFIX *=.*$|CAMLIDL_PREFIX = /usr|' \
      Makefile.config.model > Makefile.config

    sed \
      -e 's|^LDFLAGS *= *\\$|LDFLAGS = -L$(CAMLIDL_PREFIX)/lib/ocaml/camlidl \\|' \
      -i Makefile
}

build() {
    cd "$srcdir/$pkgname"
    make
}

package() {
    cd "$srcdir/$pkgname"
    make MLGMPIDL_PREFIX="$pkgdir"/usr install
}
