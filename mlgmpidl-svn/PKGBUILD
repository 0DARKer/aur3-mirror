# Maintainer: Thibault Suzanne <thi [DOT] suzanne [AT] gmail [DOT] com>
# Category: science

pkgname='mlgmpidl-svn'
pkgver=r308
pkgrel=2
pkgdesc='Interface to the GMP and MPFR libraries for OCaml'
arch=('any')
url='http://www.inrialpes.fr/pop-art/people/bjeannet/mlxxxidl-forge/mlgmpidl/'
license=('LGPL')
depends=('gmp>=5' 'mpfr>=3' 'ocaml' 'camlidl' 'ocaml-findlib')
makedepends=('svn' 'sed')
source=("$pkgname::svn://scm.gforge.inria.fr/svn/mlxxxidl/mlgmpidl/trunk")
md5sums=('SKIP')
install=mlgmpidl.install
options=('!strip')

_DESTDIR=$(ocamlfind printconf destdir)
_LDCONF=$(ocamlfind printconf ldconf)

pkgver() {
  cd "$srcdir/$pkgname"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "$srcdir/$pkgname"

  sed  \
    -e 's|^CAMLIDL_PREFIX *=.*$|CAMLIDL_PREFIX = /usr|' \
    -e "s|^OCAMLFIND *=.*$|OCAMLFIND=ocamlfind|" \
    Makefile.config.model > Makefile.config


  # Ugly hack to modify ocamlfind destdir
  sed \
    -e 's|^LDFLAGS *= *\\$|LDFLAGS = -L$(CAMLIDL_PREFIX)/lib/ocaml/camlidl \\|' \
    -e "s|^PKG-NAME *=.*$|PKG-NAME = gmp -destdir $pkgdir$_DESTDIR -ldconf $pkgdir$_LDCONF|" \
    -e 's|^SITE-LIB-PKG *=.*|SITE-LIB-PKG = $(SITE-LIB)/gmp|' \
    -i Makefile

}

build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  mkdir -p $pkgdir$_DESTDIR

  cd "$srcdir/$pkgname"
  make install
}
