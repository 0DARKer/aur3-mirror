_pkgname=netsurf
pkgname=netsurf-fb
pkgver=3.1
pkgrel=1
pkgdesc='Lightweight and fast web browser - build with framebuffer backend'
arch=('x86_64' 'i686' 'armv7h')
url='http://www.netsurf-browser.org/'
license=('GPL')
provides=('netsurf')
conflicts=('netsurf')
replaces=('netsurf')
depends=('libnsfb' 'libmng' 'curl' 'librsvg' 'desktop-file-utils' 'libnsbmp' 'libnsgif' 'libcss' 'libdom' 'js185') # 'libharu' 'gstreamer0.10' 'libwebp'
makedepends=('re2c' 'lcms' 'gendesk' 'netsurf-buildsystem' 'libglade' 'nsgenbind' 'perl-html-parser')
install="netsurf.install"
source=("netsurf.png::http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png"
        "http://download.${_pkgname}-browser.org/${_pkgname}/releases/source/${_pkgname}-$pkgver-src.tar.gz")
md5sums=('4f28912e5df4b826b819e175265bcc9c'
         'b83932b311716054a5189f121cdd5fd4')

_exec=('netsurf %U')
_genericname=('Web Browser')
_comment=('Lightweight web browser')
_mimetype=('text/html;application/xhtml+xml;x-scheme-handler/http;x-scheme-handler/https;x-scheme-handler/geo')

prepare() {
  cd "$srcdir"

  gendesk
}

build() {
  cd "${_pkgname}-$pkgver"

  make \
    PREFIX=/usr \
    TARGET=framebuffer \
    NETSURF_USE_WEBP=NO \
    NETSURF_USE_VIDEO=NO \
    NETSURF_USE_MOZJS=YES \
    NETSURF_USE_HARU_PDF=NO 
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"

  make install TARGET=framebuffer PREFIX=/usr DESTDIR="$pkgdir"
  install -Dm644 "../${_pkgname}.png" \
    "$pkgdir/usr/share/pixmaps/${_pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" \
    "$pkgdir/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/${_pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
