# Maintainer: Tomasz Maciej Nowak <com[dot]gmail[at]tmn505>
pkgname='xupnpd'
pkgdesc="eXtensible UPnP agent"
pkgver=1.033
pkgrel=1
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
url="http://xupnpd.org"
license=('GPL2')
makedepends=('subversion' 'lua' 'lua-luajson')
depends=('udpxy')
install='xupnpd.install'
source=("svn+https://tsdemuxer.googlecode.com/svn/trunk/xupnpd"
        'res_and_conf_dirs.patch'
        'xupnpd.service')
sha256sums=('SKIP'
            'e1508e6ca96bab0ba4f33b6c78991d8dc3fabfaee816ccc7a53dabafdca7d406'
            '0d84821ddf6841e1e523dd4427fd34c3aba3c404e651e0bd09f3f103fb25d9cc')

prepare() {
    cd ${srcdir}
    patch -p2 -i ./xupnpd/src/contrib/gentoo/portage/net-misc/xupnpd/files/makefile.patch
    patch -p0 -i ./res_and_conf_dirs.patch
}

build() {
    cd ${srcdir}/xupnpd/src
    make
}

package() {
    cd ${srcdir}/xupnpd/src

    install -Dm644 ../../xupnpd.service ${pkgdir}/usr/lib/systemd/system/xupnpd.service

    install -dm755 ${pkgdir}/var/lib/xupnpd/config

    install -dm755 ${pkgdir}/var/lib/xupnpd/localmedia

    install -dm755 ${pkgdir}/var/lib/xupnpd/playlists
    cp -r ./playlists/* ${pkgdir}/var/lib/xupnpd/playlists

    install -dm755 ${pkgdir}/usr/share/xupnpd/plugins
    cp -r ./plugins/* ${pkgdir}/usr/share/xupnpd/plugins

    install -dm755 ${pkgdir}/usr/share/xupnpd/profiles
    cp -r ./profiles/* ${pkgdir}/usr/share/xupnpd/profiles

    install -dm755 ${pkgdir}/usr/share/xupnpd/ui
    cp -r ./ui/* ${pkgdir}/usr/share/xupnpd/ui

    install -dm755 ${pkgdir}/usr/share/xupnpd/www
    cp -r ./www/* ${pkgdir}/usr/share/xupnpd/www

    install -Dm644 ./*.lua -t ${pkgdir}/var/lib/xupnpd

    install -Dm755 ./xupnpd ${pkgdir}/usr/bin/xupnpd
}
