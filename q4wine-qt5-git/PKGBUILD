pkgname=q4wine-qt5-git
pkgver=v1.1.r2.20.g455dcb6
pkgrel=1
pkgdesc="A Qt5 GUI for Wine. (GIT Version)"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/q4wine/"
license=('GPL3')
depends=('qt5-base' 'wine' 'sqlite3' 'which' 'icoutils' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'cmake' 'git')
optdepends=('winetricks: Tweak wine'
            'fuseiso: Mount ISO files')
conflicts=('q4wine' 'q4wine-qt5')
replaces=('q4wine' 'q4wine-qt5')
source=('git://github.com/brezerk/q4wine.git')
sha1sums=('SKIP')
install=q4wine-qt5-git.install

pkgver() {
  cd q4wine
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../q4wine \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBS_ENTRY_PATH="/usr/lib" \
    -DQT5=ON
  make
}

package() {
  make -C build DESTDIR=${pkgdir} install
}
