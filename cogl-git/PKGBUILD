# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=cogl
pkgname=$_pkgname-git
pkgver=1.17.2.8.gee5dc42
pkgrel=1
pkgdesc="An object oriented GL/GLES Abstraction/Utility Layer"
arch=('i686' 'x86_64')
url="http://www.clutter-project.org/"
license=('GPL2')
depends=('libdrm' 'libxext' 'libxdamage' 'libxcomposite' 'gdk-pixbuf2' 'pango' "wayland")
makedepends=('mesa' 'gobject-introspection' 'gtk-doc')
options=(!libtool !emptydirs)
conflicts=('cogl')
replaces=('cogl')
provides=('cogl=1.17.2')
source=('git://git.gnome.org/cogl#branch=cogl-1.18')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_pkgname"
  CC=/usr/bin/gcc ./autogen.sh --prefix=/usr --enable-cairo=yes \
               --enable-gdk-pixbuf=yes \
               --enable-cogl-pango=yes \
               --enable-cogl-gst=yes \
               --enable-glx=yes \
               --enable-xlib-egl-platform=yes \
               --enable-wayland-egl-platform=yes \
               --enable-wayland-egl-server=yes \
               --enable-kms-egl-platform \
               --enable-introspection=yes \
               --enable-gtk-doc
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make -j1 DESTDIR="$pkgdir" install
}
