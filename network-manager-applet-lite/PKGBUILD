# Maintainer: Westminboy <westminboy@gmail.com>

pkgname=network-manager-applet-lite
_pkgname=network-manager-applet
pkgver=0.9.8.0
pkgrel=1
pkgdesc="GNOME frontend to NetWorkmanager only with networkmanager/gtk2 depends (need gtk3 to compile)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=${pkgver}" 'gtk2')
makedepends=('intltool' 'gtk3')
optdepends=('gnome-bluetooth: for PAN/DUN support')
options=('!libtool')
provides=($_pkgname)
conflicts=($_pkgname)
install=network-manager-applet.install
source=("http://ftp.gnome.org/pub/GNOME/sources/network-manager-applet/0.9/network-manager-applet-${pkgver}.tar.xz")
sha256sums=('1afb6e736870ba95132bf0d211c46849e02a820ba3902a059a616be888053590')


build() {
  cd ${_pkgname}-${pkgver}
  sed -i 's/GtkBox/GtkVBox/g' src/connection-editor/ce-page-general.ui
  sed -i 's/GtkBox/GtkVBox/g' src/connection-editor/nm-connection-editor.ui
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --with-gtkver=2 \
      --disable-maintainer-mode \
      --disable-static \
      --disable-iso-codes \
      --disable-migration \
      --disable-more-warnings
  make
}

package() {
  cd ${_pkgname}-${pkgver}
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
}
