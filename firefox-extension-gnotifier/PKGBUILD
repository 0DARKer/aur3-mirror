# Maintainer: XZS <d.f.fischer at web dot de>
pkgname=firefox-extension-gnotifier
pkgver=1.7.9
pkgrel=1
pkgdesc='Add-on for Firefox that enable integration with GNOME native notification system.'
url='https://addons.mozilla.org/en-US/firefox/addon/gnotifier/'
arch=('any')
license=('MPLv2')
depends=('libnotify')
optdepends=(
  'firefox: for firefox plugin'
  'thunderbird: for thunderbird plugin'
)
source=("https://addons.mozilla.org/firefox/downloads/file/259490/gnotifier-${pkgver}-tb+fx-linux.xpi")
md5sums=('ad3c9ee4d0e994680b377716b4637496')

build() {
  cd "$srcdir"
  [[ -d $pkgname-extract ]] || mkdir $pkgname-extract
  unzip -q -d $pkgname-extract gnotifier-$pkgver-tb+fx-linux.xpi
}

package() {
  cd "$srcdir/$pkgname-extract"
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  local fxdir="/usr/lib/firefox/browser/extensions/$emid"
  local fxdstdir="${pkgdir}${fxdir}"
  local tbdstdir="$pkgdir/usr/lib/thunderbird/extensions"
  install -d "$fxdstdir" "$tbdstdir"
  mv * $fxdstdir
  ln -s "$fxdir" "$tbdstdir/$emid"
}
