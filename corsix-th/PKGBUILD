# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=corsix-th
pkgver=Beta7
pkgrel=2
pkgdesc='Reimplementation of the game engine of Theme Hospital'
arch=('i686' 'x86_64')
url='http://corsix-th.googlecode.com/'
license=('MIT')
depends=('sdl_mixer' 'lua' 'timidity++')
makedepends=('cmake')
source=("${url}files/CorsixTH-${pkgver}-Source.tar.gz"
        'CorsixTH')
sha1sums=('2f840d94cda2f2ad8257fdada1a3545e75af6cc9'
          '7fd6ae8db366b7f9c4671708e8ea7beb48f1bea3')

install=install

build() {
	cd "${srcdir}/CorsixTH-${pkgver}-Source"
	cmake \
		-D CMAKE_INSTALL_PREFIX=/usr/share/ \
		-D CMAKE_BUILD_TYPE=Release \
		-Wno-dev .
}

package() {
	cd "${srcdir}/CorsixTH-${pkgver}-Source"
	make DESTDIR="${pkgdir}" install
	install -D -m755 ../CorsixTH "${pkgdir}/usr/bin/CorsixTH"
	install -D -m644 CorsixTH/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	sed -i 's/games/share/g' DebianPackage/usr/share/applications/CorsixTH.desktop
	install -D -m644 DebianPackage/usr/share/applications/CorsixTH.desktop "${pkgdir}/usr/share/applications/CorsixTH.desktop"
}
