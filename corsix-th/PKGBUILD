# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=corsix-th
pkgver=0.21
pkgrel=1
pkgdesc='Reimplementation of the game engine of Theme Hospital'
url='http://corsix-th.googlecode.com/'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
makedepends=('cmake')
depends=('lua51' 'sdl_mixer' 'ffmpeg' 'timidity-freepats')
source=("${url}files/CorsixTH-${pkgver}-Source.tar.gz"
        'bin')
sha1sums=('48aeff966f6f09f9d3340cff8633fe4f3d05a9c2'
          '7fd6ae8db366b7f9c4671708e8ea7beb48f1bea3')

# If you do not have a copy of Theme Hospital,
# you can download the data files of the demo:
#   http://th.corsix.org/Demo.zip

build() {
	cd "${srcdir}/CorsixTH-${pkgver}-Source"
	cmake \
		-D LUA_INCLUDE_DIR=/usr/include/lua5.1 \
		-D LUA_LIBRARY=/usr/lib/liblua5.1.so \
		-D CMAKE_INSTALL_PREFIX=/usr/share/ \
		-D CMAKE_BUILD_TYPE=Release \
		-Wno-dev .
	cd CorsixTH
	make
}

package() {
	cd "${srcdir}/CorsixTH-${pkgver}-Source/CorsixTH"
	make DESTDIR="${pkgdir}" install
	install -Dm755 ../../bin "${pkgdir}/usr/bin/CorsixTH"
	install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 ../DebianPackage/usr/share/applications/CorsixTH.desktop "${pkgdir}/usr/share/applications/CorsixTH.desktop"
	sed -e 's/games/share/g' -i "${pkgdir}/usr/share/applications/CorsixTH.desktop"
}
