# Maintainer: Stefan J. Betz <info@stefan-betz.net>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=dar
pkgver=2.4.15
pkgrel=1
pkgdesc='A full featured command-line backup tool, short for Disk ARchive'
arch=('i686' 'x86_64')
url='http://dar.linux.free.fr/'
license=('GPL')
depends=('bzip2' 'lzo2' 'libgcrypt')
optdepends=('perl: for running some samples')
makedepends=('doxygen')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
options=('!libtool')
backup=('etc/darrc')
md5sums=('29e3b32ea7881e7f52c5812feac1a9d2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  if test "$CARCH" == x86_64; then EXTRA_OPTIONS="--enable-mode=64"; fi
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --disable-upx $EXTRA_OPTIONS
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d $pkgdir/usr/share/doc/$pkgname
  make DESTDIR="$pkgdir" install 
  cd $pkgdir/usr/share/$pkgname
  mv *.html  $pkgdir/usr/share/doc/$pkgname
  rm -r man
  for _i in html mini-howto samples 
  do
    mv ${_i} $pkgdir/usr/share/doc/$pkgname
  done
}
