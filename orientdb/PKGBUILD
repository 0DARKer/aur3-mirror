# Mantainer: Gianni Vialetto <gianni at rootcube dot net>

pkgname=orientdb
pkgver=1.0.1
pkgrel=1
pkgdesc="A NoSQL Graph-Document DBMS"
arch=('any')
license=('Apache')
url="http://orientdb.org"
depends=('jre7-openjdk-headless')
install='orientdb.install'
source=(http://orient.googlecode.com/files/${pkgname}-${pkgver}.zip orientdb.sh orientdb.conf)
md5sums=('95160a3d3b0bfce2df8a783a3e199d76'
         '1984b1d7b2e55e52b0f2840aefa5dcdb'
         'b1674253e357721eecf0bfe3107caa54')
sha256sums=('edf48588ac5c0b373f71aa7a39e961229275279a155f941e243db369c6bddf5a'
            'c48aa49afdba8a2b8842a09cafc939922c31af3996a93fb0cbf052d13e9120f2'
            '5237fd3d1b9d2dd18aa77e1153b712c3c921bd84bbbb2cd4336c62cfb3295ad5')
sha512sums=('98072e134653228e8aff8d0f467e14759f10b827f4876e70a939c028d7ce40038e52c9f6cc76d3d5e1f943906086c1f1eb013018cf499757550149a9c41c7a4d'
            '234a8702163f2ae748e88d06da36479c284d13327507f8e0f2b32d724522bd8a2a6c2c4a7ee84e60fa7653f5043e8161e14915ce4f01da24b7446880e13bdef6'
            '24db911ae22759adaf59c50152b2a29f65e52bf1637f64fabf369681abc5e060469d0efc65ed0cc6a9fd1cfa24ff215820281029ee6fd910e6d97ec637ccec00')

package() {
    cd "${srcdir}/"

    install -m755 -d $pkgdir/opt/orientdb
    install -m700 -d $pkgdir/opt/orientdb/config
    install -m700 -d $pkgdir/opt/orientdb/databases
    install -m755 -d $pkgdir/opt/orientdb/bin
    install -m700 -d $pkgdir/opt/orientdb/www
    install -m755 -d $pkgdir/opt/orientdb/lib
    install -d $pkgdir/etc/rc.d
    install -d $pkgdir/etc/conf.d
    install -d $pkgdir/usr/bin

    # we don't install bechmarks and demo database

    install -m700 config/* $pkgdir/opt/orientdb/config/
    install -m700 bin/shutdown.sh bin/server.sh $pkgdir/opt/orientdb/bin/
    install -m755 bin/console.sh $pkgdir/opt/orientdb/bin/
    install -m755 lib/* $pkgdir/opt/orientdb/lib/
    cp -r www/* $pkgdir/opt/orientdb/www/

    # init scripts
    install -m755 orientdb.sh $pkgdir/etc/rc.d/orientdb
    install -m755 orientdb.conf $pkgdir/etc/conf.d/orientdb
}
