# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
_pkgname=Lynis
pkgver=1.6.3
pkgrel=1
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='http://cisofy.com/lynis/'
license=('GPL3')
arch=('any')
backup=('etc/lynis/default.prf')
depends=('sh')
optdepends=('net-tools: networking tests'
            'bash-completion: completion for bash')
install=lynis.install
source=(https://github.com/CISOfy/${_pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('9227e95c05402bbf18f7ed88c5742126a7dc7883017ad0927347b721e3e224d8ed7d6f7038b9e56506a43f2d4ca18823ea8b15b2afe656bc41557fe9d67d9320')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -e 's|/path/to/lynis|/usr/bin/lynis|g' -i extras/systemd/lynis.service
}

package() {
  cd ${_pkgname}-${pkgver}

  # profile
  install -Dm 644 default.prf "${pkgdir}/etc/${pkgname}/default.prf"

  # binary
  install -Dm 755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # plugins, include, db
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -a db include plugins "${pkgdir}/usr/share/${pkgname}"

  # doc files
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README INSTALL CHANGELOG FAQ

  # manpage
  install -Dm 644 "${pkgname}.8" "${pkgdir}/usr/share/man/man8/${pkgname}.8"

  # completion
  install -Dm 644 extras/bash_completion.d/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"

  # systemd
  install -d "${pkgdir}/usr/lib/systemd/system/"
  install -m 644 extras/systemd/{lynis.service,lynis.timer} "${pkgdir}/usr/lib/systemd/system/"
}

# vim:set ts=2 sw=2 ft=sh et:
