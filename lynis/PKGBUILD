# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
pkgver=1.4.8
pkgrel=1
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='http://cisofy.com/lynis/'
license=('GPL3')
arch=('any')
backup=('etc/lynis/default.prf')
depends=('sh')
optdepends=('net-tools: networking tests')
install=lynis.install
source=(http://cisofy.com/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f219bc254464ef8993fb659f9292a4727fd23f7a0e3d6eb13f379ebdd70464f4')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix non-test includes to let lynis run as user but complain gracefully
  chmod o+rx include
  chmod o+r include/*
  chmod o-r include/tests_*
}

package() {
  cd ${pkgname}-${pkgver}

  # install profile
  install -Dm 644 default.prf "${pkgdir}/etc/${pkgname}/default.prf"

  # install binary
  install -Dm 755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # install plugins, include, db
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -a db include plugins "${pkgdir}/usr/share/${pkgname}"

  # install doc files
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README INSTALL CHANGELOG FAQ

  # install manpage
  install -Dm 644 "${pkgname}.8" "${pkgdir}/usr/share/man/man8/${pkgname}.8"
}

# vim:set ts=2 sw=2 ft=sh et:
