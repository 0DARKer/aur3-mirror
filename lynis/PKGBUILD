# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
pkgver=1.6.2
pkgrel=2
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='http://cisofy.com/lynis/'
license=('GPL3')
arch=('any')
backup=('etc/lynis/default.prf')
depends=('sh')
optdepends=('net-tools: networking tests'
            'bash-completion: completion for bash')
install=lynis.install
source=(http://cisofy.com/files/${pkgname}-${pkgver}.tar.gz)
sha512sums=('4b390027a7e1428411947696ccff20e31de623b10fd2eaef4293eefa58fd669da0900e2e13d1341ee8f374a234cd9e969d716112ff311ae0448c638cccf005c0')

package() {
  cd ${pkgname}

  # profile
  install -Dm 644 default.prf "${pkgdir}/etc/${pkgname}/default.prf"

  # binary
  install -Dm 755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # plugins, include, db
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -a db include plugins "${pkgdir}/usr/share/${pkgname}"

  # doc files
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README INSTALL CHANGELOG FAQ

  # manpage
  install -Dm 644 "${pkgname}.8" "${pkgdir}/usr/share/man/man8/${pkgname}.8"

  # completion
  install -Dm 644 dev/bash_completion.d/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}

# vim:set ts=2 sw=2 ft=sh et:
