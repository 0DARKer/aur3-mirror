# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
pkgver=1.6.0
pkgrel=1
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='http://cisofy.com/lynis/'
license=('GPL3')
arch=('any')
backup=('etc/lynis/default.prf')
depends=('sh')
optdepends=('net-tools: networking tests')
install=lynis.install
source=(http://cisofy.com/files/${pkgname}-${pkgver}.tar.gz)
sha512sums=('2358e77fec567c68bb238ce9fe3e30fed581014f349dd27b22e08d9d9ff330a3910bcb71b0c74b5052eaea4da2a25bad7099071017d59dabac37e03dbb798dfb')

package() {
  cd ${pkgname}

  # install profile
  install -Dm 644 default.prf "${pkgdir}/etc/${pkgname}/default.prf"

  # install binary
  install -Dm 755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # install plugins, include, db
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -a db include plugins "${pkgdir}/usr/share/${pkgname}"

  # install doc files
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README INSTALL CHANGELOG FAQ

  # install manpage
  install -Dm 644 "${pkgname}.8" "${pkgdir}/usr/share/man/man8/${pkgname}.8"
}

# vim:set ts=2 sw=2 ft=sh et:
