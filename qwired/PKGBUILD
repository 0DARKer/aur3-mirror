pkgname=qwired
pkgver=470
pkgrel=1
pkgdesc="Multi-platform client for the Wired protocol. It is written in C++ and using the Qt4 framework, providing a very good multi-platform experience for the user."
arch=(i686 x86_64)
license=("GPL v2")
depends=(lua qt)
makedepends=(qt subversion)
source=("qwired.desktop")
md5sums=('cd59809354a8cd7ab0254f693d4e9fd2')
url="http://qwired-suite.blogspot.com/"
_svntrunk="http://qwired-suite.googlecode.com/svn/trunk/"
_svnmod="qwired"

build() {
  cd "${srcdir}"

  if [ -d "${_svnmod}/.svn" ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver $_svnmod
  fi

  msg 'SVN checkout done or server timeout'

  rm -rf "${_svnmod}-build"
  cp -r "$_svnmod" "${_svnmod}-build"
  cd "${_svnmod}-build/qw_library"
  
  qmake qw_library.pro
  make
  cd "../qw_client"
  qmake qw_client.pro
  make
}

package() {
  cd "${srcdir}/${_svnmod}-build/"
  install -Dm755 "bin/qwired" "$pkgdir/usr/bin/qwired"
  install -Dm644 "$srcdir/qwired.desktop" "$pkgdir/usr/share/applications/qwired.desktop"
  install -Dm644 "qw_client/icons/512x512/qwired-server-logo.png" "$pkgdir/usr/share/icons/qwired.png"
}
