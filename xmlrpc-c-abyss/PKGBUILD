# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

pkgname=xmlrpc-c-abyss
pkgver=1.16.36
pkgrel=1
pkgdesc="This library provides a modular implementation of XML-RPC for C and C++"
arch=('i686' 'x86_64')
url="http://xmlrpc-c.sourceforge.net/"
license=('BSD')
depends=('curl' 'libxml2')
makedepends=('libtool')
provides=('xmlrpc-c')
conflicts=('xmlrpc-c')
source=(http://downloads.sourceforge.net/xmlrpc-c/xmlrpc-c-${pkgver}.tgz curl-types-h.patch)
options=('!libtool' '!makeflags')

build() {
	[ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"

	cd $srcdir

	patch -p1 < curl-types-h.patch

	cd xmlrpc-c-$pkgver

	./configure --prefix=/usr \
		--disable-wininet-client \
		--disable-libwww-client \
		--enable-libxml2-backend
	make CFLAGS_PERSONAL="${CFLAGS}"
}

package() {
	cd $srcdir/xmlrpc-c-$pkgver

	make DESTDIR=$pkgdir install
	install -m 644 -D doc/COPYING $pkgdir/usr/share/licenses/${pkgname}/COPYING
}

md5sums=('4d67b3bd6b866a955df33084ce8e8c62'
         '66496402e47f8f79f9bff45cf8862682')
sha256sums=('75b7c6bd0f10fe147d65864a3781cc0833d8c9835095524c5a1d98b3acf13abd'
            '96895c9cd13511970f136f4612c1004624ef651481309d151b9e8d660c351ce9')
