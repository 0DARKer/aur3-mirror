# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

pkgname=xmlrpc-c-abyss
pkgver=1.16.40
pkgrel=1
pkgdesc="This library provides a modular implementation of XML-RPC for C and C++"
arch=('i686' 'x86_64')
url="http://xmlrpc-c.sourceforge.net/"
license=('BSD')
depends=('curl' 'libxml2')
makedepends=('libtool')
provides=('xmlrpc-c')
conflicts=('xmlrpc-c')
source=(http://downloads.sourceforge.net/xmlrpc-c/xmlrpc-c-${pkgver}.tgz curl-types-h.patch)
options=('!libtool' '!makeflags')
changelog=${pkgname}.ChangeLog.markdown

build() {
	[ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"

	cd $srcdir

	patch -p1 < curl-types-h.patch

	cd xmlrpc-c-$pkgver

	./configure --prefix=/usr \
		--disable-wininet-client \
		--disable-libwww-client \
		--enable-libxml2-backend
	make CFLAGS_PERSONAL="${CFLAGS}"
}

package() {
	cd $srcdir/xmlrpc-c-$pkgver

	make DESTDIR=$pkgdir install
	install -m 644 -D doc/COPYING $pkgdir/usr/share/licenses/${pkgname}/COPYING
}

md5sums=('0b5c026d48c21937261d90bdadda7248'
         '137cd2cc14bcbc60a68184f4798ab5a6')
sha256sums=('3a2a7846a521b7b854de5d3d8cb8d1a4e6929999d3c91359a01e04a63b4629b9'
            '11e30f2f22006f96b9c920fe2fa5d99298af515c6dcc2530af492e437d8eac32')
