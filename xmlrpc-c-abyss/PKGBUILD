# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua

pkgname=xmlrpc-c-abyss
pkgver=1.06.41
pkgrel=2
pkgdesc="This library provides a modular implementation of XML-RPC for C and C++"
arch=('i686' 'x86_64')
url="http://xmlrpc-c.sourceforge.net/"
license=('BSD')
depends=('curl' 'libxml2')
makedepends=('libtool')
provides=('xmlrpc-c')
conflicts=('xmlrpc-c')
source=(http://downloads.sourceforge.net/xmlrpc-c/xmlrpc-c-${pkgver}.tgz)
options=('!libtool' '!makeflags')

build() {
	[ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"

	cd $srcdir/xmlrpc-c-$pkgver

	./configure --prefix=/usr \
		--disable-wininet-client \
		--disable-libwww-client \
		--enable-libxml2-backend
	make CFLAGS_PERSONAL="${CFLAGS}"
}

package() {
	cd $srcdir/xmlrpc-c-$pkgver

	make DESTDIR=$pkgdir install
	install -m 644 -D doc/COPYING $pkgdir/usr/share/licenses/${pkgname}/COPYING
}

md5sums=('02c6b89b8ff911341b6b6d4a6c621ea9')
sha256sums=('a13f5ef40ed75d89543b9cd086185121dead9fc7de850ca4d2edd9a0cec12205')
