# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

pkgname=xmlrpc-c-abyss
pkgver=1.16.39
pkgrel=1
pkgdesc="This library provides a modular implementation of XML-RPC for C and C++"
arch=('i686' 'x86_64')
url="http://xmlrpc-c.sourceforge.net/"
license=('BSD')
depends=('curl' 'libxml2')
makedepends=('libtool')
provides=('xmlrpc-c')
conflicts=('xmlrpc-c')
source=(http://downloads.sourceforge.net/xmlrpc-c/xmlrpc-c-${pkgver}.tgz curl-types-h.patch)
options=('!libtool' '!makeflags')
changelog=${pkgname}.ChangeLog.markdown

build() {
	[ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"

	cd $srcdir

	patch -p1 < curl-types-h.patch

	cd xmlrpc-c-$pkgver

	./configure --prefix=/usr \
		--disable-wininet-client \
		--disable-libwww-client \
		--enable-libxml2-backend
	make CFLAGS_PERSONAL="${CFLAGS}"
}

package() {
	cd $srcdir/xmlrpc-c-$pkgver

	make DESTDIR=$pkgdir install
	install -m 644 -D doc/COPYING $pkgdir/usr/share/licenses/${pkgname}/COPYING
}

md5sums=('e88c9ee202890d726405b3bdfb00cfaf'
         'e3372f32b205d62081dbd55929f677f8')
sha256sums=('54adc6dae0af044fcbbfe97625f256f8afe7d7f0768ef82cef1f98d03c3fbb39'
            '407933ad5c2dceb8c194ddb67ef68a772c737ab7474d818aae4ced0b4afc1117')
