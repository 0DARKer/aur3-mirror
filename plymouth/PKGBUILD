# Contributor: Det <nimetonmaili at gmail dot com>
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: Biginoz <biginoz _at_ free _point_ fr>

pkgname=plymouth
pkgver=0.8.3
pkgrel=7
pkgdesc="A graphical boot splash screen with kernel mode-setting support"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('initscripts' 'libdrm' 'gtk2')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
backup=('etc/plymouth/plymouthd.conf')
source=("http://www.freedesktop.org/software/${pkgname}/releases/${pkgname}-${pkgver}.tar.bz2"
        arch-logo.png
        plymouth-encrypt.initcpio_hook
        plymouth-encrypt.initcpio_install
        plymouth.functions
        plymouth.initcpio_hook
        plymouth.initcpio_install
        plymouth-update-initrd
        plymouthd.conf
        system-release)
sha1sums=(`wget ${source}.sha1sum -qO - | cut -d " " -f1`
          '3bc168dd06ff3e2790f26c131209a7fb4a5ccbd7'
          'c69466cc8b8d8e0da7c7df4ca5a5f7fb94bc6c8f'
          '489fe3a754df3711abd5ad0f905650473c5e64f0'
          'f1b7054bc61e586f7e734f2b45f114da6d615f5a'
          '39a27b5a049dc620b7820b62f351b6cb1d2a48c5'
          '85b6933cb3b7e4d011c1cfad8f2edb29c59d4f8e'
          '71b70b989b401fe583ca12983e753470112098ea'
          '01235a179d8f991a37035c15cdcc0733b581afd4'
          '4c31cbbfbdeb48593641ff600c8d4fb7bda2b01c')

build() {
  cd ${pkgname}-${pkgver}
  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' -i src/libply-splash-graphics/ply-image.c
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    -enable-tracing \
    --with-system-root-install \
    --enable-gdm-transition \
    --with-gdm-autostart-file=no \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D

  msg "Starting make..."
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf `find "${pkgdir}" -name "*.orig"`

  cd "${srcdir}"
  install -Dm755 plymouth-update-initrd ${pkgdir}/usr/lib/plymouth
  install -Dm644 plymouth.initcpio_install "${pkgdir}/lib/initcpio/install/plymouth"
  install -Dm644 plymouth.initcpio_hook "${pkgdir}/lib/initcpio/hooks/plymouth"
  install -Dm644 plymouth-encrypt.initcpio_install "${pkgdir}/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 plymouth-encrypt.initcpio_hook "${pkgdir}/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 plymouth.functions "${pkgdir}/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 arch-logo.png "${pkgdir}/usr/share/plymouth/arch-logo.png"
  install -Dm644 system-release "${pkgdir}/etc/system-release"
  install -Dm644 plymouthd.conf "${pkgdir}/etc/plymouth/plymouthd.conf"
}