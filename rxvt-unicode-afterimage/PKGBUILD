# Maintainer: Erus_Il√πvatar <erus.iluvatar@gmail.com>

_pkgname=rxvt-unicode
pkgname=rxvt-unicode-afterimage
pkgver=9.10
pkgrel=1
pkgdesc="An unicode enabled rxvt-clone terminal emulator (urxvt)"
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
arch=(i686 x86_64)
depends=('gcc-libs' 'libxft' 'libxpm' 'librsvg' 'libafterimage')
makedepends=('ncurses' 'perl>=5.10.0' 'pkgconfig')
optdepends=('perl: lots of utilities' 'gtk2-perl: to use the urxvt-tabbed')
conflicts=('rxvt-unicode-256color' 'rxvt-unicode')
license=("GPL")
source=(http://dist.schmorp.de/rxvt-unicode/${_pkgname}-${pkgver}.tar.bz2 \
        ${_pkgname}.desktop ${_pkgname}.png popup-menu-hang.diff)
md5sums=('a23aa40b31e843878b6f9c44768de430'
         '5bfefa1b41c2b81ca18f2ef847330543'
         '84328cada91751df07324d95f8be4d1b'
         'aead33ba3b08eeb251fb0c1427a4a024')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p0 < ../popup-menu-hang.diff || return 1
  ./configure --prefix=/usr \
    --with-terminfo=/usr/share/terminfo \
    --with-term=rxvt-256color \
    --enable-font-styles \
    --enable-xim \
    --enable-warnings \
    --enable-keepscrolling \
    --enable-selectionscrolling \
    --enable-smart-resize \
    --enable-transparency \
    --enable-utmp \
    --enable-wtmp \
    --enable-256-color \
    --enable-lastlog || return 1
  make || return 1
  install -d "${pkgdir}/usr/share/terminfo"
  export TERMINFO="${pkgdir}/usr/share/terminfo"
  make DESTDIR="${pkgdir}" install || return 1
 # install the tabbing wrapper ( requires gtk2-perl! )
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed || return 1
  install -Dm 755 doc/rxvt-tabbed "${pkgdir}/usr/bin/urxvt-tabbed" || return 1
 # install freedesktop menu and icon ( icon from cvs checkout )
  install -Dm644 ../${_pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 ../${_pkgname}.png \
    "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
}
