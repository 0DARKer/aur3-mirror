# Maintainer : Jonathan Gibert <jokot3@gmail.com>
# Contributor : Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>
# Contributor: Maik Broemme <mbroemme@libmpq.org>

_pkgname=asterisk
pkgname=asterisk11-lts
pkgver=11.15.0
pkgrel=5
pkgdesc="A complete PBX solution"
arch=('i686' 'x86_64')
backup=('etc/asterisk/acl.conf'
	'etc/asterisk/adsi.conf'
	'etc/asterisk/agents.conf' 
	'etc/asterisk/alarmreceiver.conf' 
	'etc/asterisk/alsa.conf' 
	'etc/asterisk/amd.conf' 
	'etc/asterisk/app_mysql.conf' 
	'etc/asterisk/app_skel.conf'
	'etc/asterisk/adsi.conf'
  	'etc/asterisk/asterisk.conf' 
	'etc/asterisk/calendar.conf' 
	'etc/asterisk/ccss.conf' 
	'etc/asterisk/cdr.conf' 
	'etc/asterisk/cdr_adaptive_odbc.conf' 
	'etc/asterisk/cdr_custom.conf' 
	'etc/asterisk/cdr_manager.conf' 
	'etc/asterisk/cdr_mysql.conf'
  	'etc/asterisk/cdr_odbc.conf' 
	'etc/asterisk/cdr_pgsql.conf' 
	'etc/asterisk/cdr_sqlite3_custom.conf' 
	'etc/asterisk/cdr_syslog.conf' 
	'etc/asterisk/cdr_tds.conf' 
	'etc/asterisk/cel.conf' 
	'etc/asterisk/cel_custom.conf' 
	'etc/asterisk/cel_odbc.conf'
  	'etc/asterisk/cel_pgsql.conf' 
	'etc/asterisk/cel_sqlite3_custom.conf' 
	'etc/asterisk/cel_tds.conf' 
	'etc/asterisk/chan_dahdi.conf' 
	'etc/asterisk/chan_mobile.conf' 
	'etc/asterisk/cli.conf' 
	'etc/asterisk/cli_aliases.conf' 
	'etc/asterisk/cli_permissions.conf'
  	'etc/asterisk/codecs.conf' 
	'etc/asterisk/confbridge.conf' 
	'etc/asterisk/config_test.conf' 
	'etc/asterisk/console.conf' 
	'etc/asterisk/dbsep.conf' 
	'etc/asterisk/dnsmgr.conf' 
	'etc/asterisk/dsp.conf' 
	'etc/asterisk/dundi.conf' 
	'etc/asterisk/enum.conf'
  	'etc/asterisk/extconfig.conf' 
	'etc/asterisk/extensions.ael'
	'etc/asterisk/extensions.conf' 
	'etc/asterisk/extensions.lua' 
	'etc/asterisk/extensions_minivm.conf' 
	'etc/asterisk/features.conf' 
	'etc/asterisk/festival.conf' 
	'etc/asterisk/followme.conf' 
	'etc/asterisk/func_odbc.conf' 
	'etc/asterisk/gtalk.conf'
  	'etc/asterisk/h323.conf' 
	'etc/asterisk/http.conf' 
	'etc/asterisk/iax.conf' 
	'etc/asterisk/iaxprov.conf' 
	'etc/asterisk/indications.conf' 
	'etc/asterisk/jabber.conf' 
	'etc/asterisk/jingle.conf' 
	'etc/asterisk/logger.conf' 
	'etc/asterisk/manager.conf' 
	'etc/asterisk/meetme.conf'
  	'etc/asterisk/mgcp.conf' 
	'etc/asterisk/minivm.conf' 
	'etc/asterisk/misdn.conf' 
	'etc/asterisk/modules.conf' 
	'etc/asterisk/motif.conf' 
	'etc/asterisk/musiconhold.conf' 
	'etc/asterisk/muted.conf' 
	'etc/asterisk/ooh323.conf' 
	'etc/asterisk/osp.conf' 
	'etc/asterisk/oss.conf'
  	'etc/asterisk/phone.conf' 
	'etc/asterisk/phoneprov.conf'
	'etc/asterisk/queuerules.conf' 
	'etc/asterisk/queues.conf' 
	'etc/asterisk/res_config_mysql.conf' 
	'etc/asterisk/res_config_sqlite.conf' 
	'etc/asterisk/res_config_sqlite3.conf'
  	'etc/asterisk/res_corosync.conf' 
	'etc/asterisk/res_curl.conf' 
	'etc/asterisk/res_fax.conf' 
	'etc/asterisk/res_ldap.conf' 
	'etc/asterisk/res_odbc.conf' 
	'etc/asterisk/res_pgsql.conf' 
	'etc/asterisk/res_pktccops.conf' 
	'etc/asterisk/res_snmp.conf'
  	'etc/asterisk/res_stun_monitor.conf' 
	'etc/asterisk/rtp.conf' 
	'etc/asterisk/say.conf' 
	'etc/asterisk/sip.conf' 
	'etc/asterisk/sip_notify.conf' 
	'etc/asterisk/skinny.conf' 
	'etc/asterisk/sla.conf' 
	'etc/asterisk/smdi.conf' 
	'etc/asterisk/telcordia-1.adsi'
	'etc/asterisk/udptl.conf' 
	'etc/asterisk/unistim.conf'
  	'etc/asterisk/users.conf' 
	'etc/asterisk/voicemail.conf' 
	'etc/asterisk/vpb.conf' 
	'etc/asterisk/xmpp.conf')
url="http://www.asterisk.org"
license=('GPL')
# dependencies sources :
# https://wiki.asterisk.org/wiki/display/AST/System+Libraries
depends=('sqlite3' 'ncurses' 'libxml2' 'openssl')
#makedepends=()
optdepends=('libedit: alternative to bundled libedit'
	    'dahdi: support for PSTN (analog and digital phones)'
	    'libpri: support for ISDN (T1/E1/J1, BRI cards, needs dahdi)'
	    'libss7: support for SS7 signaling'
	    'openr2: support for R2 signaling'
	    'spandsp: support for faxes'
	    'portaudio: for channel console'
	    'sdl: for channel oss'
	    'alsa-lib: for channel alsa'
            'lua51: use of lua in dialplan'
	    'postgresql-libs: support for postgres'
	    'unixodbc: support for unixodbc'
	    'libsrtp: support for SRTP'
	    'iksemel: support for xmpp protocol (and distributed device state if used with XMPP pubsub)'
	    'radiusclient-ng: support for radius CDR/CEL backend'
	    'neon: for calendar resources'
	    'libical: for calendar resources'
	    'gsm: GSM codec'
	    'libvorbis: vorbis codec'
	    'libogg: ogg codec'
	    'speex: speex codec'
	    'net-snmp: for SNMP resources'
	    'gmime: for HTTP resources'
	    'curl: for cURL resources')
source=(http://downloads.asterisk.org/pub/telephony/asterisk/releases/asterisk-${pkgver}.tar.gz \
	${_pkgname}.service \
	${_pkgname}.logrotated \
	${_pkgname}.tmpfile)
install=${_pkgname}.install
sha256sums=('20a20d116a70f2187f3ddcf79e3109f62054b20b402d6bdd28a2cefc245bbb99'
	'74e0b278d553499f0c648a6e3d55c0dbb11b0c6dc93a85b020a21eafadb83783'
	'caa24cfec5c6b4f8cea385269e39557362acad7e2a552994c3bc24080e3bdd4e'
	'673c0c55bce8068c297f9cdd389402c2d5d5a25e2cf84732cb071198bd6fa78a')

provides=('asterisk')
conflicts=('asterisk')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin
  make
}

package(){
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" samples
  
  sed -i -e "s/\/var\/run/\/run/" "${pkgdir}/etc/asterisk/asterisk.conf"

  mkdir -p "${pkgdir}/usr/share/doc/asterisk/examples"
  for i in "${pkgdir}"/etc/asterisk/*; do install -D -m 644 "$i" "${pkgdir}/usr/share/doc/asterisk/examples/"; done
  install -D -m 444 "${srcdir}/${_pkgname}-${pkgver}/README" "${pkgdir}/usr/share/doc/asterisk/README"
  install -D -m 444 "${srcdir}/${_pkgname}-${pkgver}/README-SERIOUSLY.bestpractices.txt" "${pkgdir}/usr/share/doc/asterisk/README-SERIOUSLY.bestpractices.txt"

  mv "${pkgdir}/var/run" "${pkgdir}"
  
  install -D -m 644 "${srcdir}/asterisk.logrotated" "${pkgdir}/etc/logrotate.d/asterisk"
  install -D -m 644 "${srcdir}/asterisk.service" "${pkgdir}/usr/lib/systemd/system/asterisk.service"
  install -D -m 644 "${srcdir}/asterisk.tmpfile" "${pkgdir}/usr/lib/tmpfiles.d/asterisk.conf"
 }
