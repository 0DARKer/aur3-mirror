# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>

pkgname=cpupower
pkgver=3.1
pkgrel=1
pkgdesc="Linux kernel ${pkgver//_/-} cpupower tool"
license=('GPL2')
arch=('i686' 'x86_64')
url='http://www.kernel.org'
options=(!strip)
conflicts=('cpufrequtils')
source=("http://ftp.kernel.org/pub/linux/kernel/v3.0/linux-$pkgver.tar.bz2") 
md5sums=('8d43453f8159b2332ad410b19d86a931')

build() {
  cd linux-${pkgver//_/-}/tools/power/cpupower
  # fix compiling fail if --as-needed flags is present
  [[ "$LDFLAGS" ]] && LDFLAGS="$LDFLAGS,--no-as-needed"
  make
}

package() {
  cd linux-${pkgver//_/-}/tools/power/cpupower
  make DESTDIR="${pkgdir}" INSTALL="/bin/install -c" install install-man
}
# vim:set ts=2 sw=2 ft=sh et:
