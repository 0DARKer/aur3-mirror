# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=android-qt5-qtjsbackend-git
pkgver=5.1.1_202.fb6c137
pkgrel=1
pkgdesc="Qt 5 for Android: qtjsbackend module (includes QtV8)"
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/Qt5ForAndroid"
license=('LGPL')
groups=('qt-android' 'qt5-android')
makedepends=('python' 'git')
options=(!libtool !strip)
depends=('android-qt5-qtbase-git')
source=("${pkgname}::git://gitorious.org/qt/qtjsbackend.git")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${pkgname}
  _basever=$(grep MODULE_VERSION .qmake.conf | awk -- '{print $3}')
  echo ${_basever}_$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  #git describe --always | sed 's|-|_|g' | sed 's|^v||'
}

build() {
  cd ${srcdir}/${pkgname}
  
  /opt/android-qt5/bin/qmake
  make
}

package() {
  cd ${srcdir}/${pkgname}
  
  make INSTALL_ROOT="${pkgdir}" install
    
  # Strip by hand, system strip doesn't work on arm binaries

  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/lib/*.so*
}
