#Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbasename=libgnome-keyring
pkgname=lib32-$_pkgbasename
pkgver=3.2.0
pkgrel=1
pkgdesc="GNOME keyring client library (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('LGPL')
depends=('lib32-libgcrypt' 'lib32-glib2' 'lib32-dbus-core' "$_pkgbasename")
makedepends=('intltool' 'gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/3.2/${_pkgbasename}-${pkgver}.tar.xz)
sha256sums=('0516403432b06ecc173c9f9dbe3eaa87b2c7a230806fcac4dfd73ad998e928f5')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib32/gnome-keyring \
      --libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,share}
}
