#Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libgnome-keyring
pkgname=lib32-$_pkgbase
pkgver=3.2.2
pkgrel=1
pkgdesc="GNOME keyring client library (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL' 'LGPL')
depends=('lib32-dbus-core' 'lib32-libgcrypt' 'lib32-glib2' "$_pkgbase")
makedepends=('intltool' 'gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/3.2/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('2bcbe47b1960737866835a516056c36d1a3d82d5ad101c70d5a87e1ad616adb3')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib32/gnome-keyring \
      --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,share}
}
