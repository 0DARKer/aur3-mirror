_pkgbasename=libgnome-keyring
pkgname=lib32-$_pkgbasename
pkgver=2.32.0
pkgrel=2
pkgdesc="GNOME keyring client library (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('LGPL')
depends=('lib32-dbus-core>=1.4.0' 'lib32-libgcrypt>=1.4.6' 'lib32-glib2>=2.26.0' "$_pkgbasename")
makedepends=('intltool' 'pkgconfig' 'python2' 'gtk-doc' 'gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.32/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('56388c0d81ddfdb57d30e4963c83ecc1c18498aab99395420e0fff69929a0f0c')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{include,share}
}
