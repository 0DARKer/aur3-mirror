#Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libgnome-keyring
pkgname=lib32-${_pkgbase}
pkgver=3.4.1
pkgrel=1
pkgdesc="GNOME keyring client library (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('GPL' 'LGPL')
depends=('lib32-dbus-core' 'lib32-glib2' 'lib32-libgcrypt' "${_pkgbase}")
makedepends=('intltool' 'gcc-multilib')
options=('!libtool' '!emptydirs')
_ver=${pkgver%.*}
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbase}/${_ver}/${_pkgbase}-${pkgver}.tar.xz)
sha256sums=('e74a3e4521767fe5f05cc97cda8a355239567d9763ec628280ede8c2a4a379e8')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib32/gnome-keyring \
      --libdir=/usr/lib32
  make
}

package() {
  cd "${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}
