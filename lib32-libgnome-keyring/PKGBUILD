_pkgbasename=libgnome-keyring
pkgname=lib32-$_pkgbasename
pkgver=3.0.3
pkgrel=1
pkgdesc="GNOME keyring client library (32 bit)"
arch=('x86_64')
url="http://www.gnome.org"
license=('LGPL')
depends=('lib32-libgcrypt' 'lib32-glib2' "$_pkgbasename")
makedepends=('intltool' 'gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('a0cc08bcd431070d3f01c2741903c2bd8b0231ee73de709d28e722e28575fdf8')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rvf ${pkgdir}/usr/{include,share}
}
