# Maintainer: drrossum <d.r.vanrossum at gmx.de>
# Contributor: jsteel <jsteel at aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname='perl-any-moose'
pkgver=0.26
pkgrel=1
pkgdesc="Use Moose or Mouse modules"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-moose' 'perl-mouse')
url='http://search.cpan.org/dist/Any-Moose'
source=(http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/Any-Moose-$pkgver.tar.gz)
md5sums=('f36ed5cc95497e1d4f4f617cc1cce03b')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir"/Any-Moose-$pkgver

    /usr/bin/perl Makefile.PL

    make
  )
}

check() {
  cd "$srcdir"/Any-Moose-$pkgver

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir"/Any-Moose-$pkgver

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
