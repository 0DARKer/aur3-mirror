# Maintainer: jsteel <jsteel at aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname='perl-any-moose'
pkgver='0.21'
pkgrel='2'
pkgdesc="Use Moose or Mouse modules"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-moose' 'perl-mouse')
url='http://search.cpan.org/dist/Any-Moose'
source=(http://search.cpan.org/CPAN/authors/id/S/SA/SARTAK/Any-Moose-$pkgver.tar.gz)
md5sums=('72cf59db8a54f73e63b30f5a17b6b851')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir"/Any-Moose-$pkgver

    /usr/bin/perl Makefile.PL

    make
  )
}

check() {
  cd "$srcdir"/Any-Moose-$pkgver

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir"/Any-Moose-$pkgver

  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
