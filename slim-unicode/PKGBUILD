# Author: Benjamin Robin <benjarobin gmail>

# $Id: PKGBUILD 175827 2013-01-22 09:52:02Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Hugo Ideler <hugoideler@dse.nl>

_pkgname=slim
pkgname=${_pkgname}-unicode
pkgver=1.3.5
pkgrel=2
pkgdesc="Desktop-independent graphical login manager for X11"
arch=('i686' 'x86_64')
url="http://slim.berlios.de/"
license=('GPL2')
depends=('pam' 'libxmu' 'libpng' 'libjpeg' 'libxft' 'xorg-xauth')
makedepends=('cmake' 'freeglut')
backup=('etc/slim.conf' 'etc/logrotate.d/slim' 'etc/pam.d/slim')
provides=('slim')
conflicts=('slim')
source=(http://download.berlios.de/$_pkgname/$_pkgname-$pkgver.tar.gz
        slim.pam
        slim.logrotate
        unicode.patch)

sha256sums=('818d209f51e2fa8d5b94ef75ce90a7415be48b45e796d66f8083a9532b655629'
            'b9a77a614c451287b574c33d41e28b5b149c6d2464bdb3a5274799842bca51a4'
            '5bf44748b5003f2332d8b268060c400120b9100d033fa9d35468670d827f6def'
            'd3768953c1443e64a8f61e4aa25042855cee4d6ded0216a1bf6b9f29ff4c66e1')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -p1 -i ../unicode.patch # Add support of unicode and minor fix

  # Fix installation path of slim.service
  sed -i 's|usr/lib/systemd/system|/&|' CMakeLists.txt

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DUSE_PAM=yes \
    -DUSE_CONSOLEKIT=no
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/slim.pam" "$pkgdir/etc/pam.d/slim"
  install -Dm644 "$srcdir/slim.logrotate" "$pkgdir/etc/logrotate.d/slim"

  # Provide sane defaults
  sed -i -e 's|#xserver_arguments.*|xserver_arguments -nolisten tcp vt07|' \
         -e 's|/var/run/slim.lock|/var/lock/slim.lock|' \
    "$pkgdir/etc/slim.conf"
}

# vim:set ts=2 sw=2 et:
