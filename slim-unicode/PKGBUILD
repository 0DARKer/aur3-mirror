# Author: Benjamin Robin <benjarobin gmail>

_pkgname=slim
pkgname=${_pkgname}-unicode
pkgver=1.3.3
pkgrel=3
pkgdesc='Desktop-independent graphical login manager for X11 with unicode support (Support for accents...)'
arch=('i686' 'x86_64')
url='http://slim.berlios.de/'
license=('GPL2')
depends=('consolekit' 'pam' 'libxmu' 'libpng' 'libjpeg' 'libxft')
makedepends=('cmake')
backup=('etc/slim.conf' 'etc/logrotate.d/slim' 'etc/pam.d/slim')
source=("http://download.berlios.de/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        'rc.d'
        'pam.d'
        'logrotate'
        'slim.service'
        'ptr_pam.patch'
        'session-name.patch'
        'fix-numlock-1.3.3.patch'
        'unicode.patch'
        'slim.sh')

md5sums=('ce53e44c1e4a2eacf5bb7688ee2a5de8'
         '83fa7f3a10d577325fb93cf3f84b12a7'
         'ebfff3417058619a1328284ae0e2f48e'
         '43da096480bf72c3ccec8ad8400f34f0'
         'aef560f5a765582b2a7fc1fac5b323a7'
         'ae4b237d472ec5d373e30080aa0ff804'
         'ebcb6829028615686de7b64ceeaaf8ed'
         '6c29c93370e44ed310ff30132424b619'
         '364dcc7144757fb0c482de18af25c1b3'
         '1f6a54e9173839f784e8fae261c78052')

install=install

provides=('slim')
conflicts=('slim')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    patch -p1 -i ../ptr_pam.patch # FS#23995: pointer mishandling confuses PAM
    patch -p1 -i ../session-name.patch # FS#26693: fix default session name
    patch -p1 -i ../fix-numlock-1.3.3.patch # FS#29818: fix numlock handling
    patch -p1 -i ../unicode.patch # Add support of unicode
    cd ${srcdir}
    mkdir build
    cd build
    cmake ../${_pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_PAM=yes -DUSE_CONSOLEKIT=yes
	make
}

package() {
    cd  ${srcdir}/build/
    make DESTDIR="${pkgdir}" install

    install -D -m755 ../rc.d "${pkgdir}"/etc/rc.d/slim
    install -D -m644 ../pam.d "${pkgdir}"/etc/pam.d/slim
    install -D -m644 ../logrotate "${pkgdir}"/etc/logrotate.d/slim

    mkdir -p "${pkgdir}"/usr/share/slim/bin/
    mv "${pkgdir}"/usr/bin/slim "${pkgdir}"/usr/share/slim/bin/
    install -D -m755 ../slim.sh "${pkgdir}"/usr/bin/slim

    # Provide sane defaults
    sed -i 's|#xserver_arguments.*|xserver_arguments -nolisten tcp vt07|' "${pkgdir}"/etc/slim.conf
    sed -i 's|/var/run/slim.lock|/var/lock/slim.lock|' "${pkgdir}"/etc/slim.conf
    # install systemd files
    install -D -m644 ${srcdir}/slim.service ${pkgdir}/usr/lib/systemd/system/slim.service
}

