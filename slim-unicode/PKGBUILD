# $Id: PKGBUILD 122020 2011-05-01 17:05:22Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Hugo Ideler <hugoideler@dse.nl>

_pkgname=slim
pkgname=${_pkgname}-unicode
pkgver=1.3.2
pkgrel=6
pkgdesc='Desktop-independent graphical login manager for X11'
arch=('i686' 'x86_64')
url='http://slim.berlios.de/'
license=('GPL2')
depends=('pam' 'libxmu' 'libpng' 'libjpeg' 'libxft')
backup=('etc/slim.conf' 'etc/logrotate.d/slim' 'etc/pam.d/slim')
source=("http://download.berlios.de/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        'rc.d'
        'pam.d'
        'logrotate'
        'gcc44.patch'
        'ptr_pam.patch'
        'no-host.patch'
        'restart.patch'
        'sigterm.patch'
        'tty-slowness.patch'
        'unicode.patch'
        'slim.sh')
sha1sums=('e421d5487732c8317f8f591906661e014b036358'
          'efc36c6f9a82e55243c5286f57715e2010f1314a'
          'a0e991ef0ac5120465a3be014a26e70ba073b6ae'
          'b969cc902c1d9915a5609141a652c77b2732407b'
          '51121d451116c768d0fc027ff1ea70aaaef036e7'
          '640668c984a13593a1bfba8d3b503c005d5f401e'
          'b86eddd083fb9f6259e46c735f55ebe76c655bd3'
          '2d526bc0c498bf307ee50e2d22b4f53ffa0c4435'
          '0b35048723c527fb824c5e0f9b9064f751871785'
          '213fefe8533c845ea8c40585b6a8097820d5e5d2'
          '97d46bde445aba4b3ebb2a7325e0941ee55b4c18'
          'a0800ada50b6b17e7263f7ecd8a74e041759bdc6')

provides=('slim')
conflicts=('slim')
install=install

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	sed -i -e 's/png12/png14/g' Makefile
	patch -p1 -i ../gcc44.patch   # FS#14815: lacks include for gcc-4.4
	patch -p1 -i ../ptr_pam.patch # FS#23995: pointer mishandling confuses PAM
	patch -p1 -i ../no-host.patch # cf patch: do not set PAM host
	patch -p1 -i ../restart.patch # cf patch: restart X server if killed
	patch -p1 -i ../sigterm.patch # FS#23984: do not wait for input when SIGTERM'd
	patch -p1 -i ../tty-slowness.patch # FS#18313: fix sluggish TTY after slim start
	patch -p1 -i ../unicode.patch # Add support of unicode

	make USE_PAM=1
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" MANDIR=/usr/share/man install

	install -D -m755 ../rc.d "${pkgdir}"/etc/rc.d/slim
	install -D -m644 ../pam.d "${pkgdir}"/etc/pam.d/slim
	install -D -m644 ../logrotate "${pkgdir}"/etc/logrotate.d/slim
	mkdir -p "${pkgdir}"/usr/share/slim/bin/
	mv "${pkgdir}"/usr/bin/slim "${pkgdir}"/usr/share/slim/bin/
	install -D -m755 ../slim.sh "${pkgdir}"/usr/bin/slim

	# Provide sane defaults
	sed -i 's|#xserver_arguments.*|xserver_arguments -nolisten tcp vt07|' "${pkgdir}"/etc/slim.conf
	sed -i 's|/var/run/slim.lock|/var/lock/slim.lock|' "${pkgdir}"/etc/slim.conf
}
