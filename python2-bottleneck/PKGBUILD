# Maintainer: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

pkgname=python2-bottleneck
_pkgname=${pkgname#python2-}
pkgver=0.6.0
pkgrel=2
pkgdesc="Fast NumPy array functions written in Cython"
arch=('i686' 'x86_64')
url="http://berkeleyanalytics.com/bottleneck/"
license=('BSD')
depends=('python2-numpy')
optdepends=('python2-nose')
makedepends=('cython2')
options=('!emptydirs')
source=("https://github.com/kwgoodman/bottleneck/archive/v$pkgver.tar.gz")
md5sums=('b90eee9dba93229d365ec50553e58611')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i -e 's/python/python2/g' Makefile
  make clean pyx build
  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}

