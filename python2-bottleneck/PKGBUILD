# Maintainer: Michael Schubert <mschu.dev at gmail>
# Contributor: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

pkgname=python2-bottleneck
_pkgname=${pkgname#python2-}
pkgver=0.7.0
pkgrel=1
pkgdesc="Fast NumPy array functions written in Cython"
arch=('i686' 'x86_64')
url="http://berkeleyanalytics.com/bottleneck/"
license=('BSD')
depends=('python2-numpy')
optdepends=('python2-nose')
makedepends=('cython2')
options=('!emptydirs')
source=("https://github.com/kwgoodman/bottleneck/archive/v$pkgver.tar.gz"
         python2.patch)
md5sums=('5e0c372027834713914766cf3a933590'
         '8eec121e4bb9f277ca53bfe72811ccbb')

prepare() {
  cd $_pkgname-$pkgver
  patch -p0 < ../python2.patch
}

build() {
  cd $_pkgname-$pkgver
  make clean pyx build
  python2 setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}

