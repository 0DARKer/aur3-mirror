# Contributor: Vitaliy Berdinskikh ur6lad[at]archlinux.org.ua

pkgname=apache-ant-devel
pkgver=1.8.2
pkgrel=1
pkgdesc="A java-based build tool."
arch=('i686' 'x86_64')
url="http://ant.apache.org/"
license=('APACHE')
provides=('apache-ant')
conflicts=('apache-ant')
depends=('java-runtime' 'junit-devel')
source=(http://archive.apache.org/dist/ant/binaries/apache-ant-${pkgver}-bin.tar.bz2 \
		apache-ant.sh)

package() {
	cd $srcdir

	# Get the ANT_HOME env var
	source apache-ant.sh

	mkdir -p ${pkgdir}/etc/profile.d ${pkgdir}/usr/bin
	mkdir -p ${pkgdir}${ANT_HOME} ${pkgdir}/usr/share/java/apache-ant
	mkdir -p ${pkgdir}/usr/share/licenses/apache-ant
	ln -s /usr/share/java/apache-ant ${pkgdir}${ANT_HOME}/lib

	install -m755 apache-ant.sh ${pkgdir}/etc/profile.d

	cd apache-ant-${pkgver}

	cp -Rp etc ${pkgdir}${ANT_HOME}
	find bin -type f -a ! -name \*.bat -a ! -name \*.cmd \
		-exec install -m755 {} ${pkgdir}/usr/bin \;
	install -m644 lib/*.jar ${pkgdir}/usr/share/java/apache-ant
	
	# The license says the NOTICE file should be redistributed for derivative
	# works, so lets supply it.
	install -m644 LICENSE NOTICE ${pkgdir}/usr/share/licenses/apache-ant

	# symlink to junit so it's on the javac build path for ant
	# matches behavior on ubuntu 9 and makes sense for compatibility
	# http://bugs.archlinux.org/task/15229
	cd ${pkgdir}/usr/share/java/apache-ant

	ln -s /usr/share/java/hamcrest.jar .
	ln -s /usr/share/java/junit.jar .
}

md5sums=('9e9ae9fc7352960191489a1286fb0928'
         'dbfa1e8dd4bab2f5821f5abe50835c83')
sha256sums=('ecb49f5017807c9320bf19702149328cf4779a01bddca2b6c47e3ebb0e39c9c7'
            '182b9212610790966d3a018d3cbab2e5327dd9fd11dc15dd1f377853216f5718')
