# Maintainer: kfgz <kfgz at interia dot pl>
# Previous Maintainer: Army <gmail.com: uli[dot]armbruster>
# Previous Maintainer: Geoffroy Carrier <geoffroy dot carrier at aur dot archlinux dot org>
# Previous Maintainer: Jason Chu <jason at archlinux dot org>
# Contributor: Charles Ghislain <charlyghislain at gmail dot com>
pkgname=jre-dev
_jdkver=7
# in order to update the PKGBUILD usually only the following two lines need to be changed + the md5sums
_jdkbuild=b145
_jdkdate=07_jun_2011

pkgver=${_jdkver}${_jdkbuild}
pkgrel=1
pkgdesc="Sun's Java Runtime Environement, snapshot release"
arch=(i686 x86_64)

  [ "${CARCH}" = "i686" ]   && ARCH=i586
  [ "${CARCH}" = "x86_64" ] && ARCH=x64

_filename=jre-${_jdkver}-ea-bin-${_jdkbuild}-linux-${ARCH}-${_jdkdate}.tar.gz
url="http://jdk7.java.net/"
depends=(libxtst)
source=(http://www.java.net/download/jdk7/archive/${_jdkbuild}/binaries/${_filename}
        jre.profile
        jre.profile.csh
        javaws-launcher)
	
if [ "${CARCH}" = "x86_64" ]; then 
   md5sums[0]=1126a6b7c4ed0f3d7fcc2e5de7c8dcf8
  else
   md5sums[0]=74686142bab553eee2be6186321d4dae
fi

md5sums[1]=ac5e8caf02b872487adac131ef7b4b56
md5sums[2]=ec287683614f1bf6c6dce8314e339c3f
md5sums[3]=45c15a6b4767288f2f745598455ea2bf

replaces=(j2re)
conflicts=(java-runtime j2re)
provides=(java-runtime=7 j2re)
license=(custom)

package() {
  cd ${srcdir}/jre1.7.0

  install -dm755 ${pkgdir}/opt/java/jre
  cp -ar * ${pkgdir}/opt/java/jre/

  install -D -m755 ${srcdir}/jre.profile ${pkgdir}/etc/profile.d/jre.sh
  install -D -m755 ${srcdir}/jre.profile.csh ${pkgdir}/etc/profile.d/jre.csh

  install -dm755 ${pkgdir}/usr/lib/mozilla/plugins

  if [ "$CARCH" = "x86_64" ]; then
     ln -s /opt/java/jre/lib/amd64/libnpjp2.so ${pkgdir}/usr/lib/mozilla/plugins
  else 
     ln -s /opt/java/jre/lib/i386/libnpjp2.so ${pkgdir}/usr/lib/mozilla/plugins
  fi
  
  install -dm755 ${pkgdir}/usr/share/licenses/jre
  install -D -m644 ${pkgdir}/opt/java/jre/COPYRIGHT ${pkgdir}/usr/share/licenses/jre
  
  # Fix system prefs folder (FS#18872)
  install -d ${pkgdir}/etc/.java/.systemPrefs

  # Fix FS#22509 - [jre] Clicking a .jnlp file does not launch that file but launch javaws -viewer instead 
  install ${srcdir}/javaws-launcher ${pkgdir}/opt/java/jre/bin
}
