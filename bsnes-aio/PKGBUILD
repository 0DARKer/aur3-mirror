# Maintainer : Alucryd <alucryd at gmail dot com>
# Thx vEX for the Qt 4.8.0 fix

pkgname=bsnes-aio
pkgver=091
pkgrel=3
pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code"
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
makedepends=('mesa')
source=("http://bsnes.googlecode.com/files/bsnes_v$pkgver-source.tar.xz" "bsnes.sh" "purify.desktop" "purify.sh")
md5sums=('ba53212c56990e83d7770393c4e2d173'
         'd1288d0ed6f42f09c1052ed64acff473'
         '7c8f3412df2f511d50882de5cf82f7d7'
         '7272fa647e963a44f005a683570c0e95')
provides=('bsnes' 'purify')
conflicts=('bsnes' 'snesfilter')
optdepends=('beat: ROM patcher')

# By default, the script will compile an accuracy build using a GTK interface without the debugger. Comment/uncomment the following variables to suit your preferences.

# Profiles: Accuracy (slower), Compatibility, or Performance (faster)
_profile=accuracy
#_profile=compatibility
#_profile=performance

# Interface: GTK or QT
depends=('gtk2' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
_interface=gtk
#depends=('qt>=4.7.0' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
#_interface=qt

# Debugger
#_debug=true
_debug=false

build() {
# QT 4.8.0 fix
  if [ $_interface == "qt" ]
  then
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp
  fi

# Compile laevateinn
  cd "${srcdir}/bsnes_v$pkgver-source"
  if [ $_debug == "true" ]
  then
    cp -R bsnes laevateinn
    cd laevateinn
    make compiler=gcc platform=x phoenix=$_interface profile=accuracy target=laevateinn
  fi    

# Compile bsnes
  cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
  make compiler=gcc platform=x phoenix=$_interface profile=$_profile target=ethos

# Compile purify
  cd "${srcdir}/bsnes_v$pkgver-source/purify"
  sed -i 's|gtk+-2.0|gtk+-2.0 x11|g' phoenix/Makefile
  make compiler=gcc platform=x phoenix=$_interface

# Compile filters
#  cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
#  make compiler=gcc platform=x
}

package() {
  cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
  sed -i 's|~\/.config|$(DESTDIR)$(prefix)\/share|g' target-ethos/Makefile
  sed -i 's|sudo install|install|g' target-ethos/Makefile
  make target=ethos install DESTDIR="${pkgdir}" prefix=/usr

  cd "${srcdir}/bsnes_v$pkgver-source"
  if [ $_debug == "true" ]
  then
    cd laevateinn
    sed -i 's|~\/.config|$(DESTDIR)$(prefix)\/share|g' target-laevateinn/Makefile
    sed -i 's|sudo install|install|g' target-laevateinn/Makefile
    make target=laevateinn install DESTDIR="${pkgdir}" prefix=/usr
    rm -rf $pkgdir/usr/share/laevateinn
  fi
    
  cd "${srcdir}/bsnes_v$pkgver-source/purify"
  install -Dm755 purify "${pkgdir}/usr/bin/purify"
    
#  cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
#  sed -i 's|~\/.config/$(DESTDIR)$(prefix)\/share|g' Makefile
#  make install DESTDIR="${pkgdir}" prefix=/usr

  cd "${srcdir}/bsnes_v$pkgver-source/shaders"
  mkdir -p "${pkgdir}/usr/share/bsnes/Video Shaders"
  cp *.shader "${pkgdir}/usr/share/bsnes/Video Shaders/"

# Tweak to copy shares to ~./config
  install -Dm755 "${srcdir}/bsnes.sh" "${pkgdir}/usr/bin/bsnes.sh"
  install -Dm755 "${srcdir}/purify.sh" "${pkgdir}/usr/bin/purify.sh"
  install -Dm755 "${srcdir}/purify.desktop" "${pkgdir}/usr/share/applications/purify.desktop"
  sed -i 's|Exec=bsnes|Exec=/usr/bin/bsnes.sh|g' "${pkgdir}/usr/share/applications/bsnes.desktop"
  chmod -R 755 "${pkgdir}/usr/share/bsnes"
}
