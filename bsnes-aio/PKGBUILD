# Maintainer : Alucryd <alucryd at gmail dot com>
# Thanks vEX for the better fix to filters and shaders.
pkgname=bsnes-aio
pkgver=084
pkgrel=1
pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code. Includes snespurify, bps, filters, shaders and cheats database."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
depends=('gtk2' 'pulseaudio' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
makedepends=('pkgconfig' 'mesa')
source=('http://bsnes.googlecode.com/files/bsnes_v084-source.tar.bz2' 'http://byuu.org/files/bps_v03.tar.bz2' 'add-usr-share-fallback.patch')
md5sums=('ba66a8aa91b8f17812cc91ffeb63e489' '65c425a5843556bef6f333c4084af8c6' '032e01298f3034ad621522e771fd9fa9')
provides=('bsnes' 'snespurify' 'bps')
conflicts=('bsnes' 'snesfilter')

build() {
    # Filters/Shaders fix
    cd "${srcdir}/bsnes"
    patch -p0 < "${srcdir}/add-usr-share-fallback.patch"

    # Cheats fix
    cd "${srcdir}/bsnes/ui/tools"
    sed -i 's/path.home("cheats.xml")/"\/usr\/share\/bsnes\/cheats.xml"/g' cheat-database.cpp

    # Profile
    cd "${srcdir}/bsnes"
    msg "Please select a profile"
    select bp in "Accuracy (Slower)" "Compatibility" "Performance (Faster)"; do
        case $bp in
            "Accuracy (Slower)" ) break;;
            "Compatibility" ) sed -i 's/accuracy/compatibility/g' Makefile; break;;
            "Performance (Faster)" ) sed -i 's/accuracy/performance/g' Makefile; break;;
        esac
    done

    # Compile bsnes
    cd "${srcdir}/bsnes"
    make compiler=gcc platform=x phoenix=gtk

    # Compile snespurify
    cd "${srcdir}/snespurify"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh

    # Compile filters
    cd "${srcdir}/snesfilter"
    make compiler=gcc

    # Compile bps
    cd "${srcdir}/bps"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh
}

package() {
    cd "${srcdir}/bsnes"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' ui/Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/snespurify"
    install -D --mode 755 snespurify-gtk "${pkgdir}/usr/bin/snespurify-gtk"

    cd "${srcdir}/snesfilter"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/snesshader"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bps"
    install -D --mode 755 bps-gtk "${pkgdir}/usr/bin/bps-gtk"
}
