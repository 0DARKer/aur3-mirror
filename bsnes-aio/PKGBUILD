# Maintainer : Alucryd <alucryd at gmail dot com>
# Thx vEX for the Qt 4.8.0 fix
pkgname=bsnes-aio
pkgver=086
pkgrel=2
pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code. Includes snespurify, bps, filters, shaders and cheats database."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
depends=('gtk2' 'qt>=4.7.0' 'pulseaudio' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
makedepends=('mesa')
source=('http://bsnes.googlecode.com/files/bsnes_v086-source.tar.bz2' 'http://byuu.org/files/bps_v03.tar.bz2')
md5sums=('54f73b38d24405186b538e01713b0c1f' '65c425a5843556bef6f333c4084af8c6')
provides=('bsnes' 'snespurify' 'bps')
conflicts=('bsnes' 'snesfilter')

build() {
    # Filters/Shaders fix
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes/ui/general"
    sed -i 's/application->basepath/"\/usr\/share\/bsnes\/"/g' main-window.cpp

    # Cheats fix
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes/ui/tools"
    sed -i 's/application->path("cheats.xml")/"\/usr\/share\/bsnes\/cheats.xml"/g' cheat-database.cpp

    # Profile
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    msg "Please select a version"
    select bp in "Accuracy (Slower)" "Compatibility" "Performance (Faster)" "Debug"; do
        case $bp in
            "Accuracy (Slower)" ) msg "Building the accuracy version..." && __profile=accuracy && __ui=ui; break;;
            "Compatibility" ) msg "Building the compatibility version..." && __profile=compatibility && __ui=ui; break;;
            "Performance (Faster)" ) msg "Building the performance version..." && __profile=performance && __ui=ui; break;;
            "Debug" ) msg "Building the performance version..." && __profile=accuracy && __ui=ui-debugger; break;;
        esac
    done

    # Compile bsnes
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    msg "Please select your preferred interface"
    select pi in "GTK" "Qt"; do
        case $pi in
            "GTK" ) msg "Building the GTK interface..." && __interface=gtk; break;;
            "Qt" ) moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp && msg "Building the Qt interface..." && __interface=qt; break;;
        esac
    done
    make compiler=gcc platform=x phoenix=$__interface profile=$__profile ui=$__ui name=bsnes

    # Compile snespurify
    cd "${srcdir}/bsnes_v$pkgver-source/snespurify"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh

    # Compile filters
    cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
    make compiler=gcc

    # Compile bps
    cd "${srcdir}/bps"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh
}

package() {
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' ui/Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr
    
    cd "${srcdir}/bsnes_v$pkgver-source/snespurify"
    install -Dm755 snespurify-gtk "${pkgdir}/usr/bin/snespurify-gtk"

    cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bsnes_v$pkgver-source/snesshader"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bps"
    install -Dm755 bps-gtk "${pkgdir}/usr/bin/bps-gtk"
}
