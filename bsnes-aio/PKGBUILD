# Maintainer : Alucryd <alucryd at gmail dot com>
# Thx vEX for the Qt 4.8.0 fix
pkgname=bsnes-aio
pkgver=085
pkgrel=4
pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code. Includes snespurify, bps, filters, shaders and cheats database."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
depends=('gtk2' 'qt>=4.7.0' 'pulseaudio' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
makedepends=('mesa')
source=('http://bsnes.googlecode.com/files/bsnes_v085-source.tar.bz2' 'http://byuu.org/files/bps_v03.tar.bz2')
md5sums=('2419710087ba28c894e5aa1c3c41b6e7' '65c425a5843556bef6f333c4084af8c6')
provides=('bsnes' 'snespurify' 'bps')
conflicts=('bsnes' 'snesfilter')

build() {
    # Filters/Shaders fix
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes/ui/general"
    sed -i 's/application->basepath/"\/usr\/share\/bsnes\/"/g' main-window.cpp

    # Cheats fix
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes/ui/tools"
    sed -i 's/application->path("cheats.xml")/"\/usr\/share\/bsnes\/cheats.xml"/g' cheat-database.cpp

    # Profile
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    msg "Please select a version"
    select bp in "Accuracy (Slower)" "Compatibility" "Performance (Faster)"; do
        case $bp in
            "Accuracy (Slower)" ) msg "Building the accuracy version..."; break;;
            "Compatibility" ) sed -i 's/accuracy/compatibility/g' Makefile && msg "Building the compatibility version..."; break;;
            "Performance (Faster)" ) sed -i 's/accuracy/performance/g' Makefile && msg "Building the performance version..."; break;;
        esac
    done

    # Compile bsnes
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    msg "Please select your preferred interface"
    select pi in "GTK" "Qt"; do
        case $pi in
            "GTK" ) msg "Building the GTK interface..." && make compiler=gcc platform=x phoenix=gtk; break;;
            "Qt" ) moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp && msg "Building the Qt interface..." && make compiler=gcc platform=x phoenix=qt; break;;
        esac
    done

    # Compile snespurify
    cd "${srcdir}/bsnes_v$pkgver-source/snespurify"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh

    # Compile filters
    cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
    make compiler=gcc

    # Compile bps
    cd "${srcdir}/bps"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh
}

package() {
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' ui/Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bsnes_v$pkgver-source/snespurify"
    install -Dm755 snespurify-gtk "${pkgdir}/usr/bin/snespurify-gtk"

    cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bsnes_v$pkgver-source/snesshader"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bps"
    install -Dm755 bps-gtk "${pkgdir}/usr/bin/bps-gtk"
}
