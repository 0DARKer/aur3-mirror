# Maintainer : Alucryd <alucryd at gmail dot com>
# Thx vEX for the Qt 4.8.0 fix

pkgname=bsnes-aio
pkgver=086
pkgrel=4
pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code. Includes snespurify, bps, filters, shaders and cheats database."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
makedepends=('mesa')
source=('http://bsnes.googlecode.com/files/bsnes_v086-source.tar.bz2' 'http://byuu.org/files/bps_v03.tar.bz2')
md5sums=('54f73b38d24405186b538e01713b0c1f' '65c425a5843556bef6f333c4084af8c6')
provides=('bsnes' 'snespurify' 'bps')
conflicts=('bsnes' 'snesfilter')

# By default, the script will compile an accuracy build using a GTK interface without the debugger. Comment/uncomment the following variables to suit your preferences.

# Profiles: Accuracy (slower), Compatibility, or Performance (faster)
_profile=accuracy
#_profile=compatibility
#_profile=performance

# Interface: GTK or QT
depends=('gtk2' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
_interface=gtk
#depends=('qt>=4.7.0' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
#_interface=qt

# Debugger
#_debug=true
_debug=false

build() {
    # Filters/Shaders fix
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes/ui/general"
    sed -i 's/application->basepath/"\/usr\/share\/bsnes\/"/g' main-window.cpp

    # Cheats fix
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes/ui/tools"
    sed -i 's/application->path("cheats.xml")/"\/usr\/share\/bsnes\/cheats.xml"/g' cheat-database.cpp

    # QT 4.8.0 fix
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp	
  
    # Compile laevateinn
    cd "${srcdir}/bsnes_v$pkgver-source"
    if [ $_debug == "true" ]
    then
        cp -R bsnes laevateinn
        cd laevateinn
        make compiler=gcc platform=x phoenix=$_interface profile=accuracy ui=ui-debugger
    fi    

    # Compile bsnes
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    make compiler=gcc platform=x phoenix=$_interface profile=$_profile

    # Compile snespurify
    cd "${srcdir}/bsnes_v$pkgver-source/snespurify"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh

    # Compile filters
    cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
    make compiler=gcc

    # Compile bps
    cd "${srcdir}/bps"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh
}

package() {
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' ui/Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bsnes_v$pkgver-source"
    if [ $_debug == "true" ]
    then
        cd laevateinn
        sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' ui-debugger/Makefile
        make ui=ui-debugger install DESTDIR="${pkgdir}" prefix=/usr
        rm -rf $pkgdir/usr/share/laevateinn
    fi
    
    cd "${srcdir}/bsnes_v$pkgver-source/snespurify"
    install -Dm755 snespurify-gtk "${pkgdir}/usr/bin/snespurify-gtk"

    cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bsnes_v$pkgver-source/snesshader"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bps"
    install -Dm755 bps-gtk "${pkgdir}/usr/bin/bps-gtk"
}
