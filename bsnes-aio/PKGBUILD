# Maintainer : Alucryd <alucryd at gmail dot com>

pkgbase=bsnes-aio
pkgname=bsnes-aio
pkgver=091
pkgrel=7
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
source=("http://bsnes.googlecode.com/files/bsnes_v091-source.tar.xz" "bsnes.desktop" "bsnes.sh")
sha256sums=('8c85a18ff44195d23b54cad53549152f034ee338e590907e8c8cbc3eaa7daf9f'
            'f02ac8afd9482eced1e0a81b9c0963a7696b8089ba137f8ee68e1635300bcf38'
            'd45290d250cd1755829f5706e0063d08fccf8ed3b99e3552a0db3acddf0a73ea')

# Choose your preferred interface below (either gtk or qt)
_interface=gtk

[ $_interface == gtk ] && pkgname=('bsnes-common' 'bsnes-gtk')
[ $_interface == qt ] && pkgname=('bsnes-common' 'bsnes-qt')

buildui() {
  make compiler=gcc platform=x target=$1 phoenix=$2 profile=$3
  if [ $1 == ethos ]
  then
    mv out/bsnes out/bsnes-$2-$3
  else
    mv out/laevateinn out/laevateinn-$2
  fi
  make clean
}

installui() {
  cd "bsnes_v$pkgver-source/bsnes"
  install -dm 755 "${pkgdir}/usr/bin"
  install -dm 755 "${pkgdir}/usr/share/applications"
  if [ $1 == ethos ]
  then
    install -m 755 "../../bsnes.sh" "${pkgdir}/usr/bin/bsnes-$2.sh"
    sed -i "s|_ui|$2|" "${pkgdir}/usr/bin/bsnes-$2.sh"  
    install -dm 755 "${pkgdir}/usr/share/applications"
    for i in accuracy compatibility performance
    do
      install -m 755 "out/bsnes-$2-$i" "${pkgdir}/usr/bin/bsnes-$2-$i"
      install -m 644 "../../bsnes.desktop" "${pkgdir}/usr/share/applications/bsnes-$2-$i.desktop"
      sed -i "s|_ui|$2|g" "${pkgdir}/usr/share/applications/bsnes-$2-$i.desktop"
      sed -i "s|_profile|$i|g" "${pkgdir}/usr/share/applications/bsnes-$2-$i.desktop"
    done
  else
    install -m 755 "../../bsnes.sh" "${pkgdir}/usr/bin/laevateinn-$2.sh"
    sed -i 's|bsnes|laevateinn|' "${pkgdir}/usr/bin/laevateinn-$2.sh"
    sed -i "s|_ui|$2|" "${pkgdir}/usr/bin/laevateinn-$2.sh"
    install -m 755 "out/laevateinn-$2" "${pkgdir}/usr/bin/laevateinn-$2"
    install -m 644 "../../bsnes.desktop" "${pkgdir}/usr/share/applications/laevateinn-$2.desktop"
    sed -i 's|bsnes|laevateinn|g' "${pkgdir}/usr/share/applications/laevateinn-$2.desktop"
    sed -i "s|_ui|$2|g" "${pkgdir}/usr/share/applications/laevateinn-$2.desktop"
    sed -i "s|-_profile||g" "${pkgdir}/usr/share/applications/laevateinn-$2.desktop"
    ln -s /usr/share/bsnes "${pkgdir}/usr/share/laevateinn"
  fi
}

build() {
  cd "${srcdir}/bsnes_v$pkgver-source/bsnes"

# QT 4.8.0 fix
  moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp

# Compile bsnes GTK GUI
  if [ $_interface == gtk ]
  then
    for i in accuracy compatibility performance
    do
      buildui ethos gtk $i
    done
  fi

# Compile bsnes QT GUI
  if [ $_interface == qt ]
  then
    for i in accuracy compatibility performance
    do
      buildui ethos qt $i
    done
  fi

# Compile laevateinn GTK GUI
#  if [ $_interface == gtk ]
#  then
#    buildui laevateinn gtk accuracy
#  fi

# Compile laevateinn QT GUI
#  if [ $_interface == qt ]
#  then
#    buildui laevateinn qt accuracy
#  fi
}

package_bsnes-common() {
  pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code - Common files"
  arch=('any')

  cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
  mkdir -p "${pkgdir}/usr/share/bsnes"
  cp -R profile/* "${pkgdir}/usr/share/bsnes/"
  cp data/cheats.xml "${pkgdir}/usr/share/bsnes/"

  mkdir "${pkgdir}/usr/share/pixmaps"
  cp data/bsnes.png "${pkgdir}/usr/share/pixmaps/"

  cd "${srcdir}/bsnes_v$pkgver-source/shaders"
  mkdir "${pkgdir}/usr/share/bsnes/Video Shaders"
  cp *.shader "${pkgdir}/usr/share/bsnes/Video Shaders/"
  
  find "${pkgdir}" -type d -print0 | xargs -0 chmod 755
  find "${pkgdir}" -type f -print0 | xargs -0 chmod 644
}

package_bsnes-gtk() {
  pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code - GTK GUI"
  depends=('bsnes-common' 'gtk2' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
  optdepends=('purify: ROM purifier'
              'beat: Delta patcher')
  provides=('bsnes' 'bsnes-aio')
  
  installui ethos gtk
}

package_bsnes-qt() {
  pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code - QT GUI"
  depends=('bsnes-common' 'qt>=4.7.0' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
  optdepends=('purify: ROM purifier'
              'beat: Delta patcher')
  provides=('bsnes' 'bsnes-aio')
  
  installui ethos qt
}

package_laevateinn-gtk() {
  pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code - Debugger GTK GUI"
  depends=('bsnes-common' 'gtk2' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
  provides=('laevateinn')
  
  installui laevateinn gtk
}

package_laevateinn-qt() {
  pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code - Debugger QT GUI"
  depends=('bsnes-common' 'qt>=4.7.0' 'libpulse' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
  provides=('laevateinn')
  
  installui laevateinn qt
}

pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code"
depends=()
