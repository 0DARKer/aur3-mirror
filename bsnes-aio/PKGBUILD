# Maintainer : Alucryd <alucryd at gmail dot com>
pkgname=bsnes-aio
pkgver=085
pkgrel=3
pkgdesc="SNES emulator focused on accuracy, debugging functionality, and clean code. Includes snespurify, bps, filters, shaders and cheats database."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
depends=('gtk2' 'pulseaudio' 'libao' 'libgl' 'libxv' 'openal' 'sdl')
makedepends=('mesa')
source=('http://bsnes.googlecode.com/files/bsnes_v085-source.tar.bz2' 'http://byuu.org/files/bps_v03.tar.bz2')
md5sums=('2419710087ba28c894e5aa1c3c41b6e7' '65c425a5843556bef6f333c4084af8c6')
provides=('bsnes' 'snespurify' 'bps')
conflicts=('bsnes' 'snesfilter')

build() {
    # Filters/Shaders fix
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes/ui/general"
    sed -i 's/application->basepath/"\/usr\/share\/bsnes\/"/g' main-window.cpp

    # Cheats fix
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes/ui/tools"
    sed -i 's/application->path("cheats.xml")/"\/usr\/share\/bsnes\/cheats.xml"/g' cheat-database.cpp

    # Profile
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    msg "Please select a profile"
    select bp in "Accuracy (Slower)" "Compatibility" "Performance (Faster)"; do
        case $bp in
            "Accuracy (Slower)" ) break;;
            "Compatibility" ) sed -i 's/accuracy/compatibility/g' Makefile; break;;
            "Performance (Faster)" ) sed -i 's/accuracy/performance/g' Makefile; break;;
        esac
    done

    # Compile bsnes
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    make compiler=gcc platform=x phoenix=gtk

    # Compile snespurify
    cd "${srcdir}/bsnes_v$pkgver-source/snespurify"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh

    # Compile filters
    cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
    make compiler=gcc

    # Compile bps
    cd "${srcdir}/bps"
    sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
    ./cc-gtk.sh
}

package() {
    cd "${srcdir}/bsnes_v$pkgver-source/bsnes"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' ui/Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bsnes_v$pkgver-source/snespurify"
    install -Dm755 snespurify-gtk "${pkgdir}/usr/bin/snespurify-gtk"

    cd "${srcdir}/bsnes_v$pkgver-source/snesfilter"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bsnes_v$pkgver-source/snesshader"
    sed -i 's/~\/.config/$(DESTDIR)$(prefix)\/share/g' Makefile
    make install DESTDIR="${pkgdir}" prefix=/usr

    cd "${srcdir}/bps"
    install -Dm755 bps-gtk "${pkgdir}/usr/bin/bps-gtk"
}
