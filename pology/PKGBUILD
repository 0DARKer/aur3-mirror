# Maintainer: Gilrain <pierre.buard+aur gmail com>
pkgname=pology
pkgver=0.11
pkgrel=2
pkgdesc='A framework for custom processing of PO files.'
arch=('any')
license=('GPL3')
url='http://techbase.kde.org/Localization/Tools/Pology'
depends=('python2' 'gettext')
makedepends=('cmake' 'libxml2' 'docbook-xsl' 'epydoc')
optdepends=('python2-dbus: to communicate with external applications'
	    'python2-pyenchant: spell checking functionality (needs a dictionary)'
	    'hunspell-lang: a spell-checking dictionary for your language(s)'
	    'aspell-lang: a spell-checking dictionary for your language(s)'
	    'ispell-lang: a spell-checking dictionary for your language(s)'
	    'languagetool: used by the check-grammar sieve'
	    'apertium: used by the pomtrans script'
	    'git: for processing files under version control'
	    'subversion: for processing files under version control')
conflicts=('pology-svn')
source=(http://pology.nedohodnik.net/release/${pkgname}-${pkgver}.tar.bz2)
md5sums=('508855b119beb1bf5cc4ad9e49e7625c')
changelog=changelog
options=(!makeflags)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' $(find ${srcdir} -name '*.py')

  mkdir -p build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DDOCBOOK_XSL_DIR=/usr/share/xml/docbook/xsl-stylesheets-1.78.0/
  make PREFIX=/usr
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/build
  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -d ${pkgdir}/usr/share/apps/katepart/syntax
  ln -s /usr/share/pology/syntax/kate/synder.xml ${pkgdir}/usr/share/apps/katepart/syntax/synder.xml

  install -d ${pkgdir}/etc/bash_completion.d
  ln -s /usr/share/pology/completion/bash/pology ${pkgdir}/etc/bash_completion.d/pology
}
