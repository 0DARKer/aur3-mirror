# Maintainer: Michael Goehler <somebody.here@gmx.de>

pkgname=synergy-fixed
_pkgname=synergy
pkgver=1.5.1
_pkgver=2398
pkgrel=1
pkgdesc="Share a single mouse and keyboard between multiple computers (nulljobbuildup fixed)"
url="http://synergy-foss.org"
arch=('i686' 'x86_64')
depends=('gcc-libs' 'libxtst' 'libxinerama' 'crypto++')
makedepends=('libxt' 'cmake' 'qt5-base' 'unzip')
optdepends=('qt5-base: gui support')
license=('GPL2')
provides=("synergy=${pkgver}")
conflicts=('synergy')
source=("http://synergy-project.org/files/packages/${_pkgname}-${pkgver}-r${_pkgver}-Source.tar.gz"
        "libcryptopp.patch"
        "socketmultiplexer.patch"
        "synergys.socket"
        "synergys.service")

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}-Source"

  # use system wide crypto++ library (thanks gentoo)
  patch -Np1 <"${srcdir}/libcryptopp.patch"

  # http://synergy-foss.org/spit/issues/details/2935/
  patch -Np1 <"${srcdir}/socketmultiplexer.patch"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_FLAGS="${CXXFLAGS} -pthread" .
  make -j1

  cd src/gui
  qmake
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}-Source/bin"

  # install binary
  install -Dm755 synergy "${pkgdir}/usr/bin/synergy"
  install -Dm755 synergyc "${pkgdir}/usr/bin/synergyc"
  install -Dm755 synergys "${pkgdir}/usr/bin/synergys"

  # install config
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}-Source/doc/${_pkgname}.conf.example" "${pkgdir}/etc/${_pkgname}.conf.example"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}-Source/doc/${_pkgname}.conf.example-advanced" "${pkgdir}/etc/${_pkgname}.conf.example-advanced"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}-Source/doc/${_pkgname}.conf.example-basic" "${pkgdir}/etc/${_pkgname}.conf.example-basic"

  # install manfiles
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}-Source/doc/${_pkgname}c.man" "${pkgdir}/usr/share/man/man1/${_pkgname}c.1"
  install -Dm644 "${srcdir}/${_pkgname}-${pkgver}-Source/doc/${_pkgname}s.man" "${pkgdir}/usr/share/man/man1/${_pkgname}s.1"

  # install systemd service and socket
  install -d "${pkgdir}/usr/lib/systemd/system"
  install -Dm644 "${srcdir}/synergys.service" "$pkgdir/usr/lib/systemd/system/"
  install -Dm644 "${srcdir}/synergys.socket" "$pkgdir/usr/lib/systemd/system/"

  # install desktop/icon stuff
  cd ../res
  install -Dm644 "synergy.ico" "${pkgdir}/usr/share/icons/${_pkgname}.ico"
  install -Dm644 "synergy.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
}

md5sums=('5437d64af1114f61cca0cbebb16d0db8'
         'ced0b8c2d543a1e57e2e267fda57f4d9'
         'd0e1d68da5d651e323efd05fa132d250'
         '58f48336836d6faf3d5eecbe4155b77e'
         'b95e4b83d8a19c0bd81a15280078fcd5')
