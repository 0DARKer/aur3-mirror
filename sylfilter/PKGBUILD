# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=sylfilter
pkgver=0.7
pkgrel=1
pkgdesc="A generic message filter library and command-line tools."
arch=(i686 x86_64)
url="http://sylpheed.sraoss.jp/$pkgname/"
license=('BSD')
depends=('glib2' 'sqlite3')
options=('!libtool')
source=("${url}src/$pkgname-$pkgver.tar.gz")
md5sums=('8445ec4306c0ee4755c5fb4195941f7e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # missing config.h fix (temporary, sent patch upstream)
  sed -i '11i#include "config.h"' lib/filters/textcontent-filter.c
  sed -i '14i#include "config.h"' lib/filters/bayes-filter.c
  sed -i '8i#include "config.h"' lib/filter-utils.c

  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
