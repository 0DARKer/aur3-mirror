# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=dinit-git
pkgver=20110423
pkgrel=1
pkgdesc="lightweight base init for mkinitcpio"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/dinit"
license=('MIT')
depends=('mkinitcpio' 'util-linux')
makedepends=('git')
conflicts=('dinit')
provides=('dinit')

_gitroot="git://github.com/falconindy/dinit.git"
_gitname="dinit"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  install -d "$pkgdir/lib/initcpio/install"
  install -m644 -t "$pkgdir/lib/initcpio/install" uubase ubase
  install -m755 init "$pkgdir/lib/initcpio/dinit"
}

# vim: ft=sh syn=sh et
