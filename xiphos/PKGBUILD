# Maintainer: Menche <menche_mt at yahoo dot com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: TripleE <eric1548@yahoo.com>

pkgname=xiphos
pkgver=3.1.6
pkgrel=2
pkgdesc="Bible study tool for GTK+"
arch=('x86_64' 'i686')
url="http://xiphos.org/"
license=('GPL')
depends=('webkitgtk2' 'libgsf' 'libglade' 'gtkhtml' 'sword')
makedepends=('gnome-doc-utils' 'intltool' 'python2')
conflicts=('gnomesword')
provides=('gnomesword')
replaces=('gnomesword')
install=xiphos.install
source=("http://downloads.sourceforge.net/gnomesword/$pkgname-$pkgver.tar.gz")
sha256sums=('21f0024d57b3976526093279ef7ab7f3ed8afe07436ab983bd3049a14c3bdfe5')

build() {
  cd "$pkgname-$pkgver"

  sed -i '0,/python/s//python2/' waf
  ./waf configure --gtk=2 --prefix=/usr
  ./waf build
}

package() {
  cd "$pkgname-$pkgver"

  ./waf install --destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
