# Maintainer: Menche <menche_mt at yahoo dot com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: TripleE <eric1548@yahoo.com>

pkgname=xiphos
pkgver=3.2.1
pkgrel=1
pkgdesc="Bible study tool for GTK+"
arch=('x86_64' 'i686')
url="http://xiphos.org/"
license=('GPL')
depends=('webkitgtk' 'libgsf' 'libglade' 'gtkhtml4' 'sword' 'docbook2x')
makedepends=('waf' 'gnome-doc-utils' 'intltool' 'python2')
conflicts=('gnomesword')
provides=('gnomesword')
replaces=('gnomesword')
install=xiphos.install
source=("http://downloads.sourceforge.net/gnomesword/$pkgname-$pkgver.tar.gz")
sha256sums=('6bf5562a44ef03757d81ba16c12036bd2e28a18e08ef61ddb941ab845c18ce75')

build() {
  cd "$pkgname-$pkgver"

  sed -i '0,/python/s//python2/' waf
  ./waf configure --gtk=3 --prefix=/usr
  ./waf build
}

package() {
  cd "$pkgname-$pkgver"

  ./waf install --destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
