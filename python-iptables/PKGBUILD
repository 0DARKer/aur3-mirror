# Contributor: Lex Black <autumn-wind at web dot de>

pkgbase=python-iptables
pkgname=('python-iptables' 'python2-iptables')
pkgver=0.5.0
pkgrel=1
pkgdesc="Python bindings for iptables"
arch=('i686' 'x86_64')
url="https://github.com/ldx/python-iptables"
license=('Apache')
depends=('iptables')
makedepends=('python' 'python2')
source=("https://pypi.python.org/packages/source/p/$pkgbase/$pkgbase-$pkgver.tar.gz")
md5sums=('22a88010ed6b247d25b42676b13dc2b5')


prepare() {
  cp -r "$srcdir/$pkgbase-$pkgver" "$srcdir/$pkgbase-$pkgver-py2"
}

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py build

  cd "$srcdir/$pkgbase-$pkgver-py2"
  python2 setup.py build
}

package_python-iptables() {
  depends+=('python')

  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir/" --prefix=/usr --optimize=1
}

package_python2-iptables() {
  depends+=('python2')

  cd "$srcdir/$pkgbase-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=1
}

# vim:set ts=2 sw=2 et:
