# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Boyan Ding <stu_dby@126.com>

pkgname=gnome-terminal-transparent
_realname=gnome-terminal
pkgver=3.12.3
pkgrel=1
pkgdesc="The GNOME Terminal Emulator - Applied patch to allow transparent background"
arch=(i686 x86_64)
license=(GPL)
provides=('gnome-terminal')
conflicts=('gnome-terminal')
depends=(vte3 gsettings-desktop-schemas dconf)
makedepends=(gnome-doc-utils intltool itstool docbook-xsl desktop-file-utils gconf
             libnautilus-extension appdata-tools gnome-shell)
optdepends=('gconf: settings migration when upgrading from older version')
options=('!emptydirs')
url="http://www.gnome.org"
groups=('gnome')
install=gnome-terminal-transparent.install
source=(http://ftp.gnome.org/pub/gnome/sources/$_realname/${pkgver:0:4}/$_realname-$pkgver.tar.xz
        'restore-transparency.patch')
sha256sums=('8450da259fb485289d61fdbe5b59037be068e0dbdd5c7dd8216b60cd49d55528'
	    '26be51a762c7c4efbec81d63f5c4309c23caed84697a506093bba08055f74f9b')

prepare() {
  cd $_realname-$pkgver
  patch -Np1 -i ../restore-transparency.patch
}

build() {
  cd $_realname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static --with-nautilus-extension
  make
}

check() {
  cd $_realname-$pkgver
  make check
}

package() {
  cd $_realname-$pkgver
  make DESTDIR="$pkgdir" install
}
