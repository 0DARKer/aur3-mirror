# $Id$
# Contributor: webjdm <web.jdm@gmail.com>
# Maintainer: Aurélien DESBRIÈRES <ice.cube@gmx.com>

pkgname=libgnutls13
_pkgname=gnutls
pkgver=2.0.4
pkgrel=2
pkgdesc="Ancient gnutls with libgnutls.so.13 library"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
options=('!libtool')
depends=('gcc-libs' 'libtasn1' 'readline' 'zlib' 'libgcrypt')
source=("ftp://ftp.gnupg.org/gcrypt/gnutls/gnutls-$pkgver.tar.bz2"{,.sig}
	"http://archive.ubuntu.com/ubuntu/pool/main/g/gnutls13/gnutls13_2.0.4-1ubuntu2.6.diff.gz")
sha256sums=('af07625c701f4d3b2e44b6cb1f9fcf55f8b45942abdf88cfa61a95ca30d6f91f'
	'3458adcde097d858604bff980d0fbeb919df81c1e28388605d3ba28c99406d43'
	'c20746491c6b42400213ddde48033870b9444844bd4c1609ab1c6328977e4368')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # http://packages.ubuntu.com/hardy/libgnutls13

  patch -Np2 -i "$srcdir/gnutls13_2.0.4-1ubuntu2.6.diff"

  for p in patches/* ; do
	  msg2 "Patch: ${p}"
	  patch < "${p}" -p1
  done
  
  ./configure --prefix=/usr --with-zlib --disable-guile \
	  --disable-static --enable-shared --with-pic --enable-ld-version-script
  make V=1

}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  mkdir -p -m 0755 "$pkgdir/tmp" "$pkgdir/usr/lib"
  make DESTDIR="$pkgdir/tmp" install

  cp -va "$pkgdir"/tmp/usr/lib/*13* "$pkgdir/usr/lib"
  rm -r "$pkgdir/tmp"
}
