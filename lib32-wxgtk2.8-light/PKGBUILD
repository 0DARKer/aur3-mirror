# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=lib32-wxgtk2.8-light
pkgver=2.8.12.1
pkgrel=2
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!) (lib32)"
arch=('x86_64')
url="http://wxwidgets.org/"
license=('custom:wxWindows')
depends=('lib32-gtk2' 'lib32-libsm' 'lib32-sdl' 'lib32-libxxf86vm')
makedepends=('lib32-gtk2' 'lib32-mesa' 'lib32-glu' 'lib32-libxt')
provides=('lib32-wxgtk2.8=2.8.12.1')
conflicts=('lib32-wxgtk2.8')
#source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2")
source=("http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2"
        "wxGTK-collision.patch")
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5'
          '21860e53f83f72acd4c184643dc59c9da07f2582')

prepare() {
  rm -fr build
  cp -R "wxPython-src-${pkgver}" build
  cd build
  patch -Np1 -i ../wxGTK-collision.patch
}

build() {
  cd "build"

# Multilib
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,opengl,regex=builtin,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode} \
              --disable-{optimize,mediactrl,precomp-headers,webview}
  make
  make -C locale allmo
  make -C contrib/src
}

package() {
  cd build

# Install
  make DESTDIR="${pkgdir}" install
  make -C contrib/src DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}

# License
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/licence.txt"
}
