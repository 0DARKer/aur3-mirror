# Maintainer: Chris Brannon <chris@the-brannons.com>
pkgname=daemonize
pkgver=1.6.2
pkgrel=1
pkgdesc="Run a program as a Unix daemon"
arch=(i686 x86_64)
url="http://bmc.github.com/daemonize/"
license=('BSD')
depends=(glibc)
makedepends=(python-markdown)
source=("$pkgname-$pkgver.tar.gz::https://github.com/bmc/$pkgname/tarball/release-$pkgver")
md5sums=('25766858df08ab8a1fa6f3f1a058d427')

build() {
  local _dirname=$(tar tf "$srcdir/$pkgname-$pkgver.tar.gz" | sed 1q);
  cd "$srcdir/$_dirname"
  ./configure --prefix=/usr
  make
  make FAQ.html
}

package() {
  local _dirname=$(tar tf "$srcdir/$pkgname-$pkgver.tar.gz" | sed 1q);
  cd "$srcdir/$_dirname"
  make DESTDIR="$pkgdir/" install
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 FAQ.html CHANGELOG.md README.md "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
