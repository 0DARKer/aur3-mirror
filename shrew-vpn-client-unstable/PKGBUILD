# Contributor: Christian Hesse <mail@earthworm.de>

pkgname=shrew-vpn-client-unstable
pkgver=2.2.0~beta~1
pkgrel=1
pkgdesc="A portable VPN client for Linux with Qt GUI - development version"
arch=('i686' 'x86_64')
url="http://www.shrew.net/"
license=('osi')
depends=('qt' 'openssl')
makedepends=('gcc' 'flex' 'libedit' 'bison' 'cmake')
optdepends=('openldap')
provides=('shrew-vpn-client')
conflicts=('shrew-vpn-client' 'shrew-vpn-client-alpha')
install=('shrew-vpn-client.install')
backup=('etc/iked.conf')
source=(http://www.shrew.net/download/ike/ike-${pkgver//\~/-}.tbz2
	adapter.patch
	ikea-qt.desktop
	iked)
md5sums=('4b02b25755753de8cafc216d8bec5023'
         'ff7aff658a5c014150db18a602e70dfc'
         '52de3fa5ca5ab7fcbfc90b2679189981'
         '1b7a27d1042e6a0f4a342fb20e93baeb')

build() {
  cd $srcdir/ike

  # fix build error
  patch -Np1 < $srcdir/adapter.patch

  # Build the whole package
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DQTGUI=YES -DETCDIR=/etc -DNATT=YES
  make all
  make DESTDIR="$pkgdir/" install
  
  # Install the daemon script
  install -D -m755 $srcdir/iked $pkgdir/etc/rc.d/iked

  # The configuration file is already ready for use; just rename it
  mv $pkgdir/etc/iked.conf.sample $pkgdir/etc/iked.conf

  # Copy our desktop files
  install -D -m644 $srcdir/ike/source/qikea/png/ikea.png $pkgdir/usr/share/icons/ikea.png
  install -D -m755 $srcdir/ikea-qt.desktop $pkgdir/usr/share/applications/ikea-qt.desktop
}
