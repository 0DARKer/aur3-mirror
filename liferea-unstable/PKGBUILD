# Contributor: Leonid Selivanov <bravebug at gmail dot com>

pkgname=liferea-unstable
pkgver=1.9.0
pkgrel=2
pkgdesc="A GTK desktop news aggregator for online news feeds and weblogs - Unstable"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf>=2.26.0' 'libwebkit3' 'libnotify>=0.4.5' 'libxslt>=1.1.24' 'gtk3>=2.91.4' 'sqlite3>=3.6.15' 'libsoup>=2.28.2' 'libunique3' 'json-glib')
makedepends=('pkgconfig' 'diffutils' 'intltool')
optdepends=()
options=('!libtool' '!emptydirs')
conflicts=('liferea' 'liferea-dev')
provides=('liferea')
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz)
md5sums=('e34b13730121b768c387b7de07b08793')

build() {
  cd "${srcdir}/liferea-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-install --with-gtk=3.0 --enable-libnotify=yes
  make
}

package() {
  cd "${srcdir}/liferea-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
