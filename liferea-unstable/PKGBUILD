# Contributor: Leonid Selivanov <bravebug at gmail dot com>

pkgname=liferea-unstable
pkgver=1.8rc2
pkgrel=2
pkgdesc="A GTK desktop news aggregator for online news feeds and weblogs - Unstable"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf>=2.26.0' 'libwebkit>=1.2.2' 'libnotify>=0.4.5' 'libxslt>=1.1.24' 'gtk2>=2.18.0' 'sqlite3>=3.6.15' 'libsoup>=2.28.2' 'libunique' 'json-glib')
makedepends=('pkgconfig' 'diffutils' 'intltool')
optdepends=()
options=('!libtool' '!emptydirs')
conflicts=('liferea' 'liferea-dev')
provides=('liferea')
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-1.8-RC2.tar.gz)
md5sums=('c7496cb096be0289843a5e72c2d3dc34')

build() {
  cd "${srcdir}/liferea-1.8-RC2"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-install
  make
}

package() {
  cd "${srcdir}/liferea-1.8-RC2"
  make DESTDIR="${pkgdir}" install
  
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
