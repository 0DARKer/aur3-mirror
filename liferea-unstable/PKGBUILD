# Contributor: Leonid Selivanov <bravebug at gmail dot com>

pkgname=liferea-unstable
pkgver=1.7.4
pkgrel=2
pkgdesc="A GTK desktop news aggregator for online news feeds and weblogs - Unstable"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf>=2.26.0' 'libwebkit>=1.1.10' 'libnotify>=0.4.5' 'libxslt>=1.1.24' 'gtk2>=2.18.0' 'sqlite3>=3.6.15' 'libsoup>=2.28.2' 'libunique')
makedepends=('pkgconfig' 'diffutils' 'intltool')
optdepends=()
options=('!libtool' '!emptydirs')
conflicts=('liferea' 'liferea-dev')
provides=('liferea')
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz
        libnotify-0.7.patch)
md5sums=('69ece60acc93a58cffb66e0e4f753704'
         'c6bb9248bdf99afe96d7fdb944d8d808')

build() {
  cd "${srcdir}/liferea-${pkgver}"
  patch -Np1 -i "$srcdir/libnotify-0.7.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var\
    --disable-schemas-install --disable-nm
  make
}

package() {
  cd "${srcdir}/liferea-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
