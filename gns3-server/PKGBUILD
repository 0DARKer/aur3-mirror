# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>

pkgname=gns3-server
pkgver=1.3.0
pkgrel=1
pkgdesc='GNS3 network simulator. Server package.'
arch=('any')
url='https://github.com/GNS3/gns3-server'
license=('GPL3')
makedepends=('python-setuptools')
depends=('python-netifaces' 'python-jsonschema' 'python-dateutil' 'python-aiohttp' 'python-jinja' 'python-raven')
optdepends=('gns3-gui: graphical user interface for GNS3 server.'
'vboxwrapper: VirtualBox wrapper for GNS3.'
'iouyap: Bridge IOU to UDP, TAP and Ethernet.'
'qemu: Used by GNS3 to run Cisco ASA, PIX and IDS.'
'vpcs: Simple PC emulation for basic network operations.'
)
install="${pkgname}.install"
md5sums=('dbeb780a4d7bc48632bd49670c97aff7'
         '623a3ee6b18b30d7bc38812798a8beb5'
         'f602390385890dab14f68e5e0a8cac2d')

source=("https://github.com/GNS3/${pkgname}/archive/v${pkgver}.tar.gz"
        "https://github.com/GNS3/${pkgname}/commit/b2d8a8d810debe6ee6a8948302c992f970117312.patch"
        "${pkgname}@.service")

prepare() {
 cd ${srcdir}/${pkgname}-${pkgver}
 patch -Np1 -i ../b2d8a8d810debe6ee6a8948302c992f970117312.patch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/${pkgname}@.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
}

# vim:set ts=2 sw=2 et:
