# Maintainer: korjjj <korjjj+aur[at]gmail[dot]com>

pkgname=gns3-server
pkgver=1.1
pkgrel=1
pkgdesc='GNS3 network simulator, the server.'
arch=('any')
url='https://github.com/GNS3/gns3-server'
license=('GPL3')
makedepends=('python-setuptools')
depends=('python-pyzmq' 'python-tornado' 'python-jsonschema')
optdepends=('gns3-gui: graphical interface for the GNS3 server.'
'vboxwrapper: VirtualBox wrapper for GNS3.'
'iouyap: Bridge IOU to UDP, TAP and Ethernet.'
'qemu: Used by GNS3 to run Cisco ASA, PIX and IDS.'
'vpcs: Simple PC emulation for basic network operations.'
'python-netifaces: Host network interface information.'
)
install="${pkgname}.install"
md5sums=('b5dfad2ea851346149ced0ec88969858'
         'f602390385890dab14f68e5e0a8cac2d')

source=("https://github.com/GNS3/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}@.service")

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/${pkgname}@.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
}

# vim:set ts=2 sw=2 et:
