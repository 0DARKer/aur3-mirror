# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=xtrojka
pkgver=1.2.3
pkgrel=1
pkgdesc="A X11 version of the old Tetris-like game called Trojka"
arch=('i686' 'x86_64')
url="http://www.happypenguin.org/show?XTrojka"
license=('custom')
depends=('libxaw')
install="${pkgname}.install"
source=("http://www.ibiblio.org/pub/Linux/games/arcade/tetris/${pkgname}${pkgver//./}.tar.gz"
        "${pkgname}.patch")
md5sums=('58f66c2e59205312af4dcd128a6a4040'
         '9bb83bde873a11bdbbb63ddb262f9a6e')

build() {
  cd "${srcdir}/${pkgname}${pkgver//./}"
  patch -p0 -i "../${pkgname}.patch"
  cp XTrojka.uk XTrojka
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}${pkgver//./}"

  # Install game files
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -dm775 -g games "${pkgdir}/var/lib/${pkgname}"

  # Install man page and license
  install -Dm644 "${pkgname}.man" "${pkgdir}/usr/share/man/man6/${pkgname}.6"
  install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
}
