# Creator: Micael Dias <kam1kaz3@gmail.com>

pkgname=monav-client-svn
pkgver=480
pkgrel=1
pkgdesc="MoNav is a Desktop / Mobile application that offers state-of-the-art fast and exact routing with OpenStreetMap Data."
arch=('i686' 'x86_64')
url="http://code.google.com/p/monav/"
license=('GPLv3')
depends=('qt' 'qt-mobility')
makedepends=('subversion' 'gcc')
conflicts=('monav-client')
provides=("monav-client")

_svnmod="monav-client"
_svntrunk=https://monav.googlecode.com/svn/trunk/

build()
{
	cd "${srcdir}"

	# get most recent data from svn
	if [ -d $_svnmod/.svn ]; then
		(cd $_svnmod && svn up -r $pkgver)
	else
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
	fi

	msg "SVN checkout done or server timeout"
	msg "Starting qmake..."

	# remove previous build data
	if [ -d $_svnmod-build ];then
		rm -rf $_svnmod-build
	fi

	# copy files to build dir
	cp -r $_svnmod $_svnmod-build
	cd $_svnmod-build

	# hack to force the client config to output to correct prefix
	echo "target.path=/usr/bin" >> ./client/client.pro

	# generate make files
	qmake CONFIG+=release monavclient.pro || return 1

	msg "Compiling..."

	# make
	make || return 1
}

package()
{
	cd "$srcdir"/$_svnmod-build
	make INSTALL_ROOT="${pkgdir}" install || return 1
}
