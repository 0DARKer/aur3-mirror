# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Jaroslav Lichtblau <tu@dragonlord.cz>
# Contributor: Michael Liptrap <xenobrain@gmail.com

pkgname=pcgen
pkgver=5.16.4
pkgrel=1
pkgdesc="An open source RPG character generator and maintenance program"
arch=('any')
url="http://pcgen.sourceforge.net/"
license=('LGPL' 'custom')
depends=('java-runtime')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}${pkgver//./}_full.zip"
        "${pkgname}.sh"
        "${pkgname}.png"
        "${pkgname}.desktop")
md5sums=('9c0c0694bb5d81743261dfa31b1a9bc5'
         '24ac58619b0f435ea032aef2c0cdee56'
         '74f84ab162a7c9237ae3395300659dab'
         '811ec47d177116db1d46bb1a24912641')

package() {
  cd "${srcdir}/${pkgname}${pkgver//./}"

# Install program files
  rm -f *.bat *.exe
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -rf * "${pkgdir}/opt/${pkgname}"

# Set file permissions
  find "${pkgdir}/opt/${pkgname}" -type d -exec chmod 755 '{}' \;
  find "${pkgdir}/opt/${pkgname}" -type f -exec chmod 644 '{}' \;
  chmod 755 "${pkgdir}/opt/${pkgname}/"*.sh
  chmod 666 "${pkgdir}/opt/${pkgname}/"*.{ini,properties}

# Install launcher
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

# Install icon
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

# Install .desktop file
  install -Dm644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

# Install licenses
  cd docs/acknowledgments
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 {ogllicense,publishersandsources,skinlflicense}.html \
    "${pkgdir}/usr/share/licenses/${pkgname}"
}
