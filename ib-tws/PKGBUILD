# Maintainer: Ben Alex <ben.alex@acegi.com.au>

# See https://github.com/benalexau/ib-tws for AUR maintenance helper

pkgname=ib-tws
pkgver=936.9
pkgrel=1
pkgdesc='Electronic trading platform from discount brokerage firm Interactive Brokers'
arch=('any')
url="http://interactivebrokers.com/"
license=('custom')
depends=(java-environment bash)
makedepends=(imagemagick)

source=('LICENSE'
	'ib-tws'
	'ib-tws.desktop'
	'http://download2.interactivebrokers.com/download/unixmacosx.jar')

md5sums=('e1cae2de592add7133bb08123e8db1ad'
         'c407d22dfafdb33ec2fe97eeb5e9f0aa'
         '1d87e86be21ab10cd7e37cbde3798fe1'
         '6ca4c298145d725622df00a838b730da')

build() {
	# Thanks to http://finance.groups.yahoo.com/group/TWSAPI/files/RPM%20spec%20file/
	cd ${srcdir}/IBJts
	jar xf jts.jar trader/common/images/ibapp_icon_48x48.gif
	convert trader/common/images/ibapp_icon_48x48.gif ${pkgname}.png
	convert trader/common/images/ibapp_icon_48x48.gif -resize 66.666% ${pkgname}-32x32.png
	convert trader/common/images/ibapp_icon_48x48.gif -resize 33.333% ${pkgname}-16x16.png
	convert trader/common/images/ibapp_icon_48x48.gif ${pkgname}-48x48.png
}

package() {
	cd ${srcdir}
	mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	install -Dm755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}
	install -Dm644 ${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
	cd ${srcdir}/IBJts
	mkdir -p ${pkgdir}/usr/share/pixmaps/
	install -Dm644 *.png "${pkgdir}/usr/share/pixmaps/"
	mkdir -p ${pkgdir}/usr/share/java/${pkgname}
	install -Dm644 *.jar ${pkgdir}/usr/share/java/${pkgname}
	install -Dm644 *.ini ${pkgdir}/usr/share/java/${pkgname}
}

