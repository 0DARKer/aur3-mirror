# Maintainer: ValHue <vhuelamo at gmail dot com>
# https://github.com/ValHue/AUR-PKGBUILDs
#
# Contributor: Moritz LÃ¼decke <ritze at skweez dot net>
# Contributor: boenki <boenki at gmx dot de>

pkgname="cover-thumbnailer"
pkgver=0.8.3
pkgrel=3
pkgdesc="A Python script which displays music album covers in nautilus, preview of pictures which are in a folder and more."
arch=('any')
url="http://software.flogisoft.com/cover-thumbnailer/en/"
license=('GPL3')
depends=('python2-pillow' 'python2-gconf')
makedepends=('gettext')
install="${pkgname}.install"
source=("http://projects.flogisoft.com/cover-thumbnailer/download/${pkgname}_${pkgver}_src.tar.gz")
md5sums=('993d38410104b15e41b1b08fa423b332')

build() {
	cd "${srcdir}/${pkgname}_${pkgver}_src"

    ### Python2 Fix ###
	sed -i "s|#!/usr/bin/python|#!/usr/bin/python2|g" ${pkgname}{,-gui}.py

    ### Python2-pillow (PIL) Fix ###
    sed -i "/import Image/i\from PIL import Image" ${pkgname}.py
	sed -i "s|import Image, urllib|import urllib|g" ${pkgname}.py
	sed -i "s|!=|is not|g" ${pkgname}.py

    ### Thumbnailers cache Fix (Gnome 3.12) ###
	sed -i "s|.thumbnails|.cache/thumbnails|g" ${pkgname}-gui.py
}

package() {
	cd "${srcdir}/${pkgname}_${pkgver}_src"
	./install.sh -p ${pkgdir}
}
