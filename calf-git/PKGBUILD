# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=calf-git
pkgver=0.0.19.r2061.8162e6a
pkgrel=1
pkgdesc="LV2/JACK audio plug-ins for musicians"
arch=('i686' 'x86_64')
url="http://calf.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('lv2' 'fluidsynth' 'fftw' 'lash')
makedepends=('git')
provides=('calf')
conflicts=('calf')
install="$pkgname.install"
source=("$pkgname::git+git://repo.or.cz/calf.git")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  echo $(awk -F '[][]' '/AC_INIT/{print $4}' configure.ac).r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname
  ./autogen.sh --prefix="/usr" \
               --enable-static=no \
               --enable-experimental \
               --enable-sse
  make
}

package(){
  cd $pkgname
  make DESTDIR="$pkgdir/" install
}
