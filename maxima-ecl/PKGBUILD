# $Id: PKGBUILD 150832 2012-02-23 12:01:17Z juergen $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>
# Modified to compile against ecl by: maribu

pkgname=maxima-ecl
provides=('maxima')
_pkgname=maxima
pkgver=5.26.0
pkgrel=2
pkgdesc="Maxima compiled against ecl - works not only on x86 but also on arm"
arch=('arm' 'i686' 'x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('ecl' 'texinfo' 'sh')
makedepends=('python2')
optdepends=('gnuplot: plotting capabilities' 'rlwrap: readline support via /usr/bin/rmaxima' 'tk: graphical xmaxima interface')
# needs rebuild when bash changes version
# needs a rebuild when sbcl changes version
options=('!makeflags' '!zipman') # don't zip info pages or they won't work inside maxima
install=maxima.install
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz" "$_pkgname.desktop" "xerprn.patch")
sha1sums=('bc7448486478b217c333605c6d8f6c0bbdd3526c'
          '4398ebb1ec85ccfa12f37516a56d60c26f74b18b'
          'cf2e27a66a114d60f11cea827d7cdfa8aca6d36c')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  # apply a patch to succesfully compile against ecl
  patch src/numerical/slatec/xerprn.lisp -i ../../xerprn.patch

  # set correct python executable to create docs
  sed -i "s|${PYTHONBIN:-python}|python2|" doc/info/extract_categories.sh

  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
	--libexecdir=/usr/lib --enable-ecl --with-default-lisp=ecl
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/${_pkgname}.desktop \
  	${pkgdir}/usr/share/applications/${_pkgname}.desktop

  # make sure, we have a nice icon for the desktop file at the right place ;)
  install -d ${pkgdir}/usr/share/pixmaps/
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png \
	${pkgdir}/usr/share/pixmaps/${_pkgname}.png
}
