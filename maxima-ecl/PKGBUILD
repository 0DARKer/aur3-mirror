# $Id: PKGBUILD 142503 2011-11-10 20:08:23Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>
# Modified by: maribu
pkgname=maxima-ecl
origname=maxima
provides=('maxima')
conflicts=('maxima')
pkgver=5.25.1
pkgrel=1
pkgdesc="Maxima - a sophisticated computer algebra system; compiled against Embeddable Common Lisp, so it does compile on ARM port of Arch Linux"
arch=('arm' 'i686' 'x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('ecl' 'texinfo' 'sh')
makedepends=('python2')
optdepends=('gnuplot: plotting capabilities' 'rlwrap: readline support via /usr/bin/rmaxima' 'tk: graphical xmaxima interface')
# needs rebuild when bash changes version
# needs a rebuild when sbcl changes version
options=('!makeflags' '!zipman') # don't zip info pages or they won't work inside maxima
install=maxima.install
source=("http://downloads.sourceforge.net/sourceforge/$origname/$origname-$pkgver.tar.gz" "$origname.desktop")
sha1sums=('9c9ea66434c9ca96549092c3640c3ba5a4fba44b'
          '4398ebb1ec85ccfa12f37516a56d60c26f74b18b')

build() {
  cd ${srcdir}/${origname}-${pkgver}

  # set correct python executable to create docs
  sed -i "s|${PYTHONBIN:-python}|python2|" doc/info/extract_categories.sh

  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
        --libexecdir=/usr/lib --enable-ecl --with-default-lisp=ecl

  make
}

package() {
  cd ${srcdir}/${origname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/${origname}.desktop \
        ${pkgdir}/usr/share/applications/${origname}.desktop

  # make sure, we have a nice icon for the desktop file at the right place ;)
  install -d ${pkgdir}/usr/share/pixmaps/
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png \
        ${pkgdir}/usr/share/pixmaps/${origname}.png
}

