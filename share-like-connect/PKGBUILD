# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=share-like-connect
pkgver=0.2
pkgrel=1
pkgdesc="A simple ubiquitous user interface for interacting with your data"
arch=('i686' 'x86_64')
url='http://plasma-active.org/'
license=('LGPL')
depends=('kdebase-runtime' 'kactivities')
makedepends=('cmake' 'automoc4')
groups=('plasma-active')
source=("http://download.kde.org/stable/active/2.0/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('432df8b41c6bcf807f3db66c7b676139')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="${pkgdir}" install
}
