# Maintainer: Dobroslaw Kijowski [dobo] <dobo90_at_gmail.com>

pkgname=lazpaint-gtk2
_pkgname=lazpaint
pkgver=5.9b
_inetver=0.6.6
_bgrabitmapver=7.5
_bgracontrolsver=3.2-20140421
pkgrel=2
pkgdesc='Image editor, like PaintBrush or Paint.Net, written in Lazarus (Free Pascal).'
arch=(i686 x86_64)
url='http://sourceforge.net/projects/lazpaint/'
license=(GPL3 LGPL)
depends=(gtk2 desktop-file-utils)
makedepends=(lazarus unzip p7zip)
provides=(${_pkgname})
conflicts=(${_pkgname}-qt4)
install=${_pkgname}.install
source=(http://downloads.sourceforge.net/project/${_pkgname}/src/${_pkgname}${pkgver}_src.zip
        http://downloads.sourceforge.net/project/${_pkgname}/src/inet${_inetver}.zip
        http://downloads.sourceforge.net/project/${_pkgname}/src/bgrabitmap${_bgrabitmapver}.zip
        http://downloads.sourceforge.net/project/bgra-controls/bgra-controls-3.0/bgra-controls-${_bgracontrolsver}.7z
        ${_pkgname}.desktop
        ${_pkgname}.png
        language_path_fix.patch)
md5sums=(d5d3c5bf6e580ab9f639ccb3c8071bb7
         63e9ae30886fdd3ee309a12bdc0ff613
         b90b67e32de3cc23b727c90f3b7a6f7e
         71441f01656ce3e033a0d9e7a7d4f626
         e6f4f8c4d9456b21ed4a25d7b775fec4
         8f26062ad1e3222d375201d96341716c
         04c16c130c5f649a92403d616b85eb22)
_ws=gtk2

prepare() {
  cd ${srcdir}

  patch -p0 < language_path_fix.patch

  mkdir config
}

build() {
  cd ${srcdir}/inet${_inetver}/lazaruspackage
  lazbuild lnetvisual.lpk --lazarusdir=/usr/lib/lazarus --widgetset=${_ws} \
    --primary-config-path=${srcdir}/config
  cd ${srcdir}/bgrabitmap${_bgrabitmapver}
  lazbuild bgrabitmappack.lpk --lazarusdir=/usr/lib/lazarus --widgetset=${_ws} \
    --primary-config-path=${srcdir}/config
  cd ${srcdir}/bgra-controls-${_bgracontrolsver}
  lazbuild bgracontrols.lpk --lazarusdir=/usr/lib/lazarus --widgetset=${_ws} \
    --primary-config-path=${srcdir}/config
  cd ${srcdir}/${_pkgname}
  lazbuild ${_pkgname}.lpi --lazarusdir=/usr/lib/lazarus --widgetset=${_ws} \
    --primary-config-path=${srcdir}/config --build-mode=Release
}

package() {
  cd ${srcdir}/${_pkgname}/release

  install -D -m 755 ${_pkgname} ${pkgdir}/usr/bin/${_pkgname}
  install -d -m 755 ${pkgdir}/usr/share/${_pkgname}/i18n
  install -D -m 644 i18n/*.po ${pkgdir}/usr/share/${_pkgname}/i18n

  install -D -m 644 ${srcdir}/${_pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${_pkgname}.desktop
  install -D -m 644 ${srcdir}/${_pkgname}.png \
    ${pkgdir}/usr/share/pixmaps/${_pkgname}.png
}
