# Contributor: Egor Vakhromtsev <e.vakhromtsev gmail.com>
# Maintainer: Andrea Fagiani <andfagiani_at_gmail_dot_com>

pkgname=gmate-git
pkgver=20110303
pkgrel=2
pkgdesc="Set of plugins for Gedit including improvements for gtksourceview2 language files."
arch=(any)
url="http://github.com/gmate/gmate"
license=('GPL3')
depends=('gedit' 'pywebkitgtk' 'pyinotify' 'shared-mime-info' 'python2-gconf')
makedepends=('git')
install=$pkgname.install
source=(gmate-activate)
md5sums=('a3f48a1750dc5cfaef8fc5c0f57cc075')

_gitroot=http://github.com/gmate/gmate.git
_gitname=gmate

build() {
  cd $srcdir
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf $srcdir/$_gitname-build
  git clone $srcdir/$_gitname $srcdir/$_gitname-build

#INFO: this is python2 stuff!
  cd $srcdir/$_gitname-build
  for file in $(find . -name '*.py' -print); do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done

}

package() {
  cd $srcdir/$_gitname-build

# Copy Gmate executable
  install -Dm755 gmate.py $pkgdir/usr/lib/gedit-2/gmate/gmate.py
#INFO: make symlink here instead of post_install!
  mkdir -p $pkgdir/usr/bin
  ln -s /usr/lib/gedit-2/gmate/gmate.py $pkgdir/usr/bin/gmate

# Register rails-related mime types
  install -Dm644 mime/rails.xml $pkgdir/usr/share/mime/packages/rails.xml

# Copy language definitions
  mkdir -p $pkgdir/usr/share/gtksourceview-2.0/language-specs/
  install -m644 lang-specs/*.lang -t $pkgdir/usr/share/gtksourceview-2.0/language-specs/

# Copy Tags
  mkdir -p $pkgdir/usr/share/gedit-2/plugins/taglist/
  install -m644 tags/*.tags.gz -t $pkgdir/usr/share/gedit-2/plugins/taglist/

# Copy Snippets
  mkdir -p $pkgdir/usr/share/gedit-2/plugins/snippets/
  install -m644 snippets/* -t $pkgdir/usr/share/gedit-2/plugins/snippets/

# Copy Plugins
  mkdir -p $pkgdir/usr/lib/gedit-2/plugins/
  cp -R plugins/* $pkgdir/usr/lib/gedit-2/plugins/

# Copy Styles
  mkdir -p $pkgdir/usr/share/gtksourceview-2.0/styles
  install -m644 styles/* -t $pkgdir/usr/share/gtksourceview-2.0/styles/

  install -Dm755 $srcdir/gmate-activate $pkgdir/usr/bin/gmate-activate
}
