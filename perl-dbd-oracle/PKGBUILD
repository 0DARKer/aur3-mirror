# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>

_perlmod=DBD-Oracle
_modnamespace=DBD
pkgname=perl-dbd-oracle
pkgver=1.68
pkgrel=2
pkgdesc="DBD::Oracle - Oracle database driver for the DBI module"
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}"
license=('GPL' 'PerlArtistic')
depends=('oracle-instantclient-basic>=10.1' 'perl-dbi>=1.51')
makedepends=('oracle-instantclient-sdk>=10.1' 'perl-data-dumper')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('d26b762edcab505fdea838307d1ff476fe4fed78d50770f3867cfd6df94ec474d45d57df84a1090c9e0fccad7a1d02c7d4f19534668eaefc8fa65cbb7160fa2d')

build() {
	cd "${srcdir}/${_perlmod}-${pkgver}"

	#
	# How much of this stuff is still necessary?
	#
	if [[ -z ${ORACLE_HOME} ]]; then
		if [[ $(pacman -Qq oracle &>/dev/null) ]]; then
			export ORACLE_HOME="$(ls -dr /home/oracle/app/oracle/product/*)/orarch"
			export LD_LIBRARY_PATH="${LD_LIBRARY_PATH}:${ORACLE_HOME}/lib"
			_version=$(pacman -Qq oracle 2>/dev/null)
		else
			error "No Oracle / Instant Client installation found."
		fi
	fi
	# AUR workaround
	(( 1 )) && _version=${_version##* }
	# You may need to put your version of Oracle's Instant Client below.
	[[ -z ${_version} ]] && _version=12.1.0.1.0

	PERL_MM_USE_DEFAULT=1 perl Makefile.PL -V ${_version} INSTALLDIRS=vendor -l
	make
}

check() {
	cd "${srcdir}/${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}/"
}
