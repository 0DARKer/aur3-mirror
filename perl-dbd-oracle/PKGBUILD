# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>

_perlmod=DBD-Oracle
pkgname=perl-dbd-oracle
pkgver=1.50
pkgrel=1
pkgdesc="DBD::Oracle - Oracle database driver for the DBI module"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
depends=('oracle-instantclient-basic>=9.2' 'perl>=5.10.0' 'perl-dbi')
makedepends=('oracle-instantclient-sdk>=9.2' 'perl-extutils-makemaker')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/DBD/$_perlmod-$pkgver.tar.gz")
sha512sums=('80b532b7d57d083e3fc99f854eedc763fadd97697370e57be5d7710e73ba3f42ad043bf4547a001bffa0207cc110c2e19bfb2de9c4365fc18068f93b03998942')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  #
  # How much of this stuff is still necessary?
  #
  if [[ -z $ORACLE_HOME ]]; then
    if [[ $(pacman -Qq oracle &>/dev/null) ]]; then
      export ORACLE_HOME="$(ls -dr /home/oracle/app/oracle/product/*)/orarch"
      export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$ORACLE_HOME/lib"
      _version=$(pacman -Qq oracle 2>/dev/null)
    elif [[ -d /opt/instantclient ]]; then
      export ORACLE_HOME=/opt/instantclient
      _version=$(pacman -Q oracle-instantclient-basic{,lite} 2>/dev/null || true)
    else
      error "No Oracle / Instant Client installation found."
    fi
  fi
  # AUR workaround
  (( 1 )) && _version=${_version##* }
  # You may need to put your version of Oracle's Instant Client below.
  [[ -z $_version ]] && _version=11.2.0.3.0

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -V $_version INSTALLDIRS=vendor -l
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
