# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>
pkgname=perl-dbd-oracle
pkgver=1.38
pkgrel=2
pkgdesc="DBD::Oracle - Oracle database driver for the DBI module"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DBD-Oracle/"
license=('GPL' 'PerlArtistic')
depends=('instantclient-basic' 'perl-dbi')
# Requires Test::Simple, but that is not packaged on Arch at the moment
makedepends=('instantclient-sdk' 'perl-extutils-makemaker')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/P/PY/PYTHIAN/DBD-Oracle-$pkgver.tar.gz)
sha512sums=('62704ebe6752041b8df9ae0f14cea43193c1732c2f607c3edffe1c8dfd6feb833ef19b51deedecc88a3f482ca49263182d474887fbdab7c024a8a6ade9f395ac')

build() {
  cd "$srcdir/DBD-Oracle-$pkgver"

  if [[ -z $ORACLE_HOME ]]; then
    if [[ $(pacman -Q oracle &>/dev/null) ]]; then
      export ORACLE_HOME="$(ls -dr /home/oracle/app/oracle/product/*)/orarch"
      export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$ORACLE_HOME/lib"
      _version=$(pacman -Q oracle 2>/dev/null)
    elif [[ -d /opt/instantclient ]]; then
      export ORACLE_HOME=/opt/instantclient
      _version=$(pacman -Q instantclient-basic{,lite} 2>/dev/null || true)
    else
      error "No Oracle / Instant Client installation found."
    fi
  fi
  # AUR workaround
  (( 1 )) && _version=${_version##* }
  [[ -z $_version ]] && _version=8.0.0.0

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -V $_version INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/DBD-Oracle-$pkgver"
  make test
}

package() {
  cd "$srcdir/DBD-Oracle-$pkgver"
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}

# vim:set ts=2 sw=2 et:
