# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>

pkgname=perl-dbd-oracle
_pkgname=DBD-Oracle
pkgver=1.44
pkgrel=1
pkgdesc="DBD::Oracle - Oracle database driver for the DBI module"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/$_pkgname"
license=('GPL' 'PerlArtistic')
depends=('oracle-instantclient-basic>=9.2' 'perl>=5.10.0' 'perl-dbi')
makedepends=('oracle-instantclient-sdk>=9.2' 'perl-extutils-makemaker')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/P/PY/PYTHIAN/$_pkgname-$pkgver.tar.gz)
sha512sums=('9ff1963b7841706116d16dcb622f3129fb63338e05a2e377d8d2050e3afcf935d493e82877f9bf4932028e78d2fa4d95eb3f87eb4ef86287e8abee60d0d96088')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # How much of this stuff is still necessary?
  if [[ -z $ORACLE_HOME ]]; then
    if [[ $(pacman -Qq oracle &>/dev/null) ]]; then
      export ORACLE_HOME="$(ls -dr /home/oracle/app/oracle/product/*)/orarch"
      export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$ORACLE_HOME/lib"
      _version=$(pacman -Qq oracle 2>/dev/null)
    elif [[ -d /opt/instantclient ]]; then
      export ORACLE_HOME=/opt/instantclient
      _version=$(pacman -Q oracle-instantclient-basic{,lite} 2>/dev/null || true)
    else
      error "No Oracle / Instant Client installation found."
    fi
  fi
  # AUR workaround
  (( 1 )) && _version=${_version##* }
  [[ -z $_version ]] && _version=8.0.0.0

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -V $_version INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
