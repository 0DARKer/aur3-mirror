# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>

pkgname=perl-dbd-oracle
_pkgname=DBD-Oracle
pkgver=1.46
pkgrel=1
pkgdesc="DBD::Oracle - Oracle database driver for the DBI module"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/$_pkgname"
license=('GPL' 'PerlArtistic')
depends=('oracle-instantclient-basic>=9.2' 'perl>=5.10.0' 'perl-dbi')
makedepends=('oracle-instantclient-sdk>=9.2' 'perl-extutils-makemaker')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/P/PY/PYTHIAN/$_pkgname-$pkgver.tar.gz")
sha512sums=('17eb4517f52911f3660e420b9f39c439c4186c950fe34ec38d3631931f5331f25347e05f4b84938cb5554b90ebc4bd4e8e756d1b59e097dcfa76629e7fdacbb6')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  #
  # How much of this stuff is still necessary?
  #
  if [[ -z $ORACLE_HOME ]]; then
    if [[ $(pacman -Qq oracle &>/dev/null) ]]; then
      export ORACLE_HOME="$(ls -dr /home/oracle/app/oracle/product/*)/orarch"
      export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$ORACLE_HOME/lib"
      _version=$(pacman -Qq oracle 2>/dev/null)
    elif [[ -d /opt/instantclient ]]; then
      export ORACLE_HOME=/opt/instantclient
      _version=$(pacman -Q oracle-instantclient-basic{,lite} 2>/dev/null || true)
    else
      error "No Oracle / Instant Client installation found."
    fi
  fi
  # AUR workaround
  (( 1 )) && _version=${_version##* }
  # You may need to put your version of Oracle's Instant Client below.
  #[[ -z $_version ]] && _version=8.0.0.0
  [[ -z $_version ]] && _version=11.2.0.3.0

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -V $_version INSTALLDIRS=vendor -l
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
