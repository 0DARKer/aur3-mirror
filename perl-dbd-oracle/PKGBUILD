# Maintainer: xduugu
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>
pkgname=perl-dbd-oracle
pkgver=1.24b
pkgrel=1
pkgdesc="Oracle database driver for the DBI module"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DBD-Oracle/"
license=('GPL' 'PerlArtistic')
depends=('instantclient-basic' 'perl-dbi')
makedepends=('instantclient-sdk')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/P/PY/PYTHIAN/DBD-Oracle-$pkgver.tar.gz)
md5sums=('f3855675d6ebfccce3113dc838110520')
sha256sums=('f4d35d0db47c687be67ec16de2b64c6b03b7b0aa80c078a1ec38f0934f5ac089')

build() {
	cd "$srcdir/DBD-Oracle-1.24"

	if [[ -z $ORACLE_HOME ]]; then
		if [[ $(pacman -Q oracle &>/dev/null) ]]; then
			export ORACLE_HOME="$(ls -dr /home/oracle/app/oracle/product/*)/orarch"
			export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$ORACLE_HOME/lib"
			_version=$(pacman -Q oracle 2>/dev/null)
		elif [[ -d /opt/instantclient ]]; then
			export ORACLE_HOME=/opt/instantclient
			_version=$(pacman -Q instantclient-basic{,lite} 2>/dev/null || true)
		else
			error "No Oracle / Instantclient installation found."
		fi
	fi
	# AUR workaround
	(( 1 )) && _version=${_version##* }
	[[ -z $_version ]] && _version=8.0.0.0

	PERL_MM_USE_DEFAULT=1 perl Makefile.PL -V $_version INSTALLDIRS=vendor
	make
}

package() {
	cd "$srcdir/DBD-Oracle-1.24"
	make DESTDIR="$pkgdir" install

	find "$pkgdir" -name '.packlist' -delete
	find "$pkgdir" -name '*.pod' -delete
}
