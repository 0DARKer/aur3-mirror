# Maintainer: GordonGR <gordongr@freemail.gr>
# Contributor: Splex

pkgname=teapotviewer
pkgver=0.2.0.0
_pngver=1.5.1
pkgrel=1
pkgdesc="An Open Source third party viewers for Second LifeÂ® (secondlife) and OpenSim (opensimulator) grids."
url="http://bitbucket.org/ArminW/teapot/"
license=('LGPL')
install=teapotviewer.install
arch=('i686' 'x86_64')



if [ "$CARCH" = "i686" ]; then
	depends=('apr-util' 'freealut' 'gtk2' 'libgl' 'libidn' 'libjpeg7' 'mesa' 'nss' 'sdl')
	optdepends=('libpulse: for PulseAudio support' 'alsa-lib: for ALSA support' 'nvidia-utils: for NVIDIA support')
	_arch=$CARCH
	source=("https://bitbucket.org/ArminW/teapot/downloads/Teapot-${pkgver}-Linux-${_arch}.tar.bz2" \
	"http://downloads.sourceforge.net/sourceforge/libpng/libpng-${_pngver}.tar.xz" \
	'teapotviewer.desktop' \
	'teapotviewer.launcher')
	sha256sums=('7803d425be680c9973334ebce2de621a4648abfa39f653a048628276e3222bb7'
	'd230837ad959b395bf7e2937e0136e6037208a29cda83000bf8b88a3692e8c69'
	'b8d927793acf47c8a87657ce078e1954575fb4c6840cc7645e79842a3b01da5c'
	'f8d0c024eddcdbe7d019bfaa748fc202af3af480d8e9c93f3516c268bbe771aa')

elif [ "$CARCH" = "x86_64" ]; then
	depends=('apr-util' 'freealut' 'gtk2' 'libgl' 'lib32-libidn' 'libjpeg7' 'mesa' 'nss' 'sdl' 'lib32-freealut' 'lib32-util-linux' 'lib32-zlib')
	optdepends=('lib32-libpulse: for PulseAudio support'
				'lib32-alsa-lib: for ALSA support'
				'lib32-nvidia-utils: for NVIDIA support')
	_arch=$CARCH
	source=("https://bitbucket.org/ArminW/teapot/downloads/Teapot-${pkgver}-Linux-${_arch}.tar.bz2" \
	"http://downloads.sourceforge.net/sourceforge/libpng/libpng-${_pngver}.tar.xz" \
	'teapotviewer.desktop' \
	'teapotviewer.launcher')
	sha256sums=('3c0c1237c38fc1c2c7178ae467e0d5a25a5e60ff99ba7772ce9a2ebeee901b70'
	'd230837ad959b395bf7e2937e0136e6037208a29cda83000bf8b88a3692e8c69'
	'b8d927793acf47c8a87657ce078e1954575fb4c6840cc7645e79842a3b01da5c'
	'f8d0c024eddcdbe7d019bfaa748fc202af3af480d8e9c93f3516c268bbe771aa')
fi



build(){
if [ "$CARCH" = "i686" ]; then
	export CC="gcc"
	export CXX="g++"
	export PKG_CONFIG_PATH="/usr/lib/pkgconfig"

elif [ "$CARCH" = "x86_64" ]; then
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

fi

cd "${srcdir}/libpng-${_pngver}"
./configure
make
}


package() {
  cd $srcdir
  
  # Rename Data Directory
  mv Teapot-${pkgver}-Linux-$CARCH teapotviewer
  
  # Install Desktop File
  install -D -m644 $srcdir/teapotviewer.desktop \
    $pkgdir/usr/share/applications/teapotviewer.desktop
  
  # Install Icon File
  install -D -m644 $srcdir/teapotviewer/teapot_icon.png \
    $pkgdir/usr/share/pixmaps/teapotviewer.png
  
  # Install Launcher
  install -D -m755 $srcdir/teapotviewer.launcher \
    $pkgdir/usr/bin/teapotviewer

  # Move Data to Destination Directory
  install -d $pkgdir/opt
  mv teapotviewer $pkgdir/opt/

  # Install libpng we built

if [ "$CARCH" = "i686" ]; then
	install -D -m755 $srcdir/libpng-${_pngver}/.libs/libpng15.so.15.1.0 \
	$pkgdir/opt/teapotviewer/lib
	cd $pkgdir/opt/teapotviewer/lib
	ln -s libpng15.so.15.1.0 libpng15.so.15
	ln -s libpng15.so.15.1.0 libpng15.so

elif [ "$CARCH" = "x86_64" ]; then
	install -D -m755 $srcdir/libpng-${_pngver}/.libs/libpng15.so.15.1.0 \
	$pkgdir/opt/teapotviewer/lib32
	cd $pkgdir/opt/teapotviewer/lib32
	ln -s libpng15.so.15.1.0 libpng15.so.15
	ln -s libpng15.so.15.1.0 libpng15.so

fi
}
