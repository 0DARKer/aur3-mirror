pkgname=ppcrossarm
pkgver=2.6.2
pkgrel=1
pkgdesc="Free Pascal cross compiler for ARM"
arch=(i686 x86_64)
url="http://www.freepascal.org"
license=("GPL" "LGPL" "custom")
depends=("fpc=2.6.2")
makedepends=("fpc=2.6.2")
source=("ftp://ftp.freepascal.org/pub/fpc/dist/$pkgver/source/fpcbuild-$pkgver.tar.gz")
md5sums=('89c7e60db6280f3d5cc006a4a9ff43a9')

_optimal_make_jobs() {
  if [ -r /proc/cpuinfo ]; then
    local core_count=$(grep -Fc processor /proc/cpuinfo)
  else
    local core_count=0
  fi
  if [ $core_count -gt 1 ]; then
    echo -n $[$core_count-1]
  else
    echo -n 1
  fi
}

build() {
  cd "$srcdir/fpcbuild-$pkgver/fpcsrc/compiler"
  fpcmake -Tall
  make -j$(_optimal_make_jobs) OPT="-O2 -dFPC_ARMEL" arm
}

package() {
  install -Dm755 "${srcdir}/fpcbuild-$pkgver/fpcsrc/compiler/ppcarm" "$pkgdir/usr/lib/fpc/$pkgver/ppcrossarm"
  mkdir -p "$pkgdir/usr/bin"
  cd "$pkgdir/usr/bin"
  ln -s "/usr/lib/fpc/$pkgver/ppcrossarm" .
}
