# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
pkgname=kernel26-tools
pkgver=2.6.39.2
pkgrel=1
pkgdesc="Tools embedded with Linux kernel $pkgver (e.g: perf)"
license=('GPL2')
arch=('i686' 'x86_64')
url='http://www.kernel.org'
options=(!strip)
depends=('python2' 'perl')
makedepends=('asciidoc' 'xmlto')
optdepends=('libnewt' 'slang')
source=( "http://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$pkgver.tar.bz2" ) 
md5sums=('51be93d92028658ec93f68b79a378b17')

build() {
  # firewire
  cd "${srcdir}/linux-$pkgver/tools/firewire"
  make

  # slabinfo
  cd "${srcdir}/linux-$pkgver/tools/slub"
  gcc -Wall -O2 -g slabinfo.c -o slabinfo
  
  # usb
  cd "${srcdir}/linux-$pkgver/tools/usb"
  make

  # perf
  cd "${srcdir}/linux-$pkgver/tools/perf"
  # fix arch python means python 3
  sed -ri 's/python-config/python2-config/' Makefile
  sed -ri '174 s/python/python2/' Makefile 
  make DESTDIR="${pkgdir}/usr" all man
}

package() {
  # firewire
  cd "${srcdir}/linux-$pkgver/tools/firewire"
  install -m755 -D nosy-dump ${pkgdir}/usr/bin/nosy-dump

  # slabinfo
  cd "${srcdir}/linux-$pkgver/tools/slub"
  install -m755 -D slabinfo ${pkgdir}/usr/bin/slabinfo

  # usb
  cd "${srcdir}/linux-$pkgver/tools/usb"
  install -m755 -D testusb ${pkgdir}/usr/bin/testusb

  # perf
  cd "${srcdir}/linux-${pkgver}/tools/perf"
  make DESTDIR="${pkgdir}/usr" install install-man
}
# vim:set ts=2 sw=2 ft=sh et:
