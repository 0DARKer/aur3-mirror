# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
pkgname=kernel26-tools
pkgver=2.6.38
pkgrel=1
pkgdesc="Performance analysis tools for Linux $pkgver"
license=('GPL2')
arch=('i686' 'x86_64')
url='http://www.kernel.org'
options=(!strip)
depends=('python2' 'perl')
makedepends=('asciidoc' 'xmlto')
optdepends=('libnewt' 'slang')
source=( "http://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$pkgver.tar.bz2" ) 
md5sums=('7d471477bfa67546f902da62227fa976')

build() {
  cd "${srcdir}/linux-$pkgver/tools/perf"
  # fix arch python means python 3
  sed -ri 's/python-config/python2-config/' Makefile
  make DESTDIR="${pkgdir}/usr" all man
}

package() {
  cd "${srcdir}/linux-$pkgver/tools/perf"
  make DESTDIR="${pkgdir}/usr" install install-man
}
# vim:set ts=2 sw=2 ft=sh et:
