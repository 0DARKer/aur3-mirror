# Maintainer: Diego Principe <cdprincipe@at@gmail@dot@com>

_pkgname=focuswriter
pkgname=focuswriter-git
pkgver=v1.3.90.135.ga4caafb
pkgrel=1
pkgdesc="A simple fullscreen word processor"
arch=('i686' 'x86_64')
url="http://gottcode.org/${_pkgname}/"
license=('GPL3')
if [ -e /usr/bin/qmake-qt5 ] || [ ! -e /usr/bin/qmake-qt4 ]; then
  depends=('qt5-base' 'enchant' 'libzip')
else
  depends=('qt4' 'enchant' 'libzip')
fi
optdepends=('sdl_mixer: typewriter sound effects')
install=${pkgname}.install
source=(git://github.com/gottcode/focuswriter.git)
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  if [ -e /usr/bin/qmake-qt5 ]; then
    qmake-qt5 PREFIX=/usr
  else
    qmake-qt4 PREFIX=/usr
  fi
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make INSTALL_ROOT="${pkgdir}/" install
}
