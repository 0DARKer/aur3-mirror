# $Id: pkgbuild-mode.el,v 1.23 2007/10/20 16:02:14 juergen Exp $
# Maintainer: Yuki Chiba <yuki.from.akita@gmail.com>

pkgname=emacs-twittering-mode-git
pkgver=v0.9.0.902.g398048e
pkgrel=1
pkgdesc="Twittering-mode is an Emacs major mode for Twitter"
arch=('any')
url="http://twmode.sourceforge.net/index.html"
license=('GPL')
depends=('emacs' 'curl')
makedepends=('git')
optdepends=('gnupg: For keeping an OAuth authorized token in a local storage encrypted with master password')
provides=( 'emacs-twittering-mode' )
install=emacs-twittering-mode-git.install
source=('twittering-mode::git+https://github.com/hayamiz/twittering-mode.git')
md5sums=('SKIP')

_gitname='twittering-mode'

pkgver() {
  cd "$srcdir/$_gitname"
	local ver="$(git describe --long)"
	printf "%s" "${ver//-/.}"
}

build() {
  cd "$srcdir/$_gitname"
  emacs -q -batch -f batch-byte-compile twittering-mode.el
}

package() {
  cd "$srcdir/$_gitname"
  install -d -m 755 "$pkgdir"/usr/share/emacs/site-lisp
  install -c -m 644 twittering-mode.el "$pkgdir"/usr/share/emacs/site-lisp
  install -c -m 644 twittering-mode.elc "$pkgdir"/usr/share/emacs/site-lisp

  install -d -m 755 "$pkgdir"/usr/share/doc/emacs-twittering-mode-git
  install -c -m 644 README.markdown "$pkgdir"/usr/share/doc/emacs-twittering-mode-git
  install -c -m 644 README.ja "$pkgdir"/usr/share/doc/emacs-twittering-mode-git
}

# vim:set ts=2 sw=2 et:
