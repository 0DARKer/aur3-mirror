# Maintainer: Jameson Pugh <imntreal@gmail.com>

pkgname=openjk-git
pkgver=ec40d79
pkgrel=1
pkgdesc="Open source version of Jedi Knight Acadamy"
arch=('i686' 'x86_64')
url="https://github.com/Razish/OpenJK"
license=('GPL')
depends=('lib32-libgl' 'lib32-openal' 'lib32-zlib' 'lib32-sdl2-hg')
optdepends=('lib32-libpng' 'lib32-libjpeg')
makedepends=('cmake' 'yasm')
provides=(openjk)
conflicts=(openjk)
source=("openjk::git://github.com/Razish/OpenJK.git")
sha256sums=('SKIP')

pkgver() {
  cd openjk
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "${srcdir}/openjk"

  mkdir build
  cd build
  cmake ../ \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_CXX_FLAGS=-m32 \
		-DCMAKE_C_FLAGS=-m32 \
		-DCMAKE_SHARED_LINKER_FLAGS=-m32 \
		-DCMAKE_SIZEOF_VOID_P=4
  make
}

package() {
  cd "${srcdir}/openjk/build"

  make DESTDIR="${pkgdir}" install
}

# vim: set ts=2 sw=2 ft=sh noet:
