# Maintainer: JÃ¶rg Hansen (joerg dot hansen at posteo dot de)

pkgname=osm2pgsql-git
pkgver=0.87.1.r30.ged86d63
pkgrel=1
pkgdesc="Utility program that converts OpenStreetMap (.OSM) data into a format that can be loaded into PostgreSQL."
arch=(i686 x86_64)
url="http://wiki.openstreetmap.org/wiki/Osm2pgsql"
license=('GPL')
depends=('postgis' 'libxml2' 'bzip2' 'zlib' 'protobuf-c' 'boost')
makedepends=('git' 'libtool')
source=('git+https://github.com/openstreetmap/osm2pgsql.git')
md5sums=('SKIP')

pkgver() {

    cd "$srcdir/osm2pgsql"
    (   set -o pipefail
        git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {

    cd "$srcdir/osm2pgsql"

    msg "Configuring..."
    ./autogen.sh
    LDFLAGS='' ./configure --prefix=/usr

    msg "Compiling..."
    make

}

package() {

    msg "Installing..."
    cd "$srcdir/osm2pgsql"
    make DESTDIR="$pkgdir" install

    msg "Additional files..."
    mkdir -p ${pkgdir}/usr/share/doc/osm2pgsql
    install -m644 README ${pkgdir}/usr/share/doc/osm2pgsql
    install -m644 900913.sql ${pkgdir}/usr/share/osm2pgsql

    msg "Cleaning up..."
    find $pkgdir -type f -name "*\.la" -exec rm {} \;

}
