# Maintainer: Lukas Weber <laochailan@web.de>
pkgname=taisei
pkgver=1.0a
pkgrel=1
pkgdesc="Open source  Touhou clone."
arch=(i686 x86_64)
url="http://taisei-project.org/"
license=('MIT')
depends=('sdl_ttf' 'hicolor-icon-theme' 'mesa' 'libpng' 'freealut')
makedepends=('git' 'cmake')
install=taisei.install
source=()
md5sums=() 

_tgitroot=http://github.com/laochailan/taisei.git
_tgitname=taisei
_gittag=v$pkgver

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_tgitname" ]]; then
    cd "$_tgitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_tgitroot" "$_tgitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_tgitname-build"
  git clone "$srcdir/$_tgitname" "$srcdir/$_tgitname-build"
  cd "$srcdir/$_tgitname-build"
  git checkout $_gittag
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_tgitname-build/build"
  make DESTDIR="$pkgdir/" install

  mkdir -p $pkgdir/usr/share/licenses/$pkgname
  cp ../COPYING $pkgdir/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
