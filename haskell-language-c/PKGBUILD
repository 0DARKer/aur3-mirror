#Maintainer: bjoern lindig <bjoern dot lindig at googlemail dot com>
# custom variables
_hkgname=language-c
_licensefile=LICENSE

# PKGBUILD options/directives
pkgname=haskell-language-c
pkgver=0.4.4
pkgrel=1
pkgdesc="Analysis and generation of C code"
url="http://www.sivity.net/projects/language.c/"
license=("BSD3")
arch=('i686' 'x86_64')
makedepends=()
depends=( 
	 "ghc"
         "sh"
         "happy"
         "alex"
         "haskell-syb"
	 )
options=('strip' 'staticlibs')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('a05c5b1b9077cfd5a14e14e8c38bc224d6bbf125c5a2c973db7d3b96656cca5d')

# PKGBUILD functions
build() {
    cd "$srcdir/$_hkgname-$pkgver"
    
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "$srcdir/$_hkgname-$pkgver"
    install -D -m744 register.sh   "$pkgdir/usr/share/haskell/$pkgname/register.sh"
    install    -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
    install -d -m755 "$pkgdir/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/$pkgname/html" "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f "$pkgdir/usr/share/doc/$pkgname/$_licensefile"
}
