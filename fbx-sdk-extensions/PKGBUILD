#Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname='fbx-sdk-extensions'
pkgver=2014.2
pkgrel=1
pkgdesc='Platform and API toolkit to transfer existing content into the FBX format, extensions.'
arch=('any')
url='http://www.autodesk.com/products/fbx/overview'
license=('custom')
depends=('fbx-sdk')
install='fbx-sdk-extensions.install'
source=("http://images.autodesk.com/adsk/files/fbx${pkgver/./}_fbxextensionssdk_linux.tar.gz")
md5sums=('62b30b4239d669cde93d33fcd8d2e4b7')

build() {
  cd "$srcdir"

  rm -rf "fbx-sdk"
  mkdir "fbx-sdk"
  chmod +x "./fbx${pkgver/./}_fbx_extension_sdk_linux"
  printf "yes\nn\n" | "./fbx${pkgver/./}_fbx_extension_sdk_linux" "$srcdir/fbx-sdk"
}

package() {
  cd "$srcdir/fbx-sdk"

  install -D "readme.txt" "$pkgdir/usr/share/doc/fbx-sdk/extensions_readme.txt"
  cp -r plugins "$pkgdir/usr/share/doc/fbx-sdk/extensions_plugins"
  install -D "License.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

