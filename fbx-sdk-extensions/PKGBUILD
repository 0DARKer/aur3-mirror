#Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname='fbx-sdk-extensions'
pkgver=2014.2.1
pkgrel=1
pkgdesc='Platform and API toolkit to transfer existing content into the FBX format, extensions.'
arch=('any')
url='http://www.autodesk.com/products/fbx/overview'
license=('custom')
depends=('fbx-sdk')
install='fbx-sdk-extensions.install'
source=("http://images.autodesk.com/adsk/files/fbx${pkgver:0:4}${pkgver:5:1}_${pkgver:7:1}_fbxextensionssdk_linux.tar.gz")
md5sums=('385687b3360844b16df4139cb29f9a02')

build() {
  cd "$srcdir"

  rm -rf "fbx-sdk"
  mkdir "fbx-sdk"
  chmod +x "./fbx${pkgver:0:4}${pkgver:5:1}_${pkgver:7:1}_fbx_extension_sdk_linux"
  printf "yes\nn\n" | "./fbx${pkgver:0:4}${pkgver:5:1}_${pkgver:7:1}_fbx_extension_sdk_linux" "$srcdir/fbx-sdk"
}

package() {
  cd "$srcdir/fbx-sdk"

  install -D "readme.txt" "$pkgdir/usr/share/doc/fbx-sdk/extensions_readme.txt"
  cp -r plugins "$pkgdir/usr/share/doc/fbx-sdk/extensions_plugins"
  install -D "License.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

