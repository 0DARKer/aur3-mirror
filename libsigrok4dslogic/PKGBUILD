# Maintainer: Thomas Krug <t.krug@elektronenpumpe.de>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>

pkgname=libsigrok4dslogic
pkgver=0.4
pkgrel=3
pkgdesc="core library for the DreamSourceLab logic analyzer (based on libsigrok)"
arch=('i686' 'x86_64')
url="http://www.dreamsourcelab.com/"
license=('GPL3')
depends=('libzip' 'libserialport' 'libusb')
makedepends=('boost')
source=("http://www.dreamsourcelab.com/download/DSLogic-v${pkgver}.tar.gz"
        'rpath.patch')
md5sums=('a90d8d4c0c601d9688ab03a772d80f8b'
         'be267fcf22e54e18e72a4a231aa03b93')

prepare() {
  cd "$srcdir/DSLogic-v0.4/libsigrok4DSLogic"

  patch -p1 -i "$srcdir/rpath.patch"
}

build() {
  cd "$srcdir/DSLogic-v0.4/libsigrok4DSLogic"

  ./autogen.sh
  ./configure --prefix=/usr --exec-prefix=/usr
  make
}

package() {
  cd "$srcdir/DSLogic-v0.4/libsigrok4DSLogic"

  make DESTDIR="$pkgdir" PREFIX=/usr install
}

# vim:set ts=2 sw=2 et:
