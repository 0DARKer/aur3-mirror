# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: Lucky <archlinux@builds.lucky.li>

pkgname=ruby-nanoc
_gemname="${pkgname#ruby-}"
pkgver=3.6.4
pkgrel=1
pkgdesc="A simple, flexible static site generator written in Ruby."
url="http://nanoc.ws"
license=("MIT")
arch=("any")
depends=("ruby" "ruby-cri")
optdepends=(
    "ruby-adsf: Provides 'nanoc view' command."
    "ruby-listen: Provides 'nanoc watch' command."
)
provides=("ruby-nanoc3")
conflicts=("ruby-nanoc3")
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
md5sums=('cb9db6ab64403aa637eb2e16591ab17c')
sha1sums=('a45e4be669e552969bf879e3253bfc4887b2e6b8')

package() {
  cd "${srcdir}"
  export HOME="/tmp"
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"

  gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"
}

# vim:set ts=2 sw=2 et:
