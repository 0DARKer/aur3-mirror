# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Contributor:  Ichimonji10 <ichimonji10@gmail.com>

pkgname=ruby-nanoc
_gemname="${pkgname#ruby-}"
pkgver=3.6.2
pkgrel=1
pkgdesc="A simple but very flexible static site generator written in Ruby."
url="http://nanoc.stoneship.org"
license=("MIT")
arch=("any")
depends=("ruby" "ruby-cri")
optdepends=("ruby-adsf: Provides 'nanoc view' command.")
provides=("ruby-nanoc3")
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
md5sums=("086f9d277d73df51125ec79b55001426")
sha1sums=("f6fdc10b4627175f52c1e88e5629aaeda0a6e087")

package() {
  cd "${srcdir}"
  export HOME="/tmp"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"
}
