# Maintainer: George Eleftheriou <eleftg>
# Contributor: George Eleftheriou <eleftg>
pkgname=scotch
pkgver=6.0.3
_downloadnum=34099  # gforge is insane
pkgrel=1
pkgdesc="a software package and libraries for graph, mesh and hypergraph partitioning, static mapping, and sparse matrix block ordering. This is the all-inclusive version (MPI/serial/esmumps)."
url="http://www.labri.fr/perso/pelegrin/scotch/"
license=("CeCILL-C free/libre software license")
depends=('zlib' 'openmpi')
provides=('ptscotch-openmpi' 'ptscotch-mpich2'
          'scotch_esmumps5')
conflicts=('ptscotch-openmpi' 'ptscotch-mpich2'
           'scotch_esmumps5')
arch=('x86_64')
source=("https://gforge.inria.fr/frs/download.php/file/${_downloadnum}/scotch_${pkgver}.tar.gz")
sha256sums=('6461cc9f28319a9dbe6cc10e28c0cbe90b4b25e205723c3edcde9a3ff974d6d8')

prepare() {
  cd ${srcdir}/scotch_${pkgver}/src

  [ -e Makefile.inc ] && rm Makefile.inc
  cp Make.inc/Makefile.inc.x86-64_pc_linux2.shlib Makefile.inc
  sed -i "s/-O3/${CFLAGS} -fPIC/g" Makefile.inc
  sed -i "s/CCD\t.*=.*gcc/CCD = mpicc/" Makefile.inc
}

build() {
  cd ${srcdir}/scotch_${pkgver}/src

  make scotch
  make ptscotch
  export MAKEFLAGS=-j1
  make esmumps
  make ptesmumps
}

package() {
  cd ${srcdir}/scotch_${pkgver}
  install -d ${pkgdir}/usr/{bin,lib,include/scotch}
  install -m 644 -t ${pkgdir}/usr/include/scotch include/*.h
  install -m 644 -t ${pkgdir}/usr/lib lib/lib*.so
  install -m 755 -t ${pkgdir}/usr/bin bin/*
  install -m 644 -D ${srcdir}/scotch_${pkgver}/doc/CeCILL-C_V1-en.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
