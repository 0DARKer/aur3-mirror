# Submitter: George Eleftheriou <eleftg>
# Maintainer: George Eleftheriou <eleftg>
# Contributor: eolianoe <eolianoe>
 
pkgname=scotch
pkgver=6.0.3
_downloadnum=34099  # gforge is insane
pkgrel=3
pkgdesc="Software package and libraries for graph, mesh and hypergraph partitioning, static mapping, and sparse matrix block ordering. This is the all-inclusive version (MPI/serial/esmumps)."
url="http://www.labri.fr/perso/pelegrin/scotch/"
license=("custom:CeCILL-C")
depends=('openmpi')
makedepends=('zlib' 'bzip2')
optdepends=('bzip2: enable bzip2 compression')
provides=('ptscotch' 'ptscotch-openmpi' 'scotch_esmumps5' 'scotch_esmumps' 'scotch_ptesmumps')
conflicts=('ptscotch-openmpi' 'scotch_esmumps' 'scotch_esmumps5')
arch=('i686' 'x86_64')
source=("https://gforge.inria.fr/frs/download.php/file/${_downloadnum}/${pkgname}_${pkgver}.tar.gz")
sha256sums=('6461cc9f28319a9dbe6cc10e28c0cbe90b4b25e205723c3edcde9a3ff974d6d8')
 
prepare() {
  cd "${srcdir}/${pkgname}_${pkgver}/src"
 
  [ -e Makefile.inc ] && rm Makefile.inc
  cp Make.inc/Makefile.inc.${CARCH/_/-}_pc_linux2.shlib Makefile.inc
 
  # Use the CFLAGS defined /etc/makepkg.conf
  sed -i "s/-O3/${CFLAGS} -fPIC/g" Makefile.inc
 
  # Fix C compiler
  sed -i "s/CCD\t.*=.*gcc/CCD = mpicc/" Makefile.inc

  # Also enable bzip2 compression
  sed -i "s/-DCOMMON_FILE_COMPRESS_GZ/-DCOMMON_FILE_COMPRESS_GZ -DCOMMON_FILE_COMPRESS_BZ2/" Makefile.inc
  sed -i "s/-lz/-lz -lbz2/" Makefile.inc
 
  # Fix the creation of directories
  sed -i "s/mkdir/mkdir\ -p/" Makefile.inc
 
  # To install headers and libs also for esmumps
  sed -i 's/scotch\*/{scotch,esmumps}\*/g' Makefile
}
 
build() {
  cd "${srcdir}/${pkgname}_${pkgver}/src"
 
  make scotch
  make ptscotch
  make -j1 esmumps
  make -j1 ptesmumps
}
 
check() {
  cd "${srcdir}/${pkgname}_${pkgver}/src"
 
  make check LD_LIBRARY_PATH=${LD_LIBRARY_PATH}:"../../lib"
 
  #make ptcheck LD_LIBRARY_PATH=${LD_LIBRARY_PATH}:"../../lib"
  # Fail with: ./test_scotch_dgraph_check(0): ERROR: main: MPI implementation is not thread-safe: recompile without SCOTCH_PTHREAD
}
 
package() {
  cd "${srcdir}/${pkgname}_${pkgver}/src"
 
  make install prefix="${pkgdir}/usr" includedir="${pkgdir}/usr/include/scotch"
 
  # To avoid conflict with extra/gpart, maybe move the package to /opt/scotch ?
  mv "${pkgdir}/usr/bin/gpart" "${pkgdir}/usr/bin/gpart-scotch"
 
  install -m 644 -D ${srcdir}/scotch_${pkgver}/doc/CeCILL-C_V1-en.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
