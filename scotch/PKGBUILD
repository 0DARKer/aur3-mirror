# Maintainer: L.Bryce Whitson Jr. <LWhitson2@gmail.com>
pkgname=scotch
pkgver=5.1.12b
_pkgver=5.1.12
pkgrel=2
pkgdesc="a software package and libraries for graph, mesh and hypergraph partitioning, static mapping, and sparse matrix block ordering"
url="http://www.labri.fr/perso/pelegrin/scotch/"
license="custom: CeCILL-C free/libre software license"
depends=('zlib')
arch=('i686' 'x86_64')
source=("http://gforge.inria.fr/frs/download.php/28977/${pkgname}_${pkgver}.tar.gz")
md5sums=('5d912599c2521b1ecbcd8d12b68eef9c')

build()
{
	cd "${srcdir}/${pkgname}_${_pkgver}/src"

	# compile both static
	sed "s/-lz -lm -lrt/-lz -lm -lrt -lpthread/" Make.inc/Makefile.inc.x86-64_pc_linux2 > Makefile.inc
	make

	# ...and shared version
	sed "s/-lz -lm -lrt/-lz -lm -lrt -lpthread/" Make.inc/Makefile.inc.i686_pc_linux2.shlib > Makefile.inc
	make
}

package()
{
	cd "${srcdir}/${pkgname}_${_pkgver}"

	install -d "${pkgdir}/usr/bin"
	install -d "${pkgdir}/usr/lib"
	install -d "${pkgdir}/usr/include/${pkgname}"
	install -d "${pkgdir}/usr/man"
	install -d "${pkgdir}/usr/share/doc/${pkgname}"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}"

	cd bin
	for prog in *
	do
		install -m755 ${prog} ${pkgdir}/usr/bin/scotch_${prog}
	done
	cd ..
	
	cp lib/* ${pkgdir}/usr/lib
	cp include/* ${pkgdir}/usr/include/${pkgname}
	cp -r man/* ${pkgdir}/usr/man
	cp -r doc/* ${pkgdir}/usr/share/doc/${pkgname}
	cp doc/CeCILL-C_V1-en.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
