pkgname=smtube-qt4
pkgver=14.7.0
pkgrel=1
pkgdesc="YouTube browser, player and video downloader"
arch=('i686' 'x86_64')
url="http://smplayer.sourceforge.net/smtube.php"
license=('GPL')
depends=('qt4')
optdepends=('mplayer: play media with MPlayer'
            'smplayer: play media with SMPlayer'
            'gnome-mplayer: play media with GNOME MPlayer'
            'kdemultimedia-dragonplayer: play media with Dragon Player'
            'totem: play media with Totem'
            'vlc: play media with VLC'
            'mpv: play media with mpv')
provides=('smtube')
conflicts=('smtube')
install="smtube.install"
source=("http://downloads.sourceforge.net/smplayer/smtube-$pkgver.tar.bz2")
md5sums=('19bbcb461112c70c0500b514a5de515c')

build() {
  cd smtube-$pkgver
  make PREFIX=/usr QMAKE=qmake-qt4 LRELEASE=lrelease-qt4
}

package() {
  cd smtube-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir/" install
}
