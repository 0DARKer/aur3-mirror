# Maintainer: Carl George < arch at cgtx.us >

_module="PyGithub"
pkgname=("python-github" "python2-github")
pkgver="1.25.2"
pkgrel="1"
pkgdesc="Python library implementing the full GitHub API v3"
arch=("any")
url="http://jacquev6.net/PyGithub/v1/"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://github.com/jacquev6/${_module}/archive/v${pkgver}.tar.gz")

prepare() {
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

package_python-github() {
	conflicts=("python-pygithub")
	depends=("python")
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-github() {
	conflicts=("python2-pygithub")
	depends=("python2")
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

sha256sums=('3afce80516998ce4478424797f341d961d1c251958859c70b8d3986d00faafd7')
