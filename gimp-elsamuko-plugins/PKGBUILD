# vim: set ts=2 sw=2 et ft=sh tw=100 foldmarker={,}: expandtab
# Maintainer: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# Contributor: Piotr Rogoża <rogoza dot piotr at gmail dot com>

pkgbase=gimp-elsamuko-plugins
# AUR workaround
pkgname=gimp-elsamuko-plugins
true && pkgname=(
  gimp-elsamuko-plugins
  gimp-plugin-depthmap 
  gimp-plugin-copy-move
  gimp-plugin-octave
  gimp-plugin-temperature
  gimp-plugin-eaw-sharpen
  gimp-plugin-facedetect
  gimp-plugin-get-curves
  gimp-plugin-heatmap
  gimp-plugin-hsv-analysis
  gimp-plugin-lab-analysis
)
pkgver=0.1
pkgrel=3
arch=(i686 x86_64)
url='https://sites.google.com/site/elsamuko/gimp'
pkgdesc='Varoius Gimp plugins made by elsamuko'
license=('GPL')
depends=(gimp)
makedepends=(gimp opencv unzip)
source=(
https://sites.google.com/site/elsamuko/gimp/depthmap/elsamuko-depthmap-cv.tar.gz
depthmap-cv.patch
https://sites.google.com/site/elsamuko/forensics/clone-detection/elsamuko-copy-move.c
https://sites.google.com/site/elsamuko/gimp/gimp-octave/elsamuko-gimp-octave.c
https://sites.google.com/site/elsamuko/gimp/gimp-octave/filter_pack.tar.gz
https://sites.google.com/site/elsamuko/gimp/temperature/elsamuko-temperature.c
https://sites.google.com/site/elsamuko/gimp/eaw-sharpen/elsamuko-eaw-sharpen.tar.gz
eaw-sharpen.patch
https://sites.google.com/site/elsamuko/gimp/facedetect/elsamuko-facedetect-cv.tar.gz
face-detect-cv.patch
https://sites.google.com/site/elsamuko/gimp/get-curves/elsamuko-get-curves.c
https://sites.google.com/site/elsamuko/gimp/get-curves/AlienSkin-examples.zip
https://sites.google.com/site/elsamuko/gimp/get-curves/elsamuko-kodachrome
https://sites.google.com/site/elsamuko/gimp/heatmap/elsamuko-heatmap.c
https://sites.google.com/site/elsamuko/forensics/hsv-analysis/elsamuko-hsv-analysis.c
https://sites.google.com/site/elsamuko/forensics/lab-analysis/elsamuko-lab-analysis.tar.gz
)
noextract=(AlienSkin-examples.zip)
# env
_pluginsdir_eval='_pluginsdir="$pkgdir"/usr/lib/gimp/2.0/plug-ins'
_curvesdir_eval='_curvesdir="$pkgdir"/usr/share/$pkgname/examples/curves'
prepare() {
  cd "$srcdir"
  patch -Np1 -i "$srcdir"/eaw-sharpen.patch
  patch -Np1 -i "$srcdir"/depthmap-cv.patch
  patch -Np1 -i "$srcdir"/face-detect-cv.patch
}
build() {
  cd "$srcdir"
  
  # octave, get-curves, hsv-analysis
  msg2 "Building octave plugin"
  gimptool --build elsamuko-gimp-octave.c
  msg2 "Building get-curves plugin"
  gimptool --build elsamuko-get-curves.c
  msg2 "Building hsv-analysis plugin"
  gimptool --build elsamuko-hsv-analysis.c
  # heatmap
  msg2 "Building heatmap plugin"
  CC='gcc -lm' gimptool --build elsamuko-heatmap.c
  # copy-move
  msg2 "Building copy-move plugin"
  CC=g++ CFLAGS=-O3 LIBS=-lpthread gimptool --build elsamuko-copy-move.c
  # temperature
  msg2 "Building temperature plugin"
  CC="g++ -O3" gimptool --build elsamuko-temperature.c 
  # eaw-sharpen
  msg2 "Building eaw-sharpen plugin"
  cd "$srcdir"/elsamuko-eaw-sharpen
  make
  # depthmap-cv
  msg2 "Building depthmap-cv plugin"
#  gimptool --build elsamuko-depthmap.c
  cd "$srcdir"/elsamuko-depthmap-cv
  make
  # face-detect-cv
  msg2 "Building face-detect-cv plugin"
  cd "$srcdir"/elsamuko-facedetect-cv
  make 
  # lab-analysis
  msg2 "Building lab-analysis plugin"
  cd "$srcdir"/elsamuko-lab-analysis
  CC='g++ -lX11' CFLAGS=-O3 gimptool --build elsamuko-lab-analysis.c
}
package_gimp-elsamuko-plugins(){
  pkgdesc='Varoius Gimp plugins made by elsamuko, transition package to split build'
}
package_gimp-plugin-depthmap (){
  pkgdesc="Generates a depthmap out of two stereoscopic images."
  url='https://sites.google.com/site/elsamuko/gimp/depthmap'
  depends=(gimp-elsamuko-plugins opencv)
  eval $_pluginsdir_eval
  cd "$srcdir"/elsamuko-depthmap-cv
  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-depthmap-cv "$_pluginsdir"/
}
package_gimp-plugin-copy-move (){
  pkgdesc="Detects image manipulations which have been created with the clone tool"
  url='https://sites.google.com/site/elsamuko/forensics/clone-detection'
  depends=(gimp-elsamuko-plugins)
  eval $_pluginsdir_eval
  cd "$srcdir"
  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-copy-move "$_pluginsdir"/
}
package_gimp-plugin-octave (){
  pkgdesc="Provides a simple interface between GIMP and GNU Octave"
  url='https://sites.google.com/site/elsamuko/gimp/gimp-octave'
  optdepends=('octave-image: run Octave script in the Gimp')
  depends=(gimp-elsamuko-plugins)
  install='gimp-plugin-octave.install'
  eval $_pluginsdir_eval
  cd "$srcdir"
  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-gimp-octave "$_pluginsdir"/
  # examples .m files
  install -dm755 "$pkgdir"/usr/share/$pkgname/examples
  cp -r filter_pack "$pkgdir"/usr/share/$pkgname/examples
  # doc files
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  mv "$pkgdir"/usr/share/$pkgname/examples/filter_pack/README \
    "$pkgdir"/usr/share/doc/$pkgname/octave-plugin.README

}
package_gimp-plugin-temperature (){
  pkgdesc="Changes the color temperature of an image via shifting it in the YUV color space"
  url='https://sites.google.com/site/elsamuko/gimp/temperature'
  depends=(gimp-elsamuko-plugins)
  eval $_pluginsdir_eval
  cd "$srcdir"
  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-temperature "$_pluginsdir"/
}
package_gimp-plugin-eaw-sharpen (){
  pkgdesc="A filter which sharpens an image without annoying halos"
  url='https://sites.google.com/site/elsamuko/gimp/eaw-sharpen'
  depends=(gimp-elsamuko-plugins)
  eval $_pluginsdir_eval
  cd "$srcdir"/elsamuko-eaw-sharpen
  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-eaw-sharpen "$_pluginsdir"/
}
package_gimp-plugin-facedetect (){
  pkgdesc="Detects faces in an input image"
  url='https://sites.google.com/site/elsamuko/gimp/facedetect'
  depends=(gimp-elsamuko-plugins opencv)
  eval $_pluginsdir_eval
  cd "$srcdir"/elsamuko-facedetect-cv
  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-facedetect-cv "$_pluginsdir"/
  install -Dm644 haarcascade_frontalface_alt.xml "$_pluginsdir"/
}
package_gimp-plugin-get-curves (){
  pkgdesc="Calculates the color curve from two different pictures"
  url='https://sites.google.com/site/elsamuko/gimp/get-curves'
  depends=(gimp-elsamuko-plugins)
  install='gimp-plugin-get-curves.install'
  _pluginsdir="$pkgdir"/usr/lib/gimp/2.0/plug-ins
  eval $_pluginsdir_eval $_curvesdir_eval
  cd "$srcdir"
  install -dm755 "$_curvesdir"
  unzip -q AlienSkin-examples.zip -d ${_curvesdir}

  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-get-curves "$_pluginsdir"/
  # GIMP curves tool settings
  install -Dm644 "$srcdir"/elsamuko-kodachrome "$_curvesdir"/
  chmod 0644 "$_curvesdir"/*
}
package_gimp-plugin-heatmap (){
  pkgdesc="Calculates a heatmap from a picture"
  url='https://sites.google.com/site/elsamuko/gimp/heatmap'
  depends=(gimp-elsamuko-plugins)
  eval $_pluginsdir_eval
  cd "$srcdir"
  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-heatmap "$_pluginsdir"/
}
package_gimp-plugin-hsv-analysis (){
  pkgdesc="HSV analysis"
  url='https://sites.google.com/site/elsamuko/forensics/hsv-analysis'
  depends=(gimp-elsamuko-plugins)
  eval $_pluginsdir_eval
  cd "$srcdir"
  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-hsv-analysis "$_pluginsdir"/
}
package_gimp-plugin-lab-analysis (){
  pkgdesc="LAB analysis"
  url='https://sites.google.com/site/elsamuko/forensics/lab-analysis'
  depends=(gimp-elsamuko-plugins)
  eval $_pluginsdir_eval
  cd "$srcdir"/elsamuko-lab-analysis
  install -dm755 "${_pluginsdir}"
  install -Dm755 elsamuko-lab-analysis "$_pluginsdir"/
}
md5sums=('fcbd60e2bd957d5f439f1982859b5d9c'
         '1c2b56e78bbb27bb49c73f89b44f8e7f'
         '13018333b230f9a31353a4fb0e80a780'
         '2fc97640122e1c6da9f84d697280119b'
         'db194c4c97231f31835f13a313219b36'
         '635b821f4508402a4f4b26daab0eea5e'
         '2708f2cf7e7579fa76211b9bc6b8b401'
         'e57a90c23e738b224da130b913df6b86'
         'dc4a2a8ffa5ae6461b27119ce1545860'
         'aeac43b83bd536c9e660202601269093'
         '0f571d1a0b01da1334fa2871f729aebf'
         'ffadbad68664168b9ad637a793851864'
         '3b64bee1c2f563257113669b82995950'
         '0741a2b44e57c1bd9dec7ff54f666b5c'
         '7b1035756f06d8e29fcd8d60046a86de'
         'bdd087a53c32c6480323fcb9d35a31ac')
