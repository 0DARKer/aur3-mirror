# Submitter:   Wessel Dirksen "p-we" <wdirksen at gmail dot com>
# Contributor: Tycho LÃ¼rsen "bas-t" (responsible for hosting and development of FFdecsawrapper)
# Contributor: Petr Vacek "vaca" (providing cardslot.conf for serial port SC readers)
# Contributor: J.P. van Best (implementing new procfs API for kernels >= 3.10 in FFdecsawrapper kernel module)

# !! Note: This package installs the Digital Devices drivers for you. They should not be pre-installed

pkgname=ffdecsawrapper-git-digital-devices
pkgver=hg
pkgrel=1
pkgdesc="FFdecsa empowered softcam (newest generation of open-sasc-ng) - compiled with Digital Devices DVB drivers + firmware"
url="http://www.linuxtv.org"
arch=('i686' 'x86_64')
license=('GPLv3')
makedepends=('linux-headers' 'mercurial' 'patchutils' 'perl-proc-processtable' 'wget' 'perl')
depends=('v4l-utils')
conflicts=('ffdecsawrapper-git-digital-devices' 'digital-devices-dvb-drivers')
provides=('ffdecsawrapper-git-digital-devices')
install='ffdecsawrapper-dd.install'

source=('git://github.com/bas-t/ffdecsawrapper.git#branch=stable' \
	'hg+http://linuxtv.org/hg/~endriss/media_build_experimental' \
	'hg+http://linuxtv.org/hg/~endriss/ngene-octopus-test' \
	'cardclient.conf' 'cardslot.conf' 'ffdecsawrapper.conf' 'ffdecsawrapper-dd.install' \
	'ffdecsawrapper.lr' 'ffdecsawrapper.service' 'ffdecsawrapper.rc' 'v4l-mutex-dd.patch')

sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '5c23db2b93d1accdc0b3f1612766de38bf7ede5658f6ef973706988dd71d1b81'
            '436eb5a612aa3cb9e45bb2031429f3d41eb596ed65d18659d3bd708919c61253'
            '6d1132cbf0cbbd7dd62cb70e171f046e62805ea7ca9db909da63f3d65b52ae57'
            '6c7a0ba35196e3a9c01b2eca41a15ff4129652dfcecfbf821bd59636613bed95'
            'f435344dc9f1c0ed7c2e0de74ec434cd73e2130a0d7589a4d38338e45925d8db'
            '1c3d9ff15aa3a5aaf3c75adc99ec83af064875c52311795683b6c7efb32e1f56'
            'c20155f72ea47683bc2b6ea1471184419db1bc41b97c1bb9e2018b180c9474eb'
            '8434304be64c1c31f885b6de252cf65104dfb9b4f6dbf718f0469c0ee57fed03')

pkgver() {

	cd $srcdir/ffdecsawrapper
 	_gitffdecsawrapper=`git describe --always | sed 's|-|.|g'`

	cd "$srcdir/media_build_experimental"
	_ddver=`printf "0.r%s.%s" "$(hg identify -n)"`
	_kernel=`uname -r | sed -r 's/-/_/g'`
	echo "$_gitffdecsawrapper"_"$_ddver""$_kernel"
}

prepare() {

	cd "$srcdir/media_build_experimental"
	make download
	make untar
	sed -i 's/system ("make") == 0 or die "build failed";/#system ("make") == 0 or die "build failed";/' build
	./build

	msg "Applying V4l-mutex-dd patch..."
	patch -p1 < $srcdir/v4l-mutex-dd.patch
	echo ""
	msg "Here's a few seconds to confirm the patch has been applied sucessfully..."
	echo ""
	sleep 5
	make

	cd "$srcdir/ngene-octopus-test/linux/Documentation/dvb/"
	perl get_dvb_firmware ngene
	perl get_dvb_firmware drxk	
}

build() {

	cd $srcdir/ffdecsawrapper      
	./configure --dvb_dir=$srcdir/media_build_experimental/linux
}

package() {

	mkdir -p $pkgdir/usr/bin
	mkdir -p $pkgdir/etc/conf.d
	mkdir -p $pkgdir/etc/rc.d
	mkdir -p $pkgdir/etc/camdir
	mkdir -p $pkgdir/etc/logrotate.d
	mkdir -p $pkgdir/usr/lib/systemd/system
	mkdir -p $pkgdir/usr/lib/modules/`uname -r`/updates/{digital-devices,ffdecsawrapper}
	mkdir -p $pkgdir/usr/lib/firmware/digital-devices

	install -m0644 $srcdir/cardclient.conf  $pkgdir/etc/camdir/cardclient.conf
	install -m0644 $srcdir/cardslot.conf  $pkgdir/etc/camdir/cardslot.conf
	install -m0755 $srcdir/ffdecsawrapper.rc  $pkgdir/etc/rc.d/ffdecsawrapper
	install -m0644 $srcdir/ffdecsawrapper.conf  $pkgdir/etc/conf.d/ffdecsawrapper
	install -m0644 $srcdir/ffdecsawrapper.lr  $pkgdir/etc/logrotate.d/ffdecsawrapper-git-digital-devices.lr
	install -m0644 $srcdir/ffdecsawrapper.service  $pkgdir/usr/lib/systemd/system/ffdecsawrapper.service      
	install -m0755 $srcdir/ffdecsawrapper/ffdecsawrapper  $pkgdir/usr/bin
	install -m0644 $srcdir/ffdecsawrapper/dvbloopback.ko  $pkgdir/usr/lib/modules/`uname -r`/updates/ffdecsawrapper

	find "$srcdir/media_build_experimental" -name '*.ko' -exec cp {} $pkgdir/usr/lib/modules/`uname -r`/updates/digital-devices \;
	find "$srcdir/ngene-octopus-test" -name '*.fw' -exec cp {} $pkgdir/usr/lib/firmware/digital-devices \;
	find "$srcdir/ngene-octopus-test" -name '*.mc' -exec cp {} $pkgdir/usr/lib/firmware/digital-devices \;

	echo ""
	msg  "Compressing modules, this will take awhile..."
	echo ""
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

	chmod 644 -R $pkgdir/usr/lib/modules/`uname -r`/updates
	chmod 644 -R $pkgdir/usr/lib/firmware
}
