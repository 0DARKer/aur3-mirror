# Maintainer: Jeremy "Ichimonji11" Audet <ichimonji10 at gmail dot com>
# Contributor: Lucky <archlinux@builds.lucky.li>

pkgname=ruby-cri
_gemname="${pkgname#ruby-}"
pkgver=2.4.1
pkgrel=1
pkgdesc="A library for building easy-to-use commandline tools."
url="http://rubygems.org/gems/cri"
license=("MIT")
arch=("any")
depends=("ruby-colored>=1.2")
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem" 'LICENSE')
noextract=("${_gemname}-${pkgver}.gem")

md5sums=('c42ba4144a49b770a90994befd19aada'
         'e39789a760c965f9cd39e583f7b075d9')
sha1sums=('b55ab16dc0bf3e1e1f23280fe70c75a0fecebbbc'
          'e9c877ef6184945cd579c48610eade2ffb9948cc')

package() {
  cd "$srcdir"
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  HOME=/tmp gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "$pkgdir/usr/bin" \
    "${_gemname}-${pkgver}.gem"
  install -Dm 644 \
    "LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
