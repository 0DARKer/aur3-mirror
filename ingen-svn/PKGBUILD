# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=ingen
pkgname=$_name-svn
pkgver=4138
pkgrel=1
pkgdesc="A JACK modular audio processing system using LV2 or LADSPA plugins."
arch=(i686 x86_64)
url="http://drobilla.net/software/$_name/"
license=('GPL3')
depends=('lv2' 'raul' 'lilv' 'suil' 'ganv' 'sratom'
         'libwebkit' 'hicolor-icon-theme')
makedepends=('subversion' 'python2')
install="$pkgname.install"
conflicts=("$_name")
#options=('!strip') # debug

_svntrunk=http://svn.drobilla.net/lad/trunk/$_name
_svnmod=$_name

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr
#                        --debug
  python2 waf
}

package() {
  cd "$srcdir/$_svnmod-build"
  DESTDIR="$pkgdir/" python2 waf install

  # provides and conflicts
  _sver=`grep "^${_name^^}_VERSION" wscript | cut -d"'" -f2`
  provides=("$_name=$_sver")

  # adjust depends to add versions
  _deps=${depends[@]} && unset depends
  for _dep in $_deps; do
    _dver=`grep "^VERSION_$_dep" build/c4che/_cache.py | cut -d"'" -f2`
    [[ $_dver ]] && depends+=("$_dep>=$_dver") || depends+=("$_dep")
  done
}
