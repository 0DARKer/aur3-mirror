# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=ingen-svn
pkgver=0.5.1.r5415
pkgrel=1
pkgdesc="A JACK modular audio processing system using LV2 or LADSPA plugins."
arch=('i686' 'x86_64')
url="http://drobilla.net/software/ingen"
license=('GPL3')
depends=('python2' 'lilv-svn' 'ganv-svn' 'suil-svn' 'jack' 'webkitgtk2')
makedepends=('subversion' 'boost' 'raul-svn')
install="${pkgname}.install"
provides=("ingen")
conflicts=("ingen")

_svnname=ingen
source=("svn+http://svn.drobilla.net/lad/trunk/${_svnname}")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_svnname}"
  local rel=`grep "^INGEN_VERSION" wscript | cut -d "'" -f 2`
  local ver="$(svnversion)"
  printf "%s.r%s" "${rel}" "${ver//[[:alpha:]]}"
}

build() {
  cd "${srcdir}/${_svnname}"

  # pick up python2 even when python3 exists
  # (the build system has flaky support for python3)
  export PYTHON="/usr/bin/python2"

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "${srcdir}/${_svnname}"
  python2 waf install --destdir="${pkgdir}"
}
