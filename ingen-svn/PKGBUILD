# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=ingen
pkgname=$_pkg-svn
pkgver=3858
pkgrel=1
pkgdesc="A JACK modular audio processing system using LV2 or LADSPA plugins."
arch=(i686 x86_64)
url="http://drobilla.net/software/$_pkg/"
license=('GPL')
depends=('flowcanvas>=1.0.0' 'raul>=0.8.1' 'liblo' 'libwebkit' 'libglademm'
         'lilv' 'suil' 'ganv' 'hicolor-icon-theme' 'lv2core>=6.0'
         'lv2-atom' 'lv2-contexts' 'lv2-resize-port' 'lv2-event' 'lv2-uri-map')
makedepends=('subversion' 'python2')
provides=("$_pkg")
conflicts=("$_pkg")
install="$pkgname.install"

_svntrunk=http://svn.drobilla.net/lad/trunk/$_pkg
_svnmod=$_pkg

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$_svnmod-build"

  DESTDIR="$pkgdir/" python2 waf install
}
