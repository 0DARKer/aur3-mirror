# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=ingen
pkgname=$_pkg-svn
pkgver=3385
pkgrel=1
pkgdesc="A modular audio processing system for GNU/Linux audio systems using the Jack audio server and LV2 or LADSPA plugins."
arch=(i686 x86_64)
url="http://drobilla.net/software/$_pkg/"
license=('GPL')
depends=('flowcanvas>=0.7.1' 'raul>=0.8.0' 'liblo'
         'libglademm' 'lilv' 'suil' 'slv2' 'libsoup')
makedepends=('subversion' 'python2' 'lv2core-svn' 'redlandmm-svn')
provides=("$_pkg")
conflicts=("$_pkg")
install="$pkgname.install"
source=("wscript-rasqal.patch")
md5sums=('59a3005f2c809c7ccc0c5a358c231b74')

_svntrunk=http://svn.drobilla.net/lad/trunk/$_pkg
_svnmod=$_pkg

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  # patch to build against rasqal
  patch -p1 -i $srcdir/${source[0]}

  python2 waf configure --prefix=/usr
  python2 waf
}

package() {
  cd "$srcdir/$_svnmod-build"

  DESTDIR="$pkgdir/" python2 waf install
}
