# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: Mika Hynn√§ <mikaDOThynnaATwippiesDOTcom>

pkgname=vdradmin-am
pkgver=3.6.9
pkgrel=4
pkgdesc="A web based interface to VDR"
arch=('any')
url="http://andreas.vdr-developer.org/vdradmin-am/"
license=('LGPL')
depends=('vdr>=1.7.42' 'perl' 'perl-locale-gettext' 'perl-authen-sasl' 'perl-digest-hmac'
         'perl-libwww')
optdepends=('perl-io-socket-inet6: IPv6 support'
            'perl-io-socket-ssl: SSL Support')
backup=('etc/vdradmin/vdradmind.conf')
options=(emptydirs)
install=vdradmin-am.install
source=(http://andreas.vdr-developer.org/vdradmin-am/download/$pkgname-$pkgver.tar.bz2
        install.sh.patch
        LSTR.patch
        vdradmind.service)
md5sums=('d5cd89325f5a5dca5846a905b17d0bc2'
         'f86101f0a5e7e8df9533c0d1acbcb0b5'
         'd63fc0d7208617571186e97590fe3bbe'
         '5a56719ab5e5f6892efa71389503d90d')

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  
  # Patches
  patch -Np1 -i "${srcdir}/LSTR.patch"
  patch -Np0 -i "${srcdir}/install.sh.patch"

  # Create directories vdradmind expects to exist
  install -d -m755 "${pkgdir}/var/cache/vdradmin"
  install -d -m755 "${pkgdir}/var/log/vdradmin"

  # Install
  DESTDIR="${pkgdir}" PIDFILE="/run/vdradmind.pid" ./make.sh install

  # Install systemd service file
  install -d -m755 "${pkgdir}/usr/lib/systemd/system/"
  install -m644 "${srcdir}/vdradmind.service" "${pkgdir}/usr/lib/systemd/system/"
  
  # Create base configuration
  echo -e "\n6419\n\n\nvdradmin\nvdradmin\n/srv/vdr/video\n/var/lib/vdr\n" | ./vdradmind --config
  sed -i "s/ST_STREAMDEV_HOST = /ST_STREAMDEV_HOST = 127.0.0.1/" vdradmind.conf
  install -d -m755 "${pkgdir}/etc/vdradmin"
  install -m644 vdradmind.conf "${pkgdir}/etc/vdradmin"
}

