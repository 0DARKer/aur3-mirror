# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: Mika Hynn√§ <mikaDOThynnaATwippiesDOTcom>

pkgname=vdradmin-am
pkgver=3.6.9
pkgrel=3
pkgdesc="A web based interface to VDR"
arch=('any')
url="http://andreas.vdr-developer.org/"
license=('LGPL')
depends=('vdr' 'perl' 'perl-locale-gettext' 'perl-authen-sasl' 'perl-digest-hmac'
         'perl-libwww')
optdepends=('perl-io-socket-inet6: IPv6 support'
            'perl-io-socket-ssl: SSL Support')
backup=('etc/vdradmin/vdradmind.conf')
install=vdradmin-am.install
source=(http://andreas.vdr-developer.org/vdradmin-am/download/$pkgname-$pkgver.tar.bz2
        install.sh.patch
        rc-vdradmind
        vdradmind.service)
md5sums=('d5cd89325f5a5dca5846a905b17d0bc2'
         'f86101f0a5e7e8df9533c0d1acbcb0b5'
         '3d7d75aaa560315d2518abcd5f3214df'
         '5a56719ab5e5f6892efa71389503d90d')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  
  # Patch removes checks for IPv6 support and SSL support, and fixes installation
  # directories and install process, removing attempted vdradmin restart
  patch -Np0 -i "${srcdir}"/install.sh.patch
  
  # Assumed VDR directory environment (VDRDIR/Make.config):
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr

  # Install
  DESTDIR="${pkgdir}" PIDFILE="/run/vdradmind.pid" ./make.sh install

  # Create directories vdradmind expects to exist
  install -d "${pkgdir}"/var/cache/vdradmin
  install -d "${pkgdir}"/var/log/vdradmin
  
  # Install rc script
  install -D -m755 "${srcdir}"/rc-vdradmind "${pkgdir}"/etc/rc.d/vdradmind
  
  # Install systemd service file
  install -D -m644 "${srcdir}/vdradmind.service" "${pkgdir}/etc/systemd/system/vdradmind.service"
  
  # Create base configuration
  echo -e "\n\n\n\nvdradmin\nvdradmin\n/var/spool/video\n/etc/vdr\n" | ./vdradmind --config
  sed -i "s/ST_STREAMDEV_HOST = /ST_STREAMDEV_HOST = 127.0.0.1/" vdradmind.conf
  install -D -m644 vdradmind.conf "${pkgdir}"/etc/vdradmin/vdradmind.conf
}

