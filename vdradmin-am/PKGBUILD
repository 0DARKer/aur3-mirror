# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: Mika Hynn√§ <mikaDOThynnaATwippiesDOTcom>

pkgname=vdradmin-am
pkgver=3.6.10
pkgrel=1
pkgdesc="A web based interface to VDR"
arch=('any')
url="http://andreas.vdr-developer.org/vdradmin-am/"
license=('LGPL')
depends=('vdr>=2.0.0' 'perl' 'perl-authen-sasl' 'perl-digest-hmac'
         'perl-libwww' 'perl-locale-gettext')
optdepends=('perl-io-socket-inet6: IPv6 support'
            'perl-io-socket-ssl: SSL Support')
backup=('etc/vdradmin/vdradmind.conf')
options=(emptydirs)
install=vdradmin-am.install
source=(http://andreas.vdr-developer.org/vdradmin-am/download/$pkgname-$pkgver.tar.bz2
        install.sh.patch
        vdradmind.service)
md5sums=('467ff4a482be2a5331ad4224a1cb9d8e'
         '6dcfdd423647973fc13e9d69d0954824'
         '5a56719ab5e5f6892efa71389503d90d')

prepare() {
  cd "${srcdir}/$pkgname-$pkgver"

  patch -Np1 -i "${srcdir}/install.sh.patch"
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  # Create directories vdradmind expects to exist
  install -d -m755 "${pkgdir}/var/cache/vdradmin"
  install -d -m755 "${pkgdir}/var/log/vdradmin"

  # Install
  DESTDIR="${pkgdir}" PIDFILE="/run/vdradmind.pid" ./make.sh install

  # Install systemd service file
  install -d -m755 "${pkgdir}/usr/lib/systemd/system/"
  install -m644 "${srcdir}/vdradmind.service" "${pkgdir}/usr/lib/systemd/system/"

  # Create base configuration
  echo -e "\n6419\n\n\nvdradmin\nvdradmin\n/srv/vdr/video\n/var/lib/vdr\n" | ./vdradmind --config
  sed -i "s/ST_STREAMDEV_HOST = /ST_STREAMDEV_HOST = 127.0.0.1/" vdradmind.conf
  install -d -m755 "${pkgdir}/etc/vdradmin"
  install -m644 vdradmind.conf "${pkgdir}/etc/vdradmin"
}

