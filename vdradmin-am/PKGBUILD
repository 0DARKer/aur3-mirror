# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: Mika Hynn√§ <mikaDOThynnaATwippiesDOTcom>

pkgname=vdradmin-am
pkgver=3.6.7
pkgrel=4
pkgdesc="A web based interface to VDR"
arch=('any')
url="http://andreas.vdr-developer.org/"
license=('LGPL')
depends=('vdr' 'perl' 'perl-locale-gettext' 'perl-authen-sasl' 'perl-digest-hmac'
         'perl-libwww')
optdepends=('perl-io-socket-inet6: IPv6 support'
            'perl-io-socket-ssl: SSL Support')
backup=('etc/vdradmin/vdradmind.conf')
install=vdradmin-am.install
source=(http://andreas.vdr-developer.org/vdradmin-am/download/$pkgname-$pkgver.tar.bz2
        rc-vdradmin-am
        install.sh.patch)
md5sums=('8b1b2f8bbff26cbb47e79d5b55dbd3c1'
         'f9002d75ed1b0fc71c6fe9210c9075c4'
         '9801ebdd7d609f6f3aa3828783d89d46')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  
  # Patch removes checks for IPv6 support and SSL support, and fixes installation
  # directories and install process, removing attempted vdradmin restart
  patch -Np0 -i ${srcdir}/install.sh.patch || return 1
  
  # Assumed VDR directory environment (VDRDIR/Make.config):
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr

  # Install
  DESTDIR=${pkgdir} ./make.sh install || return 1

  # Create directories vdradmind expects to exist
  install -d ${pkgdir}/var/cache/vdradmin
  install -d ${pkgdir}/var/log/vdradmin
  install -d ${pkgdir}/var/run/vdradmind
  
  # Install rc script
  install -D -m755 ${srcdir}/rc-vdradmin-am ${pkgdir}/etc/rc.d/vdradmind || return 1
  
  # Create base configuration
  echo -e "\n\n\n\nvdradmin\nvdradmin\n/var/spool/video\n/etc/vdr\n" | ./vdradmind --config || return 1
  sed -i "s/ST_STREAMDEV_HOST = /ST_STREAMDEV_HOST = 127.0.0.1/" vdradmind.conf || return 1
  install -D -m644 vdradmind.conf ${pkgdir}/etc/vdradmin/vdradmind.conf || return 1
}

