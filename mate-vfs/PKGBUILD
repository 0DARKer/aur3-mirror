pkgname=mate-vfs
pkgver=0.0.0
pkgrel=3
pkgdesc="The MATE Virtual File System"
arch=(i686 x86_64)
license=('LGPL')
depends=('fam' 'mate-conf' 'bzip2' 'avahi' 'smbclient' 'mate-mime-data' 'gnutls') #  'krb5' 
makedepends=('pkgconfig' 'intltool' 'gtk-doc' 'mate-common')
options=('!emptydirs')
url="http://matsusoft.com.ar/projects"
groups=('mate')
install=mate-vfs.install
source=(http://matsusoft.com.ar/uploads/gnu-linux/mate/${pkgname}.tar.gz
        gnutls-config.patch)
sha256sums=('49b930c6b6c1b29482dd766999054ead715cc809ebb69c9b63cc865d7027d818'
            '66c7cfb12995c0dd94a2caea95c7e3c55981993f05a79c585d60915ff131955d')

build() {
	cd "${srcdir}/${pkgname}"
	#Fix build with new gnutls
	patch -Np1 -i "${srcdir}/gnutls-config.patch"
	libtoolize --force
	gtkdocize
	aclocal
	autoconf
	automake
	./configure --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var --disable-static \
		--libexecdir=/usr/lib/${pkgname} \
		--enable-samba --disable-hal \
		--enable-avahi --disable-howl \
		--disable-openssl --enable-gnutls
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

	install -d -m755 "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
