# Maintainer: Mihai Militaru <mihai dot militaru at xmpp dot ro>

pkgname=iup-gtk
_realname=iup
pkgver=3.8
pkgrel=1
pkgdesc="C cross platform GUI toolkit"
arch=('i686' 'x86_64')
url="http://www.tecgraf.puc-rio.br/iup/"
license=('MIT')
depends=('webkitgtk' 'cd' 'lua')
makedepends=('im' 'mesa' 'ffcall')
provides=('iup')
conflicts=('iup')

source=(http://sourceforge.net/projects/${_realname}/files/${pkgver}/Docs%20and%20Sources/iup-${pkgver}_Sources.tar.gz)
sha1sums=('663694e3817d3953543bd8d2fe7d9a9932582efa')

build() {
  cd ${srcdir}/${_realname}
  chmod -R ugo+rwx .

  sed -i 's|$(GTK)/include/webkit-1.0|$(GTK)/include/webkit-1.0 /usr/include/webkit-1.0 /usr/include/libsoup-2.4|g' srcweb/config.mak

  export C_INCLUDE_PATH="/usr/include/webkitgtk-1.0"

  make IM_LIB=/usr/lib CD_LIB=/usr/lib LUA_LIB=/usr/lib LUA_SUFFIX="" USE_PKGCONFIG=Yes USE_LUA52=Yes
}

package() {
  cd ${srcdir}/${_realname}
	
  mkdir -p ${pkgdir}/usr/{include,lib}
  install -m644 ${srcdir}/${_realname}/lib/Linux*/libiup*.a ${pkgdir}/usr/lib/
  install -m755 ${srcdir}/${_realname}/lib/Linux*/libiup*.so ${pkgdir}/usr/lib/
  install ${srcdir}/${_realname}/include/* ${pkgdir}/usr/include/
  install -Dm644 ${srcdir}/${_realname}/COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

