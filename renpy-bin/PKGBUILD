# Contributor: Eric Forgeot < http://ifiction.free.fr > & franzrogar
# Maintainer: Ben R <thebenj88 *AT* gmail *DOT* com>

pkgname=renpy-bin
_realname=renpy
pkgver=6.17.0
pkgrel=1
pkgdesc="A free and cross-platform engine that helps you use words, pictures, and sounds to tell stories with the computer."
arch=(x86_64 i686)
url="http://www.renpy.org"
license=(MIT)
depends=(bash ffmpeg-compat glu hicolor-icon-theme python2 sdl_image)
makedepends=(imagemagick)
conflicts=(renpy)
install=$pkgname.install
options=(!strip)
source=(http://www.renpy.org/dl/$pkgver/renpy-$pkgver-sdk.tar.bz2
		renpy.launcher
		$pkgname.desktop)

sha1sums=('e694263f4de7214673b672dd84dd959e34919da1'
          'cea169e596c3c2f818fe8df18517701ea2530b23'
          '022eb749ac85d2625bd4268c8812fa872c88eafa')

prepare()
{
	[[ $CARCH == x86_64 ]] && rm -rf "$srcdir/${_realname}-${pkgver}-sdk/lib/linux-i686"
	[[ $CARCH == i686 ]] && rm -rf "$srcdir/${_realname}-${pkgver}-sdk/lib/linux-x86_64"
	rm "$srcdir/${_realname}-${pkgver}-sdk/renpy.exe"
	convert "$srcdir/${_realname}-${pkgver}-sdk/the_question/icon.ico" "$srcdir/renpy.png"
}



package() {
    cd "$srcdir/${_realname}-${pkgver}-sdk"
	
	mkdir -p "$pkgdir/usr/share/renpy"
	# Don't need windows executable
	
	cp -r $srcdir/${_realname}-${pkgver}-sdk/* "$pkgdir/usr/share/renpy"

	install -Dm755 "$srcdir/renpy.launcher" "$pkgdir/usr/bin/renpy"
	
	# desktop icons
	_folders=('256x256' '48x48' '32x32' '16x16')
	count=0
	while [ $count -lt 4 ]
	do
		install -Dm644 "$srcdir/renpy-${count}.png" \
			"$pkgdir/usr/share/icons/hicolor/${_folders[$count]}/renpy.png"
		count=$((count + 1))
	done
	install -Dm644 "$srcdir/renpy-bin.desktop" "$pkgdir/usr/share/applications/renpy.desktop"
	install -Dm644 "$srcdir/${_realname}-${pkgver}-sdk/LICENSE.txt" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
