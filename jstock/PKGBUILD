# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Kevin Peters <65bones AT gmail DOT com>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>

pkgname=jstock
pkgver=1.0.6L
pkgrel=1
pkgdesc="A stock market software that helps you make smart investment decision"
arch=('any')
url="http://jstock.sourceforge.net/"
license=('GPL')
depends=('jre7-openjdk')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-bin.zip"
        "${pkgname}.desktop"
        "${pkgname}.png"
        "${pkgname}.sh")
md5sums=('dc70d21cfdfe0c0e77dbd404aadbb75d'
         '613b51e6579fe51380eefe11fb768f4f'
         'c2483790417a4ca80b7a65006f696679'
         '35364da410d3be95f5eba9e106ee248f')

package() {
  cd "${srcdir}/${pkgname}"

  # Install program files
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -rf ./* "${pkgdir}/opt/${pkgname}"
  find "${pkgdir}/opt/${pkgname}" -type d -exec chmod 755 '{}' \;
  find "${pkgdir}/opt/${pkgname}" -type f -exec chmod 644 '{}' \;

  # Install the launcher, pixmap and .desktop file
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
