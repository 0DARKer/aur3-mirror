# Maintainer: Gilrain <pierre.buard+aur gmail com>

pkgname=nzbget-ppscript-videosort
pkgver=4.0
pkgrel=1
pkgdesc="Post-processing script to automatically organize downloaded video files."
arch=(any)
url="http://nzbget.sourceforge.net/VideoSort"
license=('GPL3')
depends=(nzbget python2-guessit)
changelog=changelog
source=("http://downloads.sourceforge.net/project/nzbget/ppscripts/videosort/videosort-ppscript-${pkgver}.zip")
sha256sums=('e6f3b54d5f5b20b64f2ac97841d3112428ca285ec4a4b850b8d500cdf102127c')

prepare() {
    sed -i -e 's|^#!/usr/bin/env python$|#!/usr/bin/env python2|' ${srcdir}/videosort/VideoSort.py
    sed -i "/sys.path.append(dirname(__file__) + '\/lib')/d" ${srcdir}/videosort/VideoSort.py
}

package() {
    cd ${srcdir}/videosort

    install -dm755 ${pkgdir}/usr/share/nzbget/ppscripts
    install -m755 VideoSort.py ${pkgdir}/usr/share/nzbget/ppscripts
    install -m644 README.txt ${pkgdir}/usr/share/nzbget/ppscripts/VideoSort.txt
}