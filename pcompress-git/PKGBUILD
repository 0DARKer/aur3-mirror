# Maintainer: "UnCO Lin" <trash__box <at> 163.com>
# Contributor: PLum <plum.michalski <at> gmail.com>
pkgname=pcompress-git
_pkgname=pcompress
pkgver=384
_wavpack_ver=4.70.0
pkgrel=1
arch=(x86_64)
pkgdesc="Pcompress is a utility to do compression/decompression and deduplication in parallel by splitting input data into chunks."
url="http://moinakg.github.io/pcompress/"
license=('LGPL v3')
options=('!makeflags')
depends=('expat' 'lzo2' 'xz' 'attr' 'acl' 'libarchive' 'zlib' 'openssl')
makedepends=('yasm' 'autoconf' 'automake')
source=("git+https://github.com/moinakg/${_pkgname}.git"
        'libarchive.patch'
        'config.patch'
        'makefile.in.patch'
        "http://wavpack.com/wavpack-${_wavpack_ver}.tar.bz2")
md5sums=('SKIP'
         '06a9275134d401242e5d897544270dea'
         'ff761645b687454b5eb238a6430414c7'
         '9cc2b89aded123f63815e60f9ce88146'
         '4c0186ef0dc8367ce5cd7cc0f398b714')

pkgver() {
  cd "$_pkgname"
  git rev-list --count HEAD
}

prepare() {
  cd "$_pkgname"
  patch -Np1 -i "$srcdir"/libarchive.patch
  patch -Np1 -i "$srcdir"/config.patch
  patch -Np1 -i "$srcdir"/makefile.in.patch
  cd archive/libarchive
  autoreconf -fi
}

build() {
  cd "$_pkgname"
  ./config --prefix=/usr \
    --wavpack-dir="$srcdir"/wavpack-${_wavpack_ver}
  make -j1
}

package() {
  cd "$_pkgname"
  make DESTDIR=${pkgdir} install
}
