# Maintainer: "UnCO Lin" <trash__box <at> 163.com>
# Contributor: PLum <plum.michalski <at> gmail.com>
pkgname=pcompress-git
_pkgname=pcompress
pkgver=374
pkgrel=1
arch=(x86_64)
pkgdesc="Pcompress is a utility to do compression/decompression and deduplication in parallel by splitting input data into chunks."
url="http://moinakg.github.io/pcompress/"
license=('LGPL v3')
options=('!makeflags')
depends=('expat' 'lzo2' 'xz' 'attr' 'acl' 'libarchive' 'zlib' 'openssl')
makedepends=('yasm')
source=("git+https://github.com/moinakg/${_pkgname}.git"
        'rabin_error_ignore.patch')
md5sums=('SKIP'
         '3f32d94604cefbc90cac33b93f81bc61')

pkgver() {
  cd "$_pkgname"
  git rev-list --count HEAD
}

prepare() {
  cd "$_pkgname"
  patch -Np1 -i "$srcdir"/rabin_error_ignore.patch
}

build() {
  cd "$_pkgname"
  ./config --prefix=/usr
  make -j1
}

package() {
  cd "$_pkgname"
  make DESTDIR=${pkgdir} install
}
