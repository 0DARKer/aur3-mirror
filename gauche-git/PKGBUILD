# Contributor: Taylor Venable <taylor@metasyntax.net>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname='gauche-git'
pkgver=0.9.3.3.756.g5ef4466
pkgrel=1
pkgdesc="R5RS Scheme implementation developed to be a handy script interpreter"
arch=('i686' 'x86_64')
url='http://practical-scheme.net/gauche/index.html'
license=('BSD')
depends=('slib')
makedepends=('gauche')
provides=('gauche')
conflicts=('gauche')
source=('git+https://github.com/shirok/Gauche.git' 'makefile_slib.diff'
        'doc-makefile.patch')
md5sums=('SKIP' '1ddf7c6dbfc61780bf5118e80d45cb1d'
         '56072e401c3e5aafefa46824f52f79c9')
install="$pkgname.install"
_gitname='Gauche'
options=('!makeflags')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --tag | sed s+[-_]+.+g | cut -c8-
}

build() {
  cd "$srcdir/$_gitname"
  ./DIST gen
  patch -p0 < "${srcdir}/doc-makefile.patch"
  ./configure --prefix=/usr --enable-multibyte=utf-8 --enable-threads=pthreads
  patch "lib/Makefile" "${srcdir}/makefile_slib.diff"
  make
  cd doc && make htmls && cd ..
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
  cd doc
  make DESTDIR="$pkgdir" install
}
