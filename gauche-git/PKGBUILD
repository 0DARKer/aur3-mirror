# Maintainer: Taylor Venable <taylor@metasyntax.net>

pkgname='gauche-git'
pkgver=20120801
pkgrel=2
pkgdesc="R5RS Scheme implementation developed to be a handy script interpreter"
arch=('i686' 'x86_64')
url='http://practical-scheme.net/gauche/index.html'
license=('BSD')
depends=('gcc-libs' 'gdbm' 'zlib' 'slib')
provides=('gauche')
conflicts=('gauche')
source=('makefile_slib.diff'
        'doc-makefile.patch')
install="$pkgname.install"
md5sums=('1ddf7c6dbfc61780bf5118e80d45cb1d'
         '56072e401c3e5aafefa46824f52f79c9')

_gitroot='git://gauche.git.sourceforge.net/gitroot/gauche/Gauche'
_gitname='gauche'

build() {
  cd "$srcdir"
  msg "Connecting to Git server ..."

  if [[ -d $_gitname ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "Git checkout done or server timeout"
  msg "Starting make ..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./DIST gen
  patch -p0 < "${srcdir}/doc-makefile.patch"
  ./configure --prefix=/usr --enable-multibyte=utf-8 --enable-threads=pthreads
  patch "lib/Makefile" "${startdir}/makefile_slib.diff"
  make
  cd doc && make htmls && cd ..
}
package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
  mkdir -p "${pkgdir}/usr/share/doc"
  cp -R "doc/gauche-refe" "${pkgdir}/usr/share/doc/gauche"
  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
