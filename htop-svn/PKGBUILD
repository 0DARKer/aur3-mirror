# Contributor:  Vincent Kriek <vincent@vincentkriek.nl>
pkgname=htop-svn
pkgver=302
pkgrel=1
pkgdesc="Interactive process viewer (from SVN)"
arch=('i686' 'x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('subversion' 'python2')
conflicts=('htop')
provides=('htop')

_svntrunk=https://htop.svn.sourceforge.net/svnroot/htop/trunk
_svnmod=htop

build() {
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver) || warning "Update failed!"
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  sed -i 's|ncursesw/curses.h|curses.h|' RichString.[ch] configure.ac
  sed -i 's|python|python2|' scripts/MakeHeader.py

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --enable-unicode \
      --enable-openvz \
      --enable-vserver \
      --enable-cgroup

  make
}

package() {
  make -C $_svnmod-build DESTDIR="$pkgdir" install
}
