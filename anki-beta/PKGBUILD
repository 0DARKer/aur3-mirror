# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=anki-beta
pkgver=2.0.2
pkgrel=2
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. beta version"
arch=(any)
url=http://ankisrs.net
license=(AGPL3 BSD)
depends=(mplayer python2-beautifulsoup3 python2-httplib2 python2-pyqt)
optdepends=('python2-pyaudio: to record audio files')
provides=(anki=$pkgver)
conflicts=(anki ankiqt-git)
install=anki.install
source=($url/download/mirror/anki-$pkgver.tgz)
sha256sums=('26176d49b10e2b06ba37f3b8fecd6559f42f262089ea20f317db01ea64ca6cd5')
sha512sums=('1f1392857689b26690938156e7c3b58cfd0e3d82c3fe0fbfcde3f3a48f6d902f5b29408b93e45eb04f56bec76a8c5f1f89c6cb079145da2bbd3fc181690e553f')

package() {
    cd "$srcdir"/anki-$pkgver/

    # remove unneeded libraries
    rm -rf libanki/thirdparty/ tools/

    sed -i 's:#!/usr/bin/env python:&2:' anki
    install -Dm755 anki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.logo "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.logo

    rm anki{,.xpm,.png,.desktop,.1,.xml} LICENSE.logo {libanki,.}/{LICENSE,ChangeLog.old} Makefile
    find -name '*.bat' -delete
    find -name 'README*' -delete

    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
