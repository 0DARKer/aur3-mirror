# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=anki-beta
pkgver=2.0
_pkgrel=rc6
pkgrel=${_pkgrel}_2
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. 2.0 beta version"
arch=(any)
url=http://ankisrs.net
license=(AGPL3 BSD)
depends=(mplayer python2-beautifulsoup3 python2-httplib2 python2-pyaudio python2-pyqt)
provides=(anki=$pkgver.$_pkgrel)
conflicts=(anki ankiqt-git)
install=anki.install
source=($url/download/mirror/anki-$pkgver-$_pkgrel.tgz)
sha256sums=('b5e92002e8bc6661d81fb344eea7e15bc82c929a0e71bff5fd1fb61b51ca310c')
sha512sums=('e5ffae2e6b66ef68e37cffd5f8482eb0b164431b46199317bb05a305cb9aebe66955c86c5d441287e96c4b8d7ac969f7e3a8f444646ce938ae2bfff323f7f984')

package() {
    cd "$srcdir"/anki-$pkgver/

    # remove unneeded libraries
    rm -rf libanki/thirdparty/ tools/

    sed -i 's:#!/usr/bin/env python:&2:' anki
    install -Dm755 anki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.logo "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.logo

    rm anki{,.xpm,.png,.desktop,.1,.xml} LICENSE.logo {libanki,.}/{LICENSE,ChangeLog.old} Makefile
    find -name '*.bat' -delete
    find -name 'README*' -delete

    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
