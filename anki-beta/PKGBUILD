# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=anki-beta
pkgver=2.0.5
pkgrel=1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. beta version"
arch=(any)
url=http://ankisrs.net
license=(AGPL3 BSD)
depends=(mplayer python2-beautifulsoup3 python2-httplib2 python2-pyqt)
optdepends=('python2-pyaudio: to record audio files')
provides=(anki=$pkgver)
conflicts=(anki ankiqt-git)
install=anki.install
source=($url/download/mirror/anki-$pkgver.tgz)
sha256sums=('311722703721eae28ac64253112f7c853b8891284a9b9cc62efe243733af0f55')
sha512sums=('529d7562ed87aa1594c77a773f8e468a444a4a624bb07a8314238edc8a11b86c1c84b9eb45e73095392bc9f579e0742765b471af8011420ac656de54dc14187f')

package() {
    cd "$srcdir"/anki-$pkgver/

    # remove unneeded libraries
    rm -rf thirdparty/ tools/ tests/

    sed -i 's:^#!/usr/bin/env python$:&2:' runanki anki/anki
    install -Dm755 runanki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.logo "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.logo

    rm runanki anki{.xpm,.png,.desktop,.1,.xml} LICENSE{,.logo} Makefile
    find -name '*.bat' -delete
    find -name 'README*' -delete

    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
