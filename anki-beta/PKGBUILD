# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=anki-beta
pkgver=2.0
_pkgrel=rc5
pkgrel=${_pkgrel}_1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. 2.0 beta version"
arch=(any)
url=http://ankisrs.net
license=(AGPL3 BSD)
depends=(mplayer python2-beautifulsoup3 python2-httplib2 python2-pyaudio python2-pyqt)
provides=(anki=$pkgver)
conflicts=(anki ankiqt-git)
install=anki.install
source=($url/download/mirror/anki-$pkgver-$_pkgrel.tgz)
sha256sums=('b17264bf80d703efabf6c67deeb42a7c3d0b63c39f6373b627a4ac4c76dd7fbf')
sha512sums=('ddb832da02acef152da6e69b9beafe98d44789f77438b25d64f0cfebc250ab55293ee57a3045e31e9c00ac9f038fbae8291638820a052b2842542b395c057a02')

package() {
    cd "$srcdir"/anki-$pkgver/

    # remove unneeded libraries
    rm -rf libanki/thirdparty/ tools/

    sed -i 's:#!/usr/bin/env python:&2:' anki
    install -Dm755 anki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.logo "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.logo

    rm anki{,.xpm,.png,.desktop,.1,.xml} LICENSE.logo {libanki,.}/{LICENSE,ChangeLog.old} Makefile
    find -name '*.bat' -delete
    find -name 'README*' -delete

    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
