# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=anki-beta
pkgver=2.0.8
pkgrel=1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. beta version"
arch=(any)
url=http://ankisrs.net
license=(AGPL3 BSD)
depends=(mplayer python2-beautifulsoup3 python2-httplib2 python2-pyqt)
optdepends=('python2-pyaudio: to record audio files')
provides=(anki=$pkgver)
conflicts=(anki ankiqt-git)
install=anki.install
source=($url/download/mirror/anki-$pkgver.tgz)
sha256sums=('dd670b02bf6b5c98dcfa9e1606ac00f6e1046f8debb5812e0f383ffe4778b868')
sha512sums=('b7408c0dbdd5da40e70a39d0305e0ac336737f7b579db9188eeaa0e26c4efe92d0028869e6e18d18066cdfd7b526815e8c931351e32e9815e844cb3656033e58')

package() {
    cd "$srcdir"/anki-$pkgver/

    # remove unneeded libraries
    rm -rf thirdparty/ tools/ tests/

    sed -i 's:^#!/usr/bin/env python$:&2:' runanki anki/anki
    install -Dm755 runanki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.logo "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.logo

    rm runanki anki{.xpm,.png,.desktop,.1,.xml} LICENSE{,.logo} Makefile
    find -name '*.bat' -delete
    find -name 'README*' -delete

    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
