# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=anki-beta
pkgver=2.0.7
pkgrel=1
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently. beta version"
arch=(any)
url=http://ankisrs.net
license=(AGPL3 BSD)
depends=(mplayer python2-beautifulsoup3 python2-httplib2 python2-pyqt)
optdepends=('python2-pyaudio: to record audio files')
provides=(anki=$pkgver)
conflicts=(anki ankiqt-git)
install=anki.install
source=($url/download/mirror/anki-$pkgver.tgz)
sha256sums=('64af49fd0be82d2664bca6879f74b54b186270379c4012344141903b7be0fe28')
sha512sums=('3ee9f6b3eb88acde2049b28cc1209dfcd600af91b569b53ff463c55a91819133fe92d4a023efb7b000ccb120d18a37a69d394a2ddabec5e6ba0bac6746d09774')

package() {
    cd "$srcdir"/anki-$pkgver/

    # remove unneeded libraries
    rm -rf thirdparty/ tools/ tests/

    sed -i 's:^#!/usr/bin/env python$:&2:' runanki anki/anki
    install -Dm755 runanki "$pkgdir"/usr/bin/anki
    install -Dm644 anki.xpm "$pkgdir"/usr/share/pixmaps/anki.xpm
    install -Dm644 anki.png "$pkgdir"/usr/share/pixmaps/anki.png
    desktop-file-install anki.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 anki.1 "$pkgdir"/usr/share/man/man1/anki.1
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm644 LICENSE.logo "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.logo

    rm runanki anki{.xpm,.png,.desktop,.1,.xml} LICENSE{,.logo} Makefile
    find -name '*.bat' -delete
    find -name 'README*' -delete

    install -d "$pkgdir"/usr/share/anki/
    cp -a * "$pkgdir"/usr/share/anki/
}
