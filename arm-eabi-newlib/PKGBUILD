# Maintainer: Andreas Messer <andi@surveycorner.de>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_target=arm-eabi

pkgname=${_target}-newlib
pkgver=2.0.0
pkgrel=2
pkgdesc="Newlib is a C library intended for use on embedded systems."
arch=('any')
groups=('devel')
url="http://sourceware.org/newlib/"
license=('BSD')
makedepends=("${_target}-binutils" "${_target}-gcc-base")
source=( ftp://sourceware.org/pub/newlib/newlib-${pkgver}.tar.gz)
options=('!libtool' '!emptydirs' '!strip')
md5sums=('e3e936235e56d6a28afb2a7f98b1a635')

prepare() {
  mkdir ${srcdir}/newlib-build

  cd ${srcdir}/newlib-build

  export CFLAGS="-Os"
  ${srcdir}/newlib-${pkgver}/configure \
   --target=${_target} \
   --prefix=/usr \
   --enable-interwork \
   --enable-multilib \
   --with-gnu-as \
   --with-gnu-ld \
   --with-float=soft \
   --disable-newlib-register-fini \
   --disable-newlib-atexit-alloc \
   --enable-newlib-multithread \
   --disable-newlib-supplied-syscalls \
   --disable-nls || return 1
}
build() {
 cd ${srcdir}/newlib-build

 make || return 1
}

package() {
 cd ${srcdir}/newlib-build || return 1

 make -j1 DESTDIR=${pkgdir} install || return 1

 rm -rf ${pkgdir}/usr/share/info

 install -D -m644 "${srcdir}/newlib-${pkgver}/COPYING.NEWLIB" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
