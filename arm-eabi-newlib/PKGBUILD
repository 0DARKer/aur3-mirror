# Maintainer: Andreas Messer <andi@surveycorner.de>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_target=arm-eabi

pkgname=${_target}-newlib
pkgver=2.0.0
pkgrel=1
pkgdesc="Newlib is a C library intended for use on embedded systems."
arch=('any')
groups=('devel')
url="http://sourceware.org/newlib/"
license=('BSD')
depends=("${_target}-binutils" "${_target}-gcc-base")
source=( ftp://sourceware.org/pub/newlib/newlib-${pkgver}.tar.gz)
options=(!libtool !emptydirs !strip)
md5sums=('e3e936235e56d6a28afb2a7f98b1a635')

build() {
 cd ${srcdir}
 
 rm -rf build
 mkdir  build
 cd     build

 export CFLAGS="-O2"

 ../newlib-${pkgver}/configure \
   --target=${_target} \
   --prefix=/usr \
   --enable-interwork \
   --enable-multilib \
   --with-gnu-as \
   --with-gnu-ld \
   --with-float=soft \
   --disable-nls || return 1

 make || return 1
}

package() {
 cd ${srcdir}/build || return 1

 make -j1 DESTDIR=${pkgdir} install || return 1

 rm -rf ${pkgdir}/usr/share/info

 install -D -m644 "${srcdir}/newlib-${pkgver}/COPYING.NEWLIB" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
