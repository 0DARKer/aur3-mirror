pkgname=nodejs-unstable
pkgver=0.5.5
pkgrel=1
pkgdesc='Evented I/O for V8 javascript, unstable version'
arch=('i686' 'x86_64' 'arm')
url='http://nodejs.org/'
license=('MIT')
depends=('python2' 'v8-current' 'c-ares')
conflicts=('nodejs')
provides=('nodejs')
checkdepends=('curl') # curl used for check()
optdepends=('openssl: TLS support')
source=("http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz")
sha256sums=('6f7ef8859e43545ff9a0e178e39a070f22c6a2abcf46b2cae079f446b5750e65')

build() {
  cd node-v${pkgver}

  # python2 fix
  for file in $(find . -name '*.py' -print) wscript tools/waf-light tools/node-waf
  do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' ${file}
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' ${file}
  done
  sed -i "s|cmd_R = 'python |cmd_R = 'python2 |" wscript
  sed -i "s|python |python2 |" Makefile

  ./configure --shared-v8 --shared-cares --prefix=/usr

  make
}

#check() {
#  cd node-v${pkgver}
#  make test
#}

package() {
  cd node-v${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/nodejs/LICENSE
}
