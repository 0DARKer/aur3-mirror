# Maintainer: James Campos <james.r.campos@gmail.com>

pkgname=nodejs-unstable
pkgver=0.3.8
pkgrel=1
pkgdesc="Evented I/O for V8 javascript"
arch=('i686' 'x86_64')
url="http://nodejs.org/"
license=('MIT')
depends=('python2')
optdepends=('openssl: TLS support')
provides=('nodejs')
source=("http://nodejs.org/dist/node-v$pkgver.tar.gz")
sha256sums=('fc71861d339ec9ced9c85cb714f14e3e6e51651833ef0fec2abf41ad113172f1')

build() {
  cd "node-v$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print) wscript tools/waf-light tools/node-waf; do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' "$file"
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' "$file"
  done
  sed -i "s|cmd_R = 'python |cmd_R = 'python2 |" wscript
  sed -i "s|python |python2 |" Makefile

  ./configure --prefix=/usr
  make
}

package() {
  cd "node-v$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
