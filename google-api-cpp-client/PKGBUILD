# Maintainer: Sebastian Lau <lauseb644@gmail.com>
# Submitter: Sebastian Lau <lauseb644@gmail.com>

pkgname=google-api-cpp-client
_branchver=0.1
pkgver=0.1.5
pkgrel=1
_mongoosever=3.7
pkgdesc="Open source C++ client libraries for Google APIs"
arch=('i686' 'x86_64')
url="https://github.com/google/google-api-cpp-client"
license=('GPL')
depends=('google-glog' 'jsoncpp' 'openssl' 'curl')
makedepends=('cmake' 'git' 'gtest' 'gflags')
provides=('google-api-cpp-client')
source=("git+https://github.com/google/${pkgname}.git#branch=v${_branchver}"
	"mongoose-${_mongoosever}.tar.gz::https://github.com/cesanta/mongoose/archive/${_mongoosever}.tar.gz"
        "mongoose_cmake.patch"
	"cmake.patch"
	"gmock_cmake.patch")
md5sums=('SKIP'
         'd35448e3b459746a04fb1319fe27755d'
         '1a155c068630d3e1d7f63fd8c5fe8916'
         '8f33d2f108aa71ac2a9b1567e20515c3'
         'c17bf19c9f244a903fea9f8ce4a439ae')
options=(staticlibs)

prepare() {
 ## Mongoose does not provide a proper Makefile
  msg2 "Adding makefile for embedded mongoose server"
   cd "${srcdir}/mongoose-${_mongoosever}"
   [ -e CMakeLists.txt ] && rm -f CMakeLists.txt
   patch CMakeLists.txt < "${startdir}/mongoose_cmake.patch"

 ## Googles CMakeLists.txt's have a few small bugs
  msg2 "Patching google's cmake"
   cd "${srcdir}/${pkgname}"
   patch CMakeLists.txt < "${startdir}/cmake.patch"
   patch src/CMakeLists.txt < "${startdir}/gmock_cmake.patch"
}


build() {
   # Since google-api-cpp-client uses functions from an pre-5.0 mongoose version, we need to include this webserver here.
   msg2 "Building mongoose ${_mongoosever}..."
   cd "${srcdir}/mongoose-${_mongoosever}"
   cmake .
   make

   cd "${srcdir}/${pkgname}"

   msg2 "Moving stuff in place..."
   rm -rf external_dependencies
   mkdir -p external_dependencies/install/{lib,include/mongoose}
   cp "${srcdir}/mongoose-${_mongoosever}/libmongoose.a" external_dependencies/install/lib
   cp "${srcdir}/mongoose-${_mongoosever}/mongoose.h" external_dependencies/install/include/mongoose

   msg2 "Building google C++ API..."
   
   cmake -Dgoogleapis_build_tests=OFF \
	-Dgoogleapis_build_samples=OFF \
	-Dgoogleapis_build_service_apis=OFF .
   make all
}
 
package() {
   cd "${srcdir}/${pkgname}"
   make install

   mkdir "${pkgdir}/usr"
   cp -r export/* "${pkgdir}/usr"

   install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.txt"
   install -Dm644 "${srcdir}/mongoose-${_mongoosever}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/mongoose/LICENSE"
}
