# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: 

pkgname=qactus-git
pkgver=r80.d31ce64
pkgrel=1
pkgdesc="A Qt-based OBS notifier application."
arch=('i686' 'x86_64')
url="https://github.com/javierllorente/qactus"
license=('GPL2' 'GPL3')
depends=('qtkeychain-qt4')
makedepends=('git')
conflicts=("${pkgname%-*}")
provides=("${pkgname%-*}")
install="${pkgname%-*}.install"
source=('git://github.com/javierllorente/qactus.git')
md5sums=('SKIP')

pkgver() {
	cd "${pkgname%-*}"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${pkgname%-*}"

	if [[ -d build ]]; then
	rm -rf build
	fi
	mkdir build && cd build

	qmake-qt4 PREFIX=/usr ..
	make
}

package() {
	cd "${pkgname%-*}"/build

	make INSTALL_ROOT="${pkgdir}" install
}
