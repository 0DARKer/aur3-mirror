# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: John Williams <jwilliams4200 liamg reverse&remove moc>
pkgname=fio
pkgver=2.1
pkgrel=1
pkgdesc="Scriptable I/O tool for storage benchmarks and drive testing"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/fio"
license=('GPL2')
depends=('bash' 'libaio')
optdepends=('gfio: GTK2 frontend'
	    'gnuplot: generating plots using fio_generate_plots'
	    'numactl: NUMA support (must also be present at build time)')
changelog=ChangeLog
source=("http://brick.kernel.dk/snaps/fio-${pkgver}.tar.bz2")
sha256sums=('5cab5cf9d1a5ddf60a321143bac5d7babd8ad4788a060ad66422ef04ab163290')

build() {
	cd $srcdir/fio-${pkgver}
	./configure
	make
}

package() {
	cd $srcdir/fio-${pkgver}
	make DESTDIR="${pkgdir}" prefix="/usr" mandir="/usr/share/man" install

	# documentation
	install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
	install -m644 \
		HOWTO \
		LICENSE \
		README \
		REPORTING-BUGS \
		SERVER-TODO \
		"${pkgdir}/usr/share/doc/${pkgname}"

	# examples
	install -dm755 "${pkgdir}/usr/share/doc/${pkgname}/examples"
	install -m644 examples/* "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

