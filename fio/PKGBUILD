# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: John Williams <jwilliams4200 liamg reverse&remove moc>
pkgname=fio
pkgver=2.0.15
pkgrel=1
pkgdesc="Scriptable I/O tool for storage benchmarks and drive testing"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/fio"
license=('GPL2')
depends=('bash' 'libaio')
optdepends=('gnuplot: generating plots using fio_generate_plots'
            'numactl: NUMA support (must also be present at build time)')
changelog=ChangeLog
source=("http://brick.kernel.dk/snaps/fio-${pkgver}.tar.bz2")
sha256sums=('3cc1b5e6c6955f9acad016b4d2ef619061ad9937874c7663e2a29777ed845ac4')

build() {
	cd $srcdir/fio-${pkgver}
	make
}

package() {
	cd $srcdir/fio-${pkgver}
	make DESTDIR="${pkgdir}" prefix="/usr" mandir="/usr/share/man" install
	install -m 755 -d "${pkgdir}/usr/share/doc/fio"
	install -m 644 HOWTO README REPORTING-BUGS SERVER-TODO "${pkgdir}/usr/share/doc/fio" 
	install -m 755 -d "${pkgdir}/usr/share/doc/fio/examples"
	install -m 644 examples/* "${pkgdir}/usr/share/doc/fio/examples"
}

