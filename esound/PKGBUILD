# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=esound
pkgver=0.2.41
pkgrel=4
pkgdesc="Enlightened Sound Daemon"
arch=(i686 x86_64)
url="http://www.tux.org/~ricdude/EsounD.html"
license=('LGPL')
depends=('audiofile')
provides=("esd=$pkgver")
replaces=('esd')
conflicts=('esd')
backup=('etc/esd.conf')
source=("http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.2/$pkgname-$pkgver.tar.bz2"
        "esd.service")
md5sums=('8d9aad3d94d15e0d59ba9dc0ea990c6c'
         'ed6431a8912d21c03842cde1246688a5')

prepare() {
  cd $pkgname-$pkgver

  # alsa drain fix
  sed -i 's/drain/drop/' audio_alsa09.c
}
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-static \
              --with-audiofile \
              --without-libwrap \
              --enable-alsa \
              --disable-artstest
  make CFLAGS+=" -lm"
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # systemd service file
  install -Dm755 ../esd.service \
    "$pkgdir/usr/lib/systemd/system/esd.service"
}
