# Maintainer: Connor Behan <connor.behan@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=lilo
pkgver=23.2
pkgrel=4
pkgdesc="A bootloader for Linux"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/lilo/"
license=('BSD')
backup=('etc/lilo.conf')
depends=('device-mapper' 'coreutils')
makedepends=('bin86' 'sharutils')
optdepends=('perl: to use keytab-lilo')
install=lilo.install
options=('!makeflags')
source=("http://lilo.alioth.debian.org/ftp/sources/${pkgname}-${pkgver}.tar.gz"
        'lilo.conf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LC_ALL=C

  # If a de-cluttered version of common.s is already there,
  # the one with the problematic lines won't be regenerated.
  cd src
  gcc -E -C -traditional -DLILO_ASM -o common.s common.h
  sed -i -e '5,41d' common.s
  cd ..

  make all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/lilo.conf "${pkgdir}"/etc/lilo.conf
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  mkdir "${pkgdir}"/usr/bin
  mv "${pkgdir}"/sbin/lilo "${pkgdir}"/usr/bin/
  mv "${pkgdir}"/usr/sbin/* "${pkgdir}"/usr/bin/
  rm -rf "${pkgdir}"/sbin
  rm -rf "${pkgdir}"/usr/sbin
}
md5sums=('51b9b9db665d3b8724919e3d46054d12' 'ca2d8cd9b63f11444861939b42df29c1')
