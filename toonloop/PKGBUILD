# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=toonloop
pkgver=2.1.16
pkgrel=1
pkgdesc="A live stop motion animation software."
arch=('i686' 'x86_64')
url="http://toonloop.com/"
license=('GPL3')
depends=('boost-libs' 'clutter-gst' 'libavc1394' 'liblo' 'stk')
makedepends=('boost')
optdepends=('bash-completion: auto-completion support')
install="$pkgname.install"
source=("http://tarballs.quessy.net/$pkgname-$pkgver.tar.gz")
md5sums=('975c24df91fd57b00d542adc94711f9c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # does not build with --as-needed
  # added missing link for libboost_date_time
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed} -lboost_date_time"

  # fix uninitalized variable error
  sed "s|\(const gchar \*blend\);|\1 = NULL;|" -i src/gui.cpp

  ./configure --prefix=/usr \
              --sysconfdir=/etc

  make CXXFLAGS="-DBOOST_FILESYSTEM_VERSION=2"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
