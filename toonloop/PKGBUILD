# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=toonloop
pkgver=2.1.10
pkgrel=1
pkgdesc="Toonloop is a live stop motion animation software."
arch=('i686' 'x86_64')
url="http://toonloop.com/"
license=('GPL3')
depends=('boost-libs' 'cairo' 'clutter-gst' 'clutter-gtk' 'libavc1394' 'liblo' 'stk')
install="$pkgname.install"
source=("http://tarballs.quessy.net/$pkgname-$pkgver.tar.gz")
md5sums=('058069d9eaadec3d5eaaa774c8544eab')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # does not build with --as-needed
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  # clutter-gst 1.3.2 fix
  sed "s_\(clutter-gst-\)0.10_\11.0_" -i configure

  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
