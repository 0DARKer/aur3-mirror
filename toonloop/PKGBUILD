# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=toonloop
pkgver=2.1.10
pkgrel=2
pkgdesc="A live stop motion animation software."
arch=('i686' 'x86_64')
url="http://toonloop.com/"
license=('GPL3')
depends=('boost-libs' 'clutter-gst' 'clutter-gtk' 'libavc1394' 'liblo' 'stk')
makedepends=('boost')
install="$pkgname.install"
source=("http://tarballs.quessy.net/$pkgname-$pkgver.tar.gz")
md5sums=('058069d9eaadec3d5eaaa774c8544eab')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # does not build with --as-needed
  # added missing link for libboost_date_time
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed} -lboost_date_time"

  # clutter-gst fix
  sed "s_\(clutter-gst-\)0.10_\11.0_" -i configure

  ./configure --prefix=/usr \
              --sysconfdir=/etc

  make CXXFLAGS="-DBOOST_FILESYSTEM_VERSION=2"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
