# Mantainer Jens Staal <staal1978@gmail.com>

pkgbase=llvmlinux
pkgname=${pkgbase}-git

pkgdesc="The Linux Kernel built with LLVM/Clang"
depends=('coreutils' 'linux-firmware' 'mkinitcpio')

pkgver=906.5dd1ca1
#todo: get a human-readable version number
pkgrel=1
url="http://llvm.linuxfoundation.org/index.php/Main_Page"
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('git' 'quilt' 'python2' 'cmake' 'llvm' 'clang') 
#todo: we want to build with stock clang and skip that part of the building
options=(!strip)

#add the linux checkout in sources to avoid having to repeat it every time.
source=('llvmlinux'::'git://git.linuxfoundation.org/llvmlinux.git')
md5sums=('SKIP')

CC=clang
CXX=clang++
KARCH=x86

pkgver() {
	cd llvmlinux
	git describe --always | sed 's|-|.|g'
}

#32-bit build assume i586, so we need a variable for that
if [ ${CARCH} == i686 ]; then
	_lll_target=i586
else
	_lll_target=x86_64
fi

prepare() {
	cd ${srcdir}/llvmlinux/targets/${_lll_target}
	make kernel-fetch
	#this part will be removed when pre-build llvm/clang is used
	rm -rf ${srcdir}/bin
	mkdir -p ${srcdir}/bin
	ln -s /usr/bin/python2 ${srcdir}/bin/python
	export PATH=${srcdir}/bin:${PATH}
	#todo: import ideas from linux-git to easily add config and patches
	#to the build...
}

build() {
	cd ${srcdir}/llvmlinux/targets/${_lll_target}/
	make kernel-build
}

package() {
	cd ${srcdir}/llvmlinux/targets/${_lll_target}/src/linux
	mkdir -p "${pkgdir}"/{lib/modules,lib/firmware,boot}
	make LOCALVERSION= INSTALL_MOD_PATH="$pkgdir" modules_install
	cp arch/$KARCH/boot/bzImage "${pkgdir}/boot/vmlinuz-${pkgbase}"
	#todo: figure out how to do the mkinitcpio stuff.
	#for now - users are expected to do it manually
}