# Mantainer Jens Staal <staal1978@gmail.com>

pkgbase=llvmlinux
pkgname=${pkgbase}-git

pkgdesc="The Linux Kernel built with LLVM/Clang"
depends=('coreutils' 'linux-firmware' 'mkinitcpio')

pkgver=5dd1ca1
#todo: get a human-readable version number

pkgrel=1
url="http://llvm.linuxfoundation.org/index.php/Main_Page"
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('git' 'quilt' 'python2' 'cmake' 'llvm' 'clang') 
#todo: we want to build with stock clang and skip that part of the building
options=(!strip)

#add the source checkouts in sources to avoid having to repeat them.
source=('llvmlinux'::'git://git.linuxfoundation.org/llvmlinux.git' \
'kernel'::'git://git.kernel.org/pub/scm/linux/kernel/git/torvalds/linux.git' 'llvm'::'git+http://llvm.org/git/llvm.git' \
'clang'::'git+http://llvm.org/git/clang.git' 'compiler-rt'::'git+http://llvm.org/git/compiler-rt.git')
md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')

export CC=clang
export CXX=clang++
export KARCH=x86

#32-bit build assume i586, so we need a variable for that
if [ ${CARCH} == i686 ]; then
	_lll_target=i586
else
	_lll_target=x86_64
fi

pkgver() {
	cd llvmlinux
	git describe --always | sed 's|-|.|g'
}

prepare() {
	#ok some hacking needed to get this working...
	ln -s ${srcdir}/kernel ${srcdir}/llvmlinux/arch/all/kernel.git
	mkdir -p ${srcdir}/llvmlinux/toolchain/clang/src
	ln -s ${srcdir}/llvm ${srcdir}/llvmlinux/toolchain/clang/src/llvm
	ln -s ${srcdir}/clang ${srcdir}/llvmlinux/toolchain/clang/src/clang
	ln -s ${srcdir}/compiler-rt ${srcdir}/llvmlinux/toolchain/clang/src/compiler-rt

	cd ${srcdir}/llvmlinux/targets/${_lll_target}
	#needed? perhaps patching some other way?
	make kernel-fetch

	#this part will be removed when pre-built llvm/clang is used
	rm -rf ${srcdir}/bin
	mkdir -p ${srcdir}/bin
	ln -s /usr/bin/python2 ${srcdir}/bin/python
	export PATH=${srcdir}/bin:${PATH}

	#todo: import ideas from linux-git to easily add config and patches
	#to the build...
}

build() {
	cd ${srcdir}/llvmlinux/targets/${_lll_target}/
	make kernel-build
}

package() {
	cd ${srcdir}/llvmlinux/targets/${_lll_target}/src/linux
	mkdir -p "${pkgdir}"/{lib/modules,lib/firmware,boot}
	make LOCALVERSION= INSTALL_MOD_PATH="$pkgdir" modules_install
	cp arch/$KARCH/boot/bzImage "${pkgdir}/boot/vmlinuz-${pkgbase}"
	#todo: figure out how to do the mkinitcpio stuff.
	#for now - users are expected to do it manually
}