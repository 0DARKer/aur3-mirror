# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=kolourmatica
pkgver=0.1.1
pkgrel=1
pkgdesc="A simple colour space calculator."
arch=(i686 x86_64)
url="https://github.com/Arlen/Kolourmatica"
license=('GPL')
depends=('qt' 'boost-libs')
makedepends=('scons' 'boost' 'eigen3')
install="$pkgname.install"
source=("$url/zipball/$pkgver"
        "$pkgname.desktop")
md5sums=('3b977dbd650905eef32f5ad86e90f37e'
         '48b9db38d9d7b8d538b1f2486c6e90ec')

build() {
  cd `find "$srcdir" -mindepth 1 -maxdepth 1 -type d`

  scons mode=release moc=on
}

package() {
  cd `find "$srcdir" -mindepth 1 -maxdepth 1 -type d`

  # bin
  install -Dm755 bin/$pkgname "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icons
  for _s in 32 64 128; do
    install -Dm644 logo/logo$_s.png \
      "$pkgdir/usr/share/icons/hicolor/${_s}x${_s}/apps/$pkgname.png"
  done
}

# vim:set ts=2 sw=2 et:
