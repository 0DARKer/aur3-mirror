# Maintainer: fauno <fauno@kiwwwi.com.ar>
# Contributor: Artefact2 <artefact2@gmail.com>
# Contributor: shahid <helllamer@gmail.com>

pkgname=bitcoin-daemon
_pkgname=bitcoin
pkgver=0.3.19
pkgrel=1
pkgdesc="Bitcoin is a peer-to-peer network based digital currency."
arch=('i686' 'x86_64')
url="http://www.bitcoin.org/"
depends=('expat' 'boost-libs>=1.43')
makedepends=('boost')
conflicts=('bitcoin-bin' 'bitcoin')
license=('MIT')
install="bitcoin-daemon.install"
source=("http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-${pkgver}-linux.tar.gz"
	"makefile.archlinux"
	"bitcoin-daemon.install"
	"rc.bitcoind")

build() {
	cd $srcdir/${_pkgname}-${pkgver}/src
	
	# copy correct makefile to src dir
	cp $srcdir/makefile.archlinux Makefile
	
	# to build (single-threaded make due to OOM issues)
	make bitcoind
}

package() {
    cd $srcdir/${_pkgname}-${pkgver}
	mkdir -p $pkgdir/{usr/bin,usr/share/licenses/$pkgname,etc/rc.d,var/lib/$pkgname}
	
	install -D -m755 ./src/bitcoind $pkgdir/usr/bin/
	install -D -m755 $srcdir/rc.bitcoind $pkgdir/etc/rc.d/bitcoind
    install -m 644 ./license.txt $pkgdir/usr/share/licenses/$pkgname/
	
	# strip shit from executables
	find $pkgdir/usr/bin/ -type f | xargs -L1 strip
	
	# the bitcoin data directory should not readable by others (wallet is sensitive data)
	chown -R 8333:8333 $pkgdir/var/lib/$pkgname
	chmod -R 700 $pkgdir/var/lib/$pkgname
}
md5sums=('67c0d4a5c7310a94ee1ed8b2effa22cb'
         'c8676f09f2ab8e2e216cc694ff00168e'
         'a388684057de2bb543cb7f795dc693cf'
         '4a9ab4997487bb5f8e3c6b1d580045af')
