# Maintainer: Sebastien Duthil <duthils@free.fr>

pkgname=factorio-demo
pkgver=0.10.12
pkgrel=1
pkgdesc="A 2D game about building and maintaining factories."
arch=('i686' 'x86_64')
url="http://www.factorio.com/"
license=('custom: commercial')
depends=('libxcursor' 'alsa-lib' 'libxrandr' 'libxinerama' 'mesa')
conflicts=('factorio')
source=(factorio.desktop
        LICENSE)
md5sums=('5cacd198a3156c157a482aeab368e9a1'
         '22fce67d66c080fa5c89ab437b3d8724')

if test "$CARCH" == i686; then
  __factorio_arch=i386
  source+=(factorio_demo_${__factorio_arch}_${pkgver}.tar.gz::http://www.factorio.com/get-download/eu-de/${pkgver}/demo/linux32)
  md5sums+=('35a3ffefa12306c54f9ef493df00d181')
elif test "$CARCH" == x86_64; then
  __factorio_arch=x64
  source+=(factorio_demo_${__factorio_arch}_${pkgver}.tar.gz::http://www.factorio.com/get-download/eu-de/${pkgver}/demo/linux64)
  md5sums+=('1de9995eca2c2fbcc3aa4e9b954a751a')
fi

# no modifications needed, the executable looks for:
# - data in /usr/share/factorio
# - config in ~/.factorio

package() {
  cd "$srcdir/factorio"

  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/usr/share/applications"
  install -d "${pkgdir}/usr/share/factorio"
  install -d "${pkgdir}/usr/share/licenses/factorio-demo"

  install -m755 "bin/${__factorio_arch}/factorio" "${pkgdir}/usr/bin/factorio"
  cp -r data/* "${pkgdir}/usr/share/factorio"
  install -m644 "${srcdir}/factorio.desktop" "${pkgdir}/usr/share/applications/factorio.desktop"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/factorio-demo/LICENSE"
}
