# Maintainer: Sebastien Duthil <duthils@free.fr>

pkgname=factorio-demo
pkgver=0.9.8
pkgrel=3
pkgdesc="A 2D game about building and maintaining factories."
arch=('i686' 'x86_64')
url="http://www.factorio.com/"
license=('custom: commercial')
depends=('libxcursor' 'gcc-libs' 'alsa-lib' 'libxrandr' 'libxinerama' 'mesa')
conflicts=('factorio')
source=(factorio.desktop
        LICENSE)
md5sums=('5cacd198a3156c157a482aeab368e9a1'
         '22fce67d66c080fa5c89ab437b3d8724')

if test "$CARCH" == i686; then
  __factorio_arch=i386
  source+=(factorio_demo_${__factorio_arch}_$pkgver.tar.gz::http://www.factorio.com/get-download/eu-de/$pkgver/demo/linux32)
  md5sums+=('05cae1024d1acbb49fb9fe0ffe63a85a')
elif test "$CARCH" == x86_64; then
  __factorio_arch=x64
  source+=(factorio_demo_${__factorio_arch}_$pkgver.tar.gz::http://www.factorio.com/get-download/eu-de/$pkgver/demo/linux64)
  md5sums+=('082069a418f35c1671c1dc45c5000435')
fi

# no modifications needed, the executable looks for:
# - data in /usr/share/factorio
# - config in ~/.factorio

package() {
  cd "$srcdir/factorio"

  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/usr/share/applications"
  install -d "${pkgdir}/usr/share/factorio"
  install -d "${pkgdir}/usr/share/licenses/factorio"

  install -m755 "bin/${__factorio_arch}/factorio" "$pkgdir/usr/bin/factorio"
  cp -r data/* "$pkgdir/usr/share/factorio"
  install -m644 "${srcdir}/factorio.desktop" "${pkgdir}/usr/share/applications/factorio.desktop"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/factorio/LICENSE"
}
