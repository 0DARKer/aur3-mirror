# Maintainer: Sebastien Duthil <duthils@free.fr>
pkgname=factorio-demo
pkgver=0.7.5
pkgrel=1
pkgdesc="A 2D game about building and maintaining factories."
arch=('i686' 'x86_64')
url="http://www.factorio.com/"
license=('custom: commercial')
conflicts=('factorio')
source=(factorio.desktop)
md5sums=('5cacd198a3156c157a482aeab368e9a1')
if test "$CARCH" == i686; then
  __factorio_arch=i386
  source+=(factorio_demo_${__factorio_arch}_$pkgver.tar.gz::http://www.factorio.com/get-download/eu-de/0.7.5/demo/linux32)
  md5sums+=('4061559f39165b9cf65fb9279f117cdd')
elif test "$CARCH" == x86_64; then
  __factorio_arch=x64
  source+=(factorio_demo_${__factorio_arch}_$pkgver.tar.gz::http://www.factorio.com/get-download/eu-de/0.7.5/demo/linux64)
  md5sums+=('ae8e4c5979bcb7eeb41f48027c82d2c6')
fi

# no modifications needed, the executable looks for:
# - data in /usr/share/factorio
# - config in ~/.factorio

package() {
  cd "$srcdir/factorio"

  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/usr/share/applications"
  install -d "${pkgdir}/usr/share/factorio"

  install -m755 "bin/${__factorio_arch}/factorio" "$pkgdir/usr/bin/factorio"
  cp -r data/* "$pkgdir/usr/share/factorio"
  install -m644 "${srcdir}/factorio.desktop" "${pkgdir}/usr/share/applications/factorio.desktop"
}
