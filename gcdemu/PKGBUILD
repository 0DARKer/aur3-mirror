# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>
# Maintainer: Alexsandr Pavlov <kidoz at mail dot ru>
pkgname=gcdemu
pkgver=1.3.0
pkgrel=4
pkgdesc="GNOME panel applet controlling cdemu-daemon"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL2')
depends=('python2' 'pygtk' 'pygobject' 'gnome-python' 'gnome-python-desktop' 'dbus-python' 'cdemu-daemon' 'gnome-doc-utils')
makedepends=('intltool')
install=gcdemu.install
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.gz)
md5sums=('d666ebe40cac1aee01eb64c6c7e60ff2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '18s/false/true/g' src/gcdemu.schemas.in
  sed -i '1s/python/python2/g' src/gcdemu
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/bin --disable-scrollkeeper PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gcdemu ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -r ${pkgdir}/etc
}
