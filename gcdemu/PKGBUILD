# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>
# Maintainer: Alexsandr Pavlov <kidoz at mail dot ru>
pkgname=gcdemu
pkgver=2.1.0
pkgrel=1
pkgdesc="GNOME panel applet controlling cdemu-daemon"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL2')
depends=('cdemu-daemon' 'python2' 'python2-gobject' 'python2-notify' 'gtk3')
makedepends=('cmake' 'intltool')
install=gcdemu.install
source=(http://downloads.sourceforge.net/cdemu/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('1428192ba37124a8f756c5cb5a3bdce0c32745ff429abde9adad4133d41e1254')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i '1s/python/python2/g' src/gcdemu

  PYTHON=python2 cmake -DPOST_INSTALL_HOOKS=off -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install
}
