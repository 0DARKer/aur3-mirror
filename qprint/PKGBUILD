# Maintainer: Limao Luo <luolimao+AUR@gmail.com>

pkgname=qprint
pkgver=1.0
pkgrel=2
pkgdesc="Quoted-Printable format (RFC1521) encode / decode."
arch=(i686 x86_64)
url=http://www.fourmilab.ch/webtools/$pkgname
license=('custom:public domain')
source=($url/$pkgname-$pkgver.tar.gz)
sha256sums=('a92f02f2b1c12e29895df7ce200bd8001ce6be9ef092ef14284f94918719bfa8')
sha512sums=('2cba2e4c986e51485f26988173d16bfdbecbc9e84aeb7abbf3e77fba8f9d5ad442c0d61a8d17f4b715c0ec0b3ec25b948438fa5c88a0777d7aff7b6d99c40a31')

build() {
    cd "$srcdir"/$pkgname-$pkgver/
    ./configure --prefix=/usr --mandir=/usr/share/man
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver/
    install -d "$pkgdir"/usr/{bin,share/man/man1}
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
