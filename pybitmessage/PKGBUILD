# Maintainer: Evgeny Kurnevsky <kurnevsky@gmail.com>
# Maintainer: Bob Mottram (4096 bits) <bob@robotics.uk.to>
# Maintainer: Andy Weidenbaum <archbaum@gmail.com>

pkgname=pybitmessage
_realname=PyBitmessage
pkgver=0.4.2
pkgrel=2
pkgdesc="Decentralized and trustless P2P communications protocol for sending encrypted messages to another person or to many subscribers"
arch=('any')
depends=('mpg123' 'openssl' 'python2' 'python2-pyqt4' 'qt4' 'sqlite')
optdepends=('python2-gevent: Python network library that uses greenlet and libevent for easy and scalable concurrency')
url="https://github.com/Bitmessage/PyBitmessage"
license=('MIT')
source=($pkgname-$pkgver.tar.gz::https://github.com/Bitmessage/$_realname/archive/v$pkgver.tar.gz
        pybitmessage.sh)
md5sums=('6feb4aed0d35e6626ae0080b4b00e831'
         '599d47f6c79e51276be2b6ebe3b32e03')
sha256sums=('c69cef72adbdfa2a0ff8c5c7e5d15e640bf3283b9089c388ea48e893d564a573'
            'f4fb3f9f77778e4282e0412248136a2dca6efecc65f120871bd9d2e9e1ba7d5c')
provides=('pybitmessage')
conflicts=('pybitmessage-git')

prepare() {
  cd "$srcdir/$_realname-$pkgver"

  msg 'Fixing Python version...'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/python#/usr/bin/python2#g'
  find . -type f -print0 | xargs -0 sed -i 's#/usr/bin/env python#/usr/bin/env python2#g'
}

package() {
  cd "$srcdir/$_realname-$pkgver"

  msg 'Installing shared files...'
  mkdir -p "$pkgdir"/usr/share/$pkgname
  cp -R src/* "$pkgdir"/usr/share/$pkgname

  msg 'Installing desktop icons...'
  install -D -m644 desktop/pybitmessage.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -D -m644 src/images/can-icon-24px.png "$pkgdir/usr/share/icons/hicolor/24x24/apps/$pkgname.png"
  install -D -m644 desktop/can-icon.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
  install -D -m644 desktop/can-icon.svg "$pkgdir/usr/share/pixmaps/$pkgname.svg"

  msg 'Installing executable...'
  install -D -m755 $srcdir/pybitmessage.sh "$pkgdir/usr/bin/$pkgname"

  msg 'Installing man page...'
  mkdir -p "$pkgdir/usr/share/man/man1"
  install -m644 man/pybitmessage.1.gz "$pkgdir/usr/share/man/man1"
}
