# Maintainer: Yann Kaiser <kaiser.yann@gmail.com>
# Maintainer: mib1982 <michael.bentlage@web.de>
pkgname=cavestory+
pkgver=1355961600
pkgrel=3
pkgdesc="A platform-adventure game set in a cave"
arch=('i686' 'x86_64')
url="http://nicalis.com/games.php?game=cavestory"
license=('custom')
depends=('libgl' 'mesa' 'glibc' 'gcc-libs'
         'libxext' 'libx11' 'libxcb' 'libxau')
makedepends=()
optdepends=('hib-dlagent: Tool to download Humble Indie Bundle binaries by file name.')
_archive_name="cave_story_plus"
source=("hib://${_archive_name}-linux-r100.tar.bz2" "cavestory+.sh" "cavestory+.desktop" "icon.png")
md5sums=('b7ecd65644b8607bc177d7ce670f2185'
         '245e7709eec9f5ee44d7ea26f2051175'
         'afec0d05cbe8e16626f145050ef1e6de'
         '2cc33b1b91383109cf1576a28215cdf7')

package() {
  cd ${srcdir}/CaveStoryPlus

  rm -r lib/ lib64/
  cp ${srcdir}/${pkgname}.sh ${srcdir}/icon.png .

  if test "${CARCH}" == "x86_64"; then
    rm CaveStory+
    mv CaveStory+_64 CaveStory+
  else
    rm CaveStory+_64
  fi

  mkdir -p ${pkgdir}/opt/ ${pkgdir}/usr/bin/ \
    ${pkgdir}/usr/share/applications
  cp -r . ${pkgdir}/opt/${_archive_name}
  cp ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/
  ln -s /opt/${_archive_name}/${pkgname}.sh \
    ${pkgdir}/usr/bin/${pkgname}
  chmod a+r ${pkgdir}/usr/share/applications/${pkgname}.desktop \
    ${pkgdir}/opt/${_archive_name}/icon.png
}

# vim: et ts=2 sw=2 sts=2