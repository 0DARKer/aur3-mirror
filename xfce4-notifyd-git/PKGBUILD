# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Zbyszek Tokarczyk <ztokarczyk (at) Gmail.com>

pkgname=xfce4-notifyd-git
pkgver=0.2.3.3.g8a38a78
pkgrel=1
pkgdesc='notification daemon for the xfce desktop - git checkout'
arch=('i686' 'x86_64')
license=('GPL')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
groups=('xfce4-goodies')
depends=('libxfce4ui' 'hicolor-icon-theme')
makedepends=('xfce4-dev-tools' 'intltool' 'git')
conflicts=('xfce4-notifyd')
provides=('notification-daemon' 'xfce4-notifyd')
options=('!libtool')
install=${pkgname}.install

source=('git://git.xfce.org/apps/xfce4-notifyd')

pkgver() {
	cd xfce4-notifyd/
	git describe --tags --long | sed 's|^xfce4-[^0-9]*||;s|-|.|g'
}

build() {
	cd xfce4-notifyd/

	./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/xfce4 \
		--localstatedir=/var \
		--disable-static
	make
}

package() {
	cd xfce4-notifyd/

	make DESTDIR=${pkgdir} install
}

sha256sums=('SKIP')
