# $Id: PKGBUILD 111127 2011-02-24 08:00:08Z bisson $
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

_pkgname=tsocks
pkgname=${_pkgname}-multilib
pkgver=1.8beta5
pkgrel=2
pkgdesc='Transparent SOCKS proxying library - multilib version'
arch=('x86_64')
url='http://tsocks.sourceforge.net/'
license=('GPL')
makedepends=('gcc-multilib')
provides=('tsocks')
conflicts=('tsocks')
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha1sums=('489f88c5df999ba21b27cdaa7836d9426d062aec')

build() {
	cp -a "${srcdir}/${_pkgname}-1.8" "${srcdir}/${_pkgname}-1.8-64"
	cp -a "${srcdir}/${_pkgname}-1.8" "${srcdir}/${_pkgname}-1.8-32"

	cd "${srcdir}/${_pkgname}-1.8-64"
	autoreconf
	./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib
	make

	cd "${srcdir}/${_pkgname}-1.8-32"
	autoreconf
	CC='gcc -m32' ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib32
	make
}

package() {
	cd "${srcdir}/${_pkgname}-1.8-64"
	make DESTDIR="${pkgdir}" install

	cd "${srcdir}/${_pkgname}-1.8-32"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/${_pkgname}"
	install -m644 tsocks.conf.{simple,complex}.example "${pkgdir}/usr/share/${_pkgname}"

	# Fix up path so ld.so will find both the 64bit and the 32bit library
	sed -i -e 's|/usr/lib/||g' "${pkgdir}/usr/bin/tsocks"
}
