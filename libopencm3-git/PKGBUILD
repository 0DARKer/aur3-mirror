# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

pkgname=libopencm3-git
pkgver=2012.11.12
pkgrel=4
pkgdesc="Open Source firmware for various ARM Cortex microcontrollers"
arch=(any)
url="http://www.libopencm3.org/"
license=('GPL')
groups=('cross-arm-none-eabi')
depends=('cross-arm-none-eabi-gcc')
makedepends=(git python-yaml)
options=(!strip)
source=(libopencm3-reset-vector-naked.patch)
sha256sums=('099b7e13d2cb9ad3803681def5af3a773d32549541e123a6911f62c9abe38aac')

_git_root="git://github.com/libopencm3/libopencm3.git"
_git_name="libopencm3"
_git_commit="70746ccd676445481511c0e8bc6b617f7b052878"

build() {
  cd "${srcdir}"
  
  # Get rid of old build directories
  rm -rf "${_git_name}-build"

  # Clone or update sources from openocd git repository
  if [ -d ${srcdir}/${_git_name} ]; then
    msg "Updating Git repository"
    cd ${_git_name} && git pull
  else
    msg "Cloning Git repository"
    git clone ${_git_root}
  fi

  msg "Preparing build directory"
  git clone "${srcdir}/${_git_name}" "${srcdir}/${_git_name}-build"

  cd "${srcdir}/${_git_name}-build"

  # Check out specific commit for repeatability
  git checkout --detach ${_git_commit}

  patch -Np1 -i "${srcdir}/libopencm3-reset-vector-naked.patch"

  make lib
}

package() {
  cd "${srcdir}/${_git_name}-build"

  make DESTDIR="${pkgdir}/usr" install
}

# vim: set ts=2 sw=2 ft=sh noet:
