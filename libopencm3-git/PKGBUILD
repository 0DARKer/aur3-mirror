# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

pkgname=libopencm3-git
pkgver=2013.02.27
pkgrel=1
pkgdesc="Open Source firmware library for various ARM Cortex microcontrollers"
arch=(any)
url="http://www.libopencm3.org/"
license=('GPL')
groups=('cross-arm-none-eabi')
depends=('cross-arm-none-eabi-gcc')
makedepends=(git python-yaml)
options=(!strip)
source=(gcc-4.8.0-CFLAGS.patch)
sha256sums=('ca715b6848a0aa56015edce1378b8140b124fdb64a8354bc4dc4b2a758bf8a54')

_git_root="git://github.com/libopencm3/libopencm3.git"
_git_name="libopencm3"
_git_commit="5b8953124e8c00cdf05169de6e304834cefacf63"

build() {
  cd "${srcdir}"
  
  # Get rid of old build directories
  rm -rf "${_git_name}-build"

  # Clone or update sources from openocd git repository
  if [ -d ${srcdir}/${_git_name} ]; then
    msg "Updating Git repository"
    cd ${_git_name} && git pull
  else
    msg "Cloning Git repository"
    git clone ${_git_root}
  fi

  msg "Preparing build directory"
  git clone "${srcdir}/${_git_name}" "${srcdir}/${_git_name}-build"

  cd "${srcdir}/${_git_name}-build"

  # Check out specific commit for repeatability
  git checkout --detach ${_git_commit}

  patch -Np1 -i "${srcdir}/gcc-4.8.0-CFLAGS.patch"

  make lib
}

package() {
  cd "${srcdir}/${_git_name}-build"

  make DESTDIR="${pkgdir}/usr" install
}

# vim: set ts=2 sw=2 ft=sh noet:
