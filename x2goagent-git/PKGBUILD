# Maintainer: Markus Opitz <mastero23 at gmail dot com>

pkgname=x2goagent-git
pkgver=20111230
pkgrel=1
pkgdesc="X proxy server for x2go"
arch=('i686' 'x86_64')
url="http://www.x2go.org/"
license=('GPL2')
depends=('freetype2' 'nxcompext-x2go-git' 'nxcompshad-x2go-git'
         'libxcomposite' 'libxpm')
conflicts=('x2goagent')
source=('x2goagent.sh')
md5sums=('e33bf541d0459ccee08179496860353a')

_gitname="${pkgname%-git}"
_gitroot="git://code.x2go.org/$_gitname.git"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make BINDIR=/usr/bin SHLIBDIR=/usr/lib/x2go World
}

package() {
  cd "$srcdir/$_gitname-build"
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/lib/x2go"

  cp -a lib/X11/libX11.so.* \
    lib/Xext/libXext.so.* \
    lib/Xrender/libXrender.so.* \
    "$pkgdir/usr/lib/x2go"

  cp -a programs/Xserver/x2goagent "$pkgdir/usr/bin/x2goagent.bin"
  install -m 755 "$srcdir/x2goagent.sh" "$pkgdir/usr/bin/x2goagent"
}
