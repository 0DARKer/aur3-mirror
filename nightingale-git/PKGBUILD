# Maintainer: Matt Parnell/ilikenwf
# Cleanup: Sam Stuewe <halosghost@archlinux.info>
_gitname="nightingale-hacking"
_branch="sb-trunk-oldxul"
pkgname="nightingale-git"
pkgver=d72624b
pkgrel=1
pkgdesc="Community port of Songbird to be more Linux native, up to date, and open."
arch=('i686' 'x86_64')
url="http://getnightingale.com/"
license=('GPL2' 'MPL' 'BSD')
makedepends=('git' 'subversion' 'python' 'unzip' 'libidl2' 'zip' 'glib2')
depends=('gstreamer0.10-base-plugins' 'gstreamer0.10' 'gstreamer0.10-base' 'gtk2' 'libxt' 'sqlite3' 
         'gstreamer0.10-good-plugins' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-ffmpeg' 
         'gstreamer0.10-ugly-plugins' 'taglib>=1.8')
conflicts=('nightingale')
provides=('nightingale')
install="nightingale.install"
source=("nightingale::git://github.com/nightingale-media-player/${_gitname}.git#branch=${_branch}"
        "Nightingale.desktop"
        'http://downloads.sourceforge.net/project/ngale/1.12-Build-Deps/vendor-1.12.zip'
        "http://downloads.sourceforge.net/project/ngale/1.12-Build-Deps/linux-${CARCH}-1.12-20130316-release-final.tar.lzma")
md5sums=('SKIP'
         '7741cc247648e95dd9dad8c953616757'
         '208c81bfd972ae4cb1f350688da304e6')
[[ "${CARCH}" == 'i686' ]] && md5sums+=('c30cc1d763d8c5cc0b0a2ae8216af18b')
[[ "${CARCH}" == 'x86_64' ]] && md5sums+=('a9b47ef0b21106f6b51231046e1758d1')

pkgver() {
	cd "nightingale"
	git describe --always | sed 's|-|.|g'
}

prepare() {
   cd "${srcdir}/nightingale/dependencies"
   ln -sf "${srcdir}/linux-$CARCH" ./
   ln -sf "${srcdir}/vendor" ./

   export GST_PLUGIN_PATH="/usr/lib/gstreamer-0.10"
   echo 'ac_add_options --with-media-core=gstreamer-system' >> "${srcdir}/nightingale/nightingale.config"
}

build() {	
	cd "${srcdir}/nightingale"
	make
	
	# copy the extensions first
	[ -d ../xpi-stage ] && rm -rf ../xpi-stage
	cp -a "${srcdir}/nightingale/compiled/xpi-stage" "${srcdir}"
}

package() {
	install -d --group=users "${pkgdir}/opt/nightingale"
	cd "nightingale/compiled/dist"
	cp -a --no-preserve=ownership * ${pkgdir}/opt/nightingale/

	chmod 755 "${pkgdir}/opt/nightingale/nightingale-bin"
	chmod 755 "${pkgdir}/opt/nightingale/xulrunner/xulrunner-bin"
	chmod -R a+r "${pkgdir}/opt/nightingale"
	
	install -D "${srcdir}/nightingale/compiled/dist/chrome/icons/default/default.xpm" \
		"${pkgdir}/usr/share/pixmaps/nightingale.xpm"
	install -Dm644 "${srcdir}/Nightingale.desktop" \
		"${pkgdir}/usr/share/applications/Nightingale.desktop"

	find "${pkgdir}" -type d -name .git -exec rm -r '{}' +
}
