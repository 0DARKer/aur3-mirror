# Maintainer: J0k3r <moebius282 e4at gmail D0_T com>

pkgname=superhexagon
pkgver=15
pkgrel=1
pkgdesc="A minimal action game by Terry Cavanagh, with music by Chipzel"
url="http://superhexagon.com/"
license=('custom')
arch=('i686' 'x86_64')
groups=("games")
depends=('gcc-libs' 'freeglut' 'libvorbis' 'openal' )
makedepends=('unzip')
_purgelibs=('libglut.so.3' 'libogg.so.0' 'libopenal.so.1' 'libstdc++.so.6' 'libvorbis.so.0' 'libvorbisfile.so.3')
source=("hib://super-hexagon-linux-${pkgver}-bin"
        # "http://www.flibitijibibo.com/patch/superhexagon/linux-03072013.tar.gz"
        "${pkgname}.desktop")
sha256sums=('3dbcf28ca1e67f2d61c1e1038947ef176f4f17b90688a24605794a08f06379b3'
            # '207dabd4a1eb463c1a64df346091e5b76246e6716e61e0cff5aa379d728af4f9'
            'c4584c99f2c29b7b5eb9258789a98427f22786eff5c286f7bfca2c98d71ee2e6')
#options=('!strip')
#PKGEXT=".pkg.tar"


# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf."; exit 1')


package() {

  mkdir -p "${pkgdir}/opt/${pkgname}/"

# unzip is stupid..
  unzip -qq "${srcdir}/super-hexagon-linux-${pkgver}-bin" -d "${pkgdir}/opt/tmp/" 'data/*' || true

  mv "${pkgdir}/opt/tmp/data/"* "${pkgdir}/opt/${pkgname}/"
  rm -r "${pkgdir}/opt/tmp/"

if [ "$CARCH" = "x86_64" ]; then

  rm -r "${pkgdir}/opt/${pkgname}/x86/"
  for i in "${_purgelibs[@]}"; do
    rm "${pkgdir}/opt/${pkgname}/x86_64/${i}"
  done
  
  # msg "Patching files ..."
  # mv -f "${srcdir}/x86_64/superhexagon.x86_64" "${pkgdir}/opt/${pkgname}/x86_64/"

else

  rm -r "${pkgdir}/opt/${pkgname}/x86_64/"
  for i in "${_purgelibs[@]}"; do
    rm "${pkgdir}/opt/${pkgname}/x86/${i}"
  done

  # msg "Patching files ..."
  # mv -f "${srcdir}/x86/superhexagon.x86" "${pkgdir}/opt/${pkgname}/x86/"

fi

  sed -i -e 's|`dirname "$0"`|$(dirname \"$(readlink -f \"$0\")\")|' "${pkgdir}/opt/${pkgname}/SuperHexagon" 

  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "${pkgdir}/opt/${pkgname}/SuperHexagon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  mkdir "${pkgdir}/usr/bin/"
  ln -s "/opt/${pkgname}/SuperHexagon" "${pkgdir}/usr/bin/${pkgname}"
}
