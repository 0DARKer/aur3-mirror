# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>

pkgname=media-explorer
pkgver=0.1.6
pkgrel=1
pkgdesc="A media centre application for Linux (including MeeGo)"
arch=('i686' 'x86_64')
url="http://media-explorer.github.com/"
license=('LGPL')
depends=('lirc-utils' 'mx>=1.3' 'hicolor-icon-theme' 'grilo-plugins'
         'gnome-desktop2' 'clutter-gst' 'libxtst' 'rest') 
makedepends=('gnome-common' 'intltool' 'gtk-doc' 'tracker' 'gssdp')
optdepends=('gnome-settings-daemon: media player keys support'
            'gnome-dvb-daemon: dvb support'
            'quvi: youtube plugin support'
            'gdata: youtube plugin support'
            'gupnp-av: upnp plugin support'
            'tracker: webremote and tracker plugin support')
provides=("mex=$pkgver")
conflicts=('mex' 'mex-git')
options=('!libtool' '!emptydirs')
install="$pkgname.install"
source=("https://github.com/media-explorer/$pkgname/tarball/$pkgver")
sha256sums=('c30e928946d47b1607eceafff3473692a9aa77c7c2153823744c746f2524121f')

build() {
  cd "$srcdir/$pkgname"*

  # python2 fix
  sed -i "s|python |python2 |" \
    {mex,applets/rebinder}/Makefile*

  ./autogen.sh --prefix=/usr \
               --libexecdir=/usr/bin \
               --sysconfdir=/etc \
               --enable-displayconf=yes \
               --enable-networks=yes \
               --enable-rebinder=yes \
               --enable-lirc
  make
}

package() {
  cd "$srcdir/$pkgname"*

  make DESTDIR="$pkgdir/" install

  # prevent conflicts with texlive-bin mex
  mv "$pkgdir/usr/bin/mex" \
    "$pkgdir/usr/bin/$pkgname"

  # fix desktop file
  sed -i "s/Exec=mex/Exec=$pkgname/" \
    "$pkgdir/usr/share/applications/mex.desktop"
}
