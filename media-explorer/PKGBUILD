# Maintainer: Jonathan Lestrelin <zanko@daemontux.org>
pkgname=media-explorer
pkgver=0.1.2
pkgrel=1
pkgdesc="Media Explorer is a media centre application for Linux (including MeeGo)."
arch=('i686' 'x86_64')
url="http://media-explorer.github.com/"
license=('LGPL')
depends=('lirc' 'gtk-doc' 'mx-1.2' 'rest' 'hicolor-icon-theme' ' xdg-utils' 'grilo' 'grilo-plugins' 'udev' 'gnome-desktop2' 'libxtst' 'clutter-gst') 
makedepends=('gnome-common' 'intltool' 'pkg-config')
source=(https://github.com/media-explorer/${pkgname}/tarball/${pkgver})
install=media-explorer.install
sha256sums=('38b9d3dd932198912c776bba7d5ddde920a3ee6581db95d4cdf5805851db6037')

build() {
  cd "${srcdir}"/${pkgname}*

  # Use the good python version
  sed -i -e "s#python#python2.7#" mex/Makefile* applets/rebinder/Makefile*
  sed -i -e "s#data.version = mx_label_new_with_text#//data.version = mx_label_new_with_text#" shell/mex-main.c

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --enable-displayconf=yes --enable-networks=yes --enable-lirc
  make
}

package() {
  cd "${srcdir}"/${pkgname}*

  DESTDIR=${pkgdir} make install

  # Avoid name clash with texlive-bin
  mv ${pkgdir}/usr/bin/mex ${pkgdir}/usr/bin/mexp
  sed -i -e "s/Exec=mex/Exec=mexp/" ${pkgdir}/usr/share/applications/mex.desktop
}
