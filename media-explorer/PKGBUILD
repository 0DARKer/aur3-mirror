# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>

pkgname=media-explorer
pkgver=0.2.2
pkgrel=1
pkgdesc="A media centre application for Linux (including MeeGo)"
arch=('i686' 'x86_64')
url="http://media-explorer.github.com/"
license=('LGPL')
depends=('lirc-utils' 'mx>=1.3' 'hicolor-icon-theme' 'grilo-plugins'
         'gnome-desktop2' 'clutter-gst' 'libxtst' 'rest' 'libunwind') 
makedepends=('gnome-common' 'intltool' 'gtk-doc' 'tracker' 'gssdp')
optdepends=('gnome-settings-daemon: media player keys support'
            'gnome-dvb-daemon: dvb support'
            'quvi: youtube plugin support'
            'gdata: youtube plugin support'
            'gupnp-av: upnp plugin support'
            'tracker: webremote and tracker plugin support')
provides=("mex=$pkgver")
conflicts=('mex' 'mex-git')
options=('!libtool' '!emptydirs')
install="$pkgname.install"
source=("https://github.com/media-explorer/$pkgname/tarball/$pkgver")
sha256sums=('b0c2c61f42a4868ad825a5dc42da9b207696d095b2f7101c5326a82ea14bed22')

build() {
  cd "$srcdir/$pkgname"*

  # python2 fix
  sed -i "s|python |python2 |" \
    {mex,applets/rebinder}/Makefile*

  ./autogen.sh --prefix=/usr \
               --libexecdir=/usr/bin \
               --sysconfdir=/etc \
               --enable-displayconf=yes \
               --enable-networks=yes \
               --enable-rebinder=yes \
               --enable-lirc
  make
}

package() {
  cd "$srcdir/$pkgname"*

  make DESTDIR="$pkgdir/" install
}
