pkgname=sundtek-ffmpeg-plugin
pkgver=20130317
pkgrel=1
pkgdesc="Sundtek driver plugin to use analog TV with tvheadend"
arch=('i686' 'x86_64')
url="http://sundtek.de/support/buildffmpeg.sh"
license=('LGPL')
depends=('sundtek')
makedepends=('git' 'lame' 'yasm')
_gitroot=git://source.ffmpeg.org/ffmpeg.git
_gitname=ffmpeg
_gitrev=e9b8523d52ca84d5012168db24fec2d50e73cf22

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server..."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"
  git checkout ${_gitrev}

  ./configure --disable-filters --disable-devices --disable-encoders --disable-decoders --disable-muxers --disable-demuxers --enable-shared --enable-pthreads --enable-libmp3lame --enable-encoder=mpeg2video,libmp3lame,mp2 --enable-nonfree --disable-bsfs --disable-protocols --disable-parsers --enable-muxer=mpegts,mpeg2video,mpeg2dvd --prefix=/opt/bin/plugins
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
