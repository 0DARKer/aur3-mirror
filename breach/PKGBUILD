# Maintainer: Eugenio Ferreira <eugfcl@gmail.com>

pkgname=breach
pkgver=0.3.20
pkgrel=1
pkgdesc="A Browser written in JS. Free. Modular. Hackable."
url="http://breach.cc/"

_pkgsubver="alpha.5"
_pkgfullver="${pkgver}-${_pkgsubver}"
_pkgfullname="${pkgname}-v${_pkgfullver}-linux-x64"

arch=('x86_64')
license=('MIT')
provides=('breach')
depends=('nodejs' 'gtk2' 'gconf' 'libxtst' 'gcc-libs-multilib' 'nss' 'alsa-lib' 'libxtst' 'python')
source=("https://raw.githubusercontent.com/breach/releases/master/v${_pkgfullver}/breach-v${_pkgfullver}-linux-x64.tar.gz")
md5sums=('98b58d060f95722158b9d4277ecdddd2')

prepare() {
  cd "$srcdir/$_pkgfullname"

  # avoid exporting useless variables
  sed -i 's/^export.*//' breach

  # specify installation path
  sed -i 's/^\$SRC_DIR/\/opt\/breach/' breach

  # apply suid patch
  # https://github.com/breach/breach_core/issues/60
  sed -i 'N;s/ \+\\.*/ --disable-setuid-sandbox/' breach 
}

package() {
  cd "$srcdir/$_pkgfullname"

  local _installdir='/opt/breach'
  local _license='__AUTO_UPDATE_BUNDLE__/breach_core/LICENSE'

  install -D -m644 "$_license" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d -m755 "$pkgdir/bin"
  install -d -m755 "$pkgdir/$_installdir"

  cp -r * "$pkgdir/$_installdir"

  ln -s "$_installdir/$pkgname" "$pkgdir/bin"
}
