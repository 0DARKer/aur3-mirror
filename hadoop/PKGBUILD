# Contributor: Mantas Vidutis <mantas.a.vidutis-at-gmail.com>

pkgname=hadoop
pkgver=0.21.0
_devver=0.21.1-dev
pkgrel=1
pkgdesc="Hadoop - MapReduce implementation and distributed filesystem"
arch=('i686' 'x86_64')
url="http://hadoop.apache.org"
license=('APACHE')
depends=('jre' 'zlib' 'gzip' 'lzo' 'bzip2')
makedepends=('jdk' 'apache-ant')
optdepends=('kfs')
conflicts=('hadoop-svn')
install=hadoop.install
source=("http://mirror.csclub.uwaterloo.ca/apache/hadoop/common/hadoop-${pkgver}/hadoop-${pkgver}.tar.gz")
md5sums=('ec0f791f866f82a7f2c1319a54f4db97')

#https://issues.apache.org/jira/secure/attachment/12394365/patch.4697
#https://issues.apache.org/jira/secure/attachment/12400552/patch.5292

compile() {
  cd ${srcdir}/hadoop-${pkgver} || return 1

  msg "Cleaning..."

  ant clean || return 1

  msg "Patching..."

  sed -i "s/${_devver}/${pkgver}/" build.xml

#  patch -Np0 -i ${srcdir}/progressReporter.patch || return 1
#  patch -Np0 -i ${srcdir}/patch.5292 || return 1

  sed -i "s|<ivysettings>|<ivysettings>\n<caches defaultCacheDir=\"${srcdir}/ivy_cache\"/>|" ivy/ivysettings.xml

  msg "Building..."

  ant -Dcompile.native=true bin-package || return 1

  cd ${srcdir}/hadoop-${pkgver}/build || return 1

  msg "Installing..."

  mkdir -p ${pkgdir}/usr/share
  mkdir -p ${pkgdir}/etc/hadoop

  mv hadoop-${pkgver} ${pkgdir}/usr/share/hadoop || return 1
  mv ${pkgdir}/usr/share/hadoop/conf ${pkgdir}/etc/hadoop/default || return 1
}

use_compiled() {
  msg "Installing..."

  mkdir -p ${pkgdir}/usr/share/hadoop
  mkdir -p ${pkgdir}/etc/hadoop

  cd ${srcdir}/hadoop-${pkgver}

  mv bin lib webapps *.jar *.txt ${pkgdir}/usr/share/hadoop/ || return 1
  mv conf ${pkgdir}/etc/hadoop/default || return 1
}

build() {
  # compile
  use_compiled

  msg "Configuring..."

  cd ${pkgdir}/etc/hadoop/default || return 1

  # Set Java home:
  echo 'export JAVA_HOME=/opt/java/jre' >> hadoop-env.sh

  # Set directories:
  echo 'export HADOOP_LOG_DIR="/var/log/hadoop"' >> hadoop-env.sh
  echo 'export HADOOP_PID_DIR="/var/run"' >> hadoop-env.sh

  # Disable IPv6 (comment out for IPv6 support):
  sed -i 's|_OPTS="-D|_OPTS="-Djava.net.preferIPv4Stack=true -D|' hadoop-env.sh

  cd ${pkgdir}/usr/share/hadoop || return 1

  sed -i 's#^export HADOOP_HOME=`dirname "$this"`/..#export HADOOP_HOME="/usr/share/hadoop"\nexport HADOOP_CONF_DIR="/etc/hadoop"#' bin/hadoop-config.sh

  if [ "$CARCH" = "i686" ]; then
    rm -rf lib/native/Linux-amd64-64
    cd lib/native/Linux-i386-32
    sed -i "s|dependency_libs='|dependency_libs='-L/opt/java/jre/lib/i386/server |" libhadoop.la
  fi

  if [ "$CARCH" = "x86_64" ]; then
    rm -rf lib/native/Linux-i386-32
    cd lib/native/Linux-amd64-64
    sed -i "s|dependency_libs='|dependency_libs='-L/opt/java/jre/lib/amd64/server |" libhadoop.la
  fi

  # Create some links, so Hadoop's KFS jar could access KFS libraries properly
  # (it is still fine if KFS is not installed)

  msg "Creating KFS links..."

  for lib in libkfsClient libkfsCommon libkfsEmulator libkfsIO libkfsMeta; do
    for ext in a so; do
      ln -s /usr/lib/${lib}.${ext}
    done
  done

  ln -s /usr/lib/libkfs_access.so
}
