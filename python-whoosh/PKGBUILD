# Maintainer: Steven Allen <steven@stebalien.com>
# Contributor: Piotr Husiaty≈Ñski <phusiatynski@gmail.com>

pkgname=python-whoosh
pkgver=2.3.2
pkgrel=2
pkgdesc="Fast, pure-Python full text indexing, search, and spell checking library"
license=("BSD")
url="http://bitbucket.org/mchaput/whoosh/wiki/Home"
depends=('python2')
makedepends=('python2-distribute' 'python2-nose')
source=("http://pypi.python.org/packages/source/W/Whoosh/Whoosh-$pkgver.tar.gz" 
        "test_final_ranges_thisyear.diff" # Fixes Bug #217
)
arch=('any')
md5sums=('b35a67db364d1e0d8d32a10220a1db70'
         'bfcfe93413cb949e193722745e9e6453')

build() {
  cd "${srcdir}/Whoosh-$pkgver"
  patch -Np1 -i "${srcdir}/test_final_ranges_thisyear.diff"
}


check() {
  cd "${srcdir}/Whoosh-$pkgver"
  python2 setup.py test
}

package() {
  cd "${srcdir}/Whoosh-$pkgver"
  python2 setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
