# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=gio-sharp-git
pkgver=20100904
pkgrel=1
pkgdesc="a branch of the official gtk-sharp/gio to get gio-sharp building on gtk-sharp 2.12"
url="http://github.com/mono/gio-sharp"
arch=(i686 x86_64)
license=(custom)
depends=(gtk-sharp-2)
makedepends=(git pkgconfig autoconf)
conflicts=(gio-sharp)
options=(!libtool)

_gitroot="git://github.com/mono/gio-sharp.git"
_gitname="gio-sharp"

build() {
  cd "$srcdir"

  msg2 "Connecting to github.com Git server...."
  if [ -d $_gitname ] ; then
    ( cd $_gitname && git pull ) 
  else
    git clone $_gitroot $_gitname
  fi
  msg2 "Git checkout done or server timeout"

  rm -rf $_gitname-build
  git clone -s $_gitname $_gitname-build
  cd $_gitname-build

  msg2 "Starting build..."

  ./autogen-2.22.sh --prefix=/usr
  make -j1
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  provides=("gio-sharp=$(grep '^PACKAGE_VERSION=' configure | awk -F\' '{print $2}')")
}
