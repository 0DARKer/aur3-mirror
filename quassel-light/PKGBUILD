# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=quassel-light
pkgver=0.7.2
pkgrel=1
pkgdesc='Modern, cross-platform, distributed IRC client; built with minimal dependencies'
url='http://quassel-irc.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('qt')
makedepends=('cmake')
source=("http://quassel-irc.org/pub/quassel-${pkgver}.tar.bz2"
        'conf.d'
        'rc.d')
sha1sums=('dbbd8ebfde5aa12fb6b6841558049220d88bb9dc'
          'bc01322a545087113eb7109d1c19650f0dd21b14'
          '13bf8171db6dfef19fe4fa209eaeb1cb9ff2fba6')

install=install
conflicts=('quassel')
backup=('etc/conf.d/quassel')

build() {
	cd "${srcdir}"
	mkdir build; cd build
	cmake -D CMAKE_INSTALL_PREFIX=/usr/ -D CMAKE_BUILD_TYPE=Release \
		-DWITH_KDE=OFF -DWITH_WEBKIT=OFF -DWITH_PHONON=OFF -DWITH_LIBINDICATE=OFF \
		../quassel-${pkgver}/ -Wno-dev
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install
	install -D -m755 ../rc.d "${pkgdir}"/etc/rc.d/quassel
	install -D -m644 ../conf.d "${pkgdir}"/etc/conf.d/quassel
}
