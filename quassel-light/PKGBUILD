# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=quassel-light
pkgver=0.7.1
pkgrel=1
pkgdesc='Modern, cross-platform, distributed IRC client; built with minimal dependencies'
arch=('i686' 'x86_64')
url='http://quassel-irc.org/'
license=('GPL')
conflicts=('quassel')
depends=('qt')
makedepends=('cmake')
source=("http://quassel-irc.org/pub/quassel-$pkgver.tar.bz2"
        'conf.d'
        'rc.d')
backup=('etc/conf.d/quassel')
sha1sums=('791086da977033a1bbee3effa317668b3726bd7f'
          'bc01322a545087113eb7109d1c19650f0dd21b14'
          '13bf8171db6dfef19fe4fa209eaeb1cb9ff2fba6')

install=install

build() {
	cd "$srcdir"
	mkdir build; cd build
	cmake -D CMAKE_INSTALL_PREFIX=/usr/ -D CMAKE_BUILD_TYPE="Release" \
		-DWITH_KDE=OFF -DWITH_WEBKIT=OFF -DWITH_PHONON=OFF -DWITH_LIBINDICATE=OFF \
		../quassel-$pkgver/ -Wno-dev
	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR="$pkgdir/" install
	install -D -m755 ../rc.d "$pkgdir/etc/rc.d/quassel"
	install -D -m644 ../conf.d "$pkgdir/etc/conf.d/quassel"
}
