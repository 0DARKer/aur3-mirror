# Maintainer: Johannes LÃ¶thberg <kyrias@archlinux.info>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=quassel-light
pkgver=0.9.0
pkgrel=1
pkgdesc="Modern, cross-platform, distributed IRC client; built with few dependencies"
arch=('i686' 'x86_64')
url="http://quassel-irc.org/"
license=('GPL')
depends=('qt4')
optdepends=('qca-ossl: For Client-Core SSL')
makedepends=('cmake' 'automoc4')
provides=('quassel' 'quasselcore' 'quasselclient')
conflicts=('quassel' 'quasselcore' 'quasselclient')
install=quassel.install
backup=(etc/conf.d/quassel)
source=("http://quassel-irc.org/pub/quassel-$pkgver.tar.bz2"
        'quassel.service'
        'quassel.conf')
sha256sums=('2e3fe06ae3731c829aa3c2f6bb5320619bad264c831f322985c3aa3fe58b6027'
            '91a1aaae47d41c11aa418dd560039ddbbfb659e02b86379d8c4789c5ada362d3'
            'f3031ea8217e01ba42cea14606169e3e27affa5918968ffd5a03c21ae92fe2b8')


build() {
  [[ ! -d build ]] && mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=OFF \
    -DWITH_WEBKIT=OFF \
    -DWITH_PHONON=OFF \
    -DWITH_LIBINDICATE=OFF \
    -DCMAKE_BUILD_TYPE="Release" \
    ../quassel-${pkgver}/ \
    -Wno-dev \

  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/quassel.service \
    "${pkgdir}"/usr/lib/systemd/system/quassel.service
  install -Dm644 "${srcdir}"/quassel.conf \
    "${pkgdir}"/etc/conf.d/quassel
}
