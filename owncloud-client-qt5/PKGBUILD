# A temporary package based on aur:owncloud-client
# Origional PKGBUILD Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds
# References from aur:owncloud-client-bata

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.

_buildtype='Release'

##############################################################

_name=mirall
pkgname=owncloud-client-qt5
pkgver=1.6.3
pkgrel=1
pkgdesc='ownCloud client based on mirall, using Qt 5'
arch=('i686' 'x86_64' 'armv7h')
url='http://owncloud.org/'
license=('GPL2')
depends=('qtkeychain-qt5' 'qt5-base' 'neon')
makedepends=('cmake' 'python-sphinx' 'qt5-tools')
provides=('mirall' 'ocsync')
conflicts=('mirall-git' 'ocsync')
install=owncloud-client.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("https://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2"
        "https://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2.asc")
md5sums=('48d111e6fcceaa0bba95970ccd0dd174'
         'SKIP')

if [[ ! ${_buildtype} == 'Release' ]] && [[ ! ${_buildtype} == 'release' ]]; then
  options=('debug')
fi

build() {
  cd ${srcdir}/${_name}-${pkgver} # Shadow build seems not to be working

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname} \
        -DAPPLICATION_SHORTNAME=ownCloud

  make
  make doc-man
}

package() {
  cd ${srcdir}/${_name}-${pkgver}
  make DESTDIR=${pkgdir} install
}
