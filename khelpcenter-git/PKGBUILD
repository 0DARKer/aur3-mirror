# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=khelpcenter-git
pkgver=r2091.91da5a0
pkgrel=1
pkgdesc="Application to show KDE Applications' documentation"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/khelpcenter'
license=('LGPL')
depends=('khtml-git' 'kdelibs4support-git' 'kcmutils-git')
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git')
conflicts=('khelpcenter')
provides=('khelpcenter')
source=('git://anongit.kde.org/khelpcenter.git')
md5sums=('SKIP')

pkgver() {
  cd khelpcenter
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../khelpcenter/ \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
