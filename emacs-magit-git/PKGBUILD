# Maintainer: Stefan Husmann <stefan.husmann@t-online.de>
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Just Lest <just.lest@gmail.com>
# Contributor: Daniel White <daniel@whitehouse.id.au>
pkgname=emacs-magit-git
pkgver=1.1.0.595.g73c72d7
pkgrel=1
pkgdesc="It's Magit! A Emacs mode for Git."
arch=("any")
url="http://github.com/magit/magit"
license=('GPL3')
depends=('emacs' 'git')
makedepends=('automake' 'autoconf' 'ed')
provides=('emacs-magit')
conflicts=('emacs-magit')
install="${pkgname}.install"
_gitname="magit"
source=(git://github.com/magit/magit.git)
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  # Git tag
  echo $(git describe --always|sed 's/-/./g')
}

build() {
  cd "$_gitname"
  make
}

package() {
  cd "$_gitname"
  install -d "${pkgdir}/usr/bin"	# work around a bug in the makefile
  make PREFIX="/usr" DESTDIR="${pkgdir}" install install_contrib
  rm "${pkgdir}/etc/emacs/site-start.d/50magit.el" "${pkgdir}/usr/share/info/dir"
  rmdir "${pkgdir}/etc/emacs/site-start.d" "${pkgdir}/etc/emacs" "${pkgdir}/etc"
}
