# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=muffin-git
pkgver=20120317
pkgrel=1
pkgdesc="A window manager for GNOME"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gconf' 'gobject-introspection' 'libcanberra' 'startup-notification' 'zenity' 'libxxf86vm')
makedepends=('git' 'intltool' 'gnome-doc-utils')
provides=('muffin')
conflicts=('muffin')
options=('!libtool' '!emptydirs')
install=muffin.install
source=('0001-Disable-Werror.patch')
sha512sums=('762ca0edff842fd8c13653709d39d47d9beb40441caaaf98fcc4b533c2f0a5924ce5139acb42a35c8cd565dd52cb64ed1d4c15b487994d57d7304bf49eee6cdf')

# At the moment, muffin-git won't compile with cogl 1.9.* from the GNOME 3.3
# development versions. A separate branch that fixes this is at:
# https://github.com/rat4/muffin
# Just uncomment the lines below to use this branch. Thanks to afv in the
# AUR for finding this :)

_gitroot='https://github.com/linuxmint/muffin.git'
# If cogl >= 1.9:
#_gitroot='https://github.com/rat4/muffin.git'
_gitname='muffin'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  # If cogl >= 1.9:
  #git checkout gtk_3.3

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Disable -Werror
  patch -Np1 -i "${srcdir}/0001-Disable-Werror.patch"

  # Fix build with libx11 >= 1.4.99.1
  CFLAGS="${CFLAGS} -Wno-error=deprecated-declarations"

  autoreconf -vfi
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin --localstatedir=/var --disable-static
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" --domain muffin "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm "${pkgdir}"/etc/gconf/schemas/*.schemas
}
