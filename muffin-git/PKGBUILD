# Maintainer: twa022 <twa022 at gmail dot com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgname=muffin
pkgname=${_pkgname}-git
pkgver=459.2799c2c
pkgrel=1
pkgdesc="A window manager for GNOME"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gobject-introspection' 'cinnamon-desktop' 'libcanberra'
         'startup-notification' 'zenity' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-common' 'git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
options=('!emptydirs')
install=${_pkgname}.install
source=("git+https://github.com/linuxmint/${_pkgname}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "${srcdir}/${_pkgname}"
  PYTHON=python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin --localstatedir=/var --disable-static --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
}
