# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord-nopolkit
pkgver=0.1.18
pkgrel=1
pkgdesc="Color daemon"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lcms2' 'sqlite3' 'udev')
makedepends=('intltool' 'gobject-introspection' 'vala' 'docbook2x')
source=($url/releases/colord-$pkgver.tar.xz)
options=('!libtool')
sha256sums=('87175f9bbc2c59fbe01bb99c4989393f5aaab5077da7cd91f099c6aec49e1099')
provides=('colord')
conflicts=('colord')

build() {
  cd "$srcdir/colord-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc --libexecdir=/usr/lib/colord \
    --localstatedir=/var --disable-static \
    --disable-polkit
  make
}

package() {
  cd "$srcdir/colord-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:

