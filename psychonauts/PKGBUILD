# Maintainer: Eric Anderson <ejona86 at gmail dot com>

pkgname=psychonauts
pkgver=20120604
_srcpkgver=06042012
pkgrel=1
pkgdesc="A mind-bending platforming adventure"
url="http://www.psychonauts.com/"
license=('custom')
arch=('i686' 'x86_64')
makedepends=(unzip)
if [ "$CARCH" = "x86_64" ]; then
    optdepends=('lib32-ati-dri: acceleration for ATI cards'
                'lib32-libtxc_dxtn: texture decoding for ATI cards'
                'lib32-nvidia-utils: acceleration for Nvidia cards')
else
    optdepends=('libtxc_dxtn: texture decoding for ATI cards')
fi
options=(!strip)
_gamepkg="${pkgname}-linux-${_srcpkgver}-bin"
source=("${pkgname}.desktop"
        "psychonauts.sh"
        "${_gamepkg}")
md5sums=('91be805610718903bea5c3a3d1d127cf'
         '6282a96e6c4c4f6f94f92d6f2dfdbefe'
         'ade9789da951bbd6a39847ef5d52ded1')
PKGEXT='.pkg.tar'

warning ""
warning "Requires purchased game download placed at ${startdir}/${_gamepkg}"
warning ""

build(){
    cd "${srcdir}"
    unzip "${_gamepkg}" -d "${pkgname}" || true
}

package(){
    cd "${srcdir}"

    mkdir -p "${pkgdir}/opt"
    cp -Rl "${srcdir}/${pkgname}/data" "${pkgdir}/opt/"
    mv "${pkgdir}/opt/data" "${pkgdir}/opt/${pkgname}"

    install -D -m644 "${srcdir}/${pkgname}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/EULA.rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/EULA.rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-de (German).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-de (German).rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-en (English).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-en (English).rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-es (Spanish).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-es (Spanish).rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-fr (French).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-fr (French).rtf"
    install -D -m644 "${srcdir}/${pkgname}/data/Documents/EULAs/Cider-EULA-it (Italian).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-it (Italian).rtf"

    install -D -m755 "${srcdir}/psychonauts.sh" "${pkgdir}/usr/bin/${pkgname}"
}
