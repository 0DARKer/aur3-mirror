# Maintainer: Eric Anderson <ejona86 at gmail dot com>

pkgname=psychonauts
pkgver=20120531
_srcpkgver=05312012
pkgrel=2
pkgdesc="A mind-bending platforming adventure"
url="http://www.psychonauts.com/"
license=('custom')
arch=('i686' 'x86_64')
makedepends=(unzip)
if [ "$CARCH" = "x86_64" ]; then
		optdepends=('lib32-ati-dri: acceleration for ATI cards'
		            'lib32-libtxc_dxtn: texture decoding for ATI cards'
		            'lib32-nvidia-utils: acceleration for Nvidia cards')
else
		optdepends=('libtxc_dxtn: texture decoding for ATI cards')
fi
options=(!strip)
_gamepkg="${pkgname}-linux-${_srcpkgver}.zip"
source=("${pkgname}.desktop"
        "psychonauts.sh"
        "${_gamepkg}")
md5sums=('91be805610718903bea5c3a3d1d127cf'
         '6282a96e6c4c4f6f94f92d6f2dfdbefe'
         'feb1d3216bbe32a317ea80cb4f88dcae')
noextract=("${_gamepkg}")
_gamedirname=${pkgname}-linux-installer
PKGEXT='.pkg.tar'

# Set to 'true' to enable patch seen at
# https://bugzilla.icculus.org/show_bug.cgi?id=5525
_usepatch=false
if $_usepatch; then
    _patchtarname=psychonuts-linux-test-06032012.tar.bz2
    source+=("http://treefort.icculus.org/psychonauts/${_patchtarname}")
    md5sums+=('fc966040f76837a77f202775e7169eec')
    noextract+=("${_patchtarname}")
fi


build(){
    cd "${srcdir}"
    unzip "${_gamepkg}"

    if $_usepatch; then
        tar xjf "${_patchtarname}"
    else
        warning "For gamepad support, or if you have problems with crashes, modify"
        warning "the PKGBUILD and set _usepatch=true"
    fi
}

package(){
    cd "${srcdir}"

    mkdir -p "${pkgdir}/opt"
    cp -Rl "${srcdir}/${_gamedirname}/data" "${pkgdir}/opt/"
    mv "${pkgdir}/opt/data" "${pkgdir}/opt/${pkgname}"

    if $_usepatch; then
        install -D -m755 "${srcdir}/Psychonauts" "${pkgdir}/opt/${pkgname}"
        install -D -m755 "${srcdir}/libSDL-1.2.so.0" "${pkgdir}/opt/${pkgname}"
    fi

    install -D -m644 "${srcdir}/${pkgname}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    install -D -m644 "${srcdir}/${_gamedirname}/data/Documents/EULAs/EULA.rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/EULA.rtf"
    install -D -m644 "${srcdir}/${_gamedirname}/data/Documents/EULAs/Cider-EULA-de (German).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-de (German).rtf"
    install -D -m644 "${srcdir}/${_gamedirname}/data/Documents/EULAs/Cider-EULA-en (English).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-en (English).rtf"
    install -D -m644 "${srcdir}/${_gamedirname}/data/Documents/EULAs/Cider-EULA-es (Spanish).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-es (Spanish).rtf"
    install -D -m644 "${srcdir}/${_gamedirname}/data/Documents/EULAs/Cider-EULA-fr (French).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-fr (French).rtf"
    install -D -m644 "${srcdir}/${_gamedirname}/data/Documents/EULAs/Cider-EULA-it (Italian).rtf" \
        "${pkgdir}/usr/share/licenses/${pkgname}/Cider-EULA-it (Italian).rtf"

    install -D -m755 "${srcdir}/psychonauts.sh" "${pkgdir}/usr/bin/${pkgname}"
}
