# Maintainer: Johann Kl√§hn <kljohann@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
# Contributor: Ilkka Laukkanen <ilkka.s.laukkanen@gmail.com>

pkgname=stgit
pkgver=0.16
pkgrel=2
pkgdesc="Pushing/popping patches to/from a stack on top of Git, similar to Quilt"
url="http://www.procode.org/stgit/"
arch=('any')
license=('GPL')
depends=('python2' 'git')
makedepends=('xmlto' 'asciidoc')
source=("http://download.gna.org/stgit/stgit-${pkgver}.tar.gz"
        "asciidoc.patch")
md5sums=('73ca6a7469d30d9d69fa561e16abc2a8'
         'a9c81a6e11ec6cf7868ac62fde3b75d3')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 -i ../asciidoc.patch
  sed -ri 's/(^#!.*)python$/\1python2/g' stg stg-build
  msg "Building documentation..."
  make doc
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --prefix=/usr
  chmod 644 "$pkgdir"/usr/share/stgit/{completion/stgit-completion.bash,contrib/stgbashprompt.sh}
  for manpage in ./Documentation/*.1; do
    install -D -m644 $manpage "$pkgdir"/usr/share/man/man1/$(basename $manpage)
  done
}
