# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-x11-idletime
_lastauthor=A/AW/AWENDT
_pkgname=X11-IdleTime
pkgver=0.08
pkgrel=3
pkgdesc="Get the idle time of X11"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-inline')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('99bc6069b0fb811a9c8e6851435690d4c5230590c0299140141c584212e06272')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # This is the standard template with exceptions for this package..
  # PS. Is there something wrong with this Makefile.PL?
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor" \
    PERL_MB_OPT="--installdirs vendor" \
    MODULEBUILDRC=/dev/null # there

  if [ -f "Build.PL" ]; then
    perl Build.PL
    perl Build
  elif [ -f "Makefile.PL" ]; then
    perl Makefile.PL INSTALLDIRS=vendor # there
    make
  else
    return 1
  fi
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build test
  elif [ -f "Makefile.PL" ]; then
    make test
  fi
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build install
  elif [ -f "Makefile.PL" ]; then
    make DESTDIR="${pkgdir}" install # and there
  fi

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
