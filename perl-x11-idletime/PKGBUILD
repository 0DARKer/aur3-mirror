# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-x11-idletime
_lastauthor=A/AW/AWENDT
_pkgname=X11-IdleTime
pkgver=0.08
pkgrel=4
pkgdesc="Get the idle time of X11"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('libxss' 'perl-inline')
url="http://search.cpan.org/dist/X11-IdleTime/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('99bc6069b0fb811a9c8e6851435690d4c5230590c0299140141c584212e06272')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor" \
    PERL_MB_OPT="--installdirs vendor" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
