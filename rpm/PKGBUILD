# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Chris Tusa <linisys@gmail.com>

pkgname=rpm
pkgver=5.2.1
pkgrel=3
pkgdesc="The RedHat Package Manager"
arch=('i686' 'x86_64')
url="http://rpm5.org/"
license=('GPL')
groups=('system')
depends=('perl' 'python2' 'zlib' 'beecrypt' 'pcre' 'file')
conflicts=("${pkgname}-org" 'rpmextract')
options=('!emptydirs')
source=("http://rpm5.org/files/${pkgname}/${pkgname}-${pkgver%\.[0-9]}/${pkgname}-${pkgver}.tar.gz")
md5sums=('71f825ede4a2ddc191132ae017c9a6e4')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-python=python2 \
              --without-selinux \
              --with-popt=/usr/lib \
              --with-pcre=/usr/lib \
              --with-bzip2=/usr/lib \
              --with-xz=/usr/lib \
              --with-file=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 ft=sh et:
