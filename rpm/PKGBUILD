# Maintainer: Paolo Stivanin <admin AT polslinux DOT it>

pkgname=rpm
pkgver=5.3.5
pkgrel=1
pkgdesc="The RedHat Package Manager"
arch=('i686' 'x86_64')
url="http://rpm5.org/"
license=('GPL')
groups=('system')
depends=('perl' 'python2' 'zlib' 'beecrypt' 'pcre' 'file')
conflicts=("${pkgname}-org" 'rpmextract')
options=('!emptydirs')
source=("http://rpm5.org/files/$pkgname/$pkgname-5.3/$pkgname-$pkgver.tar.gz")
md5sums=('7a5112fd208bfda7a1cab20b9ce17a02')

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --with-python \
              --without-selinux \
              --with-popt=/usr/lib \
              --with-pcre=/usr/lib \
              --with-bzip2=/usr/lib \
              --with-xz=/usr/lib \
              --with-file=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
