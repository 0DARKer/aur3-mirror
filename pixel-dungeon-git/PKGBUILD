# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=pixel-dungeon
pkgname=${_pkgname}-git
pkgver=1.7.1c27
pkgrel=1
pkgdesc="A rogue-like game inspired by Brogue"
url="http://pixeldungeon.watabou.ru"
license=('GPL3')
depends=('java-environment' 'bash')
makedepends=('git')
arch=('any')
source=("${_pkgname}::git+https://github.com/Arcnor/${_pkgname}-gdx.git"
        "${_pkgname}.sh"
        "${_pkgname}.desktop"
        "${_pkgname}_mouse-fix.patch"
        "${_pkgname}_version.patch")
sha512sums=('SKIP'
            'ab43ee17312fbb28daf456f569e9a9ac8c60a22faea500959f8d82794ef3d0a78e21056136742e281f024256bae01e59810c8ced96d9c9fde9abe9ccdb1ed9b6'
            '74c30ef63fec229f199dc9ad1758dab8cdd6484198e143b9defb1a91f52381a8eafd3a2563b3bbc78d97fab306b96fabcfd19ec112616d49c03b5bacc6c3b4a5'
            'f8f4ebe4a47da482fa56b7bfaf2a5e27a4597fc4d795516947333592dda707a47f65b52804ce277ecfdde0ffb97590408000cf788c594ad10aa61614e7deb0a1'
            'fbd059d661cbfa9a67c6c942a407e56ce675b03976f28a5af0c7a9aeccfb33fe1c1cbc2f82a2b043de1d7c3ed828c35f9c94686e5a0307c0c765d15ceb8141fb')

pkgver() {
    cd $_pkgname
    echo -n "1.7.1c$(git rev-list --count HEAD)"
}

prepare(){
    cd $_pkgname
    [[ "$(git rev-parse --short HEAD)" = "9f50f3f" ]] \
        && patch -p1 < ../${_pkgname}_mouse-fix.patch \
        && patch -p1 < ../${_pkgname}_version.patch
}

build() {
    cd ${_pkgname}
    unset _JAVA_OPTIONS
    GRADLE_USER_HOME="$srcdir" ./gradlew desktop:dist
}

package() {
    install -Dm755 ${_pkgname}.sh "$pkgdir"/usr/bin/${_pkgname}
    install -Dm644 ${_pkgname}.desktop "$pkgdir"/usr/share/applications/${_pkgname}.desktop
    install -Dm644 ${_pkgname}/android/res/drawable-xxxhdpi/ic_launcher.png "$pkgdir"/usr/share/pixmaps/${_pkgname}.png
    install -Dm644 ${_pkgname}/desktop/build/libs/desktop-*.jar "$pkgdir"/usr/share/${_pkgname}/${_pkgname}.jar
}
