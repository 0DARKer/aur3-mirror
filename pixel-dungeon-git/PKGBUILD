# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=pixel-dungeon
pkgname=${_pkgname}-git
_pkgver=1.7.1c
pkgver=1.7.1c.44
pkgrel=2
pkgdesc="A rogue-like game inspired by Brogue"
url="http://pixeldungeon.watabou.ru"
license=('GPL3')
depends=('java-environment' 'bash')
makedepends=('git')
arch=('any')
source=("${_pkgname}::git+https://github.com/Arcnor/${_pkgname}-gdx.git"
        "${_pkgname}.sh"
        "${_pkgname}.desktop"
        "wasd-and-diagonals.patch"
        "pdinput-nullpointer-fix.patch")
sha512sums=('SKIP'
            'ab43ee17312fbb28daf456f569e9a9ac8c60a22faea500959f8d82794ef3d0a78e21056136742e281f024256bae01e59810c8ced96d9c9fde9abe9ccdb1ed9b6'
            '74c30ef63fec229f199dc9ad1758dab8cdd6484198e143b9defb1a91f52381a8eafd3a2563b3bbc78d97fab306b96fabcfd19ec112616d49c03b5bacc6c3b4a5'
            '8ee16ff2dfabbc7f17dc7dc0c90b999dcf228e1a6f83538aec14cf7b9507cc93f5d14dc0f035c2940bf858874f5378323e32501da0f5851e199c3bc7be64c2b7'
            'aee8395d0f058f1cf670e1b6f771fbd1d354d69add21104261a476f13dbec84fb97478705d920242988cfce1d852af4109f50e785e47cd8c580fb03030efc6f3')

pkgver() {
    cd $_pkgname
    echo -n "${_pkgver}.$(git rev-list --count HEAD)"
}

prepare() {
    cd $_pkgname

    # Only apply the patch if none of the files have changed (avoid breaking the build)
    #
    # Patch: WASD + Diagnonal input on QWERTY w/o numpad
    MD5LIST=$(printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/scenes/CellSelector.java" | sed 's|\ .*||'))
    MD5LIST=$(printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/ui/AttackIndicator.java" | sed 's|\ .*||'))${MD5LIST}
    MD5LIST=$(printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/ui/QuickSlot.java" | sed 's|\ .*||'))${MD5LIST}
    MD5LIST=$(printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/ui/StatusPane.java" | sed 's|\ .*||'))${MD5LIST}
    MD5LIST=$(printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/ui/Toolbar.java" | sed 's|\ .*||'))${MD5LIST}
    MD5LIST=$(printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/windows/WndHero.java" | sed 's|\ .*||'))${MD5LIST}
    if [ "$MD5LIST" = 'bc0a40526c11443862ae88b8b017af572391ec9bfd388f5ce969de9d2cc5451a4d43d34aa063f99ce5d1f0881b4124dc985bd7659749811035510fc9f28e71fabde5e0a80bc2d9f2d1bbd6e3b7f18291d6ff66cc25d289bc3a822034715bd55b' ]; then
        echo "Patching: provide keyboard mappings for movement in all directions"
        patch -p1 < ../wasd-and-diagonals.patch
        echo
    fi

    # Patch: Fix crash when clicking both mouse buttons simultansously
    if [ $(md5sum PD-classes/src/com/watabou/input/PDInputProcessor.java | sed 's| .*$||') = '282b0adb860687ec0fb2b348488b07b0' ]; then
        echo "Patching: prevent crash from simultaneous touch/mouse input"
        patch -p1 < ../pdinput-nullpointer-fix.patch
        echo
    fi

    return 0
}

build() {
    cd $_pkgname
    unset _JAVA_OPTIONS
    GRADLE_USER_HOME="$srcdir" ./gradlew desktop:dist
}

package() {
    install -Dm755 ${_pkgname}.sh "$pkgdir"/usr/bin/${_pkgname}
    install -Dm644 ${_pkgname}.desktop "$pkgdir"/usr/share/applications/${_pkgname}.desktop
    install -Dm644 ${_pkgname}/android/res/drawable-xxxhdpi/ic_launcher.png "$pkgdir"/usr/share/pixmaps/${_pkgname}.png
    install -Dm644 ${_pkgname}/desktop/build/libs/desktop-*.jar "$pkgdir"/usr/share/${_pkgname}/${_pkgname}.jar
}
