# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=pixel-dungeon
pkgname=${_pkgname}-git
_pkgver=1.7.1c
pkgver=1.7.1c.44
pkgrel=1
pkgdesc="A rogue-like game inspired by Brogue"
url="http://pixeldungeon.watabou.ru"
license=('GPL3')
depends=('java-environment' 'bash')
makedepends=('git')
arch=('any')
source=("${_pkgname}::git+https://github.com/Arcnor/${_pkgname}-gdx.git"
        "${_pkgname}.sh"
        "${_pkgname}.desktop"
        "wasd-and-diagonals.patch")
sha512sums=('SKIP'
            'ab43ee17312fbb28daf456f569e9a9ac8c60a22faea500959f8d82794ef3d0a78e21056136742e281f024256bae01e59810c8ced96d9c9fde9abe9ccdb1ed9b6'
            '74c30ef63fec229f199dc9ad1758dab8cdd6484198e143b9defb1a91f52381a8eafd3a2563b3bbc78d97fab306b96fabcfd19ec112616d49c03b5bacc6c3b4a5'
            '1cc1da27191cf1f2f33ac4bf8afc0f64a63ce96c016abcbe245106ffaa79dac8758f985bdd262511d73edaca94359ca529fe0e6514d070135affe1b0dda91a26')

prepare() {
    cd $_pkgname

    # Only apply the patch if none of the files have changed (avoid breaking the build)
    [[ -f "${srcdir}/md5list.txt" ]] && rm "${srcdir}/md5list.txt"

    printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/scenes/CellSelector.java" | sed 's|\ .*||') > "${srcdir}/md5list.txt"
    printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/ui/AttackIndicator.java" | sed 's|\ .*||') >> "${srcdir}/md5list.txt"
    printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/ui/QuickSlot.java" | sed 's|\ .*||') >> "${srcdir}/md5list.txt"
    printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/ui/StatusPane.java" | sed 's|\ .*||') >> "${srcdir}/md5list.txt"
    printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/ui/Toolbar.java" | sed 's|\ .*||') >> "${srcdir}/md5list.txt"
    printf "%s" $(md5sum "core/src/com/watabou/pixeldungeon/windows/WndHero.java" | sed 's|\ .*||') >> "${srcdir}/md5list.txt"

    if [ $(cat "${srcdir}/md5list.txt") = "d6ff66cc25d289bc3a822034715bd55bbde5e0a80bc2d9f2d1bbd6e3b7f18291985bd7659749811035510fc9f28e71fa4d43d34aa063f99ce5d1f0881b4124dc2391ec9bfd388f5ce969de9d2cc5451abc0a40526c11443862ae88b8b017af57" ]; then
        patch -p1 < ../wasd-and-diagonals.patch
    fi

    rm "${srcdir}/md5list.txt"
    return 0
}

pkgver() {
    cd $_pkgname
    echo -n "${_pkgver}.$(git rev-list --count HEAD)"
}

build() {
    cd ${_pkgname}
    unset _JAVA_OPTIONS
    GRADLE_USER_HOME="$srcdir" ./gradlew desktop:dist
}

package() {
    install -Dm755 ${_pkgname}.sh "$pkgdir"/usr/bin/${_pkgname}
    install -Dm644 ${_pkgname}.desktop "$pkgdir"/usr/share/applications/${_pkgname}.desktop
    install -Dm644 ${_pkgname}/android/res/drawable-xxxhdpi/ic_launcher.png "$pkgdir"/usr/share/pixmaps/${_pkgname}.png
    install -Dm644 ${_pkgname}/desktop/build/libs/desktop-*.jar "$pkgdir"/usr/share/${_pkgname}/${_pkgname}.jar
}
