# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=pixel-dungeon
pkgname=${_pkgname}-git
_commit=c86ca48
_pkgver=1.7.1c
pkgver=1.7.1c.81
pkgrel=1
pkgdesc="A rogue-like game inspired by Brogue"
url="http://pixeldungeon.watabou.ru"
license=('GPL3')
depends=('java-environment' 'bash')
makedepends=('git')
arch=('any')
source=("${_pkgname}::git+https://github.com/Arcnor/${_pkgname}-gdx.git#commit=${_commit}"
        "${_pkgname}.sh"
        "${_pkgname}.desktop"
        "level-change-save.patch")
sha512sums=('SKIP'
            'ab43ee17312fbb28daf456f569e9a9ac8c60a22faea500959f8d82794ef3d0a78e21056136742e281f024256bae01e59810c8ced96d9c9fde9abe9ccdb1ed9b6'
            '74c30ef63fec229f199dc9ad1758dab8cdd6484198e143b9defb1a91f52381a8eafd3a2563b3bbc78d97fab306b96fabcfd19ec112616d49c03b5bacc6c3b4a5'
            '4fb7d44f68394ac9a096a5dfcb35c8281ce671b602acd4fe152f1cc36c7ecc3bd82b058614522d9b824e636ddccf7b564be5867d3034b7eb2ddeb6eccb12d1e5')

pkgver() {
    cd $_pkgname
    echo -n "${_pkgver}.$(git rev-list --count HEAD)"
}

prepare() {
    cd ${_pkgname}/core
    patch -p1 < ../../level-change-save.patch
}

build() {
    cd $_pkgname
    unset _JAVA_OPTIONS
    GRADLE_USER_HOME="$srcdir" ./gradlew desktop:dist
}

package() {
    install -Dm755 ${_pkgname}.sh "$pkgdir"/usr/bin/${_pkgname}
    install -Dm644 ${_pkgname}.desktop "$pkgdir"/usr/share/applications/${_pkgname}.desktop
    install -Dm644 ${_pkgname}/android/res/drawable-xxxhdpi/ic_launcher.png "$pkgdir"/usr/share/pixmaps/${_pkgname}.png
    install -Dm644 ${_pkgname}/desktop/build/libs/desktop-*.jar "$pkgdir"/usr/share/${_pkgname}/${_pkgname}.jar
}
