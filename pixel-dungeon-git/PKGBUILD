# Maintainer: Kevin MacMartin <prurigro at gmail dot com>

_pkgname=pixel-dungeon
pkgname=${_pkgname}-git
_pkgver=1.7.1c
pkgver=1.7.1c.52
pkgrel=1
pkgdesc="A rogue-like game inspired by Brogue"
url="http://pixeldungeon.watabou.ru"
license=('GPL3')
depends=('java-environment' 'bash')
makedepends=('git')
arch=('any')
source=("${_pkgname}::git+https://github.com/Arcnor/${_pkgname}-gdx.git"
        "${_pkgname}.sh"
        "${_pkgname}.desktop"
        "wasd.patch")
sha512sums=('SKIP'
            'ab43ee17312fbb28daf456f569e9a9ac8c60a22faea500959f8d82794ef3d0a78e21056136742e281f024256bae01e59810c8ced96d9c9fde9abe9ccdb1ed9b6'
            '74c30ef63fec229f199dc9ad1758dab8cdd6484198e143b9defb1a91f52381a8eafd3a2563b3bbc78d97fab306b96fabcfd19ec112616d49c03b5bacc6c3b4a5'
            '8ee16ff2dfabbc7f17dc7dc0c90b999dcf228e1a6f83538aec14cf7b9507cc93f5d14dc0f035c2940bf858874f5378323e32501da0f5851e199c3bc7be64c2b7')

pkgver() {
    cd $_pkgname
    echo -n "${_pkgver}.$(git rev-list --count HEAD)"
}

prepare() {
    cd $_pkgname
    patch -p1 < ../wasd.patch
}

build() {
    cd $_pkgname
    unset _JAVA_OPTIONS
    GRADLE_USER_HOME="$srcdir" ./gradlew desktop:dist
}

package() {
    install -Dm755 ${_pkgname}.sh "$pkgdir"/usr/bin/${_pkgname}
    install -Dm644 ${_pkgname}.desktop "$pkgdir"/usr/share/applications/${_pkgname}.desktop
    install -Dm644 ${_pkgname}/android/res/drawable-xxxhdpi/ic_launcher.png "$pkgdir"/usr/share/pixmaps/${_pkgname}.png
    install -Dm644 ${_pkgname}/desktop/build/libs/desktop-*.jar "$pkgdir"/usr/share/${_pkgname}/${_pkgname}.jar
}
