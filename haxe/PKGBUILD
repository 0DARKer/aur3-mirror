# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Kyle Brady  <kyle at spark-gap dot com>
# Contributor: Patrick Hof <courts@offensivethinking.org>
# Contributor: Michael Fellinger <mf@rubyists.com>

pkgname=haxe
pkgver=2.10rc
pkgrel=2
pkgdesc="A multiplatform open source programming language"
arch=('i686' 'x86_64')
url='http://haxe.org/'
license=('GPL2')
groups=('devel')
depends=('neko>=1.8.2')
makedepends=('subversion' 'ocaml' 'neko>=1.8.2')
provides=('haxe')
options=('!strip' 'emptydirs' '!makeflags')
install=haxe.install
source=('haxeserver'
  'haxe.sh'
  'haxe.csh')
md5sums=('27d7af131da515355cca04b8b404feaa'
         '79cbf6ebdb3d56e980d1a17fa9cb3b2d'
         '5e0e656293e92a6437d76f2f406438b5')

trunk=http://haxe.googlecode.com/svn/trunk/
_buildroot="$srcdir/$pkgname"
# http://ncannasse.fr/blog/haxe_2_10_rc
_fixedrevision=4946

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."
  if [[ -d "$pkgname/.svn" ]]; then
    (cd "$pkgname" && svn up -r "$_fixedrevision")
  else
    svn co "$trunk" --config-dir ./ -r "$_fixedrevision" "$pkgname"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."
  cd $_buildroot
  
  #
  # BUILD HERE
  #
  
  # clean up first
  make clean > /dev/null
  for i in haxe{lib,doc}; do
   [ -f "$_buildroot/std/tools/$i/$i" ] && rm "$_buildroot/std/tools/$i/$i"
  done
  
  make || return 1
  msg2 "done."

  export HAXE_LIBRARY_PATH=$_buildroot/std
  msg "Generating api docs..."  
  cd $_buildroot/doc && $_buildroot/haxe all.hxml
  if [ -f flash8.xml -a -f flash9.xml -a -f neko.xml -a -f js.xml -a -f php.xml -a -f cpp.xml ]; then 
    $srcdir/$pkgname/std/tools/haxedoc/haxedoc \
      "flash8.xml;flash8;flash" "neko.xml;neko" "js.xml;js" \
      "flash9.xml;flash" "php.xml;php" "cpp.xml;cpp"
    if [ $? -eq 0 ]; then
      echo 
      msg2 "done."
    else
      echo 
      msg2 "failed." 
      return 1
    fi
  fi
}

package() {
  install --directory "$pkgdir"{/usr/{bin,lib/haxe/lib},/opt/haxe/doc,/etc/profile.d}
  install --mode=755 "$srcdir"/haxeserver "$pkgdir"/usr/bin/
  install --mode=755 "$srcdir"/haxe.{,c}sh "$pkgdir"/etc/profile.d/
  svn export --quiet $_buildroot/std "$pkgdir"/opt/haxe/std
  install --mode=644 -D --no-target-directory $_buildroot/doc/LICENSE.txt "$pkgdir"/usr/share/${pkgname%-svn}/LICENSE
  install --mode=644 $_buildroot/doc/CHANGES.txt "$pkgdir"/opt/haxe/
  install --mode=755 $_buildroot/haxe{,lib,doc} "$pkgdir"/usr/bin/
  cp --recursive $_buildroot/doc/content "$pkgdir"/opt/haxe/doc/
  install --mode=644 $_buildroot/doc/index.html "$pkgdir"/opt/haxe/doc/
}

# vim:set ts=2 sw=2 et:
