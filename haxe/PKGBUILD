# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Kyle Brady  <kyle at spark-gap dot com>
# Contributor: Patrick Hof <courts@offensivethinking.org>
# Contributor: Michael Fellinger <mf@rubyists.com>

pkgname=haxe
pkgver=3.1.1
pkgrel=1
pkgdesc="A multiplatform open source programming language"
arch=('i686' 'x86_64')
url=http://haxe.org/
license=('GPL2' 'MIT')
groups=('devel')
depends=('neko>=2.0.0' 'sh')
makedepends=('git' 'ocaml>=4.00.0' 'zlib' 'neko>=2.0.0')
provides=('haxe')
options=('!strip' 'emptydirs' '!makeflags')
install=haxe.install
source=(
  "https://github.com/HaxeFoundation/haxe/archive/v$pkgver.tar.gz"
  'haxelib_src::git+https://github.com/HaxeFoundation/haxelib.git'
  'libs::git+https://github.com/HaxeFoundation/ocamllibs.git'
  'haxeserver'
  'haxe.sh'
  'haxe.csh')
md5sums=('fce5fbac1b492cc8575cae8655c7868e'
         'SKIP'
         'SKIP'
         '888e2aa9e2583bc9888cc9889b24e3e7'
         'acf2d50add2ccc19cf4e84d14b4bb5f8'
         'b4263f4b8a2ed3308cdff93d01b70227')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  if [ -d libs ]; then
    rm -rf libs
  fi 
  if [ -d extra/haxelib_src ]; then
    rm -rf extra/haxelib_src
  fi
  cp --recursive "$srcdir"/libs .
  cp --recursive "$srcdir"/haxelib_src extra/

  make
  make tools
}

package() {
  install --directory "$pkgdir"{/usr/{bin,lib/haxe/lib},/etc/profile.d,/opt/haxe,/usr/share/"$pkgname"}
  install --mode=755 "$srcdir"/haxeserver "$pkgdir"/usr/bin/
  install --mode=755 "$srcdir"/haxe.{,c}sh "$pkgdir"/etc/profile.d/
  cp --recursive "$srcdir/$pkgname-$pkgver"/std "$pkgdir"/opt/haxe/
  install --mode=644 --target-directory "$pkgdir"/usr/share/"$pkgname" "$srcdir/$pkgname-$pkgver"/extra/{LICENSE,CHANGES}.txt 
  install --mode=755 "$srcdir/$pkgname-$pkgver"/{haxe,haxelib} "$pkgdir"/usr/bin/
}

# vim:set ts=2 sw=2 et:
