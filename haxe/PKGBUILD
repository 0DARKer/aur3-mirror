# Maintainer: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Kyle Brady  <kyle at spark-gap dot com>
# Contributor: Patrick Hof <courts@offensivethinking.org>
# Contributor: Michael Fellinger <mf@rubyists.com>

pkgname=haxe
pkgver=2.09
pkgrel=1
pkgdesc="A multiplatform open source programming language"
arch=('i686')
url="http://haxe.org/"
license=('GPL2')
groups=('devel')
depends=('neko>=1.8.2' 'sh')
provides=('haxe')
options=('!strip' 'emptydirs')
install=haxe.install
source=('haxeserver'
  'haxe.sh'
  'haxe.csh'
  'http://haxe.org/file/haxe-2.09-linux.tar.gz')
md5sums=('27d7af131da515355cca04b8b404feaa'
         '79cbf6ebdb3d56e980d1a17fa9cb3b2d'
         '5e0e656293e92a6437d76f2f406438b5'
	 'b8dd026e01911692e8ed9cbac9632756')

_buildroot="$pkgname-$pkgver-linux"

package() {
  install --directory "$pkgdir"{/usr/{bin,lib/haxe/lib},/opt/haxe/doc,/etc/profile.d}
  install --mode=755 "$srcdir"/haxeserver "$pkgdir"/usr/bin/
  install --mode=755 "$srcdir"/haxe.{,c}sh "$pkgdir"/etc/profile.d/
  install --mode=644 -D --no-target-directory "$_buildroot"/LICENSE.txt "$pkgdir"/usr/share/$pkgname/LICENSE
  install --mode=644 "$_buildroot"/CHANGES.txt "$pkgdir"/opt/haxe/
  install --mode=755 "$_buildroot"/haxe{,lib,doc} "$pkgdir"/usr/bin/
  cp --recursive "$_buildroot"/doc "$pkgdir"/opt/haxe/
  cp --recursive "$_buildroot"/std "$pkgdir"/opt/haxe/
}

# vim:set ts=2 sw=2 et:
