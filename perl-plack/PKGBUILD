# Contributor: Christian Sturm <reezer@reezer.org>

pkgname='perl-plack'
pkgver='1.0014'
pkgrel='1'
pkgdesc="Perl Superglue for Web frameworks and Web Servers (PSGI toolkit)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.1' 'perl-stream-buffered' 'perl-devel-stacktrace>=1.23' 'perl-devel-stacktrace-ashtml>=0.11' 'perl-file-sharedir>=1.00' 'perl-filesys-notify-simple' 'perl-hash-multivalue>=0.05' 'perl-http-body>=1.06' 'perl-http-message>=5.814' 'perl-libwww' 'perl-try-tiny' 'perl-uri>=1.59')
makedepends=('perl-extutils-makemaker>=6.59' 'perl-test-requires' 'perl-test-tcp>=0.11')
url='http://plackperl.org/'
source=("http://cpan.metacpan.org/authors/id/M/MI/MIYAGAWA/Plack-${pkgver}.tar.gz")
md5sums=('810013afd5c8e4a94cb1e9f7765f21a0')
sha512sums=('e48c5fcfdb104cdde74a793c3560252609c2549e816b50613d3f1c23743dd0e476de9dad7bfed4a4fe34ced31cced5271f6e15a9deddf1084b463c8254f6182e')
_distdir="${srcdir}/Plack-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$_distdir"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
