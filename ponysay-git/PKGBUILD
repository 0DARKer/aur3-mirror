# Maintainer: Daniel Wallace <daniel.wallace at gatech dot edu>
pkgname=ponysay-git
_pkgname=ponysay
pkgver=3.0.1.1.g66c1715
pkgrel=1
pkgdesc="Pony wrapper for cowsay. "
arch=('any')
url="https://github.com/erkin/ponysay"
license=('GPL3' 'FDL')
depends=('python' 'coreutils')
optdepends=('util-say: tty2colourfultty  and ponysay2ttyponysay')
makedepends=('git' 'texinfo' 'python-distribute')
pkgver() {
  cd $_pkgname
  git describe | sed 's/-/./g'
}

epoch=1
source=("$_pkgname::git://github.com/erkin/ponysay.git")
md5sums=('SKIP')

build() {
  cd "$srcdir/$_pkgname"
  python setup.py --prefix=/usr \
    --freedom=partial \
    --without-fish \
    --without-pdf \
    build
}

package() {
  cd "$srcdir/$_pkgname"
  python setup.py --freedom=partial --dest-dir="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
