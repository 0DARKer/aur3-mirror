# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-gtk2
pkgver=2.24.8
pkgrel=1
arch=(any)
pkgdesc="The GTK+ Toolkit (v2) (mingw32)"
url="http://www.gtk.org/"
depends=('mingw32-atk>=1.30' 'mingw32-pango>=1.20'
         'mingw32-glib2>=2.28' 'mingw32-cairo>=1.6'
         'mingw32-gdk-pixbuf2>=2.22')
makedepends=(mingw32-gcc)
options=(!strip)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.xz)
sha256sums=('8a3b29f667933cf52eea2db7b066723edbc80443ca9c75b7cd7cbe8c8b90b93c')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
  
  export CFLAGS="-O2 -mms-bitfields"
  export CXXFLAGS="${CFLAGS}"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS PKG_CONFIG_PATH

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --with-gdktarget=win32 \
    --disable-modules \
    --disable-cups \
    --with-included-loaders=png,bmp,ico,pnm,xpm

    make
}

package() {
    cd "${srcdir}/gtk+-${pkgver}"
    mkdir -p ${pkgdir}/usr/i486-mingw32/lib
    make DESTDIR="${pkgdir}" install

    cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  
  rm -rf usr/i486-mingw32/share/{gtk-doc,man,gtk-2.0}
}
