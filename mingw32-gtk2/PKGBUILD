# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-gtk2
pkgver=2.22.1
pkgrel=1
arch=(any)
pkgdesc="The GTK+ Toolkit (v2) (mingw32)"
depends=('mingw32-atk>=1.30' 'mingw32-pango>=1.20'
         'mingw32-glib2>=2.24' 'mingw32-cairo>=1.6'
         'mingw32-gdk-pixbuf2')
makedepends=(mingw32-gcc)
options=(!strip)
license=('LGPL')
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.22/gtk+-${pkgver}.tar.bz2)
sha256sums=('965bc124f0d25087c4cb2a64cbfd7e4f896e05be8d560fbba68dd8685ba24d07')

build()
{
  cd "$srcdir/gtk+-$pkgver"
  
  export CFLAGS="-O2 -mms-bitfields"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS PKG_CONFIG_PATH

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --with-gdktarget=win32 \
    --disable-modules \
    --disable-cups \
    --with-included-loaders=png,bmp,ico,pnm,xpm

  make
}

package() {
  cd "$srcdir/gtk+-$pkgver"
  make install DESTDIR="$pkgdir"

  cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  
  rm -rf usr/i486-mingw32/share/{gtk-doc,man,gtk-2.0}
}
