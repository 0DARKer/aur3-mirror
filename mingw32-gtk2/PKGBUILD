pkgname=mingw32-gtk2
pkgver=2.24.10
pkgrel=2
arch=(any)
pkgdesc="The GTK+ Toolkit (v2) (mingw32)"
url="http://www.gtk.org/"
depends=('mingw32-atk>=1.30' 'mingw32-pango>=1.20'
         'mingw32-glib2>=2.28' 'mingw32-cairo>=1.6'
         'mingw32-gdk-pixbuf2>=2.22')
makedepends=(mingw32-gcc)
options=(!strip !buildflags)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.xz)
sha256sums=('ea56e31bb9d6e19ed2e8911f4c7ac493cb804431caa21cdcadae625d375a0e89')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
  
  export CFLAGS="-O2 -mms-bitfields"
  export CXXFLAGS="${CFLAGS}"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS PKG_CONFIG_PATH

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --with-gdktarget=win32 \
    --disable-modules \
    --disable-cups \
    --with-included-loaders=png,bmp,ico,pnm,xpm

    make
}

package() {
    cd "${srcdir}/gtk+-${pkgver}"
    mkdir -p ${pkgdir}/usr/i486-mingw32/lib
    make DESTDIR="${pkgdir}" install

    cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
}
