# Maintainer: Ondrej Jirman <megous@megous.com>

pkgname=mingw32-gtk2
pkgver=2.20.1
pkgrel=1
arch=(i686 x86_64)
pkgdesc="The GTK+ Toolkit (v2) (mingw32)"
depends=('mingw32-atk>=1.30' 'mingw32-pango>=1.20'
         'mingw32-glib2>=2.24' 'mingw32-cairo>=1.6')
makedepends=(mingw32-gcc)
options=(!strip)
license=('LGPL')
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.20/gtk+-${pkgver}.tar.bz2)
sha256sums=('0e081731d21e34ff45c82199490c2889504fa8b3c7e117c043e82ababaec0f65')

build()
{
  cd "$srcdir/gtk+-$pkgver"
  
  export CFLAGS="-O2 -mms-bitfields"
  export PKG_CONFIG_PATH="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --with-gdktarget=win32 \
    --without-libjpeg \
    --without-libtiff \
    --disable-modules \
    --disable-cups \
    --with-included-loaders=png,bmp,ico,pnm,xpm

  make
}

package() {
  cd "$srcdir/gtk+-$pkgver"
  make install DESTDIR="$pkgdir"

  cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  
  rm -rf usr/i486-mingw32/share/{gtk-doc,man,gtk-2.0}
}
