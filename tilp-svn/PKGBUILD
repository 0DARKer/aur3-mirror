# Contributor: Jon Sturm <Jasturm002@aol.com>
_pkgname=tilp
pkgname=$_pkgname-svn
pkgver=4290
pkgrel=2
pkgdesc="TI graphing calculator link/transfer program"
arch=(i686 x86_64)
url="http://tilp.info/"
license=('GPL')
depends=('libglade' 'libticalcs' 'libticables')
makedepends=('subversion')
provides=('tilp' 'tilp2')
conflicts=('tilp' 'tilp2')
options=('force' '!libtool')
source=(tilp.desktop
	tilp.xml)
md5sums=('e2bb0d7ff6c3b0e60ff82213480f6d15'
         'd387dfdabe9c2eb25830a686b09e9fc3')
#md5sums=() #generate with 'makepkg -g'

_svnmod=$_pkgname
_svntrunk=http://svn.tilp.info/repos/tilp/$_svnmod/trunk

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #KDE users can remove the --without-kde option to enable kde file dialogs
  ./configure --prefix=/usr --without-kde
  make || return 1
  make DESTDIR="$pkgdir/" install

  install -m755 -d ${pkgdir}/usr/share/applications
  install -m644 ${srcdir}/tilp.desktop ${pkgdir}/usr/share/applications

  install -m755 -d ${pkgdir}/usr/share/mime/packages
  install -m644 ${srcdir}/tilp.xml ${pkgdir}/usr/share/mime/packages
}
