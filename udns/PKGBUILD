# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=udns
pkgver=0.1
pkgrel=1
pkgdesc='Stub DNS resolver library with ability to perform both syncronous and asyncronous DNS queries'
url='http://www.corpit.ru/mjt/udns.html'
license=('LGPL')
depends=('glibc')
arch=('i686' 'x86_64')
source=("http://www.corpit.ru/mjt/udns/udns-$pkgver.tar.gz")
sha1sums=('e3326684653701f9219cc213bdc3656dff269c80')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	install -d "$pkgdir"/usr/{bin,include,lib}
	install -m755 dnsget rblcheck ex-rdns "$pkgdir"/usr/bin/
	install -m644 udns.h "$pkgdir"/usr/include/
	install -m644 libudns.a "$pkgdir"/usr/lib/

	install -d "$pkgdir"/usr/share/man/man{1,3}
	install -m644 {dnsget,rblcheck}.1 "$pkgdir"/usr/share/man/man1/
	install -m644 udns.3 "$pkgdir"/usr/share/man/man3/
}
