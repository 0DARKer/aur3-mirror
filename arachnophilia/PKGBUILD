# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Nathan Owe <ndowens04 at gmail>
# Contributor: Gen2ly <toddrpartridge@gmail.com>

pkgname=arachnophilia
_pkgname=Arachnophilia
pkgver=5.5.2671
pkgrel=1
pkgdesc="Web page development workshop and general programming tool"
arch=('any')
url="http://www.arachnoid.com/arachnophilia/"
license=('LGPL' 'custom:careware')
depends=('java-runtime=6')
source=("http://www.arachnoid.com/arachnophilia/${_pkgname}.jar"
        "${pkgname}.sh"
        "${pkgname}.desktop"
        "LICENSE")
noextract=("${_pkgname}.jar")
md5sums=('4ee958786726404704680dc24c2d3019'
         '007d3ddff251b4796e145090ef6d1201'
         '7aef71f46550b1937bc28ca02c038948'
         '2ed778803876e3e7274c90eafbb273bf')

package() {
  cd "${srcdir}"

  # Install program files
  install -Dm755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${_pkgname}.jar" "${pkgdir}/usr/share/${pkgname}/${_pkgname}.jar"

  # Install pixmap and .desktop files
  jar xf "${_pkgname}.jar" "${_pkgname}.png"
  install -Dm644 "${_pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install the license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
