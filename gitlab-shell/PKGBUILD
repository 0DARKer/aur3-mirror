# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com> 
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Maintainer: Jonas Heinrich <onny@project-insnaity.org>

pkgname=gitlab-shell
pkgver=1.8.0
pkgrel=1
pkgdesc="Self hosted Git management software. (shell daemon)"
arch=(any)
url="http://gitlab.org/"
license=('MIT')
depends=('redis' 'ruby' 'git')
options=('!strip')

install=gitlab-shell.install
backup=(etc/gitlab/shell.yml)
source=($pkgname-$pkgver.tar.gz::https://github.com/gitlabhq/gitlab-shell/archive/v$pkgver.tar.gz)
sha512sums=('ab829b597e4610675c773be83035506a97dc623c41f294c94b57fc7351d7985156c5882ba9b1c84d140fe0e0b5c613ab138f8161eee82c4727d049aa39e17efe')

package() {
	local datadir=/usr/share/gitlab-shell
	local homedir=/var/lib/gitlab
	
	install -d \
	  	"${pkgdir}${datadir}" \
		"${pkgdir}${homedir}" \
	 	"${pkgdir}/etc/gitlab"

	cd "$srcdir/$pkgname-$pkgver"

	sed -e 's|user: git|user: gitlab|' \
		-e "s|/home/git/repositories|$homedir/repositories|" \
		-e "s|/home/git|$homedir|" \
		config.yml.example > "${pkgdir}/etc/gitlab/shell.yml"
	ln -s /etc/gitlab/shell.yml "${pkgdir}${datadir}/config.yml"
	
	cp -a VERSION bin hooks lib spec support "${pkgdir}${datadir}"
	ln -s /usr/share/gitlab-shell ${pkgdir}/${homedir}/
	
	install -dm700 "$pkgdir$homedir/.ssh"
	touch "$pkgdir$homedir/.ssh/authorized_keys"
	
	install -dm770 "$pkgdir$homedir/"{repositories,satellites}
	
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
