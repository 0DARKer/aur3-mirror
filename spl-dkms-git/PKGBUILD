#
# Maintainer: isiachi <isiachi@rhyeworld.it>
#

pkgname="spl-dkms-git"
pkgver=0.6.3_r76_g6ab0866
pkgrel=1
license=('GPL')

pkgdesc="Solaris Porting Layer kernel modules."
depends=("spl-utils-git" "dkms")
makedepends=("git")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("git+https://github.com/zfsonlinux/spl.git#commit=6ab08667")
md5sums=('SKIP')
replaces=("spl")
provides=("spl" "spl-git")
conflicts=("spl" "spl-lts")
install=spl.install

_dirver="`echo ${pkgver} | sed "s|^\(.*\.[0-9]\).*|\1|g"`"

build() {
    cd "${srcdir}/spl"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_dirver} -f dkms.conf -n spl
}

package() {
    install -d ${pkgdir}/usr/src
    cp -a ${srcdir}/spl ${pkgdir}/usr/src/spl-${_dirver}
    rm -rf ${pkgdir}/usr/src/spl-${_dirver}/.git
    rm -f ${pkgdir}/usr/src/spl-${_dirver}/.gitignore
}
