#
# Maintainer: isiachi <isiachi@rhyeworld.it>
#

pkgname="spl-dkms-git"
pkgver=0.6.2_r33_g89aa970
pkgrel=1
license=('GPL')

pkgdesc="Solaris Porting Layer kernel modules."
depends=("spl-utils-git" "dkms")
makedepends=("git")
arch=("i686" "x86_64")
url="http://zfsonlinux.org/"
source=("git+https://github.com/zfsonlinux/spl.git#commit=89aa970")
md5sums=('SKIP')
replaces=("spl")
provides=("spl" "spl-git")
conflicts=("spl" "spl-lts")

_dirver="`echo ${pkgver} | sed "s|^\(.*\.[0-9]\).*|\1|g"`"

pkgver() {
        cd "${srcdir}/spl"
        git describe --always | sed "s|^[a-z]*-\([0-9.]*\)\(-\)|\1\2r|;s|-|_|g"
}

build() {
    cd "${srcdir}/spl"
    ./autogen.sh
    scripts/dkms.mkconf -v ${_dirver} -f dkms.conf -n spl
}

package() {
    install -d ${pkgdir}/usr/src
    cp -a ${srcdir}/spl ${pkgdir}/usr/src/spl-${_dirver}
    rm -rf ${pkgdir}/usr/src/spl-${_dirver}/.git
    rm -f ${pkgdir}/usr/src/spl-${_dirver}/.gitignore
}
