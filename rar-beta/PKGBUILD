# Maintainer: TuxSpirit <tuxspirit AT archlinux DOT fr> 

pkgname=rar-beta
pkgver=5.0.b2
pkgrel=1
pkgdesc="A command-line port of the rar compression utility Beta Version"
url="http://www.rarlab.com"
arch=('i686' 'x86_64')
license=('custom')
backup=('etc/rarfiles.lst')
conflicts=('rar' 'unrar')
provides=('rar' 'unrar')
source=('rar.1')
md5sums=('9d46739d2c89b239fe1158d95cdb8db5') 

case "$CARCH" in
      i686) source+=("http://www.rarlab.com/rar/rarlinux-${pkgver}.tar.gz")
            md5sums+=('013b5b596312b45352767f3b246b898c') ;;
    x86_64) source+=("http://www.rarlab.com/rar/rarlinux-x64-${pkgver}.tar.gz")
            md5sums+=('9c73c93d903d20cd8fa1dababac5ed91') ;;
esac

package() {
    cd "$srcdir/rar"
    install -Dm755 rar             "${pkgdir}/usr/bin/rar"
    install -Dm755 unrar           "${pkgdir}/usr/bin/unrar"
    install -Dm755 rar_static      "${pkgdir}/usr/bin/rar_static"
    install -Dm755 default.sfx     "${pkgdir}/usr/lib/default.sfx"
    install -Dm644 license.txt     "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 ${srcdir}/rar.1 "${pkgdir}/usr/share/man/man1/rar.1"
    install -Dm644 rarfiles.lst    "${pkgdir}/etc/rarfiles.lst"
}
