pkgname=wxgtk3.0
pkgver=3.0.0
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('libnotify' 'gstreamer0.10-base' 'webkitgtk' 'libjpeg-turbo')
makedepends=('gstreamer0.10-base-plugins' 'gconf')
optdepends=('webkitgtk3: for webview support')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2
        wxGTK-3.0.0-collision.patch)
sha1sums=('756a9c54d1f411e262f03bacb78ccef085a9880a'
          'be3c6a6c252b2d7c09634eec7962683b9ebc5b61')

prepare() {
  cd wxWidgets-${pkgver}
  patch -p1 -i ../wxGTK-3.0.0-collision.patch
}

build() {
  cd wxWidgets-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=3 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --enable-webview --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers --enable-stl
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
