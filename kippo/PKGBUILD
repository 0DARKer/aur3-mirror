# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Conributor: Andre Kwakernaak <andre.kwakernaak@gmail.com>
# Conributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=kippo
pkgver=0.9
pkgrel=1
pkgdesc="A medium interaction SSH honeypot designed to log brute force attacks and, most importantly, the entire shell interaction performed by the attacker"
url="https://github.com/desaster/kippo"
arch=('any')
license=('BSD')
depends=('twisted' 'pycrypto' 'zope-interface' 'python2-pyasn1')
backup=('etc/kippo.cfg')
install=kippo.install
source=(https://github.com/desaster/kippo/archive/v${pkgver}.tar.gz
        start.sh
        kippo.service
        INSTALL)
sha512sums=('0e17c410ecb8e44ba3862f0bd6c2773d6fa768750d7917ba7f5fbfdec8ddfa63fbde0e0190bcd5361a6c5ea0bcb2017e24593c4181b5cb915a605723bce07a26'
            'f8a2cc6fe2e9319ccfbb81dfeb46bdab9a63d28fc351c9c5148c969b427af50ed92d4d5b7d0c723458d00789d3b3f3f3634da27d755d34c545319c5bc2c80b57'
            '60097121e70583084e7bd4c7bd0842089ab4c9d2c462b4336dcea2dffbf3628ec148580efff59416f24d7be142f8a3031a4e660dacc4097b16e3eebdf1988d75'
            'fd82f060af248b413e62d779a4a20c6d6f4972cc724503a8c60fe435955ad7b1829caf9b5a8ceca41a2d9cf02144ab9fa9d892b59012e52a81d9dbfa832d4106')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/env python$/env python2/g' -i utils/*
  sed -e 's|/usr/bin/python$|/usr/bin/python2|g' -i utils/fsctl.py
  sed -e 's|kippo.cfg|/etc/kippo.cfg|g' -i kippo.tac
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 644 kippo.cfg.dist "${pkgdir}/etc/kippo.cfg"
  install -Dm 644 ../kippo.service \
    "${pkgdir}/usr/lib/systemd/system/kippo.service"

  install -d "${pkgdir}/opt/kippo/"
  rm kippo.cfg.dist
  cp -r * "${pkgdir}/opt/kippo/"
  install -Dm 755 ../start.sh "${pkgdir}/opt/kippo/start.sh"

  install -Dm 644 doc/COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 doc/README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 ../INSTALL "${pkgdir}/usr/share/doc/${pkgname}/INSTALL"
}

# vim: ts=2 sw=2 et:
