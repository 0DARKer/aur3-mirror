# Maintainer: OS Hazard <oshazard+thunar@gmail.com>
# Contributor: alextud <alextud@gmail.com>
# Contribuor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar-smallicons
pkgver=1.2.3
pkgrel=5
pkgdesc="Thunar file manager for Gnome3 patched with small toolbar icons"
arch=('i686' 'x86_64')
url="http://thunar.xfce.org"
license=('GPL2' 'LGPL2.1')
provides=('thunar')
conflicts=('thunar')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify' 'udev'
         'gtk2' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'gtk-doc')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'polkit-gnome: for mounting internal partitions (needs root password)'
            'thunar-archive-plugin: create and deflate archives')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/1.2/Thunar-$pkgver.tar.bz2
        fix-segfault-on-rename.patch
        fix-sorting-of-large-numbers-5356.patch
        fix-launching-multiple-files-7456.patch
        fix-crash-8168.patch
	fix-toolbar-icon-size.patch)
md5sums=('0a2c36cdd3cb3a2ab52cbf6055415b85'
         '07e568a84f3a24e32d9d37803a250099'
         '926dbefb639c429c8acf064922b4fbe0'
         '7ef67fa9faa414ba03053dc69f4cdebc'
         '54a88687621bcbd5ab7d951a952d3558'
	 '03c2adcada78d45ee9ad84faa20bc931')

build() {
  cd "$srcdir/Thunar-$pkgver"
  # add fixes from git tree
  patch -Np1 -i ../fix-sorting-of-large-numbers-5356.patch
  patch -Np1 -i ../fix-launching-multiple-files-7456.patch
  patch -Np1 -i ../fix-crash-8168.patch
  patch -Np1 -i ../fix-segfault-on-rename.patch
  patch -Np1 -i ../fix-toolbar-icon-size.patch
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-dbus \
    --enable-startup-notification \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/Thunar-$pkgver"

  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    "$pkgdir/usr/share/applications/Thunar-folder-handler.desktop"
}
