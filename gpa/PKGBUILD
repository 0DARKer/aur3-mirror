# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Ravi Desai <ravster3@hotmail.com>

pkgname=gpa
pkgver=0.9.0
pkgrel=3
pkgdesc="A graphical user interface for the GnuPG"
arch=('i686' 'x86_64')
url="http://wald.intevation.org/projects/gpa/"
license=('GPL')
depends=('gpgme' 'gtk2')
install="$pkgname.install"
source=("http://wald.intevation.org/frs/download.php/603/$pkgname-$pkgver.tar.bz2"
        "configure.patch" "server.patch")
md5sums=('8859f9cf69e94e10e4895f633a0b268a'
         '3c5a2be5df8ec3b3576bf9632de91165'
         '8d090cfd6ec46ff8833de05016393d8a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix for libassuand 2.x
  patch -p1 -i ../configure.patch
  patch -p1 -i ../server.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
