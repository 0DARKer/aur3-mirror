# Maintainer: cantabile <cantabile dot desu at gmail dot com>
# Contributor: extcake < extcake@gmail.com >
# Based on the mplayer-mt-oss-git package

pkgname=mplayer-mt-git
pkgver=20110202
pkgrel=1
pkgdesc="A movie player for linux (Multithreaded, git version)"
arch=('i686' 'x86_64')
depends=('a52dec' 'aalib' 'alsa-lib' 'bzip2' 'cdparanoia' 'enca' 'faac'
         'fontconfig' 'freetype2' 'gcc-libs' 'giflib' 'glibc' 'jack' 'lame'
         'libcaca' 'libdca' 'libdvdnav' 'libdvdread' 'libgl' 'libjpeg>=7'
         'libmad' 'libmng' 'libogg' 'libtheora' 'libvdpau' 'libx11'
         'libxext' 'libxinerama' 'libxss' 'libxv' 'libxvmc' 'libxxf86dga'
         'libxxf86vm' 'lirc-utils' 'ncurses' 'sdl' 'smbclient' 'ttf-dejavu'
         'x264>=20090925' 'xvidcore' 'zlib')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('git' 'live-media' 'mesa' 'python2' 'unzip' 'yasm')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer=31332')
conflicts=('mplayer')
source=('mplayer.desktop')
md5sums=('647b9f4ab5284a7fef3f84f992214e77')
install=mplayer.install

_gitroot="git://repo.or.cz/mplayer-build.git"
_gitname="mplayer-mt"

build() {
  # Custom CFLAGS break the mplayer build
  unset CFLAGS
  unset LDFLAGS

  cd ${srcdir}
  if [[ -d $_gitname ]]; then
    cd $_gitname
    git pull
  else
    git clone $_gitroot $_gitname  && cd $_gitname
  fi

  echo "
--prefix=/usr
--enable-runtime-cpudetection
--disable-gui
--disable-arts
--disable-speex
--disable-openal
--disable-fribidi
--disable-libdv
--disable-musepack
--disable-esd
--disable-mga
--enable-xvmc
--language=all
--confdir=/etc/mplayer
" > mplayer_options
#  for i in $(grep -r -l '/usr/bin/env python$' . 2>/dev/null); do sed -i 's:/usr/bin/env python$:&2:' "$i"; done
# for the paranoid, the following is supposed to handle filenames
# containing \n and other such abominations (thanks to grawity and others
# from irc)
  grep -r -lZ '/usr/bin/env python[^23]*' . 2>/dev/null | while read -rd '' file; do sed -i '1s:\<python\>:&2:' "$file"; done
  ./enable-mt
  ./init --shallow
  cd ${srcdir}
  cp -r $_gitname $_gitname-build 
  cd $_gitname-build

  msg "Starting make..."

  #sed -i -e 's:#!/usr/bin/awk -f:#!/bin/awk -f:g' mplayer/vidix/pci_db2c.awk
  make
  make DESTDIR=${pkgdir} install
  cp mplayer/etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
  mkdir -p ${pkgdir}/usr/share/mplayer
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
  rm -fr ${srcdir}/$_gitname-build
  # there is desktop file where the xpm is, but we don't want that
  install -Dm644 ${srcdir}/mplayer.desktop ${pkgdir}/usr/share/applications/mplayer.desktop
  install -Dm644 ${srcdir}/mplayer-mt/mplayer/etc/mplayer.xpm ${pkgdir}/usr/share/pixmaps/mplayer.xpm
}
