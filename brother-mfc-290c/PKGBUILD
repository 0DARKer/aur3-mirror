# Maintainer: libernux <dutchman55@gmx.com>
pkgname="brother-mfc-290c"
pkgver="1.1.2"
pkgrel=2
pkgdesc="LPR and CUPS driver for the Brother MFC-290c"
url="http://solutions.brother.com/linux/en_us/"
arch=("i686" "x86_64")
license=('custom:brother commercial license')
depends=("cups")
install="$pkgname.install"
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/mfc290clpr-$pkgver-$pkgrel.i386.rpm"
	"http://www.brother.com/pub/bsc/linux/dlf/mfc290ccupswrapper-$pkgver-$pkgrel.i386.rpm"
	'cupswrapper-license.txt'
	'lpr-license.txt'
)
md5sums=(
	'67e538382d55668d538729fbccbec7aa'
	'9fcba84b6993ff223ab55cb0ff7cdb0c'
	'97ad0cffd216059e9d1d3121899d8646'
	'5e87a3dc0f3e3438c088eda0f3565f0d'
)

build() {
	cd "$srcdir/usr/local/Brother/Printer/mfc290c/cupswrapper"
#  generate the ppd
	echo '#! /bin/sh' > genppd
	echo 'ppd_file_name=brmfc290c.ppd' >> genppd
	echo 'touch $ppd_file_name' >> genppd
	sed -n '/^printer_model/,/^device_model/p' cupswrappermfc290c >> genppd
	sed -n '/^cat <<ENDOFPPDFILE1/,/^ENDOFPPDFILE1/p' cupswrappermfc290c >>genppd
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genppd
	chmod +x genppd
	./genppd
#  generate the wrapper
	echo '#! /bin/sh' > genwrapper
	echo 'brotherlpdwrapper=brlpdwrappermfc290c'  >> genwrapper
	echo 'touch $brotherlpdwrapper' >>genwrapper
	sed -n '/^printer_model/p' cupswrappermfc290c >>genwrapper
	sed -n '/^device_model/p' cupswrappermfc290c >>genwrapper
	sed -n '/cat <<!ENDOFWFILTER!/,/!ENDOFWFILTER!/p' cupswrappermfc290c >>genwrapper
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genwrapper
	chmod +x genwrapper
	./genwrapper
#  generate other
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/bin/brprintconf_mfc290c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc290c/lpd/filtermfc290c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc290c/lpd/brmfc290cfilter"
	install -d "$srcdir/usr/share"
	mv "$srcdir/usr/local/Brother/" "$srcdir/usr/share/brother"
	rm "$srcdir/usr/share/brother/Printer/mfc290c/cupswrapper/cupswrappermfc290c"
	rm "$srcdir/usr/share/brother/Printer/mfc290c/cupswrapper/genppd"
	rm "$srcdir/usr/share/brother/Printer/mfc290c/cupswrapper/genwrapper"
	rm "$srcdir/usr/share/brother/Printer/mfc290c/inf/setupPrintcapij"
	rm -rf "$srcdir/usr/local"
}

package() {
	cd "$pkgdir"
	cp -R "$srcdir/usr" "$pkgdir"
	install -m 644 -D "$srcdir/usr/share/brother/Printer/mfc290c/cupswrapper/brmfc290c.ppd" "$pkgdir/usr/share/cups/model/brmfc290c.ppd"
	install -m 755 -D "$srcdir/usr/share/brother/Printer/mfc290c/cupswrapper/brlpdwrappermfc290c" "$pkgdir/usr/lib/cups/filter/brlpdwrappermfc290c"
	install -m 644 -D "$startdir"/cupswrapper-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/cupswrapper-licence.txt"
	install -m 644 -D "$startdir"/lpr-license.txt "${pkgdir}/usr/share/licenses/${pkgname}/lpr-licence.txt"
}