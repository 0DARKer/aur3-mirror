# Maintainer: Alexander Rødseth <rodseth@gmail.com>

pkgname=go-gobson
pkgver=r20120304
pkgrel=1
pkgdesc="BSON serialization for Go"
arch=('x86_64' 'i686')
url="http://labix.org/gobson"
license=('GPL')
makedepends=('go')
options=('!strip' '!emptydirs')
_gourl=launchpad.net/mgo/bson

build() {
  cd "$srcdir"

  source /etc/profile.d/go.sh

  rm -rf build
  mkdir -p build/go
  cd build/go

  for f in "$GOROOT/"*; do
    ln -s "$f"
  done

  rm pkg
  mkdir pkg
  cd pkg

  ln -s "$GOROOT/pkg/obj"
  ln -s "$GOROOT/pkg/tool"

  export GOROOT="$srcdir/build/go"
  export GOPATH="$srcdir/build"

  go get -fix "$_gourl"
}

package() {
  cd "$srcdir"

  source /etc/profile.d/go.sh

  for f in "$srcdir/build/go/pkg/"*; do
    # If it's not a symbolic link
    if [ ! -L "$f" ]; then
      cd "$f"
      mkdir -p "$pkgdir/$GOROOT/pkg/`basename $f`"
      for z in *; do
        # Check if the directory name matches
        if [ "$z" == `echo $_gourl | cut -d/ -f1` ]; then
          cp -r $z "$pkgdir/$GOROOT/pkg/`basename $f`"
        fi
      done
      cd ..
    fi
  done
}

# vim:set ts=2 sw=2 et:
