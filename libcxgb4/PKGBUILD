# Submitter: Anthony Cornehl <accornehl[at]gmail[dot]com>
# Maintainer: Mike Dacre <mike@dacre.me>
# https://github.com/twinshadow/abs
pkgname=libcxgb4
pkgver=1.3.5
pkgrel=2
pkgdesc="Chelsio T4 RNIC Open Fabrics userspace library"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibverbs')
source=("https://www.openfabrics.org/downloads/cxgb4/${pkgname}-${pkgver}.tar.gz")
# cksum start
md5sums=('d186d374318511c2a0c572f1433ac2c2')
sha512sums=('c459292e1017da7ce73453456d3ce881c90a46ffb415be77423d7d1e037b0ba412acbbfab382d06134d27e4442ab203846e3fca4b6635d58597356d4a1b28ae7')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
