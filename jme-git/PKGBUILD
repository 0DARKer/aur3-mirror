# Maintainer: Xyne <ca archlinux xyne, backwards>
pkgname=jme-git
pkgver=20120709
pkgrel=1
pkgdesc="JMicron Ethernet Linux driver"
arch=('any')
url="http://bbs.cooldavid.org/git/?p=jme.git"
license=('GPL')
makedepends=('git' 'linux-headers')
provides=('jme')
conflicts=('jme')
install="$pkgname".install

_gitroot=git://bbs.cooldavid.org/jme.git
_gitname=jme

build() {
  cd -- "$srcdir"
  msg "Connecting to git server..."

  if [[ -d $_gitname ]]
  then
    cd -- "$_gitname" && git pull origin
    msg "The local files have been updated."
    cd -
  else
    git clone "$_gitroot"
  fi

  msg "Checkout complete or server timeout"

  rm -rf "$_gitname"-build
  mkdir "$_gitname"-build
  cp -Rt "$_gitname"-build "$_gitname"/*
  cd "$_gitname"-build

  make
  gzip jme.ko
}

package() {
  cd -- "$srcdir/$_gitname"-build
  install -Dm644 jme.ko.gz "$pkgdir"/lib/modules/"$(uname -r)"/updates/jme.ko.gz
  install -Dm644 jme.h "$pkgdir"/usr/include/jme.h
}

# vim:set ts=2 sw=2 et:
