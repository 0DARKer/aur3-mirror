# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=calf-kxstudio-git
pkgver=0.0.19.96c9e77
pkgrel=1
pkgdesc='LV2/DSSI/LADSPA plug-ins suite and standalone JACK host (FalkTX fork)'
arch=('x86_64' 'i686')
url='http://calf.sf.net/'
license=('LGPL')
depends=('libglade' 'fftw' 'lash' 'dssi' 'fluidsynth' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('lv2core' 'ladspa' 'dssi')
provides=("calf=$pkgver" "calf-kxstudio=$pkgver")
conflicts=('calf')
source=("$pkgname::git+https://github.com/falkTX/calf.git")
install="$pkgname.install"
options=('!libtool')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(awk -F '[][]' '/AC_INIT/{print $4}' configure.ac).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$pkgname"
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-experimental \
              --enable-sse
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
