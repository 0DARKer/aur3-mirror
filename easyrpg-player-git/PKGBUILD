# Maintainer: carstene1ns <carsten.1 gmx net>

pkgname=easyrpg-player-git
_gitname=Player
pkgver=0.1.1.16.g153af58
pkgrel=1
pkgdesc="EasyRPG Player aims to be a free, RPG Maker 2000/2003 interpreter (latest git pull)."
arch=('i686' 'x86_64')
url="https://easy-rpg.org/"
license=('GPL3')
conflicts=('easyrpg-player')
provides=('easyrpg-player')
makedepends=('cmake>=2.8' 'boost')
depends=('sdl_mixer' 'freetype2' 'pixman' 'libpng' 'libjpeg' 'boost-libs' 'openal' 'libsndfile' 'lua51' 'expat')
install="$pkgname.install"
source=("git://github.com/EasyRPG/Player.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname

  # Use last tag and incrementing counter as version
  git describe --tags --always | sed 's|-|.|g'
}

prepare () {
  cd "$srcdir"

  # remove old build
  rm -rf build
}

build () {
  cd "$srcdir"

  mkdir build
  cd build

  cmake ../Player/builds/cmake/ -DLUA_INCLUDE_DIR="/usr/include/lua5.1" -DCMAKE_EXE_LINKER_FLAGS="-Wl,--no-as-needed" -DCMAKE_INSTALL_PREFIX="/usr"

  make
}

package () {
  cd "$srcdir/build"

  DESTDIR="$pkgdir" make install
}
