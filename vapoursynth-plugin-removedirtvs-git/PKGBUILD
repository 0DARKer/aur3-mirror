# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=removedirtvs
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=20131120.ddc78e5
pkgrel=2
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/handaimaoh/${_plug}"
license=('GPL')
depends=('vapoursynth')
makedepends=('git')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("git+https://github.com/handaimaoh/${_plug}.git"
        'README'
        'removedirtvs.py')
sha1sums=('SKIP'
          '2ae057901b8e6ae7439fd4259bf16ae7f5195c82'
          'bf3826d8944b135c0f32fbfc5e21de35718a2c33')
_gitname="${_plug}"
_sites_packages="$(python -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())")"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  echo "all:
	  g++ -shared -fPIC -fpermissive -DVS_TARGET_CPU_X86=1 -I. -o ${_plug}.so *.cpp " > Makefile
}

build() {
  cd "${_gitname}"
  make
}

package(){
  cd "${_gitname}"
  install -Dm755 "${_plug}.so" "${pkgdir}/usr/lib/vapoursynth/${_plug}.so"
  install -Dm644 "${srcdir}/removedirtvs.py" "${pkgdir}/${_sites_packages}/removedirtvs.py"
  install -Dm644 "${srcdir}/README" "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
}
