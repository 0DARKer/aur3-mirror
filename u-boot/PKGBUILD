# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=u-boot
pkgver=2010.09
pkgrel=1
pkgdesc='Utilities for working with Das U-Boot'
arch=('i686' 'x86_64')
url="http://www.denx.de/wiki/U-Boot/WebHome"
license=('GPL2')
backup=('etc/fw_env.config')
source=("ftp://ftp.denx.de/pub/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
	'no-cross-compiler.patch')
md5sums=('6dcc2cc47ecc96d4da984556d601a8c0'
	'c84bcc2ceffdf35f937d01e7f8db2afd')

build(){
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 -i ${srcdir}/no-cross-compiler.patch

  make sheevaplug_config

  # create files normally created by cross-compiler
  touch include/autoconf.mk{,dep}
  mkdir -p cpu/arm926ejs
  touch {cpu/arm926ejs,examples/standalone,tools,tools/env}/.depend

  make tools HOSTCC="gcc" HOSTSTRIP=/bin/true CROSS_COMPILE=""
  make env CC="gcc"
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -d ${pkgdir}/usr/bin
  install -m755 tools/mkimage ${pkgdir}/usr/bin/
  install -m755 tools/env/fw_printenv ${pkgdir}/usr/bin/
  ln -sf /usr/bin/fw_printenv ${pkgdir}/usr/bin/fw_setenv

  install -d ${pkgdir}/etc
  install -m644 tools/env/fw_env.config ${pkgdir}/etc/
}
