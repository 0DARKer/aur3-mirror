# Maintainer: Kwpolska <kwpolska@kwpolska.tk>
pkgname=python-nikola-git
_pyname=nikola
_gitname=nikola
pkgver=5.5.1_2_gf1741bc
pkgrel=1
pkgdesc='A static website and blog generator.  (git version)'
arch=('any')
url='http://nikola.ralsina.com.ar/'
license=('custom')
depends=('python' 'python-doit' 'python-pygments' 'python-docutils'
         'python-pillow' 'python-mako' 'python-unidecode' 'python-lxml'
         'python-yapsy' 'python-mock>=1.0.0' 'python-requests' 'python-markdown'
         'python-jinja' 'python-pyrss2gen' 'python-pytz')
makedepends=('git')
options=(!emptydirs)
provides=('nikola')
conflicts=('nikola')
source=("git://github.com/ralsina/${_gitname}.git")
md5sums=('SKIP')

package() {
  cd "${srcdir}/${_gitname}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --always | sed 's/-/_/g;s/v//'
}

# vim:set ts=2 sw=2 et:
