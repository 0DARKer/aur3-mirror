# Maintainer: Christian BÃ¼hler <christian.buehler@ipoplan.de>
pkgname=openni2
pkgver=2.1
pkgrel=8
pkgdesc="Open source SDK used for the development of 3D sensing middleware libraries and applications"
arch=('i686' 'x86_64')
url="http://www.openni.org/"
license=('Apache')
makedepends=(doxygen)
changelog=ChangeLog
source=(https://github.com/OpenNI/OpenNI2/archive/$pkgver-Beta.tar.gz)
md5sums=('a07903a3f28fbadb7e1e13b052de6253')

if [ "${CARCH}" = "x86_64" ]; then
  _platform=x64
elif [ "${CARCH}" = "i686" ]; then
  _platform=x86
else
  _platform=Arm
fi

_cfg=Release

build() {
  cd "${srcdir}/OpenNI2-2.1-Beta"
  sed -i s/-mfloat-abi=softfp// ThirdParty/PSCommon/BuildSystem/Platform.Arm
  sed -i 's%"./PS1080.ini"%"/etc/OpenNI2/PS1080.ini"%' Source/Drivers/PS1080/Sensor/XnSensor.cpp
  sed -i 's%XN_FILE_LOCAL_DIR "OpenNI.ini"%"/etc/OpenNI2/OpenNI.ini"%' Source/Core/OniContext.cpp
  sed -i 's%XN_FILE_LOCAL_DIR "OpenNI2" XN_FILE_DIR_SEP "Drivers"%"/usr/lib/OpenNI2/Drivers"%' Source/Core/OniContext.cpp
  LDFLAGS+=" -pthread"
  make CFG=${_cfg}

  cd "${srcdir}/OpenNI2-2.1-Beta/Source/Documentation"
  python2 Runme.py
}

package() {
  install -d -m755 "${pkgdir}/etc/ld.so.conf.d"
  echo /usr/lib/OpenNI2/Drivers > "${pkgdir}/etc/ld.so.conf.d/openni2.conf"

  cp -r "${srcdir}/OpenNI2-2.1-Beta/Config" "${pkgdir}/etc/OpenNI2"

  install -d -m755 "${pkgdir}/usr/"{lib/OpenNI2/Drivers,share/doc/OpenNI2}

  cp -r "${srcdir}/OpenNI2-2.1-Beta/Include" "${pkgdir}/usr/include"

  cd "${srcdir}/OpenNI2-2.1-Beta/Bin/${_platform}-${_cfg}"
  install *.so "${pkgdir}/usr/lib"

  cd "${srcdir}/OpenNI2-2.1-Beta/Bin/${_platform}-${_cfg}/OpenNI2/Drivers"
  install *.so "${pkgdir}/usr/lib/OpenNI2/Drivers"

  cd "${srcdir}/OpenNI2-2.1-Beta/Source/Documentation/html"
  install * "${pkgdir}/usr/share/doc/OpenNI2"

  cd "${srcdir}/OpenNI2-2.1-Beta/Redist/Linux"
  sed -i s%/etc/udev/rules.d/%${pkgdir}/etc/udev/rules.d/% install.sh
  install -d -m755 "${pkgdir}/etc/udev/rules.d"
  sh install.sh
}

# vim:set ts=2 sw=2 et:
