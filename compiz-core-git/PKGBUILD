pkgname=compiz-core-git
pkgver=20090508
pkgrel=1
pkgdesc="Composite manager for Aiglx and Xgl"
url="http://www.compiz-fusion.org/"
license=('GPL' 'LGPL' 'MIT')
arch=('i686' 'x86_64')
depends=('startup-notification' 'librsvg' 'libgl' 'dbus' 'mesa' 'libxslt' 'fuse')
makedepends=('git' 'intltool' 'pkgconfig')
conflicts=('compiz-git' 'compiz-core' 'compiz' 'compiz-decorator-gnome' 'compiz-kde-git')
provides=('compiz-git')
replaces=('compiz-git')
groups=('compiz-fusion-git' 'compiz-git' 'compiz-gtk-git' 'compiz-kde-git')
source=()
md5sums=()
options=(!libtool !emptydirs)

_gitroot="git://git.compiz-fusion.org/compiz/core"
_gitname="compiz"

build() {
  msg "Connecting to the GIT server...."
 
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
    cd $_gitname
    git checkout --track -b compiz-0.8 origin/compiz-0.8
  fi

  msg "GIT checkout done"
  msg "Starting make..."

  rm -rf $srcdir/$_gitname-build
  cp -r $srcdir/$_gitname $srcdir/$_gitname-build

  cd $srcdir/$_gitname-build

  ./autogen.sh \
    --prefix=/usr --with-gconf-schema-file-dir=/etc/gconf/schemas \
    --enable-librsvg --enable-dbus\
    --disable-gconf \
    --disable-schemas-install \
    --disable-gnome \
    --disable-gnome-keybindings \
    --disable-gtk \
    --disable-metacity \
    --disable-kde \
    --disable-kde4 \
    --disable-kconfig \
    --disable-static || return 1

  make || return 1
  make DESTDIR=$pkgdir install || return 1

  rm -rf $srcdir/$_gitname-build
}