# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-file-flock
pkgver=2013.07
pkgrel=1
pkgdesc="file locking with flock"
arch=('any')
url="http://search.cpan.org/dist/File-Flock"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-anyevent' 'perl-data-structure-util' 'perl-io-event')
makedepends=('perl-file-slurp' 'perl-test-sharedfork')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MU/MUIR/modules/File-Flock-$pkgver.tar.gz")
md5sums=('3242358f331dad1d1945ed75a568db9e')
sha512sums=('cf32c0ac5e7f6c63d68304c51e10b888b15f23c801724a2aa0a5627f4efdd9fd0059e30ad322e91906cb6f1a22ade2ff622fa3c103bbfc232d4f13f6f78fbc0a')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/File-Flock-${pkgver}"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/File-Flock-${pkgver}"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "${srcdir}/File-Flock-${pkgver}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
