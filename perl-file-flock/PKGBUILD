# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-file-flock
pkgver=2013.04
pkgrel=2
pkgdesc="file locking with flock"
arch=('any')
url="http://search.cpan.org/dist/File-Flock"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-anyevent' 'perl-data-structure-util' 'perl-io-event')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MU/MUIR/modules/File-Flock-$pkgver.tar.gz")
md5sums=('75f7f84172c48dfd764872147f904255')
sha512sums=('d685f5f93ea2a77fed05e5daf87a04b1a44ac670c8fd5f7123bed21b2bff5618ec310aaf086b01a09add4d5b1a95ea1adeecd57e2a5a09ec025b48f8019f32eb')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/File-Flock-${pkgver}"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/File-Flock-${pkgver}"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "${srcdir}/File-Flock-${pkgver}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
