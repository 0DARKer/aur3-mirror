# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-file-flock
pkgver=2013.08
pkgrel=1
pkgdesc="file locking with flock"
arch=('any')
url="http://search.cpan.org/dist/File-Flock"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-anyevent' 'perl-data-structure-util' 'perl-io-event')
makedepends=('perl-file-slurp' 'perl-test-sharedfork')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MU/MUIR/modules/File-Flock-$pkgver.tar.gz")
md5sums=('1325eac209306a0928ec74ae754f77eb')
sha512sums=('f4c1440b750c035856a4fea4c578cf82f918489cb8f4fa4dbda7a2b674635279966addf2c0d55f3b1cf3debb83ccb1ef00ead1e37d2e30f651bf980f25eade3d')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/File-Flock-${pkgver}"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/File-Flock-${pkgver}"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "${srcdir}/File-Flock-${pkgver}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
