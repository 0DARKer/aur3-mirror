pkgname=mingw-w64-cairo
pkgver=1.12.4
pkgrel=1
pkgdesc="Cairo vector graphics library (mingw-w64)"
arch=(any)
url="http://cairographics.org"
license=("LGPL" "MPL")
makedepends=(mingw-w64-gcc patch)
depends=(mingw-w64-crt mingw-w64-glib2 mingw-w64-libpng mingw-w64-fontconfig mingw-w64-pixman)
options=(!libtool !strip !buildflags)
source=("http://cairographics.org/releases/cairo-${pkgver}.tar.xz")
md5sums=('a64bb8774a1e476e5cdd69e635794dfb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  export CFLAGS="$CFLAGS -mms-bitfields"
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-xlib=no \
      --enable-xlib-xrender=no \
      --enable-win32=yes \
      --enable-win32-font=yes \
      --enable-png=yes \
      --enable-shared \
      --enable-static
    sed -i "s/deplibs_check_method=.*/deplibs_check_method=pass_all/g" libtool
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}