# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kwalletmanager-git
pkgver=r791.c55fbcd
pkgrel=1
pkgdesc='Wallet management tool'
arch=('i686' 'x86_64')
url='http://www.kde.org/applications/system/kwalletmanager/'
license=('LGPL')
depends=('kwallet-git' 'kdelibs4support-git')
makedepends=('extra-cmake-modules' 'git')
conflicts=(kwalletmanager)
provides=(kwalletmanager)
source=('git://anongit.kde.org/kwalletmanager.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kwalletmanager
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kwalletmanager \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# move conflicting policy file to /opt
  mv "$pkgdir"/usr/share/polkit-1 "$pkgdir"/opt/kf5/share
  rm -fr "$pkgdir"/usr
}
