# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=hotot
pkgver=0.9.6%7Ehg976
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca) in Python."
arch=('any')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('pywebkitgtk' 'python-notify' 'python-keybinder')
makedepends=('python-distutils-extra')
conflicts=("$pkgname-hg")
install="$pkgname.install"
source=("https://launchpad.net/~hotot-team/+archive/ppa/+files/${pkgname}_${pkgver}-0ubuntu0ppa1%7Eoneiric1.tar.gz")
md5sums=('d77e081c501774e3364dee15060da72d')

build() {
  cd "$srcdir/$pkgname-"*

 # python2 fixes
  sed -i "s|\(env python\).*|\12|;s|\(bin/python\).*|\12|" \
    `grep -Erl "(env python|bin/python)" .`

  # remove google analytics tracking code
  sed -i '/\/\/ 6. run track code/,+4d' data/index.html

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-"*

  python2 setup.py install --prefix=/usr --root="$pkgdir"
}
