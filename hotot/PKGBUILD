# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=hotot
pkgver=0.9.7%7Egit_1374_g70df9a6
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca) in Python."
arch=('any')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('pywebkitgtk' 'python-notify' 'python-keybinder' 'dbus-python' 'hicolor-icon-theme')
makedepends=('python-distutils-extra' 'python2-distribute')
#optdepends=('python2-pyenchant: spell checking support')
install="$pkgname.install"
source=("https://launchpad.net/~hotot-team/+archive/ppa/+files/${pkgname}_${pkgver//_/-}-0ubuntu0ppa1%7Eprecise1.tar.gz")
md5sums=('fe7c2e4601380cb895b0594f11a08e1e')

build() {
  cd "$srcdir/$pkgname-precise"

  # remove google analytics tracking code (tnx to ianux)
  sed -i '/\/\/ 6. run track code/,+14d' data/js/hotot.js
  sed -i '/ga.js/d' data/index.html

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-precise"

  python2 setup.py install --prefix=/usr --root="$pkgdir"

  # python2 fixes
  sed -i 's/\#\!.*python$/&2/' `grep -rl '\#\!.*python' "$pkgdir"`
}
