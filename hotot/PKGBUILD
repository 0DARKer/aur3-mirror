# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=hotot
pkgver=0.9.7%7Egit_1068_gec72c9e
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca) in Python."
arch=('any')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('pywebkitgtk' 'python-notify' 'python-keybinder' 'hicolor-icon-theme')
makedepends=('python-distutils-extra' 'python2-distribute')
#optdepends=('python2-pyenchant: spell checking support')
install="$pkgname.install"
source=("https://launchpad.net/~hotot-team/+archive/ppa/+files/${pkgname}_${pkgver//_/-}-0ubuntu0ppa1%7Eoneiric1.tar.gz")
md5sums=('bfbda950ea16de53549f514091ccf590')

build() {
  cd "$srcdir/$pkgname-oneiric"

  # python2 fixes
  sed -i "s|\(env python\).*|\12|;s|\(bin/python\).*|\12|" \
    `grep -Erl "(env python|bin/python)" .`

  # remove google analytics tracking code
  sed -i '/\/\/ 6. run track code/,+4d' data/index.html

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-oneiric"

  python2 setup.py install --prefix=/usr --root="$pkgdir"
}
