# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
_basename=nqp
pkgname=$_basename-moarvm
pkgver=2014.03
pkgrel=1
pkgdesc="High-level environment to create compilers and libraries for virtual machines (MoarVM backend)"
arch=('any')
url='https://github.com/perl6/nqp#readme'
license=(PerlArtistic)
depends=('bash' 'moarvm>=2014.03')
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$_basename/$_basename-$pkgver.tar.gz)
sha512sums=('08c19f514cac444af55fe51c64ed1901dc59a8d0f6cae2262e4e04cd8bdf75bfc0f420bd15b11f964bf58f2d065a1457b674089859510500430055c19c4dbb40')

build() {
    cd "$_basename-$pkgver"

    perl Configure.pl --backends=moar --prefix=/usr
    make
}
package() {
    cd "$_basename-$pkgver"
    make DESTDIR="$pkgdir/" install
}

