# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
_basename=nqp
pkgname=$_basename-moarvm
pkgver=2015.01
pkgrel=1
pkgdesc="High-level environment to create compilers and libraries for virtual machines (MoarVM backend)"
arch=('any')
url='https://github.com/perl6/nqp#readme'
license=(PerlArtistic)
depends=('bash' "moarvm>=$pkgver")
makedepends=('perl>=5.8')
options=('!makeflags')
source=(http://rakudo.org/downloads/$_basename/$_basename-$pkgver.tar.gz)
sha512sums=('61865a6bf5e6adf07397ea0c985a4b20a61cf30d125849b3f2b7d04848d12c4c3858fe627a82369b6ac9142b99e2ca3fadf775eeb76ba6a9fb45f558a5148022')

build() {
    cd "$_basename-$pkgver"

    perl Configure.pl --backends=moar --prefix=/usr
    make
}

package() {
    cd "$_basename-$pkgver"
    make DESTDIR="$pkgdir/" install
}
