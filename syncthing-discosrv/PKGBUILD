# Maintainer : Kevin MacMartin <prurigro@gmail.com>

pkgname=syncthing-discosrv
pkgver=0.8.18
pkgrel=1
pkgdesc="Discover server for the syncthing P2P synchronization utility"
url="http://syncthing.net"
license=('MIT')
install=${pkgname}.install
depends=('glibc')
makedepends=('git' 'go' 'godep' 'mercurial')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')

sha512sums=('38c01db52c5802ffc0d4ec33ce19d44070cce8d55a8342fea06600aa0112f8b51a0d200f0b90494d0c5f86813321ba69b9d0c25c1fd4038154934345d32a0ede' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
source=("${pkgname}.service"
        "git://github.com/calmh/syncthing.git"
        "git://github.com/vitrun/qart.git"
        "git://github.com/codegangsta/inject.git"
        "git://github.com/codegangsta/martini.git"
        "git://github.com/juju/ratelimit.git"
        "git://github.com/golang/groupcache.git"
        "hg+https://code.google.com/p/go.crypto/"
        "hg+https://code.google.com/p/go.text/"
        "hg+https://bitbucket.org/kardianos/osext/")

prepare() {
    # Remove Old Build Environment (if it exists)
    [[ -d src ]] && rm -rf src

    # Setup Build Environment
    install -d src/github.com/vitrun
    mv qart src/github.com/vitrun/

    install -d src/github.com/codegangsta
    mv inject src/github.com/codegangsta/
    mv martini src/github.com/codegangsta/

    install -d src/github.com/juju
    mv ratelimit src/github.com/juju/

    install -d src/github.com/golang
    mv groupcache src/github.com/golang/

    install -d src/code.google.com/p
    mv go.crypto src/code.google.com/p/

    install -d src/code.google.com/p
    mv go.text src/code.google.com/p/

    install -d src/bitbucket.org/kardianos
    mv osext src/bitbucket.org/kardianos/

    install -d src/github.com/calmh
    mv syncthing src/github.com/calmh/
    cd src/github.com/calmh/syncthing
    git checkout tags/v${pkgver}
}

build() {
    export GOPATH="$srcdir"

    echo "Building Syncthing discovery server dependencies"
    cd src/github.com/calmh/syncthing
    ./build.sh deps

    echo "Building Syncthing discovery server"
    cd discover/cmd/discosrv
    go build main.go
}

package() {
    install -Dm644 ${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
    install -Dm644 src/github.com/calmh/syncthing/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm755 src/github.com/calmh/syncthing/discover/cmd/discosrv/main "${pkgdir}/usr/bin/${pkgname}"
}
