# Maintainer : Kevin MacMartin <prurigro@gmail.com>

_pkgname=syncthing
pkgname=${_pkgname}-discosrv
pkgver=0.9.15
pkgrel=1
pkgdesc="Discover server for the ${_pkgname} P2P synchronization utility"
url="http://${_pkgname}.net"
license=('MIT')
install=${pkgname}.install
depends=('glibc')
makedepends=('git' 'go' 'godep' 'mercurial')
arch=('i686' 'x86_64' 'armv6h' 'armv7h')

sha512sums=('38c01db52c5802ffc0d4ec33ce19d44070cce8d55a8342fea06600aa0112f8b51a0d200f0b90494d0c5f86813321ba69b9d0c25c1fd4038154934345d32a0ede'
            'cc111a7df758ffac3d6f732c88e3da77bf8ba104ee0da150e68412a3d61fc3844d27a66454d0bcae24e7dbc030a06f5c839d7d5f214610fededadeba7557f936'
            'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP' 'SKIP')
source=("${pkgname}.service"
        "${pkgname}.tmpfiles.conf"
        "git+https://github.com/${_pkgname}/discosrv.git"
        "git+https://github.com/golang/groupcache"
        "git+https://github.com/juju/ratelimit"
        "git+https://github.com/syncthing/syncthing"
        "git+https://github.com/bkaradzic/go-lz4.git"
        "git+https://github.com/calmh/xdr.git"
        "git+https://github.com/syndtr/goleveldb"
        "hg+https://code.google.com/p/go.text/"
        "hg+https://code.google.com/p/snappy-go/"
)

prepare() {
    # Remove Old Build Environment (if it exists)
    [[ -d src ]] && rm -rf src

    # Setup Build Environment
    install -d src/github.com/golang
    mv groupcache src/github.com/golang/

    install -d src/github.com/juju
    mv ratelimit src/github.com/juju/

    install -d src/github.com/bkaradzic
    mv go-lz4 src/github.com/bkaradzic/

    install -d src/github.com/calmh
    mv xdr src/github.com/calmh/

    install -d src/github.com/syndtr
    mv goleveldb src/github.com/syndtr/

    install -d src/code.google.com/p
    mv go.text src/code.google.com/p/
    mv snappy-go src/code.google.com/p/

    install -d src/github.com/${_pkgname}
    mv discosrv src/github.com/${_pkgname}/
    mv $_pkgname src/github.com/${_pkgname}/
    cd src/github.com/${_pkgname}/${_pkgname}
    git checkout tags/v${pkgver}
}

build() {
    export GOPATH="$srcdir"
    cd src/github.com/${_pkgname}/discosrv
    go build *.go
}

package() {
    install -Dm644 ${pkgname}.service "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
    install -Dm644 ${pkgname}.tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/${pkgname}.conf
    install -Dm644 src/github.com/${_pkgname}/${_pkgname}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm755 src/github.com/${_pkgname}/discosrv/main "${pkgdir}/usr/bin/${pkgname}"
}
