# Maintainer: Antonio Rojas < nqn1976 @ gmail.com >
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Osman Ugus <ugus11@yahoo.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Special thanks to Nareto for moving the compile from the .install to the PKGBUILD

pkgname=sage-mathematics
pkgver=4.8
pkgrel=4
pkgdesc='Open Source Mathematics Software, a viable free alternative to Magma, Maple, Mathematica, and Matlab.'
url='http://www.sagemath.org'
arch=('i686' 'x86_64')
license=('GPL')
depends=()
makedepends=('gcc-fortran' 'desktop-file-utils' 'imagemagick' 'texlive-core')
optdepends=('imagemagick: some plotting functionality benefits from it'
            'texlive-core: some plotting functionality benefits from it, also to use SageTeX'
            'jsmath-fonts: native TeX fonts for the notebook')
install="$pkgname.install"
source=("http://boxen.math.washington.edu/home/release/sage-$pkgver/sage-$pkgver.tar"
        'SAGE-notebook.desktop')
md5sums=('3e9725846a20f624a9a6811107fc5cb0'
         'cc47a29bc32fbdea48f8a19e2ad70441')

build() {
  cd sage-$pkgver

  # fix "missing sage.all error" during build
  unset CFLAGS
  unset CXXFLAGS

  # fix linbox build with GCC 4.7 http://trac.sagemath.org/sage_trac/ticket/12762
  CXXFLAGS="-fpermissive"

  # fix build errors
  unset LDFLAGS

  # parallel build
  # export MAKE="make -j2"

  # use archlinux's fortran rather then the one that ships with sage to compile sage's fortran
  export SAGE_FORTRAN='/usr/bin/gfortran'
  export SAGE_FORTRAN_LIB='/usr/lib/libgfortran.so'

  # disable building with debugging support
  export SAGE_DEBUG='no'

  # enable fat binaries (disables processor specific optimizations)
  # comment out if you're only building it for yourself
  # export SAGE_FAT_BINARY='yes'
  
  # Fixes missing symbol _ZN23Parma_Polyhedra_Library13have_sse_unitE on i686
  if [[ "$CARCH" == "i686" ]]; then
   cd spkg/standard
   tar xjf ppl-0.11.2.p0.spkg
   sed -i 's/--enable-interfaces=c++/--enable-interfaces="c++ c"/' ppl-0.11.2.p0/spkg-install
   tar cjf ppl-0.11.2.p0.spkg ppl-0.11.2.p0
   rm -r ppl-0.11.2.p0
   cd ../..
  fi
  
  make
}

check() {
  cd sage-$pkgver

  make ptest || /bin/true
  # uncomment if you want to run all the tests (warning: very long)
  # make ptestlong || /bin/true
}

package() {
  cd sage-$pkgver

  # cp because make install is experimental and will corrupt the install
  install -d $pkgdir/opt/sage
  cp -r * $pkgdir/opt/sage

  desktop-file-install $srcdir/SAGE-notebook.desktop --dir $pkgdir/usr/share/applications 

  # create link to main binary
  install -d $pkgdir/usr/bin
  ln -s /opt/sage/sage $pkgdir/usr/bin/sage

  # remove build logs
  rm -f $pkgdir/opt/sage/install.log

  # remove source packages, since they are rarely needed, they are 300mb in size (compressed)
  # no need to package them together, put into sage-mathematics-spkgs
  rm -rf $pkgdir/opt/sage/spkg/{base,standard,logs}
}
