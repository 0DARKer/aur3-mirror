# Maintainer: veox <veox at wre dot ath dot cx>
# Contributor: Rorschach <r0rschach@lavabit.com>
# Contributor: Cedric Chabanois <cchabanois@gmail.com>

pkgname=i2p-dev
_pkgname=i2p
pkgver=0.9
pkgrel=19
pkgdesc="A distributed anonymous network (daily mtn->git sync)"
url="http://www.i2p2.de"
license=('GPLv2')
arch=('x86_64' 'i686')
depends=('java-runtime' 'gettext')
makedepends=('apache-ant' 'java-environment' 'git')
conflicts=('i2p' 'i2p-bin' 'i2p-portable' 'i2p-portable-source')
provides=('i2p')
install='i2p.install'
source=('i2prouter' 'i2prouter.service')
sha256sums=('f40c0996b4b0d83b443f1f616bc54bb18735872020e33a50b0f1fbad59c97d7d'
            'dee85e9a856b18bac257cd06e70f42e88edce3f48b92762410359a69d5c4284f')

__gitroot="git://github.com/i2p/i2p.i2p.git"
__gitname="i2p.i2p"

build() {
    cd "$srcdir"

    msg "Connecting to GIT server..."
    if [ -d "${__gitname}/.git" ]; then
        cd "$__gitname" && git pull origin
        msg "The local repository was updated."
    else
        git clone ${__gitroot} ${__gitname}
        cd "$__gitname"
        msg "The remote repository was cloned."
    fi

    source /etc/profile.d/apache-ant.sh
    ant installer-linux
}

package() {
    cd "$srcdir/$__gitname"

    echo "INSTALL_PATH=$pkgdir/opt/i2p" > install.properties
    java -jar i2pinstall_${pkgver}*.jar -options install.properties
    
    sed -i "s|$pkgdir/opt/i2p|/opt/i2p|g"       $pkgdir/opt/i2p/{i2prouter,wrapper.config,runplain.sh,eepget}
    sed -i "s|#RUN_AS_USER=.*|RUN_AS_USER=i2p|" $pkgdir/opt/i2p/i2prouter

    install -Dm755 ${srcdir}/i2prouter               "${pkgdir}/etc/rc.d/i2prouter"
    install -Dm644 ${srcdir}/i2prouter.service       "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1    "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/man/i2prouter.1 "${pkgdir}/usr/share/man/man1/i2prouter.1"
    install -d "${pkgdir}/usr/share/licenses/${_pkgname}/"
    mv ${pkgdir}/opt/i2p/licenses/* LICENSE.txt "${pkgdir}/usr/share/licenses/${_pkgname}/"
    rm -r $pkgdir/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,licenses,man}
}

