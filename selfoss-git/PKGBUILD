# Maintainer: Tetsumaki <http://goo.gl/YMBdA>

pkgname=selfoss-git
_gitname=selfoss
pkgver=20130407
pkgrel=1
pkgdesc="The new multipurpose rss reader, live stream, mashup, aggregation web application"
arch=('any')
url="http://selfoss.aditu.de/"
license=('GPL3')
depends=('php')
makedepends=('git')
conflicts=('selfoss')
optdepends=('apache: is depedency if you do not use lighttpd or nginx'
            'lighttpd'
            'nginx'
            'mariadb: is depedency if you do not use sqlite'
            'php-sqlite: is dependacy if you use sqlite'
            'wget: for automatic updating feeds with cron'
            'curl: for automatic updating feeds with cron')
install=${pkgname}.install
source=('git://github.com/SSilence/selfoss.git')
sha1sums=('SKIP')

pkgver() {
	cd "${_gitname}"
	git log -1 --format="%cd" --date=short | tr -d '-'
}

package() {
	cd "${_gitname}"
	install -dm755 "${pkgdir}/usr/share/webapps/${_gitname}"
	cp -rf . "${pkgdir}/usr/share/webapps/${_gitname}"
	cd "${pkgdir}/usr/share/webapps/${_gitname}"
	rm -rf {.git,.gitignore,_docs,README.md}
	chmod 755 data/cache data/favicons data/logs data/sqlite data/thumbnails public
}