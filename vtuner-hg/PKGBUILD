# Maintainer: Tomasz Maciej Nowak <com[dot]gmail[at]tmn505>
pkgbase=vtuner-hg
pkgdesc="access DVB devices over the network"
pkgname=('vtuner-client' 'vtuner-server')
pkgver=116.b6fa0d2b133b
pkgrel=1
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
url="http://code.google.com/p/vtuner/"
license=('GPL2')
makedepends=('mercurial' 'linux-headers' 'gcc' 'make' 'patch')
source=("hg+https://code.google.com/p/vtuner.apps"
        "hg+https://code.google.com/p/vtuner.linux-driver"
        'kernel-3.10.patch'
        'add_arm_targets.diff'
        '93-vtunerc.rules'
        'vtunerc@.service'
        'vtunerd.service'
        'vtunerc0'
        'vtunerd'
        'vtunerc-driver_kmod')
sha256sums=('SKIP'
            'SKIP'
            'bcdf6ff8e18e7f4c50bcb758700728d4aa7c3643030c7d6f90b904dc75adac5b'
            '86b861ddb581fe90bee4e16643289d4fec36f923f587205eda3da85b774fab22'
            '4b2d860cac8c8bab2d8fa79760d4405bef6fd042f4e78b680d25d1a5ebcbf83d'
            '870e48fb79b9065b5c98056df1aa6638c4dd3c5340331aed8a91af45f2162477'
            '9e4568a92caf05b5add190e792b788100aea3aac783ff1681c816a7ac87c631b'
            '964b7bff3850e613ab84770438ec602eb721e0ed5f6a3e803e76e0b657e9a5cd'
            'c47790245befba5f0c1424e48854a2c06f62edc819c797ff14a15e80128568bc'
            '8cf60a015965830d86ee213bbae1d066c72941b716acf535ccb799aa884631f3')

pkgver() {
  cd "${srcdir}/vtuner.apps"
  hg identify -ni | awk 'BEGIN{OFS=".";} {print $2,$1}'
}

prepare() {
  # linux headers or source tree location
  export KDIR="/usr/lib/modules/`uname -r`/build"
  cd "${srcdir}/vtuner.linux-driver"
  # this patch is mandtory for all kernels after ver 3.9
  if [ `awk '/PATCHLEVEL =/ { print $3 }' $KDIR/Makefile` -ge "10" ]; then
    patch -p1 -i "${srcdir}/kernel-3.10.patch"
  fi
  cd "${srcdir}/vtuner.apps"
  patch -p1 -i "${srcdir}/add_arm_targets.diff"
}

build() {
  cd "${srcdir}/vtuner.linux-driver"
  make
  gzip -9 "${srcdir}/vtuner.linux-driver/vtunerc.ko"
  cd "${srcdir}/vtuner.apps"
  make ${CARCH}
}

package_vtuner-client() {
  pkgdesc="VTuner client and kernel module for virtual DVB's"
  install='vtunerc.install'
  install -Dm644 "${srcdir}/vtuner.linux-driver/vtunerc.ko.gz" "${pkgdir}/usr/lib/modules/`uname -r`/extramodules/vtunerc.ko.gz"
  install -Dm744 "${srcdir}/vtuner.apps/dist/${CARCH}/vtunerc.${CARCH}" "${pkgdir}/usr/bin/vtunerc"
  install -Dm644 "${srcdir}/93-vtunerc.rules" "${pkgdir}/usr/lib/udev/rules.d/93-vtunerc.rules"
  install -Dm644 "${srcdir}/vtunerc@.service" "${pkgdir}/usr/lib/systemd/system/vtunerc@.service"
  install -Dm744 "${srcdir}/vtunerc-driver_kmod" "${pkgdir}/usr/lib/systemd/scripts/vtunerc-driver_kmod"
  install -Dm644 "${srcdir}/vtunerc0" "${pkgdir}/etc/conf.d/vtunerc0"
}

package_vtuner-server() {
  pkgdesc="VTuner server for broadcasting local DVB devices in network"
  install -Dm744 "${srcdir}/vtuner.apps/dist/${CARCH}/vtunerd.${CARCH}" "${pkgdir}/usr/bin/vtunerd"
  install -Dm644 "${srcdir}/vtunerd.service" "${pkgdir}/usr/lib/systemd/system/vtunerd.service"
  install -Dm644 "${srcdir}/vtunerd" "${pkgdir}/etc/conf.d/vtunerd"
}
