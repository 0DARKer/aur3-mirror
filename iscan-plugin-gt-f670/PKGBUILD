# Maintainer: garion < garion @ mailoo.org >
# Based on the PKGBUILD from http://aur.archlinux.org/packages.php?ID=40933

pkgname=iscan-plugin-gt-f670
pkgver=2.1.0_3
pkgrel=4
pkgdesc="iscan plugin for Epson Perfection Photo V200 scanners"
arch=('i686' 'x86_64')
url="http://avasys.jp/eng/linux_driver/"
license=('custom')
depends=('iscan')
makedepends=('rpmextract')
install=gt-f670.install

  _VERSION="2.1.0-3"
if [ "$CARCH" = "i686" ]; then
  source=(http://linux.avasys.jp/drivers/scanner-plugins/GT-F670/$pkgname-$_VERSION.c2.i386.rpm)
  _ARCH=i386
  md5sums=('193e270a38ac9941f03e012bb3bb4483')
elif [ "$CARCH" = "x86_64" ]; then
  source=(http://linux.avasys.jp/drivers/scanner-plugins/GT-F670/$pkgname-$_VERSION.c2.x86_64.rpm)
  _ARCH=x86_64
  md5sums=('64399c73c701d0c45b9135c43613c96d')
fi

build() {
  cd $srcdir
  rpmextract.sh $pkgname-$_VERSION.c2.$_ARCH.rpm
}

package() {
  if [ "$CARCH" = "i686" ]; then
    _lib='lib'
  elif [ "$CARCH" = "x86_64" ]; then
    _lib='lib64'
  fi
  install -d $pkgdir/usr/lib/iscan
  install -m755 $srcdir/usr/$_lib/iscan/libesint7A.so.2.0.1 $pkgdir/usr/lib/iscan/
  install -m755 $srcdir/usr/$_lib/iscan/libesint7A.so.2 $pkgdir/usr/lib/iscan/
  install -m755 $srcdir/usr/$_lib/iscan/libesint7A.so $pkgdir/usr/lib/iscan/
  install -d $pkgdir/usr/share/iscan
  install -m644 $srcdir/usr/share/iscan/esfw7A.bin $pkgdir/usr/share/iscan
  install -d $pkgdir/usr/share/licenses/iscan
  install -m644 $srcdir/usr/share/doc/$pkgname-${_VERSION%-*}/AVASYSPL.en.txt $pkgdir/usr/share/licenses/iscan/
}

