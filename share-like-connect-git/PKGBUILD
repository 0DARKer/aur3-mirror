pkgname=share-like-connect-git
pkgver=0.308.88.g4343ecf
pkgrel=1
pkgdesc="The share like and connect feature for KDE Plasma Active 2.0"
arch=(i686 x86_64)
url="http://community.kde.org/Plasma/Active"
license=('LGPL')
depends=('kdebase-workspace')
makedepends=('automoc4' 'cmake' 'git')
provides=('share-like-connect')
conflicts=('share-like-connect')
source=('git://anongit.kde.org/share-like-connect')
md5sums=('SKIP')

pkgver() {
  cd share-like-connect
  echo "0.$(git rev-list --count HEAD).$(git describe --always | sed 's|Active/3.0.||' | sed 's|-|.|g')"
}

build() {
  mkdir -p build
  cd build

  cmake ../share-like-connect \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix)
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
