# Author: RetroX <classixretrox@gmail.com>
pkgname=mingw32-dlfcn
pkgver=r19
pkgrel=1
pkgdesc="A wrapper for dlfcn to the Win32 API (mingw32)"
arch=(any)
url="http://code.google.com/p/dlfcn-win32"
license=('LGPL')
makedepends=('mingw32-gcc')
options=(!strip !buildflags)
depends=('mingw32-runtime')
source=("http://dlfcn-win32.googlecode.com/files/dlfcn-win32-${pkgver}.tar.bz2")
md5sums=('6e321b36ee289179777bab07dcfe5510')

build()
{
  cd "$srcdir/dlfcn-win32-$pkgver"
  unset LDFLAGS CPPFLAGS
  ./configure \
    --prefix=/usr/i486-mingw32 \
    --libdir=/usr/i486-mingw32/lib \
    --incdir=/usr/i486-mingw32/include \
    --enable-shared \
    --enable-static \
    --cc=i486-mingw32-gcc \
    --cross-prefix=/usr/i486-mingw32/bin/
  make
}

package() {
  cd "$srcdir/dlfcn-win32-$pkgver"
  make DESTDIR=$pkgdir install
  
  i486-mingw32-strip -x -g $pkgdir/usr/i486-mingw32/bin/*.dll
  i486-mingw32-strip -g $pkgdir/usr/i486-mingw32/lib/*.a
}
