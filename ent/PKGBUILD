# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: Nicolas Martyanoff <khaelin@gmail.com>
pkgname=ent
pkgver=1.0
pkgrel=4
pkgdesc="Pseudorandom number sequence test"
arch=('i686' 'x86_64')
url="http://www.fourmilab.ch/random"
license=('custom')
depends=('glibc')
changelog=Changelog
source=('http://www.fourmilab.ch/random/random.zip' 'ent.1')
sha256sums=('c14ed62db24511848d6b37d4bf8869185b65be9042ffad5272257f3a249569ec'
            'de8f8dac65dcbd08ccd69b7a5f11816d05801268a1307fcb10a05421f12b680b')

build() {
	cd $srcdir
	make -e
}

package() {
	cd $srcdir

	# binary
	install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname

	# license
	install -dm755 $pkgdir/usr/share/licenses/$pkgname
	cat > $pkgdir/usr/share/licenses/$pkgname/LICENSE <<EOF 
This software is in the public domain. Permission to use, copy, modify, and distribute this software and its documentation for any purpose and without fee is hereby granted, without any conditions or restrictions. 
This software is provided “as is” without express or implied warranty.
EOF

	# man page
	install -Dm644 ent.1 $pkgdir/usr/share/man/man1/ent.1

	# html documentation
	install -dm755 $pkgdir/usr/share/doc/$pkgname
	install -m644 ent.html entitle.gif $pkgdir/usr/share/doc/$pkgname
}

