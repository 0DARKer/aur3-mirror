# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: Nicolas Martyanoff <khaelin@gmail.com>
pkgname=ent
pkgver=1.1
pkgrel=2
pkgdesc="Pseudorandom number sequence test"
arch=('i686' 'x86_64')
url="http://packages.debian.org/sid/ent"
license=('custom')
depends=('glibc')
makedepends=('asciidoc' 'docbook2x' 'perl-xml-libxml')
changelog=Changelog
source=("http://ftp.de.debian.org/debian/pool/main/e/${pkgname}/${pkgname}_${pkgver}debian.orig.tar.gz"
        'LICENSE'
	'Makefile.patch'
	'randtest.patch'
	'totalcount.patch')
sha256sums=('8ac6606208ed8ed094479f6e12fe28a65bab0fd5c8eb054b74c65c86f98427a0'
            '4ebee40438cb4df5dadbe3028002fdc17f07daee751cb79d53d7f55fbc37ce36'
            '705299013de116edaab81a2cec100641647c2dab8fb3394fe80a97442854fb7c'
            '465aef1e6edacd507f74dcf5914d9a20e3c2f286289a387173c403ed8270260b'
            '47c2f45ed431b7c3f31649fc1752623e9ba325e995336493fae3842dfdc0dba3')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}debian"

	# apply debian patches
	for patch in ../*.patch ; do
		patch -Ni $patch
	done

	# fix for docbook2man binary name
	sed -i 's/docbook2x-man/docbook2man/g' Makefile
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}debian"
	make -e
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}debian"
	make test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}debian"

	# binary
	install -Dm 755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

	# license
	install -Dm 644 "../LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# man page
	install -Dm 644 ent.1 "$pkgdir/usr/share/man/man1/ent.1"
}

