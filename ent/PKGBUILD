# Maintainer: Michael Lass <bevan@bi-co.net>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: Nicolas Martyanoff <khaelin@gmail.com>
pkgname=ent
pkgver=1.1
debianver="${pkgver}debian-4"
pkgrel=3
pkgdesc="Pseudorandom number sequence test"
arch=('i686' 'x86_64')
url="http://packages.debian.org/sid/ent"
license=('custom')
depends=('glibc')
makedepends=('asciidoc' 'docbook2x' 'perl-xml-libxml')
source=("http://ftp.de.debian.org/debian/pool/main/e/${pkgname}/${pkgname}_${pkgver}debian.orig.tar.gz"
	"http://ftp.de.debian.org/debian/pool/main/e/${pkgname}/${pkgname}_${debianver}.debian.tar.xz"
	"LICENSE")
md5sums=('482e9d7ab804a37b3ed52c4a332317e9'
         'c474f3176606a139a979fa8abb4f75c7'
         '7790c4192af516c4b30b138b66355545')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}debian"

	# apply debian patches
	for patch in $(cat ../debian/patches/series) ; do
		patch -p1 < ../debian/patches/$patch
	done

	# fix for docbook2man binary name
	sed -i 's/docbook2x-man/docbook2man/g' Makefile
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}debian"
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}debian"
	make test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}debian"

	# binary
	install -Dm 755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

	# license
	install -Dm 644 "../LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# man page
	install -Dm 644 ent.1 "$pkgdir/usr/share/man/man1/ent.1"
}

