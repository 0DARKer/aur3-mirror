# Maintainer: goll <adrian.goll+aur[at]gmail>

pkgname=tixati
pkgver=2.11
pkgrel=1
pkgdesc="A simple P2P client that is compatible with the BitTorrent protocol"
arch=('i686' 'x86_64')
url="http://www.tixati.com/"
license=('custom:tixati')
depends=('gtk2' 'hicolor-icon-theme')
optdepends=('gconf: for shell integration')
install=$pkgname.install
source_i686=("http://tixati.com/download/$pkgname-${pkgver}-${pkgrel}.i686.manualinstall.tar.gz"
             "LICENSE")
source_x86_64=("http://tixati.com/download/$pkgname-${pkgver}-${pkgrel}.x86_64.manualinstall.tar.gz"
               "LICENSE")
sha1sums_i686=('6216b8ad160ce3a806372a03ce5ee4b268878a7d'
               '35d7f63a3b0dbf3f8f2bd0a45a7f3c96b453e86b')
sha1sums_x86_64=('5212097b362156fd5bbb2a088541343acd31a6a4'
                 '35d7f63a3b0dbf3f8f2bd0a45a7f3c96b453e86b')

package() {
  cd "$srcdir/$pkgname-${pkgver}-${pkgrel}.$CARCH.manualinstall"
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png \
    "$pkgdir/usr/share/icons/hicolor/48x48/apps/$pkgname.png"
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
