pkgname=lib32-gstreamer0.10-base
pkgver=0.10.35
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Base plugin libraries (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=("lib32-gstreamer0.10" 'lib32-orc' 'lib32-libxv')
makedepends=('gcc-multilib' 'lib32-alsa-lib' 'cdparanoia'
	     'lib32-libvisual' 'lib32-libvorbis' 'lib32-libtheora'
	     'lib32-pango')
options=('!libtool' '!emptydirs')
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/gst-plugins-base/gst-plugins-base-${pkgver}.tar.bz2)
sha256sums=('cd24f01bb5258a1f400bc4f2c212bb7cee9ee23c9ffb80d537a24ef366d17103')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  
  cd "${srcdir}/gst-plugins-base-${pkgver}"
  
  sed -i '/AC_PATH_XTRA/d' configure.ac
  autoreconf
  ./configure \
  	--prefix=/usr \
  	--sysconfdir=/etc \
  	--localstatedir=/var \
  	--disable-static \
  	--enable-experimental \
  	--disable-gnome_vfs \
  	--with-package-name="GStreamer Base Plugins (Archlinux)" \
	--with-package-origin="http://www.archlinux.org/" \
	--libdir=/usr/lib32 \
	--enable-introspection=no

  make
  sed -e 's/^SUBDIRS_EXT =.*/SUBDIRS_EXT =/' -i Makefile
}

package() {
  cd "${srcdir}/gst-plugins-base-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
