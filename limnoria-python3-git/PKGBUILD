# Maintainer: Cravix < dr dot neemous at gmail dot com >

pkgname=limnoria-python3-git
_pkgname=Limnoria
pkgver=0.83.4.1.8722.65b553f
pkgrel=1
pkgdesc="An IRC bot based on Supybot, with sqlite3 support and other features (unstable version)"
arch=('any')
url="https://github.com/ProgVal/Limnoria"
license=('3-clause BSD')
depends=('python')
makedepends=('git')
optdepends=('pyopenssl: SSL server support'
            'python-feedparser: RSS plugin support (but seems broken now)')
source=("git://github.com/ProgVal/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"

  echo $(awk -F"['+]" '/% version/ {print $2}' ./setup.py).$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_pkgname"
  
  rename "supybot" "supybot3" ./docs/man/* ./scripts/*
  sed -i '/man1/s/supybot/supybot3/; s/\(scripts\/supybot\)/\13/' setup.py
  python3 setup.py build
}

package() {
  cd "$srcdir/$_pkgname"

  python3 setup.py install --root="$pkgdir" || return 1
}
