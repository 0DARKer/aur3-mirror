# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=teeworlds-ddnet
pkgver=6.1.3
pkgrel=1
pkgdesc="A customized version by DDraceNetwork of this 2D shooting game"
arch=('i686' 'x86_64')
url="http://ddnet.tw"
license=('custom')
depends=('alsa-lib' 'glu' 'sdl' 'freetype2')
makedepends=('imagemagick' 'gendesk')
provides=('teeworlds')
conflicts=('teeworlds')
  # ico file from https://github.com/def-/ddnet
source=(DDNet.ico) 
md5sums=('9ab24a6ac2e2d1bbfe06a5032f4fd3b8')
  # binary tarball
_arch=x86_64
[[ $CARCH == i686 ]] && _arch=x86
source+=("$url/downloads/DDNet-$pkgver-linux_$_arch.tar.gz")
md5sums+=(`curl -sL $url/downloads/md5sums | grep "DDNet-$pkgver-linux_$_arch.tar.gz" | cut -d' ' -f1`)

prepare() {
  convert "$srcdir/DDNet.ico" "$srcdir/$pkgname.png"
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --name 'Teeworlds' --categories 'Game;ArcadeGame'
}

package() {
  cd DDNet-$pkgver-linux_$_arch
  
    # install binaries
  install -Dm755 DDNet "$pkgdir"/usr/bin/teeworlds-ddnet
  install -Dm755 DDNet-Server "$pkgdir"/usr/bin/teeworlds-ddnet_srv
  install -Dm755 dilate "$pkgdir"/usr/bin/dilate

    # Install data files
  mkdir -p ${pkgdir}/usr/share/teeworlds/data
  cp -r data/* ${pkgdir}/usr/share/teeworlds/data
  
    # Install desktop and icon files
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/${pkgname}-5.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  
    # Install license files
  install -Dm644 "license.txt" "$pkgdir/usr/share/licenses/$pkgname/license.txt"
  install -Dm644 "license_DDRace.txt" "$pkgdir/usr/share/licenses/$pkgname/license_DDRace.txt"
}
