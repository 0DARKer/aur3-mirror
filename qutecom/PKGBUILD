# QuteCom: Installer: Arch
# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>
# Contributor: Larry Hajali <larryhaja[at]gmail[dot]com>
# Contributor: said

pkgname=qutecom
_pkgname=QuteCom
pkgver=2.2
pkgrel=2
pkgdesc="A free VoIP softphone, superseding WengoPhone."
arch=("i686" "x86_64")
url="http://www.${pkgname}.org/"
license=("GPL")
depends=("qt" "gnutls" "e2fsprogs" "libxml2" "speex" "ffmpeg" "libsamplerate" "curl" "boost-libs")
makedepends=("gcc" "cmake" "boost")
conflicts=("wengophone2" "${pkgname}-hg")
replaces=("wengophone2")
options=(!emptydirs)
install=qutecom.install
source=(http://www.${pkgname}.org/downloads/${_pkgname}-${pkgver}.tar.gz new-videodev.patch)
md5sums=('c118cb2bd372fbeb41d205dac7de4001'
         '52b536108d9c9aabe6e07a18f72db81b')

build() {
  _srcdir=$(tar -tf $srcdir/${_pkgname}-$pkgver.tar.gz 2>/dev/null | \
            head -1 | cut -d '/' -f1)
  cd "${srcdir}"/${_srcdir}
  cp  ${srcdir}/DefineQuteComOptions_MOD.cmake DefineQuteComOption.cmake
  patch -p1 < ${srcdir}/new-videodev.patch
  # Fix a header name.
  (cd libs/qtutil/include/qtutil
    ln -s LanguageChangeEventFilter.h languagechangeeventfilter.h)

  mkdir -p build_${pkgname}
  cd build_${pkgname}
  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
    -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
    -DCMAKE_BUILD_TYPE=Release .. || return 1

  make VERBOSE=1 || return 1
}

package() {
  _srcdir=$(tar -tf $srcdir/${_pkgname}-$pkgver.tar.gz 2>/dev/null | \
            head -1 | cut -d '/' -f1)
  cd "${srcdir}"/${_srcdir}/build_${pkgname}

  make install DESTDIR="${pkgdir}"
}
