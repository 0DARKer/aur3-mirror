# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gd
pkgname=lib32-${_pkgbase}
pkgver=2.1.0
pkgrel=2
pkgdesc="Library for the dynamic creation of images by programmers (32 bit)"
arch=('x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('lib32-libpng' 'lib32-fontconfig' 'lib32-libjpeg' "${_pkgbase}")
makedepends=('gcc-multilib') #perl
options=('!libtool')
source=("https://bitbucket.org/libgd/gd-libgd/downloads/libgd-${pkgver}.tar.xz")
md5sums=('03588159bf4faab9079849c8d709acc6')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'  
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd libgd-${pkgver}
  ./configure --prefix=/usr --without-{xpm,vpx,tiff} --libdir=/usr/lib32
  make
}

package() {
  cd libgd-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  rm -rf "$pkgdir/usr/include"
  # Config script pointing libdir to /usr/lib32 (lib32-libgphoto2 needs it)
  mv "$pkgdir/usr/bin/gdlib-config"{,-32}
  find "$pkgdir/usr/bin" -type f ! -name "gdlib-config-32" -exec rm {} \;
  
}
