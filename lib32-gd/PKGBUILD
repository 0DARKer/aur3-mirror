# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gd
pkgname=lib32-${_pkgbase}
pkgver=2.1.0
pkgrel=3
pkgdesc="Library for the dynamic creation of images by programmers (32 bit)"
arch=('x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('lib32-fontconfig' 'lib32-libvpx' 'lib32-libxpm' 'lib32-libtiff' "${_pkgbase}")
makedepends=('gcc-multilib') #perl
options=('!libtool')
source=("https://bitbucket.org/libgd/gd-libgd/downloads/libgd-${pkgver}.tar.xz"
        'color_c_null_pointer.patch')
md5sums=('03588159bf4faab9079849c8d709acc6'
         '51f5ff073eabbd7809e0ce438d5bb1c2')

prepare() {
  cd libgd-${pkgver}
  # CVE-2014-2497
  patch -p1 -i ../color_c_null_pointer.patch
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'  
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd libgd-${pkgver}
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-rpath \
    --with-vpx=/usr \
    --with-tiff=/usr
  make
}

package() {
  cd libgd-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  rm -rf "$pkgdir/usr/include"
  # Config script pointing libdir to /usr/lib32 (lib32-libgphoto2 needs it)
  mv "$pkgdir/usr/bin/gdlib-config"{,-32}
  find "$pkgdir/usr/bin" -type f ! -name "gdlib-config-32" -exec rm {} \;
}
