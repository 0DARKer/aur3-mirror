# Maintainer: Adam Nielsen <malvineous@shikadi.net>

pkgname=mingw32-png++-svn
_pkgname=png++
pkgver=20110310
pkgrel=2
license=('custom')
pkgdesc="a C++ wrapper for libpng (mingw32)"
arch=('any')
url="http://savannah.nongnu.org/projects/pngpp/"
provides=("mingw32-${_pkgname}")
conflicts=("mingw32-${_pkgname}")
depends=('mingw32-runtime' 'mingw32-libpng')
makedepends=('mingw32-gcc' 'svn')
options=(!strip !buildflags)
source=()
md5sums=()

_svntrunk='svn://svn.sv.nongnu.org/pngpp/trunk/'

build() {
	cd "${srcdir}"
	msg "Downloading from SVN..."

	if [ -d "trunk" ] ; then
		cd "trunk" && svn update
		msg "The local files are updated."
	else
		svn checkout "$_svntrunk"
		cd "trunk"
	fi

	unset LDFLAGS

	make docs
	make
}

package() {
	cd "$srcdir/trunk"
	make install PREFIX="${pkgdir}/usr/i486-mingw32"
	install -Dm644 "$srcdir/trunk/COPYING" "${pkgdir}/usr/i486-mingw32/share/licenses/$_pkgname/COPYING"
}

