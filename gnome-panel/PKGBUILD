# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=gnome-panel
pkgver=3.8.1
pkgrel=4
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="Legacy GNOME panel"
url="https://wiki.gnome.org/Projects/GnomeFlashback"
depends=('dconf' 'evolution-data-server' 'gconf' 'gnome-desktop' 'gnome-menus' 'librsvg'
         'libwnck3' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'networkmanager')
optdepends=('gnome-applets: extra applets for the GNOME Panel'
            'gnome-flashback-git: required for GNOME Flashback session'
            'gnome-screensaver: required for GNOME Flashback session'
            'gnome-session: required for GNOME Flashback session'
            'gnome-settings-daemon-compat: required for GNOME Flashback session'
            'gnome-themes-standard: required for GNOME Flashback session'
            'metacity: required for GNOME Flashback session'
            'nautilus: required for GNOME Flashback session'
            'notification-daemon: required for GNOME Flashback session'
            'polkit-gnome: required for GNOME Flashback session')
install=$pkgname.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz
        session-modification.patch
        60_disable-cursor-plugin.gschema.override
        add-compiz-session.patch)
sha256sums=('2c84d8541787d39514bd6d9605b2bd5ecea38a5534cb874ce1ff54bd59ecb97f'
            '7b3889cd597c2187ac66bd045096c8dadbda4ecedc1be53f4ebef09a8eb42199'
            '4187138b5416ef04ae7239749505fe77f0abc1b34dfe42b7fe51002c573adc05'
            '38227c2c1f3a8919d36456b514668dfe3993e675fcde7cf6200c9c07714c0659')

prepare() {
  cd "$pkgname-$pkgver"

  # Add more required components to the session
  patch -Np1 -i "$srcdir/session-modification.patch"

  # Ensure that the gnome-flashback-compiz.session file gets installed
  patch -Np1 -i "$srcdir/add-compiz-session.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile \
              PYTHON=/usr/bin/python2 
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m 644 $srcdir/60_disable-cursor-plugin.gschema.override $pkgdir/usr/share/glib-2.0/schemas
}

