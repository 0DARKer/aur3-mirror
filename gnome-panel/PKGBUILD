# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=gnome-panel
pkgver=3.8.0
pkgrel=10
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="Legacy GNOME panel"
url="https://wiki.gnome.org/Projects/GnomeFlashback"
depends=('dconf' 'evolution-data-server' 'gconf' 'gnome-desktop' 'gnome-menus' 'librsvg'
         'libwnck3' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'networkmanager')
optdepends=('gnome-screensaver: required for GNOME Flashback session'
            'gnome-session: required for GNOME Flashback session'
            'gnome-settings-daemon-compat: required for GNOME Flashback session'
            'gnome-themes-standard: required for GNOME Flashback session'
            'metacity: required for GNOME Flashback session'
            'nautilus: required for GNOME Flashback session'
            'notification-daemon: required for GNOME Flashback session'
            'polkit-gnome: required for GNOME Flashback session')
conflicts=('gnome-panel-git')
install=$pkgname.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz
        libgweather-3.10.patch
        session-modification.patch
        fix-gweather-crash.patch
        git_fix_panel_size.patch
        git_fix_startupnotify_crash.patch)
sha256sums=('d1cda458a38cb4f7d2a50c61b1a72f4bf4c39cab65ae0e6cfdc4e0c122252e7a'
            'd9e21449fc6c5f235673fe47e5b817ac7597490518a557908b14ca875263d90e'
            '4a5196cbd65339c47d3dc0bd7e4b28be77cc6683a3bd44a47e97a5ca6543256d'
            '62224bf8d25aedcc1f2be1a27893e7a9b4bfee4d4991f98d0c3466af7816313d'
            '7bda3805e675d99d535f77106f7758a92e6ce581bae917c91fdc39c507c76d83'
            '2c0377be55c5b1960051405c1918508b069345dcf6b092da23a5cdf50d169432')

prepare() {
  cd "$pkgname-$pkgver"

  # Fix build with libgweather 3.10
  patch -Np1 -i "$srcdir/libgweather-3.10.patch"

  # Change session name to 'GNOME Flashback (Metacity)' and add extra required components
  patch -Np1 -i "$srcdir/session-modification.patch"

  # Fix clock settings crash when adding a location
  patch -Np1 -i "$srcdir/fix-gweather-crash.patch"

  # Fix panel resize problems when changing screen resolution
  patch -Np1 -i "$srcdir/git_fix_panel_size.patch"

  # Fix crash due to incorrect session registering
  patch -Np1 -i "$srcdir/git_fix_startupnotify_crash.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile \
              PYTHON=/usr/bin/python2 
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
