# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=gnome-panel
pkgver=3.8.1
pkgrel=8
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="Legacy GNOME panel"
url="https://wiki.gnome.org/Projects/GnomeFlashback"
depends=('dconf' 'evolution-data-server' 'gconf' 'gnome-desktop' 'gnome-menus' 'librsvg'
         'libwnck3' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'networkmanager')
optdepends=('gnome-applets: extra applets for the GNOME Panel')
install=$pkgname.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz
        sync-calls.patch
        do-not-create-session-files.patch
        0001-libpanel-applet-unown-bus-name-and-unregister-dbus-o.patch
        0001-Revert-calendar-window-Port-size-request-signal.patch
        0002-clock-port-size-request-signal.patch)
sha256sums=('2c84d8541787d39514bd6d9605b2bd5ecea38a5534cb874ce1ff54bd59ecb97f'
            '13768d9d6b74d98c7421c69633327a3a8281047552c277583de3e7db1a0164fa'
            '042caa802dd5f4f09a067637b3cb16ce70591cab5966f5e72741b7d763cc1d18'
            '70d35b961f74f258197c7c2d9c857bb7559d7bd2002b4145e69ed0580f1d9af8'
            '0b2dad9d5d376a39e315687a1663b114399909ff811858d6b3e6eeeb7e7a31be'
            '6750237de8fabf633749ce6f0e397be2b5e88f3d45dbf1342c8522a903864a32')


prepare() {
  cd "$pkgname-$pkgver"

  # Don't make sync calls for Logout and Shutdown
  patch -Np1 -i "$srcdir/sync-calls.patch"

  # Ensure that session files are not generated as these are now provided by the flashback module
  patch -Np1 -i "$srcdir/do-not-create-session-files.patch"

  # Stop crash when adding inprocess applet to panel, then removing and then re-adding the applet
  patch -Np1 -i "$srcdir/0001-libpanel-applet-unown-bus-name-and-unregister-dbus-o.patch"

  # Clock applet fixes
  patch -Np1 -i "$srcdir/0001-Revert-calendar-window-Port-size-request-signal.patch"
  patch -Np1 -i "$srcdir/0002-clock-port-size-request-signal.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile \
              PYTHON=/usr/bin/python2 
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

