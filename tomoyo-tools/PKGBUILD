# Maintainer: mainframe alex.mainframe@gmail.com
pkgname=tomoyo-tools
pkgver=2.3.0
_timestamp=20100820
pkgrel=1
pkgdesc="TOMOYO 2.3.x MAC userland tools (for kernel 2.6.36 or later)"
arch=('i686' 'x86_64')
depends=('ncurses')
makedepends=('help2man')
url="http://tomoyo.sourceforge.jp"
license=('GPL')
install=$pkgname.install
conflicts=('kernel26<=2.6.35' 'ccs-tools' 'tomoyo-tools<2.3.0')
replaces=('tomoyo-tools-2.2')
options=(!distcc)
source=("http://sourceforge.jp/frs/redir.php?f=/tomoyo/48663/$pkgname-$pkgver-$_timestamp.tar.gz"
        $pkgname.rc $pkgname.install)
md5sums=('bbbb4d2d351dad4bbe67f6270c3587db'
         'd27bec060bd273dbc9ebbb6e22d3349a'
         '3851d516f720b4b3bce2ce755e9ba945')

build()
{
  cd "$srcdir/$pkgname"

  make || return 1
}

package()
{
  cd "$srcdir/$pkgname"

  make INSTALLDIR="$pkgdir/" install
  install -D -m755 "$srcdir/$pkgname.rc" "$pkgdir/etc/rc.d/tomoyo"
  install -D -m644 "COPYING.tomoyo" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
# vim:set ts=2 sw=2 et:
