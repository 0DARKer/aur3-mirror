# Maintainer : Tom Englund <tomenglund26@gmail.com>
# Original arch package by:
# Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-mainline
pkgver=319.32
pkgrel=1
pkgdesc="NVIDIA drivers for linux-mainline"
_extramodules=extramodules-3.10-mainline
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-mainline' "nvidia-libgl" "nvidia-utils=${pkgver}")
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
    'pastie-7942599.diff')
    md5sums=('978339ff37c3207f3a39d3952721dcdb'
             'ea39c99175cc4fa551a8474b966dfd89')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
    'pastie-7942599.diff')
    md5sums=('d181d856768868823df3bda4607a5315'
             'ea39c99175cc4fa551a8474b966dfd89')
fi

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cp pastie-7942599.diff "${_pkg}"
    cd "${_pkg}"
    patch -p1 < pastie-7942599.diff
    cd "kernel/"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-mainline.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
