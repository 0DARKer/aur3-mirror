# AUR workaround for split packages...
pkgname="quodlibet-hg"

true && pkgbase="quodlibet-hg"
true && pkgname=("quodlibet-hg" "quodlibet-plugins-hg")
pkgver=6043.bc6d4fd97db5
pkgrel=1
arch=('any')
license=('GPL2')
url="http://code.google.com/p/quodlibet/"
makedepends=('mercurial' 'python2' 'intltool')
options=('!makeflags')
source=(hg+https://code.google.com/p/quodlibet/)
sha1sums=(SKIP)

pkgver() {
  cd quodlibet
  echo $(hg identify -n).$(hg identify -i)
}

prepare() {
  cd quodlibet
  sed -i 's/env python/env python2/' quodlibet/exfalso.py quodlibet/quodlibet.py \
    plugins/songsmenu/replaygain.py 
  sed -i 's|/usr/bin/python|/usr/bin/python2|' quodlibet/operon.py 
}

build() {
  cd quodlibet/quodlibet
  python2 setup.py build
}

package_quodlibet-plugins-hg() {
  pkgdesc="Various plugins for QuodLibet"
  depends=('quodlibet-hg')
  optdepends=('cddb-py: for "CDDB Lookup" plugin'
              'python2-musicbrainz2: for "MusicBrainz Lookup" plugin'
              'python2-pyinotify: for "Automatic library update" plugin'
	      'kakasi: for "Kana/Kanji Simple Inverter" plugin'
	      'zeitgeist: for "Event Logging" plugin'
              'gst-plugins-bad: for "Audio Pitch/Speed" plugin')
  conflicts=('quodlibet-plugins' 'quodlibet-plugins-svn')
  provides=('quodlibet-plugins')

  cd quodlibet/plugins
  install -d "${pkgdir}/usr/lib/python2.7/site-packages/quodlibet/plugins"
  cp -r editing events gstreamer playorder songsmenu "${pkgdir}/usr/lib/python2.7/site-packages/quodlibet/plugins"
}

package_quodlibet-hg() {
  pkgdesc="An audio library tagger, manager and player"
  depends=('gtk3' 'python2-gobject' 'python2-dbus' 'mutagen' 
           'gst-plugins-base' 'gst-plugins-good' 'gst-plugins-ugly' 
	   'gnome-icon-theme')
  optdepends=('gst-libav: for ffmpeg (ASF/WMA) support'
              'gst-plugins-bad: for Musepack support'
              'libgpod: for ipod support'
              'python2-feedparser: for audio feeds (podcast) support'
              'libkeybinder3: for the multimedia keys support'
              'media-player-info: for media devices support')
  conflicts=('quodlibet' 'quodlibet-svn' 'quodlibet-no-exfalso')
  provides=('quodlibet')
  install=quodlibet.install

  cd quodlibet/quodlibet
  python2 setup.py install --prefix="${pkgdir}/usr"
}

# AUR workaround for split packages...
pkgdesc="An audio library tagger, manager and player. Split package including quodlibet-plugins-hg."
