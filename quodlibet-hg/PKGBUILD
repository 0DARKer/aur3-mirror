# Maintainer: Anton Shestakov <engored*ya.ru>

pkgname=quodlibet-hg
pkgver=4714
pkgrel=1
pkgdesc='Audio library tagger, manager, and player for GTK+. Mercurial version.'
arch=('i686' 'x86_64')
url='http://code.google.com/p/quodlibet/'
license=('GPL2')
depends=('gstreamer0.10-python>=0.10.13-2' 'gstreamer0.10-base-plugins' 
         'gstreamer0.10-good-plugins' 'gstreamer0.10-ugly-plugins' 
         'mutagen' 'pygtk>=2.13.0-2')
makedepends=('mercurial' 'intltool')
optdepends=('dbus-python: audio devices support.'
            'python-gpod: iPod support.'
            'python-feedparser: audio feeds (podcasts) support.')
            
conflicts=('quodlibet' 'quodlibet-svn' 'quodlibet-no-exfalso')
provides=('quodlibet')
install=quodlibet.install

_hgroot='https://quodlibet.googlecode.com/hg'
_hgrepo='quodlibet'

build() {
  msg 'Connecting to Mercurial server....'

  if [ -d "$_hgrepo" ] ; then
    cd "$_hgrepo"
    hg pull -u
    msg 'The local files are updated.'
  else
    hg clone "$_hgroot" "$_hgrepo"
  fi

  msg 'Mercurial checkout done or server timeout'
  msg 'Starting make...'
  
  cd $srcdir
  
  rm -rf "$_hgrepo-build"
  hg clone "$_hgrepo" "$_hgrepo-build"
  cd "$_hgrepo-build/quodlibet"

  sed -i -e 's/env python/env python2/' *.py

  ./setup.py build
}  

package() {
  cd "$_hgrepo-build/quodlibet"

  ./setup.py install --prefix="$pkgdir/usr"

  # waiting for upstream to install icons system-wide
  install -D -m644 "quodlibet/images/hicolor/64x64/apps/quodlibet.png" "$pkgdir/usr/share/pixmaps/quodlibet.png"
  install -D -m644 "quodlibet/images/hicolor/64x64/apps/exfalso.png" "$pkgdir/usr/share/pixmaps/exfalso.png"
  install -D -m644 "quodlibet/images/hicolor/scalable/apps/quodlibet.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/quodlibet.svg"
  install -D -m644 "quodlibet/images/hicolor/scalable/apps/exfalso.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/exfalso.svg"
}
