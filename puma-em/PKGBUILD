# Maintainer: gborzi <gborzi@ieee.org>
pkgname=puma-em
_pkgname=Puma-EM
pkgver=0.6.2
pkgrel=1
pkgdesc="Computational electromagnetics software, MoM + MLFMM"
arch=('i686' 'x86_64')
url="http://puma-em.sourceforge.net/index.html"
license=('GPL')
depends=('python-mpi4py' 'python-matplotlib' 'python-scipy' 'python-numpy' 'blitz' 'lapack' 'gmsh')
makedepends=('gcc-fortran')
source=(http://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.gz $pkgname $pkgname.1 lapackgmsh.diff)
md5sums=('edbd134fd7eec0ca54efc469a9d1cf77'
         '71e98b45b3fc6c60c120b18b3ccebaf5'
         'ccf1903cb722b50da737aae3d7b7db06'
         'f5063afc2c4543185dd2b1419128d595')

prepare() {
	cd "$srcdir/$_pkgname"
      patch -Np1 -i "$srcdir/lapackgmsh.diff"
}

build() {
	cd "$srcdir/$_pkgname"
	make libs
}

check() {
	cd "$srcdir/$_pkgname"
	./run.sh
}

package() {
	cd "$srcdir/$_pkgname"

      install -dm755 "$pkgdir/usr/lib/puma-em/MoM"
      install -dm755 "$pkgdir/usr/share/puma-em"
      install -Dm755 "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
      install -Dm644 "$srcdir/$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"
      install -Dm644 guide.pdf "$pkgdir/usr/share/doc/$pkgname/guide.pdf"
      rm -rf run_in_out/{__pycache__,result}
      cp -r config_mpi.sh my_hostfile run_in_out "$pkgdir/usr/share/puma-em/"
      cp code/*.py "$pkgdir/usr/lib/puma-em/"
      cp code/MoM/{mesh_cubes,RWGs_renumbering,mpi_mlfma,compute_Z_near,mesh_functions_seb,distribute_Z_cubes,compute_SAI_precond,communicateZnearBlocks} "$pkgdir/usr/lib/puma-em/MoM"
}
