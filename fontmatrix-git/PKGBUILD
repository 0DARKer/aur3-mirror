# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Chebotarev Sergey <sachebotarev@gmail.com>
# Contributor: Aurélien Desbrières <ice.cube@gmx.com>
# Contributor: Stefan Husmann <Stefan-Husmann@t-online.de>

pkgname=fontmatrix-git
pkgver=0.6.0.r1171.220d519
pkgrel=1
pkgdesc='Font manager for Linux'
arch=('i686' 'x86_64')
url='http://oep-h.com/fontmatrix/'
license=('GPL')
depends=('qtwebkit')
conflicts=('fontmatrix')
makedepends=('cmake' 'git' 'mesa')
install="${pkgname%-*}.install"
source=('git+https://github.com/fontmatrix/fontmatrix.git')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-*}

  printf "0.6.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd ${pkgname%-*}

  [[ $CARCH == 'i686' ]] && _bits='32'
  [[ $CARCH == 'x86_64' ]] && _bits='64'
  export QTDIR=/usr
  export QMAKESPEC=/usr/share/qt/mkspecs/linux-g++-${_bits}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname%-*}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
