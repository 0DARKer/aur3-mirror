# Maintainer: sekret, mail=$(echo c2VrcmV0QHBvc3Rlby5zZQo= | base64 -d)
_pkgname=drumgizmo
pkgname=$_pkgname-git
pkgver=0.9.5.56.g68e1d8b
pkgrel=1
pkgdesc="an open source cross-platform drum plugin and stand-alone application (lv2)"
arch=('i686' 'x86_64')
url="http://www.drumgizmo.org"
license=('GPL')
groups=('lv2-plugins')
depends=('libsmf' 'jack' 'libao' 'qt4')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+http://git.drumgizmo.org/drumgizmo.git"
        'hugin::git+http://git.oftal.dk/hugin.git')
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "$_pkgname"
  git describe | sed -e 's/v//' -e 's/-/\./g'
}

prepare() {
  cd "$_pkgname"
  # hackish, but I don't want to clone hugin with every build
  rmdir hugin
  ln -sf "$srcdir/hugin" hugin
  ./autogen.sh # should this go to build? I think it's ok here
}

build() {
  cd "$_pkgname"
  ./configure --prefix=/usr \
              --enable-lv2 \
              --enable-cli \
              --enable-editor \
              --enable-static=no \
              --with-resample
  make
}

check() {
  cd "$_pkgname"
  make check
}

package() {
  cd "$_pkgname"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
