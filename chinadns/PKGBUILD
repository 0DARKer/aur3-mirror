# $Id$
# Maintainer: dfeng

pkgname=chinadns
pkgver=0.2.0
pkgrel=1
pkgdesc="A DNS forwarder that ignores incorrect(you know it) responses."
license=("MIT")
url="http://pypi.python.org/pypi/chinadns"
depends=('shadowsocks')
source=("http://pypi.python.org/packages/source/c/chinadns/chinadns-${pkgver}.tar.gz"
        "$pkgname-LICENSE::https://raw.github.com/clowwindy/ChinaDNS/master/LICENSE"
        "chinadns.service")
arch=('any')
install=${pkgname}.install

prepare() {
  # python2 fix
  cd "$srcdir/chinadns-$pkgver/chinadns"
  sed -e 's_#!/usr/bin/env python$_#!/usr/bin/env python2_' \
      -e 's_#!/usr/bin/python$_#!/usr/bin/python2_' \
      -i dnsrelay.py __init__.py
  # shadowsocks version fix
  cd "$srcdir/chinadns-$pkgver"
  sed -e 's_shadowsocks==2.0.11_shadowsocks>=2.0.11_' \
      -i setup.py
  cd "$srcdir/chinadns-$pkgver/chinadns.egg-info"
  sed -e 's_shadowsocks==2.0.11_shadowsocks>=2.0.11_' \
      -i requires.txt
}

package() {
  cd "$srcdir/chinadns-$pkgver"
  python2 setup.py install -O1 --root="$pkgdir"
  
  install -Dm644 "$srcdir/chinadns.service" "$pkgdir/usr/lib/systemd/system/chinadns.service"
  install -Dm644 "$srcdir/$pkgname-LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha512sums=('52f6a592d62e2c49fc8ffc39212313a0613fcab7a99ba4b77e6a2c093b2751b49f72fe55b341f1a89cc9876d06954041b4524c482ed750c29db1fe631309ac27'
            '88abf42d51d7b7baa2b9cef016bfd194373fd178980c2c6f1bbb3ce31d3267dfca71efb013c6596c57c0390fdb95d716ff4acca97bdf960e56e9edf16f770352'
            '8f33466f0a0434839645cb56a4fd59b6f625fac4ef85f70681d100d27ebc88b2815ee13ba7e776558d18ffcf70f304964aea4980aa54308a498da641cfb042ad')
