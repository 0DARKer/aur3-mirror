# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=pms-youtube-git
pkgver=git
pkgrel=1
pkgdesc="Terminal based YouTube jukebox with playlist management"
arch=('any')
url='https://github.com/np1/pms-youtube'
license=('GPL3')
depends=('mplayer' 'python' 'python-pafy-git')
optdepends=('mpv: Alternative to mplayer for playback')
install=pms-youtube-git.install
source=('git+https://github.com/np1/pms-youtube.git' 'pms-youtube-git.install')
md5sums=('SKIP'
         '5df02855c45f5f0696e6d18e258c12a9')

pkgver() {
	cd pms-youtube
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd pms-youtube
	python setup.py install --root="$pkgdir" --optimize=1
}

# vim: set ts=2 sw=2 noet:
