# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=lib32-apulse
pkgver=0.1.5
pkgrel=1
pkgdesc="PulseAudio emulation for ALSA, multilib."
arch=('x86_64')
url="https://github.com/i-rinat/apulse"
license=('custom:MIT')
depends=('lib32-glib2' 'lib32-alsa-lib')
makedepends=('cmake' 'gcc-multilib')
install="$pkgname.install"
source=("$url/archive/v$pkgver.tar.gz"
        "asoundrc.sample")
md5sums=('48cd971011f2c9d864cf3dad24803c32'
         '9d5955f853690cc0685cc3a0364982e2')

prepare() {
  cd apulse-$pkgver
  [ -d b ] || mkdir b

  # install in /usr/lib32
  sed -i '/APULSEPATH/s/lib/&32/' CMakeLists.txt
}

build() {
  cd apulse-$pkgver/b

  export CFLAGS=-m32
  export CXXFLAGS=-m32

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd apulse-$pkgver/b
  make DESTDIR="$pkgdir/" install

  # do not conflict with apulse
  mv "$pkgdir/usr/bin/apulse" \
     "$pkgdir/usr/bin/apulse32"

  # sample asoundrc
  install -Dm644 ../../${source[1]} \
    "$pkgdir/usr/share/$pkgname/${source[1]}"

  # license
  install -Dm644 ../LICENSE.MIT \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
