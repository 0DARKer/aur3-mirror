# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libatasmart
pkgname=lib32-$_pkgbase
pkgver=0.18
pkgrel=1
pkgdesc="ATA S.M.A.R.T. Reading and Parsing Library (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-udev>=174' "$_pkgbase")
url="http://0pointer.de/blog/projects/being-smart.html"
options=('!libtool')
source=("http://0pointer.de/public/${_pkgbase}-${pkgver}.tar.gz")
md5sums=('dc22b7acda1c2230f55ae98737e8b159')

build() {
  export CC='gcc -m32'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --libdir=/usr/lib32
    
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,sbin,share}
}
