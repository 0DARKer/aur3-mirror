# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=pumpa
pkgver=0.6
pkgrel=1
pkgdesc="A simple pump.io client written in C++ and Qt"
arch=('i686' 'x86_64')
url="http://gitorious.org/pumpa"
license=('GPL3')
depends=('qt5-base' 'aspell')
install="$pkgname.install"
source=("$url/pumpa/archive-tarball/v$pkgver")
md5sums=('457d2956a0ae3b4f36aa80f892725ce8')

prepare() {
  cd "$srcdir/$pkgname-$pkgname"

  # missing QInputDialog include
  # and use qt5 QUrlQuery class
  sed -e '22i#include <QInputDialog>' \
      -e 's/reply->url()/QUrlQuery(&)/' \
      -i src/pumpapp.cpp

  # desktop file paths fix
  sed -e '/Exec/s/\./\/usr\/bin/' \
      -e '/Icon/s/\.\/pumpa\/images/\/usr\/share\/pixmpas/' \
      -i $pkgname.desktop
}

build() {
  cd "$srcdir/$pkgname-$pkgname"
  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgname"

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 images/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
