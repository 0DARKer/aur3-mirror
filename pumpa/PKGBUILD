# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=pumpa
pkgver=0.8
pkgrel=1
pkgdesc="A simple pump.io client written in C++ and Qt"
arch=('i686' 'x86_64')
url="http://gitorious.org/pumpa"
license=('GPL3')
depends=('qt5-base' 'aspell')
install="$pkgname.install"
source=("$url/pumpa/archive/v$pkgver.tar.gz")
md5sums=('177917ea49384b6c15d8275b8e3d177c')

prepare() {
  cd $pkgname-$pkgname

  # icon path fix
  sed -i 's|/.*/||' $pkgname.desktop

  # missing include
  sed -i '23i#include <QMenu>' src/messageedit.cpp
}

build() {
  cd $pkgname-$pkgname
  qmake
  make
}

package() {
  cd $pkgname-$pkgname

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 images/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
