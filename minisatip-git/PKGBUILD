# Maintainer: Tomasz Maciej Nowak <com[dot]gmail[at]tmn505>
pkgname='minisatip-git'
pkgdesc="SAT>IP server for linux using local DVB-S2, DVB-S or DVB-T cards"
pkgver=r118.06cf5c6
pkgrel=1
arch=('x86_64' 'i686' 'arm' 'armv6h' 'armv7h')
url="https://github.com/catalinii/minisatip"
license=('GPL2')
provides=('minisatip')
makedepends=('git' 'libdvbcsa')
depends=('libdvbcsa')
optdepends=('oscam: channels descrambling')
backup=('etc/conf.d/minisatip.conf')
install='minisatip.install'
source=("git+https://github.com/catalinii/minisatip"
        'minisatip.service'
        'minisatip.conf')
sha256sums=('SKIP'
            '96d1cbd5d7bc0ffaf91666391aff1c84d80a5a903df7b3edcff930eb0559f79b'
            'c47790245befba5f0c1424e48854a2c06f62edc819c797ff14a15e80128568bc')

pkgver() {
    cd ${srcdir}/minisatip
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    return 0
}

build() {
    cd ${srcdir}/minisatip
    make
}

package() {
    cd ${srcdir}/minisatip
    install -Dm644 ../minisatip.service ${pkgdir}/usr/lib/systemd/system/minisatip.service
    install -Dm644 ../minisatip.conf ${pkgdir}/etc/conf.d/minisatip.conf
    install -Dm755 ./minisatip ${pkgdir}/usr/bin/minisatip
}
