# Maintainer: Lukas Braun <koomi+aur at hackerspace-bamberg dot de>
pkgname=s6-networking-musl
pkgver=2.0.0.1
pkgrel=1
pkgdesc="A suite of small networking utilities for Unix systems.."
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/s6-networking"
license=('custom:ISC')
makedepends=(musl 'skalibs>=2.1.0.0' 's6-dns>=2.0.0.1')
depends=('execline>=2.0.1.0')
provides=('s6-networking=2.0.0.1')
options=('staticlibs')
changelog=CHANGELOG
source=(${url}/s6-networking-$pkgver.tar.gz)
sha256sums=('13b07ccf4b8376762320802de0200f413c3b95516ae0e254d0673e25ab7b9aa1')

build() {
  cd "$srcdir/s6-networking-$pkgver"

  unset CFLAGS
  unset LDFLAGS
  export CPPFLAGS='-nostdinc -isystem /usr/lib/musl/include -isystem /usr/include'
  export CC='musl-gcc'
  ./configure --enable-static-libc --bindir=/usr/bin
  make
}

package() {
  cd "$srcdir/s6-networking-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
