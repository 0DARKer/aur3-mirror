# Maintainer: Lukas Braun <koomi+aur at hackerspace-bamberg dot de>
pkgname=s6-networking-musl
pkgver=2.0.1.0
pkgrel=1
pkgdesc="A suite of small networking utilities for Unix systems.."
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/s6-networking"
license=('custom:ISC')
makedepends=(musl 'skalibs-musl>=2.2.0.0' 's6-dns-musl>=2.0.0.2')
depends=('execline-musl>=2.0.1.1' 's6-musl>=2.0.1.0')
provides=('s6-networking=2.0.1.0')
options=('staticlibs')
changelog=CHANGELOG
source=(${url}/s6-networking-$pkgver.tar.gz)
sha256sums=('4582575cb95bbacd61db4b53df6eae7db31665391c27205bdb9ab9853ba83dd8')

build() {
  cd "$srcdir/s6-networking-$pkgver"

  unset CFLAGS
  unset LDFLAGS
  export CPPFLAGS='-nostdinc -isystem /usr/lib/musl/include -isystem /usr/include'
  export CC='musl-gcc'
  ./configure --enable-static-libc --bindir=/usr/bin
  make
}

package() {
  cd "$srcdir/s6-networking-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
