# Maintainer: Lukas Braun <koomi+aur at hackerspace-bamberg dot de>
pkgname=s6-networking-musl
pkgver=2.0.0.0
pkgrel=1
pkgdesc="A suite of small networking utilities for Unix systems.."
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/s6-networking"
license=('custom:ISC')
makedepends=(musl 'skalibs>=2.0.0.0' 's6-dns>=2.0.0.0')
depends=('execline>=2.0.0.0')
provides=('s6-networking=2.0.0.0')
options=('staticlibs')
changelog=CHANGELOG
source=(${url}/s6-networking-$pkgver.tar.gz)
sha256sums=('67c803e1476d97035eabd7915648db493e7bc38d8df708d27f016e4dcb03514c')

build() {
  cd "$srcdir/s6-networking-$pkgver"

  unset CFLAGS
  unset LDFLAGS
  export CPPFLAGS='-nostdinc -isystem /usr/lib/musl/include -isystem /usr/include'
  export CC='musl-gcc'
  ./configure --enable-static-libc --bindir=/usr/bin
  make
}

package() {
  cd "$srcdir/s6-networking-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
