# Maintainer: Gregor Robinson <gregor@fiatflux.co.uk>
# Contributor: Stas <stas@mainframe.lv>

pkgname=opensc-git
_gitname="OpenSC"
pkgver=0.13.0.68.g962cba9
pkgrel=1
pkgdesc="Access smart cards that support cryptographic operations"
arch=('i686' 'x86_64')
url="http://www.opensc-project.org/"
license=('LGPL2.1')
makedepends=('nasm' 'git')
depends=('zlib' 'openssl' 'pcsclite')
conflicts=('opensc')
provides=('opensc')
source=('git+https://github.com/OpenSC/OpenSC.git')
md5sums=('SKIP')


pkgver() {
    cd $_gitname
    git describe --always | sed 's|-|.|g'
}

build() {
    cd $_gitname
    ./bootstrap || return 1
    ./configure --prefix=/usr --sysconfdir=/etc --enable-pcsc --enable-sm || return 1
    make || return 1
}

package() {
    cd $_gitname
    make DESTDIR=${pkgdir} install || return 1
    mkdir -p ${pkgdir}/usr/share/licenses/opensc || return 1
    install -Dm644 COPYING* "${pkgdir}/usr/share/licenses/opensc/"
}
