# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=synthclone
pkgver=0.2.0
pkgrel=1
pkgdesc='A Qt-based application that can "clone" your MIDI-capable instruments.'
arch=('i686' 'x86_64')
url="http://code.google.com/p/synthclone/"
license=('GPL' 'LGPL')
depends=('qt' 'jack' 'libarchive' 'portaudio' 'portmidi')
makedepends=('python2' 'doxygen')
install="$pkgname.install"
source=("http://synthclone.googlecode.com/files/$pkgname-$pkgver.tar.gz")
md5sums=('8eef321bf0635565bc5120e9053c48de')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # use python2 for build-* scripts
  sed -i "/build-/s/.\//python2 /" $pkgname.pro

  python2 configure --prefix=/usr \
                    --skip-api-docs=1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install INSTALL_ROOT="$pkgdir/"
}
