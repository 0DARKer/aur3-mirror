# Maintainer: Zom <zom[at]eevul[dot]org>
# Contributor: L42y <423300@gmail.com>
# Contributor: Gianfranco 'Gianfrix' Micoli <gianfrix.mg@gmail.com>
# Contributor: Chen Xiao-Long <chillermillerlong@hotmail.com>

pkgname=libdbusmenu-gtk2
_relname=libdbusmenu
pkgver=0.5.0
pkgrel=1
pkgdesc="GTK2-libs to go with libdbusmenu"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('GPL')
depends=('glib2' 'dbus-glib' 'libxml2' 'json-glib-git' 'gtk2' 'libdbusmenu='${pkgver})
makedepends=('intltool' 'gnome-doc-utils' 'vala')
source=("http://launchpad.net/dbusmenu/0.5/${pkgver}/+download/${_relname}-${pkgver}.tar.gz")
sha512sums=('eaecae500728a61c2f3fe6a2f432710e817b4b7a822ffe4fbba6d73f28b2747455e5017474dd01822858718c5a0215dc5481f33911ed00080b26585dd4a6b959')

build() {
  cd "${srcdir}/${_relname}-${pkgver}"
  ./configure DBUSMENUGLIB_LIBS=/usr/lib/libdbusmenu-glib.la --prefix=/usr\
  --with-gtk=2\
  --disable-introspection\
  --disable-vala\
  --disable-tests\
  --disable-dumper
  cd libdbusmenu-gtk
  sed 's/..\/libdbusmenu\-glib\/libdbusmenu\-glib\.la/\/usr\/lib\/libdbusmenu\-glib\.la/' Makefile > newmakefile
  mv newmakefile Makefile
  cd ..
  make
}

package() {
  cd "${srcdir}/${_relname}-${pkgver}"
  install -d ${pkgdir}/usr/lib
  install -d ${pkgdir}/usr/lib/pkgconfig
  install -d ${pkgdir}/usr/include/libdbusmenu-0.4/libdbusmenu-gtk/

  install -m644 libdbusmenu-gtk/.libs/libdbusmenu-gtk.a ${pkgdir}/usr/lib/
  install -m644 libdbusmenu-gtk/.libs/libdbusmenu-gtk.la ${pkgdir}/usr/lib/
  install -m644 libdbusmenu-gtk/.libs/libdbusmenu-gtk.so.4.0.5 ${pkgdir}/usr/lib/

  install -m644 libdbusmenu-gtk/dbusmenu-gtk-0.4.pc ${pkgdir}/usr/lib/pkgconfig/

  install -m644 libdbusmenu-gtk/client.h ${pkgdir}/usr/include/libdbusmenu-0.4/libdbusmenu-gtk/
  install -m644 libdbusmenu-gtk/dbusmenu-gtk.h ${pkgdir}/usr/include/libdbusmenu-0.4/libdbusmenu-gtk/
  install -m644 libdbusmenu-gtk/menu.h ${pkgdir}/usr/include/libdbusmenu-0.4/libdbusmenu-gtk/
  install -m644 libdbusmenu-gtk/menuitem.h ${pkgdir}/usr/include/libdbusmenu-0.4/libdbusmenu-gtk/
  install -m644 libdbusmenu-gtk/parser.h ${pkgdir}/usr/include/libdbusmenu-0.4/libdbusmenu-gtk/

  cp -d libdbusmenu-gtk/.libs/libdbusmenu-gtk.so ${pkgdir}/usr/lib/
  cp -d libdbusmenu-gtk/.libs/libdbusmenu-gtk.so.4 ${pkgdir}/usr/lib/
}
