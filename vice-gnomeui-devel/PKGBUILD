# Maintainer: Kevin MacMartin <prurigro at gmail dot com>
# Contributor: Sebastian Wolf <fatmike303 at gmail dot com>
# Contributor: grimi <grimi at poczta dot fm>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Markus M. May <mmay@javafreedom.org>

_pkgname=vice
pkgname=${_pkgname}-gnomeui-devel
pkgver=2.4.9
pkgrel=1
pkgdesc="Versatile Commodore Emulator (development release with the Gnome UI)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://vice-emu.sourceforge.net"
depends=('libpulse' 'giflib' 'libxxf86vm' 'vte' 'gtkglext' 'libpcap')
conflicts=('vice' 'vice-ethernet' 'vice-gnome' 'vice-gnomeui' 'vice-gtkglext' 'vice-sdl' 'vice-svn' 'vice-2.2-gnomeui')
replaces=('vice')
options=('!makeflags')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/project/vice-emu/development-releases/${_pkgname}-${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}-x11video.patch"
        "${_pkgname}-${pkgver}-no-fc-cache-no-lib64.patch"
        "${_pkgname}-${pkgver}-zlib-1.2.7.patch"
        "${_pkgname}-${pkgver}-notexi-notxt.patch"
        "${_pkgname}-x64.png"
        "${_pkgname}-x64.desktop")
sha512sums=('6f213aa7e322c2b0fd798b11225b2a79c5c6144bc4819b28d0b3574154339539c3edc325cf768c8dc5d7170681d05773e280e1056bd4ab2ffc7a16c260ca8bdd'
            '0637646e8db4bdc5b160e503463c0f4dd03e34885cbe15418eba2d29de3ce7a1f358747f3e33e079fc730221f293a4e17fd1039710181943b1dcbe665c73f1c0'
            '8e6481db699848ac18995e2db32973c5fb6e1cd3c0467ac299fdc29ba7a2fa674b967744827a9f5c1df7d635e84e955a311855519f69c9e56e824b8066a4c651'
            '8910acd79d01fbde669123176d27e6d0313cc06c7755ca5cfe99a8e2156ee560102d1889e8e601ffd032523552f0633c6e010e2543c69e9a8142740da63c7f14'
            '6a410b338667821913adb6b75db0fdef93fb4792423f709d69308a60f29d0ea714f51461cd1b7c743f7abc8b2ad332e5799e6e171d063e868e326d9faa493cec'
            '1433ed9e88f5eab34e53f89201df62c0c3a6aa4b61e6855823bb1ff833886a3058bdfeb9ea79c0f8658c2ec744314638524db6e0194783b4bf04d86824f19cdf'
            'e0643f4de3d184a96a9b31dc354b4be0b7e3a9e63401e0998361f61ddfa2f49de0fbe89f75ee1b3f4414d41d5018f486d84c521a162a64c2d8a3053595ca155d')

prepare() {
    cd ${_pkgname}-${pkgver}
    patch -Np1 -i ../${_pkgname}-${pkgver}-x11video.patch
    patch -Np1 -i ../${_pkgname}-${pkgver}-no-fc-cache-no-lib64.patch
    patch -Np1 -i ../${_pkgname}-${pkgver}-zlib-1.2.7.patch
    patch -Np1 -i ../${_pkgname}-${pkgver}-notexi-notxt.patch
    autoreconf -vi
}

build() {
    cd ${_pkgname}-${pkgver}
    ./configure --prefix=/usr --enable-fullscreen --enable-gnomeui --enable-ethernet --disable-ffmpeg
    make
}

package() {
    install -Dm644 ${_pkgname}-x64.desktop "${pkgdir}/usr/share/applications/${_pkgname}-x64.desktop"
    install -Dm644 ${_pkgname}-x64.png "${pkgdir}/usr/share/pixmaps/${_pkgname}-x64.png"
    cd ${_pkgname}-${pkgver}
    make DESTDIR="$pkgdir" realdocdir=/usr/share/doc/vice install
    ln -s /usr/share/doc/vice "${pkgdir}/usr/lib/vice/doc"
    install -d "${pkgdir}/usr/share/fonts/TTF"
    ln -s /usr/lib/vice/fonts/CBM.ttf "${pkgdir}/usr/share/fonts/TTF/CBM.ttf"
}
