# Maintainer: Kevin MacMartin <prurigro at gmail dot com>
# Contributor: Sebastian Wolf <fatmike303 at gmail dot com>
# Contributor: grimi <grimi at poczta dot fm>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Markus M. May <mmay@javafreedom.org>

_pkgname=vice
pkgname=${_pkgname}-gnomeui-devel
pkgver=2.4.15
pkgrel=1
pkgdesc="Versatile Commodore Emulator (development release with the Gnome UI)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://vice-emu.sourceforge.net"
depends=('libpulse' 'giflib' 'vte' 'gtkglext' 'libpcap')
makedepends=('dos2unix' 'xorg-bdftopcf' 'xorg-mkfontdir')
options=('!makeflags')
install=${pkgname}.install
conflicts=('vice' 'vice-ethernet' 'vice-gnome' 'vice-gnomeui' 'vice-gtkglext' 'vice-sdl' 'vice-svn' 'vice-2.2-gnomeui')

source=(
    "http://downloads.sourceforge.net/project/vice-emu/development-releases/${_pkgname}-${pkgver}.tar.gz"
    "${_pkgname}.png"
    "${_pkgname}.desktop"
    'x11video.patch'
    'no-fc-cache-no-lib64.patch'
    'zlib-1.2.7.patch'
    'notexi-notxt.patch')
sha512sums=(
    '2cd1f734af2e243e4548d49ab35fed1b75889c927e6bb375bd1dd8ace5e72759bb9f1ed57b02d20c874bbfd68d5ed021b0724d86e8d3a64a7fbb6f15993384c7'
    '1433ed9e88f5eab34e53f89201df62c0c3a6aa4b61e6855823bb1ff833886a3058bdfeb9ea79c0f8658c2ec744314638524db6e0194783b4bf04d86824f19cdf'
    'dc96b8658fac1a6f605b8f0052c11a5abb653da4b9deb3401d8b8177b14a664c0b3a5ed9e7c5c3013b0bc18b831045244f2f9187de9ff8b25b90f0b1cfa0cd8a'
    '0637646e8db4bdc5b160e503463c0f4dd03e34885cbe15418eba2d29de3ce7a1f358747f3e33e079fc730221f293a4e17fd1039710181943b1dcbe665c73f1c0'
    '96149fb6cd44b0c143b8417350c60654cbfdad23f4bf9d6e953ee8da2685f24993d8410e88d005af2f11e308279a8e3436eb9657de0f472a0b0c7f2ece565009'
    'd86ad77b758dbd0846a9307fd0f71dc2f3dd2573ba32c0a6b550a49616927421ff929b220a81457f013107212df84ade6e60e8612769c0dc9101c12b799e1d38'
    '041357a920942199afdb977ef18c4d5fe386bddb953a0cd0bfb49454f22e88b5cedf58e5f39e787044a542f456d6fa18fad04ec35074e19843eef90f8e2e3322')

prepare() {
    # Apply patches
    cd $_pkgname-$pkgver
    patch -Np1 -i ../x11video.patch
    patch -Np1 -i ../no-fc-cache-no-lib64.patch
    patch -Np1 -i ../zlib-1.2.7.patch
    patch -Np1 -i ../notexi-notxt.patch

    # Convert MS-DOS linebreaks to Unix style ones
    echo -n 'Converting MS-DOS format linebreaks to Unix format... '
    for file in $(find . -type f | egrep '\.([ch]|in)'); do
        dos2unix -q "$file"
    done
    echo 'DONE!'
}

build() {
    # Configure and build the project
    cd $_pkgname-$pkgver
    ./configure --prefix=/usr \
                --enable-fullscreen \
                --enable-gnomeui \
                --enable-ethernet \
                --disable-ffmpeg
    make
}

package() {
    # Install the XDG desktop file and icon
    install -Dm644 ${_pkgname}.desktop "$pkgdir/usr/share/applications/${_pkgname}.desktop"
    install -Dm644 ${_pkgname}.png "$pkgdir/usr/share/pixmaps/${_pkgname}.png"

    # Install Vice
    cd $_pkgname-$pkgver
    make DESTDIR="$pkgdir" realdocdir=/usr/share/doc/$_pkgname install

    # Link the docs folder to where Vice is expecting it to be
    ln -s /usr/share/doc/$_pkgname "$pkgdir"/usr/lib/$_pkgname/doc

    # Link the ttf font included with Vice to the system TTF fonts folder
    install -d "$pkgdir"/usr/share/fonts/TTF
    ln -s /usr/lib/$_pkgname/fonts/CBM.ttf "$pkgdir"/usr/share/fonts/TTF/CBM.ttf
}
