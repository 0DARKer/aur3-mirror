# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=libxnvctrl
pkgver=295.33
pkgrel=1
pkgdesc="NVIDIA Ctrl X library"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('GPL2')
makedepends=('libx11' 'libxext' 'xextproto')
source=(ftp://download.nvidia.com/XFree86/nvidia-settings/nvidia-settings-$pkgver.tar.bz2)
sha256sums=('37128d2a90b22600c2df582c78dcc9d7c3cd1e26b2db8ab81a50a3fc3acfcdba')

build() {
  cd "nvidia-settings-$pkgver/src/libXNVCtrl"

  export CFLAGS="$CFLAGS -fPIC"

  make clean
  make
}

package() {
  cd "nvidia-settings-$pkgver/src/libXNVCtrl"

  # install libXNVCtrl and headers
  install -d -m 755 "$pkgdir"/usr/{include/NVCtrl,lib}
  install -m 644 nv_control.h NVCtrl{,Lib}.h "$pkgdir"/usr/include/NVCtrl/
  install -m 644 libXNVCtrl.a "$pkgdir"/usr/lib/libXNVCtrl.a
}

# vim:set ts=2 sw=2 et:
