# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=libxnvctrl
pkgver=290.10
pkgrel=1
pkgdesc="NVIDIA Ctrl X library"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('GPL2')
makedepends=('libx11' 'libxext' 'xextproto')
source=(ftp://download.nvidia.com/XFree86/nvidia-settings/nvidia-settings-290.10.tar.bz2)
md5sums=('34bfcb2944558d4fbe09f8a634fce4c1')

build() {
  cd "nvidia-settings-${pkgver}/src/libXNVCtrl"

  export CFLAGS="${CFLAGS} -fPIC"

  make clean
  make
}

package() {
  cd "nvidia-settings-${pkgver}/src/libXNVCtrl"

  # install libXNVCtrl and headers
  install -d -m755 "${pkgdir}"/usr/{include/NVCtrl,lib}
  install -m644 nv_control.h NVCtrl{,Lib}.h "${pkgdir}/usr/include/NVCtrl/"
  install -m644 libXNVCtrl.a "${pkgdir}/usr/lib/libXNVCtrl.a"
}

# vim:set ts=2 sw=2 et:
