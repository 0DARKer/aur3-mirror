# Contributor: twa022 <twa022@gmail.com>

pkgname=xine-ui-cvs
pkgver=20090828
pkgrel=1
pkgdesc="A free video player for Unix"
url="http://xine-project.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('xine-lib' 'curl>=7.16.2' 'libxtst' 'libxinerama' 'libxv' 'libpng' 'libxft' 'libsm' 'libxxf86vm' 'ncurses>=5.6-7')
makedepends=('lirc' 'libxt' 'cvs')
provides=('xine-ui')
conflicts=('xine-ui')
source=()
md5sums=()

_cvsroot=":pserver:anonymous@xine.cvs.sourceforge.net:/cvsroot/xine"
_cvsmod="xine-ui"

build() {
  cd ${srcdir}

  msg "Connecting to $_cvsmod.sourceforge.net CVS server...."
  if [ -d $_cvsmod/CVS ]; then
    cd $_cvsmod
    cvs -z3 update -d
  else
    cvs -z3 -d $_cvsroot co -P $_cvsmod
    cd $_cvsmod
  fi
  ./autogen.sh

  msg "CVS checkout done or server timeout"
  msg "Starting make..."

  cp -r ../$_cvsmod ../$_cvsmod-build
  cd ../$_cvsmod-build

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
