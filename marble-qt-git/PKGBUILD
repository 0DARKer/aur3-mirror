# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=marble-qt-git
pkgver=v15.03.97.81.g4e0489b
pkgrel=1
pkgdesc="Desktop Globe (Qt version). (GIT version)"
arch=('i686' 'x86_64')
url="https://www.kde.org/applications/system/marble"
license=('GPL')
conflicts=('marble' 'marble-qt' 'kdeedu-marble')
provides=('marble' 'marble-qt')
depends=('qt5-script' 'qt5-svg' 'qt5-webkit' 'opencv'  'hicolor-icon-theme')
makedepends=('cmake' 'git' 'extra-cmake-modules' 'gpsd' 'quazip-qt5-svn' 'shapelib' 'qextserialport' 'libwlocate' 'phonon-qt5')
optdepends=('gpsd: position information via gpsd'
            'quazip-qt5-svn: reading and displaying .kmz files'
            'shapelib: reading and displaying .shp files'
            'qextserialport: eading from serial port in APRS plugin'
            'libwlocate: Position information based on neighboring WLAN networks'
            'phonon-qt5: That enables the use of audio and video content')
source=('git://anongit.kde.org/marble.git')
sha1sums=('SKIP')
install=marble-qt-git.install

pkgver() {
  cd marble
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  mkdir -p build
  sed 's|set(QT_PLUGINS_DIR ${CMAKE_INSTALL_PREFIX}/lib${LIB_SUFFIX}/plugins)|set(QT_PLUGINS_DIR ${CMAKE_INSTALL_PREFIX}/lib${LIB_SUFFIX}/qt/plugins)|g' -i marble/CMakeLists.txt

  sed 's|quazip/|quazip5/|g' -i marble/src/plugins/runner/kml/KmzHandler.cpp

  cd marble
  for i in $(grep -R '} marblewidget )' | cut -d ':' -f1); do sed 's|} marblewidget )|} ${MARBLEWIDGET} )|g' -i ${i}; done
  sed 's|marblewidget|${MARBLEWIDGET}|g' -i examples/cpp/marble-game/CMakeLists.txt
}

build() {
  cd build
  cmake ../marble \
    -DQT5BUILD=1 \
    -DQTONLY=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_MARBLE_EXAMPLES=ON \
    -DBUILD_MARBLE_TOOLS=ON \
    -DBUILD_MARBLE_TESTS=OFF \
    -DBUILD_TESTING=OFF \
    -DMOBILE=OFF \
    -DQEXTSERIALPORT_INCLUDE_DIR=/usr/include/qt/QtExtSerialPort \
    -DQEXTSERIALPORT_LIBRARIES=/usr/lib/libQt5ExtSerialPort.so \
    -DPHONON_INCLUDE_DIR=/usr/include/phonon4qt5 \
    -DPHONON_LIBRARY=/usr/lib/libphonon4qt5.so

make
}

package() {
  make -C build DESTDIR="${pkgdir}" install

  rm -fr "${pkgdir}/usr/bin/"marble-{mobile,touch}
}
