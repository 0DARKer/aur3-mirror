# Contributor: Andreas Wagner <andreas.wagner@lowfatcomputing.org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: kerick <kerick@shiftedbit.net>
# Contributor: stefan minus husmann at t minus online dot de
# Contributor: John Gerritse <reaphsharc@gmail.com>
pkgname=autodia
pkgver=2.14
pkgrel=2
pkgdesc="An application that automatically generates Images, XML, HTML or Dia files"
arch=('any')
url="http://search.cpan.org/dist/Autodia/"
license=('GPL' 'PerlArtistic')
depends=('perl-xml-simple' 'perl-template-toolkit')
optdepends=('perl-inline-java: for Java support')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TE/TEEJAY/Autodia-${pkgver}.tar.gz")
sha512sums=('41cd0d9dd8fc66d70aaf987a8a62ed8b916e9a38263c61cc1cb7994e7bfb24843bbd3440bbdad054fccbf0fbbdaa11ec3a35db2a6f926bc87ba65a5f4ac160ee')

build() {
  cd "${srcdir}/Autodia-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/Autodia-${pkgver}"
  make DESTDIR=${pkgdir} install
}
