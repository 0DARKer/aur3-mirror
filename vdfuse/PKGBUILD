# Maintainer: TDY <tdy@gmx.com>

pkgname=vdfuse
pkgver=80
pkgrel=2
pkgdesc="A FUSE module for mounting VirtualBox disk images (VDI/VMDK/VHD) on the host"
arch=('i686' 'x86_64')
url="http://forums.virtualbox.org/viewtopic.php?f=26&t=33355"
license=('GPL3')
depends=('fuse' 'virtualbox')
makedepends=('pkgconfig' 'subversion')
optdepends=('python: for vdautomount')
install=$pkgname.install
source=($pkgname-$pkgver.c::"http://forums.virtualbox.org/download/file.php?id=2866"
        $pkgname-$pkgver.build::"http://forums.virtualbox.org/download/file.php?id=2864"
        vdautomount-0.1.py::"http://forums.virtualbox.org/download/file.php?id=2865")
md5sums=('e8874e911257f74d637dad5e75303e04'
         '870931125ac6cbd99b6ca1625c0faa08'
         'f7044bcbc724983eb7d8ff60bff4d6e9')

build() {
  cd "$srcdir"
  svn co -r 30000 http://www.virtualbox.org/svn/vbox/trunk/include/
  . /etc/vbox/vbox.cfg
  sh $pkgname-$pkgver.build ./include $pkgname-$pkgver.c
}

package() {
  cd "$srcdir"
  install -Dm755 $pkgname-$pkgver "$pkgdir/usr/bin/$pkgname"
  sed -i '1s,python,&2,' vdautomount-0.1.py
  install -Dm755 vdautomount-0.1.py "$pkgdir/usr/bin/vdautomount"
}
