# Maintainer: TDY <tdy@gmx.com>

pkgname=vdfuse
pkgver=82a
pkgrel=3
_debian_vbox_ver="4.1.18"
pkgdesc="A FUSE module for mounting VirtualBox disk images (VDI/VMDK/VHD) on the host"
arch=('i686' 'x86_64')
url="http://forums.virtualbox.org/viewtopic.php?f=26&t=33355"
license=('GPL3')
depends=('fuse' 'virtualbox')
makedepends=('pkgconfig')
install=$pkgname.install
source=("http://ftp.de.debian.org/debian/pool/main/v/virtualbox/virtualbox_${_debian_vbox_ver}-dfsg-1.debian.tar.gz"
	"http://download.virtualbox.org/virtualbox/${_debian_vbox_ver}/VirtualBox-${_debian_vbox_ver}.tar.bz2"
	vdautomount-0.1.py::"http://forums.virtualbox.org/download/file.php?id=2865")
md5sums=('e0f794a55812467c87b42b12fc7e3a56'
	 '38db0a87cba659b484af868b0c2bd3ac'
	 'f7044bcbc724983eb7d8ff60bff4d6e9')

build() {
  cd "$srcdir/debian/$pkgname"
  gcc $pkgname.c -o $pkgname \
	`pkg-config --cflags --libs fuse` \
	-I$srcdir/VirtualBox-${_debian_vbox_ver}/include \
	-Wl,-rpath,/usr/lib/virtualbox/ \
	-l:/usr/lib/virtualbox/VBoxDD.so \
	-l:/usr/lib/virtualbox/VBoxDD2.so \
	-l:/usr/lib/virtualbox/VBoxDDU.so \
	-Wall $CFLAGS
}

package() {
  cd "$srcdir/debian/$pkgname"
  install -D -m755 $pkgname "$pkgdir/usr/bin/$pkgname"
  sed -i '1s,python,&2,' "$srcdir/vdautomount-0.1.py"
  install -D -m755 "$srcdir/vdautomount-0.1.py" "$pkgdir/usr/bin/vdautomount"
}
