# Maintainer: Oskar Duwe <oskar.einstein@gmail.com>
# Based on PKGBUILD made by Jan de Groot

pkgname=nautilus-up
pkgver=3.4.1
pkgrel=2
pkgdesc="Nautilus with patch to add up button"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libexif' 'gnome-desktop' 'exempi' 'gvfs' 'desktop-file-utils' 'gnome-icon-theme' 'dconf' 'libtracker-sparql' 'libnotify')
makedepends=('intltool' 'gobject-introspection')
url="http://ubuntuforums.org/showthread.php?t=1904510"
groups=('gnome')
options=('!libtool' '!emptydirs')
provides=('nautilus')
conflicts=('nautilus')
install=nautilus.install
source=("http://ftp.gnome.org/pub/gnome/sources/nautilus/${pkgver%.*}/nautilus-${pkgver}.tar.xz" "nautilus-up_button-patch.patch" "samba-crash.patch")
sha256sums=('02581c9475e9c4250960555b248378a6a1c63a5c029728ec48347c78ca77392d' 
            'ed2a3eff837d9b65bf6d14155d332e3260ba735a5c19d2b6117f10a1a7166e39'
            'e437ced7bbebe49d2454b4daecd10cff39d1c427e9631fe0fe5325072ed6aefd')

build() {
  cd "${srcdir}/nautilus-${pkgver}"
  patch -Np1 -i "$srcdir/samba-crash.patch"
  patch -Np1 -i "$srcdir/nautilus-up_button-patch.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nautilus \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/nautilus-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
