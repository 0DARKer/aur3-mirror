# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Gilles Gagniard <gilles@gagniard.org>
# Maintainer: JD Steffen <jd at steffennet dot org>

pkgname=simutrans
pkgver=102.2.2
pkgrel=3
pkgdesc="An open source transportation simulation game"
arch=('i686' 'x86_64')
url="http://www.simutrans.com/"
license=('PerlArtistic' 'custom')
depends=('gcc-libs' 'sdl' 'zlib' 'simutrans-pak64')
makedepends=('unzip')
conflicts=('simutrans-svn')
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/102-2-2/$pkgname-src-102-2-2.zip
	http://downloads.sourceforge.net/project/$pkgname/$pkgname/102-2-2/update_texts_23052010.zip
	http://downloads.sourceforge.net/project/$pkgname/$pkgname/102-2-2/simulinux-102-2-2.zip
        $pkgname.desktop
        $pkgname.png
        $pkgname.patch)
md5sums=('f9a01921ee4dda78bd4db0bf146d90a8'
         '783e83465fc8ebfb0a924aa04478b69d'
         '2f36e7f16bbd957e9dd3eea01f025eea'
         'd0a8807370b25d833f42b3151b9119b6'
         '7b69608dd812e8bed284b10984c6170e'
         '4f2dd8e1ddb6dafebc376c8c01f63678')

build() {
  cd "$srcdir"

  # make
  msg Compiling...
  cp -f config.{template,default}
  patch -Np0 -i ../$pkgname.patch || return 1
  make || return 1

  # install data
  install -m755 -d "$pkgdir"/usr/share/$pkgname
  cp -rf $pkgname/* "$pkgdir"/usr/share/$pkgname/
  find "$pkgdir"/usr/share/$pkgname -type d -exec chmod 755 {} +
  find "$pkgdir"/usr/share/$pkgname -type f -exec chmod 644 {} +

  # copy needed file from downloaded binary package
  install -m755 -d "$pkgdir"/usr/share/$pkgname/skin
  install -Dm644 "$srcdir"/$pkgname/skin/menu.WindowSkin.pak "$pkgdir"/usr/share/$pkgname/skin/menu.WindowSkin.pak

  cd $pkgdir/usr/share/$pkgname/text/
  bsdtar -xf $srcdir/update_texts_23052010.zip

  cd "$srcdir"

  # install binary
  install -Dm755 sim "$pkgdir"/usr/bin/$pkgname

  # install licenses, .desktop file and icon
  install -Dm644 $pkgname/copyright.txt "$pkgdir"/usr/share/licenses/$pkgname/COPYRIGHT
  install -Dm644 $pkgname/licence.txt   "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 $pkgname.desktop       "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -Dm644 $pkgname.png           "$pkgdir"/usr/share/pixmaps/$pkgname.png

  msg "Finishing up..."
  # create link to font
  cd "$pkgdir"/usr/share/$pkgname/font
  ln -s Prop-Latin1.bdf PROP_FONT_FILE
  
  msg Done.
}
