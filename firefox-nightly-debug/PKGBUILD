# Maintainer: oke3 <Sekereg [at] gmx [dot] com>
#
# Based off of firefox-nightly PKGBUILD

pkgname=firefox-nightly-debug
pkgdesc='Standalone web browser from mozilla.org, nightly debug build'
url='http://www.mozilla.org/projects/firefox'
pkgver=13.0a1
pkgrel=1
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
install=$pkgname.install
source=('firefox-nightly-debug.desktop' 'firefox-nightly-safe-debug.desktop')
sha1sums=('15b6c19c89898dbf74e6b558a48753c0d727ab7c' 'aa2bd97ff8325a4b8a5265dc2f2f9243a203ecec')
depends=('alsa-lib' 'libxt' 'libnotify' 'mime-types' 'nss' 'gtk2' 'sqlite3' 'dbus-glib' 'desktop-file-utils')

package() {
  FX_SRC="firefox-${pkgver}.en-US.debug-linux-${CARCH}.tar.bz2"
  FX_SRC_URI="http://ftp.mozilla.org/pub/mozilla.org/firefox/nightly/$(date +%Y-%m-%d)-mozilla-central-debug/${FX_SRC}"

  msg "Downloading..."
  wget -N ${FX_SRC_URI}
  msg "Extracting..."
  bsdtar -x -f ${FX_SRC}
  msg "Packaging..."

#   uncomment this line to remove these
#   rm -rf firefox/{extensions,plugins,searchplugins}

  mkdir -p "${pkgdir}"/{usr/{bin,share/{applications,pixmaps}},opt}
  cp -r firefox "${pkgdir}/opt/firefox-${pkgver}-debug"

  ln -s /opt/firefox-${pkgver}-debug/firefox "${pkgdir}/usr/bin/firefox-nightly-debug"
  install -m644 "${srcdir}"/{firefox-nightly-debug.desktop,firefox-nightly-safe-debug.desktop} "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/firefox/icons/mozicon128.png" "${pkgdir}/usr/share/pixmaps/${pkgname}-icon.png"
}
