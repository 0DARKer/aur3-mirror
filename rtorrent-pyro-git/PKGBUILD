# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

# This is tha v0.9.2 tag if you wish to use whats in
# the official tarball instead of git head
#_gitrev='cd029a67c5c35fb31d8f82f75fadfd751bd4f210'

# Compile in a debug build?
_debug='n'

pkgname=rtorrent-pyro-git
_pkgname=rtorrent
pkgver=20121024
pkgrel=1
pkgdesc="rTorrent-git with Pyroscope patches"
url="https://code.google.com/p/pyroscope"
license=('GPL')
arch=('i686' 'x86_64')
depends=('libtorrent-pyro-git' 'ncurses' 'curl' 'xmlrpc-c')
makedepends=('git' 'cppunit')
options=('!strip')
conflicts=('rtorrent' 'rtorrent-git')
provides=('rtorrent')
install='pyroscope.install'

_url="https://pyroscope.googlecode.com/svn/trunk/pyrocore/docs/rtorrent-extended/patches"
_gitname="${_pkgname}"
_gitroot="git://github.com/rakshasa/${_pkgname}.git"
[[ ! $_gitrev ]] && _gitopts='--depth 1'
[[ $_debug = 'n' ]] && _debug='--disable-debug'

source=("${_url}/ps-ui_pyroscope_0.8.8.patch"
		"${_url}/pyroscope.patch"
		"${_url}/ui_pyroscope.patch"
		"${_url}/command_pyroscope.cc"
		"${_url}/ui_pyroscope.cc"
		"${_url}/ui_pyroscope.h")

md5sums=('7a88f8ab5d41242fdf1428de0e2ca182'
         'bd04a0699b80c8042e1cf63a7e0e4222'
         '0a2bbaf74c7160ba33876dcc2f050f14'
         'ca7feaa0f6fad892d10734bb5b11167b'
         '94f236bdf80c51f7bad7f6cd07cef58e'
         '1258acfc82c50a8f452ace87fef0b416')

build() {
    cd "$srcdir"

    msg "Connecting to GIT server..."
    if [ -d "${_gitname}/.git" ]; then
        cd "$_gitname" && git pull origin
        msg "The local repository was updated."
    else
        git clone $_gitopts $_gitroot $_gitname
        cd "$_gitname"
        [[ $_gitrev ]] && git checkout $_gitrev
        msg "The remote repository was cloned."
    fi

    msg "Applying patches..."

    # Used by the the pyroscope patch to determine version we are patching to
    sed -i configure.ac \
        -e "s:\\(AC_DEFINE(HAVE_CONFIG_H.*\\):\1\\nAC_DEFINE(RT_HEX_VERSION, 0x000902, for CPP if checks):"
    sed -i src/ui/download_list.cc \
        -e 's:rTorrent \" VERSION:rTorrent-pyro " VERSION:'

    for i in ${srcdir}/*.patch; do
        patch -uNp1 -i "$i"
    done
    for i in ${srcdir}/*.{cc,h}; do
        ln -s $i src
    done

    #temporary fix
    sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/g' configure.ac

    msg "Starting build.."
    export CXXFLAGS+=" -fno-strict-aliasing -std=c++11"

    ./autogen.sh
    ./configure $_debug \
        --prefix=/usr \
        --disable-dependency-tracking \
        --with-xmlrpc-c

    make
}

package() {
    cd "$srcdir/$_pkgname"
    make DESTDIR="$pkgdir" install

    install -Dm644 doc/faq.xml        "${pkgdir}/usr/share/doc/rtorrent/faq.xml"
    install -Dm644 doc/rtorrent.rc    "${pkgdir}/usr/share/doc/rtorrent/rtorrent.rc"
    install -Dm644 doc/old/rtorrent.1 "${pkgdir}/usr/share/man/man1/rtorrent.1"
}
