# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

# Compile a debug build?
_debug=n

# Compile from a specific commit?
#_commit=cd029a6  #0.9.3
#_commit=6a3234e  #0.9.4
_commit=HEAD

pkgname=rtorrent-pyro-git
pkgver=20140520
pkgrel=1
pkgdesc="Ncurses BitTorrent client based on libTorrent - rTorrent-git with Pyroscope patches"
url="https://code.google.com/p/pyroscope"
license=('GPL')
arch=('i686' 'x86_64')
depends=('libtorrent-pyro-git' 'libsigc++' 'ncurses' 'curl' 'xmlrpc-c')
makedepends=('git' 'cppunit')
conflicts=('rtorrent' 'rtorrent-git')
provides=('rtorrent')
install='pyroscope.install'

[[ $_debug = 'n' ]] &&
    _debug='--disable-debug' || options=(!strip)

_url="https://raw.githubusercontent.com/pyroscope/rtorrent-ps/master/patches"
source=("git://github.com/rakshasa/rtorrent.git#commit=$_commit"
        "rtorrent.rc.sample"
        "${_url}/ps-ui_pyroscope_0.8.8.patch"
        "${_url}/pyroscope.patch"
        "${_url}/ui_pyroscope.patch"
        "${_url}/command_pyroscope.cc"
        "${_url}/ui_pyroscope.cc"
        "${_url}/ui_pyroscope.h")

md5sums=('SKIP'
         '96858c7a1caabfc2146449da2451633a'
         '7a88f8ab5d41242fdf1428de0e2ca182'
         'bd04a0699b80c8042e1cf63a7e0e4222'
         '0a2bbaf74c7160ba33876dcc2f050f14'
         'f85ca3ddee824d4b6ee6062fc3ed25b3'
         '951b40c6d43caf0a3bffc5fffcc557d5'
         '1258acfc82c50a8f452ace87fef0b416')

pkgver() {
    cd "$srcdir/rtorrent"
    git log -1 --format="%cd" --date=short "$_commit" |tr -d -
}

prepare() {
    cd "$srcdir/rtorrent"
    #patch -Np1 -i "${startdir}/rtorrent.patch"

    sed -i configure.ac \
        -e "s:\\(AC_DEFINE(HAVE_CONFIG_H.*\\):\1\\nAC_DEFINE(RT_HEX_VERSION, 0x000904, for CPP if checks):"
    sed -i src/ui/download_list.cc \
        -e 's:rTorrent \" VERSION:rTorrent-pyro " VERSION:'

    for i in ${srcdir}/*.patch; do
        patch -uNp1 -i "$i"
    done
    for i in ${srcdir}/*.{cc,h}; do
        ln -s $i src
    done

    ./autogen.sh
}

build() {
    cd "$srcdir/rtorrent"
    export CXXFLAGS+=" -std=c++11"
    export libtorrent_LIBS="-L/usr/lib -ltorrent"
    #export libtorrent_CFLAGS="-I/usr/include/"

    ./configure $_debug \
        --prefix=/usr \
        --disable-dependency-tracking \
        --with-xmlrpc-c

    make
}

package() {
    cd "$srcdir/rtorrent"
    make DESTDIR="$pkgdir" install

    install -Dm644 "$srcdir"/rtorrent.rc.sample "${pkgdir}/usr/share/doc/rtorrent/rtorrent.rc.sample"
    install -Dm644 doc/faq.xml        "${pkgdir}/usr/share/doc/rtorrent/faq.xml"
    install -Dm644 doc/old/rtorrent.1 "${pkgdir}/usr/share/man/man1/rtorrent.1"
}
