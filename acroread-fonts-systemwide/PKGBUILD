# Maintainer: Marcel Korpel <marcel[dot]korpel[at]gmail>
# Contributor: Roger Braun <davinelulinvega@gmail.com>
# Based on work by: Lee.MaRS <leemars@gmail.com>
# Contributor: kevku <kevku@gmx.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=acroread-fonts-systemwide
pkgver=11.0.06
_fpver=11.0.00
_pkgver=${pkgver//./}
_fpverd=${_fpver//./}
_first=${pkgver%%.*}
pkgrel=1
arch=('any')
pkgdesc="Fonts from Adobe Reader ${_first}"
url="http://www.adobe.com/products/acrobat/acrrasianfontpack.html"
source=("http://ardownload.adobe.com/pub/adobe/reader/win/${_first}.x/${_fpver}/misc/FontPack${_fpverd}_XtdAlf_Lang.msi"
        "http://ardownload.adobe.com/pub/adobe/reader/win/${_first}.x/${pkgver}/en_US/AdbeRdr${_pkgver}_en_US.exe" )
makedepends=('p7zip')
noextract=("AdbeRdr${_pkgver}_en_US.exe")
conflicts=('ttf-adobe-fonts'
           'ttf-adobe-fonts-cjkext'
           'acroread-fonts'
           'acroread-font-chinese-simplified')
install=acroread-fonts-systemwide.install
license=('custom')
sha256sums=('aa7af8d43eb3bd305c64667351f4f3cbaa4e259706064da2db0e9221726dca56'
            '7eb5dd2905d5cce9912019efa89e939a187cd099583d0216e517c71bd38dc6a8')

package() {
  cd "$srcdir"
  7z -y e AdbeRdr${_pkgver}_en_US.exe
  7z -o"$pkgdir/usr/share/fonts/OTF" -ir\!*.otf e FontPack${_fpverd}_XtdAlf_Lang.msi
  7z -o"$pkgdir/usr/share/fonts/OTF" -ir\!*.otf e Data1.cab
  7z -o"$pkgdir/usr/share/licenses/$pkgname" -ir\!*.html19 e Data1.cab
  mv "$pkgdir/usr/share/licenses/$pkgname/license.html19" "$pkgdir/usr/share/licenses/$pkgname/license.html"
  chmod -R go+rX "$pkgdir"
}
