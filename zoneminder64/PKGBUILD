# This is a ZoneMinder PKGBUILD file for x86_64!
# Contributor: Vladimir Ermakov <vooon341@gmail.com>
# Contributor: Ross melin <rdmelin@gmail.com>

pkgname=zoneminder64
pkgver=1.24.2
pkgrel=4
pkgdesc="Capture, analyse, record and monitor video security cameras."
arch=('i686' 'x86_64')
url="http://www.zoneminder.com"
license=('GPL')
groups=()
depends=('apache' 'php' 'mysql' 'pcre' 'openssl' 'ffmpeg' 'perl-php-serialization' 'perl-libwww' 'perl-date-manip' 'perl-unicode-map' 'perl-dbi' 'perl-dbd-mysql' 'perl-io-stringy' 'perl-mime-lite' 'perl-timedate' 'perl-x10' 'perl-time-modules' 'perl-net-smtp-ssl' 'perl-sys-mmap' 'sudo' 'libv4l')
makedepends=()
provides=(zoneminder)
conflicts=(zoneminder)
replaces=()
backup=(/etc/zm.conf)
options=()
install=zoneminder.install
source=(http://www2.zoneminder.com/downloads/ZoneMinder-1.24.2.tar.gz \
	http://downloads.sourceforge.net/sourceforge/jscalendar/jscalendar-1.0.zip \
	http://www.charliemouse.com:8080/code/cambozola/cambozola-0.76.tar.gz \
	'http://cvs.pld-linux.org/cgi-bin/cvsweb/packages/zoneminder/zoneminder-jpeg.patch?rev=1.1' \
	zm.rc.d \
	zm.conf.patch \
	zmupdate.patch \
	Controls_Orbit.sql \
	Makefile.am.patch \
	zoneminder-make.patch \
	zm_libv4l.patch \
	zminit.arch \
	customdb \
	httpd-zm.conf \
	zmfilter.pl \
	zmeventbackup)

build() {
	cd "$srcdir/ZoneMinder-$pkgver"

	patch -p1 < $srcdir/zm.conf.patch || return 1
	patch -p1 < $srcdir/zmupdate.patch || return 1
	patch -p1 -b < $srcdir/zm_libv4l.patch || return 1
	patch -p1 < $srcdir/zoneminder-jpeg.patch\?rev=1.1 || return 1
	patch -p1 < $srcdir/Makefile.am.patch || return 1
	patch -p1 < $srcdir/zoneminder-make.patch || return 1
	cat ../Controls_Orbit.sql >> db/zm_create.sql.in
	sed -i '22s/^$/#include <cstdio>/' src/zm_utils.cpp
	sed -i 's/$max_socket_tries = 3;/$max_socket_tries = 15;/' web/ajax/stream.php || return 1

	aclocal
	autoconf
	automake

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-webuser=http \
		--with-webgroup=http \
		--with-mysql=/usr \
		--with-ffmpeg=/usr \
		--with-webdir=/var/lib/zm/www  \
		--with-cgidir=/var/lib/zm/cgi-bin \
		#--with-extralibs="-lv4l1 -lv4l2" \
		--bindir=/usr/lib/zm/bin \
		--enable-mmap=no \
		ZM_SSL_LIB=openssl

	make || return 1
	make DESTDIR="$pkgdir" install || return 1

	install -D -m 700 $srcdir/zminit.arch  $pkgdir/usr/lib/zm/bin/zminit
	install -D -m 700 $srcdir/zm.rc.d  $pkgdir/etc/rc.d/zm
	for f in zmdbbackup zmdbrestore zmeventdump; do
		install -D -m 700 scripts/$f  $pkgdir/usr/lib/zm/bin/$f
	done
	install -D -m 700 scripts/zmlogrotate.conf  $pkgdir/etc/logrotate.d/zm
	install -D -m 700 $srcdir/zmeventbackup  $pkgdir/etc/cron.hourly/zmeventbackup
	install -D -m 755 $srcdir/zmfilter.pl $pkgdir/usr/lib/zm/bin/zmfilter.pl

	install -m 644  $srcdir/cambozola-0.76/dist/cambozola.jar $pkgdir/var/lib/zm/www/cambozola.jar

	mkdir -p  $pkgdir/etc/httpd/conf/extra/
	install -m 644 $srcdir/httpd-zm.conf $pkgdir/etc/httpd/conf/extra/httpd-zm.conf

	mv $srcdir/jscalendar-1.0 $pkgdir/var/lib/zm/www/tools/jscalendar

	install -D -m 700 $srcdir/customdb $pkgdir/usr/lib/zm/upgrade/customdb
	install -D  db/zm*.sql $pkgdir/usr/lib/zm/upgrade/

	mkdir -p $pkgdir/var/run/zm

	# remove special files
	find $pkgdir -name "perllocal.pod" \
		-o -name ".packlist" \
		-o -name "*.bs" \
		-delete

	# hack. makefile's hack give error
	chown http:http $pkgdir/etc/zm.conf
	chmod 600 $pkgdir/etc/zm.conf
}

# vim:set ts=2 sw=2 et:
md5sums=('550d2f8f08852134028c3b1cf8fa437f'
         '10f2160fe68294013efcd1473cd36f72'
         '30c17059bbba8da69bb971b4082eb712'
         '1111f7bc52883094eeca4f71b0b34b32'
         'c7bbac4e0148454ffa858f6d40ca8a79'
         '1d2a4233af027452ae38a3e0b6d062ee'
         '971947cc093f7d909f39c2ce83846f35'
         '5033bc098bf497c8aed1fc1b3c9c5f9c'
         '7c4af40940d0f870e2b82d93fa590480'
         '4f5d96293c93cf6c3b628036e55edf34'
         '60f5654582873da54796a0cf0641dc3c'
         'f9720872736f26d17bc49d8725b75ae4'
         '4ace13d1e20934abe192ef7372c75988'
         '00201eba9c0a5d1ed14b10cc55410698'
         '80c52cc3575073a7462ff6a54886a345'
         '8b40137b6ff54f2fb26104b70edd22e9')
