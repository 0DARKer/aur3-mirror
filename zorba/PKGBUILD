# $Id: pkgbuild-mode.el,v 1.23 2007/10/20 16:02:14 juergen Exp $
# Author: Mathieu Boespflug <mboes@tweag.net> (original 0.9.21)
# Maintainer: Alexey Stukalov <astukalov@gmail.com> (updated to 1.4.2)
pkgname=zorba  
pkgver=2.0.0
pkgrel=1 
pkgdesc="General purpose embeddable XQuery processor written in C++."
url="http://www.zorba-xquery.com/"
arch=('i686' 'x86_64')
license=('APACHE')
depends=('libxml2>=2.2.16' 'libxslt>=1.1.24' 'icu>=3.6' 'boost>=1.47' 'xerces-c>=3.0.0')
optdepends=('curl>=7.21.0: REST and HTTP support')
makedepends=('cmake>=2.6.4' 'flex>=2.5.33' 'bison>=2.4')

source=(http://downloads.sourceforge.net/zorba/$pkgname-$pkgver.tar.gz
        fix_cstddef_include.diff
        disable_doc_xquery.diff)
sha1sums=('a9476894db7ae55573c13d5a06bb07e3dc21d352'
          '7907486098b680faf00fcd2289e03243fd8c59b8'
          'a3b7baa072eaf8414d120a05d67ec25021a6b9d7')

build() {
  cd $srcdir

  msg2 Patching...
  cd $srcdir/${pkgname}-${pkgver}
  patch -p1 -i ../fix_cstddef_include.diff
  patch -p1 -i ../disable_doc_xquery.diff
  cd ..

  mkdir build
  cd build

  msg2 Configuring...
  cmake -Wno-dev \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} -Wno-c++0x-compat" \
        -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
        $srcdir/${pkgname}-${pkgver}

  msg2 Compiling...
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}

