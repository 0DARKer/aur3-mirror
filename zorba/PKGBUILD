# Author: Mathieu Boespflug <mboes@tweag.net> (original 0.9.21)
# Maintainer: Alexey Stukalov <astukalov@gmail.com> (updated to 2.x)
pkgname=zorba  
pkgver=2.2.0
pkgrel=1
pkgdesc="General purpose embeddable XQuery processor written in C++."
url="http://www.zorba-xquery.com/"
arch=('i686' 'x86_64')
license=('APACHE')
depends=('libxml2>=2.7.0'
         'libxslt>=1.1.24'
         'icu>=3.6'
         'boost>=1.47'
         'xerces-c>=3.0.0')
optdepends=('curl>=7.21.0: REST and HTTP support'
            #'swig>=1.3.40: Java/PHP/Python/Ruby bindings'
)
makedepends=('cmake>=2.6.4'
             'flex>=2.5.33'
             'bison>=2.4'
)

source=(http://launchpad.net/$pkgname/trunk/2.2/+download/$pkgname-$pkgver.tar.gz
        process_listener.patch
)
md5sums=('83cf9edc4a2068f198ff227d942f6ed5'
         '70f461d184186dc1a7916b67a9a3ec86')

build() {
  cd $srcdir

  patch -p0 -i process_listener.patch

  mkdir build
  cd build

  msg2 Configuring...
  cmake -Wno-dev \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} -Wno-c++0x-compat" \
        -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
        $srcdir/${pkgname}-${pkgver}

  msg2 Compiling...
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}

