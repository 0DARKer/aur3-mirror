# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: Dan Vratil <progdan@progdansoft.com>

pkgname=vwm
true && pkgname=('vwm' 'vwm-modules-sysmon' 'vwm-modules-vwmterm3')
pkgver=2.1.3
pkgrel=1
pkgdesc="Viper Window Manager"
arch=('i686' 'x86_64')
url="http://vwm.sourceforge.net/"
license=('GPL')
depends=('glibc>=2.8' 'libpseudo>=1.1.0' 'ncurses>=5.4' 'libviper>=1.4.5' 'gpm'
         'libgtop' 'libvterm')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('4e7545cf635247954cc08f4433a2cde8edb595a53dc2e8dae9de72ec3c8ebce4')

build() {
  cd "$srcdir/${pkgname%%-*}"
  sed -i 's/$(DEFS)/& ${includedir}/' modules/sysmon/Makefile
  make prefix=/usr
  make prefix=/usr -C modules/sysmon
  make prefix=/usr -C modules/vwmterm3
}

package_vwm() {
  cd "$srcdir/${pkgname%%-*}"
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.h "$pkgdir/usr/include/$pkgname.h"
}

package_vwm-modules-sysmon() {
  true && depends=('vwm')
  true && pkgdesc="A system monitor module for vwm"

  install -Dm755 "$srcdir/${pkgname%%-*}/modules/sysmon/sysmon.so" \
    "$pkgdir/usr/lib/$pkgname/modules/sysmon.so"
}

package_vwm-modules-vwmterm3() {
  true && depends=('vwm')
  true && pkgdesc="A terminal shell module for vwm"

  install -Dm755 "$srcdir/${pkgname%%-*}/modules/vwmterm3/vwmterm3.so" \
    "$pkgdir/usr/lib/$pkgname/modules/vwmterm3.so"
}

# vim:set ts=2 sw=2 et:
