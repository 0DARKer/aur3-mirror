# Contributors:
#	Florian Pritz <f-p@gmx.at> (Original)
#	Xyne <xyne at archlinux dot us>
#	thiagoc
#	Chris <chris at christopherjones dot us>
#	henning mueller <henning@orgizm.net>

pkgname=fwknop
pkgver=2.0.0rc2
pkgrel=1
pkgdesc='FireWall KNock OPerator: Single Packet Authorization and Port Knocking'
arch=(i686 x86_64)
url=http://www.cipherdyne.org/fwknop/
license=(GPL)
depends=(gpgme iptables libpcap)
options=(!libtool)
source=(
	http://www.cipherdyne.org/fwknop/download/$pkgname-$pkgver.tar.bz2
	fwknopd.sh
)
md5sums=(
	b1aa82e63ae7f6f52b959e54057de9d7
	d5172562c62b0c73a0d41e1916fa88cb
)

build() {
	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
	install -D -m 755 $srcdir/fwknopd.sh $pkgdir/etc/rc.d/fwknopd
}
