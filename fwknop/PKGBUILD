# Contributors:
#   Florian Pritz <f-p@gmx.at> (Original)
#   Xyne <xyne at archlinux dot us>
#   thiagoc
#   Chris <chris at christopherjones dot us>
#   henning mueller <henning@orgizm.net>
#   Fabiano Furtado <fusca14 at gmail dot com>

pkgname=fwknop
pkgver=2.0.3
pkgrel=2
pkgdesc='FireWall KNock OPerator: Single Packet Authorization and Port Knocking'
arch=(i686 x86_64)
url=http://www.cipherdyne.org/fwknop/
license=(GPL)
depends=(gpgme iptables libpcap)
options=(!libtool)
backup=(
  etc/$pkgname/access.conf
  etc/$pkgname/fwknopd.conf
)

source=(
  http://www.cipherdyne.org/fwknop/download/$pkgname-$pkgver.tar.bz2
  fwknopd.service
)
sha256sums=(
  98ddeec9af9277b6a0c8d8e57ea65e19b20c688047e983e7488fd4af2802cd57
  7378e1cd47369a187fa95b32a09da927656704961c7bec4ca1ace33d1ef19842
)

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m 755 $srcdir/fwknopd.service $pkgdir/usr/lib/systemd/system/fwknopd.service
}
