# Maintainer: speps <speps dot archlinux dot org>

_ver=0.8.8-1
pkgname=qxmledit
pkgver=0.8.8.1
pkgrel=1
pkgdesc="Simple Qt XML editor and XSD viewer"
arch=('i686' 'x86_64')
url="http://code.google.com/p/qxmledit/"
license=('GPL3' 'LGPL3')
depends=('qt5-svg' 'qt5-xmlpatterns')
install="$pkgname.install"
source=("http://$pkgname.googlecode.com/files/$pkgname-$_ver-src.tgz")
md5sums=('49d61f1eea5f707b4d902d08206ae62c')

prepare() {
  cd $pkgname-$_ver

  # dot not install to /opt
  sed -e "s|\(INST_DIR = /\).*|\1usr/bin|" \
      -e "s|\(INST_DATA_DIR = /\).*|\1usr/share/$pkgname|" \
      -e "s|\(INST_DOC_DIR = /\).*|\1usr/share/doc/$pkgname|" \
      -e "s|\(INST_INCLUDE_DIR = /\).*|\1usr/include/$pkgname|" \
      -e "s|\(INST_LIB_DIR = /\).*|\1usr/lib|" \
      -i `find . -name "*.pro"`
}

build() {
  cd $pkgname-$_ver
  qmake
  make
}

package() {
  cd $pkgname-$_ver
  make INSTALL_ROOT="$pkgdir/" install

  # desktop
  install -Dm644 install_scripts/environment/desktop/QXmlEdit.desktop \
    "$pkgdir/usr/share/applications/QXmlEdit.desktop"

  # icon
  install -Dm644 install_scripts/environment/icon/qxmledit_48x48.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
