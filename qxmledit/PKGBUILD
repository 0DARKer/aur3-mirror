# Maintainer: speps <speps dot archlinux dot org>

pkgname=qxmledit
pkgver=0.8.3.1
pkgrel=1
pkgdesc="A simple XML editor written in QT4"
arch=('i686' 'x86_64')
url="http://code.google.com/p/qxmledit/"
license=('GPL3' 'LGPL3')
depends=('qt')
install="$pkgname.install"
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver-src.tgz")
md5sums=('b4c2b5badb343dd311ffee93d1d5f893')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # dot not install to /opt
  sed -e "s|\(INST_DIR = /\).*|\1usr/bin|" \
      -e "s|\(INST_DATA_DIR = /\).*|\1usr/share/$pkgname|" \
      -e "s|\(INST_DOC_DIR = /\).*|\1usr/share/doc/$pkgname|" \
      -e "s|\(INST_INCLUDE_DIR = /\).*|\1usr/include/$pkgname|" \
      -e "s|\(INST_LIB_DIR = /\).*|\1usr/lib|" \
      -i `find . -name "*.pro"`

  qmake

  # generate Makefiles
  make src/Makefile.QXmlEdit{Widget,}
  make src/sessions/Makefile.QXmlEditSessions

  # link fix (links against installed libs instead)
  sed -i "s|-L/usr/lib ||" `find . -name "Makefile.*"`

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install

  # desktop
  install -Dm644 install_scripts/environment/desktop/QXmlEdit.desktop \
    "$pkgdir/usr/share/applications/QXmlEdit.desktop"

  # icon
  install -Dm644 install_scripts/environment/icon/qxmledit_48x48.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
