# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=qxmledit
pkgver=0.5.1
pkgrel=1
pkgdesc="A simple XML editor written in QT4"
arch=('i686' 'x86_64')
url="http://code.google.com/p/qxmledit/"
license=('GPL3')
depends=('qt')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver-sources.tgz")
md5sums=('1f32c436776572483e5948f03132c21b')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"
  
  # path fix
  sed -i "s|/opt|/usr/share|g" QXmlEdit.pro

  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"

  make INSTALL_ROOT="$pkgdir/" install

  install -d "$pkgdir/usr/bin"
  mv "$pkgdir/usr/share/$pkgname/QXmlEdit" "$pkgdir/usr/bin/$pkgname"
}
