# Maintainer: Sandy Carter (bwrsandman) <bwrsandman@gmail.com>
# PKGBUILD source: https://github.com/bwrsandman/pkgbuild/tree/master/ogrebullet-git

pkgname=ogrebullet-git
pkgver=r17.10bdcf0
pkgrel=1
pkgdesc="Bullet Physics wrapper for OGRE"
arch=('i686' 'x86_64')
url="http://www.ogre3d.org/tikiwiki/OgreBullet"
license=('lgpl')
depends=(ogre bullet boost)
makedepends=(git cmake)
source=(git+https://bitbucket.org/alexeyknyshev/ogrebullet.git)
sha1sums=('SKIP')
conflicts=('ogrebullet-svn')
provides=('ogrebullet')


pkgver() {
  cd "${srcdir}/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  # Temporary fix, about to be integrated upstream
  sed -i 's,set(${PROJ}_BUILD_DEMOS,option(${PROJ}_BUILD_DEMOS,' ${srcdir}/${pkgname%-git}/CMakeLists.txt
}

build() {
  cd "${srcdir}/${pkgname%-git}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DOgreBullet_BUILD_DEMOS=OFF
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-git}"
  make DESTDIR=${pkgdir} install 
}

# vim:set ts=2 sw=2 et:
