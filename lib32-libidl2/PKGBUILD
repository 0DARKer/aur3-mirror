_pkgbasename=libidl2
pkgname=lib32-$_pkgbasename
pkgver=0.8.14
pkgrel=2
pkgdesc="A front-end for CORBA 2.2 IDL and Netscape's XPIDL (32 bit, library only)"
arch=('x86_64')
url="http://www.gnome.org"
license=('LGPL')
depends=('lib32-glib2>=2.24.0' 'texinfo' $_pkgbasename)
makedepends=('pkgconfig' 'gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/libIDL/0.8/libIDL-${pkgver}.tar.bz2)
sha256sums=('c5d24d8c096546353fbc7cedf208392d5a02afe9d56ebcc1cccb258d7c4d2220')

build() {
  cd "${srcdir}/libIDL-${pkgver}"  
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32'
  make
}

package() {
  cd "${srcdir}/libIDL-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{bin,include,share}
  rm ${pkgdir}/usr/lib32/libIDL-2.a
}
