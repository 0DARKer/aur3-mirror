# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Yonathan Dossow
# Contributor: Ansgar Taflinski <ataflinski@uni-koblenz.de>

pkgname=xf86-video-qxl
pkgver=0.1.2
pkgrel=2
pkgdesc='Xorg X11 qxl video driver'
arch=('x86_64' 'i686')
url='http://www.x.org'
license=('MIT')
groups=('xorg-drivers')
depends=('xf86dgaproto' 'xproto' 'fontsproto' 'randrproto' 'renderproto' 'videoproto' 'resourceproto' 'scrnsaverproto' 'spice')
optdepends=('python2: for Xspice')
makedepends=('pkgconfig' 'xorg-server-devel' 'patch' 'spice-protocol>=0.12.2')
conflicts=('xf86-video-qxl-git')
source=("http://xorg.freedesktop.org/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2"
	'fix-cast.patch')
sha256sums=('43a7d1e7b60ae32941b6a4359e26de01941012d717d7a7747affd379debc5227'
            '575243334f763c21282eccd7bd4eb713b90e96fdd46a882370925fa20d4acb70')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -Np1 < "${srcdir}/fix-cast.patch"
	sed -i '1c #!/usr/bin/python2' scripts/Xspice

	./configure --enable-xspice \
		--prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}/" INSTALL='install -p' install
}

