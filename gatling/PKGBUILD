# Contributors:
#	Alexander Ulrich <a.ulrich@student.uni-tuebingen.de>
#	henning mueller <henning@orgizm.net>

pkgname=gatling
pkgver=0.12
pkgrel=4
pkgdesc='A high performance http, ftp and smb server'
arch=(i686 x86_64)
url=http://www.fefe.de/gatling/
license=(GPL)
depends=(openssl)
makedepends=(libowfat dietlibc)
source=(
	http://dl.fefe.de/$pkgname-$pkgver.tar.bz2
)
md5sums=(
	38ed634965af837142918f631f204e1b
)

build() {
	cd $srcdir/$pkgname-$pkgver

	sed -i '0,/\/man/s//\/share\/man/' GNUmakefile
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver

	make prefix=$pkgdir/usr install

	_docdir=$pkgdir/usr/share/doc/gatling
	mkdir -p $_docdir && cp README* $_docdir
}
