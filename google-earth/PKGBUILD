# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: 458italia <svenskaparadox@gmail.com>
# Contributor: Madek <gonzaloseguel@gmail.com>
# Contributor: Berseker <berseker86@gmail.com>

pkgname=google-earth
pkgver=6.1.0.5001
pkgrel=1
pkgdesc="A 3D interface to view satellite images of Earth and other objects (i686 and x86_64)"
url="http://earth.google.com"
arch=('i686' 'x86_64')
license=('custom')
install=$pkgname.install
depends=('libsm' 'libxrender' 'libgl' 'glib2' 'libxi' 'pcre' 'fontconfig' 'libxdamage' 'ld-lsb' 'shared-mime-info' 'desktop-file-utils')
optdepends=('nss-mdns: In case the application fails to contact the servers'
            'gtk2: SCIM support'
            'nvidia-utils: 3D support for Nvidia cards'
            'catalyst-utils: 3D support for ATI/AMD cards'
            'ttf-bitstream-vera: Fonts')
conflicts=("bin32-$pkgname")
source=(http://dl.google.com/dl/earth/client/current/$pkgname-stable_current_i386.deb
        $url/intl/en/license.html
        $pkgname-mimetypes.xml)
md5sums=('5ea7dfb007ab1c0b70038762f65081ef'
         '7363c6144ebb298b1d7aec713ca8a82a'
         'e3c67b8d05c3de50535bd7e45eee728e')
_arch=i386
_instdir=/opt/google/earth/free
if [[ "$CARCH" == "x86_64" ]]; then
  source[0]="http://dl.google.com/dl/earth/client/current/$pkgname-stable_current_amd64.deb"
  md5sums[0]='9f010b1676c3f2938529ea08227b7694'
  _arch=amd64
fi

build() {
  # Extract the archive
  ar -x $pkgname-stable_current_$_arch.deb
  tar -xvf data.tar.lzma -C "$pkgdir"
}

package() {
  # Install the .desktop file
  mv "$pkgdir/$_instdir/$pkgname.desktop" "$pkgdir/usr/share/applications/"

  # Install the shared MIME info package
  install -D $pkgname-mimetypes.xml "$pkgdir/usr/share/mime/packages/$pkgname-mimetypes.xml"

  # Install the icons
  for i in 16 22 24 32 48 64 128 256; do
      install -D "$pkgdir/$_instdir/product_logo_${i}.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done

  # Install the license
  install -D license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"

#   # Remove useless directories
#   rm "$pkgdir"/usr/share/{gnome-control-center,man}
}