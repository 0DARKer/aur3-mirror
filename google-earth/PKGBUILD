# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributors: 458italia <svenskaparadox@gmail.com>, Madek <gonzaloseguel@gmail.com>, Berseker <berseker86@gmail.com>

pkgname=google-earth
pkgver=6.1.0.5001
pkgrel=3
pkgdesc="A 3D interface to view satellite images of Earth and other objects"
url="http://earth.google.com"
arch=('i686' 'x86_64')
license=('custom')
install=$pkgname.install
depends=('desktop-file-utils' 'lib32-fontconfig' 'ld-lsb' 'lib32-libgl' 'lib32-libsm' 'lib32-libxi' 'lib32-libxrender' 'lib32-pcre' 'shared-mime-info')
optdepends=('lib32-catalyst-utils: 3D support for ATI/AMD cards'
            'lib32-gtk2: SCIM support'
            'lib32-nss-mdns: In case the application fails to contact the servers'
            'lib32-nvidia-utils: 3D support for Nvidia cards'
            'qt: For changing the font size with qtconfig'
            'ttf-ms-fonts: Fonts')
source=($pkgname-stable-$pkgver-i386.deb::http://dl.google.com/dl/earth/client/current/$pkgname-stable_current_i386.deb
        $url/intl/en/license.html
        $pkgname-mimetypes.xml)
md5sums=('9f010b1676c3f2938529ea08227b7694'
         '7363c6144ebb298b1d7aec713ca8a82a'
         'e3c67b8d05c3de50535bd7e45eee728e')
[[ "$CARCH" == "x86_64" ]] && source[0]=$pkgname-stable-$pkgver-amd64.deb::http://dl.google.com/dl/earth/client/current/$pkgname-stable_current_amd64.deb

if [[ "$CARCH" == "i686" ]]; then
depends=('desktop-file-utils' 'fontconfig' 'ld-lsb' 'libgl' 'libsm' 'libxi' 'libxrender' 'pcre' 'shared-mime-info')
optdepends=('catalyst-utils: 3D support for ATI/AMD cards'
            'gtk2: SCIM support'
            'nss-mdns: In case the application fails to contact the servers'
            'nvidia-utils: 3D support for Nvidia cards'
            'qt: For changing the font size with qtconfig'
            'ttf-ms-fonts: Fonts')
md5sums[0]='5ea7dfb007ab1c0b70038762f65081ef'
fi
_instdir=/opt/google/earth/free

build() {
  msg2 "Extracting the archive"
  ar -x $pkgname-stable-$pkgver-*.deb
  tar -xvf data.tar.lzma -C "$pkgdir"
}

package() {
  msg2 "Installing the .desktop file"
  mv "$pkgdir/$_instdir/$pkgname.desktop" "$pkgdir/usr/share/applications/"

  msg2 "Installing the shared MIME info package"
  install -Dm644 $pkgname-mimetypes.xml "$pkgdir/usr/share/mime/packages/$pkgname-mimetypes.xml"

  msg2 "Installing the icons"
  for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 "$pkgdir/$_instdir/product_logo_${i}.png" "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/$pkgname.png"
  done

  msg2 "Installing the license"
  install -Dm644 license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"

  msg2 "Removing the Debian-intended cron job"
  rm -f "$pkgdir/etc/cron.daily/$pkgname"

  msg2 "Removing useless directories"
  rm -r "$pkgdir"/usr/share/{gnome-control-center,man}
}