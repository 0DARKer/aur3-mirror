# Maintainer: Det
# Contributors: 458italia, Madek, Berseker, Syr
# Check the latest version with: $ curl -s https://dl.google.com/dl/earth/client/current/google-earth-stable_current_x86_64.rpm | head -c96 | strings | cut -d "-" -f4

pkgname=google-earth
pkgver=7.1.2.2041
pkgrel=1
pkgdesc="A 3D interface to view satellite images of Earth and other objects"
arch=('i686' 'x86_64')
url="https://www.google.com/earth/index.html"
license=('custom:earth')
depends=('desktop-file-utils' 'fontconfig' 'glu' 'hicolor-icon-theme' 'ld-lsb>=3-5' 'libgl' 'libsm' 'libxrender' 'mesa' 'qt4'
         'shared-mime-info' 'xdg-utils')
optdepends=('ati-dri: For the open source AMD/ATI Radeon driver'
            'catalyst-utils: For AMD Catalyst'
            'gtk2: SCIM support'
            'intel-dri: For the open source Intel driver'
            'nouveau-dri: For the open source Nouveau driver'
            'nss-mdns: In case the application fails to contact the servers'
            'nvidia-utils: For the NVIDIA driver'
            'nvidia-utils-bumblebee: For the NVIDIA driver + Bumblebee setups'
            'ttf-ms-fonts: Fonts')
options=('!emptydirs')
install=${pkgname}.install
_arch=i386  # Workaround for the AUR Web interface Source parser
[ "$CARCH" = 'x86_64' ] && _arch=amd64
source=("${pkgname}-stable_${pkgver}_${_arch}.deb::https://dl.google.com/earth/client/current/${pkgname}-stable_current_${_arch}.deb"
        'googleearth'
        "${pkgname}-mimetypes.xml")
md5sums=('c9675d0078d1b188188c9d3b616de6fa'  # google-earth-stable_${pkgver}_i386.deb
         'e84f5d51ea3545c131d1794f89f6464a'  # googleearth
         'e3c67b8d05c3de50535bd7e45eee728e') # google-earth-mimetypes.xml
[ "$CARCH" = 'x86_64' ] && md5sums[0]='6979fd255895891fea8c98fa02fd243d' # google-earth-stable_${pkgver}_amd64.deb

_instdir=/opt/google/earth/free/

package() {
  msg2 "Extracting the data.tar.lzma"
  bsdtar -xf data.tar.lzma -C "${pkgdir}/"

  msg2 "Moving stuff in place"
  # The .desktop
  mv "${pkgdir}"/${_instdir}/${pkgname}.desktop "${pkgdir}"/usr/share/applications/

  # Icons
  for i in 16 22 24 32 48 64 128 256; do
    install -Dm644 "${pkgdir}"/${_instdir}/product_logo_${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/${pkgname}.png
  done

  # The MIME package
  install -Dm644 ${pkgname}-mimetypes.xml "${pkgdir}"/usr/share/mime/packages/${pkgname}-mimetypes.xml

  # The license (too many different ones to do this in "source=()")
  install -d "${pkgdir}"/usr/share/licenses/${pkgname}/
  curl -Ls ${url/i*}/license.html -o "${pkgdir}"/usr/share/licenses/${pkgname}/license.html

  msg2 "Removing the Debian-intended cron job and duplicated images"
  rm -r "${pkgdir}"/etc/cron.daily/ "${pkgdir}"/${_instdir}/product_logo_*.png
}