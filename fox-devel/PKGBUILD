# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=fox-devel
pkgver=1.7.28 # version 1.7.26 up breaks theme management
pkgrel=1
pkgdesc="Free Objects for X: GUI Toolkit for C++, development version."
arch=('i686' 'x86_64')
url="http://www.fox-toolkit.org/"
license=('LGPL' 'custom')
depends=('bzip2' 'libpng' 'libtiff' 'libxcursor' 'libxft' 'libxi' 'libxrandr' 'mesa' 'perl')
options=('!libtool')
provides=("fox=$pkgver")
source=("${url/www/ftp}pub/fox-${pkgver}.tar.gz")
md5sums=('83769a9122724d2de4835c38bafbd030')

build() {
  cd "$srcdir/fox-$pkgver"

  # patch to enable Xorg XIM extension
  sed -i "/IMI/s/XIMProc/XIDProc/g" lib/FXApp.cpp

  ./configure   --prefix=/usr \
                --enable-release \
                --with-xft=yes \
                --with-opengl=yes \
                --with-xim \
                --with-xshm \
                --with-shape \
                --with-xcursor \
                --with-xrender \
                --with-xrandr \
                --with-xfixes \
                --with-xinput
  make
}

package() {
  cd "$srcdir/fox-$pkgver"

  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 LICENSE_ADDENDUM "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # rename apps to prevent conflicts with fox
  for _f in `find $pkgdir/usr/bin -type f`; do
    mv $_f $_f-1.7
  done;
  for _f in `find $pkgdir/usr/share/man -type f`; do
    mv $_f `dirname $_f`/`basename $_f .1`-1.7.1
  done;
}
