# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=fox-devel
pkgver=1.7.25
pkgrel=1
pkgdesc="Free Objects for X: GUI Toolkit for C++, development version."
arch=('i686' 'x86_64')
url="http://www.fox-toolkit.org/"
license=('LGPL' 'custom')
depends=('bzip2' 'libpng' 'libtiff' 'libxcursor' 'libxft' 'libxi' 'libxrandr' 'mesa' 'perl')
options=('!libtool')
conflicts=('fox')
provides=("fox=$pkgver" "fox")
source=("${url}ftp/fox-${pkgver}.tar.gz")
md5sums=('232e2996be39148287e80b59061c7a66')

build() {
  cd "${srcdir}/fox-${pkgver}"

  # Xorg XIM extension broke => disabled
  ./configure   --prefix=/usr \
                --enable-release \
                --with-xft=yes \
                --with-opengl=yes \
                --without-xim \
                --with-xshm \
                --with-shape \
                --with-xcursor \
                --with-xrender \
                --with-xrandr \
                --with-xfixes \
                --with-xinput

  make
}

package() {
  cd "${srcdir}/fox-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE_ADDENDUM "${pkgdir}/usr/share/licenses/fox/LICENSE"
}
