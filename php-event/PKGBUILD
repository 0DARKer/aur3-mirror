# Maintainer: wolftankk <wolftankk@gmail.com>
pkgname=php-event
pkgver=1.10.2
pkgrel=1
pkgdesc="PHP extension for interfacing with Event"
url="http://pecl.php.net/package/event"
arch=('x86_64' 'i686')
license=('PHP')
depends=(
    'php'
    'libevent'
)
backup=('etc/php/conf.d/event.ini')

source=(
	"http://pecl.php.net/get/event-${pkgver}.tgz"
)

sha256sums=('eb277b3484c2399bad56815d4ea5d68dd724916e723c408c9b74e58d77a7e8de')

build() {
  cd "$srcdir/event-$pkgver"

  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/event-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
  echo ';extension=event.so' > event.ini 
  install -Dm644 event.ini "$pkgdir/etc/php/conf.d/event.ini"
}
