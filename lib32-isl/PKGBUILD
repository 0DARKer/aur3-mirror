# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=isl
pkgname=lib32-${_pkgbase}
pkgver=0.13
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints (32 bit)"
arch=('x86_64')
url="http://freecode.com/projects/isl"
depends=('lib32-gmp' "${_pkgbase}")
makedepends=('gcc-multilib')
license=('MIT')
options=('!libtool')
source=(http://isl.gforge.inria.fr/$_pkgbase-$pkgver.tar.bz2)
md5sums=('e4cf20c4c10d5d613457558b1f46b5e2')

build() {
  export CC='gcc -m32'
  export CXX='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd ${_pkgbase}-${pkgver}
  make check
}

package() {
  cd ${_pkgbase}-${pkgver}
  make -j1 DESTDIR="${pkgdir}/" install
  rm -rf "${pkgdir}/usr"/{include,lib32/libisl.so.*.*.*-gdb.py}
  install -dm755 "$pkgdir/usr/share/licenses/"
  ln -s $_pkgbase "$pkgdir/usr/share/licenses/$pkgname"
}
