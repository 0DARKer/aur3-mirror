# Maintainer : Christoffer Hirth <toffyrn [] toffyrn.net>
pkgname=pure-ftpd-mysql
pkgver=1.0.29
pkgrel=1
pkgdesc="A fast, production quality, standards-conformant FTP server with MySQL and TLS support"
arch=('i686' 'x86_64')
url="http://www.pureftpd.org/"
license=('custom')
conflicts=('pure-ftpd')
depends=('openssl' 'mysql')
backup=('etc/pure-ftpd.conf' 'etc/pureftpd-mysql.conf')
source=("ftp://ftp.pureftpd.org/pub/pure-ftpd/releases/pure-ftpd-${pkgver}.tar.bz2"
	'pure-ftpd'
	'pure-ftpd.conf'
	'pure-config.pl')
md5sums=('12a074824b509f9e7684fab333ed6915'
         '13cf942b643db7cf3e612185366391ca'
         '32eece2a4fef7ebe32ec06761bc3edd1'
         '007f1327760272c696d2e365211e552c')

build() {
	cd ${srcdir}/pure-ftpd-${pkgver}
	./configure --prefix=/usr \
	   --with-puredb \
	   --with-ftpwho \
	   --with-altlog \
	   --with-tls \
           --with-mysql
	make || return 1
}

package() {
	cd ${srcdir}/pure-ftpd-${pkgver}
	make prefix=${pkgdir}/usr install
	install -Dm755 ${srcdir}/pure-ftpd ${pkgdir}/etc/rc.d/pure-ftpd
	install -Dm644 ${srcdir}/pure-ftpd.conf ${pkgdir}/etc/pure-ftpd.conf
	install -Dm755 ${srcdir}/pure-config.pl ${pkgdir}/usr/sbin/pure-config.pl
	install -Dm644 ${srcdir}/pure-ftpd-${pkgver}/pureftpd-mysql.conf $pkgdir/etc/pureftpd-mysql.conf

	#install license
	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
