# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=breeze-git
pkgver=r35.f81d47f
pkgrel=1
pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
depends=('kwin-git')
makedepends=('extra-cmake-modules-git' 'git')
optdepends=('qtcurve-qt5: Breeze widget style' 'qtcurve-kde4: Breeze widget style for KDE4 applications')
conflicts=('breeze')
provides=('breeze')
source=('git://anongit.kde.org/breeze.git')
md5sums=('SKIP')

pkgver() {
  cd breeze
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../breeze \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# move QtCurve preset to the correct directory
  install -d -m755 "$pkgdir"/usr/share/apps/
  mv "$pkgdir"/usr/share/QtCurve "$pkgdir"/usr/share/apps/

# make breeze color scheme available for KDE4 apps too
  install -d -m755 "$pkgdir"/usr/share/apps/color-schemes
  ln -s "$pkgdir"/usr/share/color-schemes/Breeze.colors "$pkgdir"/usr/share/apps/color-schemes
}
