# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=breeze-git
pkgver=r73.5f489e5
pkgrel=1
pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
depends=('kcoreaddons-git' 'kconfig-git')
makedepends=('extra-cmake-modules' 'git')
optdepends=('qtcurve-qt5: Breeze widget style' 'qtcurve-kde4: Breeze widget style for KDE4 applications' 
'oxygen-icons: fallback icon theme')
conflicts=('breeze')
provides=('breeze')
source=('git://anongit.kde.org/breeze.git')
md5sums=('SKIP')

pkgver() {
  cd breeze
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../breeze \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_PREFIX_PATH=/opt/kf5/lib/cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# move QtCurve preset to the correct directory
  install -d -m755 "$pkgdir"/usr/share/apps/
  mv "$pkgdir"/usr/share/QtCurve "$pkgdir"/usr/share/apps/
}
