# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=breeze-git
pkgver=r2.8ff6b7c
pkgrel=1
pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules-git' 'git')
conflicts=('breeze')
provides=('breeze')
source=('git://anongit.kde.org/breeze.git')
md5sums=('SKIP')

pkgver() {
  cd breeze
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../breeze/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
