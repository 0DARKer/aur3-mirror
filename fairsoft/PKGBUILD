# Maintainer: Bastian LÃ¶her <b.loeher@gsi.de>
pkgname=fairsoft
pkgver=dec14
pkgrel=1
pkgdesc="Software collection provided by GSI/FAIR for analysis."
arch=('x86_64')
url="http://fairroot.gsi.de"
license=('LGPL3')
groups=()
makedepends=('cmake'
	'clang'
	'bison'
	'flex'
	'gcc-fortran'
	'subversion'
	'git'
	'curl')
depends=(
	'libiodbc'
	'fcgi'
	'sqlite'
	'java-environment'
	'glu'
	'fftw'
	'cfitsio'
	'postgresql-libs'
	'python'
	'libjpeg-turbo'
	'graphviz'
	'libx11'
	'libxft'
	'libxext'
	'libxpm'
	'libxmu'
	)
provides=()
conflicts=()
replaces=()
backup=()
options=('!emptydirs' '!strip' 'staticlibs' 'libtool')
install='fairsoft.install'
changelog=
source=("https://github.com/FairRootGroup/FairSoft/archive/${pkgver}.tar.gz"
	'arch.conf'
	'fairsoft.install')
noextract=()
md5sums=('77e407f713c580c9ced5e525804cd098'
         'a3c15b12aed50be801340cf05a533f61'
         '0836028042a943a32fccd50f9ff88a05')

# Do not compress the package for installation
# PKGEXT='.pkg.tar'
# Compress using lightweight gzip
PKGEXT='.pkg.tar.gz'

prepare() {
	cd "FairSoft-$pkgver"
	# Disable vc for the root package (needed for gcc 4.9.2)
	echo "Disabling vc for package root"
	sed -i "s/--enable-vc/--disable-vc/" tools/rootconfig.sh
	# Enable optimizations for zeromq (needed from gcc 4.8)
	# to suppress warning about FORTIFY_SOURCE=2 needing optimization
	echo "Enabling optimizations for zeromq"
	sed -i '/\.\/configure.*$/a echo "Patching src/Makefile" && sed -i "s/-O0/-O2/" src/Makefile' scripts/install_zeromq.sh
}

build() {
	cd "FairSoft-$pkgver"
	./configure.sh ../arch.conf
}

package() {
	cd "fairsoft_install"

	installdir=${pkgdir}/opt/fairsoft/current
	mkdir -p ${installdir}

	cp -r bin ${installdir}
	cp -r include ${installdir}
	cp -r lib ${installdir}
	ln -s lib ${installdir}/lib64
	cp -r share ${installdir}

	# Re-Link Geant4 and data dirs
	cd ${installdir}/share
	unlink Geant4 && ln -s Geant4-10.0.2 Geant4
	cd Geant4/data
	unlink G4ABLA && ln -s G4ABLA3.0 G4ABLA
	unlink G4EMLOW && ln -s G4EMLOW6.35 G4EMLOW
	unlink G4ENSDFSTATE && ln -s G4ENSDFSTATE1.0 G4ENSDFSTATE
	unlink G4NDL && ln -s G4NDL4.4 G4NDL
	unlink G4NEUTRONXS && ln -s G4NEUTRONXS1.4 G4NEUTRONXS
	unlink G4PII && ln -s G4PII1.3 G4PII
	unlink G4SAIDDATA && ln -s G4SAIDDATA1.1 G4SAIDDATA
	unlink PhotonEvaporation && ln -s PhotonEvaporation3.0 PhotonEvaporation
	unlink RadioactiveDecay && ln -s RadioactiveDecay4.0 RadioactiveDecay
	unlink RealSurface && ln -s RealSurface1.0 RealSurface

	# Patch thisroot.sh with correct install path
	sed -i "s#${srcdir}/FairSoft-$pkgver/../fairsoft_install#/opt/fairsoft/current#g" ${installdir}/bin/thisroot.sh
	sed -i "s#${srcdir}/FairSoft-$pkgver/../fairsoft_install#/opt/fairsoft/current#g" ${installdir}/bin/thisroot.csh
	# Patch roots with correct install path
	sed -i "s#${srcdir}/FairSoft-$pkgver/../fairsoft_install#/opt/fairsoft/current#g" ${installdir}/bin/roots
	# Patch root-config with correct install path
	sed -i "s#${srcdir}/FairSoft-$pkgver/../fairsoft_install#/opt/fairsoft/current#g" ${installdir}/bin/root-config
	# Patch proofserv with correct install path
	sed -i "s#${srcdir}/FairSoft-$pkgver/../fairsoft_install#/opt/fairsoft/current#g" ${installdir}/bin/proofserv
	# Patch gsl-config with correct install path
	sed -i "s#${srcdir}/FairSoft-$pkgver/../fairsoft_install#/opt/fairsoft/current#g" ${installdir}/bin/gsl-config
	# Patch geant4-config with correct install path
	sed -i "s#${srcdir}/FairSoft-$pkgver/../fairsoft_install#/opt/fairsoft/current#g" ${installdir}/bin/geant4-config
	sed -i "s#${srcdir}/fairsoft_install#/opt/fairsoft/current#g" ${installdir}/bin/geant4-config


}
