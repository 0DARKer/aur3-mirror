# Maintainer: Pieter Kokx <pieter@kokx.nl>
# Contributor: Justin Gottula <justin@jgottula.com>
pkgname=armory-git
pkgver=v0.91.beta.r3.gfd06c06
pkgrel=2
pkgdesc="Python-Based Bitcoin Software"
arch=('i686' 'x86_64')
url="https://github.com/etotheipi/BitcoinArmory"
license=('AGPL3')
depends=('crypto++' 'swig' 'python2' 'twisted' 'qt4' 'python2-pyqt' 'python2-bsddb' 'python2-psutil')
makedepends=('git' 'gcc' 'make')
provides=('armory')
conflicts=('armory')
source=("$pkgname"::'git+https://github.com/etotheipi/BitcoinArmory.git'
        'run-armory.sh'
        'makefile-01.patch'
        'build_detect_platform-02.patch')
noextract=()
sha256sums=('SKIP'
            '4b8ab285588ec07601fb4d9580b84e11a513635a102d92ee7c283261d0b6c0dc'
            'de76eed82333b7219183de30ddce67ba6c09e9d1be0067fd1d9fe81e5a7a7b93'
            'b97d08e6704f47e4794187b0ab1fea77e4f75dd9cc3628685d3d4a62fec147a4')

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/$pkgname"
    patch -p0 -i $startdir/makefile-01.patch
    patch -p0 -i $startdir/build_detect_platform-02.patch
    make
}

package() {
    install -Dm 755 "$srcdir/run-armory.sh" "$pkgdir/usr/bin/armory"

    mkdir -p "$pkgdir/opt"
    cp -R "$srcdir/$pkgname/" "$pkgdir/opt/"

    rm -rf "$pkgdir/opt/$pkgname/cppForSwig/"
    rm -rf "$pkgdir/opt/$pkgname/.git/"
    rm -rf "$pkgdir/opt/$pkgname/.gitignore"
}
