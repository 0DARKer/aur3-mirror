# Maintainer: Pieter Kokx <pieter@kokx.nl>
# Contributor: Justin Gottula <justin@jgottula.com>
pkgname=armory-git
pkgver=v0.90.beta.r2.ged67a40
pkgrel=1
pkgdesc="Python-Based Bitcoin Software"
arch=('i686' 'x86_64')
url="https://github.com/etotheipi/BitcoinArmory"
license=('AGPL3')
depends=('crypto++' 'swig' 'python2' 'twisted' 'qt4' 'python2-pyqt' 'python2-bsddb' 'python2-psutil')
makedepends=('git' 'gcc' 'make')
provides=('armory')
conflicts=('armory')
source=("$pkgname"::'git+https://github.com/etotheipi/BitcoinArmory.git'
        'run-armory.sh')
noextract=()
sha256sums=('SKIP'
            '4b8ab285588ec07601fb4d9580b84e11a513635a102d92ee7c283261d0b6c0dc')

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  make
}

package() {
  install -Dm 755 "$srcdir/run-armory.sh" "$pkgdir/usr/bin/armory"

  mkdir -p "$pkgdir/opt"
  cp -R "$srcdir/$pkgname/" "$pkgdir/opt/"

  rm -rf "$pkgdir/opt/$pkgname/cppForSwig/"
  rm -rf "$pkgdir/opt/$pkgname/.git/"
  rm -rf "$pkgdir/opt/$pkgname/.gitignore"
}
