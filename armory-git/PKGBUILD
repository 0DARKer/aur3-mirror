# Maintainer: Justin Gottula <justin@jgottula.com>
pkgname=armory-git
pkgver=20120110
pkgrel=1
pkgdesc="Python-Based Bitcoin Software"
arch=('i686' 'x86_64')
url="https://github.com/etotheipi/BitcoinArmory"
license=('AGPL3')
groups=()
depends=('crypto++' 'swig' 'python2' 'twisted' 'qt' 'python2-pyqt')
makedepends=('git' 'gcc' 'make')
provides=('armory')
conflicts=('armory')
replaces=()
backup=()
options=()
install=
source=('Makefile.patch'
        'run-armory.sh')
noextract=()
sha256sums=('2556f50fe8a0871895759246962aed2187fbf3445e4583431353231408e0f108'
            'baa1d335273af1f1f4d12dd68f066035e5b4485b215172690c307877ac62168c')

_gitroot='git://github.com/etotheipi/BitcoinArmory.git'
_gitname='BitcoinArmory'
_gitbranch='qtdev'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin $_gitbranch
    msg "The local files are updated."
  else
    git clone -b $_gitbranch "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cd cppForSwig
  patch Makefile <$srcdir/Makefile.patch
  make swig
  cd -
}

package() {
  install -Dm 755 "$srcdir/run-armory.sh" "$pkgdir/usr/bin/armory"

  mkdir -p "$pkgdir/opt"
  cp -R "$srcdir/$_gitname-build/" "$pkgdir/opt/"
  mv "$pkgdir/opt/$_gitname-build" "$pkgdir/opt/$_gitname"

  rm -rf "$pkgdir/opt/$_gitname/cppForSwig/"
  rm -rf "$pkgdir/opt/$_gitname/.git/"
  rm -rf "$pkgdir/opt/$_gitname/.gitignore"
}
