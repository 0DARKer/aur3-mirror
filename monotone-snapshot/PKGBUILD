# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Dan McGee <dpmcgee@gmail.com>

pkgname=monotone-snapshot
pkgver=1.0.90
pkgrel=8
_snapshot=8c090a8270d76f965aa87ecaae6b9c4b6edb7aca
pkgdesc='A modern distributed version control system, like hg, darcs, or git (dev snapshot)'
arch=('i686' 'x86_64')
url='https://code.monotone.ca/p/monotone'
license=('GPL')
depends=('pcre' 'lua' 'botan' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost')
provides=('monotone')
conflicts=('monotone')
install=monotone.install
DLAGENTS=('https::/usr/bin/curl -kfLC - --retry 3 --retry-delay 3 -o %o %u')
source=(monotone.zip::"https://code.monotone.ca/p/monotone/source/download/${_snapshot}")
sha256sums=('ea44e2bb5d3740396e56c5addac1d5d5bcf825f561281fb99cde605ed93d1843')

prepare() {
    cd "$srcdir"
    #fix --version reporting
    sed -i "s/echo \"unknown\"/echo \"$_snapshot\"/" 'Makefile.am'

    aclocal
    autoreconf --install
}

build() {
    cd "$srcdir"
    export DISABLE_NETWORK_TESTS=1

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-nls
    make
}

package() {
    cd "$srcdir"
    make DESTDIR="$pkgdir" install
    install -d "${pkgdir}/usr/share/vim/vimfiles/plugin/"
    install -Dm644 contrib/monotone.zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_mtn"
    install -Dm644 contrib/{mtncommit,basicio}.vim "${pkgdir}/usr/share/vim/vimfiles/plugin/"
}
