# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Dan McGee <dpmcgee@gmail.com>

pkgname=monotone-snapshot
pkgver=1.0.90
pkgrel=6
_snapshot=083dfa48871b21905f81ab22822709e0db97635b
pkgdesc='A modern distributed version control system, like hg, darcs, or git (dev snapshot)'
arch=('i686' 'x86_64')
url='https://code.monotone.ca/p/monotone'
license=('GPL')
depends=('pcre' 'lua' 'botan' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost')
provides=('monotone')
conflicts=('monotone')
install=monotone.install
DLAGENTS=('https::/usr/bin/curl -kfLC - --retry 3 --retry-delay 3 -o %o %u')
source=(monotone.zip::"https://code.monotone.ca/p/monotone/source/download/${_snapshot}")
sha256sums=('ae6ead68a18bc53ab8bdf053f48afa410cf84d8f5fb014fab1c080d0e244bc9e')

prepare() {
    cd "$srcdir"
    aclocal
    autoreconf --install
}

build() {
    cd "$srcdir"
    export DISABLE_NETWORK_TESTS=1

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-dependency-tracking --disable-nls

    make
}

package() {
    cd "$srcdir"
    make DESTDIR="$pkgdir" install
    install -d "${pkgdir}/usr/share/vim/vimfiles/plugin/"
    install -Dm644 contrib/monotone.zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_mtn"
    install -Dm644 contrib/{mtncommit,basicio}.vim "${pkgdir}/usr/share/vim/vimfiles/plugin/"
}
