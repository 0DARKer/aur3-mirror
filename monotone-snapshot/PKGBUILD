# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Dan McGee <dpmcgee@gmail.com>

pkgname=monotone-snapshot
pkgver=1.0.90
pkgrel=4
_snapshot=c2d91157493d780582ff66fcfa3971ff6a610a2c
pkgdesc='A modern distributed version control system, like hg, darcs, or git (dev snapshot)'
arch=('i686' 'x86_64')
url='https://code.monotone.ca/p/monotone'
license=('GPL')
depends=('pcre' 'lua' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost' 'botan')
provides=('monotone')
replaces=('monotone')
install=monotone.install
DLAGENTS=('https::/usr/bin/curl -kfLC - --retry 3 --retry-delay 3 -o %o %u')
source=(monotone.zip::"https://code.monotone.ca/p/monotone/source/download/${_snapshot}")
sha256sums=('a2b40e007f8426d1d7bed84ddbe9cdc6c1633594ecc72f5566af9af31e7949f5')

build() {
    cd "$srcdir"
    export DISABLE_NETWORK_TESTS=1
    aclocal
    autoreconf --install
    ./configure --prefix=/usr --sysconfdir=/etc --disable-nls
    make
}

package() {
    cd "$srcdir"
    make DESTDIR="$pkgdir" install
    install -d "${pkgdir}/usr/share/vim/vimfiles/plugin/"
    install -Dm644 contrib/monotone.zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_mtn"
    install -Dm644 contrib/{mtncommit,basicio}.vim "${pkgdir}/usr/share/vim/vimfiles/plugin/"
}
