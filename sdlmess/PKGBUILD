# Contributor: robb_force <robb_force@holybuffalo.net>
# Contributor: Anton Shestakov <engored*ya.ru>
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=sdlmess
pkgver=0.158.0
pkgrel=1
_basever=$(echo $pkgver | cut -f1,2 -d. | tr -d .)
_patchlevel=$(echo $pkgver | cut -f3 -d.)
pkgdesc='Multiple Emulator Super System, computer and game console emulator'
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('libxinerama' 'sdl2_ttf' 'qt4' 'flac' 'portmidi' 'sqlite' 'expat')
makedepends=('mesa' 'python2')
[ "$CARCH" = 'i686' ] && makedepends+=('nasm')
optdepends=('ttf-liberation: recommended UI font')
source=("${pkgname}.sh"
        'mess.ini'
        'mame-systemlibs.patch'
        "mame${_basever}s.zip::http://mamedev.org/downloader.php?file=releases/mame${_basever}s.zip")
sha256sums=('28fe6bcf5e7f846866371f3e98c699b19ed7dd73459c41b165d1916b222dc400'
            '773e632b687387fbf27af72481af30d3fd18e0326495399f1791b83a5fecae3d'
            'f19529c83cc23235770c9569dce0d906c9383e06300f9d50bf322491b55fe68e'
            '5304c2c1de8e964e55e4313f81196ba2ea11726d1e7c252c130191945e628885')
install="${pkgname}.install"
noextract=("mame${_basever}s.zip")
PKGEXT='.pkg.tar.gz'

prepare() {
  mkdir "${srcdir}/${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  bsdtar -xOf "${srcdir}/mame${_basever}s.zip" | bsdtar -xf -

  for (( i=1; i <= ${_patchlevel}; i++ )); do
    msg "Applying patch ${i}"
    patch -p0 -E --binary --quiet < "${srcdir}/${_basever}u${i}.diff"
  done

  patch -p1 < "${srcdir}/mame-systemlibs.patch"

  # Work around GCC bug where DOS line ending prevents compilation.
  #perl -pi -e 's/\r\n?/\n/g' src/emu/cpu/m68000/m68k_in.c
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make NOWERROR=1 PYTHON=python2 \
      BUILD_EXPAT=0 BUILD_ZLIB=0 BUILD_FLAC=0 BUILD_JPEGLIB=0 BUILD_MIDILIB=0 \
      BUILD_SQLITE3=0 \
      TARGET=mess SUFFIX64=
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Installing the wrapper script
  install -Dm755 "$srcdir/${pkgname}.sh" "$pkgdir/usr/bin/$pkgname"

  # Installing binaries
  install -Dm755 mess "$pkgdir/usr/share/$pkgname/$pkgname"

  # Installing extra bits
  install -d "$pkgdir/usr/share/$pkgname/"{artwork,hash,keymaps,ini}

  install -m644 artwork/* "$pkgdir/usr/share/$pkgname/artwork/"
  install -m644 hash/* "$pkgdir/usr/share/$pkgname/hash/"
  install -m644 src/osd/sdl/keymaps/* "$pkgdir/usr/share/$pkgname/keymaps/"

  install -m644 "$srcdir/mess.ini" "$pkgdir/usr/share/$pkgname/ini/"

  install -Dm644 docs/config.txt "$pkgdir/usr/share/doc/$pkgname/config.txt"
  install -Dm644 docs/SDL.txt "$pkgdir/usr/share/doc/$pkgname/SDL.txt"

  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
