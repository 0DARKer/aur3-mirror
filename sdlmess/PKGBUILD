# Contributor: robb_force <robb_force@holybuffalo.net>
# Maintainer: Anton Shestakov <engored*ya.ru>

pkgname=sdlmess
pkgver=0.144u5
pkgrel=1

_basever=0144
_patchlevel=5

pkgdesc='A port of the popular Multiple Emulator Super System using SDL.'
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gconf' 'zlib' 'expat' 'gtk2')
makedepends=('mesa')
[ "$CARCH" = 'i686' ] && makedepends=('mesa' 'nasm')
optdepends=('ttf-liberation: recommended UI font')

for i in `seq 1 ${_patchlevel}`; do
  _patches="${_patches} mess${_basever}u${i}_diff.zip::http://mess.redump.net/_media/downloads:mess${_basever}u${i}_diff.zip"
done

source=("mess${_basever}s.zip::http://mess.redump.net/_media/downloads:mess${_basever}s.zip" 
        sdlmess.sh ${_patches})
md5sums=('af4821f2575c7d8a241bc8e1243c3b20'
         '141069e7019da5e33414dc8d4c421150'
         '5dda3980fc97ded824c492f6ca7999bf'
         'be8d654c794c6f7520cf342891b70652'
         '6359e000db4c2b8af3d5f83e793edfdf'
         '2a6270904717cc9491e98249bfeea4bc'
         '130fcfe8fb3e72f88de11cef6cc75aff')
install=sdlmess.install

build() {
  cd "$srcdir"
  
  find . -type f -not -name '*.png' -exec perl -pi -e 's/\r\n?/\n/g' '{}' \;

  for i in `seq 1 ${_patchlevel}`; do
    msg2 "Applying patch: mess${_basever}u${i}.diff"
    patch --silent -p0 -E < mess${_basever}u${i}.diff
  done

  # Adjusting make options according to target architecture
  if [ "$CARCH" == 'x86_64' ]; then
    echo 'Compiling for AMD64...'
    make TARGET=mess AMD64=1 PTR64=1 SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  elif [ "$CARCH" == 'i686' ]; then
    echo 'Compiling for i686...'
    make TARGET=mess I686=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  else
    echo 'Compiling for i386...'
    make TARGET=mess PM=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  fi
}

package() {
  cd "$srcdir"
    
  # Installing the wrapper script
  install -Dm755 "$srcdir/sdlmess.sh" "$pkgdir/usr/bin/sdlmess"

  # Installing binaries
  install -Dm755 mess "$pkgdir/usr/share/sdlmess/sdlmess"

  # Installing extra bits
  install -d "$pkgdir/usr/share/sdlmess/artwork"
  install -m644 artwork/* "$pkgdir/usr/share/sdlmess/artwork/"
  install -d "$pkgdir/usr/share/sdlmess/hash"
  install -m644 hash/* "$pkgdir/usr/share/sdlmess/hash/"

  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/custom/sdlmess/license.txt"
}
