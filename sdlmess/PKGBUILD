# Contributor: robb_force <robb_force@holybuffalo.net>
# Maintainer: Anton Shestakov <engored*ya.ru>

pkgname=sdlmess
pkgver=0.141
_basever=0141
pkgrel=1
pkgdesc='A port of the popular Multiple Emulator Super System using SDL.'
url='http://rbelmont.mameworld.info/?page_id=163'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gconf' 'zlib' 'expat')
makedepends=('unzip')
[ "$CARCH" = 'i686' ] && makedepends=('unzip' 'nasm')
optdepends=('ttf-liberation: recommended UI font')
source=("http://emumovies.com/aarongiles/releases/mame${_basever}s.zip" 
        "http://www.mess.org/files/mess${_basever}s.zip" 
        sdlmess.sh)
md5sums=('95b3866cc98a2bea789f8b20e6ff7001' 
        '67459d2e86ffb9569c93a8ab71c28132' 
        '141069e7019da5e33414dc8d4c421150')
noextract=("mess${_basever}s.zip")
install=sdlmess.install

build() {
  cd "$srcdir"
  
  rm -rf mess

  # First MAME source, then MESS source on top of it (overwriting when needed)
  unzip -qo mame.zip -d mess
  unzip -qo "mess${_basever}s.zip" -d mess

  cd mess
  
  # These changes allow GCC 4.2+ to compile SDLMESS
  sed -e 's|CCOMFLAGS += -Werror|CCOMFLAGS += |' \
    -e 's|-Wno-unused-functions|-Wno-unused|' \
    -i makefile

  # Adjusting make options according to target architecture
  if [ "$CARCH" == 'x86_64' ]; then
    echo 'Compiling for AMD64...'
    make TARGET=mess AMD64=1 PTR64=1 SUFFIX64='' BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  elif [ "$CARCH" == 'i686' ]; then
    echo 'Compiling for i686...'
    make TARGET=mess I686=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  else
    echo 'Compiling for i386...'
    make TARGET=mess PM=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  fi
}

package() {
  cd "$srcdir/mess"
    
  # Installing the wrapper script
  install -Dm755 "$srcdir/sdlmess.sh" "$pkgdir/usr/bin/sdlmess"

  # Installing binaries
  install -Dm755 mess "$pkgdir/usr/share/sdlmess/sdlmess"

  # Installing extra bits
  install -d "$pkgdir/usr/share/sdlmess/artwork"
  install -m644 artwork/* "$pkgdir/usr/share/sdlmess/artwork/"
  install -d "$pkgdir/usr/share/sdlmess/hash"
  install -m644 hash/* "$pkgdir/usr/share/sdlmess/hash/"

  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/custom/sdlmess/license.txt"
}
