# Contributor: robb_force <robb_force@holybuffalo.net>
# Contributor: Anton Shestakov <engored*ya.ru>
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=sdlmess
pkgver=0.148
pkgrel=1
_basever=0148
pkgdesc='Multiple Emulator Super System, computer and game console emulator'
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gconf' 'zlib' 'expat' 'gtk2' 'alsa-lib')
makedepends=('mesa' 'unzip')
[ "$CARCH" = 'i686' ] && makedepends+=('nasm')
optdepends=('ttf-liberation: recommended UI font')
source=(sdlmess.sh
        "mame${_basever}s.zip::http://mamedev.org/downloader.php?file=releases/mame${_basever}s.zip")
md5sums=('141069e7019da5e33414dc8d4c421150'
         '38f7727c2961cd31e2ab6aa1814a23ba')
install=sdlmess.install
noextract=("mame${_basever}s.zip")
PKGEXT=.pkg.tar.gz

build() {
  mkdir "${srcdir}/${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  bsdtar -xOf "${srcdir}/mame0148s.zip" | bsdtar -xvf -

  make TARGET=mess SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 \
      ARCHOPTS="$CFLAGS" PYTHON=python2
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Installing the wrapper script
  install -Dm755 "$srcdir/sdlmess.sh" "$pkgdir/usr/bin/sdlmess"

  # Installing binaries
  install -Dm755 mess "$pkgdir/usr/share/sdlmess/sdlmess"

  # Installing extra bits
  install -d "$pkgdir/usr/share/sdlmess/artwork"
  install -m644 artwork/* "$pkgdir/usr/share/sdlmess/artwork/"
  install -d "$pkgdir/usr/share/sdlmess/hash"
  install -m644 hash/* "$pkgdir/usr/share/sdlmess/hash/"

  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/custom/sdlmess/license.txt"
}
