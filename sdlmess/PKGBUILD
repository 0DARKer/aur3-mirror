# Contributor: robb_force <robb_force@holybuffalo.net>
# Maintainer: Anton Shestakov <engored*ya.ru>

pkgname=sdlmess
pkgver=0.145u8
pkgrel=1

_basever=0145
_patchlevel=8

pkgdesc='A port of the popular Multiple Emulator Super System using SDL.'
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gconf' 'zlib' 'expat' 'gtk2')
makedepends=('mesa')
[ "$CARCH" = 'i686' ] && makedepends=('mesa' 'nasm')
optdepends=('ttf-liberation: recommended UI font')

source=(sdlmess.sh
        "http://ompldr.org/vZGJtag/mess${_basever}u5s.zip" 
        "http://ompldr.org/vZGJtZg/mess${_basever}u6_diff.zip"
        "http://ompldr.org/vZGh0ag/mess${_basever}u7_diff.zip"
        "http://ompldr.org/vZG54dg/mess${_basever}u8_diff.zip")
md5sums=('141069e7019da5e33414dc8d4c421150'
         'e1a1fb0d9d121f88e9159f80a6747b45'
         'c3d3f98ef0d843991180115a717888e4'
         'c48dccca2dbc1a8d6a8ca607dc7530f6'
         '56d09445f3fcfe14ae1f3784643e442a')
install=sdlmess.install

build() {
  cd "$srcdir"
  
  find . -type f -not -name '*.png' -exec perl -pi -e 's/\r\n?/\n/g' '{}' \;

  for i in 6 7 8; do
    msg2 "Applying patch: mess${_basever}u${i}.diff"
    patch --silent -p0 -E < mess${_basever}u${i}.diff
  done

  # Adjusting make options according to target architecture
  if [ "$CARCH" == 'x86_64' ]; then
    echo 'Compiling for AMD64...'
    make TARGET=mess AMD64=1 PTR64=1 SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  elif [ "$CARCH" == 'i686' ]; then
    echo 'Compiling for i686...'
    make TARGET=mess I686=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  else
    echo 'Compiling for i386...'
    make TARGET=mess PM=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  fi
}

package() {
  cd "$srcdir"
    
  # Installing the wrapper script
  install -Dm755 "$srcdir/sdlmess.sh" "$pkgdir/usr/bin/sdlmess"

  # Installing binaries
  install -Dm755 mess "$pkgdir/usr/share/sdlmess/sdlmess"

  # Installing extra bits
  install -d "$pkgdir/usr/share/sdlmess/artwork"
  install -m644 artwork/* "$pkgdir/usr/share/sdlmess/artwork/"
  install -d "$pkgdir/usr/share/sdlmess/hash"
  install -m644 hash/* "$pkgdir/usr/share/sdlmess/hash/"

  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/custom/sdlmess/license.txt"
}
