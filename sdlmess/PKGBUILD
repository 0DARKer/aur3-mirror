# Contributor: robb_force <robb_force@holybuffalo.net>
# Maintainer: Anton Shestakov <engored*ya.ru>

pkgname=sdlmess
pkgver=0.142u5
pkgrel=1

_basever=0142
_patchlevel=5

pkgdesc='A port of the popular Multiple Emulator Super System using SDL.'
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gconf' 'zlib' 'expat' 'gtk2')
makedepends=('unzip' 'mesa')
[ "$CARCH" = 'i686' ] && makedepends=('unzip' 'mesa' 'nasm')
optdepends=('ttf-liberation: recommended UI font')

for i in `seq 1 ${_patchlevel}`; do
  _patches="${_patches} mess${_basever}u${i}_diff.zip::http://mess.redump.net/_media/downloads:mess${_basever}u${i}_diff.zip"
done

source=("http://emumovies.com/aarongiles/releases/mame${_basever}s.zip" 
        "http://www.mess.org/files/mess${_basever}s.zip" 
        sdlmess.sh ${_patches})
md5sums=('2fc102f58ce3757f3bdaf062241fa8b8'
         'fecff43a5380267bcedba135581682a5'
         '141069e7019da5e33414dc8d4c421150'
         'a145681b8913fbee71823fd96d979e78'
         '6c521d2a4371a02d5b2569d487db4917'
         '7dece20e0f92e079e7d479565794923e'
         '923de561449c5433de8e0f5422d0e7fb'
         '0c82cfec7bc1b6fd3183e6459b1bbefb'
         )
noextract=("mess${_basever}s.zip")
install=sdlmess.install

build() {
  cd "$srcdir"
  
  rm -rf mess

  # First MAME source, then MESS source on top of it (overwriting when needed)
  unzip -qo mame.zip -d mess
  unzip -qo "mess${_basever}s.zip" -d mess

  cd mess

  find . -type f -not -name '*.png' | xargs perl -pi -e 's/\r\n?/\n/g'

  # Sorry, this is a ridiculously dirty trick to make `patch` happy.
  # Don't worry about these two gifs, they are being removed by u4 patch anyway.
  head -c8 docs/windows/images/frog.gif > docs/windows/images/newmenu.gif
  cp docs/windows/images/newmenu.gif docs/windows/images/frog.gif

  for i in `seq 1 ${_patchlevel}`; do
    msg2 "Applying patch: mess${_basever}u${i}.diff"
    patch --silent -p0 -E < ../mess${_basever}u${i}.diff
  done

  # Adjusting make options according to target architecture
  if [ "$CARCH" == 'x86_64' ]; then
    echo 'Compiling for AMD64...'
    make TARGET=mess AMD64=1 PTR64=1 SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  elif [ "$CARCH" == 'i686' ]; then
    echo 'Compiling for i686...'
    make TARGET=mess I686=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  else
    echo 'Compiling for i386...'
    make TARGET=mess PM=1 NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 ARCHOPTS="$CFLAGS"
  fi
}

package() {
  cd "$srcdir/mess"
    
  # Installing the wrapper script
  install -Dm755 "$srcdir/sdlmess.sh" "$pkgdir/usr/bin/sdlmess"

  # Installing binaries
  install -Dm755 mess "$pkgdir/usr/share/sdlmess/sdlmess"

  # Installing extra bits
  install -d "$pkgdir/usr/share/sdlmess/artwork"
  install -m644 artwork/* "$pkgdir/usr/share/sdlmess/artwork/"
  install -d "$pkgdir/usr/share/sdlmess/hash"
  install -m644 hash/* "$pkgdir/usr/share/sdlmess/hash/"

  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/custom/sdlmess/license.txt"
}
