# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: TuxSpirit <tuxspirit@archlinux.fr>

_pkgname=peazip
pkgname=$_pkgname-gtk2
pkgver=4.9.2
pkgrel=1
pkgdesc="GTK2 archiver utility"
arch=(i686 x86_64)
url=http://www.peazip.org/peazip-linux.html
license=(Artistic2.0 LGPL2.1)
depends=(balz desktop-file-utils lib32-{curl,gmp4,gtk2,ncurses} lpaq paq8o p7zip quad unace upx zpaq)
[[ $CARCH == "i686" ]] && depends=(${depends[@]/lib32-/})
provides=($_pkgname)
conflicts=($_pkgname-qt)
install=$_pkgname.install
source=($pkgname-$pkgver.tgz::http://downloads.sourceforge.net/project/$_pkgname/$pkgver/$_pkgname-$pkgver.LINUX.GTK2.tgz $_pkgname.desktop)
noextract=($pkgname-$pkgver.tgz)
sha256sums=('a142981b5e1fccdf8fc98e5f91c5a35c38ff54c16e95e7c8b8ebbcc497958332'
            '4d876c6a61f25a7e2f3dfa69b1c80e61fdda9220bbf8e23a407e34eae377091b')
sha512sums=('445c9fb5263452326e03cc007d1de9d1f79514e415474204caef3d2959d16099e9545086ad23ce09dea064a132e6ad5b3944f6a8bceb321078cc923179586eca'
            'bc86d42b33285c2709081ddca7c06fe789cc2d42c69c4cbed595c077a776d91e5526eb799dcdc404375a3bfb212927165a02d3d79301f53adb8a89039bf7bb5f')

_resdir="$pkgdir"/usr/lib/$_pkgname/res

package() {
    install -d $pkgname/
    cd $pkgname/
    bsdtar -xf ../$pkgname-$pkgver.tgz
    cd usr/local/share/PeaZip/res/

    install -Dm755 ../$_pkgname "$pkgdir"/usr/lib/$_pkgname/$_pkgname

    for i in pea pealauncher rnd; do
        install -Dm755 $i "$_resdir"/$i
    done
    for i in arc/{arc,*.sfx}; do
        install -Dm755 $i "$_resdir"/$i
    done
    for i in altconf.txt lang/* themes/{{nographic,seven}-embedded/*,*.7z} arc/arc.{ini,groups}; do
        install -Dm644 $i "$_resdir"/$i
    done

    install -d "$_resdir"/7z/Codecs/
    for i in 7z{,a,r,.so,Con.sfx,.sfx} Codecs/Rar29.so; do
        ln -sf /usr/lib/p7zip/$i "$_resdir"/7z/$i
    done
    for i in quad/{balz,quad} unace/unace upx/upx lpaq/lpaq8 paq/paq8o zpaq/zpaq; do
        install -d "$_resdir"/$(dirname $i)/
        ln -sf /usr/bin/$(basename $i) "$_resdir"/$i
    done
    install -d "$pkgdir"/usr/bin/
    for i in /usr/lib/$_pkgname/{$_pkgname,res/pea,res/pealauncher}; do
        ln -sf $i "$pkgdir"/usr/bin/$(basename $i)
    done

    install -Dm644 "$srcdir"/$pkgname/usr/local/share/icons/$_pkgname.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png
    desktop-file-install "$srcdir"/$_pkgname.desktop --dir "$pkgdir"/usr/share/applications/
}
