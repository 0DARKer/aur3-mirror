# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: TuxSpirit <tuxspirit@archlinux.fr>

_pkgname=peazip
pkgname=$_pkgname-gtk2
pkgver=4.7.2
pkgrel=1
pkgdesc="GTK2 archiver utility"
arch=(i686 x86_64)
url=http://www.peazip.org/peazip-linux.html
license=(LGPL)
depends=(balz desktop-file-utils gdk-pixbuf2 lpaq paq8o p7zip quad unace upx zpaq)
if [[ $CARCH == "i686" ]]; then
    depends+=(gmp4 gtk2)
else
    depends+=(lib32-{gmp4,gtk2,ncurses})
fi
provides=($_pkgname)
conflicts=($_pkgname-qt)
install=$_pkgname.install
source=($pkgname.tgz::http://downloads.sourceforge.net/project/$_pkgname/$pkgver/$_pkgname-$pkgver.LINUX.GTK2.tgz $_pkgname.desktop)
noextract=($pkgname.tgz)
sha256sums=('6a6875bf1adb46c39a3c8044cb710c402367337fd51045e47f05f38c99a200b2'
    '4d876c6a61f25a7e2f3dfa69b1c80e61fdda9220bbf8e23a407e34eae377091b')
sha512sums=('957ed4f08f9a5be82fb15689585e2fd02982f53966b7ff5b3b5de30c67684492435488eaebdc1e4b4ffce11a13470ecbe66c51a75a1a160d5f895c1d6478049a'
    'bc86d42b33285c2709081ddca7c06fe789cc2d42c69c4cbed595c077a776d91e5526eb799dcdc404375a3bfb212927165a02d3d79301f53adb8a89039bf7bb5f')

_resdir="$pkgdir"/usr/lib/$_pkgname/res

package() {
    install -d "$srcdir"/$pkgname/
    cd "$srcdir"/$pkgname/
    tar xf ../$pkgname.tgz -C ./
    cd usr/local/share/PeaZip/res/

    install -Dm755 ../$_pkgname "$pkgdir"/usr/lib/$_pkgname/$_pkgname

    for i in pea pealauncher rnd; do
        install -Dm755 $i "$_resdir"/$i
    done
    for i in arc/{arc,*.sfx}; do
        install -Dm755 $i "$_resdir"/$i
    done
    for i in altconf.txt lang/* themes/{{nographic,seven}-embedded/*,*.7z} arc/arc.{ini,groups}; do
        install -Dm644 $i "$_resdir"/$i
    done

    install -d "$_resdir"/7z/Codecs/
    for i in 7z{,a,r,.so,Con.sfx,.sfx} Codecs/Rar29.so; do
        ln -sf /usr/lib/p7zip/$i "$_resdir"/7z/$i
    done
    for i in quad/{balz,quad} unace/unace upx/upx lpaq/lpaq8 paq/paq8o zpaq/zpaq; do
        install -d "$_resdir"/$(dirname $i)/
        ln -sf /usr/bin/$(basename $i) "$_resdir"/$i
    done
    install -d "$pkgdir"/usr/bin/
    for i in /usr/lib/$_pkgname/{$_pkgname,res/pea,res/pealauncher}; do
        ln -sf $i "$pkgdir"/usr/bin/$(basename $i)
    done

    install -Dm644 "$srcdir"/$pkgname/usr/local/share/icons/$_pkgname.png "$pkgdir"/usr/share/pixmaps/$_pkgname.png
    desktop-file-install "$srcdir"/$_pkgname.desktop --dir "$pkgdir"/usr/share/applications/
}
