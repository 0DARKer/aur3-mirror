# Maintainer: AlexanderR <alexander r at gmx com>

_target=arm-unknown-linux-gnueabi

pkgname=archdroid-gcc
pkgver=4.7.2
pkgrel=2
pkgdesc="The GNU Compiler Collection, used by Droid Overlay project (2-stage compiler)."
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://sf.net/p/droidoverlay/wiki/Cross Toolchain/"
depends=('archdroid-binutils>=2.22' 'libmpc' 'libelf')
conflicts=('archdroid-gcc-base')
replaces=('archdroid-gcc-base')
options=(!libtool !emptydirs zipman docs !strip)
groups=('archdroid')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
	armhf-triplet-trunk.diff)
md5sums=('cc308a0891e778cfda7a151ab8a6e762'
         'bd7330bd41845929f1e0efb3b7d0a060')

build() {
  export CFLAGS=""

  cd "$srcdir"/gcc-$pkgver

  # Do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  echo ${pkgver} > gcc/BASE-VER

  patch -p1 -i "${srcdir}/armhf-triplet-trunk.diff"

  cd "$srcdir"
  mkdir -p gcc-build && cd gcc-build

  "$srcdir"/gcc-$pkgver/configure --prefix=/usr \
	--libdir=/usr/lib --libexecdir=/usr/lib \
	--mandir=/usr/share/man --infodir=/usr/share/info \
	--with-bugurl="http://sf.net/p/droidoverlay/tickets/" \
	--enable-languages=c,c++,lto \
	--program-prefix=$_target- \
	--enable-shared --disable-nls --enable-multilib \
        --disable-libstdcxx-pch --enable-libstdcxx-time \
	--enable-gnu-unique-object --enable-linker-build-id \
	--with-ppl --enable-cloog-backend=isl \
	--disable-ppl-version-check --disable-cloog-version-check \
	--disable-build-with-cxx --disable-build-poststage1-with-cxx \
	--enable-__cxa_atexit --disable-libunwind-exceptions \
	--disable-libssp --enable-threads=posix \
	--enable-softfloat --with-float=hard \
	--target=$_target \
	--with-as=/usr/bin/$_target-as \
	--with-ld=/usr/bin/$_target-ld \
	--with-system-zlib --with-sysroot=/fhs \
	--enable-checking=release --enable-lto \
	--enable-gold --enable-ld=default \
	--enable-plugin --with-plugin-ld=ld.gold \
	--with-linker-hash-style=gnu

  make
}

package() {
  cd "$srcdir"/gcc-build

  make -j1 DESTDIR="$pkgdir" install

  # strip it manually
  strip "$pkgdir"/usr/bin/* 2>/dev/null || true
  find "$pkgdir"/usr/lib -type f -exec /usr/bin/${_target}-strip --strip-unneeded {} \; 2>/dev/null || true

  rm -r "$pkgdir"/usr/share/{info,man}
  rm -r "$pkgdir"/usr/share/gcc-$pkgver/python/libstdcxx/
  mkdir -p "$pkgdir"/fhs/usr/lib/
  cp -l "$pkgdir"/usr/$_target/lib/* "$pkgdir"/fhs/usr/lib/

  mkdir -p "$pkgdir"/usr/$_target/bin/
  for compiler in gcc g++; do
    ln -s ../../bin/$_target-$compiler "$pkgdir"/usr/$_target/bin/$compiler
  done
}
