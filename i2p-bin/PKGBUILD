# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

########[ OPTIONS ]########################################
# Download sources from within i2p
_i2p_fetch=0
###########################################################

pkgname=i2p-bin
pkgver=0.9.5
pkgrel=2
pkgdesc="A distributed anonymous network (pre-compiled binary)"
url="http://www.i2p2.de"
license=('GPL2')
arch=('any')
depends=('java-runtime')
optdepends=('gtk2: for rrd graphs'
            'robert: I2P BitTorrent client')
conflicts=('i2p' 'i2p-dev' 'i2p-portable' 'i2p-portable-source')
provides=('i2p')
backup=('opt/i2p/wrapper.config')
install='i2p.install'
noextract=("i2pinstall_${pkgver}.jar")
source=("https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar"
        "https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar.sig"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2pinstall_${pkgver}.jar"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2pinstall_${pkgver}.jar.sig"
        'tmpfiles.d' 'i2prouter.service' 'i2prouter.sh')

[[ "$_i2p_fetch" = 1 ]] && {
    export http_proxy=127.0.0.1:4444
    source=("http://echelon.i2p/${pkgver}/i2pinstall_${pkgver}.jar"
            "http://echelon.i2p/${pkgver}/i2pinstall_${pkgver}.jar.sig"
            'tmpfiles.d' 'i2prouter.service' 'i2prouter.sh')
}
sha256sums=('af251596e034b751dc17b73bd35254b94f92d4a3121a6e96479c45c4d846ab30'
            '1ff1ab5abdf190af31a76fc55efdaf7dc12cc3921858b7e4d191817de098cd56'
            '875a979992c789d8191ecc17c9e43063d5dd90215afd7101c5019e3fdc3fc8a4'
            'bfa4de5936b0a0ff08c20307b0739ab6a65bee325e82fe22662ccf2062f35666'
            '25b227bc33a4b76e022d4ab40cd1eb1037f1439000d96258de90eda506ecb620')

package() {
    cd "$srcdir"
    source /etc/profile.d/jdk.sh 2>/dev/null || source /etc/profile.d/openjdk6.sh

    echo "INSTALL_PATH=${pkgdir}/opt/i2p" >install.properties
    java -jar i2pinstall_${pkgver}.jar -options install.properties

    install -dm700   ${pkgdir}/run/i2p
    install -dm755   ${pkgdir}/usr/bin
    install -dm755   ${pkgdir}/opt/i2p

    install -Dm755 ${srcdir}/i2prouter.sh         "${pkgdir}/opt/i2p/i2prouter"
    install -Dm644 ${srcdir}/i2prouter.service    "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${srcdir}/tmpfiles.d           "${pkgdir}/usr/lib/tmpfiles.d/i2prouter.conf"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1 "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/LICENSE.txt  "${pkgdir}/usr/share/licenses/i2p/LICENSE"
    mv ${pkgdir}/opt/i2p/licenses/*               "${pkgdir}/usr/share/licenses/i2p/"

    rm -r ${pkgdir}/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,LICENSE.txt,runplain.sh,licenses,man}

    chmod 755 ${pkgdir}/opt/i2p
    chmod -x ${pkgdir}/opt/i2p/*.config

    ln -s /opt/i2p/{eepget,i2prouter} ${pkgdir}/usr/bin/

    sed -i $pkgdir/opt/i2p/{eepget,wrapper.config} \
        -e "s:$pkgdir/opt/i2p:/opt/i2p:g" \
        -e 's:wrapper.signal.mode.usr1=.*:wrapper.signal.mode.usr1=RESTART:'
    sed -i $pkgdir/opt/i2p/clients.config \
        -e "s:clientApp.4.startOnLoad=.*:clientApp.4.startOnLoad=false:"
    echo 'router.updateDisabled=true' >${pkgdir}/opt/i2p/router.config
}
