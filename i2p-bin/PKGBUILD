# Maintainer : Skydrome <irc.freenode.net>
# Contributor: DaNiMoTh <jjdanimoth@gmail.com>

pkgname=i2p-bin
pkgver=0.9.1
pkgrel=1
pkgdesc="A distributed anonymous network (pre-compiled binary)"
url="http://www.i2p2.de"
license=('GPLv2')
arch=('i686' 'x86_64')
install='i2p.install'
depends=('java-runtime')
conflicts=('i2p' 'i2p-git' 'i2p-portable' 'i2p-portable-source')
provides=('i2p')
        
source=("http://mirror.i2p2.de/i2pinstall_${pkgver}.jar"
        "http://mirror.i2p2.de/i2pinstall_${pkgver}.jar.sig"
        #"https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar"
        #"https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar.sig"
        "i2prouter"
        "i2prouter.service")
noextract=("i2pinstall_${pkgver}.jar")
sha256sums=('39c19c0df042ad231a14fdf1c20e4927651a2af0f0cf734b46eba0d2b8419314'
            '466d391521c10d53693166492fa0eebc606c727c2d339608af43850dbb2a8585'
            'f40c0996b4b0d83b443f1f616bc54bb18735872020e33a50b0f1fbad59c97d7d'
            'dee85e9a856b18bac257cd06e70f42e88edce3f48b92762410359a69d5c4284f')

package() {
    cd "$srcdir"
    echo "INSTALL_PATH=${pkgdir}/opt/i2p" > install.properties
    java -jar i2pinstall_${pkgver}.jar -options install.properties

    sed -i $pkgdir/opt/i2p/{i2prouter,wrapper.config,runplain.sh,eepget} \
        -e "s|$pkgdir/opt/i2p|/opt/i2p|g"
    sed -i $pkgdir/opt/i2p/i2prouter \
        -e "s|#RUN_AS_USER=.*|RUN_AS_USER=i2p|" \
        -e "s|I2PTEMP=.*|I2PTEMP=\"\$I2P/tmp\"|"

    install -d "${pkgdir}/opt/i2p/tmp" "${pkgdir}/usr/share/licenses/i2p/"
    install -Dm755 ${srcdir}/i2prouter               "${pkgdir}/etc/rc.d/i2prouter"
    install -Dm644 ${srcdir}/i2prouter.service       "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1    "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/man/i2prouter.1 "${pkgdir}/usr/share/man/man1/i2prouter.1"
    cp LICENSE.txt                                   "${pkgdir}/usr/share/licenses/i2p/"
    mv ${pkgdir}/opt/i2p/licenses/*                  "${pkgdir}/usr/share/licenses/i2p/"
    rm -r $pkgdir/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,licenses,man}
    chmod -R 750 ${pkgdir}/opt/i2p
}
