# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

########[ OPTIONS ]########################################
# Download sources from within i2p
_i2p_fetch=0
###########################################################

pkgname=i2p-bin
pkgver=0.9.4
pkgrel=1
pkgdesc="A distributed anonymous network (pre-compiled binary)"
url="http://www.i2p2.de"
license=('GPL2')
arch=('i686' 'x86_64' 'armv6h')
install='i2p.install'
depends=('java-runtime' 'bash' 'libxtst')
optdepends=('gtk2: for rrd graphs')
conflicts=('i2p' 'i2p-dev' 'i2p-portable' 'i2p-portable-source')
provides=('i2p')
noextract=("i2pinstall_${pkgver}.jar")
source=("https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar"
        "https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar.sig"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2pinstall_${pkgver}.jar"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2pinstall_${pkgver}.jar.sig"
        'i2prouter.service')

[[ "$_i2p_fetch" = 1 ]] && {
    export http_proxy=127.0.0.1:4444
    source=("http://echelon.i2p/${pkgver}/i2pinstall_${pkgver}.jar"
            "http://echelon.i2p/${pkgver}/i2pinstall_${pkgver}.jar.sig"
            "i2prouter.service")
}
sha256sums=('8e670ba26c04176ace9246d91a09951975e2965b89628f620f5a3dff917298e4'
            '6c9ff0ecd2bd66b389e7faa6047aa73bd4df0cbc45124039419ddbeb2a4b55f4'
            '77105f3a4c52467fa5e70c3b2cf6a8435744760c53feb1358773191e746207b1')

package() {
    cd "$srcdir"
    echo "INSTALL_PATH=${pkgdir}/opt/i2p" >install.properties
    java -jar i2pinstall_${pkgver}.jar -options install.properties

    sed -i $pkgdir/opt/i2p/{i2prouter,wrapper.config,runplain.sh,eepget} \
        -e "s:$pkgdir/opt/i2p:/opt/i2p:g"
    sed -i $pkgdir/opt/i2p/i2prouter \
        -e "s:#RUN_AS_USER=.*:RUN_AS_USER=i2p:" \
        -e "s:I2P_CONFIG_DIR=.*:I2P_CONFIG_DIR=\"\$I2P/.i2p\":"
    echo 'router.updateDisabled=true' >${pkgdir}/opt/i2p/router.config

    install -d "${pkgdir}/usr/share/licenses/i2p/"
    install -Dm644 ${srcdir}/i2prouter.service       "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1    "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/man/i2prouter.1 "${pkgdir}/usr/share/man/man1/i2prouter.1"
    mv ${pkgdir}/opt/i2p/LICENSE.txt                 "${pkgdir}/usr/share/licenses/i2p/"
    mv ${pkgdir}/opt/i2p/licenses/*                  "${pkgdir}/usr/share/licenses/i2p/"

    rm -r ${pkgdir}/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,licenses,man}
    chmod -R 750 ${pkgdir}/opt/i2p
    chmod -x ${pkgdir}/opt/i2p/*.config
}
