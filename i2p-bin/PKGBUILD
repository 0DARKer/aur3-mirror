# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

########[ OPTIONS ]########################################
# Download sources from within i2p
#_i2p_fetch=1
###########################################################

pkgname=i2p-bin
pkgver=0.9.6
pkgrel=1
pkgdesc="A distributed anonymous network (pre-compiled binary)"
url="http://www.i2p2.de"
license=('GPL2')
arch=('any')
depends=('java-runtime' 'java-service-wrapper')
optdepends=('robert: I2P BitTorrent client') #'gtk2: for rrd graphs'
conflicts=('i2p' 'i2p-dev' 'i2p-portable' 'i2p-portable-source')
provides=('i2p')
backup=('opt/i2p/wrapper.config')
install='i2p.install'
noextract=("i2pinstall_${pkgver}.jar")
source=("https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar"
        "https://i2p.googlecode.com/files/i2pinstall_${pkgver}.jar.sig"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2pinstall_${pkgver}.jar"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2pinstall_${pkgver}.jar.sig"
        'i2prouter.service' 'i2prouter.sh')

[[ $_i2p_fetch ]] && {
    export http_proxy=127.0.0.1:4444
    source=("http://echelon.i2p/${pkgver}/i2pinstall_${pkgver}.jar"
            "http://echelon.i2p/${pkgver}/i2pinstall_${pkgver}.jar.sig"
            'i2prouter.service' 'i2prouter.sh')
}
sha256sums=('2dd5b67920723dd94202a408de31671b1e9543fcd5611bbe79385cc14f93b371'
            'SKIP'
            'b5343a021d11bb6d59ec69a07c4158014e47a17b7b0b3105a8ec282ca5a43995'
            '78e186482fec58e1266588c5349f911b40ac4520a16442d38099e9e8b3d0290f')

package() {
    cd "$srcdir"
    source /etc/profile.d/jre.sh

    echo "INSTALL_PATH=${pkgdir}/opt/i2p" >install.properties
    java -jar i2pinstall_${pkgver}.jar -options install.properties

    install -dm755 "$pkgdir/usr/lib/tmpfiles.d"
    install -dm700 "$pkgdir/run/i2p"
    install -dm755 "$pkgdir/usr/bin"
    install -dm755 "$pkgdir/opt/i2p"

    install -Dm755 "$srcdir/i2prouter.sh"         "$pkgdir/opt/i2p/i2prouter"
    install -Dm644 "$srcdir/i2prouter.service"    "$pkgdir/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 "$pkgdir/opt/i2p/man/eepget.1" "$pkgdir/usr/share/man/man1/eepget.1"
    install -Dm644 "$pkgdir/opt/i2p/LICENSE.txt"  "$pkgdir/usr/share/licenses/i2p/LICENSE"
    mv "$pkgdir"/opt/i2p/licenses/*               "$pkgdir/usr/share/licenses/i2p/"

    ln -s /opt/i2p/{eepget,i2prouter} "$pkgdir/usr/bin/"
    chmod 755 "$pkgdir/opt/i2p"
    chmod -x "$pkgdir"/opt/i2p/*.config

    echo 'router.updateDisabled=true' >"$pkgdir/opt/i2p/router.config"
    echo 'd /run/i2p 0700 i2p i2p'    >"$pkgdir/usr/lib/tmpfiles.d/i2prouter.conf"

    sed -i "$pkgdir"/opt/i2p/{eepget,wrapper.config} \
        -e "s:$pkgdir/opt/i2p:/opt/i2p:g" \
        -e 's:wrapper.signal.mode.usr1=.*:wrapper.signal.mode.usr1=RESTART:'
    
    sed -i "$pkgdir"/opt/i2p/clients.config \
        -e "s:clientApp.4.startOnLoad=.*:clientApp.4.startOnLoad=false:"
    
    sed -i "$pkgdir"/opt/i2p/wrapper.config \
        -e '/wrapper.java.classpath.1/a \wrapper.java.classpath.2=/usr/share/java/wrapper.jar' \
        -e '/wrapper.java.library.path.2/a \wrapper.java.library.path.3=/usr/lib/java-service-wrapper'

    rm -r "$pkgdir"/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,LICENSE.txt,runplain.sh,licenses,man,i2psvc,lib/*wrapper*}
}
