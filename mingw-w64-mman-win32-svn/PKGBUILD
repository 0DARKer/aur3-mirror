# Maintainer: Benoit Favre <benoit.favre@gmail.com>
pkgname=mingw-w64-mman-win32-svn
pkgver=r4
pkgrel=2
pkgdesc="A light implementation of the mmap functions for MinGW (mingw-w64)"
arch=('any')
url="https://code.google.com/p/mman-win32/"
license=('MIT')
makedepends=('mingw-w64-gcc')
depends=('mingw-w64-crt')
options=('!strip' '!buildflags' 'staticlibs')
source=("${pkgname}::svn+http://mman-win32.googlecode.com/svn/trunk/")
md5sums=('SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

pkgver() {
    cd "$srcdir/${pkgname}"
    local ver="$(svnversion)"
    printf "r%s" "${ver//[[:alpha:]]}"
}

build() {
    unset LDFLAGS CPPFLAGS
    cd "${srcdir}"
    for _arch in ${_architectures}; do
        cp -r ${pkgname} build-${_arch}
        pushd build-${_arch}
        chmod +x configure
        ./configure \
            --prefix=/usr/${_arch} \
            --libdir=/usr/${_arch}/lib \
            --incdir=/usr/${_arch}/include/sys \
            --cross-prefix=${_arch}-
        make
        popd
    done
}

package() {
    for _arch in ${_architectures}; do
        cd "${srcdir}/build-${_arch}"
        make DESTDIR="${pkgdir}" install
        ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    done
}
