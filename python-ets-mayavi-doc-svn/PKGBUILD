# Contributor: giniu <gginiu@gmail.com>
pkgname=python-ets-mayavi-doc-svn
pkgver=25986
pkgrel=1
pkgdesc="Documentation for Mayavi (Enthought Tool Suite)."
arch=('any')
url="http://code.enthought.com/projects/mayavi"
license=('BSD')
makedepends=('subversion' 'python2' 'python-ets-setupdocs-svn')
options=(!emptydirs)

source=('mayavi-doc-fix.patch')
md5sums=('8e3c99241ff2d7c2384419cd500cca14')

_svnmod=Mayavi
_svntrunk=https://svn.enthought.com/svn/enthought/"${_svnmod}"/trunk

build() {
  cd "$srcdir"

  if [ -d "$_svnmod"/.svn ]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_svnmod-build"
  svn export "$_svnmod" "$_svnmod-build"
  cd "$_svnmod-build"

  patch -p0 < ../mayavi-doc-fix.patch

  python2 setup.py build_docs || return 1

  install -d "${pkgdir}"/usr/share/doc/enthought
  cp -r docs/build/mayavi/html "${pkgdir}"/usr/share/doc/enthought/mayavi
  cp -r docs/build/tvtk/html   "${pkgdir}"/usr/share/doc/enthought/tvtk

  # link manuals, so mayavi2 and tvtk_doc can find them
  _target="$pkgdir`python2 -c 'from distutils.sysconfig import get_python_lib; print get_python_lib()'`"
  install -d "${_target}"/enthought/{mayavi,tvtk}
  ln -s /usr/share/doc/enthought/mayavi "${_target}"/enthought/mayavi/html
  ln -s /usr/share/doc/enthought/tvtk   "${_target}"/enthought/tvtk/html
}

