# Maintainer: Reihar <reihar@necronomicon.fr>
# Contributor: Nick Hu <nickhu00@gmail.com>
# Contributor: Fernando Carmona Varo <ferkiwi @t gmail dot com>
pkgname=cataclysm-dda-ncurses
pkgver=0.A
pkgrel=2
pkgdesc="Cataclysm: Dark Days Ahead is an actively maintained roguelike set in a post-apocalyptic world, forked from the original. (ncurses only)"
arch=('i686' 'x86_64')
url="http://www.cataclysmdda.com/"
license=('CCPL:by-sa')

depends=('gcc-libs' 'glibc' 'zlib' 'bzip2')
optdepends=('lua51')
conflicts=('cataclysm-dda' 'cataclysm-dda-git')

install='cataclysm-dda-ncurses.install'
source=('https://github.com/CleverRaven/Cataclysm-DDA/archive/0.A.tar.gz')
md5sums=('7efdf794301a1d09f8e2dbf109019cff')

build() {
  cd "$srcdir/Cataclysm-DDA-${pkgver}"
  #RELEASE=1 should normally be enabled. It optmizes the relase and treats warnings more seriously. Unfortunately a relatively recent version of gcc adds unforssen warnings, hence why it has to be disabled for this release on Arch. It appears to have been corrected on their git repository.
  #make RELEASE=1
  make
}

package() {
  cd "$srcdir/Cataclysm-DDA-${pkgver}"

  local instdir="/usr/share/cataclysm-dda"

  install -dm755 "$pkgdir/${instdir}/"
  install -Dm755 cataclysm cataclysm-launcher "$pkgdir/${instdir}/"

  install -dm775 -g games "$pkgdir/${instdir}/data/"
  cp -r data/* "$pkgdir/${instdir}/data"
 
  install -dm775 -g games "$pkgdir/var/games"
  install -dm775 -g games "$pkgdir/var/games/cataclysm-dda/save"
  ln -s "/var/games/cataclysm-dda/save" "$pkgdir/${instdir}/save"

  install -dm775 -g games "$pkgdir/var/games/cataclysm-dda/memorial"
  ln -s "/var/games/cataclysm-dda/memorial" "$pkgdir/${instdir}/memorial"

  #User default mod list needs to be writeable
  echo "{}" > $srcdir/user-default-mods.json
  install -Dm775 -g games "$srcdir/user-default-mods.json" "$pkgdir/var/games/cataclysm-dda/data/mods/user-default-mods.json"
  ln -s "/var/games/cataclysm-dda/data/mods/user-default-mods.json" "$pkgdir/${instdir}/data/mods/user-default-mods.json"

  #The doc goes in /usr/share/doc  
  install -dm755 "$pkgdir/usr/share/doc/cataclysm-dda"
  cp -r *.txt doc/* "$pkgdir/usr/share/doc/cataclysm-dda/"
  
  install -dm755  "$pkgdir/usr/bin/"
  ln -s "${instdir}/cataclysm-launcher" "$pkgdir/usr/bin/cataclysm"

}
