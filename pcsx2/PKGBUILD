#Contributor: Kessia Pinheiro <kessiapinheiro at gmail.com>
#Contributor: Stefan Lohmaier <noneuss at gmail dot com>
#Original Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
pkgname=pcsx2
pkgver=0.9.4
pkgrel=4
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
depends=('bzip2' 'gtk2' 'nvidia-cg-toolkit' 'glew' 'alsa-lib' 'sdl')
install="pcsx2.install"
source=("http://downloads.sourceforge.net/sourceforge/pcsx2/${pkgname}-${pkgver}.tar.gz"
    	'http://www.pcsx2.net/files/10778'
	    'pcsx2.sh'
        'pcsx2.desktop')
md5sums=('79628c13d356b64912b7bff5c4285f91'
         'ab4bf5cfccff23381755cc687f1fa744'
         '7e05a5d90e17f4c65892a9f7b5d9f5ab'
         '3abafdde21ba7e14328dbcb765f695c5')

build() {
    patch -p0 < ${startdir}/fix_broken_linux_compile.patch || return 1

	cd "${srcdir}/${pkgname}-${pkgver}"

	# Pcsx2 has its own flags
	unset CFLAGS
	unset CXXFLAGS

	# Put new pad plugin in place
	rm -rf plugins/pad/zeropad
	mv ../zeropad-0.2-src plugins/pad/zeropad || return 1

	# Build it all
	sh build.sh all || return 1

	# Put all files in /opt/pcsx2
	install -m0644 -d ${pkgdir}/opt/pcsx2/.pixmaps || return 1
	cp -R bin/{*,.pixmaps} ${pkgdir}/opt/pcsx2 || return 1

	# Install small script for executing pcsx2
	install -m0755 -D ${srcdir}/pcsx2.sh ${pkgdir}/usr/bin/pcsx2 || return 1

	# Change permissions so that anyone in the games group can use it
	## The different folders contain; pcsx2/bios - the PlayStation 2 BIOS files,
	## pcsx2/inis - configuration files, pcsx2/logs - log files, pcsx2/memcards -
	## emulated memory cards, pcsx2/sstates - save states, pcsx2/snap - screenshots
	chmod --recursive a+rx ${pkgdir}/opt/pcsx2 || return 1
	chgrp --recursive games ${pkgdir}/opt/pcsx2/{bios,inis,logs,memcards,sstates,snap} || return 1
	chmod --recursive g+rwx ${pkgdir}/opt/pcsx2/{bios,inis,logs,memcards,sstates,snap} || return 1

	# Install .desktop file
	install -m0644 -D ${srcdir}/pcsx2.desktop ${pkgdir}/usr/share/applications/pscx2.desktop || return 1
}
