pkgname=pcsx2
pkgver=0.9.7
pkgrel=3
pkgdesc="A PlayStation 2 emulator. (r3881, Beta version)"
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
if [ $CARCH == "x86_64" ]; then
   depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
   			'lib32-alsa-lib' 'lib32-glew' 'lib32-portaudio'
   			'lib32-sdl' 'google-sparsehash' 'lib32-libjpeg'
   			'lib32-a52dec')
   makedepends=('cmake>=2.8' 'subversion' 'gcc-multilib')
else
   depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew' 'portaudio' 
   			'alsa-lib' 'sdl' 'google-sparsehash' 'libjpeg' 'a52dec')
   makedepends=('cmake>=2.8' 'subversion')
fi
conflicts=('pcsx2-bin' 'pcsx2-svn' 'pcsx2-svn-bin') # Same destination folders
install="pcsx2.install"
source=('pcsx2.sh' 'pcsx2.xpm' 'pcsx2.desktop' 'allow64bit.patch')
md5sums=('1e494d99c38a3fa08f2f49e70ec8dfcd'
         '18cb3c5a243a27363553d3e1008210c0'
         'b055ff7c0f236f33702f4813ae4b980d'
		 '3ba263f4bad9d08d3dbf1d89c612fc0b')

# Avoid _svntrunk variable because we want the beta release (r3881)
_svnsource=http://pcsx2.googlecode.com/svn/trunk
_svnmod=pcsx2

build() {
	cd "$srcdir"

	if [ -d $_svnmod/.svn ]; then
		(cd $_svnmod && svn up -r 3881)
	else 
		svn co $_svnsource --config-dir ./ -r 3881 $_svnmod   
	fi
	rm -rf "$srcdir/$_svnmod-build"
	svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build" #unversioned copy

	msg "SVN checkout done or server timeout"
	msg "Starting make..."

	cd "$srcdir/$_svnmod-build"
	# patch CMakeLists to allow build on 64-bit systems
	patch -p0 <../allow64bit.patch

	cmake CMakeLists.txt -DCMAKE_BUILD_TYPE="Release"
   
	make
}

package() {
	cd "${srcdir}/${_svnmod}-build/bin"

	mkdir -p "${pkgdir}/opt/pcsx2" "${pkgdir}/usr/share/man/man1"
	cp -rPf * "${pkgdir}/opt/pcsx2"
	# Install Man page
   	gzip -9 "${pkgdir}/opt/pcsx2/docs/pcsx2.man"
	mv "${pkgdir}/opt/pcsx2/docs/pcsx2.man.gz" "${pkgdir}/usr/share/man/man1/pcsx2.1.gz"
	# Install runnable script, icon and desktop item
	install -Dm755 "${srcdir}/pcsx2.sh" "${pkgdir}/usr/bin/pcsx2"
	install -Dm644 "${srcdir}/pcsx2.xpm" "${pkgdir}/usr/share/pixmaps/pcsx2.xpm"
	install -Dm644 "${srcdir}/pcsx2.desktop" "${pkgdir}/usr/share/applications/pcsx2.desktop"
}
