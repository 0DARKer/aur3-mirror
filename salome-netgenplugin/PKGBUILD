# Maintainer: Michele Mocciola <mickele>

pkgname=salome-netgenplugin
pkgver=5.1.4
pkgrel=1
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation. NETGENPLUGIN Module."
url="http://www.salome-platform.org"
depends=('salome-smesh' 'python' 'qt' 'boost' 'opencascade' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'hdf5' 'med' 'vtk>=5.6' 'graphviz' 'flex' 'bison' 'sed')
makedepends=('gcc' 'automake' 'autoconf' 'libtool' 'make' 'doxygen' 'docutils' 'patch' 'coreutils')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
# salome uses only a particular version of netgen, that has never been officially released
# So it is necessary to download a full installation package from www.salome-platform.org (requires a lot of time...)
source=("http://files.opencascade.com/Salome/Salome${pkgver}/InstallWizard_${pkgver}_Debian_4.0_64bit.tar.gz" "${pkgname}.profile")
options=('libtool')

_source=InstallWizard_${pkgver}_Debian_4.0_64bit/Products/SOURCES
_installdir=/opt/salome/netgenplugin
_pythonver=2.6
_vtkver=5.6

build() {
  source /etc/profile.d/salome-kernel.sh || return 1
  source /etc/profile.d/salome-gui.sh || return 1
  source /etc/profile.d/salome-geom.sh || return 1
  source /etc/profile.d/salome-med.sh || return 1
  source /etc/profile.d/salome-smesh.sh || return 1

  msg "Untar source"
  
  cd ${srcdir}/${_source} || return 1
  bsdtar -xf netgen-4.5.tar.gz || return 1
  bsdtar -xf NETGENPLUGINsourcesV${pkgver}.tar.gz || return 1


  msg "Builds netgen-4.5"

  cd ${srcdir}/${_source}/netgen-4.5_SRC || return 1
  patch -Np1 -i ../NETGENPLUGIN_SRC_${pkgver}/src/NETGEN/netgen45ForSalome.patch || return 1
  # only for 64 bit
  if [ "$CARCH" = "x86_64" ]; then
    patch -Np1 -i ../NETGENPLUGIN_SRC_${pkgver}/src/NETGEN/netgen45lib64.patch || return 1
  fi

  # change necessary for gcc-4.5
  sed -e "s|BRepMesh_IncrementalMesh::BRepMesh_IncrementalMesh|BRepMesh_IncrementalMesh|" \
      -i libsrc/occ/occgeom.cpp
  sh makeForSalome.sh || return 1


  msg "Builds NetgenPlugin"

  cd ${srcdir}/${_source}/NETGENPLUGIN_SRC_${pkgver} || return 1

  sed -e "s|BRepMesh_IncrementalMesh::BRepMesh_IncrementalMesh|BRepMesh_IncrementalMesh|" \
      -i src/NETGENPlugin/NETGENPlugin_Mesher.cxx

  ./build_configure || return 1

  ./configure --prefix=${_installdir} \
	      --with-python-site=${_installdir}/lib/python-${_pythonver}/site-packages/salome \
	      --with-python-site-exec=${_installdir}/lib/python-${_pythonver}/site-packages/salome \
	      --with-qt=/usr \
	      --with-vtk=/usr \
	      --with-vtk-version=-${_vtkver} \
	      --with-netgen=${srcdir}/${_source}/netgen-4.5_SRC/install \
	      --disable-debug \
     	      --enable-production || return 1

  make || return 1
}

package() {
  cd ${srcdir}/${_source}/NETGENPLUGIN_SRC_${pkgver} || return 1

  make DESTDIR="${pkgdir}" install || return 1

  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/profile.d/${pkgname}.sh" || return 1
}

md5sums=('ac360176044fb18b261f36e26372f8aa'
         'a889807f3abfe5f74e1a119421a95059')
