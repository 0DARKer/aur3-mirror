# Maintainer: speps <speps at aur dot archlinux dot org>

_auth=fragglet
pkgname=lhasa
pkgver=0.0.7
pkgrel=1
pkgdesc="Free Software LHA implementation"
arch=(i686 x86_64)
url="http://fragglet.github.com/lhasa/"
license=('custom:ISC')
depends=('glibc')
makedepends=('libtool')
options=('!libtool')
source=("https://github.com/$_auth/$pkgname/tarball/v$pkgver")
md5sums=('d4f0ec07ffadf17a02a901779f99e034')

build() {
  cd "$srcdir/$_auth-$pkgname-"*
  ./autogen.sh --prefix=/usr \
               --enable-static=no
  make
}

package() {
  cd "$srcdir/$_auth-$pkgname-"*
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
