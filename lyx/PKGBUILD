# $Id: PKGBUILD 100474 2010-11-23 17:21:51Z ibiru $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=lyx
pkgver=2.0.0beta3
pkgrel=1
pkgdesc="An advanced open-source document processor."
arch=('i686' 'x86_64')
url="http://www.lyx.org"
depends=('qt' 'texlive-core' 'python2' 'perl' 'imagemagick' 'aspell' 'aiksaurus' 'boost-libs')
makedepends=('boost')
optdepends=('texlive-latex3: pdf export')
license=('GPL')
source=(ftp://ftp.lyx.org/pub/lyx/devel/lyx-2.0/beta3/$pkgname-$pkgver.tar.gz \
	lyx.desktop)
md5sums=('055ad581e38b5021459f3a02558a652e'
         'c11db315dc99254a4118827f98922623')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  find . -type f -exec sed -i 's|#!.*python|#!/usr/bin/env python2|' {} +
  sed -i 's|"python|"python2|' lib/configure.py src/support/os.cpp
  ./configure --prefix=/usr \
    --with-frontend=qt4 --without-included-boost
  make -j 8
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # install desktop entry
  install -Dm644 ${srcdir}/lyx.desktop ${pkgdir}/usr/share/applications/lyx.desktop
}
