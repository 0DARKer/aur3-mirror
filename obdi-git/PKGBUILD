# Maintainer: Ruthger Gubbels <aur@ruthger.nl>

pkgname='obdi-git'
pkgver=0.1.3
pkgrel=2
pkgdesc='Obdi is an extendable graphical user interface for running scripts on remote servers.'
arch=('x86_64')
url='https://github.com/mclarkson/obdi/'
license=('GPL3')
depends=(
  'go'
)
makedepends=('git')
backup=(
  etc/obdi/obdi.conf
  etc/obdi-worker/obdi-worker.conf
)
options=()
source=(
  'git://github.com/mclarkson/obdi.git'
  obdi.service
  obdi-worker.service
)
sha512sums=('SKIP'
            'd0b21e03e7fdc629382ea983b0b64fb6cce8ad446fbac1cbc4b5d121a2e6ef18db20e6e496827b1c4a3e4a5458299e77bf2e955fbb74cc673678c96d279d1b5a'
            '50f4e863c8f65162ad46ee4736e7066c84bda747cf68a875ff8d79e156f022a7e359a2fc8fed73f8c2cf07e460959e679b16be50fa9c40373e35c45c595add8d')

build() {
  # Include Golang binaries
  export PATH=$PATH:/usr/sbin:/usr/share/go/bin

  # Golang requirements
  export GOROOT=/usr/lib/go
  export GOPATH="$srcdir"/obdi

  # dirty github references hack
  mkdir -p "$srcdir"/obdi/src/github.com/mclarkson/obdi
  ln -s -f "$srcdir"/obdi/external "$srcdir"/obdi/src/github.com/mclarkson/obdi/external

  cd "$srcdir"/obdi/obdi
  go build -v -ldflags "-X main.VERSION $pkgver" -o obdi
  
  export GOPATH="$srcdir"/obdi
  cd ../obdi-worker
  go build -o obdi-worker
}

package() {
  # Directories
  install -dm 755 "$pkgdir"/etc/obdi "$pkgdir"/usr/share/obdi/static
  install -dm 755 "$pkgdir"/var/lib/obdi/plugin/src

  # Config
  sed -i 's/go_root = "\/usr\/local\/go"/go_root = "\/usr\/lib\/go"/' "$srcdir"/obdi/conf/obdi.conf
  install -D -m 644 "$srcdir"/obdi/conf/obdi.conf "$pkgdir"/etc/obdi/obdi.conf
  install -D -m 644 "$srcdir"/obdi/conf/obdi-worker.conf "$pkgdir"/etc/obdi-worker/obdi-worker.conf
  
  # Obdi executables
  install -D -m 755 "$srcdir"/obdi/obdi/obdi "$pkgdir"/usr/bin/obdi
  install -D -m 755 "$srcdir"/obdi/obdi-worker/obdi-worker "$pkgdir"/usr/bin/obdi-worker

  # SystemD Service
  install -Dm644 obdi.service "$pkgdir"/usr/lib/systemd/system/obdi.service
  install -Dm644 obdi-worker.service "$pkgdir"/usr/lib/systemd/system/obdi-worker.service

  # static html files
  cp "$srcdir"/obdi/static/* "$pkgdir"/usr/share/obdi/static/ -r
}
