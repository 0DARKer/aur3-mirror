# Maintainer: Zbynek Novotny <znovotny@gmail.com>
# Contributor: graysky <graysky AT archlinux dot us>
# Contributor: Dan Vratil <vratil@progdansoft.com>

pkgname=nvidia-beta-ck
pkgver=290.03
pkgrel=1
pkgdesc="NVIDIA beta drivers for linux-ck."
arch=('i686' 'x86_64')
_kernver='3.0-ck'
[ "$CARCH" = x86_64 ] && ARCH=x86_64 && _srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32 && md5sums=('90d578f828b5a4a8403dd82f8b046f09')
[ "$CARCH" = "i686" ] && ARCH=x86    && _srcname=NVIDIA-Linux-x86-${pkgver}                && md5sums=('6ad27a7e30c449018ce1dceec765d90a')
provides=("nvidia-beta-ck=${pkgver}")
url="http://www.nvidia.com/"
depends=("linux-ck>=3.0" "nvidia-utils-beta=${pkgver}")
makedepends=('linux-ck-headers')
conflicts=('nvidia-ck')
license=('custom')
install=nvidia.install
source=("ftp://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/${_srcname}.run")

build()
{
	# Extract the nvidia drivers
	cd "${srcdir}"
	if [ -d ${_srcname} ]; then
		rm -rf ${_srcname}
	fi
	sh ${_srcname}.run --extract-only
	cd ${_srcname}/kernel

	make SYSSRC=/usr/src/linux-${_kernver} module
}

package() 
{
	cd "${srcdir}/${_srcname}/kernel"

	# Install kernel module
	mkdir -p "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/"
	install -m644 nvidia.ko "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/"

	# Blacklist nouveau since 2.6.34
    # We have to store the blacklist directive in a unique conffile
    # whose name does not collide with other packages (e.g. nvidia-beta)
	mkdir -p "${pkgdir}/etc/modprobe.d/"
	echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-ck.conf"
    gzip "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
}

