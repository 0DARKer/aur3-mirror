# Maintainer: Zbynek Novotny <znovotny@gmail.com>
# Contributor: graysky <graysky AT archlinux dot us>
# Contributor: Dan Vratil <vratil@progdansoft.com>

pkgname="nvidia-beta-ck"
pkgver=290.06
pkgrel=1
pkgdesc="NVIDIA beta drivers for linux-ck."
arch=('i686' 'x86_64')
_extramods="extramodules-3.1-ck"
_kernver="$(cat /lib/modules/${_extramods}/version)"
[ "$CARCH" = x86_64 ] && ARCH=x86_64 && _srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32 && md5sums=('742bc411b475f9dab68ffd1688fd3316')
[ "$CARCH" = "i686" ] && ARCH=x86    && _srcname=NVIDIA-Linux-x86-${pkgver}                && md5sums=('e7d718a8f2bbfdd1a479e2e01db84240')
provides=("nvidia-beta-ck=${pkgver}")
url="http://www.nvidia.com/"
depends=("linux-ck>=3.1" "nvidia-utils-beta=${pkgver}")
makedepends=('linux-ck-headers')
conflicts=('nvidia-ck')
license=('custom')
install=nvidia.install
source=("ftp://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/${_srcname}.run")

build()
{
	# Extract the nvidia drivers
	cd "${srcdir}"
	if [ -d ${_srcname} ]; then
		rm -rf ${_srcname}
	fi
	sh ${_srcname}.run --extract-only
	cd ${_srcname}/kernel

	make SYSSRC=/usr/src/linux-${_kernver} module
}

package() 
{
	cd "${srcdir}/${_srcname}/kernel"

    module_path="${pkgdir}/lib/modules/${_extramods}/kernel/drivers/video"

	# Install kernel module
	mkdir -p "${module_path}"
	install -m644 nvidia.ko "${module_path}"

	# Blacklist nouveau since 2.6.34
    # We have to store the blacklist directive in a unique conffile
    # whose name does not collide with other packages (e.g. nvidia-beta)
	mkdir -p "${pkgdir}/etc/modprobe.d/"
	echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-ck.conf"
    gzip "${module_path}/nvidia.ko"
}

