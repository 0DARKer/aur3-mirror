# Maintainer: Zbynek Novotny <znovotny@gmail.com>
# Contributor: graysky <graysky AT archlinux dot us>
# Contributor: Dan Vratil <vratil@progdansoft.com>

pkgname=nvidia-beta-ck
pkgver=270.41.03
pkgrel=1
pkgdesc="NVIDIA beta drivers for kernel26-ck."
arch=('i686' 'x86_64')
_kernver='2.6.38-ck'
ARCH=x86_64 && _srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32 && md5sums=('8fa312dd8aff26299f8df1e721665960') 
[ "$CARCH" = "i686" ] && ARCH=x86 && _srcname=NVIDIA-Linux-x86-${pkgver} && md5sums=('4e3b54ee55c6cfa4437c12a8bd9a4250')
provides=("nvidia-beta-ck=${pkgver}")
url="http://www.nvidia.com/"
depends=("kernel26-ck>=2.6.38" "kernel26-ck<2.6.39" "nvidia-utils-beta=${pkgver}")
makedepends=('kernel26-ck-headers')
conflicts=('nvidia-ck')
license=('custom')
install=nvidia.install
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/${_srcname}.run")

build()
{
  # Extract the nvidia drivers
  cd "${srcdir}"
  if [ -d ${_srcname} ]; then
    rm -rf ${_srcname}
  fi
  sh ${_srcname}.run --extract-only
  cd ${_srcname}/kernel
  make SYSSRC=/usr/src/linux-${_kernver} module
}

package() {
  cd "${srcdir}/${_srcname}/kernel"

  # Install kernel module
  mkdir -p "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/"
  install -m644 nvidia.ko "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/"
}
