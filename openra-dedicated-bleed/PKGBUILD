# Maintainer: firstvirus92 <dev[at]firstvirus92[dot]de>

pkgname=openra-dedicated-bleed
pkgver=r14792.20fdd1c
pkgrel=1
pkgdesc="OpenRA bleed with minimal dependencies for servers"
arch=("any")
url="http://openra.net"
license=("GPL3")
depends=("mono" "lua51")
makedepends=("git")
conflicts=("openra" "openra-bleed")
source=("$pkgname-src::git+https://github.com/OpenRA/OpenRA.git#branch=bleed" "openra-dedicated")
sha512sums=("SKIP"
    "7034e012586aaac2801c704fe7b077ccf8dd16854f5ecad76970842f9e20238ec0fa13b585a62201a90d9b2872c58b8ad96269ed510bcf3b2c64c787555d4aba")

pkgver() {
    cd "$srcdir/$pkgname-src"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$pkgname-src"
    ./configure
    make dependencies
    make all
}

package() {
    cd "$srcdir/$pkgname-src"
    make prefix=/usr DESTDIR="$pkgdir" install-all
    install -m755 -d "$pkgdir"/usr/bin
    install -Dm755 $srcdir/openra-dedicated $pkgdir/usr/bin/openra-dedicated
}
