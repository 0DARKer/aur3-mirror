# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=pcmanfm-qt
pkgname=$_pkgname-git
pkgver=0.9.0
pkgrel=1
pkgdesc="The LXQt file manager, Qt port of PCManFM"
arch=("i686" "x86_64" "armv6h")
url="http://lxqt.org"
license=("GPL2")
depends=("liblxqt-git" "libfm" "lxmenu-data" "qt5-x11extras" "qt5-tools")
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
install="$_pkgname.install"
source=("git+https://github.com/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	mkdir -p build
	cd build

	cmake "$srcdir/$_pkgname" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
