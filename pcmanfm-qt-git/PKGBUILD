# Maintainer: Jekyll Wu <adaptee [@] gmail [dot] com>

pkgname=pcmanfm-qt-git
pkgver=20130326
pkgrel=1
pkgdesc="The Qt porting of pcmanfm. It is in its alpha stage and not ready for daily use ."
arch=('i686' 'x86_64')
url="http://pcmanfm.sourceforge.net/"
license=('GPL')
provides=('pcmanfm-qt')
conflicts=('pcmanfm-qt')
depends=('libfm-git' 'qt4')
makedepends=('git' 'cmake')
optdepends=('gvfs: mounting of local and remote drives'
            'gnome-menus: applications menu and "Open with..." dialog')
install=pcmanfm-qt.install

_gitroot=git://pcmanfm.git.sourceforge.net/gitroot/pcmanfm/pcmanfm-qt
_gitname=pcmanfm-qt

build() {
  cd "$srcdir"

  msg "Connecting to $_gitroot..."
  if [[ -d $_gitname ]]; then
    cd "${srcdir}/${_gitname}" && git pull origin
    msg "Local files updated"
  else
    git clone $_gitroot 
    msg "Git checkout done"
  fi
  
  [[ -d "${srcdir}/${_gitname}-build" ]] && rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  
  cd "${srcdir}/${_gitname}-build"

  msg "Starting make..."

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DQT_QMAKE_EXECUTABLE=qmake-qt4 .
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
}
