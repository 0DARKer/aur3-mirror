# Maintainer: Igor Babuschkin ibabuschkinatgmaildotcom

_hkgname=gtk-traymanager
pkgname=haskell-$_hkgname
pkgver=0.1.3
pkgrel=1
pkgdesc="A wrapper around the eggtraymanager library for Linux system trays"
arch=(i386 x86_64)
url="http://hackage.haskell.org/package/$_hkgname"
license=('LGPL2')
depends=('ghc'
         'haskell-gtk'
         'haskell-glib')
provides=(haskell-gtk-traymanager)
conflicts=()
replaces=()
backup=()
options=()
install=$pkgname.install
source=(http://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz)
md5sums=('aace61ff19a97c83465f9bf5cad1fb43')

build() {
	cd "$srcdir/$_hkgname-$pkgver"

	runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
		--prefix=/usr --docdir="/usr/share/doc/$pkgname" \
		--libsubdir=\$compiler/site-local/\$pkgid
	runhaskell Setup build
	runhaskell Setup haddock
	runhaskell Setup register   --gen-script
	runhaskell Setup unregister --gen-script
	sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
	cd "$srcdir/$_hkgname-$pkgver"

	install -D -m744 register.sh   "$pkgdir/usr/share/haskell/$pkgname/register.sh"
	install	   -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
	install -d -m755 "$pkgdir/usr/share/doc/ghc/html/libraries"
	ln -s "/usr/share/doc/$pkgname/html" "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
	runhaskell Setup copy --destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
