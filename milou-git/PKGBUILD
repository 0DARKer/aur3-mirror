pkgname=milou-git
pkgver=186.4bf1538
pkgrel=2
pkgdesc="A dedicated search application built on top of Nepomuk"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/base/milou"
license=('GPL')
depends=('kdebase-workspace' 'baloo-git')
makedepends=('automoc4' 'cmake' 'git')
provides=('milou')
conflicts=('milou')
source=('git://anongit.kde.org/milou')
md5sums=('SKIP')

pkgver() {
  cd milou
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../milou \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix)
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
