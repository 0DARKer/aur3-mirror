# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=milou-git
pkgver=r267.eb14c9e
pkgrel=1
pkgdesc="A dedicated search application built on top of Baloo"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdereview/milou'
license=('GPL')
depends=('kdebase-workspace' 'kdebase-runtime>=4.12.80' 'baloo')
makedepends=('automoc4' 'cmake' 'git' 'boost')
provides=('milou')
conflicts=('milou')
source=('git://anongit.kde.org/milou')
md5sums=('SKIP')

pkgver() {
  cd milou
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../milou \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix)
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
