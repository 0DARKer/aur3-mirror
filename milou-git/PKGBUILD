pkgname=milou-git
pkgver=186.4bf1538
pkgrel=1
pkgdesc="A dedicated search application built on top of Nepomuk"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/base/milou"
license=('GPL')
depends=('kdebase-workspace' 'nepomuk-core')
makedepends=('automoc4' 'cmake' 'git')
provides=('milou')
conflicts=('milou')
source=('git://anongit.kde.org/milou')
md5sums=('SKIP')

pkgver() {
  cd milou
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  mkdir -p build
  cd build

  cmake ../milou \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix)
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
