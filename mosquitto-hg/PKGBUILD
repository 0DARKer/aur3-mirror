# Maintainer: Thomas Krug <phragment@lavabit.com>
pkgname=mosquitto-hg
_hgname=mosquitto
pkgver=3070.db5147ce80f1
pkgrel=1
pkgdesc="An Open Source MQTT v3.1 Broker"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="http://mosquitto.org/"
depends=('openssl')
makedepends=('mercurial' 'python' 'docbook-xsl')
conflicts=('mosquitto')
provides=('mosquitto')
license=('BSD')
install=$pkgname.install
source=("$_hgname::hg+https://bitbucket.org/oojah/mosquitto#branch=1.2"
        'mosquitto.service'
        'docbook.patch')
md5sums=('SKIP'
         'c3375f858e72e7526629c82f919fc096'
         '8e1c14e99d7eba210b874e80b5153f0d')

pkgver() {
  cd "$srcdir/$_hgname"

  echo $(hg identify -n).$(hg identify -i)
}

build() {
  cd "$srcdir/$_hgname"

  patch -p1 < ../docbook.patch

  make
}

package() {
  cd "$srcdir/$_hgname"

  make prefix=/usr DESTDIR="$pkgdir/" install

  # systemd service file
  install -Dm644 "$srcdir/mosquitto.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  echo 'pid_file /run/mosquitto.pid' >> "$pkgdir/etc/mosquitto/mosquitto.conf"

  # license files
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 LICENSE-3rd-party.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE-3rd-party"
}

# vim:set ts=2 sw=2 et:
