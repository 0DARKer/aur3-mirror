# Maintainer: Jérémie Detrey <Jeremie.Detrey@loria.fr>
pkgname=cado-nfs
pkgver=2.0
pkgrel=1
pkgdesc="Implementation of the Number Field Sieve (NFS) algorithm for factoring integers"
arch=('i686' 'x86_64')
url="http://cado-nfs.gforge.inria.fr/"
license=('LGPL2')
depends=('gmp' 'python' 'sqlite')
makedepends=('git' 'cmake' 'inetutils')
source=("https://gforge.inria.fr/frs/download.php/33131/${pkgname}-${pkgver}.tar.gz")
md5sums=('d8539f62ff52dcdbc67492585ccdfe89')

build() {
  cd "${pkgname}-${pkgver}"
  make PREFIX="/usr/libexec/cado-nfs"
  sed -i -e 's/^cado_source_dir=.*$/cado_source_dir="@CADO_NFS_SOURCE_DIR@"/;
             s/^cado_build_dir=.*$/cado_build_dir="@CADO_NFS_BINARY_DIR@"/' \
    "build/`hostname`/factor.sh"
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX="/usr/libexec/cado-nfs" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
