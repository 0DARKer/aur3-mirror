# Maintainer: Thibault Suzanne <thi [DOT] suzanne [AT] gmail [DOT] com>
# Category: science

pkgname='apron-svn-ocaml'
pkgver=0.9.11.r1053
pkgrel=1
pkgdesc='The APRON numerical abstract domain library, with OCaml interface'
arch=('any')
url='http://apron.cri.ensmp.fr/library/'
license=('LGPL')
provides=('apron-svn')
conflicts=('apron-svn')
depends=('gmp>=5' 'mpfr>=3' 'mlgmpidl-svn')
makedepends=('svn' 'sed')
source=("$pkgname::svn://scm.gforge.inria.fr/svnroot/apron/apron/trunk")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    local rel="$(sed -n 's|PKGNAME *= *apron-\([^ ]\+\) *$|\1|p' Makefile)"
    local ver="$(svnversion)"
    printf "%s.r%s" "$rel" "${ver//[[:alpha:]]}"
}

prepare() {
    cd "$srcdir/$pkgname"
    sed  \
      -e 's|^OCAMLFIND *=.*$|OCAMLFIND=|' \
      -e 's|^APRON_PREFIX *=.*$|APRON_PREFIX=/usr|' \
      -e 's|^MLGMPIDL_PREFIX *=.*$|MLGMPIDL_PREFIX=/usr|' \
        Makefile.config.model > Makefile.config
}

build() {
    cd "$srcdir/$pkgname"
    make rebuild
    make
}

package() {
    cd "$srcdir/$pkgname"
    make APRON_PREFIX="$pkgdir"/usr install
}
