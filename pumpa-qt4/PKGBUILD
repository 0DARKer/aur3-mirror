# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=pumpa-qt4
pkgver=0.8.3
pkgrel=1
pkgdesc="A simple pump.io client written in C++ and Qt4"
arch=('i686' 'x86_64')
url="http://gitorious.org/pumpa"
license=('GPL3')
depends=('qjson' 'aspell')
provides=('pumpa')
install="pumpa.install"
source=("$url/pumpa/archive/v$pkgver.tar.gz")
md5sums=('c6e6aebb3e4afd62f6b51ed0de430411')

prepare() {
  cd pumpa-pumpa

  # icon path fix and qt4 reference
  sed -i 's|/.*/||;s|pumpa|&-qt4|;s|Pumpa|& (Qt4)|' pumpa.desktop
}

build() {
  cd pumpa-pumpa
  qmake-qt4
  make
}

package() {
  cd pumpa-pumpa

  # bin
  install -Dm755 pumpa \
    "$pkgdir/usr/bin/$pkgname"

  # desktop file
  install -Dm644 pumpa.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 images/pumpa.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
