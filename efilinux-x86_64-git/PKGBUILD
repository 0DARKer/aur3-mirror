# Maintainer : Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

_UEFI_ARCH='x86_64'
_GNU_EFI_LIB_DIR='/usr/lib'

_actualname="efilinux"
_pkgname="${_actualname}-${_UEFI_ARCH}"
pkgname="${_pkgname}-git"

pkgver=20120102
pkgrel=1
pkgdesc="Reference Implementation of a Linux Bootloader for ${_UEFI_ARCH} UEFI Firmwares"
arch=('any')
url="https://github.com/mfleming/efilinux"
license=('GPL')
makedepends=('git' 'gnu-efi')
depends=('dosfstools' 'efibootmgr')
provides=("${_actualname}" "${_pkgname}")
conflicts=("${_actualname}" "${_pkgname}")
options=(!strip docs zipman purge !libtool emptydirs)
install="${_actualname}.install"
backup=()

# _gitroot="git://git.kernel.org/pub/scm/boot/${_actualname}/${_actualname}.git"
_gitroot="git://github.com/mfleming/efilinux.git"
_gitname="${_actualname}"
_gitbranch="master"

_update_git() {
	
	cd "${srcdir}/"
	
	msg "Connecting to GIT server...."
	
	if [ -d "${srcdir}/${_gitname}/" ]
	then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout "${_gitbranch}"
		git merge "remotes/origin/${_gitbranch}"
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
		cd "${srcdir}/${_gitname}/"
		git checkout "${_gitbranch}"
		msg "GIT checkout done or server timeout"
	fi
	
	echo
	
}

build() {
	
	if [ "${CARCH}" == 'x86_64' ]
	then
		_update_git
		echo
	elif [ "${_UEFI_ARCH}" == 'x86_64' ]
	then
		echo "This package can be built only in a x86_64 system."
		exit 1
	fi
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build"
	
	## Change LIBDIR from /usr/lib64/ to /usr/lib/ in Makefile
	sed 's|/usr/lib64|/usr/lib|g' -i "${srcdir}/${_gitname}_build/Makefile"
	
	CFLAGS="" make ARCH="${_UEFI_ARCH}" CRT0="${_GNU_EFI_LIB_DIR}/crt0-efi-${_UEFI_ARCH}.o" LDSCRIPT="${_GNU_EFI_LIB_DIR}/elf_${_UEFI_ARCH}_efi.lds"
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build"
	
	install -D -m644 "${srcdir}/${_gitname}_build/${_actualname}.efi" "${pkgdir}/boot/efi/efi/${_actualname}/${_actualname}.efi"
	
}
