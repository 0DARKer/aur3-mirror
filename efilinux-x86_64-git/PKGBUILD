# Maintainer: Keshav P R <skodabenz aatt rocketmail ccoomm>

_actualname="efilinux"
_pkgname="${_actualname}-x86_64"
pkgname="${_pkgname}-git"

pkgver=20110820
pkgrel=1
pkgdesc="Reference Implementation of a Linux Bootloader for x86_64 UEFI Firmwares"
arch=('any')
url="http://git.kernel.org/?p=boot/efilinux/efilinux.git;a=summary"
license=('GPL')
makedepends=('git' 'gnu-efi')
depends=('dosfstools' 'efibootmgr')
provides=("${_actualname}")
conflicts=("${_actualname}")
options=(!strip docs zipman purge !libtool emptydirs)
install="${_actualname}.install"
backup=()

source=('archlinux_efilinux_makefile.patch')

sha256sums=('139a4cc091c4ffebc2c5f9ef813e85a707c72785238cf763f6dba35804e891f3')

_gitroot="git://git.kernel.org/pub/scm/boot/${_actualname}/${_actualname}.git"
_gitname="${_actualname}"
_gitbranch="next"

_update_git() {
	
	cd "${srcdir}/"
	
	msg "Connecting to GIT server...."
	
	if [ -d "${srcdir}/${_gitname}/" ]
	then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout ${_gitbranch}
		git merge remotes/origin/${_gitbranch}
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
		cd "${srcdir}/${_gitname}/"
		git checkout ${_gitbranch}
		msg "GIT checkout done or server timeout"
	fi
	
	echo
	
}


build() {
	
	if [ "${CARCH}" == 'x86_64' ]
	then
		_update_git
	else
		echo "This package can be built only in a x86_64 system."
		exit 1
	fi
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build"
	
	## Apply archlinux specific Makefile patch
	patch -Np1 -i "${srcdir}/archlinux_efilinux_makefile.patch"
	
	CFLAGS="" make
	echo
	
}


package() {
	
	cd "${srcdir}/${_gitname}_build"
	
	install -D -m644 "${srcdir}/${_gitname}_build/${_actualname}.efi" "${pkgdir}/boot/efi/efi/${_actualname}/${_actualname}.efi"
	
}
