# Contributor: Phil Miller <philm[at]chakra-project[dot]org
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>

pkgname=monkeystudio
pkgver=1.8.4.0
pkgrel=1
_svn=svn3727
pkgdesc="Free crossplatform C++ IDE in Qt4"
arch=('i686' 'x86_64')
url="http://monkeystudio.org"
license=('GPL')
depends=('qscintilla' 'python')
conflicts=('monkeystudio2' 'kdemod-monkeystudio2' 'kdemod-monkeystudio2-svn' 'monkeystudio2-svn')
source=("http://monkeystudio.googlecode.com/files/mks_${pkgver}-${_svn}-src.tar.gz")
md5sums=('b0969a05a70a79753145bbf333b279ce')

build() {
  cd "${srcdir}/mks_${pkgver}-${_svn}-src"
  qmake prefix=/usr

  # Compatibility fix with QScintilla 2.5 and up
  sed -i '697,698 d' monkey/src/pMonkeyStudio.cpp
  # Add QtDesigner compatibility headers for Qt 4.7 from monkeystudio svn
  cd plugins/child/QtDesigner/src/3rdparty/designer
  curl 'http://websvn.tuxfamily.org/dl.php?repname=monkeystudio%2Fmks&path=%2Fv2%2Ftrunk%2Fplugins%2Fchild%2FQtDesigner%2Fsrc%2F3rdparty%2Fdesigner%2F4.7%2F&rev=4088&peg=4088&isdir=1' > 4.7.tar.gz
  tar xf 4.7.tar.gz && rm 4.7.tar.gz
  mv 4.7{*,}
  cd -

  make
}

check() {
  cd "${srcdir}/mks_${pkgver}-${_svn}-src"
  make -k check
}

package() {
  cd "${srcdir}/mks_${pkgver}-${_svn}-src"
  make INSTALL_ROOT="${pkgdir}/" install
  chmod +x "${pkgdir}/usr/bin/monkeystudio"
}

# vim:set ts=2 sw=2 et:
