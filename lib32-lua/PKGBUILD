# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=lib32-lua 
pkgver=5.1.4
pkgrel=4
pkgdesc="A powerful light-weight programming language designed for extending applications" 
arch=('x86_64')
url="http://www.lua.org/"
depends=('lib32-glibc')
makedepends=('lib32-readline')
license=('MIT')
options=('!makeflags' '!emptydirs')
source=(http://www.lua.org/ftp/lua-${pkgver}.tar.gz
        lua-arch.patch)
md5sums=('d0870f2de55d59c1c8419f36e8fac150'
         '6c5953f63904bf20a0183cdab05b80de')

build() { 
  cd "${srcdir}/lua-${pkgver}"
  patch -p1 -i "${srcdir}/lua-arch.patch"
  CFLAGS="$CFLAGS -O2 -m32" LDFLAGS="$LDFLAGS -O2 -m32"
  make INSTALL_DATA="cp -d" TO_LIB="liblua.a liblua.so liblua.so.5.1" LUA_SO=liblua.so \
    INSTALL_TOP="${pkgdir}/usr" CFLAGS="$CFLAGS" MYLDFLAGS="$LDFLAGS" linux
}

package() {
  cd "${srcdir}/lua-${pkgver}"
  make INSTALL_DATA="cp -d" TO_LIB="liblua.a liblua.so liblua.so.5.1" LUA_SO=liblua.so \
    INSTALL_TOP="${pkgdir}/usr" install
  rm -rf ${pkgdir}/usr/{bin,include,man}
  mv ${pkgdir}/usr/lib ${pkgdir}/usr/lib32
}
