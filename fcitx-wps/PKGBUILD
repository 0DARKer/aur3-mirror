# $Id: PKGBUILD 85343 2013-03-01 10:30:04Z andrea $
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

_pkgname=fcitx
pkgname=fcitx-wps
pkgdesc='Fcitx QT4 IM Module for WPS'
pkgver=4.2.7
pkgrel=1
depends=('wps-office' "fcitx>=$pkgver")
groups=('fcitx-im')
arch=('i686' 'x86_64')
url="http://code.google.com/p/fcitx/"
license=('GPL')
makedepends=('cmake' 'lib32-qt4')
source=(http://fcitx.googlecode.com/files/${_pkgname}-${pkgver}_dict.tar.xz
	ftp://ftp.qt-project.org/qt/source/qt-everywhere-opensource-src-4.7.4.tar.gz
  fcitx-4.2.7-compat-qt474.patch
  FindQt4.cmake
  qconfig.h)

build() {
  cd "$srcdir"

  mkdir -p QtCore
  cp qconfig.h QtCore

  export CC="gcc -m32 -I$srcdir -I$srcdir/QtCore"
  export CXX="g++ -m32 -I$srcdir -I$srcdir/QtCore"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ln -s "$srcdir/FindQt4.cmake" "$srcdir/$_pkgname-$pkgver/cmake/"
  ln -s "/usr/share/cmake-2.8/Modules/CheckCXXSymbolExists.cmake" "$srcdir/$_pkgname-$pkgver/cmake/"
  ln -s "/usr/share/cmake-2.8/Modules/MacroAddFileDependencies.cmake" "$srcdir/$_pkgname-$pkgver/cmake/"
  ln -s "/usr/share/cmake-2.8/Modules/FindPackageHandleStandardArgs.cmake" "$srcdir/$_pkgname-$pkgver/cmake/"
  ln -s "/usr/share/cmake-2.8/Modules/Qt4ConfigDependentSettings.cmake" "$srcdir/$_pkgname-$pkgver/cmake/"
  ln -s "/usr/share/cmake-2.8/Modules/Qt4Macros.cmake" "$srcdir/$_pkgname-$pkgver/cmake/"

  (cd ${_pkgname}-${pkgver} && patch -p1 -i "$srcdir/fcitx-4.2.7-compat-qt474.patch")

  mkdir -p build
  cd build
  QTDIR=/opt/kingsoft/wps-office/office6 cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFORCE_OPENCC=Off \
    -DFORCE_PRESAGE=On \
    -DFORCE_ENCHANT=Off \
    -DENABLE_TEST=Off \
    -DENABLE_GTK2_IM_MODULE=Off \
    -DENABLE_GTK3_IM_MODULE=Off \
    -DENABLE_QT_IM_MODULE=ON \
    -DWPS_QT_INSTALL_PLUGINS=/opt/kingsoft/wps-office/office6/qt/plugins \
    -DWPS_QTVERSION=4.7.4 \
    -DWPS_QMAKE_MKSPECS="$srcdir/qt-everywhere-opensource-src-4.7.4/mkspecs" \
    -DWPS_QT_INSTALL_LIBS=/opt/kingsoft/wps-office/office6 \
    -DWPS_QT_INSTALL_BINS=/usr/lib/qt4/bin
    #-DWPS_QT_INSTALL_IMPORTS=

  cd "${srcdir}/build/src/frontend/qt"
  make
}

package() {
  cd "${srcdir}/build/src/frontend/qt"
  make DESTDIR="${pkgdir}" install
}

md5sums=('6e291717c24615b9dc9bfaa2949af3a7'
         '9831cf1dfa8d0689a06c2c54c5c65aaf'
         '7b3f370fb0838f5e2a65be4b8a975e5e'
         'f1ebde541542e36eea01f8ebacb8cdc4'
         '89eaf62c5271cd6c268b926bb752a744')
