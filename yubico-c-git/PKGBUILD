# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-c-git
pkgver=1.10.1.gcc67082
pkgrel=1
pkgdesc="Yubico yubikey C library"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubico-c"
license=('BSD')
depends=('curl')
replaces=('libyubikey')
provides=('libyubikey' 'yubico-c')
conflicts=('libyubikey' 'yubico-c')
makedepends=('git')
install=yubico-c.install
source=('git://github.com/Yubico/yubico-c.git'
	'70-yubikey.rules')

pkgver() {
	cd yubico-c/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd yubico-c/

	autoreconf --install
	./configure --prefix=/usr
	make
}

check() {
	cd yubico-c/
	
	make check
}

package() {
	cd yubico-c/

	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/yubico-c/COPYING"
	install -D -m644 README "${pkgdir}/usr/share/doc/yubico-c/README"
	install -D -m644 "${srcdir}/70-yubikey.rules" "${pkgdir}/usr/lib/udev/rules.d/70-yubikey.rules"
	make DESTDIR="${pkgdir}/" install
} 

sha256sums=('SKIP'
            '5ae001cd6356115123a454db8dcdb1392aa5167460b52ee6e68f3509b4c3aa86')
