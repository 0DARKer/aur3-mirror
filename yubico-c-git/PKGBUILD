# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Hugo Chargois <hugo DOT chargois AT free DOT fr>

pkgname=yubico-c-git
pkgver=20120608
pkgrel=1
pkgdesc="Yubico yubikey C library"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubico-c"
license=('BSD')
depends=('curl')
replaces=('libyubikey')
provides=('libyubikey')
conflicts=('libyubikey')
makedepends=('git')
source=('automake.patch')

_gitroot="git://github.com/Yubico/yubico-c.git"
_gitname="yubico-c"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # fix automake 1.12
  patch -Np1 < ${srcdir}/automake.patch
 
  autoreconf --install
  ./configure --prefix=/usr
  make
  make check
}

package() {
  cd "${srcdir}/${_gitname}-build"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  make DESTDIR="${pkgdir}/" install
} 
sha256sums=('8158d780ef09d1f03b481cc050c332759c9dbce68a216b58090e6cb3d3284191')
