# Contributor: noonov <noonov@gmail.com>
# Contributor: wtchappell <wtchappell@gmail.com>

pkgname=awesome-git
pkgver=20121230
pkgrel=1
pkgdesc="A highly configurable, next generation framework window manager for X"
arch=('i686' 'x86_64')
url="http://awesome.naquadah.org/"
license=('GPL2')
depends=(
  'startup-notification' 'libxdg-basedir' 'dbus' 'lua'
  'cairo>=1.12.2' 'pango' 'gdk-pixbuf2' 'lgi'
  'xcb-util'{,-image,-keysyms,-wm}'>=0.3.8' 'libxcursor' 'xorg-xmessage')
makedepends=('git' 'cmake' 'asciidoc' 'xmlto' 'doxygen' 'imagemagick')
optdepends=(
  'rlwrap: readline support for awesome-client'
  'feh: image background setter for awsetbg'
  'dex: autostart your desktop files'
  'vicious-git: widgets for the Awesome window manager')
provides=('awesome' 'notification-daemon')
conflicts=('awesome')
options=('docs')
install=${pkgname}.install
source=(awesome.desktop)
md5sums=('2763cab6a20d4b0f2676329d57ed3a45')

_gitroot="http://git.naquadah.org/git/awesome.git"
# _gitroot="git://git.naquadah.org/awesome.git"
_gitname="awesome"

build() {
  cd ${srcdir}

  msg "Connecting to GIT server..."
  if [[ -d ${_gitname} ]]; then
    (cd ${_gitname} && git pull origin)
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${_gitname}-build
  git clone ${_gitname} ${_gitname}-build

  cd ${srcdir}/${_gitname}-build

  export AWESOME_IGNORE_LGI=1
  make CMAKE_ARGS="-DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLUA_LIBRARIES=/usr/lib/liblua.so"
}

package() {
  cd ${srcdir}/${_gitname}-build

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/awesome.desktop \
    ${pkgdir}/usr/share/xsessions/awesome.desktop
  install -D -m644 ${srcdir}/awesome.desktop \
    ${pkgdir}/usr/share/apps/ksmserver/windowmanagers/awesome.desktop
}
