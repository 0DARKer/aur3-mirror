# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: laloch <laloch@atlas.cz>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-text-ui-git
_gitname="ktp-text-ui"
pkgver=0.6.1.60.gb69d15a
pkgrel=1
epoch=1
pkgdesc="KDE Telepathy text chat handler"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/network/telepathy/ktp-text-ui"
license=('GPL')
depends=('telepathy-kde-common-internals-git')
makedepends=('cmake' 'automoc4' 'git')
conflicts=('telepathy-kde-text-ui'
           'telepathy-chat-handler-git'
           'telepathy-kde-chat-handler-git')
source=(git://anongit.kde.org/ktp-text-ui.git)
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g;s|v||'
}

build() {
  cd ${srcdir}

  msg "Starting make..."
  
  rm -rf ${srcdir}/build && mkdir ${srcdir}/build
  cd ${srcdir}/build

  cmake ../$_gitname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DQT_MOC_EXECUTABLE=/usr/bin/moc-qt4 \
    -DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
    -DQT_UIC_EXECUTABLE=/usr/bin/uic-qt4 \
    -DCMAKE_POSITION_INDEPENDENT_CODE=on

  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}

