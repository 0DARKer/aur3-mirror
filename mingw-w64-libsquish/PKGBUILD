pkgname=mingw-w64-libsquish
pkgver=1.11
pkgrel=1
pkgdesc="DXT compression library (mingw-w64)"
arch=(any)
url="http://code.google.com/p/libsquish"
license=("MIT")
makedepends=(mingw-w64-gcc dos2unix)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("http://libsquish.googlecode.com/files/squish-$pkgver.zip"
"config")
md5sums=('150ba1117d2c1678de12650357787994'
         'c385a3df92e963beaf31d80a60631c54')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/squish-$pkgver"
  find . -type f -exec dos2unix {} \;
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cp -r "${srcdir}/squish-${pkgver}/"* "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    rm config
    cp "$srcdir/config" .
    sed -i "s,@DESTDIR@,$pkgdir/usr/${_arch}," config
    AR=${_arch}-ar CXX=${_arch}-g++ make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    mkdir -p "$pkgdir/usr/${_arch}/"{include,lib}
    make install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}