# $Id: PKGBUILD 194428 2013-09-16 10:38:02Z foutrelis $
# Maintainer:  Silvio Knizek <killermoehre@gmx.net>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>

_pkgname=xfce4-power-manager
pkgname=${_pkgname}-devel
pkgver=1.3.2
pkgrel=1
pkgdesc="Power manager for Xfce desktop"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('upower' 'libnotify' 'hicolor-icon-theme' 'libxrandr')
makedepends=('pkgconfig' 'intltool' 'networkmanager' 'xfce4-panel' 'lxpanel' 'polkit')
optdepends=(
    'xfce4-panel:       for Xfce panel plugin support'
    'lxpanel:           for LXDE panel plugin support'
    'polkit:            for elevating rights to suspend/hibernate, etc'
    'networkmanager:    so the network manager knows about suspend/hibernate'
    )
provides=("${_pkgname}=${pkgver}")
replaces=("${_pkgname}")
conflicts=("${_pkgname}")
options=('!libtool')
install=${_pkgname}.install
source=(http://archive.xfce.org/src/xfce/${_pkgname}/1.3/${_pkgname}-$pkgver.tar.bz2)
sha1sums=('5ed0e5ec8d6aab70a693aec03fdb7a00af1f31eb')
md5sums=('1855ccbfb0592cb94d71ebfae458d6b4')

build() {
  cd "$srcdir/${_pkgname}-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --enable-network-manager \
    --enable-polkit \
    --enable-panel-plugins \
    --enable-xfce4panel \
    --disable-debug
  make
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
