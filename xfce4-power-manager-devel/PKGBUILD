# $Id: PKGBUILD 194428 2013-09-16 10:38:02Z foutrelis $
# Maintainer:  Silvio Knizek <killermoehre@gmx.net>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>

_pkgname=xfce4-power-manager
pkgname=${_pkgname}-devel
pkgver=1.3.0
pkgrel=3
pkgdesc="Power manager for Xfce desktop"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('xfce4-panel' 'upower' 'udisks' 'libnotify' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool' 'networkmanager')
provides=("${_pkgname}=${pkgver}")
replaces=("${_pkgname}")
conflicts=("${_pkgname}")
options=('!libtool')
install=${_pkgname}.install
source=(http://archive.xfce.org/src/xfce/${_pkgname}/1.3/${_pkgname}-$pkgver.tar.bz2)
sha1sums=('b50260c890820220032812224b262637aeac271e')
md5sums=('c23a0f0919474f74f261b25baf16d34a')

build() {
  cd "$srcdir/${_pkgname}-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --enable-network-manager \
    --enable-polkit \
    --enable-dpms \
    --enable-panel-plugins \
    --disable-debug
  make
}

package() {
  cd "$srcdir/${_pkgname}-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
