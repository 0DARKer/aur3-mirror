# Maintainer: Daemon Lee Schmidt <DaemonLeeSchmidt@gmail.com>
# Contributer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=lib32-pciutils
pkgver=3.3.1
pkgrel=1
pkgdesc="PCI bus configuration space access library (32-bit)"
arch=('x86_64')
url="http://mj.ucw.cz/sw/pciutils/"
license=('GPL2')
depends=('lib32-systemd' 'pciutils' 'lib32-kmod')
makedepends=('gcc-multilib')
source=(#ftp://ftp.kernel.org/pub/software/utils/pciutils/pciutils-${pkgver}.tar.bz2
       ftp://atrey.karlin.mff.cuni.cz/pub/linux/pci/pciutils-${pkgver}.tar.gz)
sha512sums=('4f8b0e62cfd0ffad1674be13c95f5581bc0037ed9843f9866b13eea6fa2703bb39bb878e96df805972f4e9bac36fc67a0a313c463f0764218211f1276cef36b2')

build() {
  cd "${srcdir}/pciutils-${pkgver}"

  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  make CC="gcc -m32" OPT="${CFLAGS} -fPIC -DPIC" ZLIB=no SHARED=no PREFIX=/usr SHAREDIR=/usr/share/hwdata LIBDIR=/usr/lib32 MANDIR=/usr/share/man all
  cp lib/libpci.a "${srcdir}/"
  make clean
  make CC="gcc -m32" OPT="${CFLAGS}" ZLIB=no SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man LIBDIR=/usr/lib32 all
}

package() {
  cd "${srcdir}/pciutils-${pkgver}"

  make CC="gcc -m32" SHARED=yes PREFIX=/usr SHAREDIR=/usr/share/hwdata MANDIR=/usr/share/man DESTDIR="${pkgdir}" LIBDIR=/usr/lib32 install install-lib
  install -m644 "${srcdir}/libpci.a" "${pkgdir}/usr/lib32/"

  # this is now supplied by the hwids package
  rm -rf $pkgdir/usr/{sbin,share,include}
}
