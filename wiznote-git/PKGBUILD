# Maintainer: limser <echo bGltc2VyQHFxLmNvbQo=|base64 -d>

pkgname=wiznote-git
_gitname="WizQTClient"
pkgver=0.v1.4.11.2.g3f5b6e4
pkgrel=1
pkgdesc="Opensource Cross platform cloud note-taking application."
arch=('i686' 'x86_64')
url="https://github.com/WizTeam/WizQTClient"
license=('GPLv3')
depends=('qtwebkit')
makedepends=('git' 'cmake' 'crypto++' 'clucene')
provides=('wiznote')
source=(git://github.com/WizTeam/${_gitname}.git)
md5sums=(SKIP)

pkgver() {
  cd "$srcdir/$_gitname"
  echo -n "0."
  git describe --always | sed 's|-|.|g'
}

build() {
    cd "${srcdir}/$_gitname"
    msg2 "Fix an error" ##${KWSYS_NAMESPACE} ${CMAKE_DL_LIBS}
    cat >>src/CMakeLists.txt <<'EOF'
IF(UNIX)
    TARGET_LINK_LIBRARIES(wiznote ${CMAKE_DL_LIBS})
ENDIF(UNIX)
EOF
    msg2 "Begin making..."
    cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
    make -j2
}

package() {
    cd "${srcdir}/$_gitname/"
    make DESTDIR="${pkgdir}" install
    if [ -d plugins ];then
        find plugins/ -type f -exec install -Dm755 {} "${pkgdir}/usr/share/wiznote/"{} \;
    fi
    if [ -f ${pkgdir}/usr/share/wiznote/wiznote.icns ];then
        rm ${pkgdir}/usr/share/wiznote/wiznote.icns
    fi

    cat > wiznote.desktop <<'EOF'
[Desktop Entry]
Name=wiznote
Name[zh_CN]=为知笔记
GenericName=cloud note-taking application
GenericName[zh_CN]=云笔记软件
Comment=taking note and manage information
Comment[zh_CN]=记笔记和管理信息
Exec=wiznote
Icon=wiznote
Categories=KDE;QT;Utility;
StartupNotify=true
Terminal=false
Type=Application
EOF
    install -Dm644 wiznote.desktop "${pkgdir}/usr/share/applications/"
}
