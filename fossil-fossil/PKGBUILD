# $Id: PKGBUILD,v 1.4 2010/12/25 04:30:00 andres Exp $

pkgname="fossil-fossil"
_pkgname="${pkgname%-fossil}"
pkgver=25122010
pkgrel=1
pkgdesc="Simple, high-reliability, distributed software configuration management (devel)"
arch=('i686' 'x86_64')
license=('BSD')
url="http://www.fossil-scm.org/"
makedepends=("$_pkgname")
depends=('openssl')
conflicts=("$_pkgname")
provides=("$_pkgname")

build() {
	mkdir -p $_pkgname
	cd $_pkgname

	if [ -f _FOSSIL_ ]; then
		fossil pull $url
		fossil update
	else
		fossil clone $url ../$_pkgname.fossil
		fossil open ../$_pkgname.fossil
	fi

	# headers and translate targets are problematic with parallel jobs
	make -j1 bld bld/headers
	make
}

package() {
	cd $_pkgname

	install -Dm755 $_pkgname \
		$pkgdir/usr/bin/$_pkgname

	install -Dm644 COPYRIGHT-BSD2.txt \
		$pkgdir/usr/share/licenses/$_pkgname/LICENSE
} 
