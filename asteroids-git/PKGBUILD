#Maintainer truh <truhgoj[Ã¤T]truh(O)in>

pkgname=asteroids-git
pkgrel=3
_gitname="asteroids"
pkgdesc="A remake of the game 'asteroids' in C++, [development version]"
url="https://github.com/zigarrre/asteroids"
arch=('x86_64' 'i686')
license=('Simplified BSD License')
depends=('boost-libs' 'thor' 'sfml')
optdepends=()
makedepends=('boost' 'cmake' 'git')
provides=('asteroids')
replaces=()
backup=()
install='asteroids.install'
source=("git://github.com/zigarrre/asteroids.git"
        "asteroids.desktop")
sha512sums=('SKIP'
            '41e9139c51b317dea3c561a366797ffeb666616c4c633312a3af27279a8a7ff200894f19f3929a1b035fda4c0c3e3334a93fb9de2e4373e0bcf8992e3aeece0a')

pkgver() {
  cd "${srcdir}/${_gitname}"

  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

pkgver=r200.afe19df

prepare() {
  cd "${srcdir}"

  cd "${srcdir}/${_gitname}"
  git checkout master
}

build() {
  mkdir build
  cd build

  cmake "${srcdir}/${_gitname}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DASTEROIDS_PATHS_HIGHSCORE=/.asteroids_highscore.csv \
    -DASTEROIDS_PATHS_HIGHSCORE_HOME=true \
    -DASTEROIDS_PATHS_RES=/usr/share/asteroids/ \
    -DASTEROIDS_PATHS_CONFIG=/usr/share/asteroids/config.cfg

  make
}

package() {
  install -Dm644 ${_gitname}.desktop ${pkgdir}/usr/share/applications/${_gitname}.desktop

  install -Dm755 build/asteroids                      "$pkgdir/usr/bin/asteroids"

  install -Dm644 ${srcdir}/${_gitname}/res/ship.png   "$pkgdir/usr/share/pixmaps/asteroids.png"
  install -Dm644 ${srcdir}/${_gitname}/LICENSE.txt    "$pkgdir/usr/share/licenses/$_gitname/LICENSE"

  mkdir -p "$pkgdir/usr/share/asteroids/"
  install -Dm644 ${srcdir}/${_gitname}/res/*          "$pkgdir/usr/share/asteroids/"
  install -Dm644 ${srcdir}/${_gitname}/config.cfg     "$pkgdir/usr/share/asteroids/config.cfg"
}

# vim:set ts=2 sw=2 et:
