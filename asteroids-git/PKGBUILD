#Maintainer truh <truhgoj[Ã¤T]truh(O)in>

pkgname=asteroids-git
pkgver=183.ca29a12
pkgrel=3
_gitname="asteroids"
pkgdesc="A remake of the game 'asteroids' in C++"
url="https://github.com/zigarrre/asteroids"
arch=('x86_64' 'i686')
license=('Simplified BSD License')
depends=('boost-libs' 'thor' 'sfml')
optdepends=()
makedepends=('boost' 'cmake' 'git')
conflicts=()
replaces=()
backup=()
install='asteroids.install'
source=("git://github.com/zigarrre/asteroids.git"
        "asteroids.desktop")
sha512sums=('SKIP'
            '41e9139c51b317dea3c561a366797ffeb666616c4c633312a3af27279a8a7ff200894f19f3929a1b035fda4c0c3e3334a93fb9de2e4373e0bcf8992e3aeece0a')

prepare() {
  cd "${srcdir}"

  cd "${srcdir}/${_gitname}"
  git checkout dev

  msg2 "Replacing resource path to /usr/shar"
  find "${srcdir}/${_gitname}/src" -type f \
    -exec sed -i 's|"res/|"/usr/share/asteroids/|g' {} \;

  msg2 "Replacing highscore path to ~/.asteroids_highscore.csv"
  find "${srcdir}/${_gitname}/src" -type f \
    -exec sed -i 's|"highscore.csv"|(std::string(std::getenv("HOME"))+"/.asteroids_highscore.csv").c_str()|g' {} \;
}

build() {
  cd "${srcdir}/${_gitname}"

  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  install -Dm644 ${_gitname}.desktop ${pkgdir}/usr/share/applications/${_gitname}.desktop
  
  cd "${srcdir}/${_gitname}"

  install -Dm755 asteroids      "$pkgdir/usr/bin/asteroids"

  install -Dm644 res/ship.png   "$pkgdir/usr/share/pixmaps/asteroids.png"
  install -Dm644 LICENSE.txt    "$pkgdir/usr/share/licenses/$_gitname/LICENSE"

  mkdir "$pkgdir/usr/share/asteroids/"
  install -Dm644 res/*          "$pkgdir/usr/share/asteroids/"
}

# vim:set ts=2 sw=2 et:
