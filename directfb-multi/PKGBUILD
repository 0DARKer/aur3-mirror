# Maintainer: lks <lukasgraetz [et] web [dot] de>
# Contributor: Georgios N. Tsalikis <aliverius@tsalikis.net>

# adopted from directfb
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=directfb-multi
pkgver=1.7.0
pkgrel=1
pkgdesc="A thin library that provides hardware graphics acceleration, input device handling and abstraction, integrated windowing system on top of the Linux Framebuffer Device"
arch=(armv7h 'i686' 'x86_64')
url="http://www.directfb.org"
license=('LGPL')
depends=('linux-fusion>=9.0.0' 'xorg-server' 'gcc-libs' 'libjpeg' 'sdl' 'libpng' 'freetype2')
options=('!libtool')
provides=("directfb=${pkgver}")
conflicts=("directfb")
source=(http://www.directfb.org/downloads/Core/DirectFB-${pkgver%.*}/DirectFB-${pkgver}.tar.gz
	85-directfb.rules)
install=directfb.install
sha1sums=('095852479098a55be2b0c6a0250af4159ee16e7c'
          'c504695693d9e0ff2f71ff3dc7de593f48119217')

build() {
  cd DirectFB-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-static \
    --enable-zlib --enable-x11 --enable-sdl --disable-vnc --disable-osx \
    --enable-video4linux2 --disable-tests --enable-multi --disable-unique \
    --with-gfxdrivers=all --disable-trace --disable-debug
  make
} 

package() {
  cd DirectFB-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/85-directfb.rules" "${pkgdir}/etc/udev/rules.d/85-directfb.rules"
}
