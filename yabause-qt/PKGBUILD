# Maintainer: Harley Laue <losinggeneration@gmail.com>
# Contributer: agapito <agapitofrndez@ozu.es>

pkgname=yabause-qt
_pkgname=yabause
pkgver=0.9.11.1
pkgrel=2
pkgdesc='A Sega Saturn emulator with QT interface.'
url='http://yabause.org/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('cmake')
depends=('sdl' 'qt' 'freeglut')
conflicts=('yabause')
provides=('yabause')
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('86c728fe3efda655a28c645714e4b8f4')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  # Fix some defines in Yabause for package name and version
  sed -i "s/\(#define SETTINGS_H\)/\1\n#include \"QtYabause.h\"/" src/qt/Settings.h
  sed -i "s/\(#define QTYABAUSE_H\)/\1\n#define PACKAGE \"${pkgname%-qt}\"/" src/qt/QtYabause.h
  sed -i "s/\(#define QTYABAUSE_H\)/\1\n#define VERSION \"${pkgver}\"/" src/qt/QtYabause.h

  [ -e build ] && rm -rf build
  mkdir build
  cd build
  cmake \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DYAB_PORTS=qt \
    -DYAB_MULTIBUILD=OFF \
    -DYAB_NETWORK=ON \
    -DYAB_OPTIMIZED_DMA=on \
    -DYAB_PERKEYNAME=ON \
    ..

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}
