# Contributor: Yaohan Chen <yaohan.chen@gmail.com>
# Contributor: Jakob Gruber <jakob.gruber@gmail.com>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=allacrost-svn
pkgver=1969
pkgrel=1
pkgdesc='A single player 2D role-playing game inspired by classic console RPGs'
arch=('i686' 'x86_64')
url='http://www.allacrost.org/'
license=('GPL2')
depends=('libgl' 'mesa' 'qt' 'openal' 'libvorbis' 'sdl_net' 'sdl_ttf' 'luabind')
makedepends=('boost' 'subversion')
conflicts=('allacrost')
options=('!emptydirs')
_svntrunk=https://allacrost.svn.sourceforge.net/svnroot/allacrost/trunk/game
_svnmod=allacrost

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # Build
  #
  autoreconf -i
  ./configure --prefix=/usr

  sed -i 's|bindir = ${prefix}/games|bindir = ${prefix}/bin|' Makefile
  sed -i 's|datarootdir = ${prefix}/share/games|datarootdir = ${prefix}/share|' Makefile
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="${pkgdir}" install
}
