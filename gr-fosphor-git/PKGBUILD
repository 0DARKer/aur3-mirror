# Maintainer: Malte Splietker <maltespl [at] mail [dot] upb [dot] de>
pkgname=gr-fosphor-git
pkgver=r54.3dd8d53
pkgrel=1
pkgdesc="GNU Radio block for RTSA-like spectrum visualization using GPU"
arch=('x86_64') # i686 not tested
url="http://git.osmocom.org/gr-fosphor"
license=('GPL')
depends=('glfw')
makedepends=('git' 'gnuradio' 'libcl' 'opencl-headers' 'python' 'qt4')
conflicts=('gr-fosphor')
provides=('gr-fosphor')
source=("$pkgname"::'git://git.osmocom.org/gr-fosphor')
md5sums=('SKIP')
install=gr-fosphor-git.install

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  mkdir build
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$pkgname"
  cd build
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
