# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Taras Shpot <mrshpot@gmail.com>

pkgname=rust-git
pkgver=20121210
pkgrel=2
pkgdesc="A safe, concurrent, practical language from Mozilla."
arch=(i686 x86_64)
url="http://www.rust-lang.org/"
license=('custom:MIT' 'Apache')
depends=('gcc-libs')
makedepends=('git' 'gcc' 'curl'
             'libffi' 'python2')
optdepends=('emacs: to build the emacs mode'
            'pandoc: to build rust.pdf'
            'llnextgen: for build-time grammar verification'
            'naturaldocs: to build library doc')
install=rust.install

_gitroot="git://github.com/mozilla/rust.git"
_gitname="rust"

build() {
  cd "$srcdir"
  msg "Connecting to git server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin && cd ..
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "git checkout done or server timeout"

  test -e $_gitname-build || git clone $_gitname{,-build}
  cd "$srcdir/$_gitname-build"
  git checkout master; git clean -dfX

  ./configure --prefix=/usr

  msg "Starting make..."
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install
  chmod 644 "$pkgdir/usr/lib/rustc/$CARCH-unknown-linux-gnu/lib/libmorestack.a"

  install --directory "$pkgdir/usr/share/licenses/rust-git/"
  install -m644 COPYRIGHT LICENSE-* "$pkgdir/usr/share/licenses/rust-git/"

  _docdir="$pkgdir/usr/share/doc/rust"
  install --directory "$_docdir"
  for _doc in rust.pdf rust.html tutorial.html rust.css core std rust.md ; do
    test -e "doc/$_doc" || continue
    cp -r "doc/$_doc" "$_docdir/"
    chmod -R 644 "$_docdir/$_doc"
  done
  find "$_docdir" -type d -exec chmod 755 {} +

  install --directory "$pkgdir/usr/share/vim/"
  cp -a src/etc/vim "$pkgdir/usr/share/vim/vimfiles"

  cd src/etc/emacs
  if which emacs > /dev/null; then make; fi
  install --directory "$pkgdir/usr/share/emacs/site-lisp/"
  cp -a * "$pkgdir/usr/share/emacs/site-lisp/"
}
