# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Taras Shpot <mrshpot@gmail.com>
pkgname=rust-git
pkgver=0.6.2266.g7755018
epoch=2
pkgrel=1
pkgdesc="A safe, concurrent, practical language from Mozilla."
arch=(i686 x86_64)
url="http://www.rust-lang.org/"
license=('custom:MIT' 'Apache')
depends=('gcc-libs')
makedepends=('git' 'gcc' 'curl'
             'libffi' 'python2')
optdepends=('emacs: to build the emacs mode'
            'haskell-pandoc: to build rust.pdf')
provides=('rust=0.7')
conflicts=('rust')
install=rust.install
source=("git+https://github.com/mozilla/rust.git")
sha1sums=(SKIP)

pkgver() {
  cd rust
  git describe | sed -e 's/^release-//' -e 'y/-/./'
}

build() {
  cd rust

  ./configure --prefix=/usr

  make
}

package() {
  cd rust

  make DESTDIR="$pkgdir" install
  chmod 644 "$pkgdir/usr/lib/rustc/$CARCH-unknown-linux-gnu/lib/libmorestack.a"

  install --directory "$pkgdir/usr/share/licenses/rust-git/"
  install -m644 COPYRIGHT LICENSE-* "$pkgdir/usr/share/licenses/rust-git/"

  _docdir="$pkgdir/usr/share/doc/rust"
  install --directory "$_docdir"
  for _doc in rust.pdf rust.html tutorial.html rust.css core std rust.md ; do
    test -e "doc/$_doc" || continue
    cp -r "doc/$_doc" "$_docdir/"
    chmod -R 644 "$_docdir/$_doc"
  done
  find "$_docdir" -type d -exec chmod 755 {} +

  install --directory "$pkgdir/usr/share/apps/katepart/syntax/"
  cp src/etc/kate/rust.xml "$pkgdir/usr/share/apps/katepart/syntax/"

  install --directory "$pkgdir/usr/share/vim/"
  cp -a src/etc/vim "$pkgdir/usr/share/vim/vimfiles"

  cd src/etc/emacs
  if which emacs 2> /dev/null; then make; fi
  install --directory "$pkgdir/usr/share/emacs/site-lisp/"
  cp -a * "$pkgdir/usr/share/emacs/site-lisp/"
}
