# Maintainer: Kazuo Teramoto <kaz.rag at gmail dot com>
# Contributor: damir <damir@archlinux.org>

pkgname=lib32-libcdio
_pkgname=libcdio
pkgver=0.90
pkgrel=2
pkgdesc="GNU Compact Disc Input and Control Library (32-bit)"
arch=("x86_64")
license=('GPL3')
url="http://www.gnu.org/software/libcdio/"
depends=('gcc-libs-multilib>=4.4.2' "libcdio")
options=('!libtool')
install=libcdio.install
source=(http://ftp.gnu.org/gnu/libcdio/${_pkgname}-${pkgver}.tar.gz{,.sig}
        libcdio-0.83-linking.patch
        udf-Dont-return-freed-memory-from-udf_fopen.patch)
md5sums=('1b245b023fb03a58d030fd2800db3247'
         '45100d90ea49d6f8481eddf709655c0f'
         '5a7f50209c03d5919d5b932f07871af7'
         'b7b50c989575e5484d3992b455c9df48')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/libcdio-0.83-linking.patch
  patch -Np1 -i ../udf-Dont-return-freed-memory-from-udf_fopen.patch
  ./configure --prefix=/usr --disable-vcd-info --enable-cpp-progs \
      --libdir=/usr/lib32 --without-cd-drive --without-cd-info \
      --without-cdda-player --without-cd-read --without-iso-info \
      --without-iso-read --disable-cddb
  make
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install

  rm -rf ${pkgdir}/usr/bin ${pkgdir}/usr/include ${pkgdir}/usr/share
}
