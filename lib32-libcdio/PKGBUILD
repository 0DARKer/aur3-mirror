# Maintainer: Kazuo Teramoto <kaz.rag at gmail dot com>
# Contributor: damir <damir@archlinux.org>

pkgname=lib32-libcdio
_pkgname=libcdio
pkgver=0.83
pkgrel=1
pkgdesc="GNU Compact Disc Input and Control Library (32-bit)"
arch=("x86_64")
license=('GPL3')
url="http://www.gnu.org/software/libcdio/"
depends=('gcc-libs-multilib>=4.4.2' "libcdio")
options=('!libtool')
install=libcdio.install
source=(http://ftp.gnu.org/gnu/libcdio/${_pkgname}-${pkgver}.tar.gz{,.sig}
        libcdio-0.83-linking.patch)
md5sums=('b9e0f1bccb142e697cd834fe56b6e6fb'
         'a7864a4b572a1e2a28cc0a05aa4a67d3'
         '5a7f50209c03d5919d5b932f07871af7')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/libcdio-0.83-linking.patch
  ./configure --prefix=/usr --disable-vcd-info --enable-cpp-progs \
      --libdir=/usr/lib32 --without-cd-drive --without-cd-info \
      --without-cdda-player --without-cd-read --without-iso-info \
      --without-iso-read --disable-cddb
  make
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install

  rm -rf ${pkgdir}/usr/bin ${pkgdir}/usr/include ${pkgdir}/usr/share
}
