# Maintainer: SJ_UnderWater
# Based on netatalk package :
# Maintainer: Dominik Dingel <mail at wodar dot de>
# Contributor: William Udovich <nerdzrule7 at earthlink dot net>
# Contributor: Farhan Yousaf <farhany at xaviya dot com>

pkgbase=netatalk
pkgname=netatalk-ddp
pkgver=2.2.3
pkgrel=5
pkgdesc='A kernel-level implementation of AppleTalk, AFP, and PAP services'
arch=('i686' 'x86_64')
url='http://netatalk.sourceforge.net'
license=('GPL')
depends=('openslp' 'libcups' 'libgcrypt>=1.2.3')
optdepends=('cups: required for PAP support')
replaces=('netatalk-git' 'netatalk2')
conflicts=('netatalk')
backup=('etc/netatalk/afpd.conf'
	'etc/netatalk/afp_ldap.conf'
	'etc/netatalk/netatalk.conf'
	'etc/netatalk/AppleVolumes.default'
	'etc/netatalk/AppleVolumes.system'
	'etc/netatalk/atalkd.conf'
	'etc/netatalk/papd.conf'
	'etc/pam.d/netatalk')
options=('!libtool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(a2boot.rc afpd.rc atalkd.rc cnid_metad.rc netatalk.rc papd.rc timelord.rc \
	http://hivelocity.dl.sourceforge.net/project/$pkgbase/$pkgbase/$pkgver/$pkgbase-$pkgver.tar.bz2)
md5sums=('26d1f602c5de87495b6c69021202d929'
	'dcbce48475b038c0966d81dc8d61fbee'
	'244fc26daba9c95e31f839c5c6f27c89'
	'c227dc51d94f3e541daf478bd3d0be03'
	'56cc073c625abdc26553ecf1998d40cb'
	'e32f1df98f95a2a977d9a513deaf5aae'
	'425a3dead19a20f4433fc2bffedb32bc'
	'0b5a0645836183b0e548ccfbd5c022f8')

build() {
	cd $pkgbase-$pkgver
	msg2 'Fixing...'
	sed -i 's/install-exec-hook/install-data-hook/' etc/uams/Makefile.am
	autoreconf
	msg2 'Configuring...'
	CFLAGS="-Wno-unused-result" ./configure --prefix=/usr --localstatedir=/var --libexecdir='${libdir}' \
		--with-cracklib --with-cnid-cdb-backend --enable-fhs --enable-ddp --enable-srvloc --enable-timelord --enable-cups --enable-a2boot
	msg2 'Making...'
	make >/dev/null
}
package() {
	cd $pkgbase-$pkgver
	msg2 'Building...'
	make DESTDIR="$pkgdir" install >/dev/null
	msg2 'Moving Files...'
	rm "$pkgdir"/usr/include/netatalk/at.h
	msg2 'Copying initscripts...'
	install -D -m755 "$srcdir"/netatalk.rc "$pkgdir"/etc/rc.d/netatalk
	install -m755 "$srcdir"/cnid_metad.rc "$pkgdir"/etc/rc.d/cnid_metad
	install -m755 "$srcdir"/afpd.rc "$pkgdir"/etc/rc.d/afpd
	install -m755 "$srcdir"/a2boot.rc "$pkgdir"/etc/rc.d/a2boot
	install -m755 "$srcdir"/atalkd.rc "$pkgdir"/etc/rc.d/atalkd
	install -m755 "$srcdir"/papd.rc "$pkgdir"/etc/rc.d/papd
	install -m755 "$srcdir"/timelord.rc "$pkgdir"/etc/rc.d/timelord
}
