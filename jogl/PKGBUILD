# Maintainer: Victor Dmitriyev <mrvvitek@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=jogl
pkgver=2.0.2
pkgrel=1
pkgdesc='OpenGL bindings for Java'
arch=('x86_64' 'i686')
url="http://jogamp.org/"
license=('BSD')
depends=('java-runtime' 'libgl')
source=("jogl.license.txt"
  "gluegen.license.txt"
	"http://jogamp.org/deployment/v${pkgver}/jar/orig/jogl-all.jar"
	"http://jogamp.org/deployment/v${pkgver}/jar/orig/gluegen-rt.jar")
md5sums=('4f7cc7c2a95c4d1e1d0670ed497c06b3'
  '88456d8059e998bff8868c2b4d0347ce'
	'86c3508334d929ef40defe1e34b0120e'
	'4a5dbe3dec0dfe4312f1f86213879409')

[[ "$CARCH" == "i686" ]] && {
source+=("http://jogamp.org/deployment/v${pkgver}/jar/orig/jogl-all-natives-linux-i586.jar"
	"http://jogamp.org/deployment/v${pkgver}/jar/orig/gluegen-rt-natives-linux-i586.jar")
md5sums+=('0ae30c2d94c805b893285ca7664cfaa2'
	'd4c87e64c24d312680c70b5768c356ad')
}
[[ "$CARCH" == "x86_64" ]] && {
source+=("http://jogamp.org/deployment/v${pkgver}/jar/orig/jogl-all-natives-linux-amd64.jar"
	"http://jogamp.org/deployment/v${pkgver}/jar/orig/gluegen-rt-natives-linux-amd64.jar")
md5sums+=('ab4d2db5ec94d57f3d1f92010f0442b9'
	'6c84a7a3ea5de9ae344e41da226b8753')
}
noextract=({jogl-all,gluegen-rt}.jar)

package() {
  cd "${srcdir}"
  install -Dm644 "jogl-all.jar" "${pkgdir}/usr/share/java/${pkgname}/jogl-all.jar"
  install -Dm644 "gluegen-rt.jar" "${pkgdir}/usr/share/java/${pkgname}/gluegen-rt.jar"
  install -Dm644 "libgluegen-rt.so" "${pkgdir}/usr/lib/${pkgname}/libgluegen-rt.so"
  install -Dm644 "libjogl_desktop.so" "${pkgdir}/usr/lib/${pkgname}/libjogl_desktop.so"
  install -Dm644 "libjogl_mobile.so" "${pkgdir}/usr/lib/${pkgname}/libjogl_mobile.so"
  install -Dm644 "libnativewindow_awt.so" "${pkgdir}/usr/lib/${pkgname}/libnativewindow_awt.so"
  install -Dm644 "libnativewindow_x11.so" "${pkgdir}/usr/lib/${pkgname}/libnativewindow_x11.so"
  install -Dm644 "libnewt.so" "${pkgdir}/usr/lib/${pkgname}/libnewt.so"
  install -Dm644 "jogl.license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/jogl.license.txt"
  install -Dm644 "gluegen.license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/gluegen.license.txt"

  cd "${pkgdir}/usr/share/java/${pkgname}"
  ln -s "jogl-all.jar" "jogl2.jar"
  ln -s "gluegen-rt.jar" "gluegen2-rt.jar"
  cd "${pkgdir}/usr/lib"
  ln -s "jogl" "jogl2"
  ln -s "jogl" "gluegen2"
  ln -s "libgluegen-rt.so" "jogl/libgluegen2-rt.so"
}

# vim:set ts=2 sw=2 et:
