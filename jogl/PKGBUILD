# Maintainer: Victor Dmitriyev <mrvvitek@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=jogl
pkgver=2.0rc11
pkgrel=7
pkgdesc='OpenGL bindings for Java'
arch=('x86_64' 'i686')
url="http://jogamp.org/"
license=('BSD')
depends=('java-runtime' 'libgl')
source=("license.txt"
	"http://jogamp.org/deployment/v${pkgver/r/-r}/jar/orig/jogl-all.jar"
	"http://jogamp.org/deployment/v${pkgver/r/-r}/jar/orig/gluegen-rt.jar")
md5sums=('4a3f908842603c600836678ddfa1415b'
	'86c1125d33c372b025eca8acf16ac540'
	'674cd470b6b2b915f83cee0262f9050c')

[[ "$CARCH" == "i686" ]] && {
source+=("http://jogamp.org/deployment/v${pkgver/r/-r}/jar/orig/jogl-all-natives-linux-i586.jar"
	"http://jogamp.org/deployment/v${pkgver/r/-r}/jar/orig/gluegen-rt-natives-linux-i586.jar")
md5sums+=('eb5547d2787fe8c7f6dbd1d3649ae37c'
	'89bf28f76cd6d993e4ad0232b0078a30')
}
[[ "$CARCH" == "x86_64" ]] && {
source+=("http://jogamp.org/deployment/v${pkgver/r/-r}/jar/orig/jogl-all-natives-linux-amd64.jar"
	"http://jogamp.org/deployment/v${pkgver/r/-r}/jar/orig/gluegen-rt-natives-linux-amd64.jar")
md5sums+=('5c4b32509479fbe3a8f01598a4155ac5'
	'f8808f8f83ee744510c52cb6b74110ff')
}
noextract=({jogl-all,gluegen-rt}.jar)

package() {
  cd "${srcdir}"
  install -Dm644 "jogl-all.jar" "${pkgdir}/usr/share/java/${pkgname}/jogl-all.jar"
  install -Dm644 "gluegen-rt.jar" "${pkgdir}/usr/share/java/${pkgname}/gluegen-rt.jar"
  install -Dm644 "libgluegen-rt.so" "${pkgdir}/usr/lib/${pkgname}/libgluegen-rt.so"
  install -Dm644 "libjogl_desktop.so" "${pkgdir}/usr/lib/${pkgname}/libjogl_desktop.so"
  install -Dm644 "libjogl_mobile.so" "${pkgdir}/usr/lib/${pkgname}/libjogl_mobile.so"
  install -Dm644 "libnativewindow_awt.so" "${pkgdir}/usr/lib/${pkgname}/libnativewindow_awt.so"
  install -Dm644 "libnativewindow_x11.so" "${pkgdir}/usr/lib/${pkgname}/libnativewindow_x11.so"
  install -Dm644 "libnewt.so" "${pkgdir}/usr/lib/${pkgname}/libnewt.so"
  install -Dm644 "license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

  cd "${pkgdir}/usr/share/java/${pkgname}"
  ln -s "jogl-all.jar" "jogl2.jar"
  ln -s "gluegen-rt.jar" "gluegen2-rt.jar"
  cd "${pkgdir}/usr/lib"
  ln -s "jogl" "jogl2"
  ln -s "jogl" "gluegen2"
  ln -s "libgluegen-rt.so" "jogl/libgluegen2-rt.so"
}

# vim:set ts=2 sw=2 et:
