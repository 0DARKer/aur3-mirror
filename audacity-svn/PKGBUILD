# Maintainer:  rtfreedman (rob<d0t>til<d0t>freedman<aT>gmail<d0t>com
# Contributor: Federico Cinelli <cinelli.federico@gmail.com>
# Contributor: nightfrost <khashayar_naderehvandi@yahoo.co.uk>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=audacity-svn
pkgver=13845
pkgrel=1
pkgdesc="A program that lets you manipulate and record  digital audio waveforms -- development snapshot"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
#
depends=( 'twolame' 'soundtouch' 'libmad' 'libid3tag' 'wxgtk2.8' 'lame' 'desktop-file-utils' 'jack' 'vamp-plugin-sdk' 'lilv')
makedepends=('svn' 'python2' )
options=('!makeflags')
conflicts=('audacity')
provides=('audacity')
install=$pkgname.install

source=("$pkgname::svn+http://audacity.googlecode.com/svn/audacity-src/trunk/" ffmpeg1.patch)
md5sums=('SKIP'
         '14198818e2d7958550eb3907de090e89')

pkgver() {
  cd "$pkgname"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$pkgname"
  # enable with --with-ffmpeg=system
  #patch -p1 -i "${srcdir}"/ffmpeg1.patch
}

build() {
  cd "$pkgname"
#  LINGUAS="ru"
  WX_CONFIG=/usr/bin/wx-config-2.8  \
  ./configure --prefix=/usr --enable-unicode  --enable-sse \
	--with-ladspa --with-lame  --with-libvamp --with-ffmpeg=local
# --with-ffmpeg=system
# --disable-nls
  make
}
package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir/" install
}
