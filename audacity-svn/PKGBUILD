# Maintainer:  rtfreedman (rob<d0t>til<d0t>freedman<aT>gmail<d0t>com
# Contributor: Federico Cinelli <cinelli.federico@gmail.com>
# Contributor: nightfrost <khashayar_naderehvandi@yahoo.co.uk>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=audacity-svn
pkgver=13511
pkgrel=1
pkgdesc="A program that lets you manipulate and record  digital audio waveforms -- development snapshot"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('vamp-plugin-sdk' 'lilv' 'twolame' 'soundtouch' 'libmad' 'libid3tag' 'wxgtk2.8' 'lame' 'desktop-file-utils' 'jack')
makedepends=('svn' 'python2' )
options=('!makeflags')
conflicts=('audacity')
provides=('audacity')
install=$pkgname.install

source=("$pkgname::svn+http://audacity.googlecode.com/svn/audacity-src/trunk/" ffmpeg.patch)
md5sums=('SKIP'
         '86aac665e99b4a8c5c1db32c9476401d')

pkgver() {
  cd "$pkgname"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "$pkgname"
  patch -p1 -i "${srcdir}"/ffmpeg.patch
}

build() {
  cd "$pkgname"
#  LINGUAS="ru"

  WX_CONFIG=/usr/bin/wx-config-2.8  \
  ./configure --prefix=/usr --enable-unicode \
	--with-ladspa --with-lame --with-ffmpeg --with-libvamp

# --disable-nls  --enable-sse

  make
}
package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir/" install
}
