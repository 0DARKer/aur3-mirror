# Maintainer: Joshua Stiefer <facedelajunk@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: stefan-husmann@t-online.de
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=librep
pkgver=0.92.2.1
pkgrel=1
pkgdesc="Lisp system for Sawfish"
license=('GPL2')
arch=('x86_64' 'i686')
url="http://sawfish.wikia.com/wiki/Librep"
depends=('gmp' 'gdbm' 'libffi')
install=$pkgname.install
options=('!emptydirs' '!libtool')
source=("http://download.tuxfamily.org/$pkgname/$pkgname-$pkgver.tar.xz")
sha256sums=('47bb628a377e2c7cb6e22f3e691a88d88af83570ca99b0d7300a5fbcb46b6663')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  ./configure --disable-static \
    --prefix=/usr \
    --libexecdir="/usr/lib/$pkgname" \
    --with-readline
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
