# Maintainer: Nicolo' Barbon <smilzoboboz@gmail.com>
pkgname=calise
pkgver=0.3.0
pkgrel=2
pkgdesc="A program that computes ambient brightness and sets screen's correct backlight using a webcam."
arch=('i686' 'x86_64')
url="http://calise.sourceforge.net/"
license=('GPL3')
groups=()
depends=('python-pygame'
         'python-imaging'
         'python2-numpy'
         'hicolor-icon-theme'
         'pyxdg'
         'python2-pyephem'
         'python2-dbus'
         'dbus-glib')
makedepends=('python2-distutils-extra'
             'intltool'
             'libx11')
optdepends=('python2-pyqt: gui (non-service only)'
            'pm-utils: graceful pause and resume on suspend/hiberante')
provides=('calise')
conflicts=('calise-git')
replaces=('camsensor')
backup=()
options=()
install=calise.install
changelog=
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname-beta/$pkgver/$pkgname-$pkgver.tar.gz
        http://sourceforge.net/projects/$pkgname/files/$pkgname-beta/$pkgver/issue3539203_bugfix.patch)
noextract=()
sha1sums=('8fc8596b8228d150a4223b73734dc97d68690462'
          'af9bae4631783fa45dd705fb689a3665434666f2')


build() {
    cp issue3539203_bugfix.patch "${srcdir}/${pkgname}"
    cd "${srcdir}/${pkgname}"
    patch -p1 < issue3539203_bugfix.patch
    env python2 setup.py build
}

package() {
    cd "${srcdir}/${pkgname}"

    # preliminary check
    if [ ! -d /sys/class/backlight/ ]
    then
        return 1
    fi

    # udev rules creation (for all available interfaces)
    interfaces=""
    for path in /sys/class/backlight/*
    do
        interfaces="`udevadm info -a -p ${path} |
            grep "KERNEL=" |
            sed s'/KERNEL==//' |
            awk -F ['"'] '{print $2}'` ${interfaces}"
    done
    if [ -z $interfaces ]
    then
        echo "Your configuration is currently not supported."
        return 2
    fi
    for interface in ${interfaces}
    do
        if [ ! -f /sys/class/backlight/$interface/brightness ]
        then
            echo "Your configuration is currently not supported."
            return 3
        fi
        udevrule="${interface}.rules"
        echo "KERNEL==\"${interface}\", RUN+=\"/bin/chmod 664 /sys/class/backlight/${interface}/brightness\"" > $udevrule
        echo "KERNEL==\"${interface}\", RUN+=\"/bin/chgrp video /sys/class/backlight/${interface}/brightness\"" >> $udevrule
        install -Dm644 ${udevrule} "${pkgdir}/usr/lib/udev/rules.d/99-backlight-${udevrule}"
    done

    # dbus policy exception ("rm" for distutils-extra's path error workaround)
    install -Dm644 "other/org.${pkgname}.conf" "${pkgdir}/etc/dbus-1/system.d/org.${pkgname}.conf"
    rm "other/org.${pkgname}.conf"

    # pm-utils sleep script
    if [ -d /usr/lib/pm-utils ]
    then
        install -Dm755 "other/53${pkgname}d" "${pkgdir}/usr/lib/pm-utils/sleep.d/53${pkgname}d"
    fi

    # final install through setup.py
    env python2 setup.py install --prefix="${pkgdir}/usr/"
}