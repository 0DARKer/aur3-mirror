# Maintainer: Patrice Peterson <runiq at archlinux dot us>
# Contributor: Vivien Didelot <vivien+aur@didelot.org>
_ghuser=vivien
pkgname=i3blocks
pkgver=1.3
pkgrel=1
pkgdesc='Define blocks for your i3bar status line'
arch=('any')
url="https://github.com/$_ghuser/$pkgname"
license=('GPL3')
makedepends=('ruby-ronn')
optdepends=('openvpn: For openvpn script'
            'playerctl: For mediaplayer script'
            'lm_sensors: For temperature script')
source=("https://github.com/$_ghuser/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('d1c46aed1df766758e000ca18e8cd0fbf0176f99c95a80dd3b036231a0b149ab')

build () {
  cd "$pkgname-$pkgver"
  make VERSION="$pkgver"
}

package () {
  cd "$pkgname-$pkgver"
  make VERSION="$pkgver" PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim: et ts=2 sw=2
