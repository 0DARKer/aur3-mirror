# Maintainer : SpepS <dreamspepser at yahoo dot it>

_pkg=cpulimit
pkgname=limitcpu
pkgver=1.4
pkgrel=1
pkgdesc="A simple program that attempts to limit the CPU usage of a process, expressed in percentage."
arch=('i686' 'x86_64')
url="http://$pkgname.sourceforge.net/"
license=("GPL")
depends=('glibc')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$_pkg-$pkgver.tar.gz")
md5sums=('16c0177cc3d9d27b8653a33ba981c682')

build() {
  cd "$srcdir/$_pkg-$pkgver"

  # rename to limitcpu to not conflict
  # with cpulimit in [community]

  mv $_pkg.c $pkgname.c
  sed -i "s|$_pkg|$pkgname|g" Makefile
  zcat $_pkg.1.gz | sed "s|$_pkg|$pkgname|g" | gzip -9 > $pkgname.1.gz

  make
}
package() {
  cd "$srcdir/$_pkg-$pkgver"

  make PREFIX="$pkgdir/usr" install
}
