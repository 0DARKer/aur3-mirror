_pkgbasename=faad2
pkgname=lib32-$_pkgbasename
pkgver=2.7
pkgrel=2
pkgdesc="ISO AAC audio decoder (32 bit, library only)"
arch=('x86_64')
url="http://www.audiocoding.com/"
license=('GPL' 'custom:FAAD2')
depends=('lib32-glibc' $_pkgbasename)
options=('!libtool' '!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/faac/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('4c332fa23febc0e4648064685a3d4332')
sha1sums=('b0e80481d27ae9faf9e46c8c8dfb617a0adb91b5')

build() {
	cd ${srcdir}/${_pkgbasename}-${pkgver}
	./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
	make CFLAGS="${CFLAGS} -fPIC"
}

package() {
	cd ${srcdir}/${_pkgbasename}-${pkgver}
	make DESTDIR=${pkgdir} install
	# Cleanup for a lib32 package
	rm -rf ${pkgdir}/usr/{bin,include,share,lib32/*.a}
	# Use faad2 license, instead of adding more file to the system
	mkdir -p ${pkgdir}/usr/share/licenses && cd ${pkgdir}/usr/share/licenses
	ln -sf ${_pkgbasename} ${pkgname}
}
