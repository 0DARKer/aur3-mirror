# Maintainer: spider-mario <spidermario@free.fr>
pkgname=kotlin
pkgver=0.6.2500
pkgrel=1
pkgdesc="Statically typed programming language that compiles to JVM bytecode and JavaScript"
arch=('any')
url="http://kotlin.jetbrains.org/"
license=('Apache')
depends=('bash' 'java-environment')
makedepends=('apache-ant')
options=('!emptydirs')
source=(https://github.com/JetBrains/$pkgname/archive/build-$pkgver.zip)
sha512sums=('663cef8c0835e47ca050e528056745d347ef19d761ab01e90c5f705301acc42e4c56ac530e348fe7523ca8c7d67eb119c6d5c1395b43084921c5598beb5eb277')

build() {
  cd $pkgname-build-$pkgver

  ant -f update_dependencies.xml
  ant
}

package() {
  cd $pkgname-build-$pkgver/dist/kotlinc

  install --directory "$pkgdir"/usr/share/licenses/
  cp -r license "$pkgdir"/usr/share/licenses/$pkgname

  cp -r lib "$pkgdir"/usr/

  install --directory "$pkgdir"/usr/bin/
  install bin/kotlinc-{js,jvm} "$pkgdir"/usr/bin/
}

# vim:set ts=2 sw=2 et:
