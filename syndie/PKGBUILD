# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: KillYourTV <killyourtv@i2pmail.org>

pkgname=syndie
pkgver=1.102b11
pkgrel=3
pkgdesc="Java based open source system for operating secure distributed
 forums."
license=('Public Domain')
url="http://syndie.i2p2.de"
arch=('any')
depends=('java-runtime' 'i2p' 'swt' 'hsqldb-java')
makedepends=('java-environment' 'apache-ant' 'git')
source=('syndie.desktop'
        'syndie.xpm'
        'syndie.bin')
sha256sums=('a4711e9876f8e185a5de84976f2b5a72a8890329c86688ffa583ae8741ecc6cc'
            '552d02ae3724d969e668996b67d0d7b1d70f39bf03ad43287073511696c995f7'
            'a3e9d28f4e2da66cf91b4f1e57bf902dcd091ac1b97dadd9ec819f383cd83df8')

__gitroot="git://github.com/i2p/i2p.syndie.git"
__commit=0511668fbaa79e725141788197d8119b915da22a

build() {
    cd "$srcdir"
    if [[ -d "i2p.syndie/.git" ]]; then
        cd "i2p.syndie"
        ant distclean
        #msg "Cleaning repo..."
        #git clean -xdf 2&>/dev/null
        #git pull --depth 1
        #msg "The local repository was updated."
    else
        msg "Connecting to GIT server..."
        git clone --depth 100 ${__gitroot}
        cd "i2p.syndie"
        git checkout ${__commit}
        msg "The remote repository was cloned."
    fi

    source /etc/profile.d/apache-ant.sh
    sed -i build.properties \
        -e "s|#lib.dir=|lib.dir=/usr/share/java|" \
        -e "s|#i2p.jar=|i2p.jar=/opt/i2p/lib/i2p.jar|"
    sed -i build.xml \
        -e 's|source="1.5"|source="1.6"|' \
        -e 's|target="1.5"|target="1.6"|'
    export LG2=en
    ant singlejar
}

package(){
    cd "$srcdir/i2p.syndie"
    install -Dm644 LICENSE                "$pkgdir/usr/share/licenses/syndie/LICENSE"
    install -Dm644 syndie.jar             "$pkgdir/usr/share/syndie/syndie.jar"
    install -Dm644 doc/syndie.1           "$pkgdir/usr/share/man/man1/syndie.1"
    install -Dm644 $srcdir/syndie.xpm     "$pkgdir/usr/share/pixmaps/syndie.xpm"
    install -Dm644 $srcdir/syndie.desktop "$pkgdir/usr/share/applications/syndie.desktop"
    install -Dm755 $srcdir/syndie.bin     "$pkgdir/usr/bin/syndie"
}
