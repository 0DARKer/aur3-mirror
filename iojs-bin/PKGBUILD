# Maintainer: Josip Janzic <josip.janzic@gmail.com>
pkgname=iojs-bin
pkgver=1.3.0
pkgrel=1
pkgdesc="Bringing ES6 to the Node Community!"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://iojs.org/"
license=('MIT')
depends=('openssl')
optdepends=('python2: for node-gyp')
provides=('nodejs')
conflicts=('nodejs')
options=('!emptydirs')

_arch=x64
[[ $CARCH = i686 ]] && _arch=x86
[[ $CARCH = armv6h ]] && _arch=armv6l
[[ $CARCH = armv7h ]] && _arch=armv7l

source=("https://iojs.org/dist/v${pkgver}/iojs-v${pkgver}-linux-${_arch}.tar.xz")

sha256sums=('1da535d02f7948d8009239659e51e1862b61b73457ace6212bf5c35474288eb0')
[[ $CARCH = i686 ]] && sha256sums[0]='bd4c2ac9ab45810988633d64f9b409422e671f80d2027186129d9f396c6657dd'
[[ $CARCH = armv6h ]] && sha256sums[0]='a6809cdd932a007234caee06c71486fbac7a12f18c3e19e8fe7cbf45b87c560d'
[[ $CARCH = armv7h ]] && sha256sums[0]='50c00dd5e6b083a3424a2ed173fe2d30807c4bba0a1a146b0efe95854720bfc1'

_extracted_arch=$_arch
[[ $CARCH = i686 ]] && _extracted_arch=ia32

package() {
        cd "iojs-v${pkgver}-linux-${_extracted_arch}"

        mkdir "$pkgdir/usr/"

        rm -rf ./share/systemtap
        mv ./{bin,include,lib,share} "$pkgdir/usr/"

        install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/iojs/LICENSE
}
