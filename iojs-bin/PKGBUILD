# Maintainer: Josip Janzic <josip.janzic@gmail.com>
pkgname=iojs-bin
pkgver=1.5.1
pkgrel=1
pkgdesc="Evented I/O for V8 JavaScript - Node.js fork"
arch=('i686' 'x86_64' 'armv7h')
url="https://iojs.org/"
license=('MIT')
depends=('openssl')
optdepends=('python2: for node-gyp')
provides=('nodejs')
conflicts=('nodejs')
options=('!emptydirs')

_arch=x64
[[ $CARCH = i686 ]] && _arch=x86
[[ $CARCH = armv7h ]] && _arch=armv7l

source=("https://iojs.org/dist/v${pkgver}/iojs-v${pkgver}-linux-${_arch}.tar.xz")

sha256sums=('02410e24fec141264e8cfabbdf42870eaf36d1aec0bca9e6bceb0ead992ad77f')
[[ $CARCH = i686 ]] && sha256sums[0]='1049d7dfddf1cc27cd70ae769711e9ff4c2de59ad4dcdb5d4066a56c1d89f6eb'
[[ $CARCH = armv7h ]] && sha256sums[0]='41f21d874f15dce9b72ab99c8cd416a253ff5430b1c19b62e6c77468341fe015'

_extracted_arch=$_arch
[[ $CARCH = i686 ]] && _extracted_arch=ia32

package() {
        cd "iojs-v${pkgver}-linux-${_extracted_arch}"

        mkdir "$pkgdir/usr/"

        rm -rf ./share/systemtap
        mv ./{bin,include,lib,share} "$pkgdir/usr/"

        install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/iojs/LICENSE
}
