# Maintainer: Josip Janzic <josip.janzic@gmail.com>
pkgname=iojs-bin
pkgver=2.0.1
pkgrel=1
pkgdesc="Evented I/O for V8 JavaScript - Node.js fork"
arch=('i686' 'x86_64'  'armv6h' 'armv7h')
url="https://iojs.org/"
license=('MIT')
depends=('openssl')
optdepends=('python2: for node-gyp')
provides=('nodejs')
conflicts=('nodejs')
options=('!emptydirs')

_arch=x64
[[ $CARCH = i686 ]] && _arch=x86
[[ $CARCH = armv6h ]] && _arch=armv6l
[[ $CARCH = armv7h ]] && _arch=armv7l

source=("https://iojs.org/dist/v${pkgver}/iojs-v${pkgver}-linux-${_arch}.tar.xz")

sha256sums=('fc5b5f98a934f01a00d6251cf20b803f1c5274b3821a0131fb9bb9257c98abd3')
[[ $CARCH = i686 ]] && sha256sums[0]='6d74f5b5ea24dd154f1909b97d56e8b20c5fbf3d1f91747d512922ab8d9fb964'
[[ $CARCH = armv6h ]] && sha256sums[0]=''
[[ $CARCH = armv7h ]] && sha256sums[0]='abd5c97161a341ea5730da4894f1dc304837373213fa7790c2206ee5bb4635f2'

_extracted_arch=$_arch
[[ $CARCH = i686 ]] && _extracted_arch=ia32

package() {
        cd "iojs-v${pkgver}-linux-${_extracted_arch}"

        mkdir "$pkgdir/usr/"

        rm -rf ./share/systemtap
        mv ./{bin,include,lib,share} "$pkgdir/usr/"

        install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/iojs/LICENSE
}
