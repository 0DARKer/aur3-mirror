# Maintainer: Josip Janzic <josip.janzic@gmail.com>
pkgname=iojs-bin
pkgver=1.2.0
pkgrel=2
pkgdesc="Bringing ES6 to the Node Community!"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://iojs.org/"
license=('MIT')
depends=('openssl')
optdepends=('python2: for node-gyp')
provides=('nodejs')
conflicts=('nodejs')
options=('!emptydirs')

_arch=x64
[[ $CARCH = i686 ]] && _arch=x86
[[ $CARCH = armv6h ]] && _arch=armv6l
[[ $CARCH = armv7h ]] && _arch=armv7l

source=("https://iojs.org/dist/v${pkgver}/iojs-v${pkgver}-linux-${_arch}.tar.xz")

sha256sums=('b237ed613731d24d8f5070b1b705092b3a2859f8399f03ec333c0fa7a171b7af')
[[ $CARCH = i686 ]] && sha256sums[0]='88bfce1d08ea5ac156dbf171f20792f079e669319ee8a915178abb9770242d5f'
[[ $CARCH = armv6h ]] && sha256sums[0]='36336e8247342d114dfcf2b8b823a4d236aeca56b8ef8a2e4c7c13a9305b49f1'
[[ $CARCH = armv7h ]] && sha256sums[0]='fddc854ec8e4bff523f49d5da9597337f215ce08c44b176f18b1789467cefd83'

_extracted_arch=$_arch
[[ $CARCH = i686 ]] && _extracted_arch=ia32

package() {
        cd "iojs-v${pkgver}-linux-${_extracted_arch}"

        mkdir "$pkgdir/usr/"

        rm -rf ./share/systemtap
        mv ./{bin,include,lib,share} "$pkgdir/usr/"

        install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/iojs/LICENSE
}
