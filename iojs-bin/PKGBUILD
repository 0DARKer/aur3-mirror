# Maintainer: Josip Janzic <josip.janzic@gmail.com>
pkgname=iojs-bin
pkgver=1.4.2
pkgrel=1
pkgdesc="Bringing ES6 to the Node Community!"
arch=('i686' 'x86_64' 'armv7h')
url="https://iojs.org/"
license=('MIT')
depends=('openssl')
optdepends=('python2: for node-gyp')
provides=('nodejs')
conflicts=('nodejs')
options=('!emptydirs')

_arch=x64
[[ $CARCH = i686 ]] && _arch=x86
[[ $CARCH = armv7h ]] && _arch=armv7l

source=("https://iojs.org/dist/v${pkgver}/iojs-v${pkgver}-linux-${_arch}.tar.xz")

sha256sums=('418de64ada61f4eaaf841d1581ff34b1b48c6644d242dca03ac91e4a49c3491f')
[[ $CARCH = i686 ]] && sha256sums[0]='ed4cf31c229c18b118d5cfdf006a5c7501c85546893e8f3d8346c32b90a60f0c'
[[ $CARCH = armv7h ]] && sha256sums[0]='4e5fe359566a7b8057730184885cacb8186adc6722a4b18b31cb07194026c90a'

_extracted_arch=$_arch
[[ $CARCH = i686 ]] && _extracted_arch=ia32

package() {
        cd "iojs-v${pkgver}-linux-${_extracted_arch}"

        mkdir "$pkgdir/usr/"

        rm -rf ./share/systemtap
        mv ./{bin,include,lib,share} "$pkgdir/usr/"

        install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/iojs/LICENSE
}
