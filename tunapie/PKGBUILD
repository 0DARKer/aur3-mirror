# Contributor: grimi <grimi at poczta dot fm>

pkgname=tunapie
pkgver=2.1.18
pkgrel=2
pkgdesc="A directory browser for internet radio and TV streams"
arch=('any')
url="http://tunapie.sourceforge.net"
license=('GPL2')
depends=('wxpython>=2.6')
makedepends=('sed')
backup=('etc/tunapie.config')
source=("http://downloads.sourceforge.net/sourceforge/tunapie/$pkgname-$pkgver.tar.gz")
md5sums=('391684c1f26be1b12ff356e00f8c61f5')

build() {
   cd "$srcdir"/$pkgname-$pkgver/src

   python2 compile

   # some fixes
   sed 's|local/||;s|\(python\)|exec \12|' -i ../tunapie
   sed 's|tplogo|tunapie|' -i ../tunapie.desktop
   
   # config: 0 for adult tv off, 1 for on
   echo 0 > tunapie.config
}  


package() {
   cd "$srcdir"/$pkgname-$pkgver/src

   # make some dir's
   mkdir -p "$pkgdir"/usr/{bin,man/man1}
   mkdir -p "$pkgdir"/usr/share/{applications,pixmaps,$pkgname}
   
    # install Tunapie files
   install -m755  ../tunapie "$pkgdir"/usr/bin
   install -m644 ./*.py ./*.pyc ./*.png "$pkgdir"/usr/share/$pkgname
   install -m644 tplogo.xpm "$pkgdir"/usr/share/pixmaps/tunapie.xpm
   install -m644 ../tunapie.desktop "$pkgdir"/usr/share/applications
  
   # install manual 
   install -m644 ../tunapie.1 "$pkgdir"/usr/man/man1
   
   # install config
   install -Dm644 tunapie.config "$pkgdir"/etc/tunapie.config
}

