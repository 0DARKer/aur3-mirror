# Maintainer: Bjorn Neergaard (neersighted) <bjorn@neersighted.com>
# Contributor: OmeGa <omega@mailoo.org>

pkgroot=music-file-organizer
pkgname=music-file-organizer-git
pkgver=latest
pkgrel=2
pkgdesc='Audio file organizer that reads tags and renames files (development version)'
url='http://git.zx2c4.com/music-file-organizer'
license=('GPL')
source=('git+http://git.zx2c4.com/music-file-organizer')
sha1sums=('SKIP')
arch=('i686' 'x86_64')
depends=('icu' 'taglib')
makedepends=('git')
conflicts=('music-file-organizer')
provides=('music-file-organizer')

pkgver() {
  cd "${srcdir}/${pkgroot}"

  # Get the version number.
  git describe --always | sed 's/^v//;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgroot}"

  # Build it!
  # Currently broken with clang, force GCC.
  make CC=gcc CXX=g++
}

package() {
  cd "${srcdir}/${pkgroot}"

  # Install the program.
  make DESTDIR="${pkgdir}" install
}

# vim: ft=sh ts=2 sw=2 et
