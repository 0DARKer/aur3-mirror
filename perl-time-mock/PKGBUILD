pkgname=perl-time-mock
pkgver=0.0.2
_pkgver="v${pkgver}"
pkgrel=1
pkgdesc='shift and scale time'
_dist='Time-Mock'
arch=('any')
url="http://search.cpan.org/dist/Time-Mock"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-module-build')
options=('!emptydirs' 'purge')
source=("http://search.cpan.org/CPAN/authors/id/E/EW/EWILHELM/${_dist}-${_pkgver}.tar.gz")
sha1sums=('3163bae57ff8b098145c90fba189de064669277b')

build() {
    cd "${srcdir}/${_dist}-${_pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null
    perl Build.PL
    ./Build
}

check() {
    cd "${srcdir}/${_dist}-${_pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    ./Build test
}

package() {
    cd "${srcdir}/${_dist}-${_pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    ./Build install installdirs='vendor' destdir="$pkgdir"
}
