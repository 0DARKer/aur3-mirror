# Maintainer: Yosef Or Boczko <yoseforb@gmail.com>

_pkgname=gnome-contacts
pkgname=$_pkgname-git
pkgver=1629.b4e5060
pkgrel=1
pkgdesc="Contacts Manager for GNOME"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
groups=('gnome-extra')
depends=('gtk3' 'folks' 'libnotify' 'gnome-desktop' 'dconf' 'gnome-online-accounts' 'libgee' 'telepathy-glib' 'cheese')
makedepends=('intltool' 'vala' 'gobject-introspection')
install=$_pkgname.install
replaces=('gnome-contacts')
provides=('gnome-contacts' 'gnome-contacts=3.9.4')
conflicts=('gnome-contacts')
source=('git://git.gnome.org/gnome-contacts')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/$_pkgname"
  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/gnome-contacts --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
