# Maintainer: Pio <xpio at tut dot by>

pkgname=chimera_chimera
_name='Chimera Chimera'
pkgver=2
pkgrel=1
pkgdesc="Puzzle platformer: You are a radioactive bear, who must absorb the abilities of other animals to reach the goal. (PyWeek game)"
arch=(any)
url="http://www.pyweek.org/e/chimera_chimera/"
license=('BSD')
depends=(pyglet avbin)
makedepends=('gendesk')
install=${pkgname}.install
source=(http://media.pyweek.org/dl/13/chimera_chimera/${pkgname}_${pkgver}.zip
        ${pkgname}.sh)
md5sums=('e69d568492fe1a38d6d228a8517d87b1'
         '469a999dc114c1b81456b5269a4c80cb')

build() {
	# Generate desktop file
	cd "${srcdir}"
	gendesk -n
}

package() {
	# Copy game, readme and license
	cd "${srcdir}/${pkgname}"
	install -Dm644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README.txt"
	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	ln -s "/usr/share/doc/${pkgname}/README.txt" "${pkgdir}/usr/share/licenses/${pkgname}/README.txt"
	install -d "${pkgdir}/usr/share/${pkgname}"
	cp -r chimera.py config.ini images levels music sfx ${pkgdir}/usr/share/${pkgname}
	# Copy desktop file, icon and run script
	install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	install -D ../${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
}
