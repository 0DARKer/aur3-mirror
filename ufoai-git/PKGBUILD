#Contributor: Wesley <rudirennsau_at_hotmail_dot_com>

pkgname=ufoai-git
pkgver=20110101
pkgrel=1
pkgdesc="Development version of 'Ufo: Alien Invasion'. Includes Gamedata."
url="http://ufoai.sourceforge.net"
license=('custom')
arch=('i686' 'x86_64')
depends=('libgl' 'curl' 'sdl_image' 'sdl_mixer' 'sdl_ttf' 'libtheora' 'xvidcore')
makedepends=('git')
conflicts=('ufoai' 'ufoai-data' 'ufoai-svn')
source=(ufo.sh ufoded.sh ufoai.png ufoai.desktop)

md5sums=('855b91e47893f297b35b6e9f47145be0'
         '81f3c45d1c3269473d2fe85730fd7e53'
         'ae3be6e45fdd8ff584fcfc55d6a40a72'
         'f212811fecfa6eacff73b09e12df73a3')

_gitroot=git://ufoai.git.sourceforge.net/gitroot/ufoai/ufoai
_gitname=ufoai

build() {
	cd $srcdir

	msg "Connecting to GIT server...."

	if [ -d $_gitname ] ; then
	  cd $_gitname && git pull origin
	  msg "The local files are updated."
	else
	  git clone $_gitroot $_gitname
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	cd $_gitname

	./configure --prefix=/usr || return 1

	make || return 1
	make lang || return 1
	make models || return 1 #not necessary, but it will speed things up during runtime i guess
	make maps || return 1
	make pk3 || return 1
}

package() {
	cd $srcdir/$_gitname
	#gamedata
	mkdir -p $pkgdir/usr/share/ufoai/base/
	cp  base/*.pk3 base/*.cfg base/*.txt base/*.so $pkgdir/usr/share/ufoai/base/

	 #binarys
	mkdir -p $pkgdir/usr/share/ufoai/
	cp ufo ufoded ufo2map ufomodel $pkgdir/usr/share/ufoai/

	#translations
	mkdir -p $pkgdir/usr/share/locale/
	cp -r base/i18n/* $pkgdir/usr/share/locale/

	#licenses
	mkdir -p $pkgdir/usr/share/licenses/$pkgname
	cp COPYING LICENSES $pkgdir/usr/share/licenses/$pkgname/

	install -D -m644 $srcdir/ufoai.desktop $pkgdir/usr/share/applications/ufoai.desktop
	install -D -m644 $srcdir/ufoai.png $pkgdir/usr/share/pixmaps/ufoai.png
	install -D -m755 $srcdir/ufo.sh $pkgdir/usr/bin/ufo
	install -D -m755 $srcdir/ufoded.sh $pkgdir/usr/bin/ufoded
}
