# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: archtux <antonio.arias99999@gmail.com>

pkgname=gsharkdown
pkgver=0.3.7
pkgrel=2
pkgdesc="Application for downloading and playing music from grooveshark.com"
arch=('any')
url="https://bitbucket.org/vkolev/gsharkdown"
license=('GPL3')
depends=('gnome-icon-theme' 'gstreamer0.10-python' 'python-configobj' 'dbus-python')
optdepends=('python2-pylast: last.fm support'
            'python-notify: notification support')
install="$pkgname.install"
source=("https://bitbucket.org/vkolev/gsharkdown/get/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop"
        "$pkgname.sh")
md5sums=('424fd2397e04d3c0b67357ad2a2b555a'
         '81869090135791eba7e658619f4eafdd'
         'ede76e8bd3dcb1156e01f377a6c49cac')

build() {
  cd "$srcdir/vkolev"*

  # python2 fix
  sed -i "s|\(env python\).*|\12|" `grep -rl "env python" .`  
}

package() {
  cd "$srcdir/vkolev"*

  # bin
  install -Dm755 ../$pkgname.sh \
    "$pkgdir/usr/bin/$pkgname"

  # data
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a * "$pkgdir/usr/share/$pkgname"

  # icon
  install -Dm644 data/gsharkdown_64.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
