 
# Maintainer : archtux <antonio.arias99999@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=gsharkdown
pkgver=0.2.9
pkgrel=2
pkgdesc="Application for downloading and playing music from grooveshark.com"
arch=('any')
url="https://bitbucket.org/vkolev/gsharkdown"
license=('GPL3')
depends=('gnome-icon-theme' 'gstreamer0.10-python' 'python-configobj' 'python-notify')
install="$pkgname.install"
source=("https://bitbucket.org/vkolev/gsharkdown/get/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop")
md5sums=('33ec7671e18de35a53fea3cfd2a3fe59'
         '99d202cd71d3437c9f195c888bb8737a')

build() {
  cd "$srcdir/vkolev"*

  # python2 fix
  sed -i "s|\(env python\).*|\12|" `grep -rl "env python" .`

  # place configuration files in ~/.config/gsharkdown
  sed -i "s|\(data/[a-z]*\.\(ini\)*\(pkl\)*\" % \)BASE|\1HOME|;\
          51iHOMEPATH = os.path.expanduser('~/.config/gsharkdown')" $pkgname.py
}

package() {
  cd "$srcdir/vkolev"*

  # bin
  install -d "$pkgdir/usr/bin"
  cat << EOF >> "$pkgdir/usr/bin/$pkgname"
#!/bin/bash
CPATH="\$HOME/.config/$pkgname/data"
CFILE=$pkgname.ini
PLIST=playlist.pkl
cd /usr/share/$pkgname
[ -d "\$CPATH" ] || mkdir -p "\$CPATH"
[ -f "\$CPATH/\$CFILE" ] || cp data/\$CFILE "\$CPATH"
[ -f "\$CPATH/\$PLIST" ] || cp data/\$PLIST "\$CPATH"
python2 $pkgname.py $@
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  # data
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a * "$pkgdir/usr/share/$pkgname"

  # Desktop icon
  install -Dm644 data/${pkgname}_64.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}