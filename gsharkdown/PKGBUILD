# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: archtux <antonio.arias99999@gmail.com>

pkgname=gsharkdown
pkgver=0.3.5
pkgrel=2
pkgdesc="Application for downloading and playing music from grooveshark.com"
arch=('any')
url="https://bitbucket.org/vkolev/gsharkdown"
license=('GPL3')
depends=('gnome-icon-theme' 'gstreamer0.10-python' 'python-configobj')
optdepends=('python2-pylast: last.fm support'
            'python-notify: notification support')
install="$pkgname.install"
source=("https://bitbucket.org/vkolev/gsharkdown/get/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop"
        "$pkgname-opts.patch")
md5sums=('6540f4f11ba88a00b6ad3e8e348d7b21'
         '81869090135791eba7e658619f4eafdd'
         'f9da42e4ee7ac46e90123e6c5c248af8')

build() {
  cd "$srcdir/vkolev"*

  # makes last.fm and notification support optional
  patch -p1 -i ../$pkgname-opts.patch

  # python2 fix
  sed -i "s|\(env python\).*|\12|" `grep -rl "env python" .`  
}
package() {
  cd "$srcdir/vkolev"*

  # bin
  install -d "$pkgdir/usr/bin"
  cat << EOF >> "$pkgdir/usr/bin/$pkgname"
#!/bin/bash
cd /usr/share/$pkgname && python2 $pkgname.py $@
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"

  # Data
  install -d $pkgdir/usr/share/$pkgname
  cp -a * $pkgdir/usr/share/$pkgname

  # Desktop icon
  install -Dm644 data/gsharkdown_64.png $pkgdir/usr/share/pixmaps/$pkgname.png
  install -Dm644 $startdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
