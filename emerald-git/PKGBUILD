# Maintainer: Iven Hsu <ivenvd at gmail dot com>
# Contributor: Vinycius Maia <suportevg@uol.com.br>

pkgname=emerald-git
_gitname=emerald
pkgver=334.a272e26
pkgrel=1
pkgdesc="Emerald window decorator"
arch=('i686' 'x86_64')
url="http://cgit.compiz.org/fusion/decorators/emerald"
license=('GPL')
depends=('compiz-bzr' 'libwnck' 'xdg-utils' \
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'git')
options=(!libtool)
conflicts=('emerald')
provides=('emerald=0.9.5')
install=emerald.install
source=('git://anongit.compiz.org/fusion/decorators/emerald#branch=compiz++')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  LIBS+="-lm -ldl" ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $_gitname
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

