## Contributor: quantax -- contact via Arch Linux forum or AUR
## Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=xmms2-scrobbler
pkgver=0.4.0
pkgrel=5
pkgdesc="Audioscrobbler client for XMMS2"
arch=(i686 x86_64)
url="http://code-monkey.de/pages/xmms2-scrobbler"
license=(MIT)
depends=(curl xmms2)
install=install.sh
source=(ftp://ftp.code-monkey.de/pub/$pkgname/$pkgname-$pkgver.tar.gz
        install.sh)
md5sums=('f5be15c90cf7c60209031b8968f77b19'
         '45f7cadfc0a780574cd443f8f02aa6ec')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make LDFLAGS+="-Wl,--no-as-needed -lrt -pthread"
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make install DESTDIR="$pkgdir/" PREFIX=/usr
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
    install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

