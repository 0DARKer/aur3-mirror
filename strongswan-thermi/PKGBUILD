# Maintainer: Thermi <noel@familie-kuntze.de>

pkgname=strongswan-thermi
pkgver=5.2.1
pkgrel=2
pkgdesc="open source IPsec implementation"
url='http://www.strongswan.org'
license=("GPL")
arch=('i686' 'x86_64')
depends=('curl' 'gmp' 'iproute2' 'openssl' 'sqlite' 'libcap')
conflicts=('openswan' 'strongswan')
provides=('strongswan')
options=(!libtool)
backup=(etc/ipsec.conf etc/strongswan.conf etc/strongswan.d/* etc/swanctl/swanctl.conf)
source=(
	"http://download.strongswan.org/strongswan-${pkgver}.tar.bz2"
	'ikev2frag.patch'
	'lots.patch'
)
md5sums=('dd3717c0aa59ab4591ca1812941ebb82'
         'eb843cc663c1045c1f42fd8c379f8dd9'
         'c1b7d6e25e8595574fd847e6f6c674b3')

prepare() {
        cd ${srcdir}/strongswan-${pkgver}
        patch -p1 < ${srcdir}/ikev2frag.patch
        patch -p1 < ${srcdir}/lots.patch   
}

build() {
  cd ${srcdir}/strongswan-${pkgver}

  ./configure --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --with-ipsecdir=/usr/lib/strongswan \
        --enable-sqlite \
        --enable-openssl --enable-curl \
        --enable-sql --enable-attr-sql \
        --enable-farp --enable-dhcp \
        --enable-eap-sim --enable-eap-sim-file --enable-eap-simaka-pseudonym \
        --enable-eap-simaka-reauth --enable-eap-identity --enable-eap-md5 \
        --enable-eap-gtc --enable-eap-aka --enable-eap-aka-3gpp2 \
        --enable-eap-mschapv2 --enable-eap-radius --enable-xauth-eap \
        --enable-ha --enable-gcm --enable-ccm --enable-ctr --enable-unity \
        --enable-integrity-test --enable-load-tester --enable-test-vectors \
        --enable-af-alg --disable-ldap \
        --with-capabilities=libcap --enable-cmd --enable-ntru \
	--enable-vici --enable-swanctl --enable-systemd --enable-ext-auth
  make
}

package() {
  cd "${srcdir}/strongswan-${pkgver}"
  make DESTDIR=${pkgdir} install
}
