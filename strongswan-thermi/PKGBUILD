# Maintainer: Thermi <noel@familie-kuntze.de>

pkgname=strongswan-thermi
pkgver=5.2.2
pkgrel=3
pkgdesc="open source IPsec implementation"
url='http://www.strongswan.org'
license=("GPL")
arch=('i686' 'x86_64')
depends=('curl' 'gmp' 'iproute2' 'openssl' 'sqlite' 'libcap' 'libmariadbclient')
conflicts=('openswan' 'strongswan')
provides=('strongswan')
options=(!libtool)
backup=(etc/ipsec.conf etc/strongswan.conf etc/strongswan.d/* etc/swanctl/swanctl.conf)
source=(
	http://download.strongswan.org/strongswan-${pkgver}.tar.bz2
	# needed because of #814
        configure.patch::https://wiki.strongswan.org/attachments/download/586/configure.patch
        # needed because of #819
        invalid-proto-id.patch::https://wiki.strongswan.org/attachments/download/578/0001-ikev1-Set-protocol-ID-and-SPIs-in-INITIAL-CONTACT-no.patch
	# needed for charon-systemd.user and charon-systemd.group support (see #887)	
	charon-systemd.patch::https://wiki.strongswan.org/projects/strongswan/repository/revisions/f3c8332220f5be450199b909d4823cc1627bf47d/diff?format=diff
	charon-systemd-load.patch::https://bpaste.net/raw/e23e10fb8064
	missing-semicolon.patch::https://wiki.strongswan.org/projects/strongswan/repository/revisions/9c3c41f29bf5772626abde71f52c57c05e59fa94/diff/src/charon-systemd/charon-systemd.c?format=diff
)
# md5 is broken. We use sha256 now. Alternatively, we could check the signature of the file, but that
# is too complicated
sha256sums=('cf2fbfdf200a5eced796f00dc11fea67ce477d38c54d5f073ac6c51618b172f4'
            '75f372ee1ed650100aad3e42871485710d00a764725849b1cd4b4d46946ad7bf'
            '50fc25bd151ecc9d617f699e5b7436c5aef57fdc92dc5bf2728b3d36173e8b27'
            '2e147333056bb0e22e18f3b3e59b8b923d06855f23d8f6c9125391069e164c6d'
            '5b1299d36b26689175144a725eb5cce1d2e73d5caac02a11ebb16f65b7097758'
            '5d4f3b4f6525a36159d983c428c647656ca34f49fa9a8433792a3ae3c1a221d7')

# We don't build libipsec because it would get loaded before kernel-netlink and netkey, which
# would case processing to be handled in user space. Also, the plugin is experimental. If you need it,
# add --enable-libipsec and --enable-kernel-libipsec
prepare() {
        cd ${srcdir}/strongswan-${pkgver}
        patch -p1 < ${srcdir}/invalid-proto-id.patch
	patch -p1 < ${srcdir}/charon-systemd.patch
	patch -p1 < ${srcdir}/charon-systemd-load.patch
        patch -p1 < ${srcdir}/missing-semicolon.patch
        patch -p0 < ${srcdir}/configure.patch
}

build() {
  cd ${srcdir}/strongswan-${pkgver}

  ./configure --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --with-ipsecdir=/usr/lib/strongswan \
        --enable-sqlite \
        --enable-openssl --enable-curl \
        --enable-sql --enable-attr-sql \
        --enable-farp --enable-dhcp \
        --enable-eap-sim --enable-eap-sim-file --enable-eap-simaka-pseudonym \
        --enable-eap-simaka-reauth --enable-eap-identity --enable-eap-md5 \
        --enable-eap-gtc --enable-eap-aka --enable-eap-aka-3gpp2 \
        --enable-eap-mschapv2 --enable-eap-radius --enable-xauth-eap \
        --enable-ha --enable-gcm --enable-ccm --enable-ctr --enable-unity \
        --enable-integrity-test --enable-load-tester --enable-test-vectors \
        --enable-af-alg --disable-ldap --enable-kernel-libipsec --enable-libipsec \
        --with-capabilities=libcap --enable-cmd --enable-ntru \
	--enable-vici --enable-swanctl --enable-systemd --enable-ext-auth --enable-xauth-noauth
  make
}

package() {
  cd "${srcdir}/strongswan-${pkgver}"
  make DESTDIR=${pkgdir} install
}
