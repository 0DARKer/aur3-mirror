# Maintainer:  Eric Biggers <ebiggers3 at gmail dot com>

pkgname=wimlib
pkgver=1.2.5
pkgrel=2
pkgdesc="A library to extract, create, and modify WIM files"
arch=("i686" "x86_64")
url="http://sourceforge.net/projects/wimlib"
license=("GPL3")
depends=("openssl" "fuse" "libxml2" "ntfs-3g" "attr")
optdepends=("cdrkit: for making ISO image of Windows PE"
	"mtools: for making disk image of Windows PE"
	"syslinux: for making disk image of Windows PE"
	"cabextract: for extracting Windows PE from the WAIK"
	"ntfsprogs: for making NTFS filesystems")
checkdepends=("ntfsprogs")
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
	"wimlib-1.2.5.lazy_fusermount.patch")

build() {
	cd "$pkgname-$pkgver"
	patch -p1 < ${srcdir}/wimlib-1.2.5.lazy_fusermount.patch
	./configure --disable-verify-compression --with-libcrypto --with-fuse \
		    --with-ntfs-3g --enable-xattr --prefix=/usr
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
	install -Dm644 NEWS "$pkgdir/usr/share/doc/$pkgname/NEWS"
}
md5sums=('46788c0838b5de3b1d3bf4cbde229425'
         '3b441da739f5466babe2cbf0f53ba734')
