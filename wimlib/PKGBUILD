# Maintainer:  Eric Biggers <ebiggers3 at gmail dot com>

pkgname=wimlib
pkgver=0.7.0
pkgrel=1
pkgdesc="A library to extract, create, and modify WIM files"
arch=("i686" "x86_64")
url="http://sourceforge.net/projects/wimlib"
license=("LGPL")
depends=("openssl" "fuse" "libxml2")
optdepends=("cdrkit: for making ISO image of Windows PE"
	"mtools: for making disk image of Windows PE"	
	"syslinux: for making disk image of Windows PE"	
	"cabextract: for extracting Windows PE from the WAIK")
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=("0f4759f866f97f8897089a9348e7a938")

build() {
	cd "$pkgname-$pkgver"
	./configure --disable-verify-compression --with-libcrypto \
			--prefix=/usr
	make
}

package() {
	cd "$pkgname-$pkgver"  
	make DESTDIR="$pkgdir" install
}
