# Mantainer: Stephane Travostino <stephane.travostino@gmail.com>

pkgname=gw6c
pkgver="6.0"
pkgrel=1
pkgdesc="Client to connect to the Freenet6 IPv6 tunnel broker service"
arch=('i686' 'x86_64' 'ppc')
url="http://go6.net/"
license=(custom:MIT)
groups=(network)
depends=(openssl iproute2)
makedepends=('patch' 'gcc' 'binutils')
backup=('etc/gw6c/gw6c.conf')
source=(gw6c-6_0-RELEASE-src.tar.gz sane-defaults.patch gw6c)
install=gw6c.install
md5sums=('eed0ea74404a4205cfad3d085ee6c85c'
         'e1efce36ad895aa16c894a51f6e5347a'
         'c37b2693bdc39753e69fa158fd9f82d2')


build() {
	cd $startdir/src/gw6c-6_0-RELEASE/
	patch -p1 < ../sane-defaults.patch || return 1

	cd $startdir/src/gw6c-6_0-RELEASE/tspc-advanced

	# ugly fugly hack: this compilation process doesn't like -j make flags. strip them
	MAKEFLAGS=`echo $MAKEFLAGS | sed -r -e 's/-j[0-9]+//'` make all target=linux || return 1

	mkdir -p $startdir/src/build/
	make target=linux install installdir=$startdir/src/build/ || return 1
	
	mkdir -p $startdir/pkg/usr/bin
	mkdir -p $startdir/pkg/etc/rc.d/
	mkdir -p $startdir/pkg/etc/gw6c/
	mkdir -p $startdir/pkg/etc/gw6c/template/
	mkdir -p $startdir/pkg/usr/share/man/man5/
	mkdir -p $startdir/pkg/usr/share/man/man8/
	mkdir -p $startdir/pkg/usr/share/licenses/$pkgname

	install $startdir/src/build/bin/gw6c $startdir/pkg/usr/bin/
	install $startdir/src/gw6c $startdir/pkg/etc/rc.d/
	install -m 644 $startdir/src/build/bin/gw6c.conf $startdir/pkg/etc/gw6c/
	install -m 644 $startdir/src/build/template/linux.sh $startdir/pkg/etc/gw6c/template/
	install -m 644 $startdir/src/build/man/man5/gw6c.conf.5 $startdir/pkg/usr/share/man/man5/
	install -m 644 $startdir/src/build/man/man8/gw6c.8 $startdir/pkg/usr/share/man/man8/
	install -m 644 $startdir/src/gw6c-6_0-RELEASE/CLIENT-LICENSE.TXT $startdir/pkg/usr/share/licenses/$pkgname
}


