# Maintainer: orbisvicis <''.join(chr(ord(c)-1) for c in "pscjtwjdjtAhnbjm/dpn")>

pkgname=sozi-git
pkgver=20110916
pkgrel=1
pkgdesc="An Inkscape extension that creates SVG-based presentations, using JavaScript"
arch=("any")
url="http://${pkgname%-git}.baierouge.fr/"
license=("GPL3" "MIT")
depends=("inkscape" "pygtk" "python-lxml")
makedepends=("git" "ruby-juicer" "yuicompressor")
optdepends=()
provides=("sozi")
conflicts=("sozi")
options=()

_gitroot="git://github.com/senshu/Sozi.git"
_gitname="${pkgname%-git}"

build() {
  local _gitdir="${srcdir}/${_gitname}"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitdir" ]]; then
    cd "$_gitdir" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitdir"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  cd "$_gitdir"
  git clean -dfx
  git reset --hard HEAD

  # Build
  mkdir release
  YUIC_HOME="/usr/share/java" make
}

package() {
  local _gitdir="${srcdir}/${_gitname}"
  cd "${_gitdir}/release"

  install -m755 -d "${pkgdir}/usr/share/inkscape/extensions"
  install -m755 -t "${pkgdir}/usr/share/inkscape/extensions" *.py
  install -m644 -t "${pkgdir}/usr/share/inkscape/extensions" *.css *.inx *.js

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname%-git}"
  install -m644 -t "${pkgdir}/usr/share/licenses/${pkgname%-git}" *license*
}

# vim:set ts=2 sw=2 et:
