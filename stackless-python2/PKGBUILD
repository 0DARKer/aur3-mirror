# Maintainer: Jerome Rose <jrose.pub at gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=stackless-python2
pkgver=2.7.2
pkgrel=4
pkgdesc="Python is a high-level scripting language. Stackless is an experimental implementation that supports continuations, generators, microthreads, and coroutines."
arch=('i686' 'x86_64')
url="http://www.stackless.com"
depends=('bzip2' 'expat' 'gdbm' 'libffi' 'openssl' 'zlib')
makedepends=('python2')
optdepends=('tk: for tkinter')
confilcts=('python2')
provides=('python2=2.7.2')
license=('python')
source="http://www.stackless.com/binaries/stackless-${pkgver//./}-export.tar.bz2"
md5sums=('79a718db998f2cdd95478d2cb54d56f2')

build() {
	cd "$srcdir/stackless-${pkgver//./}-export"

	grep -rl "env python" . | xargs \
		sed -i 's|/usr/bin/env python|/usr/bin/env python2|'

	./configure \
		--prefix=/usr \
		--with-threads \
		--enable-shared \
		--enable-stacklessfewerregisters

	make
}

package() {
	cd "$srcdir/stackless-${pkgver//./}-export"
	make DESTDIR="$pkgdir/" install
	cd "$pkgdir/usr/bin"
	mv python python2
}
