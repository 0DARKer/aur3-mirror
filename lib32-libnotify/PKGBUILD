# $Id: PKGBUILD 99716 2010-11-16 22:24:02Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libnotify
pkgname=lib32-libnotify
pkgver=0.5.2
pkgrel=2
pkgdesc="Desktop notification library (32-bit)"
arch=('i686' 'x86_64')
url="http://www.galago-project.org/"
license=('LGPL')
depends=('lib32-dbus-glib>=0.92' 'lib32-gtk2>=2.22.1' 'libnotify')
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/0.5/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('1f6f1a2d6a0e009997c7ed31f6197f9bf09e9b45fb9cd54acabceea951d99062')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
