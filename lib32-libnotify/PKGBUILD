# Contributor: TryA <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libnotify
pkgname=lib32-$_pkgbasename
pkgver=0.7.4
pkgrel=1
pkgdesc="Desktop notification library (32-bit)"
arch=('x86_64')
url="http://library.gnome.org/devel/notification-spec/"
license=('LGPL')
depends=('lib32-gdk-pixbuf2' 'libnotify')
makedepends=('lib32-gtk3' 'gcc-multilib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/${pkgver%.*}/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('fd204a6491d826174863f16e289a2069504d9ca109e4acd7ba88cd42869b9fc6')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static --enable-introspection=no
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
