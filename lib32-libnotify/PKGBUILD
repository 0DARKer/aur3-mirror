# Maintainer: TryA <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libnotify
pkgname=lib32-libnotify
pkgver=0.7.1
pkgrel=1
pkgdesc="Desktop notification library (32-bit)"
arch=('x86_64')
url="http://library.gnome.org/devel/notification-spec/"
license=('LGPL')
depends=('lib32-gdk-pixbuf2' 'libnotify')
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/0.7/${_pkgbasename}-${pkgver}.tar.bz2
        libnotify-0.7.1-gtk3-tests.patch)
sha256sums=('84dc2a77defb25521d2f1cb7ac34b4d23b55254ba361442658430efb928536c8'
            'd9ee539a58a30ccd213b9857003c2ba04a0cf343763ed566ca0d769cbfc34664')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i "${srcdir}/libnotify-0.7.1-gtk3-tests.patch"
  autoreconf -fi
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static --enable-introspection=no
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
