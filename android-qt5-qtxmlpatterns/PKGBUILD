# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=android-qt5-qtxmlpatterns
pkgver=5.1.0
pkgrel=1
pkgdesc="Qt 5 for Android: qtxmlpatterns module"
groups=('qt-android' 'qt5-android')
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/Qt5ForAndroid"
license=('LGPL')
depends=('android-qt5-qtbase')
makedepends=("android-qt5-qtbase=${pkgver}")
options=('!libtool' '!strip')
_pkgver="${pkgver/_/-}"
_pkgfqn="qtxmlpatterns-opensource-src-${_pkgver}"
source=("http://download.qt-project.org/official_releases/qt/5.1/${_pkgver}/submodules/${_pkgfqn}.tar.xz")
md5sums=('1d1ebc4d6efa4d05368c0c716858d314')

build() {
  cd ${srcdir}/${_pkgfqn}

  /opt/android-qt5/${_pkgver}/bin/qmake
  make
}

package() {
  cd ${srcdir}/${_pkgfqn}

  make INSTALL_ROOT="${pkgdir}" install
  
  # Strip by hand, system strip doesn't work on arm binaries

  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/${_pkgver}/lib/*.so*
}
