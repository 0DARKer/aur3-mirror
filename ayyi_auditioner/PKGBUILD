# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=ayyi_auditioner
pkgver=0.1.1
pkgrel=1
pkgdesc="A Jack service that can playback audio samples triggered via Alsa Midi and Dbus."
arch=(i686 x86_64)
url="http://www.ayyi.org/"
license=('GPL3')
depends=('jack' 'dbus-glib')
options=('!libtool' '!emptydirs')
install="$pkgname.install"
source=("${url}files/$pkgname-$pkgver.tar.gz")
md5sums=('19ae6b4ec5baa19f161beb820a2682f5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # install dssi plugin and desktop in right paths
  sed -e 's|$(data.*\(ayyi/auditioner\)|/usr/lib/dssi/\1|g' \
      -e 's|ayyi/clients|applications|g' \
      -i `find . -name "Makefile.*"`

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
