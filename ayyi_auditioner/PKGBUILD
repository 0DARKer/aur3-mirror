# Maintainer: speps <speps at aur dot archlinux dot org>

DLAGENTS[1]+=' -A "Mozilla/4.0"'

pkgname=ayyi_auditioner
pkgver=0.1.1
pkgrel=2
pkgdesc="A Jack service that can playback audio samples triggered via Alsa Midi and Dbus."
arch=(i686 x86_64)
url="http://www.ayyi.org/"
license=('GPL3')
depends=('jack' 'dbus-glib')
options=('!libtool' '!emptydirs')
install="$pkgname.install"
source=("${url}files/$pkgname-$pkgver.tar.gz" $pkgname-{dssi,path}.patch)
md5sums=('19ae6b4ec5baa19f161beb820a2682f5'
         '49b470d1ba35cf5ad39c6dca00788950'
         '30c65606324b02acdef36e530063f5ea')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # dssi patch
  patch -p1 -i ../${source[1]}
  # paths patch
  patch -p1 -i ../${source[2]}
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  LDFLAGS+=" -lm -ldl" \
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
