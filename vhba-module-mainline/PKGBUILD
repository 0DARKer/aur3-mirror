# Maintainer: prettyvanilla <prettyvanilla@lavabit.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

_pkgname=vhba-module
pkgname=vhba-module-mainline
pkgver=20120422
_extramodules=extramodules-3.8-mainline
pkgrel=1
pkgdesc="Kernel module that emulates SCSI devices. For linux-mainline."
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('linux-mainline>=3.8' 'linux-mainline<3.9' 'vhba-module')
makedepends=('linux-mainline-headers')
options=(!makeflags)
install=$pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$_pkgname-$pkgver.tar.bz2")
md5sums=('d97372da1d270d1605742b2995fb6678')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"

  make KDIR=/usr/lib/modules/$_kernver/build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  install -Dm644 vhba.ko "$pkgdir/usr/lib/modules/$_extramodules/vhba.ko"
}
