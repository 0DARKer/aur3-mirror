# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbase=glib-networking
pkgname=lib32-$_pkgbase
pkgver=2.30.2
pkgrel=1
pkgdesc="Network-related giomodules for glib (32 bit)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('lib32-glib2' 'lib32-libproxy' 'lib32-gnutls' 'lib32-libgcrypt') # 'ca-certificates' 'gsettings-desktop-schemas'
makedepends=('intltool' 'gcc-multilib')
options=('!libtool')
install=glib-networking.install
source=(http://download.gnome.org/sources/$_pkgbase/${pkgver%.*}/$_pkgbase-$pkgver.tar.xz)
sha256sums=('9f7542500939ca25896f0a74955cd3889758524583f6a15d02e00c9b815559bc')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "$srcdir/$_pkgbase-$pkgver"
  
  ./configure \
    --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib32/glib-networking \
    --disable-static --libdir=/usr/lib32
    
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"  
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share"
}

