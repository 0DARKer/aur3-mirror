# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: ultraviolet <ultravioletnanokitty@gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=emprint-git
_pkgname=emprint
pkgver=0.1.0.67.a0886e1
pkgrel=1
pkgdesc="EFL based utility for taking screenshots of the entire screen, a specific window, or a specific region"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('MIT')
depends=('ecore')
makedepends=('git')
provides=('emprint-svn-arche17')
conflicts=('emprint-svn-arche17')
options=('!libtool')
source=("git://git.enlightenment.org/apps/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  
  echo $(awk -F , '/^AM_INIT_AUTOMAKE/ {print $2}' configure.ac | tr -d ' )').$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 COPYING-PLAIN "$pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN"
  install -Dm644 AUTHORS "$pkgdir/usr/share/licenses/$pkgname/AUTHORS"
}
