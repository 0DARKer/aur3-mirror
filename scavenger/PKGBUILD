# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=scavenger
pkgver=145.1
pkgrel=1
pkgdesc="2D arcade/thinking game like Lode Runner (SDL version)"
arch=('i686' 'x86_64')
url="http://www.xdr.com/dash/scavenger.html"
license=('GPL')
depends=('sdl')
source=("http://downloads.sourceforge.net/sdlscavenger/sdlscav-${pkgver}_2010_11_5.tgz"
        "${pkgname}.desktop")
md5sums=('995dc8b6b1792e1c2c9ec253bcfafbfe'
         '8157a7932c3857c337bf738c80f47c5f')

build() {
  cd "${srcdir}/sdlscav-145"
  sed -i "8,9d" Makefile
  sed -i "s_/usr/local/games_/usr/share_" names.h
  make || return 1
}

package() {
  cd "${srcdir}/sdlscav-145"

  # Install game files
  mkdir -p "${pkgdir}/usr/share/${pkgname}/rc"
  install -m644 data/*rc "${pkgdir}/usr/share/${pkgname}/rc"
  install -m644 data/*.{lbm,scl,wav} "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 sdlscav "${pkgdir}/usr/bin/${pkgname}"

  # Install the pixmap and desktop file
  install -Dm644 data/scav.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
