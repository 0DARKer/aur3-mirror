# Maintainer: Ralph Amissah <ralph.amissah@gmail.com>
# sisu
# SET pkgver & sha256sums
pkgname=('sisu')
pkgdesc="documents - structuring, publishing in multiple formats & search"
pkgver=7.0.0
pkgrel=1
#source=("http://ftp.de.debian.org/debian/pool/main/s/sisu/${pkgname}_${pkgver}.orig.tar.xz")
source=("http://www.sisudoc.org/sisu/archive/pool/main/s/sisu/${pkgname}_${pkgver}.orig.tar.xz")
#source=("http://www.jus.uio.no/sisu/archive/pool/main/s/sisu/${pkgname}_${pkgver}.orig.tar.xz")
sha256sums=('8ca6980a21d21fa6b6a9a83a37f8e0f22aabfe0c09a1e2553257ec61c20fdb12')
arch=('any')
url="http://sisudoc.org"
license=('GPL3')
depends=('ruby')
makedepends=('ruby')
optdepends=(
    'zip: epub & odf:odt files'
    'unzip: epub & odf:odt files (not required)'
    'tree: report directory structure'
    'imagemagick: image support'
    'graphicsmagick: image support'
    'texlive-core: pdf'
    'texlive-latexextra: pdf'
    'texlive-fontsextra: pdf'
    'texlive-langcjk: pdf Chinese Japanese Korean (large optional dependency, comment out if not needed)'
    'sqlite: search, populate sqlite db'
    'ruby-sqlite3: search, populate sqlite db'
    'postgresql: search, populate postgresql db'
    'ruby-pg: search, populate postresql db'
    'ruby-fcgi: search, sample search form'
    'qrencode: qrcodes based on document metadata'
  )
conflicts=('sisu-gem' 'sisu-git' 'sisu-git-gem')
options=(!emptydirs)
install=
changelog=
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  msg "Configuring source..."
  local _rbsitelib="$(ruby -e"require 'rbconfig';print RbConfig::CONFIG['sitelibdir'].gsub(/^\//,'')")"
  ruby ./setup.rb config --prefix=${pkgdir}/usr \
    --siterubyver=${pkgdir}/${_rbsitelib} \
    --sysconfdir=${pkgdir}/etc
  msg "Running setup..."
  ruby ./setup.rb setup
}
package() {
  msg "Installing files..."
  cd "${srcdir}/${pkgname}-${pkgver}"
  ruby ./setup.rb install
}
