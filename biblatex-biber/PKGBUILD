# Maintainer: Andreas dot Wagner at em dot uni-frankfurt dot de

pkgname=biblatex-biber
pkgver=0.9
pkgrel=1
pkgdesc="A BibTeX replacement for users of biblatex"
url="http://biblatex-biber.sourceforge.net"
arch=('any')
license=('PerlArtistic')
depends=( \
  'perl-unicode-collate>=0.73' \
  'perl-ipc-run' \
  'perl-file-slurp-unicode' \
  'perl-readonly-xs' \
  'perl-list-allutils' \
  'perl-log-log4perl' \
  'perl-xml-libxml-simple' \
  'perl-text-bibtex>=0.53' \
  'perl-libwww' \
  'perl-config-general' \
  'perl-data-compare' \
  'perl-data-dump' \
  'perl-date-simple' \
  'perl-regexp-common' \
  'perl-xml-libxslt' \
  'texlive-local-biblatex>=22001-1' \
#  'texlive-bibtexextra>=2010.20942-1' \
#  'perl-xml-writer' \
  )
makedepends=( \
	    'perl-config-autoconf' \
	    'perl-extutils-libbuilder' \
)
optdepends=('perl-sleepycat-dbxml: support for Berkeley DBXML databases' )
conflicts=('biblatex-biber-git')
source=(http://downloads.sf.net/project/$pkgname/$pkgname/current/$pkgname.tar.gz)

build() {
  cd "${pkgname}-${pkgver}"
  perl Build.PL installdirs=vendor destdir="$pkgdir/"
  perl Build
  perl Build test
}


package() {
  cd "${pkgname}-${pkgver}"
  perl Build install

  mv "$pkgdir/usr/bin/vendor_perl/biber" "$pkgdir/usr/bin/biber"
  rm -rf "$pkgdir/usr/bin/vendor_perl"

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}

md5sums=('87a6531522d1caef347939e87fe56e87')
