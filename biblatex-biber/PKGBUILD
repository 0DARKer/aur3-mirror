# Maintainer: Andreas dot Wagner at em dot uni-frankfurt dot de

pkgname=biblatex-biber
pkgver=0.9.4
pkgrel=1
pkgdesc="A BibTeX replacement for users of biblatex"
url="http://biblatex-biber.sourceforge.net"
arch=('any')
license=('PerlArtistic')
depends=( \
  'perl-config-general' \
  'perl-data-dump' \
  'perl-data-compare' \
  'perl-date-simple' \
  'perl-file-slurp-unicode' \
  'perl-ipc-cmd' \
  'perl-ipc-run3' \
  'perl-list-allutils' \
#  'perl-list-moreutils' \ # implied in allutils
  'perl-regexp-common' \
  'perl-log-log4perl' \
  'perl-readonly' \
  'perl-unicode-collate>=0.78' \
  'perl-xml-libxml-simple' \
  'perl-xml-libxslt' \
  'perl-text-bibtex>=0.59' \
  'perl-libwww' \
  'texlive-bibtexextra>=2010.22146-1' \
##  'perl-xml-writer' \
  )

makedepends=( \
	    'perl-config-autoconf' \
	    'perl-extutils-libbuilder' \
)

optdepends=('perl-sleepycat-dbxml: support for Berkeley DBXML databases' \
            'perl-readonly-xs' )

conflicts=('biblatex-biber-git')
# source=(http://downloads.sf.net/project/$pkgname/$pkgname/$pkgver/$pkgname.tar.gz)
source=(http://downloads.sf.net/project/$pkgname/$pkgname/current/$pkgname.tar.gz)

build() {
  cd "${pkgname}-v${pkgver}"
  perl Build.PL installdirs=vendor destdir="$pkgdir/"
  perl Build
  perl Build test
}


package() {
  cd "${pkgname}-v${pkgver}"
  perl Build install

  mv "$pkgdir/usr/bin/vendor_perl/biber" "$pkgdir/usr/bin/biber"
  rm -rf "$pkgdir/usr/bin/vendor_perl"

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}

md5sums=('5173d34b0c0db6cd217f9127f004822b')
