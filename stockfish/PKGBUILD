# Maintainer: Auguste Pop < auguste [at] gmail [dot] com >
# Contributor: P. Badredin <p dot badredin at gmail dot com>
# Contributor: Justin Blanchard <UncombedCoconut at gmail dot com>

pkgname=stockfish
pkgver=2.3
_bookver=2.1.1
pkgrel=1
pkgdesc="A strong chess engine written by Tord Romstad, Marco Costalba, Joona Kiiski"
arch=('i686' 'x86_64')
url="http://www.stockfishchess.com/"
license=('GPL3')
install=stockfish.install
_pkgfilever=${pkgver//./}
_bookfilever=${_bookver//./}
source=("http://cl.ly/3z0Z2q0Z2B0o/download/${pkgname}-${_pkgfilever}-linux.zip"
        "http://api.cld.me/2R1L1Y0V0b1M3M1B1D26/download/sf-${_bookfilever}-book.zip")
md5sums=('4e828c49da71727eb4f12ec55560630e'
         'c41f7a3e30a141415591dba2355deccb')

build()
{
    cd "$srcdir/${pkgname}-${_pkgfilever}-linux/src"
    # Change the default book path to a system-wide location
    sed -i '/Book File/s:book.bin:/usr/share/stockfish/book.bin:' ucioption.cpp

    if [[ "$CARCH" == "i686" ]];
    then
        _arch=x86-32
    elif grep popcnt /proc/cpuinfo 2>&1
    then
        _arch=x86-64-modern
    else
        _arch=x86-64
    fi
    make profile-build ARCH=$_arch
}

package()
{
    cd "$srcdir/sf-${_bookfilever}-book"
    install -D -m644 Book.bin $pkgdir/usr/share/$pkgname/book.bin

    cd "$srcdir/stockfish-${_pkgfilever}-linux/src"
    make PREFIX="$pkgdir/usr" install
}
