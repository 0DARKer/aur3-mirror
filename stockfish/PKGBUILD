# Maintainer: Auguste Pop < auguste [at] gmail [dot] com >
# Contributor: P. Badredin <p dot badredin at gmail dot com>
# Contributor: Justin Blanchard <UncombedCoconut at gmail dot com>

pkgname=stockfish
pkgver=2.2
_bookver=2.1.1
pkgrel=2
pkgdesc="A strong chess engine written by Tord Romstad, Marco Costalba, Joona Kiiski"
arch=('i686' 'x86_64')
url="http://www.stockfishchess.com/"
license=('GPL3')
install=stockfish.install
source=("http://api.cld.me/3f1E2D1F322y0z1F1k2b/download/stockfish-${pkgver//./}-linux.zip"
        "http://api.cld.me/2R1L1Y0V0b1M3M1B1D26/download/sf-${_bookver//./}-book.zip")
md5sums=('6071b67bebffd002a113433060e88ce4'
         'c41f7a3e30a141415591dba2355deccb')


build()
{
    cd "$srcdir/stockfish-${pkgver//./}-linux/src"
    # Change the default book path to a system-wide location
    sed -i '/Book File/s:book.bin:/usr/share/stockfish/book.bin:' ucioption.cpp
    # Patch Makefile to enable profile-build
    sed -i 's/\(.*\)LDFLAGS += $(CXXFLAGS)/\1LDFLAGS += -flto/' Makefile

    [ "$CARCH" = "i686" ] && _arch=x86-32 || _arch=x86-64
    make profile-build ARCH=$_arch
}

package()
{
    cd "$srcdir/sf-${_bookver//./}-book"
    install -D -m644 Book.bin $pkgdir/usr/share/$pkgname/book.bin

    cd "$srcdir/stockfish-${pkgver//./}-linux/src"
    make PREFIX="$pkgdir/usr" install
}
