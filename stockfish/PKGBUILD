# Maintainer: Auguste Pop < auguste [at] gmail [dot] com >
# Contributor: P. Badredin <p dot badredin at gmail dot com>
# Contributor: Justin Blanchard <UncombedCoconut at gmail dot com>

pkgname=stockfish
pkgver=2.3.1
_bookver=2.3.1
pkgrel=2
pkgdesc="A strong chess engine written by Tord Romstad, Marco Costalba, Joona Kiiski"
arch=('i686' 'x86_64')
url="http://www.stockfishchess.com/"
license=('GPL3')
install=${pkgname}.install
_pkgfilever=${pkgver//./}
_bookfilever=${_bookver//./}
source=("http://cl.ly/2t3y2k2Y3Y1H/download/${pkgname}-${_pkgfilever}-linux.zip"
        "http://cl.ly/3x333m0G173F/download/${pkgname}-${_bookfilever}-book.zip")
md5sums=('901c5f5ccfeae9b912690ec9a3d59ab2'
         '9e51c2e57d8b55bbc588150033e4b133')

prepare()
{
    cd "$srcdir/${pkgname}-${_pkgfilever}-linux/src"
    # Change the default book path to a system-wide location
    sed -i '/Book File/s:book.bin:/usr/share/stockfish/book.bin:' ucioption.cpp
}

build()
{
    cd "$srcdir/${pkgname}-${_pkgfilever}-linux/src"

    if [[ "$CARCH" == "i686" ]];
    then
        _arch=x86-32
    elif grep popcnt /proc/cpuinfo 2>&1
    then
        _arch=x86-64-modern
    else
        _arch=x86-64
    fi
    make build ARCH=$_arch
}

package()
{
    cd "$srcdir"
    install -D -m644 Book.bin $pkgdir/usr/share/$pkgname/book.bin

    cd "$srcdir/${pkgname}-${_pkgfilever}-linux/src"
    make PREFIX="$pkgdir/usr" install
}
