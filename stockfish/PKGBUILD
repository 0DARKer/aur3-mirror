# Maintainer: Auguste Pop < auguste [at] gmail [dot] com >
# Contributor: P. Badredin <p dot badredin at gmail dot com>
# Contributor: Justin Blanchard <UncombedCoconut at gmail dot com>

pkgname=stockfish
pkgver=2.2.1
_bookver=2.1.1
pkgrel=1
pkgdesc="A strong chess engine written by Tord Romstad, Marco Costalba, Joona Kiiski"
arch=('i686' 'x86_64')
url="http://www.stockfishchess.com/"
license=('GPL3')
install=stockfish.install
_pkgfilever=${pkgver//./}
_bookfilever=${_bookver//./}
source=("http://api.cld.me/1T1R0b0z3u3A3B363o2l/download/stockfish-${_pkgfilever}-linux.zip"
        "http://api.cld.me/2R1L1Y0V0b1M3M1B1D26/download/sf-${_bookfilever}-book.zip")
md5sums=('0ec3f2f0899f00a25dda135111e3c712'
         'c41f7a3e30a141415591dba2355deccb')


build()
{
    cd "$srcdir/stockfish-${_pkgfilever}-linux/src"
    # Change the default book path to a system-wide location
    sed -i '/Book File/s:book.bin:/usr/share/stockfish/book.bin:' ucioption.cpp
    # Patch Makefile to enable profile-build
    sed -i 's/\(.*\)LDFLAGS += $(CXXFLAGS)/\1LDFLAGS += -flto/' Makefile

    [ "$CARCH" = "i686" ] && _arch=x86-32 || _arch=x86-64
    make profile-build ARCH=$_arch
}

package()
{
    cd "$srcdir/sf-${_bookfilever}-book"
    install -D -m644 Book.bin $pkgdir/usr/share/$pkgname/book.bin

    cd "$srcdir/stockfish-${_pkgfilever}-linux/src"
    make PREFIX="$pkgdir/usr" install
}
