# Maintainer: Auguste Pop < auguste [at] gmail [dot] com >
# Contributor: P. Badredin <p dot badredin at gmail dot com>
# Contributor: Justin Blanchard <UncombedCoconut at gmail dot com>

pkgname=stockfish
pkgver=5
_bookver=2.3.1
pkgrel=1
epoch=1
pkgdesc="A strong chess engine written by Tord Romstad, Marco Costalba, Joona Kiiski"
arch=('i686' 'x86_64')
url="http://stockfishchess.org/"
license=('GPL3')
install=${pkgname}.install
_pkgfilename=${pkgname}-${pkgver}-src
_bookfilever=${_bookver//./}
source=("http://stockfish.s3.amazonaws.com/${_pkgfilename}.zip"
        "http://cl.ly/3x333m0G173F/download/${pkgname}-${_bookfilever}-book.zip")
md5sums=('181b6dcd8c7bb24ae3c3d1395455efc0'
         '9e51c2e57d8b55bbc588150033e4b133')

prepare()
{
    cd "$srcdir/${_pkgfilename}/src"
    # Change the default book path to a system-wide location
    sed -i '/Book File/s:book.bin:/usr/share/stockfish/book.bin:' ucioption.cpp
}

build()
{
    cd "$srcdir/${_pkgfilename}/src"

    if [[ "$CARCH" == "i686" ]];
    then
        _arch=x86-32
    elif grep popcnt /proc/cpuinfo 2>&1
    then
        _arch=x86-64-modern
    else
        _arch=x86-64
    fi
    make build ARCH=$_arch
}

package()
{
    cd "$srcdir"
    install -D -m644 Book.bin $pkgdir/usr/share/$pkgname/book.bin

    cd "$srcdir/${_pkgfilename}/src"
    make PREFIX="$pkgdir/usr" install
}
