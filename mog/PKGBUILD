# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=mog
pkgver=0.63_1305
pkgrel=3
pkgdesc='A remake of the classic platformer "Knightmare II: The Maze of Galious"'
arch=('i686' 'x86_64')
url="http://www.braingames.getput.com/mog/"
license=('GPL')
depends=('bash' 'gcc-libs' 'sdl_image' 'sdl_mixer' 'sdl_sound')
source=(http://braingames.jorito.net/mog/downloads/mog.src_0.63-1305.tgz \
	$pkgname.sh \
	null.wav)
md5sums=('49b6ab183396793475e4030fe9465d94'
         '68c37b2a2f84407e24b37aa2d8379c23'
         '214778ecee72b062bde3b1074ca05071')

build() {
  cd mog.src_${pkgver//_/-}

  # remove non-free graphics and sounds and replace them

  rm -rf {graphics,sound}/original

  sed 's|graphics/original|graphics/alternate|g' -i sources/main.cpp
  sed 's|sound/original|sound/jorito|g' -i sources/main.cpp
  
  for i in lavahit demon2ball owl; do
	cp -f ../null.wav sound/jorito/$i.wav
  done

  # done

  find -name 'demon-vfast.ogg' -exec chmod 644 {} \;

  sed 's|= -g3 -O3|+=|g' -i Makefile
  export CFLAGS="${CXXFLAGS}"

  make || return 1
  make	PREFIX="$pkgdir"/usr \
	GAMEDIR="$pkgdir"/usr/bin \
	BINDIR="$pkgdir"/usr/share/mog \
	install

  install -m755 ../$pkgname.sh "$pkgdir"/usr/bin/$pkgname
}

# vim:set ts=2 sw=2 et:
