# Maintainer: David Spicer <azleifel at gmail dot com>

pkgname=vdr-softhddevice-git
pkgver=20130404
pkgrel=1
pkgdesc="A software and GPU emulated HD output device plugin for VDR"
arch=('i686' 'x86_64')
url="http://projects.vdr-developer.org/projects/plg-softhddevice"
license=('AGPL3')
depends=('ffmpeg' 'vdr>=2.0.0' 'xcb-util-wm')
makedepends=('xcb-util' 'xcb-util-keysyms')
optdepends=('libva-intel-driver: Intel integrated graphics backend for VA-API'
            'libva-vdpau-driver: VDPAU backend for VA-API'
            'xvba-video-open: XVBA backend for VA-API')
provides=('vdr-softhddevice')
conflicts=('vdr-softhddevice')

_gitroot=git://projects.vdr-developer.org/vdr-plugin-softhddevice.git
_gitname=vdr-plugin-softhddevice.git

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  # Clone source to fresh build directory
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "${srcdir}/$_gitname-build"

  make
}

package() {
  cd "${srcdir}/$_gitname-build"

  make DESTDIR="${pkgdir}" install

  # Install documents
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  for _file in README.txt ChangeLog; do
    install -m644 ${_file} "${pkgdir}/usr/share/doc/${pkgname}"
  done
}
