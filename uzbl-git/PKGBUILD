# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Mark Foxwell <fastfret79@archlinux.org.uk>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>
# Contributor: Brendan Taylor <whateley@gmail.com>
# Contributor: L42y <423300@gmail.com>

pkgname=uzbl-git
pkgver=20130329
pkgrel=1
pkgdesc="All uzbl web interface tools: latest from the git master branch"
arch=('i686' 'x86_64')
url="http://github.com/Dieterbe/uzbl"
license=('GPL3')
depends=('webkitgtk3' 'socat' 'zenity' 'xclip' 'dmenu' 'python' 'pygtk')
makedepends=('git' 'pkgconfig')
provides=('uzbl-core' 'uzbl-browser' 'uzbl-tabbed')
conflicts=('uzbl-core' 'uzbl-browser' 'uzbl-tabbed' 'uzbl-experimental-git')
source=(Makefile.patch)
md5sums=('2f7ac68ab8406501741421c5db284bb2')

_gitroot="git://github.com/Dieterbe/uzbl.git"
_gitname="uzbl"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -Np0 -i "$srcdir"/Makefile.patch

  make
}

package(){
   cd "$srcdir/$_gitname-build"

   make DESTDIR="$pkgdir"/ PREFIX=/usr install
}
