# Current maintainer: Magnus Therning <magnus@therning.org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=hexer
pkgver=0.1.5
pkgrel=3
pkgdesc="A multi buffer editor for binary files with vi-like interface"
arch=('i686' 'x86_64')
url="http://devel.ringlet.net/"
license=('custom')
depends=('ncurses')
source=(http://devel.ringlet.net/editors/$pkgname/$pkgname-$pkgver.tar.gz)
changelog=$pkgname.changelog

build() {
    cd $srcdir/$pkgname-$pkgver

    cp config.{linux,h}
    sed -i "s_/usr/local_/usr_" Makefile
    sed -i "s_/man/_/share/man/_" Makefile
    make
}

package() {
    cd $srcdir/$pkgname-$pkgver

    install -m755 -d $pkgdir/usr/{bin,share/man/man1}
    make DESTDIR=$pkgdir install
    install -m644 -D COPYRIGHT $pkgdir/usr/share/licenses/$pkgname/COPYING
}

sha256sums=('bd171b4ee4c678f5c545fda1431aebbea01acff9d4bc2adbbb8584a8b2d31a02')
