# $Id: PKGBUILD 160261 2012-05-31 17:12:14Z andyrtr $
# AUR Maintainer: Timoth√©e Ravier <tim@siosm.fr>
# AUR Maintainer: aboe76 AT gmail DOT com>
# contribution: EdwardXXIV
# Core Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=pixman-git
_gitname='pixman'
pkgver=pixman.0.30.0.18.g279bdcd
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(i686 x86_64)
url="http://pixman.org/"
license=('custom')
depends=('glibc')
options=('!libtool')
conflicts=('pixman')
provides=('pixman')
makedepends=('git')

# makepkg 4.1 knows about git and will pull main branch
source=("git://anongit.freedesktop.org/${_gitname}")

# makepkg knows it's a git repo because the url starts with 'git'
# it then knows to checkout the branch upon cloning, expediating versioning.
#branch="develop"
#source=("git://anongit.freedesktop.org/${_gitname}#branch=$branch")

# makepkg also knows about tags
#tags="v0.14.1"
#source=("git://anongit.freedesktop.org/${_gitname}#tag=$tag")

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git describe --always | sed 's/-/./g')
  # for git, if the repo has no tags, comment out the above and uncomment the next line:
  #echo "0.$(git rev-list --count $branch).$(git describe --always)"
  # This will give you a count of the total commits and the hash of the commit you are on.
  # Useful if you're making a repository with git packages so that they can have sequential
  # version numbers. (Else a pacman -Syu may not update the package)
}

# because the sources are not static, skip checksums
md5sums=('SKIP')

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${_gitname}"
  make check
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${_realname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_realname}/"
}
