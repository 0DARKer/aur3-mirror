# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Figo.zhang <figo1802@gmail.com>
# Contributor: hauptmech

pkgname=flann
pkgver=1.7.1
pkgrel=1
pkgdesc="FLANN is a library for performing fast approximate nearest neighbor searches in high dimensional spaces"
arch=('i686' 'x86_64')
url='http://www.cs.ubc.ca/~mariusm/index.php/FLANN/FLANN'
license=('BSD')
makedepends=('cmake' 'python2')
optdepends=('python2: python bindings')
source=("http://people.cs.ubc.ca/~mariusm/uploads/FLANN/flann-${pkgver}-src.zip")
md5sums=('d780795f523eabda7c7ea09c6f5cf235')

build() {
  cd $srcdir/flann-${pkgver}-src

  sed -i 's/lib64/lib/g' cmake/flann_utils.cmake

  mkdir build && cd build
  cmake .. \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_MATLAB_BINDINGS=OFF \
      -DBUILD_PYTHON_BINDINGS=ON \
      -DPYTHON_EXECUTABLE=/usr/bin/python2
  make

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/python2|' \
      ../bin/download_checkmd5.py \
      ../bin/run_test.py \
      ../build/src/python/setup.py \
      ../src/python/setup.py \
      ../src/python/setup.py.tpl \
      ../test/test_clustering.py \
      ../test/test_index_save.py \
      ../test/test_nn_autotune.py \
      ../test/test_nn_index.py \
      ../test/test_nn.py

  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' \
      ../test/memusage_clustering.py \
      ../test/memusage_nn.py

}

package() {
  cd $srcdir/flann-${pkgver}-src
  
  cd build
  make DESTDIR=$pkgdir install

  #install license file
  install -D -m644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # FIXME: awful hack, but I got this error without the fix:
  # running install_lib
  # copying build/lib/pyflann/exceptions.py -> /usr/lib/python2.7/site-packages/pyflann
  # error: /usr/lib/python2.7/site-packages/pyflann/exceptions.py: Permission denied
  _python2libpath="`python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()" | tr -d '\n'`"
  mkdir -p "${pkgdir}${_python2libpath}"
  cp -pr "${pkgdir}/usr/share/flann/python/pyflann" "${pkgdir}${_python2libpath}/pyflann"

}

