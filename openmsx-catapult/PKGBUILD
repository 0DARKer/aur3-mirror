# Contributor: Tom < reztho at archlinux dot us >
pkgname=openmsx-catapult
pkgver=0.8.0
pkgrel=1
pkgdesc="Front-end for openMSX: the MSX emulator that aims for perfection."
arch=('i686' 'x86_64')
url="http://openmsx.sourceforge.net"
license=('GPL')
depends=('libxml2' 'wxgtk' 'zlib' 'libjpeg' 'libpng' 'libtiff' 'openmsx=0.8.0')
source=("http://downloads.sourceforge.net/${pkgname/-catapult/}/${pkgname}-${pkgver}.tar.gz"
	"openmsx-catapult-${pkgver}-main_mk.patch")

build() {
  cd ${srcdir}/${pkgname}-${pkgver/_/-}

  # Patching build/main.mk so catapult can be installed in $pkgdir
  patch -p1 < ${srcdir}/openmsx-catapult-${pkgver}-main_mk.patch || return 1
  
  # Changing some default configurations...
  sed -i 's@SYMLINK_FOR_BINARY:=true@SYMLINK_FOR_BINARY:=false@' build/custom.mk || return 1
  sed -i 's@/opt/openMSX-Catapult@/usr/share/openmsx-catapult@' build/custom.mk || return 1
  sed -i 's@/opt/openMSX/bin/openmsx@/usr/bin/openmsx@' build/custom.mk || return 1
  sed -i 's@/opt/openMSX/share@/usr/share/openmsx@' build/custom.mk || return 1
  echo 'INSTALL_DOC_DIR:=/usr/share/doc/openmsx-catapult' >> build/custom.mk
  echo 'INSTALL_SHARE_DIR:=/usr/share/openmsx-catapult' >> build/custom.mk
  echo 'INSTALL_BINARY_DIR:=/usr/bin' >> build/custom.mk
 
  # Compiling
  make || return 1
  mkdir -p ${pkgdir}/usr/share/applications
  make DESTDIR=${pkgdir} install || return 1

  # Fixing the .desktop file
  sed -i 's@/usr/share/openmsx-catapult/bin/catapult@/usr/bin/catapult@' \
  ${pkgdir}/usr/share/applications/openMSX-Catapult.desktop || return 1
  sed -i 's@/usr/share/openmsx-catapult/doc/@/usr/share/doc/openmsx-catapult/@' \
  ${pkgdir}/usr/share/applications/openMSX-Catapult.desktop || return 1
}

md5sums=('8764192012f3a2e59828f18be99c995b'
         '7417eddba99d34fd6445619ef13c1c28')
