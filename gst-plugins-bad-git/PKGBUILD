# Original Package: Jan de Groot <jgc@archlinux.org>
# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>

pkgname=gst-plugins-bad-git
pkgver=1.0.2.737.g58e1894
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
arch=('i686' 'x86_64')
license=('LGPL')
provides=('gst-plugins-bad=99')
conflicts=('gst-plugins-bad')
url="http://gstreamer.freedesktop.org/"
depends=('mjpegtools' 'gst-plugins-base-git' 'curl' 'chromaprint' 'libmms' 'faad2' 'mpg123' 'faac' 'celt' 'libdca' 'soundtouch' 'spandsp' 'libdvdnav' 'libmodplug' 'libgme' 'opus' 'wayland')
makedepends=('schroedinger' 'libexif' 'libdvdread' 'libvdpau' 'libmpeg2')
options=(!libtool !emptydirs)

source='git://anongit.freedesktop.org/gstreamer/gst-plugins-bad'
sha256sums=('SKIP')

_gitname='gst-plugins-bad'

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
  cd $_gitname
    
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-gtk=3.0
  make
}

package() {
  cd $_gitname
  make DESTDIR="${pkgdir}" install
}
