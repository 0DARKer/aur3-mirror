# Contributor: grimi <grimi at poczta dot fm>

pkgname=lib32-mpfr
pkgver=3.0.0
pkgrel=1
pkgdesc="Multiple-precision floating-point library (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.mpfr.org/"
groups=('lib32')
depends=('mpfr' 'lib32-gmp>=5.0')
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://www.mpfr.org/mpfr-current/mpfr-${pkgver}.tar.xz
        mpfr-3.0.0-p3.patch)
md5sums=('8ab3bef2864b8c6e6a291f5603141bbd'
         '4aa20e509e3117b04deae64c1be6ac62')

build() {
  cd "${srcdir}"/mpfr-${pkgver}
  patch -Np1 -i "${srcdir}"/mpfr-3.0.0-p3.patch
  ./configure --prefix=/usr --enable-thread-safe --enable-shared \
     --libdir='/usr/lib32' --with-gmp-include='/usr/lib32/gmp/include' \
     CC='gcc -m32' PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  make
  make check
}

package() {
  cd "${srcdir}"/mpfr-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{share,include}
}

