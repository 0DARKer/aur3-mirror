# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=nemo-media-columns
_ver=2.2.0+qiana
pkgver=2.2.0
pkgrel=1
pkgdesc="Nemo extension to display music/EXIF and PDF metadata info"
arch=('any')
url="http://packages.linuxmint.com/pool/main/n/nemo-media-columns/"
license=('GPL')
depends=('mutagen' 'nemo-python' 'python2-exiv2' 'python2-pillow' 'python2-pypdf')
source=("http://packages.linuxmint.com/pool/main/n/nemo-media-columns/nemo-media-columns_${_ver}.tar.gz"
        'fixes.patch')
sha512sums=('ed7add38ede6d96f5b9fde6ff081f21a340a939f9b2ad4751221e5e0d4b3be82f4fb0b0b5f1a46c2a38b5e2313b31dcc3f0ec4df597143d8cb797ca05053c4c4'
            '0f352129c727a74be9bdae32c86ea32a801449bcdeb86376cc10a53f9974441eb788f23156d58d97e2066eff2255aeb9c4ddd9c7ffd8430c4b957c57aa4cda2d')

prepare() {
  cd "${srcdir}/${pkgname}-${_ver}"
  sed -i "s|^\(#!.*python\)$|\12|" nemo-media-columns.py
  patch -p1 -i "${srcdir}/fixes.patch"
}

package() {
  cd "${srcdir}/${pkgname}-${_ver}"
  install -dm755 "${pkgdir}/usr/share/nemo-python/extensions/"
  install -m644 nemo-media-columns.py \
                "${pkgdir}/usr/share/nemo-python/extensions/"
}
