# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=autokey-gtk
pkgver=0.90.4
pkgrel=1
pkgdesc="A text expansion/replacement and hotkey utility for Linux and X11. Gtk3 UI."
arch=('any')
url="http://code.google.com/p/autokey/"
license=('GPL3')
depends=('python2-gobject' 'gtksourceview3' 'libnotify')
provides=("autokey=$pkgver" 'autokey-frontend')
conflicts=('autokey')
install="autokey-gtk.install"
source=("http://autokey.googlecode.com/files/autokey-$pkgver.tar.gz")
md5sums=('c2a74ddc2cb06c9798b7fa57fd65019a')

_python2_fix() {
  sed -i "s_env python_&2_" \
    `grep -rl "env python" "$pkgdir"`
}

package() {
  cd "$srcdir/autokey-$pkgver"
  python2 setup.py install --root="$pkgdir"

  # rm qt stuff
  rm "$pkgdir/usr/bin/autokey-qt"
  rm "$pkgdir/usr/share/man/man1/autokey-qt.1"
  rm "$pkgdir/usr/share/applications/autokey-qt.desktop"
  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/autokey/qt"*
  rm -rf "$pkgdir/usr/share/kde4"

  _python2_fix
}
