# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkgname=autokey
pkgname=$_pkgname-gtk
pkgver=0.71.1
pkgrel=1
pkgdesc="A text expansion/replacement and hotkey utility for Linux and X11. Gtk version."
arch=('any')
url="http://autokey.sourceforge.net/"
license=('GPLv3')
depends=('python-xlib' 'python-notify' 'python-configobj' 'pygobject' 'pygtk' 'librsvg' 'wmctrl')
conflicts=('autokey' 'autokey-qt')
provides=('autokey')
install="$pkgname.install"
source=("http://autokey.googlecode.com/files/${_pkgname}_${pkgver}.tar.gz")
md5sums=('81cd30dfcd3b94fcab4a6a675caeb445')

build() {
  cd "$srcdir/build"

  # Fix for python2
  sed -i "s_\(env python\).*_\12_" `grep -rl "env python" .`
}

package() {
  cd "$srcdir/build"

  python2 setup.py install --prefix=/usr --root="$pkgdir"

  install -Dm 755 debian/autokey-common.init "$pkgdir"/etc/rc.d/autokey

  # Remove unneeded Qt stuff
  rm -rf "$pkgdir"/usr/lib/python2.6/site-packages/autokey/qt*
  rm "$pkgdir"/usr/share/applications/autokey-qt.desktop
  rm "$pkgdir"/usr/share/man/man1/autokey-qt.1
  rm "$pkgdir"/usr/bin/autokey-qt
}
