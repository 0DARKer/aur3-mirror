# Contributor: Flamelab <panosfilip@gmail.com> 
# PKGBUILD based on libkmap-svn

pkgname=libkgeomap-git
pkgver=r514.c864ef7
pkgrel=1
pkgdesc='A wrapper around world map components for browsing and arranging photos on a map'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'kdeedu-marble')
makedepends=('git' 'automoc4' 'cmake')
conflicts=('libkgeomap')
provides=('libkgeomap')
groups=('digikam-git')
source=('git://anongit.kde.org/libkgeomap')
md5sums=('SKIP')

pkgver() {
  cd libkgeomap
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake  ../libkgeomap -DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}