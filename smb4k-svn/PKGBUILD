# Maintainer: Jordi De Groof <jordi.degroof@gmail.com>
pkgname=smb4k-svn
pkgver=1243294
pkgrel=1
pkgdesc="A KDE program that browses samba shares"
arch=('i686' 'x86_64')
url="http://smb4k.berlios.de/"
license=('GPL')
groups=()
depends=('kdebase-runtime')
makedepends=('subversion' 'cmake')
provides=('smb4k')
conflicts=('smb4k')

_svntrunk="svn://anonsvn.kde.org/home/kde/trunk/playground/network/smb4k"
_svnmod="smb4k"

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #
  cmake -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
	  -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}
