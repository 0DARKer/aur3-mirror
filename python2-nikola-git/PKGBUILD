# Maintainer: Kwpolska <kwpolska@kwpolska.tk>
pkgname=python2-nikola-git
_pyname=nikola
_gitname=nikola
pkgver=6.3.0
pkgrel=1
pkgdesc='A modular, fast, simple, static website generator.  (git version)'
arch=('any')
url='http://getnikola.com/'
license=('custom')
depends=('python2' 'python2-doit' 'python2-pygments' 'python2-pillow'
         'python2-docutils' 'python2-mako' 'python2-unidecode' 'python2-lxml'
         'python2-yapsy' 'python2-pyrss2gen' 'python2-pytz' 'python2-logbook'
         'python2-blinker')
makedepends=('git')
options=(!emptydirs)
provides=('python2-nikola')
conflicts=('python-nikola' 'python2-nikola' 'python-nikola-git')
source=("git://github.com/getnikola/${_gitname}.git")
md5sums=('SKIP')

package() {
  cd "${srcdir}/${_gitname}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

# vim:set ts=2 sw=2 et:
