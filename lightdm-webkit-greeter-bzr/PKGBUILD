# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lightdm-webkit-greeter-bzr
pkgver=13
pkgrel=3
pkgdesc="Webkit greeter for lightweight display manager - bzr checkout"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-webkit-greeter"
license=(GPL3 LGPL3)
depends=('lightdm' 'libwebkit' 'gtk-engines' 'archlinux-wallpaper')
optdepends=('xorg-xserver-xephr: run lightdm in test mode')
makedepends=('bzr' 'vala' 'gnome-common' 'gtk-doc' 'gnome-doc-utils' 'pkg-config')
conflicts=('lightdm-webkit-greeter')
provides=('lightdm-webkit-greeter')
backup=('etc/lightdm/lightdm-webkit-greeter.conf')

_bzrtrunk="lp:lightdm-webkit-greeter"
_bzrmod="lightdm-webkit-greeter-trunk"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_bzrmod}-build"
  cp -r "${srcdir}/${_bzrmod}" "${srcdir}/${_bzrmod}-build"
  cd "${srcdir}/${_bzrmod}-build"

  ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/lightdm \
    --disable-static
  make

  # What is Ambiance? This should be a GTK+ 2.x theme, so we use Clearlooks here.
  sed -i '/^theme-name=/s/Ambiance/Clearlooks/' data/lightdm-webkit-greeter.conf

  # Theme 'default' does not exist...
  sed -i '/^webkit-theme=/s/default/webkit/' data/lightdm-webkit-greeter.conf

  # this is Ubuntu branding... Replace it with something useful. ;)
  sed -i '/^background=/s|/usr/share/backgrounds/warty-final-ubuntu.png|/usr/share/archlinux/wallpaper/archlinux-arrival.jpg|' data/lightdm-webkit-greeter.conf
}

package() {
  cd "${srcdir}/${_bzrmod}-build"
  make DESTDIR="${pkgdir}" install
}
