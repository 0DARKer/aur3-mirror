# Maintainer: Pavol Hluchy (Lopo) <lopo at losys dot eu>

pkgname=netbeans-php-nightly
pkgver=latest
pkgrel=2
pkgdesc="Netbeans IDE for PHP"
license=('CDDL')
arch=('any')
url="http://netbeans.org"
depends=('java-environment' 'ttf-bitstream-vera' 'desktop-file-utils')
optdepends=('php: for developing programs in php'
			'lib32-glibc')
conflicts=('netbeans')
install=netbeans-php.install
options=(!strip)
_webroot="http://bits.netbeans.org/download/trunk/nightly/latest"
source=('netbeans.desktop')
# Don't know checksums of actual JSs
md5sums=('4365b0af18717c031c38f422d0ad5466')


build() {
	cd "${SRCDEST}"

	download_file "${_webroot}/js/build_info.js"
	download_file "${_webroot}/js/files.js"

	_zipname=`grep -e '^build_info\.ZIP_FILES_PREFIX\s' build_info.js \
			| awk 'BEGIN {FPAT="\".+\""} {gsub(/\"/, "", $NF); print}'`
	_file="${_zipname}-php.zip"
	_md5=`grep "zip/${_file}" files.js \
		| awk 'BEGIN {FPAT="\"[0-9a-z]+\""} {gsub(/\"/, "", $NF); print}'`

	download_file "${_webroot}/zip/${_file}"

	cd "${srcdir}"

	msg "$(gettext "Validating source files with %s...")" "md5sums"
	file="$(get_filename "${_file}")"
	printf "%s" "    $file ... " >&2
	local realsum="$(openssl dgst -md5 "${_file}")"
	if [ "${_md5}" != "${realsum##* }" ]; then
		printf -- "$(gettext "FAILED")\n" >&2
		return 1
	else
		printf -- "$(gettext "Passed")\n" >&2
	fi

	extract_file "${_file}"

	msg2 "cleanup OS specific files"
	rm $(find -name '*\.exe' -or -name '*\.bat')
	rm -r $(find -name 'MacOSX*' -or -name 'Windows*' -or -name 'SunOS*')
	rm -r $(find -name 'hpux*' -or -name 'mac*' -or -name 'solaris*' -or -name 'windows*')
}

package() {
	cd "$srcdir/netbeans"

	mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/applications"
	cp -r "../netbeans" "$pkgdir/usr/share"
	install -Dm644 "../netbeans.desktop" "$pkgdir/usr/share/applications/netbeans.desktop"
	install -Dm644 "nb/netbeans.png" "$pkgdir/usr/share/pixmaps/netbeans.png"
	ln -s "/usr/share/netbeans/bin/netbeans" "$pkgdir/usr/bin/netbeans"
	install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/netbeans/LICENSE"
}

