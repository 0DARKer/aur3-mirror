#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pm2ml
pkgver=2012.12.16
pkgrel=1
pkgdesc='Generate metalinks for downloading Pacman packages and databases.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pm2ml"
depends=(python3 pyalpm)
optdepends=('python3-aur: AUR support' 'reflector: Reflector support' 'aria2: ppl script support.')
backup=(etc/ppl.conf)
source=(
  http://xyne.archlinux.ca/projects/pm2ml/archives/pm2ml-2012.12.16.tar.xz
  http://xyne.archlinux.ca/projects/pm2ml/archives/pm2ml-2012.12.16.tar.xz.sig
)
sha512sums=(
  4229eddb56cbd1f771899a76907a72af294953bbdda913bf8800e3384d363a24b189cb012fe013ff28aa6c510971f5f31fe1de94e89361ad9e65a9a128657830
  95fb7a783d3c42454dc0cb804694908d07a61d189586dec809427d65b8997501d11b388c33108b80ec1b61b053016652bcb2fb32ec8c8700035e3f616aa35575
)
md5sums=(
  fe8cd9bab1bb0fc6c15115219835d699
  b25878e514ba48e022904b4bc312b2c5
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  for foo_ in ppl pplsyu ppls; do
    install -D -m755 "$foo_" "$pkgdir/usr/bin/$foo_"
  done
  install -D -m644 "ppl.conf" "$pkgdir/etc/ppl.conf"
}

# vim: set ts=2 sw=2 et:
