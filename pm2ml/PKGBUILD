#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pm2ml
pkgver=2012.10
pkgrel=1
pkgdesc='Generate metalinks for downloading Pacman packages and databases.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pm2ml"
depends=(python3 pyalpm)
optdepends=('reflector: Reflector support' 'aria2: ppl script support.' 'python3-aur: AUR support')
backup=(etc/ppl.conf)
source=(
  http://xyne.archlinux.ca/projects/pm2ml/archives/pm2ml-2012.10.tar.xz
  http://xyne.archlinux.ca/projects/pm2ml/archives/pm2ml-2012.10.tar.xz.sig
)
sha512sums=(
  af915884129c59e96e44fecf5f034026c2f73dfcef385f1d0072ff20b98e2a886c192a9b1954264f39da05c9432b385720cd1e4f48ae6d6e7512f2de6a5a5694
  0e99bcaa953ef00ee663727574df3e80dd331f7d291de118c0b73e99431c869f8ea6f85131491f6159a70fc82792872005d585b1066724164c8c0f0f7fb2f052
)
md5sums=(
  64f673cf0160d155e2a8198df9c9ca13
  98a54729cf34af60fe09818c3218db1e
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m755 "$srcdir/$pkgname-$pkgver/$pkgname" "$pkgdir/usr/bin/$pkgname"
  for foo_ in ppl pplsyu ppls; do
    install -D -m755 "$srcdir/$pkgname-$pkgver/$foo_" "$pkgdir/usr/bin/$foo_"
  done
  install -D -m755 "$srcdir/$pkgname-$pkgver/ppl.conf" "$pkgdir/etc/ppl.conf"
}

# vim: set ts=2 sw=2 et:
