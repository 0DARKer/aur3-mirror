#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pm2ml
pkgver=2014.12
pkgrel=1
pkgdesc='Generate metalinks for downloading Pacman packages and databases.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pm2ml"
depends=(python3 pyalpm)
optdepends=('aria2: ppl script support.' 'python3-aur: AUR support' 'reflector: Reflector support')
backup=(etc/ppl.conf)
source=(
  http://xyne.archlinux.ca/projects/pm2ml/src/pm2ml-2014.12.tar.xz
  http://xyne.archlinux.ca/projects/pm2ml/src/pm2ml-2014.12.tar.xz.sig
)
sha512sums=(
  381be6d9c32836f4379a01d0509bb543ebc0e7fbc6bb16a6bd70f5da6510953bd208ec6fdbc34ec5485c1a072e2abac0b85ae1b6e5b3bbdcd6f9b6d78a1af187
  56b22ce335adef1b3ef43ad9e827b0476a2ff749a25fab77231112ee1721dcd2143ad39554a903a17d1058b261a823e68a1a7eeb037874603b16ffdc12080e49
)
md5sums=(
  d0391120964ed8068a9e0cd3118bb632
  7b5e15eb1d3bfe023176cb3f9d4ef126
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  for foo_ in ppl pplsyu ppls; do
    install -Dm755 "$foo_" "$pkgdir/usr/bin/$foo_"
  done
  install -Dm644 "ppl.conf" "$pkgdir/etc/ppl.conf"
}

# vim: set ts=2 sw=2 et:
