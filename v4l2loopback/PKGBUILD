# Maintainer: SpepS <dreamspepser at yahoo dot it>

_kver=`uname -r | sed 's/\..-.//'`
pkgname=v4l2loopback
pkgver=0.5.0
pkgrel=1
pkgdesc="A kernel module to create V4L2 loopback devices"
arch=(i686 x86_64)
url="https://github.com/umlaeute/v4l2loopback/"
license=('GPL')
depends=('glibc')
makedepends=('linux-headers')
conflicts=("$pkgname-svn" "$pkgname-git")
install="$pkgname.install"
source=("${url}tarball/v$pkgver")
md5sums=('7a78b5f457809aab0d0270465d0aba34')

build() {
  cd ${srcdir}/umlaeute-$pkgname-*

  make
  cd examples && make
}

package() {
  cd ${srcdir}/umlaeute-$pkgname-*

  # module
  install -Dm644 $pkgname.ko \
    "$pkgdir/lib/modules/extramodules-$_kver/$pkgname.ko"

  # docs
  install -Dm644 doc/v4l2.html \
    "$pkgdir/usr/share/doc/$pkgname/v4l2.html"
  install -Dm644 README NEWS \
    "$pkgdir/usr/share/doc/$pkgname"

  # example
  install -d "$pkgdir/usr/share/$pkgname/examples"
  install -Dm644 examples/{*.c,README,Makefile} \
    "$pkgdir/usr/share/$pkgname/examples"
  install -Dm755 examples/yuv4mpeg_to_v4l2 \
    "$pkgdir/usr/bin/yuv4mpeg_to_v4l2"
}

# vim:set ts=2 sw=2 et:
