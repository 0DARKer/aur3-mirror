# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet-git
pkgver=r554.e171722
pkgrel=1
pkgdesc='KWallet Framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kwallet'
license=('LGPL')
depends=('knotifications-git')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
conflicts=(kwallet kwalletframeworks-git)
provides=(kwallet kwalletframeworks-git)
source=('git://anongit.kde.org/kwallet.git')
md5sums=('SKIP')

pkgver() {
  cd kwallet
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kwallet \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
