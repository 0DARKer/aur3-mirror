# Maintainer: Benjamin Cremer <bc@benjamin-cremer.de>
# https://github.com/bcremer/pkgbuild/tree/master/php-qafooprofiler

pkgname=php-qafooprofiler
pkgver=1.3.2
pkgrel=1
pkgdesc="Qafoo Profiler PHP Extension (unofficial)"
arch=('i686' 'x86_64')
url="https://github.com/QafooLabs/php-profiler-extension"
license=('Apache')
depends=('php>=5.4.0')
makedepends=('php' 'git')
provides=('php-qafooprofiler')
conflicts=('php-qafooprofiler')
source=("https://github.com/QafooLabs/php-profiler-extension/archive/v$pkgver.tar.gz"
        "https://github.com/QafooLabs/profiler/releases/download/v1.3.0/QafooProfiler.php")
sha256sums=('3f5e9d161e3dfc9c8f8ef0ebfb59e4baf413751bc2c56caf1eaab2484f86c3bf'
            '8be18856a19cb789bab36c940863f90bd2ebb5406f826b0cae6301db90b4d389')

build() {
    cd "$srcdir/php-profiler-extension-$pkgver"
    phpize
    ./configure --prefix=/usr
    make
}

check() {
    cd "$srcdir/php-profiler-extension-$pkgver"
    export NO_INTERACTION=1
    make test
    echo
}

package() {
    cd "$srcdir/php-profiler-extension-$pkgver"
    make INSTALL_ROOT="$pkgdir" install
    echo ';extension=qafooprofiler.so' > qafooprofiler.ini
    install -Dm644 ../QafooProfiler.php "$pkgdir/usr/lib/php/modules/QafooProfiler.php"
    install -Dm644 qafooprofiler.ini "$pkgdir/etc/php/conf.d/qafooprofiler.ini"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

