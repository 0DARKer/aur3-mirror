# Contributor: Claudio Pisa <clauz@ninux.org>
# based con gcc43 PKGBUILD

pkgname=gcc41
_ver=4.1
pkgver=4.1.2
pkgrel=1
pkgdesc="The GNU Compiler Collection"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
depends=('glibc' 'binutils' 'gmp' 'mpfr' 'ppl' 'cloog-ppl')
makedepends=('flex' 'bison')
options=('!libtool')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-{core,g++,fortran,objc,java}-${pkgver}.tar.bz2)
md5sums=('2af3fb599635219171c6ae1f3034888a' 
		 '75c6d5fa3415d614314caf0f509e8933' 
		 '39621038e425c73f955db8c8db411c34' 
		 'abda05c0ab99059e8f9e7a625361fd87' 
		 '83cad4d52ff4a47b69e57012a5efe534')


build() {
  if ! locale -a | grep ^de_DE; then
          echo "You need the de_DE locale to build gcc."
    return 1
  fi

  cd ${srcdir}/gcc-${pkgver}
  # Don't install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  echo ${pkgver} > gcc/BASE-VER

  # Don't run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  mkdir build
  cd build
  ../configure --prefix=/usr --enable-shared \
      --enable-languages=c,c++ \
      --enable-threads=posix --mandir=/usr/share/man --infodir=/usr/share/info \
      --enable-__cxa_atexit  --disable-multilib --libdir=/usr/lib \
      --libexecdir=/usr/lib --enable-clocale=gnu --disable-libstdcxx-pch \
      --with-tune=i686 \
      --disable-werror --enable-checking=release \
      --program-suffix=-${_ver} --enable-version-specific-runtime-libs
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # Lazy way of dealing with conflicting man and info pages...
  rm -rf ${pkgdir}/usr/share
}
