# Maintainer: lykaner <lykaner@posteo.net>
# Contributor: lykaner <lykaner@posteo.net>

pkgname=keepassc-git
pkgver=1.6.2.r9.gd60e8be
pkgrel=1
pkgdesc="KeePassC is a curses-based password manager compatible to KeePass v.1.x and KeePassX"
arch=(any)
url="http://raymontag.github.com/keepassc/"
license=('GPL')
groups=
depends=('python-kppy>=1.4.0' 'python>=3.3')
optdepends=('xsel: copy usernames and password to clipboard'
	    'openssl: to create server certificates')
makedepends=('git')
provides=('keepassc')
conflicts=('keepassc' 'keepassc-dev')
source=("$pkgname"::'git://github.com/raymontag/keepassc.git#branch=development')

md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"
  python setup.py build
}

package() {
  cd "$srcdir/$pkgname"
  python setup.py install --root="$pkgdir/" --optimize=1
}

