# Contributor: Weng Xuetian <wengxt@gmail.com>

pkgname=firefox-beta-kde-opensuse
pkgver=4.0b11
pkgrel=1
_xulver=2.0b11
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=("xulrunner-beta-kde-opensuse=${_xulver}" 'desktop-file-utils')
makedepends=('zip' 'pkg-config' 'diffutils' 'libgnomeui>=2.24.1' 'python2' 'wireless_tools' 'autoconf2.13' 'yasm')
provides=("firefox=${pkgver}")
conflicts=('firefox' 'firefox-kde-opensuse')
replaces=('firefox3')
install=firefox.install
url="http://www.mozilla.org/projects/firefox"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
        mozconfig
        firefox.desktop
        firefox-safe.desktop
        mozilla-firefox-1.0-lang.patch
        firefox-version.patch
        kde.js
        firefox-cross-desktop.patch
        firefox-kde.patch
        firefox-browser-css.patch
        firefox-shellservice.patch
        firefox-kde-114.patch
)

md5sums=('a52b8f4bb98f158ef0639db59dd39bce'
         '9384d07ee234562fa182d2a719050d95'
         'ba96924ece1d77453e462429037a2ce5'
         '6f38a5899034b7786cb1f75ad42032b8'
         'bd5db57c23c72a02a489592644f18995'
         '480807e12d762e5167b24b7e81e58501'
         '75df0f88cc7a7fa7d522459e4ff82cc5'
         'ef2ec32d2c98fbbdc0304801a3d6413d'
         '94e17bb82e8e8189bc379eb6daf9043f'
         'ff65ddf82410e299a50a1c1204ead0e8'
         '55ec65d1ce290842187da97e914f864c'
         '038666a4be4e56c1ea25265fa4a12dd2')

build() {
  cd "${srcdir}/mozilla-central"
  patch -Np1 -i "${srcdir}/mozilla-firefox-1.0-lang.patch"
  patch -Np1 -i "${srcdir}/firefox-version.patch"

  rm -f browser/components/shell/src/nsKDEShellService.cpp \
        browser/components/shell/src/nsKDEShellService.h \
        browser/components/shell/src/nsUnixShellService.cpp \
        browser/components/shell/src/nsUnixShellService.h \
        browser/base/content/browser-kde.xul
  rm -f toolkit/xre/nsKDEUtils.cpp \
        toolkit/xre/nsKDEUtils.h \
        uriloader/exthandler/unix/nsCommonRegistry.cpp \
        uriloader/exthandler/unix/nsCommonRegistry.h \
        uriloader/exthandler/unix/nsKDERegistry.cpp \
        uriloader/exthandler/unix/nsKDERegistry.h \
        toolkit/content/widgets/dialog-kde.xml \
        toolkit/content/widgets/preferences-kde.xml \

  install -m 644 "${srcdir}/kde.js" browser/app/profile/kde.js
  patch -Np1 -i "${srcdir}/firefox-cross-desktop.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-kde.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-browser-css.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-shellservice.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-kde-114.patch" || return 1

  cp "${srcdir}/mozconfig" .mozconfig
  unset CFLAGS
  unset CXXFLAGS

  export LDFLAGS="-Wl,-rpath,/usr/lib/firefox-4.0b"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
}

package() {
  cd "${srcdir}/mozilla-central"
  make -j1 DESTDIR="${pkgdir}" -C build/browser/installer install

  install -m755 -d ${pkgdir}/usr/share/{applications,pixmaps}
  install -m644 ${srcdir}/mozilla-central/other-licenses/branding/firefox/default48.png ${pkgdir}/usr/share/pixmaps/firefox.png || return 1
  install -m644 ${srcdir}/firefox.desktop ${pkgdir}/usr/share/applications/
  install -m644 ${srcdir}/firefox-safe.desktop ${pkgdir}/usr/share/applications/
}
