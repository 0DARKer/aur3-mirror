# Maintainer: Ryan Corder <ryanc@greengrey.org>
# Contributor: Garrett Ward <glward1 at gmail dot com>

pkgname=hdhomerun_config_gui
pkgver=20140121
pkgrel=1
pkgdesc="GTK2 GUI for configuring the HDHomeRun device"
arch=('i686' 'x86_64')
url="http://www.silicondust.com/downloads"
license=('GPL3')
depends=('gtk2' "libhdhomerun=${pkgver}")
optdepends=('vlc: stream live channels directly from HDHomeRun'
            'mythtv: use HDHomeRun as PVR source')
source=("http://download.silicondust.com/hdhomerun/${pkgname}_${pkgver}.tgz")
md5sums=('f65de5c235c41874461ccc715dd150ee')

build() {
  cd $srcdir/$pkgname

  # Thanks to James Edir <jimportal@gmail.com> for contributing this fix
  # This really should be fixed upstream.
  sed -i "s|SUBDIRS = ../libhdhomerun src po|SUBDIRS = src po|g" Makefile.am
  sed -i "s|-I../../libhdhomerun|-I/usr/include/hdhomerun|g" src/Makefile.am
  sed -i "s|-L../../libhdhomerun -lhdhomerun|-lhdhomerun|g" src/Makefile.am

  ./autogen.sh

  make
}

package() {
  mkdir -m 0755 -p $pkgdir/usr/bin
  install -m 755 $srcdir/$pkgname/src/hdhomerun_config_gui $pkgdir/usr/bin/hdhomerun_config_gui
}
