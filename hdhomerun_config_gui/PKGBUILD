# Maintainer: Ryan Corder <mrc at bofh dot io>
# Contributor: Garrett Ward <glward1 at gmail dot com>

pkgname=hdhomerun_config_gui
pkgver=20141210
pkgrel=1
pkgdesc="GTK2 GUI for configuring the HDHomeRun device"
arch=('i686' 'x86_64')
url="http://www.silicondust.com/downloads"
license=('GPL3')
depends=('gtk2' "libhdhomerun=${pkgver}")
optdepends=('vlc: stream live channels directly from HDHomeRun'
            'mythtv: use HDHomeRun as PVR source')
source=("http://download.silicondust.com/hdhomerun/${pkgname}_${pkgver}.tgz")
md5sums=('b4bf62b088fb58c5b0e85ed7e30294a2')

build() {
  cd $srcdir/$pkgname

  # Thanks to James Edir <jimportal at gmail dot com> for contributing this fix
  # Updated for builds 20141210 and newer (mrc)
  sed -i "s|SUBDIRS = ../libhdhomerun src|SUBDIRS = src|g" Makefile.am
  sed -i "s|-I../../libhdhomerun|-I/usr/include/hdhomerun|g" src/Makefile.am
  sed -i "s|-L../../libhdhomerun -lhdhomerun|-lhdhomerun|g" src/Makefile.am

  ./autogen.sh

  make
}

package() {
  mkdir -m 0755 -p $pkgdir/usr/bin
  install -m 755 $srcdir/$pkgname/src/hdhomerun_config_gui $pkgdir/usr/bin/hdhomerun_config_gui
}
