# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc-snapshot
_ver=4.6
_snapshot=20110305
pkgver=${_ver}.${_snapshot}
pkgrel=1
pkgdesc="The GNU Compiler Collection developmental snapshot"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://gcc.gnu.org"
depends=('glibc' 'binutils' 'gmp' 'mpfr' 'libmpc' 'isl' 'cloog' 'elfutils')
makedepends=('flex' 'bison')
options=('!libtool')
source=(ftp://gcc.gnu.org/pub/gcc/snapshots/${_ver}-${_snapshot}/gcc-{core,g++}-${_ver}-${_snapshot}.tar.bz2)
md5sums=('83c4a7396c388f600993042b45245f29'
         '1a9141c0c90a8c80ff5e3daa899c2fc8')

build()
{
  cd ${srcdir}/gcc-${_ver}-${_snapshot}

  # Do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  # Do not run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  cd ${srcdir}
  mkdir gcc-build && cd gcc-build

  ${srcdir}/gcc-${_ver}-${_snapshot}/configure --prefix=/usr \
      --enable-languages=c,c++ \
      --enable-shared --enable-threads=posix --enable-__cxa_atexit \
      --enable-clocale=gnu --enable-gnu-unique-object \
      --enable-lto --enable-plugin \
      --enable-gold --with-plugin-ld=ld.gold \
      --disable-multilib --disable-libstdcxx-pch \
      --with-system-zlib --with-ppl \
      --enable-cloog-backend=isl --disable-cloog-version-check \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --disable-werror --enable-checking=release \
      --program-suffix=-${_ver} --enable-version-specific-runtime-libs
  make
}

package()
{
  cd ${srcdir}/gcc-build

  make -j1 DESTDIR=${pkgdir} install

  # Lazy way of dealing with conflicting man and info pages and locales...
  rm -rf ${pkgdir}/usr/share/
}
