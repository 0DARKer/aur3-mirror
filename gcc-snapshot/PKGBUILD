# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=gcc-snapshot
_ver=4.7
_snapshot=20110319
pkgver=${_ver}.${_snapshot}
pkgrel=1
pkgdesc="The GNU Compiler Collection developmental snapshot"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL' 'custom')
url="http://gcc.gnu.org"
depends=('glibc' 'binutils' 'gmp' 'mpfr' 'libmpc' 'ppl' 'isl' 'cloog')
makedepends=('flex' 'bison')
options=('!libtool')
source=(ftp://gcc.gnu.org/pub/gcc/snapshots/${_ver}-${_snapshot}/gcc-{core,g++}-${_ver}-${_snapshot}.tar.bz2
	gcc_pure64.patch
	gcc-hash-style-both.patch)
md5sums=('ae6065d6fff713eb6b07ceeb7676c849'
         '8d83b4b3949b2a23d42fe0095b72eac8'
         '4030ee1c08dd1e843c0225b772360e76'
         '4df25b623799b148a0703eaeec8fdf3f')

build()
{
  cd ${srcdir}/gcc-${_ver}-${_snapshot}

  # Do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  # Do not run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  if [ "${CARCH}" = "x86_64" ]; then
    patch -Np1 -i ${srcdir}/gcc_pure64.patch
  fi
  patch -Np0 -i ${srcdir}/gcc-hash-style-both.patch

  cd ${srcdir}
  mkdir gcc-build && cd gcc-build

  ${srcdir}/gcc-${_ver}-${_snapshot}/configure --prefix=/usr \
      --libdir=/usr/lib --libexecdir=/usr/lib \
      --mandir=/usr/share/man --infodir=/usr/share/info \
      --with-bugurl=https://bugs.archlinux.org/ \
      --enable-languages=c,c++ \
      --enable-shared --enable-threads=posix \
      --with-system-zlib --enable-__cxa_atexit \
      --disable-libunwind-exceptions --enable-clocale=gnu \
      --enable-gnu-unique-object --enable-linker-build-id \
      --with-ppl --enable-cloog-backend=isl \
      --enable-lto --enable-gold --enable-ld=default \
      --enable-plugin --with-plugin-ld=ld.gold \
      --disable-multilib --disable-libstdcxx-pch \
      --enable-checking=release --disable-werror \
      --program-suffix=-${_ver} --enable-version-specific-runtime-libs

  make
}

package()
{
  cd ${srcdir}/gcc-build

  make -j1 DESTDIR=${pkgdir} install

  # Lazy way of dealing with conflicting man and info pages and locales...
  rm -rf ${pkgdir}/usr/share/

  install -Dm644 ${srcdir}/gcc-${_ver}-${_snapshot}/COPYING.RUNTIME \
    ${pkgdir}/usr/share/licenses/$pkgname/RUNTIME.LIBRARY.EXCEPTION
}
