# Maintainer: Vivien Maisonneuve <v dot maisonneuve at gmail dot com>
# Category: science
pkgname='apron-svn'
pkgver=0.9.11.r1054
pkgrel=1
pkgdesc='The APRON numerical abstract domain library'
arch=('i686' 'x86_64')
url='http://apron.cri.ensmp.fr/library/'
license=('LGPL2')
provides=(${pkgname%-*}=$pkgver)
conflicts=(${pkgname%-*})
depends=('gmp>=5' 'mpfr>=3')
makedepends=('svn' 'sed')
source=("$pkgname::svn://scm.gforge.inria.fr/svnroot/apron/apron/trunk")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    local rel="$(sed -n 's|PKGNAME *= *apron-\([^ ]\+\) *$|\1|p' Makefile)"
    local ver="$(svnversion)"
    printf "%s.r%s" "$rel" "${ver//[[:alpha:]]}"
}

prepare() {
    cd "$srcdir/$pkgname"
    sed -e 's|^APRON_PREFIX *=.*$|APRON_PREFIX=/usr|' \
        -e 's|^HAS_OCAML *=.*$|HAS_OCAML=|' \
        -e 's|^HAS_OCAMLOPT *=.*$|HAS_OCAMLOPT=|' \
        -e 's|^OCAMLFIND *=.*$|OCAMLFIND=|' \
        Makefile.config.model > Makefile.config
}

build() {
    cd "$srcdir/$pkgname"
    make
}

package() {
    cd "$srcdir/$pkgname"
    make APRON_PREFIX="$pkgdir"/usr install
}
