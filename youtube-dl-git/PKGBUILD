# Maintainer: baskerville <baskerville at lavabit dot com>

pkgname=youtube-dl-git
pkgver=20120228
pkgrel=1
pkgdesc="A small command-line program to download videos from YouTube.com and a few more sites"
arch=('any')
url="http://rg3.github.com/youtube-dl/"
license=('custom')
depends=('python2')
makedepends=('git')
provides=('youtube-dl')
conflicts=('youtube-dl')
_gitroot='git://github.com/rg3/youtube-dl.git'
_gitname='youtube-dl'

build() {
    cd "$srcdir"
    if [[ -d "$_gitname" ]] ; then
        cd "$_gitname" && git pull origin
    else
        git clone "$_gitroot" "$_gitname"
    fi
    rm -rf "$srcdir/$_gitname-build"
    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
}

package() {
    cd "$srcdir/$_gitname-build"
    sed -i 's|^#!/usr/bin/env python|#!/usr/bin/env python2|' youtube-dl
    install -D -m755 youtube-dl "${pkgdir}/usr/bin/youtube-dl"
    install -d "${pkgdir}/usr/share/licenses/${pkgname}"
    grep -i license youtube-dl > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
