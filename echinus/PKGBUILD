# Maintainer: TDY <tdy@gmx.com>

pkgname=echinus
pkgver=0.4.4
pkgrel=1
pkgdesc="A window manager for X in the spirit of dwm"
arch=('i686' 'x86_64')
url="http://plhk.ru/echinus"
license=('MIT')
depends=('libxft' 'libxrandr')
makedepends=('pkgconfig')
optdepends=('ourico: complementary taskbar for echinus')
install=echinus.install
source=(http://plhk.ru/static/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('9a6718911d2d4ece3add9f972eed4c0a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's,\${PREFIX}[/]*\(\${CONF}\),\1,' Makefile config.mk
  make PREFIX=/usr              \
       MANPREFIX=/usr/share/man \
       CONF=/etc/xdg/$pkgname   \
       X11INC=/usr/include/X11  \
       X11LIB=/usr/lib/X11
}

package () {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr              \
       MANPREFIX=/usr/share/man \
       CONF=/etc/xdg/$pkgname   \
       DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
