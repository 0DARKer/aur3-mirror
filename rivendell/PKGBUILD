# Maintainer: SÃ©bastien Leblanc
pkgname=rivendell
pkgver=2.1.5
pkgrel=1

# 2012-08-15

pkgdesc="A complete radio broadcast automation solution, with facilities for the acquisition, management, scheduling and playout of audio content."
url="http://www.rivendellaudio.org"
arch=('x86_64' 'i686')
license=('GPL')
makedepends=('qt3' 'id3lib' 'soundtouch' 'jack' 'libmad' 'libvorbis' 'twolame' 'lame' 'flac' 'chrpath')
depends=('qt3' 'id3lib' 'soundtouch' 'jack' 'libmad' 'libvorbis' 'twolame' 'lame' 'flac' 'curl' 'cdparanoia' \
         'mysql' 'qt')
optdepends=('apache: CGI scripts')
backup=('etc/rd.conf')

source=(
  "http://rivendellaudio.org/ftpdocs/rivendell/rivendell-$pkgver.tar.gz"
  'cae_Makefile_am.patch'
  'rdconfig_h.patch'
  'rdcdripper_cpp.patch'
  'rd-bin_conf_in.patch'
)

sha512sums=('c90af95bd23e099fca7d25584196dda2d30057cfe723116af60b02a035d0c8cc8e90b1d7b6fc227d3c2398e813824e042654a9b2e72af8b787b4ff06d0d91f37'
            '5576285f5f466fe804cf3d469d26008ddd2d07ff9debb0220c9c432aaa344f2133f445f5b1449b4c4ab185b29656b19285f46e2011b95de9aa8ded6e3834f677'
            '56afff03f2e6f92147f2128860603beb6eb7544c23370033892962549925a994a15f8c705cb3fb001dea70d6bf02882ce2c095a710029f94600ab7080171177d'
            '80c75d5b52a73c543896a7592a54535e9e985c023cd85d8b889e756e9197938e80de2a111718cb1fd22f4bfd8a8427b2d9a5ea636ff5d891a3fd0762c6f35cdc'
            '291a571525230149455cacc6dcdd89d1bfd95c3dcad101991b80945c70aa92b464d7f737fc1a92a284bbea1d9b89e1cea490e2c9f6995a4997f88c058a3c05c5')

build() {
  echo Making package
  PATCHES=(
    'cae_Makefile_am'
    'rdconfig_h'
    'rdcdripper_cpp'
  )

  cd "$srcdir/$pkgname-$pkgver"
 
  for patch in "${PATCHES[@]}"; do
    echo "Patching : $patch"
    patch -p1 < ../"$patch".patch
  done

  sh autogen.sh
  ./configure --bindir=/usr/bin --sbindir=/usr/sbin --libexecdir=/usr/lib --sysconfdir=/etc --sharedstatedir=/com \
    --localstatedir=/var --libdir=/usr/lib --includedir=/usr/include --oldincludedir=/usr/include \
    --datarootdir=/usr/share 
  make
}

package() {
  cd "$pkgdir"

  cd "$srcdir"/"$pkgname-$pkgver"
  
  APPS=('rdairplay' 'rdadmin' 'rdcastmanager' 'rdcatchd' 'rdcatch' 'rdlibrary' 'rdlogedit' 'rdlogin' 'rdlogmanager' \
        'rdpanel' 'rdrepld' 'rdselect' 'ripcd')

  UTILS=('rdalsaconfig' 'rdchunk' 'rdcollect' 'rddbcheck' 'rdgpimon' 'rdimport' 'rdmaint' 'rdpopup' \
         'rdpurgecasts' 'rdsoftkeys' 'rmlsend' 'sas_shim') # 'rdgen'

  CGI=('rdcastmanager' 'rdxport')
  XML=('rdfeed')
  
  ICONS=('16x16' '22x22' '32x32' '48x48' '64x64' '128x128')
  

  # special cases:
  install -D cae/.libs/caed "$pkgdir"/usr/bin/caed
  install -D lib/.libs/librd-2.1.5.so "$pkgdir"/usr/lib/librd-2.1.5.so
  install -D utils/rdgen/rdgen "$pkgdir"/usr/bin/rdgen

  ln -s /usr/lib/librd-2.1.5.so "$pkgdir"/usr/lib/librd.so
  ln -s /usr/lib/librd-2.1.5.so "$pkgdir"/usr/lib/librd.so.2.1.5

  # normal cases:
  for app in "${APPS[@]}"; do
    install -D "$app"/.libs/"$app" "$pkgdir"/usr/bin/"$app"
  done
  
  for util in "${UTILS[@]}"; do
    install -D utils/"$util"/.libs/"$util" "$pkgdir"/usr/bin/"$util"
  done

  for cgi in "${CGI[@]}"; do
    install -D web/"$cgi"/.libs/"$cgi".cgi "$pkgdir"/usr/lib/rivendell/web/"$cgi".cgi
  done

  for xml in "${XML[@]}"; do
    install -D web/"$xml"/.libs/"$xml".xml "$pkgdir"/usr/lib/rivendell/web/"$xml".xml
  done

  for file in web/tests/*.html; do
    install -D "$file" "$pkgdir"/usr/lib/rivendell/web/"$(basename "$file")"
  done

  
  # Icons
  for icon in "${ICONS[@]}"; do
    install -D icons/rivendell-"$icon".xpm "$pkgdir"/usr/share/icons/hicolor/"$icon"/rivendell.xpm
  done

  # Docs
  mkdir -p "$pkgdir"/usr/share/doc/rivendell/
  cp -R docs/* "$pkgdir"/usr/share/doc/rivendell

  # RLM
  install -D rlm/rlm.h "$pkgdir"/usr/include/rlm/rlm.h 

  # PAM
  install -D pam_rd/.libs/pam_rd.so "$pkgdir"/usr/lib/security/pam_rd.so  
  chrpath -d "$pkgdir"/usr/lib/security/pam_rd.so

  # Config files
  install -D conf/rd.conf-sample "$pkgdir"/etc/rd.conf
  install -D conf/rd-bin.conf "$pkgdir"/etc/rd-bin.conf
  for rlm in conf/rlm_*.conf; do
    install -D "$rlm" "$pkgdir"/etc/rlm/"$(basename "$rlm")"
  done
}

