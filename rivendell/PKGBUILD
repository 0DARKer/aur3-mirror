# Maintainer: SÃ©bastien Leblanc
pkgname=rivendell
pkgver=2.5.1
pkgrel=2

# 2013-05-30

pkgdesc="A complete radio broadcast automation solution, with facilities for the acquisition, management, scheduling and playout of audio content."
url="http://www.rivendellaudio.org"
arch=('x86_64' 'i686')
license=('GPL')
makedepends=('patch' 'jack' 'qt3' 'alsa-lib' 'libsamplerate' 'libvorbis' 'taglib' 'id3lib' 
'flac' 'pam' 'qtchooser' 'twolame')
depends=('qt3' 'bc' 'cdrkit' 'cdparanoia' 'flac' 'lame' 'vorbis-tools')

optdepends=(
  'apache: CGI scripts, required for a stand-alone install'
  'jack: JACK audio support'
  'mysql: Local MySQL database'
  'twolame: MPEG layer 2 support'
)

backup=('etc/rd.conf')
install='rivendell.install'

source=(
  "http://rivendellaudio.org/ftpdocs/rivendell/$pkgname-$pkgver.tar.gz"
  'rivendell.patch'
)

sha512sums=(
'89fb372e8f331da155918be80521300e891637cd524a6ef73e31644fd7ab5f38f9873ef4a62f8317ebbfb75991b76b38026be10ac34b8c4e56bc376e6cfa7825'
'b110385b4336ba4d25799e703bcc384597b7e726ab7ff480f7597c2f210adaee238167715b4b59978d3389055ef22f29bd9b6538289866f1ea0eb3c21b0f96b8'
)

build() {
  export QT_SELECT=3
  msg "Making package"
  
  cd "$srcdir/$pkgname-$pkgver"
 
  patch -p1 < ../rivendell.patch

  sh autogen.sh
  ./configure --prefix=/usr --libexecdir=/usr/lib/rivendell/web --libdir=/usr/lib --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir"/"$pkgname-$pkgver"

  touch building-debian
  make install DESTDIR="$pkgdir" RD_LIB_PATH="lib"

  msg "$pkgname: Executing final installation steps"
  
  cd "$srcdir"/"$pkgname-$pkgver"
  mkdir -p "$pkgdir"/etc/rivendell.d
  install -t "$pkgdir"/etc/rivendell.d -Dm755 conf/rlm_*.conf
  install -Dm755 conf/rd.conf-sample "$pkgdir"/etc/rd.conf
  install -Dm755 conf/rd-bin.conf "$pkgdir"/etc/httpd/conf/extra/rd-bin.conf
  mkdir -p "$pkgdir"/run/rivendell
  mkdir -p "$pkgdir"/var/snd
}
