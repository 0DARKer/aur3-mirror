# $Id: PKGBUILD 106297 2011-01-16 14:31:59Z ibiru $
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=mplayer-x
_pkgname=mplayer
pkgver=32792
pkgrel=1
pkgdesc="A movie player for linux with GUI enabled"
arch=('i686' 'x86_64')
depends=('libxxf86dga' 'libxxf86vm' 'libmad' 'cdparanoia' 'libxinerama' 'sdl' 'lame' 'libtheora' 'xvidcore' 'libmng' 'libxss'
         'libgl' 'smbclient' 'aalib' 'jack' 'libcaca' 'x264' 'faac' 'lirc-utils' 'ttf-dejavu' 'libxvmc' 'enca' 'libvdpau>=0.4'
         'opencore-amr' 'libdca' 'a52dec' 'schroedinger' 'libvpx' 'libpulse')
license=('GPL')
url="http://www.mplayerhq.hu/"
makedepends=('unzip' 'mesa' 'live-media>=2010.01.13' 'yasm')
provides=('mplayer')
conflicts=('mplayer')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.xz mplayer.desktop mplayer.png
	 http://www1.mplayerhq.hu/MPlayer/skins/Blue-1.7.tar.bz2)
md5sums=('a48845de4b410ab7799ee87a038dd58e'
         '582cd36d7c8923ebd97803c768de14a2'
         'd00874ccc644b7f43d6ef1c942fcef28'
         'e4e2020d11b681aac898103b3ba723c4')

build() {
  # Custom CFLAGS break the mplayer build
  unset CFLAGS LDFLAGS

  cd ${srcdir}/${_pkgname}

  ./configure --prefix=/usr \
      --enable-runtime-cpudetection \
      --enable-gui \
      --disable-arts \
      --disable-liblzo \
      --disable-speex \
      --disable-openal \
      --disable-fribidi \
      --disable-libdv \
      --disable-musepack \
      --disable-esd \
      --disable-mga \
      --enable-xvmc \
      --language=all \
      --confdir=/etc/mplayer

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make
  make -j1 DESTDIR=${pkgdir} install
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/
  install -dm755 ${pkgdir}/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf
  rm -rf ${pkgdir}/usr/share/mplayer/font
  #desktop file  FS#14770
  install -Dm644 ${srcdir}/mplayer.desktop ${pkgdir}/usr/share/applications/mplayer.desktop
  install -Dm644 ${srcdir}/mplayer.png ${pkgdir}/usr/share/pixmaps/mplayer.png
  #default skin
  install -dm755 ${pkgdir}/usr/share/mplayer/skins/
  cp -r ${srcdir}/Blue ${pkgdir}/usr/share/mplayer/skins/default
}
