# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>

pkgname=ocaml-pipebang
_pkgname=${pkgname/ocaml-/}
pkgver=108.08.00
pkgrel=1
pkgdesc="Syntax extension to transform x |! f into f x."
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=3.12'
         'ocaml-type-conv')
makedepends=('ocaml-findlib')
options=(!strip !makeflags)
url="http://bitbucket.org/yminsky/ocaml-core/wiki/Home"
source=("http://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('db0a07aaef2181646e1747d7da75a0d7')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
