#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=gladtex
pkgver=1.3
pkgrel=1
pkgdesc="a utility for writing LaTeX equations within HTML"
depends=('perl' 'ghostscript' 'texlive-bin' 'libpng' 'giflib')
#optdepends=('giflib')
arch=('i686' 'x86_64')
license=('GPL')
url="http://ans.hsh.no/home/mgg/gladtex/"
source=(
  "http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz"
  gladtex-1.1-png_jmpbuf.diff
)
md5sums=('849dbf55c43283802d2e55fe2f142ff3'
         '8a969dbfbe51121e4d8454303cf46a6c')
sha256sums=('276cf10f57a0fd28354a9494e76befe9c3b088e9ff873336ad82811e49e0b192'
            'bb66475e8fdf3b79b38b6965bdf5ada85945eb5ac4740daa2dbe55569889b19d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 < "$srcdir/gladtex-1.1-png_jmpbuf.diff"
  make
}
package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 eqn2img "$pkgdir"/usr/bin/eqn2img
  install -Dm755 gladtex "$pkgdir"/usr/bin/gladtex
}
# vim: set ts=2 sw=2 et:
