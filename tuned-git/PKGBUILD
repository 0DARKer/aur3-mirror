# Maintainer: Tom < reztho at archlinux dot org >
pkgname=tuned-git
pkgver=20111008
pkgrel=1
pkgdesc="Daemon that performs monitoring and adaptive configuration of devices in the system"
arch=('any')
url="https://fedorahosted.org/tuned/"
license=('GPL')
depends=('python2' 'ethtool')
makedepends=('git')
provides=('tuned')
backup=('etc/tuned.conf')
source=('tuned_rc')

_gitroot='git://git.fedorahosted.org/git/tuned'
_gitname='tuned'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

  # Patching for using python2
  find "${pkgdir}/usr/sbin/" -type f -exec sed -i 's@#!/usr/bin/python@#!/usr/bin/python2@' {} \;

  # Patching for making tuned to use /var/run/tuned.pid
  sed -i 's@/var/run/tuned/tuned.pid@/var/run/tuned.pid@' "${pkgdir}/usr/share/tuned/tuned.py"
  
  # Installing default config
  install -m644 "${pkgdir}/etc/tune-profiles/default/tuned.conf" "${pkgdir}/etc/tuned.conf"

  # Changing the config file for enabling net tuning
  sed -i 's@^enabled=False$@# enabled=False@' "${pkgdir}/etc/tuned.conf"

  msg "Removing some directories and files..."
  rm -vrf "${pkgdir}/etc/bash_completion.d"
  rm -vrf "${pkgdir}/etc/security"
  rm -vrf "${pkgdir}/etc/sysconfig"
  rm -vrf "${pkgdir}/etc/ktune.d"
  rm -vrf "${pkgdir}/etc/rc.d"
  rm -vrf "${pkgdir}/etc/pam.d"
  rm -vrf "${pkgdir}/etc/tune-profiles"
  rm -vrf "${pkgdir}/var"
  rm -vrf "${pkgdir}/usr/bin"
  rm -vrf "${pkgdir}/usr/libexec"
  rm -vrf "${pkgdir}/usr/share/man/man1"
  rm -vrf "${pkgdir}/lib"
  rm -v "${pkgdir}/usr/share/tuned/tuned_adm.py"
  find "${pkgdir}/usr/sbin/" -type f ! -iname 'tuned' -exec rm -v {} \;
  find "${pkgdir}/usr/share/man/man8/" -type f ! -iname 'tuned.*' -exec rm -v {} \;
  find "${pkgdir}/usr/sbin/" -iname '*stat' -exec rm -v {} \;
  
  # Installing the rc daemon script
  install -Dm755 "${srcdir}/tuned_rc" "${pkgdir}/etc/rc.d/tuned"
}

# vim:set ts=2 sw=2 et:
md5sums=('9b923835672995412c93f88cd963081c')
