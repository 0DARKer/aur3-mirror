# Maintainer: Tom < reztho at archlinux dot org >
pkgname=tuned-git
pkgver=20120914
pkgrel=1
pkgdesc="Daemon that performs monitoring and adaptive configuration of devices in the system"
arch=('any')
url="https://fedorahosted.org/tuned/"
license=('GPL')
depends=('python2' 'ethtool')
makedepends=('git')
provides=('tuned')
backup=('etc/tuned/active_profile')
source=('tuned_rc')

_gitroot='git://git.fedorahosted.org/git/tuned'
_gitname='tuned'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

  # Patching for using python2
  find "${pkgdir}/usr/sbin/" -type f -exec sed -i 's@#!/usr/bin/python@#!/usr/bin/python2@' {} \;

  # Patching for making tuned to use /run/tuned.pid
  find "${pkgdir}/" -type f -exec sed -i 's@/var/run/tuned@/run/tuned@' {} \;
  
  msg "Removing some directories and files..."
  rm -vrf "${pkgdir}/var"
  find "${pkgdir}/usr/sbin/" -type f ! -iname 'tuned*' -exec rm -v {} \;
  find "${pkgdir}/usr/share/man/man8/" -type f ! \( -iname 'tuned*' -o -iname 'powertop*' \) -exec rm -v {} \;
  find "${pkgdir}/usr/sbin/" -iname '*stat' -exec rm -v {} \;
  
  # Installing the rc daemon script
  install -Dm755 "${srcdir}/tuned_rc" "${pkgdir}/etc/rc.d/tuned"
  
  # Installing the systemd service
  install -Dm644 "${pkgdir}/tuned.service" "${pkgdir}/usr/lib/systemd/system/tuned.service"
  rm -v "${pkgdir}/tuned.service"
}

# vim:set ts=2 sw=2 et:
md5sums=('b628fb134992e811bb20fe341cb054b1')
