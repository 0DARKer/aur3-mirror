# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=libmetalink
pkgver=0.1.2
pkgrel=1
pkgdesc="A Metalink library written in C language."
arch=(i686 x86_64)
url="https://launchpad.net/libmetalink/"
license=('custom:MIT')
depends=('expat')
makepdepends=('libtool')
options=('!libtool')
source=("${url}trunk/packagingfix/+download/$pkgname-$pkgver.tar.bz2")
md5sums=('e60ea56d910ebfe4c303808db497e92a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
