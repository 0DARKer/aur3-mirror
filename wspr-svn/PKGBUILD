# Maintainer: Mike WB2FKO <mph at sportscliche dot com>
pkgname=wspr-svn
pkgver=2369
pkgrel=1
pkgdesc="Weak Signal Propagation Reporter"
arch=('i686')
url="http://physics.princeton.edu/pulsar/K1JT/wspr.html"
license=('GPL')
optdepends=('g95')
depends=('python2' 'python-imaging' 'python2-numpy' 'libsamplerate' 'portaudio' 'fftw')
makedepends=('subversion')
source=( startwspr
               $pkgname.png
               $pkgname.desktop)
md5sums=('9cdf4674d930b4f97935204305a96194'
         'dd21318d4aa3423f1fbefc53061cc40d'
         '9cca1194cd5f9db3885f5ba54660d239')

_svntrunk="http://svn.berlios.de/svnroot/repos/wsjt/branches/wspr"
_svnmod="wspr"

build() {
# Set the build environment to python2
 export PYTHON="/usr/bin/python2"
 export F2PY="/usr/bin/python2 f2py.py"
 unset LDFLAGS
 cd "$srcdir"

 if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

# Make problems can sometimes be fixed by replacing gfortran with g95  
  ./configure --prefix=/usr --enable-gfortran
  make
}

package(){
# Place the entire package in /opt  
   mkdir -p $pkgdir/opt/$pkgname
   mv $srcdir/$_svnmod-build $pkgdir/opt/$pkgname
   chown -R $USER:  $pkgdir/opt/$pkgname
   find "$pkgdir/opt/$pkgname/" -type f  | xargs chmod 644
   find "$pkgdir/opt/$pkgname/" -type d | xargs chmod 755

# Install a menu item with icon
  cd $srcdir
  mkdir -p $pkgdir/usr/share/pixmaps
  mkdir -p $pkgdir/usr/share/applications
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644  $pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png

# Install a simple launcher script
   install -Dm755 startwspr $pkgdir/opt/$pkgname/$_svnmod-build/startwspr

  echo -e "\n WSPR will be installed in the /opt directory of root \n"
}
