# Maintainer: Gustavo Alvarez <s1lpkn07@gmail.com>

pkgname=mpv-build-git
pkgver=20130226
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg). (GIT version)"
arch=('i686' 'x86_64')
depends=('desktop-file-utils'
         'enca'
         'fribidi'
         'jack'
         'lcms2'
         'libbluray'
         'libcaca'
         'libcdio-paranoia'
         'libgl'
         'libmng'
         'libdvdread'
         'libpulse'
         'libquvi'
         'libva'
         'libvdpau'
         'libxinerama'
         'libxss'
         'libxxf86vm'
         'lirc-utils'
         'mpg123'
         'portaudio'
         'rsound'
         'sdl'
         'smbclient')
license=('GPL')
url="http://www.mplayer2.org/"
makedepends=('git'
             'live-media'
             'mesa'
             'python'
             'python-docutils'
             'vstream-client'
             'yasm')
conflicts=('mpv')
replaces=('mpv')
options=('!emptydirs')
install="${pkgname}.install"
source=('mpv.desktop')
md5sums=('0ff83929010f5a71bda77772141ab347')

_gitroot="git://github.com/mpv-player/mpv-build.git"
_gitname="mpv"

build() {
  cd "${srcdir}"

  if [ -d "${_gitname}" ]; then
    msg "Clean and Update repository"
    cd "${_gitname}"
    ./update
  else
    msg "Clone Repository"
    git clone "${_gitroot}" "${_gitname}"
    cd "${_gitname}"
    ./update
  fi

  rm -fr "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"


  # Make mpv build flags
  sed 's|""|"--prefix=/usr"|g' -i scripts/mpv-config

  make
}

package() {
  cd "${srcdir}/${_gitname}-build/mpv"
  make DESTDIR="${pkgdir}" install-strip
  install -d "${pkgdir}"/{etc/mpv,usr/share/doc/mpv}
  install -Dm644 etc/{input,example,encoding-example-profiles}.conf "${pkgdir}/etc/mpv/"
  cp -R DOCS "${pkgdir}/usr/share/doc/mpv/"
  install -Dm644 "${srcdir}/mpv.desktop" "${pkgdir}/usr/share/applications/mpv.desktop"
  install -Dm644 etc/mpv-icon-source.svg "${pkgdir}/usr/share/pixmaps/mpv.svg"
}
