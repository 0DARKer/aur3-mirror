# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=mpv-build-git
pkgver=20150103.38b503a
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg). (GIT version)"
arch=('i686' 'x86_64' )
depends=('desktop-file-utils' 'enca' 'jack2' 'lame' 'libbluray' 'libbs2b' 'libcaca' 'libcdio-paranoia' 'libdvdnav' 'libfdk-aac' 'libguess' 'libpulse'
         'libva' 'libx264' 'libxss' 'libxv' 'libxrandr' 'lirc-utils' 'lua' 'mpg123' 'openal' 'rsound' 'sdl' 'sdl2' 'smbclient' 'v4l-utils')
license=('GPL2')
url="http://mpv.io/"
# makedepends=('git' 'python-docutils' 'yasm' 'ladspa' 'harfbuzz' 'hardening-wrapper')
optdepends=('youtube-dl: Another way to view youtuve videos with mpv'
            'zsh-completions: Additional completion definitions for Zsh users')
provides=('mpv')
conflicts=('mpv')
options=('!emptydirs')
install=mpv-build-git.install
source=('git+https://github.com/mpv-player/mpv-build.git'
        'git+https://github.com/mpv-player/mpv.git'
        'ffmpeg::git+https://github.com/FFmpeg/FFmpeg.git'
        'git+https://github.com/libass/libass.git'
        'git+http://anongit.freedesktop.org/git/fribidi/fribidi.git')
sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          'SKIP'
          'SKIP')
_gitname="mpv-build"
backup=('etc/mpv/encoding-profiles.conf')

pkgver() {
  cd mpv
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  git clone "file://${srcdir}/mpv" --depth 1
  git clone "file://${srcdir}/ffmpeg" --depth 1
  git clone "file://${srcdir}/libass" --depth 1
  git clone "file://${srcdir}/fribidi" --depth 1

  # Set ffmpeg/libass/fribidi/mpv flags
  echo "--disable-programs --enable-libx264 --enable-libmp3lame --enable-nonfree --enable-libfdk-aac --enable-openssl" > ffmpeg_options
  echo "--prefix=/usr --confdir=/etc/mpv --enable-openal --enable-sdl2 --disable-pdf --enable-joystick --enable-libmpv-shared --enable-zsh-comp --lua=52 --enable-egl-x11" > mpv_options

  # Download Waf script
  cd mpv
  ./bootstrap.py
}

build() {
  cd "${_gitname}"
  ./build
}

package() {
  cd "${_gitname}"
  DESTDIR="${pkgdir}" ./install

  install -Dm644 mpv/DOCS/encoding.rst "${pkgdir}/usr/share/doc/mpv/encoding.rst"
  install -Dm644 mpv/DOCS/edl-mpv.rst "${pkgdir}/usr/share/doc/mpv/edl-mpv.rst"

  sed 's|/usr/local/etc/mpv.conf|/usr/local/etc/mpv.conf or /etc/mpv.conf|g' -i "${pkgdir}/usr/share/doc/mpv/example.conf"

  install -Dm755 mpv/build/simple/simple "${pkgdir}/usr/bin/mpv-simple"
  install -Dm755 mpv/TOOLS/mpv_identify.sh "${pkgdir}/usr/bin/mpv-identify"
  install -Dm755 mpv/TOOLS/umpv "${pkgdir}/usr/bin/umpv"

  pushd mpv/TOOLS/lua &> /dev/null
  for i in $(find . -type f); do install -Dm644 ${i} "${pkgdir}/usr/share/mpv/scripts/${i}"; done
  popd &> /dev/null
}
