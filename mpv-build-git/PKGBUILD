# Maintainer: Gustavo Alvarez <s1lpkn07@gmail.com>

pkgname=mpv-build-git
pkgver=20131123.705a731
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg). (GIT version)"
arch=('i686' 'x86_64')
depends=('desktop-file-utils' 'enca' 'fribidi' 'hicolor-icon-theme' 'libbluray' 'libcaca' 'libcdio-paranoia' 'libguess' 'libdvdread' 'libpulse' 'libva'  'lcms2'
         'libquvi' 'libxinerama' 'libxkbcommon' 'libxss' 'libxv' 'lirc-utils' 'mpg123' 'openal' 'portaudio' 'rsound' 'sdl' 'sdl2' 'smbclient' 'v4l-utils')
license=('GPL2')
url="http://mpv.io/"
makedepends=('git' 'live-media' 'mesa' 'python-docutils' 'yasm' 'python2')
conflicts=('mpv')
replaces=('mpv')
options=('!emptydirs')
install="${pkgname}.install"
source=('git://github.com/mpv-player/mpv-build.git'
        'git://github.com/mpv-player/mpv.git'
        'git://source.ffmpeg.org/ffmpeg.git'
        'git+https://code.google.com/p/libass'
#        'git://anongit.freedesktop.org/fribidi/fribidi.git'
         'add_waf.patch')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
#         'SKIP'
         'f6a94338614cabcb144141e9a09d8c3f')
_gitname="mpv-build"

pkgver() {
  cd "${srcdir}/mpv"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  rm -fr mpv ffmpeg libass ffmpeg_build build_libs # fribidi
  mv ../{mpv,ffmpeg,libass} . # ,fribidi

  # Use python2
  rm -rf "${srcdir}/python"
  mkdir "${srcdir}/python"
  ln -s /usr/bin/python2 "${srcdir}/python/python"
  export PATH="${srcdir}"/python:$PATH

  # patch to use waf instead make
  patch -p0 -i "${srcdir}/add_waf.patch"

  # Make mpv/ffmpeg build flags
  echo "--disable-programs" > ffmpeg_options
  sed 's|OPTIONS="|OPTIONS=\"--prefix=/usr --confdir=/etc/mpv --disable-libquvi4 --enable-openal --disable-pdf |g' -i scripts/mpv-config

  #test (need fix in upstream)
  sed "s|use='libavfilter')),|use=['libavfilter', 'libav'])),|g" -i mpv/wscript
}

build() {
  cd "${_gitname}"
  make
}

package() {
  cd "${_gitname}"
  export PATH="${srcdir}"/python:$PATH
  make DESTDIR="${pkgdir}" install

  install -Dm644 mpv/etc/input.conf "${pkgdir}/etc/mpv/input.conf"
  install -Dm644 mpv/etc/example.conf "${pkgdir}/etc/mpv/mpv-example.conf"
  install -Dm644 mpv/etc/encoding-example-profiles.conf "${pkgdir}/etc/mpv/encoding-example-profiles.conf"

  sed -e 's|/usr/local/etc/mpv.conf|/etc/mpv.conf|' \
      -e 's|/path/to/the/file/you/want/to/include|/etc/mpv/encoding-profiles.conf|' \
      -e 's|/path/to/this|/etc/mpv|' \
      -i "${pkgdir}/etc/mpv/"*.conf

  install -Dm644 mpv/DOCS/tech-overview.txt "${pkgdir}/usr/share/doc/mpv/tech-overview.txt"
  install -Dm644 mpv/DOCS/encoding.rst "${pkgdir}/usr/share/doc/mpv/encoding.rst"
}
