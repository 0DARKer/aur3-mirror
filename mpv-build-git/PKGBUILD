# Maintainer: Gustavo Alvarez <s1lpkn07@gmail.com>

pkgname=mpv-build-git
pkgver=20130506.c653697
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg). (GIT version)"
arch=('i686' 'x86_64')
depends=('desktop-file-utils' 'enca' 'fribidi' 'jack' 'lcms2' 'libbluray' 'libcaca' 'libcdio-paranoia' 'libgl' 'libmng' 'libdvdread' 'libpulse'
         'libquvi' 'libvdpau' 'libxinerama' 'libxkbcommon' 'libxss' 'libxv' 'libxxf86vm' 'lirc-utils' 'mpg123' 'portaudio' 'rsound' 'sdl2-hg' 'smbclient')
license=('GPL')
url="http://mpv.io/"
makedepends=('clang' 'git' 'live-media' 'libva' 'mesa' 'python' 'python-docutils' 'sdl' 'vstream-client' 'yasm')
conflicts=('mpv')
replaces=('mpv')
options=('!emptydirs')
install="${pkgname}.install"
source=('git://github.com/mpv-player/mpv-build.git'
        'git://github.com/mpv-player/mpv.git'
        'mpv.desktop')
md5sums=('SKIP'
         'SKIP'
         '0ff83929010f5a71bda77772141ab347')
_gitname="mpv-build"

pkgver() {
  cd mpv
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  rm -fr mpv
  mv ../mpv .
  ./update
  # Make mpv build flags
  sed 's|--disable-doc|--disable-doc --cc=clang --disable-programs|g' -i scripts/ffmpeg-config
  sed 's|""|"--prefix=/usr --confdir=/etc/mpv --extra-cflags=-I/usr/include/samba-4.0"|g' -i scripts/mpv-config
}

build() {
  cd "${_gitname}"
  export CC="clang"
  make
}

package() {
  cd "${_gitname}/mpv"
  make DESTDIR="${pkgdir}" install-strip
  install -d "${pkgdir}"/{etc/mpv,usr/share/doc/mpv}
  install -Dm644 etc/{input,example,encoding-example-profiles}.conf "${pkgdir}/etc/mpv/"
  cp -R DOCS "${pkgdir}/usr/share/doc/mpv/"
  install -Dm644 "${srcdir}/mpv.desktop" "${pkgdir}/usr/share/applications/mpv.desktop"
  install -Dm644 etc/mpv-icon-source.svg "${pkgdir}/usr/share/pixmaps/mpv.svg"
}
