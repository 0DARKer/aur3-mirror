# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=mpv-build-git
pkgver=20140211.891f58e
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg). (GIT version)"
arch=('i686' 'x86_64')
depends=('desktop-file-utils' 'enca' 'fontconfig' 'hicolor-icon-theme' 'lame' 'lcms2' 'libbluray' 'libcaca' 'libcdio-paranoia' 'libguess' 'libdvdnav' 'libfdk-aac' 'libpulse' 'libva'
         'libquvi' 'libxinerama' 'libxkbcommon' 'libxss' 'libxv' 'lirc-utils' 'mesa' 'mpg123' 'openal' 'portaudio' 'rsound' 'sdl' 'sdl2' 'smbclient' 'v4l-utils' 'x264')
license=('GPL2')
url="http://mpv.io/"
makedepends=('git' 'mesa' 'python-docutils' 'yasm')
conflicts=('mpv')
replaces=('mpv')
options=('!emptydirs')
install="${pkgname}.install"
source=('git+https://github.com/mpv-player/mpv-build.git'
        'git+https://github.com/mpv-player/mpv.git'
        'ffmpeg::git+https://github.com/FFmpeg/FFmpeg.git'
        'git+https://github.com/libass/libass.git'
        'git+http://anongit.freedesktop.org/git/fribidi/fribidi.git'
        'mpv_configure_like_ffmpeg_configure.patch'
        'libass_configure_like_ffmpeg_configure.patch'
        'fribidi_configure_like_ffmpeg_configure.patch')
sha1sums=('SKIP'
          'SKIP'
          'SKIP'
          'SKIP'
          'SKIP'
          'b974f0de41c013628472d010b17fa7daac21117f'
          '628ae4468c25a94ce8400c1e4f8773ae43c8728c'
          'e2727ace604c2678f617452f20d9ce4bd5715d5d')
_gitname="mpv-build"
backup=('etc/mpv/input.conf'
        'etc/mpv/encoding-profiles.conf')

pkgver() {
  cd mpv
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  rm -fr mpv ffmpeg libass ffmpeg_build build_libs fribidi
  mv ../{mpv,ffmpeg,libass,fribidi} .

  # patch to tuning scripts/{mpv,libass,fribidi}-config like scripts/ffmpeg-config
  patch -p0 -i ../mpv_configure_like_ffmpeg_configure.patch
  patch -p0 -i ../libass_configure_like_ffmpeg_configure.patch
  patch -p0 -i ../fribidi_configure_like_ffmpeg_configure.patch

  # Set ffmpeg/libass/fribidi/mpv flags
  echo "--disable-programs --enable-libx264 --enable-libmp3lame --enable-nonfree --enable-libfdk-aac --enable-pic"> ffmpeg_options
  echo "--with-pic" > libass_options
  echo "--with-pic" > fribidi_options
  echo "--prefix=/usr --confdir=/etc/mpv --disable-libquvi4 --enable-openal --enable-sdl2 --disable-pdf --enable-joystick --enable-shared" > mpv_options

  # Download Waf script
  cd mpv
  ./bootstrap.py
}

build() {
  cd "${_gitname}"
  ./build
}

package() {
  cd "${_gitname}"
  DESTDIR="${pkgdir}" ./install

  install -Dm644 mpv/etc/input.conf "${pkgdir}/etc/mpv/input.conf"
  install -Dm644 mpv/etc/example.conf "${pkgdir}/etc/mpv/mpv-example.conf"
  install -Dm644 mpv/DOCS/encoding.rst "${pkgdir}/usr/share/doc/mpv/encoding.rst"
  install -Dm644 mpv/DOCS/edl-mpv.rst "${pkgdir}/usr/share/doc/mpv/edl-mpv.rst"

  sed -e 's|etc/input.conf|etc/mpv/input.conf|g' \
      -e 's|usr/local/etc|etc/mpv|g' \
      -i "${pkgdir}/etc/mpv/"*.conf
  sed -e 's|etc/encoding-profiles.conf|etc/mpv/encoding-profiles.conf|' \
      -i "${pkgdir}/usr/share/doc/mpv/"*.rst

  mv "${pkgdir}/usr/bin/simple" "${pkgdir}/usr/bin/mpv-simple"
}
