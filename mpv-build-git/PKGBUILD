# Maintainer: Gustavo Alvarez <s1lpkn07@gmail.com>

pkgname=mpv-build-git
pkgver=20131122.1748928
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg). (GIT version)"
arch=('i686' 'x86_64')
depends=('desktop-file-utils' 'enca' 'fribidi' 'hicolor-icon-theme' 'libbluray' 'libcaca' 'libcdio-paranoia' 'libguess' 'libdvdread' 'libpulse' 'libva'  'lcms2'
         'libquvi' 'libxinerama' 'libxkbcommon' 'libxss' 'libxv' 'lirc-utils' 'mpg123' 'openal' 'portaudio' 'rsound' 'sdl' 'sdl2' 'smbclient' 'v4l-utils')
license=('GPL2')
url="http://mpv.io/"
makedepends=('git' 'live-media' 'mesa' 'python-docutils' 'yasm')
conflicts=('mpv')
replaces=('mpv')
options=('!emptydirs')
install="${pkgname}.install"
source=('git://github.com/mpv-player/mpv-build.git'
        'git://github.com/mpv-player/mpv.git'
        'git://source.ffmpeg.org/ffmpeg.git'
        'git+https://code.google.com/p/libass'
#        'git://anongit.freedesktop.org/fribidi/fribidi.git'
        )
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
#         'SKIP'
         )
_gitname="mpv-build"

pkgver() {
  cd "${srcdir}/mpv"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  rm -fr mpv ffmpeg libass ffmpeg_build build_libs # fribidi
  mv ../{mpv,ffmpeg,libass} . # ,fribidi

  # Make mpv/ffmpeg build flags
  echo "--disable-programs" > ffmpeg_options
  sed 's|OPTIONS="|OPTIONS="--prefix=/usr --confdir=/etc/mpv --disable-libquvi4 --enable-openal --disable-pdf --lua=52 |g' -i scripts/mpv-config

  # Ugly fix for build with new system
  # use waf instead make
  sed 's|./configure --extra-cflags=-I"$BUILD"/build_libs/include --extra-ldflags=-L"$BUILD"/build_libs/lib|./waf configure|g' -i scripts/mpv-config
  sed -e 's|$(MAKE) -C mpv install|cd mpv; .\/waf install|g' \
      -e 's|$(MAKE) -C mpv distclean|cd mpv; .\/waf distclean|g' \
      -e 's|$(MAKE) -C mpv|cd mpv; .\/waf build|' -i Makefile

  # download own waf (fail if use distro package)
  cd mpv
  python bootstrap.py

}

build() {
  cd "${_gitname}"
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 mpv/etc/input.conf "${pkgdir}/etc/mpv/input.conf"
  install -Dm644 mpv/etc/example.conf "${pkgdir}/etc/mpv/mpv-example.conf"
  install -Dm644 mpv/etc/encoding-example-profiles.conf "${pkgdir}/etc/mpv/encoding-example-profiles.conf"

  sed -e 's|/usr/local/etc/mpv.conf|/etc/mpv.conf|' \
      -e 's|/path/to/the/file/you/want/to/include|/etc/mpv/encoding-profiles.conf|' \
      -e 's|/path/to/this|/etc/mpv|' \
      -i "${pkgdir}/etc/mpv/"*.conf

  install -Dm644 mpv/DOCS/tech-overview.txt "${pkgdir}/usr/share/doc/mpv/tech-overview.txt"
  install -Dm644 mpv/DOCS/encoding.rst "${pkgdir}/usr/share/doc/mpv/encoding.rst"
}
