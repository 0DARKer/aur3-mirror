# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>
# Contributor:

_pkgbasename=cloog
pkgname=lib32-$_pkgbasename
pkgver=0.17.0
pkgrel=1
pkgdesc="Library that generates loops for scanning polyhedra (32 bit)"
arch=('x86_64')
url="http://www.bastoul.net/cloog/"
license=('GPL')
depends=('lib32-isl' 'lib32-gmp' "$_pkgbasename")
conflicts=('lib32-cloog-ppl')
options=('!libtool')
source=(http://www.bastoul.net/cloog/pages/download/$_pkgbasename-$pkgver.tar.gz)
md5sums=('0aa3302c81f65ca62c114e5264f8a802')

build() {
  export CC='gcc -m32'
  
  cd "$srcdir/$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --with-isl=system --with-gmp=system \
  	--libdir=/usr/lib32
  make
}

check() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make check
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/usr"/{bin,include}
}
