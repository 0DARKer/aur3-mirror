# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=xbic
pkgver=1.0
pkgrel=1
pkgdesc="A tactical turn-based game based on the Battle Isle series"
arch=('i686' 'x86_64')
url="http://www.ibiblio.org/pub/Linux/games/strategy"
license=('GPL2')
depends=('libpng' 'qt2')
source=("http://www.ibiblio.org/pub/Linux/games/strategy/${pkgname}-${pkgver}.tgz"
        "00-makefile.patch"
        "01-includes.patch"
        "${pkgname}.png"
        "${pkgname}.desktop"
        "${pkgname}.sh")
md5sums=('d6116030331749a1da67d4bc2dc756b1'
         '60a54beaf679cd54f7926b03bf0c2ae3'
         '799c2e315f8fef08333ef480d274fd49'
         '52fad6ae9e03dcfc1efb58f05d391e31'
         '794d58f764c172b1c8574ebb18fd6a02'
         'd4364c4901a90d15d378f48e4478ce79')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}_qtlib2/src"

  # Apply patches to fix compilation issues
  patch -Np2 -i ../../00-makefile.patch
  patch -Np2 -i ../../01-includes.patch

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}_qtlib2"

  # Install game files
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -r anim bin level* pics start_game "${pkgdir}/opt/${pkgname}"

  # Install a launcher
  install -Dm755 ../${pkgname}.sh "${pkgdir}/usr/bin/${pkgname}"

  # Install a desktop entry
  install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 README* "${pkgdir}/usr/share/doc/${pkgname}"
}
