# Maintainer: Antonio Rojas 

pkgname=gwenview-git
pkgver=r5313.ae1e5d1
pkgrel=1
pkgdesc='A fast and easy to use image viewer for KDE'
arch=('i686' 'x86_64')
url='http://kde.org/applications/graphics/gwenview/'
license=('GPL')
depends=('kactivities-frameworks' 'baloo-widgets-frameworks-git' 'exiv2' 'lcms2' 'libkonq-frameworks-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
#conflicts=('kdegraphics-gwenview')
#provides=('kdegraphics-gwenview')
source=("git://anongit.kde.org/gwenview.git")
sha256sums=('SKIP')

pkgver() {
  cd gwenview
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  sed -i 's|KF5 CONFIG REQUIRED|KF5 REQUIRED|' gwenview/CMakeLists.txt
}

build() { 
  cd build
  cmake ../gwenview \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
