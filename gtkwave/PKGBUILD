# Contributor: Jared Casper <jaredcasper@gmail.com>
pkgname=gtkwave
pkgver=3.3.15
pkgrel=1
pkgdesc="A wave viewer which reads LXT, LXT2, VZT, GHW and VCD/EVCD files"
arch=(i686 x86_64)
url="http://gtkwave.sourceforge.net"
license=('GPL' 'MIT')
depends=('gtk2' 'xz' 'bzip2')
makedepends=('flex' 'gperf')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://gtkwave.sourceforge.net/$pkgname-$pkgver.tar.gz)
md5sums=('102b71665b4b4dfbf94b0d4eec06d61f')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-tcl
  make CFLAGS=-D_LARGEFILE64_SOURCE
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make -j1 prefix=$pkgdir/usr mandir=$pkgdir/usr/share/man install
  install -D -m644 $srcdir/$pkgname-$pkgver/.gtkwaverc $pkgdir/usr/share/$pkgname/sample.gtkwaverc
  install -D -m644 $srcdir/$pkgname-$pkgver/MIT.TXT $pkgdir/usr/share/licenses/$pkgname/MIT.TXT
}
