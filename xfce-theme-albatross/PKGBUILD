# Maintainer: flan_suse <windows2linux AT zoho DOT com>
pkgname=xfce-theme-albatross
_pkgname=Albatross
pkgver=1.1
pkgrel=1
pkgdesc="A dark, smooth Xfce theme, introduced in the release of Xubuntu 9.10"
arch=('any')
url="http://shimmerproject.org/projects/albatross/"
license=('GPL3' 'CCPL')
groups=('xfce-themes-shimmer-collection')
depends=('gtk-engine-murrine')
optdepends=('elementary-icons: matching icon set')
source=("https://download.github.com/shimmerproject-$_pkgname-v$pkgver-0-gd92d7fd.tar.gz")
md5sums=('1292f5375c58c5ded93045b69b438339')

package() {
    
    # Move into source directory
    cd "${srcdir}"

    # Remove unnecessary folders for Arch Linux systems
    rm -rf "shimmerproject-$_pkgname-791649c/usplash/"
    rm -rf "shimmerproject-$_pkgname-791649c/xsplash/"

    # Remove excess file, left over by the original author
    find "$PWD" -name "*~" -exec rm '{}' \;
    rm -rf "shimmerproject-$_pkgname-791649c/shearwater/"
    rm -rf "shimmerproject-$_pkgname-791649c/.gitignore"

    # Create installation directories
    install -d -m755 "${pkgdir}/usr/share/themes/"
    install -d -m755 "${pkgdir}/usr/share/backgrounds/"
    install -d -m755 "${pkgdir}/usr/share/xfce4/backdrops/"

    # Copy the wallpaper to the correct directory, then remove the backdrops folder
    cp -f "shimmerproject-$_pkgname-791649c/backdrops/albatross-2009-10.png" "${pkgdir}/usr/share/backgrounds/$_pkgname.png"
    rm -rf "shimmerproject-$_pkgname-791649c/backdrops/"

    # Install the rest of the theme
    cp -rf "shimmerproject-$_pkgname-791649c" "${pkgdir}/usr/share/themes/$_pkgname"

    # Make a symbolic link inside of the Xfce backdrops directory, for use with Desktop Settings
    cd "${pkgdir}/usr/share/xfce4/backdrops/"
    ln -s "../../backgrounds/$_pkgname.png" "$_pkgname.png"

}


