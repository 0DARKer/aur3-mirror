# Maintainer Denis Demidov <dennis.demidov@gmail.com>
pkgname=hpx
pkgver=0.9.9
pkgrel=3
pkgdesc="A general purpose C++ runtime system for parallel and distributed applications of any scale"
arch=("any")
url="http://stellar-group.org/libraries/hpx"
depends=("boost" "hwloc" "gperftools")
conflicts=("hpx-git")
license=("boost")
options=("staticlibs")

source="http://stellar.cct.lsu.edu/files/${pkgname}_${pkgver}.tar.bz2"

sha512sums=("41908beccb8991d8b57faa8b3ebd56698f412715b791480153ab195630310e6bbd6b5c5d98d0491ad6ac43b8980d9a216b6b270d00e57f5a25518be9a2b24657")

build() {
    cd "${srcdir}/${pkgname}_${pkgver}"

    mkdir build
    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DHPX_BUILD_EXAMPLES=OFF \
        -DHPX_BUILD_TESTS=OFF
    make
}

package() {
    cd "${srcdir}/${pkgname}_${pkgver}/build"
    make DESTDIR="${pkgdir}" install
}
