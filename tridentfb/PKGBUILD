# Contributor: Maribu <m1a2r3i4.h5a6h7n@uni-muenster.de> <-- delete numbers

pkgname=tridentfb
_majorver=3
_minorver=7
pkgver="${_majorver}.${_minorver}"
pkgrel=1
pkgdesc="The tridentfb kernel module - a framebuffer for trident graphic cards"
url="http://www.kernel.org/doc/Documentation/fb/tridentfb.txt"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' "linux>=${pkgver}" "linux<${_majorver}.$((${_minorver} + 1))")
makedepends=('linux-headers')
source=('Makefile' 'tridentfb.c')
install=$pkgname.install

_extramodules=extramodules-${pkgver}-ARCH

build() {
	cd "${srcdir}"
	make
}

package() {
	cd "${srcdir}"
	mkdir -p "${pkgdir}/usr/lib/modules/${_extramodules}/"
	install -m644 *.ko -t "${pkgdir}/usr/lib/modules/${_extramodules}/"
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;
}

md5sums=('f70956a336400379a597e765a4cbf5f0'  
'ffa4d0792e2f8bef819b6bd0168c0dc4')

