# Maintainer: luspi <luspi@gmx.de>
pkgname=alsa-utils-transparent
_pkgname=alsa-utils
pkgver=1.0.27
pkgrel=5
pkgdesc="A patched version of the alsa-utils package to support transparent terminals"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.24" 'dialog' 'pciutils' 'ncurses' 'psmisc' 'libsamplerate')
makedepends=('xmlto' 'docbook-xsl')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$_pkgname-${pkgver}.tar.bz2
        90alsa
        alsa-restore-1.0.27.service.patch
        alsactl-fix-buffer-overflow-1.0.27.patch
        arecord-1.0.27.patch
        transparency.patch
        alsa.conf.d)
md5sums=('cbfb21a24f63fb052b3392195639ce48'
         '529216f6a46e61a546640e08ea7a0879'
         '0a27f7b80351d6baa274c32d859bf037'
         'cd577445636542f9f60b420e4ffe3f0f'
         'cecb38f03bd245d0584bfd63370e9a05'
         '586f985eea6abf0a6a6c753ea4fa542e'
         '4c8ae9f976c7df5e271a4cb91ccc7767')
backup=(etc/conf.d/alsa)
conflicts=('alsa-utils')
provides=('alsa-utils')

prepare() {
  cd $srcdir/$_pkgname-${pkgver}
  patch -Np1 -i ${srcdir}/alsa-restore-1.0.27.service.patch
  patch -Np1 -i ${srcdir}/alsactl-fix-buffer-overflow-1.0.27.patch
  patch -Np1 -i ${srcdir}/arecord-1.0.27.patch
  patch -Np1 -i ${srcdir}/transparency.patch
  # remove wrong udev file from sources
  rm alsactl/90-alsa-restore.rules
}

build() {
  cd $srcdir/$_pkgname-${pkgver}
  ./configure --prefix=/usr --disable-alsaconf --sbindir=/usr/bin \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}
package() {
  cd $srcdir/$_pkgname-${pkgver}
  make DESTDIR=$pkgdir install
  install -D -m755 ../90alsa ${pkgdir}/etc/pm/sleep.d/90alsa
  install -D -m644 ../alsa.conf.d $pkgdir/etc/conf.d/alsa
  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}

