# Maintainer: luspi <luspi@gmx.de>
pkgname=alsa-utils-transparent
_pkgname=alsa-utils
pkgver=1.0.27.1
pkgrel=1
pkgdesc="A patched version of the alsa-utils package to support transparent terminals"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.24" 'dialog' 'pciutils' 'ncurses' 'psmisc' 'libsamplerate')
makedepends=('xmlto' 'docbook-xsl')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$_pkgname-${pkgver}.tar.bz2
        90alsa
        transparency.patch
        alsa.conf.d)
md5sums=('3d81357b997744a139881ef72bc6921a'
         '529216f6a46e61a546640e08ea7a0879'
         '586f985eea6abf0a6a6c753ea4fa542e'
         '4c8ae9f976c7df5e271a4cb91ccc7767')
backup=(etc/conf.d/alsa)
conflicts=('alsa-utils')
provides=('alsa-utils')

prepare() {
  cd $srcdir/$_pkgname-${pkgver}
  patch -Np1 -i ${srcdir}/transparency.patch
}

build() {
  cd $srcdir/$_pkgname-${pkgver}
  ./configure --prefix=/usr --disable-alsaconf --sbindir=/usr/bin \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}
package() {
  cd $srcdir/$_pkgname-${pkgver}
  make DESTDIR=$pkgdir install
  install -D -m755 ../90alsa ${pkgdir}/etc/pm/sleep.d/90alsa
  install -D -m644 ../alsa.conf.d $pkgdir/etc/conf.d/alsa
  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}

