# Maintainer: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2
pkgver=0.16.0
pkgrel=1
pkgdesc="A linkable library for Git"
arch=('i686' 'x86_64')
url="http://libgit2.github.com/"
depends=('zlib')
makedepends=('cmake')
license=('GPL2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/tarball/v$pkgver")
md5sums=('d75d4c2b0773abf2676f06dabdf5f31f')

build() {
  cd "$srcdir/$pkgname-$pkgname-d286dfe"

  rm -fr build
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgname-d286dfe/build"
  make DESTDIR="$pkgdir" install
}
