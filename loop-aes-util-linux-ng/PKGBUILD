# Maintainer: Darko Trifunovski <dtrifuno AT gmail doT com>
pkgname=loop-aes-util-linux-ng
_origname=util-linux-ng
pkgver=2.18
pkgrel=1
pkgdesc="Patched version of util-linux-ng for use with loop-aes"
arch=('i686' 'x86_64')
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
license=('GPL')
depends=('bash' 'ncurses>=5.7' 'zlib' 'filesystem')
optdepends=('perl: for chkdupexe support')
provides=('linux32' 'util-linux' 'util-linux-ng=2.18')
conflicts=('linux32' 'util-linux' 'e2fsprogs<1.41.8-2' 'util-linux-ng')
options=('!libtool')
install=util-linux-ng.install
source=(ftp://ftp.kernel.org/pub/linux/utils/${_origname}/v2.18/${_origname}-${pkgver}.tar.bz2
	fix-findmnt.patch
	util-linux-ng-nilfs2.patch
	util-linux-ng-cfdisk.patch
	util-linux-ng-2.18-20100706.diff)
# God kills a kitten everytime you use MD5
sha512sums=('8406fe29ff7ad85a0d0b92ef660ec8b70519a28d59ee81cd25fddb51007a01e5eb57a505fc5b2407eb8185cd77ef355239ff7e581247ffdd20609945744a0ed5'
            '1b8c27ece21c914da91e4f5e3e185a59d8927f32fe7176c6e4b22c72cfb28d0c521c4fe347c53e16420baa41580e8d71891e6b53d09224cd9a6d467a8421b3c7'
            '7a80ac4d80050d9388b30da6974c1dc30e3389c7fe4bb8ba5db6de75de4423e80c9482dc522e414569995f868262259210a7260b4936c27318cd8612a4bfd93a'
            '325a2b9afcd9379e33aee5edbf9bef7f586f1690a32bdab289f64cb71ea5c745aa00bf33919944657ef56f33a7aae22cccbf8f4b7c313a50149ead656927c7c4'
            'ef1ed3a7858207e72ae5da2ffac383fd2990d74758af428610fcd75ed557f6feb54c1f4c8874f3489891f386a5f12f6974c801dc3bc212578075d48217460cea')

build() {
  # prepare
  cd "${srcdir}/${_origname}-${pkgver}"
  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i hwclock/hwclock.c || return 1
  mkdir -p "${pkgdir}/var/lib/hwclock" || return 1
  # fix findmnt
  patch -p1 -i "${srcdir}/fix-findmnt.patch" 
  # add support for nilfs2 and fix cfdisk partition changing (from the official PKGBUILD)
  patch -Np1 -i "${srcdir}/util-linux-ng-nilfs2.patch"
  patch -Np1 -i "${srcdir}/util-linux-ng-cfdisk.patch"
  # apply loop-aes patch
  patch -p1 -i "${srcdir}/util-linux-ng-2.18-20100706.diff"

  # configure and make
  autoreconf || return 1
  automake || return 1
  ./configure --enable-arch --enable-write --enable-raw --disable-wall --enable-rdev --enable-partx || return 1
  make HAVE_SLN=yes ADD_RAW=yes || return 1
  make HAVE_SLN=yes ADD_RAW=yes DESTDIR="${pkgdir}" install || return 1

  # remove files
  rm -f "${pkgdir}/bin/kill"
  rm -f "${pkgdir}/usr/share/man1/kill.1"
  rm -f "${pkgdir}/usr/share/man5/nfs.5"
  rm -f "${pkgdir}/usr/share/info/dir"
}
