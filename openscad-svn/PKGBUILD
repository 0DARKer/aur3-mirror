# Maintainer: Torsten Wagner <tottiwagner@yahoo.de>
pkgname=openscad-svn
pkgver=590
pkgrel=1
pkgdesc="Creating 3D CAD objects or extrude 2D CAD drawings by a C like description language."
arch=('i686' 'x86_64')
license=('GPLv2')
url="http://openscad.org/"
depends=('qt' 'cgal' 'eigen' 'opencsg' 'bison' 'flex' 'gcc-libs')
provides=('openscad')
makedepends=('subversion' 'sed')
conflicts=('openscad')

_svntrunk=http://svn.clifford.at/openscad/trunk
_svnmod="openscad"

build() {
  cd "${srcdir}"
  echo ${pkgdir}
  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done or server timeout"
  msg "Preparing build"
  rm -rf "${srcdir}/${_svnmod}-build"
  cp -r "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"  
  msg "Starting qmake..."
  qmake || return 1
  msg "Apply patch to makefile to replace lib lboost_thread by lboost_thread-mt" 
  sed  -i "s/\-lboost_thread/\-lboost_thread-mt/" Makefile  
  msg "Apply patch to makefile to install files not in /usr/local"
  sed  -i "s|/usr/local/|/usr/|g" Makefile  
  msg "Starting make..."
  make || return 1  
}

package() {
  cd "${srcdir}/${_svnmod}-build"
  make INSTALL_ROOT="${pkgdir}"  install
}
