# Maintainer: DrZaius <lou[at]fakeoutdoorsman.com>
# Contributor: zhuqin <zhuqin83[at]gmail>
# Contributor: pressh <pressh[at]gmail>

pkgname=x264-git
_gitname="x264"
pkgver=2310.76a5c3a
pkgrel=2
pkgdesc="H.264 video encoder and library"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glibc')
makedepends=('git' 'yasm')
url="http://www.videolan.org/developers/x264.html"
conflicts=('x264')
provides=("x264=$pkgver")
source=('git://git.videolan.org/x264.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname

  # add '--bit-depth=10' if you want 10 bit output
  ./configure \
    --prefix=/usr \
    --enable-static

  make
}

package() {
  cd $_gitname
  make DESTDIR=$pkgdir install
}
