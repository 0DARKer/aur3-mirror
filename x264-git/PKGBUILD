# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>
# Contributor: zhuqin <zhuqin83[at]gmail[dot]com>
# Contributor: pressh <pressh[at]gmail[dot]com>

pkgname=x264-git
pkgver=20111002
pkgrel=1
pkgdesc="H.264/MPEG-4 AVC video encoder"
arch=('i686' 'x86_64')
url="http://www.videolan.org/developers/x264.html"
license=('GPL')
depends=('glibc')
makedepends=('git' 'yasm')
conflicts=('x264')
provides=("x264=$(LANG=C pacman -Si x264 | grep Version | awk '{ print $NF }')") 

_gitroot="git://git.videolan.org/x264.git"
_gitname="x264"

build() {
  cd $srcdir
  msg "Connecting to the Git repository..."
  
  if [[ -d $srcdir/$_gitname ]] ; then
    cd $_gitname
    git pull origin
  else
    git clone $_gitroot
  fi
  
  msg "Git clone done"
  msg "Starting make..."
  
  rm -rf $srcdir/$_gitname-build
  git clone $srcdir/$_gitname $srcdir/$_gitname-build
  
  cd $srcdir/$_gitname-build

  # add '--bit-depth=10' is you want 10 bit output 
  ./configure --prefix=/usr --enable-static
  
  make
}

package() {
  cd $srcdir/$_gitname-build
  make DESTDIR=$pkgdir install
  rm -rf $srcdir/$_gitname-build
}
