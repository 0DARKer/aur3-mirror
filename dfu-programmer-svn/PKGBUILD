# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>

pkgname=dfu-programmer-svn
pkgver=95
pkgrel=1
pkgdesc="Device Firmware Update based USB programmer for Atmel chips with a USB bootloader"
arch=('i686' 'x86_64')
url="http://dfu-programmer.sourceforge.net/"
license=('GPL')
depends=('libusb')
makedepends=('subversion')
source=('dfu-programmer-as-needed.patch')
md5sums=('a836e61ea39c240e19224de6fcfed725')

build() {
    _svntrunk=https://dfu-programmer.svn.sourceforge.net/svnroot/dfu-programmer/trunk/dfu-programmer

    if [ -d "$pkgname/.svn" ]; then
        msg "SVN tree found, reverting changes and updating to -r$pkgver"
        (cd "$pkgname" && svn revert -R . && (make distclean || :); svn up -r "$pkgver")
    else
        msg "Checking out SVN tree of -r$pkgver"
        svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$pkgname"
    fi

    cd "$srcdir/$pkgname"

    patch -p0 -i "$srcdir"/dfu-programmer-as-needed.patch || return 1
    ./bootstrap.sh || return 1
    ./configure --prefix=/usr || return 1
    make || return 1
    make DESTDIR="$pkgdir" install || return 1
}

