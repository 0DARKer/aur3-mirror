# Maintainer: Zohar Malamant <dotfloat gmail com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Dmitry Shilov <stormblast@land.ru>

pkgname=doomsday
pkgver=1.9.10
pkgrel=1
pkgdesc="An advanced Doom engine that supports DOOM, Heretic and Hexen."
url="http://dengine.net/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glu' 'qt' 'sdl_mixer' 'mesa' 'fmodex' 'wxpython')
optdepends=('doom1-wad: Doom shareware', 
            'heretic1-wad: Heretic shareware', 
            'hexen1-wad: Hexen shareware')
source=("http://downloads.sourceforge.net/deng/doomsday-${pkgver}.tar.gz"
        "lock_thread_safe.patch")
md5sums=('c5766f45102727f9fae370643c5bfa72'
         '1e81afe7c79067e81defabfcd653d094')

build() {

    # Force python2
    rm -rf ${srcdir}/python
    mkdir ${srcdir}/python
    ln -s /usr/bin/python2 ${srcdir}/python/python
    export PATH=${srcdir}/python:$PATH

    # Enter build directory
    cd ${srcdir}/doomsday-${pkgver}/doomsday

    # Apply thread lock patch
    patch -p2 < ${srcdir}/lock_thread_safe.patch

    # Fix lib64 stuff
    sed 's/lib64/lib/g' -i config_unix.pri

    # Hack for compiling fmodex libs
    echo -e "FMOD_DIR = \"${srcdir}/doomsday-${pkgver}/doomsday/plugins/fmod\"" > config_user.pri
    ln -sf /usr/include/fmodex/* ${srcdir}/doomsday-${pkgver}/doomsday/plugins/fmod/include/

    # Generate makefiles using qmake
    qmake -r ./doomsday.pro CONFIG+="deng_fmod release"

    # Build
    make $MAKEFLAGS
}

package() {
    cd ${srcdir}/doomsday-${pkgver}/doomsday
    make INSTALL_ROOT=${pkgdir} install

    sed -i 's/python/python2/' "${pkgdir}/usr/bin/launch-doomsday"
}
