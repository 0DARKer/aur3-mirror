# Maintainer: Zohar Malamant <dotfloat gmail com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Dmitry Shilov <stormblast@land.ru>

pkgname=doomsday
pkgver=1.9.9
pkgrel=1
pkgdesc="An advanced port of the Doom game engine"
url="http://dengine.net/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('qt' 'sdl_mixer' 'mesa' 'fmodex' 'python2' 'wxpython')
makedepends=('qt')
optdepends=('doom1-wad: Doom shareware', 
            'heretic1-wad: Heretic shareware', 
            'hexen1-wad: Hexen shareware')
conflicts=('doomsday-git')
source=("http://downloads.sourceforge.net/deng/doomsday-${pkgver}.tar.gz")
md5sums=('394cd3a910130af6f0130810c15b324b')

build() {

    # Force python2
    rm -rf ${srcdir}/python
    mkdir ${srcdir}/python
    ln -s /usr/bin/python2 ${srcdir}/python/python
    export PATH=${srcdir}/python:$PATH

    # Enter build directory
    cd ${srcdir}/doomsday-${pkgver}/doomsday

    # Hack for compiling fmodex libs
    echo -e "FMOD_DIR = \"${srcdir}/doomsday-${pkgver}/doomsday/plugins/fmod\"" > config_user.pri
    ln -sf /usr/include/fmodex/* ${srcdir}/doomsday-${pkgver}/doomsday/plugins/fmod/include/

    # Generate makefiles using qmake
    qmake -r ./doomsday.pro CONFIG+="deng_fmod release"

    # Build
    make $MAKEFLAGS
}

package() {

    cd ${srcdir}/doomsday-${pkgver}/doomsday
    make INSTALL_ROOT=${pkgdir} install

    sed -i 's/python/python2/' "${pkgdir}/usr/bin/launch-doomsday"
}
