# Maintainer: Julien Schueller <julien.schueller@gmail.com>

pkgname=mingw32-libgnurx
pkgver=2.5.1
pkgrel=1
arch=(any)
pkgdesc="libgnurx (mingw32)"
depends=('mingw32-runtime')
makedepends=('mingw32-gcc')
options=('!buildflags' '!strip')
license=('LGPL')
url="http://mingw.sourceforge.net/"
source=("http://download.sourceforge.net/mingw/Other/UserContributed/regex/mingw-regex-${pkgver}/mingw-libgnurx-${pkgver}-src.tar.gz"
        "mingw32-libgnurx-honor-destdir.patch")
md5sums=('35c8fed3101ca1f253e9b6b1966661f6'
         '2d87c59177adf5ec13dbcc02acc4a450')

build()
{
  cd ${srcdir}/mingw-libgnurx-${pkgver}
  patch -Np0 -i ../mingw32-libgnurx-honor-destdir.patch
  unset LDFLAGS CPPFLAGS
  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32
  make
}

package()
{
  cd ${srcdir}/mingw-libgnurx-${pkgver}
  mkdir -p $pkgdir/usr/i486-mingw32/include
  make install DESTDIR=${pkgdir}
  i486-mingw32-strip -x -g ${pkgdir}/usr/i486-mingw32/bin/libgnurx-0.dll
  i486-mingw32-strip -g ${pkgdir}/usr/i486-mingw32/lib/libregex.a
  rm -r ${pkgdir}/usr/i486-mingw32/man
}
