# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=seaview
pkgver=4.5.2
pkgrel=1
pkgdesc="Multiplatform GUI for multiple sequence alignment and molecular phylogeny"
arch=('i686' 'x86_64')
url="http://doua.prabi.fr/software/seaview"
license=('custom')
depends=('shared-mime-info' 'fltk' 'libjpeg-turbo')
optdepends=('clustal-omega: alignment'
            'muscle: alignment')
install='seaview.install'
source=('http://doua.prabi.fr/software/seaview_data/seaview.tar.gz'
	'Makefile.patch'
	'seaview.install'
        'seaview.desktop'
        'seaview.svg'
        'seaview.sharedmimeinfo'
        'copyright')
md5sums=('97b2efbab30eaecc200253a8ce069d03'
         '3a970c4daf988c7ab3f94b76cc6c72ab'
         'fd6607168def7db9cf4775cacdbb9f02'
         '94f13509470dd69f053ffc178884e292'
         '70ceabd9ae2f398612948c66c564857e'
         '9592a71ee55fa6edfbaddbce2d5469b9'
         '6634a8269933e9fa93da6fc33878a575')

prepare() {
  patch seaview/Makefile < Makefile.patch
}

build() {
  cd seaview
  make
  gzip -f seaview.1
}

package() {
  install -d $pkgdir/usr/{bin,share/{seaview,pixmaps,man/man1,applications,icons/hicolor/scalable/apps,mime/packages,licenses/seaview}}

  cd seaview
  install -m755 seaview $pkgdir/usr/bin/
  install -m644 seaview.xpm $pkgdir/usr/share/pixmaps/
  install -m644 seaview.html $pkgdir/usr/share/seaview/
  install -m644 seaview.1.gz $pkgdir/usr/share/man/man1/
  cd ..
  install -m644 seaview.desktop $pkgdir/usr/share/applications/
  install -m644 seaview.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/
  install -m644 seaview.sharedmimeinfo $pkgdir/usr/share/mime/packages/seaview.xml
  install -m644 copyright $pkgdir/usr/share/licenses/seaview/
}
