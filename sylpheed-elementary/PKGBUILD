# Contributor: Oliver Mader <b52@reaktor42.de>

pkgname=sylpheed-elementary
pkgver=3.1.3
pkgrel=1
pkgdesc="Lightweight and user-friendly e-mail client including an elementary based iconset."
arch=('i686' 'x86_64')
url="http://sylpheed.sraoss.jp/en/"
provides=('sylpheed')
license=('GPL')
depends=('gpgme' 'gtkspell')
makedepends=('compface' 'openssl')
options=('!libtool')
install="sylpheed.install"
source=("http://sylpheed.sraoss.jp/sylpheed/v3.1/sylpheed-$pkgver.tar.bz2"
        "http://reaktor42.de/~b52/code/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('2ad17c0f822b266d83dfba11c3088659'
         'cc2f5a814fca47176ac7465c6432b226')

build() {
    cd "$srcdir/sylpheed-$pkgver"

    cp -rf $srcdir/$pkgname-$pkgver/src/icons/ $srcdir/sylpheed-$pkgver/src/
    
    ./configure --prefix=/usr --enable-ldap || return 1
    make || return 1

    cd plugin/attachment_tool && make || return 1
}

package () {
    cd "$srcdir/sylpheed-$pkgver"

    make DESTDIR="$pkgdir/" install || return 1

    cd plugin/attachment_tool && make DESTDIR="$pkgdir/" install-plugin || return 1
}
