# Maintainer: Uwe L. Korn <uwelk@xhochy.com>
# Contributor: Uwe L. Korn <uwelk@xhochy.com>

pkgname=openldap-smbk5pwd
pkgver=2.4.26
pkgrel=5
pkgdesc="smbk5pwd Module LDAP Server (change LDAP, Samba and Kerberos password simultanouesly)"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.openldap.org/"
depends=("libldap>=${pkgver}" 'tcp_wrappers' 'libfetch' 'util-linux-ng' 'openldap' 'heimdal-aur')
source=("ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz")
md5sums=('f36f3086031dd56ae94f722ffae8df5e')
options=("emptydirs" "!libtool")

build() {
  cd ${srcdir}/openldap-${pkgver}
   
  LDFLAGS="$LDFLAGS -L\"${pkgdir}\"/libldap/usr/lib" 
  ./configure --prefix=/usr --mandir=/usr/share/man --libexecdir=/usr/lib \
    --sysconfdir=/etc --localstatedir=/var/lib/openldap \
    --enable-ipv6 --enable-syslog --enable-local \
    --enable-bdb --enable-hdb \
    --enable-crypt --enable-dynamic \
    --with-threads  --disable-wrappers \
    --enable-spasswd --with-cyrus-sasl \
    --enable-overlays=mod --enable-modules=yes
  
#  find . -name 'Makefile' -exec \
#  	sed -e 's|$(LDAP_LIBDIR)/liblber/liblber.la|/usr/lib/liblber-2.4.so.2|g' \
#	    -e 's|$(LDAP_LIBDIR)/libldap/libldap.la|/usr/lib/libldap-2.4.so.2|g' \
#	    -e 's|$(LDAP_LIBDIR)/libldap_r/libldap_r.la|/usr/lib/libldap_r-2.4.so.2|g' \
#	    -i {} \;

  cd include
  make
  
  cd ${srcdir}/openldap-${pkgver}/contrib/slapd-modules/smbk5pwd
  cp /usr/include/et/com_err.h ${srcdir}/openldap-${pkgver}/include 
  make
}

package() {
  cd ${srcdir}/openldap-${pkgver}/contrib/slapd-modules/smbk5pwd
  make DESTDIR=${pkgdir} install
  mkdir -p ${pkgdir}/usr/lib/openldap
  mv ${pkgdir}/usr/local/libexec/openldap/* ${pkgdir}/usr/lib/openldap/
  rmdir ${pkgdir}/usr/local/libexec/openldap
  rmdir ${pkgdir}/usr/local/libexec
  rmdir ${pkgdir}/usr/local
  
  install -Dm644 ${srcdir}/openldap-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
