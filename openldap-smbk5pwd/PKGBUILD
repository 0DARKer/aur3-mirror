# Maintainer: Uwe L. Korn <uwelk@xhochy.com>
# Contributor: Uwe L. Korn <uwelk@xhochy.com>

pkgname=openldap-smbk5pwd
pkgver=2.4.24
pkgrel=2
pkgdesc="smbk5pwd Module LDAP Server (change LDAP, Samba and Kerberos simultanouesly)"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.openldap.org/"
depends=("libldap>=${pkgver}" 'db' 'tcp_wrappers' 'libfetch' 'util-linux-ng' 'openldap')
source=("ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz"
        'slapd'
        'slapd.default')
md5sums=('116fe1e23a7b67686d5e62274367e6c0'
         '832354417c495f29affd2c772808959d'
         '6be69f6b7e522cb64cce8703da81ed32')

build() {
  cd ${srcdir}/openldap-${pkgver}
   
  export LIBS=-ldb
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --libexecdir=/usr/sbin \
              --sysconfdir=/etc \
              --localstatedir=/var/lib/openldap \
              --enable-bdb \
              --enable-crypt \
              --enable-dynamic \
              --with-threads \
              --enable-wrappers \
              --enable-spasswd \
              --with-cyrus-sasl
  
  find . -name 'Makefile' -exec \
  	sed -e 's|$(LDAP_LIBDIR)/liblber/liblber.la|/usr/lib/liblber-2.4.so.2|g' \
	    -e 's|$(LDAP_LIBDIR)/libldap/libldap.la|/usr/lib/libldap-2.4.so.2|g' \
	    -e 's|$(LDAP_LIBDIR)/libldap_r/libldap_r.la|/usr/lib/libldap_r-2.4.so.2|g' \
	    -i {} \;

  cd include
  make
  
  cd ${srcdir}/openldap-${pkgver}/contrib/slapd-modules/smbk5pwd
  make
}

package() {
  cd ${srcdir}/openldap-${pkgver}/contrib/slapd-modules/smbk5pwd
  make DESTDIR=${pkgdir} install
  mkdir -p ${pkgdir}/usr/sbin/openldap
  mv ${pkgdir}/usr/local/libexec/openldap/ ${pkgdir}/usr/sbin/
}
