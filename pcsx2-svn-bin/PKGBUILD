_pkgbasename=pcsx2
pkgname=$_pkgbasename-svn-bin
pkgver=4594
pkgrel=1
pkgdesc="A PlayStation 2 emulator (Binary)."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
if [ $CARCH == "x86_64" ]; then
	depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
		 'lib32-alsa-lib' 'lib32-glew' 'lib32-portaudio'
		 'lib32-libjpeg6' 'lib32-sdl')
else
	depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew' 'portaudio' 
		 'alsa-lib' 'libjpeg6' 'sdl')
fi
conflicts=('pcsx2' 'pcsx2-svn' 'pcsx2-bin') # Same destination folders
install="pcsx2.install"
source=("http://$_pkgbasename.googlecode.com/files/$_pkgbasename-0.9.8-r$pkgver-linux.tar.gz"
		'pcsx2.sh' 'pcsx2.xpm' 'pcsx2.desktop')
md5sums=('1e92cf5659438c24a255f0da3e9d46b4'
         '19fa49bb8624504237ead900ba663322'
         '18cb3c5a243a27363553d3e1008210c0'
         'b055ff7c0f236f33702f4813ae4b980d')

package() {
	cd "$srcdir/$_pkgbasename-0.9.8-r$pkgver-linux"
	mkdir -p "${pkgdir}/opt/pcsx2"
	cp -rPf * "${pkgdir}/opt/pcsx2"
	# Install Man page
	install -Dm644 "${pkgdir}/opt/pcsx2/docs/pcsx2.man" "${pkgdir}/usr/share/man/man1/pcsx2.1"
	# Install translation files
	mkdir -p ${pkgdir}/usr/share/locale
	mv ${pkgdir}/opt/pcsx2/Langs/* ${pkgdir}/usr/share/locale
	# Install script, icon and desktop item
	install -Dm755 "${srcdir}/pcsx2.sh" "${pkgdir}/usr/bin/pcsx2"
	install -Dm644 "${srcdir}/pcsx2.xpm" "${pkgdir}/usr/share/pixmaps/pcsx2.xpm"
	install -Dm644 "${srcdir}/pcsx2.desktop" "${pkgdir}/usr/share/applications/pcsx2.desktop"
	# Clean up unused files
	rm -f "${pkgdir}/opt/pcsx2/launch_pcsx2_linux.sh" 	# Already using pcsx2.sh
	rm -Rf "${pkgdir}/opt/pcsx2/"{Langs,bios} 		# Unneeded directories
	rm -f "${pkgdir}/opt/pcsx2/docs/pcsx2.man"		# Already installed
}
