# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Flamelab <panosfilip@gmail.com>
# Contributor: Chris Heien <chris.h.heien@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com>

pkgname=libkface-frameworks-git
pkgver=r722.370308a
pkgrel=1
pkgdesc='A Qt/C++ wrapper around LibFace library to perform face recognition and detection over pictures. KF5 Frameworks branch (GIT Version)'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('opencv')
makedepends=('git' 'automoc4' 'cmake')
conflicts=('libkface')
provides=('libkface')
sha1sums=('SKIP')
source=('libkface::git+git://anongit.kde.org/libkface#branch=frameworks')
_gitname=libkface

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  rm -fr build
  mkdir -p build
}

build() {
  cd build
  cmake "../${_gitname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  make -C build DESTDIR=${pkgdir} install
}