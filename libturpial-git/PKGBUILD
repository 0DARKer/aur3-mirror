# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Louis des Landes <psyker7 at gmail dot com>

pkgname=libturpial-git
pkgver=1.0.423.8718d3d
pkgrel=1
pkgdesc="A library that handles multiple microblogging protocols."
arch=(any)
url=https://github.com/Turpial/libturpial
license=(GPL3)
depends=(python2-oauth python2-requests python2-simplejson)
makedepends=(git python2-distribute)
source=($pkgname::git://github.com/Turpial/libturpial.git)
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
    cd $pkgname/
    echo $(head -n1 ChangeLog | awk '{print $1}' | sed 's/v//;s/-/./').$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
    cd $pkgname/
    python2 setup.py build
}

package() {
    cd $pkgname/
    python2 setup.py install --root="$pkgdir" --optimize=1
    rm "$pkgdir"/usr/{AUTHORS,COPYING,ChangeLog}
}
