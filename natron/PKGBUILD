# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=natron
pkgver=0.9.3
pkgrel=1
arch=('i386' 'x86_64')
pkgdesc='Video compositing software similar to Nuke and Adobe AfterEffects'
url='http://natron.inria.fr/'
license=MPL
depends=('qt5-base>=5.3' 'boost-libs' 'expat' 'glew' 'cairo>=1.12')
makedepends=('boost')
optdepends=(
  'tuttleofx: plugins'
  'openfx-io: read anything else than standard 8-bits images'
  'openfx-misc: plugins'
  'openfx-yadif: yadif deinterlacer plugin'
  'openfx: sample plugins in the Support and Examples directories'
)
source=(
  "Natron-${pkgver}.tar.gz::https://codeload.github.com/MrKepzie/Natron/archive/v${pkgver}.tar.gz"
  "devernay-openfx::git://github.com/devernay/openfx.git#commit=b787a4669fed13953ab0989dac589b7c0f7d85df"  # A fork by devernay
  "SequenceParsing::git://github.com/MrKepzie/SequenceParsing.git#commit=2e50dda6400513d631acc92ac8ab87e9e2a685b1"
  "tinydir::git://github.com/MrKepzie/tinydir.git#commit=60f09057d32b8f193cbebb5a38644ca686994ea3"  # Required by SequenceParsing
  "gmock-1.7.0.zip::https://googlemock.googlecode.com/files/gmock-1.7.0.zip"
  "gtest-1.7.0.zip::https://googletest.googlecode.com/files/gtest-1.7.0.zip"
)
md5sums=('147e0be59e392f3df233544dce7d2425'
         'SKIP'
         'SKIP'
         'SKIP'
         '073b984d8798ea1594f5e44d85b20d66'
         '2d6ec8ccdf5c46b05ba54a9fd1d130d7')

prepare() {
  rm -Rf "${srcdir}/Natron-${pkgver}/libs/"{OpenFX,SequenceParsing} "${srcdir}/SequenceParsing/tinydir" "${srcdir}/Natron-${pkgver}/Tests/"{google-mock,google-test}
  ln -sf "${srcdir}/devernay-openfx" "${srcdir}/Natron-${pkgver}/libs/OpenFX"
  ln -sf "${srcdir}/tinydir" "${srcdir}/SequenceParsing/tinydir"
  ln -sf "${srcdir}/SequenceParsing" "${srcdir}/Natron-${pkgver}/libs/SequenceParsing"
  ln -sf "${srcdir}/gmock-1.7.0" "${srcdir}/Natron-${pkgver}/Tests/google-mock"
  ln -sf "${srcdir}/gtest-1.7.0" "${srcdir}/Natron-${pkgver}/Tests/google-test"
}

build() {
  mkdir -p "${srcdir}/Natron-build"
  cat > ${srcdir}/Natron-${pkgver}/config.pri << EOF
boost: LIBS += -lboost_serialization
EOF
  cd "${srcdir}/Natron-build"
  qmake-qt5 -r "${srcdir}/Natron-${pkgver}/Project.pro"
  make
}

package() {
  cd "${srcdir}/Natron-build"
  #make install DESTDIR="${pkgdir}"  # Seems it installs nothing
  install -Dm0755 "${srcdir}/Natron-build/App/Natron" "${pkgdir}/usr/bin/Natron"
  install -Dm0755 "${srcdir}/Natron-build/Renderer/NatronRenderer" "${pkgdir}/usr/bin/NatronRenderer"
  ln -s Natron "${pkgdir}/usr/bin/natron"
  ln -s NatronRenderer "${pkgdir}/usr/bin/natronrenderer"
}
