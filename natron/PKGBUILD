# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=natron
pkgver=0.9.6
pkgrel=1
arch=('i386' 'x86_64')
pkgdesc='Video compositing software similar to Nuke and Adobe AfterEffects'
url='http://natron.inria.fr/'
license=MPL
depends=('qt5-base>=5.3' 'boost-libs' 'expat' 'glew' 'cairo>=1.12')
makedepends=('boost')
optdepends=(
  'tuttleofx: plugins'
  'openfx-io: read anything else than standard 8-bits images'
  'openfx-misc: plugins'
  'openfx-yadif: yadif deinterlacer plugin'
  'openfx: sample plugins in the Support and Examples directories'
)
source=(
  "Natron::git://github.com/MrKepzie/Natron.git#tag=v${pkgver}"
  "devernay-openfx::git://github.com/devernay/openfx.git#commit=239b8eeca5b7b0900934e74c1d33f6a138784a18"  # A fork by devernay
  "SequenceParsing::git://github.com/MrKepzie/SequenceParsing.git#commit=db3e0ae5481810dbd5e417664deb47d4f3c1c94d"
  "tinydir::git://github.com/MrKepzie/tinydir.git#commit=60f09057d32b8f193cbebb5a38644ca686994ea3"  # Required by SequenceParsing
  "gmock-1.7.0.zip::https://googlemock.googlecode.com/files/gmock-1.7.0.zip"
  "gtest-1.7.0.zip::https://googletest.googlecode.com/files/gtest-1.7.0.zip"
)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         '073b984d8798ea1594f5e44d85b20d66'
         '2d6ec8ccdf5c46b05ba54a9fd1d130d7')

prepare() {
  rm -Rf "${srcdir}/Natron-Release-${pkgver}/libs/"{OpenFX,SequenceParsing} "${srcdir}/SequenceParsing/tinydir" "${srcdir}/Natron-Release-${pkgver}/Tests/"{google-mock,google-test}
  ln -sf "${srcdir}/devernay-openfx" "${srcdir}/Natron-Release-${pkgver}/libs/OpenFX"
  ln -sf "${srcdir}/tinydir" "${srcdir}/SequenceParsing/tinydir"
  ln -sf "${srcdir}/SequenceParsing" "${srcdir}/Natron-Release-${pkgver}/libs/SequenceParsing"
  ln -sf "${srcdir}/gmock-1.7.0" "${srcdir}/Natron-Release-${pkgver}/Tests/google-mock"
  ln -sf "${srcdir}/gtest-1.7.0" "${srcdir}/Natron-Release-${pkgver}/Tests/google-test"
}

build() {
  mkdir -p "${srcdir}/Natron-build"
  cat > ${srcdir}/Natron-Release-${pkgver}/config.pri << EOF
boost: LIBS += -lboost_serialization
EOF
  cd "${srcdir}/Natron-build"
  qmake-qt5 -r "${srcdir}/Natron-Release-${pkgver}/Project.pro"
  make
}

package() {
  cd "${srcdir}/Natron-build"
  #make install DESTDIR="${pkgdir}"  # Seems it installs nothing
  install -Dm0755 "${srcdir}/Natron-build/App/Natron" "${pkgdir}/usr/bin/Natron"
  install -Dm0755 "${srcdir}/Natron-build/Renderer/NatronRenderer" "${pkgdir}/usr/bin/NatronRenderer"
  ln -s Natron "${pkgdir}/usr/bin/natron"
  ln -s NatronRenderer "${pkgdir}/usr/bin/natronrenderer"
}
