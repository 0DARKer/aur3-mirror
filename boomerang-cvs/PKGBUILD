# Maintainer: lukas (lukas <dot> graetz <at> web <dot>  de)
pkgname=boomerang-cvs
pkgver=20141124
pkgrel=1
pkgdesc="A general, open source, retargetable decompiler of machine code programs"
arch=('i686' 'x86_64')
license=('BSD')
url="http://boomerang.sourceforge.net/"
makedepends=('cvs')
source=("boomerang_use_misc_types.patch")
md5sums=('538d9b262217a540997d01ebe127a4a6')

_cvsmod=boomerang

pkgver() {
  # FIXME
  date +%Y%m%d 
}

prepare() {
  # Download the package and get its version.
  # The "source"-array appearingly not supports CVS.
  # There should be a svn version of boomerang but I can't find it.
  cd "$srcdir"
  msg "Connecting to CVS server...."

  if [[ -d "$_cvsmod" ]]; then
    cd "$_cvsmod"
    cvs -z3 update -d
  else
    cvs -z3 -d:pserver:anonymous@boomerang.cvs.sourceforge.net:/cvsroot/boomerang co -P "$_cvsmod"
    cd "$_cvsmod"
  fi

  msg "CVS checkout done or server timeout"

  rm -rf "$srcdir/$_cvsmod-build"
  cp -r "$srcdir/$_cvsmod" "$srcdir/$_cvsmod-build"

  cd "${srcdir}/$_cvsmod-build"
  patch -Np1 -i "${srcdir}/boomerang_use_misc_types.patch"
}

build() {
  cd "${srcdir}/$_cvsmod-build"

  ./configure --prefix=/usr
  make
}
package() {
  cd "${srcdir}/$_cvsmod-build"
  install -D lib/lib*.so "${pkgdir}/usr/lib/"
  install -D boomerang "${pkgdir}/usr/bin/"
  install -D -m 644 LICENSE.TERMS "${pkgdir}/usr/share/licenses/${pkgname}/"
}

