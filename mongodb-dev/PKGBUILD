pkgname=mongodb-dev
pkgbasename=mongodb
pkgver=2.1.0
pkgrel=1
pkgdesc="A high-performance, open source, schema-free document-oriented database."
arch=('i686' 'x86_64')
url="http://www.mongodb.org"
license=('AGPL3')
makedepends=(scons)
depends=(boost snappy)
optdepends=('libpcap: needed for mongosniff')
backup=('etc/conf.d/mongodb' 'etc/mongodb.conf')
conflicts=(mongodb)
source=("http://downloads.mongodb.org/src/mongodb-src-r${pkgver}.tar.gz"
        "mongodb.rc"
        "mongodb.conf")
install="mongodb.install"

build() {
  local SCONSFLAGS="$MAKEFLAGS"
  cd "${pkgbasename}-src-r${pkgver}"
  scons all --full --use-system-snappy
}

package() {
  cd "${pkgbasename}-src-r${pkgver}"
  scons install --full --prefix="$pkgdir/usr"
  install -D -m755 ${srcdir}/mongodb.rc ${pkgdir}/etc/rc.d/mongodb
  install -D -m644 ${srcdir}/mongodb.conf ${pkgdir}/etc/mongodb.conf
  install -d -m700 ${pkgdir}/var/lib/mongodb
  install -d -m755 ${pkgdir}/var/log/mongodb
  if [ -d ${pkgdir}/usr/lib64 ]; then
    mv ${pkgdir}/usr/lib64 ${pkgdir}/usr/lib
  fi
}
md5sums=('afb477787a557709ca04a67bb201cb40'
         '089892f44672eee186103867a787075e'
         '4839fe1d638187ca3226e8267b947318')
