# Maintainer: Guan 'kuno' Qing <neokuno#gmail#com>(replaces # with .)

pkgname=mongodb-dev
pkgbasename=mongodb
pkgver=2.0.0rc0
_realver=2.0.0-rc0
pkgrel=1
pkgdesc="A high-performance, open source, schema-free document-oriented database."
arch=('i686' 'x86_64')
url="http://www.mongodb.org"
license=('AGPL')
makedepends=(gcc scons)
depends=(boost js pcre)
optdepends=('libpcap: needed for mongosniff')
backup=('etc/conf.d/mongodb' 'etc/rc.d/mongodb' 'etc/mongodb.conf')
conflicts=(mongodb)
source=("${pkgbasename}-src-r${_realver}.tar.gz::http://downloads.mongodb.org/src/${pkgbasename}-src-r${_realver}.tar.gz"
        "mongodb.rc"
        "mongodb.conf")
install="mongodb.install"

build() {
    local SCONSFLAGS="$MAKEFLAGS"

    cd "${pkgbasename}-src-r${_realver}"

    scons all || return 1
}

package() {
    cd "${pkgbasename}-src-r${_realver}"

    scons install --prefix="$pkgdir/usr" || return 1

    install -Dm 755 "$startdir/mongodb.rc" "$pkgdir/etc/rc.d/mongodb"
    install -Dm 644 "$startdir/mongodb.conf" "$pkgdir/etc/mongodb.conf"
    install -dm 700 "$pkgdir/var/state/mongodb"

    if [ -d "$pkgdir/usr/lib64" ]
    then
        mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib"
    fi
}
md5sums=('594f9e49b9941719db3cabb1d4a61c2a'
         '089892f44672eee186103867a787075e'
         '9e0ea3f96732bb7811f0b64dace56440')
