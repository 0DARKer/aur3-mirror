# Contributor: Richard Kaka≈° <richard.kakas@gmail.com>
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=printrun
pkgver=20140801
pkgrel=3
_projectname='Printrun'
_gittag="printrun-${pkgver}"
pkgdesc='Pronterface, Pronsole, and Printcore - Pure Python 3D printing host software and GUI'
arch=('x86' 'x86_64')
url='https://github.com/kliment/Printrun'
license=('GPL')
provides=('printrun-git')
conflicts=('printrun-git')
depends=('python2-pyserial' 'wxpython' 'pyglet')
optdepends=(
  'python2-cairo: to use Projector feature'
  'python2-cairosvg: to use Projector feature'
  'python2-numpy: for 3D view'
  'python2-dbus: to inhibit sleep when printing'
  'python2-psutil: to increase process priority when printing')
makedepends=('cython2')
source=("https://github.com/kliment/${_projectname}/archive/${_gittag}.tar.gz")
sha256sums=('d72ddabc7a169375a883baede7dc2b138250770a8bb761ec363cedff3efb84e3')

build() {
  cd "${srcdir}/${_projectname}-${_gittag}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_projectname}-${_gittag}"
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
