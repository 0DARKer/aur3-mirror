# Contributor: Richard Kaka≈° <richard.kakas@gmail.com>
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=printrun
pkgver=20130604
pkgrel=1
_projectname='Printrun'
_gittag="printrun-${pkgver}"
pkgdesc='GUI interface for 3D printing on RepRap and other printers'
arch=('any')
url='https://github.com/kliment/Printrun'
license=('GPL')
provides=('printrun-git')
conflicts=('printrun-git')
depends=('python2-pyserial' 'wxpython' 'pyglet')
source=("https://github.com/kliment/${_projectname}/archive/${_gittag}.zip"
        'pronterface.desktop' 'pronterface.png')
md5sums=('65e1a84a9522fef62fc614ad2b7e7234'
         '3dfd9fca6b36d914d9b60a69dc9d0d14'
         '9010a039260f0ea34f02671025138ab2')

build() {
  cd "${srcdir}/${_projectname}-${_gittag}"

  python2 setup.py build

  sed 's/^\(#\!.*\)python$/\1python2/' -i \
    build/lib/printrun/calibrateextruder.py \
    build/lib/printrun/gcoder.py \
    build/lib/printrun/gcview.py \
    build/lib/printrun/graph.py \
    build/lib/printrun/SkeinforgeQuickEditDialog.py \
    build/lib/printrun/stlview.py
}

package() {
  cd "${srcdir}/${_projectname}-${_gittag}"
  python2 setup.py install --root="${pkgdir}" --prefix=/usr

  install -D -m644 "${srcdir}/pronterface.desktop" \
    "$pkgdir/usr/share/applications/pronterface.desktop"
  install -D -m644 "${srcdir}/pronterface.png" \
    "$pkgdir/usr/share/pixmaps/pronterface.png"
}
