# Maintainer: Alexander Rødseth <rodseth@gmail.com>
pkgname=moonscript
pkgver=0.1.0
pkgrel=2
pkgdesc="Dynamic scripting language that compiles into Lua"
arch=('x86_64' 'i686')
url="http://moonscript.org/"
license=('MIT')
depends=('luarocks' 'lpeg' 'lua' 'luafilesystem')
source=("http://moonscript.org/rocks/moonscript-dev-1.rockspec"
        "https://raw.github.com/leafo/moonscript/master/README.md"
        "https://raw.github.com/leafo/moonscript/master/docs/index.md"
        "http://moonscript.org/reference/index.html")
md5sums=('d81837a75ce5c19a2c57565f3e92936d'
         'aa25f96b2824df64fb2d2f4d3d23ca94'
         'f86246290a81e1e287abe7a5c16ad9fa'
         '05a59563d309c837483a0187be5da082')

package() {
  cd "$srcdir"

  mkdir "$pkgdir/usr"

  msg2 "Packaging moonscript..."
  luarocks install moonscript-dev-1.rockspec --to="$pkgdir/usr"
  install -Dm644 index.md "$pkgdir/usr/share/doc/$pkgname/index.md"
  install -Dm644 index.html "$pkgdir/usr/share/doc/$pkgname/index.html"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/README.md"

  msg2 "Patching..."
  sed -i "s:$pkgdir::" "$pkgdir/usr/bin/moon"
  sed -i "s:$pkgdir::" "$pkgdir/usr/bin/moonc"

  msg2 "Cleaning up..."
  rm "$pkgdir/usr/lib/lua/5.1/lfs.so"
  rm "$pkgdir/usr/lib/lua/5.1/lpeg.so"
  rm "$pkgdir/usr/share/lua/5.1/re.lua"
  chmod 755 "$pkgdir/usr" "$pkgdir/usr/bin/" "$pkgdir/usr/share/lua/5.1/" \
    "$pkgdir/usr/lib/lua/5.1/" "$pkgdir/usr/lib" "$pkgdir/usr/share" \
    "$pkgdir/usr/share/lua" "$pkgdir/usr/lib/lua"
}

# vim:set ts=2 sw=2 et:
