# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=moonscript
pkgver=0.2.4
_luaver=5.2
pkgrel=3
pkgdesc='Dynamic scripting language that compiles into Lua'
arch=('x86_64' 'i686')
url='http://moonscript.org/'
license=('MIT')
depends=('luarocks' 'lua' 'lua-filesystem' 'lpeg')
makedepends=('git' 'curl')
source=("http://moonscript.org/rocks/moonscript-dev-1.rockspec"
        'https://raw.github.com/leafo/moonscript/master/README.md'
        'https://raw.github.com/leafo/moonscript/master/docs/reference.md'
        'http://moonscript.org/reference/index.html')
sha256sums=('267ea6e6da049b79a8e40f68257c656966989390b0ee0299f0db11171d6df409'
            'd9e055952371e6d23d47459da59f8c517929e65b9e27e55f82e7a1c3051cfdea'
            'e3bced8a18c1eec251a1abe24f788e0cc2cb8fcf010fc38dda75fe265c4a8449'
            'ac3f5cb1c0a91a003d22e703f8ae638f164f3b58be58e6aa071f232b518cf170')

prepare() {
  sed -i "s:5.1:$_luaver:g" moonscript-dev-1.rockspec
}
  
package() {
  install -d "$pkgdir/usr"
  luarocks install moonscript-dev-1.rockspec --to="$pkgdir/usr"
  install -Dm644 reference.md "$pkgdir/usr/share/doc/$pkgname/reference.md"
  install -Dm644 index.html "$pkgdir/usr/share/doc/$pkgname/index.html"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/README.md"

  # Patching
  sed -i "s:$pkgdir::" "$pkgdir/usr/bin/"{moon,moonc}

  # Cleaning up
  rm -f "$pkgdir/usr/lib/lua/$_luaver/lfs.so"
  rm -f "$pkgdir/usr/lib/lua/$_luaver/lpeg.so"
  rm -f "$pkgdir/usr/share/lua/$_luaver/re.lua"

  # Fixing permissions
  chmod 755 "$pkgdir/usr" "$pkgdir/usr/bin/" "$pkgdir/usr/share/lua/$_luaver/" \
    "$pkgdir/usr/lib/lua/$_luaver/" "$pkgdir/usr/lib" "$pkgdir/usr/share" \
    "$pkgdir/usr/share/lua" "$pkgdir/usr/lib/lua"
}

# vim:set ts=2 sw=2 et:
