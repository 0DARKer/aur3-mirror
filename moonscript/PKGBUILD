# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=moonscript
pkgver=0.2.5
_luaver=5.2
pkgrel=1
pkgdesc='Dynamic scripting language that compiles into Lua'
arch=('x86_64' 'i686')
url='http://moonscript.org/'
license=('MIT')
depends=('luarocks' 'lua' 'lua-filesystem' 'lua-lpeg')
makedepends=('git' 'curl')
source=('http://moonscript.org/rocks/moonscript-dev-1.rockspec'
        'https://raw.github.com/leafo/moonscript/master/README.md'
        'https://raw.github.com/leafo/moonscript/master/docs/reference.md'
        'http://moonscript.org/reference/index.html')
sha256sums=('d60fe4acd32cfe451078656b780cd87c23cf5043bf8b39258bae9acefac0377c'
            'd9e055952371e6d23d47459da59f8c517929e65b9e27e55f82e7a1c3051cfdea'
            '21c6bebe823297f836668cbaa4cc593e2ae218e4b80e25bd2cdc4a914851f618'
            '0c9bd71b671fa3070a0ecb88635753e5642d785adf7230208817475e563f4f8d')

prepare() {
  sed -i "s:5.1:$_luaver:g" moonscript-dev-1.rockspec
}
  
package() {
  install -d "$pkgdir/usr"
  luarocks install moonscript-dev-1.rockspec --to="$pkgdir/usr"
  install -Dm644 reference.md "$pkgdir/usr/share/doc/$pkgname/reference.md"
  install -Dm644 index.html "$pkgdir/usr/share/doc/$pkgname/index.html"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/README.md"

  # Patching
  sed -i "s:$pkgdir::" "$pkgdir/usr/bin/"{moon,moonc}

  # Cleaning up
  rm -f "$pkgdir/usr/lib/lua/$_luaver/lfs.so"
  rm -f "$pkgdir/usr/lib/lua/$_luaver/lpeg.so"
  rm -f "$pkgdir/usr/share/lua/$_luaver/re.lua"

  # Fixing permissions
  chmod 755 "$pkgdir/usr" "$pkgdir/usr/bin/" "$pkgdir/usr/share/lua/$_luaver/" \
    "$pkgdir/usr/lib/lua/$_luaver/" "$pkgdir/usr/lib" "$pkgdir/usr/share" \
    "$pkgdir/usr/share/lua" "$pkgdir/usr/lib/lua"
}

# vim:set ts=2 sw=2 et:
