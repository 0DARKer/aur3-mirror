# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=moonscript
pkgver=0.2.0
pkgrel=4
pkgdesc="Dynamic scripting language that compiles into Lua"
arch=('any')
url="http://moonscript.org/"
license=('MIT')
depends=('luarocks' 'lpeg' 'lua' 'luafilesystem')
makedepends=('git' 'curl')
source=("http://moonscript.org/rocks/moonscript-dev-1.rockspec")
md5sums=('491a59714d88a4c369535e4cbc9123b2')

build() {
  cd "$srcdir"

  msg2 "Fetching documentation..."
  # These changes a lot, so md5-sum-ing them is like fighting windmills
  curl --progress-bar -OL \
    "https://raw.github.com/leafo/moonscript/master/README.md"
  curl --progress-bar -OL \
    "https://raw.github.com/leafo/moonscript/master/docs/reference_manual.html"
  curl --progress-bar -OL \
    "http://moonscript.org/reference/index.html"
}

package() {
  cd "$srcdir"

  msg2 "Packaging moonscript..."
  mkdir "$pkgdir/usr"
  luarocks install moonscript-dev-1.rockspec --to="$pkgdir/usr"
  install -Dm644 reference_manual.html "$pkgdir/usr/share/doc/$pkgname/reference_manual.html"
  install -Dm644 index.html "$pkgdir/usr/share/doc/$pkgname/index.html"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/README.md"

  msg2 "Patching..."
  sed -i "s:$pkgdir::" "$pkgdir/usr/bin/moon"
  sed -i "s:$pkgdir::" "$pkgdir/usr/bin/moonc"

  msg2 "Cleaning up..."
  rm "$pkgdir/usr/lib/lua/5.1/lfs.so"
  rm "$pkgdir/usr/lib/lua/5.1/lpeg.so"
  rm "$pkgdir/usr/share/lua/5.1/re.lua"
  chmod 755 "$pkgdir/usr" "$pkgdir/usr/bin/" "$pkgdir/usr/share/lua/5.1/" \
    "$pkgdir/usr/lib/lua/5.1/" "$pkgdir/usr/lib" "$pkgdir/usr/share" \
    "$pkgdir/usr/share/lua" "$pkgdir/usr/lib/lua"
}

# vim:set ts=2 sw=2 et:
