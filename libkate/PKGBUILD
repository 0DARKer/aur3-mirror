# Contributer: N30N <archlinux@alunamation.com>
# Contributer: linkmauve <linkmauve@linkmauve.fr>
# Contributer: mokkurkalve <xenofil@gmail.com>

pkgname=libkate
pkgver=0.3.8
pkgrel=4
pkgdesc="kate is a karaoke and text codec for embedding in ogg"
url="http://${pkgname}.googlecode.com/"
license="BSD"
arch=("i686" "x86_64")
depends=("libogg" "libpng" "python2")
makedepends=()
optdepends=("liboggz: to build example streams, or" \
	"ogg-tools: to build example streams" \
	"flex: to rebuild the parser, and" \
	"bison: to rebuild the parser" \
	"wxpython: for KateDJ")
source=(http://${pkgname}.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=("7489ddebedf8d9888a05496dedf83c71")

build() {
	cd ${pkgname}-${pkgver}

	export PYTHON="/usr/bin/python2"
	sed "1s/python$/python2/" -i tools/KateDJ/KateDJ

	./configure --prefix=/usr
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
}

# vim: set noet ff=unix
