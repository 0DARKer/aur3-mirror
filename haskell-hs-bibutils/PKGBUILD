# Maintainer: Ivy Foster <joyfulgirl (at) archlinux (dot) # us>
# Contributor: Alexander Dunlap <alexander.dunlap at gmail # dot com>

_hkgname=hs-bibutils
pkgname=haskell-hs-bibutils
pkgver=4.15
pkgrel=1
pkgdesc="Haskell bindings to bibutils, the bibliography conversion utilities."
url="http://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=()
depends=('ghc' 'haskell-syb>=0.3' 'bibutils-dynamic')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('b6d6ad65e57a0a5ae344d35204b0d865')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O \
        --enable-split-objs \
        --enable-shared \
        --prefix=/usr \
        --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid \
        --enable-library-profiling
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r \
        -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" \
        unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh \
        ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install -m744 unregister.sh \
        ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 \
        ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html \
        ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
}
