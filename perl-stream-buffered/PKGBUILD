# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname='perl-stream-buffered'
_pkgname='Stream-Buffered'
pkgver='0.02'
pkgrel='2'
pkgdesc="temporary buffer to save bytes"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.1')
makedepends=('perl-extutils-makemaker>=6.59')
url='http://search.cpan.org/dist/Stream-Buffered'
source=("http://search.cpan.org/CPAN/authors/id/D/DO/DOY/${_pkgname}-${pkgver}.tar.gz")
md5sums=('63d7776365a4ffd22f15002e03781c01')
sha512sums=('44b374982a6f55ca8b08fa3029bb6bf28d1b892cd846f77da5c8bbbf1d9d1957d71681eb62b12874cf6bae94d5ebed92954ac2f8a5ce0aaacc2e833f4094bd16')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_pkgname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
