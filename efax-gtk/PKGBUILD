# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=efax-gtk
pkgver=3.2.8
pkgrel=1
pkgdesc="GTK+2 frontend for the efax program"
arch=('i686' 'x86_64')
url="http://efax-gtk.sourceforge.net/"
license=('GPL')
depends=('dbus-glib' 'gtk2' 'ghostscript')
makedepends=('pkg-config')
optdepends=('mailx-heirloom: to use the mail_fax script')
backup=('etc/efax-gtkrc')
install=efax-gtk.install
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.src.tgz"
        'stock_send-fax.png')
md5sums=('275ad1706e5c38e2b153dddd8a16724f'
         'a5326dfce4e0ac4054e2b2a6aae460ac')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-spooldir=/var/spool/fax
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -m755 mail_fax print_fax "${pkgdir}/usr/bin"
  install -Dm755 ${srcdir}/stock_send-fax.png \
                "${pkgdir}/usr/share/pixmaps/stock_send-fax.png"
}
