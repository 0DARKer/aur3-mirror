# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Randy Heydon <randy.heydon at clockworklab dot net>

pkgname=xdot
_pkgname=${pkgname}.py
pkgver=0.6
pkgrel=2
pkgdesc="Interactive viewer for graphs written in Graphviz's dot language"
url="https://github.com/jrfonseca/xdot.py"
arch=('any')
license=('LGPL2.1')
depends=('graphviz' 'pygtk')
makedepends=('python2-setuptools')
source=(https://github.com/jrfonseca/${_pkgname}/archive/${pkgver}.tar.gz)
sha256sums=('0932bb72abaa77e6a5c4f2acd13569763c23d2761f4e9e5e49993bc69da9cce2')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -e 's/env python$/env python2/g' -i xdot.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
}

# vim: ts=2 sw=2 et:
