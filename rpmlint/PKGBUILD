# Maintainer: Thomas Dziedzic < gostrc at gmail >

pkgname=rpmlint
pkgver=1.3
pkgrel=2
pkgdesc='A tool for checking common errors in rpm packages.'
arch=('any')
url=('http://rpmlint.zarb.org/cgi-bin/trac.cgi')
license=('GPL')
depends=('python2' 'rpm-org' 'cpio' 'desktop-file-utils')
source=("http://rpmlint.zarb.org/download/rpmlint-${pkgver}.tar.xz")
md5sums=('afe1b906776d1ab8148888c25fd31006')

build() {
  cd rpmlint-${pkgver}

  make PYTHON=python2
}

package() {
  cd rpmlint-${pkgver}

  make PYTHON=python2 DESTDIR=${pkgdir} install

  sed \
    -e 's_/usr/bin/python_&2_' \
    -i ${pkgdir}/usr/bin/rpmlint
}
