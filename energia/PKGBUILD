# Maintainer: Will Price <willprice94+arch@gmail.com>
# Contributor: Andrew67 (.desktop file, icon, 64bit compatibility)

pkgname=energia
pkgver=0012
pkgrel=2
pkgdesc="Energia is a Arduino IDE clone for use with the MSP430 launchpad"
arch=('i686' 'x86_64')
url="http://energia.nu/"
license=('GPL')
depends=(java-runtime java-rxtx)
if [[ "$CARCH" == x86_64 ]]; then
  depends+=(lib32-libusb-compat)
else
  depends+=(libusb-compat)
fi
provides=('energia')
options=(!strip)
install='energia.install'
source=('energia.desktop'
        'legacy.conf')
md5sums=('a7ce061d9fc0f9530e058204e532b40b'
         'c9f7f672906a5e2de3184cb8b7462cf1')
if [[ $CARCH == "x86_64" ]]; then
 source+=('http://energia.nu/download/downloadv3.php?file=energia-0101E0012-linux64.tgz')
 md5sums+=('5046bfda7c04f9539a36fe13063ef180' )
else
 source+=('http://energia.nu/download/downloadv3.php?file=energia-0101E0012-linux.tgz')
 md5sums+=('bddf7f34ceb0288222d8ecddb206665b')
fi

# Don't compress, takes too long
PKGEXT='.pkg.tar'

package() {
  _copy_src
  _link_binary_to_bin
  _patch_package_for_lock_issues
  _add_menu_entry_and_icon
}


_copy_src() {
  mkdir -p "$pkgdir"{/etc/tmpfiles.d,/opt/energia,/usr/{bin,share/{applications,pixmaps}}}

  # --archive copies recursively, preserves permissions and links
  cp --archive "$srcdir/$pkgname-0101E$pkgver/." "$pkgdir/opt/energia"
}

_patch_package_for_lock_issues() {
  ln -sf /usr/lib/librxtxSerial.so "$pkgdir/opt/energia/lib/librxtxSerial64.so"
  ln -sf /usr/lib/librxtxSerial.so "$pkgdir/opt/energia/lib/librxtxSerial32.so"
  ln -sf /usr/share/java/rxtx/RXTXcomm.jar "$pkgdir/opt/energia/lib/RXTXcomm.jar"
  install -m644 "$srcdir/legacy.conf" "$pkgdir/etc/tmpfiles.d"
}

_link_binary_to_bin() {
  ln -s /opt/energia/energia "$pkgdir/usr/bin/"
}

_add_menu_entry_and_icon() {
  install -m644 "$srcdir/energia.desktop" "$pkgdir/usr/share/applications/"
  for size in 16 24 32 48; do
    install -m644 "$pkgdir/opt/energia/lib/energia_$size.png" "$pkgdir/usr/share/pixmaps/"
  done
}
