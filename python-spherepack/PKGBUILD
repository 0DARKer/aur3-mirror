pkgname=python-spherepack
pkgver=3.2
pkgrel=1
pkgdesc="Spherepack scientific library"
depends=('python-numpy')
arch=('i686' 'x86_64')
url="http://www.cisl.ucar.edu/css/software/spherepack/"
license=('BSD')
makedepends=('gcc-fortran')
source=(https://launchpad.net/ubuntu/+archive/primary/+files/spherepack_3.2.orig.tar.gz
        https://launchpad.net/ubuntu/+archive/primary/+files/spherepack_3.2-7.debian.tar.gz)
md5sums=('28369fa2f9f3cfdbd9112fcdd92b7fc5'
         '32bae6422c472ab8a2da3c3e905c9ec5')

prepare() {
   sed -i "s|PREFIX:=/usr/local|PREFIX:=/usr|g" "${srcdir}"/debian/patches/{make,pkgconfig}.patch

   cd "${srcdir}"/spherepack-3.2
   for i in $(cat "${srcdir}"/debian/patches/series); do 
      patch -Np1 -i "${srcdir}"/debian/patches/$i
   done
}

build() {
   cd "${srcdir}"/spherepack-3.2

   pushd Src
   make
   popd 

   python setup.py build
}

package() {
   cd "${srcdir}"/spherepack-3.2

   pushd Src
   make DESTDIR="${pkgdir}" install
   popd 

   python setup.py install --root "${pkgdir}" --skip-build --optimize=1

   rm -f "${pkgdir}"/libsphere.so
}
