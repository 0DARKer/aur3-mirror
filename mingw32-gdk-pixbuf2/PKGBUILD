# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-gdk-pixbuf2
pkgver=2.22.1
pkgrel=1
arch=(any)
pkgdesc="An image loading library for gtk2 (mingw32)"
depends=('mingw32-glib2>=2.26' mingw32-libpng mingw32-libjpeg mingw32-libtiff)
makedepends=(mingw32-gcc)
options=(!strip)
license=('LGPL')
url="http://www.gtk.org/"
source=(http://download.gnome.org/sources/gdk-pixbuf/2.22/gdk-pixbuf-${pkgver}.tar.bz2)
sha256sums=('6ce87eda24af9362307b2593c154d0b660f4e26d0abf2e71d46d0ddd55fd953d')

build()
{
  cd "$srcdir/gdk-pixbuf-$pkgver"

  export CXX=i486-mingw32-g++
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset LDFLAGS PKG_CONFIG_PATH

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --with-included-loaders=png,jpeg
  make
}

package() {
  cd "$srcdir/gdk-pixbuf-$pkgver"
  mkdir -p "$pkgdir/usr/i486-mingw32/lib/"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/{share,man}
}

