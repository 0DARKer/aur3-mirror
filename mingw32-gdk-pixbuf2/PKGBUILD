# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-gdk-pixbuf2
pkgver=2.24.0
pkgrel=1
pkgdesc="An image loading library for gtk2 (mingw32)"
arch=(any)
url="http://www.gtk.org/"
license=('LGPL')
depends=('mingw32-glib2>=2.28' mingw32-libpng mingw32-libjpeg mingw32-libtiff)
makedepends=(mingw32-gcc)
options=(!strip)
source=(http://download.gnome.org/sources/gdk-pixbuf/2.24/gdk-pixbuf-${pkgver}.tar.xz)
sha256sums=('67c11d3aa40441c1cea42c365bf2d6a9a9dd52fae2d359a707ad5e8abb4d28a7')

build()
{
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  export CXX=i486-mingw32-g++
  export CFLAGS="-O2 -pipe -march=i686 -mms-bitfields"
  export CXXFLAGS="${CFLAGS}"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset PKG_CONFIG_PATH
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --with-included-loaders=png
  make
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"
  mkdir -p "$pkgdir/usr/i486-mingw32/lib/"
  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/{share,man}
}

