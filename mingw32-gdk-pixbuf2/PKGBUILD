# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-gdk-pixbuf2
pkgver=2.24.1
pkgrel=1
pkgdesc="An image loading library (mingw32)"
arch=(any)
url="http://www.gtk.org/"
license=('GPL2')
depends=('mingw32-glib2>=2.28' mingw32-libpng mingw32-libjpeg mingw32-libtiff)
makedepends=(mingw32-gcc)
options=(!strip)
source=(http://download.gnome.org/sources/gdk-pixbuf/2.24/gdk-pixbuf-$pkgver.tar.xz
        patch-missing-init-function-f5aeddd.diff)
sha256sums=('da7a3f00db360913716368e19e336402755cafa93769f3cfa28a969303e4bee1'
            '0b951415a23f3ef14ae3d8f63c6932838b68cdd5e12bcf6b66bb7072f2cf7013')

build() {
  cd "$srcdir/gdk-pixbuf-$pkgver"

  # patch for bug https://bugzilla.gnome.org/show_bug.cgi?id=666927
  patch -Np1 -i ${srcdir}/patch-missing-init-function-f5aeddd.diff

  export CXX=i486-mingw32-g++
  export CFLAGS="-O2 -pipe -march=i686 -mms-bitfields"
  export CXXFLAGS="${CFLAGS}"
  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig/"
  unset PKG_CONFIG_PATH
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --with-included-loaders=png
  make
}

package() {
  cd "$srcdir/gdk-pixbuf-$pkgver"
  mkdir -p "$pkgdir/usr/i486-mingw32/lib/"
  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/{share,man}
}

# vim:set ts=2 sw=2 et:
