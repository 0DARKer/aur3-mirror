# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=fife
pkgver=0.3.2
pkgrel=1
pkgdesc="Flexible Isometric Free Engine is a cross platform game creation framework."
arch=(i686 x86_64)
url="http://fifengine.net/"
license=('GPL')
depends=('boost-libs' 'guichan' 'libvorbis' 'libxcursor' 'mesa' 
         'openal' 'python2' 'sdl_image' 'sdl_ttf')
makedepends=('scons' 'boost' 'swig' 'zlib')
conflicts=('fife-svn')
source=("http://downloads.sourceforge.net/project/$pkgname/active/src/$pkgname-$pkgver.tar.gz")
md5sums=('965260ef90cb39c214278f8bf3808c7b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON="python2"

  scons \
	fife-shared \
	fife-python \
	fife-swig
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  scons DESTDIR="$pkgdir/" \
	install-shared \
	install-python \
	install-dev \
	--prefix="/usr"
}

# vim:set ts=2 sw=2 et:
