# Maintainer: rtfreedman
# Contributor: speps <speps at aur dot archlinux dot org> 

pkgname=rtmidi
pkgver=2.1.0
pkgrel=1
pkgdesc="A set of C++ classes that provides a common API for realtime MIDI input/output."
arch=(i686 x86_64)
url="http://www.music.mcgill.ca/~gary/rtmidi/"
license=('MIT')
depends=('alsa-lib' 'jack')
source=("${url}release/$pkgname-$pkgver.tar.gz")
md5sums=('ea4acc28724bc21fb08b6ad5efbc5c36')

#prepare() {
#  cd "$pkgname-$pkgver"
  ## use system cflags
#  sed -i 's/^CFLAGS   =/CFLAGS   +=/' Makefile.in
#}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --with-alsa  --with-jack # --enable-debug
  make
  ## test utils
  cd tests && make
}

package() {
  cd "$pkgname-$pkgver"

  ## lib
  install -Dm755 librtmidi.so.$pkgver "$pkgdir"/usr/lib/librtmidi.so.$pkgver
  ln -s librtmidi.so.$pkgver "$pkgdir"/usr/lib/librtmidi.so.2
  ln -s librtmidi.so.$pkgver "$pkgdir"/usr/lib/librtmidi.so

  ## lib headers
  install -Dm644 RtMidi.h "$pkgdir"/usr/include/RtMidi.h

  ## lib config
  install -Dm644 librtmidi.pc  "$pkgdir"/usr/lib/pkgconfig/librtmidi.pc
  install -Dm755 rtmidi-config "$pkgdir"/usr/bin/rtmidi-config

  ## test utils installed with prefix 'rtmidi-'
  for _bin in `find tests -type f -perm 755`; do
    install -Dm755 $_bin "$pkgdir/usr/bin/rtmidi-"`basename $_bin`
  done

  ## text docs
  install -d "$pkgdir"/usr/share/doc/rtmidi
  install -Dm644 readme doc/release.txt  "$pkgdir"/usr/share/doc/rtmidi

  ## html docs
  #install -d "$pkgdir"/usr/share/doc/rtmidi/{html,images}
  #install -Dm644 doc/html/*  "$pkgdir"/usr/share/doc/rtmidi/html
  #install -Dm644 doc/images/*  "$pkgdir"/usr/share/doc/rtmidi/images

  ## license
  csplit -s readme "%LEGAL AND%"
  install -Dm644 xx00 "$pkgdir"/usr/share/licenses/rtmidi/LICENSE
}

# vim:set ts=2 sw=2 et:
