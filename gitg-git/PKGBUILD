# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Matt Arsenault <arsenm2@rpi.edu>

pkgname=gitg-git
pkgver=0.2.5.483.g685835b
pkgrel=1
pkgdesc="A GitX clone for GTK+/GNOME - git checkout"
arch=('i686' 'x86_64')
url="http://git.gnome.org/cgit/gitg/"
license=('GPL')
depends=('dconf' 'gtksourceview3' 'libgit2-glib-git' 'git' 'desktop-file-utils' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'libwebkit3' 'libpeas')
makedepends=('autoconf' 'intltool' 'python-gobject')
provides=('gitg')
conflicts=('gitg')
source="git://git.gnome.org/gitg"

pkgver() {
	cd gitg/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd gitg/

	./autogen.sh
	./configure --prefix=/usr --disable-debug
	
	# vala stuff does not like parallel building...
	export MAKEFLAGS="-j1"
	make
}

package() {
	cd gitg/

	make DESTDIR=${pkgdir} install
}

sha256sums=('SKIP')
