# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=geogebra5-beta
pkgver=4.9.18.0
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics (2D and 3D), algebra and spreadsheet"
arch=('any')
url="http://www.geogebra.org/"
license=('custom')
# Application and source code under GPL / Language files under CCPL
depends=('java-runtime' 'jogl' 'desktop-file-utils')
optdepends=('kde-thumbnailer-geogebra: generates thumbnails of GeoGebra files in KDE'
	    'gnome-thumbnailer-geogebra: generates thumbnails of GeoGebra files in GNOME')
install='geogebra5-beta.install'
noextract=(*.jar)
_root="http://www.geogebra.org/webstart/5.0/"
source=("${pkgname}.png"
	"${pkgname}.desktop"
	"${pkgname}.sh"
	"${_root}geogebra.jar"
	"${_root}geogebra_3d.jar"
	"${_root}geogebra_algos.jar"
	"${_root}geogebra_cas.jar"
	"${_root}geogebra_export.jar"
	"${_root}geogebra_gui.jar"
	"${_root}geogebra_javascript.jar"
	"${_root}geogebra_main.jar"
	"${_root}geogebra_properties.jar"
	"${_root}geogebra_usb.jar"
	"${_root}jlatexmath.jar"
	"${_root}jlm_cyrillic.jar"
	"${_root}jlm_greek.jar"
	"${url}download/license.txt")
md5sums=('e324ee3a2bb438cee625e3f29770a315'
         '9dcd074a16b1ec498c7aefd3241e73fb'
         'd7e8d83a6bf9c0ba9f6a889cfa46c551'
         '2b1579a5b37b6c41915157cca37c45e1'
         'bb6321aa9808662376bbb5ad0854204c'
         'e1ff3fb5a9fdb4d916e0476eee9416bf'
         '58f683780ef472dc651fef4904e1b5af'
         '398eb53451dc8cc08046075b372e3299'
         'ce9325bba2bedfb2e165f79937cca91d'
         'ececf36ff8360e88c546a623501c3bcd'
         '51355bc60b477872d41f9a643fa45798'
         '403bfda77a3c52103535bb34b7413ff1'
         '19e7084fa60b3a1ba0639e3e978e0f41'
         '72f846f5b09077bc6018aa626a22e16b'
         '5954036600d5f55dc51daaff68991803'
         '229bb3b016e4c4d8febf2b155f4663bc'
         '39e2fbaf507a7b8db047152332d695c5')

package() {
  cd "${srcdir}"

  # Installing application
  install -d -m755 "${pkgdir}/usr/share/java/${pkgname}"
  install -D -m644 *.jar "${pkgdir}/usr/share/java/${pkgname}"
  install -D -m755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Creating menu item
  install -D -m644 "${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -D -m644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Installing license
  install -D -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Needed to use the jogl package
  ln -s "/usr/share/java/jogl/gluegen-rt.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/gluegen-rt.jar"
  ln -s "/usr/share/java/jogl/jogl.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/jogl.jar"

  # Needed to use the jlatexmath package
#   ln -s "/usr/share/java/jlatexmath-minimal/jlatexmath-minimal.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlatexmath.jar"
#   ln -s "/usr/share/java/jlatexmath-minimal/jlm_greek.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlm_greek.jar"
#   ln -s "/usr/share/java/jlatexmath-minimal/jlm_cyrillic.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlm_cyrillic.jar"
}
