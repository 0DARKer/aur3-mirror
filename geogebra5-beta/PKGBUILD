# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=geogebra5-beta
pkgver=4.9.20.0
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics (2D and 3D), algebra and spreadsheet"
arch=('any')
url="http://www.geogebra.org/"
license=('custom')
# Application and source code under GPL / Language files under CCPL
depends=('java-runtime' 'jogl' 'desktop-file-utils')
optdepends=('kde-thumbnailer-geogebra: generates thumbnails of GeoGebra files in KDE'
	    'gnome-thumbnailer-geogebra: generates thumbnails of GeoGebra files in GNOME')
install='geogebra5-beta.install'
noextract=(*.jar)
_root="http://www.geogebra.org/webstart/5.0/"
source=("${pkgname}.png"
	"${pkgname}.desktop"
	"${pkgname}.sh"
	"${_root}geogebra.jar"
	"${_root}geogebra_3d.jar"
	"${_root}geogebra_algos.jar"
	"${_root}geogebra_cas.jar"
	"${_root}geogebra_export.jar"
	"${_root}geogebra_gui.jar"
	"${_root}geogebra_javascript.jar"
	"${_root}geogebra_main.jar"
	"${_root}geogebra_properties.jar"
	"${_root}geogebra_usb.jar"
	"${_root}jlatexmath.jar"
	"${_root}jlm_cyrillic.jar"
	"${_root}jlm_greek.jar"
	"${url}download/license.txt")
md5sums=('e324ee3a2bb438cee625e3f29770a315'
         '9dcd074a16b1ec498c7aefd3241e73fb'
         'd7e8d83a6bf9c0ba9f6a889cfa46c551'
         '4dee57e4c034ba17bf7e63383a6e0bc2'
         '687ad6f719648bf61b68900cc8e13b05'
         '4033413a1f49158f72399a830b9e011b'
         '4cbbf24604237e35efd3df4294d0c5ea'
         '9a2a8a73e0b26afc35334ffaf516d378'
         '6c9bf04eef80a39574d3754346bcab20'
         '35fd467783ded5866d00e89505a137d2'
         '6943c473dcc819b9364aff82919eeb9f'
         'e101a1e814cbbd8320616642b2f48239'
         '314e0ae3b36fa1ac7507221a7d4faf94'
         'd639920f0b2eccfd605a8d1ea1a9faa1'
         '1d94f05e6cddfa98136970a0b37ab6f9'
         '9ca63c86922838c8a4929c0d1b7dae89'
         '39e2fbaf507a7b8db047152332d695c5')

package() {
  cd "${srcdir}"

  # Installing application
  install -d -m755 "${pkgdir}/usr/share/java/${pkgname}"
  install -D -m644 *.jar "${pkgdir}/usr/share/java/${pkgname}"
  install -D -m755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Creating menu item
  install -D -m644 "${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -D -m644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Installing license
  install -D -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Needed to use the jogl package
  ln -s "/usr/share/java/jogl/gluegen-rt.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/gluegen-rt.jar"
  ln -s "/usr/share/java/jogl/jogl.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/jogl.jar"

  # Needed to use the jlatexmath package
#   ln -s "/usr/share/java/jlatexmath-minimal/jlatexmath-minimal.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlatexmath.jar"
#   ln -s "/usr/share/java/jlatexmath-minimal/jlm_greek.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlm_greek.jar"
#   ln -s "/usr/share/java/jlatexmath-minimal/jlm_cyrillic.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlm_cyrillic.jar"
}
