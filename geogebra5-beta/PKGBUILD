# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=geogebra5-beta
pkgver=4.9.7.0
pkgrel=1
pkgdesc="Dynamic mathematics software with interactive graphics (2D and 3D), algebra and spreadsheet"
arch=('any')
url="http://www.geogebra.org/"
license=('custom')
# Application and source code under GPL / Language files under CCPL
depends=('java-runtime' 'jogl' 'desktop-file-utils')
optdepends=('kde-thumbnailer-geogebra: generates thumbnails of GeoGebra files in KDE'
	    'gnome-thumbnailer-geogebra: generates thumbnails of GeoGebra files in Gnome')
install='geogebra5-beta.install'
noextract=("*.jar")
_root="http://www.geogebra.org/webstart/5.0/"
source=("${pkgname}.png"
	"${pkgname}.desktop"
	"${pkgname}.sh"
	"${_root}geogebra.jar"
	"${_root}geogebra_3d.jar"
	"${_root}geogebra_algos.jar"
	"${_root}geogebra_cas.jar"
	"${_root}geogebra_export.jar"
	"${_root}geogebra_gui.jar"
	"${_root}geogebra_javascript.jar"
	"${_root}geogebra_main.jar"
	"${_root}geogebra_properties.jar"
	"${_root}geogebra_usb.jar"
	"${_root}jlatexmath.jar"
	"${_root}jlm_cyrillic.jar"
	"${_root}jlm_greek.jar"
	"${url}download/license.txt")

package() {
  cd "${srcdir}"

  # Installing application
  install -d -m755 "${pkgdir}/usr/share/java/${pkgname}"
  install -D -m644 *.jar "${pkgdir}/usr/share/java/${pkgname}"
  install -D -m755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Creating menu item
  install -D -m644 "${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -D -m644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Installing license
  install -D -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Needed to use the jogl package
  ln -s "/usr/share/java/jogl/gluegen-rt.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/gluegen-rt.jar"
  ln -s "/usr/share/java/jogl/jogl.jar" \
    "${pkgdir}/usr/share/java/${pkgname}/jogl.jar"

  # Needed to use the jlatexmath package
#   ln -s "/usr/share/java/jlatexmath-minimal/jlatexmath-minimal.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlatexmath.jar"
#   ln -s "/usr/share/java/jlatexmath-minimal/jlm_greek.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlm_greek.jar"
#   ln -s "/usr/share/java/jlatexmath-minimal/jlm_cyrillic.jar" \
#     "${pkgdir}/usr/share/java/${pkgname}/jlm_cyrillic.jar"
}

md5sums=('e324ee3a2bb438cee625e3f29770a315'
         '9dcd074a16b1ec498c7aefd3241e73fb'
         'd7e8d83a6bf9c0ba9f6a889cfa46c551'
         '5131ee66212ea06b6aed48816e36c8d7'
         '31cbbeea49ab9a18c52baee19c7d1ccb'
         'a603bc3bdcf729d39b76dfb482468f72'
         'e2b623f13fbd0dde403bba322c316112'
         '3cbc125a2d522e857253f9e7161c6af7'
         'bd616b4f1a2fe9fb51a7119464159ebe'
         '57fdc181766978b6f9ccbe32950b74f2'
         '799a503e8e919370e1c40eab407ff8ad'
         '794f437ebf6a8118debc7bae22c4b072'
         'c281f24cc8a62e9a7d86857127fe8951'
         '21eee97b27c5ef949f8ba2280f905423'
         '2b3fa7025fa4d16cc29bb5b3efdcf2a1'
         '249c52c10e3ada6a583bac095228ff56'
         '39e2fbaf507a7b8db047152332d695c5')
