# Maintainer: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>

pkgname=mdbtools
pkgver=0.7
pkgrel=3
pkgdesc="Utilities for viewing data and exporting schema from Microsoft Access Database files"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mdbtools/"
license=('LGPL' 'GPL')
depends=('libgnomeui' 'unixodbc')
makedepends=('bison' 'flex' 'txt2man')
options=('!libtool')
source=(https://github.com/brianb/${pkgname}/archive/${pkgver}.tar.gz
        gmdb2.desktop)
md5sums=('8e453aa456157d454feaab82f932f052'
         '289f236ca91d73510e842922b5776999')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i s/libtooloze/libtoolize/ autogen.sh
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --with-unixodbc=/usr
  make -j1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  rmdir "${pkgdir}/usr/share/man/mant"
  install -Dm644 ../gmdb2.desktop "${pkgdir}/usr/share/applications/gmdb2.desktop"
}
