# Maintainer: Zachary Huff <zach.huff.386@gmail.com>

pkgname=drivnal
pkgver=0.1.25
pkgrel=1
pkgdesc="Linux backup utility with web interface"
arch=("any")
license=("AGPL3")
url="https://github.com/${pkgname}/${pkgname}"
depends=(
    "python2"
    "python2-flask"
    "python2-cherrypy"
    "rsync"
)
makedepends=(
    "python2-distribute"
)
provides=("${pkgname}")
conflicts=("${pkgname}")
install=${pkgname}.install
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=("3b04c1b6346b82d70b0c94f4a2abef55f72a7d46d5573b30cace443a61589e83")
options=("emptydirs")
backup=(
    "etc/${pkgname}.conf"
    "var/lib/${pkgname}/${pkgname}.db"
    "var/log/${pkgname}.log"
)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p "${pkgdir}/var/lib/${pkgname}"
    python2 setup.py install --root="${pkgdir}" --prefix=/usr --no-upstart
}
