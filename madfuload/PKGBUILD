# Contributor: Max Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=madfuload
pkgver=1.2
pkgrel=4
pkgdesc="USB and MIDI firmware loaders for M-Audio (or Midiman) hardware"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/usb-midi-fw"
license=('GPL')
source=("http://downloads.sourceforge.net/usb-midi-fw/$pkgname-$pkgver.tar.gz"
        '42-madfuload.rules')
md5sums=('6a2c68021e2e05c4eb35e67158b9414e'
         'a04bf1e9d6f49640397bad8f36ca494f')
install=madfuload.install

build() {
 cd "$srcdir/$pkgname-$pkgver"
 sed -i 's/udevinfo -V | awk \x27{ print $3 }\x27/udevadm -V/' configure
 ./configure --prefix=/usr --with-udev=/etc/udev
 make
}

package() {
 cd "$srcdir/$pkgname-$pkgver"
 make DESTDIR="$pkgdir" install
 install -Dm 644 "$srcdir/42-madfuload.rules" "$pkgdir/etc/udev/rules.d/42-madfuload.rules"
}
