# Contributor: joyfulgirl@archlinux.us
# Contributor: nathan owe ndowens04 at gmail dot com
# Contributor: Tomato <kario@wsisiz.edu.pl>
# Maintainer: N30N <archlinux@alunamation.com>

pkgname="cclive-git"
pkgver=v0.9.3.r0.g9f77d01
pkgrel=1
pkgdesc="Commandline downloader for popular video websites."
arch=("i686" "x86_64")
url="http://cclive.sourceforge.net/"
license=("AGPL3")
depends=("boost-libs" "pcre" "curl" "libquvi" "glibmm")
makedepends=("git" "boost")
provides=("cclive")
conflicts=("cclive")
source=("git://repo.or.cz/cclive.git")
md5sums=("SKIP")

pkgver() {
	cd cclive
	git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
	cd cclive
	sed -i "s/-Werror//" configure.ac
}

build() {
	cd cclive
	./bootstrap.sh
	./configure --prefix=/usr boost_cv_rpath_link_ldflag="-L/usr/lib"
	make
}

package(){
	cd cclive
	make DESTDIR="${pkgdir}" install
}

# vim: set noet ff=unix:
