# Maintainer: Jean-Philippe Gagn√© Guay <jeanphilippe150@gmail.com>

_gitname=sinit
pkgname=${_gitname}-git
pkgver=0.8.r2.g7a2bb2c
pkgrel=1
pkgdesc="A suckless init"
url="http://git.2f30.org/sinit/"
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('git')
provides=('sinit')
conflicts=('sinit')
install=sinit.install
source=(git://git.2f30.org/sinit
	config.h)
md5sums=('SKIP'
         'cd8f4f5dde08019b5012b63445fffaee')

pkgver() {
  cd $srcdir/$_gitname
  git describe --long --tags | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd $srcdir/$_gitname
  cp $srcdir/config.h config.h
  make
}

package() {
  cd $srcdir/$_gitname
  make DESTDIR=$pkgdir PREFIX=/usr install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
}
