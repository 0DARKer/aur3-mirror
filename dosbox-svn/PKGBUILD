# Contributor: Jose Valecillos <valecillosjg (at) gmail (dot) com>
pkgname=dosbox-svn
pkgver=3786
pkgrel=1
pkgdesc="An emulator with builtin DOS for running DOS Games"
arch=('i686' 'x86_64')
url="http://dosbox.sourceforge.net/"
license=('GPL')
groups=()
depends=('sdl_net' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'gcc-libs')
makedepends=('subversion')
provides=('dosbox')
conflicts=('dosbox')
replaces=()
backup=()
options=()
source=('dosbox.desktop' 'dosbox-16.png' 'dosbox-48.png' 'dosbox-128.png')
install=dosbox.install
noextract=()
md5sums=('eac69b50c2d7b34503a97cb521fe21ff'
         '4b26bcedfc56e4ef390ee4c66a774827'
         'b49ff452861ba1118f06cf5421878636'
         'c48983033ba1ad1f742577b5df5d8ae6')

_svntrunk="https://dosbox.svn.sourceforge.net/svnroot/dosbox/dosbox/trunk"
_svnmod="trunk"

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #
  # BUILD
  #
  msg "Building and installing..."
  chmod +x autogen.sh
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc/dosbox
  make
}


package() {
	cd "$srcdir/$_svnmod-build"
	make DESTDIR="${pkgdir}" install

	# Fix invalid permissions FS#10732
	chmod 755 "${pkgdir}"/usr/share/man/man1
	
	# Install desktop files and icon
	for _i in 16 48 128; do
		install -D -m644 $srcdir/dosbox-$_i.png "$pkgdir"/usr/share/icons/hicolor/${_i}x${_i}/apps/dosbox.png
	done
	install -D -m644 $srcdir/dosbox.desktop "$pkgdir"/usr/share/applications/dosbox.desktop

	msg "Removing build directory..."
	cd "$srcdir"
	rm -Rf ${_svnmod}-build
}

