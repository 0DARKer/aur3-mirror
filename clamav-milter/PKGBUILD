# Maintainer: Michiel Broek <mbsek at mbse dot eu>

pkgname=clamav-milter
_pkgname=clamav
pkgver=0.97.5
pkgrel=1
pkgdesc='Anti-virus toolkit for Unix with libmilter (by sendmail) build in.'
arch=('i686' 'x86_64')
depends=('bzip2' 'zlib' 'libtool')
makedepends=('m4' 'patch')
options=('!libtool')
install="$_pkgname.install"
license=('GPL')
backup=('etc/clamav/clamd.conf' 'etc/clamav/clamav-milter.conf' 'etc/clamav/freshclam.conf' 'etc/conf.d/clamav')
url='http://www.clamav.net/'
sendmailver=8.14.5
conflicts=('clamav')

md5sums=('4d4b93243a5add0216acc4f24f43a895'
         'fe2ff856cdf9bd0e0db1513305e9767b'
	       '20344c6610ff957cb8df15dbe8ea6c0f'
	       '15b9ace0108254917bd3b11318f5c057'
	       '3ba234525033b870149a33f5d10152a1'
	       '7d13bb47a3e8e5510c8105484acecadf'
	       '9ea7c696979f3977120791bdb355acf9'
	       '89ed33d37ca79d45b8c02418df2486e6'
	       '1495e41055a9588739520f39c31e67e9'
	       '02ccfc331cc81ed00ec8bb5ecfc69018')

source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz"
        'clamav'
        'clamav.confd'
        'clamav.logrotate'
        'config.patch'
	      'service'
	      'service.fresh'
	      'tmpfiles.d'
	      'site.config.m4'
	      "ftp://ftp.sendmail.org/pub/sendmail/sendmail.${sendmailver}.tar.gz")



build() {
	# First build a static libmilter from sendmail
	cd "$srcdir/sendmail-$sendmailver"
	cp "$srcdir/site.config.m4" devtools/Site/site.config.m4
	cd libmilter
	./Build || exit 1

	cd "$srcdir/$_pkgname-$pkgver"
	patch -p0 < ../config.patch

	LDFLAGS="$LDFLAGS -L$srcdir/sendmail-$sendmailver/obj.Linux.$(uname -r).$(uname -m)/libmilter/" \
	CFLAGS="$CFLAGS -I$srcdir/sendmail-$sendmailver/include" \
	./configure --prefix=/usr --sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav --disable-clamav \
		--enable-milter 
	make
}

package() {
	cd "$srcdir/$_pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	# make sure conf files get installed, cause make install
	# doesn't do that if clamav is already installed upon building.
	install -Dm644 etc/clamd.conf "$pkgdir/etc/clamav/clamd.conf"
	install -Dm644 etc/freshclam.conf "$pkgdir/etc/clamav/freshclam.conf"
	install -Dm644 etc/clamav-milter.conf "$pkgdir/etc/clamav/clamav-milter.conf"

	install -Dm644 ../service.fresh "${pkgdir}"/usr/lib/systemd/system/freshclamd.service
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/clamd.service
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/clamav.conf
	install -Dm644 ../clamav.logrotate "$pkgdir/etc/logrotate.d/clamav"
	install -Dm644 ../clamav.confd "$pkgdir/etc/conf.d/clamav"
	install -Dm755 ../clamav "$pkgdir/etc/rc.d/clamav"

  # Fix references to $srvdir
  sed -i -e "s|-I${srcdir}/sendmail-$sendmailver/include||" ${pkgdir}/usr/bin/clamav-config
  sed -i -e "s|-I${srcdir}/sendmail-$sendmailver/include||" ${pkgdir}/usr/lib/pkgconfig/libclamav.pc
}

# vim:set ts=2 sw=2 et:
