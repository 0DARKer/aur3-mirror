# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>

pkgname=ocaml-fieldslib
_pkgname=${pkgname/ocaml-/}
pkgver=108.08.00
pkgrel=1
pkgdesc="OCaml syntax extension for folding over record fields."
arch=('i686' 'x86_64')
# LGPL + linking exception
license=('LGPL')
depends=('ocaml' 'ocaml-type-conv')
makedepends=('ocaml-findlib')
url="http://bitbucket.org/yminsky/ocaml-core/wiki/Home"
source=("http://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('f164899a74c2ce48b0d09e37dc45522e')
options=(!strip !makeflags)

build(){
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package(){
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
