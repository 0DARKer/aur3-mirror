# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>

pkgname=ocaml-fieldslib
pkgver=107.01
pkgrel=2
pkgdesc="OCaml syntax extension for folding over record fields."
arch=('i686' 'x86_64')
url="http://ocaml.janestcapital.com/?q=node/13"
# LGPL + linking exception
license=('LGPL')
depends=('ocaml' 'ocaml-type-conv')
makedepends=('ocaml-findlib')
source=(http://www.janestreet.com/ocaml/${pkgname/ocaml-/}-$pkgver.tar.gz)
md5sums=('f2ed8e1b485881405dba601ffd87112b')
options=(!strip !makeflags)

build(){
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  ocaml setup.ml -configure --prefix /usr --destdir ${pkgdir}
  ocaml setup.ml -build || return 1
}

package(){
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"

  mkdir -p $OCAMLFIND_DESTDIR
  make install
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
