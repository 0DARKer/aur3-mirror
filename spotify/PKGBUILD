# Maintainer: Gadget3000 <gadget3000@msn.com>
# Contributor: Eothred <yngve.levinsen@gmail.com>

pkgname=spotify
pkgver=0.6.6.10
_anotherpkgver=.gbd39032.58-1
pkgrel=1
pkgdesc="A proprietary peer-to-peer music streaming service"
makedepends=('tar')
arch=('x86_64' 'i686')
license=('custom:"Copyright (c) 2006-2010 Spotify Ltd"')
install=spotify.install
url="http://www.spotify.com"
changelog='spotify.changelog'
options=('!strip')
conflicts=('spotify-beta')

#Spotify changelog is installed to /usr/share/doc/spotify-client-qt/

if [ "${CARCH}" = "x86_64" ]; then
  md5sums=('405101c92eb0755973bd6ed4c3ebc0f4'
           'b38dc759e47d476e34aa7a453353c494'
           '5267317ef2b23cd0a1621c61474f8dc6'
	   'ef25ddc5b6bf8fe1a0d64cbd79e1f7b4')
  _carch=_amd64
elif [ "${CARCH}" = "i686" ]; then
  md5sums=('2e98d59b11387f9e74ac440de49ec11d'
           'b38dc759e47d476e34aa7a453353c494'
           '5267317ef2b23cd0a1621c61474f8dc6'
	   'ef25ddc5b6bf8fe1a0d64cbd79e1f7b4')
  _carch=_i386
fi

depends=("alsa-lib>=1.0.14" "glibc>=2.6" "qt>=4.5.0" "libxss" "openssl" "qtwebkit")
optdepends=('desktop-file-utils: Adds URI support to compatible desktop environments')
source=("http://repository.spotify.com/pool/non-free/s/${pkgname}/${pkgname}-client-qt_${pkgver}${_anotherpkgver}${_carch}.deb"
        'spotify.desktop'
        'spotify.png'
	'spotify.protocol')

build() {
  cd ${srcdir}
  ar x ${pkgname}-client-qt_${pkgver}${_anotherpkgver}${_carch}.deb > /dev/null || return 1
}

package() {
  cd ${srcdir}
  tar -xzf data.tar.gz -C ${pkgdir} || return 1

#Copy license
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -D -m644 ${pkgdir}/usr/share/doc/${pkgname}-client-qt/copyright \
	  ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1

#Copy icon
  install -d ${pkgdir}/usr/share/pixmaps/
  install -D -m644 ${srcdir}/spotify.png ${pkgdir}/usr/share/pixmaps/ || return 1

#Copy desktop file
  install -d ${pkgdir}/usr/share/applications/
  install -D -m644 ${srcdir}/spotify.desktop ${pkgdir}/usr/share/applications/ || return 1

#Copy protocol file if KDE is installed
if [ -f /usr/bin/startkde ]; then
  echo "Installing with KDE support"
  install -d ${pkgdir}/usr/share/kde4/services/
  install -D -m644 ${srcdir}/spotify.protocol ${pkgdir}/usr/share/kde4/services/ || return 1
fi

#Link ssl 0.9.8 libraries to most up to date openssl
#as long as the libraries don't already exist as a standard file (ie and isn't a symlink)
if [ ! -f /usr/lib/libssl.so.0.9.8 ] || [ -L /usr/lib/libssl.so.0.9.8 ]; then 
if [ ! -f /usr/lib/libcrypto.so.0.9.8 ] || [ -L /usr/lib/libcrypto.so.0.9.8 ]; then
  install -d ${pkgdir}/usr/lib
  ln -s /usr/lib/libssl.so ${pkgdir}/usr/lib/libssl.so.0.9.8
  ln -s /usr/lib/libcrypto.so ${pkgdir}/usr/lib/libcrypto.so.0.9.8
fi
fi
}

