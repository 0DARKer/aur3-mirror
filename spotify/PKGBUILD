# Maintainer: Gadget3000 <gadget3000@msn.com>
# Contributor: Eothred <yngve.levinsen@gmail.com>

pkgname=spotify
pkgver=0.8.2.637
_anotherpkgver=.g252b980.486-1
pkgrel=2
pkgdesc="A proprietary peer-to-peer music streaming service"
arch=('x86_64' 'i686')
license=('custom:"Copyright (c) 2006-2010 Spotify Ltd"')
install=spotify.install
url="http://www.spotify.com"
changelog='spotify.changelog'
options=('!strip')
conflicts=('spotify-beta')

#Spotify changelog is installed to /usr/share/doc/spotify-client/

if [ "${CARCH}" = "x86_64" ]; then
  md5sums=('96f4f950555cad73fcbf29c81d5e2957'
           'ef25ddc5b6bf8fe1a0d64cbd79e1f7b4')
  _carch=_amd64
elif [ "${CARCH}" = "i686" ]; then
  md5sums=('0fabe525f867da4fac0cb7da3132291f'
           'ef25ddc5b6bf8fe1a0d64cbd79e1f7b4')
  _carch=_i386
fi

depends=("alsa-lib>=1.0.14" "glibc>=2.6" "qt>=4.5.0" "libxss" "openssl098" "qtwebkit")
optdepends=('desktop-file-utils: Adds URI support to compatible desktop environments')
source=("http://repository.spotify.com/pool/non-free/s/${pkgname}/${pkgname}-client_${pkgver}${_anotherpkgver}${_carch}.deb"
        'spotify.protocol')

build() {
  cd ${srcdir}
  ar x ${pkgname}-client_${pkgver}${_anotherpkgver}${_carch}.deb > /dev/null || return 1
}

package() {
  cd ${srcdir}
  tar -xzf data.tar.gz -C ${pkgdir} || return 1

#Copy license
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -D -m644 ${pkgdir}/usr/share/doc/${pkgname}-client/copyright \
	  ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1

#Copy protocol file if KDE is installed
if [ -f /usr/bin/startkde ]; then
  echo "Installing with KDE support"
  install -d ${pkgdir}/usr/share/kde4/services/
  install -D -m644 ${srcdir}/spotify.protocol ${pkgdir}/usr/share/kde4/services/ || return 1
fi
}

