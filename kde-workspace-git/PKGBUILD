# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kde-workspace-git
pkgver=r58056.75540f2
pkgrel=1
pkgdesc='Provides the interface and basic tools for the KDE workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('frameworkintegration-git' 'kcmutils-git' 'knotifyconfig-git' 'kross-git' 'kded-git'
         'kdesu-git' 'knewstuff-git' 'qimageblitz-svn' 'kidletime-git' 'kitemmodels-git'
         'libxkbfile' 'libxcursor' 'lm_sensors' 'libraw1394' 'libusb-compat' 'mesa' 'gpsd' 'khtml-git' 'kinit-git'
         'pciutils' 'kdoctools-git' 'kde4support-git' 'kjsembed-git' 'kemoticons-git' 'krunner-git'
         'qt5-tools' 'glu' 'libxdamage' 'libxrandr' 'appmenu-qt5') # 'kf5-akonadi-git' useless without kdepimlibs
makedepends=('boost' 'extra-cmake-modules' 'git' 'kf5umbrella-git')
conflicts=('kde-workspace')
provides=('kde-workspace')
source=('git://anongit.kde.org/kde-workspace.git'
        'kf5-plasma.desktop'
        '10-kf5')
md5sums=('SKIP'
         '28548acea99d29f37ee3d18184a661f4'
         '5de1255c33c4b76668485f51449aee9e')

pkgver() {
  cd kde-workspace
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
  export CMAKE_PREFIX_PATH="/opt/kf5"

  cd build
  cmake ../kde-workspace \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed s/qtpaths/qtpaths-qt5/g -i "$pkgdir/opt/kf5/bin/startkde"

  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  install -dm755 "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/kf5-plasma.desktop" "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -sr "${pkgdir}/usr/share/apps/kdm/sessions/kf5-plasma.desktop" -t "${pkgdir}/usr/share/xsessions/"
  
  install -Dm755 "${srcdir}/10-kf5" "${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5"
}
