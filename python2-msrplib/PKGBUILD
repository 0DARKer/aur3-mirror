# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=python2-msrplib
pkgver=0.15.0
pkgrel=1
pkgdesc="A python MSRP client library, implements RFC4975 and RFC4976"
license=('custom:MIT')
arch=('any')
url="http://download.ag-projects.com/SipClient"
depends=('python2-application' 'python2-gnutls' 'python2-zope-interface' 'twisted')
source=("http://download.ag-projects.com/SipClient/${pkgname/2}-$pkgver.tar.gz")
md5sums=('6e5bb9c1d5c9183ff0b287889f466808')

build() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  # license
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
