#  Maintainer: sudokode <sudokode@gmail.com>
# Contributor: Christos Nouskas <nous at archlinux dot us>
# Contributor: Jakub Luzny <limoto94@gmail.com>

pkgname=arora-git
pkgver=0.11.0.14.ge310d63
pkgrel=2
pkgdesc="A lightweight, cross-platform Qt4 WebKit browser"
arch=('i686' 'x86_64')
url="http://arora.googlecode.com/"
license=('GPL2')
depends=('qt4>=4.8.0' 'qtwebkit')
makedepends=('git')
provides=('arora')
conflicts=('arora')
source=("$pkgname::git://github.com/Arora/arora.git"
        'facebook.patch')
sha256sums=('SKIP'
            'd510a740d5650aabba35b741b4106d2e1ed66ee72ef8afe16de0d857db4fdac6')

pkgver() {
  cd "$srcdir"/$pkgname

  git describe | sed 's/-/./g'
}

prepare() {
  cd "$srcdir"/$pkgname

  # Fix facebook modify.php nirvana
  patch -Np1 < "$srcdir"/facebook.patch
}

build() {
  cd "$srcdir"/$pkgname

  qmake-qt4 PREFIX=/usr
  make sub-src-qmake_all

  make
}


package() {
  cd "$srcdir"/$pkgname

  make INSTALL_ROOT="$pkgdir" install

  find "$pkgdir" -type d -name '.git' -exec rm -r '{}' +
}


