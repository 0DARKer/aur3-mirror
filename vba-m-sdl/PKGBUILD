pkgname=vba-m-sdl
pkgver=1.8.0.1138
pkgrel=1
pkgdesc="A VBA fork that focuses on consolidating existing ports and improving VBA in general"
arch=(i686 x86_64)
url="http://vba-m.com/"
license=('GPL2')
depends=('sdl' 'libpng' 'mesa')
provides=('vba-m')
conflicts=('vba-m' 'vba-m-gtk')
makedepends=('cmake' 'yasm')
backup=('etc/vbam.cfg')
source=("http://sourceforge.net/projects/vbam/files/VBA-M%20svn%20r1138/vbam-$pkgver-src.tar.gz"
	http://sourceforge.net/projects/sfml/files/sfml/1.6/SFML-1.6-sdk-linux-32.tar.gz
	libsfml-1.6-deps-and-flags.patch
	libsfml-1.6-libpng15.patch
        libsfml-1.6-destdir.patch
	libsfml-1.6-gcc47.patch
	libsfml-1.6-gcc46.patch)
md5sums=('e35153271382bca12c185a7726747073'
         'acc678933c19558587aad8332ea6f459'
         '69037b57700ca701492c633b41ea2b0d'
         '926cab1880ad9112bf3f3290f9503328'
         '6248e478ef5d5ce61c4a18e75ede1c8b'
         'bde833cf03b513d6cefff5a54f698c19'
         'd4c4be26ffbd52a11bd0222e0b729d25')

build() {
  # SFML 1.6 is ancient, but everyone except Arch has it
  pushd SFML-1.6
  patch -Np1 -i "$srcdir/libsfml-1.6-destdir.patch"
  patch -Np1 -i "$srcdir/libsfml-1.6-deps-and-flags.patch"
  patch -Np1 -i "$srcdir/libsfml-1.6-gcc46.patch"
  patch -Np1 -i "$srcdir/libsfml-1.6-gcc47.patch"
  patch -Np0 -i "$srcdir/libsfml-1.6-libpng15.patch"
  make STATIC=yes

  # support for non-system and static SFML haven't arrived yet
  popd
  sed "s|\${SFML_LIBRARY_SEARCH_DIR}|$srcdir/SFML-1.6|" -i CMakeScripts/FindSFML.cmake
  sed "s|\${SFML_INCLUDE_SEARCH_DIR}|$srcdir/SFML-1.6/include/SFML|" -i CMakeScripts/FindSFML.cmake
  sed "s|sfml-\${LOWERCOMPONENT}|sfml-\${LOWERCOMPONENT}-s|" -i CMakeScripts/FindSFML.cmake

  CXXFLAGS="$CXXFLAGS -I$srcdir/SFML-1.6/include" cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATA_INSTALL_DIR:PATH=share/vbam \
    -DVERSION="$pkgver" -DENABLE_LINK=ON \
    -DENABLE_WX=OFF -DENABLE_GTK=OFF \
    -DENABLE_SDL=ON -DENABLE_ASM_CORE=ON \
    -DENABLE_ASM_SCALERS=ON -DENABLE_FFMPEG=OFF

  make

  sed 's/wxvbam/vbam/' -i "$srcdir"/src/wx/wxvbam.desktop
  sed 's/=vbam/=vba-m-sdl.png/' -i "$srcdir"/src/wx/wxvbam.desktop
}

package() {
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/vbam.1 "$pkgdir"/usr/share/man/man1/vbam.1
  install -Dm644 doc/ReadMe.SDL.txt "$pkgdir"/usr/share/doc/vba-m/README
  install -Dm644 src/wx/wxvbam.desktop "$pkgdir"/usr/share/applications/${pkgname}.desktop
  install -Dm644 src/gtk/icons/32x32/apps/vbam.png "$pkgdir"/usr/share/pixmaps/${pkgname}.png
}
