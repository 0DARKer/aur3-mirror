# Maintainer: Mijo Medvedec <mijo dot medvedec at gmail dot com>
# Original Thunar PKGBUILD edited to apply the thunar.split-view-support.patch

# Main Maintainer & Contributor:
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar-split-view
pkgver=1.6.3
pkgrel=1
pkgdesc="Thunar FM with split view patch applied"
arch=('i686' 'x86_64')
url="http://docs.xfce.org/xfce/thunar/start"
license=('GPL2' 'LGPL2.1')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
		 'udev' 'gtk2' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'gtk-doc' 'xfce4-panel')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
			'thunar-volman: manages removable devices'
			'xfce4-panel: for trash applet')
conflicts=('thunar')
install=${pkgname}.install
source=("http://archive.xfce.org/src/xfce/thunar/1.6/Thunar-${pkgver}.tar.bz2"
		"0001-Fix-Open-With-default-app-with-glib-2.41.patch"
		"thunar-split-view-support.patch"
		"thunar-split-view.install")
md5sums=('4f10d5d5576ce5127308d6badbac3afa'
		 'e9bc3bbe27a53eed117ed7988847511e'
		 'fc25b9389cd0a19a5f1cbb70af657ee0'
		 '296c227029a2fa0ab1673696c6beb293')
sha256sums=('9a2706f6881ac29cda9f0b0325d179153bc09e37bcbafcab9823c1c1ec89579d'
			'0c818a244c3d542f5b3e8e3b1c0eab4773cffb6c42692d75a1b290daad7dd05a'
			'2eb1cd0304db9f4e8a9bfdaad22ecad3948911c4cab4258b0cd70e5e4a225d62'
			'27fb96a1cb23edf3a7b765e8609d1f63c7abef5d232732dabdb5cbd835e2de49')

prepare() {
	cd "$srcdir/Thunar-$pkgver"

	# Thunar Split View Mode ( F3 to enable it )
	# https://bugzilla.xfce.org/show_bug.cgi?id=10232
	# https://bugzilla.xfce.org/attachment.cgi?id=5751&action=edit
	patch -Np1 -i ../thunar-split-view-support.patch

	# https://bugzilla.xfce.org/show_bug.cgi?id=11212
	patch -Np1 -i ../0001-Fix-Open-With-default-app-with-glib-2.41.patch
}

build() {
	cd "$srcdir/Thunar-$pkgver"

	./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-gio-unix \
	--enable-dbus \
	--enable-startup-notification \
	--enable-gudev \
	--enable-notifications \
	--enable-exif \
	--enable-pcre \
	--enable-gtk-doc \
	--disable-debug

	make
}

package() {
	cd "$srcdir/Thunar-$pkgver"

	make DESTDIR=${pkgdir} install

	sed -i 's:x-directory/gnome-default-handler;::' \
		"$pkgdir/usr/share/applications/Thunar-folder-handler.desktop"
}
