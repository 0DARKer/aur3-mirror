# Maintainer: Mijo Medvedec <mijo dot medvedec at gmail dot com>

pkgname=thunar-split-view
pkgver=1.6.6
pkgrel=1
pkgdesc="Thunar FM with the split view patch applied"
arch=('i686' 'x86_64')
url="http://docs.xfce.org/xfce/thunar/start"
license=('GPL2')
depends=('gtk2' 'glib2' 'exo' 'intltool' 'dbus')
conflicts=('thunar')
provides=('thunar')
source=("http://archive.xfce.org/xfce/4.12/src/Thunar-${pkgver}.tar.bz2"
	"thunar-split-view-support.patch")
md5sums=('27a5343174593dd0067906c922e6af96'
	 '8416f236f2e635a718e1ec9551ec2781')
sha256sums=('b9a55104b18ae01c5190d7ea02cbd7ba56d7affed9df416bf7fec6dff2d889b2'
	    '6a5514cf5cfd9eecf1b8793e9d98d5957e181a28442058b908705e1308bb377e')

prepare() {
	cd "$srcdir/Thunar-$pkgver"

	# Thunar split view mode ( F3 to enable it )
	# https://bugzilla.xfce.org/show_bug.cgi?id=10232
	# https://bugzilla.xfce.org/attachment.cgi?id=5751&action=edit
	patch -Np1 -i ../thunar-split-view-support.patch
}

build() {
	cd "$srcdir/Thunar-$pkgver"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--localstatedir=/var
	make
}

package() {
	cd "$srcdir/Thunar-$pkgver"
	make DESTDIR=${pkgdir} install
}
