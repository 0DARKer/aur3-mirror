# Contributor: Simon Bachmann <simonbachmann@freesurf.ch>
# Maintainer: Anton Larionov <wanton dot clarion at gmail dot com>
# Previous versions you can find here: http://github.com/wanton/pkgbuilds

pkgname=mirrormagic
pkgver=2.0.2
pkgrel=2
pkgdesc="A game with a magic ray and many, many mirrors..."
arch=('i686' 'x86_64')
url="http://www.artsoft.org/mirrormagic/"
license="GPL"
depends=(glibc sdl sdl_image sdl_mixer)
source=(http://www.artsoft.org/RELEASES/unix/mirrormagic/${pkgname}-${pkgver}.tar.gz
	main_h.diff)
md5sums=('32fd3909c1e27f493d89bc2276da6744'
         '6722c4b1b9e2e0db71014c75c1369c03')

build() {

  cd "${srcdir}/${pkgname}-${pkgver}"
  #fix an error in main.h
  patch -p1 < "${srcdir}/main_h.diff"
  #compile...
  make RO_GAME_DIR="/usr/share/${pkgname}" RW_GAME_DIR=~/.${pkgname} sdl

}

package() {

  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 "${pkgdir}/usr/share/${pkgname}"
  cp -r graphics levels music scores sounds "${pkgdir}/usr/share/${pkgname}"

  install -dm755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 CHANGES CREDITS INSTALL README TODO "${pkgdir}/usr/share/doc/${pkgname}"

  install -D -m755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

}

# vim:set ts=2 sw=2 et:
