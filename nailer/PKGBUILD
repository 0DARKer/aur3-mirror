# Contributor: Christoph Zeiler <rabyte*gmail>
# Contributor: Mr_Smiley <archlinux__mr-smiley.org> (GConf support)

pkgname=nailer
pkgver=0.4.3
pkgrel=6
pkgdesc="A command-line application using MPlayer to generate thumbnails of video files"
arch=('i686' 'x86_64')
url="http://kdekorte.googlepages.com/"
license=('GPL2')
depends=('mplayer')
makedepends=('pkgconfig>=0.9')
install=$pkgname.install
source=(http://mplayer-video-thumbnailer.googlecode.com/files/$pkgname-$pkgver.tar.gz \
	check-args.diff)
md5sums=('d5f0e1bc62a93abe4e312d922e76eb6b'
         '6862b2893019cb82bc0b90fbae7c6c74')

build() {
  cd $pkgname-$pkgver

  patch -Np0 -i ../check-args.diff || return 1
  sed 's|\+ogg||g' -i $pkgname.schemas.in

  ./configure --prefix=/usr --sysconfdir=/usr/share --disable-static
  make AM_CFLAGS= || return 1
  make DESTDIR="$pkgdir" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}
