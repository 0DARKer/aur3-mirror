# Maintainer: Army

_pkgname=fltk
pkgname=lib32-$_pkgname
pkgver=1.3.2
pkgrel=1
pkgdesc="Graphical user interface toolkit for X"
arch=('x86_64')
url="http://www.fltk.org"
license=('custom:LGPL')
depends=('fltk' 'lib32-gcc-libs' 'lib32-libjpeg-turbo' 'lib32-libpng' 'lib32-libxft' 'lib32-libxinerama')
install=lib32-fltk.install
source=(http://ftp.easysw.com/pub/$_pkgname/$pkgver/$_pkgname-$pkgver-source.tar.gz)
md5sums=('9f7e707d4fb7a5a76f0f9b73ff70623d')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i -e 's/$(LINKFLTK)/$(LINKSHARED)/' \
         -e 's/$(LINKFLTKIMG)/$(LINKSHARED)/' test/Makefile
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32/fltk --enable-threads --enable-xft --enable-shared CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  (cd fluid; make DESTDIR="$pkgdir" install install-linux)

  chmod 644 "$pkgdir"/usr/lib32/*.a
  rm -rf ${pkgdir}/usr/{bin,include,share} # would conflict with package fltk, that's why it's listed as a dependency

  install -Dm644 "COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
