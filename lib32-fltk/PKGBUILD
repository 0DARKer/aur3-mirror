# Maintainer : 
# Contributor : 

_pkgbasename=fltk
pkgname=lib32-$_pkgbasename
pkgver=1.3.0
pkgrel=1
pkgdesc="Graphical user interface toolkit for X (32 bit)"
arch=('x86_64')
license=('LGPL')
url="http://www.fltk.org/"
depends=('fltk' 'lib32-gcc-libs' 'lib32-libjpeg-turbo'
	 'lib32-libpng' 'lib32-libxft' 'lib32-libxinerama')
makedepends=('lib32-mesa' 'gcc-multilib')
source=("http://ftp.easysw.com/pub/fltk/$pkgver/fltk-$pkgver-source.tar.gz")
md5sums=('44d5d7ba06afdd36ea17da6b4b703ca3')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32/fltk \
		  --enable-xft --enable-shared CC='gcc -m32' CXX='g++ -m32' \
		  --enable-threads
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR=$pkgdir install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}