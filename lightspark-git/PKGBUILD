# Maintainer: mmm
# Contributor: bslackr <brendan at vastactive dot com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=lightspark-git
pkgver=20130215
pkgrel=2
pkgdesc="An alternative Flash Player for Linux"
arch=(i686 x86_64)
url="http://lightspark.sourceforge.net"
license=(LGPL3)
depends=(mesa ftgl gtk2 curl zlib ffmpeg glew pcre libffi boost-libs
         hicolor-icon-theme desktop-file-utils libxml++ rtmpdump) 
makedepends=(git cmake nasm xulrunner llvm glproto boost fontconfig
             sdl libpulse)
optdepends=('gnash-gtk: Gnash fallback support'
            'libpulse: PulseAudio audio backend'
            'sdl: SDL audio backend')
provides=(lightspark)
conflicts=(lightspark)
source=("git+git://github.com/lightspark/lightspark.git")
md5sums=('SKIP')

_gitname="lightspark"

build() {
  cd "$srcdir"
  rm -rf $_gitname-build
  mkdir $_gitname-build
  cd $_gitname-build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPILE_PLUGIN=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DGNASH_EXE_PATH=/usr/bin/gtk-gnash \
    -DAUDIO_BACKEND="pulseaudio sdl" \
    ../$_gitname

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}

