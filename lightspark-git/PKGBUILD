# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=lightspark-git
pkgver=20110508
pkgrel=1
pkgdesc="An alternative Flash Player for Linux - Flash for real. Now."
arch=(i686 x86_64)
url="http://lightspark.sourceforge.net"
license=(LGPL3)
depends=(mesa ftgl sdl gtk2 curl zlib ffmpeg glew pcre libpulse libffi
         boost-libs gtkglext desktop-file-utils libxml++ rtmpdump) 
makedepends=(git cmake nasm xulrunner llvm glproto boost fontconfig)
optdepends=('gnash-gtk: Gnash fallback support')
provides=(lightspark)
conflicts=(lightspark)
install=lightspark.install
_gitroot="git://github.com/lightspark/lightspark.git"
_gitname="lightspark"

build() {
  cd "$srcdir"

  if [ -d $_gitname ] ; then
    ( cd $_gitname && git pull ) || warning "Git pull failed!"
  else
    git clone $_gitroot $_gitname
  fi

  rm -rf $_gitname-build
  mkdir $_gitname-build
  cd $_gitname-build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPILE_PLUGIN=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DGNASH_EXE_PATH=/usr/bin/gtk-gnash \
    ../$_gitname

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install
}
