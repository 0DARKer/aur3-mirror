# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=lightspark-git
pkgver=20110202
pkgrel=1
pkgdesc="An alternative Flash Player for Linux - Flash for real. Now."
arch=(i686 x86_64)
url="http://lightspark.sourceforge.net"
license=(LGPL3)
depends=(mesa ftgl sdl gtk2 curl zlib ffmpeg 'glew>=1.5.4' pcre pulseaudio
         libffi gettext boost-libs libxml2 glibmm rtmpdump) 
makedepends=(git cmake nasm xulrunner llvm pkgconfig glproto boost
             glibmm-docs)
optdepends=('gnash-gtk: Gnash fallback support')
provides=(lightspark)
conflicts=(lightspark)
install=lightspark.install
_xmlver=2.33.1
source=("http://ftp.gnome.org/pub/GNOME/sources/libxml++/2.33/libxml++-$_xmlver.tar.bz2")
md5sums=(90919b5a5e92381722ef004898eba343)

_gitroot="git://github.com/lightspark/lightspark.git"
_gitname="lightspark"

build() {
  cd "$srcdir"

  export CFLAGS+=" -fPIC"
  export CXXFLAGS+=" -fPIC"

msg2 "Building libxml++..."

  if [[ ! -d static ]]; then (
    cd libxml++-$_xmlver
    ./configure \
      --prefix="$srcdir/static" \
      --enable-static \
      --disable-shared \
      --disable-documentation \
      --disable-examples
    make
    make install
  ) fi

  rm -rf libxml++-$_xmlver
  export PKG_CONFIG_PATH="$srcdir/static/lib/pkgconfig"

msg2 "Connecting to github...."

  if [ -d $_gitname ] ; then
    ( cd $_gitname && git pull ) || warning "Git pull failed!"
  else
    git clone $_gitroot $_gitname
  fi

  rm -rf $_gitname-build
  cp -r $_gitname $_gitname-build
  cd $_gitname-build

msg2 "Starting make..."

  sed '213iLINK_DIRECTORIES(${LIBXMLPP_LIB_DIR})' \
    -i CMakeLists.txt
  [[ -f /usr/include/llvm/Support/DataTypes.h ]] &&
    sed 's|llvm/System/DataTypes.h|llvm/Support/DataTypes.h|' -i swftypes.h

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGNASH_EXE_PATH=/usr/bin/gtk-gnash \
    -DCOMPILE_PLUGIN=1 \
    -DLIBXMLPP_LIB_DIR="$srcdir/static/lib"

  make
} 

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
