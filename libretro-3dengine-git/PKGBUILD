# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>

pkgname=libretro-3dengine-git
pkgver=70.ae5ebea
pkgrel=1
pkgdesc="3D Engine for libretro GL."
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/libretro-3dengine"
license=('unknown')
depends=('zlib' 'libgl')
makedepends=('git')

_gitname=libretro-3dengine
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/3dengine_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd ${_gitname}
  make
}

package() {
  install -Dm644 "${_gitname}/3dengine_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-3dengine.so"
  install -Dm644 "3dengine_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-3dengine.info"
}
