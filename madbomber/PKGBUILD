# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=madbomber
pkgver=0.2.5
pkgrel=3
pkgdesc="A clone of the classic Atari 2600 video game Kaboom"
arch=('i686' 'x86_64')
url="http://www.newbreedsoftware.com/madbomber/"
license=('GPL')
depends=('sdl_image' 'sdl_mixer')
source=("ftp://ftp.tuxpaint.org/unix/x/${pkgname}/src/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('90772baaec90a10f6d9467bc4d3963a6'
         'bb013faed035254ff639a2d042f388ee')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install game files
  mkdir -p "${pkgdir}/usr/bin"
  make DATA_PREFIX="${pkgdir}/usr/share/${pkgname}" \
       BIN_PREFIX="${pkgdir}/usr/bin" \
       install || return 1

  # Install pixmap and .desktop file
  install -Dm644 data/images/icon.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install docs
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 *.txt "${pkgdir}/usr/share/doc/${pkgname}"
}
