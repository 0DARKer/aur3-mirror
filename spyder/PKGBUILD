# Maintainer: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=2.1.13.1
pkgrel=2
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
arch=('any')
url='http://code.google.com/p/spyderlib/'
license=('MIT')
install=spyder.install
depends=('python2-pyqt' 'python2-pyflakes' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('python2-sphinx' 'python2-distribute')
optdepends=('pylint: powerful code analysis'
            'ipython2: enhanced Python interpreter'
            'python2-rope: editor code completion, calltips and go-to-definition'
            "python2-sphinx: object inspector's rich text mode"
            'python2-numpy: N-dimensional arrays'
            'python2-scipy: signal/image processing'
            'python2-matplotlib: 2D/3D plotting')
source=("http://spyderlib.googlecode.com/files/spyder-${pkgver}.zip")
md5sums=('ba402b09409acb7759cf507e108fb19c')

build() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -D -m644 spyderlib/__init__.py \
    ${pkgdir}/usr/share/licenses/spyder/LICENSE
  #Icon
  sed -i -e '$a\Icon=spyder' "${srcdir}"/spyder-"${pkgver}"/scripts/spyder.desktop
  install -D -m644 "${srcdir}"/spyder-"${pkgver}"/scripts/spyder.desktop \
    "${pkgdir}"/usr/share/applications/spyder.desktop
  install -D -m644 "${srcdir}"/spyder-"${pkgver}"/spyderlib/images/spyder.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/spyder.svg 

}
# vim:ts=2:sw=2:et:
