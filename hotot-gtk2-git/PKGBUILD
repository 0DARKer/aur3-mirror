# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=hotot-gtk2-git
pkgver=20120227
pkgrel=1
pkgdesc="A lightweight & open source microblogging software (twitter identi.ca)."
arch=('any')
url="http://www.hotot.org/"
license=('LGPL3')
depends=('hotot-data-git' 'pywebkitgtk' 'python-notify'
         'python-keybinder' 'dbus-python' 'desktop-file-utils')
makedepends=('git' 'cmake' 'intltool')
optdepends=('libappindicator: unity menubar integration')
provides=("${pkgname/-git}" 'hotot')
conflicts=("${pkgname/-git}" 'hotot')
install="$pkgname.install"

_gitroot="https://github.com/shellex/Hotot.git"
_gitname="hotot"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DWITH_GTK=ON \
          -DWITH_GIR=OFF \
          -DWITH_QT=OFF \
          -DWITH_KDE=OFF \
          -DWITH_CHROME=OFF \
          -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  # bin
  install -Dm755 scripts/hotot \
    "$pkgdir/usr/bin/hotot"

  # libs
  install -d "$pkgdir/usr/lib/python2.7/site-packages/hotot"
  install -Dm644 hotot/*.py* \
    "$pkgdir/usr/lib/python2.7/site-packages/hotot"

  # desktop file
  install -Dm644 misc/hotot.desktop \
    "$pkgdir/usr/share/applications/hotot.desktop"
}

# vim:set ts=2 sw=2 et:
