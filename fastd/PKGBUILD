pkgname=fastd
arch=(i686 x86_64)
pkgver=12
pkgrel=1
pkgdesc="Fast and secure tunneling daemon"

# Enable for fast AES implementations
ENABLE_OPENSSL=0

makedepends=('cmake' 'bison' 'libnacl' 'pkg-config')
depends=('glibc' 'libuecc>=4' 'systemd')

[ $ENABLE_OPENSSL -eq 0 ] || depends=(${depends[@]} 'openssl')

url="https://projects.universe-factory.net/projects/fastd"
license=('BSD')
source=("https://projects.universe-factory.net/attachments/download/73/${pkgname}-${pkgver}.tar.xz")
md5sums=('1dadc61f4d712a10844afcb9b9f49a41')

build() {
  mkdir -p "$srcdir/build-${pkgver}"
  cd "$srcdir/build-${pkgver}"

  opts=''
  [ $ENABLE_OPENSSL -eq 0 ] || opts="${opts} -D ENABLE_OPENSSL=ON"

  cmake -D CMAKE_BUILD_TYPE=RELEASE -D CMAKE_INSTALL_PREFIX=/usr -D ENABLE_SYSTEMD=ON ${opts} "../${pkgname}-${pkgver}"
  make
}

package() {
  cd "$srcdir/build-${pkgver}"
  make install DESTDIR="${pkgdir}"

  install -dm755 "${pkgdir}/etc/fastd/"

  install -dm755 "${pkgdir}/usr/lib/systemd/system/"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/examples/fastd@.service" "${pkgdir}/usr/lib/systemd/system/"
}
