pkgname=fastd
arch=(i686 x86_64)
pkgver=14
pkgrel=1
pkgdesc="Fast and secure tunneling daemon"

# Enable for fast AES implementations
ENABLE_OPENSSL=0

makedepends=('cmake' 'bison' 'pkg-config')
depends=('libcap' 'gcc-libs' 'libuecc>=4' 'libsodium')

[ $ENABLE_OPENSSL -eq 0 ] || depends=(${depends[@]} 'openssl')

url="https://projects.universe-factory.net/projects/fastd"
license=('BSD')
DLAGENTS=('https::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 --insecure -o %o %u')
source=("https://projects.universe-factory.net/attachments/download/75/${pkgname}-${pkgver}.tar.xz")
sha512sums=('adb5849ed121038b4e32441f6644ffe441da01aeac33a85f300b795f0bcc5c6dcacb1cbd223580c3934aa6d97ddbae3d6bbd5f0ba020d64096c611667ff44c49')

build() {
  mkdir -p "$srcdir/build-${pkgver}"
  cd "$srcdir/build-${pkgver}"

  opts=''
  [ $ENABLE_OPENSSL -eq 0 ] || opts="${opts} -D ENABLE_OPENSSL=ON"

  cmake -D CMAKE_BUILD_TYPE=RELEASE -D CMAKE_INSTALL_PREFIX=/usr -D ENABLE_SYSTEMD=ON -D ENABLE_LTO=ON ${opts} "../${pkgname}-${pkgver}"
  make
}

package() {
  cd "$srcdir/build-${pkgver}"
  make install DESTDIR="${pkgdir}"

  install -dm755 "${pkgdir}/etc/fastd/"

  install -Dm644  "$srcdir/${pkgname}-${pkgver}/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/doc/fastd@.service" "${pkgdir}/usr/lib/systemd/system/fastd@.service"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/doc/fastd.1" "${pkgdir}/usr/share/man/man1/fastd.1"
}
