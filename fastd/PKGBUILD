pkgname=fastd
arch=(i686 x86_64)
pkgver=11
pkgrel=1
pkgdesc="Fast and secure tunneling daemon"

# Enable for fast AES implementations
USE_OPENSSL=0

makedepends=('cmake' 'bison' 'libnacl' 'pkg-config')
depends=('glibc' 'libuecc>=4')

[ $USE_OPENSSL -eq 0 ] || depends=(${depends[@]} 'openssl')

url="https://projects.universe-factory.net/projects/fastd"
license=('BSD')
source=("https://projects.universe-factory.net/attachments/download/72/${pkgname}-${pkgver}.tar.xz")
md5sums=('bdd89fc171c4d961169ff231c1198f12')

build() {
  mkdir -p "$srcdir/build-${pkgver}"
  cd "$srcdir/build-${pkgver}"

  opts=''
  [ $USE_OPENSSL -eq 0 ] || opts="${opts} -D USE_OPENSSL=ON"

  cmake -D CMAKE_BUILD_TYPE=RELEASE -D CMAKE_INSTALL_PREFIX=/usr ${opts} "../${pkgname}-${pkgver}"
  make
}

package() {
  cd "$srcdir/build-${pkgver}"
  make install DESTDIR="${pkgdir}"

  install -dm755 "${pkgdir}/etc/fastd/"

  install -dm755 "${pkgdir}/usr/lib/systemd/system/"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/examples/fastd@.service" "${pkgdir}/usr/lib/systemd/system/"
}
