# Maintainer: Ricardo Vieira <ricardo.vieira@tecnico.ulisboa.pt>

pkgname=budgie-desktop-git
pkgver=7
pkgrel=1
pkgdesc="Simple GTK3 desktop experience"
arch=('any')
url="http://evolve-os.com/"
license=('GPLv2')
depends=('gtk3' 'upower' 'libwnck3' 'gnome-menus' 'gnome-settings-daemon' 'libpeas' 'mutter' 'desktop-file-utils' 'libpulse' 'vala' 'libgee')
makedepends=('autoconf' 'git')
provides=('budgie-desktop')
install=budgie-desktop.install
source=("$pkgname"::'git+https://github.com/evolve-os/budgie-desktop.git#branch=master'
			'budgie-desktop.install')
noextract=()
md5sums=('SKIP'
			'86768d3e91e3d83e766f18e5e3cd2104')
pkgver() {
	cd "$srcdir/$pkgname"
	git describe | sed "s/-/./g" | sed 's/^.//'
}

prepare() {
	cd "$srcdir/$pkgname"
	git submodule init
	git submodule update
}

build() {
	cd "$srcdir/$pkgname"
	autoreconf --force --install
	./configure --prefix=/usr
	make
}

check() {
	cd "$srcdir/$pkgname"
	make -k check
}

package() {
	cd "$srcdir/$pkgname"
	make DESTDIR="$pkgdir/" install
}
