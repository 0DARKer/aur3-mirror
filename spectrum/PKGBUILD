# Maintainer: Sonny Piers <sonny.piers@gmail.com>
# Contributor: Tim Besard <tim.besard@gmail.com>

pkgname=spectrum
pkgver=1.4.7
pkgrel=3
pkgdesc="XMPP libpurple transport/gateway."
arch=('i686' 'x86_64')
url="http://spectrum.im"
license=('GPL')
depends=('gloox' 'libpurple-minimal' 'poco' 'python2')
replaces=('spectrum-git')
makedepends=('cmake' 'gettext')
optdepends=('mysql: MySQL database backend support'
            'postgresql-libs: PostgreSQL database backend support'
            'sqlite3: SQLite database backend support'
            'libevent: support for libevent'
            'imagemagick: support for avatars synchronization with legacy networks'

            # Protocols plugins
            'mbpurple-svn: microblogging services support (like Twitter or identi.ca/StatusNet)'
            'msn-pecan: a different implementation of the MSN protocol for libpurple'
            'pidgin-facebookchat: Facebook support, with more features than Facebook XMPP server'
            'pidgin-sipe: SIPE protocol support'
)
backup=('etc/conf.d/spectrum')
source=('http://spectrum.im/attachments/download/37/spectrum-1.4.7.tar.gz'
        'logrotate.spectrum'
	'poco_cmake.patch')
md5sums=('a2104c4e5e8842dcdc053dd042f29c1e'
         '4f9cbfe3878fe5f4137d1bb5acc9e850'
	'1e8bb3188ed26dd22561d133767666ba')

install='spectrum.install'


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i ../poco_cmake.patch || return 1

  msg "Starting make..."
  
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make

  cd spectrumctl
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Various needed folders -- correct permissions are set in spectrum.install
  install -d "${pkgdir}/etc/spectrum"
  install -d "${pkgdir}/var/log/spectrum"
  install -d "${pkgdir}/var/run/spectrum"
  install -d "${pkgdir}/var/lib/spectrum"

  # Install binaries and Python control script
  make DESTDIR="${pkgdir}" install

  cd spectrumctl
  python2 setup.py install --root="${pkgdir}"

  # Install initscript and logrotate config
  install -Dm755 "${srcdir}/${pkgname}-${pkgver}/initscripts/archlinux/spectrum-rc.d"   "${pkgdir}/etc/rc.d/spectrum"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/initscripts/archlinux/spectrum-conf.d" "${pkgdir}/etc/conf.d/spectrum"
  install -Dm644 "${srcdir}/logrotate.spectrum"                                      "${pkgdir}/etc/logrotate.d/spectrum"

  # Patch to use python2
  sed -i -e "s|#!/usr/bin/python|#!/usr/bin/python2|" "${pkgdir}/usr/bin/spectrumctl"
}
