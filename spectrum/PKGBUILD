# Maintainer: Sonny Piers <sonny.piers@gmail.com>
# Contributor: Tim Besard <tim.besard@gmail.com>
# Contributor: miniature <miniature@archlinux.us>

pkgname=spectrum
pkgver=1.4.8
pkgrel=2
pkgdesc="XMPP libpurple transport/gateway."
arch=('i686' 'x86_64')
url="http://spectrum.im"
license=('GPL')
depends=('gloox' 'libpurple-minimal' 'poco' 'python2' libev)
replaces=('spectrum-git')
makedepends=('cmake' 'gettext')
optdepends=('mysql: MySQL database backend support'
            'postgresql-libs: PostgreSQL database backend support'
            'sqlite3: SQLite database backend support'
            'libevent: support for libevent'
            'imagemagick: support for avatars synchronization with legacy networks'

            # Protocols plugins
            'mbpurple-svn: microblogging services support (like Twitter or identi.ca/StatusNet)'
            'msn-pecan: a different implementation of the MSN protocol for libpurple'
            'pidgin-facebookchat: Facebook support, with more features than Facebook XMPP server'
            'pidgin-sipe: SIPE protocol support'
)
backup=('etc/conf.d/spectrum')
source=('http://spectrum.im/attachments/download/43/spectrum-1.4.8.tar.gz'
        'logrotate.spectrum')
md5sums=('edd88e1e70f86fe9aa4b43d787529a47'
	'4f9cbfe3878fe5f4137d1bb5acc9e850')

install='spectrum.install'


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg "Starting make..."
  
  sed -i -e 's/${PYTHON_EXECUTABLE}/python2/g' CMakeLists.txt
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make

  cd spectrumctl
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Various needed folders -- correct permissions are set in spectrum.install
  install -d "${pkgdir}/etc/spectrum"
  install -d "${pkgdir}/var/log/spectrum"
  install -d "${pkgdir}/var/run/spectrum"
  install -d "${pkgdir}/var/lib/spectrum"

  # Install binaries and Python control script
  make DESTDIR="${pkgdir}" install

  cd spectrumctl
  python2 setup.py install --root="${pkgdir}"

  # Install initscript and logrotate config
  install -Dm755 "${srcdir}/${pkgname}-${pkgver}/initscripts/archlinux/spectrum-rc.d"   "${pkgdir}/etc/rc.d/spectrum"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/initscripts/archlinux/spectrum-conf.d" "${pkgdir}/etc/conf.d/spectrum"
  install -Dm644 "${srcdir}/logrotate.spectrum"                                      "${pkgdir}/etc/logrotate.d/spectrum"

  # Patch to use python2
  sed -i -e "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "${pkgdir}/usr/bin/spectrumctl"
}
