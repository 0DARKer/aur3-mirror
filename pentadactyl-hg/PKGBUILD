# Maintainer: Patrice Peterson <runiq at archlinux dot us>
# Inspiration: Michael Witten <mfwitten>
# (Original) Inspiration: Gustavo Dutra <mechamo@gustavodutra.com>

pkgname="pentadactyl-hg"
license=(MIT)
pkgver=6858
pkgrel=1
pkgdesc="Vim-like five-fingered interface for Firefox"
arch=(i686 x86_64)
url="http://dactyl.sourceforge.net/pentadactyl"
depends=("firefox>=8.0")
makedepends=("coreutils" "mercurial" "sh" "zip" "unzip")
provides=("pentadactyl")
conflicts=("pentadactyl")

_hgroot="https://dactyl.googlecode.com"
_hgrepo="hg"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [[ -d "$_hgrepo" ]]; then
    cd "$_hgrepo"
    hg pull -u
    msg "The local files are updated."
  else
    hg clone "$_hgroot" "$_hgrepo"
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_hgrepo-build"
  cp -r "$srcdir/$_hgrepo" "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  make -C pentadactyl xpi
  }

package() {
  cd "$srcdir/$_hgrepo-build"
  local _version_pentadactyl="$(awk -F 'em:version="|"' '/em:version/ {print $2; exit}' $srcdir/$_hgrepo-build/pentadactyl/install.rdf)"
  local _extension_id="$(awk -F 'em:id="|"' '/em:id/ {print $2; exit}' $srcdir/$_hgrepo-build/pentadactyl/install.rdf)"
  local _path_xpi="$srcdir/$_hgrepo-build/downloads/pentadactyl-$_version_pentadactyl.xpi"
  local _path_install="$pkgdir/usr/lib/firefox/extensions/$_extension_id"

  echo $_version_firefox
  install -d "$_path_install"
  unzip -od "$_path_install" "$_path_xpi"
  }

