# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=raul-svn
pkgver=0.8.5.r5335
pkgrel=1
pkgdesc="C++ Realtime Audio Utility Library."
arch=('any')
url="http://drobilla.net/software/raul/"
license=('GPL')
makedepends=('subversion' 'python')
provides=('raul')
conflicts=('raul')

_svnname=raul
source=("svn+http://svn.drobilla.net/lad/trunk/${_svnname}")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_svnname}"
  local rel=`grep "^RAUL_VERSION" wscript | cut -d "'" -f 2`
  local ver="$(svnversion)"
  printf "%s.r%s" "${rel}" "${ver//[[:alpha:]]}"
}

build() {
  cd "${srcdir}/${_svnname}"
  python waf configure --prefix=/usr
  python waf
}

package() {
  cd "${srcdir}/${_svnname}"
  python waf install --destdir="${pkgdir}"
}
