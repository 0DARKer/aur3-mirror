# Maintainer: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Asa Marco <marcoasa90[at]gmail[.]com>
pkgname=kupfer
pkgver=v208
pkgrel=3
pkgdesc="Launcher application written in python. Similar to Gnome-Do / Launchy"
arch=('i686' 'x86_64')
url="http://kaizer.se/wiki/kupfer/"
license=('GPL')
depends=('python2-xdg' 'pygtk' 'python2-dbus'  'python-keybinder' 'python-keyring' 'gtk2>=2.16' 'pygobject>=2.18' 'hicolor-icon-theme' 'shared-mime-info')
optdepends=('gnome-python-desktop: enables all plugins and gnome integration' 'python-wnck: tracks running applications (part of gnome-python-desktop, free of gnome dependencies)' 'realpath: Required for nautilus integration' 'python-gdata: Needed by the gmail plugin')
makedepends=('intltool')
source=(http://kaizer.se/publicfiles/kupfer/$pkgname-$pkgver.tar.xz)
md5sums=('8aba99f4c26989e7c9182d3d782d5806')
install=kupfer.install
build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  export PYTHON="/usr/bin/python2"
  sed -i 's|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|' ./waf
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
  -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
  $(find $srcdir/$pkgname-$pkgver -name '*.py')
  
 ./waf configure --prefix=/usr
 ./waf

}

package() {
  cd "$srcdir/$pkgname-$pkgver"

 ./waf install -f --destdir=$pkgdir
}

