# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com> ([community] package)

pkgname=remmina-git
pkgver=1.0.0.131.g948de6f
pkgrel=1
pkgdesc="A remote desktop client written in GTK+ - git checkout"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/remmina/"
license=('GPL')
depends=('gtk2' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3'
	'libgcrypt' 'libxdmcp' 'libgnome-keyring' 'libvncserver')
makedepends=('git' 'intltool' 'pkgconfig' 'cmake'
	'avahi' 'libxkbfile' 'freerdp-git' 'telepathy-glib')
optdepends=('avahi' 'libxkbfile' 'freerdp-git' 'telepathy-glib')
replaces=('remmina-plugins')
provides=('remmina' "grdc" "grdc=$pkgver" 'remmina-plugins')
conflicts=('remmina' "grdc")
install=remmina.install
source=('remmina::git://github.com/FreeRDP/Remmina'
	'uninitialized.patch')

pkgver() {
	cd remmina/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd remmina/

	# do not install files to builder's home dir
	sed -i -e '/^set(EXTERNAL_TOOLS_DIR/c set(EXTERNAL_TOOLS_DIR "${CMAKE_INSTALL_FULL_DATADIR}/remmina/external_tools")' \
		-e '/^file(MAKE_DIRECTORY/d' remmina/external_tools/CMakeLists.txt

	# fix uninitialized warning/error
	patch -Np1 < ${srcdir}/uninitialized.patch

	# names changed...
	sed -i -e 's/CODEC_ID_REMOTEFX/RDP_CODEC_ID_REMOTEFX/' -e 's/CODEC_ID_NONE/RDP_CODEC_ID_NONE/' remmina-plugins/rdp/rdp_gdi.c

	# header file dropped
	sed -i '/unicode.h/d' remmina-plugins/rdp/rdp_cliprdr.c

	export LDFLAGS="-lX11 -lgdk_pixbuf-2.0 -lgio-2.0 -lgobject-2.0 -lgmodule-2.0 -lgthread-2.0 -lpthread"
	cmake -DWITH_APPINDICATOR=OFF -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd remmina/

	make DESTDIR=${pkgdir} install
	mv ${pkgdir}/usr/lib64 ${pkgdir}/usr/lib
}

sha256sums=('SKIP'
            '6cbd7813f601df8f2ed8d8a27733862393ee0b5ff0db7fa11f91bdd3c8003099')
