# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com> ([community] package)

pkgname=remmina-git
pkgver=1.0.0.r135.ge6f2943
pkgrel=1
pkgdesc="A remote desktop client written in GTK+ - git checkout"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/remmina/"
license=('GPL')
depends=('gtk2' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3'
	'libgcrypt' 'libxdmcp' 'libgnome-keyring' 'libvncserver')
makedepends=('git' 'intltool' 'pkgconfig' 'cmake'
	'avahi' 'libxkbfile' 'freerdp-git' 'telepathy-glib')
optdepends=('avahi' 'libxkbfile' 'freerdp-git' 'telepathy-glib')
replaces=('remmina-plugins')
provides=('remmina' "grdc" "grdc=$pkgver" 'remmina-plugins')
conflicts=('remmina' "grdc")
install=remmina.install
source=('remmina::git://github.com/FreeRDP/Remmina')

pkgver() {
	cd remmina/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

prepare() {
	cd remmina/

	# do not install files to builder's home dir
	sed -i -e '/^set(EXTERNAL_TOOLS_DIR/c set(EXTERNAL_TOOLS_DIR "${CMAKE_INSTALL_FULL_DATADIR}/remmina/external_tools")' \
		-e '/^file(MAKE_DIRECTORY/d' remmina/external_tools/CMakeLists.txt
}

build() {
	cd remmina/

	export LDFLAGS="-lX11 -lgdk_pixbuf-2.0 -lgio-2.0 -lgobject-2.0 -lgmodule-2.0 -lgthread-2.0 -lpthread"
	cmake -DWITH_APPINDICATOR=OFF -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd remmina/

	make DESTDIR=${pkgdir} install
	mv ${pkgdir}/usr/lib64 ${pkgdir}/usr/lib
}

sha256sums=('SKIP')
