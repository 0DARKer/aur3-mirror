# Maintainer: Adam Hani Schakaki <krzd@krzd.net>
pkgname=caribou
pkgver=0.3.2
pkgrel=1
pkgdesc="A text entry and UI navigation application being developed as an alternative to the Gnome On-screen Keyboard."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Caribou"
license=('LGPL')
depends=('pyatspi' 'python2-gconf' 'pygtk' 'python-virtkey' 'pyclutter' 'dbus-python')
makedepends=('gnome-doc-utils')
install=caribou.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.3/$pkgname-$pkgver.tar.xz)
sha256sums=('5683b9c133632232b4a57e6196a579911760250ed16616a3d8c7945a1230b9df')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

  sed -i s'|#!/usr/bin/python|#!/usr/bin/python2|'g ${pkgdir}/usr/bin/caribou

   install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
   gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain caribou ${pkgdir}/etc/gconf/schemas/*.schemas
   rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
