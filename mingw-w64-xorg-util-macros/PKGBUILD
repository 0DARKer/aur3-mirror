pkgname=mingw-w64-xorg-util-macros
pkgver=1.19.0
pkgrel=3
pkgdesc="X.Org Autotools macros (mingw-w64)"
arch=('any')
license=('custom')
url="http://xorg.freedesktop.org"
options=('staticlibs' '!strip' '!buildflags')
depends=("mingw-w64-crt")
source=("http://xorg.freedesktop.org/releases/individual/util/util-macros-1.19.0.tar.gz")
md5sums=("40e1caa49a71a26e0aa68ddd00203717")
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/util-macros-$pkgver"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure 
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/util-macros-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    # mingw-w64-pkg-config does not seem to look inside /usr/${_arch}/share/pkgconfig
    install -d "$pkgdir"/usr/${_arch}/lib/pkgconfig/
    install -Dm644 "$pkgdir"/usr/${_arch}/share/pkgconfig/*.pc "$pkgdir"/usr/${_arch}/lib/pkgconfig/
    rm -fr "$pkgdir/usr/${_arch}/share/pkgconfig"
  done
}

