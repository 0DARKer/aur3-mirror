pkgbase=xorg-server-ubuntu
pkgname=xorg-server-ubuntu
true && pkgname=('xorg-server-ubuntu' 'xorg-server-xephyr-ubuntu' 'xorg-server-xdmx-ubuntu' 'xorg-server-xvfb-ubuntu' 'xorg-server-xnest-ubuntu' 'xorg-server-common-ubuntu' 'xorg-server-devel-ubuntu')
pkgver=1.10.3.901
_ubuntu_ver=1ubuntu3
_ubuntu_ver2=1.10.2.902
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
makedepends=('pixman' 'libx11' 'mesa' 'libgl' 'xf86driproto' 'xcmiscproto' 'xtrans' 'bigreqsproto' 'randrproto' 'inputproto-ubuntu' 'fontsproto' 'videoproto' 'compositeproto' 'recordproto' 'scrnsaverproto' 'resourceproto' 'xineramaproto' 'libxkbfile' 'libxfont' 'renderproto' 'libpciaccess' 'libxv' 'xf86dgaproto' 'libxmu' 'libxrender' 'libxi' 'dmxproto' 'libxaw' 'libdmx' 'libxtst' 'libxres' 'xorg-xkbcomp' 'xorg-util-macros' 'xorg-font-util' 'glproto' 'dri2proto')
options=('!libtool')
source=(${url}/releases/individual/xserver/${pkgbase%-*}-${pkgver}.tar.bz2
        http://archive.ubuntu.com/ubuntu/pool/main/x/${pkgbase%-*}/${pkgbase%-*}_${_ubuntu_ver2}-${_ubuntu_ver}.diff.gz
        randr-compare-all-the-bytes-in-rrpostpendingproperties.patch
        bg-none-revert.patch
        xserver-1.10-pointer-barriers.patch
        autoconfig-nvidia.patch
        xvfb-run
        xvfb-run.1
        10-quirks.conf)
sha512sums=('c81307fe3ffc3a16a2ff730a00cbd7de449d6bc54e6619705303050dd1cb1c3b557c82197989236ed755fa87d14e340454be21bd16a64cc56cbd45a2a68eb614'
            '894260ec79565b33678d87d9c52c443c4beb45e312abd842c5b23c89bee7b7157334e1a0c33371822aefe799fa9eb15d8b8020c3f089b9773d8acb332234ca0e'
            '324fea6bb2e5e6c7c1148e6edcd2a23ee272676e945972128f9f8676bd909463cae8e007da68de47a34a92e5d41ae7a3e79fa98bdf13ad3739655a4fef06a7cb'
            '760286fb03ced96261555c72bd822f8a275a50a10f7b430462245043aeb14b4a9b6782a0317b18f75eca0008daa1edb0445105353998a09a0f3159aacd4603ab'
            '4d66b5f8fcdbf01896f2a05dcfcecfe9e95acf138affee4091c4d479e743015663e950e05e0bc8baa791bc2e917578c1439692182d8641ed4afc5eef1e0ea75b'
            '35ad4b942a2e816eeddf29f749b070fcf50b737b207378e9b85397713b6358da43798c58703a34c6e32e2e91962e0e6eecb7a4fd831b8358d8da9f11f8d99a1c'
            'ca1cda27016f7c269cbdecc45da36255afeef5c1973cc484544f9dfbf56ed1868365c93a4c7f93e3a23e5322f084ec0cdd137e15b43872aae7f0c03040028ce6'
            'de5e2cb3c6825e6cf1f07ca0d52423e17f34d70ec7935e9dd24be5fb9883bf1e03b50ff584931bd3b41095c510ab2aa44d2573fd5feaebdcb59363b65607ff22'
            '9a1a5568be751435daea720cfc4bad209d62545cc10ea2f49113c41669c8130809a680492256ef331757fe8539d2e0e5e9e67a36f7c48c8d92d9b3e957d28fa2')

build() {
  cd "${srcdir}"
  patch -Np1 -i "${srcdir}/${pkgbase%-*}_${_ubuntu_ver2}-${_ubuntu_ver}.diff"

  cd "${srcdir}/${pkgbase%-*}-${pkgver}"
  # Upstream post-release commit
  patch -Np1 -i "${srcdir}/randr-compare-all-the-bytes-in-rrpostpendingproperties.patch"

  # Add pointer barrier support, patch from Fedora
  patch -Np1 -i "${srcdir}/xserver-1.10-pointer-barriers.patch"

  # Patches from ~ajax/xserver xserver-next branch
  patch -Np1 -i "${srcdir}/bg-none-revert.patch"

  # Use nouveau/nv/nvidia drivers for nvidia devices
  patch -Np1 -i "${srcdir}/autoconfig-nvidia.patch"

  # Ubuntu patches for utouch
  patch -Np1 -i "${srcdir}/debian/patches/220_xi21_always_deliver_raw_events.diff"
  patch -Np1 -i "${srcdir}/debian/patches/500_xi2.1.patch"
  patch -Np1 -i "${srcdir}/debian/patches/501_xf86CoordinatesToWindow.patch"
  patch -Np1 -i "${srcdir}/debian/patches/502_gestures-extension.patch"
  patch -Np1 -i "${srcdir}/debian/patches/503_fix_masked_transformed_valuators.patch"
  patch -Np1 -i "${srcdir}/debian/patches/504_fix_no_coords.patch"

  autoreconf
  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts \
      --enable-gestures
  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-common-ubuntu() {
  pkgdesc="Xorg server common files"
  depends=('xkeyboard-config' 'xorg-xkbcomp' 'xorg-setxkbmap' 'xorg-fonts-misc')
  provides=('xorg-server-common')
  conflicts=('xorg-server-common')

  cd "${srcdir}/${pkgbase%-*}-${pkgver}"
  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-common"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xorg-server-common"
  
  make -C xkb DESTDIR="${pkgdir}" install-data

  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m644 doc/man/Xserver.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/lib/xorg"
  install -m644 dix/protocol.txt "${pkgdir}/usr/lib/xorg/"
}

package_xorg-server-ubuntu() {
  pkgdesc="Xorg X server"
  depends=(libxdmcp libxfont udev libpciaccess libdrm pixman libgcrypt libxau xorg-server-common xf86-input-evdev)
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  provides=('x-server' 'xorg-server')
  conflicts=('xorg-server')
  groups=('xorg')

  cd "${srcdir}/${pkgbase%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  install -m644 "${srcdir}/10-quirks.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  rmdir "${pkgdir}/usr/share/X11"

  # Needed for non-mesa drivers, libgl will restore it
  mv "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" \
     "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.xorg"

  rm -rf "${pkgdir}/var"

  rm -f "${pkgdir}/usr/share/man/man1/Xserver.1"
  rm -f "${pkgdir}/usr/lib/xorg/protocol.txt"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server/COPYING"

  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/share/aclocal"
}

package_xorg-server-xephyr-ubuntu() {
  pkgdesc="A nested X server that runs as an X application"
  depends=(libxfont libgl libgcrypt libxv pixman xorg-server-common)
  provides=('xorg-server-xephyr')
  conflicts=('xorg-server-xephyr')

  cd "${srcdir}/${pkgbase%-*}-${pkgver}/hw/kdrive"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xephyr"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xephyr/COPYING"
}

package_xorg-server-xvfb-ubuntu() {
  pkgdesc="Virtual framebuffer X server"
  depends=(libxfont libxdmcp libxau libgcrypt pixman xorg-server-common)
  provides=('xorg-server-xvfb')
  conflicts=('xorg-server-xvfb')

  cd "${srcdir}/${pkgbase%-*}-${pkgver}/hw/vfb"
  make DESTDIR="${pkgdir}" install

  install -m755 "${srcdir}/xvfb-run" "${pkgdir}/usr/bin/"
  install -m644 "${srcdir}/xvfb-run.1" "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xvfb"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xvfb/COPYING"
}

package_xorg-server-xnest-ubuntu() {
  pkgdesc="A nested X server that runs as an X application"
  depends=(libxfont libxext libgcrypt pixman xorg-server-common)
  provides=('xorg-server-xnest')
  conflicts=('xorg-server-xnest')

  cd "${srcdir}/${pkgbase%-*}-${pkgver}/hw/xnest"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xnest"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xnest/COPYING"
}

package_xorg-server-xdmx-ubuntu() {
  pkgdesc="Distributed Multihead X Server and utilities"
  depends=(libxfont libxi libgcrypt libxaw libxrender libdmx libxfixes pixman xorg-server-common)
  provides=('xorg-server-xdmx')
  conflicts=('xorg-server-xdmx')

  cd "${srcdir}/${pkgbase%-*}-${pkgver}/hw/dmx"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-xdmx"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-xdmx/COPYING"
}

package_xorg-server-devel-ubuntu() {
  pkgdesc="Development files for the X.Org X server"
  depends=(xproto randrproto renderproto xextproto inputproto kbproto fontsproto videoproto dri2proto xineramaproto xorg-util-macros pixman libpciaccess)
  provides=('xorg-server-devel')
  conflicts=('xorg-server-devel')

  cd "${srcdir}/${pkgbase%-*}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/bin"
  rm -rf "${pkgdir}/usr/share/man"
  rm -rf "${pkgdir}/usr/share/doc"
  rm -rf "${pkgdir}/usr/share/X11"
  rm -rf "${pkgdir}/usr/lib/xorg"
  rm -rf "${pkgdir}/var"

  install -m755 -d "${pkgdir}/usr/share/licenses/xorg-server-devel"
  ln -sf ../xorg-server-common/COPYING "${pkgdir}/usr/share/licenses/xorg-server-devel/COPYING"
}
