# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.9.15
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
license=('MIT')
conflicts=('syncthing')
provides=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')

case "$CARCH" in
	armv6h) _pkgarch="armv6"
		sha1sums+=('8b362072e880ef681b03ad2a277a1a2bad3f1f29')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('21918e2cce7dc38f6aaeba4dfc05af678b14176f')
		;;
	i686) _pkgarch="386"
		sha1sums+=('a894f25fcd0ea11d90e4117cc8c0816dbfe9eca7')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('f22e2fa042ecf9c5d3a26a36524e38aef37f05a2')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "../syncthing.1" "${pkgdir}/usr/share/man/man1/syncthing.1"
}
