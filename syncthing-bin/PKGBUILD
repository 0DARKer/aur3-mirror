# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.4
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL')
provides=('syncthing=0.10.4')
conflicts=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')

case "$CARCH" in
	arm) _pkgarch="armv5"
		sha1sums+=('fdf758029b461483b9066d9b49e4d4925b27c37f')
		;;
	armv6h) _pkgarch="armv6"
		sha1sums+=('0e8f29f09eae19f6cf070d6d1cd5a2c603a87d19')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('37ca885e537966a5c03b49a63f14d1bed86323fe')
		;;
	i686) _pkgarch="386"
		sha1sums+=('bb98a5fb09bb5b1a02b0f985de3b7d2a35243eec')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('2c00a8a9e992aebf154b2e199509486d4ffb9168')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "../syncthing.1" "${pkgdir}/usr/share/man/man1/syncthing.1"
}
