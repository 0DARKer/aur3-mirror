# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.14
pkgrel=2
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL')
provides=('syncthing=0.10.14')
conflicts=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          '2d3870eff21ad0724a48ba93e87064686693e3b9'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')
validpgpkeys=('7817E3FF578EEEE19F64D40C445E52EAB1BD4E40')  # jason ryan
 

case "$CARCH" in
	arm) _pkgarch="armv5"
		sha1sums+=('2e6b87097209d73bbbed8dcee08266c7753daab0')
		;;
	armv6h) _pkgarch="armv6"
		sha1sums+=('aacbcb53831764d19cb943169698adfecea644b3')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('c0312e4a835188430ff8d95e383adb54df25f3a1')
		;;
	i686) _pkgarch="386"
		sha1sums+=('c2aa440a8927b0d952aacc57a47714e9a50ea267')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('6be8d36b8bae58278c424628f7a779c8049b1d54')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 ${_realname} "${pkgdir}/usr/bin/${_realname}"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../${_realname}@.service" "${pkgdir}/usr/lib/systemd/system/${_realname}@.service"
    install -D -m 644 "../${_realname}.service" "${pkgdir}/usr/lib/systemd/user/${_realname}.service"
    install -D -m 644 "../${_realname}.1" "${pkgdir}/usr/share/man/man1/${_realname}.1"
}
