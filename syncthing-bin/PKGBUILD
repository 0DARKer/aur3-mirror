# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.18
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL')
provides=('syncthing=0.10.18')
conflicts=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          '2d3870eff21ad0724a48ba93e87064686693e3b9'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')
 

case "$CARCH" in
	arm) _pkgarch="armv5"
		sha1sums+=('c54e611b968dc065f17807e8d2a0b11a45e13615')
		;;
	armv6h) _pkgarch="armv6"
		sha1sums+=('bfda91ba6973bf74d9b1d6e349fd2fa56b455e40')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('a5aac3700decbab18e458945bfbe132fd4643cf8')
		;;
	i686) _pkgarch="386"
		sha1sums+=('061981f9f7e84e475356730daeb972fbd9fcb35d')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('85e2426f9c7ac83751e94ac0d05414d20f2a48e7')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 ${_realname} "${pkgdir}/usr/bin/${_realname}"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../${_realname}@.service" "${pkgdir}/usr/lib/systemd/system/${_realname}@.service"
    install -D -m 644 "../${_realname}.service" "${pkgdir}/usr/lib/systemd/user/${_realname}.service"
    install -D -m 644 "../${_realname}.1" "${pkgdir}/usr/share/man/man1/${_realname}.1"
}
