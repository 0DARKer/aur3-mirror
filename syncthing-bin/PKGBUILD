# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.21
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL')
provides=('syncthing=0.10.21')
conflicts=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          '2d3870eff21ad0724a48ba93e87064686693e3b9'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')
 

case "$CARCH" in
	arm) _pkgarch="arm"
		sha1sums+=('4702440a1755e2adb50be0b2ed8f3821bc98aaa9')
		;;
	armv6h) _pkgarch="arm"
		sha1sums+=('4702440a1755e2adb50be0b2ed8f3821bc98aaa9')
		;;
	armv7h) _pkgarch="arm"
		sha1sums+=('4702440a1755e2adb50be0b2ed8f3821bc98aaa9')
		;;
	i686) _pkgarch="386"
		sha1sums+=('ae12eb80337eca93d063d43a250412a5273fd4af')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('641382e9f9c2753dc9db69a3adee4f1cd10d4eaa')
		;;
esac

source+=("https://github.com/syncthing/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 ${_realname} "${pkgdir}/usr/bin/${_realname}"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../${_realname}@.service" "${pkgdir}/usr/lib/systemd/system/${_realname}@.service"
    install -D -m 644 "../${_realname}.service" "${pkgdir}/usr/lib/systemd/user/${_realname}.service"
    install -D -m 644 "../${_realname}.1" "${pkgdir}/usr/share/man/man1/${_realname}.1"
}
