# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.0
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('MIT')
conflicts=('syncthing')
provides=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')

case "$CARCH" in
	arm) _pkgarch="armv5"
		sha1sums+=('be9a16a65fd1c51797c6367239c48a814b2a237c')
		;;
	armv6h) _pkgarch="armv6"
		sha1sums+=('54fed58c8c7fa66ef5d44c944dc3ee97443d28bd')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('8a552afec22325d7ecea6076188cb374cfc6285d')
		;;
	i686) _pkgarch="386"
		sha1sums+=('6de5ffbfcdcd76ecd28da6bdac25a0c39f1536eb')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('f3becf985d8cccb9179e3c962e549a37d542feb1')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "../syncthing.1" "${pkgdir}/usr/share/man/man1/syncthing.1"
}
