# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.9.9
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
license=('MIT')
conflicts=('syncthing')
provides=('syncthing')
install=${_realname}.install

case "$CARCH" in
    armv6h) _pkgarch="armv6" ;;
    armv7h) _pkgarch="armv7" ;;
      i686) _pkgarch="386"   ;;
    x86_64) _pkgarch="amd64" ;;
esac

source=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz"
        "syncthing@.service"
        "syncthing.1")
sha1sums=('6aac2cff1b3725e66efea69df46327701bd17a2f'
          '204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "../syncthing.1" "${pkgdir}/usr/share/man/man1/syncthing.1"
}
