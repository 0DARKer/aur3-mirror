# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.9
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL')
provides=('syncthing=0.10.9')
conflicts=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')

case "$CARCH" in
	arm) _pkgarch="armv5"
		sha1sums+=('730346fcd348f06eaf993d869dae6329b59652b7')
		;;
	armv6h) _pkgarch="armv6"
		sha1sums+=('34fe1a8ffa4e341a8b17d88399086160fa7f8a03')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('0c9a75b2fa80daae9cba3a1294564600dd5db2b5')
		;;
	i686) _pkgarch="386"
		sha1sums+=('675e05909dbdb85e235e759d62ddedf7cc3bce7a')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('edccae208a1bd71d757259f19b158344815f4773')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "../syncthing.1" "${pkgdir}/usr/share/man/man1/syncthing.1"
}
