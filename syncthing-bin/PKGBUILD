# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.9.13
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
license=('MIT')
conflicts=('syncthing')
provides=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')

case "$CARCH" in
	armv6h) _pkgarch="armv6"
		sha1sums+=('d3ca5a5771f18abbe5f9496abb3edd6c4c449d75')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('942a36b991c8b43e946cfa85f1e9f377decaedda')
		;;
	i686) _pkgarch="386"
        sha1sums+=('1b5aaaab5521d9175c86ef1aea2654d4a4ecbc11')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('b5b94f682c72b24ad70845e000f87c58ef746571')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "../syncthing.1" "${pkgdir}/usr/share/man/man1/syncthing.1"
}
