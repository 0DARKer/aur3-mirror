# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.9.18
pkgrel=2
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('MIT')
conflicts=('syncthing')
provides=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')

case "$CARCH" in
	  arm) _pkgarch="armv5"
		sha1sums+=('52cf2fd21c4546e9f4ee83777244f23096aabc41')
		;;
	armv6h) _pkgarch="armv6"
		sha1sums+=('f16d751a6d38827934f6f78950e06b6325b376bb')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('fe3d5b73e20f31f2014cda40aabc336788377da7')
		;;
	i686) _pkgarch="386"
		sha1sums+=('1565742ef03a7ca51ec94bb7abeed6538967cd93')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('544c92818133df7519bebd1e1a16ee6be749348f')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "../syncthing.1" "${pkgdir}/usr/share/man/man1/syncthing.1"
}
