# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.8
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL')
provides=('syncthing=0.10.8')
conflicts=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')

case "$CARCH" in
	arm) _pkgarch="armv5"
		sha1sums+=('f344d5972a810ecccb0c2551270310cdd12358ad')
		;;
	armv6h) _pkgarch="armv6"
		sha1sums+=('6c4015c7ac48b43da83a81296a6b0c8444a25390')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('96f01b07f3bfd4ceaa1c68deefba5dfa5a40c195')
		;;
	i686) _pkgarch="386"
		sha1sums+=('7fd8e6bb32f11d9bf223ebb0a30176e955f1ef8c')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('7cc92257d600f87b68005415a98340c82451e189')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "../syncthing.1" "${pkgdir}/usr/share/man/man1/syncthing.1"
}
