# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.13
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL')
provides=('syncthing=0.10.13')
conflicts=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')

case "$CARCH" in
	arm) _pkgarch="armv5"
		sha1sums+=('64ded1c4413f8d24f37e9f7fc1135315c30de248')
		;;
	armv6h) _pkgarch="armv6"
		sha1sums+=('e0521735c20a45a4577ff88220842231da955845')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('b5d524878216a4504cabd5ea7c58f44e4fcccf9a')
		;;
	i686) _pkgarch="386"
		sha1sums+=('18c9aca5c6b09eeca73ffeeae1a07f00293b63ac')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('99974fe3292e1d823a781376545faab64ec2ce83')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 syncthing "${pkgdir}/usr/bin/syncthing"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../syncthing@.service" "${pkgdir}/usr/lib/systemd/system/syncthing@.service"
    install -D -m 644 "../syncthing.1" "${pkgdir}/usr/share/man/man1/syncthing.1"
}
