# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.23
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL')
provides=('syncthing=0.10.23')
conflicts=('syncthing')
install=${_realname}.install
source=( 'syncthing.1')
sha1sums=('cabf3bf78457ce17057ae39fe3b0009a4aa446c4')
 

case "$CARCH" in
	arm) _pkgarch="arm"
		sha1sums+=('17eea7798b4d4a584b92ebd4547329f51dd4ad9c')
		;;
	armv6h) _pkgarch="arm"
		sha1sums+=('17eea7798b4d4a584b92ebd4547329f51dd4ad9c')
		;;
	armv7h) _pkgarch="arm"
		sha1sums+=('17eea7798b4d4a584b92ebd4547329f51dd4ad9c')
		;;
	i686) _pkgarch="386"
		sha1sums+=('249cb9b0534c042200e74c259c72a3a04169d821')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('de3ab0107590ed79c34c31d71024b34c5b0d8ceb')
		;;
esac

source+=("https://github.com/syncthing/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 ${_realname} "${pkgdir}/usr/bin/${_realname}"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "etc/linux-systemd/system/${_realname}@.service" \
                      "${pkgdir}/usr/lib/systemd/system/${_realname}@.service"
    install -D -m 644 "etc/linux-systemd/user/${_realname}.service" \
                      "${pkgdir}/usr/lib/systemd/user/${_realname}.service"
    install -D -m 644 "../${_realname}.1" "${pkgdir}/usr/share/man/man1/${_realname}.1"
}
