# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=syncthing-bin
_realname=syncthing
pkgver=0.10.17
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing: binary."
url="http://syncthing.net/"
arch=('arm' 'armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL')
provides=('syncthing=0.10.17')
conflicts=('syncthing')
install=${_realname}.install
source=('syncthing@.service' 'syncthing.service' 'syncthing.1')
sha1sums=('204cad3823306c793a14a17a23220e26199160d4'
          '2d3870eff21ad0724a48ba93e87064686693e3b9'
          'cabf3bf78457ce17057ae39fe3b0009a4aa446c4')
 

case "$CARCH" in
	arm) _pkgarch="armv5"
		sha1sums+=('df50828edd8b34d8e4783d4beac152607ef83c49')
		;;
	armv6h) _pkgarch="armv6"
		sha1sums+=('45d7ab15b56c0abbb40f05cd84b96a592f3997f4')
		;;
	armv7h) _pkgarch="armv7"
		sha1sums+=('30bb30515da249f53aecea6a012ac658b41e2914')
		;;
	i686) _pkgarch="386"
		sha1sums+=('02ede3eb6b20e7084fdfc2cf7f9c824e1568a8f9')
		;;
	x86_64) _pkgarch="amd64"
		sha1sums+=('bf31ee83c5fd081d78a5f051c9426179ecd0226d')
		;;
esac

source+=("https://github.com/calmh/${_realname}/releases/download/v${pkgver}/${_realname}-linux-${_pkgarch}-v${pkgver}.tar.gz")

package() {
    cd "${_realname}-linux-${_pkgarch}-v${pkgver}"
    install -D -m 755 ${_realname} "${pkgdir}/usr/bin/${_realname}"
    install -D -m 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${_realname}/LICENSE"
    install -D -m 644 README.txt "${pkgdir}/usr/share/doc/${_realname}/README.md"
    install -D -m 644 "../${_realname}@.service" "${pkgdir}/usr/lib/systemd/system/${_realname}@.service"
    install -D -m 644 "../${_realname}.service" "${pkgdir}/usr/lib/systemd/user/${_realname}.service"
    install -D -m 644 "../${_realname}.1" "${pkgdir}/usr/share/man/man1/${_realname}.1"
}
