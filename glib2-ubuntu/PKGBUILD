# Maintainer: Nishant Rodrigues <nishantjr@ymail.com>
 
pkgname=glib2-ubuntu
pkgver=2.29.4
_uver=0ubuntu1
pkgrel=1
pkgdesc="Common C routines used by GTK+ 2.4 and other libs"
url="http://www.gtk.org/"
arch=(i686 x86_64)
license=("LGPL")
depends=("pcre>=8.02")
provides=("glib2=${pkgver}")
conflicts=("glib2")
makedepends=("python2")
options=('!libtool' '!docs')
source=("http://archive.ubuntu.com/ubuntu/pool/main/g/glib2.0/glib2.0_${pkgver}.orig.tar.bz2"
	"http://archive.ubuntu.com/ubuntu/pool/main/g/glib2.0/glib2.0_${pkgver}-${_uver}.debian.tar.gz"
        "glib2.sh"
        "glib2.csh")
md5sums=('d3a9e4530995aacf79d3573677ffa444'
         'c81a65a29219799da3be59acf86b0057'
         '803017b365bd35dc20b092ce43b8c8c5'
         '90c7b830bef4baf225c2eb8b7ead0cab')

build() {
  
  cd "${srcdir}/glib-${pkgver}"
  patch -Np1 -i $srcdir/debian/patches/0001-Run-gio-tests-with-a-dbus-session.patch
  patch -Np1 -i $srcdir/debian/patches/01_gettext-desktopfiles.patch
  patch -Np1 -i $srcdir/debian/patches/02_gettext-desktopfiles-ubuntu.patch
  patch -Np1 -i $srcdir/debian/patches/03_blacklist-directories.patch
  patch -Np1 -i $srcdir/debian/patches/04_homedir_env.patch
  patch -Np1 -i $srcdir/debian/patches/60_wait-longer-for-threads-to-die.patch
  patch -Np1 -i $srcdir/debian/patches/71_gio_launch_handler.patch
  patch -Np1 -i $srcdir/debian/patches/gio-modules-multiarch-compat.patch
 
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam
  make
}

package() {
  cd "${srcdir}/glib-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/glib2.sh" "${pkgdir}/etc/profile.d/"
  install -m755 "${srcdir}/glib2.csh" "${pkgdir}/etc/profile.d/"

  for _i in "${pkgdir}/etc/bash_completion.d/"*; do
      chmod -x "${_i}"
  done
}
