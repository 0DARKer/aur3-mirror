# Maintainer: Nishant Rodrigues <nishantjr@ymail.com>
 
pkgname=glib2-ubuntu
pkgver=2.28.6
pkgrel=1
pkgdesc="Common C routines used by GTK+ 2.4 and other libs"
url="http://www.gtk.org/"
arch=(i686 x86_64)
license=("LGPL")
depends=("pcre>=8.02")
provides=("glib2=${pkgver}")
conflicts=("glib2")
makedepends=("python2")
options=('!libtool' '!docs')
source=("http://archive.ubuntu.com/ubuntu/pool/main/g/glib2.0/glib2.0_${pkgver}.orig.tar.gz"
	"http://archive.ubuntu.com/ubuntu/pool/main/g/glib2.0/glib2.0_${pkgver}-0ubuntu1.debian.tar.gz"
        "glib2.sh"
        "glib2.csh")
md5sums=('52ea832b9c182a23739932e9b36bb218'
         'f31d72cd159ce5c239f45507cb378563'
         '803017b365bd35dc20b092ce43b8c8c5'
         '90c7b830bef4baf225c2eb8b7ead0cab')


build() {
  rm $srcdir/debian/patches/series
  cd "${srcdir}/glib-${pkgver}"
  for i in $srcdir/debian/patches/*; do patch -p1 < $i
  done
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --with-pcre=system \
      --disable-fam
  make
}

package() {
  cd "${srcdir}/glib-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/glib2.sh" "${pkgdir}/etc/profile.d/"
  install -m755 "${srcdir}/glib2.csh" "${pkgdir}/etc/profile.d/"

  for _i in "${pkgdir}/etc/bash_completion.d/"*; do
      chmod -x "${_i}"
  done
}