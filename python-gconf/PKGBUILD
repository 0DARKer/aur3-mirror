# Maintainer: Hilton Medeiros <medeiros.hilton at gmail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=python-gconf
_pkgname=gnome-python
pkgver=2.28.1
pkgrel=2
pkgdesc="Python bindings for the GConf configuration database system."
arch=('i686' 'x86_64')
license=('LGPL')
depends=('pygtk' 'gconf')
conflicts=('gnome-python')
url="http://www.pygtk.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgname/2.28/$_pkgname-$pkgver.tar.bz2)
md5sums=('a17ad952813ed86f520de8e07194a2bf')
sha256sums=('759ce9344cbf89cf7f8449d945822a0c9f317a494f56787782a901e4119b96d8')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  export PYTHON="python2"
  ./configure \
    --prefix=/usr \
    --disable-allbindings \
    --enable-gconf
  cd gconf && make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver/gconf"
  make DESTDIR="$pkgdir/" install
}
