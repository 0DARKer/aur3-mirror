# Maintainer: flan_suse <windows2linux AT zoho DOT com>
pkgname=xfce-theme-bluebird
_pkgname=Bluebird
pkgver=0.3.2
pkgrel=9
pkgdesc="A light blue Xfce theme, introduced in the release of Xubuntu 10.10"
arch=('any')
url="http://shimmerproject.org/projects/bluebird/"
license=('GPL3')
groups=('xfce-themes-shimmer-collection')
depends=('gtk-engine-murrine')
optdepends=('elementary-icons: matching icon set')
source=("https://download.github.com/shimmerproject-$_pkgname-v$pkgver-0-g1e33efe.tar.gz")
md5sums=('0c213d9914b41e58bf8f7ab13ceb420d')

package() {
    
    # Move into source directory
    cd "${srcdir}"

    # Remove excess files, left over by the original author
    find "$PWD" -name "*~" -exec rm '{}' \;
    rm -rf "shimmerproject-$_pkgname-9276581/releases"
    rm -rf "shimmerproject-$_pkgname-9276581/.gitignore"

    # Create installation directories
    install -d -m755 "${pkgdir}/usr/share/themes/"
    install -d -m755 "${pkgdir}/usr/share/backgrounds/"
    install -d -m755 "${pkgdir}/usr/share/xfce4/backdrops/"

    # Copy the wallpaper to the correct directory, then remove the backdrops folder
    cp -f "shimmerproject-$_pkgname-9276581/backdrops/bluebird-2010-08-1920x1440-notext.png" "${pkgdir}/usr/share/backgrounds/$_pkgname.png"
    rm -rf "shimmerproject-$_pkgname-9276581/backdrops"

    # Install the rest of the theme
    cp -rf "shimmerproject-$_pkgname-9276581" "${pkgdir}/usr/share/themes/$_pkgname"

    # Make a symbolic link inside of the Xfce backdrops directory, for use with Desktop Settings
    cd "${pkgdir}/usr/share/xfce4/backdrops/"
    ln -s "../../backgrounds/$_pkgname.png" "$_pkgname.png"

}


