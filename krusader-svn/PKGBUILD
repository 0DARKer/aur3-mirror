# Contributor: CtHx

pkgname=krusader-svn
pkgver=1145486
pkgrel=1
pkgdesc="Advanced twin panel file manager for KDE - SVN version"
arch=('i686' 'x86_64')
url="http://www.krusader.org/"
license=('GPL')
depends=('kdelibs>=4.3.0' 'kdebase-runtime>=4.3.0')
makedepends=('cmake' 'automoc4' 'subversion')
provides=('krusader')
conflicts=('krusader' 'krusader-kde4-svn')
replaces=('krusader' 'krusader-kde4-svn')
source=()
md5sums=()

_svnroot=svn://anonsvn.kde.org/home/kde/trunk
_svntrunk=$_svnroot/extragear/utils/krusader
_svnmod=krusader

build() {
  cd $srcdir

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "l10n..."

  cd $_svnmod
  _i18nlangs=`svn cat ${_svnroot}/l10n-kde4/subdirs`
  _i18nlangs=`echo $_i18nlangs|sed 's/x-test //'`
  ## custom langs
  #_i18nlangs="ru"
  #_i18nlangs="de fr"

  mkdir -p po
  echo -n "">po/CMakeLists.txt
  echo -n "Downloading l10n files: "
  for _lang in ${_i18nlangs} ; do
    _dest=po/${_lang}
    mkdir -p ${_dest}
#    svn cat ${_svnroot}/l10n-kde4/${_lang}/messages/extragear-utils/krusader.po 2>/dev/null 1>${_dest}/krusader.po || true
    wget -q -O ${_dest}/krusader.po "http://websvn.kde.org/*checkout*/trunk/l10n-kde4/${_lang}/messages/extragear-utils/krusader.po" || true
    
    if [ -s ${_dest}/krusader.po ] ; then
    echo -n "${_lang} "

    echo "add_subdirectory( ${_lang} )">>po/CMakeLists.txt

    echo "file(GLOB _po_files *.po)">${_dest}/CMakeLists.txt
    echo "GETTEXT_PROCESS_PO_FILES( ${_lang} ALL INSTALL_DESTINATION \${LOCALE_INSTALL_DIR} \${_po_files} )">>${_dest}/CMakeLists.txt
    else
      rm -rf ${_dest}
    fi
  done
  echo ""

  cp CMakeLists.txt CMakeLists.txt.tmp
  
  echo "find_package(Msgfmt REQUIRED)" >> CMakeLists.txt
  echo "find_package(Gettext REQUIRED)" >> CMakeLists.txt
  echo "add_subdirectory( po )" >> CMakeLists.txt


  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf build
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` -DCMAKE_BUILD_TYPE=Release ..
  make  || return 1
  make DESTDIR=$pkgdir install || return 1
  cd .. && rm -rf build
  mv CMakeLists.txt.tmp CMakeLists.txt
  
}
