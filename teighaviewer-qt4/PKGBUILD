# Maintainer: josephgbr <rafael.f.f1@gmail.com>

# NOTE about libgl dependency (same note applies to its 'lib32-' version)
# if you have and use 'mesa',     install 'mesa-libgl'
# if you have and use 'nvidia',   install 'nvidia-libgl'
# if you have and use 'catalyst', install 'catalyst-libgl' (or catalyst-total)
# p.s.: there is no 'libgl' pkg, instead it is provided by the above pkgs

pkgname=teighaviewer-qt4
_altname=teighaviewer
pkgver=4.00.1.0
pkgrel=1
pkgdesc="Application for the visualization of CAD drawing files, QT4 version"
arch=('i686' 'x86_64')
url="http://www.opendesign.com/guestfiles"
license=('custom')
conflicts=('teighaviewer' 'teighaviewer-qt5')
provides=('teighaviewer')

if [[ $CARCH == i686 ]]; then
  depends=('qt4' 'libgl' 'bash' 'hicolor-icon-theme')
elif [[ $CARCH == x86_64 ]]; then
  depends=('lib32-qt4' 'lib32-libgl' 'bash' 'hicolor-icon-theme')
fi
install=${pkgname}.install

_file=TeighaViewer_QT4_lnxX86_4.4dll.deb
source=(http://www.opendesign.com/files/guestdownloads/TeighaViewer/$_file)
md5sums=('4a16b6a46515eda5a6d6b39ce87e0c44')

build() {
  [ ! -d ${_altname}-${pkgver} ] && mkdir ${_altname}-${pkgver}
  tar xf data.tar.gz -C ${_altname}-${pkgver}
}

package() {
  cd ${_altname}-${pkgver}
  
  # persistent application storage (libs, executables, etc.)
  if [ $CARCH == i686 ]; then
    appstoragedir=usr/lib/teighaviewer
  elif [ $CARCH == x86_64 ]; then
    appstoragedir=usr/lib32/teighaviewer
  fi
  for file in $(ls usr/bin/TeighaViewer_${pkgver}/); do
    install -Dm755 usr/bin/TeighaViewer_${pkgver}/${file} \
        "${pkgdir}/${appstoragedir}/${file}"
  done
  
  # binary
  install -Dm755 usr/bin/TeighaViewer \
    "${pkgdir}/usr/bin/teighaviewer"
  sed -e "s#usr/bin/TeighaViewer_${pkgver}#${appstoragedir}#" \
    -i "${pkgdir}/usr/bin/${_altname}"
  
  # desktop item
  install -Dm644 usr/share/applications/TeighaViewer_${pkgver}.desktop \
    "${pkgdir}/usr/share/applications/teighaviewer.desktop"
  sed -e "s#/TeighaViewer#/teighaviewer#" \
    -i "${pkgdir}/usr/share/applications/teighaviewer.desktop"
  
  # hicolor icons
  for icon in $(find usr/share/icons -type f); do
    install -Dm644 ${icon} \
        "${pkgdir}/${icon}"
  done
  
  # copyright
  install -Dm644 usr/share/doc/TeighaViewer/copyright \
    "${pkgdir}/usr/share/licenses/${_altname}/COPYRIGHT"
  
}
