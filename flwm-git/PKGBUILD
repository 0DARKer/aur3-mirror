# Maintainer: Brian Bidulock <bidulock@openss7.org>
pkgname=flwm-git
_pkgname=flwm
pkgver=1.14.r2.ged60e46
pkgrel=1
pkgdesc="Fast Light Window Manager"
arch=('i686' 'x86_64')
url="http://flwm.sourceforge.net/"
license=('GPL')
provides=('flwm')
conflicts=('flwm' 'flwm-tc')
depends=('fltk')
source=("$pkgname::git+https://github.com/bbidulock/flwm.git"
        "flwm.desktop")
md5sums=('SKIP'
         '098123145a64d185f4de68784fd4d1bc')

pkgver() {
  cd $pkgname
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make V=0
}

package() {
  cd $pkgname
  install -Dm0755 flwm "$pkgdir/usr/bin/flwm"
  install -Dm0644 flwm.1 "$pkgdir/usr/share/man/man1/flwm.1"
  install -Dm0644 ../flwm.desktop "$pkgdir/usr/share/xsessions/flwm.desktop"
}

# vim:set ts=2 sw=2 et:
