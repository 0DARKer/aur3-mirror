# Contributor: Flamelab <panosfilip@gmail.com>

pkgname=libkdcraw-git
pkgver=r937.baf426f
pkgrel=1
pkgdesc='A C++ interface used to decode RAW picture'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'libraw')
makedepends=('git' 'automoc4' 'cmake')
conflicts=('libkdcraw')
provides=('libkdcraw')
groups=('digikam-git')
source=('git://anongit.kde.org/libkdcraw')
md5sums=('SKIP')

pkgver() {
  cd libkdcraw
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake  ../libkdcraw -DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}