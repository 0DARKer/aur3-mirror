_tarname=lms
pkgname=intel-${_tarname}
_tarver=8.0.0
_tarrel=7
_tarfullname=${_tarname}-${_tarver}-${_tarrel}
pkgver=${_tarver}.${_tarrel}
pkgrel=1
pkgdesc="Intel Local Manageability Service"
arch=('i686' 'x86_64')
url="https://software.intel.com/en-us/articles/download-the-latest-intel-amt-open-source-drivers/"
license=('BSD')
depends=('gcc-libs')
source=("https://software.intel.com/sites/default/files/${_tarfullname}.tar.gz"
        gcc-47.patch)
md5sums=('3cbd027a0e6e9ced8238478b24cde3c6'
         'a6e645b4abe32bfd7206c246a6b9ae83')

prepare() {
	cd "${_tarfullname}"
	patch -p1 < ../gcc-47.patch
}

build() {
	cd "${_tarfullname}"
	./configure --prefix=/usr \
	            --sbindir=/usr/bin
	make
}

package() {
	cd "${_tarfullname}"
	make DESTDIR="$pkgdir/" install

    rm -r "${pkgdir}/etc"
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
