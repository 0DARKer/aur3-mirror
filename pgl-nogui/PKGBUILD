# Maintainer: Pierre Buard <pierre.buard+aur gmail com>
pkgname=pgl-nogui
pkgver=2.0.4
pkgrel=1
pkgdesc='PeerGuardian helps protect your network by blocking many ranges of aggressive IPs.'
url='http://phoenixlabs.org/'
arch=('i686' 'x86_64')
depends=('start-stop-daemon' 'libnfnetlink' 'libnetfilter_queue' 'iptables' 'wget')
optdepends=('dbus-core'
	    'zlib: to directly load gz compressed blocklists'
	    'p7zip: for blocklists packed as .7z'
	    'unzip: for blocklists packed as .zip')
license=('GPL')
conflicts=('blockcontrol' 'moblock')
source=("http://sourceforge.net/projects/peerguardian/files/PeerGuardian%20Linux/$pkgver/pgl_no-GUI-$pkgver.tar.gz")
md5sums=('2086dc9e233e7c99be5152385e14698a')

build() {
    # Adapt paths to Arch Linux
    sed -i 's|/sbin/start-stop-daemon|/usr/bin/start-stop-daemon|g' $srcdir/pgl-$pkgver/pglcmd/pglcmd.lib
    find $srcdir/pgl-$pkgver -type f -exec sed -i 's|/etc/init.d|/etc/rc.d|g' {} \;

    cd $srcdir/pgl-$pkgver/
    make PREFIX=/usr sysconfdir=/etc localstatedir=/var 
}

package() {
    cd $srcdir/pgl-$pkgver/
    make PREFIX=/usr sysconfdir=/etc localstatedir=/var DESTDIR=$pkgdir install

    # Not needed?
    rm -r $pkgdir/etc/network
}
