# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=faac
pkgname=lib32-$_pkgbasename
pkgver=1.28
pkgrel=4
pkgdesc="FAAC is an AAC audio encoder (32 bit)"
arch=('x86_64')
url="http://www.audiocoding.com/"
license=('GPL' 'custom:FAAC')
depends=('lib32-libmp4v2' 'lib32-glibc' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib' 'lib32-libtool')
options=('!libtool')
source=("http://downloads.sourceforge.net/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz"
	'mp4v2-1.9.patch')
md5sums=('80763728d392c7d789cde25614c878f6'
         '9d52a17e68fa15ca94e88e1457b94b74')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  
  patch -Np1 -i ${srcdir}/mp4v2-1.9.patch
  find . -type f -print0 | xargs -0 sed -i 's/\r//g'
  ./bootstrap
  ./configure --prefix=/usr --with-mp4v2 --libdir=/usr/lib32 \
		--libexecdir=/usr/lib32/${_pkgbasename} CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  
  make DESTDIR=${pkgdir} install  
  rm -rf ${pkgdir}/usr/{bin,include,share}
  install -Dm644 "${srcdir}/${_pkgbasename}-${pkgver}/libfaac/kiss_fft/COPYING" \
				"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
