# Contributor: refujee <refujee@gmail.com>
# Maintainer: sausageandeggs <sausageandeggs at archlinux.us>
pkgname=powder-toy
pkgver=80.3
pkgrel=1
_build=Build-178
pkgdesc="Desktop version of the classic falling sand physics sandbox,simulates air pressure, velocity & heat!"
arch=(i686 x86_64)
depends=('sdl' 'lua' 'fftw' 'bzip2')
url="http://powdertoy.co.uk/"
conflicts=(powder)
license=('GPL3')
source=(http://powdertoy.co.uk/Download/Builds/${_build}/powder-src.zip
        $pkgname.desktop $pkgname.png)
#http://powdertoy.co.uk/Download/powder-$pkgver-src.zip

#Thanks to Jesse Jaara for simplifying the PKGBUILD & his sse script that I massively canibalised

build() {
  unset _xarch _ssever
  cd ${srcdir}
  mkdir -p ${srcdir}/build/obj/elements
  #sed -i 's|VERSION 0|VERSION 1|g' ./includes/defines.h
  sed -i 's|lua5.1/||g' includes/luaconsole.h
  sed -i -e 's|echo python|echo python2|g' \
      -i -e 's|-llua5.1|-llua -lrt|g' Makefile

  if $(cat /proc/cpuinfo | grep -q 'pni');then _ssever="-sse3"
  elif $(cat /proc/cpuinfo | grep -q sse2);then _ssever="-sse2"
  else _ssever="sse"
  fi

  [[ ${CARCH} == "x86_64" ]] && _xarch="-64"
  msg2 "building powder${_xarch}${_ssever}"
  make powder${_xarch}${_ssever}
  mv ${srcdir}/build/{powder${_xarch}${_ssever},binary}
}

package() {
  cd "${srcdir}"
  install -Dm 755 "${srcdir}/build/binary" "${pkgdir}/usr/bin/powder"
  install -Dm 644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm 644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

md5sums=('21dc4f72b4928d613435505eb30c0e62'
         '3d0768fadfcdf1e27a03a192d71cfa84'
         'f7b06b42a0a3793fa219d8e7b62593d1')
