# Contributor:	refujee		<gmail.com: refujee>
# Contributor:	sausageandeggs	<archlinux.us: sausageandeggs>
# Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>

pkgname=powder-toy
_sver=90
_mver=2
_build=322
pkgver=${_sver}.${_mver}
pkgrel=2
pkgdesc="Desktop version of the classic falling sand physics sandbox, simulates air pressure, velocity & heat!"
arch=(i686 x86_64)
depends=('sdl' 'lua' 'fftw' 'bzip2' 'zlib')
makedepends=('python2' 'scons')
url="http://powdertoy.co.uk/"
license=('GPL3')
source=(http://github.com/FacialTurd/The-Powder-Toy/archive/v${_sver}.${_mver}.${_build}.tar.gz
	${pkgname}.desktop ${pkgname}.png lua-5.2-arch.patch)

prepare() {
  cd "${srcdir}/The-Powder-Toy-${_sver}.${_mver}.${_build}"

  #Disable the updates. I cant get the buildsystem to not compile a beta version.
  #Also I do not know the logic behind the generated snapshotids.
  sed 's|//#define I|#define I|' -i src/Config.h

  patch -p0 -i ../lua-5.2-arch.patch
}

build() {
  unset _xarch _ssever
  cd "${srcdir}/The-Powder-Toy-${_sver}.${_mver}.${_build}"

  if $(cat /proc/cpuinfo | grep -q 'pni');then _ssever="sse3"
  elif $(cat /proc/cpuinfo | grep -q sse2);then _ssever="sse2"
  else _ssever="sse"
  fi

  [[ "${CARCH}" == "x86_64" ]] && _xarch="--64bit"

  msg2 "building powder with options with following extra flags ${_xarch} --${_ssever}"
  scons --lin ${_xarch} --release  --${_ssever} --save-version=${_sver} \
	--minor-version=${_mver} --build-number=${_build} ${MAKEFLAGS}

  mv build/{powder*,binary}
}

package() {
  install -Dm 755 "${srcdir}/The-Powder-Toy-${_sver}.${_mver}.${_build}/build/binary" "${pkgdir}/usr/bin/powder-toy"
  install -Dm 644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm 644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

md5sums=('0eec9ce14da705b1525e9c641460cc2c'
         '8901d334c53c04738cbd3518c80fa37c'
         'bb40bf9c2fa3982e2872b5d32de3b006'
         'e80268ba83549ce91d5915afce5e8295')
