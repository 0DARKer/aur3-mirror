# Contributor:	refujee		<gmail.com: refujee>
# Contributor:	sausageandeggs	<archlinux.us: sausageandeggs>
# Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>

pkgname=powder-toy
_sver=83
_mver=9
_build=232
pkgver=${_sver}.${_mver}
pkgrel=2
pkgdesc="Desktop version of the classic falling sand physics sandbox,simulates air pressure, velocity & heat!"
arch=(i686 x86_64)
depends=('sdl' 'lua' 'fftw' 'bzip2')
makedepends=('python2' 'scons')
url="http://powdertoy.co.uk/"
conflicts=(powder)
license=('GPL3')
##At the moment the master branch fails to build on GCC 4.7 and is incompitable with 64bit machines
##So I have created a separate branch wit fixes needed to build on Arch. This is the reason
##for un-oofical tar-ball.
source=(http://huulivoide.pp.fi/Arch/sources/powder-${pkgver}-${_build}.tar.xz
#source=(http://powdertoy.co.uk/Download/Builds/${_build}/powder-src.zip
#source=(http://powdertoy.co.uk/Download/powder-${pkgver}-src.zip
	${pkgname}.desktop ${pkgname}.png)

build() {
  unset _xarch _ssever
  cd "${srcdir}/powder-${pkgver}-${_build}"

  if $(cat /proc/cpuinfo | grep -q 'pni');then _ssever="sse3"
  elif $(cat /proc/cpuinfo | grep -q sse2);then _ssever="sse2"
  else _ssever="sse"
  fi

  [[ "${CARCH}" == "x86_64" ]] && _xarch="--64bit"

  msg2 "building powder with options with following extra flags ${_xarch} --${_ssever}"
  scons --lin ${_xarch} --release --${_ssever} --save-version=${_sver} \
	--minor-version=${_mver} --build-number=${_build} ${MAKEFLAGS}

  mv build/{powder*,binary}
}

package() {
  install -Dm 755 "${srcdir}/powder-${pkgver}-${_build}/build/binary" "${pkgdir}/usr/bin/powder"
  install -Dm 644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm 644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

md5sums=('8a03589c3335ad9b60811a810968e718'
         'a09048c1e650c303395816942c083f30'
         'bb40bf9c2fa3982e2872b5d32de3b006')
