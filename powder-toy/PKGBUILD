# Contributor:	refujee		<gmail.com: refujee>
# Contributor:	sausageandeggs	<archlinux.us: sausageandeggs>
# Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>

pkgname=powder-toy
_sver=84
_mver=3
_build=253
pkgver=${_sver}.${_mver}
pkgrel=2
pkgdesc="Desktop version of the classic falling sand physics sandbox,simulates air pressure, velocity & heat!"
arch=(i686 x86_64)
depends=('sdl' 'lua5.1' 'fftw' 'bzip2')
makedepends=('python2' 'scons')
url="http://powdertoy.co.uk/"
conflicts=(powder)
license=('GPL3')
source=(https://github.com/FacialTurd/The-Powder-Toy/archive/build${_build}.tar.gz
	https://github.com/Huulivoide/The-Powder-Toy/commit/8ad680d9558a88534889bca0330f34d0e046d2cc.patch
	${pkgname}.desktop ${pkgname}.png)

build() {
  unset _xarch _ssever
  cd "${srcdir}/The-Powder-Toy-build${_build}"

  #Disable the updates. I cant get the buildsystem to not compile a beta version.
  #Also I do not know the logicg behind the generated snapshotids.
  sed 's|//#define I|#define I|' -i src/Config.h

  #Search for liblua5.1 before liblua
  patch -Np1 -i ../8ad680d9558a88534889bca0330f34d0e046d2cc.patch

  if $(cat /proc/cpuinfo | grep -q 'pni');then _ssever="sse3"
  elif $(cat /proc/cpuinfo | grep -q sse2);then _ssever="sse2"
  else _ssever="sse"
  fi

  [[ "${CARCH}" == "x86_64" ]] && _xarch="--64bit"

  msg2 "building powder with options with following extra flags ${_xarch} --${_ssever}"
  scons --lin ${_xarch} --release  --${_ssever} --save-version=${_sver} \
	--minor-version=${_mver} --build-number=${_build} \
	${MAKEFLAGS}

  mv build/{powder*,binary}
}

package() {
  install -Dm 755 "${srcdir}/The-Powder-Toy-build${_build}/build/binary" "${pkgdir}/usr/bin/powder"
  install -Dm 644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm 644 "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

md5sums=('42900d344f22787bc4bee182ceba02f5'
         'eee736d6b7653916a2cc5be9a5e765a9'
         'a09048c1e650c303395816942c083f30'
         'bb40bf9c2fa3982e2872b5d32de3b006')
