# Contributor: refujee <refujee@gmail.com>
# Maintainer: sausageandeggs <sausageandeggs at archlinux.us>
pkgname=powder-toy
pkgver=64.3
pkgrel=2
pkgdesc="Desktop version of the classic falling sand physics sandbox,simulates air pressure, velocity & heat!"
arch=(i686 x86_64)
depends=('sdl' 'lua' 'fftw')
url="http://powdertoy.co.uk/"
conflicts=(powder)
license=('GPL2')
source=(http://powdertoy.co.uk/Download/powder-$pkgver-src.zip
        $pkgname.desktop $pkgname.png)

#thanks to Jesse Jaara for his PKGBUILD & sse script that I've massively canibalised
build() {
    unset _xarch _ssever
    cd ${srcdir}
    #sed -i 's|VERSION 0|VERSION 1|g' ./includes/defines.h
    sed -i 's|lua5.1/||g' includes/luaconsole.h
    sed -i -e 's|echo python|echo python2|g' \
    -i -e 's|-llua5.1|-llua|g' Makefile

    #Prefer x86info over /proc/cpuinfo if installed on system
    #/proc/cpuinfo doesen't list all supported features on certain cpus
    if [ -f /usr/bin/x86info ]; then
        if $(x86info -f | grep -q sse3);then _ssever="-sse3"
        elif $(x86info -f | grep -q sse2);then _ssever="-sse2"
        else _ssever="-sse"
        fi
    else
        ##Athlon64 & prescott cpus support sse3, but they don't report it in /proc/cpuinfo
        if $(cat /proc/cpuinfo | grep -q 'Athlon(tm) 64');then _ssever="-sse3"
        elif $(cat /proc/cpuinfo | grep -q 'Phenom(tm)');then _ssever="-sse3"
        elif $(cat /proc/cpuinfo | grep -q 'pni');then _ssever="-sse3"
        elif  $(cat /proc/cpuinfo | grep -q sse3);then _ssever="-sse3"
        elif $(cat /proc/cpuinfo | grep -q sse2);then _ssever="-sse2"
        else _ssever="sse"
        fi
    fi

    [[ ${CARCH} == "x86_64" ]] && _xarch="-64"
    msg2 "building powder${_xarch}${_ssever}"
    make powder${_xarch}${_ssever}

}

package() {
  cd "${srcdir}"

  install -Dm 755 $srcdir/build $pkgdir/usr/bin/powder
  install -Dm 644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm 644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
}

md5sums=('7851533b023bc68fb29d69982ac9b526'
         '3d0768fadfcdf1e27a03a192d71cfa84'
         'f7b06b42a0a3793fa219d8e7b62593d1')
