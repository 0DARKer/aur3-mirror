# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=argus
pkgver=3.7
pkgrel=1
pkgdesc="A system and network monitoring tool"
arch=('i686' 'x86_64')
url="http://argus.tcp4me.com/"
license=('custom:artistic')
depends=('fping' 'perl')
optdepends=('sendmail: Notifications by email'
	    'perl-socket6: IPv6 support'
	    'perl-gd: Graphing support'
	    'perl-dbi: Database testing support'
	    'perl-net-ssleay: Extended SSL testing support'
	    'perl-digest-hmac: HMAC support'
	    'perl-digest-sha1: SHA1 support'
	    'perl-crypt-des: DES support')
makedepends=('perl')
install=argus.install
source=(http://www.tcp4me.com/code/argus-archive/$pkgname-$pkgver.tgz \
	argusd.init argus.runit)

md5sums=('845b0ed052cc7487ace8d0fcff1db5ce'
         '22a0bf5f02551f538a3c0233d39a845e'
         '731fdfa40ad098140a81c44fc8865fdf')
  
build() {
  cd $srcdir/$pkgname-$pkgver
  
  # This Makefile is created by the interactive Configure script and should
  # be regenerated for each new version of argus

  ./Configure --bin_dir=../../pkg/opt/argus/bin --sbin_dir=../../pkg/opt/argus/sbin --lib_dir=../../pkg/opt/argus/lib --data_dir=../../pkg/opt/argus/data --cgi_dir=../../pkg/opt/argus/cgi-bin --www_dir=../../pkg/opt/argus/www
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  
  # Fix broken permissions, the Makefile is crap at this :-(
  chmod 744 $pkgdir/opt/argus/data/ $pkgdir/opt/argus/data/gcache/ $pkgdir/opt/argus/data/locale/de_DE \
                      $pkgdir/opt/argus/data/locale/sp_ar $pkgdir/opt/argus/data/locale/nl_NL \
                      $pkgdir/opt/argus/data/locale/lt_LT $pkgdir/opt/argus/data/perl/ \
                      $pkgdir/opt/argus/data/gdata/
    
  # Fix broken paths :-(
  
  pushd $pkgdir/opt
  
  for _f in argus/lib/picasso \
                argus/lib/graphd \
                argus/lib/conf.pl \
                argus/sbin/argusctl \
                argus/sbin/argusd \
                argus/bin/argus-config \
                argus/cgi-bin/argusvxml \
                argus/cgi-bin/arguscgi
  do
    sed -i -e 's/\.\.\/\.\.\/pkg//' $_f
  done

  popd

  install -d $pkgdir/etc/rc.d
  install -m0755 ../argusd.init $pkgdir/etc/rc.d
  install -m0755 -D ../argus.runit $pkgdir/etc/sv/argus/run
  install -D Artistic $pkgdir/usr/share/licenses/$pkgname/COPYING
}
