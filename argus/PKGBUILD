# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=argus
pkgver=3.6
pkgrel=1
pkgdesc="A system and network monitoring tool"
arch=('i686' 'x86_64')
url="http://argus.tcp4me.com/"
license=('custom:artistic')
depends=('fping' 'perl')
makedepends=('perl')
install=argus.install
source=(http://www.tcp4me.com/code/argus-archive/$pkgname-$pkgver.tgz \
	Makefile argusd.init argus.runit)

md5sums=('c3ad73e0e0189daca31e42e21faf78da'
         '5dfbe988c6aed6e63e5514c165c8689f'
         '22a0bf5f02551f538a3c0233d39a845e'
         '731fdfa40ad098140a81c44fc8865fdf')
  
build() {
  cd $srcdir/$pkgname-$pkgver
  
  # This Makefile is created by the interactive Configure script and should
  # be regenerated for each new version of argus
  cp ../Makefile .
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  
  # Fix broken permissions, the Makefile is crap at this :-(
  chmod 744 $pkgdir/opt/argus/var/ $pkgdir/opt/argus/var/gcache/ $pkgdir/opt/argus/var/locale/de_DE \
                      $pkgdir/opt/argus/var/locale/sp_ar $pkgdir/opt/argus/var/locale/nl_NL \
                      $pkgdir/opt/argus/var/locale/lt_LT $pkgdir/opt/argus/var/perl/ \
                      $pkgdir/opt/argus/var/gdata/
    
  # Fix broken paths :-(
  
  pushd $pkgdir/opt
  
  for _f in argus/lib/picasso \
                argus/lib/graphd \
                argus/lib/conf.pl \
                argus/sbin/argusctl \
                argus/sbin/argusd \
                argus/bin/argus-config \
                argus/cgi-bin/argusvxml \
                argus/cgi-bin/arguscgi
  do
    sed -i -e 's/\.\.\/\.\.\/pkg//' $_f
  done

  popd

  install -d $pkgdir/etc/rc.d
  install -m0755 ../argusd.init $pkgdir/etc/rc.d
  install -m0755 -D ../argus.runit $pkgdir/etc/sv/argus/run
  install -D Artistic $pkgdir/usr/share/licenses/$pkgname/COPYING
}
