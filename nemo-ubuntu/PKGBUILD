# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Original Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ner0

pkgname=nemo-ubuntu
_ppa_ver=2.2.1
_ppa_rel=0~webupd8~trusty2
pkgver=2.2.1
pkgrel=1
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('libexif' 'gvfs' 'dconf' 'desktop-file-utils' 'exempi' 'python2'
         'gnome-icon-theme' 'libnotify' 'libxml2'
         'cinnamon-translations')
makedepends=('gtk-doc' 'gobject-introspection' 'intltool' 'gnome-common' 'python2-gobject' 'python2-polib')
provides=("nemo=${pkgver}")
conflicts=('nemo')
options=('!emptydirs')
install=nemo.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/nemo/tarball/$pkgver"
        "http://ppa.launchpad.net/webupd8team/nemo/ubuntu/pool/main/n/nemo/nemo_${_ppa_ver}-${_ppa_rel}.debian.tar.xz")
sha512sums=('7f78fbdbcec16576caf67d66a610c20377925dbb3c827bfba794fa6b34a1478c33b70ac249091c8d2a9786bc0e22d8991f68cabbc8a78860ab07b2ef1c944d86'
            '004c0cca62b70e90362f7979871f854e95c3a43ab4c883dff90aebac6d1dd94195b7477ca852e895dcec6f52e7f2ddd58f13a10f8fd56c6a7c874a9f8bd6db6a')

prepare() {
  cd linuxmint-nemo-*

  # Disable patches
  sed -i '/15_use_ubuntu_help.patch/d' ../debian/patches/series

  for i in $(grep -v '#' ../debian/patches/series); do
    patch -p1 -i ../debian/patches/${i}
  done

  # Python2 fix
  sed -i 's/bin\/python/bin\/python2/g' files/usr/share/nemo/actions/myaction.py
  find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Fix desktop style with Adwaita theme
  sed -i 's/add_class (\(.*\), "nemo-/add_class (\1, "nautilus-/' \
    libnemo-private/nemo-icon-{canvas-item,container}.c

  # Fix build
  sed -i '/AC_SUBST(DISABLE_DEPRECATED_CFLAGS)/d' configure.in

  # Rename 'Files' app name to avoid having the same as nautilus
  sed -i 's/^Name\(.*\)=.*/Name\1=Nemo/' data/nemo.desktop.in.in
}

build() {
  cd linuxmint-nemo-*

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-update-mimedb \
      --disable-tracker \
      --disable-gtk-doc-html \
      --disable-schemas-compile
  make
}

package() {
  cd linuxmint-nemo-*

  make DESTDIR="$pkgdir/" install
}
