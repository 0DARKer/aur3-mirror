# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from parole package)
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: TDY <tdy@gmx.com>

_pkgname=parole
pkgname=$_pkgname-git
pkgver=20121112
pkgrel=1
pkgdesc="A modern simple media player based on the GStreamer framework"
arch=(i686 x86_64)
url=http://goodies.xfce.org/projects/applications/$_pkgname
license=(GPL)
depends=(desktop-file-utils gstreamer0.10-base libnotify libxfce4ui taglib)
makedepends=(git xfce4-dev-tools)
provides=($_pkgname=0.3.0.3)
conflicts=($_pkgname)
install=$_pkgname.install

_gitroot=git://git.xfce.org/apps/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib \
        --enable-taglib \
        --enable-libnotify
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
