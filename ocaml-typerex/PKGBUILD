# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>

pkgname=ocaml-typerex
pkgver=1.0.0rc1
pkgrel=3
pkgdesc="TypeRex is a powerful development environment for OCaml."
arch=("i686" "x86_64")
url=("http://www.typerex.org/")
license=("GPLv3")
depends=("emacs" "ocaml")
source=("http://www.typerex.org/pub/typerex-${pkgver}.tar.gz")
md5sums=('df584b011601bbddbc383daedaa80b98')

build() {
  cd "$srcdir/${pkgname/ocaml-/}-${pkgver}"

  ./configure --prefix="${pkgdir}/usr"
  make
  sed "s|${pkgdir}[/]*|/|g" -i emacs.append
}

package() {
  cd "$srcdir/${pkgname/ocaml-/}-${pkgver}"
  extra="${pkgdir}/usr/share"

  make install
  install -d "${extra}/"{licenses,,doc}"/${pkgname}"
  install -m 0644 -t "${extra}/licenses/${pkgname}" LICENSE*
  install -m 0644 -t "${extra}/${pkgname}"          emacs.append
  install -m 0644 -t "${extra}/doc/${pkgname}"      docs/user-manual/*.pdf
}
