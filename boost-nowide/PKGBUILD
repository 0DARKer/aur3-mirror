# Maintainer: naelstrof <naelstrof@gmail.com>

# This PKGBUILD is modified from Arch's official Boost PKGBUILD to build the Boost.nowide library.

pkgname=boost-nowide
pkgdesc="Boost.Nowide is a library implemented by Artyom Beilis that make cross platform Unicode aware programming easier."
depends=('boost')
pkgver=1.0
_boostbase=boost
_boostver=1_50_0
pkgrel=2
arch=('i686' 'x86_64')
url="http://cppcms.com/files/nowide/html/"
makedepends=('bzip2' 'boost')
source=(http://downloads.sourceforge.net/sourceforge/${_boostbase}/${_boostbase}_${_boostver}.tar.gz
        http://cppcms.com/files/nowide/nowide.zip)
license=('custom')
options=('!ccache')
md5sums=('dbc07ab0254df3dda6300fd737b3f264'
         '05869af83b7f72ef310e690ca2444078')

_stagedir="${srcdir}/stagedir"

build() {
  cp -a ${srcdir}/nowide/boost ${srcdir}/${_boostbase}_${_boostver}
  cp -a ${srcdir}/nowide/libs ${srcdir}/${_boostbase}_${_boostver}

  # build libs
  cd "${srcdir}/${_boostbase}_${_boostver}"

  bjam \
      release debug-symbols=off \
      runtime-link=shared link=shared,static \
      cflags=-fno-strict-aliasing \
      --prefix="${_stagedir}" \
      --with-nowide \
      ${MAKEFLAGS} \
      install
}

package()
{
    install -dm 755 "${pkgdir}"/usr/include/boost
    install -dm 755 "${pkgdir}"/usr/lib
    # headers/source files
    cp -r "${_stagedir}"/include/boost/nowide "${pkgdir}"/usr/include/boost

    # static libs
    cp -r "${_stagedir}"/lib/*nowide* "${pkgdir}"/usr/lib/
}
