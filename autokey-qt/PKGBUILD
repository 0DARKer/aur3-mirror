# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkgname=autokey
pkgname=$_pkgname-qt
pkgver=0.71.1
pkgrel=2
pkgdesc="A text expansion/replacement and hotkey utility for Linux and X11. Qt version."
arch=('any')
url="http://code.google.com/p/autokey/"
license=('GPL3')
depends=('python-xlib' 'kdebindings-python' 'python2-qscintilla' 'wmctrl')
conflicts=('autokey' 'autokey-gtk')
provides=('autokey')
install="$pkgname.install"
source=("http://autokey.googlecode.com/files/${_pkgname}_${pkgver}.tar.gz")
md5sums=('81cd30dfcd3b94fcab4a6a675caeb445')

build() {
  cd "$srcdir/build"

  # Fix for python2
  sed -i "s_\(env python\).*_\12_" `grep -rl "env python" .`
}

package() {
  cd "$srcdir/build"

  python2 setup.py install --prefix=/usr --root="$pkgdir"

  install -Dm 755 debian/autokey-common.init "$pkgdir/etc/rc.d/autokey"

  # Remove unneeded Gtk stuff
  rm -rf "$pkgdir"/usr/lib/python2.6/site-packages/autokey/gtk*
  rm "$pkgdir"/usr/share/applications/autokey-gtk.desktop
  rm "$pkgdir"/usr/share/man/man1/autokey-gtk.1
  rm "$pkgdir"/usr/bin/autokey-gtk
}
