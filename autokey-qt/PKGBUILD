# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=autokey
pkgname=$_pkg-qt
pkgver=0.71.3
pkgrel=1
pkgdesc="A text expansion/replacement and hotkey utility for Linux and X11. Qt version."
arch=('any')
url="http://code.google.com/p/autokey/"
license=('GPL3')
depends=('python-xlib' 'kdebindings-python' 'python2-qscintilla' 'wmctrl')
conflicts=("$_pkg" "$_pkg-gtk")
provides=("$_pkg")
install="$pkgname.install"
source=("http://autokey.googlecode.com/files/${_pkg}_${pkgver}.tar.gz")
md5sums=('4822c89beff6a1ce4c70e2caf65e516b')

build() {
  cd "$srcdir/build"

  # Fix for python2
  sed -i "s_\(env python\).*_\12_" `grep -rl "env python" .`
}

package() {
  cd "$srcdir/build"

  python2 setup.py install --prefix=/usr --root="$pkgdir"

  install -Dm 755 debian/autokey-common.init "$pkgdir/etc/rc.d/autokey"

  # Remove unneeded Gtk stuff
  rm -rf "$pkgdir"/usr/lib/python2.7/site-packages/autokey/gtk*
  rm "$pkgdir"/usr/share/applications/autokey-gtk.desktop
  rm "$pkgdir"/usr/share/man/man1/autokey-gtk.1
  rm "$pkgdir"/usr/bin/autokey-gtk
}
