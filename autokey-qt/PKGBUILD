# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=autokey-qt
pkgver=0.90.1
pkgrel=1
pkgdesc="A text expansion/replacement and hotkey utility for Linux and X11. Kde/Qt GUI"
arch=('any')
url="http://code.google.com/p/autokey/"
license=('GPL3')
depends=("autokey>=$pkgver" 'kdebindings-python2')
provides=('autokey-frontend')
install="autokey-frontend.install"
source=("http://autokey.googlecode.com/files/autokey_$pkgver.tar.gz")
md5sums=('5124ea39bb0adbcd5db555097d4f2a28')

_python2_fix() {
  sed -i "s_env python_&2_" \
    `grep -rl "env python" "$pkgdir"`
}

package() {
  cd "$srcdir/autokey-$pkgver"

  # bin
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # man
  install -Dm644 doc/man/$pkgname.1 \
     "$pkgdir/usr/share/man/man1/$pkgname.1"

  # libs
  install -d "$pkgdir/usr/lib/python2.7/site-packages/autokey"
  cp -a src/lib/qt{ui,app.py} \
    "$pkgdir/usr/lib/python2.7/site-packages/autokey"

  # desktop file
  install -Dm644 config/$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # KDE4 apps
  install -Dm644 config/autokeyui.rc \
    "$pkgdir/usr/share/kde4/apps/autokey/autokeyui.rc"

  _python2_fix
}