# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=autokey-qt
pkgver=0.90.4
pkgrel=1
pkgdesc="A text expansion/replacement and hotkey utility for Linux and X11. Kde/Qt GUI"
arch=('any')
url="http://code.google.com/p/autokey/"
license=('GPL3')
depends=('kdebindings-python2')
provides=("autokey=$pkgver" 'autokey-frontend')
install="autokey-qt.install"
source=("http://autokey.googlecode.com/files/autokey-$pkgver.tar.gz")
md5sums=('c2a74ddc2cb06c9798b7fa57fd65019a')

_python2_fix() {
  sed -i "s_env python_&2_" \
    `grep -rl "env python" "$pkgdir"`
}

package() {
  cd "$srcdir/autokey-$pkgver"
  python2 setup.py install --root="$pkgdir"

  # rm gtk stuff
  rm "$pkgdir/usr/bin/autokey-gtk"
  rm "$pkgdir/usr/share/man/man1/autokey-gtk.1"
  rm "$pkgdir/usr/share/applications/autokey-gtk.desktop"
  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/autokey/gtk"*

  _python2_fix
}