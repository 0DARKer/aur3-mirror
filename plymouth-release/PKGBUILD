#
# Maintainer: Padfoot <padfoot@exemail.com.au>
#
# Contributors: Abbradar
#               Zephyr
#               Christian Autermann
#               Biginoz
#               Martin Lee
#               Ricardo Funke
#               PirateJonno
#               lh
#               Cilyan Olowen
#               Shaffer
#               Brcha
#               Lyle Putnam
#               Det
#               Boohbah

pkgname=plymouth-release
_pkgname=plymouth
pkgver=0.8.8
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support"
url="http://cgit.freedesktop.org/cgit/plymouth/"

arch=('i686' 'x86_64')
license=('GPL')

depends=('libdrm' 'pango' 'systemd')
makedepends=('git' 'docbook-xsl')
optdepends=('ttf-dejavu')

options=('!libtool' '!emptydirs')

install=$_pkgname.install

provides=('plymouth')
conflicts=('plymouth')
backup=('etc/plymouth/plymouthd.conf')

source=('http://cgit.freedesktop.org/plymouth/snapshot/plymouth-0.8.8.tar.gz' 
        'arch-logo.png'
        'encrypt_hook'
        'encrypt_install'
        'gdm-plymouth.service'
        'kdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
	    'lxdm-plymouth.service'
	    'lightdm-plymouth.service'
	    'slim-plymouth.service'
	    "$_pkgname.install"
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-quit.service'
        'plymouth-set-default-theme.in.patch'
        'plymouth-update-initrd.patch'
        'plymouth-configure.ac.patch'
        'system-release')
        
md5sums=('SKIP'
         '65fa2763d5c9bb9f80973ea5e6e3db3e'
         '79613b70a0a0be4c33978ef37c30f00a'
         '65eb269910f1b81e3f0970af456394ca'
         '34d0ed7bbcb3d0ea498de24bc17b2ee9'
         '75ec0ac73eb9a1e3f6104df64520d431'
         'ceec65ce58accdb270f3eaa23d794b4a'
         '62c3b4e894330a2ca4c2b8fbcaba083c'
         'e6d573b1913f3aa020cbde3863ee80f0'
         '225b953976e1c824f62ba406a63c18c7'
         '3124cc765b14e873c379f25f0a76b2ac'
         '4f3be343eea9956bbc94d615d5f41def'
         '1ab622d6d96699d98767e668c6cc0f64'
         '7e8930e9855833fcc0952307a9ace06f'
         'f79edbbb30c71b0dbcd102c7dd31660b'
         '6ef414ed2b34fd04e397727e7d1d7347'
         'a6e5941ce861aaa4ee7255f968970ae4'
         'ab8a557d10f74bec5c94ed6cae34729c')

build() {
	cd "$srcdir"/${_pkgname}-${pkgver}
	
	patch -p1 -i $srcdir/plymouth-configure.ac.patch
	patch -p1 -i $srcdir/plymouth-set-default-theme.in.patch
	patch -p1 -i $srcdir/plymouth-update-initrd.patch

	LDFLAGS="$LDFLAGS -ludev" ./autogen.sh --prefix=/usr \
		--exec-prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--sbindir=/usr/bin \
		--enable-systemd-integration \
		--enable-drm \
		--enable-tracing \
		--enable-pango \
		--with-gdm-autostart-file=yes \
		--with-logo=/usr/share/plymouth/arch-logo.png \
		--with-background-color=0x000000 \
		--without-rhgb-compat-link \
		--without-system-root-install

	make
}

package() {
  cd "$srcdir"/${_pkgname}-${pkgver}

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/arch-logo.png"             "$pkgdir/usr/share/plymouth/arch-logo.png"
  install -Dm644 "$srcdir/encrypt_hook"              "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "$srcdir/encrypt_install"           "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "$srcdir/plymouth.initcpio_hook"    "$pkgdir/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "$srcdir/plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"
  install -Dm644 "$srcdir/system-release"            "$pkgdir/etc/system-release"

  # Plymouth<->systemd integration, including units for DMs
  for i in {{gdm,kdm{,-unpatched},lxdm,slim,lightdm}-plymouth,plymouth-quit}.service; do
    install -Dm644 "$srcdir/$i" "$pkgdir/usr/lib/systemd/system/$i"
  done
  
}
