# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Mick Elliot <micke at sfu dot ca>

pkgname=mafft
pkgver=6.850
pkgrel=1
pkgdesc="Multiple alignment program for amino acid or nucleotide sequences"
arch=('i686' 'x86_64')
url="http://mafft.cbrc.jp/alignment/software"
license=('custom')
depends=('ruby')
source=($url/$pkgname-$pkgver-with-extensions-src.tgz)
md5sums=('6afb242e8aeb5f8e1a20ea844ba57a79')

build() {
  cd $srcdir/$pkgname-$pkgver-with-extensions/core
  make -e PREFIX=/usr

  cd ../extensions
  make -e PREFIX=/usr
}

package() {
  cd $srcdir/$pkgname-$pkgver-with-extensions/core
  make PREFIX=$pkgdir/usr install

  cd ../extensions
  make PREFIX=$pkgdir/usr install

  cd ..
  install -Dm644 license $pkgdir/usr/share/licenses/mafft/license
  install -Dm644 license.extensions $pkgdir/usr/share/licenses/mafft/license.extensions

  rm $pkgdir/usr/lib/mafft/*.1
}