# Maintainer: boenki <boenki at gmx dot de>
# Contributor: Mick Elliot <micke at sfu dot ca>

pkgname=mafft
pkgver=6.864
pkgrel=1
pkgdesc="Multiple alignment program for amino acid or nucleotide sequences"
arch=('i686' 'x86_64')
url="http://mafft.cbrc.jp/alignment/software"
license=('custom')
depends=('ruby')
source=($url/$pkgname-$pkgver-with-extensions-src.tgz)
md5sums=('3255f8ff21ae02b56148e69109d8f20b')

build() {
  cd $srcdir/$pkgname-$pkgver-with-extensions/core
  make -e PREFIX=/usr LIBDIR=/usr/lib/mafft

  cd ../extensions
  make -e PREFIX=/usr LIBDIR=/usr/lib/mafft
}

package() {
  cd $srcdir/$pkgname-$pkgver-with-extensions/core
  make PREFIX=$pkgdir/usr LIBDIR=$pkgdir/usr/lib/mafft install

  cd ../extensions
  make PREFIX=$pkgdir/usr LIBDIR=$pkgdir/usr/lib/mafft install

  cd ..
  install -Dm644 license $pkgdir/usr/share/licenses/mafft/license
  install -Dm644 license.extensions $pkgdir/usr/share/licenses/mafft/license.extensions

  rm $pkgdir/usr/lib/mafft/*.1

  ln -sf /usr/lib/mafft/mafft-profile $pkgdir/usr/bin
  ln -sf /usr/lib/mafft/mafft-distance $pkgdir/usr/bin
}
