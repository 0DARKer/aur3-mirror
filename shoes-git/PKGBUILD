# Maintainer: Kuredant <kuredant[at]gmail[dot]com>
# Original contributor: Michael Fellinger <m.fellinger@gmail.com>
# Updated by:
# Tapio Saarinen <admin@bitlong.org>
# Steve Klabnik <steve@steveklabnik.com>
# Michael Fellinger <m.fellinger@gmail.com>

pkgname=shoes-git
_pkgname=shoes
pkgver=20120905
pkgrel=1
pkgdesc="The best little GUI toolkit, for Ruby"
url="http://shoesrb.com/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('ruby' 'giflib' 'gtk2' 'curl' 'portaudio' 'sqlite' 'tcl' 'java-environment')
makedepends=('git')
conflicts=('shoes')
provides=('shoes')
source=("${_pkgname}.desktop")
md5sums=('9cc51078b725e1e500cca95203155bf2')

_gitroot="git://github.com/${_pkgname}/${_pkgname}.git"
_gitname="${_pkgname}"

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server..."
  if [ -d "${_gitname}" ]; then
    cd "${_gitname}"
    git pull origin || return 1
    msg "The local files are updated."
  else
    git clone --depth=1 "${_gitroot}" "${_gitname}" || return 1
    cd "${_gitname}"
    sed -i 's/if File.exists? tp$/if File.exists? tp and not File.directory? tp/' "make/linux/tasks.rb"
  fi
  msg "GIT checkout done or server timeout"

  rake clean && rake
}

package() {
  cd "${srcdir}"

  install -d -m 755 "${pkgdir}/usr/"{"bin","lib/${_pkgname}"}
  find "${_gitname}/dist" -mindepth 1 -maxdepth 1 -and -exec cp -a {} "${pkgdir}/usr/lib/${_pkgname}/" \;

  ln -sf "/usr/lib/${_pkgname}/${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
  install -D -m 644 "${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -D -m 644 "${_gitname}/dist/static/app-icon.png" "${pkgdir}/usr/share/pixmaps/${_pkgname}.png"
  install -D -m 644 "${_gitname}/COPYING" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
