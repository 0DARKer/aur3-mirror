# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban, jjacky, EasySly, Det

pkgname=ejre
_major=7
_minor=10
_build=b18
_pkg="${_major}u${_minor}"
_date=28_nov_2012
pkgver="${_major}.${_minor}"
pkgrel=1
pkgdesc="Java SE Embedded Runtime Environment $_major"
arch=('i686' 'armv6h')
url="http://www.oracle.com/technetwork/java/embedded/downloads/index.html"
license=('custom')
provides=("java-runtime=$_major" "java-runtime-headless=$_major")
conflicts=("${provides[@]}")
backup=('etc/profile.d/jre.sh')
install='jre.install'

if [[ "$CARCH" = 'armv6h' ]]; then
    _archive="ejre-$_pkg-fcs-$_build-linux-arm-vfp-client_headless"
    md5sums=('a0b97eb1d0f230d234d5ce4742ab7eb2')
else
    _archive="ejre-${_pkg}-fcs-${_build}-linux-i586-headless"
    md5sums=('0abb4cc7db74b1bc6415dcf79cc2e5b0')
fi
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -O --header "Cookie: gpw_e24=h"')
source=("http://download.oracle.com/otn-pub/java/ejre/${_pkg}-${_build}/${_archive}-${_date}.tar.gz"
        'jre.sh')
md5sums+=('1efe5755b77f4533bd5b9f4728b40fad')

package() {
    cd "ejre1.${_major}.0_${_minor}"
    mkdir -p "${pkgdir}"/{opt/$pkgname/jre,usr/{bin,/share/licenses/$pkgname},etc/{.java/.systemPrefs,profile.d}}
    cp -r COPYRIGHT *.txt "$pkgdir"/usr/share/licenses/$pkgname/
    cp -r * "$pkgdir"/opt/$pkgname/jre/
    ln -s /opt/java-$pkgname/jre/bin/java "$pkgdir"/usr/bin/java
    install -m755 "$srcdir"/jre.sh "$pkgdir"/etc/profile.d/
}