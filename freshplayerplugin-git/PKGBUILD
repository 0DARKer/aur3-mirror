# Maintainer: Behem0th <grantipak@gmail.com>

pkgname=freshplayerplugin-git
_realname=freshplayerplugin
pkgver=20140330
pkgrel=1
pkgdesc='PPAPI-host NPAPI-plugin adapter.'
arch=('i686' 'x86_64')
url='https://github.com/i-rinat/freshplayerplugin'
license=('MIT')
depends=('pango' 'alsa-lib' 'freetype2' 'xulrunner' 'uriparser')
makedepends=('git' 'cmake')
provides=('freshplayerplugin')
conflicts=('freshplayerplugin')
_chromever=33.0.1750.152
_channel=stable
_arch=i386
source=('git://github.com/i-rinat/freshplayerplugin.git'
        'build-fix.patch'
        "google-chrome-${_channel}_${_chromever}_${_arch}.deb::https://dl.google.com/linux/direct/google-chrome-${_channel}_current_${_arch}.deb")
md5sums=('SKIP'
         '88e72f1beead2ddbf0ca715515d9f711'
         'c9bbee389d339913cc90e16fec930978')

pkgver() {
  cd "$srcdir/$_realname"
  git log -1 --format="%cd" --date=short | tr -d -
}

prepare() {
  cd "$srcdir/$_realname"
  patch -Np1 -i "$srcdir/build-fix.patch"
}

build() {
  cd "$srcdir/$_realname"
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  msg2 "Extracting the data.tar.lzma"
  mkdir "$srcdir/temp"
  bsdtar -xf data.tar.lzma -C "$srcdir/temp"

  install -d "$pkgdir/usr/lib/mozilla/plugins"
  install -Dm644  "$srcdir/temp/opt/google/chrome/PepperFlash/libpepflashplayer.so" "$pkgdir/usr/lib/mozilla/plugins"

  cd "$srcdir/$_realname"
  install -Dm644 libfreshwrapper.so "$pkgdir/usr/lib/mozilla/plugins"
}

