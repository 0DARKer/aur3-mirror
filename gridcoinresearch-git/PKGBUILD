# Maintainer: L <alaviss0 at gmail dot com>

pkgbase=gridcoinresearch-git
pkgname=('gridcoinresearch-daemon-git' 'gridcoinresearch-qt-git')
pkgver=3.3.7.1.r295.aa54cf3
pkgrel=1
arch=('i686' 'x86_64')
url="http://gridcoin.us"
license=('custom:gridcoin')
makedepends=('boost' 'git' 'qt4' 'openssl' 'qrencode' 'db' 'gendesk')
source=('gridcoinresearch::git+https://github.com/gridcoin/Gridcoin-Research.git')

sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgbase%-git}"

  printf "%s.r%s.%s" \
    "$(grep CLIENT_VERSION src/clientversion.h | awk '{print $NF}' | sed ':a;N;$!ba;s/\n/./g')" \
    "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${pkgbase%-git}"

  mkdir -p src/obj/zerocoin
  chmod 755 src/leveldb/build_detect_platform

  patch -Np1 -i "$srcdir/gridcoin-qt-pro.patch"
}

build() {
  cd "$srcdir/${pkgbase%-git}"

  cd src
  #make ${MAKEFLAGS} -f makefile.unix DEBUGFLAGS="" USE_UPNP=-

  cd ..
  qmake-qt4 "USE_UPNP=-"
  make ${MAKEFLAGS}
}

package_gridcoinresearch-daemon-git() {
  pkgdesc="GridCoin is a PoS-based cryptocurrency - Daemon"
  depends=('boost-libs' 'db' 'openssl')
  optdepends=('boinc: [NECESSARY] used to build RAC')
  install=gridcoin.install

  cd "$srcdir/${pkgbase%-git}/src"
  install -Dm 755 gridcoinresearchd "$pkgdir/usr/bin/gridcoinresearchd"

  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_gridcoinresearch-qt-git() {
  pkgdesc="GridCoin is a PoS-based cryptocurrency - Qt"
  depends=('boost-libs' 'db' 'openssl' 'qrencode' 'qt4')
  optdepends=('boinc: [NECESSARY] used to build RAC')
  install=gridcoin.install

  cd "$srcdir/${pkgbase%-git}"
  install -Dm 755 gridcoinresearch "$pkgdir/usr/bin/gridcoinresearch"

  gendesk -n --pkgname "Gridcoin-Research" --pkgdesc "$pkgdesc" \
    --exec "/usr/bin/gridcoinresearch"

  install -Dm644 Gridcoin-Research.desktop "$pkgdir/usr/share/applications/Gridcoin-Research.desktop"

  install -Dm644 share/pixmaps/grc-small.png "$pkgdir/usr/share/pixmaps/Gridcoin-Research.png"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
