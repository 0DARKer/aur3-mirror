# Maintainer: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=gb-git
_name="${pkgname/-git/}"
pkgver=fca8a8e
pkgrel=1
pkgdesc="The project based build tool for Go"
url="https://github.com/constabulary/${_name}"
license=('MIT')
arch=('i686' 'x86_64')
conflicts=()
provides=()
makedepends=('git' 'go')
source=("${_name}::git+${url}")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_name}"
	git describe --always --tags | sed 's/-/./g'
}

prepare() {
	cd "${srcdir}"
	mkdir -p "src/github.com/constabulary"
	mv "${_name}" "src/github.com/constabulary/${_name}"
}

build() {
	cd "${srcdir}/src/github.com/constabulary/${_name}"
	for tool in gb gb-env gb-vendor; do
		pushd "cmd/${tool}"
		GOPATH="${srcdir}" go build
		popd
	done
}

package() {
	cd "${srcdir}/src/github.com/constabulary/${_name}"
	for tool in gb gb-env gb-vendor; do
		install -Dm755 "cmd/${tool}/${tool}" "${pkgdir}/usr/bin/${tool}"
	done
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
