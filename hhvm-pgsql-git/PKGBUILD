# Author: Jan Wa≈õ <janek.jan@gmail.com>

_pkgname=hhvm-pgsql
pkgname=$_pkgname-git
pkgver=2313d2ec33
pkgrel=2
pkgdesc="PostgreSQL extension for HHVM"
arch=('i686' 'x86_64')
url="https://github.com/PocketRent/$_pkgname"
license=(PHP)
depends=('hhvm' 'postgresql-libs')
makedepends=('git' 'cmake')
md5sums=('SKIP'
         'ee772bc3205a436a5619cd8f1ad80a2a')
source=("$_pkgname::git+https://github.com/PocketRent/$_pkgname.git"
        'version.h')

prepare() {
    mkdir -p "${srcdir}/build"
    mkdir -p "${srcdir}/hphp/runtime"
    cp "${srcdir}/version.h" "${srcdir}/hphp/runtime/"
    cp "${srcdir}/${_pkgname}/ext_pgsql.php" "${srcdir}/build/"
    cd "${srcdir}/${_pkgname}"
    hphpize
    cd ../build
    CXXFLAGS="-I${srcdir}" CFLAGS="-I${srcdir}" cmake "../${_pkgname}"
}

build() {
    cd "${srcdir}/build"
    make
}

package() {
    cd "${srcdir}/build"
    make DESTDIR="${pkgdir}" install
    mv "$pkgdir"/usr/lib{64,}
    install -Dm644 "${srcdir}/${_pkgname}/LICENSE.PHP" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.PHP"
}

# vim:set ts=2 sw=2 et:
