# Contributor: Christopher Bayliss <christopher.j.bayliss@gmail.com>
pkgname=xiphos-gtk3
pkgver=4857
pkgrel=2
pkgdesc="A Bible study tool -- GTK3 svn version"
arch=('i686' 'x86_64')
url="http://xiphos.org"
license=('GPL')
depends=('webkit2gtk' 'libgsf' 'gconf' 'sword' 'biblesync')
makedepends=('gnome-common' 'gnome-doc-utils' 'intltool' 'subversion')
provides=('gnomesword' 'xiphos' 'xiphos-svn')
conflicts=('gnomesword' 'xiphos' 'xiphos-svn')
install=xiphos-gtk3.install
source=('xiphos::svn://svn.code.sf.net/p/gnomesword/code/trunk')
md5sums=('SKIP')
_svnmod=xiphos

pkgver() {
  cd "$srcdir/${_svnmod}"
  local ver="$(svnversion)"
  printf "%s" "${ver//[[:alpha:]]}"
}

build() {
  cd "$srcdir/$_svnmod"
  CXX=g++ python2 ./waf --prefix=/usr --gtk=3 configure 
  python2 ./waf --prefix=/usr --gtk=3 build 
}

package() {
  cd "$srcdir/$_svnmod"
  python2 ./waf --destdir=$pkgdir --no-post-install install 
}
