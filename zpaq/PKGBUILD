# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>
# Contributor: Jan Stępień <jstepien@users.sourceforge.net>

pkgname=zpaq
pkgdesc='Programmable file compressor, library and utilities. Based on the PAQ compression algorithm'
url='http://mattmahoney.net/dc/zpaq.html'
pkgver=6.49
pkgrel=1

_zpaq_ver=649

arch=(i686 x86_64)
license=(GPL3 MIT)

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip)
sha512sums=('67e5fb2fb5d0c6f8b05e126aeaf3b9b2173c343487627ebaddbf63ce56ef044842e6e7b7ed20c80e813fe04c625421101ef157e21fc72d6411daec4ad25c77ae')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]
	then
		CC=gcc
	fi
	if [ -z "$CXX" ]
	then
		CXX=g++
	fi

	msg "Building libzpaq"
	$CXX $CXXFLAGS $LDFLAGS -fPIC -shared -Dunix -DNDEBUG libzpaq.cpp -o libzpaq.so

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX ${CXXFLAGS/-flto/} ${LDFLAGS/-flto/} -Dunix -DNDEBUG -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq
}

package()
{
	install -Dm 644 libzpaq.h  "$pkgdir/usr/include/libzpaq.h"
	install -Dm 644 libzpaq.so "$pkgdir/usr/lib/libzpaq.so"
	install -Dm 755 zpaq       "$pkgdir/usr/bin/zpaq"
}
