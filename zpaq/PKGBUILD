# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>
# Contributor: Jan Stępień <jstepien@users.sourceforge.net>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=6.27
pkgrel=1

_zpaq_ver=627

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip)
sha512sums=('358a5d75d63f9cbd643662339798a6fd830ebf76a8a6bb4b70c9e81a41fd1470bde6223dbf7edb55d71c33e90ca0f82545b497cdfb356d677dc75c3483466bc4')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]
	then
		CC=gcc
	fi
	if [ -z "$CXX" ]
	then
		CXX=g++
	fi

	msg "Building libzpaq"
	$CXX -fPIC -shared $CXXFLAGS -DNDEBUG libzpaq.cpp -o libzpaq.so

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq

	msg "Building zpaqd"
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG zpaqd.cpp -L. -lzpaq -o zpaqd
}

package()
{
	install -Dm 644 libzpaq.h  "$pkgdir/usr/include/libzpaq.h"
	install -Dm 644 libzpaq.so "$pkgdir/usr/lib/libzpaq.so"
	install -Dm 755 zpaq       "$pkgdir/usr/bin/zpaq"
	install -m  755 zpaqd      "$pkgdir/usr/bin/zpaqd"
}
