# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=4.02
pkgrel=1

_zpaq_ver=402
_libzpaq_ver=400
_divsufsort_ver=200

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')
makedepends=('perl')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip
	http://mattmahoney.net/dc/libzpaq${_libzpaq_ver}.zip
	http://mattmahoney.net/dc/divsufsort${_divsufsort_ver}.zip)
sha512sums=('6ca66583bf7b6442fee234a7a5cd8ffd30ba5ad64c21f67f6d5f21ea522eeeb36629e5f9c457bf2ba142f2ed4170f467c3519e1d9d7dea42cdd3c1772779b74e'
            '6107ec911f3a8c080272395403346de4c067df62c3f0b339e9615765543fb95add0ba0fa8880ae9777ddf75ac1b367879e2b6f8ae39aac0b3a2f1380548f4bff'
            'b810fdfa725012862befb350299677c89821e11d654c5c98d70ba3c3d3f798f5470f58f722e953724734eefcf7fa7d77b0530b89e9d781511203947f46bd5c48')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]; then
		CC=gcc
	fi
	if [ -z "$CXX" ]; then
		CXX=g++
	fi

	msg "Converting man pages"
	pod2man zpaq.1.pod zpaq.1
	pod2man libzpaq.3.pod libzpaq.3

	msg "Building libzpaq"
	$CXX $CXXFLAGS -DNDEBUG -c libzpaq.cpp
	ar rcs libzpaq.a libzpaq.o

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -lpthread -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq
}

package()
{
        mkdir -p $pkgdir/usr/{bin,include,lib,share/man/man{1,3}}

	cp zpaq.1 $pkgdir/usr/share/man/man1
	cp libzpaq.3 $pkgdir/usr/share/man/man3

	cp libzpaq.h $pkgdir/usr/include
	cp libzpaq.a $pkgdir/usr/lib

	cp zpaq $pkgdir/usr/bin
}
