# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=2.05
pkgrel=1

_libzpaq_ver=202a
_zp_ver=103
_zpaq_ver=205
_zpipe_ver=201

arch=('i686' 'x86_64')
license=('GPL')
makedepends=('gzip' 'perl' 'unzip')
depends=('libdivsufsort')

source=(http://mattmahoney.net/dc/libzpaq.${_libzpaq_ver}.zip
        http://mattmahoney.net/dc/zp.${_zp_ver}.zip
        http://mattmahoney.net/dc/zpaq.${_zpaq_ver}.zip
        http://mattmahoney.net/dc/zpipe.${_zpipe_ver}.zip)
sha512sums=('550b02eb7085e0fec52efe9b1e82723aefa60e2bd3022e20e61a70d649988d343f57e71396f848f6f4ab7807e97720aaba1b27389941554a9735085fb3ec3791'
            '6d58cf1e6fb5de97723f3e983ba926b853e1ee169d8f1460e914952b481ab81934324152385e696b6cf18ea2ce0ecc93005085f9376f77b33c777410b70f3c06'
            '0dcebd7fb7b0b8d09469e1edfcb60037ffe947cb7a23ca4b084da9e4da567f3838744ddbd01fb370be31f9436e33d25e91bc9b7c3a532ddb2cb13554aa2be478'
            '4c7fc5afd96c3db806679a56863752a18889d8e8dad7648b993a32bfa7ceb426782ec5f83567ad58ca5c5b79f145e8953667dc1a3b854fc8a76eaac165e28cb7')

build()
{
	## It's easier if we ignore install.sh and do everything manually

	cd "$srcdir"

	echo Generating man pages
	pod2man zpaq.1.pod zpaq.1
	pod2man libzpaq.3.pod libzpaq.3

	echo Compiling libzpaq
	$CXX $CXXFLAGS -DNDEBUG -c libzpaq.cpp
	$CXX $CXXFLAGS -DNDEBUG -c libzpaqo.cpp
	ar rcs libzpaq.a libzpaq.o libzpaqo.o

	echo Compiling zp
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG zp.cpp -lpthread -ldivsufsort -L. -lzpaq -o zp

	echo Compiling zpaq
	# TODO: Define -DOPT
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG zpaq.cpp -L. -lzpaq -o zpaq

	echo Compiling zpipe
        $CXX $CXXFLAGS $LDFLAGS -DNDEBUG zpipe.cpp -L. -lzpaq -o zpipe
}

package()
{
        mkdir -p $pkgdir/usr/{bin,include,lib,share/man/man{1,3}}

	cp zpaq.1 $pkgdir/usr/share/man/man1
	cp libzpaq.3 $pkgdir/usr/share/man/man3

	cp libzpaq.h $pkgdir/usr/include
	cp libzpaq.a $pkgdir/usr/lib

	cp zp zpaq zpipe $pkgdir/usr/bin
}
