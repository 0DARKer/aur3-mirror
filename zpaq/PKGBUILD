# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>
# Contributor: Jan Stępień <jstepien@users.sourceforge.net>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=6.21
pkgrel=1

_zpaq_ver=621

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip)
sha512sums=('d1219fb1c1f478a23069806de8373f8eecc006b14c2100600973b0941d1454006335e64757b990fc451aeb03a212f12080a418e3111e13f0c8c2886b1a483f35')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]; then
		CC=gcc
	fi
	if [ -z "$CXX" ]; then
		CXX=g++
	fi

	msg "Building libzpaq"
	$CXX -fPIC $CXXFLAGS -DNDEBUG -c libzpaq.cpp
	$CXX -shared $LDFLAGS -o libzpaq.so libzpaq.o

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq

	msg "Building zpaqd"
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG zpaqd.cpp -L. -lzpaq -o zpaqd
}

package()
{
        install -d $pkgdir/usr/{bin,include,lib}

	install libzpaq.h $pkgdir/usr/include
	install libzpaq.so $pkgdir/usr/lib

	install zpaq $pkgdir/usr/bin
}
