# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=4.04
pkgrel=1

_zpaq_ver=404
_libzpaq_ver=501
_divsufsort_ver=200

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')
makedepends=('perl')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip
	http://mattmahoney.net/dc/libzpaq${_libzpaq_ver}.zip
	http://mattmahoney.net/dc/divsufsort${_divsufsort_ver}.zip)
sha512sums=('b624eeb358550a77979813ad950c940ff9de91636292eaa3b6e034a58474882615c7e8d154e7efd1dbb6ad77e20709d271ddb95897e294281a53d16ce0f26432'
            '67e7301297a56eb1b75db863f5e9dc79c66c1b478b92dfbe4bdeeef0605323c78f940798faaf2828510d3f40064a19e31032ca35f32c177462b9958d7f6b7308'
            'b810fdfa725012862befb350299677c89821e11d654c5c98d70ba3c3d3f798f5470f58f722e953724734eefcf7fa7d77b0530b89e9d781511203947f46bd5c48')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]; then
		CC=gcc
	fi
	if [ -z "$CXX" ]; then
		CXX=g++
	fi

	msg "Converting man pages"
	pod2man zpaq.1.pod zpaq.1
	pod2man libzpaq.3.pod libzpaq.3

	msg "Building libzpaq"
	$CXX $CXXFLAGS -DNDEBUG -c libzpaq.cpp
	ar rcs libzpaq.a libzpaq.o

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -lpthread -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq
}

package()
{
        mkdir -p $pkgdir/usr/{bin,include,lib,share/man/man{1,3}}

	cp zpaq.1 $pkgdir/usr/share/man/man1
	cp libzpaq.3 $pkgdir/usr/share/man/man3

	cp libzpaq.h $pkgdir/usr/include
	cp libzpaq.a $pkgdir/usr/lib

	cp zpaq $pkgdir/usr/bin
}
