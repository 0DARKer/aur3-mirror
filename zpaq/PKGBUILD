# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=4.00
pkgrel=1

_zpaq_ver=400
_divsufsort_ver=200

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')
makedepends=('perl')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip
	http://mattmahoney.net/dc/libzpaq${_zpaq_ver}.zip
	http://mattmahoney.net/dc/divsufsort${_divsufsort_ver}.zip)
sha1sums=('de701edd2d0c53ba9b730b4fbfbb16454623b0a9'
          '70aa26857e897890b678df831686de9da1c70acb'
          'fa590e187b70461439b941247a4f5b8943023b0e')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]; then
		CC=gcc
	fi
	if [ -z "$CXX" ]; then
		CXX=g++
	fi

	msg "Converting man pages"
	pod2man zpaq.1.pod zpaq.1
	pod2man libzpaq.3.pod libzpaq.3

	msg "Building libzpaq"
	$CXX $CXXFLAGS -DNDEBUG -c libzpaq.cpp
	ar rcs libzpaq.a libzpaq.o

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -lpthread -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq
}

package()
{
        mkdir -p $pkgdir/usr/{bin,include,lib,share/man/man{1,3}}

	cp zpaq.1 $pkgdir/usr/share/man/man1
	cp libzpaq.3 $pkgdir/usr/share/man/man3

	cp libzpaq.h $pkgdir/usr/include
	cp libzpaq.a $pkgdir/usr/lib

	cp zpaq $pkgdir/usr/bin
}
