# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>
# Contributor: Jan Stępień <jstepien@users.sourceforge.net>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=6.24a
pkgrel=1

_zpaq_ver=624a

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip)
sha512sums=('108d1fa6f199428469b825ad924a531baec7b1aa50c1c576198944544ef37208928dd8ac06e658fbe9aac8d55f233a8310773c7d0384807fcf0a830b89134dfe')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]
	then
		CC=gcc
	fi
	if [ -z "$CXX" ]
	then
		CXX=g++
	fi

	msg "Building libzpaq"
	$CXX -fPIC -shared $CXXFLAGS -DNDEBUG libzpaq.cpp -o libzpaq.so

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq

	msg "Building zpaqd"
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG zpaqd.cpp -L. -lzpaq -o zpaqd
}

package()
{
	install -Dm 644 libzpaq.h  "$pkgdir/usr/include/libzpaq.h"
	install -Dm 644 libzpaq.so "$pkgdir/usr/lib/libzpaq.so"
	install -Dm 755 zpaq       "$pkgdir/usr/bin/zpaq"
	install -m  755 zpaqd      "$pkgdir/usr/bin/zpaqd"
}
