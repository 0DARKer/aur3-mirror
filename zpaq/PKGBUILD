# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>
# Contributor: Jan Stępień <jstepien@users.sourceforge.net>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=6.18
pkgrel=1

_zpaq_ver=618

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip)
sha512sums=('a5a6094a9183289d684479061f97bcdc8636f7fc9cf3f3a4c9c33e977d32903c118752f90c121a5ead652f28d6f11cac734000716098cb402c43357481bf8e3b')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]; then
		CC=gcc
	fi
	if [ -z "$CXX" ]; then
		CXX=g++
	fi

	msg "Building libzpaq"
	$CXX -fPIC $CXXFLAGS -DNDEBUG -c libzpaq.cpp
	$CXX -shared $LDFLAGS -o libzpaq.so libzpaq.o

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -lpthread -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq
}

package()
{
        install -d $pkgdir/usr/{bin,include,lib}

	install libzpaq.h $pkgdir/usr/include
	install libzpaq.so $pkgdir/usr/lib

	install zpaq $pkgdir/usr/bin
}
