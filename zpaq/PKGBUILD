# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>
# Contributor: Jan Stępień <jstepien@users.sourceforge.net>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=6.19
pkgrel=1

_zpaq_ver=619

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip)
sha512sums=('0ffc68229cdd0ed0f0fadb421eb4c7f418042202b35bc1a2ad82e262d2377ac9c16669bc68a3aba33733ac027a394aa346c99a0e73f0fd40419a46ddafd6a038')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]; then
		CC=gcc
	fi
	if [ -z "$CXX" ]; then
		CXX=g++
	fi

	msg "Building libzpaq"
	$CXX -fPIC $CXXFLAGS -DNDEBUG -c libzpaq.cpp
	$CXX -shared $LDFLAGS -o libzpaq.so libzpaq.o

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq

	msg "Building zpaqd"
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG zpaqd.cpp -L. -lzpaq -o zpaqd
}

package()
{
        install -d $pkgdir/usr/{bin,include,lib}

	install libzpaq.h $pkgdir/usr/include
	install libzpaq.so $pkgdir/usr/lib

	install zpaq $pkgdir/usr/bin
}
