# Maintainer: Marco Schulze <marco.c.schulze-gmail.com>
# Contributor: TuxSpirit<tuxpsiritATarchlinuxDOTfr>
# Contributor: Jan Stępień <jstepien@users.sourceforge.net>

pkgname=zpaq
pkgdesc="Programmable file compressor, library and utilities. Based on the PAQ compression algorithm"
url="http://mattmahoney.net/dc/zpaq.html"
pkgver=6.26
pkgrel=1

_zpaq_ver=626

arch=('i686' 'x86_64')
license=('GPL3' 'MIT')

source=(http://mattmahoney.net/dc/zpaq${_zpaq_ver}.zip)
sha512sums=('e5b7a088b3a932cef7845137bc43a5c912a167f724675ac5846215288ab899b2ba0b8123446c2179921a92e4a4f224d99d9b17cf33ea5817368a2fd693e230da')

build()
{
	cd "$srcdir"

	if [ -z "$CC" ]
	then
		CC=gcc
	fi
	if [ -z "$CXX" ]
	then
		CXX=g++
	fi

	msg "Building libzpaq"
	$CXX -fPIC -shared $CXXFLAGS -DNDEBUG libzpaq.cpp -o libzpaq.so

	msg "Building zpaq"
	$CC $CFLAGS -DNDEBUG -fopenmp -c divsufsort.c
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG -fopenmp zpaq.cpp divsufsort.o -L. -lzpaq -o zpaq

	msg "Building zpaqd"
	$CXX $CXXFLAGS $LDFLAGS -DNDEBUG zpaqd.cpp -L. -lzpaq -o zpaqd
}

package()
{
	install -Dm 644 libzpaq.h  "$pkgdir/usr/include/libzpaq.h"
	install -Dm 644 libzpaq.so "$pkgdir/usr/lib/libzpaq.so"
	install -Dm 755 zpaq       "$pkgdir/usr/bin/zpaq"
	install -m  755 zpaqd      "$pkgdir/usr/bin/zpaqd"
}
