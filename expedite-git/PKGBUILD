# Maintainer: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=expedite-git
_pkgname=expedite
pkgver=1.7.99.308.02f1fb2
pkgrel=1
pkgdesc="A benchmark tool around the EFL library"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('efl-git')
makedepends=('git')
optdepends=("python2: Compare Expedite outputs")
source=("git://git.enlightenment.org/tools/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"

  local _ver=$(awk -F , '/^AC_INIT/ {print $2}' configure.ac | tr -d '[ ]')

  echo $_ver.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
# set python scripts to run with python2
  sed -i 's/env python$/python2/' "$srcdir/$_pkgname/src/bin/expedite-cmp"
}

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_pkgname"
  
  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 AUTHORS "$pkgdir/usr/share/licenses/$pkgname/AUTHORS"
  install -Dm644 COPYING-PLAIN "$pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN"
}

