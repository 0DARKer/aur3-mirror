# Maintainer: Carl George < arch at cgtx.us >

_module="cliff-tablib"
pkgname=("python-${_module}" "python2-${_module}")
pkgver="1.1"
pkgrel="1"
pkgdesc="tablib formatters for cliff"
arch=("any")
url="https://github.com/dreamhost/cliff-tablib"
license=("Apache")
makedepends=(
	"python-setuptools"
	"python2-setuptools"
)
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('4a8ed81f6678e76d0e0497f2708d0fe5d2b83ec442a03f982aaa711a08c74139')

prepare() {
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

package_python-cliff-tablib() {
	depends=(
		"python-cliff"
		"python-tablib"
	)
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-cliff-tablib() {
	depends=(
		"python2-cliff"
		"python2-tablib"
	)
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
