# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=libdvdread-svn
pkgver=1243
pkgrel=1
pkgdesc="A simple foundation for reading DVD video disks"
arch=('i686' 'x86_64')
url="http://dvdnav.mplayerhq.hu/"
license=('GPL2')
depends=()
makedepends=('subversion')
provides=('libdvdread')
conflicts=('libdvdread')
options=(!libtool)
source=()
md5sums=()

_svntrunk="svn://svn.mplayerhq.hu/dvdnav/trunk/libdvdread"
_svnmod="libdvdread"

build() {
  cd "$srcdir"/
  msg "Connecting to libdvdnav svn server...."
  if [ -d $_svnmod/.svn ]; then
    cd $_svnmod && svn up
    cd ..
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir"/$_svnmod-build ]; then
    rm -rf "$srcdir"/$_svnmod-build
  fi

  cp -r "$srcdir"/$_svnmod "$srcdir"/$_svnmod-build
  cd "$srcdir"/$_svnmod-build

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$_svnmod-build

  make DESTDIR="$pkgdir" install
}
