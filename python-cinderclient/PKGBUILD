# Maintainer: Carl George < arch at cgtx.us >

_module="python-cinderclient"
pkgname=("python-cinderclient" "python2-cinderclient")
pkgver="1.2.0"
pkgrel="1"
pkgdesc="OpenStack Block Storage API Client Library"
arch=("any")
url="https://github.com/openstack/python-cinderclient"
license=("Apache")
makedepends=("python-pbr" "python-setuptools" "python2-pbr" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz"
        "cinder.bash_completion"
        "cinder2.bash_completion")
sha256sums=('a30b3f2cd42f4b1b4f486008136876364df744e1b06a88db243428fe6db75c76'
            '44256d990399e08ff60e75f73c05e39fd90b9203dc19dc2bc1960edb16dc7801'
            '40ef74782172c6d31d19df1f9b45df35b6c771a65a823bec8da41e4a909f2052')

prepare() {
    sed -i '/argparse/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
    sed -i '/pbr/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-cinderclient() {
    depends=("python-babel>=1.3"
             "python-keystoneclient>=1.1.0"
             "python-prettytable>=0.7"
             "python-requests>=2.2.0"
             "python-simplejson>=2.2.0"
             "python-six>=1.9.0")
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 "${srcdir}/cinder.bash_completion" "${pkgdir}/etc/bash_completion.d/cinder"
}

package_python2-cinderclient() {
    depends=("python2-babel>=1.3"
             "python2-keystoneclient>=1.1.0"
             "python2-prettytable>=0.7"
             "python2-requests>=2.2.0"
             "python2-simplejson>=2.2.0"
             "python2-six>=1.9.0")
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 "${srcdir}/cinder2.bash_completion" "${pkgdir}/etc/bash_completion.d/cinder2"
    mv "${pkgdir}/usr/bin/cinder" "${pkgdir}/usr/bin/cinder2"
}
