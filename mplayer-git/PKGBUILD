# Contributor: Laochailan <laochailan@web.de>
# modification of extra/mplayer by Hugo Doria <hugo@archlinux.org>
pkgname=mplayer-git
pkgver=20100728
pkgrel=1
pkgdesc="MPlayer from Uoti's git repository"
arch=(i686 x86_64)
url="http://repo.or.cz/w/mplayer.git"
license=('GPL')
depends=('ffmpeg' 'libass' 'libdvdnav' 'libxxf86dga' 'libmad' 'cdparanoia' 'libxinerama'
         'libmng' 'libxss' 'libgl' 'smbclient' 'aalib' 'jack' 'libcaca' 'lirc-utils' 'ttf-dejavu' 'libxvmc'
         'libdca' 'a52dec')
makedepends=('git' 'mesa' 'live-media>=2010.01.13')
provides=(mplayer)
conflicts=(mplayer)
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')

_gitroot="git://repo.or.cz/mplayer.git"
_gitname="mplayer"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # gif disabled since repo's giflib is too old
  ./configure --prefix=/usr \
      --enable-runtime-cpudetection \
      --disable-gui \
      --disable-arts \
      --disable-liblzo \
      --disable-speex \
      --disable-openal \
      --disable-fribidi \
      --disable-libdv \
      --disable-musepack \
      --disable-esd \
      --disable-mga \
      --enable-xvmc \
	  --disable-gif \
      --language=all \
      --confdir=/etc/mplayer || return 1

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make || return 1
  make -j1 DESTDIR="$pkgdir/" install

  install -Dm644 etc/{codecs.conf,input.conf,example.conf} ${pkgdir}/etc/mplayer/ || return 1
  install -dm755 ${pkgdir}/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/mplayer/subfont.ttf || return 1
  rm -rf ${pkgdir}/usr/share/mplayer/font
} 
