# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

_pkgname=cairomm
pkgname=mingw32-${_pkgname}
pkgver=1.8.4
pkgrel=1
arch=(any)
pkgdesc="C++ bindings for cairo (mingw32)"
depends=('mingw32-cairo>=1.8.8' 'mingw32-libsigc++2.0>=2.2.3')
makedepends=(mingw32-gcc 'pkgconfig')
url="http://www.cairographics.org"
license=(LGPL)
options=(!strip)
source=(http://www.cairographics.org/releases/${_pkgname}-${pkgver}.tar.gz)
md5sums=('559afbc47484ba3fad265e38a3dafe90')

build()
{
  cd ${srcdir}/${_pkgname}-$pkgver

  export CPPFLAGS="$CPPFLAGS -D_REENTRANT"
  export PKG_CONFIG_PATH="/usr/i486-mingw32/lib/pkgconfig"
  unset LDFLAGS

  sed -i -e 's/ps svg xlib xlib-render/ps svg/' configure
  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-documentation \
    --enable-static --enable-shared

  make
}

package() {
  cd ${srcdir}/${_pkgname}-$pkgver
  make install DESTDIR=${pkgdir}

  cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
}
