# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-chaco
pkgver=4.3.0
pkgrel=1
pkgdesc="Interactive 2-Dimensional Plotting"
arch=('i686' 'x86_64')
url="https://github.com/enthought/chaco"
license=('BSD')
depends=('python2-enable')
makedepends=('python2-distribute' 'gcc')
conflicts=('python-enthought-chaco' 'python2-chaco-git' 'python-ets-chaco-svn')
options=(!emptydirs)

source=("https://github.com/enthought/chaco/archive/${pkgver}.tar.gz")
md5sums=('770a626f24a7323025fdf3a1cfe39be0')

build() {
  cd "$srcdir"/chaco-$pkgver

  python2 setup.py build
}

package() {
  cd "$srcdir"/chaco-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find "${pkgdir}" -name '*.py')

  install -D "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

