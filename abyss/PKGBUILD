# Maintainer:  Eric Biggers <ebiggers3 at gmail dot com>

pkgname=abyss
pkgver=1.3.3
pkgrel=1
pkgdesc="A de novo, parallel, paired-end sequence assembler"
arch=("i686" "x86_64")
url="http://www.bcgsc.ca/platform/bioinfo/software/abyss"
license=("custom")
depends=("openmpi" "boost-libs")
makedepends=("boost" "sparsehash")
source=("http://www.bcgsc.ca/downloads/$pkgname/$pkgname-$pkgver.tar.gz"
	"abyss-fixmate.patch")

build() {
	cd "$pkgname-$pkgver"
	patch -p1 < "$srcdir/abyss-fixmate.patch"
	./configure  --with-boost=/usr/include/boost --with-mpi=/usr/include \
			--prefix=/usr
	make
}

package() {
	cd "$pkgname-$pkgver"  
	make DESTDIR="$pkgdir" install
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

md5sums=("15953363d4ef5795d3a82d129e4ca240"
	 "30ee17b85cb2376d68cbf526102958d8")
