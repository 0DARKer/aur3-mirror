# Maintainer:  Eric Biggers <ebiggers3 at gmail dot com>

pkgname=abyss
pkgver=1.5.2
pkgrel=1
pkgdesc="Assembly By Short Sequences - a de novo, parallel, paired-end sequence assembler"
arch=("i686" "x86_64")
url="http://www.bcgsc.ca/platform/bioinfo/software/abyss"
license=("custom")
depends=("openmpi" "boost-libs")
makedepends=("boost" "sparsehash")
source=("https://github.com/bcgsc/abyss/releases/download/1.5.2/abyss-1.5.2.tar.gz")

build() {
  cd "${pkgname}-${pkgver}" 
  ./configure  --with-boost=/usr/include/boost     \
               --with-mpi=/usr/include             \
               --prefix=/usr			   \
               CXXFLAGS="$CXXFLAGS -Wno-unused-local-typedefs"
  # The CXXFLAGS modification is needed because this software is compiled with
  # -Werror by default.
  make 
}

package() {
  cd "${pkgname}-${pkgver}" 
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

sha1sums=('f28189338efdee0167cf73f92b43181caccd2b1d')
