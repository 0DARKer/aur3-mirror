# Maintainer:  Eric Biggers <ebiggers3 at gmail dot com>

pkgname=abyss
pkgver=1.3.6
pkgrel=1
pkgdesc="Assembly By Short Sequences - a de novo, parallel, paired-end sequence assembler"
arch=("i686" "x86_64")
url="http://www.bcgsc.ca/platform/bioinfo/software/abyss"
license=("custom")
depends=("openmpi" "boost-libs")
makedepends=("boost" "sparsehash")
source=("${url}/releases/${pkgver}/abyss-${pkgver}.tar.gz")

build() {
  cd "${pkgname}-${pkgver}" 
  ./configure  --with-boost=/usr/include/boost     \
               --with-mpi=/usr/include             \
               --prefix=/usr                       \
	       CXXFLAGS=-Wno-unused-local-typedefs
  # The CXXFLAGS modification is needed because this software is compiled with
  # -Werror by default.  Inevitably, g++ (probably v4.8) added a new warning
  # thereby breaking the build.
  make
}

package() {
  cd "${pkgname}-${pkgver}" 
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
md5sums=('4182c24de5b74a1294fc8f9a452ae743')
