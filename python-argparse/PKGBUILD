# Maintainer: mawcomw <mawcomw@gmail.com>

pkgbase=python-argparse
pkgname=('python-argparse' 'python2-argparse')
pkgver=1.2.1
pkgrel=2
arch=('i686' 'x86_64')
url="http://code.google.com/p/argparse/"
license=('CUSTOM')
makedepends=('python2' 'python')
source=("https://pypi.python.org/packages/source/a/argparse/argparse-$pkgver.tar.gz")
md5sums=('2fbef8cb61e506c706957ab6e135840c')

prepare() {
   cp -r argparse-${pkgver} python2-argparse-${pkgver}
}

build() {
   cd $srcdir/argparse-${pkgver}
   python setup.py build
   
   cd $srcdir/python2-argparse-${pkgver}
   python2 setup.py build
}

#check(){
#   cd $srcdir/argparse-${pkgver}
#   python2 setup.py test
#   
#   cd $srcdir/python2-argparse-${pkgver}
#   python setup.py test
#}

package_python-argparse() {
   depends=('python' )
   pkgdesc="Python3 command-line parsing library"

   cd argparse-${pkgver}
   python setup.py install --root="${pkgdir}" --optimize=1

   install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-argparse() {
   depends=('python2' )
   pkgdesc="Python2 command-line parsing library"
   
   cd python2-argparse-${pkgver}
   python2 setup.py install --root="${pkgdir}" --optimize=1

   install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE.txt
}