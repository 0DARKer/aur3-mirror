# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=freediams
pkgver=0.5.4
pkgrel=1
pkgdesc="The FreeMedForms Pharmaceutical Drugs Prescriber"
arch=(i686 x86_64)
url="http://www.freemedforms.com/en/"
license=('GPL')
depends=('qt')
install="$pkgname.install"
source=("http://freemedforms.googlecode.com/files/${pkgname}fullsources-$pkgver.tgz")
md5sums=('162a7fbf1a7a55cd6a317f35d7d91b45')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # qbuttonlineedit.h mistaped fix
  sed -i "s|qbuttonlineedit.h|QButtonLineEdit.h|g" \
    plugins/listviewplugin/fancytreeview.ui

  qmake freediams.pro -r -config release \
    "CONFIG+=LINUX_INTEGRATED" \
    "INSTALL_ROOT_PATH=$pkgdir/usr/" \
    "LOWERED_APPNAME=freediams"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make install

  # Fix doc directories exec bit
  chmod a+x "$pkgdir/usr/share/freediams/doc/freediams/"{en,fr}/{html,}
}

# vim:set ts=2 sw=2 et:
