# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Sebastien Binet <binet@lblbox>

pkgname=python25-pip
pkgver=0.8.2
pkgrel=1
pkgdesc="An easy_install replacement for installing pypi python packages"
url="http://pypi.python.org/pypi/pip"
arch=('any')
license=('MIT')
depends=('python25' 'python25-distribute')
source=(http://pypi.python.org/packages/source/p/pip/pip-$pkgver.tar.gz
        python25.patch)
md5sums=('df1eca0abe7643d92b5222240bed15f6'
         '85c0c7fac490bd52fde2dcac777d2ce9')

build() {
  cd $srcdir/pip-$pkgver
  patch -p1 -i $srcdir/python25.patch
  python2.5 setup.py build
}

package() {
  cd $srcdir/pip-$pkgver
  python2.5 setup.py install --root=$pkgdir
  install -m 0644 -D docs/license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
