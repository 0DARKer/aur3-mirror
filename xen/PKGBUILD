# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Luceo
# Contributor: Revellion

pkgname=xen
pkgver=4.2.0
pkgrel=2
pkgdesc="Xen Hypervisor & Tools"
arch=(i686 x86_64)
url=http://xen.org
license=(GPL)
depends=(bin86 bridge-utils gnutls lzo2 python2 sdl)
[[ "$CARCH" == "x86_64" ]] && depends+=(lib32-glibc)
makedepends=(dev86 git iasl ocaml-findlib yajl)
optdepends=('xen-docs: Official Xen Documentation')
conflicts=(xen-4.2{,-testing-hg} xen-hg-unstable xen-gdbsx xen4-hg)
backup=(etc/$pkgname/xend-{config,pci-{permissive,quirks}}.sxp)
options=(!buildflags !strip)
install=$pkgname.install
source=(http://bits.xensource.com/oss-xen/release/$pkgver/$pkgname-$pkgver.tar.gz
    archinit.patch
    xend.service)
sha256sums=('43f4a086e4e0330145a27b7ace8365c42b5afbc95cefadafe067be91bd3e5cfb'
    '6c69da9b9b055f0e0d66316f23805d82219e5160dad01bf4996c83a808d1de97'
    'aef1bd1cf50eb8dcebaae73b1e21726a6d5b3f33c83faa033999e65fd5c72f75')
sha512sums=('4fb56c79d722fb307bc657f16d02079c6636427e7650c4354193632d38d2d1db8e588f844ff0ca6e757c108ed639a528565ec9fc7c00bb4d5b6fbc9d122d8a70'
    '0ed6eacd6c0a6484685f837ffbf4abf8ee3814f40877faf96228e34519867b3656d23db5d839d08ef1fec023d52c7bd0016d52bb88a16e73c7c80dc09575ee8e'
    '0036e5da6568bc281816de827e821de2cd2eb499fcd5546e32e6cbb16df52ba51d4eeded05e545c1bdf2a66b2033109382c6412ca2cbb61e2ffdc6434f6198aa')

build() {
    cd "$srcdir"/$pkgname-$pkgver/

    patch -Np1 -i ../archinit.patch

    ./autogen.sh
    ./configure PYTHON=/usr/bin/python2 --prefix=/usr
    make PYTHON=python2 DESTDIR="$pkgdir" install-xen
    make PYTHON=python2 DESTDIR="$pkgdir" install-tools

    # stubdom won't build with multiple makethreads
    make -j1 PYTHON=python2 DESTDIR="$pkgdir" install-stubdom

    sed -i ':XENDOM_CONFIG=/etc/:s:sysconfig/xendomains:conf.d/xendomains:' "$pkgdir"/etc/init.d/xendomains
    sed -i 's:touch /var/lock/subsys/xend:mkdir -p /var/lock/subsys\n       &:' "$pkgdir"/etc/init.d/xend

    ##### Begin $pkgdir cleanup #####
    cd "$pkgdir"

    if [[ -d usr/lib64 ]]; then
        cd usr/
        cp -r lib64/* lib/
        rm -rf lib64
    fi

    mv etc/{init,rc}.d
    rm -f usr/share/man/man1/qemu{,-img}.1*

    ##### Kill unwanted stuff #####
    # stubdom: newlib
    rm -rf usr/*-xen-elf

    # hypervisor symlinks
    rm -f boot/xen{,-4,-4.2}.gz

    # silly doc dir fun
    rm -rf usr/share/doc/xen
    rm -rf usr/share/doc/qemu

    # Pointless helper
    rm -f usr/sbin/xen-python-path

    # qemu stuff (unused or available from upstream)
    rm -rf usr/share/xen/man
    rm -rf usr/bin/qemu-*-xen
    for file in bios.bin openbios-sparc32 openbios-sparc64 ppc_rom.bin \
        pxe-e1000.bin pxe-ne2k_pci.bin pxe-pcnet.bin pxe-rtl8139.bin \
        vgabios.bin vgabios-cirrus.bin video.x openbios-ppc bamboo.dtb; do
        rm -f usr/share/xen/qemu/$file
    done

    # adhere to Static Library Packaging Guidelines
    rm -rf usr/lib/*.a
}
