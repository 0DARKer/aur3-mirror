# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Luceo
# Contributor: Revellion

pkgname=xen
pkgver=4.2.0
pkgrel=1
pkgdesc="Xen Hypervisor & Tools"
arch=(i686 x86_64)
url=http://xen.org
license=(GPL)
depends=(bin86 bridge-utils gnutls lzo2 pkgconfig python2 sdl)
[[ "$CARCH" == "x86_64" ]] && depends+=(lib32-glibc)
makedepends=(dev86 ocaml-findlib iasl)
optdepends=('xen-docs: Official Xen Documentation')
conflicts=(xen-4.2{,-testing-hg} xen-hg-unstable xen-gdbsx xen4-hg)
backup=(etc/$pkgname/xend-{config,pci-{permissive,quirks}}.sxp)
options=(!strip)
install=$pkgname.install
source=(http://bits.xensource.com/oss-xen/release/${pkgver}/xen-${pkgver}.tar.gz
    dom0_xz_decompression.patch
    archinit.patch
    xen.patch)
sha256sums=('43f4a086e4e0330145a27b7ace8365c42b5afbc95cefadafe067be91bd3e5cfb'
    '7bacc1316175079a74afd2f8350e2b65820168223418995b4d844b92908efa64'
    '2d94de6859d3cff82bbf708ef0cd237335239535c0267317867e917632d4bb5f'
    '4e9505e55589d6141466f9ab16c035382e099e78adc74e7e4cc30568818d84f6')
sha512sums=('4fb56c79d722fb307bc657f16d02079c6636427e7650c4354193632d38d2d1db8e588f844ff0ca6e757c108ed639a528565ec9fc7c00bb4d5b6fbc9d122d8a70'
    'b400e9ed9989d830354375b29a1a40a3eb893ccfbf2d3943ef590f3e4980e72ba4a81944368557d0af700792e8218e7a275741a0f0b389b8d36b57a7c0784ff7'
    '45d2497fd390754013633950fa4982a6e2fafacececed0315f4080be4a089cb5e1661a135865bcc1bb16452e04969581a678fb242a090a338e754be15f220eca'
    '37b095a0c2130033f76ac28f37c3a92551a12e0d97b0758fa89c327f3f7fbcf096c0125d7e37e1a69cea7ed01cd59a36dbf708e1b0191b773ca65b92ee192358')

build() {
    cd "$srcdir"/$pkgname-$pkgver/

    patch -Np1 -i ../archinit.patch
    patch -Np1 -i ../dom0_xz_decompression.patch
    patch -Np1 -i ../xen.patch

    unset CFLAGS LDFLAGS

    make PYTHON=python2 DESTDIR="$pkgdir" install-xen
    make PYTHON=python2 DESTDIR="$pkgdir" install-tools

    # stubdom won't build with multiple makethreads
    make -j1 PYTHON=python2 DESTDIR="$pkgdir" install-stubdom

    sed -i ':XENDOM_CONFIG=/etc/:s:sysconfig/xendomains:conf.d/xendomains:' "$pkgdir"/etc/init.d/xendomains
    sed -i 's:touch /var/lock/subsys/xend:mkdir -p /var/lock/subsys\n       &:' "$pkgdir"/etc/init.d/xend

    if [[ -d "$pkgdir"/usr/lib64 ]]; then
        cd "$pkgdir"/usr/
        cp -R lib64/* lib/;
        rm -R lib64;
    fi

    cd "$pkgdir"/etc/ && mv init.d rc.d
    rm -f "$pkgdir"/usr/share/man/man1/qemu{,-img}.1*

    ####### Kill unwanted stuff #######

    # stubdom: newlib
    rm -rf "$pkgdir"/usr/*-xen-elf

    # hypervisor symlinks
    rm -rf "$pkgdir"/boot/xen-4.1.gz
    rm -rf "$pkgdir"/boot/xen-4.gz
    rm -rf "$pkgdir"/boot/xen.gz

    # silly doc dir fun
    rm -rf "$pkgdir"/usr/share/doc/xen
    rm -rf "$pkgdir"/usr/share/doc/qemu

    # Pointless helper
    rm -f "$pkgdir"/usr/sbin/xen-python-path

    # qemu stuff (unused or available from upstream)
    rm -rf "$pkgdir"/usr/share/xen/man
    rm -rf "$pkgdir"/usr/bin/qemu-*-xen
    for file in bios.bin openbios-sparc32 openbios-sparc64 ppc_rom.bin \
        pxe-e1000.bin pxe-ne2k_pci.bin pxe-pcnet.bin pxe-rtl8139.bin \
        vgabios.bin vgabios-cirrus.bin video.x openbios-ppc bamboo.dtb; do
        rm -f "$pkgdir"/usr/share/xen/qemu/$file
    done

    # adhere to Static Library Packaging Guidelines
    rm -rf "$pkgdir"/usr/lib/*.a
}
