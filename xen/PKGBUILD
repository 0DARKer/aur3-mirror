#Maintainer: Luceo
#Contributer: Revellion
pkgname=xen
pkgver=4.1.3
pkgrel=3
pkgdesc="Xen Hypervisor & Tools"
arch=(i686 x86_64)
url="http://xen.org"
license="GPL"
depends=('bin86' 'xz' 'bzip2' 'iproute' 'bridge-utils' 'python2' 'sdl' 'zlib' 'e2fsprogs' 'pkgconfig' 'gnutls' 'lzo2' 'glibc')
[ "$CARCH" == "x86_64" ] && depends=(${depends} 'lib32-glibc')
optdepends=('xen-docs: Official Xen Documentation')
makedepends=('dev86' 'ocaml-findlib' 'iasl')
conflicts=('xen-4.2' 'xen-hg-unstable' 'xen-gdbsx' 'xen4-hg' 'xen4' 'xen3' 'xen-hv-tools' 'libxen4')
provides=('xen')
backup=('etc/xen/xend-config.sxp' 'etc/xen/xend-pci-permissive.sxp' 'etc/xen/xend-pci-quirks.sxp')
options=(!strip)
install=xen.install
source=(
	http://bits.xensource.com/oss-xen/release/${pkgver}/xen-${pkgver}.tar.gz
	dom0_xz_decompression.patch
	archinit.patch
	xen.patch
	)

build() {

	cd $srcdir/xen-${pkgver}

		patch -p1 -i ../archinit.patch
		patch -p1 -i ../dom0_xz_decompression.patch
		patch -p1 -i ../xen.patch

	unset CFLAGS LDFLAGS

		make PYTHON=python2 DESTDIR=$pkgdir install-xen
		make PYTHON=python2 DESTDIR=$pkgdir install-tools

		# stubdom won't build with multiple makethreads
		make -j1 PYTHON=python2 DESTDIR=$pkgdir install-stubdom

	sed -i 's#XENDOM_CONFIG=/etc/sysconfig/xendomains#XENDOM_CONFIG=/etc/conf.d/xendomains#' $pkgdir/etc/init.d/xendomains
	sed -i "s#touch /var/lock/subsys/xend#mkdir -p /var/lock/subsys\n	touch /var/lock/subsys/xend#" $pkgdir/etc/init.d/xend

	[ -d $pkgdir/usr/lib64 ] && ( cd $pkgdir/usr && cp -R lib64/* lib/ && rm -R lib64 )
	( cd $pkgdir/etc && mv init.d rc.d ) || return 1

	rm -f $pkgdir/usr/share/man/man1/qemu-img.1* \
	$pkgdir/usr/share/man/man1/qemu.1*

####### Kill unwanted stuff #######

# stubdom: newlib
rm -rf $pkgdir/usr/*-xen-elf

# hypervisor symlinks
rm -rf $pkgdir/boot/xen-4.1.gz
rm -rf $pkgdir/boot/xen-4.gz
rm -rf $pkgdir/boot/xen.gz

# silly doc dir fun
rm -rf $pkgdir/usr/share/doc/xen
rm -rf $pkgdir/usr/share/doc/qemu

# Pointless helper
rm -f $pkgdir/usr/sbin/xen-python-path

# qemu stuff (unused or available from upstream)
rm -rf $pkgdir/usr/share/xen/man
rm -rf $pkgdir/usr/bin/qemu-*-xen
for file in bios.bin openbios-sparc32 openbios-sparc64 ppc_rom.bin \
         pxe-e1000.bin pxe-ne2k_pci.bin pxe-pcnet.bin pxe-rtl8139.bin \
         vgabios.bin vgabios-cirrus.bin video.x openbios-ppc bamboo.dtb
do
	rm -f $pkgdir/usr/share/xen/qemu/$file
done

# adhere to Static Library Packaging Guidelines
rm -rf $pkgdir/usr/lib/*.a
  
}

md5sums=(
	'bed929d5c5e5135cab40e2a6aab73fa0'
	'c0d1ce9fcade14124b0356c6b512732b'
	'7a1ed81ecc828037724bb3280058c9fc'
	'f149bae1a6b420e49c51b9f3a74338a4'
	)
