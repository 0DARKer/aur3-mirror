# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wes Brown <wesbrown18@gmail.com>

pkgname=volatility
pkgver=2.3.1
pkgrel=1
pkgdesc="Advanced memory forensics framework"
url="https://code.google.com/p/volatility/"
arch=('any')
license=('GPL2')
depends=('python2-crypto')
makedepends=('python2-setuptools')
source=(https://${pkgname}.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
sha512sums=('0b41bfc85804049f1cdb9a57493bd35ca39d416c72706e0d15f3634d8cae71138c8ced3570f73f427635e27b02af79744bc3a14dde9f7999aa8f9dcda3854828')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/env python$/env python2/g' -i tools/vtype_diff.py
}

build() {
  cd ${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}

  python2 setup.py install -O1 --prefix=/usr --root="${pkgdir}"
  mv "${pkgdir}/usr/bin/vol.py" "${pkgdir}/usr/bin/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  mv "${pkgdir}/usr/tools" "${pkgdir}/usr/share/${pkgname}"
  mv "${pkgdir}/usr/contrib" "${pkgdir}/usr/share/${pkgname}"
  install -Dm 644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim: ts=2 sw=2 et:
