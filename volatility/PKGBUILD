# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wes Brown <wesbrown18@gmail.com>

pkgname=volatility
pkgver=2.4
pkgrel=1
pkgdesc="Advanced memory forensics framework"
url="https://code.google.com/p/volatility/"
arch=('any')
license=('GPL2')
depends=('python2-crypto')
makedepends=('python2-setuptools')
source=(http://downloads.volatilityfoundation.org/releases/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('19a10b735c0ec0d8ccbd246f2eac6968762f67f41da253e38d5026a4ee12ad9eef5a111039585afa6d6d3d67923f6e080ddc94ed98c9759c4f5486f506e0c295')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/env python$/env python2/g' -i tools/vtype_diff.py
}

build() {
  cd ${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}

  python2 setup.py install -O1 --prefix=/usr --root="${pkgdir}"
  mv "${pkgdir}/usr/bin/vol.py" "${pkgdir}/usr/bin/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  mv "${pkgdir}/usr/tools" "${pkgdir}/usr/share/${pkgname}"
  mv "${pkgdir}/usr/contrib" "${pkgdir}/usr/share/${pkgname}"
  install -Dm 644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim: ts=2 sw=2 et:
