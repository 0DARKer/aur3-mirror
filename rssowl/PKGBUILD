# Maintainer: Luca Bennati <lucak3 AT gmail DOT com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=rssowl
pkgver=2.1.4
pkgrel=1
pkgdesc="A java based RSS feed reader with Google Reader synchronization"
arch=('i686' 'x86_64')
url="http://rssowl.org/"
license=('GPL')
depends=('java-environment' 'alsa-lib' 'gtk2' 'gconf' 'libxt' 'nss')
install=rssowl.install

if [[ "$CARCH" = "x86_64" ]]; then
  depends+=('xulrunner192')
	source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.linux.x86_64.zip")
	md5sums=('c42da4c2f811029e47e6e225c7d96e34')
else
	source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.linux.x86.zip")
	md5sums=('0e8cd495b9fb7b2dc6887118890134a2')
fi
source+=("${pkgname}.desktop")
md5sums+=('8cdc03eb95a959e4196d1995783e24b1')

build() {
  cd "${srcdir}/${pkgname}"

  echo -e "#!/bin/sh\n\n/usr/share/${pkgname}/RSSOwl \"\${@}\"" > "${pkgname}"

  if [[ "$CARCH" = "x86_64" ]]; then
    sed -i -e 's:moz_libdir=.*:moz_libdir=/usr/lib/xulrunner-1.9.2:' xulrunner/xulrunner
  fi
}

package() {
  cd "${srcdir}/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  cp -r * "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
