# Maintainer: Luca Bennati <lucak3 AT gmail DOT com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=rssowl
pkgver=2.1.2
pkgrel=1
pkgdesc="A java based RSS feed reader with Google Reader synchronization"
arch=('i686' 'x86_64')
url="http://rssowl.org/"
license=('GPL')
depends=('java-environment' 'alsa-lib' 'gtk2' 'gconf' 'libxt' 'nss')

if [[ "$CARCH" = "x86_64" ]]; then
	source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.linux.x86_64.zip")
	md5sums=('50efeca005cc0594d7713434efd02811')
else
	source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.linux.x86.zip")
	md5sums=('a56b24d7ee7749b1630c8dce2c0ccc5c')
fi
source+=("${pkgname}.desktop")
md5sums+=('4e9dcbe25291435d3bd8396ed88a1d62')

build() {
  cd "${srcdir}/${pkgname}"

  echo -e "#!/bin/sh\n\n/usr/share/${pkgname}/RSSOwl" > "${pkgname}"
}

package() {
  cd "${srcdir}/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  cp -r * "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
