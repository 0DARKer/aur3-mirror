# $Id: PKGBUILD 49631 2009-08-14 06:17:04Z tobias $
# Maintainer: tobias [ tobias at archlinux org ]

pkgname=vim-cvs
pkgver=20091007
pkgrel=1
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
arch=(i686 x86_64)
license=('custom:vim')
url="http://www.vim.org"
depends=('gpm' 'perl' 'python')
makedepends=('wget' 'sed' 'grep' 'gettext' 'cvs' 'rsync')
conflicts=(vim)
provides=(vim)
source=(vimrc archlinux.vim
        pythoncomplete.vim::http://www.vim.org/scripts/download_script.php\?src_id=10872)
md5sums=('6228d36c3702d4e9afc4d2a1edcc3aff'
         '10353a61aadc3f276692d0e17db1478e'
         '6e7adfbd5d26c1d161030ec203a7f243')
backup=(etc/vimrc)

_cvsroot=":pserver:anonymous@vim.cvs.sf.net:/cvsroot/vim"
_cvsmod="vim7"

fetch() {
  # update source
  cd "$srcdir"
  msg "Connecting to $_cvsmod CVS server...."
  if [ -d $_cvsmod/CVS ]; then
    cd $_cvsmod
    cvs -z3 update -d
  else
    cvs -z3 -d $_cvsroot co -D $pkgver -f $_cvsmod
    cd $_cvsmod
  fi
  msg "CVS checkout done or server timeout"
  rm -rf "$srcdir/$_cvsmod-build"
  cp -r "$srcdir/$_cvsmod" "$srcdir/$_cvsmod-build"
  # update runtime
  cd "$srcdir/$_cvsmod-build/"
  rsync -avzcP --delete --exclude="dos" --exclude="spell" ftp.nluug.nl::Vim/runtime/ runtime
}

build()
{
  fetch

  cd "$srcdir/$_cvsmod-build"

  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' src/feature.h
  # build party
  ./configure --prefix=/usr --localstatedir=/var/lib/vim --mandir=/usr/share/man \
  --with-compiledby=ArchLinux --with-features=big \
  --enable-gpm --enable-acl --with-x=no --disable-gui \
  --enable-multibyte --enable-cscope \
  --enable-perlinterp --enable-pythoninterp

  make || return 1
}

package() {
  cd "$srcdir/$_cvsmod-build"
  make VIMRCLOC=/etc DESTDIR=${pkgdir} install

  cd "$pkgdir/usr/bin"
  rm ex view                # provided by (n)vi in core

  # delete some manpages
  cd ${pkgdir}/usr/share/man
  rm -f {*/,}man1/ex.1 {*/,}man1/view.1      # provided by (n)vi
  rm -f {*/,}man1/evim.1                     # this does not make sense in the console version

  # patch runtime
  cd $pkgdir/usr/share/vim/vim72/
  sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" filetype.vim
  cp $srcdir/pythoncomplete.vim autoload/pythoncomplete.vim

  install -Dm644 ${srcdir}/vimrc  ${pkgdir}/etc/vimrc
  install -Dm644 ${srcdir}/archlinux.vim  \
                 ${pkgdir}/usr/share/vim/vimfiles/archlinux.vim
  install -dm755 ${pkgdir}/usr/share/licenses/$pkgname
  cd ${pkgdir}/usr/share/licenses/$pkgname
  ln -s ../../vim/vim72/doc/uganda.txt license.txt
}

# vim:set ts=2 sw=2 et:
