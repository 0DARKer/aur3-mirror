# Contributor: TDY <tdy@gmx.com>
# Contributor: adriano <adriano.src@gmail.com>

pkgname=foxkit
pkgver=0.0.5
pkgrel=6
pkgdesc="A QtWebKit web browser"
arch=('i686' 'x86_64')
url="http://code.google.com/p/foxkit/"
license=('GPL')
depends=('kdebase-runtime')
makedepends=('automoc4' 'cmake')
source=(http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.gz
        $pkgname-$pkgver-x86_64.diff)
md5sums=('18f7d959c971cb5e4772966e2614c0db'
         'a3f560b2b49d21de83329776801a36c1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [[ $CARCH == x86_64 ]] && patch -Np1 -i ../$pkgname-$pkgver-$CARCH.diff
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -Dm644 src/foxkitui.rc "$pkgdir/opt/kde/share/apps/foxkit/foxkitui.rc"
}

# vim:set ts=2 sw=2 et:
