# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: IncredibleLaser
# Contributor: zuf <kontakt.zuf@gmail.com>
# Contributor: agapito <agapitofrndez@ozu.es>

pkgname=mlt-qt5
pkgver=0.9.2
pkgrel=1
pkgdesc="An open source multimedia framework, designed and developed for television broadcasting. Special build with Qt5 Support"
arch=(i868 x86_64)
url="http://www.mltframework.org/"
license=('GPL')
makedepends=('ladspa' 'frei0r-plugins' 'libdv' 'sdl_image' 'libsamplerate' 'sox' 'ffmpeg' 'vid.stab' 'qt5-svg' 'jack2' 'libexif')
optdepends=('sdl_image: for SDL plugin'
            'libsamplerate: for libavresample plugin'
            'sox: for SOX (Audio Swiss Army Knife) plugin'
            'ffmpeg: for ffmpeg plugin'
            'vid.stab: for video stabilize plugin'
            'qt5-svg: for Qt5 plugins'
            'jack2: for JACK sound output plugin'
            'libexif: for auto rotate plugin')
conflicts=('mlt' 'mlt++')
provides=('mlt')
source=("http://downloads.sourceforge.net/sourceforge/mlt/mlt-${pkgver}.tar.gz"
        'fix-segmentation-fault-at-exit-by-calling-av_lockmgr.patch')
sha1sums=('eb1cdb8a1d9e69512716431054e5da7eb3bedb6d'
          '6e44c172eeed329386a5e22ce23c86cf9ae9885d')

prepare() {
  cd "mlt-${pkgver}"
  patch -p1 -i ../fix-segmentation-fault-at-exit-by-calling-av_lockmgr.patch
}

build() {
  cd "mlt-${pkgver}"
  ./configure --prefix=/usr \
    --enable-glp \
    --enable-glp3 \
    --enable-opengl \
    --disable-gtk2 \
    --avformat-vdpau \
    --qt-libdir=/usr/lib \
    --qt-includedir=/usr/include/qt
  make
}

package() {
  make -C "mlt-${pkgver}" DESTDIR="${pkgdir}" install
}
