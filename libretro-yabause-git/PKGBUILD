# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-yabause-git
pkgver=2418.fd35ac9
pkgrel=1
pkgdesc="libretro implementation of Yabause. (Sega Saturn)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/yabause"
license=('GPL')
makedepends=('git')

_gitname=yabause
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/yabause_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}/libretro"
  make
}

package() {
  install -Dm644 "${_gitname}/libretro/yabause_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-yabause.so"
  install -Dm644 "yabause_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-yabause.info"
}
