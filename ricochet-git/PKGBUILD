# Maintainer: Boohbah <boohbah at gmail.com>
# Contributor: Mikkel Kroman <mk@maero.dk>

pkgname=ricochet-git
pkgver=1.0.1.r42.g04e0536
pkgrel=2
pkgdesc="An experiment with a different kind of instant messaging (git version)"
arch=('i686' 'x86_64')
url="https://ricochet.im/"
license=('custom')
depends=('tor' 'qt5-declarative')
makedepends=('git' 'qt5-tools')
source=("${pkgname}::git+https://github.com/ricochet-im/ricochet.git")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

build() {
  cd "${pkgname}"
  qmake CONFIG+=release DEFINES+=RICOCHET_NO_PORTABLE
  make
}

package() {
  cd "${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  INSTALL_ROOT="${pkgdir}" make install
}
