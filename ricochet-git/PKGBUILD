# Maintainer: Boohbah <boohbah at gmail.com>

pkgname=ricochet-git
pkgver=1.0.1.r42.g04e0536
pkgrel=1
pkgdesc="Anonymous peer-to-peer instant messaging"
arch=('i686' 'x86_64')
url="https://ricochet.im/"
license=('custom')
depends=('tor' 'qt5-declarative')
makedepends=('git' 'qt5-tools')
source=("${pkgname}::git://github.com/ricochet-im/ricochet")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

build() {
  cd "${pkgname}"
  qmake DEFINES+=RICOCHET_NO_PORTABLE
  make
}

package() {
  cd "${pkgname}"
  install -Dm755 ricochet "${pkgdir}/usr/bin/ricochet"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 src/ricochet.desktop \
    "${pkgdir}/usr/share/applications/ricochet.desktop"
}
