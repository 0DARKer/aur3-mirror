pkgname=mingw-w64-icu
pkgver=53.1
pkgrel=1
pkgdesc="International Components for Unicode library (mingw-w64)"
arch=('any')
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!buildflags' 'staticlibs' '!strip')
source=("http://download.icu-project.org/files/icu4c/${pkgver}/icu4c-${pkgver//./_}-src.tgz"
        '0004-move-to-bin.mingw.patch'
        '0007-actually-move-to-bin.mingw.patch'
        '0008-data-install-dir.mingw.patch'
        '0009-fix-bindir-in-config.mingw.patch'
        '0011-sbin-dir.mingw.patch'
        '0012-libprefix.mingw.patch'
        '0013-dont-short-names.mingw.patch'
        '0013-mingwize-pkgdata.mingw.patch'
        '0014-debug.mingw.patch')


md5sums=('b73baa6fbdfef197608d1f69300919b9'
         '0a85f663f96122bfe969cce8bfc60645'
         '0dac864b7397c0ad084265bcf4b6a41a'
         '1e083d7d23caa03553b6d31687374226'
         '0a1f606c34503400014a9935a1b5aecd'
         'dec4498c62c8cc91df1181630968f82c'
         '546e1e985f6e751e3c91d97e1082dd81'
         '0ea224ab4ce86e48fa3721ddabc3fe45'
         '90545b29de72d7e96f63456907f1f66b'
         '5d6b2659bc767f5757b11a855b36dd9f')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/icu/source"
  # http://pkgs.fedoraproject.org/cgit/mingw-icu.git
  patch -Np1 -i "$srcdir"/0004-move-to-bin.mingw.patch
  patch -Np1 -i "$srcdir"/0007-actually-move-to-bin.mingw.patch
  patch -Np1 -i "$srcdir"/0008-data-install-dir.mingw.patch
  patch -Np1 -i "$srcdir"/0009-fix-bindir-in-config.mingw.patch
  patch -Np1 -i "$srcdir"/0011-sbin-dir.mingw.patch
  patch -Np1 -i "$srcdir"/0012-libprefix.mingw.patch
  patch -Np1 -i "$srcdir"/0013-dont-short-names.mingw.patch
  patch -Np1 -i "$srcdir"/0013-mingwize-pkgdata.mingw.patch
  patch -Np1 -i "$srcdir"/0014-debug.mingw.patch
  autoreconf -vfi
}

build() {
  cd "$srcdir/icu/source"
  mkdir -p nativebuild && pushd nativebuild
  CFLAGS=-fno-stack-protector
  ../configure --enable-static --disable-shared
  make
  popd
  for _arch in ${_architectures}; do
    unset CFLAGS LDFLAGS
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --with-cross-build=$PWD/../nativebuild \
      --with-data-packaging=library \
      --enable-static
    make
    popd
  done
}

package() {
	mkdir -p "$pkgdir/usr/bin"
  for _arch in ${_architectures}; do
    cd "$srcdir/icu/source/build-${_arch}"
    make install DESTDIR="$pkgdir"
    rm -rf "$pkgdir"/usr/${_arch}/share
    rm "$pkgdir"/usr/${_arch}/bin/*.exe
    mv "$pkgdir"/usr/${_arch}/lib/*.dll "$pkgdir"/usr/${_arch}/bin
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    ln -s "/usr/${_arch}/bin/icu-config" "$pkgdir/usr/bin/${_arch}-icu-config"
  done
}