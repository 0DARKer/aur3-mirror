# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=blue
pkgver=1.7.5
pkgrel=1
pkgdesc="A unique, dynamic programming language with a minimalistic, intuitive syntax"
arch=('i686' 'x86_64')
url="http://www.lechak.info/blue/"
license=('GPL')
depends=('freetype2' 'libpng' 'mesa')
source=(http://www.lechak.info/$pkgname/$pkgname$pkgver.tgz)
md5sums=('6a0e31bcc5d4a4c2a0633631a4569e30')

build() {
  cd "$srcdir/$pkgname"
  sed -i 's#CFLAGS= -Wall -O3#CFLAGS+=#; /ln/ d' Makefile
  make || return 1
}

package() {
  cd "$srcdir/$pkgname"
  make prefix="$pkgdir/usr/lib" install
  install -dm755 "$pkgdir/usr/bin"
  ln -sf ../lib/$pkgname/$pkgname "$pkgdir/usr/bin/$pkgname"
}
