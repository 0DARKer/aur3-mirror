# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=mididings
pkgver=20120312
pkgrel=1
pkgdesc="A MIDI router and processor based on Python, supporting ALSA and JACK MIDI."
arch=('i686' 'x86_64')
url="http://das.nasophon.de/mididings/"
license=('GPL')
depends=('boost-libs' 'python2' 'jack' 'libsmf')
makedepends=('boost')
optdepends=('dbus-python: to send DBUS messages'
            'pyliblo: to send or receive OSC messages'
            'pyinotify: to automatically restart')
source=("http://das.nasophon.de/download/$pkgname-$pkgver.tar.gz")
md5sums=('139e46ff590bcf7136c2820075a972f3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build --enable-smf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --prefix=/usr --root=$pkgdir

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 doc/*.* \
    "$pkgdir/usr/share/doc/$pkgname"

  # examples
  install -d "$pkgdir/usr/share/$pkgname/examples"
  install -Dm644 doc/examples/* \
    "$pkgdir/usr/share/$pkgname/examples"
}