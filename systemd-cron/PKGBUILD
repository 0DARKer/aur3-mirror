# Maintainer: Dwayne Bent <dbb@dbb.io>
pkgname=systemd-cron
pkgver=1.3.1
pkgrel=3
pkgdesc='systemd units to run cron scripts'
arch=(any)
url='https://github.com/systemd-cron/systemd-cron'
license=('MIT')
depends=('systemd>=212' 'run-parts' 'python2')
provides=('cron')
conflicts=('cron')
source=("https://github.com/dbent/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('499183f671c280dfa585077fab9782c87467aee16cce05d78b35b3175c8acd305b987a35050f0b821816ea2d95c111effc087583d4fda531d432275976e03bdc')

prepare() {
    # /usr/bin/python is Python 3 on Arch Linux
    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' "$srcdir/$pkgname-$pkgver"/src/bin/*
}

build() {
    cd "$srcdir/$pkgname-$pkgver"

    ./configure --prefix=/usr --confdir=/etc --statedir=/var/spool/cron --enable-yearly --enable-persistent
    make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install

    install -d "$pkgdir"/etc/cron.{boot,hourly,daily,weekly,monthly,yearly}
    install -d "$pkgdir"/var/spool/cron
}

