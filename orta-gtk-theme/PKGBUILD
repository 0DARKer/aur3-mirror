# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: twa022 <twa022@gmail.com>
# Contributor: Adria Arrufat <swiftscythe@gmail.com>

pkgname=orta-gtk-theme
pkgver=1.4.1
pkgrel=9
pkgdesc="Orta GTK+, Metacity, Xfwm4 and more themes"
arch=(any)
url="http://skiesofazel.deviantart.com/art/Orta-184118297"
license=(GPL)
depends=(python-gobject gksu)
conflicts=(orta-gtk3-theme)
options=(!emptydirs)
install=orta.install
DLAGENTS=('http::/usr/bin/wget -c -t 3 --waitretry=3 -H -U Mozilla -O %o %u')
source=(http://www.deviantart.com/download/184118297/orta_by_skiesofazel-d31mal5.zip
    orta.desktop
    orta-install-chrome-themes
    OrtaSettingsManager.py)
sha256sums=('f2c0709475ccaf057c0c3252e88b0c57e45735eb557014d2e729fc7aaa85f619'
    '0c048e234eb737433d8fd68e66ded5946f9c2de5ea5d298e4d7a9e96705612d0'
    '6dbb211106f2ed5a8c6d20de4e3f3ef88fcdf305a755c2beee7481a643a38cf1'
    '231c5376644c28bb8b3f050939319e1a4718a3c7b892092ac28dc6ffe9a18414')
sha512sums=('2f5ea88c399acc56b0130118c7cb87be92cb6311979335578abb7fe6dd87845f51f280b1264a097a75dfb46837450e5072045df76b8201af753564521147fcbb'
    'f8ac44b9072de3d5f08fee53c2bda129ba35b2302fe2d2411d9908656bb3aaea5b53e2361e6d17b5e947bab80a5a7c0f4b17b4a1d41b7343d73b7ed48a5b493f'
    '5cbcb8b8602b21e49366105d6470d8754664cfbd4a3310400013467556cf304fa955934daa52e133f39661fb982c7264773bed851067e2b734609e3ce3ef9a53'
    '873553dc8a3e68dd25f26c8d133d97d692c70aee6573c1f7d57d4f7567ba60329535b9efc7c16a54e2ecd848e886a3d300f6b7b2368234a3e958c452f0a4f67c')

package() {
    install -d $srcdir/$pkgname/
    tar xf extractme.tar.gz -C $srcdir/$pkgname/
    cd $srcdir/$pkgname/

    msg "Installing AWN themes.."
    install -d $pkgdir/usr/share/avant-window-navigator/themes/
    tar xf Awn/OrtaAWN-0.2.0.tgz -C $pkgdir/usr/share/avant-window-navigator/themes/

    msg "Installing Emerald themes.."
    cd Emerald/
    for i in *.emerald; do 
	_folder=$pkgdir/usr/share/emerald/themes/$(echo $i | sed 's:\.emerald$::')
	install -d $_folder
	tar xf $i -C $_folder
    done
    cd ../

    msg "Installing Chromium theme files..."
    find Chromium/ -name '*.crx' -execdir install -Dm644 '{}' $pkgdir/usr/share/themes/orta-chromium-themes/'{}' \;
    install -Dm755 $srcdir/orta-install-chrome-themes $pkgdir/usr/bin/orta-install-chrome-themes

    msg "Installing Settings Manager..."
    install -Dm755 $srcdir/OrtaSettingsManager.py $pkgdir/usr/lib/orta/OrtaSettingsManager.py
    cp -r orta/ $pkgdir/usr/lib/orta/
    file_perms $pkgdir/usr/lib/orta/orta/
    ln -s /usr/lib/orta/OrtaSettingsManager.py $pkgdir/usr/bin/orta-settings-manager

    msg "Installing GTK Themes..."
    install -d $pkgdir/usr/share/themes/
    for i in Orta{,-Old,-Old-Squared,-Squared}.tar.gz; do
	tar xf $i -C $pkgdir/usr/share/themes/
	install -Dm644 $i $pkgdir/usr/lib/orta/$i
    done
    file_perms $pkgdir/usr/share/themes/

    ## Desktop file (shortcut)
    desktop-file-install $srcdir/orta.desktop --dir=$pkgdir/usr/share/applications/

    ## Fixing incorrect file ownerships
    chown -R root:root $pkgdir/usr/
}


file_perms() {
    find $1 -type d -exec chmod 755 '{}' \;
    find $1 -type f -exec chmod 644 '{}' \;
}
