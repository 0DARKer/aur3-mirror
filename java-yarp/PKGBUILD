# Contributor: Victor NoÃ«l

_realpkgname=yarp
pkgname=java-$_realpkgname
pkgver=2.3.4
pkgrel=1
pkgdesc="Yet Another Robot Platform"
url="http://eris.liralab.it/yarp/"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('cmake' 'swig' 'jdk')
depends=('yarp' 'jre' 'gcc-libs')
options=(!libtool)
source=(http://downloads.sourceforge.net/yarp0/$_realpkgname-$pkgver.tar.gz)

build() {
  cd $srcdir/$_realpkgname-$pkgver/example/swig

  cd $srcdir/

  rm -rf build-$pkgname-$pkgver
  mkdir build-$pkgname-$pkgver
  cd build-$pkgname-$pkgver

  # CXX_FLAGS is to prevent jni crash... (see the README file in swig dir)
  cmake ../$_realpkgname-$pkgver/example/swig/ -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_FLAGS="-fno-strict-aliasing" -DCREATE_JAVA=TRUE
  make

  mkdir classes
  javac -d classes generated_src/*.java
  cd classes
  jar cf ../$pkgname-$pkgver.jar yarp
}

package() {
  cd $srcdir/build-$pkgname-$pkgver

  # no install script...
  mkdir -p $pkgdir/usr/share/doc/yarp/
  mkdir -p $pkgdir/usr/lib/
  cp libjyarp.so $pkgdir/usr/lib/
  cp $pkgname-$pkgver.jar $pkgdir/usr/share/doc/yarp/
}

md5sums=('00a2b21ab6e6469bfc99fedf00a74c0f')
