# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kde-gtk-config-git
pkgver=r343.7d0e123
pkgrel=1
pkgdesc='GTK2 and GTK3 Configurator for KDE'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/base/kde-gtk-config'
license=('LGPL')
depends=('kcmutils' 'knewstuff')
makedepends=('extra-cmake-modules' 'git' 'gtk2' 'gtk3')
conflicts=('kde-gtk-config-frameworks')
provides=('kde-gtk-config-frameworks')
source=('git://anongit.kde.org/kde-gtk-config.git')
md5sums=('SKIP')

pkgver() {
  cd kde-gtk-config
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kde-gtk-config \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
