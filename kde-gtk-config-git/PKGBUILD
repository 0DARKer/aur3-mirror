# Maintainer: Jekyll Wu <adaptee at gmail [dot] com>
# Contributor: vicky91 <vickypaiers@gmail.com>

_gitname='kde-gtk-config'
pkgname=kde-gtk-config-git
pkgver=315.e3a31c2
pkgrel=1
pkgdesc="GTK2 and GTK3 Configurator for KDE"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kde-gtk-config"
license=('GPL')
depends=('kdebase-runtime' )
makedepends=('git' 'automoc4' 'cmake' 'gettext' 'gtk2' 'gtk3')
conflicts=('chakra-gtk-config' 'chakra-gtk-config-git' 'kde-gtk-config')
options=(!strip)
install=${pkgname}.install
source=("${_gitname}::git://anongit.kde.org/kde-gtk-config")
sha1sums=('SKIP')


pkgver() {
  cd "${srcdir}/${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/${_gitname}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=debugfull -DQT_QMAKE_EXECUTABLE=qmake-qt4 .
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}/" install
}
