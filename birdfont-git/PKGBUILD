pkgname=birdfont-git
pkgver=r1206.feb09dd
pkgrel=2

pkgdesc='A font editor that lets you create vector graphics and export TTF, EOT & SVG fonts.'
url='http://birdfont.org/'
arch=('i686' 'x86_64')
license=('GPL3')

depends=('webkitgtk2' 'libgee')
makedepends=('git' 'vala' 'python2')

install='birdfont-git.install'

source=('git+https://git.gitorious.org/birdfont/birdfont.git'
        'http://www.unicode.org/Public/UNIDATA/NamesList.txt'
        'use_xdg_config_home.patch')

md5sums=('SKIP'
         'a07e68e9cc6fb9c5001727d2813930ab'
         'c6d2ab7e61a7db249e64a81878db4207')

pkgver() {
    cd birdfont
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git describe --always)"
}

prepare() {
    cd birdfont
    # Yet another project which claims to be "python" (i.e. supports both) but
    # isn't.  Yippe
    sed -i '1 s/python/&2/' configure install.py

    # Lets not create a directory as 1755 (stickybit) in the users home and
    # instead use XDG_CONFIG_HOME (HOME/.config) like sane people
    patch -Np1 -i "$srcdir"/use_xdg_config_home.patch
}

build() {
    cd birdfont
    ./configure --prefix=/usr
    ./scripts/linux_build.py --prefix=/usr
}

package() {
    cd birdfont
    ./install.py --dest="$pkgdir" --manpages-directory=/share/man --libdir=/lib
    install -Dm644 "$srcdir"/NamesList.txt "$pkgdir"/usr/share/unicode/ucd/NamesList.txt
}
