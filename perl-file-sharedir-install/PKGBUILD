# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: AUR Perl <aurperl@juster.info>

pkgname=perl-file-sharedir-install
_pkgname=File-ShareDir-Install
pkgver=0.04
pkgrel=2
pkgdesc="File::ShareDir::Install - Install shared files"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url="http://search.cpan.org/dist/$_pkgname"
source=(http://search.cpan.org/CPAN/authors/id/G/GW/GWYN/$_pkgname-$pkgver.tar.gz)
sha512sums=('92faba6ae97235f9fea3a645daee863d25e886773ff9f6ef3d5529294e7f8f901956da2300d004b0602c71bc8d28fd63798357e90a0ed3eb1a228f576f1df4e4')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/$_pkgname-$pkgver"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
