# Maintainer: Jamie Nguyen <jamie AT tomoyolinux.co.uk>

pkgname=ccs-tools
_basever=1.8.0
_timestamp=20110214
pkgver=${_basever}.${_timestamp}
pkgrel=3
pkgdesc='TOMOYO Linux 1.8.x userspace tools'
arch=('i686' 'x86_64')
url='http://tomoyo.sourceforge.jp/'
license=('GPL')
depends=('ncurses')
install=ccs-tools.install
source=("http://sourceforge.jp/frs/redir.php?f=/tomoyo/49693/${pkgname}-${_basever}-${_timestamp}.tar.gz"
		"ccs-auditd.rc.d"
		"tomoyo.logrotate")

build() {
  cd "${srcdir}/ccstools"

  # honor cflags
  sed -i -e '/CFLAGS/d' Include.make
  echo 'CFLAGS          := ${CFLAGS}' >> Include.make

  make -j1 || return 1
}

package() {
  cd "${srcdir}/ccstools"

  make INSTALLDIR="${pkgdir}" install || return 1
  rm "${pkgdir}/usr/lib/ccs/COPYING.ccs"

  # place example programs in /usr/share/ccs/examples
  install -m755 -d "${pkgdir}/usr/share/ccs/examples"
  for i in examples/*.c; do
	  install -m644 ${i} "${pkgdir}/usr/share/ccs/${i}"
  done

  # install daemon
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/ccs-auditd.rc.d" "${pkgdir}/etc/rc.d/ccs-auditd"

  # install logrotate file
  install -m755 -d "${pkgdir}/etc/logrotate.d"
  install -m644 "${srcdir}/tomoyo.logrotate" "${pkgdir}/etc/logrotate.d/tomoyo"

  # create log folder
  install -m755 -d "${pkgdir}/var/log/tomoyo"
}
sha256sums=('4ffec375b335477b6aadb352c6a749ee09f283eb58d991420ac74e12037cc670'
            'f79996cdd7f6d78d5ad1101113f99a85ae5cb77647ddee73c65c71f95643179f'
            'd88cdc21bb68a7e2abf0512f10dc56e57db7ca172e179d0fb50998ccf3caf648')
