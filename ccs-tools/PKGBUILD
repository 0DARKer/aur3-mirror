# Maintainer: Jamie Nguyen <jamie tomoyolinux co uk>

pkgname=ccs-tools
_basever=1.8.3
_timestamp=20111025
pkgver=${_basever}.${_timestamp}
pkgrel=1
pkgdesc='TOMOYO Linux 1.8.x userspace tools'
arch=('i686' 'x86_64')
url='http://tomoyo.sourceforge.jp/'
license=('GPL')
depends=('ncurses')
conflicts=('tomoyo-tools')
install=ccs-tools.install
_url_ccs="http://sourceforge.jp/frs/redir.php?f=/tomoyo/49693"
source=("${_url_ccs}/${pkgname}-${_basever}-${_timestamp}.tar.gz"
        "ccs-auditd.rc.d"
        "ccs-auditd.service"
        "tomoyo.logrotate")

build() {
  cd "${srcdir}/ccstools"

  # honour cflags
  sed -i -e '/CFLAGS/d' Include.make
  echo 'CFLAGS          := ${CFLAGS}' >> Include.make

  make || return 1
}

package() {
  cd "${srcdir}/ccstools"

  make INSTALLDIR="${pkgdir}" install || return 1

  # place example programs in /usr/share/ccs/examples
  install -m755 -d "${pkgdir}/usr/share/ccs/examples"
  for i in examples/*.c; do
    install -m644 ${i} "${pkgdir}/usr/share/ccs/${i}"
  done

  # install daemon
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/ccs-auditd.rc.d" "${pkgdir}/etc/rc.d/ccs-auditd"

  # install systemd service
  install -m755 -d "${pkgdir}/lib/systemd/system"
  install -m644 "${srcdir}/ccs-auditd.service" \
	  "${pkgdir}/lib/systemd/system/ccs-auditd.service"

  # install logrotate file
  install -m755 -d "${pkgdir}/etc/logrotate.d"
  install -m644 "${srcdir}/tomoyo.logrotate" "${pkgdir}/etc/logrotate.d/tomoyo"

  # create log folder
  install -m700 -d "${pkgdir}/var/log/tomoyo"
}

sha256sums=('295676d3e8171970b0e1bd678d09112943f21cbe7ac5d212c4a3d8d163adca01'
            'a055ffcf6ca13c77d03883ce30379adfa0540b09985d1bc57d89748d7bd669d6'
            '12ed66ea44d931e61b07d4436f5f9c6d89c01aab091560206f2a710c3a5981c7'
            'd88cdc21bb68a7e2abf0512f10dc56e57db7ca172e179d0fb50998ccf3caf648')
