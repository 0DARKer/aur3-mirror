# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=chibitracker
pkgver=1.2
pkgrel=2
pkgdesc="A portable Impulse Tracker clone"
arch=('i686' 'x86_64')
url="http://chibitracker.berlios.de"
license=('GPL2')
depends=('gcc-libs' 'sdl')
makedepends=('scons' 'unzip')
source=(http://download.berlios.de/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        $pkgname.desktop $pkgname.sh)
md5sums=('1dfd7c4b7ffbc7d1377393d040cd5433'
         '197bdef02ae24e4c4534c3d1fb1e1dd0'
         'd89671948da85245d25838db736d5070')

if [ "$CARCH" = "x86_64" ]; then
   makedepends[${#makedepends[@]}]='patch'
   source[${#source[@]}]='chibitracker-1.1-amd64.patch'
   md5sums[${#md5sums[@]}]='c814ecb20737afaa10100ccaa78db1bf'
fi
   

build() {
  cd "$srcdir"/$pkgname-$pkgver
  if [ "$CARCH" = "x86_64" ]; then
     patch -Np0 -i $srcdir/chibitracker-1.1-amd64.patch || return 1
  fi
  scons prefix=/usr || return 1
  cd skins
  for x in *.zip; do unzip -q $x && rm $x; done
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  install -d $pkgdir/usr/share/${pkgname}/skins
  for x in skins/*; do
      install -d $pkgdir/usr/share/$pkgname/$x
      install -m644 $x/* $pkgdir/usr/share/$pkgname/$x/
  done
  install -m755 program/$pkgname $pkgdir/usr/share/$pkgname/
  install -Dm755 $srcdir/$pkgname.sh $pkgdir/usr/bin/$pkgname
  install -Dm644 program/cticon.png $pkgdir/usr/share/pixmaps/$pkgname.png
  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  #scons install prefix="$pkgdir"/usr || return 1
}

