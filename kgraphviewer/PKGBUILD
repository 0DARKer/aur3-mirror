# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
pkgname=kgraphviewer
pkgver=2.1.1
pkgrel=4
pkgdesc="A Graphviz dot graph file viewer for KDE"
arch=('i686' 'x86_64')
url="http://www.kde.org/applications/graphics/kgraphviewer/"
license=('GPL' 'FDL')
depends=('kdebase-runtime' 'kdepimlibs' 'graphviz')
makedepends=('cmake' 'automoc4' 'boost' 'docbook-xsl')
install=kgraphviewer.install
source=("https://api.opensuse.org/public/source/home:milianw:kdeapps/${pkgname}/${pkgname}-${pkgver}.tar.gz" \
        "01-fix_parser.diff" "02-port_to_cgraph_library.diff" "03-port_to_kpluginfactory.diff" "04-remove_boost_includes.diff") 
md5sums=('6cc548221d8a741d13929993615c00f3'
         'c3e1a1db48232dc271e8c13662d92216'
         '2ec96f67ea6f288a20628ff6ad917499'
         '9a6988567abef34373287ff6295fbff7'
         'd8b6472db81e71c0f66637de86328d63')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 < "$srcdir/01-fix_parser.diff"
  patch -Np1 < "$srcdir/02-port_to_cgraph_library.diff"
  patch -Np1 < "$srcdir/03-port_to_kpluginfactory.diff"
  patch -Np1 < "$srcdir/04-remove_boost_includes.diff"
}

build(){
  cd "$srcdir"

  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
}
