# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
pkgname=kgraphviewer
pkgver=2.1.90
pkgrel=1
pkgdesc="A Graphviz dot graph file viewer for KDE"
arch=('i686' 'x86_64')
url="http://www.kde.org/applications/graphics/kgraphviewer/"
license=('GPL' 'FDL')
depends=('kdebase-runtime' 'kdepimlibs' 'graphviz')
makedepends=('cmake' 'automoc4' 'boost' 'docbook-xsl')
install=kgraphviewer.install
source=("http://master.kde.org/unstable/kgraphviewer/$pkgver/src/kgraphviewer-${pkgver}.tar.xz")
md5sums=('b3a18cbaf661d1cf186b3a3674c31186')

build(){
  cd "$srcdir"

  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
}
