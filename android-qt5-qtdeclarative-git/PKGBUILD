# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=android-qt5-qtdeclarative-git
pkgver=5.1.1_4627.e171d1e
pkgrel=1
pkgdesc="Qt 5 for Android: qtdeclarative module (QtQuick 2, QML)"
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/Qt5ForAndroid"
license=('LGPL')
groups=('qt-android' 'qt5-android')
depends=('android-qt5-qtbase-git' 'android-qt5-qtxmlpatterns-git' 'android-qt5-qtjsbackend-git')
makedepends=('gcc' 'git')
options=('!libtool' '!strip')
source=("${pkgname}::git://gitorious.org/qt/qtdeclarative.git")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${pkgname}
  _basever=$(grep MODULE_VERSION .qmake.conf | awk -- '{print $3}')
  echo ${_basever}_$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  #git describe --always | sed 's|-|_|g' | sed 's|^v||'
}

build() {
  cd ${srcdir}/${pkgname}
  
  /opt/android-qt5/bin/qmake
  make
}

package() {
  cd ${srcdir}/${pkgname}
  
  make INSTALL_ROOT="${pkgdir}" install
    
  # Strip by hand, system strip doesn't work on arm binaries

  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/lib/*.so* \
    ${pkgdir}/opt/android-qt5/plugins/qmltooling/*.so \
    ${pkgdir}/opt/android-qt5/qml/*/*.so \
    ${pkgdir}/opt/android-qt5/qml/*/*/*.so \
    ${pkgdir}/opt/android-qt5/qml/*/*/*/*.so
  # linux static libs in lib
  strip ${STRIP_STATIC} ${pkgdir}/opt/android-qt5/lib/*.a
}
