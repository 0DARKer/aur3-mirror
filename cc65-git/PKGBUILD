# Contributor: queue <queueRAM@gmail.com>

pkgname=cc65-git
pkgver=2.14.r1673a40
pkgrel=1
pkgdesc="C compiler for 6502 family microprocessors"
depends=('glibc')
arch=('i686' 'x86_64')
license=('custom')
url="http://cc65.github.io/cc65/"
source=('git://github.com/cc65/cc65.git')
conflicts=('cc65')
provides=('cc65')
md5sums=('SKIP')

_gitname="cc65"

pkgver() {
  # no tags in git repo, so "git describe" just errors
  cd "$srcdir/${_gitname}/src/common"
  _vermajor=`grep "#define VER_MAJOR" version.c | sed -e 's/#define\s\+VER_MAJOR\s\+\([0-9]\+\).*/\1/'`
  _verminor=`grep "#define VER_MINOR" version.c | sed -e 's/#define\s\+VER_MINOR\s\+\([0-9]\+\).*/\1/'`
  _gitrev=`git show --pretty="format:%h"`
  echo "${_vermajor}.${_verminor}.r${_gitrev}"
}

build() {
  cd "$srcdir/${_gitname}"
  make
}

package() {
  cd "$srcdir/${_gitname}"
  make prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
