# $Id$
# Maintainer: monty <linksoft [at] gmx [dot] de>

_pkg=mongo-cxx-driver
_subpkg=legacy-0.0-26compat
pkgname=${_pkg}-${_subpkg}
pkgver=2.6.1
pkgrel=1
pkgdesc='Official MongoDB C++ driver (26compat).'
arch=('i686' 'x86_64')
url='http://www.mongodb.org'
license=(APACHE)
depends=('boost-libs' 'pcre' 'snappy' 'openssl' 'gperftools' 'libsasl')
makedepends=('scons' 'boost' 'readline' 'ncurses')
source=("https://github.com/mongodb/${_pkg}/archive/${_subpkg}-${pkgver}.tar.gz")
conflicts=("mongodb<2.6.0")
options=("staticlibs")
provides=("mongo-cxx-driver")

build() {
  cd ${pkgname}-${pkgver}

  scons \
    --use-system-boost \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --ssl \
    --sharedclient
}

package() {
  cd ${pkgname}-${pkgver}
  
  scons \
    --full \
    --use-system-boost \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --ssl \
    --sharedclient \
    --prefix="$pkgdir/usr" \
    install-mongoclient
}

sha512sums=('52f012a25190cdd46b361f27935b4fe8a58151f514388ee121f881a0ab641c3f109c98ef6a29517e8505215ef28dc50183fcb6e4c7dcd372019fee054e0bb3d8')
