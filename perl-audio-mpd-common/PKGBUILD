# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=perl-audio-mpd-common
_pkgname=Audio-MPD-Common
pkgver=1.110550
pkgrel=2
pkgdesc="Audio::MPD::Common - common helper classes for MPD"
arch=('any')
url="http://search.cpan.org/dist/$_pkgname"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-moose>=0.92' 'perl-moosex-has-sugar' 'perl-moosex-types' 'perl-readonly')
source=(http://search.cpan.org/CPAN/authors/id/J/JQ/JQUELIN/$_pkgname-$pkgver.tar.gz)
sha512sums=('9d02c018a72941e735767bced7ee3d3a0a259821bc3ff5367da2b47f7a45481297c5a31addba5364716ec5eec8dd5ec3ca3f2c645669ca56546ccc404286c9b4')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  perl Build.PL
  perl Build
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  perl Build install
}

# vim:set ts=2 sw=2 et:
