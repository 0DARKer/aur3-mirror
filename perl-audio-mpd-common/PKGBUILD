# Maintainer: Jason St. John <jstjohn .. purdue . edu>

pkgname=perl-audio-mpd-common
_pkgname=Audio-MPD-Common
pkgver=1.120610
pkgrel=1
pkgdesc="Audio::MPD::Common - common helper classes for MPD"
arch=('any')
url="http://search.cpan.org/dist/$_pkgname"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-moose>=0.92' 'perl-moosex-has-sugar' 'perl-moosex-types' 'perl-readonly')
source=(http://search.cpan.org/CPAN/authors/id/J/JQ/JQUELIN/$_pkgname-$pkgver.tar.gz)
sha512sums=('9caec6b3860288e23c1ff5ea01daa4c4145dfef7a0d98fe4e8bacf756307c1c2e39ef4894d3a495a292aeafd52b4d22eaf8ca995606caa6b90e7813cd79594bc')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/$_pkgname-$pkgver"
  $PERL Build.PL
  $PERL Build
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  $PERL Build test
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  $PERL Build install
}

# vim:set ts=2 sw=2 et:
