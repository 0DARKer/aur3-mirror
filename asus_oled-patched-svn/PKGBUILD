# Maintainer: SysGhost <sysghost[at]gmail[dot]com>
pkgname=asus_oled-patched-svn
_pkgname=asus_oled
pkgver=r103
pkgrel=3
pkgdesc="Driver for small OLED displays found in some older Asus laptops. Patched (Ugly-hacked) for 3.xx kernels."
url="http://lapsus.berlios.de/asus_oled.html"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('dkms')
makedepends=('subversion sed')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
install='asus_oled.install'
source=("${_pkgname}::svn+svn://svn.berlios.de/lapsus/asus_oled/trunk"
        "asus_oled.patch"
        "asus_oled.install"
        "dkms.conf")
md5sums=('SKIP'
         'afa9aaa7d7c8ac6559bf2cb947d36db2'
         '5a84189c7f72ba5ee108d59ac5b1e5fa'
         'bee7870f4f8b4efaae77110a1511e032')

pkgver() {
  cd "$srcdir/${_pkgname}"
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  cd "${srcdir}/${_pkgname}"
  
  # Integrate the included ugly patch.
  patch -p1 < "${srcdir}/asus_oled.patch"
}

build() {
  cd "${srcdir}/${_pkgname}"
  make clean
  make
  rm modules.order
}

package() {
  cd "${srcdir}/${_pkgname}"

  # Disabling depmod. Cannot run it during package time anyway. We'll call depmod from the install script instead.
  sed -i 's/depmod/\#depmod/g' Makefile

  # Install the module inside the chroot.
  make PREFIX="${pkgdir}/usr" DESTDIR="${pkgdir}/usr" INSTALL_MOD_PATH="/usr/lib/modules/$(uname -r)/extra" install

  # Copy license, "dkms.conf" and relevant sources.
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/COPYING"
  install -Dm644 "${srcdir}"/dkms.conf "${pkgdir}"/usr/src/${_pkgname}-${pkgver}/dkms.conf
  cp -r "${srcdir}/${_pkgname}"/* "${pkgdir}"/usr/src/"${_pkgname}-${pkgver}"/

  # Prepare "dkms.conf"
  sed -e "s/@_PKGBASE@/${_pkgname}/" \
      -e "s/@PKGVER@/${pkgver}/" \
      -i "${pkgdir}"/usr/src/${_pkgname}-${pkgver}/dkms.conf
}

# vim:set ts=2 sw=2 et:
