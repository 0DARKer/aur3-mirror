# Maintainer: Martin Wimpress <code@flexion.org>

_pkgname=archis-gtk-theme
pkgname=archis-gtk-theme-git
pkgver=262.997d074
pkgrel=2
pkgdesc="An elegant, modern desktop theme that has semi-flat style and eschewing some of the traditional design patterns, it creates an elegant desktop experience."
arch=('any')
url="https://github.com/moka-project/orchis-gtk-theme"
license=('GPL3')
depends=('gnome-themes-standard' 'gtk-engine-murrine')
makedepends=('git')
conflicts=("${_pkgname}")
provides=("${_pkgname}")
replaces=("${_pkgname}")
options=(!strip)
source=("archis-gtk-theme"::"git+https://github.com/moka-project/orchis-gtk-theme.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
    cd "${srcdir}/${_pkgname}"
    mv Orchis Archis
    mv Orchis-Dark Archis-Dark

    # Replace the Orchis Purple with Arch Linux Blue,
    #  - https://www.archlinux.org/art/
    for FILE in \
            Archis/gtk-2.0/gtkrc \
            Archis/gtk-3.0/assets/grid-selection-checked.svg \
            Archis/gtk-3.0/assets/sidebar-radio-selected-prelight.svg \
            Archis/gtk-3.0/assets/switch-dark.svg \
            Archis/gtk-3.0/assets/switch-on-dark.svg \
            Archis/gtk-3.0/assets/switch-on.svg \
            Archis/gtk-3.0/gtk-common.css \
            Archis/gtk-3.0/gtk-dark.css \
            Archis/gtk-3.0/gtk.css \
            Archis/gtk-3.0/settings.ini \
            Archis/index.theme
    do
        sed -i 's/#A348B1/#1793D1/g' "${FILE}"
        sed -i 's/Orchis/Archis/g' "${FILE}"
    done
    sed -i 's/Orchis/Archis/g' Archis-Dark/index.theme
    sed -i 's/GtkTheme=Archis-Dark/GtkTheme=Archis/' Archis-Dark/index.theme
}

package() {
    install -d -m 755 "${pkgdir}"/usr/share/themes/{Archis,Archis-Dark}
    cp -dr --no-preserve=ownership "${_pkgname}"/{Archis,Archis-Dark} "${pkgdir}"/usr/share/themes/
    find "${pkgdir}"/usr/share/themes/ -type d -exec chmod 755 {} \;
    find "${pkgdir}"/usr/share/themes/ -type f -exec chmod 644 {} \;
}
