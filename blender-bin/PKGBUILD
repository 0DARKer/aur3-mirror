# Maintainer: Mikael Eriksson <mikael_eriksson@miffe.org>

pkgname=blender-bin
# Please blender, stick to one way of naming your versions
pkgver=2.56abeta
_hpkgver=2.56a-beta
_pkgver=2.56
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite (binary version)"
arch=('i686' 'x86_64')
url="http://blender.org/"
license=('GPL')
depends=('libjpeg6' 'libpng12' 'zlib' 'libsamplerate' 'freetype2' 'sdl' 'libgl' 'mesa')
provides=('blender')
conflicts=('blender')
install=blender.install
source=('blender.desktop' 'blender.sh')
md5sums=('b5b9720b9430b1f5e2aa0b2df533fed8'
         'ee1723515be5a2a88839fe02c1eaf174')

case ${CARCH} in
  i686)
    source=(${source[@]} "http://ftp.nluug.nl/pub/graphics/blender/release//Blender${pkgver}/blender-${_hpkgver}-linux-glibc27-i686.tar.bz2")
    md5sums=(${md5sums[@]} '0906b78ca4a86728c14d302e3f2a309d')
    ;;
  x86_64)
    source=(${source[@]} "http://ftp.nluug.nl/pub/graphics/blender/release//Blender${pkgver}/blender-${_hpkgver}-linux-glibc27-x86_64.tar.bz2")
    md5sums=(${md5sums[@]} '7b34824dafb9bac64fcf6bf310ccb633')
    ;;
esac

build() {
  case ${CARCH} in
    i686)
      cd "$srcdir/blender-${_hpkgver}-linux-glibc27-i686"
      ;;
    x86_64)
      cd "$srcdir/blender-${_hpkgver}-linux-glibc27-x86_64"
      ;;
  esac

  install -d -m755 $pkgdir/usr/share/blender

  install -Dm755 "$srcdir/blender.sh" "$pkgdir/usr/bin/blender"
  cp -r blender ${_pkgver} "$pkgdir/usr/share/blender"

  # Icons
  install -Dm644 icons/scalable/blender.svg "$pkgdir/usr/share/icons/hicolor/scalable/blender.svg"
  for S in 16 22 32; do
    install -Dm644 icons/${S}x${S}/blender.png "$pkgdir/usr/share/icons/hicolor/${S}x${S}/blender.png"
  done

  # Stolen from blender in extra
  install -Dm644 $srcdir/blender.desktop $pkgdir/usr/share/applications/blender.desktop
}

# vim:set ts=2 sw=2 et:
