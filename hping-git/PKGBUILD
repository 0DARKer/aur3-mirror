# Maintainer: Sirat18 <aur@sirat18.de>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Kevin Piche <kevin@archlinux.org>

_gitname=hping
pkgname=hping-git
pkgver=r13.3547c76
pkgrel=1
pkgdesc='A command-line oriented TCP/IP packet assembler/analyzer.'
arch=('i686' 'x86_64')
url='http://www.hping.org'
license=('GPL2' 'BSD')
depends=('libpcap')
makedepends=('git')
source=("$_gitname"::'git+https://github.com/antirez/hping.git'
        'Makefile.patch'
		'tshzset.patch')
sha512sums=('SKIP'
            '120005e7ebc8c26bb199aed79cafd9333126ddaf284aa02823eecefc138e342f5f5437af88940598e4437972a4bb98386647ef82c7eef5c9b9b480b8fdbc0ad2'
			'f282b6af4612e1a9262a1e9f90a559598fe05e2e03bf907a09786d4070897671e80327389f6529fe590912e14d39ae177805e947a2ec71623d1444ea3d62377a')
provides=('hping')
conflicts=('hping')

pkgver() {
  cd "$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/$_gitname"

  patch -Np1 -i ../tshzset.patch

  MANPATH=/usr/share/man ./configure --no-tcl
  make
}

package() {
  cd "${srcdir}/$_gitname"

  patch -p1 < ../Makefile.patch

  make DESTDIR="${pkgdir}" install
  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
