pkgname=ratpoison-git
pkgver=20120823
pkgrel=1
pkgdesc="Ratpoison from git"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/ratpoison/"
license=('GPL')
depends=('libxinerama' 'readline' 'bash' 'perl' 'libxtst' 'libxft')
makedepends=('git')
provides=('ratpoison')
replaces=('ratpoison')
conflicts=('ratpoison')
source=()
md5sums=()

_gitroot="git://git.savannah.nongnu.org/ratpoison.git"
_gitname="ratpoison"

build() {

  cd "$srcdir"
  if [[ -d $_gitname ]]
  then
    msg "Git checkout:  Updating existing tree"
    (cd "$_gitname"; git pull origin)
    msg "Git checkout:  Tree has been updated"
  else
    msg "Git checkout:  Retrieving sources"
    git clone "$_gitroot"
  fi
  msg "Checkout completed"

  rm -rf "${_gitname}-build"
  git clone "$_gitname" "$_gitname-build"
  cd "$_gitname-build"

  ./autogen.sh
  ./configure --prefix=/usr
  make

  cd contrib
  ./genrpbindings
}

package() {
  cd "$srcdir/$_gitname-build"

  install -d "$pkgdir/usr/share/ratpoison/bindings"

  make DESTDIR="$pkgdir" install
  install -m 644 contrib/{Ratpoison.pm,ratpoison-cmd.el,ratpoison.rb,ratpoison.lisp,ratpoison.py} \
            "$pkgdir/usr/share/ratpoison/bindings"

  chmod a+x "$pkgdir/usr/share/ratpoison/"{allwindows.sh,clickframe.pl,rpshowall.sh,rpws,split.sh}
  rm -rf "$pkgdir/usr/share/info/dir"
}
