# $Id: PKGBUILD 57440 2011-10-27 20:16:15Z lcarlier $
# Contributor: Hector <hsearaDOTatDOTgmailDOTcom>

pkgname=python-mdtraj
_pkgname=mdtraj
pkgver=1.0.0
pkgrel=2
pkgdesc='A modern, open library for the analysis of molecular dynamics trajectories'
url='http://mdtraj.org/'
license=("LGPL")
arch=('i686' 'x86_64')
depends=('python-numpy' 'cython')
optdepends=('python-scipy: For loading and saving AMBER netcdf formatted trajectories.'
            'python-pandas: Some functionality, including mol2 parsing, requires pandas.'
            'python-pytables: Working with HDF5 formatted trajectories requires the PyTables package.')
makedepends=('python-setuptools')
options=('!libtool')
source=('https://github.com/SimTk/mdtraj/archive/1.0.0.tar.gz')
sha1sums=('188457c0969c83664668f7a7d4ed1c34a37ae5f8')


build() {
  msg2 "Building ${_pkgname}"
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  msg2 "Installing ${_pkgname}"
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  # Remove left over directories from distribute utils.
  find ${pkgdir} -type d -name "__pycache__" -exec rm -r {} \; -prune
}
