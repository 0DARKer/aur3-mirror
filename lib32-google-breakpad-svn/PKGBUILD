# Maintainer:  John Schoenick <john@pointysoftware.net>
# Contributor: Geoffrey Teale <tealeg@googlemail.com>
pkgname=lib32-google-breakpad-svn
pkgver=1091
pkgrel=1
pkgdesc="An open-source multi-platform crash reporting system"
arch=('x86_64')
url="http://code.google.com/p/google-breakpad/"
license=('BSD')
depends=('curl' 'lib32-gcc-libs')
makedepends=('subversion' 'gcc-multilib')

_svntrunk=http://google-breakpad.googlecode.com/svn/trunk
_svnmod=google-breakpad-read-only

build() {
  cd "$srcdir"

  if [ -d "$_svnmod"/.svn ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"

  rm -rf "$_svnmod-build"
  svn export "$_svnmod" "$_svnmod-build"
  cd "$_svnmod-build"

  #
  # BUILD
  #
  msg2 "Running autotools"
  autoreconf -v --force --install
  msg2 "Building"
  ./configure --enable-m32 --program-suffix=-32 \
	--prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install
  mv "${pkgdir}/usr/share/doc/breakpad-0.1" "${pkgdir}/usr/share/doc/breakpad32-0.1"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/COPYING"
}
