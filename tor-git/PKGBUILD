# Contributor: skydrome <skydrome@i2pmail.org>
# Maintainer: skydrome <skydrome@i2pmail.org>

pkgname=tor-git
pkgver=0.2.4.10.alpha.374
pkgrel=1
pkgdesc="An anonymizing overlay network (development version)"
arch=('i686' 'x86_64' 'armv6h')
url="http://www.torproject.org"
license=('BSD')
depends=('openssl' 'ca-certificates' 'libevent')
makedepends=('asciidoc')
optdepends=('torsocks-git: for torify support')
# this is an optdep because the torsocks in community requires tor which is wrong
# and the tor in extra installs tsocks which support for has been removed since last year
# use torsocks-git instead
conflicts=('tor' 'tor-unstable')
provides=('tor')
install='tor.install'
backup=('etc/tor/torrc')
_gitname=tor
source=("git://git.torproject.org/${_gitname}.git"
        'torrc'
        'tor.service')
sha256sums=('SKIP'
            'acde77cf0f4163481e6a1e26ef496b1451eb3d40554cd57b3a09cdce346d86b4'
            '466e82691f4f864765f70fe06878c2972cd36b451e94fb63c079e84ea2f7f03c')

pkgver () {
    cd "$srcdir/$_gitname"
    git describe |sed 's/tor-//;s/-[^-]*$//;s/^v//;s/-/./g'
}

build() {
    cd "$srcdir/$_gitname"

    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var #--disable-asciidoc

    make
}

check() {
    cd "$srcdir/$_gitname"
    make test || true
}

package() {
    cd "$srcdir/$_gitname"
    make DESTDIR="$pkgdir" install
    
    install -dm0700 "${pkgdir}/var/lib/tor"
    rm -f "${pkgdir}/etc/tor/tor-tsocks.conf"
    mv "${pkgdir}/etc/tor/torrc.sample"     "${pkgdir}/etc/tor/torrc-dist"
    install -Dm0644 "${srcdir}/torrc"       "${pkgdir}/etc/tor/torrc"
    install -Dm0644 "${srcdir}/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"
    install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/tor/LICENSE"
}
