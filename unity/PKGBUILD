# Contributor: Flamelab <panosfilip@gmail.com>
# Previous contributor: Maintainer: Adam Hani Schakaki (krzd) <krzd@krzd.net>

# Fixes thanks to: pressh

pkgname=unity
pkgver=3.4.4
pkgrel=1
pkgdesc="Ubuntu's new desktop interface"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity/"
license=('GPL')
depends=('compiz-core-unity-git' 'nux-bzr' 'clutter' 'clutter-gtk' 'clutk' 'dbus-glib' 'glib2' 'gtk2' 'gjs' 'gconf' 'gnome-desktop' 'libgnomeui' \
         'libdbusmenu' 'dee' 'bamf' 'ccsm-unity-git' 'libindicator')
makedepends=('vala-git' 'intltool' 'gobject-introspection' 'cmake' 'libgee' 'libunity-misc')
optdepends=('libindicate: For Ubuntu indicator support'
            'ayatana-ubuntu: Ubuntu Ayatana -indicator- project'
            'utouch-grail: Utouch support')
source=("http://launchpad.net/${pkgname}/3.0/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
install=unity.install
md5sums=('fbe45db0cd52f82f0f1e826531019870')

#Change this to "0" if you don't want the message before starting building to appear
_buildmsg=1


build() {

  if [ "$_buildmsg" == "1" ];then
    GR=$'\x1b[1;32m'
    REDB=$'\x1b[31;1m'
    NC=$'\x1b[0m'
    echo "******************************************************************************"
    echo "In order to build this package you must build the Compiz 0.9.x packages (not the 0.8.x ones) with an /opt/unity prefix. That means"
    echo "you will add a $REDB \"-DCMAKE_INSTALL_PREFIX=/opt/unity\" $NC in the cmake line in the Compiz 0.9.x PKGBUILDs"
    echo "BEFORE you build unity. The ones added as dependencies, they *do* use /opt/unity as prefix."
    echo "*******************************************************************************"
    echo "Unity will be available as Compiz $REDB plugin $NC in Compiz Config Settings Manager (ccsm) as \"Ubuntu Unity Plugin\" "
    echo "Then, add /opt/unity/bin in your \$PATH in i.e. /etc/profile"
    sleep 2
fi

  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p build && cd build
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
        -DCMAKE_INSTALL_PREFIX=/opt/unity ..
  make

}

package() {

  cd "${srcdir}/${pkgname}-${pkgver}"/build
  make DESTDIR="${pkgdir}" install

}

