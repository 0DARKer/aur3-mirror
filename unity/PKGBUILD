# Maintainer: Charles Bos <charlesbos1 AT gmail>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=unity
pkgver=7.1.1
pkgrel=1
_translations=20130418
pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
depends=('bamf' 'boost' 'compiz-ubuntu' 'clutter-gtk' 'gjs' 'gnome-desktop' 'gnome-screensaver' 'gnome-session-ubuntu' 'gnome-settings-daemon-ubuntu' 'hud-bzr' 'ido-bzr' 'indicator-session' 'libgnomeui' 'libindicator-bzr' 'libnotify' 'libunique' 'libunity' 'libunity-misc-bzr' 'libxfixes' 'libxi' 'libzeitgeist' 'nux' 'unity-asset-pool' 'unity-lens-applications-bzr' 'xpathselect-bzr' 'zeitgeist')
makedepends=('cmake' 'doxygen' 'intltool' 'patchutils' 'python2-setuptools')
optdepends=('gnome-control-center-ubuntu: The Control Center for GNOME with Ubuntu patches'
            'indicator-application: take menus from applications and place them in the panel'
            'indicator-appmenu: host the menus from an application'
            'indicator-bluetooth: Bluetooth menu'
            'indicator-datetime: a very, very simple clock'
            'indicator-messages: a place on the users desktop that collects messages that need a response'
            'indicator-power: show the power status of your devices'
            'indicator-printers: show active print jobs'
            'indicator-sound: a unified sound menu'
            'indicator-sync: aggregate services performing background synchronization'
            'nautilus-ubuntu: The GNOME file manager with Ubuntu patches'
            'notify-osd: The desktop notification system from Canonical'
            'unity-lens-files: exposing your files and file history'
            'unity-lens-gwibber: microblogging lens for Unity'
            'unity-lens-music: music, in the dash'
            'unity-lens-photos: all your photos, in the Dash'
            'unity-lens-shopping: gives you more suggestions when searching Unity'
            'unity-lens-video: lens for Unity to search local and remote videos'
            'unity-scope-gdocs: allows you to browse and search your Google Documents'
            'unity-scope-video-remote: Videos Lens scope for online sources')
groups=('unity')
install=unity.install
provides=('unity-core')
source=("http://launchpad.net/unity/7.1/$pkgver/+download/$pkgname-$pkgver.tar.bz2"
        "https://dl.dropboxusercontent.com/u/486665/Translations/translations-${_translations}-unity.tar.gz"
        "http://www.cmake.org/files/v2.8/cmake-2.8.11.2.tar.Z"
        'remove-gtest.patch'
        'branding.patch'
        'werror.patch'
        'launchers.patch'
        'systemd.patch'
        'launcher_bfb.png'
        'unity-migrate-dconf-path.desktop'
        '10_unity.gschema.override'
        'com.canonical.Unity.Panel.Service.service')
md5sums=('e216851d2115e9df3749d031c6f7de20'
         '170a6c6c9b992813b193373b5fbfd319'
         '2ac4e3f94511bcaf1fa2a6332f7fb70e'
         'bbb5ed72f80df4a8a6009a02130ae2b4'
         '318432052ad0f81d61829fab5973f32f'
         '0e55afe10481e14938c5ef5c91926cfc'
         'c1399586a580ceaed1dab21a70f047c6'
         'aba0b1ef1e76eddf4ad7bdfb391ae487'
         'efc27caaa1fb85ba45d35eee540b9ebc'
         'b840e90b553e88937e1c0b1b90f94b78'
         '2f5e8c42689a467dc0b1c1241fcef6a7'
         'fb793aad87393de356c48f40717e6a16')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Remove Ubuntu branding (with thanks to qiuwei!)
  patch -p1 -i "${srcdir}/branding.patch"

  # Do not treat warnings as errors
  patch -p1 -i "${srcdir}/werror.patch"

  # Remove Ubuntu specific and commercial launchers
  patch -p1 -i "${srcdir}/launchers.patch"

  # Remove dependency on the Upstart init system
  patch -p1 -i "${srcdir}/systemd.patch"

  # Disable gtest (removes the need to rebuild gmock)
  patch -p1 -i "${srcdir}/remove-gtest.patch"

  msg "Merging translations from ${_translations}"
  rm -f po/LINGUAS po/*.pot
  mv "${srcdir}"/po/*.pot po/
  for i in "${srcdir}"/po/*.po; do
    FILE=$(sed -n "s|.*/${pkgname}-||p" <<< ${i})
    mv ${i} po/${FILE}
    echo ${FILE%.*} >> po/LINGUAS
  done

  # Ubuntu Desktop -> Unity Desktop
  sed -i '/msgid "Ubuntu Desktop"/ {n;s/Ubuntu/Unity/}' po/*.po
  sed -i 's/Ubuntu Desktop/Unity Desktop/g' po/*.po
}

build() {
  cd "${srcdir}/cmake-2.8.11.2"

  ./bootstrap --prefix="${srcdir}/cmake"
  make
  make install
  cp {/usr/share,"${srcdir}/cmake/share"}/cmake-2.8/Modules/FindCompiz.cmake

  cd "${srcdir}/${pkgname}-${pkgver}"

  # (From debian/rules) http://ccache.samba.org/manual.html#_precompiled_headers
  export CCACHE_SLOPPINESS=time_macros

  [[ -d build ]] && rm -rvf build/
  mkdir build/ && cd build/

  PATH="${srcdir}/cmake/bin:${PATH}" \
  cmake -Wno-dev \
    -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
    -DCOMPIZ_PACKAGING_ENABLED=TRUE \
    -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_GSETTINGS=TRUE \
    -Duse_pch=OFF \
    `# Use our cmake` \
    -DCMAKE_MODULE_PATH="${srcdir}/cmake/share/cmake-2.8/Modules:/usr/share/cmake-2.8/Modules" \
    ..

  make

  # Make sure that the GSettings schema files were created
  pushd generated/glib-2.0/schemas/
  if \
    [ ! -f org.compiz.networkarearegion.gschema.xml ] || \
    [ ! -f org.compiz.unitydialog.gschema.xml ] || \
    [ ! -f org.compiz.unitymtgrabhandles.gschema.xml ] ||
    [ ! -f org.compiz.unityshell.gschema.xml ]; then
    make compiz_gsettings_compile_local
  fi
  popd
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}/" install

  # Install dconf path migration stuff
  install -dm755 "${pkgdir}/etc/xdg/autostart/"
  install -m644 "${srcdir}/unity-migrate-dconf-path.desktop" \
    "${pkgdir}/etc/xdg/autostart/"
  install -m755 ../tools/migration-scripts/01_unity_change_dconf_path \
    "${pkgdir}/usr/lib/unity/"

  # Install profile convert files
  install -dm755 "${pkgdir}/usr/lib/compiz/migration/"
  install -m644 ../tools/convert-files/* "${pkgdir}/usr/lib/compiz/migration/"

  # Fix Python 2 scripts
  sed -i 's|^\(#!.*python$\)|\12|g' \
    "${pkgdir}/usr/bin/unity" \
    "${pkgdir}/usr/lib/unity/makebootchart.py"

  # Arch Linux logo
  install -m644 "${srcdir}/launcher_bfb.png" \
    "${pkgdir}/usr/share/unity/icons/launcher_bfb.png"

  # Check for missing GSettings schemas
  pushd "${pkgdir}/usr/share/glib-2.0/schemas/"
  if \
    [ ! -f org.compiz.networkarearegion.gschema.xml ] || \
    [ ! -f org.compiz.unitydialog.gschema.xml ] || \
    [ ! -f org.compiz.unitymtgrabhandles.gschema.xml ] || \
    [ ! -f org.compiz.unityshell.gschema.xml ]; then
    error "GSettings schemas didn't get installed"'!'
    error "Please tar and upload the src/ directory and report a bug."
    exit 1
  fi
  popd

  # Change window dragging key back to Alt. Super conflicts with Unity's other hotkeys.
  install -m644 "${srcdir}/10_unity.gschema.override" \
    "${pkgdir}/usr/share/glib-2.0/schemas/"

  # Install Upstart user job for unity-panel-service
  install -dm755 "${pkgdir}/usr/share/dbus-1/services/"
  install -m644 "${srcdir}/com.canonical.Unity.Panel.Service.service" \
    "${pkgdir}/usr/share/dbus-1/services/"
}

# vim:set ts=2 sw=2 et:
