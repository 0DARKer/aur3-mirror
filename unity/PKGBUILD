# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=unity
pkgver=5.0.0
pkgrel=2
pkgdesc="A desktop experience designed for efficiency of space and interaction"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
depends=('unity-common' 'compiz-decorator-gtk-devel' 'compizconfig-backend-gconf-devel' 'bamf' 'libdbusmenu' 'libunity-misc' 'libgdu' 'unity-asset-pool' 'gnome-session' 'gnome-settings-daemon')
makedepends=('cmake' 'boost' 'intltool' 'glproto' 'dri2proto')
optdepends=('indicator-application: take menus from applications and place them in the panel'
            'indicator-appmenu: host the menus from an application'
            'indicator-datetime: a very, very simple clock'
            'indicator-messages: a place on the users desktop that collects messages that need a response'
            'indicator-power: show the power status of your devices'
            'indicator-session: change your status, switch users'
            'indicator-sound: a unified sound menu'
            'unity-lens-applications: exposes your applications with their usage statistics and status'
            'unity-lens-files: exposing your files and file history'
            'unity-lens-music: music, in the dash')
install=unity.install
source=(http://launchpad.net/$pkgname/5.0/$pkgver/+download/$pkgname-$pkgver.tar.bz2
        launchpad-export.tar.gz
        make-optional.patch
        disable-multitouch.patch
        unity.ini
        unity.sh
        unity.session
        unity.desktop
        unity.xml.defaults)
md5sums=('e41f5991ca0cdaac73f5c807a671b374'
         'abba8f4ebdcc90983633248ebab361e1'
         '11c9e0c3a0f9cc7b0e532866bb2525cb'
         'da352c4fc856e7ee8b2d0b2b20fa3046'
         'd954f6aba40501f4baeec94247aefef4'
         '00713cf28268247a582f7bf1fcf1fc20'
         'e0b1d90fc06601ccc57fd49827266f9d'
         'fe17963c172e71d68704dfa4bb04bea4'
         'a4d4fd820a458962faf07238d613ffbe')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release \
           -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
           -DCOMPIZ_PACKAGING_ENABLED=TRUE \
           -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
           -DBUILD_THEME=OFF \
           -DBUILD_COMMON=OFF \
           -DBUILD_TESTS=OFF
  make
}

package_unity() {

  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir/" install

  # Install gconf schema
  mkdir -p "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir"/usr/share/gconf/schemas/compiz-unity.schemas \
        "$srcdir"/unity-gconf/usr/share/gconf/schemas/*.schemas
  sed -i '/<schemalist\/>/d' "$pkgdir"/usr/share/gconf/schemas/compiz-unity.schemas

  # Install Compiz profile and settings
  install -dm755 "$pkgdir"/etc/gconf/unity.xml.defaults
  gconftool-2 --direct --config-source xml:readwrite:"$pkgdir/etc/gconf/unity.xml.defaults" --load "$srcdir/unity.xml.defaults"
  install -Dm644 "$srcdir/unity.ini" "$pkgdir/etc/compizconfig/unity.ini"
  install -Dm755 "$srcdir/unity.sh" "$pkgdir/etc/profile.d/unity.sh"

  # Install session and desktop files
  install -Dm644 "$srcdir/unity.session" "$pkgdir/usr/share/gnome-session/sessions/unity.session"
  install -Dm644 "$srcdir/unity.desktop" "$pkgdir/usr/share/xsessions/unity.desktop"
}
