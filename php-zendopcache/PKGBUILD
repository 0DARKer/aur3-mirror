pkgname=php-zendopcache
pkgver=7.0.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='The Zend OPcache provides faster PHP execution through opcode caching and optimization.'
url='http://pecl.php.net/package/ZendOpCache'
depends=('php')
license=('PHP')
source=("http://pecl.php.net/get/zendopcache-${pkgver}.tgz")
backup=('etc/php/conf.d/zendopcache.ini')
md5sums=('3a0a43a4819c72763bc35ecf5689221e')

build() {
	cd $srcdir/zendopcache-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

package() {
	cd $srcdir/zendopcache-$pkgver
	make INSTALL_ROOT=$pkgdir install
	echo ';zend_extension=/usr/lib/php/modules/opcache.so' > zendopcache.ini
	install -D -m644 zendopcache.ini $pkgdir/etc/php/conf.d/zendopcache.ini
	install -D -m644 README $pkgdir/usr/share/doc/php-zendopcache/README
}
