# Maintainer: Yngve Inntjore Levinsen <yngve.inntjore.levinsen@cern.ch>

pkgname=madx-svn
pkgver=2805
pkgrel=1
pkgdesc="Accelerator Optics simulation code, svn trunk version"
url="http://cern.ch/mad"
license=("custom")
depends=('gcc-libs' 'libx11')
conflicts=('madx-dev')
provides=('madx')
makedepends=('subversion' 'cmake' 'python2' 'cython')
arch=('i686' 'x86_64')

_svntrunk=http://svnweb.cern.ch/guest/madx/trunk/
_svnmod=madx

build() {
    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
    else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"
    msg "Starting make..."
    
    cd $srcdir/$_svnmod/
    [ -d "build" ] && rm -rf build
    mkdir build
    cd build

      
    cmake -DCMAKE_C_COMPILER=gcc \
          -DCMAKE_Fortran_COMPILER=gfortran \
          -DMADX_STATIC=OFF \
          -DMADX_ONLINE=OFF \
          -DBUILD_SHARED_LIBS=ON \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
         ../madX

      make
}

package() {
    cd ${srcdir}/${_svnmod}/build
    make DESTDIR=${pkgdir} install

    cd ${srcdir}/${_svnmod}/madX
    install -D -m644 License.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
