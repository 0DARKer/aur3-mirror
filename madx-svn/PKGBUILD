# Maintainer: Yngve Inntjore Levinsen <yngveTODlevinsenTAcernTODch>

pkgname=madx-svn
pkgver=4064
pkgrel=1
pkgdesc="Accelerator Optics simulation code, svn trunk version"
url="http://cern.ch/mad"
license=("custom")
depends=('gcc-libs' 'libx11')
conflicts=('madx-dev')
provides=('madx')
makedepends=('subversion' 'cmake')
arch=('i686' 'x86_64')

source=('madx_install_header.patch')
md5sums=('8372da080c770f46a6c8426fcbd3a491')

_svntrunk=http://svnweb.cern.ch/guest/madx/trunk/
_svnmod=madx

build() {
    _srcdir=madX-patched

    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
    else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"

    msg "Patching.."
    cd $srcdir/$_svnmod/
    [ -d "$_srcdir" ] && rm -rf $_srcdir
    cp -r madX $_srcdir
    cd $_srcdir
    # because patch level is 00 we need this for now:
    patch -p0 -i ../../madx_install_header.patch

    msg "Starting make..."

    cd $srcdir/$_svnmod/
    [ -d "build" ] && rm -rf build
    mkdir build
    cd build

    # Note, BINARY_POSTFIX forced to not conflict with package 'madx' 
    cmake -DCMAKE_C_COMPILER=gcc \
          -DCMAKE_Fortran_COMPILER=gfortran \
          -DMADX_STATIC=OFF \
          -DMADX_ONLINE=OFF \
          -DBUILD_SHARED_LIBS=ON \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DBINARY_POSTFIX=_dev \
          -DCMAKE_BUILD_TYPE=RelWithDebInfo \
         ../$_srcdir

      make
}
check() {
     cd "$srcdir/$_svnmod/build/"
     ctest -E LONG
}
package() {
    cd "${srcdir}/${_svnmod}/build"
    make DESTDIR=${pkgdir} install

    cd ${srcdir}/${_svnmod}/madX
    install -D -m644 License.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
