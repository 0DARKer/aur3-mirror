# Maintainer: Hubert Kario <kario@wsisiz.edu.pl>
# based on official PKGBUILD for smartmontools made by:
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Jani Talikka <jani.talikka@gmail.com>
# Contributor: Ralf Barth <archlinux.org@haggy.org>

pkgname=smartmontools-svn
_realname=smartmontools
pkgver=3501
pkgrel=1
pkgdesc="Control and monitor S.M.A.R.T. enabled ATA and SCSI Hard Drives - latest SVN version"
url="http://smartmontools.sourceforge.net"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gcc-libs')
makedepends=('subversion')
backup=('etc/smartd.conf'
        'etc/conf.d/smartd')
source=(smartd.conf
        smartd.rc)
md5sums=('28c1b80c41dac52d433961dcfb4f70e0'
         'fb30e07f87f2c1d99532e018291ad3e8')
provides=($_realname)
conflicts=($_realname)

_svntrunk=https://smartmontools.svn.sourceforge.net/svnroot/smartmontools/trunk/smartmontools
_svnmod=smartmontools

build()
{
  cd "$srcdir"

  if [[ -d $_svnmod/.svn ]]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  cd $_realname

  ./autogen.sh

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-drivedb

  make || return 1
  make DESTDIR=${pkgdir}/ install || return 1
  rm -rf ${pkgdir}/etc/rc.d
  install -Dm755 ${srcdir}/smartd.rc ${pkgdir}/etc/rc.d/smartd
  install -Dm644 ${srcdir}/smartd.conf ${pkgdir}/etc/conf.d/smartd
}
