# Maintainer: TDY <tdy@gmx.com>

pkgname=oleo-motif
pkgver=1.99.16
pkgrel=2
pkgdesc="GNU's lightweight spreadsheet program with a Motif-based interface"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/oleo/"
license=('GPL')
depends=('libcups' 'xbae')
makedepends=('autoconf' 'automake' 'texi2html')
provides=('oleo')
conflicts=('oleo')
install=oleo.install
source=(http://ftp.gnu.org/pub/gnu/oleo/oleo-$pkgver.tar.gz
        http://archive.ubuntu.com/ubuntu/pool/universe/o/oleo/oleo_$pkgver-11ubuntu1.diff.gz)
md5sums=('d59801055b8ba6c6980ed1247a603f5f'
         '23816dc1949c5d130eab69ccf7214fbf')

build() {
  cd "$srcdir/oleo-$pkgver"
  patch -Np1 -i ../oleo_$pkgver-11ubuntu1.diff || return 1
  sed -i '/MAKE/ s/install-data-am//' Makefile.in
  ./configure --prefix=/usr --infodir=/usr/share/info
  find -type f -exec sed -i \
    -e 's#the file COPYING for the#the license for the conditions:#g' \
    -e 's#conditions\.#/usr/share/licenses/common/GPL2/license.txt#g' '{}' \;
  make || return 1
}

package() {
  cd "$srcdir/oleo-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 doc/oleo.html "$pkgdir/usr/share/doc/oleo/oleo.html"
}
