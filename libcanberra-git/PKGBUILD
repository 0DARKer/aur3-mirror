# $Id: PKGBUILD 173082 2012-12-10 12:00:17Z jgc $
# Maintainer (stable): Jan de Groot <jgc@archlinux.org>
# Maintainer (git): Progandy <admin@progandy.de>

pkgbase=libcanberra-git
_pkgbase=libcanberra
pkgname="$pkgbase" ## AUR workaroung
true && pkgname=(libcanberra-git libcanberra-pulse-git libcanberra-gstreamer-git)
pkgver=20130312
_pkgver=0.30
pkgrel=1
pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification (GIT build)"
arch=(i686 x86_64)
license=(LGPL)
depends=(libvorbis libltdl alsa-lib tdb sound-theme-freedesktop)
makedepends=(gtk-doc libpulse gstreamer gtk2 gtk3)
options=(!emptydirs)
url=http://0pointer.de/lennart/projects/libcanberra
source=(libcanberra.xinit)
sha256sums=('68a68d66b7491f99d5aa09fe7c6ef4eb38352a2a8ff7997d184d1cbc5e80c0d3')

_plugindir=usr/lib/$_pkgbase-$_pkgver
_gitroot=http://git.0pointer.de/repos/libcanberra.git
_gitname=libcanberra


build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh
  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
      --disable-static --with-builtin=dso --enable-null --disable-oss \
      --enable-alsa --enable-gstreamer --enable-pulse \
      --with-systemdsystemunitdir=/usr/lib/systemd/system

  # Fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package_libcanberra-git() {
  install=libcanberra.install
  optdepends=("$pkgbase-pulse: PulseAudio driver"
              "$pkgbase-gstreamer: GStreamer driver")
  provides=("libcanberra")
  conflicts=("libcanberra")

  cd "$srcdir/$_gitname-build"

  make -j1 DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libcanberra-gtk*.la
  rm "$pkgdir"/usr/lib/gtk-{2,3}.0/modules/*.la

  install -D "$srcdir/libcanberra.xinit" \
    "$pkgdir/etc/X11/xinit/xinitrc.d/40-libcanberra-gtk-module"

  # Split plugins
  for _p in pulse gstreamer; do
    [ -d ../$_p-plugin ] || mkdir ../$_p-plugin
    mv "$pkgdir/$_plugindir"/libcanberra-$_p.* ../$_p-plugin
  done
}

package_libcanberra-pulse-git() {
  pkgdesc="PulseAudio plugin for libcanberra (GIT build)"
  depends=("$pkgbase=$pkgver-$pkgrel" libpulse)
  provides=("libcanberra-pulse")
  conflicts=("libcanberra-pulse")

  install -d "$pkgdir/$_plugindir"
  mv pulse-plugin/* "$pkgdir/$_plugindir"
}

package_libcanberra-gstreamer-git() {
  pkgdesc="GStreamer plugin for libcanberra (GIT build)"
  depends=("$pkgbase=$pkgver-$pkgrel" gstreamer)
  provides=("libcanberra-gstreamer")
  conflicts=("libcanberra-gstreamer")

  install -d "$pkgdir/$_plugindir"
  mv gstreamer-plugin/* "$pkgdir/$_plugindir"
}

## FIX for AUR
pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification (GIT build)"
depends=(libvorbis libltdl alsa-lib tdb sound-theme-freedesktop)
