# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=hyperrogue
pkgver=4.3
_pkgver=43
pkgrel=1

_music="-nomusic"  # set to "" to enable

pkgdesc="You are a lone outsider in a strange, non-Euclidean hyperbolic world.  Optional music."
arch=('i686' 'x86_64')
url="http://www.roguetemple.com/z/hyper.php"
license=('GPL2')
depends=('sdl_gfx' 'sdl_ttf' 'sdl_mixer')
source=(http://www.roguetemple.com/z/$pkgname-${_pkgver}${_music}.zip)
md5sums=('3466806b6d6b3e93d4f692d74ff19b54')
if [[ -z "$_music" ]]; then
    md5sums=('0debd09b7e2fbfb1ffbb8c346fd16b42')
fi

build() {
    cd "$srcdir/$pkgname-${_pkgver}${_music}"
    chmod -x *
    sed -i 's|VeraBd.ttf|/usr/share/hyperrogue/&|' graph.cpp
    make
}

package() {
    cd "$srcdir/$pkgname-${_pkgver}${_music}"
    install -Dm755 hyper      "$pkgdir/usr/bin/hyperrogue"
    install -Dm644 VeraBd.ttf "$pkgdir/usr/share/hyperrogue/VeraBd.ttf"
    if [[ -z "$_music" ]]; then
	install -Dm644 hyperrogue-music.txt "$pkgdir/usr/share/hyperrogue/hyperrogue-music.txt"
	cp *.ogg "$pkgdir/usr/share/hyperrogue/"
    fi
}


