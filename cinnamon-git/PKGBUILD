# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=cinnamon-git
pkgver=20111221
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/Cinnamon"
license=('GPL')
groups=()
depends=('gnome-menus' 'libpulse' 'gnome-desktop' 'python' 'gnome-bluetooth' 'gjs' 'mutter' 'libcroco' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'nautilus' 'telepathy-mission-control')
makedepends=('git')
provides=('cinnamon')
conflicts=('cinnamon')
options=('!libtool')
install=${pkgname}.install

_gitroot='https://github.com/linuxmint/Cinnamon.git'
_gitname='Cinnamon'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/bin/cinnamon --localstatedir=/var --disable-static --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
