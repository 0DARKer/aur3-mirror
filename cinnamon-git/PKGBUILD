# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=cinnamon-git
pkgver=20120612
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/Cinnamon"
license=('GPL')
depends=('gnome-menus' 'libpulse' 'gnome-desktop' 'python2' 'gnome-bluetooth' 'gjs' 'libcroco' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'nautilus' 'gnome-session' 'muffin-git>=20120507' 'python2-gconf' 'libgnomekbd' 'gnome-menus2')
makedepends=('git' 'intltool' 'gnome-common')
provides=('cinnamon')
conflicts=('cinnamon' 'cinnamon-applets-git' 'cinnamon-applets' 'cinnamon-session-git' 'cinnamon-session' 'cinnamon<20120118')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=('0001-cinnamon-settings-hack-by-Ner0.patch'
        'cinnamon2d.desktop'
        'cinnamon2d.session'
        'gnome-session-cinnamon2d')
sha512sums=('896b8f5842d4552d95128f50088d28d4b2f58eb38db68169a3aa96e77b1da2cbbf71dfd72fa7228080976571e95313576c42420461781c6e6f54198a153cf223'
            '931431fa8b0fe095d55961265aa0e02a9fc1ed12b4d6eb69bdcddb167c7aa964796b47b39830cd7fe71f1af1365a4d90c5ad01d928d4f8de2c36ec78be41d617'
            '366aeff450fa717a0cc3d89369f1e6c482e807c4c02bf44a6700076491d31ce88c98733ee16fd889ca1ad67267af9cb2f2b83c1c8f52414f32781aa47b30e858'
            '3b01857cbc28ed9a2d3b9ed89303108f79ea00ba1fd107d534cc23b0887fb9398a97ffe2b7878b95a59abaeb653d91b86775da5bbb3947b610dea34fd20e1837')

_gitroot='https://github.com/linuxmint/Cinnamon.git'
_gitname='Cinnamon'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # Hack to make cinnamon-settings work. The proper fix is to install
  # gnome-settings-daemon-ubuntu
  if ! pacman -Q gnome-settings-daemon-ubuntu &>/dev/null; then
    patch -Np1 --no-backup-if-mismatch -i \
      "${srcdir}/0001-cinnamon-settings-hack-by-Ner0.patch"
  fi

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon --localstatedir=/var --disable-static --disable-schemas-compile --disable-schemas-install --enable-compile-warnings=yes
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}/" install

  # Python 2 fix
  sed -i \
    -e 's|^\(#!.*python\)$|\12|g' \
    -e '/python[ \t]\+[-a-zA-Z0-9]\+$/ s|^\(#!.*python\)[ \t]*\(.*\)$|\12 \2|g' \
    "${pkgdir}/usr/bin/cinnamon-extension-tool" \
    "${pkgdir}/usr/lib/cinnamon-settings/cinnamon-settings.py" \
    "${pkgdir}/usr/bin/cinnamon-settings" \
    "${pkgdir}/usr/bin/cinnamon-menu-editor"

  # Install Cinnamon 2D session files
  install -m644 "${srcdir}/cinnamon2d.desktop" \
                "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/cinnamon2d.session" \
                "${pkgdir}/usr/share/gnome-session/sessions/"
  install -m755 "${srcdir}/gnome-session-cinnamon2d" \
                "${pkgdir}/usr/bin/"
}

# vim:set ts=2 sw=2 et:
