# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=cinnamon-git
pkgver=20111221
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/Cinnamon"
license=('GPL')
depends=('gnome-menus' 'libpulse' 'gnome-desktop' 'python' 'gnome-bluetooth' 'gjs' 'mutter' 'libcroco' 'folks' 'telepathy-logger' 'networkmanager' 'caribou' 'nautilus' 'telepathy-mission-control' 'gnome-common')
makedepends=('git' 'intltool')
provides=('cinnamon')
conflicts=('cinnamon')
options=('!libtool')
install=${pkgname}.install
source=('cinnamon.session'
        'cinnamon.xsession')
sha512sums=('aa2155631763ddf47734f170fffc82c61209dc3dab348c3442fe8a244e946def51d5c94309db4dd44737afffe0b0fe37628ae1fd3754571e08117cbf350e9756'
            '44d5d2588832e93f3bc4f49d34cb83b5b558b15893ab3c6d4c4ba9cda3133ae54869de86cac59ccbd63383913bf820203a2ded35c854ea8150f6053a2394eb85')

_gitroot='https://github.com/linuxmint/Cinnamon.git'
_gitname='Cinnamon'

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/cinnamon --localstatedir=/var --disable-static --disable-schemas-compile --disable-schemas-install --enable-compile-warnings=yes
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}/" install

  # GConf2 stuff
  install -dm755 "${pkgdir}/usr/share/gconf/schemas/"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname%-*}.schemas" --domain cinnamon "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm "${pkgdir}"/etc/gconf/schemas/*.schemas

  # Missing files
  install -m644 js/ui/windowlist.js "${pkgdir}/usr/share/cinnamon/js/ui/"
  install -m644 js/ui/showDesktopButton.js "${pkgdir}/usr/share/cinnamon/js/ui/"
  install -m644 js/ui/mintMenu.js "${pkgdir}/usr/share/cinnamon/js/ui/"
  install -m644 js/ui/panelLaunchers.js "${pkgdir}/usr/share/cinnamon/js/ui/"
  install -m644 js/ui/themeManager.js "${pkgdir}/usr/share/cinnamon/js/ui/"
  install -m644 js/ui/workspaceSwitcher.js "${pkgdir}/usr/share/cinnamon/js/ui/"
  install -m644 data/theme/menu.png "${pkgdir}/usr/share/cinnamon/theme/"
  install -m644 data/theme/overview-hover.png "${pkgdir}/usr/share/cinnamon/theme/"
  install -m644 data/theme/overview.png "${pkgdir}/usr/share/cinnamon/theme/"
  install -m644 data/theme/mediaplayer/player-paused.svg "${pkgdir}/usr/share/cinnamon/theme/"
  install -m644 data/theme/mediaplayer/player-playing.svg "${pkgdir}/usr/share/cinnamon/theme/"
  install -m644 data/theme/mediaplayer/player-stopped.svg "${pkgdir}/usr/share/cinnamon/theme/"

  # Python 2 fix
  sed -i 's|^\(#!.*python\)$|\12|g' "${pkgdir}/usr/bin/cinnamon-extension-tool"

  # Install GNOME Session manager session file
  install -dm755 "${pkgdir}/usr/share/gnome-session/sessions/"
  install -m644 "${srcdir}/cinnamon.session" "${pkgdir}/usr/share/gnome-session/sessions/cinnamon.session"

  # Install X11 session file
  install -dm755 "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/cinnamon.xsession" "${pkgdir}/usr/share/xsessions/cinnamon.desktop"
}

# vim:set ts=2 sw=2 et:
