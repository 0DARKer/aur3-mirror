# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Based on r8168 [community]

pkgname=r8168-openvz-testing
_realname=r8168
pkgver=8.035.00
pkgrel=13
pkgdesc="A kernel module for Realtek 8168 network cards for linux-openvz-testing"
url="http://www.realtek.com.tw"
license=("GPL")
arch=("i686" "x86_64")
depends=("glibc" "linux-openvz-testing")
makedepends=("linux-openvz-testing-headers")
source=("http://r8168.googlecode.com/files/${_realname}-${pkgver}.tar.bz2")
install="${_realname}.install"
md5sums=("80b8d23e463e5408dced1b1377579dae")
sha256sums=("b904d31fc9bd6c97cc34479a0eba0e3a9bba5351a43b56dd5fff6b7dc1a5df73")

build() {
  _kernver=$(pacman -Q linux-openvz-testing | cut -d . -f 3 | cut -f 1 -d -)
  KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-2.6.${_kernver}-openvz-testing/version)

  cd "${_realname}-${pkgver}"

  # avoid using the Makefile directly -- it doesn't understand
  # any kernel but the current.
  make -C "/usr/lib/modules/${KERNEL_VERSION}/build" \
      SUBDIRS="${srcdir}/${_realname}-${pkgver}/src" \
      EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
  _kernver=$(pacman -Q linux-openvz-testing | cut -d . -f 3 | cut -f 1 -d -)
  depends=("linux-openvz-testing>=2.6.${_kernver}" "linux-openvz-testing<2.6.$(expr ${_kernver} + 1)")
  KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-2.6.${_kernver}-openvz-testing/version)
  msg "Kernel = ${KERNEL_VERSION}"

  cd "${_realname}-${pkgver}"
  install -Dm644 "src/${_realname}.ko" "${pkgdir}/usr/lib/modules/extramodules-2.6.${_kernver}-openvz-testing/${_realname}.ko"
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} +

  sed -i "s|extramodules-.*-openvz-testing|extramodules-2.6.${_kernver}-openvz-testing|" "${startdir}/${_realname}.install"
}

