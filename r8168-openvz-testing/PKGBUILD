# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Based on r8168 [community]

pkgname=r8168-openvz-testing
_realname=r8168
pkgver=8.038.00
pkgrel=5
pkgdesc="A kernel module for Realtek 8168 network cards for linux-openvz-testing"
url="http://www.realtek.com.tw"
license=("GPL")
arch=("i686" "x86_64")
depends=("glibc" "linux-openvz-testing")
makedepends=("linux-openvz-testing-headers")
source=("http://r8168dl.appspot.com/files/${_realname}-${pkgver}.tar.bz2")
install="${_realname}.install"
md5sums=("fe2962824587070a2ec53f77e40b0fea")
sha256sums=("1384bdf4d33b219f937bf1a600c72b22962baf7796e562b08e4da3c000f49072")

build() {
  _kernver=$(pacman -Q linux-openvz-testing | cut -d . -f 3 | cut -f 1 -d -)
  KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-2.6.${_kernver}-openvz-testing/version)

  cd "${_realname}-${pkgver}"

  # avoid using the Makefile directly -- it doesn't understand
  # any kernel but the current.
  make -C "/usr/lib/modules/${KERNEL_VERSION}/build" \
      SUBDIRS="${srcdir}/${_realname}-${pkgver}/src" \
      EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
      modules
}

package() {
  _kernver=$(pacman -Q linux-openvz-testing | cut -d . -f 3 | cut -f 1 -d -)
  depends=("linux-openvz-testing>=2.6.${_kernver}" "linux-openvz-testing<2.6.$(expr ${_kernver} + 1)")
  KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-2.6.${_kernver}-openvz-testing/version)
  msg "Kernel = ${KERNEL_VERSION}"

  cd "${_realname}-${pkgver}"
  install -Dm644 "src/${_realname}.ko" "${pkgdir}/usr/lib/modules/extramodules-2.6.${_kernver}-openvz-testing/${_realname}.ko"
  find "${pkgdir}" -name '*.ko' -exec gzip -9 {} +

  sed -i "s|extramodules-.*-openvz-testing|extramodules-2.6.${_kernver}-openvz-testing|" "${startdir}/${_realname}.install"
}

