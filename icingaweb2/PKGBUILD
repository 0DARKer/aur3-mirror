# Maintainer: Malte Rabenseifner <mail@malte-rabenseifner.de>

pkgname=icingaweb2
pkgver=2.0.0_beta1
_pkgver=2.0.0-beta1
pkgrel=1
pkgdesc="Icinga Web 2 Interface"
license=('GPL')
arch=('any')
depends=('php')
optdepends=('php-gd: export graphs to PDF'
            'php-intl: support for internationalization'
            'php-ldap: for LDAP authentication'
            'php-pgsql: for PostgreSQL backend'
            'python: running python scripts')
url="http://www.icinga.org"
source=("https://github.com/Icinga/${pkgname}/archive/v${_pkgver}.tar.gz"
        "icingaweb2.install")
install='icingaweb2.install'
sha256sums=('87470c948a67226c0339372316940197e939796d677fd3c899a29a58afa41e31'
            'a8f9794254350c5e2d74d49779e691ddb302d1833b684de02a7ce535363785da')

package() {
  cd "$srcdir/$pkgname-$_pkgver"

  #mkdir -p "$pkgdir/etc/webapps/icingaweb2"
  mkdir -p "$pkgdir/etc/icingaweb"
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/webapps/icingaweb2"
  mkdir -p "$pkgdir/var/log/icingaweb2"

  cp -r application doc library modules public bin "$pkgdir/usr/share/webapps/icingaweb2"
  ln -s /usr/share/webapps/icingaweb2/bin/icingacli "$pkgdir/usr/bin/icingacli"

  install -Dm644 etc/bash_completion.d/icingacli "$pkgdir/etc/bash_completion.d/icingacli"
  install -Dm644 etc/schema/mysql.schema.sql "$pkgdir/usr/share/icingaweb2/mysql.schema.sql"
  install -Dm644 etc/schema/pgsql.schema.sql "$pkgdir/usr/share/icingaweb2/pgsql.schema.sql"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/icingaweb2/LICENSE"
}
