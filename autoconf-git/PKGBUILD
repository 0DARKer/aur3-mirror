# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=autoconf-git
pkgver=20120531
pkgrel=1
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
license=('GPL2' 'GPL3' 'custom')
url="http://www.gnu.org/software/autoconf"
groups=('base-devel')
depends=('awk' 'm4' 'diffutils' 'bash')
provides=('autoconf')
conflicts=('autoconf')
install=autoconf.install

_gitroot="git://git.sv.gnu.org/autoconf"
_gitname="autoconf"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  aclocal -I m4 --install
  autoconf
  automake --add-missing
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install
  rm "$pkgdir/usr/share/info/standards.info"
}
