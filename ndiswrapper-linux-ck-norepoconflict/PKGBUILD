# Maintainer: cip91 <cip91sk at gmail . com>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_linux=3.7
_linuxnext=3.8
_extramodules=extramodules-$_linux-ck
_kver="$(cat /lib/modules/${_extramodules}/version)"
_pkgname=ndiswrapper
pkgname=$_pkgname-linux-ck-norepoconflict
pkgver=1.57
pkgrel=2
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors. This package won't conflict with the one for the repo kernel. For linux-ck-$_linux"
license=('GPL')
arch=('i686' 'x86_64')
url="http://ndiswrapper.sourceforge.net"
install=$_pkgname.install
depends=("linux-ck>=$_linux" "linux-ck<$_linuxnext" 'wireless_tools' 'perl')
makedepends=("linux-ck-headers>=$_linux" "linux-ck-headers<$_linuxnext")
#conflicts=("$_pkgname")
provides=("$_pkgname-utils" "$_pkgname-bin")
replaces=("$_pkgname-utils" "$_pkgname-bin")
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz" 
        "linux33.patch"
        "linux37.patch")

options=('!strip')
md5sums=('7a401dc540938bf07893c67f418b6152'
         '061e59a934ca3ae4be55a86d86b2c1f5' 
         'd0dc483d2a1dfe59353b373c2c29c499 ')

(
cat <<EOF
post_install() { 
	post_upgrade 
}
post_upgrade(){ 
	depmod $_kver 
}
post_remove() { 
	post_upgrade 
}
EOF
) > $startdir/$_pkgname.install

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # modinfo path fix
  sed -i "/modinfo/s/s/usr\//" driver/Makefile
  patch -i "$srcdir/linux33.patch" -N -p0

  sed -i "s|/include/linux/version.h|/include/generated/uapi/linux/version.h|g" driver/Makefile
  patch -i $srcdir/linux37.patch -N -p1

  make KVERS=$_kver
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make INST_DIR="lib/modules/$_extramodules" \
    KVERS=$_kver DESTDIR="$pkgdir/" install
   
  rm -r $pkgdir/sbin
  rm -r $pkgdir/usr

  gzip "$pkgdir/lib/modules/$_extramodules/$_pkgname.ko"
}

# vim:set ts=2 sw=2 et:
