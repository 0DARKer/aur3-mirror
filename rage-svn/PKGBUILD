# Maintainer: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Fabio Falcinelli <fabio.falcinelli@gmail.com>

pkgname=rage-svn
pkgver=83451
pkgrel=1
pkgdesc="An Enlightened multimedia library browser"
arch=('i686' 'x86_64')
license=('BSD')
url="http://www.enlightenment.org"
makedepends=('subversion')
depends=('emotion')
source=('rage.desktop')
md5sums=('7e56125cc786187267a33cb3ef7a9465')

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/rage"
_svnmod="rage"

build() {
  cd "$srcdir"

  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ./autogen.sh --prefix=/usr

  make
}

package() {
  cd "$srcdir/$_svnmod-build"

  make DESTDIR="$pkgdir" install

# install desktop file
  install -Dm644 "$srcdir/rage.desktop" "$pkgdir/usr/share/applications/rage.desktop"

# install license files
  install -Dm644 AUTHORS "$pkgdir/usr/share/licenses/$pkgname/AUTHORS"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

# delete build directory
  rm -r "$srcdir/$_svnmod-build"
}

