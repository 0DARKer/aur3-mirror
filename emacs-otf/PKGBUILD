# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# based on a PKGBUILD of Mathias Nedreb√∏ <mathias <at> nedrebo.org>

pkgname=emacs-otf
pkgver=103679
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Emacs with libotf support, bazaar-version from the trunk"
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('xaw3d' 'dbus-core' 'gnutls' 'alsa-lib' 'librsvg' 'gpm' 'giflib' 'm17n-lib' 'imagemagick' 'hicolor-icon-theme')
makedepends=('bzr' 'texinfo')
options=('docs' '!emptydirs')
provides=('emacs=24.0.50.1')
conflicts=('emacs' 'emacs-bzr' 'emacs-git' 'emacs-tabs')
install=emacs.install
backup=('usr/share/applications/emacs.desktop' 'usr/share/emacs/site-lisp/subdirs.el')
source=()
md5sums=()
_bzrtrunk=http://bzr.savannah.gnu.org/r/emacs/trunk
_bzrmod=emacs

build() {
  if [[ -d $_bzrmod/.bzr ]]; then
    (cd $_bzrmod && bzr update -v && cd ..)
    msg "Local checkout updated or server timeout"
  else
    bzr co --lightweight -v $_bzrtrunk $_bzrmod
    msg "Checkout done or server timeout"
  fi
  
  msg "checkout done"
  msg "starting build ..."

  [ -d $srcdir/${_bzrmod}-build ] && rm -rf $srcdir/${_bzrmod}-build
  cp -r $srcdir/${_bzrmod} $srcdir/${_bzrmod}-build

  # Build
  cd $srcdir/${_bzrmod}-build 
  
    ./configure --prefix=/usr --with-xft --with-xpm --with-jpeg \
      --with-tiff --libexecdir=/usr/lib --localstatedir=/var \
      --with-gif --with-png --with-x-toolkit=athena --with-libotf \
      --without-gconf --with-rsvg
  make bootstrap
  }
  package() {
  cd $srcdir/${_bzrmod}-build
  make prefix=$pkgdir/usr libexecdir=$pkgdir/usr/lib \
    localstatedir=$pkgdir/var/lib install
  for _i in 16x16 24x24 32x32 48x48 128x128
  do
    mv $pkgdir/usr/share/icons/hicolor/${_i}/apps/emacs.png \
      $pkgdir/usr/share/icons/hicolor/${_i}/apps/$pkgname.png
  done
  mv $pkgdir/usr/share/icons/hicolor/scalable/apps/emacs.svg \
    $pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg
  mv $pkgdir/usr/share/icons/hicolor/scalable/mimetypes/emacs-document.svg \
    $pkgdir/usr/share/icons/hicolor/scalable/mimetypes/$pkgname-document.svg
  chown -R root:root $pkgdir/usr/share/emacs/24.0.50
}
