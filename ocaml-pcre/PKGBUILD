# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Magnus Therning <magnus@therning.org>

pkgname=ocaml-pcre
pkgver=6.2.2
pkgrel=3
pkgdesc="Perl compatible regular expressions for OCaml"
arch=('i686' 'x86_64')
url="http://www.ocaml.info/home/ocaml_sources.html#toc18"
license=('LGPL')
depends=('ocaml' 'pcre>=4.5')
makedepends=('ocaml-findlib')
replaces=('pcre-ocaml')
conflicts=('pcre-ocaml')
source=(http://hg.ocaml.info/release/pcre-ocaml/archive/release-$pkgver.tar.bz2
        fixldflags.patch)
sha256sums=('02b08d3a15bcfea22ac309a747f881bb947c7e1f90c85c9c6927a908cb8a8927'
            '32bd0c6a7da32f95da1570f41680d97aa66dbac3fe64bd859ed00d67f558fa1e')
install=pcre-ocaml.install
options=(!strip)

build() {
  cd $srcdir/pcre-ocaml-release-$pkgver
  patch -p3 < $srcdir/fixldflags.patch
  make all htdoc || return 1
}

package() {
  cd $srcdir/pcre-ocaml-release-$pkgver

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  export OCAMLFIND_LDCONF="ignore"

  mkdir -p $OCAMLFIND_DESTDIR
  make install

  mkdir -p $pkgdir/usr/share/doc/pcre-ocaml
  cp -r lib/doc/pcre/html/* $pkgdir/usr/share/doc/pcre-ocaml
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
