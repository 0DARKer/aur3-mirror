# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Magnus Therning <magnus@therning.org>

pkgname=ocaml-pcre
pkgver=7.0.2
pkgrel=2
pkgdesc="Perl compatible regular expressions for OCaml"
arch=('i686' 'x86_64')
url="http://www.ocaml.info/home/ocaml_sources.html#toc18"
license=('LGPL')
depends=('ocaml' 'pcre>=4.5')
makedepends=('ocaml-findlib')
replaces=('pcre-ocaml')
conflicts=('pcre-ocaml')
source=("https://bitbucket.org/mmottl/pcre-ocaml/downloads/pcre-ocaml-$pkgver.tar.gz")
md5sums=('412eec5674a8bab76ccd09e006a24e10')
options=(!strip)

build() {
  cd "$srcdir/pcre-ocaml-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package() {
  cd "$srcdir/pcre-ocaml-$pkgver"
  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
