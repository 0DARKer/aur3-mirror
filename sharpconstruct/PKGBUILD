# Contributor: Alexander Rødseth <rodseth@gmail.com>
pkgname=sharpconstruct
pkgver=0.12
pkgrel=4
pkgdesc="Create 3D models by sculpting"
arch=('i686' 'x86_64')
url="http://sharp3d.sourceforge.net"
depends=('gtkglextmm' 'libglademm' 'libsigc++')
license=('GPL2')
md5sums=('59ef5f2579cdf820b3cfe6641cf744ce'
         'b103f01e6d5b5fae4521f7276d678732')
source=('http://kent.dl.sourceforge.net/sourceforge/sharp3d/sharpconstruct-0.12-rc3.tar.bz2'
        'arch.patch')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Patching..."
  patch -p0 < ../../arch.patch
  # Patch include/DataDir.hh to include malloc.h
  mv include/DataDir.hh include/DataDir.temp
  echo "#include <malloc.h>" > include/DataDir.hh
  cat include/DataDir.temp >> include/DataDir.hh
  rm include/DataDir.temp

  msg2 "Configuring..."
  ./configure --enable-sse --prefix=/usr --mandir=/usr/share/man

  msg2 "Compiling..."
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  msg2 "Packaging..."
  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
