# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: ceri

_pkgbase='citra'
pkgname=$_pkgbase-git
pkgver=r961.c0cd0fa
pkgrel=1
pkgdesc="An experimental open-source Nintendo 3DS emulator/debugger"
arch=('i686' 'x86_64')
url="https://github.com/citra-emu/citra/"
license=('GPL2')
depends=('qt5-base' 'libxkbcommon-x11' 'glfw')
makedepends=('git' 'cmake')
source=('git+https://github.com/citra-emu/citra')
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgbase"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_pkgbase"
	git submodule update --init
	mkdir build
}

build() {
	cd "$srcdir/$_pkgbase/build"
	cmake .. -DCMAKE_BUILD_TYPE=Debug -DCMAKE_CXX_FLAGS="-O2"
	make
}

package() {
	cd "$srcdir/$_pkgbase/build/src/"
	install -Dm755 citra/citra $pkgdir/usr/bin/citra
	install -Dm755 citra_qt/citra-qt $pkgdir/usr/bin/citra-qt
}
