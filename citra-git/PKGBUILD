# Maintainer: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: ceri

pkgbase='citra'
pkgname=("$pkgbase-git" "$pkgbase-qt-git")
pkgver=r2062.df71ed1
pkgrel=1
pkgdesc="An experimental open-source Nintendo 3DS emulator/debugger"
arch=('x86_64')
url="https://github.com/citra-emu/citra/"
license=('GPL2')
makedepends=('git' 'cmake')
source=("$pkgbase::git+https://github.com/citra-emu/citra")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgbase"
	echo "r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$pkgbase"
	git submodule update --init
	mkdir -p build
}

build() {
	cd "$srcdir/$pkgbase/build"
	cmake .. -DCMAKE_BUILD_TYPE=Release
	make
}

package_citra-git() {
	depends=('glfw')
	install -Dm755 "$srcdir/$pkgbase/build/src/citra/citra" "$pkgdir/usr/bin/$pkgbase"
}

package_citra-qt-git() {
	depends=('qt5-base' 'libxkbcommon-x11')
	install -Dm755 "$srcdir/$pkgbase/build/src/citra_qt/citra-qt" "$pkgdir/usr/bin/$pkgbase-qt"
}
