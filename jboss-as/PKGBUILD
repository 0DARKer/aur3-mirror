# JBoss Application Server package
# Maintainer: Zdenek Obst <zdenek dot obst at gmail dot com>

pkgname=jboss-as
_majorver=7.1
pkgver=${_majorver}.1.Final
pkgrel=1
pkgdesc='Java EE 6 Application Server'
arch=('any')
url='http://www.jboss.org/jbossas'
license=('LGPL')
depends=('java-environment')
makedepends=('java-environment>=6')
_pkgloc=usr/share
_confloc=etc/${pkgname}
backup=(${_confloc}/${pkgname}.conf
        ${_pkgloc}/${pkgname}/standalone/configuration/standalone.xml
        ${_pkgloc}/${pkgname}/standalone/configuration/mgmt-users.properties
        ${_pkgloc}/${pkgname}/standalone/configuration/logging.properties
        ${_pkgloc}/${pkgname}/domain/configuration/domain.xml
        ${_pkgloc}/${pkgname}/domain/configuration/mgmt-users.properties
        ${_pkgloc}/${pkgname}/domain/configuration/logging.properties
        ${_pkgloc}/${pkgname}/bin/standalone.conf
        ${_pkgloc}/${pkgname}/bin/domain.conf)
install=${pkgname}.install
source=(http://download.jboss.org/jbossas/${_majorver}/jboss-as-${pkgver}/jboss-as-${pkgver}.tar.gz
        ${pkgname}
	${pkgname}.conf
        ${pkgname}.install)
md5sums=('1e33096182ac7cac96089e39f7479b35'
         '0a1ca1969f691409c0c04e2cb2d281a0'
         '44c43f66c556df2ae643aad8a6c0fefc'
         'cd619ca7fa5f7e548a164f1b1664f8b9')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rm -f bin/*.{bat,exe}
  install -dm755 ${pkgdir}/${_pkgloc}
  cp -R ${srcdir}/${pkgname}-${pkgver} ${pkgdir}/${_pkgloc}/${pkgname}
  install -Dm755 ${srcdir}/${pkgname}.conf ${pkgdir}/${_confloc}/${pkgname}.conf
  install -Dm755 ${srcdir}/${pkgname} ${pkgdir}/etc/rc.d/${pkgname}
}
