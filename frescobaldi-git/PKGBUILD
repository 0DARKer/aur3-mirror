# Contributor:RaphaÃ«l Doursenaud <rdoursenaud@free.fr>

pkgname=frescobaldi-git
pkgver=v2.0.15.r71.g128d2e2
pkgrel=1
pkgdesc="A LilyPond sheet music text editor."
arch=('i686' 'x86_64')
url="http://www.frescobaldi.org/" 
license="GPL"
depends=(
	'lilypond'
	'python2>=2.6.0'
	'qt4>=4.7'
	'python2-pyqt4>=4.8.3'
	'poppler'
	'python2-poppler-qt'
	'tango-icon-theme'
	'hyphen'
)
optdepends=(
	'python-portmidi: MIDI playback (default engine)'
	'python2-pygame: MIDI playback (alternate engine)'
	'portmidi: MIDI playback (fallback engine)'
	'hyphen-de: German hyphenation rules'
	'hyphen-en: English hyphenation rules'
	'hyphen-es: Spanish hyphenation rules'
	'hyphen-fr: French hyphenation rules'
	'hyphen-hu: Hungarian hyphenation rules'
	'hyphen-it: Italian hyphenation rules'
	'hyphen-nl: Dutch hyphenation rules'
	'hyphen-ro: Romanian hyphenation rules'
)
makedepends=(
	'git'
	'automoc4'
	'cmake'
	'docbook-xsl'
)
conflicts=('frescobaldi')
provides=('frescobaldi')
source=("$pkgname"::'git://github.com/wbsoft/frescobaldi.git')
md5sums=('SKIP')

_gitroot='https://github.com/wbsoft/frescobaldi.git'
_gitname='frescobaldi'

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  # Provided by tango-icon-theme
  rm -rf "$srcdir/$pkgname/frescobaldi_app/icons/Tango"
  # Provided by hyphen-*
  rm -rf "$srcdir/$pkgname/frescobaldi_app/hyphdicts"
}

package() {
  cd "$srcdir/$pkgname"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
