# Contributor (Arch): Peter Simons <simons@cryp.to>
# Contributor (Arch): Nicolas Pouillard <nicolas.pouillard@gmail.com>
pkgname=tahoe-lafs
pkgver=1.10.0
pkgrel=2
pkgdesc="a secure, decentralized, fault-tolerant filesystem"
arch=('any')
url='http://tahoe-lafs.org/'
license=('GPL')
# net-tools is needed because Tahoe-LAFS uses /sbin/ifconfig
depends=('net-tools' 'python2' 'python2-zope-interface>=3.6.5'
         'twisted' 'python2-pyasn1' 'python2-crypto' 'pycryptopp' 'nevow'
         'python2-foolscap' 'python2-simplejson' 'zfec' 'python2-pyopenssl'
         'pyutil' 'python2-argparse' 'zbase32' 'python2-mock>=0.8.0')
source=(nodaemon.patch
        0001-auto_deps-Workaround-dep-resolution-issue.patch
        https://tahoe-lafs.org/source/tahoe-lafs/releases/allmydata-tahoe-$pkgver.tar.bz2)
sha256sums=('7fb13a50bdca729b9c78f12369e56fccf55594b646bddb270d55c08c83ce3449'
            '055079c36e58a604765312eeb98df4133c77634bdb9a88da5a8c0142ea9e4e10'
            '9c5516700fe95fdf8c57eca1e2a7b4aebf2de9a4d327d50afd684ebf833ccfe2')

prepare() {
  cd "$srcdir/allmydata-tahoe-$pkgver"
  patch -p0 < ../nodaemon.patch
  patch -p1 < ../0001-auto_deps-Workaround-dep-resolution-issue.patch
}

build(){
  cd "$srcdir/allmydata-tahoe-$pkgver"
  python2 setup.py build
  # disabled due to the nodaemon patch and the test_filesystem* tests
  #python2 setup.py test || return 1
}

package(){
  cd "$srcdir/allmydata-tahoe-$pkgver"
  python2 setup.py install --root="$pkgdir"
}
