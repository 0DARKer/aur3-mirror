# Maintainer: Michael Schubert <mschu.dev at gmail>

pkgname=dunnart-git
pkgver=20130301
pkgrel=1
pkgdesc="A constrained-based diagram editor"
arch=('i686' 'x86_64')
url="http://www.csse.monash.edu.au/~mwybrow/dunnart/"
license=('GPL')
depends=('qt4')
makedepends=('git')
provides=('adaptagrams-git')
conflicts=('adaptagrams-git')
source=(dunnart.sh)
md5sums=('00995a6a00f2798333181ef416e442a1')

_gitroot=("git://github.com/mjwybrow/dunnart.git")
_gitname=("dunnart")

build() {
  cd "$srcdir"

  if [ -d $srcdir/$_gitname ] ; then
    msg "Git checkout:  Updating existing tree"
    cd $_gitname && git pull origin
    msg "Git checkout:  Tree has been updated"
  else
    msg "Git checkout:  Retrieving sources"
    git clone $_gitroot
  fi
  msg "Checkout completed"

  cd "$srcdir/$_gitname"
  qmake-qt4 -recursive -config release dunnart.pro
  make
}

package() {
  cd "$srcdir/$_gitname"

  for FILE in $(find lib* -name "*.h"); do
    install -Dm644 $FILE "$pkgdir/usr/include/$FILE"
  done

  cd build
  mkdir -p "$pkgdir"/usr/{bin,lib/dunnart/plugins,share/dunnart}

  for FILE in libavoid libcola libdunnartcanvas libogdf libtopology libvpsc; do
    install -m755 $FILE.so.1.0.0 "$pkgdir/usr/lib"
    ln -s /usr/lib/$FILE.so.1.0.0 "$pkgdir/usr/lib/$FILE.so"
    ln -s /usr/lib/$FILE.so.1.0.0 "$pkgdir/usr/lib/$FILE.so.1"
    ln -s /usr/lib/$FILE.so.1.0.0 "$pkgdir/usr/lib/$FILE.so.1.0"
    ln -s /usr/lib/$FILE.so.1.0.0 "$pkgdir/usr/lib/dunnart/$FILE.so.1"
  done

  install -m755 Dunnart "$pkgdir/usr/lib/dunnart"
  install -m755 plugins/* "$pkgdir/usr/lib/dunnart/plugins"
  install -m644 *.png "$pkgdir/usr/share/dunnart"
  install -Dm755 "$srcdir/dunnart.sh" "$pkgdir/usr/bin/dunnart"
}

