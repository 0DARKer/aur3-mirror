pkgname=mingw-w64-smpeg
pkgver=0.4.5.svn399
pkgrel=1
pkgdesc="SDL MPEG Player Library (mingw-w64)"
arch=(any)
url="http://icculus.org/smpeg"
license=("LGPL")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt mingw-w64-sdl)
conflicts=("mingw-w64-smpeg-svn=399")
provides=("mingw-w64-smpeg-svn=399")
options=(!libtool !strip !buildflags)

_svntrunk="svn://svn.icculus.org/smpeg/trunk"
_svnmod=smpeg
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
	if [ -d $_svnmod/.svn ]; then
		(cd $_svnmod && svn up -r 399)
	else
		svn co $_svntrunk --config-dir ./ -r 399 $_svnmod
	fi
	
	msg "SVN checkout done or server timeout"
	msg "Starting make..."
	
	cd "smpeg"
	./autogen.sh
	
	for _arch in ${_architectures}; do
		unset LDFLAGS
		mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		${srcdir}/smpeg/configure \
			--prefix=/usr/${_arch} \
			--build=$CHOST \
			--host=${_arch} \
			--disable-sdltest \
			--disable-gtk-player \
			--disable-gtktest \
			--disable-opengl-player \
			--with-sdl-prefix=/usr/${_arch} \
			--without-x
		make LDFLAGS+="-lstdc++"
		${_arch}-gcc -shared .libs/*.o -lSDL -lstdc++ -o libsmpeg.dll -Xlinker --out-implib \
			-Xlinker libsmpeg.dll.a
	done
}

package() {
	for _arch in ${_architectures}; do
		cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		make DESTDIR="$pkgdir" install
		install -m644 libsmpeg.dll "$pkgdir/usr/$_arch/bin/"
		install -m644 libsmpeg.dll.a "$pkgdir/usr/$_arch/lib/"
		find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
		find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
		find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
		rm -r "$pkgdir/usr/${_arch}/share"
	done
}
