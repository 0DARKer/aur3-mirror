# Maintainer: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=comodo
pkgver=1.1.268025
_revision=1
pkgrel=2
pkgdesc="An antivirus suite with real-time protection"
arch=('x86_64' 'i686')
url="https://www.comodo.com/"
license=('custom')
depends=('bzip2' 'expat' 'fontconfig' 'freetype2' 'gcc-libs' 'glib2' 'glibc' 'libdbus' 'libffi' 'libice' \
         'libpng' 'libsm' 'libutil-linux' 'libx11' 'libxau' 'libxcb' 'libxdmcp' 'libxext' 'libxrender' 'openssl' \
         'pam' 'pcre' 'qt4' 'zlib')
optdepends=('postfix'
            'exim'
            'qmail'
            'sendmail')
options=(!strip)
install=comodo.install
source=("http://cdn.download.comodo.com/cavmgl/download/installs/1000/standalone/CAV_LINUX-${pkgver}-${_revision}.${CARCH}.rpm"
        comodo.sh)

[[ $CARCH == 'x86_64' ]] && md5sums=('555ee6f51d116d2462962a2e4256aeba' \
                                     'cf564cf0421e14d1121eaa7112003c1e')
[[ $CARCH == 'i686' ]] && md5sums=('94c3b08023754ecc09c81be1cc5e7ba0' \
                                   'cf564cf0421e14d1121eaa7112003c1e')

package() {
  # dump opt files to opt
  mv opt "$pkgdir/"

  # don't install to /usr/local
  mv usr/local "$pkgdir/usr"

  # remove libs provided by dependencies
  rm -rf "$pkgdir/usr/Trolltech/Qt-4.7.4-Comodo/lib"

  # install profile script to add binaries to PATH
  install -Dm644 comodo.sh "$pkgdir/etc/profile.d/comodo.sh"
}
