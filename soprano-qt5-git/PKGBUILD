# Maintainer: Dimitri Merejkowsky <d.merej@archlinux.org>

pkgname=soprano-qt5-git
_gitname=soprano
pkgver=v2.9.3.90.g3e00d37
pkgrel=2
pkgdesc="A library which provides a highly usable object-oriented C++/Qt5 framework for RDF data"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('qt5-base' 'redland-storage-virtuoso' 'libiodbc' 'virtuoso-base')
makedepends=('cmake' 'doxygen' 'git')
conflicts=('soprano')
provides=('soprano')
groups=('kde-git')
source=('git://anongit.kde.org/soprano'
        '0001-Install-libraries-into-usr-lib-not-usr-lib64.patch')
md5sums=('SKIP'
         '3f5b47eb83a1c2ff14e81535e57f239e')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd $srcdir/$_gitname
  patch -p1 < ../0001-Install-libraries-into-usr-lib-not-usr-lib64.patch
}

build() {
  cd $srcdir
  msg "GIT checkout done"
  msg "Starting make..."
  if [[ -d ${srcdir}/build ]]; then
    msg "Cleaning the previous build directory..."
    rm -rf ${srcdir}/build
  fi
  mkdir $srcdir/build
  cd $srcdir/build
  cmake $srcdir/$_gitname -DCMAKE_BUILD_TYPE=debugfull \
		-DCMAKE_SKIP_RPATH=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSOPRANO_DISABLE_CLUCENE_INDEX=ON \
        -DQT5_BUILD=ON
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
