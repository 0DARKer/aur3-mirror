# Maintainer: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.4.11
pkgrel=2
pkgdesc="A tiling window manager initialy based on a dwm code rewriting."
arch=('i686' 'x86_64')
url="http://awesome.naquadah.org/"
license=('GPL2')
depends=('cairo-xcb' 'pango' 'imlib2' 'lua' 'libev' 'dbus' 'libxdg-basedir'
         'startup-notification' 'xcb-util'{-image,-keysyms,-wm})
makedepends=('docbook-xsl' 'asciidoc' 'xmlto' 'doxygen' 'gperf' 'cmake' 'luadoc' 'imagemagick')
optdepends=('rlwrap: readline support for awesome-client')
options=('!libtool' 'docs')
install=awesome.install
source=($url/download/$pkgname-$pkgver.tar.xz
        awesome.desktop)
md5sums=('d6aa71334b5cd4ef63ce69d6c612ecf2'
         '2763cab6a20d4b0f2676329d57ed3a45')
sha256sums=('576b4f6d2c3f56dfbe52ad92f2eecece111f0f05816e32126cd03d4b4387761d'
            '5c5437448cc9f01be6ccbb298f5c86d0f8c4bcae23a22e6af699aff0d10f642f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make CMAKE_ARGS=" -DPREFIX=/usr -DSYSCONFDIR=/etc \
	-DCMAKE_BUILD_TYPE=RELEASE"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

 # install desktop file so you can start awesome from your login manager
 install -Dm644 "$srcdir/awesome.desktop" \
        "$pkgdir/usr/share/xsessions/awesome.desktop"
}
