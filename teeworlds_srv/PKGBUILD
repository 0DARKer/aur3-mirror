# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Armin 'IsSuE' Praher [issue.at.gmx.dot.at]
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=teeworlds_srv
pkgver=0.5.2
_bamver=0.2.0
pkgrel=3
pkgdesc="A multiplayer 2D shooter - server only"
arch=('i686' 'x86_64')
url="http://teeworlds.com"
license=('custom')
depends=()
makedepends=('python2')
replaces=('teewars_srv')
conflicts=('teeworlds')
source=(http://www.teeworlds.com/files/teeworlds-${pkgver}-src.tar.gz
        http://www.teeworlds.com/files/bam-${_bamver}.tar.gz
        fix_datadir_search.patch)
md5sums=('f605f6df9f1714dcda4cba1d281cc757'
         'd4efe74591a73c1cec8b34d76ffd1049'
         '673791ecb74274346456f0fae53b4428')


build() {
	# Build bam (used to build teewars)
	# Now it is released separately I should make a separate package...
	cd ${srcdir}/bam-${_bamver}
	./make_unix.sh

	# Build teeworlds
	cd ${srcdir}/teeworlds-${pkgver}-src

	# fix for FS 13118 which will be applied in next release
	patch -p1 -i ../fix_datadir_search.patch

	# Use Python 2
	sed -i 's/python /python2 /' default.bam

	../bam-${_bamver}/src/bam server_release
}

package() {
	cd ${srcdir}/teeworlds-${pkgver}-src

	# Install data files
	mkdir -p ${pkgdir}/usr/share/teeworlds/data
	cp -r ${srcdir}/teeworlds-${pkgver}-src/data/* \
		${pkgdir}/usr/share/teeworlds/data

	install -Dm755 ${srcdir}/teeworlds-${pkgver}-src/${pkgname} \
		${pkgdir}/usr/bin/${pkgname}

	install -Dm644 ${srcdir}/teeworlds-${pkgver}-src/license.txt \
		${pkgdir}/usr/share/licenses/teeworlds/license.txt
}
