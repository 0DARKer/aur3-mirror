#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=pkg_scripts
pkgver=2014
pkgrel=1
pkgdesc='A collection of pacman and package-related utilities.'
arch=(any)
license=(GPL)
url="http://xyne.archlinux.ca/projects/pkg_scripts"
depends=(python3 pyalpm)
source=(
  http://xyne.archlinux.ca/projects/pkg_scripts/src/pkg_scripts-2014.tar.xz
  http://xyne.archlinux.ca/projects/pkg_scripts/src/pkg_scripts-2014.tar.xz.sig
)
sha512sums=(
  e654956f30c363972075551565dba76f65960a255daa24345ba2c6cc372d0ca73a12234816838ac07a1f5323d58b998a9a338cd2011e7ce03ccee96cb7c681d3
  1f392f4770908c1f8db03533fa42d8dc305961d887d5aa6c7be03394ad44cfac695e7644ca4b1dc0a3377f8f76b0e75d9d1a0585c773dd796bed2505a99109a4
)
md5sums=(
  fb512373150ef084c3f99521fcca699c
  2d224b5ea2cac8a8cd676ab999dea69e
)

package ()
{
  cd "$srcdir/$pkgname-$pkgver/src"
  for pkg in pkg-*
  do
    name="${pkg##*/}"
    install -Dm755 "$name" "$pkgdir/usr/bin/$name"
  done
}

# vim: set ts=2 sw=2 et:
