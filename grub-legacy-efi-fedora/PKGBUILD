# Maintainer : Keshav P R <(skodabenz) (aatt) (rocketmail) (ddoott) (ccoomm)>

# This PKGBUILD is based on grub.spec at http://pkgs.fedoraproject.org/gitweb/?p=grub.git;a=blob_plain;f=grub.spec;hb=HEAD .

# This grub-legacy package includes support for GPT (default for any EFI based boot loader) and for reading EXT4 filesystem (/boot can be ext4).

pkgname="grub-legacy-efi-fedora"
pkgver=20110705
pkgrel=1
pkgdesc="Fedora's grub-legacy fork - compiled for UEFI systems"
url="http://git.kernel.org/?p=boot/grub-fedora/grub-fedora.git"
arch=('i686' 'x86_64') ## Not sure about i686 or i386 - I have tried only x86_64 version till now.
license=('GPL')
makedepends=('git' 'glibc' 'ncurses' 'diffutils' 'autoconf' 'automake' 'gnu-efi')
depends=('dosfstools' 'efibootmgr')
optdepends=('mtools')
backup=()
conflicts=('grub-efi-fedora')
provides=()
source=()
install="${pkgname}.install"

_gitroot="git://git.kernel.org/pub/scm/boot/grub-fedora/grub-fedora.git"
_gitname="grub-fedora"


_update_git() {
	
	cd "${srcdir}"
	
	msg "Connecting to GIT server...."
	
	if [ -d "${srcdir}/${_gitname}/" ]
	then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout master
		git merge remotes/origin/master
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
		msg "GIT checkout done or server timeout"
	fi
	
	echo
	
}


build() {
	
	_update_git
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build/"
	
	sed -i "s|0\.97|0\.97-${pkgver}|" "${srcdir}/${_gitname}_build/configure.in" || true
	sed -i "s|0\.97|0\.97-${pkgver}|" "${srcdir}/${_gitname}_build/configure.ac" || true
	echo
	
	autoreconf
	echo
	autoconf
	echo
	
	./configure --with-platform=efi \
				--disable-auto-linux-mem-opt \
				--without-curses \
				--libdir=/usr/lib
	echo
	
	CFLAGS="-Os -g -fno-strict-aliasing -Wall -Werror -Wno-shadow -Wno-unused -Wno-pointer-sign" make
	echo
	
}


package() {
	
	cd "${srcdir}/${_gitname}_build/"
	
	install -D -m644 "${srcdir}/${_gitname}_build/efi/grub.efi" "${pkgdir}/boot/efi/EFI/grub-legacy/grub-legacy.efi"
	
}
