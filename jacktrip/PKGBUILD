# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=jacktrip
pkgver=1.0.5.patch2
pkgrel=2
pkgdesc="A System for High-Quality Audio Network Performance over the Internet."
arch=(i686 x86_64)
url="http://code.google.com/p/jacktrip/"
license=('custom:MIT')
depends=('qt4' 'jack')
source=("http://jacktrip.googlecode.com/files/$pkgname-$pkgver.tar.gz")
md5sums=('2fbdd43514b0cfd786ee4449f3fcd355')

prepare() {
  cd $pkgname-${pkgver/2}/src

  # gcc fix
  sed -i '42i#include <unistd.h>' jacktrip_globals.cpp
}

build() {
  cd $pkgname-${pkgver/2}/src
  qmake-qt4
  make clean
  make release
}

package() {
  cd $pkgname-${pkgver/2}/src

  # bin
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"

  # license
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '1,30p' ../documentation/documentation.cpp > \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
