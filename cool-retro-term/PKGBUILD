# Maintainer: Doug Newgard <scimmia at archlinux dot info>
# Contributor: Andrey Mivrenik <gim at fastmail dot fm>
# Contributor: Glen Oakley <goakley123@gmail.com>

pkgname=cool-retro-term
pkgrel=1
_pkgver=1.0.0-RC1
pkgver=${_pkgver/-/}
pkgdesc='A good looking terminal emulator which mimics the old cathode display'
arch=('i686' 'x86_64')
url='https://github.com/Swordfish90/cool-retro-term'
license=('GPL3')
depends=('qmltermwidget-git' 'qt5-quickcontrols' 'qt5-graphicaleffects')
makedepends=('git')
source=("https://github.com/Swordfish90/cool-retro-term/archive/v$_pkgver.tar.gz")
sha256sums=('c1c2896c90a28e59e38898abddf1e464a96d59def0310bf3b3494eba96f235bc')

prepare() {
  cd "$srcdir/$pkgname-$_pkgver"

  sed -i '/qmltermwidget/d' cool-retro-term.pro
}

build() {
  cd "$srcdir/$pkgname-$_pkgver"

  qmake
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"

  make INSTALL_ROOT="$pkgdir" install
}
