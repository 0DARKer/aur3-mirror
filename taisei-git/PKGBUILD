# Maintainer: Lukas Weber <laochailan@web.de>
pkgname=taisei-git
pkgver=20120809
pkgrel=1
pkgdesc="Open source  Touhou clone."
arch=(i686 x86_64)
url="http://taisei-project.org/"
license=('MIT')
depends=('sdl_ttf' 'hicolor-icon-theme' 'mesa' 'libpng' 'freealut')
provides=('taisei')
conflicts=('taisei')
makedepends=('git' 'cmake')
install=taisei.install
source=()
md5sums=() 

_gitroot=git://github.com/laochailan/taisei.git
_gitname=taisei

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build/build"
  make DESTDIR="$pkgdir/" install

  mkdir -p $pkgdir/usr/share/licenses/taisei-git
  cp ../COPYING $pkgdir/usr/share/licenses/taisei-git
}

# vim:set ts=2 sw=2 et:
