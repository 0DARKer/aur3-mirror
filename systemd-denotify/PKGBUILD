# Maintainer: Ilias Stamatis <stamatis.iliass at gmail dot com>

pkgname=systemd-denotify
pkgver=r441.3e0887d
pkgrel=2
pkgdesc='Classes providing desktop notifications upon a user login, when systemd files are modified and when services fail'
arch=(any)
url='https://github.com/gkarakou/systemd-denotify'
license=('GPL3')
depends=('python2-dbus' 'python2-gobject' 'python2-notify' 'python2-pyinotify' 'python2-systemd')
makedepends=('git' 'python2-setuptools')
backup=('etc/systemd-denotify.conf')
install=systemd-denotify.install
source=("${pkgname}::git+https://github.com/gkarakou/systemd-denotify")
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$pkgname"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}
