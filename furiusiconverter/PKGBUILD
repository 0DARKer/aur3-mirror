# Maintainer: TDY <tdy@archlinux.info>

pkgname=furiusiconverter
pkgver=0.2.0.0
pkgrel=1
pkgdesc="A simple Gtk+ interface to convert any video to iPod, iPod Touch, and iPhone MP4 format"
arch=('any')
url="https://launchpad.net/furiusiconverter"
license=('GPL3')
depends=('pygtk' 'ffmpeg')
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/${pkgname}_$pkgver.tar.gz
        $pkgname.py
        $pkgname.desktop)
md5sums=('2d107d1c369811bf63995bc9182d14ef'
         'd2bbf671a6cafa25c89e3576ff58b8c3'
         'd8e7c60eead01a85b1089c6f8edba74f')

build() {
  cd "$srcdir/${pkgname}_$pkgver/src"

  # python2
  find -type f -name '*.py' -exec sed -i '1s/python/&2/' '{}' \;
  sed -i 's/python/&2/' ../$pkgname

  # dist
  install -dm755 "$pkgdir"/usr/share/$pkgname/{doc,pix,res}
  install -cm644 *.* "$pkgdir/usr/share/$pkgname"
  install -cm644 doc/* "$pkgdir/usr/share/$pkgname/doc"
  install -cm644 pix/* "$pkgdir/usr/share/$pkgname/pix"
  install -cm644 res/* "$pkgdir/usr/share/$pkgname/res"

  # launchers
  install -Dm755 "$srcdir/$pkgname.py" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
