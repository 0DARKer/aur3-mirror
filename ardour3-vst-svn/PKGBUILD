# Maintainer : prettyvanilla <prettyvanilla@lavabit.com>
# Contributor: Bernardo Barros <bernardobarros@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Uli Armbruster <uli_armbruster@web.de>

_pkgname=ardour3
pkgname=ardour3-vst-svn
pkgver=14043
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation (with linuxVST support)"
arch=('i686' 'x86_64')
url="http://www.ardour.org/"
license=('GPL2')
depends=('libgnomecanvasmm>=2.12.0' 'liblrdf>=0.4.0' 'liblo' 'aubio'
         'libsmf' 'lilv<=1.0.0' 'suil<=1.0.0' 'desktop-file-utils' 'jack')
makedepends=('subversion' 'python2' 'boost' 'cppunit>=1.12.0' 'doxygen' 'graphviz' 'steinberg-vst')
provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-svn")
install="$pkgname.install"
source=("$pkgname.desktop")
md5sums=('2736c648e452ae45c703e48cf64f75e7')

_svntrunk="http://subversion.ardour.org/svn/ardour2/branches/3.0"
_svnmod="$_pkgname"

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  python2 waf configure \
    --prefix=/usr \
    --configdir=/etc \
    --docs \
    --freedesktop \
    --lv2 \
    --lxvst \
    --noconfirm \
    --optimize

  python2 waf build
}

package() {
  cd "$srcdir/$_svnmod-build"

  python2 waf --destdir="$pkgdir/" install

  # desktop file
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
