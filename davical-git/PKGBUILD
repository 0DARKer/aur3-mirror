# Maintainer: Nikolay Amiantov <nikoamia@gmail.com>
# Contributor: Andre Wayand <aur-davical@awayand.sleepmail.com>
# Contributor: <zenlord@gmail.com>
# Contributor: Michael Seiwald <michael@mseiwald.at>
pkgname=davical-git
pkgver=0.9.5.2.r2130.g6fa5630
pkgrel=1
pkgdesc="CalDAV CardDAV Server"
arch=('any')
url="http://davical.org/"
license=('GPL')
provides=('davical')
conflicts=('davical')
options=('!strip')
depends=('php>=5.1' 'awl>=0.53' 'php-pgsql' 'perl-dbd-pg' 'perl-yaml' 'perl-dbi')
optdepends=(
	'postgresql>=8.1: Run postgresql locally'
	'apache>=1.3.0: Web server to run davical'
	'nginx: Web server to run davical'
)
backup=(
	"etc/httpd/conf/extra/httpd-davical.conf"
	"etc/davical/localhost-conf.php"
)
install="davical.install"
changelog=ChangeLog
source=(
	"git+https://github.com/DAViCal/davical.git"
	httpd-davical.conf
	README.archlinux
)

pkgver() {
  cd "$srcdir/davical"
  git describe --long | sed -r 's/^r//;s/([^-]*-g)/r\1/;s/-/./g'
}

package() {
  cd "${srcdir}/davical"
  
  install -d "${pkgdir}/srv/http"
  cp -a . "${pkgdir}/srv/http/davical"

  install -d "${pkgdir}/etc/davical"
  install -D "config/example-config.php" "${pkgdir}/etc/davical/localhost-conf.php"
  install -D "${srcdir}/README.archlinux" "${pkgdir}/srv/http/davical/"

  install -d "${pkgdir}/etc/httpd/conf/extra"
  install -t "${pkgdir}/etc/httpd/conf/extra" "${srcdir}/httpd-davical.conf"
}
sha1sums=('SKIP'
          'a3d7860a6da5966d53b3e4b798c17d0d8785952b'
          '5040b5dfa278cf2a5b6a097a318a50086f55602a')
