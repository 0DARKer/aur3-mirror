# Maintainer: Gonzalo Exequiel Pedone <hipersayan DOT x AT gmail DOT com>

pkgname=webcamoid-git
pkgver=5.1.0.r36.geb2c9ed
pkgrel=1
pkgdesc="Webcamoid is a full featured webcam capture application."
url='https://github.com/hipersayanX/Webcamoid'
license=('GPL')
arch=('i686' 'x86_64')
depends=('qt5-multimedia' 'ffmpeg' 'opencv')
makedepends=('git' 'qt5-tools')
provides=('webcamoid')
conflicts=('kdeplasma-applets-webcamoid' 'webcamoid')
source=("git://github.com/hipersayanX/Webcamoid.git")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/Webcamoid"
    (
        set -o pipefail
        git describe --long --tags 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

build() {
    cd "$srcdir/Webcamoid"
    qmake-qt5 Webcamoid.pro
    make
}

package() {
    cd "$srcdir/Webcamoid"
    make INSTALL_ROOT="${pkgdir}" install
}
