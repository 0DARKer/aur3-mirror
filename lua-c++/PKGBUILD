# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=lua-c++
pkgver=5.1.4
pkgrel=1
pkgdesc="A powerful, lightweight programming language for extending applications, compiled as C++ instead of C" 
arch=('i686' 'x86_64')
url="http://www.lua.org/"  
license=('MIT')
depends=('readline' 'gcc-libs')
provides=('lua')
conflicts=('lua')
source=($url/ftp/lua-$pkgver.tar.gz \
	lua-arch.patch \
	lua-5.1-cflags.diff)
md5sums=('d0870f2de55d59c1c8419f36e8fac150'
         '1bd1164a19abf9165e231ba0d8a0bbc7'
         '249582bf1fd861ccf492d2c35a9fe732')

build() { 
  cd lua-$pkgver

  patch -Np1 -i ../lua-arch.patch || return 1
  export CFLAGS="${CXXFLAGS}"

  [ "$CARCH" = "x86_64" ] && {
		patch -Np1 -i ../lua-5.1-cflags.diff || return 1
		export CFLAGS+=" -fPIC"
  }
 
  rm -f src/liblua.so

  make	CC=c++ \
	LUA_SO=liblua.so \
	linux || return 1

  make	INSTALL_DATA="cp -d" \
	INSTALL_TOP="$pkgdir"/usr \
	INSTALL_MAN="$pkgdir"/usr/share/man/man1 \
	TO_LIB="liblua.a liblua.so liblua.so.5.1" \
	linux install

  install -Dm644 etc/lua.pc "$pkgdir"/usr/lib/pkgconfig/lua.pc
  install -Dm644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et ft=sh
