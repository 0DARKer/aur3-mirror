# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=wingide
pkgver=5.0.4
pkgrel=1
_pkgarch=`uname -m`
pkgdesc="Wing IDE Professional is the full-featured Python IDE for professional programmers."
url="http://www.wingware.com"
license=('custom')
arch=('i686' 'x86_64')
source=("http://wingware.com/pub/$pkgname/$pkgver/$pkgname-$pkgver-$pkgrel-x86_64-linux.tar.gz")
depends=('glib2' 'hicolor-icon-theme' 'xdg-utils')
optdepends=("qt4: use the system Qt4 rather than the version built into ${pkgname}")
makedepends=('python2')
options=(!strip)
sha1sums=('a0f60c71f43e2f35399ac7f7b2c7d2f3553bb0aa')
install=${pkgname}.install
PKGEXT=".pkg.tar"

if [ "$CARCH" == "i686" ]; then
    _pkgarch="i386"
    sha1sums[0]='bf1b0584db8f323431988c548b3d1c1041d0e14c'
    source[0]="http://wingware.com/pub/$pkgname/$pkgver/$pkgname-$pkgver-$pkgrel-i386-linux.tar.gz"
fi

prepare() {
    cd "$srcdir/$pkgname-$pkgver-$pkgrel-$_pkgarch-linux"
    sed -i 's/python/python2/' wing-install.py
}

package() {
    cd "$srcdir/$pkgname-$pkgver-$pkgrel-$_pkgarch-linux"
    ./wing-install.py \
        --install-binary \
        --install-source \
        --winghome "${pkgdir}/opt/${pkgname}" \
        --bin-dir ${pkgdir}/usr/bin > /dev/null 2>&1
        
    # Correct the WINGHOME environment variable
    sed -i "s|${pkgdir}||" ${pkgdir}/opt/${pkgname}/wingdbstub.py
    sed -i "s|${pkgdir}||" ${pkgdir}/opt/${pkgname}/wing
    # Correct the file permissions
    chown -R root:root "${pkgdir}/opt/${pkgname}"
    chmod +x ${pkgdir}/opt/${pkgname}/resources/linux/desktop/install-linux-desktop.sh
}
