# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=wingide
pkgver=5.0.6
pkgrel=1
_pkgarch="x86_64"
pkgdesc="Wing IDE Professional is the full-featured Python IDE for professional programmers."
url="http://www.wingware.com"
license=('custom')
arch=('i686' 'x86_64')
source=("http://wingware.com/pub/$pkgname/$pkgver/$pkgname-$pkgver-$pkgrel-x86_64-linux.tar.gz")
depends=('hicolor-icon-theme' 'libpng12' 'python2' 'xdg-utils')
options=(!strip !emptydirs)
sha1sums=('5fcaf9658a3e46c3f48d63a3ac76ccdf5df6f3db')
install=${pkgname}.install
PKGEXT=".pkg.tar"

if [ "$CARCH" == "i686" ]; then
    _pkgarch="i386"
    sha1sums[0]='fa02513eaccc667d12a16b599816cbe7d5ce981d'
    source[0]="http://wingware.com/pub/$pkgname/$pkgver/$pkgname-$pkgver-$pkgrel-i386-linux.tar.gz"
fi

prepare() {
    cd "$srcdir/$pkgname-$pkgver-$pkgrel-$_pkgarch-linux"
    sed -i 's/python/python2/' wing-install.py
}

package() {
    cd "$srcdir/$pkgname-$pkgver-$pkgrel-$_pkgarch-linux"
    ./wing-install.py \
        --install-binary \
        --winghome "${pkgdir}/opt/${pkgname}" \
        --bin-dir ${pkgdir}/usr/bin > /dev/null 2>&1

    # Correct the WINGHOME environment variable
    sed -i "s|${pkgdir}||" ${pkgdir}/opt/${pkgname}/wingdbstub.py
    sed -i "s|${pkgdir}||" ${pkgdir}/opt/${pkgname}/wing
    sed -i "s|python|python2|" ${pkgdir}/opt/${pkgname}/bin/wing.py
    # Correct the file permissions
    chown -R root:root "${pkgdir}/opt/${pkgname}"
    chmod +x ${pkgdir}/opt/${pkgname}/resources/linux/desktop/install-linux-desktop.sh
    # Install the LICENSE
    install -D -m 644 "${pkgdir}/opt/${pkgname}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
