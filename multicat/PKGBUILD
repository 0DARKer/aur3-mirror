# Maintained by SÃ©bastien Leblanc ( seb@sebleblanc.net )

pkgname=multicat
pkgver=2.0
pkgrel=2

_name=${pkgname}-${pkgver}
_bitstreamver=1.0
# 2012-03-12

pkgdesc="A simple and efficient multicast and transport stream manipulation utility, by VideoLAN"
url="http://www.videolan.org/projects/multicat.html"
arch=('x86_64' 'i686')
license=('GPL')

source=("http://downloads.videolan.org/pub/videolan/${pkgname}/${pkgver}/${_name}.tar.bz2"
       "http://download.videolan.org/pub/videolan/bitstream/1.0/bitstream-1.0.tar.bz2")
       
md5sums=('3aaa226a421c378c2ede0c8db8fd609c'
         '5ba0c96d6c7c9131ba60a9a8fc147556')

build() {
  ln -s "${srcdir}/bitstream-1.0" "${srcdir}/${_name}/bitstream"
  cd "${srcdir}/${_name}"
  sed -i -e's:<bitstream/mpeg/ts.h>:"bitstream/mpeg/ts.h":' -e's:<bitstream/ietf/rtp.h>:"bitstream/ietf/rtp.h":' *.c
  make
}

package() {
  cd "${srcdir}/${_name}"
  make DESTDIR="${pkgdir}" PREFIX=usr install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}