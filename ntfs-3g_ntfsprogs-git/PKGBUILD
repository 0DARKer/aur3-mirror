# Maintainer: Keshav P R <skodabenz aatt rocketmail ddoott ccomm>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Bächler <thomas.archlinux.org>

_pkgname="ntfs-3g_ntfsprogs"
pkgname="${_pkgname}-git"

pkgver=20110705
pkgrel=1
pkgdesc="Read and write NTFS driver and utilities - GIT version"
url="http://www.tuxera.com"
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('pkgconfig')
depends=('glibc' 'fuse' 'util-linux')
conflicts=("${_pkgname}" 'ntfs-3g' 'ntfsprogs' 'ntfs-3g-ar')
provides=("${_pkgname}" 'ntfs-3g' 'ntfsprogs' 'ntfs-3g-ar')
options=(strip !emptydirs zipman !libtool docs)

source=('25-ntfs-config-write-policy.fdi')
sha256sums=('2fb07cecf5b63881ef2ea8ad298df7a640ec84aebb883979bbc222113756b0d2')

_gitroot="git://ntfs-3g.git.sourceforge.net/gitroot/ntfs-3g/ntfs-3g_ntfsprogs"
_gitname="${_pkgname}"

_update_git() {
	
	cd "${srcdir}"
	msg "Connecting to GIT server...."
	
	if [ -d "${srcdir}/${_gitname}/" ]
	then
		cd "${srcdir}/${_gitname}/"
		git reset --hard
		git fetch
		git checkout edge
		git merge remotes/origin/edge
		msg "The local GIT repo has been updated."
	else
		git clone "${_gitroot}" "${_gitname}"
		msg "GIT checkout done or server timeout"
	fi
	
	echo
	
}

build() {
	
	_update_git
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build/"
	
	./autogen.sh
	echo
	
	ac_cv_path_LDCONFIG=/bin/true ./configure --prefix=/usr \
											--mandir=/usr/share/man \
											--enable-posix-acls \
											--enable-xattr-mappings \
											--enable-extras \
											--with-fuse=external \
											--disable-static
	echo
	
	make
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build/"
	
	make DESTDIR="${pkgdir}" install
	echo
	
	ln -s "/bin/ntfs-3g" "${pkgdir}/sbin/mount.ntfs"
	ln -s "/usr/bin/ntfsck" "${pkgdir}/sbin/fsck.ntfs"
	ln -s "/usr/bin/ntfsck" "${pkgdir}/sbin/fsck.ntfs-3g"
	
	install -d "${pkgdir}/usr/share/hal/fdi/policy/10osvendor"
	install -D -m644 "${srcdir}/25-ntfs-config-write-policy.fdi" "${pkgdir}/usr/share/hal/fdi/policy/10osvendor/25-ntfs-config-write-policy.fdi"
	
}
