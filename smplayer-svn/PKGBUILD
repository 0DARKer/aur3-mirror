# Maintainer: -
# Contributors: Det, goetzc, Ner0, Lari Tikkanen, oke3, Flamelab, WAntilles

pkgname=smplayer-svn
pkgver=14.9.0.r6824
pkgrel=1
pkgdesc="Advanced front-end for MPlayer/MPV (Subversion)"
arch=('i686' 'x86_64')
url="https://www.assembla.com/code/smplayer/subversion/commits"
license=('GPL')
depends=('qt5-script' 'libxkbcommon-x11' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'subversion')
optdepends=('smplayer-themes-svn: icon themes collection [AUR]'
            'smplayer-skins-svn: skin themes collection [AUR]'
            'smtube-svn: browse and play YouTube videos [AUR]'
            'mplayer: necessary backend for video playback'
            'mpv: alternative modern backend, based on MPlayer/MPlayer2')
provides=('smplayer')
conflicts=('smplayer')
install=$pkgname.install
source=('smplayer::svn+https://subversion.assembla.com/svn/smplayer/smplayer/trunk/')
md5sums=('SKIP')

pkgver() {
  cd smplayer
  echo $(grep -m1 'version' smplayer.spec | cut -d " " -f3).r$(svnversion)
}

build() {
  cd smplayer
  
  msg2 "Running 'make clean'..."
  make clean

  msg2 "Running 'make'..."
  make PREFIX=/usr \
       DOC_PATH="\\\"/usr/share/doc/smplayer\\\""
}

package() {
  cd smplayer

  msg2 "Running 'make install'..."
  make DOC_PATH=/usr/share/doc/smplayer \
       DESTDIR="$pkgdir" \
       PREFIX="/usr" \
       install
}