# Maintainer: oke3 < Sekereg {at} gmx {dot} com
# Contributor: Flamelab <panosfilip@gmail.com>
# Fixes and patches: WAntilles <wantilles@adslgr.com>

pkgname=smplayer-svn
pkgver=3646
_realver=0.6.9
pkgrel=1
pkgdesc="A complete front-end for MPlayer"
arch=('i686' 'x86_64')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('qt>=4.7.2' 'mplayer' 'desktop-file-utils' 'xdg-utils')
makedepends=('subversion')
conflicts=('smplayer')
provides=("smplayer=$_realver")
install=$pkgname.install
source=("optionally-enable-vdpau-divx-decoder-ng.patch"
    "smplayer-display-matroska-chapter-names-2.patch"
    "seek-while-pausing-mplayer2-fix.diff")
sha1sums=('298dab715923348c180afaa6df13b0e7b0893c81'
    '2c264379ab9a8a0c0fa649c771ae08f72a58d6a9'
    '82bc185df78c02a5fd485d226ac9ae9d87c0ad1a')

_svnmod="smplayer"
_svntrunk=https://smplayer.svn.sourceforge.net/svnroot/smplayer/smplayer/trunk/

build() {
     cd "${srcdir}"

     if [[ -d $_svnmod/.svn ]]; then
          (cd $_svnmod && svn up -r $pkgver)
     else
          svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
     fi

     msg "SVN checkout done or server timeout"
     msg "Starting make..."

     [[ -d $_svnmod-build ]] && rm -r $_svnmod-build
     cp -r $_svnmod $_svnmod-build
     cd $_svnmod-build

    # remove broken translation
    sed -i "s|translations/smplayer_gl.ts|#translations/smplayer_gl.ts|" src/smplayer.pro

    # give the option to enable vdpau divx decoding through smplayer.ini
    #patch -Np2 -i ${srcdir}/optionally-enable-vdpau-divx-decoder-ng.patch

    # display matroska container chapter names
    # http://smplayer.sourceforge.net/forum/viewtopic.php?f=4&t=661
    patch -Np2 -i ${srcdir}/smplayer-display-matroska-chapter-names-2.patch

    # mplayer2 seek while paused
    # http://smplayer.sourceforge.net/forum/viewtopic.php?f=4&t=1005
    #patch -Np2 -i ${srcdir}/seek-while-pausing-mplayer2-fix.diff

    make PREFIX=/usr
}

package() {
    cd ${srcdir}/$_svnmod-build
    make PREFIX=${pkgdir}/usr install

    install -dm755 ${pkgdir}/usr/share/pixmaps
    ln -s /usr/share/icons/hicolor/64x64/apps/smplayer.png \
        ${pkgdir}/usr/share/pixmaps/

    sed -i 's|Exec=smplayer %U|Exec=smplayer %F|' ${pkgdir}/usr/share/applications/smplayer.desktop
    sed -i 's|Exec=smplayer -add-to-playlist %U|Exec=smplayer -add-to-playlist %F|' \
    ${pkgdir}/usr/share/applications/smplayer_enqueue.desktop
}

