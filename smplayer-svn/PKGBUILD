# Maintainer: oke3 < Sekereg {at} gmx {dot} com
# Contributor: Flamelab <panosfilip@gmail.com>
# Fixes and patches: WAntilles <wantilles@adslgr.com>

pkgname=smplayer-svn
pkgver=3762
pkgrel=1
pkgdesc="A complete front-end for MPlayer"
arch=('i686' 'x86_64')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('qt' 'mplayer')
makedepends=('subversion')
conflicts=('smplayer')
provides=('smplayer')
install=$pkgname.install
source=("smplayer-display-matroska-chapter-names-2.patch")
sha1sums=('2c264379ab9a8a0c0fa649c771ae08f72a58d6a9')

_svnmod="smplayer"
_svntrunk=https://smplayer.svn.sourceforge.net/svnroot/smplayer/smplayer/trunk/

build() {
     cd "$srcdir"

     if [[ -d "$_svnmod/.svn" ]]; then
          (cd "$_svnmod" && svn up -r "$pkgver")
     else
          svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
     fi

     msg "SVN checkout done or server timeout"
     msg "Starting make..."

     rm -rf "$_svnmod-build"
     cp -r "$_svnmod" "$_svnmod-build"
     cd "$_svnmod-build"

    # remove broken translation
     sed -i "s|translations/smplayer_gl.ts|#translations/smplayer_gl.ts|" src/smplayer.pro

    # display matroska container chapter names
    # http://smplayer.sourceforge.net/forum/viewtopic.php?f=4&t=661
     patch -Np2 -i "$srcdir/smplayer-display-matroska-chapter-names-2.patch"

    make PREFIX=/usr
}

package() {
    cd "$srcdir/$_svnmod-build"

    make PREFIX="$pkgdir/usr" install

    install -dm755 "$pkgdir/usr/share/pixmaps"
    ln -s /usr/share/icons/hicolor/64x64/apps/smplayer.png \
        "$pkgdir/usr/share/pixmaps/"

    sed -i 's|Exec=smplayer %U|Exec=smplayer %F|' "$pkgdir/usr/share/applications/smplayer.desktop"
    sed -i 's|Exec=smplayer -add-to-playlist %U|Exec=smplayer -add-to-playlist %F|' \
   "$pkgdir/usr/share/applications/smplayer_enqueue.desktop"
}

