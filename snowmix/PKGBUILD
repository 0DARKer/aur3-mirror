# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=snowmix
pkgver=0.3.2
pkgrel=1
pkgdesc="Video mixer for mixing live and recorded video feeds"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/snowmix/"
license=('GPL3')
depends=('sdl' 'tcl' 'pango' 'gcc-libs')
source=(http://downloads.sourceforge.net/project/snowmix/Snowmix-$pkgver.tar.gz)
md5sums=('a7f7702db40e719e368739e350e78954')

build() {
  cd "$srcdir/Snowmix-$pkgver"
  sh strapboot
  sh bootstrap
  sed -i '33i \\t-lpng15\t\t\t\\' src/Makefile.am
  sed -i 's|tcl8.5/||g'           src/tcl_interface.h
  ./configure --prefix=/usr
  cd src
  make
}

package() {
  cd "$srcdir/Snowmix-$pkgver/src"
  make DESTDIR="$pkgdir" install
}
