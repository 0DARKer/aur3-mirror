# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=snowmix
pkgver=0.3.4
pkgrel=1
pkgdesc="Video mixer for mixing live and recorded video feeds"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/snowmix/"
license=('GPL3')
depends=('sdl' 'tcl' 'pango' 'gcc-libs')
source=(http://downloads.sourceforge.net/project/snowmix/Snowmix-$pkgver.tar.gz)
md5sums=('302c884e61f5806731541269edf1f955')

build() {
  cd "$srcdir/Snowmix-$pkgver"
  sed -i '34i \\t-lpng15\t\t\t\\' src/Makefile.am
  sed -i 's|-ltcl8.5/|-ltcl|g'    src/Makefile.am
  sed -i 's|tcl8.5/||g'           src/tcl_interface.h
  sed -i 's|tcl8.5/||g'           configure.ac
  sh strapboot
  sh bootstrap
  ./configure --prefix=/usr
  cd src
  sed -i 's|//#include <unistd.h>|#include <unistd.h>|' video_feed.cpp
  make
}

package() {
  cd "$srcdir/Snowmix-$pkgver/src"
  make DESTDIR="$pkgdir" install
}
