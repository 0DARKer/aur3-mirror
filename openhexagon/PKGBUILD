# Maintainer: Vin√≠cius dos Santos Oliveira <vini.ipsmaker@gmail.com>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=openhexagon
pkgver=1.84
pkgrel=1
pkgdesc="C++ open source clone of Super Hexagon"
arch=(i686 x86_64)
url="http://vittorioromeo.info/projects.html"
license=('MIT')
depends=(sfml-git lua51 sparsehash)
makedepends=(cmake)
options=(emptydirs)
source=(https://github.com/SuperV1234/SSVOpenHexagon/archive/${pkgver}.tar.gz
    https://github.com/SuperV1234/SSVEntitySystem/archive/5377ac0621c200111a15c4cdac1a2e1aaafa566c.zip
    https://github.com/SuperV1234/SSVLuaWrapper/archive/670a3df0c7c1cc6317a1fc5cb816d69f1781e6a4.zip
    https://github.com/SuperV1234/SSVMenuSystem/archive/126b075575147a2e7202c36b1537c00664ab4204.zip
    https://github.com/SuperV1234/SSVStart/archive/83f6680f9fd2a3241a8fdae17d50f0918db36036.zip
    https://github.com/flibitijibibo/OpenHexagon-Unix/archive/0b73ff3c3b8314ae1863335782e4ddcde0ca0dec.zip
    http://vittorioromeo.info/Downloads/OpenHexagon/Linux/OpenHexagonV${pkgver}.tar.gz
    Makefile.patch
    Makefile2.patch
    OpenHexagon)
md5sums=('d6585da40e81df953b1154ca96a4340c'
         'b3957eac66b65c86923d807739220a2b'
         'dd51da92be3b16c08f1e6d911bc85901'
         'af8c36f76eb1c406047eddb0e114b558'
         'a051be8883d6c584e19a2f682cb05729'
         '4f7a32cb14ab67fa7bb40e993be47253'
         '0749e06db4cb0aa2a4f0a0b12b87436b'
         '9feb1acdf4340ed54e943987796fffe8'
         '587f2913a683121c8a6c8be9d8b05293'
         '8140e782393994123798582e81e261c8')

build() {
  cd "$srcdir/SSVOpenHexagon-${pkgver}"

  rm -rf SSVEntitySystem SSVLuaWrapper SSVMenuSystem SSVStart Unix

  cp -r "$srcdir/SSVEntitySystem-5377ac0621c200111a15c4cdac1a2e1aaafa566c" \
    SSVEntitySystem
  cp -r "$srcdir/SSVLuaWrapper-670a3df0c7c1cc6317a1fc5cb816d69f1781e6a4" \
    SSVLuaWrapper
  cp -r "$srcdir/SSVMenuSystem-126b075575147a2e7202c36b1537c00664ab4204" \
    SSVMenuSystem
  cp -r "$srcdir/SSVStart-83f6680f9fd2a3241a8fdae17d50f0918db36036" SSVStart
  cp -r "$srcdir/OpenHexagon-Unix-0b73ff3c3b8314ae1863335782e4ddcde0ca0dec" Unix

  sed -i -e 's/#include "Utils.h"//g' SSVEntitySystem/Utils/Repository.h

  cd Unix
  patch -p0 < "$srcdir/Makefile.patch"
  patch -p0 < "$srcdir/Makefile2.patch"

  # Allows i686 compilation even with an x86_64 kernel
  [ "$CARCH" = "i686" ] && sed -i '/^FORCE32/s/n$/y/' Makefile

  make
}

package() {
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share"

  BINDIR=""

  if [ "$CARCH" = "x86_64" ]; then
    BINDIR="x86_64"
  else
    BINDIR="x86"
  fi

  cp -r "$srcdir/OpenHexagonV${pkgver}" "$pkgdir/usr/share/OpenHexagon"
  chmod -R a+r "$pkgdir/usr/share/OpenHexagon"
  cp "$srcdir/SSVOpenHexagon-${pkgver}/Unix/${BINDIR}/openhexagon.${BINDIR}" \
      "$pkgdir/usr/share/OpenHexagon/OpenHexagon"
  cp "$srcdir/OpenHexagon" "$pkgdir/usr/bin"

  cd "$pkgdir/usr/share/OpenHexagon"
  rm -r x86 x86_64
}

# vim:set ts=2 sw=2 et:
