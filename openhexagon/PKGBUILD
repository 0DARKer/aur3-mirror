# Maintainer: Vin√≠cius dos Santos Oliveira <vini.ipsmaker@gmail.com>
pkgname=openhexagon
pkgver=1.82
pkgrel=1
pkgdesc="C++ open source clone of Super Hexagon"
arch=(i686 x86_64)
url="https://www.facebook.com/OpenHexagon"
license=('MIT')
depends=(sfml-git lua51 sparsehash)
makedepends=(cmake)
options=(emptydirs)
source=(https://github.com/SuperV1234/SSVOpenHexagon/archive/${pkgver}.tar.gz
    https://github.com/SuperV1234/SSVEntitySystem/archive/5377ac0621c200111a15c4cdac1a2e1aaafa566c.zip
    https://github.com/SuperV1234/SSVLuaWrapper/archive/670a3df0c7c1cc6317a1fc5cb816d69f1781e6a4.zip
    https://github.com/SuperV1234/SSVMenuSystem/archive/126b075575147a2e7202c36b1537c00664ab4204.zip
    https://github.com/SuperV1234/SSVStart/archive/83f6680f9fd2a3241a8fdae17d50f0918db36036.zip
    https://github.com/flibitijibibo/OpenHexagon-Unix/archive/0b73ff3c3b8314ae1863335782e4ddcde0ca0dec.zip
    http://vittorioromeo.info/Downloads/OpenHexagon/Linux/OpenHexagonV${pkgver}.tar.gz
    Makefile.patch
    OpenHexagon)
md5sums=('46dca43e69d02a3b78f8dacdd5ba0d15'
         '4a123814bf33c396894e2489359aa0e2'
         '39c4f40295d30a2b930c765ace33406c'
         'ab31362648bc761eb15e1572b1705b9b'
         'c52ac606e7647d59303d76ea1468d6e7'
         'f25ce8d3f672cdcc28c01d6ac79a5f03'
         '87a258195da2721c8364aebf1c2c01d3'
         '9feb1acdf4340ed54e943987796fffe8'
         '8140e782393994123798582e81e261c8')

build() {
  cd "$srcdir/SSVOpenHexagon-${pkgver}"

  rm -rf SSVEntitySystem SSVLuaWrapper SSVMenuSystem SSVStart Unix

  cp -r "$srcdir/SSVEntitySystem-5377ac0621c200111a15c4cdac1a2e1aaafa566c" \
    SSVEntitySystem
  cp -r "$srcdir/SSVLuaWrapper-670a3df0c7c1cc6317a1fc5cb816d69f1781e6a4" \
    SSVLuaWrapper
  cp -r "$srcdir/SSVMenuSystem-126b075575147a2e7202c36b1537c00664ab4204" \
    SSVMenuSystem
  cp -r "$srcdir/SSVStart-83f6680f9fd2a3241a8fdae17d50f0918db36036" SSVStart
  cp -r "$srcdir/OpenHexagon-Unix-0b73ff3c3b8314ae1863335782e4ddcde0ca0dec" Unix

  sed -i -e 's/#include "Utils.h"//g' SSVEntitySystem/Utils/Repository.h

  cd Unix
  patch -p0 < "$srcdir/Makefile.patch"
  make
}

package() {
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share"

  BINDIR=""

  if [ "$(uname -m)" = "x86_64" ]; then
    BINDIR="x86_64"
  else
    BINDIR="x86"
  fi

  cp -r "$srcdir/OpenHexagonV${pkgver}" "$pkgdir/usr/share/OpenHexagon"
  chmod -R a+r "$pkgdir/usr/share/OpenHexagon"
  cp "$srcdir/SSVOpenHexagon-${pkgver}/Unix/${BINDIR}/openhexagon.${BINDIR}" \
      "$pkgdir/usr/share/OpenHexagon/OpenHexagon"
  cp "$srcdir/OpenHexagon" "$pkgdir/usr/bin"

  cd "$pkgdir/usr/share/OpenHexagon"
  rm -r x86 x86_64
}

# vim:set ts=2 sw=2 et:
