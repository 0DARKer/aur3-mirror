# Maintainer: Vin√≠cius dos Santos Oliveira <vini.ipsmaker@gmail.com>
pkgname=openhexagon
pkgver=1.2
pkgrel=2
pkgdesc="C++ open source clone of Super Hexagon"
arch=(i686 x86_64)
url="https://www.facebook.com/OpenHexagon"
license=('MIT')
depends=(sfml-git jsoncpp lua)
makedepends=(cmake)
options=(emptydirs)
source=(SSVOpenHexagon-174ec89a3d07cc624e433f9aee0003b645dc1eac.zip::https://nodeload.github.com/SuperV1234/SSVOpenHexagon/zip/174ec89a3d07cc624e433f9aee0003b645dc1eac \
    SSVEntitySystem-d259b9d803b598dc1224a7a1ee5b746978a22309.zip::https://github.com/SuperV1234/SSVEntitySystem/archive/d259b9d803b598dc1224a7a1ee5b746978a22309.zip \
    SSVStart-bb19ae18ca7d84cea2507b7851edba43370ae748.zip::https://github.com/SuperV1234/SSVStart/archive/bb19ae18ca7d84cea2507b7851edba43370ae748.zip \
    http://www.flibitijibibo.com/files/OpenHexagonv1.2.tar.gz \
    OpenHexagon)
md5sums=(dddf2ed025489e99e8e74e26f6f7f3c2 cb56aa3c8842379131019ccaf999376b \
    25580daa9507c82c64318534e4aed909 9c3e5d1489a275fb064372084f984efc \
    8140e782393994123798582e81e261c8)

build() {
  cd "$srcdir/SSVOpenHexagon-174ec89a3d07cc624e433f9aee0003b645dc1eac"

  rm -r SSVEntitySystem
  cp -r "$srcdir/SSVEntitySystem-d259b9d803b598dc1224a7a1ee5b746978a22309" \
    SSVEntitySystem

  rm -r SSVStart
  cp -r "$srcdir/SSVStart-bb19ae18ca7d84cea2507b7851edba43370ae748" SSVStart

  rm cmake/FindSFML.cmake
  sed -i -e 's/SFML 2/SFML/g' CMakeLists.txt

  sed -i -e "s/asFloat/asDouble/g" Data/EventData.cpp
  sed -i -e "s/asFloat/asDouble/g" Data/LevelData.cpp
  sed -i -e "s/asFloat/asDouble/g" Data/ProfileData.cpp
  sed -i -e "s/asFloat/asDouble/g" Data/StyleData.cpp
  sed -i -e "s/asFloat/asDouble/g" Global/Config.cpp
  sed -i -e "s/asFloat/asDouble/g" HexagonGame.cpp
  sed -i -e "s/asFloat/asDouble/g" Utils/Utils.cpp

  sed -r -i -e "s/mArray\[([[:alnum:]]+)\]/mArray[unsigned(\1)]/g" Utils/Utils.cpp

  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
      -DJSONCPP_INCLUDE_DIR=/usr/include/jsoncpp ..
  make
}

package() {
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share"

  cp -r "$srcdir/OpenHexagonv1.2" "$pkgdir/usr/share/OpenHexagon"
  cp "$srcdir/SSVOpenHexagon-174ec89a3d07cc624e433f9aee0003b645dc1eac/build/OpenHexagon" \
      "$pkgdir/usr/share/OpenHexagon"
  cp "$srcdir/OpenHexagon" "$pkgdir/usr/bin"

  cd "$pkgdir/usr/share/OpenHexagon"
  rm Music/vorbisgain.exe
  rm -r x86 x86_64
}

# vim:set ts=2 sw=2 et:
