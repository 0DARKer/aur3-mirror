# Maintainer: Vin√≠cius dos Santos Oliveira <vini.ipsmaker@gmail.com>
pkgname=openhexagon
pkgver=1.7
pkgrel=3
pkgdesc="C++ open source clone of Super Hexagon"
arch=(i686 x86_64)
url="https://www.facebook.com/OpenHexagon"
license=('MIT')
depends=(sfml-git lua51 sparsehash)
makedepends=(cmake)
options=(emptydirs)
source=(https://github.com/SuperV1234/SSVOpenHexagon/archive/v1.7.tar.gz
    https://github.com/SuperV1234/SSVEntitySystem/archive/a7ad046b109b882c18784ea3084608d675e30ead.zip
    https://github.com/SuperV1234/SSVLuaWrapper/archive/dd3b63cfd7f76534b54d65e7127b7b75f6fcef57.zip
    https://github.com/SuperV1234/SSVStart/archive/9e6925e9a59cdfed83b5699f6f462cd06f987445.zip
    https://github.com/flibitijibibo/OpenHexagon-Unix/archive/53ef509b35d845d4e6f7c47d35012b2e529e3170.zip
    http://vittorioromeo.info/Downloads/OpenHexagon/Linux/OpenHexagonV1.7Linux.tar.gz
    Makefile.patch
    OpenHexagon)
md5sums=('4650ba2992c0d201d540afc077fd643a'
    'c86ac3c0840a9de1ecfb3999f1c150dd'
    '630ba63d1d6c8768593ed5d28d7b4bf6'
    '6ef5682bf1179d51ec5ed125c0768968'
    '05befe0c4dfecd6325a7f3f0d26efd62'
    '26718f74ce26cecea90b7e68e1cdbbda'
    '7bb7a872c7d867c96fc89a270ae2da16'
    '8140e782393994123798582e81e261c8')

build() {
  cd "$srcdir/SSVOpenHexagon-1.7"

  rm -rf SSVEntitySystem SSVLuaWrapper SSVStart Unix

  cp -r "$srcdir/SSVEntitySystem-a7ad046b109b882c18784ea3084608d675e30ead" \
    SSVEntitySystem
  cp -r "$srcdir/SSVLuaWrapper-dd3b63cfd7f76534b54d65e7127b7b75f6fcef57" \
    SSVLuaWrapper
  cp -r "$srcdir/SSVStart-9e6925e9a59cdfed83b5699f6f462cd06f987445" SSVStart
  cp -r "$srcdir/OpenHexagon-Unix-53ef509b35d845d4e6f7c47d35012b2e529e3170" Unix

  cd Unix
  patch -p0 < "$srcdir/Makefile.patch"
  make
}

package() {
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share"

  BINDIR=""

  if [ "$(uname -m)" = "x86_64" ]; then
    BINDIR="x86_64"
  else
    BINDIR="x86"
  fi

  cp -r "$srcdir/OpenHexagonV1.7" "$pkgdir/usr/share/OpenHexagon"
  chmod -R a+r "$pkgdir/usr/share/OpenHexagon"
  cp "$srcdir/SSVOpenHexagon-1.7/Unix/${BINDIR}/openhexagon.${BINDIR}" \
      "$pkgdir/usr/share/OpenHexagon/OpenHexagon"
  cp "$srcdir/OpenHexagon" "$pkgdir/usr/bin"

  cd "$pkgdir/usr/share/OpenHexagon"
  rm -r x86 x86_64
}

# vim:set ts=2 sw=2 et:
