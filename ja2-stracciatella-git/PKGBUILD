# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=ja2-stracciatella-git
pkgver=7396.9953764
pkgrel=1
pkgdesc="Jagged Alliance 2 Stracciatella project with a goal of improving JA2"
arch=('i686' 'x86_64')
url="https://bitbucket.org/gennady/ja2-stracciatella"
license=('custom')
depends=('sdl' 'gcc-libs')
makedepends=('git')
source=('git+https://bitbucket.org/gennady/ja2-stracciatella.git' \
        'change_externalized_path.diff')
md5sums=('SKIP'
         '612b88ac861ee095b555bc9175b82559')

pkgver() {
  cd "$srcdir/ja2-stracciatella"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
  cd "$srcdir/ja2-stracciatella"

  cp -a _build/distr-files-linux/* .
  patch -Np1 < "$srcdir/change_externalized_path.diff" || true
}

build() {
  cd "$srcdir/ja2-stracciatella"

  make
}

package() {
  cd "$srcdir/ja2-stracciatella"

  make PREFIX="$pkgdir/usr" MANPREFIX="$pkgdir/usr/share" install

  install -d -m755 "$pkgdir/usr/share/ja2"
  cp -a externalized "$pkgdir/usr/share/ja2"

  install -d -m755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 README.md "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "SFI Source Code license agreement.txt" "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
