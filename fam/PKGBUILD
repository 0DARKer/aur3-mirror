# Maintainer: kfgz <kfgz at interia pl>
# Contributor: Allan McRae <allan at archlinux dot org>
# Contributor: Tom Newsom <Jeepster at gmx dot co dot uk>

pkgname=fam
pkgver=2.7.0
pkgrel=17
pkgdesc="File Alteration Monitor"
arch=('i686' 'x86_64')
license=('LGPL' 'GPL')
depends=('rpcbind' 'gcc-libs' 'bash')
url="http://oss.sgi.com/projects/fam/"
backup=('etc/fam/fam.conf')
options=('!makeflags' 'libtool')
source=(ftp://oss.sgi.com/projects/fam/download/stable/${pkgname}-${pkgver}.tar.gz
	fam-2.7.0-dnotify.patch
	fam-2.7.0-largefiles.patch
	fam-2.7.0-limits.patch
	fam-2.7.0-buildfixes.patch
	fam
	fam.service)
md5sums=('1bf3ae6c0c58d3201afc97c6a4834e39'
         '073d1763318344635ea316293390205c'
         '47b41e0b0498793af004696a096d7da1'
         '2638b8ffacb9f03b6e438e08ea7b290a'
         'fc0cabc0ac4f819680401eb3090c29c9'
         'ec50139679bc31508a843cafa47cd298'
         '6fd784899a6f1bcadbaf9c649822ff1e')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/fam-2.7.0-dnotify.patch
  patch -p1 -i "${srcdir}"/fam-2.7.0-largefiles.patch
  patch -p0 -i "${srcdir}"/fam-2.7.0-limits.patch
  patch -p1 -i "${srcdir}"/fam-2.7.0-buildfixes.patch

  chmod 755 configure

  autoheader
  aclocal
  automake -a -c
  autoconf
  libtoolize --copy --force

  ./configure --prefix=/usr --sysconfdir=/etc/fam
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m755 "${srcdir}"/fam "${pkgdir}"/etc/rc.d/fam
  install -D -m644 "${srcdir}"/fam.service "${pkgdir}"/usr/lib/systemd/system/fam.service
}
