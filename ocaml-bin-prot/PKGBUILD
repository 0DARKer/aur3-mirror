# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>
# Contributor: Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>
pkgname=ocaml-bin-prot
pkgver=2.0.7
pkgrel=1
pkgdesc="binary protocol generator for OCaml"
arch=('i686' 'x86_64')
url="https://forge.ocamlcore.org/projects/bin-prot"
license=('LGPL')
depends=('ocaml' 'ocaml-type-conv>=3.0.4')
makedepends=('ocaml-findlib')
source=(http://forge.ocamlcore.org/frs/download.php/724/bin-prot-$pkgver.tar.gz)
md5sums=('26288c780ffee9a7199613d75762a1fc')
options=(!strip !makeflags)

build () {
  cd "$srcdir/bin-prot-$pkgver"
  make CONFIGUREFLAGS="--prefix /usr --destdir $pkgdir --disable-tests" build
}

package () {
  cd "$srcdir/bin-prot-$pkgver"
  export OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
