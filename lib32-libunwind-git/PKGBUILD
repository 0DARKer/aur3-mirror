# Maintainer: Moritz Lipp <mlq@pwmt.org>

_pkgbasename=libunwind
pkgname=lib32-$_pkgbasename-git
pkgver=20120911
pkgrel=1
pkgdesc="Portable and efficient C programming interface (API) to determine the call-chain of a program"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/libunwind/"
license=('GPL')

depends=('glibc')
provides=(lib32-libunwind)
conflicts=(lib32-libunwind)

_gitroot='git://git.sv.gnu.org/libunwind.git'
_gitname='libunwind'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  autoreconf -i
  CFLAGS="-m32 -U_FORTIFY_SOURCE" LDFLAGS="-m32" ./configure --prefix=/usr \
    --libdir=/usr/lib32 --target=i686
  make
}

check() {
  cd "$srcdir/$_gitname-build"
  make check || return 0
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

  rm "$pkgdir/usr/include/$_pkgbasename-common.h"
  rm "$pkgdir/usr/include/$_pkgbasename-coredump.h"
  rm "$pkgdir/usr/include/$_pkgbasename-dynamic.h"
  rm "$pkgdir/usr/include/$_pkgbasename-ptrace.h"
}

# vim:set ts=2 sw=2 et:
