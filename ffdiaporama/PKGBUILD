# Maintainer: boenki <boenki at gmx dot de>

pkgname=ffdiaporama
pkgver=1.2.1
pkgrel=3
pkgdesc="Creation of videos sequences from images, titles, music"
arch=('i686' 'x86_64')
url="http://ffdiaporama.tuxfamily.org"
license=('GPL2')
conflicts=('ffdiaporama-devel')
depends=('qt' 'ffmpeg' 'exiv2' 'taglib')
source=(http://download.tuxfamily.org/${pkgname}/Archives/${pkgname}_${pkgver}.tar.gz
        gcc47.patch)
md5sums=('e9159e186f7daa19f910e6a53a5ed6d0'
         'dcf6b723c0fc423d3d40db90d611fdf4')

build() {
  cd ${srcdir}/ffDiaporama

  patch src/sharedfiles/cBaseApplicationConfig.h $srcdir/gcc47.patch

  qmake PREFIX=/usr ffDiaporama.pro
  make
}

package() {
  cd ${srcdir}/ffDiaporama
  make install INSTALL_ROOT=${pkgdir}
}