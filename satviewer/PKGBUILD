# Maintainer: moostik <mooostik_at_gmail.com>

pkgname=satviewer
pkgver=105
pkgrel=1
pkgdesc="View, animate, and solve up to three functions in 3D-space in an interactive manner"
arch=('i686' 'x86_64')
url="http://satviewer.net/"
license=('GPL3')
depends=('qt' 'mesa')
source=("https://${pkgname}.googlecode.com/files/src${pkgver}.tgz"
	"${pkgname}.desktop"
	"${pkgname}.sh")

build() {
  cd "${srcdir}/src${pkgver}"
  qmake SatViewer.pro
  make
}

package() {
  cd "${srcdir}/src${pkgver}/bin"
  install -D -m755 SatViewer "${pkgdir}/opt/${pkgname}/SatViewer"
  cp -r profile plugins "${pkgdir}/opt/${pkgname}/"
  install -d -m755 "${pkgdir}/usr/bin"
  install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/satviewer"

  # Install shortcut and icon
  install -D -m644 "profile/icons/sat.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

}

md5sums=('cfc7ad122eb58a5e2562f356c83a0314'
         '70fd11e47233f5116cb09ed4ed00a02c'
         'be0dc35a489563c1d16b0df004efb43c')
