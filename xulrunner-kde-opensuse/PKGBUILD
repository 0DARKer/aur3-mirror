# Contributor: Weng Xuetian <wengxt@gmail.com>

pkgname=xulrunner-kde-opensuse
pkgver=2.0
_ffoxver=4.0
pkgrel=2
pkgdesc="Mozilla Runtime Environment with OpenSUSE patch, integrate better with KDE"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'gcc-libs' 'libidl2' 'mozilla-common' 'nss' 'libxt' 'libxrender' 'hunspell' 'startup-notification' 'mime-types' 'dbus-glib' 'alsa-lib' 'libevent' 'sqlite3>=3.7.4' 'libnotify' 'libproxy' 'kmozillahelper>=0.6')
makedepends=('zip' 'pkg-config' 'diffutils' 'python2' 'wireless_tools' 'yasm' 'mesa')
provides=("xulrunner=${pkgver}")
conflicts=('xulrunner')
url="http://wiki.mozilla.org/XUL:Xul_Runner"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${_ffoxver}/source/firefox-${_ffoxver}.source.tar.bz2
        mozconfig
        mozilla-pkgconfig.patch
        xulrunner-version.patch
        xulrunner-omnijar.patch
        port_gnomevfs_to_gio.patch
        mozilla-nongnome-proxies.patch
        mozilla-kde.patch
)
options=('!emptydirs')

md5sums=('3468a2c463b4fc2788ba621e4b511c30'
         'dbdac796052ecad6d1526a1f2e552b37'
         '639ea80e823543dd415b90c0ee804186'
         'a0236f6c3e55f60b7888d8cf137ff209'
         'dea8c2a57a588bf5f6198ce7b4155655'
         '42f83468b296452fb754a81a4317ca64'
         'ea37f26534d1bab452da7945695e2b32'
         '12b98813187ce9912de42b49713087ab')

build() {
  cd "${srcdir}/mozilla-2.0"
  cp "${srcdir}/mozconfig" .mozconfig

  rm -f toolkit/xre/nsKDEUtils.cpp \
        toolkit/xre/nsKDEUtils.h \
        uriloader/exthandler/unix/nsCommonRegistry.cpp \
        uriloader/exthandler/unix/nsCommonRegistry.h \
        uriloader/exthandler/unix/nsKDERegistry.cpp \
        uriloader/exthandler/unix/nsKDERegistry.h \
        toolkit/content/widgets/dialog-kde.xml \
        toolkit/content/widgets/preferences-kde.xml \
        extensions/gio/Makefile.in \
        extensions/gio/makefiles.sh \
        extensions/gio/nsGIOProtocolHandler.cpp

  #fix libdir/sdkdir - fedora
  patch -Np1 -i "${srcdir}/mozilla-pkgconfig.patch"

  #Force installation to the same path for every version
  patch -Np1 -i "${srcdir}/xulrunner-version.patch"

  #https://bugzilla.mozilla.org/show_bug.cgi?id=620931
  patch -Np1 -i "${srcdir}/xulrunner-omnijar.patch"

  #https://bugzilla.mozilla.org/show_bug.cgi?id=494163
  patch -Np1 -i "${srcdir}/port_gnomevfs_to_gio.patch"

  msg "patch mozilla-nongnome-proxies"
  patch -Np0 -i "${srcdir}/mozilla-nongnome-proxies.patch" || return 1

  msg "patch mozilla-kde"
  patch -Np1 -i "${srcdir}/mozilla-kde.patch" || return 1

  unset CFLAGS
  unset CXXFLAGS

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd "${srcdir}/mozilla-2.0"
  make -j1 -f client.mk DESTDIR="${pkgdir}" install

  #Remove included dictionaries, add symlink to system myspell path.
  #Note: this will cause file conflicts when users have installed dictionaries in the old location
  rm -rf "${pkgdir}/usr/lib/xulrunner-2.0/dictionaries"
  ln -sf /usr/share/myspell/dicts "${pkgdir}/usr/lib/xulrunner-2.0/dictionaries"

  # add xulrunner library path to ld.so.conf
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo "/usr/lib/xulrunner-2.0" > ${pkgdir}/etc/ld.so.conf.d/xulrunner.conf
}