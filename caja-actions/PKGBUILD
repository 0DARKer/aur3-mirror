# Mantainer: 3ED <krzysztof1987@gmail.com>

pkgname=caja-actions
pkgver=1.6.2
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="Configures programs to be launched when files are selected in Caja (mate-file-manager)"
url="https://github.com/NiceandGently/caja-actions"
license=('GPL')
makedepends=('intltool' 'mate-doc-utils')
depends=('caja' 'libgtop')
install=${pkgname}.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/NiceandGently/caja-actions/archive/v${pkgver}.tar.gz)
sha256sums=('5dffe57a8cea79eaa937f207912347a8464e4f9203795c47afc92cd417cce811')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  NOCONFIGURE=1 ./autogen.sh

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-gtk=2 \
    --disable-scrollkeeper 

  make
}
package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Remove redundant documentation
  rm -r "$pkgdir/usr/share/doc/"

  find "$pkgdir" -type f -name "*.la" -delete
}
