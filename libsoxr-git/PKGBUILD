# Maintainer: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=libsoxr-git
pkgver=20130110
pkgrel=1
pkgdesc="SoX Resampler library"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gcc-libs')
makedepends=('cmake' 'git')
url="http://sourceforge.net/projects/soxr/"
_gitroot="git://git.code.sf.net/p/soxr/code"
_gitname="soxr"

build() {
  cd "$srcdir"
  msg "Connecting to remote Git repository..."
  if [ -d "$_gitname" ] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone --depth=1 "$_gitroot" "$_gitname"
  fi
  msg "Git checkout done or server timeout"
  rm -rf "$srcdir/$_gitname-build"
  cp -a "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  rm -f CMakeCache.txt
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DBUILD_TESTS=ON .
  make
}

check() {
  cd "$srcdir/$_gitname-build"
  make test
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
  rm -rf "$srcdir/$_gitname-build"
}
