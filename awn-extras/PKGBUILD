# Contributor: Kvasthval
pkgname=awn-extras
_realname=awn-extras-applets
pkgver=0.3.2.1
pkgrel=2
pkgdesc="A collection of applets for Avant Window Navigator."
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL')
depends=('avant-window-navigator' 'gnome-menus' 'libgtop' 'libnotify' 'libsexy' 'libwebkit' 'vte' 'libtracker')
makedepends=('gstreamer0.10-python' 'intltool' 'librsvg' 'libxt' 'python-feedparser' 'python-notify' 'vala>=0.5.4' 'python-pyalsaaudio')
optdepends=('python-xlib: needed for PyNot applet' 'tsclient2: needed for tsclient applet')
provides=('avant-window-navigator-extras' 'awn-extras-applets')
conflicts=('awn-extras-applets')
install=awn-extras.install
source=(http://launchpad.net/$pkgname/0.2/$pkgver/+download/$_realname-$pkgver.tar.gz awn-extras.install)
md5sums=('54f975f4a9a0798b5cce1b3239ed7b96' '3adbd18a594b898cd163d35b88c62a57')
options=('!libtool')

build()
{
	cd $srcdir/$_realname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc --disable-pymod-checks --with-webkit --disable-schemas-install || return 1
	make || return 1
	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install || return 1
	
	mkdir -pv $pkgdir/usr/share/gconf/schemas || return 1
	gconf-merge-schema $pkgdir/usr/share/gconf/schemas/$pkgname.schemas $pkgdir/etc/gconf/schemas/*.schemas || return 1
	rm -rfv $pkgdir/etc || return 1
}
