# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=mir
pkgver=0.1.4
pkgrel=1
pkgdesc="Ubuntu's new display server"
arch=('i686' 'x86_64')
url="https://launchpad.net/mir"
# Server is GPL, client is LGPL
license=('GPL' 'LGPL')
groups=('unitynext')
depends=('boost' 'libdrm' `# Maybe gcc4.6` 'glm' 'mesa-libgl' 'mesa')
makedepends=('bzr' 'cmake' 'doxygen' 'gflags' 'google-glog' 'graphviz' 'libxkbcommon' 'libxslt' 'protobuf')

# Provide Ubuntu's library names
provides=("libmirprotobuf=${pkgver}"     # Protocol shared library
          "libmirserver=${pkgver}"       # Server shared library
          "libmirclient=${pkgver}"       # Client shared library
          "libmirclient-demos=${_real_ver}" # Example clients
          "mir=${_real_ver}")               # Stable version of Mir
conflicts=('libmirprotobuf'
           'libmirserver'
           'libmirclient'
           'libmirclient-demos'
           'mir-bzr')

source=("https://launchpad.net/mir/trusty/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('37a360998c0c4c967d2bf18b224cead627c1e28ac760ab993ed53b453554805ee52348eb3ff64e693e40db6ea04c923a5fced741ca73fc58bcd2c0be65224e70')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  COMMON_PARAMS=('-DCMAKE_C_COMPILER=gcc'
                 '-DCMAKE_CXX_COMPILER=g++'
                 '-DCMAKE_INSTALL_PREFIX=/usr'
                 '-DMIR_DISABLE_EPOLL_REACTOR=YES'
                 `# Failing to build '-DBUILD_DOXYGEN=YES'`)

  cmake . ${COMMON_PARAMS[@]}

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  GTEST_OUTPUT=xml:./ ctest -j1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # Install examples
  install -dm755 "${pkgdir}"/usr/share/doc/mir/examples/
  install -m644 examples/demo_client.c               \
                examples/demo_client_accelerated.cpp \
                examples/demo_client_unaccelerated.c \
                examples/README                      \
                examples/graphics.h                  \
                examples/mir_image.h                 \
                examples/graphics_utils.cpp          \
                "${pkgdir}"/usr/share/doc/mir/examples/
}

# vim:set ts=2 sw=2 et:
