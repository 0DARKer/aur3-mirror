# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=clearlooks-phenix-gtk-theme-git
pkgver=3.0.16.r15.g6af1ac3
pkgrel=1
pkgdesc="GTK3 theme visually close to Clearlooks"
arch=('any')
url='ttps://gitorious.org/projets-divers/andrewsh-clearlooks-phenix'
license=('GPL')
depends=('gtk-engines')
provides=('clearlooks-phenix-gtk-theme')
conflicts=('clearlooks-phenix-gtk-theme')
makedepends=('git')
#source=('git://gitorious.org/projets-divers/clearlooks-phenix.git')
source=('git://gitorious.org/projets-divers/andrewsh-clearlooks-phenix.git')

pkgver() {
	cd andrewsh-clearlooks-phenix/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd andrewsh-clearlooks-phenix/

	rm -rf .git/
}

package() {
	cd andrewsh-clearlooks-phenix/

	for FILE in $(find doc/ -type f); do
		install -D -m0644 ${FILE} ${pkgdir}/usr/share/doc/clearlooks-phenix-gtk-theme/${FILE}
	done
	rm -rf doc/

	for FILE in $(find -type f); do
		install -D -m0644 ${FILE} ${pkgdir}/usr/share/themes/Clearlooks-Phenix/${FILE}
	done
}

sha256sums=('SKIP')
