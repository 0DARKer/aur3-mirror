# Maintainer: m1kc <m1kc@yandex.ru>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=wxmaxima-git
pkgver=1704.2cd5b16
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('i686' 'x86_64')
url="https://github.com/andrejv/wxmaxima"
license=('GPL2')
depends=('maxima' 'libxml2' 'wxgtk' 'shared-mime-info' 'desktop-file-utils')
source=(wxmaxima::git+git://github.com/andrejv/wxmaxima.git#branch=master)
install=wxmaxima.install
sha1sums=('SKIP')
conflicts=('wxmaxima')
provides=('wxmaxima')

pkgver() {
  cd $srcdir/wxmaxima
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${srcdir}/wxmaxima"
  if [ ! -e README ]; then msg2 "Fixing README"; cp README.md README; fi
}

build() {
  cd "${srcdir}/wxmaxima"
  ./bootstrap
  ./configure --prefix=/usr
  make

  cd locales
  make allmo
  cd ..
}

check() {
  cd "${srcdir}/wxmaxima"
  make check
}

package() {
  cd "${srcdir}/wxmaxima"

  make DESTDIR="${pkgdir}" install 

  # Fix category in .desktop file
  sed -i -e 's/Utility;X-Red-Hat-Base;X-Red-Hat-Base-Only;/Science;Math;/' wxmaxima.desktop

  # Install desktop file and icon
  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m644 wxmaxima.desktop "${pkgdir}/usr/share/applications/" 
  install -m644 data/wxmaxima.png "${pkgdir}/usr/share/pixmaps/" 
}
