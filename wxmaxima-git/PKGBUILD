# Maintainer: m1kc <m1kc@yandex.ru>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=wxmaxima-git
pkgver=2380.18b5ef9
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('i686' 'x86_64')
url="https://github.com/andrejv/wxmaxima"
license=('GPL2')
depends=('maxima' 'libxml2' 'wxgtk' 'shared-mime-info' 'desktop-file-utils')
source=('wxmaxima::git+git://github.com/andrejv/wxmaxima.git#branch=master'
        'wxwidgets::git+git://github.com/wxWidgets/wxWidgets.git#branch=master'
        'wxmaxima.desktop')
install=wxmaxima.install
sha1sums=('SKIP'
          'SKIP'
          '2c17467d119d4761aee548775fee4c6533b5bff8')
conflicts=('wxmaxima')
provides=('wxmaxima')

pkgver() {
  cd $srcdir/wxmaxima
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "${srcdir}/wxmaxima"
  if [ ! -e README ]; then msg2 "Fixing README"; cp README.md README; fi

  cd "${srcdir}/wxmaxima"
  if [ ! -e acinclude.m4 ]; then
    msg2 "Including wxWidgets' wxwin.m4 as acinclude.m4"
    cp -v ../wxwidgets/wxwin.m4 ./acinclude.m4
  fi
}

build() {
  cd "${srcdir}/wxmaxima"
  ./bootstrap
  ./configure --prefix=/usr
  make

  cd locales
  make allmo
  cd ..
}

check() {
  cd "${srcdir}/wxmaxima"
  make check
}

package() {
  cd "${srcdir}/wxmaxima"

  make DESTDIR="${pkgdir}" install

  # Install desktop file and icon
  if [ -e "${pkgdir}/usr/share/applications/wxMaxima.desktop" ]; then
    msg2 "Removing built-in .desktop file"
    rm -v "${pkgdir}/usr/share/applications/wxMaxima.desktop"
  fi
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m644 wxmaxima.desktop "${pkgdir}/usr/share/applications/" 
  install -m644 wxmaxima/data/wxmaxima.png "${pkgdir}/usr/share/pixmaps/"
}
