# Maintainer: Gadget3000 <gadget3000 at msn dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=openbor-svn
pkgver=3372
pkgrel=1
pkgdesc="A freeware open source video game and moddable game engine"
arch=('i686' 'x86_64')
url="http://lavalit.com:8080/"
license=('BSD' 'custom')
depends=('bash' 'sdl_gfx' 'libvorbis' 'libpng')
makedepends=('yasm' 'sdl_mixer')
source=(openbor.png openbor.desktop openbor.sh)
md5sums=('0715288e945be4f161bd39028b99ccad'
         '5963f4ce5dd7923790c940666b685046'
         'abcc8962dd907573480b16a67af7e34c')

_svntrunk=https://LavaLit.com/svn/openbor/engine
_svnmod=OpenBOR


build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf $srcdir/$_svnmod-build
  cp -r $srcdir/$_svnmod $srcdir/$_svnmod-build
  cd $srcdir/$_svnmod-build

  #
  # BUILD
  #

  sed -i -e "s/-Werror/-fno-strict-aliasing/g" \
         -e "s|local/lib|lib|" \
         -e "s/MMX             = 1//g" Makefile

  source environ.sh 4
  if [ "$CARCH" = "x86_64" ]; then
	make BUILD_LINUX=1 TARGET_ARCH=64 || return 1
  else
	make BUILD_LINUX=1 || return 1
  fi

  install -D -m755 ${srcdir}/openbor.sh ${pkgdir}/usr/bin/openbor
  install -D -m755 OpenBOR ${pkgdir}/usr/share/openbor/openbor
  install -d -m777 ${pkgdir}/usr/share/openbor/{Logs,Paks,Saves,Screenshots}
  install -D -m644 README ${pkgdir}/usr/share/doc/openbor/README
  install -D -m644 ${srcdir}/openbor.desktop ${pkgdir}/usr/share/applications/openbor.desktop
  install -D -m644 ${srcdir}/openbor.png ${pkgdir}/usr/share/pixmaps/openbor.png
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
