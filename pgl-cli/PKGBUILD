# Maintainer: Gilrain <pierre.buard+aur gmail com>
pkgname=pgl-cli
pkgver=2.2.2
pkgrel=4
pkgdesc='PeerGuardian is a privacy oriented firewall application (Daemon & CLI).'
url='http://sourceforge.net/projects/peerguardian/'
arch=('i686' 'x86_64')
depends=('cron' 'logrotate' 'wget' 'libnetfilter_queue' 'iptables' 'dbus')
optdepends=('unzip: for zipped blocklists'
	    'p7zip: for 7z blocklists'
	    'net-tools: to whitelist local IP addresses'
	    'networkmanager: to whitelist newly started network interfaces'
	    'smtp-forwarder: to send reports')
license=('GPL')
replace=('pgl-nogui')
conflicts=('blockcontrol' 'moblock' 'pgl')
backup=('etc/pgl/allow.p2p'
	'etc/pgl/blocklists.list'
	'etc/pgl/pglcmd.conf'
	'etc/logrotate.d/pglcmd'
	'etc/logrotate.d/pgld')
source=(http://downloads.sourceforge.net/project/peerguardian/PeerGuardian%20Linux/${pkgver}/pgl-${pkgver}.tar.gz
	service)
md5sums=('c623c92802281f69e51d140c8b1bb171'
	 'bcd4e7ad78493e3ba66d1b3827dce1eb')
install=install
changelog=changelog.cli

build() {
    cd ${srcdir}/pgl-${pkgver}/
    ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
		--with-piddir=/run --without-initddir --without-qt4
    make
}

package() {
    cd ${srcdir}/pgl-${pkgver}/
    make DESTDIR=${pkgdir} install

    install -Dm644 ${srcdir}/service ${pkgdir}/usr/lib/systemd/system/pgl.service

    # integrates logs in systemd journal
    sed -i '/^LOG_SYSLOG/s/0/1/g' ${pkgdir}/usr/lib/pgl/pglcmd.defaults
    sed -i '/^LOG_LOGFILE/s/1/0/g' ${pkgdir}/usr/lib/pgl/pglcmd.defaults
}
