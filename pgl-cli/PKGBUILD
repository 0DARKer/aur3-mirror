# Maintainer: Gilrain <pierre.buard+aur gmail com>
pkgname=pgl-cli
pkgver=2.2.0
pkgrel=1
pkgdesc='PeerGuardian is a privacy oriented firewall application (Daemon & CLI).'
url='http://sourceforge.net/projects/peerguardian/'
arch=('i686' 'x86_64')
depends=('cron' 'logrotate' 'wget' 'libnetfilter_queue' 'iptables')
optdepends=('unzip: for zipped blocklists'
	    'p7zip: for 7z blocklists'
	    'net-tools: to whitelist local IP addresses'
	    'networkmanager: to whitelist newly started network interfaces'
	    'smtp-forwarder: to send reports')
license=('GPL')
replace=('pgl-nogui')
conflicts=('blockcontrol' 'moblock' 'pgl')
backup=('etc/pgl/allow.p2p'
	'etc/pgl/blocklists.list'
	'etc/pgl/pglcmd.conf'
	'etc/logrotate.d/pglcmd'
	'etc/logrotate.d/pgld')
source=(http://downloads.sourceforge.net/project/peerguardian/PeerGuardian%20Linux/${pkgver}/pgl-${pkgver}.tar.gz
	pgl.service)
md5sums=('ccbc30d2b66f2ad9dc1e05d9dd7af2f4'
	 'fd537cb0ce0fa6a9e048bb79bb8a5b58')
install=pgl.install

build() {
    cd ${srcdir}/pgl-${pkgver}/

    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
		--with-initddir=/etc/rc.d --with-piddir=/run --without-qt4 \
		--disable-networkmanager --disable-dbus
    make
}

package() {
    cd ${srcdir}/pgl-${pkgver}/
    make DESTDIR=${pkgdir} install

    install -Dm644 ${srcdir}/pgl.service ${pkgdir}/usr/lib/systemd/system/pgl.service
}