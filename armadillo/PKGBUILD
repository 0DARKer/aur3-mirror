# Maintainer: Lucas Hermann Negri <lucashnegri@gmail.com>
# Contributor: shacristo

pkgname=armadillo
pkgver=4.650.2
pkgrel=1
pkgdesc="C++ linear algebra library"
arch=('i686' 'x86_64')
url="http://arma.sourceforge.net/"
license=('MPL 2.0')
depends=('lapack' 'blas' 'hdf5')
makedepends=('cmake' 'boost')
source=("http://downloads.sourceforge.net/sourceforge/arma/$pkgname-$pkgver.tar.gz")
md5sums=('c8e5c94e5d3753fa5cdb2080598ccdfb')

prepare() {
    cd "${srcdir}/armadillo-$pkgver"
    if [ "$CARCH" == "x86_64" ]; then
        sed -i "s|// #define ARMA_64BIT_WORD|#define ARMA_64BIT_WORD|g" include/armadillo_bits/config.hpp.cmake
    fi
}

build() {
  ARMA64BITWORD=""
  if [ "$CARCH" == "x86_64" ]; then
      ARMA64BITWORD="ARMA_64BIT_WORD"
      ARMA64LIBDIR="-DINSTALL_LIB_DIR:PATH=/usr/lib"
  fi
  cd "${srcdir}/$pkgname-$pkgver"
  cmake $ARMA64BITWORD $ARMA64LIBDIR -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

