# Maintainer: Lucas Hermann Negri <lucashnegri@gmail.com>
# Contributor: shacristo

pkgname=armadillo
pkgver=4.400.1
pkgrel=1
pkgdesc="C++ linear algebra library"
arch=('i686' 'x86_64')
url="http://arma.sourceforge.net/"
license=('MPL 2.0')
depends=('lapack' 'blas' 'hdf5')
makedepends=('cmake' 'boost')
source=("http://downloads.sourceforge.net/sourceforge/arma/$pkgname-$pkgver.tar.gz")
md5sums=('c8dbe38f6995c68888e8445cca682782')

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DINSTALL_LIB_DIR=lib .
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

