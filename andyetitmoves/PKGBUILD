# Maintainer: Claudio Kozicky <claudiokozicky@gmail.com>
# Contributor: jose <jose1711 [at] gmail (dot) com>

pkgname=andyetitmoves
pkgver=1.2.2
pkgrel=2
pkgdesc="physics-based platform game (full version)"
arch=('i686' 'x86_64')
url="http://www.andyetitmoves.net/"
license=("custom:shareware")
source=("andyetitmoves.desktop")
depends=("libxft" "sdl_image" "libvorbis" "libpng12" "libtheora" "glu")
md5sums=('4f4792260832fe1b52ca8150a03ca176')

[ "$CARCH" = "i686"   ] && _gamepkg="andyetitmoves-${pkgver}-1_i386.tar.gz"
[ "$CARCH" = "x86_64" ] && _gamepkg="andyetitmoves-${pkgver}-1_x86_64.tar.gz"

[ "$CARCH" = "i686"   ] && _gamesum='c530630520f062db1d402d99fb25a725'
[ "$CARCH" = "x86_64" ] && _gamesum='86544c86a117a653221c38ebfa58c68d'

build() {
    cd "${srcdir}"

    msg "You need a full copy of this game in order to install it"
    msg "Searching for ${_gamepkg} in dir: \"${startdir}\""
    pkgpath="${startdir}"
    if [[ ! -f "${startdir}/${_gamepkg}" ]]; then
        error "Game package not found, please type absolute path to \"${_gamepkg}\" (/home/joe/downloads) or enter HIB key (e. g. abcdefgh1234):"
        read pkgpath
        if echo ${pkgpath} | egrep -q "^\s*/"; then
            if [[ ! -f "${pkgpath}/${_gamepkg}" ]]; then
                error "Unable to find game package." && return 1
            fi
        else
            key=${pkgpath}
            echo "your key is: ${key}"
            pkgpath="${startdir}"
            downurl=$(wget -qO - http://www.humblebundle.com/?key=${key} | sed -n '/'${_gamepkg}'/s/.* href='\''\(.*\)'\'' data-web=.*/\1/p')
            echo ${downurl}
            if [ -z "${downurl}" ]; then
                error "provided key seems to be invalid or something has changed on hib website"
                return 1
            fi
            wget -O "${pkgpath}/${_gamepkg}" "${downurl}" || rm "${pkgpath}/${_gamepkg}"
        fi
    fi
    if [ "$(/usr/bin/md5sum ${pkgpath}/${_gamepkg} | awk '{print $1}')" != ${_gamesum} ]; then
        error  "md5sum does not match"
        return 1
    fi
    msg "Found game package, installing."
    tar xvf "${pkgpath}/${_gamepkg}"
}

package() {
    cd "${srcdir}/AndYetItMoves"
    mkdir -p "${pkgdir}"/usr/{share/games/AndYetItMoves,bin}
    /bin/tar cf - * | ( cd "${pkgdir}/usr/share/games/AndYetItMoves/"; tar xfp - )
    ln -s /usr/share/games/AndYetItMoves/AndYetItMoves "${pkgdir}/usr/bin/AndYetItMoves"
    install -D -m644 icons/48x48.png "${pkgdir}/usr/share/pixmaps/andyetitmoves.png"
    install -D -m644 "${srcdir}/andyetitmoves.desktop" "${pkgdir}/usr/share/applications/andyetitmoves.desktop"
}
