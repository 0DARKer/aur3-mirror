# Maintainer: Claudio Kozicky <claudiokozicky@gmail.com>
# Contributor: jose <jose1711 [at] gmail (dot) com>

pkgname=andyetitmoves
pkgver=1.2.2
pkgrel=3
pkgdesc="A physics-based platform game."
arch=(i686 x86_64)
url="http://www.andyetitmoves.net/"
license=(custom)
depends=(gtk-update-icon-cache hicolor-icon-theme libpng12 libtheora libvorbis libxft sdl_image)
install=$pkgname.install
source=($pkgname.desktop)
md5sums=(af422568e73ea08c1b54c4ea5bd233dd)
[ $CARCH = i686   ]\
    && source+=(hib://andyetitmoves-$pkgver-1_i386.tar.gz)\
    && md5sums+=(c530630520f062db1d402d99fb25a725)
[ $CARCH = x86_64 ]\
    && source+=(hib://andyetitmoves-$pkgver-1_x86_64.tar.gz)\
    && md5sums+=(86544c86a117a653221c38ebfa58c68d)
DLAGENTS+=('hib::/usr/bin/echo Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf.; exit 1')
PKGEXT=.pkg.tar

package() {
    cd "$srcdir"

    # data
    find -name icons -prune -o -type f -exec install -Dm644 '{}' "$pkgdir"/opt/'{}' \;
    chmod +x "$pkgdir"/opt/AndYetItMoves/{AndYetItMoves{,_debug},lib/AndYetItMoves}

    # launcher
    install -d "$pkgdir"/usr/bin
    ln -s /opt/AndYetItMoves/AndYetItMoves "$pkgdir"/usr/bin/AndYetItMoves

    # desktop integration
    for i in 16x16 22x22 32x32 48x48 64x64 128x128
    do
        install -Dm644 AndYetItMoves/icons/$i.png "$pkgdir"/usr/share/icons/hicolor/$i/apps/$pkgname.png
    done
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}

# vim: set sts=4 sw=4 et:
