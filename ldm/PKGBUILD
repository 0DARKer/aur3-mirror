# Maintainer: Robin Hahling
pkgname=ldm
pkgver=0.4
pkgrel=1
pkgdesc="A lightweight device mounter"
arch=('i686' 'x86_64')
url="https://github.com/LemonBoy/ldm"
license=('MIT')
depends=('udev')
provides=('ldm')
conflicts=('ldm-git')
source=(http://github.com/LemonBoy/${pkgname}/archive/v${pkgver}.tar.gz)
install=ldm.install
md5sums=('671c161c5f9e707ede9b5d2ddf7d3b6f')

build() {
  cd ${pkgname}-${pkgver}
  # needed before it is fixed in the Makefile
  export LDFLAGS=""
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="/usr" DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
