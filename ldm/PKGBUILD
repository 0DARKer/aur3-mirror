## ldm PKGBUILD
# Maintainer: The Lemon Man

pkgname=ldm
pkgver=0.5
pkgrel=2
pkgdesc="A lightweight device mounter"
arch=('i686' 'x86_64')
url="https://github.com/LemonBoy/ldm"
license=('MIT')
depends=('udev')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz"::"http://github.com/LemonBoy/$pkgname/archive/v$pkgver.tar.gz")
md5sums=('e8e34ce55e530255be85b2ddd712bcae')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i 's|/usr/local/bin/ldm|/usr/bin/ldm|' ldm.service
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

## ldm.install
post_install() {
  echo 'ldm expects a config file at /etc/ldm.conf containing your user and group id (uid and gid respectively).'
  echo 'Just add those two lines into it:'
  echo 'USER_GID=<output of `id -g` ran from your user>'
  echo 'USER_UID=<output of `id -u` ran from your user>'
}

