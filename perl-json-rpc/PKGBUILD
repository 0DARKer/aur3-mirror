pkgname=perl-json-rpc
pkgver=1.03
pkgrel=2
pkgdesc="Perl implementation of JSON-RPC 1.1 protocol"
arch=('i686' 'x86_64')
conflicts=('perl-json-rpc-legacy')
url="http://search.cpan.org/~dmaki/JSON-RPC-1.03/"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-libwww' 'perl-plack' 'perl-router-simple' 'perl-json')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/D/DM/DMAKI/JSON-RPC-${pkgver}.tar.gz")
md5sums=('a978df8ace00cdd286317087bdcd92b0')

prepare() {
  rm -fr build
  cp -R "JSON-RPC-${pkgver}" build
}

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd build
    /usr/bin/perl Makefile.PL
    make
  )
}


check() {
  cd build
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd build
  make install
  find "${pkgdir}" -name '.packlist' -o -name 'perllocal.pod' -delete
}

