pkgname=perl-json-rpc
pkgver=1.04
pkgrel=1
pkgdesc="Perl implementation of JSON-RPC 1.1 protocol"
arch=('i686' 'x86_64')
conflicts=('perl-json-rpc-legacy')
url="http://search.cpan.org/dist/JSON-RPC/"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-libwww' 'perl-plack' 'perl-router-simple' 'perl-json')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/D/DM/DMAKI/JSON-RPC-${pkgver}.tar.gz")
sha1sums=('ea6857c410dd3820421c2ef5d9e582f8ee34cf86')

prepare() {
  rm -fr build
  cp -R "JSON-RPC-${pkgver}" build
}

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd build
    perl Build.PL
    perl Build
  )
}


check() {
  cd build
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    perl Build test
  )
}

package() {
  cd build
  perl Build install
  find "${pkgdir}" -name '.packlist' -o -name 'perllocal.pod' -delete
}

