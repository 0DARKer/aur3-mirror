# Maintainer : Nicolas Pouillard <nicolas(dot)pouillard(at)gmail(dot)com>
# Contributor: Sylvester Johansson <scj(at)archlinux(dot)us>
# Contributor: Sergey Plaksin <serp256(at)gmail(dot)com>
pkgname=ocaml-batteries
pkgver=1.4.1
pkgrel=1
pkgdesc="Batteries Included for OCaml"
arch=('i686' 'x86_64')
url="https://forge.ocamlcore.org/projects/batteries/"
license=('LGPL')
depends=('ocaml' 'ocaml-findlib' 'ocaml-camomile' 'ocaml-ounit')
install=ocaml-batteries.install
source=(https://forge.ocamlcore.org/frs/download.php/684/batteries-${pkgver}.tar.gz)
md5sums=('733d602a3a25ba9c4366eec9356fe5fb')
options=(!strip !makeflags)

build(){
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  OCAMLBUILD="ocamlbuild -no-links" make all doc || return 1
}

package(){
  cd "$srcdir/${pkgname/ocaml-/}-$pkgver"

  export DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  export OCAMLFIND_DESTDIR="$DESTDIR"
  mkdir -p $OCAMLFIND_DESTDIR

  OCAMLBUILD="ocamlbuild -no-links" make install || return 1
  DOCROOT="$pkgdir/usr/share/doc/$pkgname/" make install-doc || return 1
  install -Dm 644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE || return 1
  install -Dm 644 ocamlinit $pkgdir/usr/share/doc/$pkgname/ocamlinit || return 1
}

# vim:set ts=2 sw=2 et:
