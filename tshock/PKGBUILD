# Maintainer: Vincent Post <vincent.post@w1r3.net>

pkgname=tshock
pkgver=4.2.3.0720
_pkgver=4.2301
# Because of tag 4.2200 is 4.2.2.1228 and tag 4.2202 is 4.2.2.0224 epoch must be 1
epoch=1
pkgrel=1
pkgdesc="A Terraria Server administration modification that runs on the open source TerrariaAPI."
arch=(any)
url="http://tshock.co/xf/"
license=("GPL")
depends=('tmux'
         'mono')
source=("https://github.com/NyxStudios/TShock/releases/download/v${_pkgver}/TShock.${pkgver}.zip"
        'tshock.sh'
        'tshock.install'
        'tshock.service')

sha512sums=("9c8f8cf44b6b28bff08c17af2f243ed6c8b8e898f1272ba9806055b13cc200addceebab9153bc996a2af1cf554586c70bff16e826281725952e46b0b24f2e082"
            'a8dbf2342871a0cffadce17697ec5d998389b51bd0d6cb8e4361123edc1cf7399d38f4b7d361fe1942dd16177d157dbda64510047bd57eac509366b6249a0365'
            '5d22c7dd54adf426aa041df6bffb7e1f25797a9c5ff0d620076b48dbdbfdd30a02320a84544b85ccc7ff008fa5f08c6ad81b155a46bbb7b8505d805f5c621dea'
            '2d4582ddd908ebf58f6935631977794f4ab654d09f31215f2bc595c966ed18bf8db7cabc1f7ee01cbb6199711ab73a624b9de62d7a2d9256599054536ccf2482')

install=tshock.install
backup=('srv/tshock/tshock.sh')

package() {
  install -d "${pkgdir}/srv/tshock/ServerPlugins/"

  install -Dm744 "${srcdir}/tshock.sh" "${pkgdir}/srv/tshock/"
  install -Dm644 "${srcdir}/tshock.service" "${pkgdir}/usr/lib/systemd/system/tshock.service"

  install -Dm644 "${srcdir}/ServerPlugins"/* "${pkgdir}/srv/tshock/ServerPlugins/"
  install -Dm644 "${srcdir}/sqlite3.dll" "${pkgdir}/srv/tshock/"
  install -Dm644 "${srcdir}/TerrariaServer.exe" "${pkgdir}/srv/tshock/"
}
