pkgname=vuze
pkgver=4.7.2.0
pkgrel=2
pkgdesc="One of the most powerful bitTorrent client with GUI in the world, written in Java."
arch=('i686' 'x86_64')
url="http://azureus.sf.net/"
license=('GPL')
depends=('java-runtime' 'swt' 'desktop-file-utils')
optdepends=('libgnomeui: for vuze GUI')
install=vuze.install
source=("http://cf1.vuze.com/torrent/files/Azureus$pkgver.jar"
	"http://downloads.sourceforge.net/azureus/Vuze_${pkgver:0:1}${pkgver:2:1}${pkgver:4:1}${pkgver:6:1}_linux.tar.bz2")
noextract=("Azureus$pkgver.jar")
sha256sums=('314f46a541fd1cec994bdb57c5cba4bea23de992b93081c6c53e636ee9cac48b'
            '9a706b6a340378d453d2e2340d009a0572ee889f0915bd4b3aa8148369f9dddc')

package() {
  cd "$srcdir/$pkgname"
  #install systemwide plugins
  install -Dm644 plugins/azplugins/azplugins_2.1.6.jar "$pkgdir/usr/share/vuze/plugins/azplugins/azplugins_2.1.6.jar"
  install -Dm644 plugins/azrating/azrating_1.3.1.jar "$pkgdir/usr/share/vuze/plugins/azrating/azrating_1.3.1.jar"
  install -Dm644 plugins/azupdater/Updater.jar "$pkgdir/usr/share/vuze/plugins/azupdater/Updater.jar"
  install -Dm644 plugins/azupdater/azupdaterpatcher_1.8.17.jar "$pkgdir/usr/share/vuze/plugins/azupdater/azupdaterpatcher_1.8.17.jar"
  install -Dm644 plugins/azupdater/azureus.sig "$pkgdir/usr/share/vuze/plugins/azupdater/azureus.sig"
  install -Dm644 plugins/azupdater/plugin.properties "$pkgdir/usr/share/vuze/plugins/azupdater/plugin.properties"
  install -Dm644 plugins/azupnpav/azupnpav_0.4.1.jar "$pkgdir/usr/share/vuze/plugins/azupnpav/azupnpav_0.4.1.jar"
  install -Dm644 plugins/azupnpav/azureus.sig "$pkgdir/usr/share/vuze/plugins/azupnpav/azureus.sig"
  install -Dm644 plugins/azupnpav/plugin.properties "$pkgdir/usr/share/vuze/plugins/azupnpav/plugin.properties"
  #install desktop entries
  install -Dm644 vuze.desktop  "${pkgdir}/usr/share/applications/vuze.desktop"
  install -Dm644 vuze.png "${pkgdir}/usr/share/pixmaps/vuze.png"
  install -Dm644 vuze.torrent.png "${pkgdir}/usr/share/pixmaps/vuze.torrent.png"
  install -Dm644 vuze.schemas "${pkgdir}/usr/share/gconf/schemas/vuze.schemas"
  #link SWT
  ln -s /usr/share/java/swt.jar "$pkgdir/usr/share/vuze/swt.jar"
  #install main program
  install -Dm755 vuze "${pkgdir}/usr/bin/vuze"
  sed -i 's|#PROGRAM_DIR="/home/username/apps/azureus"|PROGRAM_DIR="/usr/share/vuze"|' ${pkgdir}/usr/bin/vuze
  install -Dm644 "$srcdir/Azureus$pkgver.jar" "${pkgdir}/usr/share/vuze/Azureus2.jar"
}

# vim:set ts=2 sw=2 et:
