# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: NeoRaider <neoraider@universe-factory.net>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=libxcb-git
pkgver=1.10.r14.g4ffa6f8
pkgrel=1
pkgdesc="X11 client-side library - git version"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto-git' 'libxdmcp' 'libxau')
makedepends=('git' 'libxslt' 'python')
conflicts=('libxcb')
provides=("libxcb=$pkgver")
license=('custom')
source=("git+http://anongit.freedesktop.org/git/xcb/libxcb.git"
        'libxcb-1.1-no-pthread-stubs.patch')
sha256sums=('SKIP'
            'eccd47717ac928cb0e502ea607daa5dcdf82d841da123f280094b185a7204f2d')

pkgver() {
  cd $srcdir/${pkgname%-*}
  git describe --tags | sed 's/-/.r/;s/-/./g'
}

prepare() {
  cd $srcdir/${pkgname%-*}

  patch -Np1 < ../libxcb-1.1-no-pthread-stubs.patch
}

build() {
  cd $srcdir/${pkgname%-*}

  ./autogen.sh \
    --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static

  make
}

package() {
  cd $srcdir/${pkgname%-*}

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
# vim:set ts=2 sw=2 bs=2:
