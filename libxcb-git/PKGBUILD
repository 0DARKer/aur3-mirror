# Contributor: NeoRaider <neoraider@universe-factory.net>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=libxcb-git
pkgver=1.9.7.g9ae84ad
pkgrel=1
pkgdesc="X11 client-side library (git version)"
arch=(i686 x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto-git' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python' 'autoconf')
conflicts=('libx11<1.1.99.2' 'libxcb')
provides=('libxcb=1.7')
options=('!libtool')
license=('custom')
source=('git://git.freedesktop.org/git/xcb/libxcb' 'libxcb-1.1-no-pthread-stubs.patch')
sha1sums=('SKIP'
          'ea5ff407389b38de43652e022e270e6a8ab03158')
_gitname="libxcb"

pkgver() {
 cd $srcdir/$_gitname
 git describe --always | sed 's|-|.|g' 
}

prepare() {
  cd "${srcdir}"/$_gitname
  patch -Np1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"
}

build() {
  cd "${srcdir}"/$_gitname
  ./autogen.sh --prefix=/usr --enable-xinput
  make
}

package() {
  cd "${srcdir}/build"
  make install_sh=install DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
