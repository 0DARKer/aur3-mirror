# Maintainer: crazyelf5
# Contributor: paterbrown <paterbrown at silberhelme dot de>
# Contributor: Daniel Milde <info@milde.cz>

pkgname=elementary-gtk-theme
_pkgname=egtk
pkgver=2.1
pkgrel=6
pkgdesc="The official elementary GTK theme designed to be smooth, attractive, fast, and usable."
arch=('any')
url="https://launchpad.net/egtk"
license=('GPL2')
depends=('gtk-engine-murrine')
optdepends=('elementary-icons: the elementary icon theme [AUR]'
	'marlin-bzr: A Sleek and fast GTK-3 file manager [AUR]'
	'nautilus-elementary-bzr: the fork of Nautilus for simplicity [AUR]')
install=$pkgname.install
source=(https://launchpad.net/egtk/2.0/${pkgver}/+download/${_pkgname}.zip)
noextract=(${_pkgname}.zip)
md5sums=('f685d659c72271944ca2e76d638ca2ed')

package () {
  bsdtar -xf ${_pkgname}.zip --exclude gtk-3.0

  cd egtk
  find . -type f -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/elementary/{}" \;
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  
  cd "$pkgdir/usr/share/themes/elementary"
  rm -rf ./{.bzr,debian,COPYING,index.theme.~1~}
  ln -fs gtk-2.0 gtk-3.0
}	
