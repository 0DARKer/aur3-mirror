# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Wes Brewer <brewerw@gmail.com>

pkgname=cdcat
pkgver=1.3
pkgrel=1
pkgdesc="CD/DVD/Media catalog software (QT based)"
arch=('i686' 'x86_64')
url="http://cdcat.sourceforge.net/"
license=('GPL')
depends=('qt')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop")
md5sums=('f2bfe2eb89e19e1bc870dd786630dca3'
         '9572c09ee3a4e695487bc070fce6a19f')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  # Path fix
  sed -i "s|/local||g" $pkgname.pro

  qmake $pkgname.pro && make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"

  make INSTALL_ROOT="$pkgdir" install

  # Desktop and pixmaps
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
# vim:set ts=2 sw=2 et:
