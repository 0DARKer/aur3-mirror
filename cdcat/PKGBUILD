# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Wes Brewer <brewerw@gmail.com>

pkgname=cdcat
pkgver=1.5
pkgrel=1
pkgdesc="CD/DVD/Media catalog software (QT based)"
arch=('i686' 'x86_64')
url="http://cdcat.sourceforge.net/"
license=('GPL')
depends=('qt' 'bzip2')
makedepends=('libtar' 'lib7zip')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop")
md5sums=('0ccd6b61d0c28bbfa5f3898e9bd17960'
         '9572c09ee3a4e695487bc070fce6a19f')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  # Path fix
  sed -i "s|/local||g" $pkgname.pro

  qmake $pkgname.pro && make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"

  make INSTALL_ROOT="$pkgdir" install

  # Desktop file
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # translations
  cd lang
  for _f in *.ts; do
    _tdir="$pkgdir/usr/share/locale/${_f:6:2}/LC_MESSAGES"
    install -d "$_tdir"
    lrelease -silent -qm "$_tdir/${_f/ts/qm}" $_f
  done
}
# vim:set ts=2 sw=2 et:
