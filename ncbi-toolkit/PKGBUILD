# Maintainer: Mick Elliot <micke at sfu dot ca>
# Contributor: Mick Elliot <micke at sfu dot ca>

pkgname=ncbi-toolkit
pkgver=2008.12.31
origver=2008.12.31
pkgrel=3
pkgdesc="Applications, scripts and C++ libraries for the analysis of biological data"
arch=('i686' 'x86_64')
url="http://www.ncbi.nlm.nih.gov/books/bv.fcgi?rid=toolkit.TOC&depth=2"
license=('custom')
depends=('python' 'pcre' 'sqlite3' 'lzo2' 'libtiff' 'giflib' 'libxpm' 'libmysqlclient' 'libpng' 'gnutls')
makedepends=('gcc' 'make' 'autoconf')
source=(ftp://ftp.ncbi.nih.gov/toolbox/ncbi_tools++/2008/Dec_31_2008/ncbi_cxx--Dec_31_2008.tar.gz include_string_h.patch license)
md5sums=('6ddac6c0b37fac2376626e918651f111' '4d179c874fa9cf7d5637ae6e3a69a1eb' '1e2d6a958f3a8adc8478215295146d7c')

build() 
{
  # patch the source so that libraries and binaries can be built using the latest gcc
  patch -p1 -d $srcdir/ncbi_cxx--Dec_31_2008 < include_string_h.patch || return 1
  
  # configure and build release version
  cd $startdir/src/ncbi_cxx--Dec_31_2008
  if [ "${CARCH}" = "i686" ]; then
    ./configure --with-static --with-dll --without-debug --with-runpath=/opt/ncbi-toolkit/release/lib --with-optimization --with-mt --without-64 --with-build-root=release || return 1
  else
    ./configure --with-static --with-dll --without-debug --with-runpath=/opt/ncbi-toolkit/release/lib --with-optimization --with-mt --with-64 --with-build-root=release || return 1
  fi
  make || return 1

  # configure and build debug version
  cd $startdir/src/ncbi_cxx--Dec_31_2008
  if [ "${CARCH}" = "i686" ]; then
    ./configure --with-static --with-dll --with-debug --with-runpath=/opt/ncbi-toolkit/debug/lib --with-mt --without-64 --with-build-root=debug || return 1
  else
    ./configure --with-static --with-dll --with-debug --with-runpath=/opt/ncbi-toolkit/debug/lib --with-mt --with-64 --with-build-root=debug || return 1
  fi
  make || return 1
   
  # copy the entire tree into the opt directory
  mkdir -p $startdir/pkg/opt
  cp -r $startdir/src/ncbi_cxx--Dec_31_2008 $startdir/pkg/opt/$pkgname
  
  # install binaries
  cd $startdir/src/ncbi_cxx--Dec_31_2008/release
  mkdir -p $startdir/pkg/usr/bin
  install -D -m755 bin/* $startdir/pkg/usr/bin/  

  cd $startdir/src/
  install -D -m644 license $startdir/pkg/usr/share/licenses/$pkgname/license

  echo "The ncbi source tree is located at /opt/ncbi-toolkit."
  echo "Build roots are located at /opt/ncbi-toolkit/release/build and /opt/ncbi-toolkit/debug/build."
  echo "Static and dynamic libraries are located at /opt/ncbi-toolkit/release/lib and /opt/ncbi-toolkit/debug/lib."
  echo "Executables are located at /opt/ncbi-toolkit/release/bin and /opt/ncbi-toolkit/debug/bin."
  echo "Release executables are also copied into /usr/bin."
  echo "Scripts are located at /opt/ncbi-toolkit/scripts."
  echo "Include files are located at /opt/ncbi-toolkit/include."
  echo "The ncbi-toolkit source is located at /opt/ncbi-toolkit/src."
}

