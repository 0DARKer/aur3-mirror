# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

pkgname=kernel26-reiser4-module
_kernelname=${pkgname#kernel26}
_basekernel=2.6.31
_kernver="2.6.31-ARCH"
pkgver=${_basekernel}.5
pkgrel=1
_patchname="patch-${pkgver}-${pkgrel}-ARCH"
pkgdesc="Reiser4 support module for Linux Kernel."
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.kernel.org"
depends=('coreutils' 'kernel26>=2.6.31.5' 'module-init-tools')
install=kernel26.install
source=(ftp://ftp.kernel.org/pub/linux/kernel/v2.6/linux-$_basekernel.tar.bz2
        ftp://ftp.archlinux.org/other/kernel26/${_patchname}.bz2
        # reiser4 patch
        ftp://www.kernel.org/pub/linux/kernel/people/edward/reiser4/reiser4-for-2.6/reiser4-for-$_basekernel.patch.gz
	# the main kernel config files
        config config.x86_64)
md5sums=('84c077a37684e4cbfa67b18154390d8a'
         'ad74a0005058dfe5459bbe3319317297'
         '62ab025dda9d5c291b6e7ce763e557c1'
         'ad1c31d17b3c05733cf4ebcbc13205aa'
         '6ba35cc0e792c3a4e324100e5976a218')

build() {
  KARCH=x86

  cd ${srcdir}/linux-$_basekernel
  # Add -ARCH patches
  # See http://projects.archlinux.org/linux-2.6-ARCH.git/
  patch -Np1 -i ${srcdir}/${_patchname} || return 1

  # applying reiser4 patch
  msg "Applying reiser4 patch"
  patch -Np1 -i ${srcdir}/reiser4-for-2.6.31.patch || return 1

  if [ "$CARCH" = "x86_64" ]; then
    cat ../config.x86_64 >./.config
  else
    cat ../config >./.config
  fi
  if [ "${_kernelname}" != "" ]; then
    sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"-ARCH\"|g" ./.config
  fi

  make prepare || return 1

  # build tools
  make modules_prepare || return 1

  # build reiser4 support module
  make modules SUBDIRS=fs/reiser4 || return 1

  mkdir -p ${pkgdir}/lib/modules/${_kernver}/kernel/fs/reiser4
  cp ${srcdir}/linux-$_basekernel/fs/reiser4/reiser4.ko \
    ${pkgdir}/lib/modules/${_kernver}/kernel/fs/reiser4
}
