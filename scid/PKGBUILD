# Maintainer: Luis Sarmiento < Luis.Sarmiento-ala-nuclear.lu.se >

pkgname=scid
_pkgname=Scid
pkgver=4.4
pkgrel=1
pkgdesc="A Free Chess Database Application"
url="http://scid.sourceforge.net"
arch=('x86_64' 'i686')
license=('GPL')
depends=('tk' 'python')
optdepends=('snack: for sound support')
options=('!emptydirs')

source=("$pkgname-$pkgver.tar.bz2::http://prdownloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2?download")
md5sums=('be14e3f4317d6b8799ef32505499e79f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure BINDIR=${pkgdir}/usr/bin SHAREDIR=${pkgdir}/usr/share/${pkgname}
  make
}

package () {
  cd ${srcdir}/${pkgname}-${pkgver}
  make install
  msg "Creating Desktop file"
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/svg/scid.ico ${pkgdir}/usr/share/scid/scid.ico
  echo "
[Desktop Entry]
Version=1.0
Name=${_pkgname}
Comment=${pkgdesc}
Exec=${pkgname}
Icon=/usr/share/${pkgname}/${pkgname}.ico
Categories=Game;BoardGame;
Type=Application
Terminal=false
StartupNotify=false
" > $srcdir/$pkgname.desktop

  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop

  msg "Fix some permission issues"
  find $pkgdir/usr/share/scid/books -type f -exec chmod 644 {} \;

  msg "Copying sound files"
  install -d $pkgdir/usr/share/sounds
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/sounds/*.wav $pkgdir/usr/share/sounds
}
