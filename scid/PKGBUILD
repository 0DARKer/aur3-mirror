# Maintainer: Luis Sarmiento < Luis.Sarmiento-ala-nuclear.lu.se >

pkgname=scid
_pkgname=Scid
pkgver=4.5.2
_pkgver=4.5
pkgrel=2
pkgdesc="A Free Chess Database Application"
url="http://scid.sourceforge.net"
arch=('x86_64' 'i686')
license=('GPL')
# namcap says 'tk' is not needed. tlc is needed but tk has it as a dependency.
depends=('python' 'tk')
optdepends=('snack: for sound support'
            'tkimg: for using some alternate sets of pieces')

options=('!emptydirs')

source=("http://sourceforge.net/projects/${pkgname}/files/${_pkgname}/${_pkgname}%20${_pkgver}/${_pkgname}-${pkgver}.zip")
md5sums=('4cd8a9a846bacf60c401f45d89f3df14')

_folder="scid-code-dd495411672b5a6360d046f0aeec310fe691fe1d"

build() {
  cd ${srcdir}/${_folder}
  ./configure BINDIR=${pkgdir}/usr/bin SHAREDIR=${pkgdir}/usr/share/${pkgname}
  make
}

package () {
  cd ${srcdir}/${_folder}
  make install
  msg "Creating Desktop file"
  install -Dm644 ${srcdir}/${_folder}/svg/scid.ico ${pkgdir}/usr/share/scid/scid.ico
  echo "
[Desktop Entry]
Version=1.0
Name=${_pkgname}
Comment=${pkgdesc}
Exec=${pkgname}
Icon=/usr/share/${pkgname}/${pkgname}.ico
Categories=Game;BoardGame;
Type=Application
Terminal=false
StartupNotify=false
" > $srcdir/$pkgname.desktop

  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop

  msg "Fix world writtable bit in books folder"
  find $pkgdir/usr/share/scid/books -type f -exec chmod 644 {} \;

  msg "Copying sound files"
  install -d $pkgdir/usr/share/sounds
  install -Dm644 ${srcdir}/${_folder}/sounds/*.wav $pkgdir/usr/share/sounds
}
