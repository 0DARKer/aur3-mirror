# Maintainer: Det
# Based on jre: https://aur.archlinux.org/packages.php?ID=51908

pkgname=jre-devel
_major=8
_build=b54
_date=30_aug_2012
pkgver=$_major$_build
pkgrel=1
pkgdesc="Java $_major Runtime Environment Snapshot"
url=http://jdk$_major.java.net/
arch=('i686' 'x86_64')
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound support'
            'ttf-dejavu: fonts')
provides=("java-runtime=$_major" "java-runtime-headless=$_major")
conflicts=("${provides[@]}")
backup=('etc/profile.d/jre8.sh'
        'etc/profile.d/jre8.csh')
install=$pkgname.install
_arch=i386; [ "$CARCH" = 'x86_64' ] && _arch=amd64
source=("http://download.java.net/jdk$_major/archive/$_build/binaries/jre-$_major-ea-bin-$_build-linux-i586-$_date.tar.gz"
        'java-policy-settings8.desktop'
        'javaws-launcher8'
        'jre8.csh'
        'jre8.sh'
        'sun-java8.desktop'
        'sun-javaws8.desktop'
        'sun_java8.desktop')
md5sums=(`curl -Ls ${source/.t*}.md5 | cut -d " " -f4` # jre-$_major-ea-bin-$_build-linux-i586/x64-$_date.tar.gz"
         'edaf208448123d37eaff7cb4c0cb60f2'  # java-policy-settings8.desktop
         '6bdc10db69bd73e1b9a734b6a395da4f'  # javaws-launcher8
         'c4155eeee2b349880d30ec4ecc470792'  # jre8.csh
         '4882d0c1a8dac93717146b842b1e4a88'  # jre8.sh
         '62e0560d13d222d634063b8ac7a1968e'  # sun-java8.desktop
         '5243d3783992981e6a7f32afdc82bff9'  # sun-javaws8.desktop
         'c2f8178963bb4c23e5fcefde2a2b7a97') # sun_java8.desktop
[ "$CARCH" = 'x86_64' ] && source[0]="http://download.java.net/jdk$_major/archive/$_build/binaries/jre-$_major-ea-bin-$_build-linux-x64-$_date.tar.gz"

package() {
  msg2 "Creating required dirs"
  cd jre1.$_major.0
  mkdir -p "$pkgdir"/{opt/java8/jre,usr/{share/{,licenses/jre8},lib/mozilla/plugins},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Re-ordering folders a bit"
  mv lib/desktop/{applications,icons,mime} "$pkgdir"/usr/share/

  msg2 "Removing empty and redundant dirs"
  rm -r lib/{applet,desktop} man/ja plugin

  msg2 "Suffixing binaries, scripts, man pages, MIME packages, .desktops and PNGs with an '8'"
  for i in bin/*; do
    mv $i ${i}8
  done
  for i in man{,/ja_JP.UTF-8}/man1/*; do
    mv $i ${i/.1}8.1
  done
  for i in "$pkgdir"/usr/share/mime/packages/*; do
    mv $i ${i/.*}8.xml
  done
  for i in "$pkgdir"/usr/share/applications/*; do
    mv $i ${i/.*}8.desktop
  done
  for i in $(find "$pkgdir"/usr/share/icons/ -name "*.png"); do
    mv $i ${i/.*}8.png
  done
  ln -sf jcontrol8 bin/ControlPanel8

  msg2 "Moving stuff in place"
  mv COPYRIGHT LICENSE THIRDPARTYLICENSEREADME.txt "$pkgdir"/usr/share/licenses/jre8/
  mv man "$pkgdir"/usr/share/
  mv * "$pkgdir"/opt/java8/jre/

  msg2 "Symlinking the plugin"
  cd "$srcdir"
  ln -s /opt/java8/jre/lib/$_arch/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/jre8-libnpjp2.so

  msg2 "Installing scripts and .desktop files"
  install -m755 javaws-launcher8 "$pkgdir"/opt/java8/jre/bin/
  install -m644 {java-policy-settings8,sun{-java{8,ws8},_java8}}.desktop "$pkgdir"/usr/share/applications/
  install -m755 jre8.{c,}sh "$pkgdir"/etc/profile.d/
}