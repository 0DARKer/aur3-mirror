# Maintainer: Det
# Based on jre: https://aur.archlinux.org/packages.php?ID=51908

pkgname=jre-devel
_major=9
#_minor=none
_build=b25
_date=30_jul_2014
_pkgver=$_major
pkgver=$_major$_build
#_pkgver=${_major}u$_minor
#pkgver=${_major}u$_minor.$_build
pkgrel=1
pkgdesc="Java $_major Runtime Environment Snapshot"
arch=('i686' 'x86_64')
url=https://jdk$_major.java.net/
license=('custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libxrender' 'libxtst' 'shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound'
            'ttf-dejavu: fonts')
provides=("java-runtime=$_major" "java-runtime-headless=$_major" "java-web-start=$_major")
conflicts=("${provides[@]}" "${provides[@]/$_major/8}" "${provides[@]/$_major/7}")
backup=('opt/java/jre/lib/security/java.policy')
install=$pkgname.install
# Archs
_arch=x64
_arch2=amd64
if [ "$CARCH" = 'i686' ]; then
  _arch=i586
  _arch2=i386
fi
source=("http://download.java.net/jdk$_pkgver/archive/$_build/binaries/jre-$_pkgver-ea-bin-$_build-linux-$_arch-$_date.tar.gz"
        'java-policy-settings.desktop'
        'javaws-launcher'
        'jre.csh'
        'jre.sh'
        'OTN-Early-Adopter-License-Terms.txt')
md5sums=("$(curl -Ls ${source/.t*}.md5 | cut -d " " -f4)" # jre-$_pkgver-ea-bin-$_build-linux-$_arch-$_date.tar.gz"
         'f4e25ef1ccef8d36ff2433c3987a64fe'  # java-policy-settings.desktop
         '224bcf5e1f5d14bfa493ed243a5a5287'  # javaws-launcher
         '92dec202858f2bf7729c6805b1bd3ae4'  # jre.csh
         '85ba1d2b39d5e6efad89ef98d0f19909'  # jre.sh
         '55acc7d9282c3826afeac6157582384c') # OTN-Early-Adopter-License-Terms.txt

package() {
  msg2 "Creating required dirs"
  cd jre1.$_major.0
  #cd jre1.$_major.0_$_minor
  mkdir -p "$pkgdir"/{opt/java/jre,usr/{lib/mozilla/plugins,share/licenses/jre},etc/{.java/.systemPrefs,profile.d}}

  msg2 "Removing redundancies"
  rm -r plugin/ man/ja

  msg2 "Moving stuff in place"
  mv lib/desktop/* man "$pkgdir"/usr/share/
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/jre/
  mv * "$pkgdir"/opt/java/jre/

  msg2 "Symlinking plugin"
  ln -s /opt/java/jre/lib/$_arch2/libnpjp2.so "$pkgdir"/usr/lib/mozilla/plugins/

  msg2 "Installing additions"
  cd "$srcdir"
  install -m755 jre.{,c}sh "$pkgdir"/etc/profile.d/
  install -m755 javaws-launcher "$pkgdir"/opt/java/jre/bin/
  install -m644 java-policy-settings.desktop "$pkgdir"/usr/share/applications/
  install -m644 OTN-Early-Adopter-License-Terms.txt "$pkgdir"/usr/share/licenses/jre/

  msg2 "Tweaking javaws .desktop"
  sed -e 's/Exec=javaws/&-launcher %f/' \
      -e '/NoDisplay=true/d' \
      -i "$pkgdir"/usr/share/applications/sun-javaws.desktop

  # Copy/paste from system clipboard to unsigned Java applets has been disabled since 6u24:
  # - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java
  # - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html
  msg2 "Enabling copy+paste to unsigned applets"
  _line=$(awk '/permission/{a=NR}; END{print a}' "$pkgdir"/opt/java/jre/lib/security/java.policy)
  sed "$_line a\\\\n \
        // (AUR) Allow unsigned applets to read system clipboard, see:\n \
        // - https://blogs.oracle.com/kyle/entry/copy_and_paste_in_java\n \
        // - http://slightlyrandombrokenthoughts.blogspot.com/2011/03/oracle-java-applet-clipboard-injection.html\n \
        permission java.awt.AWTPermission \"accessClipboard\";" \
     -i "$pkgdir"/opt/java/jre/lib/security/java.policy
}