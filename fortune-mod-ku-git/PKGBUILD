# Contributor: Vojtech Kral <kral.vojtech (at) gmail.com>

pkgname=fortune-mod-ku-git
pkgver=r33.1225309
pkgrel=1
pkgdesc="Fortune cookies from the AbcLinuxu.cz & KERNEL ULTRAS community"
url="http://www.abclinuxu.cz/"
arch=('any')
license=('unknown')
depends=('fortune-mod')
optdepends=('kfortune: read cookies from a /dev file')
source=('kfortune.sh' 'ku-kfortune.rules')
sha256sums=('e48e51ea9e8eb2574d6ca68ecc80b4938461a614578c4c28b2170954bef768da'
            '16c2826f2ef494a256d1582ce0bee80897f61c32b01e46f489c0661c531363e1')

_gitroot="https://bitbucket.org/kralyk/ku-fortune-cookies.git"
_gitname="ku-fortune-cookies"
_builddir=$_gitname-build


pkgver()
{
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build()
{
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_builddir"
  git clone "$srcdir/$_gitname" "$srcdir/$_builddir"
  cd "$srcdir/$_builddir"

  mv abc-nourl abc
  mv abc-nourl.dat abc.dat
}

package()
{
  cd "$srcdir/$_builddir"
  install -d "$pkgdir/usr/share/fortune/"
  install -m 644 abc abc.dat "$pkgdir/usr/share/fortune/"
  cd "$srcdir"
  install -D -m 644 kfortune.sh "$pkgdir/etc/rc.d/functions.d/kfortune.sh"
  install -D -m 644 ku-kfortune.rules "$pkgdir/etc/udev/rules.d/10-ku-kfortune.rules"
}
