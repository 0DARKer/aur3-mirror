#maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kcharselect-frameworks-git
pkgver=r277.83412c2
pkgrel=1
pkgdesc="Character Selector. KF5 Frameworks branch (GIT version)"
url="http://utils.kde.org/projects/kcharselect"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kwidgetsaddons-git' 'kxmlgui')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
conflicts=('kdeutils-kcharselect')
source=("git://anongit.kde.org/kcharselect.git#branch=frameworks")
sha1sums=('SKIP')
install="kcharselect-frameworks-git.install"
_gitname=kcharselect

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  rm -fr build
  mkdir -p build
  cd build
  
  cmake "../${_gitname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  
  make DESTDIR="${pkgdir}" install
}
