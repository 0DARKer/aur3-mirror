# Maintainer: Kniyl <mathias dot ettinger at gmail dot com>

pkgname=xkaapi-git
_gitname='xkaapi'
pkgver=2.0
pkgrel=1
pkgdesc="C++ library that implements work stealing algorithm for multithreaded computation (Git version)"
url="http://kaapi.gforge.inria.fr/dokuwiki/doku.php"
arch=('i686' 'x86_64')
provides=('xkaapi')
conflicts=('xkaapi')
depends=('glibc' 'hwloc')
makedepends=('git')
license=('CeCILL')
source=("${_gitname}::git://scm.gforge.inria.fr/kaapi/kaapi.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|xkaapi_||g' | sed 's|-|.|g'
}

prepare() {
  cd $_gitname

  msg "Applying patches..."
  sed -i 's|  bool work_queue|  inline bool work_queue|g' \
    api/kastl/kastl/kastl_workqueue.h
}

build() {
  cd $_gitname
  
  msg "Starting make..."
  ./bootstrap
  ./configure \
    --prefix=/usr \
    --enable-mode=release \
    --disable-api-kaapif \
    --disable-api-kastl \
    --disable-libgomp \
    --enable-api-kaapic \
    --enable-api-kaapixx \
    --disable-api-quark

  make 
}

package() {
  cd $_gitname
  make DESTDIR="${pkgdir}" install
}
