# Maintainer: Michael Sierks <msierks117 (AT) gmail (dot) com>

pkgname=policyd
pkgver=2.0.13
pkgrel=1
pkgdesc='Policyd v2 policy daemon codenamed "cluebringer"'
_codename=cluebringer
arch=('i686' 'x86_64')
url='http://www.policyd.org/'
license=('GPLv2')
depends=('perl-net-server' 'perl-config-inifiles' 'perl-dbi' 'perl-cache-fastmmap')
optdepends=('perl-dbd-mysql' 'perl-dbd-pg' 'perl-dbd-sqlite')
source=("http://devlabs.linuxassist.net/attachments/download/262/cluebringer-2.0.13.tar.gz" "policyd.service" "convert-tsql.patch" "cbpadmin.patch")
md5sums=('e49d067a253c2194e6766906588b006e' '5a9dd928f01518f2afe6d69be85debad' '5c97560f47ec96cae6aa6fe4a329746f' 'c08e13eaebedbe99e9e9d52f2d0c64fa')
backup=('etc/policyd/cluebringer.conf')
install=policyd.install

build() {
  cd "$srcdir/${_codename}-$pkgver"

  # run patches
  patch cbpadmin < ../cbpadmin.patch
  patch database/convert-tsql < ../convert-tsql.patch

  # generate sql scripts
  cd database

  for x in  mysql55 mysql4 mysql pgsql sqlite
  do
    for i in  core.tsql access_control.tsql quotas.tsql amavis.tsql checkhelo.tsql checkspf.tsql greylisting.tsql
    do 
      ./convert-tsql $x $i
    done > policyd.$x
  done
}


package() {
  cd ${_codename}-${pkgver}

  install -dm755 $pkgdir/{etc/policyd,usr/{bin,sbin,lib/policyd-2.0/{cbp,database}}}
  install -D -m644 ${srcdir}/policyd.service ${pkgdir}/usr/lib/systemd/system/policyd.service

  cp -a cluebringer.conf $pkgdir/etc/policyd/cluebringer.conf
  cp -ra cbp/*           $pkgdir/usr/lib/policyd-2.0/cbp/
  cp -ra database/*      $pkgdir/usr/lib/policyd-2.0/database/
  cp -a cbpadmin         $pkgdir/usr/bin/
  cp -a cbpolicyd        $pkgdir/usr/sbin/
}
