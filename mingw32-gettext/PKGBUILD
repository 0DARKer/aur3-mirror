pkgname=mingw32-gettext
pkgver=0.18.1.1
pkgrel=5
arch=(any)
pkgdesc="GNU internationalization library (mingw32)"
depends=(mingw32-runtime mingw32-libiconv)
makedepends=(mingw32-gcc)
options=(!strip !buildflags !libtool)
conflicts=(mingw32-gettext-static)
provides=(mingw32-gettext-static)
optdepends=('mono: for GNU.Gettext.dll'
'java-runtime: for libintl.jar')
license=(GPL LGPL)
url="http://www.gnu.org/software/gettext/"
source=("http://ftp.gnu.org/pub/gnu/gettext/gettext-$pkgver.tar.gz")
md5sums=('3dd55b952826d2b32f51308f2f91aa89')

build()
{
  cd "$srcdir/gettext-$pkgver/gettext-runtime"
  unset LDFLAGS
  export CFLAGS="$CFLAGS -mms-bitfields"
  mkdir -p build && cd build
  ../configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --enable-threads=win32
  make
}

package() {
  cd "$srcdir/gettext-$pkgver/gettext-runtime"
  make DESTDIR=$pkgdir install

  cd $pkgdir
  i486-mingw32-strip $pkgdir/usr/i486-mingw32/bin/*.exe
  i486-mingw32-strip -x $pkgdir/usr/i486-mingw32/bin/*.dll
  i486-mingw32-strip -g $pkgdir/usr/i486-mingw32/lib/*.a
  rm -rf $pkgdir/usr/i486-mingw32/share/{doc,man}
  rm "$pkgdir/usr/i486-mingw32/share/gettext/ABOUT-NLS"
}
