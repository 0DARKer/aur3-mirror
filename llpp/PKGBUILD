# Maintainer: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=llpp
pkgver=16
pkgrel=1
pkgdesc='Lightweight but fast and featureful PDF viewer based on MuPDF'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/llpp.git'
license=('GPL')
conflicts=('llpp-git')
depends=('ncurses' 'mesa' 'glu')
makedepends=('wget' 'unzip' 'ocaml')
options=('!strip')
source=(http://files.bpiotrowski.pl/mirror/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        llpp.desktop)
sha256sums=('73cf48058a15d1c8ed7117b3606e556fba7f149f9b24f3bd19810d54925d0abf'
            '46999cd48fd6bf8fb62e8c9e891047972ebcd0cd9d6cf8cb46fa7d720c3786df'
            '05c8b918ae575e18a89d4b88f8af1828ed8df8e8ea05322473fd9dc6b0634a57')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  MAKEFLAGS='-j1' bash buildall.sh
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  install -D llpp "$pkgdir"/usr/bin/llpp
  install -Dm0755 "$srcdir"/llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
}
