# Maintainer: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>

pkgname=llpp
pkgver=15
pkgrel=1
pkgdesc='Lightweight but fast and featureful PDF viewer based on MuPDF'
arch=('i686' 'x86_64')
url='http://repo.or.cz/w/llpp.git'
license=('GPL')
conflicts=('llpp-git')
depends=('ncurses' 'mesa' 'glu')
makedepends=('wget' 'unzip' 'ocaml')
options=('!strip')
source=(http://mirror.bpiotrowski.pl/$pkgname/$pkgname-$pkgver.tar.xz
        llpp.desktop)
sha256sums=('9c79e417302e15d63e826157a7509795923e953cfed4e677633fd6eea367a8ac'
            '05c8b918ae575e18a89d4b88f8af1828ed8df8e8ea05322473fd9dc6b0634a57')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  MAKEFLAGS='-j1' bash buildall.sh
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  install -D llpp "$pkgdir"/usr/bin/llpp
  install -Dm0755 "$srcdir"/llpp.desktop "$pkgdir"/usr/share/applications/llpp.desktop
}
