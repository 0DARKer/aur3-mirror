# Contributor: rabyte <rabyte__gmail>

pkgname=slump
pkgver=0.003.01
pkgrel=4
pkgdesc="A SLIGE-based random map generator for Freedoom"
arch=('i686')
url="http://www.samiam.org/slump.html"
license=('GPL' 'custom:"slump"')
install=slump.install
source=(http://www.samiam.org/software/$pkgname-$pkgver.tar.bz2 \
	license.txt)
md5sums=('ef3944131efeba004dae7b8fac0073ff' '03dd7b8aeec8aea1cebe7d51d633e621')

build() {
  cd $startdir/src/$pkgname-$pkgver

  # fix compiler flags
  sed -i "s|-g ||g" Makefile

  # add revenant to monster range and fix a naming bug
  sed 	-e '9229 s|FALSE|TRUE|' \
	-e 's|"slige|"slump|g' \
	-i slump.c

  # uncomment to add baron and hellknight (present in freedoom-svn only):
  # sed -i -e '9158 s|FALSE|TRUE|' -e '9246 s|FALSE|TRUE|' slump.c

  make FLAGS="${CFLAGS}" || return 1
  install -m755 -D slump $startdir/pkg/usr/bin/slump
  install -m644 -D ../license.txt $startdir/pkg/usr/share/licenses/custom/slump/license.txt
}
