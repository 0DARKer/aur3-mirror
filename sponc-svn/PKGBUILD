# Maintainer: Padfoot <padfoot at exemail dot com dot au>

pkgname=sponc-svn
pkgver=6822
pkgrel=1
pkgdesc="Multitouch Pong game."
arch=('any')
url="http://www.sponc.de"
license=('GPL3')
depends=('libavg'
         'hicolor-icon-theme')
optdepends=('scsynth: Audio relies on supercollider')
makedepends=('subversion'
             'gtk-update-icon-cache')
install='sponc.install'
source=("sponc.desktop")
md5sums=('92ecb013110c556b1b126c28b90b901e')

_svntrunk=https://www.libavg.de/svn/trunk/avg_media/mtc/sponc
_svnmod=sponc

build() {
    cd "$srcdir"
    msg "Connecting to SVN server..."

    if [[ -d "$_svnmod/.svn" ]]; then
        (cd "$_svnmod" && svn up -r "$pkgver")
    else
        svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
    fi

    msg "SVN checkout done or server timeout"
  
    rm -rf "$srcdir/$_svnmod-build"
    cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
    cd "$srcdir/$_svnmod-build"

    sed -i "s/python/python2/g" scripts/sponc
    sed -i "s/python/python2/g" sponc/Game.py
    sed -i "s/games/games\/sponc/g" scripts/sponc
    sed -i "s/import sponc/import Game as sponc/g" scripts/sponc
    cp *.sh sponc
}

package() {
    mkdir -p \
        "$pkgdir/usr/bin" \
        "$pkgdir/usr/share/games" \
        "$pkgdir/usr/share/icons/hicolor/scalable/apps" \
        "$pkgdir/usr/share/applications" || return 1

    cd "$srcdir/$_svnmod-build"
    cp -r sponc "$pkgdir/usr/share/games"
    cp sponc/media/finger.png "$pkgdir/usr/share/icons/hicolor/scalable/apps/sponc.png"
    
    cd ./scripts
    install -D -m755 sponc "$pkgdir/usr/bin"
    
    cd "$srcdir"
    install -D -m755 sponc.desktop "$pkgdir/usr/share/applications"
}
