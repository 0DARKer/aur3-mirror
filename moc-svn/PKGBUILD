# Maintianer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Cainã Costa <cainan.costa@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: kotyz <kotyz.king@gmail.com>

pkgname=moc-svn
pkgver=2356
pkgrel=1
pkgdesc='A ncurses console audio player with support for the mp3, ogg, and wave formats'
url="http://moc.daper.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmad' 'libid3tag' 'jack' 'curl' 'libsamplerate' 'libtool')
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug' 'subversion' 'sidplay2')
optdepends=('speex: for using the speex plugin' 
            'ffmpeg: for using the ffmpeg plugin'
            'taglib: for using the musepack plugin' 
            'libmpcdec: for using the musepack plugin' 
            'wavpack: for using the wavpack plugin' 
            'libmodplug: for using the modplug plugin'
            'sidplay2: for playing c64 sid files')
conflicts=('moc')
provides=('moc')
options=('!libtool')

_svntrunk='svn://daper.net/moc/trunk'
_svnmod='moc'

build() {
  if [ -d ${_svnmod} ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} -r ${pkgver} ${_svnmod}
  fi

  msg 'SVN checkout done or server timeout'
  msg 'Starting make...'

  cd ${_svnmod}
  
  export CPPFLAGS="${CPPFLAGS} -I/usr/lib/oss/include"

  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --without-rcc \
    --without-aac \
    --with-oss \
    --with-alsa \
    --with-jack \
    --with-mp3 \
    --with-sidplay2 \
    --with-musepack \
    --with-vorbis \
    --with-flac \
    --with-wavpack \
    --with-sndfile \
    --with-modplug \
    --with-ffmpeg \
    --with-speex \
    --with-samplerate \
    --with-curl \
    --disable-debug

  make
}

package() {
  cd ${_svnmod}

  make DESTDIR=${pkgdir} install
}
