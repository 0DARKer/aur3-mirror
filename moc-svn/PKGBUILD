# Maintianer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Cainã Costa <cainan.costa@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Changes: kotyz <kotyz.king@gmail.com>

pkgname=moc-svn
pkgver=2282
pkgrel=1
pkgdesc="A ncurses console audio player with support for the mp3, ogg, and wave formats"
url="http://moc.daper.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmad' 'libid3tag' 'jack' 'curl' 'libsamplerate' 'libtool')
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug' 'subversion')
optdepends=('speex: for using the speex plugin' 
            'ffmpeg: for using the ffmpeg plugin'
            'taglib: for using the musepack plugin' 
            'libmpcdec: for using the musepack plugin' 
            'wavpack: for using the wavpack plugin' 
            'libmodplug: for using the modplug plugin')
conflicts=('moc')
provides=('moc')
options=('!libtool' 'force')

_svntrunk=svn://daper.net/moc/trunk
_svnmod=moc-svn

build() {
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cd ${pkgname}
  
  export CPPFLAGS="$CPPFLAGS -I/usr/lib/oss/include"

  libtoolize -f

  ./autogen.sh

  ./configure \
    --prefix=/usr \
    --without-rcc \
    --without-aac \
    --with-oss \
    --with-alsa \
    --with-jack \
    --with-mp3 \
    --with-musepack \
    --with-vorbis \
    --with-flac \
    --with-wavpack \
    --with-sndfile \
    --with-modplug \
    --with-ffmpeg \
    --with-speex \
    --with-samplerate \
    --with-curl \
    --disable-debug

  make
}

package() {
  cd ${pkgname}

  make DESTDIR=${pkgdir} install
}
