# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=nut-multimedia-git
pkgver=20110326.5e471c2
pkgrel=1
pkgdesc="free multimedia container format (Git Version)"
arch=('i686' 'x86_64')
url="http://wiki.multimedia.cx/index.php?title=NUT"
license=('GPL')
makedepends=('git')
conflicts=('nut-multimedia')
provides=('nut-multimedia')
source="git://git.ffmpeg.org/nut.git"
md5sums=('SKIP')
_gitname="nut"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}/src/trunk"
  sed 's|/local||' -i config.mak
}

build() {
  cd "${_gitname}/src/trunk"

  CFLAGS+="-fPIC" make libnut/libnut.so
  make nututils
}

package() {
  cd "${_gitname}/src/trunk"
  make DESTDIR="${pkgdir}" install-libnut-shared
  make DESTDIR="${pkgdir}" install-nututils
}