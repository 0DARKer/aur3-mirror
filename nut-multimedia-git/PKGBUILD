# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=nut-multimedia-git
pkgver=20140222.4b13db0
pkgrel=1
pkgdesc="free multimedia container format (Git Version)"
arch=('i686' 'x86_64')
url="http://wiki.multimedia.cx/index.php?title=NUT"
license=('GPL')
depends=('glibc')
makedepends=('git')
conflicts=('nut-multimedia' 'libnut-git')
provides=('nut-multimedia' 'libnut')
source="git://git.ffmpeg.org/nut.git"
md5sums=('SKIP')
_gitname="nut"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

build() {
  cd "${_gitname}/src/trunk"

  CFLAGS+=" -fPIC" make PREFIX=/usr libnut/libnut.so
  make PREFIX=/usr nututils
}

package() {
  cd "${_gitname}/src/trunk"
  make PREFIX=/usr DESTDIR="${pkgdir}" install-libnut-shared
  make PREFIX=/usr DESTDIR="${pkgdir}" install-nututils
}