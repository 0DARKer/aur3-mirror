#Maintainer: max-k <max-k AT post DOT com>

pkgname=pydio
pkgver=5.2.2
pkgrel=1
pkgdesc='PHP file sharing platform, formerly AjaXplorer.'
arch=('any')
url='http://pyd.io/'
license=('AGPL3')
depends=('php>=5.3' 'php-mcrypt>=5.3' 'php-gd>=5.3')
optdepends=('php-sqlite: Store your data in sqlite database'
            'libssh2: File management functionalities over SSH2 [access.sftp]'
            'smbclient: Browse a Samba Server [access.smb]'
            'imagemagick: View PDF and various images formats [editor.imagick]'
            'ghostscript: View PDF and various images formats [editor.imagick]'
            'subversion: Extract SVN informations from workspace'
            'php-libgit2-git: Keep tracks of modifications using a Git repo'
            'pear-http-oauth: Allows accessing a dropbox account'
            'php-ldap: Authentication datas are stored in an LDAP/AD directory'
            'pear-mail-mimedecode: Email reader wich supports eml format'
            'pear-http-webdavclient: Access a WebDAV server'
            'php-aws-sdk: Access an AWS server'
            'pecl-rsync: Use desktop sync client')
options=('!strip')
install="${pkgname}.install"

_srcroot="http://sourceforge.net/projects/ajaxplorer/files"
_srcbase="${_srcroot}/${pkgname}/stable-channel"
source=("${_srcbase}/${pkgver}/${pkgname}-core-${pkgver}.tar.gz"
        "bootstrap_context.php.patch"
        "example_nginx_vhost.conf"
        "example_nginx_vhost_ssl.conf"
        "nginx-drop.conf"
        "nginx-php.conf"
        "${pkgname}.install")

md5sums=('a59de469d8309cc05605853b2af31a80'
         '4852094d1b423d62fee10bf5fde38b63'
         '5ac4f07c89cd927ef24d14894a6ae6cd'
         'dbeddf61b20d2ae3e56233dc32ef6142'
         '726a6b1c5e74ec9ec14094cb0f3b8751'
         'ef491cefd28c407a8a24be548225e2b1'
         'bbbfb63374614e4aa75985595f1adaff')

package() {
  cd ${srcdir}/${pkgname}-core-${pkgver}

  patch -p0 -i ${srcdir}/bootstrap_context.php.patch conf/bootstrap_context.php

  local _INSTDIR="${pkgdir}/usr/share/webapps/${pkgname}"
  install -d "${_INSTDIR}"
  cp -r *.php core phpunit plugins "${_INSTDIR}/"

  local _CONFDIR="$pkgdir/etc/webapps/${pkgname}"
  install -d "${_CONFDIR}"
  cp -r conf/* "${_CONFDIR}/"
  ln -s "/etc/webapps/${pkgname}" "${_INSTDIR}/conf"

  local _DATADIR="$pkgdir/var/lib/${pkgname}"
  install -d "${_DATADIR}"
  cp -r data/* "${_DATADIR}/"
  ln -s "/var/lib/${pkgname}" "${_INSTDIR}/data"
  chgrp -R 33 "${_DATADIR}"
  chmod -R 770 "${_DATADIR}"

  local _DOCDIR="$pkgdir/usr/share/doc/${pkgname}"
  install -d "${_DOCDIR}"
  install -Dm644 ${srcdir}/example_nginx_vhost.conf "${_DOCDIR}/"
  install -Dm644 ${srcdir}/example_nginx_vhost_ssl.conf "${_DOCDIR}/"
  install -d "${_DOCDIR}/nginx-conf.d"
  install -Dm644 ${srcdir}/nginx-drop.conf "${_DOCDIR}/nginx-conf.d/drop.conf"
  install -Dm644 ${srcdir}/nginx-php.conf "${_DOCDIR}/nginx-conf.d/php.conf"
}

