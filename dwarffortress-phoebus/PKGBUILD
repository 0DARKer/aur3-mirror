# Maintainer: ian kremlin <ian [AT] kremlin [DOT] cc>
# Former Maintainer: Dennis Hamester <dennis.hamester [AT] gmail [DOT] com>

pkgname=dwarffortress-phoebus
pkgver=0.40.2

# rewrite by kremlin, july 8th 2014
pkgrel=8
epoch=1

pkgdesc='Dwarf Fortress preloaded with Phoebus tileset'

arch=('x86_64')
url="http://www.bay12games.com/dwarves/"
license=('custom:dwarffortress_phoebus')
groups=('games')
depends=('lib32-sdl_image' 'lib32-sdl_ttf' 'lib32-gtk2' 'lib32-glu')

source=('https://kremlin.cc/Phoebus_34_11v00.zip'
		'https://kremlin.cc/dfmirror/df_40_02_linux.tar.bz2'
		'dwarf-fortress'
		'LICENSE')

md5sums=('6b5d7734093a7f72d6b6038db9a342dc'
         '4990f4b3a67a92729f994691916e2fe0'
         '5766ed7195876418b497d912d2f1512a'
         'eddfb4bd478f9c64e6801bda7c7791ba')

sha512sums=('cea7e22e91713bdb4cc41827111063ca02a1f4b9bdb148a2eadd218a1edefc706a1d1fd6a2d121a85b62fbba0b808ba81df1f91541d2e892d14eeea05e0684ab'
            '7cfce4c07253331d5e680dbb5785a94e11fe27c32f1e0da76f44131b428a462fb4418c38b561bd912de6f162a77f2a1b64c2b08233939cf730f5474d08e075e4'
            '1d0de246499808128aab81d76ac8a2035de72c5cb5967fb226ad5391e150760693b5255103e8e5224e73660f0582c7ab31a43471d9ce2a60bf7902b2a62e8d39'
            '7da52d8a51b00c97b3f6ad715475c7fbc53090d76f182e2820d886b0996a106fc8dffac0ad4458e203b8aec96670f0cb052f59b7a7ecc364f46c484a2ce6d86e')

prepare() {
	
	# inject DF with phoebus' tilesets
	mv "$srcdir"/raw/graphics/* "$srcdir"/df_linux/raw/graphics/
	mv "$srcdir"/raw/objects/* "$srcdir"/df_linux/raw/objects/
	cp -a --no-preserve=ownership "$srcdir"/data/art/* "$srcdir"/df_linux/data/art/
	cp -a --no-preserve=ownership "$srcdir"/data/init/phoebus/* "$srcdir"/df_linux/data/init/

	chmod -R a+rx "$srcdir"/df_linux/libs/Dwarf_Fortress
	chmod -R a+rx "$srcdir"/df_linux/data/*
	chmod -R a+rx "$srcdir"/df_linux/raw/*
	chmod -R a+rx "$srcdir"/df_linux/g_src/*

	chmod a+rx "$srcdir"/df_linux/*
	chmod a+r "$srcdir"/df_linux/sdl/sdl\ license.txt
}

package() {

	install -d "$pkgdir"/usr/lib32/"$pkgname"

	# install provided libs if they aren't already there
	if [[ ! -a /usr/lib32/libgcc_s.so.1 ]]; then
    	install -m644 "$srcdir"/df_linux/libs/libgcc_s.so.1 "$pkgdir"/usr/lib32/libgcc_s.so.1
	fi

	if [[ ! -f /usr/lib32/libstdc++.so.6 ]]; then
    	install -m644 "$srcdir"/df_linux/libs/libstdc++.so.6 "$pkgdir"/usr/lib32/libstdc++.so.6
	fi
	
	if [[ ! -f /usr/lib32/libgraphics.so ]]; then
    	install -m644 "$srcdir"/df_linux/libs/libgraphics.so "$pkgdir"/usr/lib32/"$pkgname"/libgraphics.so
	fi

	cp -a --no-preserve=ownership "$srcdir"/df_linux/* "$pkgdir"/usr/lib32/"$pkgname"/

	install -D -m755 "$srcdir"/dwarf-fortress "$pkgdir"/usr/bin/dwarf-fortress
	mkdir -p -m777 "$pkgdir"/usr/lib32/"$pkgname"/data/save/current/ # savedir needs worldwrite, savefiles are locked to user
	chmod a+w "$pkgdir"/usr/lib32/"$pkgname"/data/index # crashes otherwise

	install -D -m644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}

