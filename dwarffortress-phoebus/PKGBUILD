# Maintainer: ian kremlin <ian [AT] kremlin [DOT] cc>
# Former Maintainer: Dennis Hamester <dennis.hamester [AT] gmail [DOT] com>

pkgname=dwarffortress-phoebus
pkgver=0.40.4

# rewrite by kremlin, july 8th 2014
# with help from halosghost
pkgrel=12
epoch=1

pkgdesc='Dwarf Fortress preloaded with Phoebus tileset'

arch=('x86_64')
url='http://www.bay12games.com/dwarves/'
license=('custom:dwarffortress_phoebus')
groups=('games')
depends=('lib32-sdl_image' 'lib32-sdl_ttf' 'lib32-gtk2' 'lib32-glu' 'lib32-gcc-libs')

source=('https://kremlin.cc/dfmirror/Phoebus_40_04v00.zip'
		'https://kremlin.cc/dfmirror/df_40_04_linux.tar.bz2'
		'dwarf-fortress'
		'LICENSE')

sha512sums=('fe3abdcbdabf87fb24ea9f354c6abb620df7ca5ae39ecd71098a70a3b9fa1d4586b56fd46d81bdb71fa3ad2235194ac6bd13554e9fa961f89d1f451302de63d6'
            '3d2403c04881b2e4447581cf43d9f6ad40b36fcc4524e98d442b0a9b4ee32b93b243677f876b89bcf75bc451664cb3f03b315c6d2b749a493454a5a6e8c9c76b'
            '1d0de246499808128aab81d76ac8a2035de72c5cb5967fb226ad5391e150760693b5255103e8e5224e73660f0582c7ab31a43471d9ce2a60bf7902b2a62e8d39'
            '7da52d8a51b00c97b3f6ad715475c7fbc53090d76f182e2820d886b0996a106fc8dffac0ad4458e203b8aec96670f0cb052f59b7a7ecc364f46c484a2ce6d86e')

prepare() {
	
	# inject DF with phoebus' tilesets
	mv "$srcdir"/raw/graphics/* "$srcdir"/df_linux/raw/graphics/
	mv "$srcdir"/raw/objects/* "$srcdir"/df_linux/raw/objects/

	cp -a --no-preserve=ownership "$srcdir"/data/art/* "$srcdir"/df_linux/data/art/
	cp -a --no-preserve=ownership "$srcdir"/data/init/phoebus/* "$srcdir"/df_linux/data/init/

	chmod -R a+rx "$srcdir"/df_linux/libs/Dwarf_Fortress
	chmod -R a+rx "$srcdir"/df_linux/data/*
	chmod -R a+rx "$srcdir"/df_linux/raw/*
	chmod -R a+rx "$srcdir"/df_linux/g_src/*

	chmod a+rx "$srcdir"/df_linux/*
	chmod a+r "$srcdir"/df_linux/sdl/sdl\ license.txt
}

package() {

	install -d "$pkgdir"/usr/lib32/"$pkgname"
   	install -m644 "$srcdir"/df_linux/libs/libgraphics.so "$pkgdir"/usr/lib32/"$pkgname"/libgraphics.so
	install -D -m755 "$srcdir"/dwarf-fortress "$pkgdir"/usr/bin/dwarf-fortress
	install -D -m644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

	cp -a --no-preserve=ownership "$srcdir"/df_linux/* "$pkgdir"/usr/lib32/"$pkgname"/
	mkdir -p "$pkgdir"/usr/lib32/"$pkgname"/data/save/current/

	chmod a+rw "$pkgdir"/usr/lib32/"$pkgname"/data/save/
	chmod a+rw "$pkgdir"/usr/lib32/"$pkgname"/data/save/current # save dir is worldwrite
	chmod a+w "$pkgdir"/usr/lib32/"$pkgname"/data/index # crashes otherwise
}

