# Maintainer: ian kremlin <ian [AT] kremlin [DOT] cc>
# Former Maintainer: Dennis Hamester <dennis.hamester [AT] gmail [DOT] com>

pkgname=dwarffortress-phoebus
pkgver=0.40.9

# rewrite by kremlin, july 8th 2014
# with help from halosghost
pkgrel=14
epoch=1

pkgdesc='Dwarf Fortress preloaded with Phoebus tileset'

arch=('x86_64')
url='http://www.bay12games.com/dwarves/'
license=('custom:dwarffortress_phoebus')
groups=('games')
depends=('lib32-sdl_image' 'lib32-sdl_ttf' 'lib32-gtk2' 'lib32-glu' 'lib32-gcc-libs')

source=('https://kremlin.cc/dfmirror/Phoebus_40_08v00.zip'
		'https://kremlin.cc/dfmirror/df_40_09_linux.tar.bz2'
		'dwarf-fortress'
		'LICENSE')

sha512sums=('81eb77f1cf31dfc285aa3daebd5183401edd147791b55e06d367496e12b11a64e988a1a6ea9e81e539cbe002f2c0eb7f0647f27524ba74a7f9d5d360da585108'
            '61e0a8890212194cfcab2ccf734769253cd1c14f43eb5827393342d81e7de6d04154000abed24d9d95443c7f4d55ba2a53021cb2b5951b262f52d042fd3599cb'
            '1d0de246499808128aab81d76ac8a2035de72c5cb5967fb226ad5391e150760693b5255103e8e5224e73660f0582c7ab31a43471d9ce2a60bf7902b2a62e8d39'
            '7da52d8a51b00c97b3f6ad715475c7fbc53090d76f182e2820d886b0996a106fc8dffac0ad4458e203b8aec96670f0cb052f59b7a7ecc364f46c484a2ce6d86e')
prepare() {
	
	# inject DF with phoebus' tilesets
	mv "$srcdir"/raw/graphics/* "$srcdir"/df_linux/raw/graphics/
	mv "$srcdir"/raw/objects/* "$srcdir"/df_linux/raw/objects/

	cp -a --no-preserve=ownership "$srcdir"/data/art/* "$srcdir"/df_linux/data/art/
	cp -a --no-preserve=ownership "$srcdir"/data/init/phoebus/* "$srcdir"/df_linux/data/init/

	chmod -R a+rx "$srcdir"/df_linux/libs/Dwarf_Fortress
	chmod -R a+rx "$srcdir"/df_linux/data/*
	chmod -R a+rx "$srcdir"/df_linux/raw/*
	chmod -R a+rx "$srcdir"/df_linux/g_src/*

	chmod a+rx "$srcdir"/df_linux/*
	chmod a+r "$srcdir"/df_linux/sdl/sdl\ license.txt
}

package() {

	install -d "$pkgdir"/usr/lib32/"$pkgname"
   	install -m644 "$srcdir"/df_linux/libs/libgraphics.so "$pkgdir"/usr/lib32/"$pkgname"/libgraphics.so
	install -D -m755 "$srcdir"/dwarf-fortress "$pkgdir"/usr/bin/dwarf-fortress
	install -D -m644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

	cp -a --no-preserve=ownership "$srcdir"/df_linux/* "$pkgdir"/usr/lib32/"$pkgname"/
	mkdir -p "$pkgdir"/usr/lib32/"$pkgname"/data/save/current/

	chmod a+rw "$pkgdir"/usr/lib32/"$pkgname"/data/save/
	chmod a+rw "$pkgdir"/usr/lib32/"$pkgname"/data/save/current # save dir is worldwrite
	chmod a+w "$pkgdir"/usr/lib32/"$pkgname"/data/index # crashes otherwise
}

