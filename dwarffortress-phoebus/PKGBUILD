# Maintainer: Dennis Hamester <dennis.hamester [AT] gmail [DOT] com>
# Original from cummunity by:
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>
pkgname=dwarffortress-phoebus
_dfver_a=0
_dfver_b=34
_dfver_c=11
_dfver=${_dfver_b}_${_dfver_c}
_phver_a=00
_phver=${_dfver}v${_phver_a}
pkgver=$_dfver_a.$_dfver_b.$_dfver_c.$_phver_a
pkgrel=4
pkgdesc="A single-player fantasy game. You control a dwarven outpost or an adventurer in a randomly generated persistent world. Packed with Phoebus' graphics set."
arch=(i686 x86_64)
url="http://www.bay12forums.com/smf/index.php?topic=57557.0"
install="$pkgname.install"
license=('custom:dwarffortress_phoebus')
depends=(dwarffortress=${_dfver_a}.${_dfver_b}.${_dfver_c})

source=(http://www.bay12games.com/dwarves/df_${_dfver}_linux.tar.bz2
        Phoebus_${_phver}.zip::"http://dffd.wimbli.com/download.php?id=2430&f=Phoebus_${_phver}.zip"
        enable_sound_and_intro.patch
        dwarffortress-phoebus.sh
        dwarffortress-phoebus.desktop
        dwarffortress-phoebus.png)

backup=(opt/df_linux-phoebus/data/init/init.txt)

# Change this to 1 to disable TrueType fonts.
_nott=0

build() {
  cd $srcdir
  patch -p1 -i enable_sound_and_intro.patch
}

package() {
  install -dm775 -o root -g games $pkgdir/opt/df_linux-phoebus

  cp -r $srcdir/df_linux/data $pkgdir/opt/df_linux-phoebus/
  cp -r $srcdir/df_linux/raw $pkgdir/opt/df_linux-phoebus/
  cp -rf $srcdir/raw/ $pkgdir/opt/df_linux-phoebus/
  if [[ $_nott == 0 ]]; then
      cp -rf $srcdir/data/init/phoebus/* $pkgdir/opt/df_linux-phoebus/data/init/
  else
      cp -rf $srcdir/data/init/phoebus_nott/* $pkgdir/opt/df_linux-phoebus/data/init/
  fi 
  cp -rf $srcdir/data/art/ $pkgdir/opt/df_linux-phoebus/data/
  cp $srcdir/df_savegame_updater.sh $pkgdir/opt/df_linux-phoebus/

  # Yay for precompiled stuff with junk permissions! :D
  find $pkgdir/opt/df_linux-phoebus/raw -type d -exec chmod 755 {} +
  find $pkgdir/opt/df_linux-phoebus/raw -type f -exec chmod 644 {} +
  find $pkgdir/opt/df_linux-phoebus/data -type d -exec chmod 775 {} +
  find $pkgdir/opt/df_linux-phoebus/data -type f -exec chmod 664 {} +
  chmod 755 $pkgdir/opt/df_linux-phoebus/df_savegame_updater.sh
  install -dm775 -o root -g games $pkgdir/opt/df_linux-phoebus/data/save

  # Desktop launcher with icon
  install -Dm755 $srcdir/dwarffortress-phoebus.sh $pkgdir/usr/bin/dwarffortress-phoebus
  install -Dm644 $srcdir/dwarffortress-phoebus.desktop $pkgdir/usr/share/applications/dwarffortress-phoebus.desktop
  install -Dm644 $srcdir/dwarffortress-phoebus.png $pkgdir/usr/share/pixmaps/dwarffortress-phoebus.png

  install -Dm644 $srcdir/df_linux/readme.txt $pkgdir/usr/share/licenses/dwarffortress-phoebus/readme.txt
}

# vim:set ts=2 sw=2 et:

sha1sums=('a66d56562ffba698198127c8d9a1194a2313c992'
          '781d5e11fadbe80bddc9058d9dd44399eada21f2'
          '1336eb91716a8ff6a172b67dec59be81bc6d4d1f'
          '012a41a3b2523a0ddbb2797647a7bd89c218e6de'
          '59dd658ade880b861e00730bc3150555728ad27b'
          '9b7f721588e0ff9c9d115a28d51d63823e3a1e7c')
