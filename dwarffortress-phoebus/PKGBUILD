# Maintainer: Dennis Hamester <dennis.hamester [AT] gmail [DOT] com>
# Original from cummunity by:
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: djnm <nmihalich [at} gmail dott com>
pkgname=dwarffortress-phoebus
pkgver=0.34.11.0
_dfver=34_11
_phver=${_dfver}v00
pkgrel=1
pkgdesc="A single-player fantasy game. You control a dwarven outpost or an adventurer in a randomly generated persistent world. Packed with Phoebus' graphics set."
arch=(i686 x86_64)
url="http://www.bay12forums.com/smf/index.php?topic=57557.0"
install="$pkgname.install"
license=('custom:dwarffortress')
depends=(gtk2 mesa sdl_image libsndfile openal sdl_ttf)
if [[ $CARCH == 'x86_64' ]]; then
  depends=(lib32-gtk2 lib32-mesa lib32-sdl_image lib32-libsndfile lib32-openal
  lib32-libxdamage lib32-ncurses lib32-sdl_ttf)
  optdepends=('lib32-nvidia-utils: If you have nvidia graphics'
              'lib32-catalyst-utils: If you have ATI graphics'
              'lib32-alsa-lib: for alsa sound'
              'lib32-libpulse: for pulse sound')
fi

source=(http://www.bay12games.com/dwarves/df_${_dfver}_linux.tar.bz2
        Phoebus_${_phver}.zip::"http://dffd.wimbli.com/download.php?id=2430&f=Phoebus_${_phver}.zip"
        enable_sound_and_intro.patch
        dwarffortress-phoebus
        dwarffortress-phoebus.desktop
        dwarffortress-phoebus.png)

backup=(opt/df_linux-phoebus/data/init/init.txt)

build() {
  cd $srcdir
  patch -p1 -i enable_sound_and_intro.patch
}

package() {
  cd $srcdir/df_linux
  install -dm755 $pkgdir/opt/
  cp -r $srcdir/df_linux/ $pkgdir/opt/
  cp -rf $srcdir/raw/ $pkgdir/opt/df_linux/
  cp -rf $srcdir/data/init/phoebus/* $pkgdir/opt/df_linux/data/init/
  cp -rf $srcdir/data/art/ $pkgdir/opt/df_linux/data/
  cp $srcdir/df_savegame_updater.sh $pkgdir/opt/df_linux/

  # Yay for precompiled stuff with junk permissions! :D
  find $pkgdir/opt/df_linux -type d -exec chmod 755 {} +
  find $pkgdir/opt/df_linux -type f -exec chmod 644 {} +

  install -Dm755 $srcdir/dwarffortress-phoebus $pkgdir/usr/bin/dwarffortress-phoebus

  chmod 755 $pkgdir/opt/df_linux/libs/Dwarf_Fortress
  chmod 755 $pkgdir/opt/df_linux/df_savegame_updater.sh

  install -d -m775 -o root -g games $pkgdir/opt/df_linux/data/save

  # This probably is overkill, but I don't know what specific files df needs permission for in here.
  chmod 775 $pkgdir/opt/df_linux
  chown root:games $pkgdir/opt/df_linux
  chown -R root:games $pkgdir/opt/df_linux/data
  find $pkgdir/opt/df_linux/data -type d -exec chmod 775 {} +
  find $pkgdir/opt/df_linux/data -type f -exec chmod 664 {} +

  # Desktop launcher with icon
  install -Dm644 $srcdir/dwarffortress-phoebus.desktop $pkgdir/usr/share/applications/dwarffortress-phoebus.desktop
  install -Dm644 $srcdir/dwarffortress-phoebus.png     $pkgdir/usr/share/pixmaps/dwarffortress-phoebus.png

  install -Dm644 $srcdir/df_linux/readme.txt $pkgdir/usr/share/licenses/dwarffortress-phoebus/readme.txt

  mv $pkgdir/opt/df_linux $pkgdir/opt/df_linux-phoebus
}

# vim:set ts=2 sw=2 et:

md5sums=('33e26a93e5914f7545fa1aaa53706eeb'
         '6b5d7734093a7f72d6b6038db9a342dc'
         '64b1ab457d83b92b4ce3d5ef82a451de'
         'e019138395f8a8d905299ce6630da3a0'
         '2de336b1749959fab8f59ff4ff8133be'
         'b1d51f82400073af9bb179e34a9209d0')
