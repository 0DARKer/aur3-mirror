# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
# Contributor: Daniele Marinello <m2346zc5-aur@yahoo.it>

pkgname=powermanga
pkgver=0.93
pkgrel=1
pkgdesc="Arcade 2D shoot-them-up game with 41 levels and more than 200 sprites"
arch=('i686' 'x86_64')
url="http://linux.tlk.fr/"
license=('GPL3')
depends=('sdl_mixer' 'libpng' 'hicolor-icon-theme')
backup=('var/games/powermanga/powermanga.hi' 'var/games/powermanga/powermanga.hi-easy' 'var/games/powermanga/powermanga.hi-hard')
options=(!emptydirs)
install=pm.install
source=(http://linux.tlk.fr/games/Powermanga/download/${pkgname}-${pkgver}.tgz 
	${pkgname}.16.png
	${pkgname}.32.png
	${pkgname}.48.png
)

build() {
  cd $pkgname-$pkgver
  autoreconf -fvi	
  ./configure --prefix=/usr 
  make 
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR=$pkgdir install
    mkdir -p $pkgdir/usr/bin/
    mv $pkgdir/usr/games/$pkgname $pkgdir/usr/bin/$pkgname 	
    ## fix permissions
    chmod 755 $pkgdir/usr/bin/$pkgname
    chown root:root $pkgdir/usr/bin/$pkgname
    chmod -R 775 $pkgdir/var/games	

    install -m644 $srcdir/${pkgname}.16.png -D $pkgdir/usr/share/icons/hicolor/16x16/apps/${pkgname}.png	
    install -m644 $srcdir/${pkgname}.32.png -D $pkgdir/usr/share/icons/hicolor/32x32/apps/${pkgname}.png	
    install -m644 $srcdir/${pkgname}.48.png -D $pkgdir/usr/share/icons/hicolor/48x48/apps/${pkgname}.png	

   mkdir -p $pkgdir/usr/share/applications
   cat > $pkgdir/usr/share/applications/${pkgname}.desktop << EOF
[Desktop Entry]
Name=PowerManga
Comment=Shoot 'em up game with 3D graphics
Exec=powermanga
Icon=powermanga
Terminal=false
Type=Application
StartupNotify=true
Categories=Game;ArcadeGame;
EOF

}
md5sums=('0b058d15caaa45ab0a60933484bd1f29'
         '97b5fae1c2fbaa39bf58c8cc3602d222'
         'b5e95c97e86168919e46c6081c825094'
         'acb8cadc965ca9bd1a9db887eb882bad')
