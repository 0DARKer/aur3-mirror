# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
# Contributor: Daniele Marinello <m2346zc5-aur@yahoo.it>
# patch from Mageia's srpm

pkgname=powermanga
pkgver=0.91
pkgrel=1
pkgdesc="Arcade 2D shoot-them-up game with 41 levels and more than 200 sprites"
arch=('i686' 'x86_64')
url="http://linux.tlk.fr/"
license=('GPL3')
depends=('sdl_mixer' 'libpng' 'sdl_mixer' 'hicolor-icon-theme')
backup=('var/games/powermanga/powermanga.hi' 'var/games/powermanga/powermanga.hi-easy' 'var/games/powermanga/powermanga.hi-hard')
options=(!emptydirs)
install=pm.install
source=(http://linux.tlk.fr/games/Powermanga/download/${pkgname}-${pkgver}.tgz 
	powermanga-0.91-linking2.patch
	powermanga-0.91-automake-1.13.patch
	powermanga-0.91-upstream-png.patch
	${pkgname}.16.png
	${pkgname}.32.png
	${pkgname}.48.png
)

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i $srcdir/powermanga-0.91-linking2.patch
  patch -Np1 -i $srcdir/powermanga-0.91-automake-1.13.patch
  patch -Np1 -i $srcdir/powermanga-0.91-upstream-png.patch
  autoreconf -fvi	
  ./configure --prefix=/usr 
  make 
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR=$pkgdir install
    mkdir -p $pkgdir/usr/bin/
    mv $pkgdir/usr/games/$pkgname $pkgdir/usr/bin/$pkgname 	
    ## fix permissions
    chmod 755 $pkgdir/usr/bin/$pkgname
    chown root:root $pkgdir/usr/bin/$pkgname
    chmod -R 775 $pkgdir/var/games	

    install -m644 $srcdir/${pkgname}.16.png -D $pkgdir/usr/share/icons/hicolor/16x16/apps/${pkgname}.png	
    install -m644 $srcdir/${pkgname}.32.png -D $pkgdir/usr/share/icons/hicolor/32x32/apps/${pkgname}.png	
    install -m644 $srcdir/${pkgname}.48.png -D $pkgdir/usr/share/icons/hicolor/48x48/apps/${pkgname}.png	

   mkdir -p $pkgdir/usr/share/applications
   cat > $pkgdir/usr/share/applications/${pkgname}.desktop << EOF
[Desktop Entry]
Name=PowerManga
Comment=Shoot 'em up game with 3D graphics
Exec=powermanga
Icon=powermanga
Terminal=false
Type=Application
StartupNotify=true
Categories=Game;ArcadeGame;
EOF

}

md5sums=('6e7e101af43233893d8cd3f01cc932e3'
         '8ee8f09919a2b371d3c7a81b3cfb5a6b'
         '2f81903e9a8bffbbdea3fe3b6a9d1aac'
         '993f00f07bb5b313e50a7aefc786eb3c'
         '97b5fae1c2fbaa39bf58c8cc3602d222'
         'b5e95c97e86168919e46c6081c825094'
         'acb8cadc965ca9bd1a9db887eb882bad')
