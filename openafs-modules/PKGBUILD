# Maintainer: Michael Lass <bevan@bi-co.net>

pkgname=openafs-modules
_srcname=openafs
pkgver=1.6.10
pkgrel=3
pkgdesc="Kernel module for OpenAFS"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=('openafs')
makedepends=('linux-headers')
conflicts=('openafs-features-libafs' 'openafs<1.6.6-2')
options=(!emptydirs)
source=(http://openafs.org/dl/openafs/${pkgver}/${_srcname}-${pkgver}-src.tar.bz2
        0001-Linux-3.17-No-more-typedef-for-ctl_table.patch
        0002-Linux-3.17-Deal-with-d_splice_alias-errors.patch
        0003-Linux-3.18-d_invalidate-can-no-longer-return-an-erro.patch
        0004-Linux-3.18-key_type-no-longer-has-a-match-op.patch
        0005-LINUX-Avoid-check-for-key_type.match-existence.patch
	0001-Quick-Dirty-fix-build-with-Linux-3.18.1.patch)
install=openafs-modules.install
md5sums=('f5ecfe357f6c56053f7bbab40644496e'
         '83b41405e7faf8f028f704dfde3e34d5'
         '4f966af9236c0eef88d00dd81e8345b4'
         'c50e0927e7f948b2e3eee7b862209d18'
         'f07c43433e78b03be20fb731a1e33cc5'
         '1fbcdab1d5f6d3f72bf3aa67b3f5aa2a'
         '424d127923f15e35837d717911e22fa5')

# Heuristic to determine version of installed kernel
# You can modify this if the heuristic fails
_extramodules=$(ls -dt /usr/lib/modules/extramodules-* | head -n1)
_kernelver=$(cat ${_extramodules}/version)

build() {
  cd ${srcdir}/${_srcname}-${pkgver}

  # patches needed for linux 3.17
  patch -p1 < ${srcdir}/0001-Linux-3.17-No-more-typedef-for-ctl_table.patch
  patch -p1 < ${srcdir}/0002-Linux-3.17-Deal-with-d_splice_alias-errors.patch

  # patches needed for linux 3.18 + regression fix
  patch -p1 < ${srcdir}/0003-Linux-3.18-d_invalidate-can-no-longer-return-an-erro.patch
  patch -p1 < ${srcdir}/0004-Linux-3.18-key_type-no-longer-has-a-match-op.patch
  patch -p1 < ${srcdir}/0005-LINUX-Avoid-check-for-key_type.match-existence.patch

  # Uncomment this for a quick&dirty fix to get openafs running on Linux 3.18.1 and newer
  # Use at your own risk!
  #patch -p1 < ${srcdir}/0001-Quick-Dirty-fix-build-with-Linux-3.18.1.patch

  # Only needed when changes to configure were made
  ./regen.sh -q

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --libexecdir=/usr/bin \
              --with-linux-kernel-packaging \
              --disable-fuse-client \
              --with-linux-kernel-build="/usr/lib/modules/${_kernelver}/build"

  make only_libafs
}


package() {
  cd ${srcdir}/${_srcname}-${pkgver}

  make DESTDIR=${pkgdir} install_only_libafs

  # install kernel module
  install -dm755 ${pkgdir}${_extramodules}
  mv ${pkgdir}/lib/modules/${_kernelver}/extra/openafs/openafs.ko ${pkgdir}${_extramodules}/openafs.ko
  gzip -9 ${pkgdir}${_extramodules}/openafs.ko

  # install license
  install -Dm644 ${srcdir}/${_srcname}-${pkgver}/src/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove binaries and headers (already included in openafs package)
  rm -rf ${pkgdir}/usr/bin ${pkgdir}/usr/include

  # update major kernel version in install file
  sed -i "s/depmod .*/depmod ${_kernelver}/g" "${startdir}/openafs-modules.install"
}
