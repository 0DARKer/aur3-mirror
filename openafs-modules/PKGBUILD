# Maintainer: Michael Lass <bevan@bi-co.net>

pkgname=openafs-modules
_srcname=openafs
pkgver=1.6.9
pkgrel=2
pkgdesc="Kernel module for OpenAFS"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=('openafs')
makedepends=('linux-headers')
conflicts=('openafs-features-libafs' 'openafs<1.6.6-2')
options=(!emptydirs)
source=(http://openafs.org/dl/openafs/${pkgver}/${_srcname}-${pkgver}-src.tar.bz2
        0001-Linux-3.16-Switch-to-iter_file_splice_write.patch
        0002-Linux-3.16-Convert-to-new-write_iter-read_iter-ops.patch)
install=openafs-modules.install
md5sums=('5c2d0ce12e0e676d6e7d67fb08547cce'
         '1c658c31338776a90f7c992ff71534e1'
         'b364b7af04aa107699b6b35775259871')

# Heuristic to determine version of installed kernel
# You can modify this if the heuristic fails
_extramodules=$(ls -dt /usr/lib/modules/extramodules-* | head -n1)
_kernelver=$(cat ${_extramodules}/version)

build() {
  cd ${srcdir}/${_srcname}-${pkgver}

  # patches needed for linux 3.16
  patch -p1 < ${srcdir}/0001-Linux-3.16-Switch-to-iter_file_splice_write.patch
  patch -p1 < ${srcdir}/0002-Linux-3.16-Convert-to-new-write_iter-read_iter-ops.patch

  # Only needed when changes to configure were made
  ./regen.sh -q

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --libexecdir=/usr/bin \
              --with-linux-kernel-packaging \
              --disable-fuse-client \
              --with-linux-kernel-build="/usr/lib/modules/${_kernelver}/build"

  make only_libafs
}


package() {
  cd ${srcdir}/${_srcname}-${pkgver}

  make DESTDIR=${pkgdir} install_only_libafs

  # install kernel module
  install -dm755 ${pkgdir}${_extramodules}
  mv ${pkgdir}/lib/modules/${_kernelver}/extra/openafs/openafs.ko ${pkgdir}${_extramodules}/openafs.ko
  gzip -9 ${pkgdir}${_extramodules}/openafs.ko

  # install license
  install -Dm644 ${srcdir}/${_srcname}-${pkgver}/src/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove binaries and headers (already included in openafs package)
  rm -rf ${pkgdir}/usr/bin ${pkgdir}/usr/include

  # update major kernel version in install file
  sed -i "s/depmod .*/depmod ${_kernelver}/g" "${startdir}/openafs-modules.install"
}
