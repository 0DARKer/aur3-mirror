# Maintainer: Michael Lass <bevan@bi-co.net>

pkgname=openafs-modules
_srcname=openafs
pkgver=1.6.10
pkgrel=5
pkgdesc="Kernel module for OpenAFS"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=('openafs')
makedepends=('linux-headers')
conflicts=('openafs-features-libafs' 'openafs<1.6.6-2')
options=(!emptydirs)
source=(http://openafs.org/dl/openafs/${pkgver}/${_srcname}-${pkgver}-src.tar.bz2
        0001-Linux-3.17-No-more-typedef-for-ctl_table.patch
        0002-Linux-3.17-Deal-with-d_splice_alias-errors.patch
        0003-Linux-3.18-d_invalidate-can-no-longer-return-an-erro.patch
        0004-Linux-3.18-key_type-no-longer-has-a-match-op.patch
        0005-LINUX-Avoid-check-for-key_type.match-existence.patch
        0006-Linux-Move-code-to-reset-the-root-to-afs-LINUX.patch
        0007-Linux-d_alias-becomes-d_u.d_alias.patch
        0008-Linux-3.19-No-more-f_dentry.patch
        0009-Linux-3.19-Use-mgs_iter-in-struct-msghdr.patch
        0010-Linux-3.19-struct-nameidata-becomes-opaque.patch
        0011-Linux-d_splice_alias-may-drop-inode-reference-on-err.patch)
install=openafs-modules.install
sha256sums=('09b3c4f9500107c6e1cb737a5642e50f338d6acb0579e5e03c772cba1c6f1276'
            'c5fdacfb0675f508d69a85829c109b0e01c704f752e1d658885e3dda7416b3e0'
            '0575f250809984b02907982c0eb8422e7f842f4470ce02ad9b49cdd6d47362d4'
            '20cd4dba92d5d95fdf5ba421366fd5bdd04d956e9b57d0eaa28c01359a4fb977'
            '63b1acaadaca950b189614146b9b9ba750d4c02cde0dfcabf7a81f2d49d64437'
            'd942957e4a317d06c2df10a5e798386f1d8767e9d7ed65a6926093e4eee8e391'
            '82f15ad570145b5ac9460a40ba1e2968fcb2ddf3ba494fcdaad30f183b8e912a'
            '321eb715b3fb63370d66cc8503db8bc4274503ce024c6c41fdfd89af1ea86bd4'
            '09a5ba6143d60c1d9fad79911a1726e64962a270852f2804e698177bd1385694'
            '753074028982c32cb50edcd9c75a0b4555be8e26fc8e558d19578cd1a28d7074'
            '4b4b520540057be4714d5483c61ea4f06a94e0acdc54bc88586a3b64521a4671'
            'dde73649ec6ee5befed2e3fa9e344e91cd9858d96c8bd3445c662eeedd17ad53')

# Heuristic to determine version of installed kernel
# You can modify this if the heuristic fails
_extramodules=$(ls -dt /usr/lib/modules/extramodules-* | head -n1)
_kernelver=$(cat ${_extramodules}/version)

build() {
  cd ${srcdir}/${_srcname}-${pkgver}

  # patches needed for linux 3.17
  patch -p1 < ${srcdir}/0001-Linux-3.17-No-more-typedef-for-ctl_table.patch
  patch -p1 < ${srcdir}/0002-Linux-3.17-Deal-with-d_splice_alias-errors.patch

  # patches needed for linux 3.18
  patch -p1 < ${srcdir}/0003-Linux-3.18-d_invalidate-can-no-longer-return-an-erro.patch
  patch -p1 < ${srcdir}/0004-Linux-3.18-key_type-no-longer-has-a-match-op.patch

  # fix a possible regression from patches above
  patch -p1 < ${srcdir}/0005-LINUX-Avoid-check-for-key_type.match-existence.patch

  # structural changes required for the following patches
  patch -p1 < ${srcdir}/0006-Linux-Move-code-to-reset-the-root-to-afs-LINUX.patch

  # patch needed for linux 3.18.1
  patch -p1 < ${srcdir}/0007-Linux-d_alias-becomes-d_u.d_alias.patch

  # patches needed for linux 3.19
  patch -p1 < ${srcdir}/0008-Linux-3.19-No-more-f_dentry.patch
  patch -p1 < ${srcdir}/0009-Linux-3.19-Use-mgs_iter-in-struct-msghdr.patch
  patch -p1 < ${srcdir}/0010-Linux-3.19-struct-nameidata-becomes-opaque.patch

  # fix used after freed inode reference on linux 3.17.3 and newer
  # (still in upstream's review process for stable branch)
  patch -p1 < ${srcdir}/0011-Linux-d_splice_alias-may-drop-inode-reference-on-err.patch

  # Only needed when changes to configure were made
  ./regen.sh -q

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --libexecdir=/usr/bin \
              --with-linux-kernel-packaging \
              --disable-fuse-client \
              --disable-linux-d_splice_alias-extra-iput \
              --with-linux-kernel-build="/usr/lib/modules/${_kernelver}/build"

  make only_libafs
}


package() {
  cd ${srcdir}/${_srcname}-${pkgver}

  make DESTDIR=${pkgdir} install_only_libafs

  # install kernel module
  install -dm755 ${pkgdir}${_extramodules}
  mv ${pkgdir}/lib/modules/${_kernelver}/extra/openafs/openafs.ko ${pkgdir}${_extramodules}/openafs.ko
  gzip -9 ${pkgdir}${_extramodules}/openafs.ko

  # install license
  install -Dm644 ${srcdir}/${_srcname}-${pkgver}/src/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove binaries and headers (already included in openafs package)
  rm -rf ${pkgdir}/usr/bin ${pkgdir}/usr/include

  # update major kernel version in install file
  sed -i "s/depmod .*/depmod ${_kernelver}/g" "${startdir}/openafs-modules.install"
}
