#Maintainer: Jameson Pugh <imntreal@gmail.com>

pkgname=lib32-sdl2-image
pkgver=2.0.0
pkgrel=2
pkgdesc="SDL2 image libraries"
arch=('i686' 'x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=(lib32-sdl2)
optdepends=(lib32-libpng lib32-libtiff lib32-libjpeg lib32-libwebp)
provides=(lib32-sdl2_image)
options=(!libtool)
source=("http://www.libsdl.org/tmp/SDL_image/release/SDL2_image-${pkgver}.tar.gz")
sha256sums=('a005117a6122dbaec24eab29724582eed6e24e8a3081178fec77888d338cc0c3')

build() {
  export CC='gcc -m32'
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${srcdir}/SDL2_image-${pkgver}/"
  ./configure --disable-static --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/SDL2_image-${pkgver}/"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rf "$pkgdir/usr/share/aclocal/" "$pkgdir/usr/include/" "${pkgdir}/usr/bin/"
}
## vim:set ts=2 sw=2 et:
