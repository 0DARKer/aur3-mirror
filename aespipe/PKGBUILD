# Contirbutor: Juergen Hoetzel <juergen@hoetzel.info>
# Contributor: Joerie de Gram <j.de.gram@gmail.com>
# Contributor: bender02 at archlinux dot us
pkgname=aespipe
pkgver=2.4b
pkgrel=1
pkgdesc="Encrypts data from stdin to stdout"
arch=('i686' 'x86_64')
url="http://loop-aes.sourceforge.net"
license=('GPL')
depends=('glibc')
source=(http://loop-aes.sourceforge.net/$pkgname/$pkgname-v$pkgver.tar.bz2)
md5sums=('9854ed1dfdc38838e784a79000ce82e7')

build() {
  cd "$srcdir/$pkgname-v$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-v$pkgver"
  make prefix="$pkgdir/usr" install
  install -d -m755 "$pkgdir"/usr/{bin,share/doc/$pkgname}
  install -D -m755 bz2aespipe "$pkgdir"/usr/bin/
  install -D -m644 README ChangeLog "$pkgdir"/usr/share/doc/$pkgname/
}
