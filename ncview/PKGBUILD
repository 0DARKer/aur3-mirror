# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Graziano Giuliani <giuliani@lamma.rete.toscana.it>

pkgname=ncview
pkgver=1.93g
pkgrel=1
pkgdesc="A visual browser for netCDF format files"
arch=('i686' 'x86_64')
url="http://meteora.ucsd.edu/~pierce/ncview_home_page.html"
license=('GPL3')
depends=('netcdf' 'udunits' 'netpbm' 'libxaw')
source=(ftp://cirrus.ucsd.edu/pub/$pkgname/$pkgname-$pkgver.tar.gz \
  ncview.patch)
md5sums=('09615ad95fe1f1cc684c24e67bb78a78'
         '52f14cec5a343d3243c7f9bf03a62fac')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p0 -i ../ncview.patch || return 1
  ./configure \
    --prefix=/usr \
    --with-netcdf_incdir=/usr/include \
    --with-netcdf_libname=libnetcdf.a \
    --with-netcdf_libdir=/usr/lib \
    --with-udunits_incdir=/usr/include \
    --with-udunits_libdir=/usr/lib \
    --with-ppm_incdir=/usr/include \
    --with-ppm_libdir=/usr/lib || return 1
  make || return 1
  install -m755 -d $pkgdir/usr/share/X11/app-defaults
  install -m755 -d $pkgdir/usr/share/ncview
  make \
    prefix=/usr \
    BINDIR=$pkgdir/usr/bin \
    MANDIR=$pkgdir/usr/share/man/man1 \
    install || return 1
  install -m644 *.ncmap $pkgdir/usr/share/ncview
  install -m644 nc_overlay.* $pkgdir/usr/share/ncview
}
