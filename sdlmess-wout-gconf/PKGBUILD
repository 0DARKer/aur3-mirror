# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based on: Anton Shestakov <engored*ya.ru>
# Contributor: robb_force <robb_force@holybuffalo.net>

_patchlevel=0
_basever=0.146

pkgname=sdlmess-wout-gconf
_pkgname=sdlmess
pkgver="${_basever}.u${_patchlevel}"
pkgrel=1
pkgdesc='A port of the popular Multiple Emulator Super System using SDL. WITHOUT GCONF'
url='http://www.mess.org/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gcc-libs' 'fontconfig')
makedepends=('mesa')
[ "$CARCH" = 'i686' ] && makedepends+=('nasm')
optdepends=('ttf-liberation: recommended UI font')
#DLAGENTS=('http::/usr/bin/wget -U "Mozilla/5.0 (X11; U; Linux x86_64; en-US; rv:1.9.1.2) Gecko/20090804 Shiretoko/3.5.2" -c -t 3 --waitretry=3 -O %o %u')
install="${_pkgname}".install

#for i in `seq 1 "${_patchlevel}"`; do
#  _patches=""${_patches}" mess"${_basever/./}"u"${i}"_diff.zip::http://www.mess.org/_media/downloads:mess"${_basever/./}"u"${i}"_diff.zip"
#done

source=(
        "sdlmess.sh" 
        sdlmess-wout-gconf-v2.patch
#        ${_patches})
        "http://ompldr.org/vZHV6ag/mess${_basever}s.zip")
md5sums=('141069e7019da5e33414dc8d4c421150'
         'e17515824b113725c573d91710337f7a'
         '3ad4e780d1d7d3b66edf985417018c5b')

build() {
  cd "${srcdir}"
  
  find . -type f -not -name '*.png' -exec perl -pi -e 's/\r\n?/\n/g' '{}' \;

  #for i in `seq 1 "${_patchlevel}"`; do
  #  msg "Applying patch: mess"${_basever/./}"u"${i}".diff"
  #  patch --silent -p0 -E < mess"${_basever/./}"u"${i}".diff
  #  msg2 "Done"
  #done

  msg "Patch disable GCONF and GTK dependencies"
    patch --silent -p0 -E < ../sdlmess-wout-gconf-v2.patch
  msg2 "Done"

  # Adjusting make options according to target architecture
  if [ "$CARCH" == 'x86_64' ]; then
    echo 'Compiling for AMD64...'
    make ${MAKEFLAGS} TARGET=mess AMD64=1 PTR64=1 SUFFIX64='' NOWERROR=1 NO_DEBUGGER=1
  elif [ "$CARCH" == 'i686' ]; then
    echo 'Compiling for i686...'
    make ${MAKEFLAGS} TARGET=mess I686=1 NOWERROR=1 NO_DEBUGGER=1
  else
    echo 'Compiling for i386...'
    make ${MAKEFLAGS} TARGET=mess PM=1 NOWERROR=1 NO_DEBUGGER=1
  fi
}

package() {
  cd "${srcdir}"
    
  # Installing the wrapper script
  install -Dm755 "${srcdir}"/"${_pkgname}".sh "${pkgdir}"/usr/bin/"${_pkgname}"

  # Installing binaries
  install -Dm755 mess "${pkgdir}"/usr/share/"${_pkgname}"/"${_pkgname}"

  # Installing extra bits
  install -d "${pkgdir}"/usr/share/"${_pkgname}"/{artwork,hash}
  install -m644 artwork/* "${pkgdir}"/usr/share/"${_pkgname}"/artwork/
  install -m644 hash/* "${pkgdir}"/usr/share/"${_pkgname}"/hash/

  # The license
  install -Dm644 docs/license.txt "${pkgdir}"/usr/share/licenses/custom/"${pkgname}"/license.txt
}
