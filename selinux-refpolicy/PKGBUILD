# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)

pkgname=selinux-refpolicy
_origname=refpolicy
pkgver=20101213
pkgrel=1
pkgdesc="Modular SELinux reference policy including headers and docs"
arch=('any')
url="http://oss.tresys.com/projects/refpolicy"
license=('GPL')
groups=('selinux' 'selinux-policies')
depends=('kernel26-selinux')
makedepends=('selinux-usr-checkpolicy>=2.0.16' 'selinux-usr-policycoreutils>=2.0.0' 
	     'selinux-usr-libsepol>=2.0.29' 'selinux-usr-libsemanage>=2.0.29' 
	     'pyxml') 
backup=(etc/selinux/config)
install=${pkgname}.install
source=(http://oss.tresys.com/files/${_origname}/${_origname}-2.${pkgver}.tar.bz2
	config)
md5sums=('260a9fea8893734d1818d1ed22d748b1'
	 '45fd5c19d2a5d693c258096e3382970c')

build() {
  cd "${srcdir}/${_origname}"
  sed -i -e "s/MONOLITHIC = y/MONOLITHIC = n/" build.conf
  sed -i -e "s/python/python2/" Makefile
  make bare
  make conf
  make
}

package(){
  cd "${srcdir}/${_origname}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-headers
  make DESTDIR="${pkgdir}" install-docs

  # Create some files and directories necesary for loading policy,
  # which is done via install script.
  install -d -m0755 "${pkgdir}/etc/selinux/${_origname}/modules"
  install -d -m0700 "${pkgdir}/etc/selinux/${_origname}/modules/active"
  install -d -m0700 "${pkgdir}/etc/selinux/${_origname}/modules/active/modules"
  install -d -m0755 "${pkgdir}/etc/selinux/${_origname}/policy"
  touch "${pkgdir}/etc/selinux/${_origname}/modules/"{semanage.read.LOCK,semanage.trans.LOCK}
  touch "${pkgdir}/etc/selinux/${_origname}/policy/policy.24" 
  # Link the policy file for selinux-sysvinit to find it
  cd "${pkgdir}/etc"
  ln -s "selinux/${_origname}/policy/policy.24" "policy.bin"

  # Install main SELinux config file defaulting to refpolicy
  install -m644 -D "${srcdir}/config" "${pkgdir}/etc/selinux/config"

  # Some changes due to python2
  sed -i -e "s/python/python2/" \
	"${pkgdir}/usr/share/selinux/refpolicy/include/support/segenxml.py"
  sed -i -e "s/python/python2/" \
	"${pkgdir}/usr/share/selinux/refpolicy/include/Makefile"
}
