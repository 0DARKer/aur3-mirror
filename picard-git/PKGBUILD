pkgname=picard-git
pkgver=1.2.1011.g3b9d29f
pkgrel=1
pkgdesc="MusicBrainz tagging application"
arch=('i686' 'x86_64')
url="http://musicbrainz.org/doc/MusicBrainz_Picard"
license=('GPL')
depends=('python2-pyqt4' 'mutagen')
optdepends=('python2-discid: CD-Lookup feature'
            'chromaprint: AcoustID integration')
makedepends=('git')
provides=('picard')
conflicts=('picard' 'picard-qt' 'picard-bzr')
source=('git://github.com/musicbrainz/picard.git')
md5sums=('SKIP')

pkgver(){
    cd "$srcdir/picard"
    echo "$(git describe --tags --always | sed 's|release-||g' | sed 's/-/./g' )"
}

package() {
    cd "$srcdir/picard"
    python2 setup.py install --root=$pkgdir
    cp -r contrib/plugins/* "${pkgdir}/usr/lib/python2.7/site-packages/picard/plugins/"
}
