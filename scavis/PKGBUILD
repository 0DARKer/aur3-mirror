# Maintainer: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>

pkgname=scavis
pkgver=1.6
pkgrel=1
pkgdesc="An environment for scientific computation, data analysis and data visualization"
arch=('any')
url="http://jwork.org/scavis/"
license=('custom')
depends=('java-runtime')
source=("${pkgname}.png"
        "${pkgname}.desktop"
        "http://sourceforge.net/projects/scavis/files/${pkgver}/${pkgname}-${pkgver}.zip")
md5sums=('7497f0ea6fc54e4a927a9deb01d88154'
         '833aa84ff09e48d6fcd681f24aee0d7c'
         '34e8411818162d0dc871ee8ad88cbdf3')
sha512sums=('c8ee268ed079f6b2dd4b0d14910e0e107460e89aadee082759d6a04d1090909735c6601cdaec57227efc444ef392471f090fd78517ebc64ece7a8cec773ab6a4'
            '6f53e07d8b2b2cff762c04c4f2eec6a61ae356493cf726a6ad8361fbbcca28d81adbab7ac41d912fae3366fedd845dbba9ddf029bdefbabe70df9a03d004d62d'
            '1e0351a1fc119373560194d300c1f338c9bb92ac52d8eb8dae4832632efedf106c94d2a40adc1fbf942847c3b52267598a37c31adcc4622174455b315b7289a8')

prepare() {
  rm "${srcdir}/${pkgname}/"*.bat
  sed -i "s#\`pwd\`#\"/usr/share/java/${pkgname}\"#g" "${srcdir}/${pkgname}/${pkgname}.sh"
  sed -i 's#JYTHON_HOME=$JEHEP_HOME\"/lib/jython\"#JYTHON_HOME=$HOME\"/.jehep/jython\"#g' "${srcdir}/${pkgname}/${pkgname}.sh"
}

package() {
  install -d "${pkgdir}/usr/share/java/${pkgname}/"
  cp -dr --no-preserve=mode,ownership "${srcdir}/${pkgname}/"* "${pkgdir}/usr/share/java/${pkgname}/"
  install -m644 -D "${srcdir}/${pkgname}/Docs/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -m755 -D "${srcdir}/${pkgname}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}.sh"
  install -m644 -D "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -m644 -D "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
