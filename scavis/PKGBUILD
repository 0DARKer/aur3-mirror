# Maintainer: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>

pkgname=scavis
pkgver=1.8
pkgrel=1
pkgdesc="An environment for scientific computation, data analysis and data visualization"
arch=('any')
url="http://jwork.org/scavis/"
license=('custom')
depends=('java-runtime')
source=("${pkgname}.png"
        "${pkgname}.desktop"
        "http://sourceforge.net/projects/scavis/files/${pkgver}/${pkgname}-${pkgver}.zip")
md5sums=('7497f0ea6fc54e4a927a9deb01d88154'
         '833aa84ff09e48d6fcd681f24aee0d7c'
         'd54617cff0fd67f9af2035621f8ad9f1')
sha512sums=('c8ee268ed079f6b2dd4b0d14910e0e107460e89aadee082759d6a04d1090909735c6601cdaec57227efc444ef392471f090fd78517ebc64ece7a8cec773ab6a4'
            '6f53e07d8b2b2cff762c04c4f2eec6a61ae356493cf726a6ad8361fbbcca28d81adbab7ac41d912fae3366fedd845dbba9ddf029bdefbabe70df9a03d004d62d'
            '24240b0b9c2f8c9f8bfd5be4f3ee19af8d2c20a1cdfb4f81a6919cc0816e6978a5d8191a04effce096d2b79751ef2d259f4be57d2f6c0b0c4fd9fd53e316c0ed')

prepare() {
  rm "${srcdir}/${pkgname}/"*.bat
  sed -i "s#\`pwd\`#\"/usr/share/java/${pkgname}\"#g" "${srcdir}/${pkgname}/${pkgname}.sh"
  sed -i 's#JYTHON_HOME=$JEHEP_HOME\"/lib/jython\"#JYTHON_HOME=$HOME\"/.jehep/jython\"#g' "${srcdir}/${pkgname}/${pkgname}.sh"
}

package() {
  install -d "${pkgdir}/usr/share/java/${pkgname}/"
  cp -dr --no-preserve=mode,ownership "${srcdir}/${pkgname}/"* "${pkgdir}/usr/share/java/${pkgname}/"
  install -m644 -D "${srcdir}/${pkgname}/Docs/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -m755 -D "${srcdir}/${pkgname}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}.sh"
  install -m644 -D "${srcdir}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -m644 -D "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
