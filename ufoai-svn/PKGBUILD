#Contributor: Wesley <rudirennsau_at_hotmail_dot_com>

pkgname=ufoai-svn
pkgver=32055
pkgrel=1
pkgdesc="Development version of 'Ufo: Alien Invasion'. Includes Gamedata and Mapeditor."
url="http://ufoai.sourceforge.net"
license=('GPL')
arch=('i686' 'x86_64')
depends=('libgl' 'libjpeg' 'libpng' 'curl' 'sdl_mixer' 'sdl_ttf' 'gtkglext' 'gtksourceview2' 'openal' 'libxml2')
#libxml2 is not needed by ufoai but it is by the map editor uforadiant
makedepends=('subversion')
conflicts=('ufoai' 'ufoai-data')
source=(ufo.sh ufoded.sh ufoai.png ufoai.desktop uforadiant.sh uforadiant.desktop)
md5sums=('2b371e0e45408b4a6c2d42837c921851'
         '81f3c45d1c3269473d2fe85730fd7e53'
         'ae3be6e45fdd8ff584fcfc55d6a40a72'
         'f212811fecfa6eacff73b09e12df73a3'
	 'aab84b679d2b3172071c03dd46a10499'
	 '3855a686ff59fa4eb90fef83e0660701')


_svntrunk=http://ufoai.svn.sourceforge.net/svnroot/ufoai/ufoai/trunk
_svnmod=trunk

build() {
	cd $startdir/src

	if [ -d $_svnmod/.svn ]; then
		(cd $_svnmod && svn up -r $pkgver)
	else
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
	fi

	msg "SVN checkout done or server timeout"
	msg "Starting make..."

	cd $_svnmod

	./configure --prefix=/usr || return 1

	make || return 1
	make lang || return 1
	make models || return 1 #not necessary, but it will speed things up during runtime
	make maps || return 1
	make pk3 || return 1

	#making the mapeditor
	make uforadiant || return 1
}

package() {
	cd $startdir/src/$_svnmod
	#gamedata
	mkdir -p $startdir/pkg/usr/share/ufoai/{base,radiant}/
	cp  base/*.pk3 base/*.cfg base/*.txt base/*.so $startdir/pkg/usr/share/ufoai/base/
	cp -r radiant/bitmaps/ radiant/games/ radiant/plugins/ radiant/prefabs/ radiant/shaders/ radiant/sourceviewer/ $startdir/pkg/usr/share/ufoai/radiant

	 #binarys
	mkdir -p $startdir/pkg/usr/share/ufoai/
	cp ufo ufoded ufo2map $startdir/pkg/usr/share/ufoai/
	cp radiant/uforadiant $startdir/pkg/usr/share/ufoai/radiant/

	#translations
	#mkdir -p $startdir/pkg/usr/share/locale/
	cp -r base/i18n/ $startdir/pkg/usr/share/ufoai/base/
	cp -r radiant/i18n/ $startdir/pkg/usr/share/ufoai/radiant/

	install -D -m644 $startdir/src/ufoai.desktop $startdir/pkg/usr/share/applications/ufoai.desktop
	install -D -m644 $startdir/src/uforadiant.desktop $startdir/pkg/usr/share/applications/uforadiant.desktop
	install -D -m644 $startdir/src/ufoai.png $startdir/pkg/usr/share/pixmaps/ufoai.png
	install -D -m644 $startdir/src/trunk/radiant/bitmaps/logo.png $startdir/pkg/usr/share/pixmaps/uforadiant.png
	install -D -m755 $startdir/src/ufo.sh $startdir/pkg/usr/bin/ufo
	install -D -m755 $startdir/src/ufoded.sh $startdir/pkg/usr/bin/ufoded
	install -D -m755 $startdir/src/uforadiant.sh $startdir/pkg/usr/bin/uforadiant
}
