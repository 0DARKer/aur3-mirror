# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor: megadriver <megadriver at gmx dot com>
# Contributor: Vasil Yonkov <bustervill at gmail dot com>
# Contributor: Guten Ye <ywzhaifei [at] gmail [dot] com>

pkgname=ruby-build-git
pkgver=20130408_2f8dcaf
pkgrel=1
pkgdesc="Compile and install Ruby"
arch=('any')
url="http://github.com/sstephenson/ruby-build"
license=("MIT")
makedepends=('git')
provides=('ruby-build')
conflicts=('ruby-build')
_gitname="ruby-build"
source=("git://github.com/sstephenson/ruby-build.git")
sha1sums=('SKIP')

pkgver() {
    cd "$srcdir"/"$_gitname"
    printf '%s\n' "$(awk '{field = $NF}; END {print field}'\
        <(git log --grep=^ruby-build -1))_$(awk -F- '{print $NF}'\
        <(git describe --always))"
}

package() {
  cd "$srcdir"/"$_gitname"

  mkdir -p "$pkgdir"/usr/bin
  mkdir -p "$pkgdir"/usr/share/ruby-build

  cp bin/* "$pkgdir"/usr/bin
  cp share/ruby-build/* "$pkgdir"/usr/share/ruby-build
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/"$_gitname"/LICENSE
}
