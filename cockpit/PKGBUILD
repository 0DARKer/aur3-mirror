# Maintainer: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Mark Constable <markc@renta.net>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=cockpit
pkgver=0.5
pkgrel=1
pkgdesc='A systemd web based user interface for Linux servers'
arch=(i686 x86_64)
url='http://www.cockpit-project.org/'
license=(LGPL2.1)
conflicts=(cockpit-git)
depends=(libssh krb5 sshpass accountsservice perl-json udisks2 json-glib)
makedepends=(git intltool python2-pyscss gtk-doc perl-javascript-minifier-xs gobject-introspection networkmanager storaged libgsystem)
source=(https://github.com/cockpit-project/cockpit/archive/${pkgver}.tar.gz
        cockpit.pam)
sha1sums=('e6ab161d95db9253171f457d0a621445358018cc'
          '9d1268e4e9ad6aca14d2afd5e64176f497cc027d')

build() {
  cd cockpit-${pkgver}

  # Build failed with --as-needed
  export LDFLAGS="${LDFLAGS//,--as-needed}"

  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd cockpit-${pkgver}
  make DESTDIR="$pkgdir" install

  # Overwrite cockpit pam file with ArchLinux supported pam file
  cp ../cockpit.pam $pkgdir/etc/pam
}
