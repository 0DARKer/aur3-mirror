# Maintainer: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Mark Constable <markc@renta.net>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=cockpit
pkgver=0.31
pkgrel=1
pkgdesc='A systemd web based user interface for Linux servers'
arch=(i686 x86_64)
url='http://www.cockpit-project.org/'
license=(LGPL2.1)
conflicts=(cockpit-git)
depends=(libssh krb5 sshpass accountsservice perl-json perl-locale-po udisks2 json-glib)
makedepends=(git intltool python2-pyscss gtk-doc perl-javascript-minifier-xs gobject-introspection networkmanager storaged libgsystem xmlto)
optdepends=(storaged)
source=(https://github.com/cockpit-project/cockpit/archive/${pkgver}.tar.gz
        nodejs-dependencies.patch)
sha1sums=('d8ec99617d9ad1305fe209ad0415647a800fb0e9'
          'ff00e0be490993cad1378c213f7befc0aa888522')

prepare() {
  cd cockpit-${pkgver}
  patch -p1 -i "${srcdir}/nodejs-dependencies.patch"
}

build() {
  cd cockpit-${pkgver}

  # Build failed with --as-needed
  export LDFLAGS="${LDFLAGS//,--as-needed}"
  mkdir -p build
  cd build
  
  ../autogen.sh
  ../configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd cockpit-${pkgver}/build
  make DESTDIR="$pkgdir" install
}
