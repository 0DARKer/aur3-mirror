# Contributor: fredbezies <fredbezies@gmail.com>
# Maintainer: Lari Tikkanen <lartza@wippies.com>
pkgname=transmission-svn-gtk
pkgver=13255
pkgrel=1
pkgdesc="Fast, easy, and free BitTorrent client (GTK+ GUI)(Git version)"
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
depends=('curl' 'libevent' 'gtk3' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'subversion')
optdepends=('notification-daemon: Desktop notification support'
  	    'transmission-cli: daemon and web support')
provides=(transmission-gtk)
conflicts=(transmission-gtk)
install=transmission-svn-gtk.install

_svntrunk="svn://svn.transmissionbt.com/Transmission/trunk"
_svnmod="transmission"

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  ./autogen.sh --prefix=/usr --disable-cli --disable-daemon
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make -C gtk DESTDIR="$pkgdir/" install
  make -C po DESTDIR="$pkgdir/" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
