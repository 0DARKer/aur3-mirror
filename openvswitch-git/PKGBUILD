pkgname="openvswitch-git"
_pkgname="openvswitch"
pkgver=20130324
pkgrel=1
pkgdesc="A multilayer virtual switch"
arch=("i686" "x86_64")
url='http://openvswitch.org/'
license=('Apache')
conflicts=('openvswitch')
options=('!upx' '!strip')
install="$pkgname.install"
backup=('etc/conf.d/openvswitch')
source=(openvswitch-git.init 
        openvswitch-git.conf
        ovsdb-server.service
        ovs-vswitchd.service  
        openvswitch.tmpfiles
        openvswitch.netcfg-connection
        openvswitch.netcfg-example
        netcfg-openvswitch@.service)
_gitroot="git://openvswitch.org/openvswitch"
_gitname="openvswitch"

sha1sums=('c0ee4181450f966633873037ab7ae17ec2872a98'
          '78e6e79495419642eb361b78c8c74eacf5259388'
          '6ca6bbb3df8ae1a0dee4bb3eec0bdfbd8a937bee'
          'd8342cbb9ffa69632e3ac694ab72f361401b1f4f'
          '160ecab73ffd4d9f4b25982886c08cde0eee31df'
          '49515ae7ba92ae2a0c9eb1aea795e67148edfdb7'
          'db66b60ae4eb5ae2800e511df0d356ee68a45b32'
          'e35ffcf061db21784e9de41ed20422ba0d2de132')

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

   ./boot.sh
   ./configure \
                --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --with-rundir=/run/openvswitch \
                PYTHON=/usr/bin/python2


  make 
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
  cd "$srcdir"
  install -D "$srcdir/openvswitch.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/openvswitch.conf"
  install -D "$srcdir/ovsdb-server.service" "$pkgdir/usr/lib/systemd/system/ovsdb-server.service"
  install -D "$srcdir/ovs-vswitchd.service" "$pkgdir/usr/lib/systemd/system/ovs-vswitchd.service"
  install -D "$srcdir/openvswitch.netcfg-connection" "$pkgdir/usr/lib/network/connections/openvswitch"
  install -D "$srcdir/openvswitch.netcfg-example" "$pkgdir/etc/network.d/examples/openvswitch"
  install -D "$srcdir/netcfg-openvswitch@.service" "$pkgdir/usr/lib/systemd/system/netcfg-openvswitch@.service"
  install -d "$pkgdir/etc/openvswitch"
  install -m 0770 -d "$pkgdir/run/openvswitch"
}

# vim:set ts=2 sw=2 et:

