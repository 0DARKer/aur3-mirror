pkgname="openvswitch-git"
_pkgname="openvswitch"
pkgver=20120109
pkgrel=1
pkgdesc="A multilayer virtual switch"
arch=("i686" "x86_64")
url='http://openvswitch.org/'
license=('Apache')
depends=('dkms')
conflicts=('openvswitch')
install="$pkgname.install"
source=( dkms.conf openvswitch-git.init)
_gitroot="git://openvswitch.org/openvswitch"
_gitname="openvswitch"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  autoreconf -v -i
  patch -p0 -i "$startdir/configure-python2.patch"
  ./configure --with-python=python2 --prefix=/usr --localstatedir=/var

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/src" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/$_pkgname" \
    "$pkgdir/var/run/$_pkgname" "$pkgdir/usr/share/$_pkgname"
  cp -a "$srcdir/$_gitname-build" "$pkgdir/usr/src/$_pkgname-$pkgver"
  install -t "$pkgdir/usr/src/$_pkgname-$pkgver/" "$startdir/dkms.conf"
  install -m 744 "$srcdir/$pkgname.init" "$pkgdir/etc/rc.d/$_pkgname"
  install -t "$pkgdir/usr/share/$_pkgname/" "$srcdir/$_gitname-build/vswitchd/vswitch.ovsschema"
  sed -e "s;@version@;$pkgver;g" -i $pkgdir/usr/src/$_pkgname-$pkgver/dkms.conf

  # This line replaces references to $pkgdir with /usr/src/openvswitch
  sed -e "s;$srcdir/;/usr/src/openvswitch/;g" -i $(grep -riIs "$srcdir/" $pkgdir | awk -F':' '{print $1}' | sort | uniq)
}

sha384sums=('ffddc01eb366a2aa2beab977b695cc40429c1d74f22a539f6d0be2a7c88d9909050cdad0ca5d6495af0bf517c6efd12d'
            'bd156dd589c3797267607f19c4d0f26b164d816c70fd1ba473c1b144d87c261a66a02811334cf90f7ac0889afc4d26d9')

# vim:set ts=2 sw=2 et:

