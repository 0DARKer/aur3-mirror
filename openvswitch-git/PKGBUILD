pkgname="openvswitch-git"
_pkgname="openvswitch"
pkgver=20120602
pkgrel=1
pkgdesc="A multilayer virtual switch"
arch=("i686" "x86_64")
url='http://openvswitch.org/'
license=('Apache')
depends=('dkms')
conflicts=('openvswitch')
options=('!upx' '!strip')
install="$pkgname.install"
backup=('etc/conf.d/openvswitch')
source=(dkms.conf openvswitch-git.init openvswitch-git.conf)
_gitroot="git://openvswitch.org/openvswitch"
_gitname="openvswitch"

sha1sums=('444044f2ac30978c541ce63c35d48b5bf4c4c584'
          'bb76ad0856fc3690ae2619fd6bd2879acb784237'
          'a2dcb958ee5cbdb694be7176624be3815c20a842')

make() {
  if which colormake &>/dev/null ; then
    /usr/bin/env colormake $@
  else
    /usr/bin/env make $@
  fi
}

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  autoreconf -v -i
  ./configure --prefix=/usr --localstatedir=/var

  make PYTHON="python2"
}

package() {
  cd "$srcdir/$_gitname-build"

  make PYTHON="python2" DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/src" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/$_pkgname" \
    "$pkgdir/var/run/$_pkgname" "$pkgdir/usr/share/$_pkgname"
  cp -a "$srcdir/$_gitname-build" "$pkgdir/usr/src/$_pkgname-$pkgver"
  install -t "$pkgdir/usr/src/$_pkgname-$pkgver/" "$startdir/dkms.conf"
  install -m 744 "$srcdir/$pkgname.init" "$pkgdir/etc/rc.d/$_pkgname"
  install -m 644 "$srcdir/$pkgname.conf" "$pkgdir/etc/conf.d/$pkgname"
  install -t "$pkgdir/usr/share/$_pkgname/" "$srcdir/$_gitname-build/vswitchd/vswitch.ovsschema"
  sed -e "s;@version@;$pkgver;g" -i $pkgdir/usr/src/$_pkgname-$pkgver/dkms.conf

  # This line replaces references to $pkgdir with /usr/src/openvswitch
  sed -e "s;$srcdir/;/usr/src/openvswitch/;g" -i $(grep -riIs "$srcdir/" $pkgdir | awk -F':' '{print $1}' | sort | uniq)
}

# vim:set ts=2 sw=2 et:

