_qtver=5.4.0
_type=debug
pkgname=telegram-desktop-git
pkgver=r515.8ed19618
pkgrel=1
pkgdesc='Official desktop version of Telegram messaging app.'
arch=('i686' 'x86_64')
url="https://desktop.telegram.org/"
license=('GPL3')
depends=('opusfile')
makedepends=('git' 'libunity' 'libappindicator-gtk2' 'xorg-server-xvfb')
source=(http://download.qt-project.org/official_releases/qt/${_qtver%.*}/$_qtver/single/qt-everywhere-opensource-src-$_qtver.tar.gz
        git+https://github.com/telegramdesktop/tdesktop.git)

pkgver() {
  cd "tdesktop"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  if ! [ -d Libraries ]; then
    mkdir Libraries
    mv qt-everywhere-opensource-src-$_qtver Libraries/QtStatic
    cp tdesktop/Telegram/_qt_${_qtver//./_}_patch.diff Libraries/QtStatic
    cd Libraries/QtStatic
    git apply _qt_${_qtver//./_}_patch.diff
  fi

  sed -i 's/CUSTOM_API_ID//g' "$srcdir"/tdesktop/Telegram/Telegram.pro

  (
    echo 'INCLUDEPATH += "/usr/lib/glib-2.0/include"'
    echo 'INCLUDEPATH += "/usr/lib/gtk-2.0/include"'
    echo 'INCLUDEPATH += "/usr/include/opus"'
  ) >> "$srcdir"/tdesktop/Telegram/Telegram.pro
}

build() {
  local x _type
  # Building patched Qt
  cd Libraries/QtStatic
  ./configure -prefix "$srcdir/qt" -release -opensource -confirm-license -qt-xcb -no-opengl -static -nomake examples -nomake tests -skip qtquick1 -skip qtdeclarative
  make module-qtbase module-qtimageformats
  make module-qtbase-install_subtargets module-qtimageformats-install_subtargets

  export PATH="$srcdir/qt/bin:$PATH"

  mkdir -p "$srcdir"/tdesktop/Linux/{Debug,Release}Intermediate{Style,Emoji,Lang,Updater,}

  for _type in debug release; do
    for x in Style Lang; do
      cd "$srcdir"/tdesktop/Linux/${_type^}Intermediate$x
      qmake CONFIG+=${_type} ../../Telegram/Meta$x.pro
      make
    done
    cd "$srcdir"/tdesktop/Linux/${_type^}Intermediate
    qmake CONFIG+=${_type} ../../Telegram/Telegram.pro
    xvfb-run -a make -j1
  done
}

package() {
  install -dm755 "$pkgdir"/usr/bin
  install -Dm755 "$srcdir"/tdesktop/Linux/Release/Telegram "$pkgdir"/usr/bin/telegram
}
md5sums=('e8654e4b37dd98039ba20da7a53877e6'
         'SKIP')
