# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>

pkgname=ocaml-pa_ounit
_pkgname=${pkgname/ocaml-/}
pkgver=108.07.01
pkgrel=1
pkgdesc="Syntax extension that helps writing in-line test in OCaml."
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=3.12'
         'ocaml-type-conv'
         'ocaml-ounit')
makedepends=('ocaml-findlib')
options=(!strip !makeflags)
url="http://bitbucket.org/yminsky/ocaml-core/wiki/Home"
source=("http://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('b8c567cef070d7d5bf2645735d43c72d')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
