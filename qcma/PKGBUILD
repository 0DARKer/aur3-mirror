# Maintainer: codestation <codestation404@gmail.com>

pkgname=qcma
pkgver=0.3.6
pkgrel=1
pkgdesc="Content Manager Assistant for the PS Vita"
arch=("i686" "x86_64")
url="https://github.com/codestation/qcma"
license=('GPL')
depends=('qt4' 'vitamtpmod>=2.5.4' 'ffmpeg' 'libnotify')

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/codestation/${pkgname}/archive/v${pkgver}.tar.gz")
install=qcma.install
md5sums=('9a8994dab63355065577187ef51aa979')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # workaround for qtchooser defaulting to Qt5
  sed -i 's/system(qdbuscpp2xml-qt5/system(QT_SELECT=5 qdbuscpp2xml/' "${srcdir}/${pkgname}-${pkgver}/qcma_gui.pro"
  sed -i 's/system(qdbuscpp2xml/system(QT_SELECT=4 qdbuscpp2xml/' "${srcdir}/${pkgname}-${pkgver}/qcma_gui.pro"

  lrelease-qt4 resources/translations/*.ts
  qmake-qt4 qcma.pro PREFIX="/usr"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install
}
