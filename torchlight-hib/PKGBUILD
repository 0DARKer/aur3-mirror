# Maintainer: Sam S. <smls75@gmail.com>

pkgname=torchlight-hib
pkgver=1.15_20130521
_hibver=2013-05-21
pkgrel=1
pkgdesc='A hack and slash action role-playing game (Humble Bundle version)'
url='http://www.torchlightgame.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('libgl' 'mesa' 'freeglut' 'libxrandr' 'expat' 'libxaw' 'libxft'
         'libxinerama' 'freeimage' 'util-linux' 'zziplib' 'pcre' 'libtxc_dxtn')
source=('torchlight-hib.desktop')
md5sums=('36903fc0f19910870440b68ac4cd22a4')
options=('!strip' '!upx')
PKGEXT='.pkg.tar'

_installer="Torchlight-${_hibver}.sh"
_installer_md5='a747a31547202d7dc85c8a6cd9b47c9f'


package() {
  cd $srcdir
  _target="/opt/Torchlight"
  case $CARCH in i686) _arch=x86;; x86_64) _arch=x86_64;; esac

  # Get installer
  _get_local_source "${_installer}" --md5 "${_installer_md5}" || {
    error "Unable to find the game archive. Please download it from your Humble
           Bundle page, and place it into one of the above locations."
    exit 1; }
  
  # Extract installer
  msg "Starting setup..."
  [[ -d archive ]] && rm -r archive; mkdir archive; mkdir -p "${pkgdir}${_target}"
  while read line; do echo -n '.'; done < <(  # show progress as dots
    sh "${_installer}" --tar xvf -C archive/
    lzma -d < archive/instarchive_linux_${_arch} | tar xvf - -C "${pkgdir}${_target}"
    lzma -d < archive/instarchive_all | tar xvf - -C "${pkgdir}${_target}"
    lzma -d < archive/subarch | tar xvf - -C archive
    for i in archive/deps/{CEGUI,Ogre,SDL2,fmod,pcre}/*${_arch}; do
      lzma -d < $i | tar xvf - -C "${pkgdir}${_target}"
    done
  ); echo

  # Fix permissions
  find "${pkgdir}" -type f -exec chmod 644 "{}" +
  chown root:root -R "${pkgdir}"

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "${pkgdir}${_target}/Torchlight.png" \
                 "${pkgdir}/usr/share/pixmaps/torchlight.png"

  # Install launcher symlink
  chmod 755 "${pkgdir}${_target}/Torchlight.bin.${_arch}"
  install -d "${pkgdir}/usr/bin"
  ln -s "${_target}/Torchlight.bin.${_arch}" "${pkgdir}/usr/bin/torchlight"

  # Install license file
  install -Dm644 "archive/config/license" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}


###### HELPER FUNCTIONS ######

# Locates a file or folder provided by the user, and symlinks it into $srcdir
_get_local_source() {
  msg "Looking for '$1'..."; rm -f "$srcdir/$1"
  declare -A _search=(['build dir']="$startdir"
                      ['$LOCAL_PACKAGE_SOURCES']="$LOCAL_PACKAGE_SOURCES")
  for _key in "${!_search[@]}"; do local _dir="${_search["$_key"]}"
    echo -n "    - in $_key [${_dir:-<undefined>}] ... ";
    if [[ -z "$_dir" || ! -e "$_dir/$1" ]]; then
      echo "NOT FOUND"
    elif [[ -n $2 && "$(${2#--}sum "$_dir/$1"|awk '{print $1}')" != $3 ]]; then
      echo "CHECKSUM FAILED";
    else
      echo "FOUND"; ln -sfT "$(readlink -f "$_dir/$1")" "$srcdir/$1"; break; fi
  done
  if [ ! -e "$srcdir/$1" ]; then return 1; fi
}
