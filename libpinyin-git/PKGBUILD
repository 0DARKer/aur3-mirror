# Maintainer: Yangtse <yangtsesu@gmail.com>
#Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin-git
_gitname=libpinyin
pkgver=1226.4cebaa9
pkgrel=1
pkgdesc="Library to deal with pinyin."
arch=('i686' 'x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('db' 'glib2')
provides=('libpinyin')
conflicts=('libpinyin')
source=('git+https://github.com/libpinyin/libpinyin.git'
	'http://downloads.sourceforge.net/libpinyin/models/model6.text.tar.gz')
noextract=("model6.text.tar.gz")
md5sums=('SKIP'
         '4ca306358b4b5e3d0f6bb2133c59b6bd')
options=(!libtool)

pkgver() {
  cd $_gitname
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd $_gitname

  cp "${srcdir}/model6.text.tar.gz" "${srcdir}/${_gitname}/data/"
  sed -i '/wget.*model6\.text\.tar\.gz/ d' ${srcdir}/${_gitname}/data/Makefile.am
  
  aclocal && libtoolize --force && autoheader && automake -a && autoconf
  ./configure --prefix=/usr && make
}

package() {
  cd $_gitname
  make DESTDIR=${pkgdir} install
}
