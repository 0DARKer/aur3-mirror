pkgname=totd
pkgver=1.5.1
pkgrel=6
pkgdesc="Totd (Trick Or Treat Daemon) is DNS-proxy for IPv4/IPv6 translation (DNS-ALG)"
arch=('x86_64' 'i686')
url="http://www.dillema.net/software/totd.html"
license=('BSD')
groups=(network)
depends=('glibc')
optdepends=(socat)
backup=(etc/totd.conf)
install=totd.install
source=("http://www.dillema.net/software/$pkgname/$pkgname-$pkgver.tar.gz"
        Makefile.in.patch
        totd.conf
        rc-totd
        totd.service
        totd.tmpfiles)
md5sums=('7edaedae9f6aca5912dd6c123582cf08'
         'd2700ca1d451747d937fd3c1a3cf465f'
         'ff07c559419f8b7ee05351182a140483'
         '31cf603d6b87cb8b0f8a1eb287b09c5d'
         'bc4ba66a49dff88fa5ee3ab65a6a57ff'
         '07b927909c9c440f15552ddf1a5ff56e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/Makefile.in.patch"
  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # prepare directories
  mkdir -p ${pkgdir}/usr/{sbin,man/man8}

  # build program
  make DESTDIR="$pkgdir" install

  # install default config
  install -D -m664 "$srcdir/totd.conf" "$pkgdir/etc/totd.conf"

  # rc script
  install -D -m755 "$srcdir/rc-totd" "$pkgdir/etc/rc.d/totd"

  # systemd
  install -D -m644 "$srcdir/totd.service" "$pkgdir/usr/lib/systemd/system/totd.service"
  install -D -m644 "$srcdir/totd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/totd.conf"
}
# vim:set ts=2 sw=2 et:
