# Maintainer: al.janitor <al.janitor [at] sdf [dot] org>
# Please report non-janitorial issues to the submitter: https://github.com/jojosch/pkgbuilds

pkgname=metasploit-git
pkgver=20140804.25726.282633f
pkgrel=1
pkgdesc="A development platform for creating security tools and exploits."
arch=('any')
url='http://metasploit.com/'
license=('BSD')
depends=('ruby1.9' 'ruby-msgpack' 'postgresql-libs')
makedepends=('git')
provides=('metasploit')
conflicts=('metasploit')
options=('!strip')
install=$pkgname.install
optdepends=('java-runtime: msfgui' 'ruby-pg: database support')
source=('metasploit-git::git+https://github.com/rapid7/metasploit-framework.git')
sha1sums=('SKIP')

pkgver () {
  cd "$srcdir/$pkgname"
  _date=$(date +"%Y%m%d")
  echo "$_date.$(git rev-list --count master).$(git rev-parse --short master)"
}

package() {
  cd $pkgdir

  mkdir -p usr/{bin,src}

  cp -R $srcdir/$pkgname usr/src/$pkgname

### Uncomment these if you know what you're doing
#  rm -R usr/src/$pkgname/.git
#  rm usr/src/$pkgname/msfupdate

  install -Dm644 usr/src/$pkgname/COPYING usr/share/licenses/$pkgname/COPYING
  install -Dm644 usr/src/$pkgname/LICENSE usr/share/licenses/$pkgname/LICENSE

  cd usr/bin

  for f in ../src/$pkgname/msf*; do
    ln -s $f .
  done
}
