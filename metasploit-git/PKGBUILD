# Maintainer: Johannes Schleifenbaum <johannes [at] js-webcoding [dot] de>

pkgname=metasploit-git
pkgver=20131118.21595.36db6a4
pkgrel=1
pkgdesc="A development platform for creating security tools and exploits."
arch=('any')
url='http://metasploit.com/'
license=('BSD')
depends=('ruby' 'ruby-msgpack' 'postgresql-libs')
makedepends=('git')
provides=('metasploit')
conflicts=('metasploit')
options=('!strip')
install=$pkgname.install
optdepends=('java-runtime: msfgui' 'ruby-pg: database support')
source=('metasploit-git::git+https://github.com/rapid7/metasploit-framework.git')
sha1sums=('SKIP')

pkgver () {
  cd "$srcdir/$pkgname"
  _date=`date +"%Y%m%d"`
  echo "$_date.$(git rev-list --count master).$(git rev-parse --short master)"
}

package() {
  cd $pkgdir

  mkdir -p usr/{bin,src}

  cp -R $srcdir/$pkgname usr/src/$pkgname
  rm -R usr/src/$pkgname/.git
  rm usr/src/$pkgname/msfupdate

  install -Dm644 usr/src/$pkgname/COPYING usr/share/licenses/$pkgname/COPYING
  install -Dm644 usr/src/$pkgname/LICENSE usr/share/licenses/$pkgname/LICENSE

  cd usr/bin

  for f in ../src/$pkgname/msf*; do
    ln -s $f .
  done
}
