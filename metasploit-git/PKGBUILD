# Maintainer: Babken Vardanyan - axper <483ken 4t gma1l
# Contributor: MrZYX from Arch Linux Forums
# Contributor: Sabart Otto - Seberm <seberm[at]seberm[dot]com>
# Contributor: Tobias Veit - nIcE <m.on.key.tobi[at]gmail[dot]com>
# Contributor: al.janitor <al.janitor [at] sdf [dot] org>

pkgname=metasploit-git
pkgver=2014120301.r262.g4f3ac43
pkgrel=1
pkgdesc='An advanced platform for using exploits (git version)'
arch=('i686' 'x86_64')
url='http://www.metasploit.com/'
license=('BSD')
depends=('ruby1.9' 'ruby1.9-bundler' 'postgresql-libs' 'sqlite')
makedepends=('git')
provides=('metasploit')
conflicts=('metasploit')
options=('!strip')
source=("$pkgname::git+https://github.com/rapid7/metasploit-framework.git")
md5sums=('SKIP')
PKGEXT='.pkg.tar'

pkgver() {
  cd "$pkgname"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${pkgname}"
  bundle-1.9 install --deployment
}

package() {
  install -Dm644 "${srcdir}/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/${pkgname}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  mkdir -p "${pkgdir}/usr/share" "${pkgdir}/usr/bin"

  mv "${srcdir}/${pkgname}" "${pkgdir}/usr/share/${pkgname}"

  # The files in `robots` gem have incorrect permissions and are not
  # users/groups other than root.
  # See the bug report: https://github.com/fizx/robots/issues/6
  find "${pkgdir}/usr/share/${pkgname}/vendor/bundle/ruby/1.9.1/gems/robots-0.10.1" -type f -exec chmod 644 {} \;

  mkdir -p "${pkgdir}/usr/share/${pkgname}/log"

  for f in ${pkgdir}/usr/share/${pkgname}/msf*; do
    local _msffile="${pkgdir}/usr/bin/`basename "${f}"`"
    echo "ruby-1.9 /usr/share/${pkgname}/`basename "${f}"` \"\$@\"" > "${_msffile}"
    chmod 755 $_msffile
  done
}
# vim:set ts=2 sw=2 et:
