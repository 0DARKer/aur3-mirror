pkgname=mingw-w64-libsodium
pkgver=0.1
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl, the Networking and Cryptography library (mingw-w64)"
arch=(any)
url="https://github.com/jedisct1/libsodium"
license=("Public Domain")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("http://download.dnscrypt.org/libsodium/releases/libsodium-$pkgver.tar.gz")
md5sums=('a46aa749dac39f41212cd8fd7e50fd4e')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

_optimal_make_jobs() {
  if [ -r /proc/cpuinfo ]; then
    local core_count=$(grep -Fc processor /proc/cpuinfo)
  else
    local core_count=0
  fi
  if [ $core_count -gt 1 ]; then
    echo -n $[$core_count-1]
  else
    echo -n 1
  fi
}

build() {
  sed -i "s,Windows.h,windows.h,g" "$srcdir/libsodium-$pkgver/src/libsodium/randombytes/randombytes_sysrandom.c"
  sed -i "s,Windows.h,windows.h,g" "$srcdir/libsodium-$pkgver/src/libsodium/randombytes/randombytes_salsa20_random.c"
  sed -i "s,Wincrypt.h,wincrypt.h,g" "$srcdir/libsodium-$pkgver/src/libsodium/randombytes/randombytes_sysrandom.c"
  sed -i "s,Wincrypt.h,wincrypt.h,g" "$srcdir/libsodium-$pkgver/src/libsodium/randombytes/randombytes_salsa20_random.c"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make -j$(_optimal_make_jobs)
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
