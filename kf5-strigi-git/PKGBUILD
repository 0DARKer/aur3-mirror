# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kf5-strigi-git
pkgver=v0.7.8
pkgrel=1
pkgdesc='Fast crawling desktop search engine with Qt GUI'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('qt5-base' 'exiv2' 'libxml2' 'boost-libs')
makedepends=('git' 'cmake' 'boost')
conflicts=('kf5-strigi')
provides=('kf5-strigi')
source=(git://anongit.kde.org/strigi.git)
md5sums=('SKIP')

pkgver() {
  cd strigi
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd strigi
  git submodule update --init
}

build() {
  mkdir -p build
  cd build
  cmake ../strigi \
    -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_INOTIFY=ON \
    -DENABLE_LOG4CXX=OFF \
    -DENABLE_FAM=OFF \
    -DENABLE_CLUCENE=OFF \
    -DENABLE_CLUCENE_NG=OFF \
    -DENABLE_FFMPEG=OFF \
    -DENABLE_QT4=OFF
  make
}

package() {
  pushd build/libstreams
  make DESTDIR="${pkgdir}" install
  popd

  pushd build/libstreamanalyzer
  make DESTDIR="${pkgdir}" install
  popd

  pushd build/strigiclient/lib/searchclient/qtdbus
  make DESTDIR="${pkgdir}" install
  popd
  
  pushd build/strigiutils
  make DESTDIR="${pkgdir}" install
  popd
}
