# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: jiribb <jiribb@gmail.com>
# Contributor: David Spicer <azleifel at googlemail dot com>
# Contributor: Andrew Brouwers
# Contributor: ponsfoot @ AUR
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer: David Spicer <azleifel at gmail dot com>

pkgbase=handbrake-svn
pkgname=handbrake-svn
true && pkgname=('handbrake-svn' 'handbrake-cli-svn')
_svnmod=HandBrake
pkgver=5384
pkgrel=1
arch=('i686' 'x86_64')
url="http://handbrake.fr/"
license=('GPL')
makedepends=('automake-1.12' 'intltool' 'python2' 'gettext' 'subversion' 'wget' 'yasm')
source=('HandBrake::svn://svn.handbrake.fr/HandBrake/trunk')
md5sums=('SKIP')

pkgver() {
  cd "$SRCDEST/$_svnmod"
  svnversion
}

build() {
  cd "$srcdir"

  ln -sf /usr/bin/automake-1.12 ./automake
  ln -sf /usr/bin/aclocal-1.12  ./aclocal
  PATH="$srcdir:$PATH"

  cd "$srcdir/$_svnmod"

  # Substitutions for python2
  sed -i -e 's|python $(srcdir)|python2 $(srcdir)|' "gtk/src/Makefile.am"
  sed -i -e '1c#! /usr/bin/python2' "gtk/src/makedeps.py"
  sed -i -e '1c#! /usr/bin/python2' "gtk/src/create_resources.py"
  sed -i -e '1c#! /usr/bin/python2' "gtk/src/quotestring.py"

  # Use --disable-gtk-update-checks because we don't want to download gui updates
  ./configure --force --verbose --prefix=/usr --disable-gtk-update-checks --launch-jobs=0

  cd "build"

  # Create build-specific temporary directory for ffmpeg
  install -d -m755 "ffmpeg_tmp"

  TMPDIR="$srcdir/$_svnmod/build/ffmpeg_tmp" make
}

package_handbrake-svn() {
  pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (GUI version)"
  optdepends=('gst-plugins-base: For Preview Window'
              'gst-plugins-bad: For Preview Window'
              'gst-plugins-good: For Preview Window'
              'gst-plugins-ugly: For Preview Window'
              'gst-libav: For Preview Window') 
  provides=('handbrake')
  conflicts=('handbrake')
  install=${pkgname}.install

  cd "$srcdir/$_svnmod/build"
  make DESTDIR="$pkgdir/" install

  rm "$pkgdir/usr/bin/HandBrakeCLI"

  # Delete icon-theme.cache (which is rebuilt on install)
  rm -f "$pkgdir/usr/share/icons/hicolor/icon-theme.cache"
}

package_handbrake-cli-svn() {
  pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (CLI version)"
  depends=('libass' 'libsamplerate' 'libtheora' 'libxml2')
  provides=('handbrake-cli')
  conflicts=('handbrake-cli')

  install -D -m755 "$srcdir/$_svnmod/build/HandBrakeCLI" "$pkgdir/usr/bin/HandBrakeCLI"
}

# Global pkgdesc and depends are here so that they will be picked up by AUR
pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter"
depends=('dbus-glib' 'desktop-file-utils' 'gst-plugins-base-libs' 'gtk3' 'hicolor-icon-theme' 'libass' 'libnotify' 'libsamplerate' 'libtheora')

