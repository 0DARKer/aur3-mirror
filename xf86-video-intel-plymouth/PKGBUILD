# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel-plymouth
_pkgname=xf86-video-intel
pkgver=2.14.0
pkgrel=1
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(intel-dri libxvmc libpciaccess libdrm xcb-util libxfixes udev)
makedepends=('xorg-server-devel' 'libx11' 'libdrm' 'xf86driproto' 'glproto' 'mesa' 'libxvmc' 'xcb-util')
conflicts=('xorg-server<1.10.0' 'xf86-video-i810' 'xf86-video-intel-legacy' 'xf86-video-intel')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
provides=(xf86-video-intel=${pkgver})
source=(${url}/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2
        copy-fb.patch
        intel-2.11.0-vga-clock-max.patch
        intel-2.14.0-backports.patch)
sha1sums=('103193a01b9c29d6f71a620ad99c6e1495276e68'
          '2d74522b1e8b8466f229216358fe0ab9e651c081'
          '299a5571c3573f69a05b4e0f64c5732c28edeaf2'
          'cc5be0c128dce43641fb14b0377384e3f270c3f5')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/copy-fb.patch"
  patch -Np1 -i "${srcdir}/intel-2.11.0-vga-clock-max.patch"
  patch -Np1 -i "${srcdir}/intel-2.14.0-backports.patch"

  autoreconf -v --install
  ./configure --prefix=/usr --enable-dri
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
