# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libdvdcss-git
pkgver=20130313.7381c95
pkgrel=1
epoch=1
pkgdesc="A portable abstraction library for DVD decryption. (GIT version)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.videolan.org/libdvdcss"
makedepends=('git')
options=('!libtool')
provides=('libdvdcss')
conflicts=('libdvdcss')
source=('git://git.videolan.org/libdvdcss.git#commit=7381c95')
md5sums=('SKIP')
_gitname="libdvdcss"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

build() {
  cd "${_gitname}"

  autoreconf -if
  ./configure --prefix=/usr --disable-doc
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
