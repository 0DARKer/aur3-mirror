# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libdvdcss-git
pkgver=20130208
pkgrel=1
pkgdesc="A portable abstraction library for DVD decryption. (GIT version)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.videolan.org/libdvdcss"
makedepends=('git' 'texlive-latexextra' 'doxygen')
options=('!libtool')
provides=('libdvdcss')
conflicts=('libdvdcss')

_gitroot="git://git.videolan.org/libdvdcss.git"
_gitname="libdvdcss"

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."

  if [ -d "${_gitname}" ]; then
      cd "${_gitname}" && git pull
  else
      git clone --depth=1 "${_gitroot}" "${_gitname}"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -fr "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd  "${srcdir}/${_gitname}-build"

  # Fix for automake 1.13.x
  sed -e 's|AM_CONFIG_HEADER(config.h)|AC_CONFIG_HEADERS([config.h])|' -e '5a AC_CONFIG_MACRO_DIR([m4])' -i configure.ac
  sed 's|for v in 12|for v in 13 12|' -i bootstrap
  sed '2a ACLOCAL_AMFLAGS = -I m4' -i Makefile.am

  # Fix include path in libdvdcss.pc
  sed 's| -I${includedir}/@PACKAGE@||g' -i src/libdvdcss.pc.in
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/doc/libdvdcss"
  install -m644 doc/html/* "${pkgdir}/usr/share/doc/libdvdcss/"
}
