# Contributor: Thomas Dahms <thdahms@gmx.de>

pkgname='dwm-gtx-hg'
pkgver=1434
pkgrel=1
pkgdesc="The latest hg pull of dwm-gtx, a branch of dwm, with improved layouts and Xinerama support"
url="http://s01.de/~gottox/index.cgi/proj_dwm"
license='MIT'
arch=('i686' 'x86_64')
depends=('libx11')
makedepends=('mercurial' 'sed')
conflicts=('dwm' 'dwm-hg' 'dwm-gtx')
provides=('dwm' 'dwm-gtx')
source=()
md5sums=()

_hgroot='http://s01.de/~gottox/hg'
_hgrepo='dwm'

build() {
	cd $startdir/src

	# update the repo, else clone a new one
	if [ -d $_hgrepo ]; then
		cd $_hgrepo
		make clean
		hg pull -u
	else
		hg clone ${_hgroot}/${_hgrepo}
		cd $_hgrepo
	fi

	# include custom config.h, if it exists. Note that default
	# config.def.h will be used if not
        if [ -f $startdir/config.h ]; then
          cp $startdir/config.h .
        fi

        # add correct settings to config.mk
	sed -i "s|^PREFIX =.*|PREFIX = /usr|" config.mk
	sed -i "s|^X11INC =.*|X11INC = /usr/include/X11|" config.mk
	sed -i "s|^X11LIB =.*|X11LIB = /usr/lib/X11|" config.mk

	msg "Starting build process."
	make || return 1
	make PREFIX=$startdir/pkg/usr install

	mkdir -p $startdir/pkg/usr/share/licenses/dwm
	cp LICENSE $startdir/pkg/usr/share/licenses/dwm
}
