# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=xfce4-indicator-plugin
pkgver=0.5.0
pkgrel=1
pkgdesc="Plugin to display information from applications in the Xfce4 panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-indicator-plugin"
license=('GPL')
depends=('xfce4-panel' 'libindicator' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
optdepends=('indicator-application-gtk2: take menus from applications and place them in the panel'
            'indicator-sound-gtk2: unified sound menu')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
md5sums=('11476f8d4020e0737096b7403828063f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
