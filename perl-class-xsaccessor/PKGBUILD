# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname='perl-class-xsaccessor'
pkgver='1.19'
pkgrel='1'
_realname='Class-XSAccessor'
pkgdesc="Generate fast XS accessors without runtime compilation"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/dist/Class-XSAccessor'
source=("http://search.cpan.org/CPAN/authors/id/S/SM/SMUELLER/${_realname}-${pkgver}.tar.gz")
md5sums=('5c5dea74f00ad37c5119dd22b28a5563')
build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}
}

