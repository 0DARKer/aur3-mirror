# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-pam-git
pkgver=2.13.1.ge5c7725
pkgrel=1
pkgdesc="Yubico yubikey PAM module"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubico-pam"
license=('BSD')
depends=('pam' 'yubico-c-client-git' 'yubikey-personalization-git')
makedepends=('git')
provides=('pam_yubico' 'yubico-pam')
conflicts=('pam_yubico' 'yubico-pam')
source=('git://github.com/Yubico/yubico-pam.git'
	'yubico-pam-debug.patch')

pkgver() {
	cd yubico-pam/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd yubico-pam/

	# silence errors on missing key, etc. (on show with debug)
	patch -Np1 < ${srcdir}/yubico-pam-debug.patch

	autoreconf --install
	./configure --prefix=/usr
	make
}

check() {
	cd yubico-pam/

	make check
}

package() {
	cd yubico-pam/
  
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/yubico-pam/COPYING"
	install -D -m644 README "${pkgdir}/usr/share/doc/yubico-pam/README"
	make DESTDIR="${pkgdir}/" install
}

sha256sums=('SKIP'
            'f224d411a895347c25ab350e0c97f774c12d502d349f08f8c3495d1573d35f51')
