# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-pam-git
pkgver=20120614
pkgrel=1
pkgdesc="Yubico yubikey PAM module"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubico-pam"
license=('BSD')
depends=('pam' 'yubico-c-client-git' 'yubikey-personalization-git')
makedepends=('git')
provides=('pam_yubico' 'yubico-pam')
conflicts=('pam_yubico' 'yubico-pam')

_gitroot="git://github.com/Yubico/yubico-pam.git"
_gitname="yubico-pam"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  autoreconf --install
  ./configure --prefix=/usr
  make
  make check
}

package() {
  cd "${srcdir}/${_gitname}-build"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  make DESTDIR="${pkgdir}/" install
}

