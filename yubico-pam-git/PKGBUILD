# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-pam-git
pkgver=20120610
pkgrel=1
pkgdesc="Yubico yubikey PAM module"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubico-pam"
license=('BSD')
depends=('pam' 'yubico-c-client-git' 'yubikey-personalization-git')
makedepends=('git')
provides=('pam_yubico')
conflicts=('pam_yubico')
source=('memcmp.patch')

_gitroot="git://github.com/Yubico/yubico-pam.git"
_gitname="yubico-pam"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # fix compilation
  patch -Np1 < ${srcdir}/memcmp.patch

  autoreconf --install
  ./configure --prefix=/usr
  make
  make check
}

package() {
  cd "${srcdir}/${_gitname}-build"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  make DESTDIR="${pkgdir}/" install
}

sha256sums=('1b5e474bef65e9cff88bb1ac5326aaf2ba16e45188c910229c7d3c41713f2ccc')
