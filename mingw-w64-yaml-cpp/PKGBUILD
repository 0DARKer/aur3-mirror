# Maintainer: Daniel Kirchner <daniel at ekpyron dot org>
pkgname=mingw-w64-yaml-cpp
_basename=yaml-cpp
pkgver=0.5.1
pkgrel=1
pkgdesc="YAML parser and emitter in C++, written around the YAML 1.2 spec (mingw-w64)"
license=('MIT')
arch=('any')
url="http://code.google.com/p/yaml-cpp/"
depends=('mingw-w64-boost' 'mingw-w64-crt')
makedepends=('cmake' 'mingw-w64-gcc' 'mercurial')
source=(http://yaml-cpp.googlecode.com/files/${_basename}-${pkgver}.tar.gz)
options=(!strip !buildflags)
sha512sums=('3c6928684d603815c016d663af36be94507f2cccf167d6d8d7cd7dea3ea5f73ec88d62952a2b5d11796e40132857afcbbacd9eafd688f2dc11d0c339caf2e013')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS

  for _arch in ${_architectures}; do
    rm -rf "${srcdir}/build-${_arch}"
    mkdir "${srcdir}/build-${_arch}"
    cd "${srcdir}/build-${_arch}"
    
    echo "SET(CMAKE_SYSTEM_NAME Windows)" > win32.cmake
    echo "SET(CMAKE_C_COMPILER ${_arch}-gcc)" >> win32.cmake
    echo "SET(CMAKE_CXX_COMPILER ${_arch}-g++)" >> win32.cmake
    echo "SET(CMAKE_RC_COMPILER ${_arch}-windres)" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH /usr/${_arch})" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)" >> win32.cmake
    cmake ../$_basename-$pkgver \
      -DCMAKE_TOOLCHAIN_FILE=win32.cmake \
      -DCMAKE_INSTALL_PREFIX="/usr/${_arch}"
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    make install DESTDIR="${pkgdir}"
    rm -rf "${pkgdir}/usr/${_arch}/bin"
	${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
  done
}
