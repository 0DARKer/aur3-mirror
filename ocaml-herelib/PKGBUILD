# Maintainer: Aaron Chen <nextAaron at gmail.com>

_pkgname=herelib
pkgname=ocaml-$_pkgname
pkgver=109.15.00
pkgrel=1
pkgdesc="Syntax extension for inserting the location"
arch=('i686' 'x86_64')
# LGPL + linking exception
license=('LGPL')
depends=('ocaml' 'ocaml-type_conv>=109.15.00')
makedepends=('ocaml-findlib')
url="http://janestreet.github.com"
source=("https://ocaml.janestreet.com/ocaml-core/$pkgver/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('7bd90218e5560915d923810b734a6515')
options=(!strip !makeflags)

build(){
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package(){
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
