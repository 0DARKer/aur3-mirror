# Maintainer: bebehei <bebe@bebehei.de>
# Based on the icinga-PKGBUILD from the AUR

pkgname=icinga2
pkgver=2.1.1
pkgrel=3
pkgdesc="An open source host, service and network monitoring program."
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.icinga.org"
depends=('boost')
makedepends=('cmake' 'python-setuptools' 'libmariadbclient' 'postgresql-libs')
optdepends=('monitoring-plugins: plugins needed for icinga checks')
source=("https://github.com/Icinga/$pkgname/archive/v$pkgver.tar.gz"
        "icinga2.install")
sha256sums=('57755272a32f6286632f07153225bb066cae07affe8731d4628cef7695de74a2'
            'faf6f39565aaa6b0ffefd475e11dba3be885e8da182e9402dbbd1362a3752330')
install='icinga2.install'

#if you change this, change the variable in icinga2.install too!
_icinga_user="icinga"
_icinga_group="icinga"

build() {
	cd "$srcdir/$pkgname-$pkgver"

	export _tmp_build_dir=`mktemp -dp $srcdir`

	cd $_tmp_build_dir

	msg "Building in directory $_tmp_build_dir"

	cmake $srcdir/$pkgname-$pkgver \
		-DCMAKE_INSTALL_PREFIX=/usr/ \
		-DICINGA2_USER=${_icinga_user} \
		-DICINGA2_GROUP=${_icinga_group} \
		-DICINGA2_COMMAND_USER=icinga \
		-DICINGA2_COMMAND_GROUP=icingacmd \
		-DICINGA2_PLUGINDIR=/usr/share/nagios/libexec \
		-DCMAKE_INSTALL_SYSCONFDIR=/etc/ \
		-DCMAKE_INSTALL_LOCALSTATEDIR=/var \
		-DICINGA2_SYSCONFIGFILE=/etc/default/icinga2 \
		-DUSE_SYSTEMD=ON
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	cd $_tmp_build_dir
	make DESTDIR="$pkgdir" install

	rm -r $pkgdir/var
	mv $pkgdir/etc/icinga2 \
		$pkgdir/usr/share/icinga2/sampleconfig

	mkdir $pkgdir/etc/tmpfiles.d
	cat > $pkgdir/etc/tmpfiles.d/icinga2.conf <<- EOF
	d /run/icinga2 0750 ${_icinga_user} ${_icinga_group} - -"
	EOF
}
