# Maintainer: bebehei <bebe@bebehei.de>

pkgname=icinga2
pkgver=2.2.2
pkgrel=1
pkgdesc="An open source host, service and network monitoring program."
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.icinga.org"
depends=('boost')
makedepends=('cmake' 'python-setuptools' 'libmariadbclient' 'postgresql-libs')
optdepends=('monitoring-plugins: plugins needed for icinga checks')
source=("https://github.com/Icinga/$pkgname/archive/v$pkgver.tar.gz"
        "icinga2.install")
sha256sums=('b04f49b8083d4507b4a3902843cd8d29a655833327706b390bf2a9c060d96caa'
            '466358b6d199f85ba61906a71f0bb932160ac8e1e81cab0c95466e42651f0e5b')
install='icinga2.install'

#if you change this, change the variable in icinga2.install too!
_icinga_user="icinga"
_icinga_group="icinga"
_icinga_cmd="${_icinga_group}cmd"

build() {
	cd "$srcdir/$pkgname-$pkgver"

	export _tmp_build_dir=`mktemp -dp $srcdir`

	cd $_tmp_build_dir

	msg "Building in directory $_tmp_build_dir"

	cmake $srcdir/$pkgname-$pkgver \
		-DCMAKE_INSTALL_PREFIX=/usr/ \
		-DICINGA2_USER=${_icinga_user} \
		-DICINGA2_GROUP=${_icinga_group} \
		-DICINGA2_COMMAND_USER=${_icinga_user} \
		-DICINGA2_COMMAND_GROUP=${_icinga_cmd} \
		-DICINGA2_PLUGINDIR=/usr/lib/monitoring-plugins/ \
		-DCMAKE_INSTALL_SYSCONFDIR=/etc/ \
		-DCMAKE_INSTALL_LOCALSTATEDIR=/var \
		-DICINGA2_SYSCONFIGFILE=/etc/default/icinga2 \
		-DUSE_SYSTEMD=ON
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	cd $_tmp_build_dir
	make DESTDIR="$pkgdir" install

	rm -r $pkgdir/var
	mv $pkgdir/etc/icinga2 \
		$pkgdir/usr/share/icinga2/sampleconfig

	mkdir $pkgdir/etc/tmpfiles.d
	cat > $pkgdir/etc/tmpfiles.d/icinga2.conf <<- EOF
	d /run/icinga2 0750 ${_icinga_user} ${_icinga_group} - -"
	EOF
}
