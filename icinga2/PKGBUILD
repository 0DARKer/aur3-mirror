# Maintainer: bebehei <bebe@bebehei.de>

pkgname=icinga2
pkgver=2.2.3
pkgrel=1
pkgdesc="An open source host, service and network monitoring program."
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.icinga.org"
depends=('boost')
makedepends=('cmake' 'python-setuptools' 'libmariadbclient' 'postgresql-libs')
optdepends=('monitoring-plugins: plugins needed for icinga checks')
source=("https://github.com/Icinga/$pkgname/archive/v$pkgver.tar.gz"
        "icinga2.install")
sha256sums=('1cd6eebc5f9427d03ed12f188f73af0fe26b5b34f2bbdf29fe31024a0d8846b1'
            '466358b6d199f85ba61906a71f0bb932160ac8e1e81cab0c95466e42651f0e5b')

install='icinga2.install'

#if you change this, change the variable in icinga2.install too!
_icinga_user="icinga"
_icinga_group="icinga"
_icinga_cmd="${_icinga_group}cmd"

build() {
	mkdir -p "$srcdir/build"
	cd "$srcdir/build"

	cmake $srcdir/$pkgname-$pkgver \
		-DCMAKE_INSTALL_PREFIX=/usr/ \
		-DICINGA2_USER=${_icinga_user} \
		-DICINGA2_GROUP=${_icinga_group} \
		-DICINGA2_COMMAND_USER=${_icinga_user} \
		-DICINGA2_COMMAND_GROUP=${_icinga_cmd} \
		-DICINGA2_PLUGINDIR=/usr/lib/monitoring-plugins/ \
		-DCMAKE_INSTALL_SYSCONFDIR=/etc/ \
		-DCMAKE_INSTALL_LOCALSTATEDIR=/var \
		-DICINGA2_SYSCONFIGFILE=/etc/default/icinga2 \
		-DUSE_SYSTEMD=ON
	make
}

package() {
	# Fix for pacman 4.2 don't know how to resolve it
	mkdir -p /usr/{lib,bin}
	cd "$pkgdir/usr/"
	ln -s lib lib64
	ln -s bin sbin

	cd "$srcdir/build"

	make DESTDIR="$pkgdir" install

	rm -r $pkgdir/var
	mv $pkgdir/etc/icinga2 \
		$pkgdir/usr/share/icinga2/sampleconfig

	mkdir $pkgdir/etc/tmpfiles.d
	cat > $pkgdir/etc/tmpfiles.d/icinga2.conf <<- EOF
	d /run/icinga2 0750 ${_icinga_user} ${_icinga_group} - -"
	EOF
	
	# Fix for pacman 4.2 don't know how to resolve it
	rm $pkgdir/usr/{sbin,lib64}
}
