# Maintainer: bebehei <bebe@bebehei.de>
# Based on the icinga-PKGBUILD from the AUR

pkgname=icinga2
pkgver=2.1.0
pkgrel=2
pkgdesc="An open source host, service and network monitoring program."
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.icinga.org"
depends=('boost')
makedepends=('cmake' 'python-setuptools' 'libmariadbclient' 'postgresql-libs')
optdepends=('monitoring-plugins: plugins needed for icinga checks')
source=("https://github.com/Icinga/$pkgname/archive/v$pkgver.tar.gz"
        "icinga2.install")
sha256sums=('6aad43c69385db3463ca0a3f14f7373d03a967451eb976a89955d1c99f9300e6'
            'faf6f39565aaa6b0ffefd475e11dba3be885e8da182e9402dbbd1362a3752330')
install='icinga2.install'

#if you change this, change the variable in icinga2.install too!
_icinga_user="icinga"
_icinga_group="icinga"
_tmp_build_dir="icinga_for_arch"

build() {
	cd "$srcdir/$pkgname-$pkgver"

	if [[ ! -d $_tmp_build_dir ]]; then
		mkdir $_tmp_build_dir
	fi
	cd $_tmp_build_dir

	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr/ \
		-DICINGA2_USER=${_icinga_user} \
		-DICINGA2_GROUP=${_icinga_group} \
		-DICINGA2_COMMAND_USER=icinga \
		-DICINGA2_COMMAND_GROUP=icingacmd \
		-DICINGA2_PLUGINDIR=/usr/share/nagios/libexec \
		-DCMAKE_INSTALL_SYSCONFDIR=/etc/ \
		-DCMAKE_INSTALL_LOCALSTATEDIR=/var \
		-DICINGA2_SYSCONFIGFILE=/etc/default/icinga2 \
		-DUSE_SYSTEMD=ON
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	cd $_tmp_build_dir
	make DESTDIR="$pkgdir" install

	rm -r $pkgdir/var
	mv $pkgdir/etc/icinga2 \
		$pkgdir/usr/share/icinga2/sampleconfig

	mkdir $pkgdir/etc/tmpfiles.d
	cat > $pkgdir/etc/tmpfiles.d/icinga2.conf <<- EOF
	d /run/icinga2 0750 ${_icinga_user} ${_icinga_group} - -"
	EOF
}
