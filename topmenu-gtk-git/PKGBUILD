# $Id: PKGBUILD 111946 2014-05-27 08:42:58Z jgc $
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=topmenu-gtk-git
_gitname=topmenu-gtk
pkgver=2014.12.21
pkgrel=1
pkgdesc="A standalone global menu bar for Gtk+ 2.0 based desktop environments."
url="https://gitorious.org/topmenu"
arch=('i686' 'x86_64')
license=('LGPL3')
depends=('mate-panel')
makedepends=('git')
optdepends=('topmenu-gtk3-git: for gtk3 apps menu'
            'topmenu-qt-git: for qt apps menu')
groups=('mate-extra')
options=(!libtool)
source=("git://gitorious.org/topmenu/$_gitname.git")
sha1sums=('SKIP')
install=$_gitname.install

pkgver() {
   cd "$_gitname"
   git log -1 --format="%cd" --date=short | sed 's|-|.|g'
}

build() {
   cd "${srcdir}/${_gitname}"

   autoreconf -fi
   ./configure --prefix=/usr --with-gtk=2 --with-wnck=wnck1 --disable-static \
      --enable-mate-applet --libexecdir=/usr/lib/topmenu
   make
}

package() {
    cd "${srcdir}/${_gitname}"
    make DESTDIR="${pkgdir}" install
}
