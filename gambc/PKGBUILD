# Contributor: Philipp Br√ºschweiler <blei42@gmail.com>
# Contributor: Sebastian A. Liem <sebastian@liem.se>
# Contributor: gravicappa 
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=gambc
pkgver=4.6.0
pkgrel=2
pkgdesc="A Scheme interpreter and a optimized compiler which generates \
portable C code"
url="http://www.iro.umontreal.ca/~gambit/"
depends=('sh')
install=gambc.install
makedepends=('texinfo')
arch=('i686' 'x86_64')
license=('LGPL' 'APACHE')
options=(libtool)
source=(http://www.iro.umontreal.ca/~gambit/download/gambit/v${pkgver%??}/source/${pkgname}-v${pkgver//\./_}.tgz)
md5sums=('4f0e8b3e90a96f2203cbaf1e1cc1388a')

build() {
  cd $srcdir/$pkgname-v${pkgver//\./_}

  ./configure \
    --prefix=/usr --infodir=/usr/share/info \
    --libdir=/usr/lib/gambit-c --includedir=/usr/include \
    --docdir=/usr/share/doc/$pkgname --bindir=/usr/bin \
    --enable-gcc-opts --enable-single-host \
    --enable-compiler-name=gambit-gsc --without-x || return 1

  (make && make doc) || return 1

  make DESTDIR=$pkgdir install || return 1
  
  rm -rf $pkgdir/usr/current || return 1
  gzip -9r $pkgdir/usr/share/info
}
