# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: dorphell <dorphell@archlinux.org>
pkgname=lib32-libao
pkgver=1.1.0
pkgrel=1
pkgdesc="A cross-platform audio output library and plugins (32-bit)"
url="http://www.xiph.org/ao/"
arch=('x86_64')
license=('GPL')
depends=('libao' 'lib32-glibc' 'lib32-alsa-lib' 'lib32-libpulse')
makedepends=('gcc-multilib')
groups=('lib32')
options=('!libtool')
source=("http://downloads.xiph.org/releases/ao/libao-$pkgver.tar.gz")
md5sums=('2b2508c29bc97e4dc218fa162cf883c8')

build() {
  cd "$srcdir/libao-$pkgver"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC="gcc -m32" \
     PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
}

package() {
  cd "$srcdir/libao-$pkgver"
  make DESTDIR=$pkgdir/ install
  rm -rf "$pkgdir/usr/share" "$pkgdir/usr/include"
}
# vim:set ts=2 sw=2 et:
