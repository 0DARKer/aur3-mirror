# Maintainer: flan_suse < windows2linux AT zoho DOT com >

# This package does not compile the GTK1 version.
# It only compiles and builds the CLI and GTK2 versions.
# This means: 
#        * fewer makedeps to pull in
#        * shorter compile time
#        * smaller package size

pkgname=unison-232
_pkgname=unison
pkgver=2.32.52
_pkgver=232
pkgrel=3
pkgdesc="File-synchronization tool (legacy 2.32 version)"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.cis.upenn.edu/~bcpierce/unison"
provides=('unison')
optdepends=('gtk2: for the GTK2 frontend')
makedepends=('ocaml' 'lablgtk2' 'imagemagick')
source=("http://www.cis.upenn.edu/~bcpierce/$_pkgname/download/releases/$_pkgname-$pkgver/$_pkgname-$pkgver.tar.gz"
        "$_pkgname-ssh-ocaml.patch"
        "$_pkgname-$_pkgver.desktop")
md5sums=('0701f095c1721776a0454b94607eda48'
         '81a28349471434c75a4206d07ad21eb1'
         '07bcf2e70e87fef5b91b929c6865777a')
options=(!makeflags)

build() {
  # Move into the source directory
  cd $srcdir/$_pkgname-$pkgver
  CFLAGS=""

  # Apply SSH Ocaml patch
  patch -Np1 -i "../$_pkgname-ssh-ocaml.patch"

  # Compile Unison 2.32 command-line version
  make clean
  make UISTYLE=text DEBUGGING=false THREADS=true
  mv "$_pkgname" "$_pkgname-cli"

  # Compile Unison 2.32 GTK2 graphical version
  make clean
  make UISTYLE=gtk2 DEBUGGING=false THREADS=true
  mv "$_pkgname" "$_pkgname-gtk2"

  # Create a .png icon to be used in the menu
  convert "win32rc/U.ico" "$_pkgname-$_pkgver.png"

}

package() {

  # Move into the source directory
  cd $srcdir/$_pkgname-$pkgver

  # Install the compiled binary files
  install -Dm755 "$_pkgname-cli" "$pkgdir/usr/bin/$_pkgname-$_pkgver"
  install -Dm755 "$_pkgname-gtk2" "$pkgdir/usr/bin/$_pkgname-$_pkgver-gtk2"

  # Make a symbolic link named "unison-x11-232" that points to "unison-gtk2-232"; not required, but good for scripting / compatibility reasons
  ln -s "$pkgdir/usr/bin/$_pkgname-$_pkgver-gtk2" "$pkgdir/usr/bin/$_pkgname-$_pkgver-x11"

  # Install the icon to be used with the menu
  install -Dm644 "$_pkgname-$_pkgver-3.png" "$pkgdir/usr/share/pixmaps/$_pkgname-$_pkgver.png"

  # Install the .desktop menu file, which can launch the GTK2 version
  install -Dm644 "../$_pkgname-$_pkgver.desktop" "$pkgdir/usr/share/applications/$_pkgname-$_pkgver.desktop"

}
