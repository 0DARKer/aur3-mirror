# Maintainer: franzrogar <franzrogar at gmail dot com>
# Contributor: David Gowers <00ai99 at gmail dot com>
pkgname=fontforge-cvs
pkgver=20110225
pkgrel=1
pkgdesc="An outline and bitmap font editor"
arch=('i686' 'x86_64')
url="http://www.fontforge.org"
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python2' 'libjpeg' 'libpng')
options=('!libtool' '!makeflags')
makedepends=('cvs')
optdepends=('libspiro: bezier curves support')
conflicts=('fontforge')
provides=('fontforge-cvs')

_cvsroot=":pserver:anonymous@fontforge.cvs.sourceforge.net:/cvsroot/fontforge"
_cvsmod="${pkgname}-${pkgver}"

build() {
  cd "${srcdir}"

  msg "Updating CVS...."
  if [ -d fontforge/CVS ]; then
    cd fontforge
    cvs -z3 update -d || exit 1
  else
    cvs -z3 -d $_cvsroot co -f fontforge || exit 1
  fi

  msg "Creating build copy..."
  cp -rf "${srcdir}/fontforge" "${srcdir}/fontforge-build"

  msg "Patching files..."
  cd "${srcdir}/fontforge-build"
  sed -i 's/python /python2 /g' Makefile.dynamic.in
  export CFLAGS="${CFLAGS// -O2 / -O1 }" # on i686, -O2 yields FS#20430

  msg "Starting make..."
  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --enable-type3 \
      --enable-double \
      --enable-devicetables \
      --with-regular-link \
      --with-python=python2 \
      --enable-pyextension
  make
}

package(){
  msg "Building package..."
  cd "${srcdir}/fontforge-build"
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
