# Maintainer: Sarkasper <echo a2FzcGVyLm1lbnRlbkBnbXguY29tCg== | base64 -d>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord-unsane
realname=colord
pkgver=0.1.21
pkgrel=1
pkgdesc="Color daemon without sane dependency"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lcms2' 'libgusb' 'polkit' 'shared-color-profiles' 'sqlite' 'udev')
makedepends=('intltool' 'gobject-introspection' 'vala' 'docbook2x')
source=($url/releases/$realname-$pkgver.tar.xz)
options=('!libtool')
sha1sums=('dbf981beec70e81c45cf46b150f426fc1eb56c24')
provides=('colord')
conflicts=('colord' 'colord-nopolkit')

build() {
  cd "$realname-$pkgver"

  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure

  ./configure --prefix=/usr \
    --sysconfdir=/etc --libexecdir=/usr/lib/colord \
    --localstatedir=/var --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd "$realname-$pkgver"
  make DESTDIR="$pkgdir" install \
    bashcompletiondir=/usr/share/bash-completion/completions
}

# vim:set ts=2 sw=2 et:
