# Maintainer: Sam S. <smls75@gmail.com>

pkgname=dustforce-hib
pkgver=1.0.20120917
_hibver=1347954459
pkgrel=0
pkgdesc='A fast-paced 2D platformer game (Humble Bundle version)'
url='http://dustforce.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
makedepends=('coreutils')
depends=('mesa' 'sdl' 'libxau' 'libxdamage' 'libxdmcp' 'libxinerama' 'freealut'
         'fontconfig' 'libidn' 'libvorbis' 'zlib')
source=('dustforce-hib.desktop')
md5sums=('fa688d289f57cd54eabde9e7ef354407')
PKGEXT='.pkg.tar'

_installer="dustforce-linux-${_hibver}.sh"
_installer_md5='afaca0c6241904becb66a29821f4dce4'

package() {
  cd $srcdir

  # Get installer
  _get_local_source "${_installer}" --md5 "${_installer_md5}" || {
    error "Unable to find the game archive. Please download it from your Humble
           Bundle page, and place it into one of the above locations."
    exit 1; }

  # Execute installer
  msg "Starting setup..."
  [[ -d "./temp" ]] && rm -r "./temp"
  sh "./${_installer}" --unattended --no-register --packager pacman \
                       --keep --overwrite \
                       --target  "${srcdir}/temp" \
                       --bindir  "${srcdir}/bin" \
                       --datadir "${pkgdir}/opt"

  # Remove bundled libraries & helper binaries (use system packages instead)
  rm -rf "${pkgdir}/opt/Dustforce/lib64/lib"{SDL-1.2.so.0}
  rm -rf "${pkgdir}/opt/Dustforce/"{xdg-open,xdg-utils}

  # Install desktop entry
  install -Dm644 "${pkgname}.desktop" \
                 "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install icon
  install -Dm644 "${pkgdir}/opt/Dustforce/Dustforce.png" \
                 "${pkgdir}/usr/share/pixmaps/dustforce.png"

  # Install launcher symlink
  case $CARCH in i686) _arch=x86 ;; x86_64) _arch=x86_64;; esac
  chmod 755 "${pkgdir}/opt/Dustforce/Dustforce.bin.$_arch"
  install -d "${pkgdir}/usr/bin"
  ln -s "/opt/Dustforce/Dustforce.bin.${_arch}" "${pkgdir}/usr/bin/dustforce"
}


###### HELPER FUNCTIONS ######

# Locates a file or folder provided by the user, and symlinks it into $srcdir
_get_local_source() {
  msg "Looking for '$1'..."; rm -f "$srcdir/$1"
  declare -A _search=(['build dir']="$startdir"
                      ['$LOCAL_PACKAGE_SOURCES']="$LOCAL_PACKAGE_SOURCES")
  for _key in "${!_search[@]}"; do local _dir="${_search["$_key"]}"
    echo -n "    - in $_key [${_dir:-<undefined>}] ... ";
    if [[ -z "$_dir" || ! -e "$_dir/$1" ]]; then
      echo "NOT FOUND"
    elif [[ -n $2 && "$(${2#--}sum "$_dir/$1"|awk '{print $1}')" != $3 ]]; then
      echo "CHECKSUM FAILED";
    else
      echo "FOUND"; ln -sfT "$(readlink -f "$_dir/$1")" "$srcdir/$1"; break; fi
  done
  if [ ! -e "$srcdir/$1" ]; then return 1; fi
}
