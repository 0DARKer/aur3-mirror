# Maintainer: Sam S. <smls75@gmail.com>

pkgname=dustforce-hib
pkgver=1.0.20120917
_hibver=1347954459
pkgrel=0
pkgdesc='A fast-paced 2D platformer game (Humble Bundle version)'
url='http://dustforce.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('mesa' 'libgl' 'sdl' 'libxdamage' 'freealut' 'libidn' 'libvorbis'
         'libxft' 'xdg-utils')
PKGEXT='.pkg.tar'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

_installer="dustforce-linux-${_hibver}.sh"
source=("hib://${_installer}"
        'dustforce-hib.desktop')
md5sums=('afaca0c6241904becb66a29821f4dce4'
         'fa688d289f57cd54eabde9e7ef354407')

package() {
  cd $srcdir
  _installdir=/opt/Dustforce; _target=$pkgdir/$_installdir
  [ $CARCH == "i686" ] && _arch='x86' || _arch='x86_64'

  # Extract installer
  mkdir -p $_target
  while read line; do echo -n '.'; done < <(  # show progress as dots
    sh $_installer --tar xvf
    lzma -d < instarchive_linux_$_arch | tar xvf - -C $_target
    lzma -d < instarchive_all | tar xvf - -C $_target
    lzma -d < subarch | tar xvf -
    lzma -d < deps/cURL/cURL_files_linux_$_arch | tar xvf - -C ${_target}
  ); echo

  # Fix permissions
  find "${pkgdir}" -type f -exec chmod 644 "{}" +
  chown root:root -R "${pkgdir}"

  # Install desktop entry
  install -Dm644 "$pkgname.desktop" \
                 "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Install icon
  install -Dm644 "$_target/Dustforce.png" \
                 "$pkgdir/usr/share/pixmaps/dustforce.png"

  # Install launcher symlink
  case $CARCH in i686) _arch=x86 ;; x86_64) _arch=x86_64;; esac
  chmod 755 "$_target/Dustforce.bin.$_arch"
  install -d "$pkgdir/usr/bin"
  ln -s "$_installdir/Dustforce.bin.$_arch" "$pkgdir/usr/bin/dustforce"
}
