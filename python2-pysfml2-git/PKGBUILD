# Maintainer: Philippe Huerlimann <phihue@gmail.com>
pkgname=python2-pysfml2-git
pkgver=20110720
pkgrel=1
pkgdesc="A Python 2 binding for SFML 2, written with Cython."
arch=('i686' 'x86_64')
url="http://bastien-leonard.alwaysdata.net/pysfml/doc/index.html"
license=('ZLIB')
depends=('sfml-git' 'python2')
makedepends=('cython' 'git')
source=()
md5sums=()

_gitroot='https://github.com/bastienleonard/pysfml2-cython.git'
_gitname='pysfml2-cython'

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [ -d $_gitname ] ; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot $_gitname
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"

	python2 setup.py build
}

package() {
	cd "$srcdir/$_gitname-build"
	python2 setup.py install --root="${pkgdir}" --prefix=/usr

	# Copying the examples and tests
	cp -R examples "${pkgdir}/usr/lib/python2.7/site-packages/pysfml"
	cp -R tests/* "${pkgdir}/usr/lib/python2.7/site-packages/pysfml/tests"

	# Copying the License file
	install -D -m644 "$srcdir/$_gitname-build/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	# Fixing permissions
	chmod 644 "${pkgdir}"/usr/lib/python2.7/site-packages/sf.so
}
