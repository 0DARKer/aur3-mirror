# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: akojo
# Contributor: quizzlo

pkgname=tin
pkgver=2.0.1
pkgrel=3
pkgdesc="Curses based threaded NNTP and spool based UseNet newsreader"
arch=('i686' 'x86_64')
url="http://www.tin.org"
license=('BSD')
depends=('dante' 'gnupg' 'icu' 'ispell' 'libidn' 'ncurses' 'perl')
source=("ftp://ftp.tin.org/pub/news/clients/tin/stable/$pkgname-$pkgver.tar.bz2"
        'LICENSE')
md5sums=('e61837b8559e7cdbc35bc11f15c3b7ce'
         '22de35b1bdc6f0df87c93ae794198b21')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-screen=ncursesw --with-pcre=/usr \
	--disable-prototypes --disable-echo --enable-ipv6 --with-gpg=/usr/bin/gpg \
	--with-coffee --enable-nls --with-ispell=/usr/bin/ispell --with-socks --with-socks5
  make build || return 1
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/man/man5/mbox.5
  rm -f $pkgdir/usr/share/man/man5/mmdf.5
  mv $pkgdir/usr/bin/url_handler.pl $pkgdir/usr/bin/tin_url_handler.pl
  install -D -m644 $srcdir/LICENSE \
    $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
