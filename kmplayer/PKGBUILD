# Maintainer: A Rojas <nqn1976 @ gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.11.3.a
_pkgver=0.11.3a
pkgrel=1
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
url="http://kmplayer.kde.org"
depends=('kdebase-runtime' 'cairo' 'dbus-glib')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://kmplayer.kde.org/pkgs/${pkgname}-${_pkgver}.tar.bz2")
md5sums=('5d37eaea3bfbf8040e4ed303b878c43d')

build() {
  cd ${srcdir}
  # compile translations
  echo "add_subdirectory( po )" >> ${pkgname}-${_pkgver}/CMakeLists.txt
  mkdir build
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
