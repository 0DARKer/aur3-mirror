# Maintainer: Moritz Bunkus <moritz@bunkus.org>
pkgname='iwatch'
pkgver='0.2.2'
pkgrel='1'
pkgdesc="realtime filesystem monitoring program using inotify"
arch=('any')
license=('GPL')
options=('!emptydirs')
depends=('perl-event' 'perl-linux-inotify2' 'perl-mail-sendmail' 'perl-xml-simpleobject-libxml')
backup=('etc/iwatch/iwatch.xml')
url='http://iwatch.sourceforge.net/'
source=('http://sourceforge.net/projects/iwatch/files/iwatch/0.2.2/iwatch-0.2.2.tgz/download'
        'iwatch.service')
md5sums=('b32155e844b0d58871dc72041160938d'
         'a858296a0bfb8bf5b46d32a502d1bf56')
sha512sums=('2aecf2f23e0768076c076cbd58040970d6a7fe05e7030ccae4f856b448be6dee8dfdd4180c0979783d56bb3ccd2edb420e19c65c20ca9222b2a63276b2d79232'
            'ec3baa4fe33b5edb2441923aec91528fb1d8637e12bdd10f5465bc78fa5d2a41e5619f2151df09604ee55c3b3df4053aecc9fa9db78f7dd0ef8a80219fe89bec')

build() {
  cd "${srcdir}/iwatch"
  sed -i -e 's:/etc/iwatch.dtd:/etc/iwatch/iwatch.dtd:g' iwatch.xml
}

package() {
  install -Dm644 iwatch.service "${pkgdir}/usr/lib/systemd/system/iwatch.service"
  cd "${srcdir}/iwatch"
  for file in AUTHORS ChangeLog COPYING iwatch.xml.example README ; do
    install -Dm644 "${file}" "${pkgdir}/usr/share/doc/iwatch/${file}"
  done
  install -D iwatch "${pkgdir}/usr/sbin/iwatch"
  install -Dm644 iwatch.xml "${pkgdir}/etc/iwatch/iwatch.xml"
  install -Dm644 iwatch.dtd "${pkgdir}/etc/iwatch/iwatch.dtd"
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
