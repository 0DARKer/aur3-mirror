# Maintainer: Michael Hansen <zrax0111 gmail com>

pkgname=qt5-doc
_srcpkg=qt5
pkgdesc='Documentation package for qt5'
pkgver=5.0.1
pkgrel=1
arch=('any')
url='http://qt-project.org/'
license=('LGPL')
makedepends=('python2' 'ruby' 'gperf')
depends=('qt5')

_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://releases.qt-project.org/${_srcpkg}/${pkgver}/single/${_pkgfqn}.tar.gz"
        "qtwebkit-use-python2.patch")
md5sums=('8e8137c23c8ff078a560d3c62b4383e7'
         '9f5fbd8497e379f60192a92d3ae0e280')

build() {
    cd ${_pkgfqn}

    # build with python2 (thanks to ascarpino for pointing out the patch at
    # https://projects.archlinux.org/svntogit/packages.git/plain/trunk/use-python2.patch?h=packages/qtwebkit
    patch -uNp1 -i ../qtwebkit-use-python2.patch

    ./configure --prefix=/usr \
        -release \
        -opensource \
        --confirm-license \
        -nomake libs \
        -nomake tools \
        -nomake demos \
        -nomake tests \
        -nomake examples \
        -make docs \
        -opengl desktop \
        -no-egl \
        -no-eglfs \
        -no-rpath \
        -docdir /usr/share/doc/qt5

    make && make docs
}

package() {
    cd ${_pkgfqn}

    # Despite telling Qt not to build the libs, it builds the libs...
    # And even then, make install doesn't install the docs!  So although
    # "make install_*_docs" seems like a hack, it's necessary until Qt
    # fixes their build/install process :(
    make INSTALL_ROOT="${pkgdir}" install_qch_docs
    make INSTALL_ROOT="${pkgdir}" install_html_docs
}
