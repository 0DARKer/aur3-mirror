# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.2.2
pkgrel=1
pkgdesc="LinuxSampler Qt GUI"
arch=('i686' 'x86_64')
url="http://qsampler.sourceforge.net/"
license=('GPL')
depends=('qt4' 'linuxsampler' 'liblscp')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'lx11.patch')
md5sums=('3178e7c30c843af992469c2714f722a8'
         '55f250cde02bb6931fc0f6f549d2c7bb')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix X11 linkage issue
  patch -Np1 -i "$srcdir/lx11.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # prog segfaults, enabling debugging if you still want to try (fixing) it
  ./configure --prefix=/usr --enable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
