# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=gxine
pkgver=0.5.905
pkgrel=2
pkgdesc="GTK+ frontend for xine"
arch=('i686' 'x86_64')
url="http://www.xine-project.org/"
license=('GPL')
depends=('xine-lib' 'gtk2' 'js' 'udev' 'desktop-file-utils' 'hicolor-icon-theme' 'lirc-utils')
makedepends=('libxaw')
optdepends=('libxaw: Mozilla browser plugin')
options=('!libtool')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/xine/gxine-$pkgver.tar.bz2"
        "$pkgname-$pkgver-non-separate-toolbar.patch"
        "$pkgname-$pkgver-spidermonkey-update.patch"
        "$pkgname-$pkgver-desktop.patch"
        "$pkgname-$pkgver-lirc.patch"
        "$pkgname-$pkgver-dso.patch"
        "$pkgname-$pkgver-js.patch")
md5sums=('3c9092f1c5c8dc85e95ca327cdcc77af'
         '7410b7db685fd0f4da73ee19a1087563'
         'b45d6904f18e3f86475b57ff891127d4'
         '5cea20310e01d28e6c4e9593a367fbc1'
         'b21c96dedbc4ea93c68d9f20d4b7ca0d'
         '28bdd1da78e5777b981c334dea3cbd24'
         'f6884d909970e93ef7c339b938a0e36d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # js patches
  patch -p1 -i ../$pkgname-$pkgver-non-separate-toolbar.patch
  patch -p1 -i ../$pkgname-$pkgver-spidermonkey-update.patch
  patch -p1 -i ../$pkgname-$pkgver-desktop.patch
  patch -p1 -i ../$pkgname-$pkgver-lirc.patch
  patch -p1 -i ../$pkgname-$pkgver-dso.patch
  patch -p1 -i ../$pkgname-$pkgver-js.patch

  autoreconf
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-integration-wizard \
              --with-spidermonkey=/usr/include/js \
              --with-browser-plugin \
              --with-logo-format=image \
              --with-dbus \
              --with-gudev \
              --enable-watchdog \
              --disable-deprecated \
              --disable-own-playlist-parsers \
              VENDOR_PKG_VERSION="$pkgver-$pkgrel; Arch Linux"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # mozilla plugin
  install -d "$pkgdir/usr/lib/mozilla/plugins"
  ln -s /usr/lib/gxine/gxineplugin.so "$pkgdir/usr/lib/mozilla/plugins"
}
