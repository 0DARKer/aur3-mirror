# Maintainer : Sauyon Lee <sauyonl@sauyon.com>

pkgname=syncthing-git
pkgver=0.8.9.r12.g04130fc
pkgrel=3
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing"
url="http://syncthing.net/"
license=('MIT')
makedepends=('git' 'go' 'godep' 'mercurial')
conflicts=('syncthing')
provides=('syncthing')
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
source=("$pkgname::git+https://github.com/calmh/syncthing.git"
        "syncthing@.service")
sha256sums=('SKIP'
            '2b8ae688a7d60be7899b6124591804eacca7dd29e980d3eaa8a28b0f7a28ac44')
install=${pkgname}.install

pkgver() {
	cd "$srcdir/go/src/github.com/calmh/syncthing"
	git describe --long | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
	export GOPATH="$srcdir"

	mkdir -p "$srcdir/src/github.com/calmh"
	mv "$srcdir/$pkgname" "$srcdir/src/github.com/calmh/syncthing"
}

build() {
	cd "$srcdir/src/github.com/calmh/syncthing"

	GOPATH="$srcdir" ./build.sh
}

check() {
	cd "$srcdir/src/github.com/calmh/syncthing"

	GOPATH="$srcdir" ./build.sh test
}

package() {
  cd "$srcdir/src/github.com/calmh/syncthing"
  install -D -m 755 syncthing "$pkgdir/usr/bin/syncthing"
  install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -D -m 644 "$srcdir/syncthing@.service" "$pkgdir/usr/lib/systemd/system/syncthing@.service"
}
