# Maintainer: Paul Colomiets <pc@gafol.net>

pkgname=zerogw
pkgver=0.5.13
pkgrel=1
pkgdesc="A http/zeromq gateway"
arch=('i686' 'x86_64')
url="http://github.com/tailhook/zerogw"
license=('MIT')
depends=('zeromq' 'libyaml' 'openssl' 'libev' 'mime-types')
makedepends=('coyaml' 'libwebsite' 'pyzmq-git' 'mime-types')
backup=("etc/zerogw.yaml")
source=(https://github.com/downloads/tailhook/zerogw/$pkgname-$pkgver.tar.bz2)
md5sums=('b5b160651a2696f97e3c70759994417d')

build() {
  cd $srcdir/$pkgname-$pkgver
  LDFLAGS="$LDFLAGS -Wl,--no-as-needed" ./waf configure --prefix=/usr
  ./waf build
}

check() {
  cd $srcdir/$pkgname-$pkgver
  ./waf test
}

package() {
  cd $srcdir/$pkgname-$pkgver
  ./waf install --destdir=$pkgdir
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
