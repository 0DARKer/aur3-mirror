# Maintainer: Paul Colomiets <pc@gafol.net>

pkgname=zerogw
pkgver=0.5.22
pkgrel=1
pkgdesc="A http/zeromq gateway"
arch=('i686' 'x86_64')
url="http://github.com/tailhook/zerogw"
license=('MIT')
depends=('zeromq' 'libyaml' 'openssl' 'libev' 'mime-types')
makedepends=('coyaml' 'libwebsite>=0.2.19' 'python-pyzmq>=2.1.9' 'mime-types')
backup=("etc/zerogw.yaml")
source=(https://github.com/downloads/tailhook/zerogw/$pkgname-$pkgver.tar.bz2)
md5sums=('87139fd50c6a6f70b3f0c6b5ef0f2add')

build() {
  cd $srcdir/$pkgname-$pkgver
  LDFLAGS="$LDFLAGS -Wl,--no-as-needed" ./waf configure --prefix=/usr
  ./waf build
}

check() {
  cd $srcdir/$pkgname-$pkgver
  ./waf test
}

package() {
  cd $srcdir/$pkgname-$pkgver
  ./waf install --destdir=$pkgdir
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
