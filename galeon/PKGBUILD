# Maintainer: M Rawash <mrawash@gmail.com>
pkgname=galeon
pkgver=2.0.7
pkgrel=3
pkgdesc="A GNOME web browser based on the gecko rendering engine."
arch=('i686' 'x86_64')
license=('GPL2')
url="http://galeon.sourceforge.net/"
depends=('xulrunner>=1.9.2' 'gnome-desktop' 'libgnomeui')
makedepends=('intltool' 'rarian')
install=galeon.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	patches.tar.gz)
md5sums=('2eede1f43e3f6f2ac4ce7d4db99b15b2'
         'f11222c53f7363da52c3c18932392b2f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  for i in $(ls ${srcdir}/*.patch); do
      patch -Np1 -i ${i} || return 1
  done

  sed -i s/libxul-embedding-unstable/libxul/ configure.in || return 1
  sed -i s/TextZoom/FullZoom/ mozilla/GaleonWrapper.cpp || return 1

  autoreconf -fi || return 1
  ./configure --prefix=/usr -sysconfdir=/etc \
              --disable-nautilus-view \
              --disable-werror \
              --disable-dependency-tracking \
              --with-mozilla=libxul

  make || return 1
  make  GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install || return 1

  #gconf schema
  install -m755 -d "$pkgdir"/usr/share/gconf/schemas
  gconf-merge-schema "$pkgdir"/usr/share/gconf/schemas/${pkgname}.schemas \
                        "$pkgdir"/etc/gconf/schemas/*.schemas || return 1
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas

  #docs
  mkdir -p ${pkgdir}/usr/share/doc/${pkgname}
  cp {AUTHORS,ChangeLog,NEWS,README*,INSTALL,THANKS,TODO,FAQ*} ${pkgdir}/usr/share/doc/${pkgname}
}
