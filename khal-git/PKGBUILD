# Maintainer: Ã‰tienne Deparis <etienne [at] depar.is>

pkgname=khal-git
_gitname=khal
pkgver=0.2.0.r151.gef5cf30
pkgrel=1
pkgdesc="Command line CalDav client"
license=("MIT")
url="http://lostpackets.de/khal/"
depends=('sqlite' 'python2-dateutil' 'python2-lxml' 'python2-requests'
  'python2-icalendar' 'python2-urwid' 'python2-xdg'
  'python2-vdirsyncer' 'python2-docopt')
makedepends=('python2-setuptools' 'git')
source=("${_gitname}::git+https://github.com/geier/khal.git")
md5sums=('SKIP')
install='khal.install'
provides=('khal')
conflicts=('khal')
arch=('any')
options=(!emptydirs)

pkgver() {
  cd "$srcdir/${_gitname}"
  MAJOR=`sed -n "s/MAJOR\s\?=\s\?\(\d*\)/\1/p" setup.py`
  MINOR=`sed -n "s/MINOR\s\?=\s\?\(\d*\)/\1/p" setup.py`
  PATCH=`sed -n "s/PATCH\s\?=\s\?\(\d*\)/\1/p" setup.py`
  GITVER=`git describe --always --dirty --tags | cut -d- -f2- | sed -r 's/([^-]*-g)/r\1/;s/-/./g'`

  echo "${MAJOR}.${MINOR}.${PATCH}.${GITVER}"
}

package() {
  cd "$srcdir/${_gitname}/"
  python2 setup.py install --root=$pkgdir

  install -Dm 644 khal.conf.sample "${pkgdir}/etc/skel/khal.conf"
  install -Dm 644 doc/parts/license.rst "${pkgdir}/usr/share/licenses/khal/LICENSE"
  install -Dm 644 misc/khal.1 "${pkgdir}/usr/share/man/man1/khal.1"

  # You can uncomment the following to grab zsh completion
  # install -Dm 644 misc/__khal "${pkgdir}/usr/share/zsh/site-functions/_khal"

}
