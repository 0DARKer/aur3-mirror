# $Id: PKGBUILD 70841 2010-03-02 03:28:23Z allan $
# Maintainer (sudo): Allan McRae <allan@archlinux.org>
# Maintainer (sudo-ldap): Scott Sinclair <scott@nullis.net>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sudo-ldap
pkgver=1.7.2p5
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root (with LDAP support)"
arch=('i686' 'x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
provides=('sudo=1.7.2p5')
conflicts=('sudo')
depends=('glibc' 'pam')
backup=('etc/sudoers' 'etc/pam.d/sudo')
source=(ftp://ftp.sudo.ws/pub/sudo/sudo-$pkgver.tar.gz 
        sudo.pam)
options=('!libtool' '!makeflags' 'force')
md5sums=('398f584e831bd75b3c0179e28368c2a3'
         '758d6a1b9a8c6a6e106614cfc35c24fe')

build() {
  cd $srcdir/sudo-$pkgver || return 1

  ./configure --prefix=/usr --with-pam --libexecdir=/usr/lib \
    --with-env-editor --with-all-insults --with-logfac=auth --with-ldap || return 1
  make || return 1
}

package() {
  cd $srcdir/sudo-$pkgver || return 1

  make DESTDIR=$pkgdir install || return 1
  install -Dm644 $srcdir/sudo.pam $pkgdir/etc/pam.d/sudo || return 1

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/sudo/LICENSE || return 1
}
