pkgname=libcap-ng-svn
_ver_in_arch="$(expac -S -1 "%v" libcap-ng | cut -f1 -d -)"
pkgver=22
pkgrel=3
pkgdesc="An alternate posix capabilities library"
arch=('i686' 'x86_64')
url="http://people.redhat.com/sgrubb/libcap-ng"
license=('LGPL2.1')
depends=('glibc')
makedepends=('subversion' 'attr' 'libcap' 'expac')
provides=("libcap-ng=$_ver_in_arch")
conflicts=('libcap-ng')
options=('!libtool')
source=('pscap-dont-drop-caps.patch')
sha256sums=('345358dc4193cd5f3939712dab931833670d651a0163702fc52cac2711b19443')
_svntrunk=http://svn.fedorahosted.org/svn/libcap-ng/trunk
_svnmod=libcap-ng

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  patch -Np1 -i "$srcdir/pscap-dont-drop-caps.patch"

  ./autogen.sh
  ./configure --prefix=/usr \
    --disable-static --enable-shared --with-pic \
    --without-python

  make

}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
