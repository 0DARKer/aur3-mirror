# Maintainer: Braden Pellett (daBrado) <aurcontact@dabrado.net>
# Contributor: oldbay <old_bay@mail.ru>

pkgname=ossim-nogui-svn
pkgver=22270
pkgrel=1
pkgdesc='A powerful suite of geospatial libraries and applications used to process imagery, maps, terrain, and vector data; built w/o GUIs'
url=http://www.ossim.org/
license=(LGPL)
install=$pkgname.install
source=(ossim::svn+http://svn.osgeo.org/ossim/trunk)
md5sums=(SKIP)
arch=(i686 x86_64)
backup=(etc/ossim_preferences)
depends=(openthreads ffmpeg gdal)
makedepends=(cmake)
conflicts=(ossim)
provides=(ossim)

pkgver() {
  cd "$srcdir"/ossim
  svnversion | tr -d [A-z]
}

build() {
  cd "$srcdir"
  cmake -G "Unix Makefiles" \
    -DCMAKE_RULE_MESSAGES=OFF -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DOSSIM_DEV_HOME="$srcdir"/ossim \
    -DCSMAPI_INCLUDE_DIRS="$srcdir"/ossim/csmApi/include \
    -DBUILD_RUNTIME_DIR=bin \
    -DBUILD_LIBRARY_DIR=lib \
    -DOSSIM_COMPILE_WITH_FULL_WARNING=ON \
    -DBUILD_OSSIM=ON \
    -DBUILD_OSSIMCONTRIB_PLUGIN=ON \
    -DBUILD_OSSIMGDAL_PLUGIN=ON \
    -DBUILD_OSSIMPNG_PLUGIN=ON \
    -DBUILD_OSSIM_MPI_SUPPORT=OFF \
    -DBUILD_OSSIMPLANET=OFF \
    -DBUILD_OSSIMPLANETQT=OFF \
    -DBUILD_OSSIMQT4=OFF \
    -DBUILD_OSSIMGUI=OFF \
    ossim/ossim_package_support/cmake
  make
  echo 'export OSSIM_DATA=/usr/share' >ossim.sh
  echo 'export OSSIM_PREFS_FILE=/etc/ossim_preferences' >>ossim.sh
  sed \
    -e 's,$(OSSIM_DATA)/ossim/share/ossim/ossim_wkt_pcs.csv,/usr/share/ossim/projection/ossim_wkt_pcs.csv,' \
    -e 's,$(OSSIM_DATA)/ele1/geoid/geoid96/egm96.grd,/usr/share/ossim/geoids/geoid1996/egm96.grd,' \
    -e 's,$(OSSIM_DATA)/ossim/share/ossim/,/usr/share/ossim/,g' \
    ossim/ossim/etc/templates/ossim_preferences_template >ossim_preferences
  echo $'\n''plugin.dir1: /usr/lib/ossim/plugins' >>ossim_preferences
}

package() {
  cd "$srcdir"
  make DESTDIR="${pkgdir}" install
  install -D -m755 ossim.sh ${pkgdir}/etc/profile.d/ossim.sh
  install -D -m644 ossim_preferences ${pkgdir}/etc/ossim_preferences
}
