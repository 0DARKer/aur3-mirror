# Maintainer: Stanislav GE <ginermail@gmail.com>

pkgname=doublecmd-svn
pkgver=5190
pkgrel=1
pkgdesc="Double Commander is a cross platform open source file manager with two panels side by side. It is inspired by Total Commander and features some new ideas."
arch=(i686 x86_64)
url=http://doublecmd.sourceforge.net
license=(GPL2)
makedepends=('lazarus' 'fpc' 'subversion')
_svnmod=doublecmd-code
source=("$_svnmod::svn+http://svn.code.sf.net/p/doublecmd/code/trunk"
	'lazarusdir.patch')
md5sums=('SKIP'
         '0c1412421c057fb3e90c9f21302b13c7')

# pkgver autobump
pkgver() {
	cd "$SRCDEST/$_svnmod"
	svnversion
}

# Allow doublecmd to get subversion revision number while it is built
prepare() {
	cp -a "$SRCDEST/$_svnmod/.svn" "$srcdir/$_svnmod/"
}

build() {
	cd "$srcdir/$_svnmod"

	# apply patch
	patch -i "$startdir/lazarusdir.patch"

	# build (choose all instead of beta when starts to work again)
	#./build.sh all
	./build.sh beta
}

package() {
	cd "$srcdir/$_svnmod"

	# OPTION 1
	# Install using official installation script

	#install/linux/install.sh -I $pkgdir

	# OPTION 2
	# Install manually (not using official installation script)

	# create doublecmd link to /usr/bin
	prefix=/opt/doublecmd
	install -d $pkgdir/usr/bin
	ln -s $prefix/doublecmd $pkgdir/usr/bin/doublecmd

	# install desktop file and icon
	install -D -m 644 install/linux/doublecmd.desktop $pkgdir/usr/share/applications/doublecmd.desktop
	install -D -m 644 install/linux/doublecmd.xpm $pkgdir/usr/share/pixmaps/doublecmd.xpm
	install -D -m 644 doublecmd.png $pkgdir/usr/share/pixmaps/doublecmd.png

	# install man files
	install -d $pkgdir/usr/share/man/man1
	install -m 644 install/linux/*.1 $pkgdir/usr/share/man/man1

	# install plugins
	for plugin in plugins/wcx/*/lib/*.wcx \
			plugins/wdx/*/lib/*.wdx \
			plugins/wlx/*/lib/*.wlx \
			plugins/dsx/*/lib/*.dsx \
			plugins/wfx/*/lib/*.wfx \
			plugins/wdx/scripts/*.lua \
			; do
		dest_plugin_path=$(echo $pkgdir/$prefix/$plugin|sed 's/\/lib\//\//')
		install -D -m 644 $plugin $dest_plugin_path
	done

	# install doc, language, pixmaps
	cp -r doc $pkgdir/$prefix/doc
	cp -r language $pkgdir/$prefix/language
	cp -r pixmaps $pkgdir/$prefix/pixmaps

	# install bin, misc
	install -D -m 755 doublecmd $pkgdir/$prefix
	install -D -m 644 install/linux/doublecmd.xml \
		doublecmd.ext.example \
		doublecmd.zdli \
		pixmaps.txt \
		multiarc.ini \
		$pkgdir/$prefix
}
