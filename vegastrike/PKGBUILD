# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=vegastrike
pkgver=0.5.0
pkgrel=6
pkgdesc="3D Action-Space-Sim to trade and bounty hunt in a vast universe"
arch=('i686' 'x86_64')
url="http://vegastrike.sourceforge.net/"
license=('GPL')
depends=('gcc-libs' 'freetype2' 'freeglut' 'gtk2' 'openal' 'sdl' 'python2' 'libvorbis')
makedepends=('patch')
optdepends=('vegastrike-data: original dataset'
            'vegastrike-data-svn: original dataset, svn version'
            'paralleluniverse: revival of Privateer')
conflicts=('vegastrike-svn')
options=('!makeflags')
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-src-$pkgver.tar.bz2 \
        $pkgname-gcc.diff)
md5sums=('8cfd7d1691173652e14fc89e4da97d1a'
         'a70f892935b8681954111fd5b071c4b0')

build() {
  cd ${srcdir}/$pkgname-source-$pkgver

  patch -Np1 -i ${srcdir}/$pkgname-gcc.diff

  ./configure --prefix=/usr --with-data-dir=/usr/share/vegastrike
  make
}

package() {
  cd ${srcdir}/$pkgname-source-$pkgver
  make DESTDIR=${pkgdir} install

#move objconv dir to right path and avoiding conflict with mysql's 'replace' file
  install -d ${pkgdir}/usr/share/$pkgname
  mv -f ${pkgdir}/usr/objconv ${pkgdir}/usr/share/$pkgname/objconv
  mv ${pkgdir}/usr/bin/replace ${pkgdir}/usr/bin/vsreplace
}
