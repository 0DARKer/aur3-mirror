# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=qvdpautest-git
pkgver=20140321.702fe83
pkgrel=1
pkgdesc="A VDPAU Benchmark. (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.nvnews.net/vbulletin/showthread.php?p=2015214"
makedepends=('cmake')
depends=('qt4' 'libvdpau')
provides=('qvdpautest')
conflicts=('qvdpautest')
source=('git+https://github.com/robertmassaioli/qvdpautest.git')
sha1sums=('SKIP')
_gitname="qvdpautest"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  cd "${_gitname}"
  sed -e 's|QString directoryString("");|QString directoryString("/usr/share/qvdpautest/data/");|g' -i src/main.cpp
}

build() {
  cd "${_gitname}"
  cmake .
  make
}

package() {
  cd "${_gitname}"
  install -Dm755 src/qvdpautest "${pkgdir}/usr/bin/qvdpautest"
  install -d "${pkgdir}/usr/share/qvdpautest/data"
  install -Dm644 data/* "${pkgdir}/usr/share/qvdpautest/data"
}
