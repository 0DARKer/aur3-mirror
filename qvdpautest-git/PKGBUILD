# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=qvdpautest-git
pkgver=20130301
pkgrel=1
pkgdesc="A VDPAU Benchmark. (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.nvnews.net/vbulletin/showthread.php?p=2015214"
depends=('qt4' 'libvdpau')
provides=('qvdpautest')
conflicts=('qvdpautest')
source=('qvdpautest.sh')
md5sums=('d4f50d76f9e4f9837956fa521879f44a')

_gitroot="git://github.com/robertmassaioli/qvdpautest.git"
_gitname="qvdpautest"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server..."

  if [ -d "${_gitname}" ] ; then
    cd "${_gitname}" && git pull --depth=1
  else
    git clone --depth=1 "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"

  msg "Starting build..."

  rm -fr "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  install -Dm755 "${srcdir}/qvdpautest.sh" "${pkgdir}/usr/bin/qvdpautest"
  install -Dm755 src/qvdpautest "${pkgdir}/usr/share/qvdpautest/qvdpautest"
  install -m644 data/* "${pkgdir}/usr/share/qvdpautest/"
}
