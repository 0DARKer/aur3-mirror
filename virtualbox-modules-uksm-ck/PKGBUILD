# Mainainter: Piotr Gorski <sir_lucjan@openlinux.pl>

pkgbase=virtualbox-modules-uksm-ck
pkgname=('virtualbox-host-modules-uksm-ck' 'virtualbox-guest-modules-uksm-ck')
pkgver=4.3.12
pkgrel=4
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-uksm-ck>=3.15' 'linux-uksm-ck<3.16'
             'linux-uksm-ck-headers>=3.15' 'linux-uksm-ck-headers<3.16'
             "virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver")

# remember to also adjust the .install files and the package deps below
_extramodules=extramodules-3.15-uksm-ck

build() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-host-modules-uksm-ck(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Host kernel modules for VirtualBox'
  depends=('linux-uksm-ck>=3.15' 'linux-uksm-ck<3.16')
  replaces=('virtualbox-modules-uksm-ck')
  conflicts=('virtualbox-modules-uksm-ck')
  install=host.install

  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

package_virtualbox-guest-modules-uksm-ck(){
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=('linux-uksm-ck>=3.15' 'linux-uksm-ck<3.16')
  replaces=('virtualbox-modules-uksm-ck')
  conflicts=('virtualbox-modules-uksm-ck')
  install=guest.install

  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules/"
  install -m644 * "$pkgdir/usr/lib/modules/$_extramodules/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

# vim:set ts=2 sw=2 et:
md5sums=()