# Maintainer: Fernando Jim√©nez Solano (fjim) <fjim@sdfeu.org>
# Contributor: FUBAR <mrfubar@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

pkgname=bitlbee-libpurple
_pkgname=bitlbee
pkgver=3.0.6
pkgrel=1
pkgdesc="An IRC to IM gateway. Supports MSN, ICQ, Jabber, Yahoo!, and AIM. libpurple enabled."
arch=('i686' 'x86_64')
url="http://www.bitlbee.org/" 
license=('GPL')
depends=('gnutls' 'glib2' 'libpurple')
makedepends=('gnutls' 'glib2' 'libpurple' 'pkg-config')
optdepends=('xinetd: to run bitlbee through xinetd')
conflicts=('bitlbee' 'bitlbee-bzr' 'bitlbee-bzr-libpurple' 'bitlbee-msn' 'bitlbee-msn-xiaoi' 'bitlbee-otr' 'bitlbee-otr-bzr' 'bitlbee-recode')
install=${_pkgname}.install
backup=(etc/bitlbee/bitlbee.conf etc/bitlbee/motd.txt etc/xinetd.d/bitlbee)
source=(http://get.bitlbee.org/src/${_pkgname}-${pkgver}.tar.gz
	'bitlbee.xinetd'
	'bitlbee.rcd')
sha512sums=('01142dbd6b89e1f80d58a337b3cd2fc7256925e59381563a9010153fce5fb863a76e7855a678d70e109519d2f8fe28f17bdeb2c976eb27f0987d72b82b93f891'
            'ff417d4e2b4d79273e8fac2704b609caa908d60bb2f9acbc35d9bd5ea59d9d674cac0c3ed81eaadc349bc1e0aa153faec82dcb68816424621c11bcdf33691ff7'
            '90c7de716ad4f1ce1faf0204c3a379a5b4a880903b522a8c6efd82cb8e1d27b26b223234f9d2ec1219c82b9599049308a12316826a2f0c90a8301bee8fa5c3a2')

build() { 
	cd ${srcdir}/${_pkgname}-${pkgver}
	
	./configure --prefix=/usr \
		--ssl=gnutls \
		--etcdir=/etc/bitlbee \
		--pidfile=/var/run/bitlbee/bitlbee.pid \
		--ipcsocket=/var/run/bitlbee/bitlbee.sock \
		--purple=1
	make
}

package() {
	cd ${srcdir}/${_pkgname}-${pkgver}

	install -dm755 ${pkgdir}/var/lib
	install -o65 -g65 -dm0770 ${pkgdir}/var/lib/bitlbee
	install -dm755 ${pkgdir}/var/run
	install -o65 -g65 -dm755 ${pkgdir}/var/run/bitlbee
	install -Dm644 ${srcdir}/bitlbee.xinetd \
		${pkgdir}/etc/xinetd.d/bitlbee
	install -Dm755 ${srcdir}/bitlbee.rcd \
		${pkgdir}/etc/rc.d/bitlbee
	make DESTDIR=${pkgdir} install
	make DESTDIR=${pkgdir} install-etc
	make DESTDIR=${pkgdir} install-dev
}
