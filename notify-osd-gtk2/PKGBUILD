# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: György Balló <ballogy@freestart.hu>
# Contributor: Johannes Pfau <johannespfau at gmail dot com>

_pkg=notify-osd
pkgname=$_pkg-gtk2
pkgver=0.9.30
pkgrel=1
pkgdesc="Canonical's on-screen-display notification agent, implementing the freedesktop.org Desktop Notifications Specification. Gtk2 version."
arch=('i686' 'x86_64')
url="https://launchpad.net/notify-osd"
license=('GPL')
depends=('gconf' 'libwnck' 'libnotify>=0.4.5')
provides=('notification-daemon')
conflicts=('notification-daemon')
source=("http://launchpad.net/$_pkg/natty/natty-alpha3/+download/$_pkg-$pkgver.tar.gz")
md5sums=('aa52780eb7be9eab6d6bd3a6b055ac09')

build() {
  cd "$srcdir/$_pkg-$pkgver"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libexecdir=/usr/lib/$_pkg \
              --disable-static

  # disable tests
  sed -i '/^SUBDIRS/s/tests //' Makefile  

  make
}

package() {
  cd "$srcdir/$_pkg-$pkgver"

  make DESTDIR="$pkgdir/" install
}
