# Maintainer: Peter Marheine <peter@taricorp.net>
# based on 'cross-arm-elf-gcc' by Andreas Messer <andi@surveycorner.de>

pkgname=rx-elf-gcc
pkgver=4.7.2
pkgrel=1
pkgdesc="The GNU Compiler Collection - cross compiler for Renesas RX target"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
#an installed libc/newlib is needed for libstdc++ compile
depends=('rx-elf-binutils' 'cloog' 'rx-elf-newlib')
# cross-rx-elf-gcc is an superset of cross-rx-elf-gcc-base
conflicts=('rx-elf-gcc-base')
provides=("rx-elf-gcc-base=${pkgver}")
options=(!libtool !emptydirs !strip zipman docs)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2)
md5sums=('cc308a0891e778cfda7a151ab8a6e762')

build() {
  cd ${srcdir}/gcc-$pkgver

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  rm -rf build
  mkdir build
  cd build

  ../configure --prefix=/usr \
	--target=rx-elf \
        --disable-nls \
        --enable-languages=c,c++ \
        --enable-multilib \
        --enable-interwork \
	--with-local-prefix=/usr/lib/cross-rx \
	--with-as=/usr/bin/rx-elf-as \
        --with-ld=/usr/bin/rx-elf-ld \
	--with-newlib \
	--host=$CHOST \
	--build=$CHOST 

  make all-gcc all-target-libgcc all-target-libstdc++-v3 || return 1
  make DESTDIR=${pkgdir} install-gcc install-target-libgcc install-target-libstdc++-v3 || return 1

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info
  rm -rf $pkgdir/usr/share/gcc-${pkgver}

  cp -r  $pkgdir/usr/libexec/* $pkgdir/usr/lib/ && \
  rm -rf $pkgdir/usr/libexec 

}
