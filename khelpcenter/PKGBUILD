# Contributor: Antonio Rojas

pkgname=khelpcenter
pkgver=4.96.0
pkgrel=2
pkgdesc="Application to show KDE Applications' documentation"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/khelpcenter'
license=('LGPL')
depends=('khtml' 'kdelibs4support' 'kcmutils')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz" "fix-doc-conflicts.patch::http://quickgit.kde.org/?p=khelpcenter.git&a=commitdiff&h=df8f37b5904557331ff9bd44ed5517261e922db4&o=plain")
md5sums=('006ab4529ee58d02a467a616588fe7e1'
         '6aa5ce8e223019e103790d2da6c32e95')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -F3 -i ${srcdir}/fix-doc-conflicts.patch
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
