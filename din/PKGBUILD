# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=din
pkgver=3.0
pkgrel=1
pkgdesc="A software musical instrument and audio synthesizer using Bezier curves to draw waveforms."
arch=(i686 x86_64)
url="http://dinisnoise.org/"
license=('GPL3')
depends=('fftw' 'jack' 'liblo' 'libgl' 'libircclient' 'tcl')
makedepends=('mesa')
install="$pkgname.install"
source=("http://din.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop" "$pkgname.png" "$pkgname-gcc4.7.patch")
md5sums=('b01aabccab8c456d67c1ff754f072a1f'
         '5c0492262c13b4d6393c176c057f0c8f'
         'a13fda9d0c9640197aa7d5dd6a14a8d4'
         'df888bb0173b5ca8c858c0ea47868ffb')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.7 patch
  patch -p1 -i ../${source[3]}

  # set right share path
  sed -i "s|var/|share/|" data/checkdotdin

  ./configure --prefix=/usr \
              --localstatedir=/usr/share

  make CXXFLAGS="$CXXFLAGS -lrt -lX11"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # desktop file
  install -Dm644 ../$pkgname.desktop \
          "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 ../$pkgname.png \
          "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
