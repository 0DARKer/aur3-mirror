# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=din
pkgver=3.5
pkgrel=1
pkgdesc="A software musical instrument and audio synthesizer using Bezier curves to draw waveforms."
arch=(i686 x86_64)
url="http://dinisnoise.org/"
license=('GPL3')
depends=('fftw' 'jack' 'liblo' 'libgl' 'libircclient' 'tcl')
makedepends=('mesa')
install="$pkgname.install"
source=("http://din.googlecode.com/files/$pkgname-$pkgver.tar.gz")
md5sums=('01a4b397419d27021cd2f2d6d9b68fbc')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # set right share path
  sed -i "s|var/|share/|" data/checkdotdin

  # do not install din.{png,desktop}
  sed -i "/din\.[dp]/d" data/Makefile.in

  ./configure --prefix=/usr \
              --localstatedir=/usr/share
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # desktop file
  install -Dm644 data/$pkgname.desktop \
          "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 data/$pkgname.png \
          "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
