# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname='perl-catalyst-plugin-session'
_pkgname='Catalyst-Plugin-Session'
pkgver='0.37'
pkgrel='1'
pkgdesc="Generic Session plugin - ties together server side storage and client side state required to maintain session data."
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.0' 'perl-catalyst-runtime' 'perl-mro-compat' 'perl-moose>=0.76' 'perl-moosex-emulate-class-accessor-fast>=0.00801' 'perl-object-signature' 'perl-namespace-clean')
makedepends=('perl-extutils-makemaker')
url="http://search.cpan.org/dist/Catalyst-Plugin-Session"
source=("http://search.cpan.org/CPAN/authors/id/B/BO/BOBTFISH/${_pkgname}-${pkgver}.tar.gz")
md5sums=('cce1bff083076a2874e2dc36740b498e')
sha512sums=('fcd07857c62f681c4243f08cd2aab66cf565eaf4b056e1dd09adcdc8069d3ed5a11e7db7a715864bf2ee7846487b1cd4b3f8126a8b6569cb67e95469544b60b3')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/${_pkgname}-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
