# Maintainer: trya <tryagainprod@gmail.com>

pkgname=mupen64plus-extraplugins
pkgver=1.99.5
pkgrel=1
pkgdesc="Additional plugins for Mupen64Plus"
arch=('i686' 'x86_64')
url="http://code.google.com/p/mupen64plus"
license=('GPL')
depends=('glew' 'mesa' "mupen64plus=$pkgver")
options=(!strip)
source=("http://mupen64plus.googlecode.com/files/mupen64plus-extraplugins-src-$pkgver.tar.gz")
sha1sums=('acafe01c076b3ceea160f7910228ccaf217b92fb')

build() {
  cd "$srcdir/$pkgname-src-$pkgver"
  for _plugdir in mupen64*; do
    cd $_plugdir
    make -C projects/unix PREFIX=/usr all
    cd ..
  done
}

package() {
  cd "$srcdir/$pkgname-src-$pkgver"
  for _plugdir in mupen64*; do
    cd $_plugdir
    make -C projects/unix PREFIX=/usr DESTDIR="$pkgdir" install
    cd ..
  done
}
