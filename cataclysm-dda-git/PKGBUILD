# -*- shell-script -*-
# Contributor: Fernando Carmona Varo <ferkiwi @t gmail dot com>
pkgname=cataclysm-dda-git
pkgver=0.A.1940.g8d4e2ef
pkgrel=1
pkgdesc="Cataclysm: Dark Days Ahead is an actively maintained roguelike set in a post-apocalyptic world. Both SDL tiles and ascii version included."
arch=('i686' 'x86_64')
url="http://www.cataclysmdda.com/"
groups=()
license=('CCPL:by-sa')
conflicts=('cataclysm-dda')
depends=('ncurses' 'sdl2' 'sdl2_image' 'sdl2_ttf' 'freetype2')
makedepends=('git' 'gettext')
checkdepends=()
optdepends=('lua51')
backup=()
options=()
install=
changelog=
source=("$pkgname"::'git://github.com/CleverRaven/Cataclysm-DDA.git#branch=master')
noextract=()
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  make version >&2
  printf "%s" $(sed -n 's/.*VERSION \"\(.*[^\\]\)\"/\1/;s/[- ]/./gp' src/version.h)
}

build() {
  cd "${srcdir}/${pkgname}"
  make RELEASE=1
  make RELEASE=1 TILES=1
}

package() {
  cd "${srcdir}/${pkgname}"

  local instdir="/usr/share/cataclysm-dda"

  install -dm755 "$pkgdir/${instdir}/"
  install -Dm755 cataclysm cataclysm-tiles cataclysm-launcher "$pkgdir/${instdir}/"

  install -dm774 -g games "$pkgdir/${instdir}/data/"
  cp -r data/* "$pkgdir/${instdir}/data"

  install -dm774 -g games  "$pkgdir/${instdir}/gfx/"
  cp -r gfx/* "$pkgdir/${instdir}/gfx"

  install -dm774 -g games "$pkgdir/${instdir}/doc"
  cp -r *.txt doc/* "$pkgdir/${instdir}/doc"
  
  install -dm774 -g games "$pkgdir/var/games/cataclysm-dda/save"
  ln -s "/var/games/cataclysm-dda/save" "$pkgdir/${instdir}/save"

  install -dm755  "$pkgdir/usr/bin/"
  ln -s "${instdir}/cataclysm-launcher" "$pkgdir/usr/bin/cataclysm"
  ln -s "${instdir}/cataclysm-launcher" "$pkgdir/usr/bin/cataclysm-tiles"
}
