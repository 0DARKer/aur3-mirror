# -*- shell-script -*-
# Contributor: Fernando Carmona Varo <ferkiwi @t gmail dot com>
pkgname=cataclysm-dda-git
pkgver=0.9.848.g0692aae
pkgrel=1
pkgdesc="Cataclysm: Dark Days Ahead is an actively maintained roguelike set in a post-apocalyptic world, forked from the original."
arch=('i686' 'x86_64')
url="http://www.cataclysmdda.com/"
groups=()
license=('CCPL:by-sa')
conflicts=('cataclysm-dda')
depends=('gcc-libs' 'glibc' 'ncurses')
makedepends=('git' 'gettext')
checkdepends=()
optdepends=()
backup=()
options=()
install=
changelog=
source=("$pkgname"::'git://github.com/Ferk/Cataclysm-DDA.git#branch=master')
noextract=()
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  make version >&2
  printf "%s" $(sed -n 's/.*VERSION \"\(.*[^\\]\)\"/\1/;s/[- ]/./gp' src/version.h)
}

build() {
  cd "${srcdir}/${pkgname}"
  make RELEASE=1
  make RELEASE=1 TILES=1
}

package() {
  cd "${srcdir}/${pkgname}"

  install -dm755 "$pkgdir/usr/share/cataclysm-dda/"
  install -Dm755 cataclysm cataclysm-tiles cataclysm-launcher "$pkgdir/usr/share/cataclysm-dda/"
  cp -r README.md "$pkgdir/usr/share/cataclysm-dda"
  ln -s "/var/games/cataclysm-dda/save" "$pkgdir/usr/share/cataclysm-dda/save"

  install -dm774 -g games "$pkgdir/usr/share/cataclysm-dda/data/"
  cp -r data/* "$pkgdir/usr/share/cataclysm-dda/data"

  install -dm774 -g games  "$pkgdir/usr/share/cataclysm-dda/gfx/"
  cp -r gfx/* "$pkgdir/usr/share/cataclysm-dda/gfx"

  install -dm755  "$pkgdir/usr/bin/"
  ln -s "/usr/share/cataclysm-dda/cataclysm-launcher" "$pkgdir/usr/bin/cataclysm"
  ln -s "/usr/share/cataclysm-dda/cataclysm-launcher" "$pkgdir/usr/bin/cataclysm-tiles"
}
