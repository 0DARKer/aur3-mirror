# Maintainer: Sam S. <smls75@gmail.com>
# Contributor: Daniel Scharrer <daniel@constexpr.org>

pkgname=arx-libertatis
pkgver=1.0.3
pkgrel=4
pkgdesc='Cross-platform port of Arx Fatalis, a first-person role-playing game (executables only)'
url='http://arx-libertatis.org/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('sdl' 'devil' 'openal' 'freetype2' 'zlib' 'boost-libs' 'glew' 'libgl')
optdepends=('qt: enable built-in crash handler (recompile needed)'
            'gdb: generate detailed crash reports')
makedepends=('boost' 'cmake')
provides=('arx')
conflicts=('arx-git')
source=("http://downloads.sourceforge.net/arx/${pkgname}-${pkgver}.tar.xz"
        'cmake-2.8.10.patch')
sha1sums=('5c341ea6a55cbe2cfdd78f6cc95baa2dd0782285'
          'cd9d0ae2e97a5d3e4d9bab3ed976bdbdfbbc8ab6')
install='arx-libertatis.install'

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  patch -p1 -i "${srcdir}/cmake-2.8.10.patch"
  
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DUNITY_BUILD=ON
  
  # (Note: UNITY_BUILD speeds up the compilation and produces a more
  # optimized binary, but uses a lot of RAM during compilation - disable
  # if low on memory.)
  
  # To get a debug build change -DCMAKE_BUILD_TYPE=Release to
  # -DCMAKE_BUILD_TYPE=Debug - this will run signifincantly slower but
  # enable more runtime checks and generate better crash reports.
  
  make -j`getconf _NPROCESSORS_ONLN`
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
  
  install -Dm644 README.md \
          "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 CHANGELOG \
          "${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG"
  install -Dm644 AUTHORS \
          "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 ARX_PUBLIC_LICENSE.txt \
          "${pkgdir}/usr/share/licenses/${pkgname}/ARX_PUBLIC_LICENSE.txt"
}
