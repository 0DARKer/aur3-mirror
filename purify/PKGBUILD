# Maintainer : Alucryd <alucryd at gmail dot com>

pkgbase=purify
pkgname=purify
pkgver=01
pkgrel=5
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL3')
source=("http://bsnes.googlecode.com/files/bsnes_v091-source.tar.xz" "purify.desktop" "purify.sh")
sha256sums=('8c85a18ff44195d23b54cad53549152f034ee338e590907e8c8cbc3eaa7daf9f'
            'b81a78178b35907f30d22731093cb588c5531ad4041a60cd31379c59dbf0f315'
            '8e5f75edea3827c94bca8e252592512b2ef80f2bbaae0edc71c88461a9ccaff9')

# UI
_gtk=true
_qt=false

if [ "$_gtk" == true ]; then
  pkgname+=('purify-gtk')
fi

if [ "$_qt" == true ]; then
  pkgname+=('purify-qt')
fi

buildui() {
  make compiler=gcc platform=x phoenix=$1
  mv purify purify-$1
  make clean
}

installui() {
  cd "${srcdir}/bsnes_v091-source/purify"
  install -Dm 755 purify-$1 "${pkgdir}/usr/bin/purify-$1"
  install -m 755 "../../purify.sh" "${pkgdir}/usr/bin/purify-$1.sh"
  sed -i "s|_ui|$1|" "${pkgdir}/usr/bin/purify-$1.sh"
  install -dm 755 "${pkgdir}/usr/share/applications"
  install -m 644 "../../purify.desktop" "${pkgdir}/usr/share/applications/purify-$1.desktop"
  sed -i "s|_ui|$1|g" "${pkgdir}/usr/share/applications/purify-$1.desktop"
}

build() {
  cd "${srcdir}/bsnes_v091-source/purify"

# QT 4.8.0 fix
  moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp

# Linker fix
  sed -i 's|gtk+-2.0|gtk+-2.0 x11|g' phoenix/Makefile
  sed -i 's|QtCore QtGui|QtCore QtGui x11|g' phoenix/Makefile

# Compile purify GTK GUI
  if [ $_gtk == true ]; then
    buildui gtk
  fi

# Compile purify QT GUI
  if [ $_qt == true ]; then
    buildui qt
  fi
}

package_purify-gtk() {
  pkgdesc="ROM purifier for the higan emulator - GTK GUI"
  depends=('bsnes' 'gtk2')
  provides=("purify-ui=${pkgver}-${pkgrel}")

  installui gtk
}

package_purify-qt() {
  pkgdesc="ROM purifier for the higan emulator - QT GUI"
  depends=('bsnes' 'qt>=4.7.0')
  provides=("purify-ui=${pkgver}-${pkgrel}")

  installui qt
}

package_purify() {
  pkgdesc="ROM purifier for the higan emulator - Dummy package for AUR helpers"
  depends=("purify-ui>=${pkgver}-${pkgrel}")
}

pkgdesc="ROM purifier for the higan emulator"
depends=()
