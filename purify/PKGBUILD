# Maintainer : Alucryd <alucryd at gmail dot com>

pkgbase=purify
pkgname=purify
true && pkgname=('purify-gtk' 'purify-qt')
pkgver=01
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('gtk2' 'qt>=4.7.0')
url="http://byuu.org/bsnes/"
license=('GPL3')
source=("http://bsnes.googlecode.com/files/bsnes_v091-source.tar.xz" "purify.desktop" "purify.sh")
sha256sums=('8c85a18ff44195d23b54cad53549152f034ee338e590907e8c8cbc3eaa7daf9f'
            '2709c20ff58b14e53b664df0ea39fa2b5349918c14ffbbc79fc636b9adb369d9'
            'cf667623b24ede0ae0a6a4445dc697a9c29fc8bc4f6190f4c3ca771c7a286446')

buildui() {
  make compiler=gcc platform=x phoenix=$1
  mv purify purify-$1
  make clean
}

installui() {
  cd "${srcdir}/bsnes_v091-source/purify"
  install -Dm 755 purify-$1 "${pkgdir}/usr/bin/purify-$1"
  install -m 755 "../../purify.sh" "${pkgdir}/usr/bin/purify-$1.sh"
  sed -i "s|_ui|$1|" "${pkgdir}/usr/bin/purify-$1.sh"
  install -dm 755 "${pkgdir}/usr/share/applications"
  install -m 644 "../../purify.desktop" "${pkgdir}/usr/share/applications/purify-$1.desktop"
  sed -i "s|_ui|$1|" "${pkgdir}/usr/share/applications/purify-$1.desktop"
}

build() {
  cd "${srcdir}/bsnes_v091-source/purify"

# QT 4.8.0 fix
  moc -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp

# Linker fix
  sed -i 's|gtk+-2.0|gtk+-2.0 x11|g' phoenix/Makefile
  sed -i 's|QtCore QtGui|QtCore QtGui x11|g' phoenix/Makefile

# Compile purify GTK GUI
  buildui gtk

# Compile purify QT GUI
  buildui qt
}

package_purify-gtk() {
  pkgdesc="ROM purifier for the higan emulator - GTK GUI"
  depends=('bsnes' 'gtk2')
  provides=('purify')

  installui gtk
}

package_purify-qt() {
  pkgdesc="ROM purifier for the higan emulator - QT GUI"
  depends=('bsnes' 'qt>=4.7.0')
  provides=('purify')

  installui qt
}

pkgdesc="ROM purifier for the higan emulator"
depends=()
