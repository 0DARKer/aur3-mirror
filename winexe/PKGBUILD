# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=winexe
pkgver=1.00
pkgrel=1
pkgdesc="Remotely execute commands on Windows NT/2000/XP/2003 systems"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/winexe/"
license=('GPL3')
depends=('gnutls' 'libsasl' 'popt' 'talloc' 'tdb')
makedepends=('python2')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz")
noextract=("$pkgname-$pkgver.tar.gz")
md5sums=('42a1710e259e4c2cb2a4e4a43466f441')

build() {
  cd "$srcdir"
  bsdtar -x -f "$pkgname-$pkgver.tar.gz"
  cd "$srcdir/$pkgname-$pkgver/source4"
  ./autogen.sh
  PYTHON_VER=2 ./configure --enable-fhs --prefix=/usr
  make basics
  make bin/winexe
}

package() {
  cd "$srcdir/$pkgname-$pkgver/source4"
  install -D -m755 bin/winexe "$pkgdir/usr/bin/winexe"
}

# vim:set ts=2 sw=2 et:
