# Maintainer: Jens Adam <jra@byte.cx>

_perlmod=Travel-Routing-DE-VRR
pkgname=perl-travel-routing-de-vrr
pkgver=2.04
pkgrel=1
pkgdesc='Unofficial efa.vrr.de command line client and Perl module'
url='http://finalrewind.org/projects/Travel-Routing-DE-VRR/'
license=('custom:WTFPL')
arch=('any')
depends=('perl-class-accessor' 'perl-exception-class' 'perl-libwww' 'perl-xml-libxml')
makedepends=('perl-file-slurp' 'perl-test-compile' 'perl-test-fatal' 'perl-test-pod')
options=('!emptydirs')
source=("http://finalrewind.org/projects/${_perlmod}/${_perlmod}-${pkgver}.tar.gz"
        'http://www.wtfpl.net/txt/copying/COPYING')
sha256sums=('1bef7af54fe2c87f96aad07e34fcff69bb356f9944505bd286757f21fc802f8d'
            '0356258391e190dc1d44ea01565cfe627fe44e27dad693a0a54c2483a7b223e5')

build() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  perl Build.PL installdirs=vendor destdir="${pkgdir}"
  ./Build
}

check() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  ./Build test
}

package() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  ./Build install
  install -Dm644 ../COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
  install -Dm644 provides/zsh/completions/_efa "${pkgdir}"/usr/share/zsh/site-functions/_efa
}
