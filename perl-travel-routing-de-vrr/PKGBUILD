# Maintainer: Jens Adam (byte/jra) <j_adam@web.de>

_perlmod=Travel-Routing-DE-VRR
pkgname=perl-travel-routing-de-vrr
pkgver=1.05
pkgrel=1
pkgdesc='Unofficial efa.vrr.de command line client and Perl module'
url='http://finalrewind.org/projects/Travel-Routing-DE-VRR/'
license=('custom:WTFPL')
arch=('any')
conflicts=('efa')
provides=('efa')
depends=('perl-class-accessor' 'perl-exception-class' 'perl-www-mechanize' 'perl-xml-libxml')
makedepends=('perl-test-compile' 'perl-test-fatal' 'perl-test-pod')
options=('!emptydirs')
source=("http://finalrewind.org/projects/${_perlmod}/${_perlmod}-${pkgver}.tar.gz"
        'http://sam.zoy.org/wtfpl/COPYING')
md5sums=('1d7104d7468f5091d54ec12a78841579'
         '389a9e29629d1f05e115f8f05c283df5')

build() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  perl Build.PL installdirs=vendor destdir="${pkgdir}"
  ./Build
}

check() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  ./Build test
}

package() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  ./Build install
  install -Dm644 ../COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
