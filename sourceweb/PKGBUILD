# vim: set shiftwidth=3:
# Maintainer: Adrian Bacircea <adrian.bacircea@gmail.com>
pkgname=sourceweb
pkgver=r377.b8a25dc
pkgrel=1
pkgdesc="Source code indexer and code navigation tool for C/C++ code."
arch=('i686' 'x86_64')
url="https://github.com/rprichard/sourceweb"
license=('BSD')
depends=('qt4' 'python' 'gcc-libs')
makedepends=('qt4')

ARCH=${CARCH}
[[ ${ARCH} = "i686" ]] && ARCH="x86"
SRC='https://s3.amazonaws.com/rprichard-released-software/clang-redist/release-1'
CLANG_REDIST="clang-3.2-1-$ARCH-linux.tar.bz2"
source=("${pkgname}::git+https://github.com/rprichard/sourceweb"
        "${SRC}/${CLANG_REDIST}"
        "clang3.2_gcc4.9.patch")
sha256sums=('SKIP'
            '51d135a5bef77fbb59c93c1f5715e0e8be9e332a2ff1fb512533fe884bbeb013'
            '8bfa70e45de802ae28d1584f242847272ff098e23b970e5e2222ad440cf3deb6')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
   cd "${CLANG_REDIST%.tar.bz2}"
   patch -p0 < ${srcdir}/clang3.2_gcc4.9.patch
}

build() {
   cd "${pkgname}"
   ./configure --prefix="${pkgdir}/usr" --with-clang-dir="${srcdir}/${CLANG_REDIST%.tar.bz2}"
   sed -i 's|-L/usr/lib||g' clang-indexer/Makefile
   make
}

package() {
   cd "${pkgname}"
   make install
}
