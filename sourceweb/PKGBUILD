# vim: set shiftwidth=3:
# Maintainer: Adrian Bacircea <adrian.bacircea@gmail.com>
pkgname=sourceweb
pkgver=r386.fbc2553
pkgrel=1
pkgdesc="Source code indexer and code navigation tool for C/C++ code."
arch=('i686' 'x86_64')
url="https://github.com/rprichard/sourceweb"
license=('BSD')
depends=('qt4' 'python' 'clang')
makedepends=('qt4')

source=("${pkgname}::git+https://github.com/adytzu2007/sourceweb")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
   cd "${pkgname}"

   if [[ "${CARCH}" = "x86_64" ]] && [[ -d "/usr/lib32" ]]; then
      # this is a hack to build the 32-bit version of libsw-btrace
      # on 64-bit multilib systems

      pushd "btrace"
      qmake-qt4  QMAKE_CC=/usr/bin/clang QMAKE_CXX=/usr/bin/clang++ \
                 QMAKE_LINK=/usr/bin/clang++ PREFIX=/usr CLANG_DIR=/usr \
                 -spec linux-g++-32 btrace.pro
      make
      popd
   fi

   ./configure --prefix="/usr"
   make clean && make
}

package() {
   cd "${pkgname}"
   make INSTALL_ROOT=${pkgdir} install

   if [[ "${CARCH}" = "x86_64" ]] && [[ -d "/usr/lib32" ]]; then
      cp -r "${srcdir}/${pkgname}/btrace/lib32" "${pkgdir}/usr"
   fi
}
