# Maintainer: Sam S. <smls75@gmail.com>
pkgname=mnemosyne-bzr
pkgver=2.0_beta10++
pkgrel=2
pkgdesc="A pyqt flash-card program with a sophisticated card review algorithm (latest development snapshot)"
arch=('i686' 'x86_64')
url='http://www.mnemosyne-proj.org'
license=('GPL')
depends=('python2-pyqt' 'python2-matplotlib' 'python2-cherrypy')
makedepends=('bzr' 'python2-distribute')
optdepends=('texlive-core: support for mathematical formulae in cards')
conflicts=('mnemosyne')
source=('python2_name.patch')
install='mnemosyne-bzr.install'
md5sums=('82ca643ef7cc34c5581e6bda60ce202c')

_bzrbranch=lp:mnemosyne-proj
_bzrmod=mnemosyne-proj

build() {
  cd ${srcdir}

  msg "Connecting to the server...."
  if [ ! -d ./${_bzrmod} ]; then
    bzr co ${_bzrbranch} ${_bzrmod}
  else
    bzr up ${_bzrmod}
  fi
  msg "BZR checkout done or server timeout."

  msg "Patching makefiles..."
  cd ${srcdir}/${_bzrmod}
  patch -p0 -i ${srcdir}/python2_name.patch

  msg "Starting setup..."
  cd ${srcdir}/${_bzrmod}/mnemosyne/mnemosyne/pyqt_ui
  make clean
  make
  pyrcc4 -o mnemosyne_rc.py mnemosyne.qrc
  cd ${srcdir}/${_bzrmod}/mnemosyne/po
  make
  cd ${srcdir}/${_bzrmod}/mnemosyne
  python2 setup.py install --root=${pkgdir}
}
