# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: A Rojas <nqn1976 @ gmail.com>

pkgname=krunner-git
pkgver=r42.f60014c
pkgrel=1
pkgdesc='Framework for providing different actions given a string query'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/krunner'
license=('LGPL')
depends=('plasma-framework-git' 'threadweaver-git')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
conflicts=('krunner')
provides=('krunner')
source=('git://anongit.kde.org/krunner.git')
md5sums=('SKIP')

pkgver() {
  cd krunner
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../krunner \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
