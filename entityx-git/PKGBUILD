# Maintainer: Scott Linder <scott.linder18@gmail.com>
pkgname=entityx-git
pkgver=101.dd01f4d
pkgrel=1
pkgdesc="A fast, type-safe C++ Entity Component System"
arch=('i686' 'x86_64')
url="https://github.com/alecthomas/entityx"
license=('MIT')
depends=('gcc-libs-multilib')
makedepends=('git' 'gcc>=4.7' 'cmake')
provides=('entityx')
conflicts=('entityx')
_gitroot=github.com/alecthomas
_gitname=entityx
source=("git://$_gitroot/$_gitname")
md5sums=('SKIP')


pkgver() {
    cd "$srcdir/$_gitname"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
    cd "$srcdir/$_gitname"
    # Build with testing enabled for check(); important because repo is active
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DENTITYX_BUILD_SHARED=1 \
        -DENTITYX_BUILD_TESTING=1
    make
}

check() {
    cd "$srcdir/$_gitname"
    make test
}

package() {
    cd "$srcdir/$_gitname"
    make DESTDIR="$pkgdir/" install
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=4 sw=4 et:
