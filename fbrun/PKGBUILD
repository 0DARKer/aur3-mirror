# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Fabio Linux <fbo.linux [at] gmail [dot] com>

pkgname=fbrun
pkgver=1.5
pkgrel=5
pkgdesc="A simple run utility from Fluxbox"
arch=('i686' 'x86_64')
url="http://www.fluxbox.org/"
license=('MIT')
depends=('gcc-libs' 'imlib2>=1.0.0' 'libxft' 'libxpm' 'libxrandr' 'libxinerama')
makedepends=('pkgconfig')
conflicts=('fluxbox')
source=(http://downloads.sourceforge.net/fluxbox/fluxbox-1.1.1.tar.bz2
        fbrun-$pkgver.diff)
md5sums=('fa9fa8fe9a44f86522de5754f8b285ca'
         '6cf8ae5bc1fb9496217e008c028eef29')

build() {
  cd "$srcdir/fluxbox-1.1.1"
  patch -Np1 -i ../fbrun-$pkgver.diff || return 1
  ./configure --prefix=/usr \
      --enable-kde --enable-gnome \
      --enable-xft --enable-xinerama \
      --enable-imlib2 --enable-nls
  make -C util/fbrun || return 1
  install -Dm755 util/fbrun/fbrun "$pkgdir/usr/bin/fbrun"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/fbrun/COPYING"
}
