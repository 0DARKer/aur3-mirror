# Maintainer: Sam S. <smls75@gmail.com>

pkgname=tinyandbig-hib
pkgver=1.4_20130604
_hibver=1.4_Linux_1370288461
pkgrel=1
pkgdesc="Tiny & Big: Grandpa's Leftovers - a 3D jump-and-slice platformer (Humble Bundle version)"
url='http://www.tinyandbig.com/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('sdl' 'libgl' 'nvidia-cg-toolkit' 'openal')
options=('!strip' '!upx')
PKGEXT='.pkg.tar'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or setup hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

_archive="TinyAndBig_${_hibver}.deb"
source=("hib://${_archive}" "tinyandbig-hib.desktop")
md5sums=('ac8df6e52b199176fc238d72b030e695' '8a852ad5cac241c5e871b3da67dd0c78')

package() {
  cd $pkgdir

  # Install game files
  bsdtar xf "$srcdir/data.tar.lzma"
  mkdir -p "opt/"
  mv "usr/local/games/tinyandbig-episode1" "opt/"

  # Remove files that are not needed
  [ $CARCH == "i686" ] && _arch=32 && _other=64 || _arch=64 && _other=32
  rm "opt/tinyandbig-episode1/"{tinyandbig-launcher,license.txt}
  rm "opt/tinyandbig-episode1/bin$_arch"/{libCg.so,libCgGL.so}
  rm -r "opt/tinyandbig-episode1/bin$_other"
  rm -r "usr/share/"{lintian,applications}

  # Install launch script
  echo -e "#!/bin/sh\ncd /opt/tinyandbig-episode1/ && ./bin$_arch/tinyandbig" \
        > "$srcdir/launcher"
  install -Dm755 "$srcdir/launcher" "usr/bin/tinyandbig-episode1"

  # Install desktop entry
  install -Dm644 "$srcdir/$pkgname.desktop" \
                 "usr/share/applications/$pkgname.desktop"

  # Install license & readme
  install -Dm644 "license" "usr/share/licenses/$pkgname/license"
  install -Dm644 "README" "usr/share/doc/$pkgname/license.txt"
  rm {license,README}
}
