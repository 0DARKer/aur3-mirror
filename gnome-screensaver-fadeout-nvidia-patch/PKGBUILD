# $Id: PKGBUILD 117764 2011-04-04 17:44:42Z heftig $
# Maintainer: aji <ajiproduction gmail com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgname=gnome-screensaver
pkgname=gnome-screensaver-fadeout-nvidia-patch
pkgver=3.0.0
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the GNOME desktop."
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/GnomeScreensaver"
backup=(etc/pam.d/gnome-screensaver)
depends=('dbus-glib' 'gnome-menus' 'libgnomekbd' 'gnome-desktop' 'gsettings-desktop-schemas')
makedepends=('pkgconfig' 'intltool' 'libxss')
optdepends=('xscreensaver')
groups=('gnome')
options=(!emptydirs)
conflicts=("${_pkgname}")
provides=("${_pkgname}")
install=gnome-screensaver.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.bz2
       gnome-screensaver.pam
       fadeout-with-nvidia.patch)
sha256sums=('e00e801d94c1d9f1e62049f77b91d822047eb1dcbd2e01cc38b5dd1b0f67c948'
            '2744d1fc39da46fc681b7cf1c9230d2035b2bb8d9f510213fbe439c114eb76a2'
            '847a55736bc8231bc3dc5ea2a01a3d8d7a543a389841906cd95530554e65f0cf')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fadeout-with-nvidia.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-screensaver \
      --localstatedir=/var \
      --with-mit-ext

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/gnome-screensaver.pam" "${pkgdir}/etc/pam.d/gnome-screensaver"
}
