# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Bernardo Barros <bernardobarros dot gmail dot com>
# Contributor: Maciej Ciemborowicz <moonkey@op.pl>

pkgname=qsampler-svn
pkgver=0.2.2.33.527
pkgrel=1
pkgdesc="Qt frontend to LinuxSampler"
arch=('i686' 'x86_64')
url="http://www.linuxsampler.org/"
license=('GPL')
depends=('qt4' 'liblscp-svn' 'linuxsampler-svn')
makedepends=('svn')
optdepends=('gigedit: edit gigasampler files')
provides=('qsampler')
conflicts=('qsampler')
install="$pkgname.install"
source=("$pkgname::svn+https://qsampler.svn.sourceforge.net/svnroot/qsampler/trunk")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  echo $(sed -n 's/AC_INIT(Qsampler, \([^,]*\).*/\1/p' configure.ac).$(svnversion)
}

build() {
  cd "$srcdir/$pkgname"
  make -f Makefile.svn
  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir/" install
}

