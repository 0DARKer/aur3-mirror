# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Mathieu Clabaut <mathieu.clabaut@gmail.com>
pkgname=thinkingrock
pkgver=2.2.1
pkgrel=4
pkgdesc="Java-based GTD application"
arch=('any')
url="http://www.thinkingrock.com.au"
license=('CDDL')
depends=('java-runtime>=6' 'xdg-utils' 'hicolor-icon-theme')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/thinkingrock/tr-$pkgver.tar.gz"
        "$pkgname.desktop"
        'dirstructure.patch'
        "$pkgname.png"
        "$pkgname.install")
md5sums=('a3d1405559becd7796eb931f1a324ea6'
         '9b0d8b36148546659e6503bf9d0f7695'
         'ca5cadae91c3b28802ea4910ee3b836a'
         '6a007d8d489027a199835a2bfc16308b'
         '9d5fa3d242cec4ce4f0635fbfeec9696')         
build() {
  cd "$srcdir/tr-$pkgver"
  patch -Np1 < ../dirstructure.patch
  install -m755 -d "$pkgdir/opt/$pkgname"
  install -m755 -d "$pkgdir/usr/share/pixmaps"
  cp -r etc ide10 platform9 tr "$pkgdir/opt/$pkgname"
  rm -f "$pkgdir/opt/$pkgname/etc/tr.conf~"
  install -D -m644 "$srcdir/$pkgname.desktop" \
      "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -D -m644 bin/tr.png \
      "$pkgdir/usr/share/icons/hicolor/48x48/apps/$pkgname.png"
  install -D -m644 "../$pkgname.png" \
      "$pkgdir/usr/share/icons/hicolor/128x128/apps/$pkgname.png"
  ln -s "../icons/hicolor/128x128/apps/$pkgname.png" \
      "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -D -m755 bin/tr "$pkgdir/usr/bin/$pkgname"
}
# vim:set ts=2 sw=2 et:
