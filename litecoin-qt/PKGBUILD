# Maintainer: Lo√Øc Guitaut <flink at belfalas dot eu>

pkgname='litecoin-qt'
pkgver=0.6.3c
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.litecoin.org/"
makedepends=('boost' 'automoc4' 'qrencode' 'miniupnpc')
license=('MIT')
pkgdesc="Peer-to-peer network based digital currency (QT)"
depends=(boost-libs qt4 miniupnpc qrencode)
conflicts=(litecoin)
install=litecoin-qt.install
source=("https://github.com/litecoin-project/litecoin/archive/v$pkgver.tar.gz"
        "$pkgname.desktop")
sha256sums=('f566d24a1c05392329fbaabf3d09f8244d2da52de320ede4d8476f1f1f835bac'
            '65fd2e3bbefdb77d955446cc94b54d5f79644dea93d4c9a1425ea01ee14c902a')

build() {
  cd "$srcdir/litecoin-$pkgver"

  # and make qt gui
  qmake-qt4 USE_QRCODE=1 USE_UPNP=1
  make
}


package() {
  install -Dm644 litecoin-qt.desktop "$pkgdir"/usr/share/applications/litecoin.desktop
  cd "$srcdir/litecoin-$pkgver"
  install -Dm755 litecoin-qt "$pkgdir"/usr/bin/litecoin-qt
  install -Dm644 share/pixmaps/bitcoin80.xpm "$pkgdir"/usr/share/pixmaps/litecoin80.xpm

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

