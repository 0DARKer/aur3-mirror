# Maintainer: Julien Quintard <mycure@gmail.com>

pkgname=mingw32-openssl
pkgver=1.0.0f
pkgrel=2
arch=('any')
pkgdesc="Toolkit implementing the Secure Sockets Layer (SSL v2/v3) and Transport Layer Security (TLS v1) protocols. Mingw32 build."
depends=('mingw32-runtime' 'mingw32-zlib')
options=(!strip !buildflags)
license=('BSD')
url="http://www.openssl.org"
source=(http://openssl.org/source/openssl-$pkgver.tar.gz)
md5sums=('e358705fb4a8827b5e9224a73f442025')

build()
{
  cd $srcdir/openssl-$pkgver

  ./Configure --cross-compile-prefix=i486-mingw32- \
              -DOPENSSL_NO_CAPIENG \
              --prefix=/usr/i486-mingw32 mingw shared zlib-dynamic

  make
  make INSTALL_PREFIX=$pkgdir install
  install -m755 -o 0 -g 0 libeay32.dll ssleay32.dll \
    $pkgdir/usr/i486-mingw32/bin/

  cd $pkgdir
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/{bin/c_rehash,ssl/misc,ssl/man}
}
