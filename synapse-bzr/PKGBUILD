# Contributor: Ignacio Losiggio <naquo@geneu.co.cc>
# Maintainer: Ner0

pkgname=synapse-bzr
pkgver=501
pkgrel=1
pkgdesc="A semantic launcher that you can use to start applications as well as find and access relevant documents"
arch=('i686' 'x86_64')
url="http://launchpad.net/synapse-project"
license=('GPL3')
depends=('dbus' 'dbus-glib' 'glibc' 'gtkhotkey' 'hicolor-icon-theme' 'json-glib'
         'libgee' 'libnotify' 'libunique' 'libzeitgeist' 'zeitgeist' 'zeitgeist-datahub')
makedepends=('bzr' 'gnome-common' 'vala')
optdepends=('rythmbox: Rythmbox support'
            'banshee: Banshee support'
            'pastebinit: pastebin support'
            'bc: calculator support'
            'zeitgeist-extensions: For best integration [AUR]'
            'rest: RESTful services support')
conflicts=('synapse')
provides=('synapse')
install=$pkgname.install

_bzrtrunk=lp:synapse-project
_bzrmod=synapse-project

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build

  #sed -i 's/--pkg zeitgeist-1.0 //' src/ui/Makefile.am
  #sed -i '/zeitgeist-1.0 is not installed/d' configure.ac

  CFLAGS="$CFLAGS -lm"
  ./autogen.sh --prefix=/usr --enable-zeitgeist=yes
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"
  make DESTDIR="$pkgdir/" install
}
