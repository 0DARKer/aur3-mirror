# Maintainer: ValHue <vhuelamo at gmail dot com>
# https://github.com/ValHue/AUR-PKGBUILDs
#
# Contributor: Ignacio Losiggio <naquo at geneu dot co dot cc>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>

_pkgname="synapse"
pkgname="${_pkgname}-bzr"
pkgver=507
pkgrel=3
pkgdesc="A semantic file launcher"
arch=('i686' 'x86_64')
url="http://launchpad.net/synapse-project"
license=('GPL3')
depends=('gtkhotkey' 'hicolor-icon-theme' 'json-glib' 'libgee06' 'libnotify' 'libunique' 'libzeitgeist' 'rest' 'xdg-utils')
makedepends=('bzr' 'gnome-common' 'intltool' 'vala')
optdepends=('banshee: banshee plugin'
            'bc: calculator plugin'
            'devhelp: documentation plugin'
            'gnome-screensaver: screensaver plugin'
            'gnome-dictionary: dictionary plugin'
            'openssh: ssh plugin'
            'pastebinit: pastebin plugin'
            'rhythmbox: rhythmbox plugin'
            'xnoise: xnoise plugin'
            'zeitgeist: zeitgeist plugin'
            'zeitgeist-extensions: For best integration [AUR]')
conflicts=('synapse')
provides=('synapse')
install="${pkgname}.install"
source=("${_pkgname}::bzr+http://bazaar.launchpad.net/~synapse-core/synapse-project/trunk/")
md5sums=('SKIP')

pkgver() {
    cd "${_pkgname}"
    bzr revno
}

build() {
    cd "${_pkgname}"
    CFLAGS="$CFLAGS -lm"

    ./autogen.sh \
    --prefix=/usr \
    --enable-zeitgeist=yes

    make ${MAKEFLAGS}
}

package() {
    cd "${_pkgname}"
    make DESTDIR="${pkgdir}" install
}