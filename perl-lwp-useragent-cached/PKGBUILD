# Maintainer: Moritz Bunkus <moritz@bunkus.org>
pkgname='perl-lwp-useragent-cached'
pkgver='0.04'
pkgrel='1'
pkgdesc="LWP::UserAgent with simple caching mechanism"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-libwww')
makedepends=('perl-test-mock-lwp-dispatch')
url='https://metacpan.org/release/LWP-UserAgent-Cached'
source=("http://cpan.metacpan.org/authors/id/O/OL/OLEG/LWP-UserAgent-Cached-${pkgver}.tar.gz")
md5sums=('fa3a91c52351509ad249f50171764e9c')
sha512sums=('6acb8e321225573748affcf36c48b7689ab6c4b2846fa131a15e1b71b199d06c727d5d68185afaccfdc3bf11a2ba293aaade40973ef931b48a19468233d9755e')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/LWP-UserAgent-Cached-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
