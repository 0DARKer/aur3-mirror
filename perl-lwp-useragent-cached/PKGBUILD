# Maintainer: Moritz Bunkus <moritz@bunkus.org>
pkgname='perl-lwp-useragent-cached'
pkgver='0.05'
pkgrel='1'
pkgdesc="LWP::UserAgent with simple caching mechanism"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-libwww')
makedepends=('perl-test-mock-lwp-dispatch')
url='https://metacpan.org/release/LWP-UserAgent-Cached'
source=("http://cpan.metacpan.org/authors/id/O/OL/OLEG/LWP-UserAgent-Cached-${pkgver}.tar.gz")
md5sums=('b79ef44eaa6541f28496ff8eef409893')
sha512sums=('0aa072cbfbfa5742649c0412f48e21c5e97b396a059a3ba783b448f04a311c315786e1cef3db7ea4b41beeaf0a404cc3317f23d6d87d72d2f1f375af23e269de')

prepare_environment() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/LWP-UserAgent-Cached-${pkgver}"
}

build() {
  prepare_environment
  /usr/bin/perl Makefile.PL
  make
}

check() {
  prepare_environment
  make test
}

package() {
  prepare_environment
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
