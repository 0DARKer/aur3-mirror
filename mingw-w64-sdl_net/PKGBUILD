pkgname=mingw-w64-sdl_net
pkgver=1.2.8
pkgrel=2
pkgdesc="A small sample cross-platform networking library (mingw-w64)"
arch=(any)
url="http://libsdl.org/projects/SDL_net"
license=("zlib")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-sdl)
options=(!libtool !strip !buildflags)
source=("http://www.libsdl.org/projects/SDL_net/release/SDL_net-$pkgver.tar.gz")
md5sums=('20e64e61d65662db66c379034f11f718')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/SDL_net-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
