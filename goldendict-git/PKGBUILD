# Contributor: Eugene Yudin aka Infy <Eugene dot Yudin at gmail dot com>
# Maintainer:  VirtualTam <virtualtam[at]myopera[dot]com>
pkgname=goldendict-git
pkgver=1.5.0.RC.35.g195e284
pkgrel=1
pkgdesc="Feature-rich dictionary lookup program."
arch=('i686' 'x86_64')
url="http://goldendict.org/"
license=('GPL3')
depends=('hunspell' 'libxtst' 'qt4' 'qtwebkit' 'zlib' 'libvorbis' 'phonon' 'libao')
makedepends=('git')
conflicts=('goldendict' 'goldendict-svn' 'goldendict-git-opt')
provides=('goldendict')
replaces=('goldendict' 'goldendict-svn' 'goldendict-git-opt')
_gitname="goldendict"
source=(git://github.com/goldendict/goldendict.git
	goldendict-paths.diff)
sha256sums=(SKIP
	    baffe21e373bf26a5acb8b9bab4dbf78849d0f02f041c99b790b27b6e8c1ae77)

pkgver(){
  cd ${srcdir}/${_gitname}
  git describe --always | sed 's|-|.|g'
}

build(){
  cd ${srcdir}/${_gitname}
  msg "Starting make..."

  #Patch installation paths
  msg "Patching installation directory paths"
  patch goldendict.pro ../goldendict-paths.diff

  #Fixing flags
  echo "QMAKE_CXXFLAGS_RELEASE = $CFLAGS" >> goldendict.pro
  echo "QMAKE_CFLAGS_RELEASE = $CXXFLAGS" >> goldendict.pro

  #Localization workaround
  msg "Fixing locales"
  cd locale
  for l in `ls *.ts`; do
      lrelease-qt4 ${l} -qm `basename ${l} .ts`.qm
  done
  cd ..

  #Building
  qmake-qt4 || return 1
  make  || return 1
}
package() {
  cd ${srcdir}/${_gitname}
  make INSTALL_ROOT=${pkgdir} install || return 1

  #Removing unneeded
  rm -rf ${pkgdir}/usr/share/app-install || return 1
}
