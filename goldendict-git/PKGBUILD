# Contributor: Eugene Yudin aka Infy <Eugene dot Yudin at gmail dot com>
# Maintainer:  VirtualTam <virtualtam[at]myopera[dot]com>
pkgname=goldendict-git
pkgver=1.0.1.718.g663d0ee
pkgrel=1
pkgdesc="Feature-rich dictionary lookup program."
arch=('i686' 'x86_64')
url="http://goldendict.org/"
license=('GPL3')
depends=('hunspell' 'libxtst' 'qt4' 'qtwebkit' 'zlib' 'libvorbis' 'phonon')
makedepends=('git')
conflicts=('goldendict' 'goldendict-svn' 'goldendict-git-opt')
provides=('goldendict')
replaces=('goldendict' 'goldendict-svn' 'goldendict-git-opt')
_gitname="goldendict"
source=(git://github.com/goldendict/goldendict.git)
sha512sums=(SKIP)

pkgver(){
  cd ${srcdir}/${_gitname}
  git describe --always | sed 's|-|.|g'
}

build(){
  cd ${srcdir}/${_gitname}
  msg "Starting make..."

  #Fixing flags
  echo "QMAKE_CXXFLAGS_RELEASE = $CFLAGS" >> goldendict.pro
  echo "QMAKE_CFLAGS_RELEASE = $CXXFLAGS" >> goldendict.pro

  #Localization workaround
  msg "Fixing locales"
  cd locale
  for l in `ls *.ts`; do
      lrelease-qt4 ${l} -qm `basename ${l} .ts`.qm
  done
  cd ..

  #Building
  qmake-qt4 || return 1
  make  || return 1
}
package() {
  cd ${srcdir}/${_gitname}
  make INSTALL_ROOT=${pkgdir} install || return 1

  #removing unneeded
  rm -rf ${pkgdir}/usr/share/app-install || return 1

}
