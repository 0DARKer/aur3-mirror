# Maintainer: kevku <kevku@msn.com>
pkgname=roccat
pkgver=0.5.2
pkgrel=1
pkgdesc="Library with functionality shared by all ROCCAT device specific packages"
arch=('i686' 'x86_64')
url="http://roccat.sourceforge.net"
license=('GPL2')
depends=('gtk2' 'udev' 'libcanberra' 'libnotify' 'hicolor-icon-theme')
makedepends=('cmake')
optdepends=('kmod-roccat: drivers for roccat devices')
install=('roccat.install')
source=("http://downloads.sourceforge.net/project/roccat/roccat/roccat-$pkgver.tar.bz2")
md5sums=('b97b8ce9bc7387cb5f42108408888a9e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|SET(LIBDIR lib64)|SET(LIBDIR lib)|g' cmake_modules/X86.cmake
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

