pkgname=d2x-xl-data
pkgver=1.16.34
pkgrel=1
pkgdesc="Required D2X-XL data and Descent 1 and 2 retail data."
arch=(any)
url="http://www.descent2.de"
license=("custom, Public Domain")
makedepends=(dos2unix innoextract unrar graphicsmagick mmv)
source=("http://downloads.sourceforge.net/d2x-xl/d2x-xl-data-$pkgver.rar")
noextract=("d2x-xl-data-$pkgver.rar")
md5sums=('005ac393032bfd414f7d9f43bc0a3d90')

prepare() {
	cd "$srcdir"
	_gog_md5="a16f246783339962eb5bc0d6e630952b"
	_gog_exe="setup_descent_1_2.exe"
	unrar x d2x-xl-data-$pkgver.rar
	if [[ -f ../$_gog_exe ]]; then
		echo "GOG installer detected; checking md5sum ..."
		if ! echo "$_gog_md5 ../$_gog_exe" | md5sum -c --status; then
			error "Invalid md5sum; verify your download and try again."
			return 1
		else
			ln -s ../$_gog_exe .
			innoextract $_gog_exe
		fi
	else
		error "You must have $_gog_exe present (.exe in main dir)."
		error "Download the game from your GOG shelf and try again."
		return 1
	fi
	find . -type f -exec dos2unix {} \;
	cd "app/Descent 2"
	gm convert descent2.ico descent2.png
}
package() {
	cd "$srcdir"
	folders="config data models profiles sounds textures"
	mkdir -p "$pkgdir/opt/d2x-xl/"
	for f in $folders; do
		cp -r $f "$pkgdir/opt/d2x-xl/"
		chmod -R 644 "$pkgdir/opt/d2x-xl/$f"
	done
	ln -s "$pkgdir/opt/d2x-xl/data/descent.tex.eng" "$pkgdir/opt/d2x-xl/data/descent.tex"
	
	cd app/Descent
	d1main="DESCENT.HOG DESCENT.PIG"
	for f in $d1main; do
		install -m644 $f "$pkgdir/opt/d2x-xl/data/$f"
	done
	find . -name 'CHAOS.*' -exec install -Dm644 {} "$pkgdir/opt/d2x-xl/missions/"{} \;
	find . -name '*.DEM' -exec install -Dm644 {} "$pkgdir/opt/d2x-xl/demos/"{} \;
	
	cd "../Descent 2"
	install -Dm644 descent2.png "$pkgdir/usr/share/icons/descent2.png"
	find . -name '*.CFG' -exec install -m644 {} "$pkgdir/opt/d2x-xl/config/" \;
	find . -name '*.INI' -exec install -m644 {} "$pkgdir/opt/d2x-xl/config/" \;
	find . -name '*.PIG' -exec install -m644 {} "$pkgdir/opt/d2x-xl/data/" \;
	find . -name '*.MVL' -exec install -Dm644 {} "$pkgdir/opt/d2x-xl/movies/"{} \;
	d2main="DESCENT2.HAM DESCENT2.HOG DESCENT2.S11 DESCENT2.S22"
	for f in $d2main; do
		install -m644 $f "$pkgdir/opt/d2x-xl/data/$f"
	done
	install -m644 DEMOS/descent2.dem "$pkgdir/opt/d2x-xl/demos/"
	cd MISSIONS
	find . -type f -exec install -m644 {} "$pkgdir/opt/d2x-xl/missions/" \;
	folders2="config data demos missions movies"
	for f in $folders2; do cd "$pkgdir/opt/d2x-xl/$f" && mmv "*" "#l1"; done
}
# vim:syntax=sh
