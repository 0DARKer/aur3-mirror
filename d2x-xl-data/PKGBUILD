pkgname=d2x-xl-data
pkgver=1.17.34
pkgrel=1
pkgdesc="Required D2X-XL data, extras, and Descent 1 and 2 retail data."
arch=(any)
url="http://www.descent2.de"
license=("custom, Public Domain")
depends=(d2x-xl)
makedepends=(dos2unix innoextract graphicsmagick p7zip)
source=("http://www.descent2.de/files/d2x-xl-data-$pkgver.7z"
"http://downloads.sourceforge.net/d2x-xl/D1-music.7z"
"http://downloads.sourceforge.net/d2x-xl/D2-music.7z"
"http://downloads.sourceforge.net/d2x-xl/loading-screens.7z"
"http://downloads.sourceforge.net/d2x-xl/hires-sounds.7z"
"http://downloads.sourceforge.net/d2x-xl/D1-textures-512x512.7z"
"http://downloads.sourceforge.net/d2x-xl/D2-textures-512x512.7z"
"http://downloads.sourceforge.net/d2x-xl/hires-models.7z")
sha1sums=('16b6c1757d4d729efc0e13c111ca5de0d0ff1f34'
          'ba7206b1c27bab7ad622c9547026d503a94cbde4'
          '459069c83378d702531c6635265c8d6a397043e5'
          'd7a53161967b086ef29fc3cdc85071b534b7af6c'
          '199c6aacf52091362d43cac9fc94bca31d053ae9'
          '8ed4bdd6fc1f81e0a2e4c599f3ad6915cb45e8c0'
          'c03fece50545336bddb25e9611dd29d0eb418e53'
          '42c081e631fa387841a12dcd1e21a33fab94fc9e')

prepare() {
	cd "$srcdir"
	_gog_md5="94e576d4be579a79be7845729f57ad8a"
	_gog_exe="setup_descent12_2.0.0.7.exe"
	if [[ -f ../$_gog_exe ]]; then
		echo "GOG installer detected; checking md5sum ..."
		if ! echo "$_gog_md5 ../$_gog_exe" | md5sum -c --status; then
			error "Invalid md5sum; verify your download and try again."
			return 1
		else
			ln -s ../$_gog_exe .
			innoextract $_gog_exe
		fi
	else
		error "You must have $_gog_exe present (.exe in main dir)."
		error "Download the game from your GOG shelf and try again."
		return 1
	fi
	find . -type f -exec dos2unix {} \;
	cd "app/Descent 2"
	gm convert descent2.ico descent2.png
}

package() {
	cd "$srcdir"
	folders="config data models sounds textures"
	mkdir -p "$pkgdir/usr/share/d2x-xl/"
	for f in $folders; do
		cp -r $f "$pkgdir/usr/share/d2x-xl/"
		chmod -R 644 "$pkgdir/usr/share/d2x-xl/$f"
	done
	ln -s "$pkgdir/usr/share/d2x-xl/data/descent.tex.eng" "$pkgdir/usr/share/d2x-xl/data/descent.tex"
	folders2="models mods music sounds textures"
	for f in $folders2; do
		cp -r $f "$pkgdir/usr/share/d2x-xl/"
		chmod -R 644 "$pkgdir/usr/share/d2x-xl/$f"
	done

	cd app/Descent
	d1main="DESCENT.HOG DESCENT.PIG"
	for f in $d1main; do
		install -m644 $f "$pkgdir/usr/share/d2x-xl/data/$f"
	done
	find . -name 'CHAOS.*' -exec install -Dm644 {} "$pkgdir/usr/share/d2x-xl/missions/"{} \;

	cd "../Descent 2"
	install -Dm644 descent2.png "$pkgdir/usr/share/icons/descent2.png"
	install -m644 DESCENT.CFG "$pkgdir/usr/share/d2x-xl/config/descent.cfg"
	find . -name '*.PIG' -exec install -m644 {} "$pkgdir/usr/share/d2x-xl/data/" \;
	find . -name '*.MVL' -exec install -Dm644 {} "$pkgdir/usr/share/d2x-xl/movies/"{} \;
	d2main="DESCENT2.HAM DESCENT2.HOG DESCENT2.S11 DESCENT2.S22"
	for f in $d2main; do
		install -m644 $f "$pkgdir/usr/share/d2x-xl/data/$f"
	done
	cd MISSIONS
	find . -type f -exec install -m644 {} "$pkgdir/usr/share/d2x-xl/missions/" \;
	find "$pkgdir" -name '*.txt' -exec rm {} \;
	cd "$pkgdir/usr/share/d2x-xl"
	mv data/ALIEN1.PIG data/alien1.pig
	mv data/ALIEN2.PIG data/alien2.pig
	mv data/DESCENT.HOG data/descent.hog
	mv data/DESCENT.PIG data/descent.pig
	mv data/DESCENT2.HAM data/descent2.ham
	mv data/DESCENT2.HOG data/descent2.hog
	mv data/DESCENT2.S11 data/descent2.s11
	mv data/DESCENT2.S22 data/descent2.s22
	mv data/FIRE.PIG data/fire.pig
	mv data/GROUPA.PIG data/groupa.pig
	mv data/ICE.PIG data/ice.pig
	mv data/WATER.PIG data/water.pig
	mv missions/CHAOS.HOG missions/chaos.hog
	mv missions/CHAOS.MSN missions/chaos.msn
	mv movies/INTRO-H.MVL movies/intro-h.mvl
	mv movies/OTHER-H.MVL movies/other-h.mvl
	mv movies/ROBOTS-H.MVL movies/robots-h.mvl
	mv movies/ROBOTS-L.MVL movies/robots-l.mvl
	find $pkgdir -type d -exec chmod 755 {} \;
}
# vim:syntax=sh
