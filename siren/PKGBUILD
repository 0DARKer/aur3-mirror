# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=siren
pkgver=0
pkgrel=1
pkgdesc="A text-based audio player"
arch=(i686 x86_64)
url="http://www.kariliq.nl/siren/"
license=('custom:ISC')
depends=('ncurses')
makedepends=('libao' 'libpulse' 'libid3tag' 'libmad')
optdepends=('libao: audio output support'
            'libpulse: pulseaudio support'
            'libid3tag: id3 tags support'
            'libmad: mpeg decoder support')
source=(${url}dist/${pkgname}-${pkgver}.tar.gz)
md5sums=('09bb8198134c724c16a979d176c08dfe')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export LDFLAGS="$LDFLAGS -ldl"

  ./configure prefix=/usr \
              mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # license
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n15 $pkgname.h > \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
