# Maintainer: Xyne <ca dot archlinux at xyne, backwards>
pkgname=qd
pkgver=2.3.13
pkgrel=2
pkgdesc='C++/Fortran-90 double-double and quad-double library'
arch=('i686' 'x86_64')
url="http://crd-legacy.lbl.gov/~dhbailey/mpdist/"
license=('BSD')
source=(
  "http://crd.lbl.gov/~dhbailey/mpdist/$pkgname-$pkgver.tar.gz"
  "dd.h"
  "qd.h"
)
build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-shared --with-gnu-ld
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install DESTDIR="$pkgdir"
  install -Dm644 "$srcdir/qd.h" "$pkgdir/usr/include/qd/qd.h"
  install -Dm644 "$srcdir/dd.h" "$pkgdir/usr/include/qd/dd.h"
}

sha512sums=('40b1c400544e3c6306bec58b90561dec7781e80caa3e53eebd8a77933447d2f2aeb44599ff0bc19528452c7486f34bb3876e5d9581f4c9963692c99689f4451f'
            '9cf6e91745ca474ea890c0a85889ec387947416049142432c5ccf28a3643d09f02975426cbd7685aac648e874b7238832f4375a2b77a40ae736ec3f337a76fd0'
            '82ab4bd78438302b6e4dc9cba3a86812908a896b394b0ea3a57caf8ae03e58f28a7184b6532ff24286b80916e48abb8c5a63d5b9529a09b457ed8770f1d973f5')
