# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=istanbul
pkgver=0.2.2
pkgrel=6
pkgdesc='Desktop Session Recorder'
arch=('i686' 'x86_64')
url='http://live.gnome.org/Istanbul'
license=('GPL')
options=('!libtool')
depends=(
  'pygtk'
  'gnome-python-extras'
  'gstreamer0.10'
  'gstreamer0.10-python'
  'gstreamer0.10-base-plugins'
  'python2-gconf'
  'python-xlib'
  'libxdamage'
  'libxext'
  'libsm'
)
makedepends=(
  'intltool'
  'gettext'
)
install=istanbul.install
source=(
  "http://zaheer.merali.org/istanbul-$pkgver.tar.bz2"
  'python2.patch'
)
md5sums=(
  '8ddcfd5a29dcd10fdafc10af9f66848b'
  'f68801fbc1e6063721e6ed72be45429c'
)
sha1sums=(
  'f9649990429ef4fb083057685fec723c466ee4b0'
  'b3e667582c28a139b1edd840e7300dadd25e5fee'
)

build() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../python2.patch
  ./configure --prefix=/usr --disable-schemas-install --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$pkgname-$pkgver"
  make prefix="${pkgdir}/usr" install

  install -d -m 755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/usr/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/usr/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 ft=sh et:
