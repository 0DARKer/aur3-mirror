# Maintainer: Filip Brcic <brcha@gna.org>

pkgname='android-qt5-qtbase'
pkgver=5.1.0_rc2
pkgrel=1
pkgdesc="Qt 5 for Android: qtbase module"
arch=('i686' 'x86_64')
url='http://qt-project.org/wiki/Qt5ForAndroid'
license=('GPL3' 'LGPL')
makedepends=('libgl' 'sqlite' 'zlib' 'python2' 'ruby' 'gperf' 'libxslt' 'fontconfig')
depends=('android-sdk' 'android-sdk-platform-tools' 'android-sdk-build-tools' 'android-ndk-necessitas' 'jdk7-openjdk' 'android-platform-10')
groups=('qt-android' 'qt5-android')
options=('!libtool' '!strip')
optdepends=('qtcreator: Qt IDE with support for Android')
_pkgver="${pkgver/_/-}"
_pkgfqn="qtbase-opensource-src-${_pkgver}"
source=("http://download.qt-project.org/development_releases/qt/5.1/${_pkgver}/submodules/${_pkgfqn}.tar.xz")
md5sums=('3bbe94b69e6df69329bf33de5c4b64f7')

build() {
  cd ${srcdir}/${_pkgfqn}

  unset CC
  unset CXX
  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  unset CARCH
  unset CHOST
  unset QMAKESPEC
  unset QTDIR

  ./configure -confirm-license -opensource \
    -developer-build \
    -xplatform android-g++ \
    -android-ndk /opt/android-ndk \
    -android-sdk /opt/android-sdk \
    -prefix /opt/android-qt5/${_pkgver} \
    -docdir /usr/share/doc/android-qt5-${_pkgver} \
    -nomake examples \
    -nomake docs \
    -nomake tests \
    -no-rpath

  make
}

package() {
  cd ${srcdir}/${_pkgfqn}
  make INSTALL_ROOT="${pkgdir}" install

  # Strip by hand, system strip doesn't work on arm binaries
  
  # Linux binaries in bin
  strip ${STRIP_BINARIES} ${pkgdir}/opt/android-qt5/${_pkgver}/bin/{moc,qdoc,qmake,rcc,uic} # can't use * because of syncqt
  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/${_pkgver}/lib/*.so* \
    ${pkgdir}/opt/android-qt5/${_pkgver}/plugins/*/*.so \
    ${pkgdir}/opt/android-qt5/${_pkgver}/plugins/platforms/android/*.so
  # arm static libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_STATIC} \
    ${pkgdir}/opt/android-qt5/${_pkgver}/lib/libQt5PlatformSupport.a
  # linux static libs in lib
  strip ${STRIP_STATIC} ${pkgdir}/opt/android-qt5/${_pkgver}/lib/libQt5{Bootstrap,OpenGLExtensions}.a
}
