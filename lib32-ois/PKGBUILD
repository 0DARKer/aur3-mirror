# Maintainer: Dan Vratil <dan@progdan.cz>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

_pkgbasename=ois
pkgname=lib32-$_pkgbasename
pkgver=1.3
_oisver=${pkgver/./-}
pkgrel=3
pkgdesc="Object Oriented Input System (32 bit)"
arch=('x86_64')
url="http://sourceforge.net/projects/wgois"
license=('custom:zlib/libpng')
depends=('lib32-libx11' 'lib32-gcc-libs' "$_pkgbasename")
makedepends=('lib32-libltdl' 'gcc-multilib' 'lib32-libxaw')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/wgois/Source%20Release/${pkgver}/ois_v${pkgver/./-}.tar.gz"
	'LICENSE')
md5sums=('9697fead17eac6025151cd2e1fca1518'
         'a03443907a68f1878ee55002a4e00c39')

build() {
  cd ${srcdir}/${_pkgbasename}-v${pkgver/./-}
  chmod +x bootstrap
  ./bootstrap
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-v${pkgver/./-}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/include
  install -Dm644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
