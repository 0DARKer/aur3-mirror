# Contributor: Lawrence Lee <valheru@facticius.net>
pkgname=qt-copy-svn
pkgver=660297
pkgrel=2
pkgdesc="The QT gui toolkit patched for KDE4."
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('libpng' 'libjpeg' 'libxi' 'libxcursor' 'libxinerama' 'mesa' \
         'fontconfig' 'libxrandr' 'dbus' 'glib2' 'nas')
makedepends=('mysql' 'postgresql' 'unixodbc' 'libmng' 'subversion')
provides=(qt4)
conflicts=(qt4 qt4-beta)
replaces=(qt4)
options=(!strip)
install=qt4.install
source=(qt4.profile qt-config)
groups=(kde4)
_svntrunk=svn://anonsvn.kde.org/home/kde/trunk/qt-copy
_svnmod=qt-copy
md5sums=('67ab87a3e63dc038bfdacb37ce756075' 'e843903bdc3b421ea8b967c48dc89cde')

build() {

  # start building
  cd $startdir/src

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  unset QMAKESPEC
  export QTDIR=$startdir/src/$_svnmod_build
  export PATH=${QTDIR}/bin:${PATH}
  export LD_LIBRARY_PATH=${QTDIR}/lib:${LD_LIBRARY_PATH}

  #patches
  ./apply_patches

  # remove unwanted mkspecs
  rm -rf mkspecs/{*aix*,*bsd*,darwin*,hpux*,hurd*,irix*,lynxos*,macx*,sco*,solaris*,tru64*,unixware*,win32*}
  rm -rf mkspecs/qws/{*bsd*,linux-{arm,cellon,ipaq,mips,sharp,zylonite}*,mac*,solaris*}

  # start compiling qt
  sed -i 's|-cp -P -f|-cp -L -f|' qmake/Makefile.unix
  sed -i "s|-O2|$CXXFLAGS|" mkspecs/common/g++.conf
  sed -i "s|-I. |$CXXFLAGS -I. |" qmake/Makefile.unix

  ./configure -v -confirm-license -prefix /opt/qt4 \
    -debug -platform linux-g++ \
    -system-zlib -qt-gif -shared -sm -nis \
    -system-libpng -system-libjpeg \
    -system-nas-sound \
    -plugin-sql-{mysql,psql,sqlite,odbc} \
    -no-separate-debug-info -stl \
    -glib -qdbus -silent

  make || return 1
  make INSTALL_ROOT=$startdir/pkg install

  # profile
  install -D -m644 $startdir/qt4.profile $startdir/pkg/etc/profile.d/qt4.sh

  # simple script to switch between qt3 and qt4
  mkdir -p $startdir/pkg/usr/sbin
  install -D -m755 $startdir/src/qt-config $startdir/pkg/usr/sbin/qt-config

  # lots of cleanup and path fixes - thanks to crazy (frugalware)
  find $startdir/pkg/opt/qt4/ -type f -name '*prl' -print -exec sed -i -e "/^QMAKE_PRL_BUILD_DIR/d" {} \;

  #clean up build directory
  rm -rf $startdir/src/$_svnmod-build
}
