# Maintainer: Carl Reinke <mindless2112 gmail com>

pkgname=include-what-you-use
pkgver=3.0.1
_pkgver=3.0-1
pkgrel=1
pkgdesc="A tool for use with clang to analyze #includes in C and C++ source files"
url="http://code.google.com/p/include-what-you-use/"
license=('LLVM Release License')
source=("http://include-what-you-use.googlecode.com/files/${pkgname}-${_pkgver}.tar.gz"
        'http://llvm.org/releases/3.0/llvm-3.0.tar.gz'
        'http://llvm.org/releases/3.0/clang-3.0.tar.gz')
sha1sums=('c9fe7eaeb02edef76bca89930562386a85123af1'
          'b683e7294fcf69887c0d709025d4640f5dca755b'
          '1fa11f07f957bd9c9de003d1b5a7a9ba1e0055e4')
arch=('i686' 'x86_64')
depends=()
makedepends=('clang=3.0')
optdepends=('python2')

build()
{
	cd "${srcdir}"
	
	mv "${pkgname}" clang-3.0.src/tools/
	mv clang-3.0.src llvm-3.0.src/tools/clang
	
	cd "${srcdir}/llvm-3.0.src"
	
	./configure
	make
	
	cd "${srcdir}/llvm-3.0.src/tools/clang/tools/${pkgname}"
	
	sed -i "s|^#!/usr/bin/python$|#!/usr/bin/python2|" fix_includes.py
	
	make
}

package()
{
	install -Dm755 "${srcdir}/llvm-3.0.src/Release/bin/include-what-you-use"             "${pkgdir}/usr/bin/iwyu"
	install -Dm755 "${srcdir}/llvm-3.0.src/tools/clang/tools/${pkgname}/fix_includes.py" "${pkgdir}/usr/bin/iwyu-fix_includes.py"
}

