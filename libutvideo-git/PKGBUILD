# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: HitomiTenshi <Johann.Rekowski@googlemail.com>

pkgname=libutvideo-git
pkgver=v15.0.2.92.ffc9061
pkgrel=1
pkgdesc="utvideo with patches for the buildsystem (GIT Version)"
arch=('i686' 'x86_64')
url="https://github.com/qyot27/libutvideo"
license=('GPL')
makedepends=('git')
conflicts=('libutvideo')
provides=('libutvideo')
source=('git://github.com/qyot27/libutvideo.git#branch=buildsystem')
sha1sums=('SKIP')

pkgver() {
  cd libutvideo
  echo "v$(git tag -l | tail -1 |tr -d v).$(git rev-list buildsystem | sort | wc -l | awk '{print $1}').$(git rev-list HEAD -n 1 --abbrev-commit)"
}

build() {
  cd libutvideo

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --enable-pic
  make
}

package() {
  make -C libutvideo DESTDIR="${pkgdir}" install
}