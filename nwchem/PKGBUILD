# Maintainer: Scott Tincman <sctincman at gmail dot com>

pkgname=nwchem
pkgver=6.3
pkgrel=2
pkgdesc="Ab initio computational chemistry software package"
arch=('i686' 'x86_64')
url="http://www.nwchem-sw.org/index.php/Main_Page"
license=('custom:ECL')
depends=('lapack' 'openmpi' 'python2')
makedepends=('gcc' 'gcc-fortran')
optdepends=()
install=nwchem.install
source=("http://www.nwchem-sw.org/download.php?f=Nwchem-6.3.revision2-src.2013-10-17.tar.gz"
	"config.sh"
	"nwchemrc")
md5sums=('f961c1e5ee46fbc8718cf7b1b59bafa9'
	 '7995a25203a840f8f2063de73d9109ed'
	 '4fded32842a6cc300d1f4fd55d19e827')

prepare() {
  cd $srcdir
  #Pieces of script/fortran have 65 character limit, this should help avoid it
  mv nwchem-6.3.revision2-src.2013-10-17 nwchem-6.3
}

build() {
  cd "$srcdir/nwchem-6.3"

  source ${srcdir}/config.sh

  if test "$CARCH" == x86_64; then
    export TARGET=LINUX64
    export NWCHEM_TARGET=LINUX64
  else
    export TARGET=LINUX
    export NWCHEM_TARGET=LINUX
  fi

  cd src
  make nwchem_config
  make
}

package() {

  if test "$CARCH" == x86_64; then
    export TARGET=LINUX64
  else
    export TARGET=LINUX  
  fi

  cd "$srcdir/nwchem-6.3"

  install -d -m 755 $pkgdir/usr/bin
  install -m 755 $srcdir/nwchem-6.3/bin/${TARGET}/nwchem $pkgdir/usr/bin/

  install -d -m 755 $pkgdir/usr/share/nwchem/
  cp -r $srcdir/nwchem-6.3/src/basis/libraries $pkgdir/usr/share/nwchem/
  cp -r $srcdir/nwchem-6.3/src/data $pkgdir/usr/share/nwchem/

  install -d -m 755 $pkgdir/usr/share/nwchem/libraryps
  cp -r $srcdir/nwchem-6.3/src/nwpw/libraryps/{development_psps,HGH_LDA,library1,library2,ofpw_default,paw_default,pspw_default,pspw_new,pspw_old,Spin_Orbit,TETER,TM} $pkgdir/usr/share/nwchem/libraryps
  # ugh...
  chmod -R go=rX $pkgdir/usr/share/nwchem/
  chmod -R u=wrX $pkgdir/usr/share/nwchem/

  install -d -m 755 $pkgdir/etc/skel/
  install -m 644 $srcdir/nwchemrc $pkgdir/etc/skel/.nwchemrc

  install -d -m 755 $pkgdir/usr/share/licenses/nwchem
  install -m 0644 $srcdir/nwchem-6.3/LICENSE.TXT $pkgdir/usr/share/licenses/nwchem/
}
