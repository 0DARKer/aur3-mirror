pkgname=mingw32-libjpeg-turbo
pkgver=1.2.1
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression (mingw32)"
arch=(any)
url="http://libjpeg-turbo.virtualgl.org/"
license=("GPL, custom")
depends=(mingw32-runtime)
makedepends=(nasm mingw32-gcc)
provides=("mingw32-libjpeg=8.0.2")
conflicts=(mingw32-libjpeg)
replaces=(mingw32-libjpeg)
options=(!libtool !strip !buildflags)
source=("http://sourceforge.net/projects/libjpeg-turbo/files/$pkgver/libjpeg-turbo-$pkgver.tar.gz")
md5sums=('f61e60ff01381ece4d2fe65eeb52669e')

build() {
  cd "$srcdir/libjpeg-turbo-$pkgver"

  ./configure \
    --host=i486-mingw32 \
    --prefix=/usr/i486-mingw32 \
    --with-jpeg8
  make
}

package() {
  cd "$srcdir/libjpeg-turbo-$pkgver"

  make DESTDIR=$pkgdir/ install
  
  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h "$pkgdir/usr/i486-mingw32/include/jpegint.h"
  
  i486-mingw32-strip "$pkgdir/usr/i486-mingw32/bin/"*.exe
  i486-mingw32-strip -x -g "$pkgdir/usr/i486-mingw32/bin/"*.dll
  i486-mingw32-strip -g "$pkgdir/usr/i486-mingw32/lib/"*.a
  
  # do not distributre libturbojpeg as it is unversioned
  rm "$pkgdir/usr/i486-mingw32/bin/tjbench.exe"
  rm "$pkgdir/usr/i486-mingw32/bin/libturbojpeg.dll"
  rm "$pkgdir/usr/i486-mingw32/lib/libturbojpeg."{dll.a,a}
  rm "$pkgdir/usr/i486-mingw32/include/turbojpeg.h"
  rm -r "$pkgdir/usr/i486-mingw32/share"
}
