# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=mingw32-libjpeg-turbo
_pkgname=libjpeg-turbo
pkgver=1.2.0
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression (mingw32)"
arch=('any')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('mingw32-runtime')
makedepends=('nasm' 'mingw32-gcc')
provides=('mingw32-libjpeg=8.0.2')
conflicts=('mingw32-libjpeg')
replaces=('mingw32-libjpeg')
options=('!libtool' '!strip' '!buildflags')
source=(http://sourceforge.net/projects/$_pkgname/files/$pkgver/$_pkgname-$pkgver.tar.gz)
md5sums=('5329fa80953938cb4f097afae55059e2')

build() {
  cd $srcdir/$_pkgname-$pkgver

  # put documentation in a reasonable directory...
  sed -i "/docdir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in
  sed -i "/exampledir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in

  ./configure --host=i486-mingw32 --prefix=/usr/i486-mingw32 --with-jpeg8 --mandir=/usr/share/man
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  make DESTDIR=$pkgdir/ install
  
  # provide jpegint.h as it is required by various software
  install -m644 jpegint.h $pkgdir/usr/i486-mingw32/include/
  
  # do not distributre libturbojpeg as it is unversioned
  rm $pkgdir/usr/i486-mingw32/bin/libturbojpeg.dll
  rm $pkgdir/usr/i486-mingw32/lib/libturbojpeg.{dll.a,a}
  rm $pkgdir/usr/i486-mingw32/include/turbojpeg.h

  rm -r $pkgdir/usr/share
  rm -r $pkgdir/usr/i486-mingw32/share
}
