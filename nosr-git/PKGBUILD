# Contributor: Dave Reisner <d@falconindy.com>

pkgname=nosr-git
pkgver=20110828
pkgrel=1
pkgdesc="search for files in uninstalled packages"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/nosr"
license=('MIT')
depends=('pacman')
makedepends=('git' 'perl')
conflicts=('nosr')
provides=('nosr')

_gitroot="git://github.com/falconindy/nosr.git"
_gitname="nosr"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d $_gitname ]] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim: ft=sh syn=sh et
