# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Filippo Squillace <sqoox85@gmail.com>

pkgname=visit
pkgver=2.8.0
_pkgver=2_8_0
pkgrel=2
pkgdesc="Interactive parallel visualization and graphical analysis tool."
arch=('i686' 'x86_64')
url="https://wci.llnl.gov/simulation/computer-codes/visit"
_url="http://portal.nersc.gov"
license=('BSD' 'custom')
makedepends=('qtchooser' 'cmake')
depends=('qt4' 'python2' 'openmpi' 'glu')
conflicts=('visit-bin')
source=("${_url}/svn/${pkgname}/trunk/releases/${pkgver}/build_${pkgname}${_pkgver}"
        "visit.sh")

md5sums=('cb3fa9df2cd6e66c9187e125f72fe3c7'
        'eda322f9745ff8ace1cfbec7536929df')

prepare(){
  cd ${srcdir}

  chmod 700 build_${pkgname}${_pkgver}

  # Fix to use python2
  sed  -i 's/python-config/python2-config/g' build_${pkgname}${_pkgver}
  sed  -i 's/PYTHON_COMMAND="python"/PYTHON_COMMAND="python2"/g' build_${pkgname}${_pkgver}

}

build() {
  cd $srcdir

  export PAR_COMPILER=/usr/bin/mpicxx
  export PAR_INCLUDE=-I/usr/include
  export SILO_EXTRA_OPTIONS="LIBS='-lstdc++'"

  # To avoid some incompatibilities with qt5
  export QT_SELECT=4

  mkdir -p build
  mkdir -p thirdparty

  echo yes | ${srcdir}/./build_${pkgname}${_pkgver} --console \
    --installation-build-dir ${srcdir}/build \
    --thirdparty-path ${srcdir}/thirdparty \
    --system-cmake --system-qt --system-python \
    --parallel \
    --parallel-build \
    --fortran \
    --silo \
    --netcdf \
    --szip \
    --hdf5

  # Fix to use python 2
  sed -i 's/exec\ python/exec\ python2/' build/${pkgname}${pkgver}/src/bin/frontendlauncher

}

package(){
  cd ${srcdir}/build/${pkgname}${pkgver}/src

  # The prefix could not be set in the build_visit script,
  # because it will install it in the build function.
  sed -i 's/\/usr\/local/\/opt\/visit/' cmake_install.cmake

  make install DESTDIR=${pkgdir}

  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd ${srcdir}
  install -Dm755 ${srcdir}/visit.sh ${pkgdir}/etc/profile.d/visit.sh
}

# vim:set ts=2 sw=2 et:
