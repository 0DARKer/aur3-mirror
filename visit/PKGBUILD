# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Filippo Squillace <sqoox85@gmail.com>

pkgname=visit
pkgver=2.8.1
_pkgver=2_8_1
pkgrel=1
pkgdesc="Interactive parallel visualization and graphical analysis tool."
arch=('i686' 'x86_64')
url="https://wci.llnl.gov/simulation/computer-codes/visit"
_url="http://portal.nersc.gov"
license=('BSD' 'custom')
makedepends=('qtchooser' 'cmake' 'svn')
depends=('qt4' 'qtwebkit' 'python2' 'openmpi' 'glu' 'boost')
optdepends=('java-environment')
conflicts=('visit-bin' 'visit-svn')
source=("${_url}/svn/${pkgname}/trunk/releases/${pkgver}/build_${pkgname}${_pkgver}"
        "visit.sh"
        "VTK_Mesa_${pkgver}.patch")

md5sums=('5f14c90383754e0aba486c17affe5dd1'
         'eda322f9745ff8ace1cfbec7536929df'
         'd23ba9e2ded8d69490db81cb1c8abf18')

prepare(){
  cd ${srcdir}

  chmod 700 build_${pkgname}${_pkgver}

  # Fix to use python2
  sed -i 's/python-config/python2-config/g' build_${pkgname}${_pkgver}
  sed -i 's/PYTHON_COMMAND="python"/PYTHON_COMMAND="python2"/g' build_${pkgname}${_pkgver}

  # Fix for Mesa 10.3.x and VTK see https://bugs.freedesktop.org/show_bug.cgi?id=83631 or 
  # https://bugzilla.redhat.com/show_bug.cgi?id=1138466
  patch build_${pkgname}${_pkgver} < VTK_Mesa_${pkgver}.patch
}

build() {
  cd ${srcdir}

  export PAR_COMPILER=/usr/bin/mpicxx
  export PAR_INCLUDE=-I/usr/include
  export SILO_EXTRA_OPTIONS="LIBS='-lstdc++'"
  export FC_COMPILER=gfortran

  # To avoid some incompatibilities with qt5
  export QT_SELECT=4

  mkdir -p build-${pkgver}
  mkdir -p thirdparty-${pkgver}

  echo yes | ./build_${pkgname}${_pkgver} --console \
    --installation-build-dir ${srcdir}/build-${pkgver} \
    --thirdparty-path ${srcdir}/thirdparty-${pkgver} \
    --system-cmake --system-qt --system-python \
    --parallel-build --parallel \
    --cc gcc --cxx g++ --fortran \
    --build-mode Release \
    --pyside \
    --silo \
    --xdmf \
    --netcdf \
    --szip --zlib \
    --hdf4 --hdf5


  # Fix to use python 2
  sed -i 's/exec\ python/exec\ python2/' \
    build-${pkgver}/${pkgname}${pkgver}/src/bin/frontendlauncher
}

package(){
  cd ${srcdir}/build-${pkgver}/${pkgname}${pkgver}/src

  # The prefix could not be set in the build_visit script,
  # because it will install it in the build function.
  sed -i 's/\/usr\/local/\/opt\/visit/' cmake_install.cmake

  make install DESTDIR=${pkgdir}

  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd ${srcdir}
  install -Dm755 ${srcdir}/visit.sh ${pkgdir}/etc/profile.d/visit.sh
}

# vim:set ts=2 sw=2 et:
