# Maintainer: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Filippo Squillace <sqoox85@gmail.com>

pkgname=visit
pkgver=2.8.2
_pkgver=2_8_2
pkgrel=3
pkgdesc="Interactive parallel visualization and graphical analysis tool."
arch=('i686' 'x86_64')
url="https://wci.llnl.gov/simulation/computer-codes/visit"
_url="http://portal.nersc.gov"
license=('BSD' 'custom')
makedepends=('qtchooser' 'cmake' 'svn' 'java-runtime' 'gcc-fortran')
depends=('qt4' 'qtwebkit' 'python2' 'openmpi' 'glu' 'boost' 'java-environment')
conflicts=('visit-bin')
source=("${_url}/svn/${pkgname}/trunk/releases/${pkgver}/build_${pkgname}${_pkgver}"
        "visit.sh"
        "VTK_Mesa_${pkgver}.patch")

sha256sums=('d33ced22f26edddef20d49222cdb050e443549af653926f6330885152c1dd9da'
            'd07a11e67ad646579fbc341f30e1eb63ebd38a5fbdd4f3ea36e8f460419028da'
            'c156405c2daf933d29af53835ecc27efd9b6fc34e767c924767c1a223a8eda65')

changelog=${pkgname}.changelog

prepare(){
  cd ${srcdir}

  # Fix to use python2
  sed -i 's/python-config/python2-config/g' build_${pkgname}${_pkgver}
  sed -i 's/PYTHON_COMMAND="python"/PYTHON_COMMAND="python2"/g' build_${pkgname}${_pkgver}

  # Fix for python 2.7.9 which gives '-lpython2.7 -lpthread -ldl -lutil -lm'
  # instead of '-lpthread -ldl -lutil -lm -lpython2.7' from python <= 2.7.8
  # So for now, we hardcode it!
  sed -i 's/PYTHON_LIBRARY="${PYTHON_LIBRARY_DIR}\/${PYTHON_LIBRARY}"/PYTHON_LIBRARY="\/usr\/lib\/libpython2.7.so"/' build_${pkgname}${_pkgver}

  # Fix for Mesa >= 10.3.x and VTK see https://bugs.freedesktop.org/show_bug.cgi?id=83631 or
  # https://bugzilla.redhat.com/show_bug.cgi?id=1138466
  patch build_${pkgname}${_pkgver} < VTK_Mesa_${pkgver}.patch
}

build() {
  cd ${srcdir}

  export PAR_COMPILER=/usr/bin/mpicxx
  export PAR_INCLUDE=-I/usr/include
  export SILO_EXTRA_OPTIONS="LIBS='-lstdc++'"
  export FC_COMPILER=gfortran

  # To avoid some incompatibilities with Qt5
  export QT_SELECT=4

  mkdir -p build-${pkgver}
  mkdir -p thirdparty-${pkgver}

  # Fix in case the ${MAKEFLAGS} variable is empty
  if [ -z "${MAKEFLAGS}" ]; then
    MAKEFLAGS="-j1"
  fi

  echo yes | sh build_${pkgname}${_pkgver} --console \
    --installation-build-dir ${srcdir}/build-${pkgver} \
    --thirdparty-path ${srcdir}/thirdparty-${pkgver} \
    --system-cmake --system-qt --system-python \
    --parallel-build --parallel \
    --makeflags ${MAKEFLAGS} \
    --cc gcc --cxx g++ --fortran \
    --build-mode Release \
    --java \
    --pyside \
    --silo \
    --xdmf \
    --netcdf \
    --szip --zlib \
    --hdf4 --hdf5


  # Fix to use python 2
  cd "build-${pkgver}/${pkgname}${pkgver}"
  sed -i 's/exec\ python/exec\ python2/' \
    src/bin/frontendlauncher
  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
    -i $(find . -name '*.py')
}

package(){
  cd ${srcdir}/build-${pkgver}/${pkgname}${pkgver}/src

  # The prefix could not be set in the build_visit script,
  # because it will install it in the build function of the PKGBUILD.
  sed -i 's/\/usr\/local/\/opt\/visit/' cmake_install.cmake

  make install DESTDIR=${pkgdir}

  # License
  install -Dm644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # Install script to set path
  cd ${srcdir}
  install -Dm755 ${srcdir}/visit.sh ${pkgdir}/etc/profile.d/visit.sh

}

# vim:set ts=2 sw=2 et:
