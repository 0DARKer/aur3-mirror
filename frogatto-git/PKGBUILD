# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>
pkgname=frogatto-git
pkgver=1.2.4472.g3d0a22a
# The versioning scheme changed from date-based to git tag based on 2013-05-27
epoch=1
pkgrel=1
pkgdesc="An action-adventure game, starring a certain quixotic frog."
arch=(any)
url="http://www.frogatto.com/"
license=('custom')
groups=()
depends=(anura-git)
makedepends=('git')
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
source=(git+https://github.com/frogatto/frogatto.git
        frogatto-git.sh
        frogatto-git.desktop)
noextract=()
md5sums=(SKIP
         b988ac59596fefe86852c0a748f43bda
         c5556b185a9f34080a619efd485ccb91)

_gitname=frogatto

pkgver() {
  cd $_gitname
  if _tag=$(git describe 2>/dev/null)
  then
    # Use the tag of the last commit
    echo $_tag | sed 's|-|.|g'
  else
    # The project currently has no tags yet
    echo 0.0.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  fi
}

package() {
  install -DT -m755 frogatto-git.sh $pkgdir/usr/bin/$pkgname
  install -D -m644 frogatto-git.desktop $pkgdir/usr/share/applications/$pkgname.desktop

  cd $_gitname
  # the LICENSE file is currently missing in the repository
  # install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  _installdir=$pkgdir/usr/share/anura-git/modules/$pkgname
  install -DT -m644 module.cfg $_installdir/module.cfg
  cp -r data images locale music sounds $_installdir

}

# vim:set ts=2 sw=2 et:
