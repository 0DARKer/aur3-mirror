pkgname=mruby
pkgver=1.0.0
pkgrel=1
pkgdesc="Lightweight Ruby"
arch=(i686 x86_64)
url="http://www.mruby.org"
license=("MIT")
depends=()
makedepends=(bison ruby)
source=("https://github.com/mruby/mruby/archive/$pkgver.tar.gz")
md5sums=('d9aec6a20fc1add65ee07ff6cf0e1ef2')
options=(staticlibs !emptydirs)

build() {
	cd "${srcdir}/mruby-$pkgver"
	make
}

package() {
	cd "${srcdir}/mruby-$pkgver"
	install -Dm644 "LEGAL" "$pkgdir/usr/share/licenses/mruby/LEGAL"
	install -m644 "MITL" "$pkgdir/usr/share/licenses/mruby/"
	cd include
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/include/"{} \;
	cd ../build/host/bin
	find . -type f -exec install -Dm755 {} "$pkgdir/usr/bin/"{} \;
	cd ../lib
	find . -name '*.a' -exec install -Dm644 {} "$pkgdir/usr/lib/"{} \;
} 
