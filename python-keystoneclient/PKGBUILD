# Maintainer: Carl George < arch at cgtx.us >

_module="python-keystoneclient"
pkgname=("python-keystoneclient" "python2-keystoneclient")
pkgver="1.0.0"
pkgrel="1"
pkgdesc="Client Library for OpenStack Identity"
arch=("any")
url="https://github.com/openstack/${_module}"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")

prepare() {
	sed -i '/argparse/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
	sed -i 's_keystone_keystone2_g' "${srcdir}/${_module}-${pkgver}-python2/tools/keystone.bash_completion"
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

package_python-keystoneclient() {
	conflicts=("python-keystoneclient-git")
	depends=(
		"python>=3.3"
		"python-pbr>=0.6"
		"python-babel>=1.3"
		"python-iso8601>=0.1.9"
		"python-netaddr>=0.7.12"
		"python-oslo-config>=1.4.0"
		"python-oslo-i18n>=1.0.0"
		"python-oslo-serialization>=1.0.0"
		"python-oslo-utils>=1.1.0"
		"python-prettytable>=0.7"
		"python-requests>=2.2.0"
		"python-six>=1.7.0"
		"python-stevedore>=1.1.0"
	)
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	install -Dm644 tools/keystone.bash_completion "${pkgdir}/etc/bash_completion.d/keystone"
}

package_python2-keystoneclient() {
	conflicts=("python2-keystoneclient-git")
	depends=(
		"python2>=2.6"
		"python2-pbr>=0.6"
		"python2-babel>=1.3"
		"python2-iso8601>=0.1.9"
		"python2-netaddr>=0.7.12"
		"python2-oslo-config>=1.4.0"
		"python2-oslo-i18n>=1.0.0"
		"python2-oslo-serialization>=1.0.0"
		"python2-oslo-utils>=1.1.0"
		"python2-prettytable>=0.7"
		"python2-requests>=2.2.0"
		"python2-six>=1.7.0"
		"python2-stevedore>=1.1.0"
	)
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	install -Dm644 tools/keystone.bash_completion "${pkgdir}/etc/bash_completion.d/keystone2"
	mv "${pkgdir}/usr/bin/keystone" "${pkgdir}/usr/bin/keystone2"
}

sha256sums=('6d960d2196efc7a181519a77f757a27ceeeac71f41f624318ae7f1088d2e6db4')
