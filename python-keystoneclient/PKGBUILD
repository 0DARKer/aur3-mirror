# Maintainer: Carl George < arch at cgtx.us >

_module="python-keystoneclient"
pkgname=("python-keystoneclient" "python2-keystoneclient")
pkgver="1.3.0"
pkgrel="1"
pkgdesc="Client Library for OpenStack Identity"
arch=("any")
url="https://github.com/openstack/${_module}"
license=("Apache")
makedepends=("python-pbr" "python-setuptools" "python2-pbr" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('c009445d9a36fa788a14b093f67501091b246fefccb30fb63925e6e4fbeaac04')

prepare() {
    sed -i '/argparse/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
    sed -i '/pbr/d' "${srcdir}/${_module}-${pkgver}/requirements.txt"
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
    sed -i 's_keystone_keystone2_g' "${srcdir}/${_module}-${pkgver}-python2/tools/keystone.bash_completion"
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-keystoneclient() {
    depends=("python-babel>=1.3"
             "python-iso8601>=0.1.9"
             "python-netaddr>=0.7.12"
             "python-oslo-config>=1.9.0"
             "python-oslo-i18n>=1.3.0"
             "python-oslo-serialization>=1.2.0"
             "python-oslo-utils>=1.2.0"
             "python-prettytable>=0.7"
             "python-requests>=2.2.0"
             "python-six>=1.9.0"
             "python-stevedore>=1.1.0")
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 tools/keystone.bash_completion "${pkgdir}/etc/bash_completion.d/keystone"
}

package_python2-keystoneclient() {
    depends=("python2-babel>=1.3"
             "python2-iso8601>=0.1.9"
             "python2-netaddr>=0.7.12"
             "python2-oslo-config>=1.9.0"
             "python2-oslo-i18n>=1.3.0"
             "python2-oslo-serialization>=1.2.0"
             "python2-oslo-utils>=1.2.0"
             "python2-prettytable>=0.7"
             "python2-requests>=2.2.0"
             "python2-six>=1.9.0"
             "python2-stevedore>=1.1.0")
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 tools/keystone.bash_completion "${pkgdir}/etc/bash_completion.d/keystone2"
    mv "${pkgdir}/usr/bin/keystone" "${pkgdir}/usr/bin/keystone2"
}
