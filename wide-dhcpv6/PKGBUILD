# Maintainer: Spyros Stathopoulos <foucault.online@gmail.com>
pkgname=wide-dhcpv6
pkgver=20080615
pkgrel=5
pkgdesc="WIDE-DHCPv6 is an open source implementation of DHCPv6 developed by KAME project (with Debian patches)"
arch=('i686' 'x86_64')
url="http://wide-dhcpv6.sourceforge.net/"
license=('custom')
backup=('etc/wide-dhcpv6/dhcp6c.conf')

source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "http://ftp.de.debian.org/debian/pool/main/w/${pkgname}/${pkgname}_${pkgver}-11.1.debian.tar.gz"
        "0002-Fix-makefile.patch"
        "dhcp6c@.service")

md5sums=('1011e165850fe40d3e332dad2bfd30b3'
         'e6bb556a2791b8a825ea2209ce10f27e'
         'dfba035ab0fd99ae56ab2e37a000cba0'
         '8d2dd6a854cf7b00c31b1fe130d1677f')

prepare() {
	#patch -p1 -i "$srcdir/$pkgname-$pkgver.patch"
	cd ${srcdir}
	cp debian/patches/*patch ${srcdir}
	# this is included in the new 0002 patch, so rm it
	rm "0002-Don-t-strip-binaries.patch"
	cd ${srcdir}/$pkgname-$pkgver
	for i in ../*patch ; do
		patch -p1 < ${i};
	done
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	pwd
	./configure --prefix=/usr --mandir=/usr/share/man \
		--with-localdbdir=/var/lib/dhcpv6 \
		--sysconfdir=/etc/wide-dhcpv6
	# -j > 1 breaks build dependencies
	make -j1
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	mkdir -p "${pkgdir}/usr/sbin"
	mkdir -p "${pkgdir}/usr/share/man"
	mkdir -p "${pkgdir}/usr/share/man/man5"
	mkdir -p "${pkgdir}/usr/share/man/man8"
	install -D -m644 ${srcdir}/${pkgname}-${pkgver}/dhcp6c.conf.sample \
		${pkgdir}/etc/wide-dhcpv6/dhcp6c.conf
	install -D -m644 ${srcdir}/${pkgname}-${pkgver}/dhcp6s.conf.sample \
		${pkgdir}/etc/wide-dhcpv6/dhcp6s.conf
	install -D -m644 ${srcdir}/dhcp6c@.service \
		${pkgdir}/usr/lib/systemd/system/dhcp6c@.service
	install -D -m644 ${srcdir}/${pkgname}-${pkgver}/COPYRIGHT \
		${pkgdir}/usr/share/licenses/wide-dhcpv6/LICENSE
	make DESTDIR="$pkgdir/" install
}
