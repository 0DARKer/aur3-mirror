# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxpython-light
pkgver=3.0.1.1
pkgrel=1
pkgdesc="A wxWidgets GUI toolkit for Python (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
license=('custom:wxWindows')
url="http://www.wxpython.org"
depends=('wxgtk-light' 'python2-opengl' 'python2-cairo')
makedepends=('git' 'svn' 'mesa')
provides=('wxpython')
conflicts=('wxpython')
source=("git+https://github.com/wxWidgets/wxPython.git#tag=wxPy-${pkgver}")
sha1sums=('SKIP')
_gitname=wxPython

prepare() {
  cd "${_gitname}"
  git cherry-pick e7ade15
  cd "${_gitname}/wx/lib"
  svn co https://svn.wxwidgets.org/svn/wx/wxPython/3rdParty/AGW/agw
  svn co https://svn.wxwidgets.org/svn/wx/wxPython/3rdParty/FloatCanvas/floatcanvas
  svn co https://svn.wxwidgets.org/svn/wx/wxPython/3rdParty/PDFViewer/pdfviewer
  svn co https://svn.wxwidgets.org/svn/wx/wxPython/3rdParty/PubSub/pubsub
  cd ../tools
  svn co https://svn.wxwidgets.org/svn/wx/wxPython/3rdParty/XRCed
  svn co https://svn.wxwidgets.org/svn/wx/wxPython/3rdParty/Editra
  sed 's/sys.exit(1)//' -i "Editra/setup.py"
  cd "${srcdir}"
  find . -type f -exec sed -i 's/env python/env python2/' {} \;


}

build() {
  cd "${_gitname}"
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,regex=builtin,opengl,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode,stl} \
              --disable-{mediactrl,precomp-headers,webview}
  python2 setup.py WXPORT=gtk2 UNICODE=1 BUILD_GLCANVAS=1 build
}

package() {
  cd "${_gitname}/wxPython"
  python2 setup.py WXPORT=gtk2 UNICODE=1 BUILD_GLCANVAS=1 install --root="${pkgdir}"
  install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd wx/tools/Editra
  python2 setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}/usr/lib/python2.7/site-packages/wx-3.0-gtk2/wx/tools/Editra"
}
