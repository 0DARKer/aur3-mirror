# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxpython-light
pkgver=3.0.0.0
_editraver=0.7.20
pkgrel=1
pkgdesc="A wxWidgets GUI toolkit for Python (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
license=('custom:wxWindows')
url="http://www.wxpython.org"
depends=('wxgtk-light' 'python2-opengl' 'python2-cairo')
makedepends=('mesa')
provides=('wxpython')
conflicts=('wxpython')
source=("http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2"
        "http://editra.org/uploads/src/Editra-${_editraver}.tar.gz")
sha1sums=('48451763275cfe4e5bbec49ccd75bc9652cba719'
          'f439f4aa2cb90b8348cebb1670df9cf47ba341ab')

prepare() {
  rm -fr build
  cp -R "wxPython-src-${pkgver}" build

  find . -type f -exec sed -i 's/env python/env python2/' {} \;
  sed 's/sys.exit(1)//' -i "Editra-${_editraver}/setup.py"
}

build() {
  cd build
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,regex=sys,opengl,sdl} \
              --enable-{unicode,graphics_ctx} \
              --disable-{optimize,mediactrl,precomp-headers}
  cd wxPython
  python2 setup.py WXPORT=gtk2 UNICODE=1 BUILD_GLCANVAS=1 build
}

package() {
  cd build/wxPython
  python2 setup.py WXPORT=gtk2 UNICODE=1 BUILD_GLCANVAS=1 install --root="${pkgdir}"
  install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "${srcdir}/Editra-${_editraver}"
  python2 setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}/usr/lib/python2.7/site-packages/wx-3.0-gtk2/wx/tools/Editra"
}
