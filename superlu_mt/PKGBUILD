# Contributor: Giuseppe Borzi <gborzi@ieee.org>
pkgname=superlu_mt
pkgver=2.0
pkgrel=1
pkgdesc="Parallel subroutines to solve a sparse linear system on shared memory computer"
arch=('i686' 'x86_64')
url="http://crd.lbl.gov/~xiaoye/SuperLU/"
license=('custom')
depends=('gcc-libs' 'blas')
makedepends=('tcsh')
options=(!makeflags)
install=${pkgname}.install
source=(http://crd.lbl.gov/~xiaoye/SuperLU/${pkgname}_$pkgver.tar.gz http://crd.lbl.gov/~xiaoye/SuperLU/License.txt arch.patch)
md5sums=('a1cf0dba3308a2e4a0a7290714f1799d'
         'f78e2ac527dbb50f53766475a9c542bd'
         '4c8cec36828957803a8c2bd262a84724')

build() {
  cd "$srcdir/SuperLU_MT_$pkgver"
  patch -Np1 -i $srcdir/arch.patch

  mkdir -p shared static
  msg "Building shared library..."
  cd shared
  make -f ../SRC/Makefile VPATH=../SRC srcdir=../SRC \
          CFLAGS="$CFLAGS -fPIC -D_PTHREAD" NOOPTS="-fPIC"\
          ARCH="echo" ARCHFLAGS="" RANLIB="echo" \
          SUPERLULIB=$srcdir/SuperLU_MT_$pkgver/lib/lib$pkgname.a
  gcc -shared -Wl,-soname,lib$pkgname.so.2 -o ../lib/lib$pkgname.so.2.0 *.o \
      -lblas -lpthread -lm
  cd ../static
  msg "Building static library..."
  make -f ../SRC/Makefile VPATH=../SRC srcdir=../SRC \
          CFLAGS="$CFLAGS -D_PTHREAD" SUPERLULIB=lib$pkgname.a
  msg "Testing library..."
  cd ../TESTING/MATGEN
  make CFLAGS="-O2"
  cd ..
  LS_COLORS="" make CFLAGS="-O2"
  if [ "x`grep failed *.out`" != "x" ]; then
    msg 'Testing failed'
    return 1
  fi
}

package() {
  mkdir -p $pkgdir/usr/lib $pkgdir/usr/include/$pkgname \
           $pkgdir/usr/share/doc/$pkgname $pkgdir/usr/share/licenses/$pkgname
  install -p -m644 $srcdir/SuperLU_MT_$pkgver/lib/lib$pkgname.a $pkgdir/usr/lib
  install -p -m755 $srcdir/SuperLU_MT_$pkgver/lib/lib$pkgname.so.2.0 $pkgdir/usr/lib
  cd $pkgdir/usr/lib
  ln -s lib$pkgname.so.2.0 lib$pkgname.so.2
  ln -s lib$pkgname.so.2 lib$pkgname.so
  install -m644 $srcdir/SuperLU_MT_$pkgver/SRC/*.h $pkgdir/usr/include/$pkgname
  install -m644 $srcdir/SuperLU_MT_$pkgver/DOC/ug.pdf $pkgdir/usr/share/doc/$pkgname/
  install -m644 $srcdir/SuperLU_MT_$pkgver/README $pkgdir/usr/share/doc/$pkgname
  install -m644 $srcdir/License.txt $pkgdir/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
