# Contributor: Giuseppe Borzi <gborzi@ieee.org>
pkgname=superlu_mt
pkgver=2.0
pkgrel=2
pkgdesc="Parallel subroutines to solve a sparse linear system on shared memory computer"
arch=('i686' 'x86_64')
url="http://crd.lbl.gov/~xiaoye/SuperLU"
license=('custom')
depends=('glibc' 'blas')
makedepends=('tcsh')
options=(!makeflags)
install=${pkgname}.install
source=($url/${pkgname}_$pkgver.tar.gz $url/License.txt arch.patch)
md5sums=('ae6d691cd48b208effac2ae459071437'
         'f78e2ac527dbb50f53766475a9c542bd'
         '4029a5be74e79a04527c95dcebed261d')

build() {
  cd "$srcdir/SuperLU_MT_$pkgver"

  patch -Np1 -i "$srcdir/arch.patch"
  mkdir -p shared static

  msg "Building shared library..."
  cd shared
  make -f ../SRC/Makefile VPATH=../SRC srcdir=../SRC \
          PLAT="" BLASDEF="" BLASLIB="-lblas" ARCH="echo" \
          ARCHFLAGS="" RANLIB="echo" CFLAGS="$CFLAGS -fPIC -D_PTHREAD" \
          FORTRAN=gfortran FFLAGS="$CFLAGS -fPIC" NOOPTS="-O0 -fPIC" \
          SUPERLULIB=$srcdir/SuperLU_MT_$pkgver/lib/lib$pkgname.a
  rm dclock.o
  gcc -shared -Wl,-soname,lib$pkgname.so.2 -o ../lib/lib$pkgname.so.2.0 *.o \
      -lblas -lpthread -lm
  cd ../static
  msg "Building static library..."
  make -f ../SRC/Makefile VPATH=../SRC srcdir=../SRC \
          PLAT="" BLASDEF="" BLASLIB="-lblas" \
          CFLAGS="$CFLAGS -D_PTHREAD" \
          FORTRAN=gfortran FFLAGS="$CFLAGS" NOOPTS="-O0" \
          SUPERLULIB=lib$pkgname.a
  ar d ../lib/lib$pkgname.a dclock.o
}

check(){
  cd "$srcdir/SuperLU_MT_$pkgver/TESTING/MATGEN"
  make CFLAGS="-O2"
  cd ..
  make CFLAGS="-O2" PLAT="" BLASLIB="-lblas"
  if [ "x`grep failed *.out`" != "x" ]; then
    msg 'Testing failed'
    return 1
  fi
}

package() {
  mkdir -p $pkgdir/usr/lib $pkgdir/usr/include/$pkgname \
           $pkgdir/usr/share/doc/$pkgname $pkgdir/usr/share/licenses/$pkgname
  install -p -m644 $srcdir/SuperLU_MT_$pkgver/lib/lib$pkgname.a $pkgdir/usr/lib
  install -p -m755 $srcdir/SuperLU_MT_$pkgver/lib/lib$pkgname.so.2.0 $pkgdir/usr/lib
  cd $pkgdir/usr/lib
  ln -s lib$pkgname.so.2.0 lib$pkgname.so.2
  ln -s lib$pkgname.so.2 lib$pkgname.so
  install -m644 $srcdir/SuperLU_MT_$pkgver/SRC/*.h $pkgdir/usr/include/$pkgname
  install -m644 $srcdir/SuperLU_MT_$pkgver/DOC/ug.pdf $pkgdir/usr/share/doc/$pkgname/
  install -m644 $srcdir/SuperLU_MT_$pkgver/README $pkgdir/usr/share/doc/$pkgname
  install -m644 $srcdir/License.txt $pkgdir/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
