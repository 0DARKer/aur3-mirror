# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kitemviews-git
pkgver=r46.3650be8
pkgrel=1
pkgdesc='KItemViews'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kitemviews'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
groups=('kf5')
conflicts=(kitemviews)
provides=(kitemviews)
source=('git://anongit.kde.org/kitemviews.git')
md5sums=('SKIP')

pkgver() {
  cd kitemviews
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kitemviews \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
