# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: mac <poczciwiec at gmail dot com>

pkgname=brackets-bin
_pkgname=brackets
_build=35
pkgver=sprint$_build
pkgrel=2
pkgdesc="A code editor for HTML, CSS and JavaScript. "
arch=("i686" "x86_64")
url="http://brackets.io"
license=("MIT")
depends=("gconf" "nodejs" "nspr" "nss" "systemd")
makedepends=("prelink")
optdepends=(
	"google-chrome: to enable Live Preview"
	"gnuplot: to enable node benchmarking"
	"gtk2: to enable native UI"
	"ruby: to enable LiveDevelopment Inspector"
	"desktop-file-utils: for update-desktop-database"
	"hicolor-icon-theme: for hicolor theme hierarchy"
)
provides=("brackets=$pkgver")
conflicts=('brackets' 'brackets-git')
install=$pkgname.install

if [[ "$CARCH" == "i686" ]]; then
	sha512sums=(
		"fb7d2108ae4c1fb3690c0c7bf3bed0a976de9093d9474c35953f390911c5fbff2c79ff64ff3317d0e7d28b67fea40aff4dfe017c72554248fabff674643f9432"
		"5dccdb4fa9d4270d27dc1d065b8384b7c72889806b23c62ab07a216a93b95e650f1020395a12b96d7851797d57971aeb4fa63c417e24ca34ff216e128abed74f"
		"f96b22c7de868ee8fc4e303e54ce079046fd487f37f6c655f5bd7f41c1b6a4c76fdd6c08acc73fad599c590e3df43858f1fb213adf013ce88949711d78c4c39a")
	_arch=32
	_
elif [[ "$CARCH" == "x86_64" ]]; then
	sha512sums=(
		"4316e10b3717f47a27876695129f957ae49f8e0cd4fb5717f1fbeb29ddc2bea2bb0a761ed39065fe74f7ba8d06b724e3a70ce6775e717b3208f5e29f876e40ea"
		"5dccdb4fa9d4270d27dc1d065b8384b7c72889806b23c62ab07a216a93b95e650f1020395a12b96d7851797d57971aeb4fa63c417e24ca34ff216e128abed74f"
		"f96b22c7de868ee8fc4e303e54ce079046fd487f37f6c655f5bd7f41c1b6a4c76fdd6c08acc73fad599c590e3df43858f1fb213adf013ce88949711d78c4c39a")
	_arch=64
fi

source=(
	"brackets-sprint-$_build-LINUX$_arch.deb::http://download.brackets.io/file.cfm?platform=LINUX$_arch&build=$_build"
	"libgcrypt-i686.so.11"
	"libgcrypt-x86_64.so.11"
	)

export DLAGENTS='http::/usr/bin/curl -A "Mozilla/4.0" -fLC - --retry 3 --retry-delay 3 -o %o %u'

prepare() {

	msg2 "  -> Extracting files..."
	tar -xf data.tar.gz

	msg2 "  -> Fixing executable stack..."
	execstack -c opt/$_pkgname/Brackets

}

package() {

	msg2 "  -> Installing program..."

	install -d $pkgdir/opt
	cp -r opt/$_pkgname $pkgdir/opt/$_pkgname

	chmod -R a+rw $pkgdir/opt/$_pkgname/samples

	install -d $pkgdir/usr/bin
	ln -s /opt/brackets/brackets $pkgdir/usr/bin/$_pkgname

	msg2 "  -> Installing icons..."
	local _icon_dir="usr/share/icons/hicolor"
	install -d $pkgdir/$_icon_dir/scalable/apps
	install -Dm644 $_icon_dir/scalable/apps/$_pkgname.svg $pkgdir/$_icon_dir/scalable/apps/$_pkgname.svg
	for _icon in "opt/brackets/appshell"*.png; do
		local _icon_size=${_icon##*/appshell}
		install -d $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps
		install -Dm644 $_icon $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps/$_pkgname.png
	done

	msg2 "  -> Installing .desktop file..."
	install -d $pkgdir/usr/share/applications
	sed 's/Development/Development;/' opt/$_pkgname/$_pkgname.desktop > $pkgdir/usr/share/applications/$_pkgname.desktop

	msg2 "Installing compatible Gcrypt lib"
	install -m755 libgcrypt-$CARCH.so.11 $pkgdir/opt/brackets/libgcrypt.so.11

	msg2 "  -> Installing license..."
	install -Dm755 usr/share/doc/$_pkgname/copyright $pkgdir/usr/share/licenses/$_pkgname/copyright
}
