# Maintainer:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: mac <poczciwiec at gmail dot com>

pkgname=brackets-bin
_pkgname=brackets
_build=36
pkgver=sprint$_build
pkgrel=1
pkgdesc="A code editor for HTML, CSS and JavaScript. "
arch=("i686" "x86_64")
url="http://brackets.io"
license=("MIT")
depends=("gconf" "nodejs" "nspr" "nss" "systemd")
makedepends=("prelink")
optdepends=(
	"google-chrome: to enable Live Preview"
	"gnuplot: to enable node benchmarking"
	"gtk2: to enable native UI"
	"ruby: to enable LiveDevelopment Inspector"
	"desktop-file-utils: for update-desktop-database"
	"hicolor-icon-theme: for hicolor theme hierarchy"
)
provides=("brackets=$pkgver")
conflicts=('brackets' 'brackets-git')
install=$pkgname.install
sha512sums=(
	"055296bd29940d955bf415d00e64003c6358773d8816eb4b98e931390b0083e43912d5a3292278c04a0ec45ebc41f46d8461d9e80db81a20143f5c70064010f3"
	"5dccdb4fa9d4270d27dc1d065b8384b7c72889806b23c62ab07a216a93b95e650f1020395a12b96d7851797d57971aeb4fa63c417e24ca34ff216e128abed74f"
	"f96b22c7de868ee8fc4e303e54ce079046fd487f37f6c655f5bd7f41c1b6a4c76fdd6c08acc73fad599c590e3df43858f1fb213adf013ce88949711d78c4c39a")
_arch=64
if [[ $CARCH == "i686" ]]; then
	sha512sums[0]="5677912f11c6234c227f91fdce31c1e10f63a53807f0ee0c2244e7aeae34772347395d46237a1cab883e4ac0afdbd915172ed2060ac05b6f7fa02bbb0f547359"
	_arch=32
fi

source=(
	"brackets-sprint-$_build-LINUX$_arch.deb::http://download.brackets.io/file.cfm?platform=LINUX$_arch&build=$_build"
	"libgcrypt-i686.so.11"
	"libgcrypt-x86_64.so.11"
	)

export DLAGENTS='http::/usr/bin/curl -A "Mozilla/4.0" -fLC - --retry 3 --retry-delay 3 -o %o %u'

prepare() {

	msg2 "  -> Extracting files..."
	tar -xf data.tar.gz

	msg2 "  -> Fixing executable stack..."
	execstack -c opt/$_pkgname/Brackets

}

package() {

	msg2 "  -> Installing program..."

	install -d $pkgdir/opt
	cp -r opt/$_pkgname $pkgdir/opt/$_pkgname

	chmod -R a+rw $pkgdir/opt/$_pkgname/samples

	install -d $pkgdir/usr/bin
	ln -s /opt/brackets/brackets $pkgdir/usr/bin/$_pkgname

	msg2 "  -> Installing icons..."
	local _icon_dir="usr/share/icons/hicolor"
	install -d $pkgdir/$_icon_dir/scalable/apps
	install -Dm644 $_icon_dir/scalable/apps/$_pkgname.svg $pkgdir/$_icon_dir/scalable/apps/$_pkgname.svg
	for _icon in "opt/brackets/appshell"*.png; do
		local _icon_size=${_icon##*/appshell}
		install -d $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps
		install -Dm644 $_icon $pkgdir/$_icon_dir/${_icon_size%.png}x${_icon_size%.png}/apps/$_pkgname.png
	done

	msg2 "  -> Installing .desktop file..."
	install -d $pkgdir/usr/share/applications
	sed 's/Development/Development;/' opt/$_pkgname/$_pkgname.desktop > $pkgdir/usr/share/applications/$_pkgname.desktop

	msg2 "Installing compatible Gcrypt lib"
	install -m755 libgcrypt-$CARCH.so.11 $pkgdir/opt/brackets/libgcrypt.so.11

	msg2 "  -> Installing license..."
	install -Dm755 usr/share/doc/$_pkgname/copyright $pkgdir/usr/share/licenses/$_pkgname/copyright
}
