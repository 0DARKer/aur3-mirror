# $Id: PKGBUILD 188521 2013-06-15 07:18:30Z andrea $
# Maintainer: Benjamin Chretien <chretien at lirmm dot fr>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jonahtan Schaeffer <joschaeffer@gmail.com>
# COntributor: Drake Arconis <lightdrake@gmail.com>

_pkgname=jsoncpp
pkgname=${_pkgname}-pc
pkgver=0.5.0
pkgrel=1
pkgdesc="A JSON C++ library. pkg-config supported."
url='http://jsoncpp.sourceforge.net/'
license=('custom:Public_Domain')
arch=('i686' 'x86_64')
depends=('gcc-libs')
makedepends=('scons')
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-src-${pkgver}.tar.gz")
md5sums=('24482b67c1cb17aac1ed1814288a3a8f')

# Add pkg-config support
source+=('jsoncpp.pc')
md5sums+=('01bc7f5caffe2d1c8769cb1a7cf1001c')

conflicts=(${_pkgname})
provides=(${_pkgname})

build() {
  cd ${_pkgname}-src-${pkgver}
  scons platform=linux-gcc
}

package() {
  cd ${_pkgname}-src-${pkgver}

  install -d "${pkgdir}"/usr/include/jsoncpp
  cp -r include/json "${pkgdir}"/usr/include/jsoncpp/json

  install -Dm644 buildscons/linux-gcc-*/src/lib_json/libjson_linux-gcc*libmt.so \
    "${pkgdir}"/usr/lib/libjsoncpp.so

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${_pkgname}/LICENSE

  # Install pkg-config file
  cd ${srcdir}
  install -Dm644 jsoncpp.pc "${pkgdir}"/usr/lib/pkgconfig/jsoncpp.pc
}

