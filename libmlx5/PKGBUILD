# Submitter: Anthony Cornehl <accornehl[at]gmail[dot]com>
# Maintainer: Mike Dacre <mike@dacre.me>
# https://github.com/twinshadow/abs
pkgname=libmlx5
pkgver=1.0.1
pkgrel=2
pkgdesc="Userspace driver for Mellanox ConnectX InfiniBand HCAs"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibverbs')
source=("https://www.openfabrics.org/downloads/mlx5/${pkgname}-${pkgver}.tar.gz")
# cksum start
md5sums=('c7fc7da15828b013362195d47eb1ad16')
sha512sums=('9e5ae3157d9ce7a9de5417aae15630cf5e893ebcab5a27889a3399a292bac9fd730b768748fff4550aa54f79e358edd285eaa0cc228ad8ff530f21327c99fdcb')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
