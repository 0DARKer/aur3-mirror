# Maintainer: Lazaros Koromilas <koromilaz@gmail.com>
pkgname=libclens
_distname=clens
pkgver=0.5.0
pkgrel=2
pkgdesc="Layer for porting OpenBSD stuff to other systems"
arch=('i686' 'x86_64')
url="https://opensource.conformal.com/wiki/clens"
license=('custom:ISC')
_distbase="https://opensource.conformal.com/snapshots"
source=($_distbase/$_distname/$_distname-$pkgver.tar.gz
        patch-GNUmakefile
        LICENSE)
md5sums=('65fa9d32a474e4ac7ecf3249d5b7e373'
         '95348b0a245873adf150b075e177c7d0'
         '7d20b8325e2901dc41e18425fdc695cd')

build() {
  cd "$srcdir/$_distname-$pkgver"
  patch < ../patch-GNUmakefile
  sed -i "s|<vis.h>|<bsd/vis.h>|" src/strnvis.c src/strnunvis.c
  make LOCALBASE="/usr"
}

package() {
  cd "$srcdir/$_distname-$pkgver"
  make LOCALBASE="/usr" DESTDIR="$pkgdir/" install
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
