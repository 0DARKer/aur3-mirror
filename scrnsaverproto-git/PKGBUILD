# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s scrnsaverproto

_pkgname=scrnsaverproto
pkgname=$_pkgname-git
pkgver=1.2.2.2.g6145320
pkgrel=1
pkgdesc="X11 Screen Saver extension wire protocol - Git version"
arch=('any')
url="http://cgit.freedesktop.org/xorg/proto/$_pkgname/"
license=('custom')
makedepends=('git' 'xorg-util-macros')
provides=("$_pkgname=99")
conflicts=("$_pkgname")
source=("git://anongit.freedesktop.org/xorg/proto/$_pkgname")
md5sums=('SKIP')
_gitname=$_pkgname

pkgver() {
  cd $_gitname
  echo $(git describe --always | sed 's/-/./g' | cut -d "." -f2-)
}

build() {
  cd $_gitname
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}