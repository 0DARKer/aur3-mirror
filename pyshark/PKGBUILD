# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgbase=pyshark
pkgname=('python-pyshark' 'python2-pyshark')
pkgver=0.3.3
pkgrel=1
pkgdesc="Python wrapper for tshark, allowing python packet parsing using wireshark dissectors"
url="https://github.com/KimiNewt/pyshark"
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=(https://pypi.python.org/packages/source/p/${pkgbase}/${pkgbase}-${pkgver}.zip)
sha512sums=('906feea8454d731551346d2aeca28b713b1d9ddfc1e1bf10041b255a125db4f679c6c4f53605d7f3c0adcd3b686649ccfe6862009f456c9e7df3be4850be8e0a')

package_python-pyshark() {
  depends=('wireshark-cli' 'pypy3' 'python-lxml' 'python-trollius' 'python-logbook')

  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-pyshark() {
  depends=('wireshark-cli' 'pypy' 'python2-lxml' 'python2-trollius' 'python2-logbook')

  cd ${pkgbase}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.txt "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
