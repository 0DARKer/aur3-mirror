# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: guini <sidtrun@googlemail.com>
# Contributor: Forrest Loomis <cybercyst _ at _ gmail dot com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: fana-m <geminin@gmx.net>
pkgname=freeorion
pkgver=0.3.17
pkgrel=3
pkgdesc="Turn-based galactic conquest game inspired by Master of Orion"
arch=('x86_64' 'i686')
url="http://www.freeorion.org/"
license=('GPL')
if [ "$CARCH" == "x86_64" ]
then
  depends=('lib32-nvidia-cg-toolkit')
else
  depends=('nvidia-cg-toolkit')
fi
depends=('bullet' 'freealut' 'gigi' 'libvorbis' 'python2')
makedepends=('boost<1.48.0' 'ogre' 'sdl' 'gigi<0.8.0-5' 'graphviz' 'libogg' 'desktop-file-utils' 'clang')
install=freeorion.install
source=("$pkgname.png"::"http://a.fsdn.com/con/icons/fr/freeorion@sf.net/FO_Icon_256x256.png"
        "$pkgname.desktop"
        "$pkgname.sh"
        "CMakeLists.patch")
md5sums=('642d3b488be85180cf6ad144bce2f102'
         '6ef93d69a3dc1961a34d46a4d37f2788'
         '5af9750d77a6cca3bca834243ebdbda9'
         '5dce694db11eb7b58c45e1ccba3db267')
options=(!strip)

build() {
  cd "$srcdir"

  if [[ -d "$pkgname/.svn" ]]; then
    (cd "$pkgname" && svn up -r 4517)
  else
    svn co \
      https://freeorion.svn.sourceforge.net/svnroot/freeorion/trunk/FreeOrion/ \
      --config-dir ./ -r 4517 "$pkgname"
  fi

  rm -rf "$srcdir/$pkgname-build"
  cp -r "$srcdir/$pkgname" "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  patch < "$srcdir/CMakeLists.patch"
  cmake -G "Unix Makefiles"
  make -j1

  # OGRE fix
  sed -i 's|PluginFolder=.|PluginFolder=/usr/lib/OGRE/|g' \
    "$srcdir/$pkgname-build/ogre_plugins.cfg"
}

package() {
  cd "$srcdir/$pkgname-build"

  msg2 "Packaging documentation..."
  install -Dm644 loki_setup/README.txt \
    "$pkgdir/usr/share/doc/$pkgname/README.txt"

  msg2 "Packaging binaries..."
  install -Dm 755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/freeorion"
  install -Dm 755 freeorion "$pkgdir/usr/bin/freeorion.elf"
  install -Dm 755 freeorionca "$pkgdir/usr/bin/freeorionca"
  install -Dm 755 freeoriond "$pkgdir/usr/bin/freeoriond"

  msg2 "Packaging data..."
  mkdir -p "$pkgdir/usr/share/$pkgname" "$pkgdir/usr/lib/$pkgname"
  cp -r default "$pkgdir/usr/share/$pkgname"
  install -Dm 644 OISInput.cfg "$pkgdir/usr/share/OISInput.cfg"

  msg2 "Packaging license..."
  install -Dm 644 default/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  cd ../..

  msg2 "Packaging icon and shortcut..."
  install -Dm 644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/freeorion.png"
  install -Dm 644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/freeorion.desktop"

  install -Dm 644 "$srcdir/$pkgname-build/ogre_plugins.cfg" \
    "$pkgdir/usr/share/freeorion/ogre_plugins.cfg"

  find "$pkgdir" -name ".svn" -print0 | xargs -0 rm -rf
}

# vim:set ts=2 sw=2 et:
