# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=freeorion
pkgver=0.3.15
pkgrel=2
pkgdesc="Turn-based galactic conquest game inspired by Master of Orion"
arch=('x86_64' 'i686')
url="http://www.freeorion.org/"
license=('GPL')
if [ "$CARCH" = "x86_64" ]
then
  depends=('lib32-nvidia-cg-toolkit')
else
  depends=('nvidia-cg-toolkit')
fi

makedepends=('setconf')
install=freeorion.install
source=("http://freeorion.psitronic.de/download/FreeOrion-$pkgver-Linux-i386.tar.gz"
				"http://a.fsdn.com/con/icons/fr/freeorion@sf.net/FO_Icon_256x256.png"
        "freeorion.desktop")
md5sums=('150b5d24f71ec0efb0d976f6dbfd675c'
         '642d3b488be85180cf6ad144bce2f102'
         '6ef93d69a3dc1961a34d46a4d37f2788')

build() {
  cd "$srcdir/$pkgname"

  setconf freeorion ROOT "/usr/share/$pkgname"
  setconf freeorion "export PYTHONHOME" "/usr/lib/$pkgname/python2.5"
  sed -i 's:ldd freeorionca:ldd /usr/bin/freeorionca:' freeorion 
  sed -i 's: ./freeorion: /usr/bin/freeorion.elf -f:' freeorion 
  rmdir application/default/data/art/galaxy_decoration/ultramarine_set
}

package() {
  cd "$srcdir/$pkgname"

  msg2 "Packaging documentation..."
  install -Dm644 README.txt \
    "$pkgdir/usr/share/doc/$pkgname/README.txt"
  install -Dm644 README \
    "$pkgdir/usr/share/doc/$pkgname/README"
  install -Dm644 RELEASE-NOTES-V03.txt \
    "$pkgdir/usr/share/doc/$pkgname/RELEASE-NOTES-V03.txt"

  msg2 "Packaging binaries..."
  install -Dm755 freeorion "$pkgdir/usr/bin/$pkgname"

  cd application

  install -Dm755 freeorion "$pkgdir/usr/bin/freeorion.elf"
  install -Dm755 freeorionca "$pkgdir/usr/bin/freeorionca"
  install -Dm755 freeoriond "$pkgdir/usr/bin/freeoriond"

  msg2 "Packaging data..."
  mkdir -p "$pkgdir/usr/share/$pkgname" "$pkgdir/usr/lib/$pkgname"
  cp -r default "$pkgdir/usr/share/$pkgname"
  cp -r python2.5 "$pkgdir/usr/lib/$pkgname/python2.5"
  install -Dm644 OISInput.cfg "$pkgdir/usr/share/OISInput.cfg"

  cd ..

  install -Dm644 logo.png "$pkgdir/usr/share/$pkgname/logo.png"

  msg2 "Packaging license..."
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  cd ..

  msg2 "Packaging icon and shortcut..."
  install -Dm644 FO_Icon_256x256.png \
    "$pkgdir/usr/share/pixmaps/freeorion.png"
  install -Dm644 freeorion.desktop \
    "$pkgdir/usr/share/applications/freeorion.desktop"
}

# vim:set ts=2 sw=2 et:
