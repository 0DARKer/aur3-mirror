# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=freeicd
pkgver=0.1.0
pkgrel=1
pkgdesc="A multilingual coding assistant for the International Classification of Diseases version 10 (ICD10)."
arch=(i686 x86_64)
url="http://www.freemedforms.com/en/"
license=('GPL')
depends=('qt')
install="$pkgname.install"
source=("http://freemedforms.googlecode.com/files/${pkgname}fullsources-$pkgver.tgz")
md5sums=('5f06112fd398e643fbc009eef5319bfa')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake $pkgname.pro -r -config release \
    "CONFIG+=LINUX_INTEGRATED" \
    "INSTALL_ROOT_PATH=$pkgdir/usr/" \
    "LOWERED_APPNAME=$pkgname"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make install

  # Fix doc directories exec bit
  chmod a+x "$pkgdir/usr/share/$pkgname/doc/$pkgname/"{en,fr}/{html,}
}

# vim:set ts=2 sw=2 et:
