# $Id: PKGBUILD 58445 2011-11-12 19:02:51Z andrea $
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de
# Contributor: Cristian Maureira <saint [at] archlinux.cl>
# Contributor: Dr.Egg <rwhite @ archlinux . us>

pkgname=musescore
pkgver=1.1
pkgrel=4
pkgdesc="A music score editor written in Qt"
arch=('i686' 'x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('qt' 'qtwebkit' 'libsndfile' 'qtscriptgenerator' 'portaudio')
makedepends=('cmake' 'doxygen')
optdepends=('jack')
install=musescore.install
source=("http://downloads.sourceforge.net/mscore/mscore-${pkgver}.tar.bz2"
  '20-fix-dso-linking-et-al.patch' 'paths.patch' 'enable_portaudio_as_default.patch')
md5sums=('68b43af92093a16f7f074c0eb560a867'
         'f458c6604c04b9fffc9318e70dbdd066'
         'a0419cbe08b655820d52ea440653b1d3'
         'f30e9e6b60e789a2931016f4cf9d889a')

build() {
  cd $srcdir/mscore-${pkgver}
  export QT_PLUGINS_DIR=/usr/lib/qt/plugins 
  [ -d build ] && make clean
  patch -p1 < $srcdir/paths.patch
  patch -p1 < $srcdir/20-fix-dso-linking-et-al.patch
  patch -p2 < $srcdir/enable_portaudio_as_default.patch
  make PREFIX=/usr release 
}
package() {
  cd $srcdir/mscore-${pkgver}
  make DESTDIR="$pkgdir" install
}
