pkgname=hunspell-ca
pkgver=2.2.0
pkgrel=1
pkgdesc="Catalan hunspell dictionaries"
arch=(any)
url="http://www.softcatala.org/wiki/Projectes/Corrector_ortogr%C3%A0fic"
license=('GPL')
makedepends=('hunspell')
optdepends=('hunspell:	the spell checking libraries and apps')
source=('http://www.softcatala.org/diccionaris/actualitzacions/OOo/catalan.oxt')
md5sums=('5f84d53ebcaed6c419b8bf800bdfa3c7')

package(){

    #copy hunspell
    cd "${srcdir}"
    install -dm755 "${pkgdir}"/usr/share/hunspell
    cp -p dictionaries/catalan.aff "${pkgdir}"/usr/share/hunspell/ca_ES.aff
    cp -p dictionaries/catalan.dic "${pkgdir}"/usr/share/hunspell/ca_ES.dic
    
    #alias hunspell
    ca_ES_alias="ca_AD ca_FR ca_IT"
    pushd "${pkgdir}"/usr/share/hunspell/
    for lang in ${ca_ES_alias}; do
        ln -s ca_ES.aff "${lang}".aff
        ln -s ca_ES.dic "${lang}".dic
    done
    popd

    #myspell symlinks
    install -dm755 "${pkgdir}"/usr/share/myspell/dicts
    pushd "${pkgdir}"/usr/share/myspell/dicts
    for file in "${pkgdir}"/usr/share/hunspell/*; do
        ln -sv /usr/share/hunspell/"$(basename "${file}")" .
    done
    popd

    #docs
    #install -dm755 "${pkgdir}"/usr/share/doc/"${pkgname}"
    
}

