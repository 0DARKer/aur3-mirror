# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: FzerorubigD <Fzerorubigd {AT} GMail {DOT} com>

pkgname=dropbox-uploader-git
pkgver=0.12.1.r16.g70cfb6d
pkgrel=1
pkgdesc="A bash script to manage Dropbox from the CLI"
arch=('any')
url="https://github.com/andreafabrizi/Dropbox-Uploader"
license=('GPL3')
depends=('bash' 'curl')
makedepends=('git')
source=('git://github.com/andreafabrizi/Dropbox-Uploader.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir"/Dropbox-Uploader
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/v//'
}

prepare() {
  cd "$srcdir"/Dropbox-Uploader
  sed -i '/^DU=/s|=.*$|="/usr/bin/dropbox_uploader"|' dropShell.sh
}

package() {
  cd "$srcdir"/Dropbox-Uploader
  install -D -m 755 dropbox_uploader.sh "$pkgdir"/usr/bin/dropbox_uploader
  install -D -m 755 dropShell.sh "$pkgdir"/usr/bin/dropShell
}
