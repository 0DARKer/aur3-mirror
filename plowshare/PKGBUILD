# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: breakdown <breakdown(at)archlinux(dot)us>

pkgname=plowshare
pkgver=snapshot_git20120916
pkgrel=1
pkgdesc="Command-line downloader and uploader for Rapidshare, Mediafire and other file sharing websites."
arch=('any')
url="http://code.google.com/p/plowshare/"
license=('GPL')
depends=('curl' 'recode' 'js')
optdepends=('bash-completion: enable bash auto completion'
            'libcaca: ascii display for no X server'
            'tiv: ascii display for no X server'
            'aview: ascii display for no X server')
conflicts=("$pkgname-git")
source=("http://$pkgname.googlecode.com/files/$pkgname-${pkgver/_/-}.tar.gz")
md5sums=('9c129cf7a8a642220702a0ba348c84bf')

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  DESTDIR="$pkgdir/" PREFIX=/usr ./setup.sh install

  # bash completion script
  install -Dm644 etc/$pkgname.completion \
    "$pkgdir/etc/bash_completion.d/$pkgname"

  # set CDIR for bash completion
  sed -i "s|local/||" "$pkgdir/etc/bash_completion.d/$pkgname"
}

# vim:set ts=2 sw=2 et:
