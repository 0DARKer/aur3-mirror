# $Id: PKGBUILD 94458 2013-07-25 11:22:51Z tpowa $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168-aufs
_pkgname=r8168
pkgver=8.036.00
pkgrel=3
pkgdesc="A kernel module for Realtek 8168 network cards(aufs_friendly kernel)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux-aufs_friendly')
makedepends=('linux-aufs_friendly-headers')
source=(http://r8168.googlecode.com/files/$_pkgname-$pkgver.tar.bz2 linux-3.10.patch)
install=$_pkgname.install

prepare() {
	cd "$_pkgname-$pkgver"
	patch -p0 -i "$srcdir/linux-3.10.patch"
}

build() {
	_kernver=$(pacman -Q linux-aufs_friendly | cut -d . -f 2 | cut -f 1 -d -)
	KERNEL_RELEASE=$(cat /usr/lib/modules/extramodules-3.$_kernver-aufs_friendly/version)

	cd "$_pkgname-$pkgver"

	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/lib/modules/$KERNEL_RELEASE/build \
			SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	_kernver=$(pacman -Q linux-aufs_friendly | cut -d . -f 2 | cut -f 1 -d -)
	depends=("linux-aufs_friendly>=3.${_kernver}" "linux-aufs_friendly<3.$(expr ${_kernver} + 1)")
	KERNEL_VERSION=$(cat /usr/lib/modules/extramodules-3.$_kernver-aufs_friendly/version)
	msg "Kernel = $KERNEL_VERSION"

	cd "$_pkgname-$pkgver"
	install -Dm644 src/$_pkgname.ko "$pkgdir/usr/lib/modules/extramodules-3.$_kernver-aufs_friendly/$_pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +

	sed -i "s|extramodules-.*-aufs_friendly|extramodules-3.$_kernver-aufs_friendly|" "$startdir/$_pkgname.install"
}

sha256sums=('937fd5d7d4cbfc95cf2859d8c3beb15a8536dc4e41752f7a53f2a25f07cdcd33'
            '1fb115b4d1b6b058d23322b89fad52d29748ce75b2774815e72c679a235d0e87')
