# Maintainer: Jakub Klinkovsk√Ω <kuba.klinkovsky@gmail.com>

pkgname=offlineimap-git
_pkgname=offlineimap
pkgver=v6.5.5.rc2.7.ga73b4b3
pkgrel=1
pkgdesc="A powerful IMAP/Maildir synchronization tool"
url="http://offlineimap.org/"
arch=('any')
license=('GPL2')
depends=('python2')
makedepends=('git' 'python2-docutils')
conflicts=('offlineimap')
provides=('offlineimap')
source=('git://github.com/OfflineIMAP/offlineimap.git')
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "$_pkgname"
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         $(find ./ -name '*.py')
} 

package() {
  cd "$_pkgname"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  # manually build documentation (doc/Makefile is broken)
	sed -i 's/\^/=/g' docs/MANUAL.rst
	rst2man2 docs/MANUAL.rst offlineimap.1

  install -D -m644 offlineimap.1 ${pkgdir}/usr/share/man/man1/offlineimap.1
  install -D -m644 offlineimap.conf ${pkgdir}/usr/share/offlineimap/offlineimap.conf
  install -D -m644 offlineimap.conf.minimal ${pkgdir}/usr/share/offlineimap/offlineimap.conf.minimal
}

# vim:set ts=2 sw=2 et:
