# Ex-Maintainer: Scott Olson <scott@scott-olson.org>
# Maintainer: Corey Powell <mistdragon100@gmail.com>

pkgname=rock
pkgver=0.9.8
pkgrel=1
pkgdesc="An ooc compiler written in ooc"
arch=('i686' 'x86_64')
license=('BSD')
url="http://ooc-lang.org/"
source=("https://github.com/nddrylliog/rock/archive/v${pkgver}.tar.gz"
        "http://downloads.ooc-lang.org/rock/${pkgver}/latest-bootstrap.tar.bz2")
makedepends=('gcc')
depends=('sh')
optdepends=(
  'gcc: can be used as backend C compiler'
  'clang: can be used as backend C compiler'
  'tcc: can be used as backend C compiler'
)
md5sums=('d02e7575e38849436ac8f9167ffbebb9'
         '7445a924cfc3fa38589579dbb3353511')

build() {
  mv build "${srcdir}/rock-${pkgver}"
  cd "${srcdir}/rock-${pkgver}"

  make bootstrap || return 1
}

package() {
  cd "${srcdir}/rock-${pkgver}"

  install -d "$pkgdir/opt/$pkgname"
  cp -rt "$pkgdir/opt/$pkgname" libs sdk sdk.use rock.use pcre.use
  rm -rf "$pkgdir/opt/$pkgname/libs/sources"

  install -D bin/rock "$pkgdir/opt/$pkgname/bin/rock"
  install -d "$pkgdir/usr/bin"
  echo '#!/bin/sh' > "$pkgdir/usr/bin/rock"
  echo 'ROCK_DIST=/opt/rock /opt/rock/bin/rock "$@"' >> "$pkgdir/usr/bin/rock"
  chmod +x "$pkgdir/usr/bin/rock"

  install -D docs/rock.1 "$pkgdir/usr/share/man/man1/rock.1"
  install -D LICENSE "$pkgdir/usr/share/licenses/rock/LICENSE"
}
