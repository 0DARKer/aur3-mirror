# Maintainer: Scott Olson <scott@scott-olson.org>

pkgname=rock
pkgver=0.9.6
pkgrel=2
pkgdesc="An ooc compiler written in ooc"
arch=('i686' 'x86_64')
license=('BSD')
url="http://ooc-lang.org/"
source=("https://github.com/nddrylliog/rock/archive/v${pkgver}.tar.gz"
        "http://ooc-lang.org/downloads/${pkgver}/latest-bootstrap.tar.bz2")
makedepends=('gcc')
depends=('sh')
optdepends=(
  'gcc: can be used as backend C compiler'
  'clang: can be used as backend C compiler'
  'tcc: can be used as backend C compiler'
)
md5sums=('86ffb6a315aec01128caadeabbd0959a'
         '27b1a157a4dd8abe9cf7b82a1d2ddccd')

build() {
  mv build "${srcdir}/rock-${pkgver}"
  cd "${srcdir}/rock-${pkgver}"

  make bootstrap || return 1
}

package() {
  cd "${srcdir}/rock-${pkgver}"

  install -d "$pkgdir/opt/$pkgname"
  cp -rt "$pkgdir/opt/$pkgname" libs sdk sdk.use rock.use pcre.use
  rm -rf "$pkgdir/opt/$pkgname/libs/sources"

  install -D bin/rock "$pkgdir/opt/$pkgname/bin/rock"
  install -d "$pkgdir/usr/bin"
  echo '#!/bin/sh' > "$pkgdir/usr/bin/rock"
  echo 'ROCK_DIST=/opt/rock /opt/rock/bin/rock "$@"' >> "$pkgdir/usr/bin/rock"
  chmod +x "$pkgdir/usr/bin/rock"

  install -D docs/rock.1 "$pkgdir/usr/share/man/man1/rock.1"
  install -D LICENSE "$pkgdir/usr/share/licenses/rock/LICENSE"
}
