# Maintainer: Idares <idares@seznam.cz>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>
# Contributor: Vadym Abramchuk <abramm@gmail.com>
# Contributor: karol_007 <karol.blazewicz@gmail.com>

pkgname=zabbix-agent
pkgver=2.0.6
pkgrel=2
pkgdesc="Software for monitoring of your applications, network and servers."
arch=('i686' 'x86_64')
url="http://www.zabbix.com"
license=('GPL')
backup=('etc/zabbix/zabbix_agent.conf'
        'etc/zabbix/zabbix_agentd.conf'
		)
install="zabbix-agent.install"
source=("http://downloads.sourceforge.net/project/zabbix/ZABBIX%20Latest%20Stable/$pkgver/zabbix-$pkgver.tar.gz"
        "zabbix-agentd"
        "zabbix-agent.install"
		"zabbix-agentd.service"
		)

build() {
	cd $srcdir/zabbix-$pkgver
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/zabbix \
		--enable-agent
	make
}

package() {
	cd $srcdir/zabbix-$pkgver
	make DESTDIR=$pkgdir install

	install -d -m0750 $pkgdir/var/run/zabbix
	install -d -m0750 $pkgdir/var/log/zabbix

	install -D -m0755 $startdir/src/zabbix-agentd $pkgdir/etc/rc.d/zabbix-agentd 
	install -D -m0644 $srcdir/zabbix-agentd.service $pkgdir/usr/lib/systemd/system/zabbix-agentd.service

	# change pid file location
	sed -i 's:# PidFile=.*:PidFile=/var/run/zabbix/zabbix_agentd.pid:' $pkgdir/etc/zabbix/zabbix_agentd.conf
}

md5sums=('f7261987731dd74b58cb1da890655ddc'
         '3a3830fa4cd239cf8631c33e3fa69d13'
         '9d929ffc6cf5e14cbe6172ea5fab2b4f'
         'b3b9487aadc6deaefce56a495f4dbb5d')
sha1sums=('75a747ddcfa4bcd5792f69dc8d7de9c5839b8595'
          '0d0d2a22bb3985b241c94c2bf45070aba534dfa0'
          'cb9c3d436ba19fd2305ef6ff35f5ff2d6589ed5f'
          '7b0a18c648e3a11cf5e6bb896a38213dcdb609dc')

