# Maintainer: Maciej Å»ok <maciek.zok@gmail.com>
# Contributor: Florian Krauthan <fkrauthan@gmx.net>
pkgname=toggl-desktop
pkgver=7.1.73
pkgrel=1
pkgdesc="Time tracker application"
arch=("x86_64")
url="http://www.toggl.com/"
license=(custom)
depends=("hicolor-icon-theme" "icu" "libxss" "poco"
	 "qt5-base" "qt5-declarative" "qt5-location" "qt5-sensors" "qt5-webkit")
conflicts=("toggl")
replaces=("toggl")
install="toggl-desktop.install"
source=("http://assets.toggl.com/installers/toggldesktop_7.1.73_amd64.deb"
        "toggl-desktop.install")
sha256sums=("f61327712abab2e924422b9dca8368099ca962cdd890d65c50d56d7d79f32570"
            "6f3c40e5d18170e99a002e075bf203d2cedcd445252de6e10f0de77dedf3a8bc")

prepare() {
  cd "${srcdir}"

  msg2 "Extracting files..."
  tar -xf data.tar.gz
}

package() {
  cd "${srcdir}"

  msg2 "Installing program..."
  install -d "${pkgdir}/opt/${pkgname}"
  install -d "${pkgdir}/usr/bin"
  install -Dm0755 "${srcdir}/opt/toggldesktop/TogglDesktop" \
    "${pkgdir}/opt/${pkgname}/TogglDesktop"
  install -Dm0755 "${srcdir}/opt/toggldesktop/cacert.pem" \
    "${pkgdir}/opt/${pkgname}/cacert.pem"
  ln -s "/opt/${pkgname}/TogglDesktop" "${pkgdir}/usr/bin/TogglDesktop"

  msg2 "Installing libraries..."
  install -Dm0644 "${srcdir}/opt/toggldesktop/lib/libbugsnag-qt.so.1" \
    "${pkgdir}/opt/${pkgname}/lib/libbugsnag-qt.so.1"
  install -Dm0644 "${srcdir}/opt/toggldesktop/lib/libjson.so.7" \
    "${pkgdir}/opt/${pkgname}/lib/libjson.so.7"
  install -Dm0644 "${srcdir}/opt/toggldesktop/lib/libTogglDesktopLibrary.so.1" \
    "${pkgdir}/opt/${pkgname}/lib/libTogglDesktopLibrary.so.1"

  msg2 "Installing icons..."
  install -d "${pkgdir}/usr/share"
  cp -r "${srcdir}/usr" "${pkgdir}"
  rm -rf "${pkgdir}/usr/share/doc"

  msg2 "Installing license..."
  install -Dm755 "${srcdir}/usr/share/doc/toggldesktop/copyright" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

