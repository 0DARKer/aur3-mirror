# Contributor: JokerBoy <jokerboy at punctweb dot ro>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-bfs-beta
pkgver=280.13
_kernver='3.0-bfs'
pkgrel=2
pkgdesc="NVIDIA beta drivers for linux-bfs."
arch=('i686' 'x86_64')
url="http://www.nvidia.com"
depends=('linux-bfs>=3.0' 'linux-bfs<3.1' "nvidia-utils-beta=${pkgver}")
makedepends=('linux-bfs-headers>=3.0' 'linux-bfs-headers<3.1')
conflicts=('nvidia-bfs' 'nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-all' 'nvidia-beta-all')
provides=("nvidia=${pkgver}" "nvidia-bfs=${pkgver}")
license=('custom')
install=nvidia.install
options=(!strip)
[ "$CARCH" = "i686" ] && _arch='x86' && _pkg="NVIDIA-Linux-${_arch}-${pkgver}" && md5sums=('c3992c8cef02b7773a398640d8830e77')
[ "$CARCH" = "x86_64" ] && _arch='x86_64' && _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32" && md5sums=('05e62b61feb34bf412f83ca1074e1917')
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

build() {
    cd "${srcdir}"
    [ -d "${_pkg}" ] && rm -rf "${_pkg}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC="/lib/modules/${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
    install -d -m755 "${pkgdir}/etc/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist_bfs.conf"
    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
}
