# Maintainer: willemw <willemw12@gmail.com>

_gitname=youtube-dl-gui
pkgname=$_gitname-git
pkgver=r11.79bcc5b
pkgrel=1
pkgdesc="A cross platform front-end GUI of the popular youtube-dl written in wxPython"
arch=('any')
url="https://github.com/MrS0m30n3/youtube-dl-gui"
license=('custom:UNLICENSE')
depends=('python2' 'wxpython' 'youtube-dl')
optdepends=('ffmpeg: convert video files to audio-only files')
makedepends=('git' 'python2-distribute')
provides=($_gitname)
conflicts=($_gitname)
source=(git://github.com/MrS0m30n3/$_gitname.git
        youtube-dl-gui.desktop
        UNLICENSE)
#        http://unlicense.org/UNLICENSE)
md5sums=('SKIP'
         '8111176e2ac4e20e4e3157ab59c7cc59'
         '7246f848faa4e9c9fc0ea91122d6e680')

pkgver() {
  cd $_gitname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_gitname
  sed -i 's|#![ ]*/usr/bin/env python[ ]*$|#!/usr/bin/env python2|' setup.py youtube_dl_gui/*.py
  sed -i 's|../icons/ytube.png|/usr/share/pixmaps/youtube-dl-gui.png|g' youtube_dl_gui/YoutubeDLGUI.py
}

package() {
  install -Dm644 youtube-dl-gui.desktop "$pkgdir/usr/share/applications/youtube-dl-gui.desktop"
  install -Dm644 UNLICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd $_gitname
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 icons/ytube.png "$pkgdir/usr/share/pixmaps/youtube-dl-gui.png"
  install -Dm755 youtube_dl_gui/__main__.py "$pkgdir/usr/bin/youtube-dl-gui"
}

