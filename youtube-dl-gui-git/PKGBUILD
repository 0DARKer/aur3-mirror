# Maintainer: willemw <willemw12@gmail.com>

_pkgname=youtube-dl-gui
pkgname=$_pkgname-git
pkgver=r32.e2caed1
pkgrel=1
pkgdesc="A cross platform front-end GUI of the popular youtube-dl written in wxPython"
arch=('any')
url="https://github.com/MrS0m30n3/youtube-dl-gui"
license=('custom:UNLICENSE')
depends=('python2' 'wxpython' 'youtube-dl')
optdepends=('ffmpeg: convert video files to audio-only files')
makedepends=('git' 'python2-distribute')
provides=($_pkgname)
conflicts=($_pkgname)
source=(git://github.com/MrS0m30n3/$_pkgname.git
        youtube-dl-gui.desktop
        UNLICENSE)
#        http://unlicense.org/UNLICENSE)
md5sums=('SKIP'
         '3cb12f083117f21d9a161fae9fc97d73'
         '7246f848faa4e9c9fc0ea91122d6e680')

pkgver() {
  cd $_pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $_pkgname
  sed -i "s|#![ ]*/usr/bin/env python[ ]*$|#!/usr/bin/env python2|" setup.py youtube_dl_gui/*.py
  sed -i "s|get_icon_path(\['ytube.png'.*|get_icon_path(\['youtube-dl-gui.png', '/usr/share/pixmaps'\], '/')|" youtube_dl_gui/YoutubeDLGUI.py
}

package() {
  install -Dm644 youtube-dl-gui.desktop "$pkgdir/usr/share/applications/youtube-dl-gui.desktop"
  install -Dm644 UNLICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd $_pkgname
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 icons/ytube.png "$pkgdir/usr/share/pixmaps/youtube-dl-gui.png"
  install -Dm755 youtube_dl_gui/__main__.py "$pkgdir/usr/bin/youtube-dl-gui"
}

