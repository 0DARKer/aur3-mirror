# Maintainer: Cravix < dr dot neemous at gmail dot com >
# combination of renpy and python-renpy PKGBUILD written by AlexanderR <rvacheva at nxt dot ru>
# the motivation is simple: i don't want to download the same file twice.
pkgbase=renpy-allinone
pkgname=renpy-allinone
true && pkgname=(renpy python-renpy renpy-allinone)
_pkgname=renpy
pkgver=6.15.4
pkgrel=1
pkgdesc="Visual novel engine Ren'Py along with its platdeps libs (release version)"
arch=('i686 x86_64')
license=('MIT')
url='http://www.renpy.org'
# is ttf-dejavu required by renpy or games itself?
depends=('ttf-dejavu' 'ffmpeg' 'fribidi' 'glew' 'python2-pygame')
makedepends=('cython2')
conflicts=('renpy-bin' 'renpy64')
replaces=('renpy64')
install=renpy.install
source=("http://www.renpy.org/dl/$pkgver/$_pkgname-$pkgver-source.tar.bz2"
        "${pkgname}.desktop"
        "${pkgname}."{sh,csh}
        'renpy-launcher.sh')

md5sums=('6752216e8aa6009296d18e5fdb9427b3'
         'a9beb35fa6c6d8af7ba5d2a764c33158'
         'd206d24b78e207a2c3b603fef14ac47f'
         '8b9922e26e567248a2a5adc1d0cdfdd4'
         'dfa92cdecc15e5c1ddee387fbbbb2d9c')

build() {
	cd "$srcdir/renpy-$pkgver-source"
	find . -name "*.py" -exec sed -i 's/env python$/env python2/' {} \;
	RENPY_CYTHON=/usr/bin/cython2 python2 module/setup.py build
}

package_renpy(){
	pkgdesc="Ren'Py is a visual novel engine that helps you use words, images, and sounds to tell stories with the computer. Both player and development tools included."
	arch=('any')
	depends=('ttf-dejavu' 'python-renpy')
	conflicts=('renpy-bin' 'renpy64')
	replaces=('renpy64')

	mkdir -p "$pkgdir/"{usr/share/{$pkgname,doc/$pkgname},etc/profile.d}

	cd "$srcdir"

	install -m755 ${pkgname}.{sh,csh} "$pkgdir/etc/profile.d"
	install -D -m755 $pkgname-launcher.sh "$pkgdir/usr/bin/$pkgname"
	install -D -m644 ${pkgname}.desktop "$pkgdir/usr/share/applications/${pkgname}.desktop"

	cd ${pkgname}-$pkgver-source

	cp -r launcher renpy renpy.py	template the_question tutorial "$pkgdir/usr/share/$pkgname"
	cp -r doc/* "$pkgdir/usr/share/doc/$pkgname"
	install -D -m644 launcher/game/logo.png "$pkgdir/usr/share/pixmaps/${pkgname}.png"
	install -D -m644 'LICENSE.txt' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	chgrp -R games "$pkgdir"/usr/share/renpy/{the_question,tutorial}
	chmod g+w "$pkgdir"/usr/share/renpy/{the_question,tutorial}
}

package_python-renpy() 
{
	pkgdesc="Platform-dependant Ren'Py libraries."
	depends=('ffmpeg' 'fribidi' 'glew' 'python2-pygame')
	conflicts=('renpy64' 'renpy-bin')

	cd "$srcdir"/$_pkgname-${pkgver}-source

	RENPY_CYTHON=/usr/bin/cython2 python2 module/setup.py install --root="$pkgdir/" --prefix=/usr --optimize=1
	install -D -m644 'LICENSE.txt' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_renpy-allinone()
{
	pkgdesc="Visual novel engine Ren'Py along with its platdeps libs (release version)"
	#why i create an empty package? yeah, right, to make it much easier for you to update.
	depends=("renpy>=$pkgver" "python-renpy>=$pkgver")
}
