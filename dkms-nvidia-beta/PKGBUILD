# Mantainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=('dkms-nvidia-beta')
_modname=nvidia
pkgver=290.03
pkgrel=1
pkgdesc="NVIDIA Dynamic Kernel Module (DKMS) drivers, utilities and libraries. BETA DRIVERS"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('custom')
makedepends=('linux-headers' 'dkms' 'xorg-server')
conflicts=('nvidia' 'libgl' 'nvidia-utils' 'opencl-nvidia' 'libcl')
remplace=('nvidia=285.05.09')
provides=("nvidia=${pkgver}" 'nvidia-utils' 'libgl' "nvidia-utils=${pkgver}-${pkgrel}" 'libcl' 'opencl-nvidia')
install="dkms-nvidia-beta.install"
options=('!strip')
optdepends=('cuda-toolkit: CUDA toolkit with cuda headers for NVIDIA cards'
            'lib32-nvidia-utils-beta: Nvidia 32bit libs'
            'gtk2: nvidia-settings' 'pkgconfig: nvidia-xconfig')
backup=('etc/X11/xorg.conf.d/20-nvidia.conf')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('6ad27a7e30c449018ce1dceec765d90a')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('90d578f828b5a4a8403dd82f8b046f09')
fi

source[1]="dkms.conf"
source[2]="dkms-nvidia-beta.install"
source[3]="20-nvidia.conf"
md5sums[1]='aaed3d3b1a9def2310b572e713c0fb49'
md5sums[2]='604c1b3d2eca54a0de5c411f95f455c5'
md5sums[3]='646e64d99c44eb24b02b28defe182317'


build() {
    cd $srcdir
    sh ${_pkg}.run --extract-only
}

package() {
    cd ${srcdir}/${_pkg}

# Copy header in DKMS makepath
    mkdir -p ${pkgdir}/usr/src/${_modname}-${pkgver}
    cp -a ${srcdir}/${_pkg}/kernel/* ${pkgdir}/usr/src/${_modname}-${pkgver}
    install -d -m755 ${pkgdir}/etc/modprobe.d
    cp ${srcdir}/dkms.conf ${pkgdir}/usr/src/${_modname}-${pkgver}/

# Add Noveau Blacklist
    mkdir -p ${pkgdir}/etc/modprobe.d/
    echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf"

# Create install dirs
    mkdir -p "${pkgdir}"/usr/{lib,bin,share/applications,share/pixmaps,share/man/man1}
    mkdir -p "${pkgdir}"/usr/lib/xorg/modules/{extensions,drivers}
    mkdir -p ${pkgdir}/usr/lib/vdpau
    mkdir -p ${pkgdir}/usr/share/licenses/nvidia
    mkdir -p ${pkgdir}/etc/OpenCL/vendors
    mkdir -p ${pkgdir}/etc/X11/xorg.conf.d

# Install OpenCL configuration
    install nvidia.icd "${pkgdir}/etc/OpenCL/vendors"

# Install libraries
    install {libGL,libnvidia-cfg,libnvidia-compiler,libnvidia-glcore,libcuda,tls/libnvidia-tls,libnvidia-wfb,libnvcuvid,libnvidia-ml}.so.${pkgver} ${pkgdir}/usr/lib/
    install -m755 libvdpau_nvidia.so.${pkgver} ${pkgdir}/usr/lib/vdpau/
    install -m755 libOpenCL.so.1.0.0 ${pkgdir}/usr/lib
    install {libXvMCNVIDIA.a,libXvMCNVIDIA.so.${pkgver}} ${pkgdir}/usr/lib/
    install nvidia_drv.so ${pkgdir}/usr/lib/xorg/modules/drivers
    install libglx.so.${pkgver} ${pkgdir}/usr/lib/xorg/modules/extensions

# Install manpages
    install -m644 nvidia-{settings,xconfig,smi}.1.gz ${pkgdir}/usr/share/man/man1/

# Install license
    install -m644 LICENSE ${pkgdir}/usr/share/licenses/nvidia/
    ln -s nvidia ${pkgdir}/usr/share/licenses/nvidia-utils

# Install readme
    install -D -m644 README.txt ${pkgdir}/usr/share/doc/nvidia/README

# Install .desktop file
    install -m644 nvidia-settings.desktop ${pkgdir}/usr/share/applications/

# Fix nvidia .desktop file
    sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i ${pkgdir}/usr/share/applications/nvidia-settings.desktop

# Install pixmaps
    install -m644 nvidia-settings.png ${pkgdir}/usr/share/pixmaps/

# Install binaries
    install -m755 nvidia-{settings,xconfig,smi,bug-report.sh} ${pkgdir}/usr/bin/

# Create symlinks
    cd ${pkgdir}/usr/lib/
    ln -s libOpenCL.so.1.0.0 libOpenCL.so.1
    ln -s libOpenCL.so.1 libOpenCL.so
    ln -s libGL.so.${pkgver} libGL.so.1
    ln -s libGL.so.${pkgver} libGL.so
    ln -s libnvidia-glcore.so.${pkgver} libnvidia-glcore.so.1
    ln -s libnvidia-glcore.so.${pkgver} libnvidia-glcore.so
    ln -s libnvidia-cfg.so.${pkgver} libnvidia-cfg.so.1
    ln -s libnvidia-cfg.so.${pkgver} libnvidia-cfg.so
    ln -s libnvidia-compiler.so.${pkgver} libnvidia-compiler.so.1
    ln -s libnvidia-compiler.so.${pkgver} libnvidia-compiler.so
    ln -s libnvidia-tls.so.${pkgver} libnvidia-tls.so.1
    ln -s libnvidia-tls.so.${pkgver} libnvidia-tls.so
    ln -s libcuda.so.${pkgver} libcuda.so.1
    ln -s libcuda.so.${pkgver} libcuda.so
    ln -s libvdpau_nvidia.so.${pkgver} vdpau/libvdpau_nvidia.so.1
    ln -s libvdpau_nvidia.so.${pkgver} vdpau/libvdpau_nvidia.so
    ln -s libnvcuvid.so.${pkgver} libnvcuvid.so.1
    ln -s libnvcuvid.so.${pkgver} libnvcuvid.so
    ln -s libXvMCNVIDIA.so.${pkgver} libXvMCNVIDIA_dynamic.so.1
    ln -s libnvidia-ml.so.${pkgver} libnvidia-ml.so.1
    ln -s libnvidia-ml.so.${pkgver} libnvidia-ml.so
    cd ${pkgdir}/usr/lib/xorg/modules/extensions
    ln -s libglx.so.${pkgver} libglx.so

    find ${pkgdir}/usr -type d -exec chmod 755 {} \;
    chmod 644 ${pkgdir}/usr/lib/libXvMCNVIDIA.a

# Install nvidia file for xorg autodection
    install -D -m644 ${srcdir}/20-nvidia.conf ${pkgdir}/etc/X11/xorg.conf.d/20-nvidia.conf
}