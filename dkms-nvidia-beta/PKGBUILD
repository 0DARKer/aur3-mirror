# Mantainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname="dkms-nvidia-beta"
_modname=nvidia
pkgver=275.09.04
pkgrel=1
pkgdesc="NVIDIA dynamic kernel module (DKMS) drivers, utilities and libraries for kernel26. BETA DRIVERS"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('custom')
depends=("nvidia-utils-beta=${pkgver}")
makedepends=('kernel26-headers' 'dkms')
conflicts=('nvidia-96xx' 'nvidia-71xx' 'nvidia-legacy' 'nvidia')
provides=("nvidia=${pkgver}")
install=("dkms-nvidia-beta.install")

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('f051e4e11e13e7bf0f5ffb6d0025111a')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('0433003778f2be8c72deb4fde25ebd00')
fi

source[1]="dkms.conf"
source[2]="dkms-nvidia-beta.install"
md5sums[1]='1773e7cea6406e847a6cf104f9c5ff4c'
md5sums[2]='4975647e28426a94484e2963457f74cb'


build() {
    cd $srcdir
    sh ${_pkg}.run --extract-only
}

package() {
    mkdir -p ${pkgdir}/usr/src/${_modname}-${pkgver}
    cp -a ${srcdir}/${_pkg}/kernel/* ${pkgdir}/usr/src/${_modname}-${pkgver}
    install -d -m755 ${pkgdir}/etc/modprobe.d
    cp ${srcdir}/dkms.conf ${pkgdir}/usr/src/${_modname}-${pkgver}/
}