# Mantainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname="dkms-nvidia-beta"
_modname=nvidia
pkgver=270.41.19
pkgrel=1
pkgdesc="NVIDIA dynamic kernel module (DKMS) drivers, utilities and libraries for kernel26. FOR [testing] REPO"
arch=('any')
url="http://www.nvidia.com/"
license=('custom')
optdepends=("nvidia-utils=${pkgver}")
conflicts=('nvidia')
provides=("nvidia=${pkgver}")
install="nvidia.install"

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('c167e32702f56599bd600add97943312')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('cb518bdd70f356425b8c4a5a48c0ad9c')
fi

source[1]="dkms.conf"
source[2]="nvidia.install"
md5sums[1]='4a1fd931eab1755692b656383f7f396b'
md5sums[2]='3f49a6504d1d43b06483e7d1127fb993'


build() {
    cd $srcdir
    sh ${_pkg}.run --extract-only
}

package() {
    mkdir -p ${pkgdir}/usr/src/${_modname}-${pkgver}
    cp -a ${srcdir}/${_pkg}/kernel/* \
    ${pkgdir}/usr/src/${_modname}-${pkgver}
    install -d -m755 ${pkgdir}/etc/modprobe.d
    echo "blacklist nouveau" >> ${pkgdir}/etc/modprobe.d/nouveau_blacklist.conf
    cp ${srcdir}/dkms.conf ${pkgdir}/usr/src/${_modname}-${pkgver}/
}