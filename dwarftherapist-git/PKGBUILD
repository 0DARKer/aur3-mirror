pkgname=dwarftherapist-git
pkgver=r682.34d8fe548e4c
pkgrel=1
pkgdesc="Heavily modified version of the original Dwarf Therapist."
url="https://github.com/splintermind/Dwarf-Therapist"
arch=('x86_64' 'i686')
license=('MIT')
depends=('qt5-base' 'qt5-script' 'libxkbcommon-x11')
makedepends=()
conflicts=()
replaces=()
backup=()
install="dwarftherapist.install"
source=("$pkgname"::'git://github.com/kiryl/Dwarf-Therapist#branch=DF2014'
  'fix-file-path.patch' 'dwarftherapist.desktop' 'dwarftherapist.install')
md5sums=('SKIP'
  'de3e664686f9f296a9a971f7060f93a6'
  '32c80fd6e3b86900c1677b4e2859b07b'
  'be44acd8f3f81816e2c946dbbfb9f19b')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  patch -p1 < "$srcdir/fix-file-path.patch"
  qmake -qt=5
  make
}

package() {
  cd "$srcdir/$pkgname"
  make INSTALL_ROOT="$pkgdir" install
  mv "$pkgdir/usr/bin/DwarfTherapist" "$pkgdir/usr/bin/dwarftherapist"
  install -Dm644 "$srcdir/dwarftherapist.desktop" \
    "$pkgdir"/usr/share/applications/dwarftherapist.desktop
  install -Dm644 img/hammer.png \
    "$pkgdir"/usr/share/icons/hicolor/128x128/apps/dwarftherapist.png
}

# vim:set ts=2 sw=2 et:
