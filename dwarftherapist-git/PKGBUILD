# Maintainer: Kirill A. Shutemov <kirill+arch@shutemov.name>
pkgname=dwarftherapist-git
pkgver=r946.6597186bc107
pkgrel=1
pkgdesc="Heavily modified version of the original Dwarf Therapist."
url="https://github.com/splintermind/Dwarf-Therapist"
arch=('x86_64' 'i686')
license=('MIT')
depends=('qt5-declarative' 'hicolor-icon-theme')
makedepends=('git' 'texlive-latexextra')
conflicts=()
replaces=()
backup=()
install="dwarftherapist.install"
source=("$pkgname"::'git://github.com/splintermind/Dwarf-Therapist#branch=DF2014'
  'dwarftherapist.desktop' 'dwarftherapist.install')
md5sums=('SKIP'
  '32c80fd6e3b86900c1677b4e2859b07b'
  'caae1175c3bb8104bdc9e3014e648e57')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  qmake -qt=5
  make
}

package() {
  cd "$srcdir/$pkgname"
  make INSTALL_ROOT="$pkgdir" install
  rm "$pkgdir/usr/bin/dwarftherapist"
  mv "$pkgdir/usr/bin/DwarfTherapist" "$pkgdir/usr/bin/dwarftherapist"
  install -Dm644 "$srcdir/dwarftherapist.desktop" \
    "$pkgdir"/usr/share/applications/dwarftherapist.desktop
  install -Dm644 resources/img/hammer.png \
    "$pkgdir"/usr/share/icons/hicolor/128x128/apps/dwarftherapist.png
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
