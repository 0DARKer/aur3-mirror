# Maintainer: Lukas Braun <koomi+aur at hackerspace-bamberg dot de>
# Contributor: David Arroyo <droyo@aqwari.us>
pkgname=execline-musl
pkgver=2.0.0.0
pkgrel=1
pkgdesc="A (non-interactive) scripting language, like sh. Statically linked against musl."
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/execline"
license=('custom:ISC')
makedepends=('skalibs-musl>=2.0.0.0' 'musl')
provides=(execline=2.0.0.0)
options=('staticlibs')
changelog=CHANGELOG
source=("${url}/execline-$pkgver.tar.gz" 'execlineb.eb')
sha256sums=('e17039a8321bc6a99a3728e662e0ed22d439632c08c7241913f738dd5934bbbc'
            '70e1e8c90ccf81fd76aea1d786102dcb1e172b6ec06bf957a07722b1f70a2278')

build() {
  cd "$srcdir/execline-$pkgver"

  CC="musl-gcc" ./configure --enable-static-libc --bindir=/usr/lib/execline --with-include=/usr/include
  make
}

package() {
  cd "$srcdir/execline-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -D -m755 "$srcdir/execlineb.eb" "$pkgdir/usr/bin/execlineb"
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
