# Maintainer: Lukas Braun <koomi+aur at hackerspace-bamberg dot de>
# Contributor: David Arroyo <droyo@aqwari.us>
pkgname=execline-musl
pkgver=1.3.1.1
pkgrel=1
pkgdesc="A (non-interactive) scripting language, like sh. Statically linked against musl."
arch=('i686' 'x86_64')
url="http://www.skarnet.org/software/execline"
license=('custom:ISC')
makedepends=('skalibs-musl>=1.6.0.0' 'musl')
provides=(execline)
options=('staticlibs')
changelog=CHANGELOG
source=("${url}/execline-$pkgver.tar.gz" 'execlineb.eb')
sha256sums=('efdb3292ac8dc48d2c42928aab5d3d171580fd92740434c95dd398e0e2c723af'
            '70e1e8c90ccf81fd76aea1d786102dcb1e172b6ec06bf957a07722b1f70a2278')

build() {
  cd "$srcdir/admin/execline-$pkgver"
  echo musl-gcc                 > conf-compile/conf-cc
  echo musl-gcc -static         > conf-compile/conf-ld
  echo musl-gcc                 > conf-compile/conf-dynld
  echo /usr/lib/skalibs/sysdeps > conf-compile/import
  echo /usr/lib/execline        > conf-compile/conf-install-command
  echo /usr/lib/execline        > conf-compile/conf-install-library
  echo /usr/include/execline    > conf-compile/conf-install-include
  echo /usr/include/skalibs     > conf-compile/path-include
  echo /usr/lib/skalibs         > conf-compile/path-library
  rm -f conf-compile/flag-slashpackage
  rm -f conf-compile/flag-allstatic
  package/compile
}

package() {
  cd "$srcdir/admin/execline-$pkgver"
  for i in package/*.exported
  do
    case `basename $i` in
    library.so.exported) d=usr/lib                ;;
    include.exported)    d=usr/include/execline   ;;
    library.exported)    d=usr/lib/execline       ;;
    command.exported)    d=usr/lib/execline       ;;
    esac
    f=`basename $i|sed 's/.exported//'`
    mkdir -p $pkgdir/$d
    install `sed s,^,$f/, $i` "$pkgdir/$d"
  done
  install -D -m755 "$srcdir/execlineb.eb" "$pkgdir/usr/bin/execlineb"
  install -D -m644 package/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
