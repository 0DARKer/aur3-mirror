# Maintainer: Jameson Pugh <imntreal@gmail.com>

pkgname=calligra
true && pkgname=('calligra-filters' 'calligra-interfaces' 'calligra-libs' 'calligra-plugins' \
  'calligra-pics' 'calligra-servicetypes' 'calligra-templates' 'calligra-tools' \
  'calligra-karbon' 'calligra-kexi' 'calligra-kounavail' \
  'calligra-plan' 'calligra-stage' 'calligra-krita' 'calligra-sheets' \
  'calligra-sheets-doc' 'calligra-words' 'calligra-handbook' 'calligra-thesaurus-doc' \
  'calligra-flow' 'calligra-braindump' 'calligra-mobile' 'calligra-active')
pkgver=2.4.0
pkgrel=2
arch=('i686' 'x86_64')
license=('FDL1.2' 'GPL2' 'LGPL')
source=("http://download.kde.org/stable/calligra-latest/calligra-$pkgver.tar.bz2"
	'pole.patch')
sha256sums=('7c8cd1ec8cd2b26d5544f3b5a283052c5cd82f212749475b91cc340f7e4029b0'
  'ccda1e774d572fbcf7815a534b0f94da78476ffd5f42ebb49633099fd1bcd82e')
makedepends=('kdepimlibs' 'lcms2' 'gsl' 'glew' 'libqtgtl' 'libpqxx' 'fftw' \
  'libspnav' 'automoc4' 'boost' 'cmake' 'eigen' 'pstoedit' \
  'libwpd' 'libwpg' 'docbook-xml' 'libgsf' 'kdegraphics-okular' \
  'kdeedu-marble' 'libkdcraw' 'freetds' 'libwps' 'libvisio')
groups=('calligra')


build() {

  cd $srcdir
  patch -p0 < pole.patch
  mkdir -p $srcdir/build
  cd $srcdir/build  
  cmake ../calligra-$pkgver -DCMAKE_BUILD_TYPE=Release \
  -DCMAKE_SKIP_RPATH=ON \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DIHAVEPATCHEDQT=1
  make
}

package_calligra-filters() {
  pkgdesc="Filters for the Calligra office suite"
  optdepends=('libgsf: Microsoft Word document importer'
    'libwpd: WordPerfect document importer'
    'libwpg: Corel WordPerfect Graphics image importer'
    'pstoedit: Karbon EPS import filter'
    'libkdcraw: support for raw images'
    'libwps: Words WPS import filter'
    'libvisio: Visio Import Filter Library')
  conflicts=('koffice-filters' 'calligra-filters-git')
  install=calligra.install

  cd "${srcdir}/build/filters"
  make DESTDIR="${pkgdir}" install
}

package_calligra-interfaces() {
  pkgdesc="Interfaces for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-interfaces' 'calligra-interfaces-git')

  cd "${srcdir}/build/interfaces"
  make DESTDIR="${pkgdir}" install
}

package_calligra-libs(){
  pkgdesc="Libraries for the Calligra office suite"
  depends=('kdepimlibs' 'lcms2' 'calligra-servicetypes' 'kdeedu-marble')
  conflicts=('koffice-libs' 'calligra-libs-git')

  cd "${srcdir}/build/libs"
  make DESTDIR="${pkgdir}" install
}

package_calligra-plugins(){
  pkgdesc="Plugins for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-plugins' 'koffice-kchart' 'calligra-plugins-git')

  cd "${srcdir}/build/plugins"
  make DESTDIR="${pkgdir}" install
}

package_calligra-pics(){
  pkgdesc="Icons for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-pics' 'calligra-pics-git')
  install=calligra.install

  cd "${srcdir}/build/pics"
  make DESTDIR="${pkgdir}" install
}

package_calligra-servicetypes(){
  pkgdesc="Servicetypes for the Calligra office suite"
  conflicts=('koffice-servicetypes' 'calligra-servicetypes-git')

  cd "${srcdir}/build/servicetypes"
  make DESTDIR="${pkgdir}" install
}

package_calligra-templates(){
  pkgdesc="Templates for the Calligra office suite"
  conflicts=('koffice-templates' 'calligra-templates-git')

  cd "${srcdir}/build/templates"
  make DESTDIR="${pkgdir}" install
}

package_calligra-tools(){
  pkgdesc="Tools for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-tools' 'calligra-tools-git')

  cd "${srcdir}/build/tools"
  sed -i '/mobile/d' cmake_install.cmake
  make DESTDIR="${pkgdir}" install
}

package_calligra-karbon(){
  pkgdesc="Create scalable vector drawings"
  depends=('calligra-libs' 'calligra-templates')
  conflicts=('koffice-karbon' 'koffice-karbon-doc' 'calligra-karbon-doc-git' 'calligra-karbon-git')
  install=calligra.install

  cd "${srcdir}/build/karbon"
  make DESTDIR="${pkgdir}" install
}

package_calligra-kexi(){
  pkgdesc="Develop desktop database applications"
  depends=('calligra-libs' 'calligra-templates' 'freetds')
  conflicts=('koffice-kexi' 'koffice-kexi-doc' 'calligra-kexi-doc-git' 'calligra-kexi-git')

  cd "${srcdir}/build/kexi"
  make DESTDIR="${pkgdir}" install
}

package_calligra-kounavail(){
  pkgdesc="Unavailable Calligra Document"
  depends=('calligra-libs')
  conflicts=('koffice-kounavail' 'calligra-kounavail-git')

  cd "${srcdir}/build/kounavail"
  make DESTDIR="${pkgdir}" install
}

package_calligra-plan(){
  pkgdesc="Calligra Project Management Component"
  depends=('calligra-words')
  conflicts=('calligra-kplato' 'koffice-kplato' 'calligra-kplato-git' 'calligra-plan-git')
  replaces=('calligra-kplato')
  install=calligra.install

  cd "${srcdir}/build/plan"
  make DESTDIR="${pkgdir}" install
}

package_calligra-stage(){
  pkgdesc="Write presentation documents"
  depends=('calligra-libs' 'calligra-templates')
  optdepends=('calligra-filters: import/export filters')
  conflicts=('calligra-kpresenter' 'calligra-stage-git')
  install=calligra.install

  cd "${srcdir}/build/stage"
  make DESTDIR="${pkgdir}" install
}

package_calligra-krita(){
  pkgdesc="Edit and paint images"
  depends=('calligra-libs' 'calligra-templates' 'calligra-plugins' 'glew' \
  'qimageblitz' 'poppler-qt' 'openexr' 'libqtgtl')
  optdepends=('calligra-filters: import/export filters')
  conflicts=('koffice-krita' 'calligra-krita-git')
  install=calligra.install

  cd "${srcdir}/build/krita"
  make DESTDIR="${pkgdir}" install
}

package_calligra-sheets(){
  pkgdesc="Write spreadsheet documents"
  depends=('gsl' 'calligra-libs' 'calligra-templates' 'calligra-plugins')
  optdepends=('calligra-filters: import/export filters')
  conflicts=('koffice-kspread' 'calligra-sheets-git' 'calligra-tables')
  install=calligra.install

  cd "${srcdir}/build/sheets"
  make DESTDIR="${pkgdir}" install
}

package_calligra-sheets-doc(){
  pkgdesc="Documentation for sheets"
  conflicts=('calligra-tables-doc')

  cd "${srcdir}/build/doc/sheets"
  make DESTDIR="${pkgdir}" install
}

package_calligra-words(){
  pkgdesc="Word processor"
  depends=('calligra-libs' 'calligra-templates' 'calligra-pics' \
  'calligra-kounavail' 'kdegraphics-okular')
  optdepends=('calligra-filters: import/export filters')
  conflicts=('koffice-kword' 'calligra-words-git')
  install=calligra.install

  cd "${srcdir}/build/words"
  make DESTDIR="${pkgdir}" install
}

package_calligra-handbook() {
  pkgdesc="Documentation for Calligra"
  conflicts=('koffice-handbook' 'calligra-handbook-git')

  cd "${srcdir}/build/doc/calligra"
  make DESTDIR="${pkgdir}" install
}

package_calligra-thesaurus-doc() {
  pkgdesc="Documentation for Thesaurus"
  conflicts=('koffice-thesaurus-doc' 'calligra-thesaurus-doc-git')

  cd "${srcdir}"/build/doc/thesaurus
  make DESTDIR="${pkgdir}" install
}

package_calligra-flow() {
  pkgdesc="Flowchart & Diagram Editing"
  depends=('calligra-libs')
  conflicts=('calligra-flow-git')
  install=calligra.install

  cd "${srcdir}/build/flow"
  make DESTDIR="${pkgdir}" install
}

package_calligra-braindump() {
  pkgdesc="Notes and idea gathering"
  depends=('calligra-libs')
  conflicts=('calligra-braindump-git')
  install=calligra.install
	
  cd "${srcdir}/build/braindump"
  make DESTDIR="${pkgdir}" install
}

package_calligra-mobile() {
  pkgdesc="Calligra Mobile is a mobile user interaction of Calligra Suite"
  depends=('calligra-tools')
  conflicts=('calligra-mobile-git')

  cd "${srcdir}/build/tools/mobile"
  make DESTDIR="${pkgdir}" install
}

package_calligra-active() {
  pkgdesc="QtQuick GUI for Calligra targeted for mobile devices"
  depends=('calligra-libs')
  conflicts=('calligra-active-git')

  cd "${srcdir}/build/active"
  make DESTDIR="${pkgdir}" install
}

url="http://www.calligra-suite.org/"
pkgdesc="Calligra Suite is a set of applications written to help you to accomplish your work."

# vim:set ts=2 sw=2 et:
