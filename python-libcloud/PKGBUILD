# Maintainer: Carl George < arch at cgtx.us >

_module="apache-libcloud"
pkgname=("python-${_module#apache-}" "python2-${_module#apache-}")
pkgver="0.17.0"
pkgrel="1"
pkgdesc="A standard Python library that abstracts away differences among multiple cloud provider APIs."
arch=("any")
url="https://libcloud.apache.org"
license=("Apache")
makedepends=("python-setuptools" "python2-setuptools")
source=("https://pypi.python.org/packages/source/${_module:0:1}/${_module}/${_module}-${pkgver}.tar.gz")
sha256sums=('8ac4895c5ed2fa51812237dfd587675e3cbc4b7e57d9b44722ce849eab2131c2')

prepare() {
	cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py build
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py build
}

package_python-libcloud() {
	depends=("python")
	cd "${srcdir}/${_module}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-libcloud() {
	depends=("python2")
	cd "${srcdir}/${_module}-${pkgver}-python2"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
