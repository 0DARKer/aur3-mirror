# Maintainer: Adam Reichold <adamreichold@myopera.com>
pkgname=qpdfview
pkgver=0.4beta1
pkgrel=2
pkgdesc='A tabbed PDF viewer using the poppler library.'
arch=('i686' 'x86_64')
url='https://launchpad.net/qpdfview'
license=('GPL2')
depends=('file' 'libcups' 'poppler-qt')
optdepends=('libspectre: for PostScript support' 'djvulibre: for DjVu support')
install='qpdfview.install'
source=("http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz")
md5sums=('c4ef15d2d30ddeefac26f1e08fa8ee46')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  if ! pkg-config --exists libspectre; then
    local config="$config without_ps"
  fi
  
  if ! pkg-config --exists ddjvuapi; then
    local config="$config without_djvu"
  fi

  qmake "CONFIG+=$config" qpdfview.pro
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make "INSTALL_ROOT=$pkgdir" install
}

