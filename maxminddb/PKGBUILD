# Maintainer: felix
pkgbase=maxminddb
pkgname=(
	mmdblookup
	libmaxminddb
)
pkgver=0.5.6
pkgrel=1
epoch=
pkgdesc="MaxMind GeoIP2 database - split package with library and utilities"
license=('Apache')
arch=(i686 x86_64)
url="http://dev.maxmind.com/geoip/geoip2/downloadable/"
depends=()
makedepends=()
checkdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
source=("https://github.com/maxmind/libmaxminddb/releases/download/$pkgver/libmaxminddb-$pkgver.tar.gz")
sha512sums=('1a8937843e4e456ff7dbb8437747fe5a77babddb12e5d22c3fcb7c3e2e2f9788dc7ee39ea2e16ce6857cc3e9fc6e1f8e7faa6716adad7271c936e902a0cb472b')
noextract=()

build() {
	cd "$srcdir/libmaxminddb-$pkgver"
	./configure --prefix=/usr
	make
}

check() {
	msg2 "Checking the library and utilities"
	cd "$srcdir/libmaxminddb-$pkgver"
	make check
}

package_mmdblookup() {
	pkgdesc="MaxMindDB GeoIP2 database - lookup utility"
	license=('Apache')
	depends=(libmaxminddb)

	cd "$srcdir/libmaxminddb-$pkgver/bin"
	make install DESTDIR="$pkgdir"
	cd "$srcdir/libmaxminddb-$pkgver"
	make install-man1 DESTDIR="$pkgdir"
}

package_libmaxminddb() {
	pkgdesc="MaxMindDB GeoIP2 database - library"
	license=('Apache')
	depends=(glibc)
	optdepends=(
		geolite2-country
		geolite2-city
	)

	cd "$srcdir/libmaxminddb-$pkgver/src"
	make install DESTDIR="$pkgdir"
	cd "$srcdir/libmaxminddb-$pkgver"
	make install-man3 DESTDIR="$pkgdir"
}
