# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from garcon package)
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

_pkgname=garcon
pkgname=$_pkgname-git
pkgver=20121111
pkgrel=1
pkgdesc="Freedesktop.org compliant menu library"
arch=(i686 x86_64)
license=(GPL2)
url=http://wiki.xfce.org/dev/garcon
groups=(xfce4-git)
depends=(libxfce4util-git)
makedepends=(git xfce4-dev-tools)
provides=($_pkgname=0.2.0)
conflicts=($_pkgname libxfce4menu{,-git})
replaces=(libxfce4menu{,-git})
options=(!libtool !makeflags)

_gitroot=git://git.xfce.org/xfce/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-static \
        --disable-debug
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
