# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Aliaksandr Stelmachonak <mail.avatar at gmail dot com>

_pkg=gnome-globalmenu
pkgname=$_pkg-xfce4
pkgver=0.7.8
pkgrel=6
pkgdesc="Global Menu Bar built for using with Xfce desktop"
url="http://code.google.com/p/gnome2-globalmenu/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('xfce4-panel')
makedepends=('intltool' 'gnome-menus')
options=('!libtool')
conflicts=('gtk2-aqd' 'gtk2-globalmenu' 'gnome-globalmenu-svn'
           'gnome-globalmenu' 'gnome-globalmenu-git')
install=$pkgname.install
source=("http://gnome2-globalmenu.googlecode.com/files/$_pkg-$pkgver.tar.bz2")
md5sums=('c2900eb3ff345457f65d9be2793cc2dc')

build() {
  cd "$srcdir/$_pkg-$pkgver"

  # Does not build with --as-needed
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  # fix xfce4 paths retrieving with pkg-config
  sed -e "s|\$.*libexec.*\(/xfce4/panel-plugins\)|/usr/lib\1|" \
      -e "s|\$.*datadir.*\(/xfce4/panel-plugins\)|/usr/share\1|" \
      -i configure

  ./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--without-gnome-panel \
	--with-xfce4-panel \
	--disable-tests

  make
}

package() {
  cd "$srcdir/$_pkg-$pkgver"

  make DESTDIR="$pkgdir/" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}
