# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: Aliaksandr Stelmachonak <mail.avatar at gmail dot com>

_pkgbasename=gnome-globalmenu
pkgname=$_pkgbasename-xfce4
pkgver=0.7.8
pkgrel=5
pkgdesc="Global Menu Bar built for using with Xfce desktop"
url="http://code.google.com/p/gnome2-globalmenu/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('xfce4-panel')
makedepends=('intltool' 'gnome-menus')
options=('!libtool')
conflicts=('gtk2-aqd' 'gtk2-globalmenu' 'gnome-globalmenu-svn'
           'gnome-globalmenu' 'gnome-globalmenu-git')
install=$pkgname.install
source=("http://gnome2-globalmenu.googlecode.com/files/$_pkgbasename-$pkgver.tar.bz2")
md5sums=('c2900eb3ff345457f65d9be2793cc2dc')

build() {

  # Does not build with --as-needed
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  cd "$srcdir/$_pkgbasename-$pkgver"

  ./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--without-gnome-panel \
	--with-xfce4-panel \
	--disable-tests

  make
}

package() {

  cd "$srcdir/$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir/" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install
}
