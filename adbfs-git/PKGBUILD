# Contributor: Nathan Hourt <nat.hourt@gmail.com>

pkgname=adbfs-git
pkgver=r41.b687e00
pkgrel=1
pkgdesc="A utility to mount a connected Android device filesystem using ADB and FUSE"
arch=('i686' 'x86_64')
url="http://collectskin.com/adbfs/"
license=('custom')
depends=('adb' 'fuse')
makedepends=('git' 'make' 'gcc')

_gitroot='git://github.com/isieo/adbFS.git'
_gitname=adbFS

source=("$_gitroot" 'headers.patch')
md5sums=('SKIP' 'dc91e3a77ce06b332a4f80058cb8c3d2')

pkgver() {
  cd "$srcdir/$_gitname"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

prepare() {
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -p1 < "$srcdir/headers.patch"
}

build() {
  cd "$srcdir/$_gitname-build"
  make
}

package() {
  mkdir -p "$pkgdir/opt/android-sdk/tools/"  
  cp "$srcdir/$_gitname-build/adbfs" "$pkgdir/opt/android-sdk/tools/"
  
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  cp "$srcdir/$_gitname/license" "${pkgdir}/usr/share/licenses/$pkgname/"
}
