# Maintainer: Qhor Vertoe <vertoe at qhor dot net>

# Contributor: Noel Maersk <veox at wemakethings dot net>
# Contributor: Mike Lenzen <lenzenmi at gmail dot com>

pkgname=cachecoin-git
_gitname=CACHeCoin
epoch=1
pkgver=71.ff8a44d
pkgrel=1
pkgdesc="Cachecoin is a peer-to-peer network-based digital currency (git version)."
arch=('i686' 'x86_64')
url="http://cachecoin.org/"
license=('MIT')
depends=('qt4>=4.6' 'boost-libs>=1.46' 'miniupnpc>=1.6')
makedepends=('git' 'boost' 'gcc' 'make' 'automoc4' 'dos2unix')
provides=('cachecoin')
source=('git://github.com/kalgecin/CACHeCoin.git')
md5sums=('SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_gitname"
  dos2unix "$srcdir/$_gitname/share/genbuild.sh"

  # make CACHeCoin-QT client
  qmake-qt4 yacoin-qt.pro
  make

  # make cachecoind daemon
  cd "$srcdir/$_gitname/src"
  make $MAKEFLAGS -f makefile.unix cachecoind
}

package() {
  # install CACHeCoin-QT client
  install -D -m755 "$srcdir/$_gitname/cachecoin-qt" "$pkgdir/usr/bin/cachecoin-qt"

  # install cachecoind daemon
  install -D -m755 "$srcdir/$_gitname/src/cachecoind" "$pkgdir/usr/bin/cachecoind"

  # install license
  install -D -m644 "$srcdir/$_gitname/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

