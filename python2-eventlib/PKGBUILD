# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=python2-eventlib
pkgver=0.1.1
pkgrel=1
pkgdesc="Coroutine-based networking library"
arch=('any')
url="http://www.icanblink.com"
license=('custom:MIT')
depends=('python2-greenlet' 'python2-pyopenssl')
source=("http://download.ag-projects.com/SipClient/${pkgname/2}-$pkgver.tar.gz")
md5sums=('2956f8e9e0ab36e2169248911801633c')

build() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/${pkgname/2}-$pkgver"
  python2 setup.py install --root="$pkgdir/"

  # python2 fix
  sed -i "s|bin/python|&2|" `grep -rl bin/python "$pkgdir"`

  # license
  install -Dm644 debian/copyright \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
