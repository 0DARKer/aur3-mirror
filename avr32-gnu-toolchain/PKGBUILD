# Maintainer: Andre Bartke <dev@bartke.cc>

_target=avr32-unknown-linux-gnu
_hdrver=6.1.3.1475
_hdrname=atmel-headers-${_hdrver}

pkgname=avr32-gnu-toolchain
pkgver=3.4.2.435
pkgrel=2
pkgdesc="Atmel AVR32 Toolchain for Linux"
arch=('i686' 'x86_64')
url="http://www.atmel.com/"
license=('GPL')
options=('!strip' 'staticlibs')
conflicts=('cross-avr32-gnu-toolchain' 'avr32_gnu_toolchain')

if test "$CARCH" == x86_64; then
    source=("http://www.atmel.com/Images/avr32-gnu-toolchain-${pkgver}-linux.any.x86_64.tar.gz"
            "http://www.atmel.com/Images/${_hdrname}.zip")
else
    source=("http://www.atmel.com/Images/avr32-gnu-toolchain-${pkgver}-linux.any.x86.tar.gz"
            "http://www.atmel.com/Images/${_hdrname}.zip")
fi

package() {
    mkdir -p $pkgdir/usr/lib/cross-${_target}
    mkdir -p $pkgdir/usr/bin

    msg "Copying files to cross compiler tree..."
    if test "$CARCH" == x86_64; then
        cp -r $srcdir/avr32-gnu-toolchain-linux_x86_64/* $pkgdir/usr/lib/cross-${_target}
    else
        cp -r $srcdir/avr32-gnu-toolchain-linux_x86/* $pkgdir/usr/lib/cross-${_target}
    fi
    msg "Copying avr headers to cross compiler tree..."
    mkdir -p $pkgdir/usr/lib/cross-${_target}/avr32/include/
    cp -r $srcdir/${_hdrname}/avr32 $pkgdir/usr/lib/cross-${_target}/avr32/include/

    msg "Cleaning-up cross compiler tree..."
    rm -rf ${pkgdir}/usr/lib/cross-${_target}/{info,man}
    rm -rf ${pkgdir}/usr/lib/cross-${_target}/share/{info,man}

    msg "Creating out-of-path executables..."
    mkdir -p ${pkgdir}/usr/lib/cross-${_target}/avr32/bin/
    rm -f ${pkgdir}/usr/lib/cross-${_target}/avr32/bin/*
    cd ${pkgdir}/usr/lib/cross-${_target}/avr32/bin/
    for bin in ${pkgdir}/usr/lib/cross-${_target}/bin/avr32-*; do
        bbin=`basename "$bin"`;
        ln -s "/usr/lib/cross-${_target}/bin/${bbin}" `echo "$bbin" | sed "s#^avr32-##"`;
    done

    msg "Creating /usr/bin symlinks..."
    for bin in ${pkgdir}/usr/lib/cross-${_target}/bin/avr32-*; do
        bbin=`basename "$bin"`;
        ln -s "/usr/lib/cross-${_target}/bin/${bbin}" "${pkgdir}/usr/bin/${bbin}";
    done
}

if test "$CARCH" == x86_64; then
    md5sums=('3f78fa13370212d13f88a77d8a2c5f77'
             'd69e8e188470e4fea68a4650442b5750')
else
    md5sums=('8c39949420f7e42caf6da48dac0df03f'
             'd69e8e188470e4fea68a4650442b5750')
fi

# vim: set noet ci pi sts=0 sw=4 ts=4: