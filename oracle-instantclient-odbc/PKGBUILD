# Contributor: Adam Nielsen <malvineous@shikadi.net>
# Contributor: Vitaliy Berdinskikh <skipper13@archlinux.org.ua>
# Contributor: Andrea Agosti <cifvts@gmail.com>
# Contributor: Viliam Pucik <viliam.pucik@gmail.com>
# Maintainer: Adam Nielsen <malvineous@shikadi.net>

_pkgname=instantclient-odbc
pkgname=oracle-${_pkgname}
pkgver=12.1.0.1.0
pkgrel=1
pkgdesc="Additional libraries for enabling ODBC applications with Instant Client"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technetwork/database/features/instant-client/"
license=('custom:OTN')
depends=(oracle-instantclient-basic=$pkgver)
replaces=('instantclient-odbc')
options=(!strip)

# These are the only files we want to include in the source package
source=(LICENSE)
md5sums=('2d62e22e4f2d7e6d220fafde0f692a7d')
sha256sums=('f904a30b07ddf7806a33620f93b94c3d315154d26a371ece48695bb3555064a2')

# Skip the next bit if we're building a source package for AUR, as we don't
# want these files included.
if [ $SOURCEONLY -ne 1 ]; then

	# Add the main file, depending on which architecture we're building for
	case "$CARCH" in
		i686)
			source[1]="${_pkgname}-linux-$pkgver.zip"
			md5sums[1]='e8ba53efe62b6f3f139a30c55baf78b5'
			sha256sums[1]='a9c2b4ec4e54883c89826c2d0934632fca8d845626afe66a429431ac3bbd908c'
			;;
		x86_64)
			source[1]="${_pkgname}-linux.x64-$pkgver.zip"
			md5sums[1]='b0e9e3b10ba22b34dbe335426a4fe001'
			sha256sums[1]='7714e369abbf4aa551a050161bcdba37f7a4ea1b59a6ae50f410bb50eabd7540'
			;;
	esac

	# Warn the user if they haven't downloaded the file already
	if [ ! -f "${source[1]}" ]; then
		error "You need to download ${source[1]}"
		echo
		msg2 "This software cannot be downloaded automatically.  You will need to sign up"
		msg2 "for an Oracle account and download the software from Oracle directly.  Place"
		msg2 "the downloaded file in the same directory as the PKGBUILD and re-run makepkg."
		msg2 ""
		msg2 "The source .zip files can be downloaded from:"
		msg2 ""
		msg2 "i686   - http://www.oracle.com/technetwork/topics/linuxsoft-082809.html"
		msg2 "x86_64 - http://www.oracle.com/technetwork/topics/linuxx86-64soft-092277.html"
		echo
		exit 1
	fi

fi

package() {
	local basedir="$srcdir/instantclient_12_1"
	install -d "$pkgdir/usr/lib"
	install -m 755 -t "$pkgdir/usr/lib" "$basedir/"*.so.*

	install -d "$pkgdir/usr/share/oracle"
	install -m 755 -t "$pkgdir/usr/share/oracle" "$basedir/"*.sh

	install -d "$pkgdir/usr/share/doc/oracle"
	install -m 644 -t "$pkgdir/usr/share/doc/oracle" "$basedir/"*.htm*

	# create required symlinks
	cd "$pkgdir/usr/lib" || return 1
	local lib link
	for lib in *.so*; do
		link=$lib
		while [[ ${link#*.} != so ]]; do
			link=${link%.*}
			ln -s $lib $link
		done
	done

}
