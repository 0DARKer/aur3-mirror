pkgname=mate-applets
pkgver=20120702
pkgrel=1
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gstreamer0.10-base-plugins' 'mate-panel' 'libgtop' 'libmatenotify'
         'cpufrequtils' 'libmatewnck')
makedepends=('mate-doc-utils' 'pkgconfig' 'mate-settings-daemon' 'intltool'
             'mate-icon-theme' 'networkmanager' 'mate-character-map') # 'mate-python-desktop'
groups=('mate-extras')
options=(!emptydirs)
#replace=('gnome-applets')
#conflicts=('gnome-applets')
url="http://matsusoft.com.ar/projects/mate/"
install=mate-applets.install
source=()
sha256sums=()

_gitroot=git://github.com/mate-desktop/mate-applets.git
_gitname=mate-applets

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [[ -d "$_gitname" ]]; then
		cd "$_gitname" && git pull origin
		msg "The local files are updated."
	else
		git clone "$_gitroot" "$_gitname"
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting build..."

	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"


	PYTHON=/usr/bin/python2 ./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/mate-applets \
		--disable-static \
		--disable-scrollkeeper \
		--enable-polkit \
		--enable-networkmanager \
		--enable-mixer-applet \
		--enable-mini-commander \
		--enable-frequency-selector \
		--enable-ipv6 \
		--disable-schemas-install || return 1
	make || return 1
}

package() {
	cd "$srcdir/$_gitname-build"

	make MATECONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

	install -m755 -d "${pkgdir}/usr/share/mateconf/schemas"
	mateconf-merge-schema "${pkgdir}/usr/share/mateconf/schemas/${pkgname}.schemas" --domain ${pkgname} ${pkgdir}/etc/mateconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/mateconf/schemas/*.schemas
}
