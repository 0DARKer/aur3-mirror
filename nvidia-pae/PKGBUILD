# $Id: PKGBUILD 103019 2010-12-14 14:33:57Z ibiru $
# Maintainer : <hurley@gmail.com>

pkgname=nvidia-pae
pkgver=275.21
_kernver='2.6.39-pae'
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26-pae."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-pae>=2.6.39' 'kernel26-pae<2.6.40' "nvidia-utils=${pkgver}")
makedepends=('kernel26-pae-headers>=2.6.39' 'kernel26-pae-headers<2.6.40')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
        _arch='x86'
        _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('8c0b4392c9222023d0827e2743c42d40')
elif [ "$CARCH" = "x86_64" ]; then
        _arch='x86_64'
        _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('7568cd7a34634506bdfef9d9887de7ec')
fi

build() {
        cd $srcdir
        sh ${_pkg}.run --extract-only
        cd ${_pkg}/kernel
        make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
        install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
                $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
        install -d -m755 $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist-pae.conf
        sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install
        # gzip -9 all modules to safe 100MB of space
        find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
        }
