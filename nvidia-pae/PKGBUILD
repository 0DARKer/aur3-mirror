# Maintainer: Robert Knauer <robert@privatdemail.net>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-pae
pkgver=313.30
_extramodules=extramodules-3.8-pae
pkgrel=1
pkgdesc="NVIDIA drivers for linux-pae"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-pae>=3.8' 'linux-pae<3.9' "nvidia-utils=${pkgver}-1")
makedepends=('linux-pae-headers>=3.8' 'linux-pae-headers<3.9')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
  _arch='x86'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
  source+=(
    "http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
  )
  sha256sums+=(
    '0a0f10979b1bca8e4a244e204b58aeb9a580924a9dd7b11b21d15abfe1ad49ad'
  )
elif [ "$CARCH" = "x86_64" ]; then
  _arch='x86_64'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
  source+=(
    "http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
  )
  sha256sums+=(
    '57da25838e4941273cf6d04e2bc1c0b3c8f7802b24397897ed2975d5551eedbd'
  )
fi

build() {
  _kernver="$(cat /lib/modules/${_extramodules}/version)"
  cd "${srcdir}"
  rm -rf "${_pkg}"
  sh "${_pkg}.run" --extract-only
  cd "${_pkg}/kernel"
  make SYSSRC=/lib/modules/"${_kernver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
  install -d -m755 "${pkgdir}/etc/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-pae.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
  gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
