# $Id: PKGBUILD 103019 2010-12-14 14:33:57Z ibiru $
# Maintainer : <hurley@gmail.com>

pkgname=nvidia-pae
pkgver=270.41.19
_kernver='2.6.38-pae'
pkgrel=2
pkgdesc="NVIDIA drivers for kernel26-pae."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-pae>=2.6.38' 'kernel26-pae<2.6.39' "nvidia-utils=${pkgver}")
makedepends=('kernel26-pae-headers>=2.6.38' 'kernel26-pae-headers<2.6.39')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install

if [ "$CARCH" = "i686" ]; then
        _arch='x86'
        _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('c167e32702f56599bd600add97943312')
elif [ "$CARCH" = "x86_64" ]; then
        _arch='x86_64'
        _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('cb518bdd70f356425b8c4a5a48c0ad9c')
fi

build() {
        cd $srcdir
        sh ${_pkg}.run --extract-only
        cd ${_pkg}/kernel
        make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
        install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
                $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
        install -d -m755 $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist-pae.conf
        sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install
        # gzip -9 all modules to safe 100MB of space
        find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
        }
