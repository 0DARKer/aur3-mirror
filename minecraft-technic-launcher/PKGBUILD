# Maintainer:  ainola <opp310@alh.rqh> (ROT13)
# Contributor: kozec <kozec at kozec dot com>

pkgname=minecraft-technic-launcher
pkgver=4.283
pkgrel=1
pkgdesc=("Choose from thousands of community-made modpacks available on the Technic Platform.")
arch=('any')
license=('GPL2')
url=("http://www.technicpack.net/")
depends=('java-runtime' 'java-environment' 'xorg-xrandr' 'hicolor-icon-theme')
makedepends=('icoutils')
source=("http://launcher.technicpack.net/launcher${pkgver:0:1}/${pkgver:2}/TechnicLauncher.jar"
        "technic-launcher"
        "technic-launcher.desktop"
        "technic-launcher.install")
sha256sums=("7380e81e468d6e7b28789587454ea1758ff5324230baede9c9eaadafeda161ed"
            "bcf657996e45b436382f401457e35d834baa1e35c0c483e7918ab6f93a2252c1"
            "15a3013478fab69d0ca00345990c1e0d6e10c891fc660866144c45c4c44f9f1f"
            "4e967927f460029e27efd8f2417d18e064d37dfbd3af82393402636f0dfbd095")
install=("technic-launcher.install")
package(){
    cd "$srcdir"
    install -D -m755 "${srcdir}/technic-launcher" "${pkgdir}/usr/bin/technic-launcher"
    install -D -m644 "${srcdir}/TechnicLauncher.jar" "${pkgdir}/usr/share/technic-launcher/technic-launcher.jar"

    # Desktop integration
    install -D -m644 "${srcdir}/technic-launcher.desktop" "${pkgdir}/usr/share/applications/technic-launcher.desktop"
    icotool -x -o "$srcdir" "${srcdir}/net/technicpack/launcher/resources/icon.ico"
    for size in 16 32 48 64; do
        install -D -m644 ${srcdir}/icon_?_${size}x${size}x32.png \
            "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/technic-launcher.png"
    done
}
