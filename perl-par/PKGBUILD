# Maintainer: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>
# vim:set ts=2 sw=2 et ft=sh tw=100: expandtab

_author='R/RS/RSCHUPP'
_perlmod=PAR
pkgname=perl-par
pkgver=1.007
pkgrel=2
pkgdesc="Perl Archive Toolkit"
arch=('any')
url="http://search.cpan.org/~rschupp/PAR"
license=('GPL' 'PerlArtistic')
depends=('perl-archive-zip' 'perl-par-dist')
provides=(
perl-par-heavy 
perl-par-setupprogname 
perl-par-setuptemp
)
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/$_author/$_perlmod-$pkgver.tar.gz")

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}
check(){
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}
package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
md5sums=('eff0397dc552f52f071908f3cc178637')
