# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

pkgname=torsocks-git
pkgver=1.3
pkgrel=1
pkgdesc='Torsocks allows you to use most socks-friendly applications in a safe way with Tor.'
url='http://code.google.com/p/torsocks'
license=('GPL2')
arch=('i686' 'x86_64')
conflicts=('torsocks' 'tsocks')
provides=('torsocks')
options=(!strip)

_gitname=torsocks
source=("git://git.torproject.org/${_gitname}.git")
md5sums=('SKIP')

pkgver () {
    cd "$srcdir/$_gitname"
    git describe |sed 's/-[^-]*$//;s/^v//;s/-/./g'
}

build() {
    cd "$srcdir/$_gitname"

	./autogen.sh
	./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var
	
    make
}

package() {
    cd "$srcdir/$_gitname"
    make DESTDIR="$pkgdir" install

    install -dm0755 "${pkgdir}/usr/share/torsocks"
    find "${pkgdir}/usr/share" -maxdepth 1 \
        ! -name share ! -name man ! -name torsocks -print0 |
        xargs -0 mv -t "${pkgdir}/usr/share/torsocks"
}
