# Maintainer: Narve <narve AT hush DOT ai>

pkgname=tor-alpha
pkgver=0.2.4.12
pkgrel=1
pkgdesc='Anonymizing overlay network in its Alpha Version'
arch=('i686' 'x86_64')
url='http://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'tsocks' 'bash')
provides=('tor')
conflicts=('tor')
makedepends=('ca-certificates')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf'
        'etc/conf.d/tor')
install='tor.install'
source=("http://www.torproject.org/dist/tor-${pkgver}-alpha.tar.gz"{,.asc}
        'torrc'
        'tor'
        'tor.conf.d'
        'tor.service')
md5sums=('0ebb8722c9816f2e6ca37065a1010a89'
         'ce319bddd3549441092cf42e748423b9'
         '56c75d4e8a66f34167d31e38c43793dd'
         'f8e6868a389877346e7eebaacd1078bb'
         '5c7c6834064b3530c442def6079ac3aa'
         'b8989a7b3ab39d697624b243f7fc255d')

build() {
  cd "${srcdir}/tor-${pkgver}-alpha"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/tor-${pkgver}-alpha"

  make DESTDIR="${pkgdir}" install

  install -dm0700 "${pkgdir}/var/lib/tor"

  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"
  install -Dm0755 "${srcdir}/tor" "${pkgdir}/etc/rc.d/tor"
  install -Dm0644 "${srcdir}/tor.conf.d" "${pkgdir}/etc/conf.d/tor"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm0644 "${srcdir}/tor.service" "${pkgdir}/usr/lib/systemd/system/tor.service"
}
