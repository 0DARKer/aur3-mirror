# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Paolo Giangrandi <peoro.noob@gmail.com>
# Contributor: Marc poiroud <marciun@free.fr>

pkgname=snowballz
pkgver=0.9.5.1
pkgrel=4
pkgdesc="A fun RTS game featuring snowball fights with penguins"
arch=('any')
url="http://www.happypenguin.org/show?Snowballz"
license=('GPL')
depends=('python2' 'python-opengl' 'python-pygame' 'python-rabbyt-old')
source=("http://ftp.debian.org/debian/pool/main/s/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
        "${pkgname}.patch"
        "${pkgname}.png"
        "${pkgname}.desktop"
        "${pkgname}.sh")
md5sums=('197e3281cc85f9fe89a45c622e08ba0d'
         '11ed78c154b11fa0c776c46a162ea927'
         '70fcff90ac9fc65c4801236730473f46'
         '058e3dc39fb326f6ce260aa32082642c'
         '88357c6f8ab454cc1c96ad17b36ecda6')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply a patch (Python2 fix + Debian bug fixes)
  patch -Np1 -i "../${pkgname}.patch"

  # Install game files
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  cp -r buildings cellulose gooeypy data maps plugins *.py "${pkgdir}/usr/share/${pkgname}"

  # Install launcher
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

  # Install pixmap and .desktop file
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 AUTHORS README.txt "${pkgdir}/usr/share/doc/${pkgname}"
}
