# Maintainer: ZKenJ <zkenjm@gmail.com>
# Contributor: Dan Serban

pkgname=slowmovideo-git
pkgver=20130409
pkgrel=1
pkgdesc="Video slow motion effect via interpolation"
arch=('i686' 'x86_64')
url="http://slowmovideo.granjow.net/index.html"
license=('GPL')
depends=('ffmpeg' 'freeglut' 'glew' 'libjpeg-turbo' 'libpng' 'opencv' 'qt4' 'sdl')
makedepends=('cmake' 'git')
provides=('slowmovideo')
conflicts=('slowmovideo')
source=('git+https://github.com/slowmoVideo/slowmoVideo.git')
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/slowmoVideo
  git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {
  mkdir ${srcdir}/slowmoVideo/slowmoVideo/build ${srcdir}/slowmoVideo/V3D/build ${srcdir}/include
  cp ${srcdir}/slowmoVideo/slowmoVideo/lib/flow*.h ${srcdir}/include/
  cd ${srcdir}/slowmoVideo/slowmoVideo/build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
  cd "${srcdir}"/slowmoVideo/V3D/build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSVFLOW_LIB=${srcdir}/slowmoVideo/slowmoVideo/build/lib/libsVflow.a \
    -DBUILD_INCLUDE_DIR=${srcdir}/include ..
  make
}

package() {
  cd ${srcdir}/slowmoVideo/slowmoVideo/build 
  make DESTDIR=${pkgdir} install
  cd ${srcdir}/slowmoVideo/V3D/build
  make DESTDIR=${pkgdir} install
}
