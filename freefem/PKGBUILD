# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Andrea 'dedalus' Turconi <andy.dedalus@gmail.com>

pkgname=freefem
pkgver=3.19.1
_pkgver=3.19-1
pkgrel=1
pkgdesc="A PDE oriented language using the finite element method"
arch=('i686' 'x86_64')
url="http://www.freefem.org/ff++/index.htm"
license=('LGPL')
depends=('arpack' 'fftw' 'freeglut' 'umfpack')
makedepends=('gcc-fortran')
source=("http://www.freefem.org/ff++/ftp/${pkgname}++-${_pkgver}.tar.gz"
        "disable-doc.patch")
md5sums=('74c09591d35ffc7d50de6cd820ebad01'
         'e485ab97d37b457f0f51aa672a95121f')

build() {
  cd "${srcdir}/${pkgname}++-${_pkgver}"

  sed -i "1i#include <unistd.h>" src/Graphics/getprog-unix.hpp
  patch -Np1 -i ../disable-doc.patch

  ./configure \
    --prefix=/usr \
    --disable-pdf
  make
}

package() {
  cd "${srcdir}/${pkgname}++-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
