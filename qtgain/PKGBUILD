# Contributor: AsamK <asamk@gmx.de>
# Contributor: Artem Sereda  <overmind88@gmail.com>

pkgname=qtgain
pkgver=0.9.8
pkgrel=1
pkgdesc="Simply frontend for mp3gain, vorbisgain and metaflac to replay gain your tracks"
url="http://www.qt-apps.org/content/show.php/QtGain?content=56842"
license=('GPL')
arch=('i686' 'x86_64')
depends=('qt4')
makedepends=('make' 'gcc')
optdepeds=('mp3gain: For MP3 support'
	'vorbisgain: For OGG support'
	'flac: For FLAC suppot'
	'aacgain: For AAC support')
source=("${pkgname}::git://git.code.sf.net/p/qtgain/code#commit=a7329dcea0eef248a4dbfbbb5294f0917dd9d192"
	'qtgain.desktop'
	'qtgain-flac-fixes.patch')
md5sums=('SKIP'
         '88a43f5dff0b157471132bf6d13fcdcd'
         '38ccea8b07fbc0081eeb68cd4705d8e6')

prepare() {
	cd "$srcdir/${pkgname}"
	patch -p1 -i "$srcdir/qtgain-flac-fixes.patch"
}

build() {
	cd "$srcdir/${pkgname}"
	qmake-qt4
	make
}

package () {
	cd "$srcdir/${pkgname}"
	install -D -m 755 "bin/qtgain" "${pkgdir}/usr/bin/qtgain"
	install -D -m 644 "${srcdir}/qtgain.desktop" "${pkgdir}/usr/share/applications/qtgain.desktop"
	install -D -m 644 "Icons/lsongs.png" "${pkgdir}/usr/share/pixmaps/qtgain.png"
}
