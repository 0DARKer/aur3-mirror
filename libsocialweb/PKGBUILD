# Maintainer: Renzo Carbonara <gnuk0001 at gmail dot com>

pkgname=libsocialweb
pkgver=0.25.15
pkgrel=1
pkgdesc="Social web services integration framework"
arch=('i686' 'x86_64')
url="http://git.gnome.org/browse/libsocialweb/"
license=('LGPL')
source=("http://ftp.gnome.org/pub/GNOME/sources/libsocialweb/0.25/${pkgname}-${pkgver}.tar.bz2"
        'python2-path.patch')
options=('!libtool')
depends=('libsoup-gnome' 'librest' 'json-glib')
makedepends=('gtk-doc' 'intltool')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  patch -Np0 -i ${srcdir}/python2-path.patch

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/libsocialweb \
    --enable-all-services=yes \
    --with-online=always

  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

sha256sums=('0ef6094818e35e86a1238a2792d3a9fab3fe0d7c30fd3f888f5977cb493def7f'
            '11adf454850dd3d07c98eb57740e44e4601e96c794ea30dfc9b06bff16cccd32')
