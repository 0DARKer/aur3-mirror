# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>

pkgname=flashplugin-10
pkgver=10.3.183.15
dirver=10_3r183_15
pkgrel=1
pkgdesc="Adobe Flash Player v10 (32-bit) - with GPU hardware acceleration"
arch=("i686" "x86_64")
url="http://get.adobe.com/flashplayer"
license=("custom")
if [ "${CARCH}" == "i686" ]; then
	depends=("mozilla-common" "hicolor-icon-theme" "libxt" "gtk2" "nss" "curl" "alsa-lib")
	optdepends=("libvdpau: video hardware decoding for supporting players")
elif [ "${CARCH}" == "x86_64" ]; then
	depends=("mozilla-common" "hicolor-icon-theme" "lib32-libxt" "lib32-gtk2" "lib32-nss" "lib32-curl" "lib32-alsa-lib")
	optdepends=("lib32-libvdpau: video hardware decoding for supporting players"
				"nspluginwrapper: make this plugin work with 64-bit browsers"
				"firefox32: 32-bit Firefox for 64-bit systems")
fi
conflicts=("flashplugin")
options=(!strip)
install="${pkgname}.install"
backup=("etc/adobe/mms.cfg")
archive="fp_${pkgver}_archive.zip"
source=("http://fpdownload.macromedia.com/get/flashplayer/installers/archive/${archive}"
		"mms.cfg")

package() {
	cd "${srcdir}"

	# Variables
	local directory tarball license;
	directory="${archive%.*}"
	tarball=${directory}/${dirver}/flashplayer${dirver}_linux.tar.gz
	license=${directory}/readme.txt

	# Directories
	install -d -m755 "${pkgdir}/usr/lib/mozilla/plugins"
	install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
	install -d -m755 "${pkgdir}/etc/adobe"

	# Files
	unzip -p "${archive}" "${tarball}" | tar -zx -C "${pkgdir}/usr/lib/mozilla/plugins" libflashplayer.so
	unzip -p "${archive}" "${license}" > "${pkgdir}/usr/share/licenses/${pkgname}/readme.txt"
	install -Dm644 "${srcdir}/mms.cfg" "${pkgdir}/etc/adobe/mms.cfg"
}

sha1sums=('27639d0b47a1195441f12fbebdf6c4f3036e30e4'
          '4b5fb41986cf78628dc50050cfb847f3021cbe47')
