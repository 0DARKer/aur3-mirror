# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Xavion <Xavion0@gmail.com>
# Contributor: Michael Witten <mfwitten>

_langname=flash
_appname=${_langname}player
_basename=${_langname}plugin
_ver1=10
_ver2=3
_ver3=183
_ver4=50
pkgname=$_basename-$_ver1
_pkgver=$_ver1.$_ver2.r$_ver3.$_ver4
pkgver=$_ver1.$_ver2.$_ver3.$_ver4
pkgrel=1
pkgdesc="Adobe Flash Player v10 (32-bit) - with GPU hardware acceleration"
arch=(i686 x86_64)
url=http://kb2.adobe.com/cps/142/tn_14266.html#main_Archived_versions
license=(custom)
depends=(hicolor-icon-theme lib32-alsa-lib lib32-curl lib32-gtk2 lib32-libxt lib32-nss mozilla-common)
optdepends=('lib32-libvdpau: video hardware decoding for supporting players'
    'nspluginwrapper: make this plugin work with 64-bit browsers'
    'firefox32: 32-bit Firefox for 64-bit systems')
if [[ $CARCH == "i686" ]]; then
    depends=(${depends[@]/lib32-/})
    optdepends=('libvdpau: video hardware decoding for supporting players')
fi
provides=($_basename=$pkgver)
conflicts=($_basename)
options=(!strip)
install=$pkgname.install
backup=(etc/adobe/mms.cfg)
source=($pkgname-$pkgver.zip::http://fpdownload.macromedia.com/get/${_appname}/installers/archive/fp_${pkgver}_archive.zip
    mms.cfg)
sha256sums=('1b0d51b7126c47633e3ed3db913e0827588331c7596fc6e456370c1f6e59d25b'
    '27dad99feecfd7152305bd86e8149e8c6482b7dc787ccd205c05d1dc13aafead')
sha512sums=('5995943b3303d5bcd8401ecff4d130ba17f4cd9bfab502fb9511f18f703f6c924a36a8abce2c3bb30a2fc8494238514139acc23d7470040aa188552c3192d88b'
    'a5e273179d026c9f0c8609a8076cc5c129faf21dd605899b7904e53c2bdf899de94c7e258c2b0ac381a38b68675bdd381a47d1bfbf8ebcd6f10e6a4c8ac9207a')

package() {
    cd "$srcdir"/fp_${pkgver}_archive/
    local dirver=${_pkgver//./_}

    bsdtar -C "$pkgdir" -xf $dirver/${_appname}_${dirver/_r/r}_linux.tar.gz
    install -Dm644 redme.txt "$pkgdir"/usr/share/licenses/$pkgname/README
    install -Dm644 ../mms.cfg "$pkgdir"/etc/adobe/mms.cfg

    cd "$pkgdir"
    chown -R root:root .
    install -Dm755 lib$_appname.so usr/lib/mozilla/plugins/lib$_appname.so
    rm lib$_appname.so
}
