pkgname=aurora-sim-optional-modules-git
pkgver=20599
pkgrel=1
pkgdesc="An Open Source Virtual World Server derived from OpenSim (git)"
arch=(i686 x86_64)
license=("BSD")
depends=(mono p7zip)
makedepends=(git)
conflicts=(aurora-sim)
provides=(aurora-sim)
optdepends=("mariadb: To use Aurora alongside MySQL instead of SQLite")
url="http://aurora-sim.org"
source=("${pkgname%-optional-modules-git}::git+https://github.com/aurora-sim/Aurora-Sim.git"
"git+https://github.com/aurora-sim/Aurora-Sim-Optional-Modules.git"
"aurora-sim.sh"
"aurora-grid-server.sh"
"aurora-server.sh"
"console-combiner.sh")
backup=("srv/aurora-sim/shutdown_commands.txt")
md5sums=('SKIP'
         'SKIP'
         '71d6aae09f25620139397b251a24707c'
         '601cd9353b26061166c1bf8aaa9b32ef'
         '5ff6e4b5c09aa2a5779ce98e1d6c63f6'
         'b9c49db953df60adc170fd4f83b8d4d2')

pkgver () {
	cd "$srcdir/${pkgname%-optional-modules-git}"
	echo $(git rev-list --count master)
}

build() {
	cd "$srcdir/${pkgname%-optional-modules-git}"
	cp -r "$srcdir/Aurora-Sim-Optional-Modules/"* "./addon-modules/"
	./runprebuild.sh
	xbuild Aurora.sln
}

package() {
	cd "$srcdir/${pkgname%-optional-modules-git}/bin"
	for file in `find . -name '*.pdb'`; do
		pdb2mdb ${file%.pdb}.dll
	done
	find . -name '*.bat' -o -name '*.pdb' -o -name '*.dylib' -o -name '.gitignore' -o -name '*.sh' |
		xargs -rtl1 rm
	rm {7za,Prebuild}.exe
	rm {*64,Physics/*64}.so
	rm {odex*,libbulletnet,openjpeg-dotnet*,lib32/BulletSim}.dll
	rm -r {lib64,Physics/ODEPlugins}
	install -Dm755 "$srcdir/aurora-sim.sh" "$pkgdir/usr/bin/aurora-sim"
	install -m755 "$srcdir/aurora-grid-server.sh" "$pkgdir/usr/bin/aurora-grid-server"
	install -m755 "$srcdir/aurora-server.sh" "$pkgdir/usr/bin/aurora-server"
	install -m755 "$srcdir/console-combiner.sh" "$pkgdir/usr/bin/console-combiner"
	mkdir -p "$pkgdir/srv/aurora-sim"
	cp -r . "$pkgdir/srv/aurora-sim/"
}
