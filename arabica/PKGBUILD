# Maintainer : speps <speps at aur dot archxlinuyahoo dot it>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>

_ver=2012-November
pkgname=arabica
pkgver=201211
pkgrel=1
pkgdesc="An XML and HTML Processing Toolkit"
arch=('i686' 'x86_64')
url="http://www.jezuk.co.uk/cgi-bin/view/arabica"
license=('GPL')
depends=('expat')
makedepends=('xerces-c' 'libxml2' 'libxslt' 'boost')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/November-12/$pkgname-$_ver.tar.bz2"
        "$pkgname-gcc.patch")
md5sums=('5fd3a9e68fce87f3efa742e7061f6fa5'
         '4fd128a9fa799d4f764b417c068e97de')

build() {
  cd "$srcdir/$pkgname-$_ver"

  # remove hardcoded rpath
  sed -i "s|\${wl}-rpath[^']*||g" configure m4/libtool.m4

  # gcc 4.7+ patch
  patch -p1 -i "$srcdir/${source[1]}"

  autoreconf -if
  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$_ver"
  make DESTDIR="$pkgdir/" install
}
