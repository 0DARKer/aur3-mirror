# Actual maintainer: Luis Moreno <luismoreno83 at gmail dot com>
# Last maintainer: Christos Nouskas <nous at archlinux dot us>
# Contributor: Trent McPheron <twilightinzero@gmail.com>
# Based on nvidia-beta-all by Ng Oon-Ee <n g o o n e e AT g mail dot com>
# ...Which is based on nvidia-beta by Dan Vratil <vratil@progdansoft.com>

pkgname=nvidia-96xx-all
pkgver=96.43.19
pkgrel=8
pkgdesc="NVIDIA drivers, 96xx branch. Builds modules for all kernels detected on system."
arch=('i686' 'x86_64')
_kernver=`uname -r`
[ "$CARCH" = "i686" ] && ARCH=x86 && NV=0
[ "$CARCH" = "x86_64" ] && ARCH=x86_64 && NV=2
provides=('nvidia-96xx')
url="http://www.nvidia.com/"
depends=('kernel26' 'kernel26-headers' "nvidia-96xx-utils>=${pkgver}" "xorg-server<10.0.0")
conflicts=('nvidia-71xx' 'nvidia-96xx' 'nvidia-173xx' 'nvidia-legacy' 'nvidia-beta' 'nvidia')
license=('custom')
install="nvidia.install"
source=(ftp://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg${NV}.run)

sha256sums=('e25810e809ea56ab33ebb3e79e885eb19784dcce2bb0102c0cb5daa372aaf1c8')
[ "$CARCH" = "x86_64" ] && sha256sums=('81939f9ee45255cc137719a10b8947bc90cf5697da9e27991cee5a7061d5de19')

build()
{
  # Extract the nvidia drivers
  cd $startdir/src/
  sh NVIDIA-Linux-${ARCH}-${pkgver}-pkg${NV}.run --extract-only
  cd NVIDIA-Linux-${ARCH}-${pkgver}-pkg${NV}

  cd usr/src/nv
  ln -s Makefile.kbuild Makefile

  msg 'Building the kernel module...'
  # Loop through all detected kernels
  for _kernver in `file /boot/* | grep 'Linux kernel.*boot executable' | grep 'vmlinuz' | sed 's/.*version \([^ ]\+\).*/\1/'`;
  do
    msg2 "Building module for $_kernver..."

    # check for kernels older than .33
    KERNEL26_VERSION=$(echo ${_kernver} | cut -d- -f1)
    KERNEL26_SUBVERSION=$(echo $KERNEL26_VERSION | cut -d. -f3)

    if [ $KERNEL26_SUBVERSION -lt 33 ]; then
		NEW_AUTOCONF_DIR=/usr/src/linux-${_kernver}/include/generated
		if [ ! -d $NEW_AUTOCONF_DIR ]; then mkdir $NEW_AUTOCONF_DIR; fi
		if [ ! -f ${NEW_AUTOCONF_DIR}/autoconf.h ]; then
			ln -s /usr/src/linux-${_kernver}/include/linux/autoconf.h ${NEW_AUTOCONF_DIR}/autoconf.h
		fi
    fi

    make SYSSRC=/lib/modules/${_kernver}/build module

    # Install kernel module
    mkdir -p $startdir/pkg/lib/modules/${_kernver}/kernel/drivers/video/
    install -m644 nvidia.ko $startdir/pkg/lib/modules/${_kernver}/kernel/drivers/video/

  done
}

