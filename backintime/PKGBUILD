# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# Contributor: Matias Korhonen <webadmin@draco-vulgaris.net>
# Contributor: Dylon Edwards <deltaecho@archlinux.us>

pkgname=(backintime backintime-gtk backintime-gnome backintime-kde4)
pkgver=1.0.40
pkgrel=3
arch=('any')
url="https://launchpad.net/backintime"
license=('GPL')
depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring' 'python2-secretstorage')
optdepends=('openssh: Support for remote backups over SFTP')
source=("https://launchpadlibrarian.net/188941953/backintime-1.0.40.tar.gz"
"https://launchpadlibrarian.net/188941954/backintime-1.0.40.tar.gz.asc")
sha256sums=('09a79fe95b654e628a9d76d19f110bf5d29c7cf142b2ec5df2f1c8561b5551f2'
'SKIP')

validpgpkeys=('3E70692EE3DB8BDDA5991C90615F366D944B4826')	# Germar Reitze
# Add Germar's key to your local keyring to allow checks to work
# do NOT add it to pacman's keyring and do NOT sign it!
# gpg --keyserver pgp.mit.edu --recv-key 944B4826

prepare() {
	# per Germar's request on 2014-03-03 13:03
	# upstream will not be patching as this seems to be an Arch-specific issue
	sed -i 's/gksu/gksudo/' "$srcdir/gnome/backintime-gnome-root.desktop"
}

build() {
	cd common
	./configure
	make

	cd "$srcdir/notify"
	./configure
	make

	for t in gnome kde4; do
		cd "$srcdir/$t"
		./configure --no-check
		make
	done
}

package_backintime() {
	pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. CLI version."
	depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring' 'python2-secretstorage')
	optdepends=('backintime-gtk: GTK GUI version'
	'backintime-gnome: Gnome GUI version'
	'backintime-kde4: KDE4 GUI version'
	'sshfs: FUSE client based on the SSH File Transfer Protocol'
	'encfs: Encrypted filesystem in user-space')
	conflicts=('backintime-bzr')

	cd common
	make DESTDIR="$pkgdir" install

	cd "$srcdir/notify"
	make DESTDIR="$pkgdir" install

	sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
		-i "$pkgdir"/usr/bin/*
}

package_backintime-gnome() {
pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. Gnome GUI version."
depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring' 
"backintime=$pkgver" 'python2-secretstorage' 'pygtk' 'python2-notify'
'gnome-python' 'meld' 'gksu' 'gnome-session')
optdepends=('sshfs: FUSE client based on the SSH File Transfer Protocol'
'encfs: Encrypted filesystem in user-space')
conflicts=('backintime-bzr' 'backintime-gtk')

cd "$srcdir/gnome"
make DESTDIR="$pkgdir" install
sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
	-i "$pkgdir"/usr/bin/*
}

package_backintime-gtk() {
pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. GTK GUI version."
depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring'
"backintime=$pkgver" 'python2-secretstorage' 'pygtk' 'python2-notify'
'gnome-python' 'meld' 'gksu')
optdepends=('sshfs: FUSE client based on the SSH File Transfer Protocol'
'encfs: Encrypted filesystem in user-space')
conflicts=('backintime-bzr' 'backintime-gnome')

cd "$srcdir/gnome"
make DESTDIR="$pkgdir" install
sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
	-i "$pkgdir"/usr/bin/*
}

package_backintime-kde4() {
pkgdesc="Simple backup system inspired from the Flyback Project and TimeVault. KDE4 GUI version."
depends=('openssh' 'python2' 'rsync' 'cron' 'python2-keyring'
"backintime=$pkgver" 'xorg-utils' 'python2-pyqt' 'kdebindings-python2')
replaces=('backintime-qt4')
optdepends=('sshfs: FUSE client based on the SSH File Transfer Protocol'
'encfs: Encrypted filesystem in user-space'
'kdesdk-kompare: Diff/Patch Frontend')
conflicts=('backintime-bzr')

cd "$srcdir/kde4"
make DESTDIR="$pkgdir" install
sed -e 's|^python |python2 |g' -e 's|^ssh-agent python |ssh-agent python2 |g' \
	-i "$pkgdir"/usr/bin/*
sed -i 's/kdesudo/kdesu/' "$pkgdir/usr/share/applications/kde4/backintime-kde4-root.desktop"
}
