# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=virtualbox-extension-pack
pkgver=4.2.12
pkgrel=1
pkgdesc='Oracle VM VirtualBox Extension Pack'
arch=('any')
url='http://www.virtualbox.org/'
license=('custom:PUEL')
depends=('virtualbox')
conflicts=('virtualbox-ext-oracle')
optdepends=('rdesktop: client to connect vm via RDP')
options=('!strip')
source=("http://download.virtualbox.org/virtualbox/${pkgver}/Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack"
        'PUEL')

package() {
	install -d -m0755 "${pkgdir}/usr/lib/virtualbox/ExtensionPacks/Oracle_VM_VirtualBox_Extension_Pack"
	tar -C "${pkgdir}/usr/lib/virtualbox/ExtensionPacks/Oracle_VM_VirtualBox_Extension_Pack" \
		--no-same-owner -xzf "${srcdir}/Oracle_VM_VirtualBox_Extension_Pack-${pkgver}.vbox-extpack"
	install -D -m0644 "${srcdir}/PUEL" "${pkgdir}/usr/share/licenses/${pkgname}/PUEL"
}

sha256sums=('ad15a92e49095c2115bd1793b3b957d3eaf44af0f5d24bb53d6b4fc81c3e2fc4'
            '50658c653cde4dc43ba73a64c72761d2e996fd8e360fc682aa2844e1ad5b045f')
