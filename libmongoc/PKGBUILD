#Maintainer: Tyler Brock <tyler@mongodb.com>

pkgname='libmongoc'
_pkgname='mongo-c-driver'
pkgver='1.1.0'
pkgrel='1'

pkgdesc='The official MongoDB client library written in C.'
arch=('i686' 'x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=(
    'libbson>=1.0.0'
)
makedepends=(perl)
optdepends=(
    'xmlto: documentation'
    'asciidoc: documentation'
    'python: documentation'
    'libsasl>=2.1.6: sasl support'
    'openssl: ssl support'
)
source=("https://github.com/mongodb/$_pkgname/archive/$pkgver.tar.gz")
sha256sums=('39297b93127cccbb8b1ba72fdeeba7ddb2ea213f47692c08863abd62d8210e93')

prepare() {
    cd "$srcdir/$_pkgname-$pkgver"
    ./autogen.sh
}

build() {
    cd "$srcdir/$_pkgname-$pkgver"
    ./configure --prefix='/usr' CFLAGS='-pthread' LDFLAGS='-lpthread' --enable-man-pages=yes --enable-examples=no --enable-tests=no
    make
}

package() {
    cd "$srcdir/$_pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
}
