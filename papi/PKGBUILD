# Contributor: Jed Brown <jed@59A2.org>

pkgname=papi
pkgver=5.0.1
pkgrel=1
pkgdesc="Performance Application Programming Interface"
arch=(x86_64 i686)
url="http://icl.cs.utk.edu/papi/"
license=('BSD')
depends=(glibc linux-api-headers)
makedepends=()
provides=()
conflicts=()
replaces=()
source=(http://icl.cs.utk.edu/projects/papi/downloads/${pkgname}-${pkgver}.tar.gz)
md5sums=('39e6cbbd6cd72fe421de77cb75c4eb00')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/src

  unset MAKEFLAGS # Makefiles are broken for parallel make
  ./configure --prefix=/usr --mandir=/usr/share/man --with-perf-events \
    LDFLAGS="${LDFLAGS} -Wl,--no-as-needed" # Configure has a problem detecting dlopen()
  make
}
package() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  make DESTDIR=$pkgdir install
  chmod 644 $pkgdir/usr/share/papi/papi_events.csv
  install -D $srcdir/$pkgname-$pkgver/LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
