# Contributor: Jed Brown <jed@59A2.org>

pkgname=papi
pkgver=4.2.0
pkgrel=1
pkgdesc="Performance Application Programming Interface"
arch=(x86_64 i686)
url="http://icl.cs.utk.edu/papi/"
license=('BSD')
depends=(glibc linux-api-headers)
makedepends=()
provides=()
conflicts=()
replaces=()
source=(http://icl.cs.utk.edu/projects/papi/downloads/${pkgname}-${pkgver}.tar.gz)
md5sums=('ca5f85fd2474c11bc847a6c784ca2668')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  # Internal library has noisy code, but they set -Werror anyway.
  patch -p1 <<EOF
--- a/libpfm-3.y/config.mk   2010-07-21 13:56:22.152052317 +0200
+++ b/libpfm-3.y/config.mk   2010-07-21 13:56:38.568721079 +0200
@@ -168,7 +168,7 @@
 LN?=ln -sf
 PFMINCDIR=\$(TOPDIR)/include
 PFMLIBDIR=\$(TOPDIR)/lib
-DBG?=-g -Wall -Werror
+DBG?=-g -Wall
 # gcc/mips64 bug
 ifeq (\$(CONFIG_PFMLIB_ARCH_SICORTEX),y)
 OPTIM?=-O
EOF
  patch -p1 <<EOF
--- i/libpfm4/config.mk
+++ w/libpfm4/config.mk
@@ -172,7 +172,7 @@ INSTALL=install
 LN?=ln -sf
 PFMINCDIR=\$(TOPDIR)/include
 PFMLIBDIR=\$(TOPDIR)/lib
-DBG?=-g -Wall -Werror
+DBG?=-g -Wall
 CFLAGS+=\$(OPTIM) \$(DBG) -I\$(SYSINCDIR) -I\$(PFMINCDIR)
 MKDEP=makedepend
 PFMLIB=\$(PFMLIBDIR)/libpfm.a
EOF

  unset MAKEFLAGS # Makefiles are broken for parallel make
  ./configure --prefix=/usr --with-perf-events \
    LDFLAGS="${LDFLAGS} -Wl,--no-as-needed" # Configure has a problem detecting dlopen()
  make
  make DESTDIR=$pkgdir install
  install -D $srcdir/$pkgname-$pkgver/LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
