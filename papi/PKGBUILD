# Contributor: Jed Brown <jed@59A2.org>

pkgname=papi
pkgver=4.1.3
pkgrel=1
pkgdesc="Performance Application Programming Interface"
arch=(x86_64 i686)
url="http://icl.cs.utk.edu/papi/"
license=('BSD')
depends=(glibc kernel26-headers)
makedepends=()
provides=()
conflicts=()
replaces=()
source=(http://icl.cs.utk.edu/projects/papi/downloads/${pkgname}-${pkgver}.tar.gz)
md5sums=('14975be670ba7234470569807b84ada9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  # Internal library has noisy code, but they set -Werror anyway.
  patch -p1 <<EOF
--- a/libpfm-3.y/config.mk   2010-07-21 13:56:22.152052317 +0200
+++ b/libpfm-3.y/config.mk   2010-07-21 13:56:38.568721079 +0200
@@ -168,7 +168,7 @@
 LN?=ln -sf
 PFMINCDIR=\$(TOPDIR)/include
 PFMLIBDIR=\$(TOPDIR)/lib
-DBG?=-g -Wall -Werror
+DBG?=-g -Wall
 # gcc/mips64 bug
 ifeq (\$(CONFIG_PFMLIB_ARCH_SICORTEX),y)
 OPTIM?=-O
EOF
  unset MAKEFLAGS # Makefiles are broken for parallel make
  ./configure --prefix=/usr --with-perf-events \
    LDFLAGS="${LDFLAGS} -Wl,--no-as-needed" # Configure has a problem detecting dlopen()
  make
  make DESTDIR=$pkgdir install
  install -D $srcdir/$pkgname-$pkgver/LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
