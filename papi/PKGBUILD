# Contributor: Jed Brown <jed@59A2.org>

pkgname=papi
pkgver=4.4.0
pkgrel=1
pkgdesc="Performance Application Programming Interface"
arch=(x86_64 i686)
url="http://icl.cs.utk.edu/papi/"
license=('BSD')
depends=(glibc linux-api-headers)
makedepends=()
provides=()
conflicts=()
replaces=()
source=(http://icl.cs.utk.edu/projects/papi/downloads/${pkgname}-${pkgver}.tar.gz)
md5sums=('2844448db0b53441a55487881b955ea5')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/src

  patch -p1 <<EOF
--- i/linux-context.h
+++ w/linux-context.h
@@ -4,7 +4,7 @@
 /* Signal handling functions */
 
 #undef hwd_siginfo_t
-typedef struct siginfo hwd_siginfo_t;
+typedef siginfo_t hwd_siginfo_t;
 
 #undef hwd_ucontext_t
 typedef ucontext_t hwd_ucontext_t;
EOF

  unset MAKEFLAGS # Makefiles are broken for parallel make
  ./configure --prefix=/usr --with-perf-events \
    LDFLAGS="${LDFLAGS} -Wl,--no-as-needed" # Configure has a problem detecting dlopen()
  make
}
package() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  make DESTDIR=$pkgdir install
  install -D $srcdir/$pkgname-$pkgver/LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
