# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Allan McRae (allan <at> archlinux <dot> org)
# Contributor: Tom Newsom (Jeepster <at> gmx <dot> co <dot> uk)

pkgname=selinux-sudo
_origname=sudo
pkgver=1.8.0
pkgrel=4
pkgdesc="Give certain users the ability to run some commands as root with SELinux support"
arch=('i686' 'x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
groups=('selinux' 'selinux-system-utilities')
depends=('glibc' 'selinux-pam' 'selinux-usr-libselinux')
conflicts=('sudo')
provides=("${_origname}=${pkgver}")
backup=('etc/sudoers' 'etc/pam.d/sudo')
options=('!libtool' '!makeflags')
source=(ftp://ftp.sudo.ws/pub/sudo/${_origname}-${pkgver}.tar.gz
	${_origname}_l.patch
	${_origname}_validate_exitval.patch
	${_origname}_noninteractive.patch
        ${_origname}.pam)
md5sums=('fa0a35330691af14cb1869f64a65aebc'
	 '29656b2f2365e14fa0f8eb94e61f3690'
	 '4751aa5557fe43fd8e03e0c7b5affcfc'
	 '47d152ade2c9a726684fa1227e46bfe3'
         '4e7ad4ec8f2fe6a40e12bcb2c0b256e3')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  # http://www.sudo.ws/bugs/show_bug.cgi?id=474
  patch -Np1 -i "${srcdir}/${_origname}_l.patch"

  # http://www.sudo.ws/bugs/show_bug.cgi?id=479
  patch -Np1 -i "${srcdir}/${_origname}_validate_exitval.patch"

  # http://www.sudo.ws/bugs/show_bug.cgi?id=478
  patch -Np1 -i "${srcdir}/${_origname}_noninteractive.patch"

  ./configure --prefix=/usr --with-pam --libexecdir=/usr/lib \
    --with-env-editor --with-all-insults --with-logfac=auth \
    --with-selinux
  make
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  install -dm755 "${pkgdir}/var/lib"

  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/sudo.pam" "${pkgdir}/etc/pam.d/sudo"

  install -Dm644 doc/LICENSE "${pkgdir}/usr/share/licenses/sudo/LICENSE"
}
