# $Id: PKGBUILD 185547 2013-05-14 21:53:57Z foutrelis $
# Maintainer: Nicky726 <Nicky726@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=selinux-sudo
_origname=sudo
_sudover=1.8.6p8
#pkgver=${_sudover/p/.p}
pkgver=1.8.6.p8
pkgrel=2
pkgdesc="Give certain users the ability to run some commands as root with SELinux support"
arch=('i686' 'x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
groups=('selinux' 'selinux-system-utilities')
depends=('glibc' 'selinux-pam' 'selinux-usr-libselinux')
conflicts=("${_origname}")
provides=("${_origname}=${pkgver}-${pkgrel}")
backup=('etc/sudoers' 'etc/pam.d/sudo')
options=('!libtool')
source=(http://www.sudo.ws/sudo/dist/$_origname-$_sudover.tar.gz{,.sig}
        sudo.pam)
sha256sums=('c0baaa87f59153967b650a0dde2f7d4147d358fa15f3fdabb47e84d0282fe625'
            'SKIP'
            'e7de79d2c73f2b32b20a8e797e54777a2bf19788ec03e48decd6c15cd93718ae')

build() {
  cd "$srcdir/$_origname-$_sudover"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-logfac=auth \
    --with-pam \
    --with-env-editor \
    --with-passprompt="[sudo] password for %p: " \
    --with-all-insults \
    --with-selinux
  make
}

check() {
  cd "$srcdir/$_origname-$_sudover"
  make check
}

package() {
  cd "$srcdir/$_origname-$_sudover"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/sudo.pam" "$pkgdir/etc/pam.d/sudo"

  install -Dm644 doc/LICENSE "$pkgdir/usr/share/licenses/sudo/LICENSE"
}

# vim:set ts=2 sw=2 et:
