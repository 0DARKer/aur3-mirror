# Maintainer: Rich Li <rich@dranek.com>
# Contributor: Filip S. Adamsen <fsa [at] fsadev [dot] com>
# Contributor: Samed Beyribey <ras0ir AT eventualis DOT org>
pkgname=python2-pyspf
_pkgname=pyspf
pkgver=2.0.8
pkgrel=1
pkgdesc="Python 2 implementation of Sender Policy Framework (SPF)"
arch=('any')
url="http://bmsi.com/python/milter.html"
license=('Python')
depends=('python2-pydns' 'python2' 'python2-ipaddr')
checkdepends=('python2-yaml' 'python2-authres')
optdepends=('python2-authres: support Authentication-Results headers')
#conflicts=('python-pyspf')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/p/$_pkgname/$_pkgname-$pkgver.tar.gz)
md5sums=('8ef1a5714939d6df5ab40006ac8930ac')
sha1sums=('279634a93614d3887d4bbf44852bd6d73944af5c')
sha256sums=('b9ff846a7b6c883383451d1d4be526762f1d88de84e89d6de05f73f51c2c3dcf')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py build
}

check() {
  # Note that python2-authres, which is normally optional, is required for the tests to finish successfully
  cd "$srcdir/$_pkgname-$pkgver/test"
  # Use the SPF from this package, not the system-installed version
  sed -i -e "s|import spf|sys.path.append('..')\nimport spf|" testspf.py
  python2 testspf.py
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
