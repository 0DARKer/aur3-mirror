#Maintainer issue <issue at archlinux dot info>

pkgname=racer-git
_pkgname=racer
pkgdesc="code completion for Rust"
url="https://github.com/phildawes/racer"
pkgrel=2
depends=('rust')
makedepends=('git' 'cargo')
arch=('x86_64')
license=('MIT')
md5sums=('SKIP')
pkgver='r158.05a80e8'
install=$pkgname.install
source=("git+https://github.com/phildawes/racer.git")

pkgver() {
    cd "$_pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build(){
    cd $_pkgname
    cargo build --release
}

package(){
    install -Dm755 "$srcdir/$_pkgname/target/release/racer" "$pkgdir/usr/bin/racer"
    install -Dm644 "$srcdir/$_pkgname/editors/racer.el" "$pkgdir/usr/share/emacs/site-lisp/racer.el"
    install -Dm644 "$srcdir/$_pkgname/LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}

