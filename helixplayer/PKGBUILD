# $Id: PKGBUILD,v 1.12 2007/06/21 05:44:00 tardo Exp $
# Maintainer: tardo <tardo@nagi-fanboi.net>

pkgname=helixplayer
pkgver=1.0.8
pkgrel=4
pkgdesc="An Open-Source media player produced by RealNetworks"
arch=('i686' 'x86_64')
url="http://www.helixcommunity.org"
license=("GPL" "custom:RPSL" "custom:RCSL")
depends=('gtk2' 'gcc' 'libxv' 'bash')
source=(https://helixcommunity.org/projects/player/files/download/2156 \
        https://community.helixcommunity.org/content/rpsl.txt \
        https://community.helixcommunity.org/content/Real_Format_Source_Code_Decoder.pdf \
        https://community.helixcommunity.org/content/RCSL_Commercial_Client.pdf \
        https://community.helixcommunity.org/content/Real_Format_Code_Commercial.pdf rcsl.txt gcc4.patch \
		amd64-fixes.patch x11-path-fixes.patch)
md5sums=('6c07239fbe84d9a5002746a6fa4be8a8'
         'b3b2a3eb663b96e569ad792a3e2d2f2e'
         '9961cac41f010e1c9e4e9779e23fad89'
         '04cb3dfc03ad3c5dfefe19bd42262c43'
         '1692e15b68773aca964b1ef568573a40'
         'ef4f38d05a7c0931171aadbd6ddb7e84'
         'b1daa26c3e9e1ded3a2ea12ffc311803'
         '0cebf030173e09a8a2038fa162ba5c7b'
         '42a29bb4da89e9f6a31da64aa9a40a61')

build() {
  # compiling/add patches
  cd $startdir/src/hxplay-$pkgver
  patch -p1 < ../gcc4.patch || return 1
  patch -p1 < ../x11-path-fixes.patch || return 1
  [ "$CARCH" == "x86_64" ] && (patch -p1 < ../amd64-fixes.patch || return 1)
  [ "$CARCH" == "i686" ] && export SYSTEM_ID=linux-2.6-glibc23-i686
  [ "$CARCH" == "x86_64" ] && export SYSTEM_ID=linux-2.6-glibc23-amd64

  # override wrong libogg/libvorbis paths
  echo "SetSDKPath(\"oggvorbissdk\", \"/usr\")" > $startdir/src/hxplay-$pkgver/buildrc
  export BUILDRC=$startdir/src/hxplay-$pkgver/buildrc
  export BUILD_ROOT=$startdir/src/hxplay-$pkgver/build
  build/bin/build.py -m hxplay_gtk_release -trelease -k -j 1 -P helix-client-all-defines-free player_installer_app

  # prepare dirs for copying
  mkdir -p $startdir/src/builddir
  tar -xjf release/hxplay-${pkgver}.806-$SYSTEM_ID.tar.bz2 -C $startdir/src/builddir
  install -d  $startdir/pkg/opt/helixplayer
  install -d $startdir/pkg/usr/{bin,share}

  # copying files
  cd $startdir/src/builddir
  cp -R codecs common lib plugins share $startdir/pkg/opt/helixplayer
  find $startdir/pkg/opt/helixplayer/share -type f -exec chmod 644 {} \;
  find $startdir/pkg/opt/helixplayer -type d -exec chmod 755 {} \;
  install -D -m755 hxplay hxplay.bin $startdir/pkg/opt/helixplayer
  ln -s /opt/helixplayer/hxplay $startdir/pkg/usr/bin/hxplay

  # installing icons
  for res in 16 192 32 48; do
    install -d $startdir/pkg/usr/share/icons/hicolor/${res}x${res}/apps
    cp $startdir/pkg/opt/helixplayer/share/icons/hxplay_${res}x${res}.png \
       $startdir/pkg/usr/share/icons/hicolor/${res}x${res}/apps/hxplay.png
  done

  install -d $startdir/pkg/usr/share/icons/hicolor/{48x48,192x192}/mimetypes

  for mime in generic ogg ram rpm smil; do
    ln -s /opt/helixplayer/share/icons/mime-application-${mime}_48x48.png \
        $startdir/pkg/usr/share/icons/hicolor/48x48/mimetypes/hxplay-application-${mime}.png
    ln -s /opt/helixplayer/share/icons/mime-application-${mime}_192x192.png \
        $startdir/pkg/usr/share/icons/hicolor/192x192/mimetypes/hxplay-application-${mime}.png
  done

  for mime in aiff au generic ogg wav; do
    ln -s /opt/helixplayer/share/icons/mime-audio-${mime}_48x48.png \
        $startdir/pkg/usr/share/icons/hicolor/48x48/mimetypes/hxplay-audio-${mime}.png
    ln -s /opt/helixplayer/share/icons/mime-audio-${mime}_192x192.png \
        $startdir/pkg/usr/share/icons/hicolor/192x192/mimetypes/hxplay-audio-${mime}.png
  done

  ln -s /opt/helixplayer/share/icons/mime-text-realtext_48x48.png \
        $startdir/pkg/usr/share/icons/hicolor/48x48/mimetypes/hxplay-text-realtext.png
  ln -s /opt/helixplayer/share/icons/mime-text-realtext_192x192.png \
        $startdir/pkg/usr/share/icons/hicolor/192x192/mimetypes/hxplay-text-realtext.png

  for mime in generic ogg; do
    ln -s /opt/helixplayer/share/icons/mime-video-${mime}_48x48.png \
        $startdir/pkg/usr/share/icons/hicolor/48x48/mimetypes/hxplay-video-${mime}.png
    ln -s /opt/helixplayer/share/icons/mime-video-${mime}_192x192.png \
        $startdir/pkg/usr/share/icons/hicolor/192x192/mimetypes/hxplay-video-${mime}.png
  done

  # setting up locales
  for locale in de es fr hi it ja ko pl pt_BR zh_CN zh_TW; do
    install -d $startdir/pkg/usr/share/locale/${locale}/LC_MESSAGES
    rm -f $startdir/pkg/opt/helixplayer/share/locale/${locale}/{LICENSE,README}
    ln -s /opt/helixplayer/share/locale/${locale}/player.mo \
        $startdir/pkg/usr/share/locale/${locale}/LC_MESSAGES/hxplay.mo
    ln -s /opt/helixplayer/share/locale/${locale}/widget.mo \
        $startdir/pkg/usr/share/locale/${locale}/LC_MESSAGES/libgtkhx.mo
  done

  # installing pixmap, .desktop file, etc.
  install -d $startdir/pkg/usr/share/pixmaps
  ln -s /opt/helixplayer/share/hxplay.png $startdir/pkg/usr/share/pixmaps/hxplay.png

  install -D -m644 $startdir/pkg/opt/helixplayer/share/hxplay.applications \
                   $startdir/pkg/usr/share/application-registry/hxplay.applications

  install -D -m644 $startdir/pkg/opt/helixplayer/share/hxplay.desktop \
                   $startdir/pkg/usr/share/applications/hxplay.desktop

  install -D -m644 $startdir/pkg/opt/helixplayer/share/hxplay.keys \
                   $startdir/pkg/usr/share/mime-info/hxplay.keys

  install -D -m644 $startdir/pkg/opt/helixplayer/share/hxplay.mime \
                   $startdir/pkg/usr/share/mime-info/hxplay.mime
  # installing mozilla plugin
  install -d $startdir/pkg/opt/mozilla/lib/plugins
  install -D -m755 mozilla/nphelix.so $startdir/pkg/opt/mozilla/lib/plugins/nphelix.so
  install -D -m755 mozilla/nphelix.xpt $startdir/pkg/opt/mozilla/lib/plugins/nphelix.xpt

  # installing license
  install -D -m644 $startdir/src/rpsl.txt $startdir/pkg/usr/share/licenses/$pkgname/rpsl.txt
  install -D -m644 $startdir/src/rcsl.txt $startdir/pkg/usr/share/licenses/$pkgname/rcsl.txt
  install -D -m644 $startdir/src/Real_Format_Source_Code_Decoder.pdf $startdir/pkg/usr/share/licenses/$pkgname/Real_Format_Source_Code_Decoder.pdf
  install -D -m644 $startdir/src/RCSL_Commercial_Client.pdf $startdir/pkg/usr/share/licenses/$pkgname/RCSL_Commercial_Client.pdf
  install -D -m644 $startdir/src/Real_Format_Code_Commercial.pdf $startdir/pkg/usr/share/licenses/$pkgname/Real_Format_Code_Commercial.pdf
  install -D -m644 LICENSE $startdir/pkg/usr/share/licenses/$pkgname/LICENSE
}

