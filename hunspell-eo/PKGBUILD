pkgname=hunspell-eo
pkgver=1.1
pkgrel=1
pkgdesc='Esperanto dictionary for Hunspell'
arch=('any')
url="http://wiki.services.openoffice.org/wiki/Dictionaries#Esperanto_.28anywhere.29"
license=('GPL')
#makedepend=(sed)
optdepends=('hunspell:	the spell checking libraries and apps')
source=('http://ftp.services.openoffice.org/pub/OpenOffice.org/contrib/dictionaries/eo.zip')
md5sums=('518edcd9a9ecda594e519b2af1fd7e80')

package() {
  cd "${srcdir}"
  install -dm755 ${pkgdir}/usr/share/hunspell
  install -m644 eo_l3.dic ${pkgdir}/usr/share/hunspell/
  install -m644 eo_l3.aff ${pkgdir}/usr/share/hunspell/
  
  # UTF-8 vortaroj
  conv='iconv -f iso8859-3 -t utf-8'
  $conv eo_l3.dic > eo_EO.dic
  $conv eo_l3.aff | sed '1s/SET ISO8859-3/SET UTF-8/' > eo_EO.aff
  install -m644 eo_EO.dic ${pkgdir}/usr/share/hunspell/
  install -m644 eo_EO.aff ${pkgdir}/usr/share/hunspell/

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd ${pkgdir}/usr/share/myspell/dicts
    for file in ${pkgdir}/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename ${file}) .
    done
  popd
  
  # docs
  install -dm755 ${pkgdir}/usr/share/doc/${pkgname}
  install -m644 README_eo_l3.txt $pkgdir/usr/share/doc/${pkgname}/README_eo.txt
}
