# Maintainer: ledti <theprimevaloddity at gmail.com>

pkgname=evilvte-git
pkgver=143.9abeb9a
pkgrel=1
pkgdesc='VTE based, highly customizable terminal emulator, devel version.'
arch=('i686' 'x86_64')
url="http://www.calno.com/evilvte/"
license=('GPL2')
depends=('vte3' 'hicolor-icon-theme')
makedepends=('pkg-config' 'git')
provides=('evilvte')
conflicts=('evilvte')
install=evilvte.install
source=('git://github.com/caleb-/evilvte.git#branch=master')
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/evilvte"
	echo $(git rev-list --count master).$(git rev-parse --short master)
}

prepare() {
	# copy config.h to root build directory and use it to ease configuration.
	if [[ -e "$startdir/config.h" ]]; then
		cp -f "$startdir/config.h" "$srcdir/evilvte/src/"
	else
		cp "$srcdir/evilvte/src/config.h" "$startdir/"
	fi
	echo "Edit $startdir/config.h to customize evilvte, or remove it"
	echo "to generate a new one with the default options."
}

build(){
	cd "$srcdir/evilvte"
	./configure --prefix=/usr --with-gtk=3.0
	make
}

package(){
	cd "$srcdir/evilvte"
	make DESTDIR="$pkgdir/" install
}
