# Maintainer: Erdbeerkaese <erbeerkaese underscore arch at yahoo dot de>

pkgname=starfarer
pkgver=0.51a_RC3
pkgrel=1
pkgdesc="A single-player sandbox style space role-playing game with strategic elements."
arch=(any)
url="http://fractalsoftworks.com/"
license=('custom')
depends=('java-runtime' 'libgl' 'openal' 'ttf-dejavu' 'bash')
install='starfarer.install'
source=(
http://ftp2.bmtmicro.com/starfarer_linux-${pkgver//_/-}.zip
http://www.fractalsoftworks.com/starfarer/docs/StarfarerManual.pdf
http://sourceforge.net/projects/java-game-lib/files/Official%20Releases/LWJGL%202.8.3/lwjgl-2.8.3.zip
starfarer.sh
starfarer.desktop)
md5sums=('fcd7e344ce42545a531bb307266ca881'
         'd677fdea9b9e8c42e6407e2d739be4f8'
         'c23a4c90e898caff00c19e5e27d8c82c'
         '3e486e8f153bed9a8f4d03498bb1c825'
         '8f34b524db75095cccbd46f0a630d1de')
package() {
    install -D -m644 "${srcdir}/StarfarerManual.pdf" \
                     "${pkgdir}/usr/share/doc/${pkgname}/StarfarerManual.pdf"
    install -d  "${pkgdir}/usr/share/${pkgname}" \
                "${pkgdir}/usr/share/java/${pkgname}" \
                "${pkgdir}/usr/lib/${pkgname}"
    for jar in "${srcdir}/${pkgname}/*.jar" ; do
        install -D -m644 $jar "${pkgdir}/usr/share/java/${pkgname}/"
    done
    install -D -m644 "${srcdir}/lwjgl-2.8.3/jar/lwjgl.jar" \
                     "${pkgdir}/usr/share/java/${pkgname}/"
    install -D -m644 "${srcdir}/lwjgl-2.8.3/jar/lwjgl_util.jar" \
                     "${pkgdir}/usr/share/java/${pkgname}/"
    install -D -m644 "${srcdir}/lwjgl-2.8.3/jar/jinput.jar" \
                     "${pkgdir}/usr/share/java/${pkgname}/"
    if test "$CARCH" == x86_64; then
        cp  ${srcdir}/lwjgl-2.8.3/native/linux/*64.so \
            "${pkgdir}/usr/lib/${pkgname}/"
    else
        cp  ${srcdir}/lwjgl-2.8.3/native/linux/*[!"64"].so \
            "${pkgdir}/usr/lib/${pkgname}/"
    fi
    cp -R "${srcdir}/${pkgname}/data" "${pkgdir}/usr/share/${pkgname}/"
    cp -R "${srcdir}/${pkgname}/graphics" "${pkgdir}/usr/share/${pkgname}/"
    cp -R "${srcdir}/${pkgname}/sounds" "${pkgdir}/usr/share/${pkgname}/"
    install -D -m755 "${srcdir}/starfarer.sh" "${pkgdir}/usr/bin/starfarer"
    install -D -m644 "${srcdir}/starfarer.desktop" \
                     "${pkgdir}/usr/share/applications/starfarer.desktop"
    install -D -m644 "${srcdir}/${pkgname}/LICENSE.txt" \
                     "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=4 sw=4 et:
