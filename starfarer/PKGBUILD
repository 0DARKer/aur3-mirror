# Maintainer: Erdbeerkaese

pkgname=starfarer
pkgver=0.34a_RC2
pkgrel=2
pkgdesc="A single-player sandbox style space role-playing game with strategic elements."
arch=('i686' 'x86_64')
url="http://fractalsoftworks.com/"
license=('custom')
depends=('java-runtime' 'xorg-server-utils' 'openal' 'bash')
install='starfarer.install'
source=(
http://www.fractalsoftworks.com/starfarer/release/${pkgname}_linux-0.34a-RC2.zip
http://www.fractalsoftworks.com/starfarer/docs/StarfarerManual.pdf
starfarer.sh
starfarer.desktop)
md5sums=('4e310dcfb485363de8038088af7372d6'
         '727e6021b0c9d385b070e6ddf3c141e5'
         '8edf75d52af91f280b9b9aee9f8d1d7b'
         '8f34b524db75095cccbd46f0a630d1de')

package() {
# Manual
    install -D -m644 "${srcdir}/StarfarerManual.pdf" \
                     "${pkgdir}/usr/share/doc/${pkgname}/StarfarerManual.pdf"
# Data files
    install -d "${pkgdir}/usr/share/${pkgname}"
    install -d "${pkgdir}/usr/share/java/${pkgname}"
    install -d "${pkgdir}/usr/lib/${pkgname}"

    for jar in "${srcdir}/${pkgname}/*.jar" ; do
      install -D -m644 $jar "${pkgdir}/usr/share/java/${pkgname}/"
    done
    cp ${srcdir}/${pkgname}/native/linux/* "${pkgdir}/usr/lib/${pkgname}/"
    cp -R "${srcdir}/${pkgname}/data" "${pkgdir}/usr/share/${pkgname}/"
    cp -R "${srcdir}/${pkgname}/graphics" "${pkgdir}/usr/share/${pkgname}/"
    cp -R "${srcdir}/${pkgname}/mods" "${pkgdir}/usr/share/${pkgname}/"
    cp -R "${srcdir}/${pkgname}/sounds" "${pkgdir}/usr/share/${pkgname}/"
    install -D -m755 "${srcdir}/starfarer.sh" "${pkgdir}/usr/bin/starfarer"
    install -D -m644 "${srcdir}/starfarer.desktop" \
                     "${pkgdir}/usr/share/applications/starfarer.desktop"
    install -D -m644 "${srcdir}/${pkgname}/LICENSE.txt" \
                     "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
