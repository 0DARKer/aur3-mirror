# Maintainer: Erdbeerkaese <erbeerkaese underscore arch at yahoo dot de>

pkgname=starfarer
pkgver=0.53.1a_RC5
pkgrel=1
pkgdesc="A single-player sandbox style space role-playing game with strategic elements."
arch=(any)
url="http://fractalsoftworks.com/"
license=('custom')
depends=('java-runtime' 'libgl' 'openal' 'ttf-dejavu' 'bash')
install='starfarer.install'
source=(
http://s3.amazonaws.com/fractalsoftworks/starfarer/release/starfarer_linux-${pkgver//_/-}.zip
http://www.fractalsoftworks.com/starfarer/docs/StarfarerManual.pdf
starfarer.sh
starfarer.desktop)
md5sums=('dd7a7cb09533cceebe39f27555155353'
         'd677fdea9b9e8c42e6407e2d739be4f8'
         '3e486e8f153bed9a8f4d03498bb1c825'
         '8f34b524db75095cccbd46f0a630d1de')
package() {
    install -D -m644 "${srcdir}/StarfarerManual.pdf" \
                     "${pkgdir}/usr/share/doc/${pkgname}/StarfarerManual.pdf"
    install -d  "${pkgdir}/usr/share/${pkgname}" \
                "${pkgdir}/usr/share/java/${pkgname}" \
                "${pkgdir}/usr/lib/${pkgname}"
    for jar in "${srcdir}/${pkgname}/*.jar" ; do
        install -D -m644 $jar "${pkgdir}/usr/share/java/${pkgname}/"
    done
    if test "$CARCH" == x86_64; then
        cp  ${srcdir}/${pkgname}/native/linux/*64.so \
            "${pkgdir}/usr/lib/${pkgname}/"
    else
        cp  ${srcdir}/${pkgname}/native/linux/*[!"64"].so \
            "${pkgdir}/usr/lib/${pkgname}/"
    fi
    cp -R "${srcdir}/${pkgname}/data" "${pkgdir}/usr/share/${pkgname}/"
    cp -R "${srcdir}/${pkgname}/graphics" "${pkgdir}/usr/share/${pkgname}/"
    cp -R "${srcdir}/${pkgname}/sounds" "${pkgdir}/usr/share/${pkgname}/"
    install -D -m755 "${srcdir}/starfarer.sh" "${pkgdir}/usr/bin/starfarer"
    install -D -m644 "${srcdir}/starfarer.desktop" \
                     "${pkgdir}/usr/share/applications/starfarer.desktop"
    install -D -m644 "${srcdir}/${pkgname}/LICENSE.txt" \
                     "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=4 sw=4 et:
