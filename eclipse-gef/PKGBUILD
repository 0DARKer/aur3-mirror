# $Id: PKGBUILD 52595 2011-07-26 09:43:06Z angvp $
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Rubin Simons <rubin@xs4all.nl>
# Contributor: St√©phane Marguet (Stemp) <smarguet@gmail.com>

pkgname=eclipse-gef
pkgver=3.7.2
_reldate=201201171043
pkgrel=1
pkgdesc="GEF framework for the Eclipse platform"
arch=('any')
url="http://www.eclipse.org/gef/"
license=('EPL')
depends=('eclipse')
changelog=ChangeLog
source=(http://download.eclipse.org/tools/gef/downloads/drops/${pkgver}/R${_reldate}/GEF-ALL-${pkgver}.zip)
md5sums=('3333ea4f3f73c4835bbe7856ec81aed4')

build() {
  return 0
}

package() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}/eclipse

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}

# vim:set ts=2 sw=2 et:
