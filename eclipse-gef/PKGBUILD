# Maintainer: Arthur Zamarin <arthurzam@gmail.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: St√©phane Marguet (Stemp) <smarguet@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Rubin Simons <rubin@xs4all.nl>

pkgname=eclipse-gef
pkgver=3.9.100
_reldate=201405261516
pkgrel=1
pkgdesc="GEF framework for the Eclipse platform"
arch=(any)
url=http://www.eclipse.org/gef/
license=(EPL)
depends=(eclipse)
_mirror="http://www.eclipse.org/downloads/download.php?r=1&file="
source=($_mirror/tools/gef/downloads/drops/$pkgver/R$_reldate/GEF-ALL-$pkgver.zip)
sha256sums=('16cea32769af78f5c3b1e3300c3e9488fe0879edd3c70e066ad18707f020ec98')
sha512sums=('c0239a09d0c3d197365da296849f04c5b21ec61c5d1571fe8b68ad42eac88d891568ff551284b3d82996e044ae840a001c5fd20ee7fa52bca36512a3a097ad5d') 

package() {
    _dest="$pkgdir"/usr/share/eclipse/dropins/${pkgname#*-}/eclipse
    
    cd eclipse/
    # Features
    find features -type f | while read _feature ; do
        if [[ $_feature =~ (.*\.jar$) ]] ; then
            install -d "${_dest}"/${_feature%*.jar}
            cd "$_dest"/${_feature/.jar}
            jar xf "${srcdir}/${_feature}"
        else
            install -Dm644 $_feature "$_dest/$_feature"
        fi
    done

    # Plugins
    find plugins -type f -exec install -Dm644 '{}' "$_dest"/'{}' \;
}
