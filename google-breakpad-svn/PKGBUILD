# Contributor: Geoffrey Teale <tealeg@googlemail.com>
pkgname=google-breakpad-svn
pkgver=791
pkgrel=1
pkgdesc="An open-source multi-platform crash reporting system"
arch=('i386' 'x86_64')
url="http://code.google.com/p/google-breakpad/"
license=('BSD')
depends=('curl')
makedepends=('subversion')
source=('fix_curl_compile.patch')
md5sums=('e080d05e01e1941d79098c1f54b834d6')

_svntrunk=http://google-breakpad.googlecode.com/svn/trunk
_svnmod=google-breakpad-read-only

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"
  
  # patch
  msg2 "Applying curl compile fix"
  patch -Np1 -i "$srcdir"/fix_curl_compile.patch || return 1

  #
  # BUILD
  #
  autoreconf -v --force --install || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="$pkgdir/" install || return 1
}
