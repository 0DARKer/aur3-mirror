# Maintainer: Rob McCathie <korrode AT gmail>

_name=xfdashboard
pkgname=xfdashboard-dev
pkgver=0.3.6
pkgrel=1
pkgdesc="Maybe a Gnome shell like dashboard for Xfce"
arch=('i686' 'x86_64')
url="http://xfdashboard.froevel.de"
license=('GPL')
depends=('clutter>=1.12' 'dbus-glib>=0.98' 'garcon>=0.2.0' 'glib2>=2.30.3' 'libwnck>=2.30' 'xfconf>=4.10.0' 'gtk3>=3.2')
makedepends=('xfce4-dev-tools')
conflicts=("${_name}")
provides=("${_name}=${pkgver}")
source=("${url}/releases/${_name}-${pkgver}.tar.bz2"
        "fix-settings-crash.patch::https://github.com/gmc-holle/xfdashboard/commit/896648557d545eb355df159f7003f4e0d5775004.patch")
sha1sums=('ac84b387df3ab755eb7d5c573c486becabd4e206'
          '15f9a8e4e2d4d18c861504d99cc1506fa7a40a67')

prepare() {
  cd "${srcdir}/${_name}-${pkgver}"
  patch -p1 -i "${srcdir}/fix-settings-crash.patch"
}

build() {
  cd "${srcdir}/${_name}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_name}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
