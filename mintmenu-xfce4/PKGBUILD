# Contributor: XaBz <xabz.2k5@gmail.com>
# Contributor: Giuseppe Borzi <gborzi _AT_ ieee _DOT_ org>
# Contributor: ryooichi <ryooichi+arch AT gmail DOT com>
# Contributor: twa022 <twa022 at gmail dot com>
# Real thanks goes to jelly, mike_93 and the original creators of mintmenu, usp, slab...
# See also:
#   http://bbs.archlinux.org/viewtopic.php?id=66987
#   http://bbs.archlinux.org/viewtopic.php?id=68633
#   http://github.com/jelly/archmenu

_pkgname=mintmenu
pkgname=$_pkgname-xfce4
pkgver=5.1.6
pkgrel=5
pkgdesc="Linux Mint Menu for Xfce4"
arch=('i686' 'x86_64')
url="http://packages.linuxmint.com/pool/main/m/mintmenu"
license=('GPL')
depends=('gnome-panel-bonobo' 'gnome-python' 'python-gnomeapplet' 'python-gnomedesktop'
         'pyxdg' 'pygtk' 'archlinux-artwork' 'exo>=0.5' 'gksu' 'xdg-utils' 'xfce4-xfapplet-plugin'
         'xfce4-settings' 'xfce-utils' 'xfce4-session')
optdepends=('alacarte: for editing the gnome menu'
            'wakka: to manage your packages')
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
source=("${url}/${_pkgname}_${pkgver}.tar.gz"
        'http://aur.archlinux.org/packages/mintmenu/mintmenu.tar.gz'
        'removescript')
install=${_pkgname}.install

build() {
  cd "$srcdir/${_pkgname}"
  rm -f usr/lib/linuxmint/mintMenu/*.pyc usr/lib/linuxmint/mintMenu/plugins/*pyc
  patch -Np0 -i $srcdir/mintmenu/arch-patch.diff
  
  ## Xfce4 specific changes
  sed -i 's:nautilus:exo-open --launch FileManager:g' usr/lib/linuxmint/mintMenu/plugins/places.py
  sed -i -e 's:gnome-terminal:exo-open --launch TerminalEmulator:g' \
         -e 's:gnome-control-center:xfce4-settings-manager:g' \
         -e 's:xdg-screensaver lock:xflock4:g' \
         -e 's:gnome-session-save --logout-dialog:xfce4-session-logout --fast:g' \
         -e 's:gnome-session-save --shutdown-dialog:xfce4-session-logout --fast:g' \
         -e 's:Log out or switch user:Log out:g' \
         -e 's:gtkpacman:wakka:g' usr/lib/linuxmint/mintMenu/plugins/system_management.py

  sed -i -e "s/__version__/$pkgver/" usr/lib/linuxmint/mintMenu/mintMenu.py
  cp -R usr $pkgdir/
  chmod 644 $pkgdir/usr/lib/bonobo/servers/mintMenu.server
  install -m755 $srcdir/removescript $pkgdir/usr/lib/linuxmint/mintMenu/
  cd "${pkgdir}"/usr/lib/linuxmint/mintMenu
  ./compile.py || return 1
}

md5sums=('fc3c4a91baba87487c48bd522be1ec89'
         '2d6366952c1122e05042b35a70df92cf'
         'bcdbc95fc35331f1f5e13a86b9d770f0')
