# Maintainer: josephgbr <rafael.f.f1 at gmail.com>
# Contributor: Maribu <leonidas200 at web dot de>

_pkgbase=gdbm
pkgname=lib32-$_pkgbase
pkgver=1.11
pkgrel=1
pkgdesc="GNU database library (32 bit)"
license=('GPL')
url="http://www.gnu.org/software/gdbm/gdbm.html"
arch=('x86_64')
depends=('lib32-glibc' "$_pkgbase")
makedepends=('gcc-multilib')
source=("ftp://ftp.gnu.org/gnu/gdbm/${_pkgbase}-${pkgver}.tar.gz"
        'gdbm-1.10-zeroheaders.patch')
options=('!libtool' '!makeflags')
sha1sums=('ce433d0f192c21d41089458ca5c8294efe9806b4'
          '950bce437298448f71fab8291f00ea27bd6a419a')

prepare() {
  rm -fr build
  cp -R "${_pkgbase}-${pkgver}" build
}

build() {
  export CC='gcc -m32'

  cd build

  # Prevent gdbm from storing uninitialized memory content
  # to database files. This patch improves security, as the
  # uninitialized memory might contain sensitive informations
  # from other applications.
  # https://bugzilla.redhat.com/show_bug.cgi?id=4457
  # http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=208927
  patch -Np1 -i ../gdbm-1.10-zeroheaders.patch

  ./configure --prefix=/usr \
              --enable-libgdbm-compat \
              --libdir=/usr/lib32

  make prefix=/usr
}

check() {
  make -C build check
}

package() {
  make -C build DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr"/{bin,share,include}
}
