# Maintainer: TDY <tdy@gmx.com>

pkgname=pcmanfm-git
pkgver=20130605_1.1.0_33_g7b87375
pkgrel=1
pkgdesc="An extremely fast, lightweight, yet feature-rich file manager with tabbed browsing"
arch=('i686' 'x86_64')
url="http://pcmanfm.sourceforge.net/"
license=('GPL')
depends=('libfm-git')
makedepends=('git' 'intltool' 'pkg-config')
optdepends=('gvfs: mounting of local and remote drives'
            'gnome-menus: applications menu and "Open with..." dialog')
provides=('pcmanfm')
conflicts=('pcmanfm')
install=pcmanfm.install
source=(git://pcmanfm.git.sourceforge.net/gitroot/pcmanfm/pcmanfm)
md5sums=('SKIP')

_gitname=pcmanfm

pkgver() {
    cd "$srcdir/$_gitname"

    echo "$(LANG=C date '+%Y%m%d')_$(git describe --always | sed 's/-/_/g')"
}

build() {
  cd "${srcdir}/${_gitname}"


  msg "Starting make..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make LDFLAGS="-lm ${LDFLAGS}"
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}
