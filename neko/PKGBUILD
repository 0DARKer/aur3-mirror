# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Dwight Schauer <dschauer at gmail dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>

pkgname=neko
pkgver=2.0.0
pkgrel=1
pkgdesc='High-level language which can also be used as an embedded scripting language'
url='http://nekovm.org/'
license=('LGPL')
arch=('x86_64' 'i686')
depends=('gc' 'gtk2' 'libmysqlclient' 'pcre' 'zlib' 'sqlite' 'apache')
makedepends=('pkgconfig' 'setconf' 'apr') # 'subversion')
options=('!strip')
source=("http://nekovm.org/_media/$pkgname-$pkgver.tar.gz"
        'install.neko.patch')
sha256sums=('47a4714df7a603fc64995fc353f09205e52da4df954b6c6969d7d1b9a00a95d1'
            '999e85cb77941c908bd4650f9d7177519fb2b35e50cc24f9d8465506c03d8d4f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  setconf Makefile MAKESO '$(CC) -shared'
  sed 's|= -Wall -O3|+=|g;s|-mkdir|mkdir -p|g;/strip/d' -i Makefile
  mkdir -p bin
  [ "$CARCH" = "x86_64" ] && export CFLAGS+=" -D_64BITS"
  patch -p2 -i $srcdir/install.neko.patch
  make clean
  make libneko
  make neko std
  CFLAGS+=`pkg-config --cflags apr-1` make -j1
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir"/usr/{bin,lib}
  make INSTALL_PREFIX="$pkgdir"/usr install
  find "$pkgdir"/usr/lib -type f -exec chmod 644 {} \;
}

# vim:set ts=2 sw=2 et:
