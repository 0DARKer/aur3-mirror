# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Dwight Schauer <dschauer@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Caleb McCombs <erdrick016+aur@gmail.com>

pkgname=neko
pkgver=2.0.0
pkgrel=4
pkgdesc='High-level language which can also be used as an embedded scripting language'
url='http://nekovm.org/'
license=('LGPL')
arch=('x86_64' 'i686')
depends=('gc' 'gtk2' 'libmariadbclient' 'sqlite' 'apache')
makedepends=('pkgconfig' 'setconf' 'apr')
options=('!strip')
source=("http://nekovm.org/_media/$pkgname-$pkgver.tar.gz"
        'install.neko.patch'
        'apache24.patch::http://patch-tracker.debian.org/patch/series/dl/neko/2.0.0-3/apache2.4.diff')
sha256sums=('47a4714df7a603fc64995fc353f09205e52da4df954b6c6969d7d1b9a00a95d1'
            '999e85cb77941c908bd4650f9d7177519fb2b35e50cc24f9d8465506c03d8d4f'
            '25a2dc511bcadbdee7f54fd608fce1c7f69fac1af758e064be3fd3006c261af3')

# TODO: See if the hacky/unsual build procedures can be cleaned up now,
#       possibly in cooperation with upstream.

prepare() {
  cd "$pkgname-$pkgver"


  sed 's|= -Wall -O3|+=|g;s|-mkdir|mkdir -p|g;/strip/d' -i Makefile
  setconf Makefile MAKESO '$(CC) -shared'
  mkdir -p bin
  patch -p2 -i "$srcdir/install.neko.patch"
  patch -p1 -i "$srcdir/apache24.patch"
}

build() {
  cd "$pkgname-$pkgver"

  [ $CARCH = x86_64 ] && export CFLAGS+=" -D_64BITS"
  make clean
  # Attempt to build libneko up to three times if it fails
  for x in 1 2 3; do make libneko && break; done
  make neko std
  make
}

check() {
  make -C "$pkgname-$pkgver" test
}

package() {
  install -d "$pkgdir/usr/"{bin,lib}
  make -C "$pkgname-$pkgver" INSTALL_PREFIX="$pkgdir/usr" install
  find "$pkgdir/usr/lib" -type f -exec chmod 644 {} \;
}

# vim:set ts=2 sw=2 et:
