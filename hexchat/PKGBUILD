#Maintainer: TingPing tingping@tingping.se
pkgname=hexchat
pkgver=2.9.2
pkgrel=1
pkgdesc='A GTK+ based IRC client'
arch=('i686' 'x86_64')
url='http://www.hexchat.org/'
license=('GPL')
options=('!libtool')
conflicts=()
depends=('gtk2' 'openssl' 'dbus-glib' 'hicolor-icon-theme' 'libnotify' 'libproxy')
makedepends=('perl' 'python2' 'tcl')
optdepends=('enchant: for spell check'
            'perl: for perl plugin'
            'tcl: for tcl plugin'
            'python2: for python plugin')
install="$pkgname.install"
source=("https://github.com/downloads/hexchat/hexchat/hexchat-$pkgver.tar.xz")
md5sums=('31b9e38a7d1a66e74a6753259cc15c66')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  chmod +x autogen.sh
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --datadir=/usr/share \
    --enable-openssl --enable-dbus --enable-nls --enable-ipv6 --enable-shm --enable-spell=static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m644 hexchat.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/hexchat.svg"
}
