# Maintainer: Det <nimetonmaili at-gmail-dot com>
# Contributor: Charles Bos <charlesbos1 AT gmail>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Christopher Reimer <github@creimer.net>
# Based on gtk3 trunk: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/gtk3

pkgname=gtk3-ubuntu
pkgver=3.12.2
_rel=0ubuntu4
pkgrel=1
pkgdesc="GObject-based multi-platform toolkit (v3) from Ubuntu"
arch=(i686 x86_64)
url="https://launchpad.net/ubuntu/+source/gtk+3.0/"
install=gtk3.install
depends=(atk cairo gtk-update-icon-cache libcups libxcursor libxinerama libxrandr libxi
         libxcomposite libxdamage pango shared-mime-info colord at-spi2-atk wayland libxkbcommon)
makedepends=(gobject-introspection)
optdepends=('gnome-themes-standard: Default widget theme'
            'gnome-icon-theme: Default icon theme')
provides=(gtk3=$pkgver)
conflicts=(gtk3)
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver:0:4}/gtk+-$pkgver.tar.xz
        https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_$pkgver-$_rel.debian.tar.gz
        settings.ini)
sha256sums=('61d74eea74231b1ea4b53084a9d6fc9917ab0e1d71b69d92cbf60a4b4fb385d0'
            '9af4bc896b3121cca8b1995429d8d59f1b776c71a6a3f16044c6c2ac36db019a'
            '14369dfd1d325c393e17c105d5d5cc5501663277bd4047ea04a50abb3cfbd119')

prepare() {
    cd "gtk+-$pkgver"

#     ## Enable Ubuntu Patches
#     # Empty list
#     rm "${srcdir}/debian/patches/series"
#     # Do not allow offscreen widgets to grab the cursor
#     echo '016_no_offscreen_widgets_grabbing.patch' >> "$srcdir/debian/patches/series"
#     echo '017_no_offscreen_device_grabbing.patch' >> "$srcdir/debian/patches/series"
#     # Allow tapping (press/release) modifier keys
#     echo 'bzg_gtkcellrenderer_grabbing_modifier.patch' >> "$srcdir/debian/patches/series"
#     # Allow printing to printers advertised using Avahi/Bonjour when CUPS 1.6
#     echo 'print-dialog-show-options-of-remote-dnssd-printers.patch' >> "$srcdir/debian/patches/series"
#     # Implement interface for creating widgets for menu items
#     echo 'ubuntu_gtk_custom_menu_items.patch' >> "$srcdir/debian/patches/series"
#     # Guard against nested node updates
#     echo 'uimanager-guard-against-nested-node-updates.patch' >> "$srcdir/debian/patches/series"

    # Apply Patches
    for i in $(grep -v '#' "$srcdir/debian/patches/series" | sort); do
        msg2 "Applying $i ..."
        patch -p1 -i "$srcdir/debian/patches/$i"
    done
}

build() {
    cd "gtk+-$pkgver"

    msg2 "Running autoreconf..."
    autoreconf -vfi

    msg2 "Running ./configure..."
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --enable-x11-backend \
        --enable-broadway-backend \
        --enable-wayland-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    msg2 "Running make..."
    make
}

package() {
    cd "gtk+-$pkgver"
    msg2 "Running make install..."
    make DESTDIR="$pkgdir" install
    install -Dm644 ../settings.ini "$pkgdir/usr/share/gtk-3.0/settings.ini"
}
