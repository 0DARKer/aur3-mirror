# Maintainer: 
# Contributors: Det, Charles Bos, Xiao-Long Chen, Christopher Reimer
# Based on gtk3 trunk: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/gtk3

pkgname=gtk3-ubuntu
pkgver=3.12.2
_debrel=0ubuntu10
pkgrel=2
pkgdesc="GObject-based multi-platform toolkit (v3) from Ubuntu"
arch=(i686 x86_64)
url="https://launchpad.net/ubuntu/+source/gtk+3.0/"
install=gtk3-ubuntu.install
depends=(atk cairo gtk-update-icon-cache libcups libxcursor libxinerama libxrandr libxi
         libxcomposite libxdamage pango shared-mime-info colord at-spi2-atk wayland libxkbcommon adwaita-icon-theme)
makedepends=(gobject-introspection python2)
provides=(gtk3=$pkgver)
conflicts=(gtk3)
license=(LGPL)
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/${pkgver:0:4}/gtk+-$pkgver.tar.xz
        https://launchpad.net/ubuntu/+archive/primary/+files/gtk+3.0_$pkgver-$_debrel.debian.tar.xz
        settings.ini)
sha256sums=('61d74eea74231b1ea4b53084a9d6fc9917ab0e1d71b69d92cbf60a4b4fb385d0'
            '5d29c425066462fef500a432ae9c0c62a1d6ede9f4c89231822c0a0355971c64'
            '14369dfd1d325c393e17c105d5d5cc5501663277bd4047ea04a50abb3cfbd119')

prepare() {
    cd "gtk+-$pkgver"

    # Apply Patches
    for i in $(grep -v '#' "$srcdir/debian/patches/series" | sort); do
        msg2 "Applying $i ..."
        patch -p1 -i "$srcdir/debian/patches/$i"
    done
}

build() {
    cd "gtk+-$pkgver"

    msg2 "Running autoreconf..."
    autoreconf -vfi

    msg2 "Running ./configure..."
    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-gtk2-dependency \
        --disable-schemas-compile \
        --enable-x11-backend \
        --enable-broadway-backend \
        --enable-wayland-backend

    #https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    msg2 "Running make..."
    make
}

package() {
    cd "gtk+-$pkgver"
    msg2 "Running make install..."
    make DESTDIR="$pkgdir" install
    install -Dm644 ../settings.ini "$pkgdir/usr/share/gtk-3.0/settings.ini"
}
