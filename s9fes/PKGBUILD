# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: Balwinder S "bsd" Dheeman (bdheeman AT gmail.com)
pkgname=s9fes
pkgver=20121005
pkgrel=2
pkgdesc="Scheme 9 from Empty Space"
arch=('i686' 'x86_64')
url="http://tx97.net/s9fes/"
license=('custom')
depends=('ncurses')
changelog=Changelog
source=("http://tx97.net/pub/distfiles/${pkgname}-${pkgver}.tgz"
	"Makefile.patch")
sha256sums=('acef489940904d4ad3c9708f6364beba1a1b8702256bcf46790fbe867543f7dd'
            '4c2ca5d50aa7da481fa36d12b76e9e5374a71ca76e4c30e2f1de52874ac0ef87')

prepare() {
	cd $srcdir/s9
	patch -p1 < $srcdir/Makefile.patch
}

build() {
	cd $srcdir/s9

	case $CARCH in
		x86_64) make BITS_PER_WORD_64=1 all ;;
		*) make all ;;
	esac
}

package() {
	cd $srcdir/s9
	make DESTDIR=$pkgdir install

	# license
	install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

	# some documentation
	install -dm755 $pkgdir/usr/share/doc/$pkgname
	install -m644 Todo CHANGES ABOUT README $pkgdir/usr/share/doc/$pkgname
}

