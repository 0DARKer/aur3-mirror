# Maintainer: Renan Manola <rmanola@gmail.com>
# Based on a modified version of the gcc PKGBUILD

pkgname=('gcc-gcj')
pkgver=4.6.2
pkgrel=2
pkgdesc="The GNU Compiler for Java"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
depends=('gcc=4.6.2' 'gtk2' 'file' 'zip' 'libsm' 'libxtst' 'alsa-lib'
         'libart-lgpl')
makedepends=('binutils>=2.20.1' 'mpfr>=2.4.2-2' 'cloog>=0.16.2-1' 'elfutils'
             'libmpc>=0.8.2-2' 'jack')
options=('!libtool')
install=$pkgname.install
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-{core,g++,java}-${pkgver}.tar.bz2 
  gcc_pure64.patch 
  gcc-hash-style-both.patch
  libjava-disable-static.dpatch
  libjava-sjlj.dpatch 
  gcc47-pr50888.patch)

build() {
  cd ${srcdir}/gcc-${pkgver}
  
  # Do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  # Do not run fixincludes
  sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  if [ "${CARCH}" = "x86_64" ]; then
    patch -Np1 -i ../gcc_pure64.patch
  fi
  patch -Np0 -i ${srcdir}/gcc-hash-style-both.patch

  chmod +x ${srcdir}/*.dpatch
  ${srcdir}/libjava-disable-static.dpatch -patch
  ${srcdir}/libjava-sjlj.dpatch -patch
  patch -Np0 -i ${srcdir}/gcc47-pr50888.patch 

  echo ${pkgver} > gcc/BASE-VER

  [ -d build ] || mkdir build

  cd build
  ../configure --prefix=/usr --enable-shared \
      --enable-languages=java \
      --enable-threads=posix --mandir=/usr/share/man --infodir=/usr/share/info \
      --enable-__cxa_atexit  --disable-multilib --libdir=/usr/lib \
      --libexecdir=/usr/lib --enable-clocale=gnu --disable-libstdcxx-pch \
      --with-tune=generic --enable-java-awt=gtk --with-java-home="$JAVA_HOME" \
      --enable-libgcj-multifile --disable-plugin --with-system-zlib
  make
  make -j1 DESTDIR=${pkgdir} install-target-libjava

  cd gcc
  make -j1 DESTDIR=${pkgdir} java.install-common java.install-man

  install -m755 jc1 ${pkgdir}/usr/lib/gcc/${CHOST}/${pkgver}/
  install -m755 jvgenmain ${pkgdir}/usr/lib/gcc/${CHOST}/${pkgver}/

  # Remove files which belong to the base gcc package
  rm -f ${pkgdir}/usr/bin/{c,g}++
  if [ "${CARCH}" = "x86_64" ]; then
    rm -f ${pkgdir}/usr/bin/x86_64-unknown-linux-gnu-{c,g}++
   else
    rm -f ${pkgdir}/usr/bin/i686-pc-linux-gnu-{c,g}++
  fi
  rm -f ${pkgdir}/usr/man/man1/g++.*

  find ${pkgdir}/usr/lib -type f -name '*.so.*' -exec strip --strip-unneeded {} \;

  linkdir=`basename $pkgdir/usr/lib/gcj-${pkgver}*`
  ln -sf $linkdir ${pkgdir}/usr/lib/gcj-4.5
  ln -sf libgcj-${pkgver}.jar ${pkgdir}/usr/share/java/libgcj-4.5.jar
  ln -sf libgcj-${pkgver}.jar ${pkgdir}/usr/share/java/libgcj.jar
  ln -sf libgcj-tools-${pkgver}.jar ${pkgdir}/usr/share/java/libgcj-tools-4.5.jar
  ln -sf libgcj-tools-${pkgver}.jar ${pkgdir}/usr/share/java/libgcj-tools.jar
}
md5sums=('780f614ab18c7a9066dec6387d7490b2'
         '87ecd60431e41096419dd8a10f76e46b'
         '52de580642d7092b7b2790d9a81ab7a6'
         '4030ee1c08dd1e843c0225b772360e76'
         'dce3162ff4f4eafab0daccf01ad2d2de'
         'e5e13a1815b676b66e119c7f46550a90'
         'afe3541abf5ce163223f94ccdbc66e30'
         'aa18df5916f7f1a2b47672f97e1f8ecf')
