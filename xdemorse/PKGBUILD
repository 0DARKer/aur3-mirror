# Maintainer: not_anonymous <nmlibertarian@gmail.com>
# Contributor: Maintainer: qs9rx <that nick at enjoys dÃ¶t it>
# Original author: Bennett Goble <nivardus at gmail dot com>

pkgname=xdemorse
pkgver=2.9
pkgrel=2
pkgdesc="An X/GTK+ application - decodes Morse Code signals into text."
arch=('i686' 'x86_64')
url="http://5b4az.chronos.org.uk/pages/morse.html"
license=(GPL)
depends=('gtk2' 'alsa-lib' 'desktop-file-utils')
makedepends=('autoconf' 'automake' 'intltool' 'imagemagick' 'pkg-config')
install=$pkgname.install
source=(http://www.qsl.net/5b4az/pkg/morse/$pkgname/$pkgname-$pkgver.tar.bz2
	diff.autogen.sh
	$pkgname.desktop
	$pkgname.1
)

prepare() {
	cd $srcdir/$pkgname-$pkgver

	patch -p0 < ../diff.autogen.sh
	sed -i s:xpm:png: Makefile.am
	convert files/$pkgname.xpm files/$pkgname.png
	install -m644 ../$pkgname.desktop files/$pkgname.desktop
}

build() {
	cd $srcdir/$pkgname-$pkgver

	export LDFLAGS="-lm $LDFLAGS"
	./autogen.sh
	./configure --prefix=/usr
	make
}

check() {
	cd $srcdir/$pkgname-$pkgver

	make check
}

package() {
	cd $srcdir/$pkgname-$pkgver

	mkdir -p $pkgdir/usr/share/doc/$pkgname/examples/
	mkdir -p $pkgdir/usr/share/{applications,pixmaps}
	make DESTDIR=$pkgdir install

	mkdir -p $pkgdir/usr/bin
	mv $pkgdir/usr/bin/$pkgname $pkgdir/usr/bin/$pkgname.1
	install -D -m 755 ../$pkgname.1 $pkgdir/usr/bin/$pkgname
}
md5sums=('1d5cf3d42cd86ce477b3d33f12149a20'
         'ad6b0e6dd69c1d0efd01efef63ace093'
         'abb9f6e6510c9f747d70c738eeb5a824'
         '755a16fb41cd9be2269f525f4ece44d2')
sha256sums=('21f507ca6445cadbbdfed39c0b386ea8d71046f3847f784eb2abdee0524cca58'
            'cc38902be0fc1026cc027cd0fdab7e78bbfb336df36b90b8f25060775636924a'
            '1a690f6c7fdb73be3ebcedb59390faf74fcfb5f71484290d9d5a682650d3c9b1'
            'c20430e5c7e1e0713bbc17b4774b51ca455786aaca5f8a423730d437fffca893')
