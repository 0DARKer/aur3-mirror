# Original author: Bennett Goble <nivardus at gmail dot com>
# Maintainer: Maintainer: qs9rx <that nick at enjoys dÃ¶t it>

pkgname=xdemorse
pkgver=2.6
pkgrel=1
pkgdesc="A X/GTK+ application for decoding Morse code signals into text."
arch=('i686' 'x86_64')
url="http://5b4az.chronos.org.uk/pages/morse.html"
license=(GPL)
depends=("gtk2" "alsa-lib")
install=xdemorse.install

source=(
    http://www.qsl.net/5b4az/pkg/morse/xdemorse/xdemorse-${pkgver}.tar.bz2
    "xdemorse.install"
    "xdemorse.desktop"
)

sha1sums=(
    "511e94351f75fab03cacad77ab71c5e5a3248b99"
    "017a02b92fbb7ed0a3cb7896499acca3eb4bee73"
    "bae1ed5e60e62335f7c58581b77f6eb8a74761a1"
)

build() {
    cd "$srcdir/$pkgname-$pkgver"
    export LDFLAGS="-lm $LDFLAGS"
    ./autogen.sh
    ./configure --prefix=/usr
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    mkdir -p "$pkgdir/usr/share/doc/$pkgname/"
    install -Dm644 doc/* "$pkgdir/usr/share/doc/$pkgname/"
    install "$srcdir/$pkgname-$pkgver/default/.xdemorserc" "$pkgdir/usr/share/doc/$pkgname/xdemorserc"
    install -Dm644 $srcdir/xdemorse.desktop $pkgdir/usr/share/applications/xdemorse.desktop
}