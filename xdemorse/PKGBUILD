# Original author: Bennett Goble <nivardus at gmail dot com>
# Maintainer: Maintainer: qs9rx <that nick at enjoys dÃ¶t it>

pkgname=xdemorse
pkgver=2.4
pkgrel=1
pkgdesc="A X/GTK+ application for decoding Morse code signals into text."
arch=('i686' 'x86_64')
url="http://5b4az.chronos.org.uk/pages/morse.html"
license=(GPL)
depends=("atk" "glibc" "cairo" "fontconfig" "freetype2" "glib" "gtk" "pango")
install=xdemorse.install

source=(
    http://5b4az.chronos.org.uk/pkg/morse/xdemorse/xdemorse-${pkgver}.tar.bz2
    "xdemorse.install"
    "xdemorse.desktop"
)

sha1sums=(
    "72f3e7f690cd3a48f4e6180ccd4fb5c073f2ad32"
    "017a02b92fbb7ed0a3cb7896499acca3eb4bee73"
    "bae1ed5e60e62335f7c58581b77f6eb8a74761a1"
)

build() {
    cd "$srcdir/$pkgname-$pkgver"
    export LDFLAGS="-lm $LDFLAGS"
    ./autogen.sh
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    mkdir -p "$pkgdir/usr/share/doc/$pkgname/"
    install -Dm644 doc/* "$pkgdir/usr/share/doc/$pkgname/"
    install "$srcdir/$pkgname-$pkgver/default/.xdemorserc" "$pkgdir/usr/share/doc/$pkgname/xdemorserc"

    install -Dm644 $srcdir/xdemorse.desktop \
        $pkgdir/usr/share/applications/xdemorse.desktop
}