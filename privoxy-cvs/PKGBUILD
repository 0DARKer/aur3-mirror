# Maintainer: Mark Foxwell <fastfret79@archlinux.org.uk>
pkgname=privoxy-cvs
pkgver=20110906
pkgrel=1
pkgdesc="A web proxy with advanced filtering capabilities. CVS version"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/ijbswa"
license=('GPL')
depends=('pcre' 'zlib')
makedepends=('cvs')
provides=('privoxy')
backup=('etc/conf.d/privoxy'
        'etc/privoxy/'{config,trust,default.action,user.filter,default.filter}
        'etc/privoxy/'{match-all,user}.action
        'etc/logrotate.d/privoxy')
install='privoxy.install'
source=('privoxy'
        'privoxy.logrotate.d'
        'privoxy.conf.d')
md5sums=('1b4ed8c4e7e5b04b10ef41b597dc3f3b'
         'a364c34c8dc6eb78c9a6b777237932de'
         '27830ef79418e277b90c1c1fa933f876') 

_cvsroot=":pserver:anonymous:@ijbswa.cvs.sourceforge.net:/cvsroot/ijbswa"
_cvsmod="current"

build() {
  cd "$srcdir"
  msg "Connecting to $_cvsmod.sourceforge.net CVS server...."
  if [ -d $_cvsmod/CVS ]; then
    cd $_cvsmod
    cvs -z3 update -d
  else
    cvs -z3 -d $_cvsroot co -D $pkgver -f $_cvsmod
    cd $_cvsmod
  fi

  msg "CVS checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_cvsmod-build"
  cp -r "$srcdir/$_cvsmod" "$srcdir/$_cvsmod-build"
  cd "$srcdir/$_cvsmod-build"


  #
  # BUILD HERE
  #
  autoheader
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc/privoxy

  make
}

package() {
  cd "$srcdir/$_cvsmod-build"

  make prefix="${pkgdir}/usr" MAN_DEST="${pkgdir}/usr/share/man/man1" GROUP=42 \
    CONF_BASE="${pkgdir}/etc/privoxy" VAR_DEST="${pkgdir}/var" install

  # Don't overwrite existing log files!
  rm "${pkgdir}/var/log/privoxy/"*

  install -Dm0755 "${srcdir}/privoxy" "${pkgdir}/etc/rc.d/privoxy"
  install -Dm0644 "${srcdir}/privoxy.logrotate.d" "${pkgdir}/etc/logrotate.d/privoxy"
  install -Dm0644 "${srcdir}/privoxy.conf.d" "${pkgdir}/etc/conf.d/privoxy"

  # Fix config paths.
  sed -i -e 's#^confdir.*$#confdir /etc/privoxy#' -e 's#^logdir.*$#logdir /var/log/privoxy#' \
    -e '/^user-manual/s#.*#\#user-manual /usr/share/doc/privoxy/user-manual/#' \
    "${pkgdir}/etc/privoxy/config"

  # Fix group ownership and permissions.
  chgrp -R 42 "${pkgdir}/etc/privoxy/"
  chgrp -R 42 "${pkgdir}/var/log/privoxy/"

  find "${pkgdir}/etc/privoxy/" -type d | xargs chmod 0770
  find "${pkgdir}/etc/privoxy/" -type f | xargs chmod 0660
  chmod 0770 "${pkgdir}/var/log/privoxy"


}
