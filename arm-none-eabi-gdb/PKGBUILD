# Maintainer: Martin Schm√∂lzer <mschmoelzer@gmail.com>

# Based on the summon-arm-toolchain script by Piotr Esden-Tempski
# https://github.com/esden/summon-arm-toolchain

pkgname=arm-none-eabi-gdb
pkgver=7.7
pkgrel=2
pkgdesc="The GNU Debugger for the ARM EABI (bare-metal) target."
arch=(i686 x86_64)
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
groups=('arm-none-eabi-toolchain')
depends=('ncurses' 'expat' 'python2')
makedepends=('texinfo')
optdepends=('openocd: for debugging JTAG targets')
replaces=('cross-arm-none-eabi-gdb')
options=(!libtool !emptydirs)
source=(ftp://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.bz2)
sha256sums=('0404d1c6ee6b3ddd811722b5197944758d37b4591c216030effbac204f5a6c23')

build() {
  cd ${srcdir}/gdb-${pkgver}

  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir build
  cd build

  ../configure --target=arm-none-eabi \
               --prefix=/usr \
               --enable-multilib \
               --enable-interwork \
               --with-system-readline \
               --disable-nls
  make
}

package() {
  cd ${srcdir}/gdb-${pkgver}/build

  make DESTDIR=${pkgdir} install

  # libiberty.a conflicts with host version
  rm -f  ${pkgdir}/usr/lib/libiberty.a

  # We don't want these files in a cross version
  rm -rf ${pkgdir}/usr/share/info
  rm -rf ${pkgdir}/usr/share/gdb
  rm -rf ${pkgdir}/usr/include/gdb

  rm -f  ${pkgdir}/usr/share/man/man1/gdb*
  rm -rf ${pkgdir}/usr/share/man/man5
}

# vim: set ts=2 sw=2 ft=sh noet:

