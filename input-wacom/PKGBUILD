# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=input-wacom
pkgver=0.10.10_1
pkgrel=2
pkgdesc="Kernel driver for Wacom tablets"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('kernel26>=2.6.26')
makedepends=('kernel26-headers')
options=('!libtool')
source=(http://prdownloads.sourceforge.net/linuxwacom/input-wacom-${pkgver/_/-}.tar.bz2
        kernel2.6.37.patch)
md5sums=('9f7a13fe3b76395e3eb3b18cfafbc613'
         '8fbf7ddf14f2cbbb93a5d185b1f0d985')

build() {
  cd "${srcdir}"/input-wacom-${pkgver/_/-}
  patch -p1 < ../kernel2.6.37.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/input-wacom-${pkgver/_/-}
  _ver=`uname -r | sed 's/[0-9]*\.[0-9]*\.\([0-9]*\).*/\1/'`
  if test $_ver -ge 36
  then
    _ver=2.6.36
  else
    _ver=2.6.30
  fi
  install -D -m 644 $_ver/wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
}

