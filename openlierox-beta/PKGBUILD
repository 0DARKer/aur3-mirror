# Maintainer: Kevin <arc.mission at gmail com>

pkgname=openlierox-beta
_pkgname=openlierox
pkgver=0.59_beta9
pkgrel=1
pkgdesc="A real-time excessive Worms-clone (beta version)"
arch=('i686' 'x86_64')
url="http://www.openlierox.net/"
license=('LGPL')
depends=('sdl_image' 'sdl_mixer' 'boost' 'gd' 'hawknl' 'libxml2' 'libzip' 'curl' 'openal' 'freealut' 'libvorbis' 'lua')
makedepends=('cmake')
provides=('openlierox')
conflicts=('openlierox' 'openlierox-svn')
source=(http://downloads.sourceforge.net/$_pkgname/OpenLieroX_$pkgver.src.tar.bz2)
md5sums=('2b6f9f15ec4f029dced99a404dff0ab9')

build() {
  cd "$srcdir"/OpenLieroX/

  # compile
  cmake . -DHAWKNL_BUILTIN=OFF -DLIBLUA_BUILTIN=OFF -DBREAKPAD=OFF -DDEBUG=OFF -DSYSTEM_DATA_DIR=/usr/share || return 1
  make || return 1

  # install binary
  install -Dm755 bin/$_pkgname "$pkgdir"/usr/bin/$_pkgname

  # install data
  install -m755 -d "$pkgdir"/usr/share/OpenLieroX
  cp -r share/gamedir/* "$pkgdir"/usr/share/OpenLieroX/
  find "$pkgdir"/usr/share/OpenLieroX -type d -exec chmod 755 {} +
  find "$pkgdir"/usr/share/OpenLieroX -type f -exec chmod 644 {} +

  # install manual
  install -Dm644 doc/$_pkgname.6 "$pkgdir"/usr/share/man/man6/$_pkgname.6

  # install icon
  install -Dm644 share/OpenLieroX.svg \
    "$pkgdir"/usr/share/pixmaps/OpenLieroX.svg

  # install .desktop file
  install -Dm644 share/$_pkgname-$_pkgname.desktop \
    "$pkgdir"/usr/share/applications/$_pkgname.desktop
}
