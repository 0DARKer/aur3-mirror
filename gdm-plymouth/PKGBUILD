# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gdm-plymouth
_pkgname=gdm
pkgver=3.0.4
pkgrel=5
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
backup=('etc/pam.d/gdm' 'etc/pam.d/gdm-autologin' 'etc/gdm/custom.conf')
groups=('gnome-extra')
options=('!libtool')
depends=('libcanberra' 'libxklavier' 'gnome-session' 'upower' 'accountsservice' 'consolekit' 'xorg-xrdb' 'plymouth-systemd-git')
makedepends=('intltool' 'gnome-doc-utils' 'xorg-server')
conflicts=('fast-user-switch-applet' 'gdm')
replaces=('fast-user-switch-applet')
provides=("gdm=${pkgver}")
install=gdm.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz
        2.91_fix_external_program_directories.patch
        gdm.pam
        gdm-autologin.pam
        gdm
        plymouth.patch)
sha256sums=('7d2947fbc96c43cc94fd5915882655820c5453a1b1b11643b3c1be361f4288fd'
            '92c5eb913b9556cffe9b5bb89e5c3435703e929addfb98145442f58af5d532c1'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '3daff680ff6b7ea56f84f40843e46e72477c81e9e405028203c942af04d07ae5'
            '272c08d8e8b50bf424d0705ac864d4c18c47ec4f6893b1af732c2efbc86c9550'
            '0ad3e177addd4fa6f2909c89e6fe379fe0743195e1749ba4586380d343304c90')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/2.91_fix_external_program_directories.patch"
  # Plymouth Patch, copied from Fedora
  patch -Np1 -i "${srcdir}/plymouth.patch"

  # Regenerate configure files
  autoreconf -i -f

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm \
      --localstatedir=/var \
      --with-at-spi-registryd-directory=/usr/lib/at-spi \
      --disable-scrollkeeper \
      --disable-static \
      --without-tcp-wrappers
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_pkgname}.schemas" --domain gdm ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -r "${pkgdir}/etc/gconf/schemas/"

  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm"
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin"

  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/"

  rmdir "${pkgdir}/var/gdm"
  chmod 1770 "${pkgdir}/var/log/gdm"
  rm -rf "${pkgdir}/var/run"

  #Add spool dir - needed for plymouth integration
  mkdir -p ${pkgdir}/var/spool/gdm
}
