# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gdm-plymouth
_pkgname=gdm
pkgver=3.2.0
pkgrel=1
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
backup=('etc/pam.d/gdm-autologin' 'etc/pam.d/gdm-fingerprint' 'etc/pam.d/gdm' 'etc/pam.d/gdm-password'
        'etc/pam.d/gdm-smartcard' 'etc/pam.d/gdm-welcome' 'etc/gdm/custom.conf')
groups=('gnome-extra')
options=('!libtool')
depends=('libcanberra' 'libxklavier' 'gnome-session' 'gnome-settings-daemon' 'metacity' 'upower' 'accountsservice' 'consolekit' 'xorg-xrdb' 'nss' 'plymouth-systemd-git')
makedepends=('intltool' 'gnome-doc-utils' 'xorg-server' 'gobject-introspection')
conflicts=('fast-user-switch-applet' 'gdm')
replaces=('fast-user-switch-applet')
provides=("gdm=${pkgver}")
install=gdm.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/${pkgver%.*}/${_pkgname}-${pkgver}.tar.xz
        fix_external_program_directories.patch
        gdm
        gdm-autologin.pam gdm-fingerprint.pam gdm.pam gdm-password.pam gdm-smartcard.pam gdm-welcome.pam
        check-for-shell.patch::http://git.gnome.org/browse/gdm/patch/?id=64e6b10f98fe7226a2f41807268dae3afa80236d
        plymouth.patch)
sha256sums=('f827f5500827acef94ad2d60f02ec489bc35cb6392d8f4a60ec814599c2991b7'
            'e6bcc4ef3b5309193a1701f6ffb6f87f21b7f7e36c63cd445a11f7ab446617e6'
            '272c08d8e8b50bf424d0705ac864d4c18c47ec4f6893b1af732c2efbc86c9550'
            'b30dfa217718b43ff3aa4e8af08985963175b79ff30698ec54e8396d2905922d'
            '5bc3ff3ea7b31219dfcb7d9fc0eb2819eca1c5573a0f426d288a17560a69633e'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '0dbb37b4e2a2a2dd2305f0f69cf32d63d353d34aacaf805d2c2ec52fbe558bb4'
            'df74b0dcfb17ea43cc1e268a27cd256c552672457db1af40b285452d16b3e2fd'
            '39bf3824cc505043dc3136356da16dab9df9f369f1077435a324be2ac16cf834'
            'ae54f11e593e97e031d2dd7b33ce9899038356cef87b354f3c4c596a3153155d'
            '64d10827b1ac88986ed063a346d01a620084215d8b02a642dffdca8977cb3fdd')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/check-for-shell.patch"
  patch -Np1 -i "${srcdir}/fix_external_program_directories.patch"
  # Plymouth Patch, copied from Fedora
  patch -Np1 -i "${srcdir}/plymouth.patch"

  # Regenerate configure files
  autoreconf -i -f

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm \
      --localstatedir=/var \
      --with-at-spi-registryd-directory=/usr/lib/at-spi2-core \
      --disable-scrollkeeper \
      --disable-static \
      --without-tcp-wrappers
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_pkgname}.schemas" --domain gdm ${pkgdir}/etc/gconf/schemas/*.schemas

  for i in gdm-autologin.pam gdm-fingerprint.pam gdm.pam gdm-password.pam gdm-smartcard.pam gdm-welcome.pam;do
      install -m644 "${srcdir}/${i}" "${pkgdir}/etc/pam.d/${i%.pam}"
  done

  install -Dm755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/gdm"

  chmod 1770 "${pkgdir}/var/log/gdm"
  chmod 700 "${pkgdir}/var/lib/gdm/.config/dconf"
  rm -rf "${pkgdir}/var/run" "${pkgdir}/var/gdm" "${pkgdir}/etc/gconf"

  #Add spool dir - needed for plymouth integration
  mkdir -p ${pkgdir}/var/spool/gdm
}
