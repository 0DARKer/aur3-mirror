# Maintainer: willemw <willemw12@gmail.com>

_targets="common qt4"
_pkgname=backintime-qt4
pkgname=$_pkgname-bzr
pkgver=r930
pkgrel=1
#pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault"
pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault ('experimental' development branch)"
url="http://backintime.le-web.org"
license=('GPL')
arch=('any')
depends=('rsync' 'cron' 'openssh' 'python-dbus'
         'libnotify' 'python-pyqt4')
optdepends=('cron: for scheduled backups'
            'encfs: for encrypted filesystems'
            'gksu: for running as root' 'kdesudo: for running as root'
            'meld: diff tool' 'kompare: diff tool'
            'python-keyring: for storing passwords' 'python-secretstorage: for storing passwords'
            'sshfs: for remote filesystems')
makedepends=('bzr')
provides=(backintime $_pkgname)
conflicts=(backintime $_pkgname
           backintime-gnome backintime-gtk backintime-kde4)     # backintime reverse dependency conflicts
#source=($pkgname::bzr+https://code.launchpad.net/~bit-team/backintime/trunk)
source=($pkgname::bzr+https://code.launchpad.net/~bit-team/backintime/Qt4)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s" "$(bzr revno)"
}

build() {
  for i in $_targets; do
    cd "$srcdir/$pkgname/$i"
    ./configure
    make 
  done
}

package() {
  for i in $_targets; do
    cd "$srcdir/$pkgname/$i"
    make DESTDIR="$pkgdir" install 
  done
}

