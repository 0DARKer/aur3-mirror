# Maintainer: willemw <willemw12@gmail.com>

_targets="common qt4"
_pkgname=backintime-qt4
pkgname=$_pkgname-bzr
pkgver=r926
pkgrel=2
#pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault"
pkgdesc="Simple backup/snapshot system inspired by Flyback and TimeVault ('experimental' development branch)"
url="http://backintime.le-web.org"
license=('GPL')
arch=('any')
depends=('rsync' 'cron' 'openssh' 'python-dbus'
         'libnotify' 'python-pyqt4')
optdepends=('gksu: run as root' 'kdesudo: run as root'
            'meld: diff tool' 'kompare: diff tool'
            'sshfs: remote filesystems'
            'encfs: encrypted filesystems'
            'python-keyring: store passwords'
            'python-secretstorage: store passwords')
makedepends=('bzr')
provides=(backintime $_pkgname)
conflicts=(backintime $_pkgname
           backintime-gnome backintime-gtk backintime-kde4)     # backintime reverse dependency conflicts
#source=($pkgname::bzr+https://code.launchpad.net/~bit-team/backintime/trunk)
source=($pkgname::bzr+https://code.launchpad.net/~bit-team/backintime/Qt4)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s" "$(bzr revno)"
}

build() {
  for i in $_targets; do
    cd "$srcdir/$pkgname/$i"
    ./configure
    make 
  done
}

package() {
  for i in $_targets; do
    cd "$srcdir/$pkgname/$i"
    make DESTDIR="$pkgdir" install 
  done
}

