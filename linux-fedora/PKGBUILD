# Maintainer: 
# Maintainer:

pkgbase="linux-fedora"
 pkgname=linux-fedora       # Build kernel with a different name
_kernelname=${pkgname#linux}
_basekernel=3.0
pkgver=${_basekernel}.1
pkgrel=2
makedepends=('xmlto' 'docbook-xsl')
arch=(i686 x86_64)
license=('GPL2')
url="http://www.kernel.org"
options=(!strip)
source=(ftp://ftp.kernel.org/pub/linux/kernel/v3.0/linux-${pkgver}.tar.bz2
        # the main kernel config files
        config-i386 config.x86_64
        # standard config files for mkinitcpio ramdisk
        linux.preset
	acpi-ec-add-delay-before-write.patch
	add-appleir-usb-driver.patch
	die-floppy-die.patch
	disable-i8042-check-on-apple-mac.patch
	dmar-disable-when-ricoh-multifunction.patch
	efi-dont-map-boot-services-on-32bit.patch
	epoll-fix-spurious-lockdep-warnings.patch
	fix-cdc-ncm-dma-stack-vars.patch
	fix_xen_guest_on_old_EC2.patch
	hda_intel-prealloc-4mb-dmabuffer.patch
	hfsplus-ensure-bio-requests-are-not-smaller-than-the.patch
	linux-2.6.29-sparc-IOC_TYPECHECK.patch
	linux-2.6.30-no-pcspkr-modalias.patch
	linux-2.6-32bit-mmap-exec-randomization.patch
	linux-2.6-acpi-debug-infinite-loop.patch
	linux-2.6-acpi-video-dos.patch
	linux-2.6-crash-driver.patch
	linux-2.6-debug-taint-vm.patch
	linux-2.6-debug-vm-would-have-oomkilled.patch
	linux-2.6-defaults-acpi-video.patch
	linux-2.6-defaults-aspm.patch
	linux-2.6-e1000-ich9-montevina.patch
	linux-2.6-i386-nx-emulation.patch
	linux-2.6-input-kill-stupid-messages.patch
	linux-2.6-intel-iommu-igfx.patch
	linux-2.6-makefile-after_link.patch
	linux-2.6-serial-460800.patch
	linux-2.6-silence-acpi-blacklist.patch
	linux-2.6-silence-fbcon-logo.patch
	linux-2.6-silence-noise.patch
	linux-2.6-zd1211rw-fix-invalid-signal-values-from-device.patch
	udlfb-bind-framebuffer-to-interface.patch
	ums-realtek-driver-uses-stack-memory-for-DMA.patch
	utrace.patch)

md5sums=('aed949984b878b7fb77ffe9cbee8ce0c'
         'fc6aae0fb4d70feff92ec762d29dee45'
         'b1465805b4a388a15e90318ac2a94798'
         '7cc9c4fbbdf9c5f5921aa4a3e582bf53'
         '2ddca0c1935f919a50c37dc9bff1b19d'
         'c2d501533484496ad9cab3ff4a9f9319'
         '0d5ef2e9201c71ed8f4ff85a981b93dc'
         'b5541dd0d354884dd1ff79d7ad7661f4'
         '4dea7c2a16cc7c3688fead16c4c872d1'
         'c0cd97255f54e2f6ecf96f53f8cfdfe6'
         'ba4bfee2e493cc9fcbeec811e0dbec3f'
         '2639868ccf116d8eed18ffdb1032220b'
         '65f3d623fd0c55bc8d2b90c5083940cb'
         '8ce79f332a567b9b08699aae1afb34e0'
         '3c32ff95e3ee30cc667e98bf44f31120'
         '40d2145c5bc73ea4703dbcb2566f17e8'
         '12e762540b9926bd972986d1ff116bcc'
         '33d9ec1a2c9d5f3abd33e23f3729b3a3'
         '29d0aa1d221d842b6a57bf65db411e05'
         '96bd5bb046edb6de1f713e72fe8c6346'
         '19e00573377f9a5f8bf7b47192918d0f'
         '5a21fc827b1199943887fd1f20bf89e3'
         '3389b817f2e12765039600a9d49f2849'
         '731ff804d9dde997c1ffd98e2355bc2d'
         'e7b0133bfd9cd2bdf4ca3c231585ea73'
         '36a49cb34d83f8960da13f21a7564ba0'
         '7ab98e71597275cdf79af9c9540e9423'
         '10b7b35635f787cb5596aed2206c9187'
         '71d72a3eaeba9f9d27842c554fe204b5'
         '012de79081ed3c3f280702a78f6d4982'
         '840c57e74a3e43009396d986f3fb6902'
         '529a3da98d3fd021c6fb949301c98eee'
         '13fa4b7b4f5cfaba0c39f3780b3835d0'
         'c1f2020cb0b50c4ac7fdc86132f385be'
         '1e5469248218f1f694a0eea2dabec60c'
         'f3d8847446994f7ccb2955f41cbb1082'
         '0a10c4342db2e05cf913fe6469a7f9c9'
         'b5bd83e57c3b325c94ede99c09f33a3e')

build() {
  cd ${srcdir}/linux-${pkgver}

  # Add -ARCH patches
  # See http://projects.archlinux.org/linux-2.6-ARCH.git/
  #patch -Np1 -i ${srcdir}/${_patchname}

  echo "patching ...1"
  # build tweak for build ID magic, even for -vanilla
  patch -Np1 -i ${srcdir}/linux-2.6-makefile-after_link.patch

  # Git trees.
  # Standalone patches
  echo "patching fedora stuff"
  patch -Np1 -i ${srcdir}/linux-2.6.29-sparc-IOC_TYPECHECK.patch

  patch -Np1 -i ${srcdir}/linux-2.6-32bit-mmap-exec-randomization.patch
  patch -Np1 -i ${srcdir}/linux-2.6-i386-nx-emulation.patch

  patch -Np1 -i ${srcdir}/linux-2.6-debug-taint-vm.patch
  patch -Np1 -i ${srcdir}/linux-2.6-debug-vm-would-have-oomkilled.patch

  patch -Np1 -i ${srcdir}/linux-2.6-defaults-aspm.patch

  patch -Np1 -i ${srcdir}/linux-2.6-defaults-acpi-video.patch
  patch -Np1 -i ${srcdir}/linux-2.6-acpi-video-dos.patch
  patch -Np1 -i ${srcdir}/acpi-ec-add-delay-before-write.patch
  patch -Np1 -i ${srcdir}/linux-2.6-acpi-debug-infinite-loop.patch

  patch -Np1 -i ${srcdir}/linux-2.6-input-kill-stupid-messages.patch
  patch -Np1 -i ${srcdir}/linux-2.6.30-no-pcspkr-modalias.patch

  patch -Np1 -i ${srcdir}/linux-2.6-serial-460800.patch

  patch -Np1 -i ${srcdir}/die-floppy-die.patch

  patch -Np1 -i ${srcdir}/linux-2.6-silence-noise.patch
  patch -Np1 -i ${srcdir}/linux-2.6-silence-fbcon-logo.patch

  patch -Np1 -i ${srcdir}/hda_intel-prealloc-4mb-dmabuffer.patch

  patch -Np1 -i ${srcdir}/linux-2.6-e1000-ich9-montevina.patch

  patch -Np1 -i ${srcdir}/linux-2.6-crash-driver.patch

  # virt + ksm patches
  echo "patching virt, ksm"
  patch -Np1 -i ${srcdir}/fix_xen_guest_on_old_EC2.patch

  patch -Np1 -i ${srcdir}/linux-2.6-intel-iommu-igfx.patch

  # Quiet boot fixes
  # silence the ACPI blacklist code
  echo "patching fedora quiet boot"
  patch -Np1 -i ${srcdir}/linux-2.6-silence-acpi-blacklist.patch

  # fs fixes
  # NFSv4
  # patches headed upstream
  echo "patching fedora NFSv4"
  patch -Np1 -i ${srcdir}/add-appleir-usb-driver.patch
  patch -Np1 -i ${srcdir}/disable-i8042-check-on-apple-mac.patch
  patch -Np1 -i ${srcdir}/linux-2.6-zd1211rw-fix-invalid-signal-values-from-device.patch
  patch -Np1 -i ${srcdir}/udlfb-bind-framebuffer-to-interface.patch
  patch -Np1 -i ${srcdir}/fix-cdc-ncm-dma-stack-vars.patch
  patch -Np1 -i ${srcdir}/ums-realtek-driver-uses-stack-memory-for-DMA.patch

  # Runtime power management 
  echo "patching fedora ..."
  patch -Np1 -i ${srcdir}/dmar-disable-when-ricoh-multifunction.patch

  patch -Np1 -i ${srcdir}/epoll-fix-spurious-lockdep-warnings.patch
  patch -Np1 -i ${srcdir}/hfsplus-ensure-bio-requests-are-not-smaller-than-the.patch

  patch -Np1 -i ${srcdir}/efi-dont-map-boot-services-on-32bit.patch
  patch -Np1 -i ${srcdir}/utrace.patch

  if [ "$CARCH" = "x86_64" ]; then
    cat ../config.x86_64 >./.config
  else
    cat ../config >./.config
  fi
  if [ "${_kernelname}" != "" ]; then
    sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"${_kernelname}\"|g" ./.config
  fi
  # get kernel version  
  make prepare
  # load configuration
  # Configure the kernel. Replace the line below with one of your choice.
  #make menuconfig # CLI menu for configuration
  #make nconfig # new CLI menu for configuration
  #make xconfig # X-based configuration
  #make oldconfig # using old config from previous kernel version
  # ... or manually edit .config
  ####################
  # stop here
  # this is useful to configure the kernel
  #msg "Stopping build"
  #return 1
  ####################
  yes "" | make config
  # build!
  make ${MAKEFLAGS} bzImage modules
}

package() {
  pkgdesc="The Linux Kernel plus Fedora patches, modules and headers with debug enabled for systemtap"
  groups=('base')
  backup=(etc/mkinitcpio.d/${pkgname}.preset)
  depends=('coreutils' 'linux-firmware' 'module-init-tools>=3.12-2' 'mkinitcpio>=0.6.8-2')
  # pwc, ieee80211 and hostap-driver26 modules are included in kernel26 now
  # nforce package support was abandoned by nvidia, kernel modules should cover everything now.
  # kernel24 support is dropped since glibc24
  replaces=('kernel24' 'kernel24-scsi' 'kernel26-scsi'
            'alsa-driver' 'ieee80211' 'hostap-driver26'
            'pwc' 'nforce' 'squashfs' 'unionfs' 'ivtv'
            'zd1211' 'kvm-modules' 'iwlwifi' 'rt2x00-cvs'
            'gspcav1' 'atl2' 'wlan-ng26' 'rt2500' 'nouveau-drm')
  install=linux.install
  optdepends=('crda: to set the correct wireless channels of your country')

  KARCH=x86
  cd ${srcdir}/linux-${pkgver}
  # get kernel version
  _kernver="$(make kernelrelease)"
  mkdir -p ${pkgdir}/{lib/modules,lib/firmware,boot}
  make INSTALL_MOD_PATH=${pkgdir} modules_install
  cp System.map ${pkgdir}/boot/System.map${_kernelname}
  cp arch/$KARCH/boot/bzImage ${pkgdir}/boot/vmlinuz${_kernelname}
  #  # add vmlinux
  install -m644 -D vmlinux ${pkgdir}/usr/src/linux-${_kernver}/vmlinux

  # install fallback mkinitcpio.conf file and preset file for kernel
  install -m644 -D ${srcdir}/linux.preset ${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset
  # set correct depmod command for install
  sed \
    -e  "s/KERNEL_NAME=.*/KERNEL_NAME=${_kernelname}/g" \
    -e  "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
    -i $startdir/linux.install
  sed \
    -e "s|source .*|source /etc/mkinitcpio.d/linux${_kernelname}.kver|g" \
    -e "s|default_image=.*|default_image=\"/boot/${pkgname}.img\"|g" \
    -e "s|fallback_image=.*|fallback_image=\"/boot/${pkgname}-fallback.img\"|g" \
    -i ${pkgdir}/etc/mkinitcpio.d/${pkgname}.preset

  echo -e "# DO NOT EDIT THIS FILE\nALL_kver='${_kernver}'" > ${pkgdir}/etc/mkinitcpio.d/${pkgname}.kver
  # remove the firmware
  rm -rf ${pkgdir}/lib/firmware
  # gzip -9 all modules to safe 100MB of space
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

  # Add the headers package
  mkdir -p ${pkgdir}/lib/modules/${_kernver}
  cd ${pkgdir}/lib/modules/${_kernver}
  ln -sf ../../../usr/src/linux-${_kernver} build
  cd ${srcdir}/linux-${pkgver}
  install -D -m644 Makefile \
    ${pkgdir}/usr/src/linux-${_kernver}/Makefile
  install -D -m644 kernel/Makefile \
    ${pkgdir}/usr/src/linux-${_kernver}/kernel/Makefile
  install -D -m644 .config \
    ${pkgdir}/usr/src/linux-${_kernver}/.config
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include

  for i in acpi asm-generic config crypto drm generated linux math-emu \
    media net pcmcia scsi sound trace video xen; do
    cp -a include/$i ${pkgdir}/usr/src/linux-${_kernver}/include/
  done

  # copy arch includes for external modules
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/x86
  cp -a arch/x86/include ${pkgdir}/usr/src/linux-${_kernver}/arch/x86/

  # copy files necessary for later builds, like nvidia and vmware
  cp Module.symvers ${pkgdir}/usr/src/linux-${_kernver}
  cp -a scripts ${pkgdir}/usr/src/linux-${_kernver}
  # fix permissions on scripts dir
  chmod og-w -R ${pkgdir}/usr/src/linux-${_kernver}/scripts
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/.tmp_versions

  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel

  cp arch/$KARCH/Makefile ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
  if [ "$CARCH" = "i686" ]; then
    cp arch/$KARCH/Makefile_32.cpu ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/
  fi
  cp arch/$KARCH/kernel/asm-offsets.s ${pkgdir}/usr/src/linux-${_kernver}/arch/$KARCH/kernel/

  # add headers for lirc package
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video
  cp drivers/media/video/*.h  ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/
  for i in bt8xx cpia2 cx25840 cx88 em28xx et61x251 pwc saa7134 sn9c102; do
   mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/$i
   cp -a drivers/media/video/$i/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/video/$i
  done
  # add docbook makefile
  install -D -m644 Documentation/DocBook/Makefile \
    ${pkgdir}/usr/src/linux-${_kernver}/Documentation/DocBook/Makefile
  # add dm headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  cp drivers/md/*.h  ${pkgdir}/usr/src/linux-${_kernver}/drivers/md
  # add inotify.h
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/linux
  cp include/linux/inotify.h ${pkgdir}/usr/src/linux-${_kernver}/include/linux/
  # add wireless headers
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  cp net/mac80211/*.h ${pkgdir}/usr/src/linux-${_kernver}/net/mac80211/
  # add dvb headers for external modules
  # in reference to:
  # http://bugs.archlinux.org/task/9912
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core
  cp drivers/media/dvb/dvb-core/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-core/
  # add dvb headers for external modules
  # in reference to:
  # http://bugs.archlinux.org/task/11194
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  cp include/config/dvb/*.h ${pkgdir}/usr/src/linux-${_kernver}/include/config/dvb/
  # add dvb headers for http://mcentral.de/hg/~mrec/em28xx-new
  # in reference to:
  # http://bugs.archlinux.org/task/13146
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  cp drivers/media/dvb/frontends/lgdt330x.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  cp drivers/media/video/msp3400-driver.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  # add dvb headers  
  # in reference to:
  # http://bugs.archlinux.org/task/20402
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-usb
  cp drivers/media/dvb/dvb-usb/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/dvb-usb/
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends
  cp drivers/media/dvb/frontends/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/dvb/frontends/
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/common/tuners
  cp drivers/media/common/tuners/*.h ${pkgdir}/usr/src/linux-${_kernver}/drivers/media/common/tuners/
  # add xfs and shmem for aufs building
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/fs/xfs
  mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/mm
  cp fs/xfs/xfs_sb.h ${pkgdir}/usr/src/linux-${_kernver}/fs/xfs/xfs_sb.h
  # copy in Kconfig files
  for i in `find . -name "Kconfig*"`; do 
    mkdir -p ${pkgdir}/usr/src/linux-${_kernver}/`echo $i | sed 's|/Kconfig.*||'`
    cp $i ${pkgdir}/usr/src/linux-${_kernver}/$i
  done

  chown -R root.root ${pkgdir}/usr/src/linux-${_kernver}
  find ${pkgdir}/usr/src/linux-${_kernver} -type d -exec chmod 755 {} \;
  # strip scripts directory
  find ${pkgdir}/usr/src/linux-${_kernver}/scripts  -type f -perm -u+w 2>/dev/null | while read binary ; do
  case "$(file -bi "$binary")" in
    *application/x-sharedlib*) # Libraries (.so)
    /usr/bin/strip $STRIP_SHARED "$binary";;
    *application/x-archive*) # Libraries (.a)
    /usr/bin/strip $STRIP_STATIC "$binary";;
    *application/x-executable*) # Binaries
    /usr/bin/strip $STRIP_BINARIES "$binary";;
    esac 
  done 
  # remove unneeded architectures
  rm -rf ${pkgdir}/usr/src/linux-${_kernver}/arch/{alpha,arm,arm26,avr32,blackfin,cris,frv,h8300,ia64,m32r,m68k,m68knommu,mips,microblaze,mn10300,parisc,powerpc,ppc,s390,sh,sh64,sparc,sparc64,um,v850,xtensa}
}

