# Maintainer: Archan Paul <arp@archan.org>
# Reference : http://aur.archlinux.org/packages/eg/egit/PKGBUILD

pkgname=eclipse-egit
pkgver=1.1.0.201109151100
pkgrel=2
pkgdesc="An Eclipse Team provider for the Git version control system"
arch=('i686' 'x86_64')
url="http://eclipse.org/egit/"
license=('EPL')
depends=('eclipse>=3.6' 'eclipse-mylyn>=3.6')
source=("http://download.eclipse.org/egit/site.zip")
md5sums=('c7e654d44eb6a132cdff30642296726a')

build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}
  find site -name '*source*' -exec rm {} \;
  mv site/* ${srcdir}
  
  # Features
  find features -type f | while read _feature ; do
    echo ${_feature}
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}

