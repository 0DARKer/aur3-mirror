# Contributor: Philip Nilsson <pnilsson@nullref.se>
# Contributor: Valentin V. Bartenev <ne@vbart.ru> http://vbart.info/
# Contributor: Frédéric Mangano <fmang@mg0.fr>

pkgname='mongoc'
pkgver='0.7.1'
pkgrel='2'
pkgdesc='The officially supported MongoDB C driver and BSON library.'
arch=('i686' 'x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('glibc')
makedepends=('make' 'gcc')
source=("$pkgname.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/v$pkgver.tar.gz")
md5sums=('b9c90dfd51813047be041e1c9374f62b')

build() {
  cd "mongo-c-driver-$pkgver"
  sed -i Makefile -e '/INSTALL/ s,src/bson\.h,& src/bcon.h src/gridfs.h,'
  make
}

package() {
  cd "mongo-c-driver-$pkgver"
  make install \
    INSTALL_INCLUDE_PATH="$pkgdir/usr/include" \
    INSTALL_LIBRARY_PATH="$pkgdir/usr/lib"
}
