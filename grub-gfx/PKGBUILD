# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Mario A. Vazquez
# Contributor: Jerzy Goca <juras256@epf.pl>
# Contributor: Keshav P R
# Contributor: Eugene Lamskoy <e.lamskoy@gmail.com>

_pkgname="grub"
pkgname="${_pkgname}-gfx"
pkgver=0.97
pkgrel=24
pkgdesc="A GNU multiboot boot loader"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/grub/grub-legacy.en.html"
# groups=('base')
makedepends=()
depends=('ncurses')
conflicts=('grub' 'grub-legacy')
provides=('grub' 'grub-legacy')
backup=('boot/grub/menu.lst')
install="${pkgname}.install"
backup=('boot/grub/menu.lst')

source=("ftp://alpha.gnu.org/gnu/grub/grub-${pkgver}.tar.gz"
        'menu.lst'
        'install-grub'
        '040_all_grub-0.96-nxstack.patch'
        '05-grub-0.97-initrdaddr.diff'
        'i2o.patch'
        'special-devices.patch'
        'more-raid.patch'
        'intelmac.patch'
        'grub-inode-size.patch'
        'ext4.patch'
        'grub-0.97-graphics.patch'
        'grub-0.97-gpt.patch'
        'objcopy-absolute.patch'
        'splash.xpm.gz'
)

md5sums=('cd3f3eb54446be6003156158d51f4884'
         'b3150de8306f4b041e74cf851ea6c4db'
         '3182c4ae4963a16930bc772bba89dacf'
         'eb9d69c46af3a0667c1f651817d7f075'
         'ccd2d757e79e3a03dc19ede7391ed328'
         '826fdbf446067f9861baf9f6a69a4583'
         '49f6d4bcced0bc8bbcff273f3254bbfa'
         'f41f702014a064918d7afc6fc23baa6e'
         '175dc6b9f4ab94e8056c3afb3e34460a'
         '69c648d2b8d0965df70a74014424f31c'
         '39e0f9a05b7e04aceb24fc7bc4893e3d'
         '12f043616b51ce2ba82e46c9186a528d'
         '52cd09a6966f12961d11f7b3b7e76bd2'
         '0b8733828c00d9af927f2f2b200496b9'
         '342f59f24cf5de5f013eacda68e617eb')

if [ "${CARCH}" == 'x86_64' ]; then
    echo
    makedepends=(${makedepends[@]} 'gcc-multilib' 'gcc-libs-multilib' 'lib32-glibc' 'binutils-multilib' 'libtool-multilib')
fi

# set destination architecture before build from cmdline:
# DESTARCH="i686" makepkg -src
# DESTARCH="x86_64" makepkg -src
# Both Arch 64bit and 32bit are tested with this.
# If you won't specify DESTARCH, you'll get a package for your architecture

DESTARCH=${DESTARCH:-$CARCH}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  fgrep -rlZ pkglib_DATA --include Makefile.am . | xargs -0 sed -i 's/pkglib_DATA/pkgdata_DATA/g'

  # graphics patch
  patch -Np1 -i "${srcdir}/grub-0.97-graphics.patch"
  
  # optimizations break the build -- disable them
  # adding special devices to grub, patches are from fedora
  patch -Np1 -i "${srcdir}/special-devices.patch"
  patch -Np1 -i "${srcdir}/i2o.patch"
  patch -Np1 -i "${srcdir}/more-raid.patch"
  patch -Np1 -i "${srcdir}/intelmac.patch"
  
  # Add support for bigger inode size to e2fs_stage1_5
  patch -Np1 -i "${srcdir}/grub-inode-size.patch"
  
  # Add ext4 support
  # http://www.mail-archive.com/bug-grub@gnu.org/msg11458.html
  patch -Np1 -i "${srcdir}/ext4.patch"

  # Gpt patch is optional, official archbuild includes it, but patch is not used
  #patch -Np1 -i "${srcdir}/grub-0.97-gpt.patch"

  # Absolute objcopy configure patch, taken from official archbuild
  patch -Np1 -i "${srcdir}/objcopy-absolute.patch"

  # correcting problems for new wersion of autotools
  sed -i -e'/^AC_PROG_CC/ a\AM_PROG_CC_C_O\ ' ./configure.ac || true
  sed -i -e'/^AC_PROG_CC/ a\AM_PROG_AS\ ' ./configure.ac || true


  aclocal
  autoconf
  automake

  # Stupid workaround, incase objcopy-absolute.patch fails
  #sed 's|grub_cv_prog_objcopy_absolute=no|grub_cv_prog_objcopy_absolute=yes|g' -i ./configure

  if [ "${DESTARCH}" == 'x86_64' ]; then
    # patch from gentoo for fixing a segfault
    patch -Np1 -i "${srcdir}/040_all_grub-0.96-nxstack.patch"

    # patch from frugalware to make it boot when more than 2GB ram installed
    patch -Np1 -i "${srcdir}/05-grub-0.97-initrdaddr.diff"

    _CFLAGS="-m32 -static"
  else
    _CFLAGS="-m32"
  fi

  CFLAGS="${_CFLAGS}" ./configure --with-platform=pc \
                                  --host=i386-pc-linux-gnu \
                                  --disable-auto-linux-mem-opt \
                                  --prefix=/usr --bindir=/bin --sbindir=/sbin \
                                  --mandir=/usr/share/man --infodir=/usr/share/info \
                                  --libdir=/usr/lib

  CFLAGS= make

}

package() {

  cd "${srcdir}/${_pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/menu.lst" "${pkgdir}/boot/grub/menu.lst"
  install -D -m755 "${srcdir}/install-grub" "${pkgdir}/sbin/install-grub"
  install -D -m644 "${srcdir}/splash.xpm.gz" $pkgdir/boot/grub/splash.xpm.gz
  export CARCH="${DESTARCH}"

}
