_pkgbasename=zziplib
pkgname=lib32-$_pkgbasename
pkgver=0.13.58
pkgrel=1
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file (32 bit)"
arch=('x86_64')
url="http://zziplib.sourceforge.net"
license=('LGPL' 'MPL')
depends=('lib32-zlib' "$_pkgbasename")
makedepends=('python2' 'gcc-multilib')
options=('!libtool')
source=(http://downloads.sourceforge.net/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('a0f743a5a42ca245b2003ecaea958487')

build() {
	cd ${srcdir}/${_pkgbasename}-${pkgver}
	./configure --prefix=/usr --libdir=/usr/lib32 \
					CC='gcc -m32' PYTHON='/usr/bin/python2'
	make
}

package() {
	cd ${srcdir}/${_pkgbasename}-${pkgver}
	make DESTDIR=${pkgdir} install
	rm -rf ${pkgdir}/usr/{bin,include,share}
}
