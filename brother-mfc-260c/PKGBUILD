# Maintainer: libernux <dutchman55@gmx.com>
pkgname="brother-mfc-260c"
pkgver="1.0.1"
pkgrel=1
pkgdesc="LPR and CUPS driver for the Brother MFC-260c"
url="http://solutions.brother.com/linux/en_us/"
arch=("i686" "x86_64")
license=("GPL" 'custom:brother commercial license')
depends=("cups")
install="$pkgname.install"
source=(
	"http://www.brother.com/pub/bsc/linux/dlf/mfc260clpr-$pkgver-$pkgrel.i386.rpm"
	"http://www.brother.com/pub/bsc/linux/dlf/mfc260ccupswrapper-$pkgver-$pkgrel.i386.rpm"
)
md5sums=(
	'ac8a42774d7334830f8d55906240a1b8'
	'49c85d5f7dac073cbcef8168a119a70f'
)

build() {
	cd "$srcdir/usr/local/Brother/Printer/mfc260c/cupswrapper"
#  generate the ppd
	echo '#! /bin/sh' > genppd
	echo 'ppd_file_name=brmfc260c.ppd' >> genppd
	echo 'touch $ppd_file_name' >> genppd
	sed -n '/^printer_model/,/^device_model/p' cupswrappermfc260c >> genppd
	sed -n '/^cat <<ENDOFPPDFILE1/,/^ENDOFPPDFILE_END/p' cupswrappermfc260c >>genppd
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genppd
	chmod +x genppd
	./genppd
#  generate the wrapper
	echo '#! /bin/sh' > genwrapper
	echo 'brotherlpdwrapper=brlpdwrappermfc260c'  >> genwrapper
	echo 'touch $brotherlpdwrapper' >>genwrapper
	sed -n '/^printer_model/p' cupswrappermfc260c >>genwrapper
	sed -n '/^device_model/p' cupswrappermfc260c >>genwrapper
	sed -n '/cat <<!ENDOFWFILTER!/,/!ENDOFWFILTER!/p' cupswrappermfc260c >>genwrapper
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' genwrapper
	chmod +x genwrapper
	./genwrapper
#  generate other
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/bin/brprintconf_mfc260c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc260c/lpd/filtermfc260c"
	sed -i  's|/usr/local/Brother|/usr/share/brother|g' "$srcdir/usr/local/Brother/Printer/mfc260c/lpd/brmfc260cfilter"
	install -d "$srcdir/usr/share"
	mv "$srcdir/usr/local/Brother/" "$srcdir/usr/share/brother"
	rm "$srcdir/usr/share/brother/Printer/mfc260c/cupswrapper/cupswrappermfc260c"
	rm "$srcdir/usr/share/brother/Printer/mfc260c/cupswrapper/genppd"
	rm "$srcdir/usr/share/brother/Printer/mfc260c/cupswrapper/genwrapper"
	rm "$srcdir/usr/share/brother/Printer/mfc260c/inf/setupPrintcapij"
	rm -rf "$srcdir/usr/local"
}

package() {
	cd "$pkgdir"
	cp -R "$srcdir/usr" "$pkgdir"
	install -m 644 -D "$srcdir/usr/share/brother/Printer/mfc260c/cupswrapper/brmfc260c.ppd" "$pkgdir/usr/share/cups/model/brmfc260c.ppd"
	install -m 755 -D "$srcdir/usr/share/brother/Printer/mfc260c/cupswrapper/brlpdwrappermfc260c" "$pkgdir/usr/lib/cups/filter/brlpdwrappermfc260c"
}