pkgname=amule-svn
pkgver=10835
pkgrel=2
pkgdesc='Client for the eD2k and Kad networks'
arch=(i686 x86_64)
url=http://amule.org
license=(GPL2)
depends=(crypto++ gd geoip libupnp wxgtk2.8)
makedepends=(subversion)
provides=(amule)
conflicts=(amule)
source=(amule::svn+https://amule.googlecode.com/svn/trunk/
        http://chakra.sourceforge.net/sources/oxygen-themes/aMule_Oxygen_Skin.zip)
noextract=(aMule_Oxygen_Skin.zip)
sha256sums=(SKIP
            a340cadb86697acde60b94187fe0134d5262635233870e583a766aa1b8aa06ad)

_confopts=(
  --disable-debug
  --disable-rpath
  --enable-alcc
  --enable-amule-daemon
  --enable-amulecmd
  --enable-cas
  --enable-ccache
  --enable-geoip
  --enable-mmap
  --enable-optimize
  --enable-webserver
  --enable-xas
  --mandir=/usr/share/man
  --prefix=/usr
  --with-wx-config=wx-config-2.8
)

pkgver() {
  cd amule

  svnversion
}

build() {
  cd amule

  ./autogen.sh

  ./configure ${_confopts[@]}

  make
}

package() {
  cd amule

  make DESTDIR=$pkgdir install

  install -m644 $srcdir/aMule_Oxygen_Skin.zip $pkgdir/usr/share/amule/skins

  install -m644 *.txt docs/{*.dia,AUTHORS,COPYING,README.*} README* $pkgdir/usr/share/doc/amule
}
