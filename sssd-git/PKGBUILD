# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=sssd-git
pkgver=20111204
pkgrel=2
pkgdesc="System Security Services Daemon"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/sssd/"
license=('GPL3')
depends=(
  'bind'          # for nsupdate
  'c-ares'
  'dbus-core'
  'ding-libs'
  'krb5'
  'ldb'
  'libldap'
  'libnl'
  'libsasl'
  'libunistring'
  'libxml2'
  'nspr'
  'nss'
  'pcre'
  'talloc'
  'tdb'
  'tevent'
)
makedepends=(
  'docbook-xsl'
  'doxygen'
  'git'
)
provides=('sssd')
conflicts=('sssd')
source=('sssd.service'
        'sssd.rcd')
sha1sums=('10945fa294c5ace62f2dcc7db6091710b46d6ffe'
          'e40e22cecc2cd6d842a040e3266554bff17ba079')

_gitroot="git://git.fedorahosted.org/git/sssd.git"
_gitname="sssd"

build() {
  cd "$srcdir"
  msg "Updating from $_gitroot"

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
  else
    git clone $_gitroot $_gitname
  fi

  msg "Starting make"

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  autoreconf -i -f
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-pammoddir=/lib/security \
    --with-os=fedora \
    --with-pid-path=/run \
    --without-python-bindings \
    --without-selinux \
    --without-semanage \
    --with-initscript=systemd \
    --with-systemdunitdir=/lib/systemd/system
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/etc/rc.d"
  rm -f "$pkgdir/usr/modules/ldb/memberof.la"
  # The included unit requires way too much sed'ing.
  install -Dm0644 "$srcdir/sssd.service" "$pkgdir/lib/systemd/system/sssd.service"
  install -Dm0755 "$srcdir/sssd.rcd" "$pkgdir/etc/rc.d/sssd"
} 

# vim: ts=2:sw=2:et
