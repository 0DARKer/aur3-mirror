# Maintainer: Mantas MikulÄ—nas <grawity@gmail.com>
pkgname=sssd-git
pkgver=1.11.0.r488.g034ffb3
pkgrel=1
pkgdesc="System Security Services Daemon"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/sssd/"
license=('GPL3')
depends=(
  'bind'          # for nsupdate
  'c-ares'
  'cyrus-sasl-gssapi'   # for LDAP auth, especially Active Directory
                        # it *could* be an optdepend, but I like gssapi so whatever :3
  'dbus-core'
  'ding-libs'
  'krb5'
  'ldb'
  'libldap'
  'libnl'
  'libsasl'
  'libunistring'
  'libxml2'
  'nspr'
  'nss'
  'pcre'
  'smbclient'     # for libndr-nbt
  'talloc'
  'tdb'
  'tevent'
)
makedepends=(
  'docbook-xsl'
  'doxygen'
  'git'
  'python2'
  'samba'         # for libndr-nbt headers
)
provides=("sssd=$pkgver")
conflicts=('sssd')
source=('git://git.fedorahosted.org/git/sssd.git'
        'sssd.service')
sha1sums=('SKIP'
          '2d10db3d6f54a58bbf8d1f27328e68555256e0ce')

pkgver() {
  cd "$srcdir/sssd"
  git describe | sed 's/^sssd-//; s/-/.r/; s/[_-]/./g'
}

prepare() {
  cd "$srcdir/sssd"
  sed -i '1s/\<python\>/&2/' src/sbus/sbus_codegen
}

build() {
  cd "$srcdir/sssd"
  export PYTHON=/usr/bin/python2
  autoreconf -i -f
  ./configure \
    --prefix=/usr                                 \
    --sbindir=/usr/bin                            \
    --sysconfdir=/etc                             \
    --localstatedir=/var                          \
    --libexecdir=/usr/lib/sssd                    \
    --datadir=/usr/share                          \
    --enable-all-experimental-features            \
    --enable-pammoddir=/usr/lib/security          \
    --with-autofs                                 \
    --with-initscript=systemd                     \
    --with-os=fedora                              \
    --with-pid-path=/run                          \
    --with-python-bindings                        \
    --without-selinux                             \
    --without-semanage                            \
    --with-ssh                                    \
    --with-sudo                                   \
    --with-syslog=journald                        \
    --with-systemdunitdir=/usr/lib/systemd/system \
    ;
  make
}

package() {
  cd "$srcdir/sssd"
  make DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir/etc/rc.d"
  rm -rf "$pkgdir/lib"
  rm -rf "$pkgdir/run"
  rm -f "$pkgdir/usr/lib/ldb/modules/ldb/memberof.la"
  find "$pkgdir/usr" -depth -type d \
    -exec rmdir --ignore-fail-on-non-empty {} \;

  cd "$srcdir"
  rm -rf "$pkgdir/etc/systemd" # remove the drop-in
  install -Dm0644 sssd.service  "$pkgdir/usr/lib/systemd/system/sssd.service"
} 

# vim: ts=2:sw=2:et
