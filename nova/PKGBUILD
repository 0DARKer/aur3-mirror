# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=nova
pkgver=2013.1.g3
pkgrel=1
_clientname=python-novaclient
_clientver=2.11.1
pkgdesc="OpenStack cloud computing fabric controller"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(glance python2-amqplib python2-babel python2-carrot python2-cheetah python2-daemon
    python2-feedparser python2-gflags python2-lockfile python2-netaddr python2-paramiko
    python2-prettytable python2-suds)
makedepends=(python2-distribute)
source=($url/grizzly/grizzly-${pkgver#*g}/+download/$pkgname-$pkgver.tar.gz
    http://pypi.python.org/packages/source/${_clientname:0:1}/$_clientname/$_clientname-$_clientver.tar.gz)
sha256sums=('e00cddaf458868a50c78742ecc35619f7cb1167836eb2874c67831f83fc926a7'
    '16da7dd327ffa2f87ca62015e4b0ccbbcf6d747c2c9238f72fc3b4cb60075687')
sha512sums=('b6d3e63fba3b4ca96376bd00b6ae2159f8cb7cfedde22316ae6b891d0341a5282b60203597e39655210c93d9edcc7dbd0261b373ddedf8ef300bd7d6712b04fa'
    'bfccef01a5ffd612af759578fc768eb9f37e72ec7d6cc8df790fdd4903cfde50fe87cda4584ad13764e960577b10673750a3da4ba5affcb695b6ac08e05445d7')

build() {
    find -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;

    cd $_clientname-$_clientver/
    python2 setup.py build

    cd ../$pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $_clientname-$_clientver/
    python2 setup.py install --root="$pkgdir"

    cd ../$pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    mv "$pkgdir"/usr/{,share/}novaclient/
}
