# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=nova
pkgver=2013.1.g3
pkgrel=2
pkgdesc="OpenStack cloud computing fabric controller"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(glance python2-amqplib python2-babel python2-carrot python2-cheetah python2-daemon
    python2-feedparser python2-gflags python2-lockfile python2-netaddr python2-novaclient
    python2-paramiko python2-prettytable python2-suds)
makedepends=(python2-distribute)
source=($url/grizzly/grizzly-${pkgver#*g}/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('e00cddaf458868a50c78742ecc35619f7cb1167836eb2874c67831f83fc926a7')
sha512sums=('b6d3e63fba3b4ca96376bd00b6ae2159f8cb7cfedde22316ae6b891d0341a5282b60203597e39655210c93d9edcc7dbd0261b373ddedf8ef300bd7d6712b04fa')

build() {
    cd $pkgname-$pkgver/
    find -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
