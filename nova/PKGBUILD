# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=nova
_relname=juno
pkgver=2014.2.b1
pkgrel=1
pkgdesc="OpenStack cloud computing fabric controller"
arch=(any)
url=https://launchpad.net/nova
license=(custom)
depends=(glance python2-amqplib python2-babel python2-carrot python2-cheetah python2-daemon
    python2-feedparser python2-gflags python2-lockfile python2-novaclient python2-paramiko
    python2-suds)
makedepends=(python2-setuptools)
source=($url/$_relname/$_relname-1/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('a4018f3ea576eb75662045420bf253018261259b507746082293aa77524e0411')
sha512sums=('0c8bb93a6f3c5d149753db352c78d79d485e151d7b3630c40505963823995334ec86dcd13f7e4065f5efada5b6435797415c5b4046eca41a7f9a3ff78176a8af')

prepare() {
    find $pkgname-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
