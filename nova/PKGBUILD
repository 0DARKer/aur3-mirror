# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=nova
pkgver=2012.2.1
pkgrel=1
_clientname=python-novaclient
_clientver=2.9.0
pkgdesc="OpenStack cloud computing fabric controller"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(glance python2-amqplib python2-babel python2-carrot python2-cheetah python2-daemon
    python2-feedparser python2-gflags python2-lockfile python2-netaddr python2-paramiko
    python2-prettytable python2-suds)
makedepends=(python2-distribute)
source=($url/folsom/$pkgver/+download/$pkgname-$pkgver.tar.gz
    http://pypi.python.org/packages/source/${_clientname:0:1}/$_clientname/$_clientname-$_clientver.tar.gz)
sha256sums=('ed5eb62d0268a132c100f41ce6b205fcbd43d124ea618cba8306faa26134c1d5'
    '007e4ec881f6918e7e0763b2cc24fcc6a8bb56212db2ae640b1d0d64f34a25f8')
sha512sums=('a0670059710aefdb5060d2db350853b9bb7aa85d0f3aefc28ebb99c649bd88ec1ad12b03f4d2f45f622ffd3f2103427b898f72e276c436eadcc96d558630429d'
    '01b5638e28b06dad3cbfaeecff4297c907d8219e86dbbc877f4971cc7f36a374a3e13bcfb357011d2ded479131ece472d5812dd7bb92da04ed2a52ac0cac657b')

build() {
    find "$srcdir" -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;

    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py build

    cd ../$pkgname-$pkgver
    python2 setup.py build
}

package() {
    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py install --root="$pkgdir"

    cd "$srcdir"/$pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
