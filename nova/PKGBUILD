# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=nova
_relname=icehouse
pkgver=2014.1.b2
pkgrel=1
pkgdesc="OpenStack cloud computing fabric controller"
arch=(any)
url=https://launchpad.net/nova
license=(custom)
depends=(glance python2-amqplib python2-babel python2-carrot python2-cheetah python2-daemon
    python2-feedparser python2-gflags python2-lockfile python2-novaclient python2-paramiko
    python2-suds)
makedepends=(python2-setuptools)
source=($url/$_relname/$_relname-2/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('1d27063097ba5eebc7ae888af04a175e9bb7643b9d138dcb7f10a046d40f5919')
sha512sums=('42efdcbb184e60558b00e9805cddc854479415768fdaca350ede829fab6ffa3ccdb0576241c967e63f360594840a46665fd8ee5c8252154e14f8f0d7fccf8ce6')

prepare() {
    find $pkgname-$pkgver -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd $pkgname-$pkgver/
    python2 setup.py build
}

package() {
    cd $pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
