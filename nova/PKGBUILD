# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Josh Chase <jcjoshuachase@gmail.com>

pkgname=nova
pkgver=2013.1~g2
pkgrel=2
_clientname=python-novaclient
_clientver=2.10.0
pkgdesc="OpenStack cloud computing fabric controller"
arch=(any)
url=https://launchpad.net/$pkgname
license=(custom)
depends=(glance python2-amqplib python2-babel python2-carrot python2-cheetah python2-daemon
    python2-feedparser python2-gflags python2-lockfile python2-netaddr python2-paramiko
    python2-prettytable python2-suds)
makedepends=(python2-distribute)
source=($url/grizzly/grizzly-2/+download/$pkgname-$pkgver.tar.gz
    http://pypi.python.org/packages/source/${_clientname:0:1}/$_clientname/$_clientname-$_clientver.tar.gz)
sha256sums=('0272570adbebd1cdc328c9fce50fc560503f8470ef478aad53dc6b8cc1275ebe'
    '0e67598b514972b7481dd088ed8de14da57f09d316868afc3d60fc444665ee46')
sha512sums=('66c68f01ada6138e1ce75c886e5fcb64794e51dd76b3a856727f23c66d5df0c18a4c4e5e1b4d9faf0991e53c462fe32c74c7c977835785bccdf0505039e026ae'
    'baf5946a100844484b2e1992009c2a0b2566e678fc03ddde809d18847f31d206097ed8e5666d3b5e3b0035e6b0dcedc637660eaebb342e42c671dc508cd352a9')

build() {
    find "$srcdir" -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;

    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py build

    cd ../$pkgname-$pkgver
    python2 setup.py build
}

package() {
    cd "$srcdir"/$_clientname-$_clientver/
    python2 setup.py install --root="$pkgdir"

    cd "$srcdir"/$pkgname-$pkgver/
    python2 setup.py install --root="$pkgdir" --optimize=1

    install -d "$pkgdir"/etc/
    cp -r etc/ "$pkgdir"/etc/$pkgname/
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
