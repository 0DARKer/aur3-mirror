# Maintainer: Ivan Shapovalov <intelfx100@gmail.com>

pkgname=filter_audio-git
pkgver=r10.4668f58
pkgrel=2
pkgdesc="An easy to use audio filtering library made from webrtc code"
arch=('i686' 'x86_64')
url="https://github.com/irungentoo/filter_audio"
license=('custom:BSD')
makedepends=('git')
provides=("filter_audio")
conflicts=("filter_audio")
source=("git://github.com/irungentoo/filter_audio.git"
        'Makefile'
		'COPYING')
sha512sums=('SKIP'
            'c05ec200025f259f5ca60974339b3bba19b5d3612b1fb1fed7b7cdca26bb6dd17de63ce0b63f148af52ae391079b6b4575811ec5b6c4484ab509be878d93256a'
            '2e1adab66b8b286d6ac5a373808e3f2e3a00c981bbede4bda16576d091e7df306b92aa24a8f759f53e0efcf12ca86d46e8d02845fd83d763c30f6153ee91ef23')

pkgver() {
    cd filter_audio
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd filter_audio
	make -f "$srcdir/Makefile"
}

package() {
    cd filter_audio
    make -f "$srcdir/Makefile" DESTDIR="$pkgdir" install
    install -Dm644 "$srcdir/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
