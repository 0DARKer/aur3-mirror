# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubikey-personalization
pkgver=1.16.0
pkgrel=4
pkgdesc='Yubico YubiKey Personalization library and tool'
arch=('i686' 'x86_64')
url='https://github.com/Yubico/yubikey-personalization'
license=('BSD')
depends=('libusb' 'yubico-c-client')
makedepends=('git')
provides=('ykpers')
conflicts=('ykpers' 'yubikey-personalization-git')
source=("git://github.com/Yubico/yubikey-personalization.git#tag=v${pkgver}"
	'product_ids.patch')

prepare() {
	cd yubikey-personalization/

	patch -Np1 < ${srcdir}/product_ids.patch
}

build() {
	cd yubikey-personalization/

	autoreconf -fi
	./configure --prefix=/usr --with-udevrulesdir=/usr/lib/udev/rules.d/
	make
}

check() {
	cd yubikey-personalization/

	make check
}

package() {
	cd yubikey-personalization/

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/yubikey-personalization/COPYING"
	install -D -m0644 README "${pkgdir}/usr/share/doc/yubikey-personalization/README"
	make DESTDIR="${pkgdir}/" install
}

sha256sums=('SKIP'
            '0ba4fa9769152168acb5dcd83fc41972323006263a205853eea4d4cc975e2079')
