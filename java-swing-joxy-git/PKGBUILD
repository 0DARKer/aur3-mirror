# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=java-swing-joxy-git
pkgver=20121124
pkgrel=1
pkgdesc="Java Swing Look and Feel theme integrated with KDE Oxygen"
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/joxy/"
license=('GPL3')
depends=('java-runtime' 'qt')
makedepends=('git' 'java-environment')
source=(remove-JAVA_HOME.diff)
md5sums=('df79f1fc2d827bdcba3728a54b891035')

_gitroot=git://git.code.sf.net/p/joxy/code
_gitname=joxy

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # source profile to make sure the $JAVA_HOME is defined
  if [ -f /etc/profile/openjdk6.sh ] ; then
    . /etc/profile/openjdk6.sh
  else
    . /etc/profile.d/jdk.sh
  fi

  # patch the compile script so it uses our JAVA_HOME
  git apply "$srcdir/remove-JAVA_HOME.diff" | true

  cd src

  # compile the java sources
  find joxy/ -name '*.java' -exec javac -bootclasspath "$JAVA_HOME/jre/lib/rt.jar" '{}' '+'
  # compile the libjoxy.so
  ./compile.sh

  # create a jar file
  find joxy/ -name '*.class' -exec jar cf joxy-$pkgver.jar '{}' '+'
}

package() {
  cd "$srcdir/$_gitname-build/src"

  install -Dm644 joxy-$pkgver.jar "$pkgdir/$JAVA_HOME/jre/lib/ext/joxy-$pkgver.jar"
  [ $CARCH == 'i686' ] && _arch=i386
  [ $CARCH == 'x86_64' ] && _arch=amd64
  install -Dm644 libjoxy.so "$pkgdir/$JAVA_HOME/jre/lib/$_arch/libjoxy.so"
}

# vim:set ts=2 sw=2 et:
