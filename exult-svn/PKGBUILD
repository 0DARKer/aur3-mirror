# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=exult-svn
pkgver=7386
pkgrel=1
pkgdesc="A recreation of the Ultima 7 engine"
arch=('i686' 'x86_64')
url="http://exult.sourceforge.net/"
license=('GPL2')
depends=('sdl_mixer' 'alsa-lib' 'gcc-libs' 'libxft')
optdepends=('libpng' 'timidity++')
makedepends=('subversion')
provides=('exult')
conflicts=('exult' 'exult-cvs')
replaces=('exult-cvs')
source=('exult::svn+https://exult.svn.sourceforge.net/svnroot/exult/exult/trunk' \
        http://downloads.sourceforge.net/exult/exult_audio.zip \
        add_cstring.diff)
md5sums=('SKIP'
         '1905328f00bbbfee33812c1942bc7880'
         '240f885762211923bf9ec4ab88cdf9ff')

pkgver() {
  cd "$srcdir/exult"
  svnversion | tr -d [A-z]
}

build() {
  cd "$srcdir/exult"
  ./autogen.sh

  patch -Np0 < "$srcdir/add_cstring.diff" || true

  LIBS="-lXft -lX11" ./configure --prefix=/usr --enable-shared --disable-static \
    --with-timidity="/etc/timidity++/timidity.cfg"
  make
}

package() {
  cd "$srcdir/exult"
  make DESTDIR="$pkgdir" install

  # Install audio files
  install -d -m755 "$pkgdir"/usr/share/exult/music
  install "$srcdir"/*.flx "$pkgdir"/usr/share/exult/
  install "$srcdir"/*.txt "$pkgdir"/usr/share/exult/
  install "$srcdir"/music/*.ogg "$pkgdir"/usr/share/exult/music
}
