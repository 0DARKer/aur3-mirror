# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=exult-svn
pkgver=7106
pkgrel=1
pkgdesc="A recreation of the Ultima 7 engine"
arch=('i686' 'x86_64')
url="http://exult.sourceforge.net/"
license=('GPL2')
depends=('sdl_mixer' 'alsa-lib' 'gcc-libs' 'libxft')
optdepends=('libpng' 'timidity++')
makedepends=('subversion')
provides=('exult')
conflicts=('exult' 'exult-cvs')
replaces=('exult-cvs')
source=(http://downloads.sourceforge.net/exult/exult_audio.zip)
md5sums=('1905328f00bbbfee33812c1942bc7880')

_svntrunk="https://exult.svn.sourceforge.net/svnroot/exult/exult/trunk"
_svnmod="exult"

build() {
  cd "$srcdir"
  msg "Connecting to $_svnmod.sourceforge.net SVN server...."
  if [ -d $_svnmod/.svn ]; then
  	cd $_svnmod && svn up -r $pkgver
  else
  	svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi
  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir"/$_svnmod-build ]; then
  	rm -rf "$srcdir"/$_svnmod-build
  fi

  cp -r "$srcdir"/$_svnmod "$srcdir"/$_svnmod-build
  cd "$srcdir"/$_svnmod-build
  ./autogen.sh

  LIBS="-lXft -lX11" ./configure --prefix=/usr --enable-shared --disable-static \
    --with-timidity="/etc/timidity++/timidity.cfg"
  make
}

package() {
  cd "$srcdir"/$_svnmod-build
  make DESTDIR="$pkgdir" install

  # Install audio files
  install -d -m755 "$pkgdir"/usr/share/exult/music
  install "$srcdir"/*.flx "$pkgdir"/usr/share/exult/
  install "$srcdir"/*.txt "$pkgdir"/usr/share/exult/
  install "$srcdir"/music/*.ogg "$pkgdir"/usr/share/exult/music
}
