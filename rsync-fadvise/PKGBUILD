# $Id: PKGBUILD 149915 2012-02-11 20:09:00Z ibiru $
# Maintainer: Francesc Ortiz <francescortiz@gmail.com> 
pkgname=rsync-fadvise
_pkgname=rsync
pkgver=3.0.9
pkgrel=2
patchver=$pkgver
patchrel=2
pkgdesc="patched version of rsync that prevents it from using disk cache through parameter --drop-cache + slowdown patch (allow to specify a delay between links and sorts file operation based on inode)"
arch=('i686' 'x86_64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=('acl')
conflicts=('rsync')
provides=('rsync')
backup=('etc/rsyncd.conf' 'etc/xinetd.d/rsync')
changelog=ChangeLog
source=(http://rsync.samba.org/ftp/rsync/$_pkgname-$pkgver.tar.gz \
        http://tobi.oetiker.ch/patches/rsync-$patchver-$patchrel-fadvise.patch \
	http://tobi.oetiker.ch/patches/rsync-$patchver-slowdown.patch \
        rsyncd.conf rsyncd rsync.xinetd)

md5sums=('5ee72266fe2c1822333c407e1761b92b'
         '4221c286d9a9a815f1e7b545d2ebb540'
         '8d83828654895ef814668b79a7a6540e'
         '4395c0591638349b1a7aeaaa4da1f03a'
         '7a9ce3b5de97f3aae29b906f93e1d157'
         'ea3e9277dc908bc51f9eddc0f6b935c1')


build() {
	cd "$srcdir/$_pkgname-$pkgver"
	patch < $srcdir/rsync-$patchver-$patchrel-fadvise.patch || return 1
	patch < $srcdir/rsync-$patchver-slowdown.patch || return 1
	./prepare-source
	./configure --prefix=/usr --with-included-popt \
              --enable-acl-support --enable-xattr-support
	make
}

package() {
	cd "$srcdir/$_pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm755 ../rsyncd "$pkgdir/etc/rc.d/rsyncd"
	install -Dm644 ../rsyncd.conf "$pkgdir/etc/rsyncd.conf"
	install -Dm644 ../rsync.xinetd "$pkgdir/etc/xinetd.d/rsync"
	install -Dm755 support/rrsync "$pkgdir/usr/lib/rsync/rrsync"
}
