# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: kevin <kevin@archlinux.org>
# TU: Kritoke <kritoke@gamebox.net>
# Contributor: Luca Roccia <little_rock@users.sourceforge.net>
 
pkgname=boost1.38
_pkgname=boost
pkgver=1.38.0
_boostver=1_38_0
pkgrel=1
pkgdesc="Boost provides free peer-reviewed portable C++ source libraries."
arch=(i686 x86_64)
url="http://www.boost.org/"
depends=('python>=2.5' 'bzip2' 'zlib')
conflicts=('boost')
provides=('boost')
source=(http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}_${_boostver}.tar.gz)
license=('custom')
 
build() {
  cd ${srcdir}/${_pkgname}_${_boostver}
  export CFLAGS="${CFLAGS} -fno-strict-aliasing"
  #patch -Np1 -i ${srcdir}/0001-date_time-gcc-4.3-fix.patch || return 1

  # build bjam
  cd ${srcdir}/${_pkgname}_${_boostver}/tools/jam/src
  ./build.sh cc || return 1

  _bindir="bin.linuxx86"
  [ "${CARCH}" = "x86_64" ] && _bindir="bin.linuxx86_64"

  install -m755 -d ${pkgdir}/usr/bin
  install -m755 ${_bindir}/bjam ${pkgdir}/usr/bin/bjam || return 1
 
  # build bcp
  cd ${srcdir}/${_pkgname}_${_boostver}/tools/bcp
  ../jam/src/${_bindir}/bjam || return 1
  install -m755 ${srcdir}/${_pkgname}_${_boostver}/dist/bin/bcp \
                ${pkgdir}/usr/bin/bcp || return 1

  # build libs
  cd ${srcdir}/${_pkgname}_${_boostver}
  # default "debug release <runtime-link>static/dynamic <threading>single/multi"
  # --layout=system removes the -gcc suffix from libraries and installs
  # includes in /usr/include/boost.
  ./tools/jam/src/${_bindir}/bjam \
            release debug-symbols=off threading=single,multi \
			runtime-link=shared link=shared,static \
            --prefix=${pkgdir}/usr \
            -sPYTHON_ROOT=/usr \
            -sPYTHON_VERSION=2.5 \
            -sTOOLS=gcc \
            --layout=system \
            install || return 1

  # build pyste
  cd ${srcdir}/${_pkgname}_${_boostver}/libs/python/pyste/install
  python setup.py install --root=${pkgdir} || return 1

  # license
  install -m755 -d ${pkgdir}/usr/share/licenses/${_pkgname}
  install -m644 ${srcdir}/${_pkgname}_${_boostver}/LICENSE_1_0.txt \
                ${pkgdir}/usr/share/licenses/${_pkgname}/ || return 1
}
md5sums=('a4fc691c4e7c49b3b9b54a667d8f5529')

