# Submitter: George Eleftheriou <eleftg>
# Maintainer: George Eleftheriou <eleftg>
 
pkgname=su2
pkgver=3.2.8
pkgrel=1
pkgdesc="open-source collection of software tools written in C++ for performing Partial Differential Equation (PDE) analysis and solving PDE-constrained optimization problems"
url="http://su2.stanford.edu/index.html"
license=('LGPL')
depends=('lapack' 'jsoncpp' 'libcgns-paraview' 'zlib' 'openmpi' 'bzip2'
         'hdf5-openmpi')
makedepends=('git')
arch=('i686' 'x86_64')
source=("git+https://github.com/su2code/SU2#tag=v3.2.8"
        "configure.ac.patch")
sha256sums=('SKIP'
            '41aafd425ba9345a58b8086fa582dd1c9ef2f3f6d7f88255786accb02c67194a')

prepare() {
  cd ${srcdir}/SU2
  patch < ${startdir}/configure.ac.patch
  autoreconf -i
  echo "export PATH=\${PATH}:/opt/${pkgname}/bin" > ${startdir}/su2_env.sh
}

build() {
  cd ${srcdir}/SU2

  ./configure --prefix=/opt/${pkgname} --with-MPI=/usr/bin/mpicxx \
  --with-CGNS-lib=/usr/lib --with-CGNS-include=/usr/include \
  --with-Jsoncpp-lib=/usr/lib --with-Jsoncpp-include=/usr/include \
  --with-LAPACK-lib=/usr/lib --with-LAPACK-include=/usr/include LIBS="-lhdf5"
 
  make
}

package() {
  cd ${srcdir}/SU2
  make prefix=${pkgdir}/opt/${pkgname} install
  install -D -m755 ${startdir}/su2_env.sh ${pkgdir}/etc/profile.d/su2_env.sh
}
