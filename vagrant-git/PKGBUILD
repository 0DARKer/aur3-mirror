# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>

_pkgname=vagrant
pkgname=vagrant-git
pkgver=1.5.1.14.g8770f9d
pkgrel=2
pkgdesc="Build and distribute virtualized development environments"
arch=('i686' 'x86_64')
url="http://vagrantup.com"
license=('MIT')
conflicts=('vagrant')
options=('!emptydirs')
makedepends=('git')
depends=('ruby' 'curl' 'lzo2' 'libidn' 'rtmpdump')
source=(git://github.com/mitchellh/$_pkgname.git
        git://github.com/mitchellh/$_pkgname-installers.git
        http://pkgbuild.com/~jsteel/aur/$_pkgname/substrate_archlinux_$CARCH.zip)
md5sums=('SKIP' 'SKIP')
[[ $CARCH == i686 ]] && md5sums[2]='c04042b24de56d18a041bc005e5ecaa4'
[[ $CARCH == x86_64 ]] && md5sums[2]='53d8a137e0b50fee8509a7d1c15fd33b'

pkgver() {
  cd "$srcdir"/$_pkgname

  git describe --tags --long | sed 's/-/./g;s/^v//'
}

build() {
  cd "$srcdir"/$_pkgname
    REV=$( git log -n 1 --pretty=format:"%H" )

  "$srcdir"/$_pkgname-installers/package/support/install_$_pkgname.sh \
    "$srcdir"/substrate/ $REV "$srcdir"/substrate/${_pkgname}_version
}

package() {
  install -d "$pkgdir"/{opt/,usr/bin/}

  cp -r "$srcdir"/substrate/ "$pkgdir"/opt/$_pkgname/

  ln -s /opt/$_pkgname/bin/$_pkgname "$pkgdir"/usr/bin/$_pkgname
}
