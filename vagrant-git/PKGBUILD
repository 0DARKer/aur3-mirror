# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>

_gemname=vagrant
pkgname=vagrant-git
pkgver=1.4.3.699.gfa53774
pkgrel=5
pkgdesc="Build and distribute virtualized development environments"
arch=('any')
url="http://vagrantup.com"
license=('MIT')
depends=('ruby' 'ruby-childprocess' 'ruby-erubis' 'ruby-i18n' 
         'ruby-log4r' 'ruby-net-ssh-2.7' 'ruby-net-scp' 'ruby-wdm'
         'ruby-archive-tar-minitar' 'ruby-json-1.7' 'ruby-bundler'
         'ruby-listen-2.4' 'ruby-timers-1.1' 'ruby-rb-kqueue')
options=('!emptydirs')
makedepends=('git')
source=('git://github.com/mitchellh/vagrant.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir"/vagrant

  git describe --tags --long | sed 's/-/./g;s/^v//'
}

build() {
  cd "$srcdir"/vagrant

  gem build vagrant.gemspec
}

package() {
  cd "$srcdir"/vagrant

  local _gemver=$( "$srcdir"/$_gemname/bin/vagrant -v | sed 's/Vagrant //' )
  local _gemdir="$( ruby -rubygems -e'puts Gem.default_dir' )"

  gem install --no-user-install --ignore-dependencies \
    -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin $_gemname-$_gemver.gem

  rm "$pkgdir/$_gemdir"/cache/$_gemname-$_gemver.gem

  install -Dm644 "$pkgdir"/$_gemdir/gems/$_gemname-$_gemver/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # suppress warning about using official packages
  sed -i '0,/^$/s/^$/\nENV\["VAGRANT_INSTALLER_ENV"\] \= "1"\n/' \
    "$pkgdir"/usr/bin/vagrant
}
