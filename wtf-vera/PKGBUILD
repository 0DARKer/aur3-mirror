pkgname=wtf-vera
pkgver=7.6
pkgrel=1
pkgdesc="V.E.R.A. --- Virtual Entity of Relevant Acronyms (wtf acronymdb format)"
arch=(any)
url="http://home.snafu.de/ohei/vera/vueber-e.html"
license=('FDL')
depends=('wtf')
source=(http://home.snafu.de/ohei/FTP/vera${pkgver//./}.zip
        vera.patch)
md5sums=('6e7759cde9529e5a8b16f3bbb9cdb6ff'
         'c3d7a181e8dfe7ce3faa3aedd46dc9dc')

build() {
  patch -Np1 -i vera.patch

  iconv -f ISO8859-1 -t UTF-8 vera.1 | tr -d '\r' |
  LC_CTYPE=C awk '
  BEGIN {
    len = 13 # fixed length of the left column
  }
  !/^[^[:space:]]+: |^[*%[:space:]]|^$/ {
    a = toupper(substr($0, 1, len)) # wtf requires acronyms to be uppercase
    sub(/[[:space:]]+$/, "", a)
    printf("%s\t%s\n", a, substr($0, len+1))
  }' > vera
}

package() {
  install -D -m 644 vera "$pkgdir"/usr/share/misc/acronyms.vera
}
