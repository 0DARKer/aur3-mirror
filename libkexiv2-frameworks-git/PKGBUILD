# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libkexiv2-frameworks-git
pkgver=r728.5e982dd
pkgrel=1
pkgdesc='A wrapper around Exiv2 library to manipulate pictures metadata as EXIF IPTC and XMP. KF5 Frameworks branch. (GIT version)'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdegraphics/libs/libkexiv2'
license=('LGPL')
depends=('ktextwidgets' 'exiv2')
makedepends=('git' 'extra-cmake-modules' 'kdoctools')
source=('git://anongit.kde.org/libkexiv2#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd libkexiv2
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libkexiv2 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}