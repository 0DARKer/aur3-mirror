# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libkexiv2-frameworks-git
pkgver=r685.298cd82
pkgrel=1
pkgdesc='A wrapper around Exiv2 library to manipulate pictures metadata as EXIF IPTC and XMP. - KF5 frameworks branch. (GIT version)'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdegraphics/libs/libkexiv2'
license=('LGPL')
depends=('kdelibs4support' 'exiv2')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'kdoctools')
conflicts=('libkexiv2')
provides=('libkexiv2')
source=('git://anongit.kde.org/libkexiv2#branch=frameworks')
md5sums=('SKIP')
_gitname=libkexiv2

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_gitname}"
  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}