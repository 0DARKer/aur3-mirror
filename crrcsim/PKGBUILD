# Maintainer: DavidK <david_king at softhome dot net>
# Contributor: Olivier Bordes <package@obordes.com>
pkgname=crrcsim
pkgver=0.9.12
pkgrel=2
pkgdesc="Model airplane flight simulation program."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/crrcsim/"
license=('GPL')
depends=('sdl>=1.2.5' 'portaudio' 'libjpeg' 'glu' 'mesa')
makedepends=('gawk' 'plib>=1.8.4')
optdepends=('cgal: Windfield import')
source=(http://sourceforge.net/projects/crrcsim/files/latest/download/$pkgname-$pkgver.tar.gz)
md5sums=('d6144138c82bff36d55b4517a9bf1a7b')

build() {
  cd $pkgname-$pkgver
  sed -i "s/-lCGAL/\"-lCGAL -lgmp\"/g" ./configure.ac
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:


