# Maintainer: Jeremy Pope <jpope@jpope.org> PGP-Key: E00B4261
pkgname=pumpio-git
pkgver=acf143c
pkgrel=1
pkgdesc="This is pump.io. It's a stream server that does most of what people really want from a social network. (git version)"
url='http://pump.io'
license=('Apache')
arch=('i686' 'x86_64')
depends=('nodejs' 'graphicsmagick')
makedepends=('git')
optdepends=('mongodb' 'redis')
provides=('pumpio')
options=(!strip)
install=${pkgname}.install
backup=('etc/webapps/pumpio/pump.io.json' 'usr/lib/systemd/system/pumpio.service')
source=('git://github.com/e14n/pump.io.git'
        'pumpio.service'
        "$pkgname.install"
        'pump.io.json')

_pkgname=pumpio
_gitname='pump.io'

pkgver() {
  cd $srcdir/$_gitname
  git describe --always | sed -e 's|-|.|g' -e 's|^v||'
}

build() {
  cd $srcdir/$_gitname
  npm install
  npm install databank-mongodb
  npm install databank-redis

  rm -rf $srcdir/$_gitname/.git*
}

package() {
  mkdir -p $pkgdir/usr/share/webapps/
  mkdir -p $pkgdir/usr/lib/systemd/system/
  mkdir -p $pkgdir/etc/webapps/pumpio/

  cp -r $srcdir/$_gitname $pkgdir/usr/share/webapps/$_pkgname
  cp pump.io.json $pkgdir/etc/webapps/pumpio/pump.io.json
  cp pumpio.service $pkgdir/usr/lib/systemd/system/
}
md5sums=('SKIP'
         '8c550e818a16c05654129fddb185de70'
         '4c2ed1f7bf0c044dbb3b7e77b4e371d9'
         'f20c00aeb6d95a5868cb3edc12eebe05')
