# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gstreamer0.10-bad
pkgname=lib32-${_pkgbase}
pkgver=0.10.22
pkgrel=1
arch=('x86_64')
license=('LGPL' 'GPL')
pkgdesc="GStreamer Multimedia Framework Bad Plugin libraries (gst-plugins-bad) (32 bit)"
depends=('lib32-gstreamer0.10-base>=0.10.34' "${_pkgbase}")
makedepends=('gcc-multilib' 'lib32-libvdpau' 'lib32-libass' 'lib32-libcdaudio' 'lib32-curl' 'lib32-libdc1394' 'lib32-libdvdnav' 'lib32-libmms' 'lib32-liblrdf' 'lib32-libmpcdec' 'lib32-libmodplug' 'lib32-musicbrainz' 'lib32-neon' 'lib32-libofa' 'lib32-librsvg' 'lib32-schroedinger' 'lib32-soundtouch' 'lib32-rtmpdump' 'lib32-bzip2' 'lib32-libdca' 'lib32-faac' 'lib32-faad2' 'lib32-libsndfile') #'xvidcore' 'mjpegtools' 'jasper' 'libvpx' 'gsm' 'libgme'

url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-bad/gst-plugins-bad-${pkgver}.tar.bz2
        mjpeg-2.0.patch)
md5sums=('9a2acee1f386f71247003d0d7090fb1c' '9415bdab8eb9cfba95df37aaf41c1172')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  patch -Np1 -i "${srcdir}/mjpeg-2.0.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Bad Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --libdir=/usr/lib32

  make
  sed -e 's/gst sys ext/gst/' -i Makefile
}

package() {
  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{include,share}
}

