# Contributor: funkyou
# Maintainer:  Tristero <tristero@online.de>

pkgname=retroshare
_pkgver=0.5.1
pkgver=${_pkgver}b
pkgrel=2
pkgdesc="Serverless encrypted instant messenger with filesharing, chatgroups, e-mail."
arch=('i686' 'x86_64')
url="http://retroshare.sourceforge.net/"
license=('LGPL' 'GPL')
depends=('qt' 'openssl' 'libupnp' 'gpgme' 'libgnome-keyring' 'mesa' 'libxss')
install="${pkgname}.install"

source=(http://sourceforge.net/projects/retroshare/files/RetroShare/${pkgver}/RetroShare-v${pkgver}.tar.gz/download \
	retroshare-${_pkgver}-upnp.patch \
        ${pkgname}.desktop)

build() {

  cd ${srcdir}
  patch -N -p0 -i ${startdir}/retroshare-${_pkgver}-upnp.patch

  msg "Compile libbitdht"
  cd ${srcdir}/${_pkgver}/libbitdht/src
  qmake || return 1
  make || return 1

  msg "Compile libretroshare"
  cd ${srcdir}/${_pkgver}/libretroshare/src
  qmake || return 1
  make || return 1

  msg "Compile retroshare-gui"
  cd ${srcdir}/${_pkgver}/retroshare-gui/src
  qmake || return 1
  make || return 1

  # --- Install Files ---

  msg "Install files to fakeroot-environment"

  # - Binaries
  install -D -m 644 \
    "${srcdir}/${_pkgver}/libbitdht/src/lib/libbitdht.a" \
    "${pkgdir}/usr/lib/libbitdht.a"
  install -D -m 644 \
    "${srcdir}/${_pkgver}/libretroshare/src/lib/libretroshare.a" \
    "${pkgdir}/usr/lib/libretroshare.a"
  install -D -m 755 \
    "${srcdir}/${_pkgver}/retroshare-gui/src/RetroShare" \
    "${pkgdir}/usr/bin/${pkgname}"

  if [ "x$_EXPERIMENTAL_VERSION" == "xTRUE" ] ; then
    # - Plugins
    install -D -m 755 \
    "${srcdir}/${_pkgver}/plugins/bin/libcalendar_plugin.so" \
    "${pkgdir}/usr/lib/retroshare/plugins/libcalendar_plugin.so" || return 1
    install -D -m 755 \
    "${srcdir}/${_pkgver}/plugins/bin/libpuzzle_plugin.so" \
    "${pkgdir}/usr/lib/retroshare/plugins/libpuzzle_plugin.so" || return 1
    install -D -m 755 \
    "${srcdir}/${_pkgver}/plugins/bin/libqcheckers_plugin.so" \
    "${pkgdir}/usr/lib/retroshare/plugins/libqcheckers_plugin.so" || return 1
    install -D -m 755 \
    "${srcdir}/${_pkgver}/plugins/bin/libqdiagram_plugin.so" \
    "${pkgdir}/usr/lib/retroshare/plugins/libqdiagram_plugin.so" || return 1
  fi

  # - Icons
  install -D -m 644 \
    "${srcdir}/${_pkgver}/retroshare-gui/src/gui/images/retrosharelogo1.png" \
    "${pkgdir}/usr/share/pixmaps/retroshare_blue.png" || return 1
  install -D -m 644 \
    "${srcdir}/${_pkgver}/retroshare-gui/src/gui/images/retrosharelogo2.png" \
    "${pkgdir}/usr/share/pixmaps/retroshare.png" || return 1

  # - Desktop File
  install -D -m 644 \
    "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop" || return 1
}

md5sums=('f234f5a948670b58de950cee2f3a4d26'
         '407cbcf104e639442e41dca1686f0e7f'
         'f6dc374d95c775bc587ef13841abdab2')
