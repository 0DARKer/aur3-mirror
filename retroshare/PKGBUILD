# Contributor: funkyou
# Maintainer:  Tristero <tristero@online.de>

pkgname=retroshare
_pkgver=0.5.0
pkgver=${_pkgver}g
pkgrel=2
pkgdesc="Serverless encrypted instant messenger with filesharing, chatgroups, e-mail."
arch=('i686' 'x86_64')
url="http://retroshare.sourceforge.net/"
license=('LGPL' 'GPL')
depends=('qt' 'openssl' 'libupnp' 'gpgme')
install="${pkgname}.install"

source=(http://sourceforge.net/projects/retroshare/files/RetroShare/${pkgver}/RetroShare_v${pkgver}.tar.gz/download \
	retroshare-v${pkgver}-arch_compile.patch \
	retroshare-gui_experimental.patch \
        ${pkgname}.desktop)

build() {

  # NOTE Setting this to TRUE activates the compilation of some experimental
  # non-release features of retroshare as the support for blogs, the
  # plugin architecture and some plugins.
  local _EXPERIMENTAL_VERSION=TRUE

  cd ${srcdir}
  patch -N -p0 -i ${startdir}/retroshare-v${pkgver}-arch_compile.patch

  msg "Compile libretroshare"
  cd ${srcdir}/v${_pkgver}/libretroshare/src
  qmake || return 1
  make || return 1

  msg "Compile retroshare-gui"
  cd ${srcdir}/v${_pkgver}/retroshare-gui/src

  if [ "x$_EXPERIMENTAL_VERSION" == "xTRUE" ] ; then
    echo ""
    msg "   ! EXPERIMENTAL VERSION!"
    msg "   ! Unreleased features like plugins, blogs etc. may be unstable!"
    echo ""
    msg "Patch retroshare to use experimental features"
    patch -N -p0 -i ${startdir}/retroshare-gui_experimental.patch || return 1
    msg "Compile retroshare plugins"
    cd ${srcdir}/v${_pkgver}/plugins/
    qmake || return 1
    make || return 1
    cd ${srcdir}/v${_pkgver}/retroshare-gui/src
  fi

  qmake || return 1
  make || return 1

  # --- Install Files ---

  msg "Install files to fakeroot-environment"

  # - Binaries
  install -D -m 644 \
    "${srcdir}/v${_pkgver}/libretroshare/src/lib/libretroshare.a" \
    "${pkgdir}/usr/lib/libretroshare.a"
  install -D -m 755 \
    "${srcdir}/v${_pkgver}/retroshare-gui/src/RetroShare" \
    "${pkgdir}/usr/bin/${pkgname}"

  if [ "x$_EXPERIMENTAL_VERSION" == "xTRUE" ] ; then
    # - Plugins
    install -D -m 755 \
    "${srcdir}/v${_pkgver}/plugins/bin/libcalendar_plugin.so" \
    "${pkgdir}/usr/lib/retroshare/plugins/libcalendar_plugin.so" || return 1
    install -D -m 755 \
    "${srcdir}/v${_pkgver}/plugins/bin/libpuzzle_plugin.so" \
    "${pkgdir}/usr/lib/retroshare/plugins/libpuzzle_plugin.so" || return 1
    install -D -m 755 \
    "${srcdir}/v${_pkgver}/plugins/bin/libqcheckers_plugin.so" \
    "${pkgdir}/usr/lib/retroshare/plugins/libqcheckers_plugin.so" || return 1
    install -D -m 755 \
    "${srcdir}/v${_pkgver}/plugins/bin/libqdiagram_plugin.so" \
    "${pkgdir}/usr/lib/retroshare/plugins/libqdiagram_plugin.so" || return 1
  fi

  # - Icons
  install -D -m 644 \
    "${srcdir}/v${_pkgver}/retroshare-gui/src/gui/images/retrosharelogo1.png" \
    "${pkgdir}/usr/share/pixmaps/retroshare_blue.png" || return 1
  install -D -m 644 \
    "${srcdir}/v${_pkgver}/retroshare-gui/src/gui/images/retrosharelogo2.png" \
    "${pkgdir}/usr/share/pixmaps/retroshare.png" || return 1

  # - Desktop File
  install -D -m 644 \
    "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop" || return 1
}

md5sums=('fa8df4057cc350cb0546362ababc5762'
         '3020c8eb7522342ff56e577efcbe1b1a'
         '704cae892a2f3adf1bf7415acd57d47c'
         'f6dc374d95c775bc587ef13841abdab2')

