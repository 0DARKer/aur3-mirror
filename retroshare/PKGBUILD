# Maintainer: stqn
# Contributor: JHeaton <jheaton at archlinux dot us>
# Contributor: Tristero <tristero at online dot de>
# Contributor: funkyou

# Set this to true to build and install retroshare-nogui
_build_nogui=false

# Set this to true to build and install the plugins
_build_linkscloud=true
_build_voip=true

### Nothing to be changed below this line ###

pkgname=retroshare
pkgver=0.5.4d
pkgrel=2
pkgdesc="Serverless encrypted instant messenger with filesharing, chatgroups, e-mail."
arch=('i686' 'x86_64')
url="http://retroshare.sourceforge.net/"
license=('LGPL' 'GPL')
depends=('qt4' 'libupnp' 'libgnome-keyring' 'libxss')
install="${pkgname}.install"

source=(http://sourceforge.net/projects/retroshare/files/RetroShare/${pkgver}/RetroShare-v${pkgver}.tar.gz \
	${pkgname}.install \
	${pkgname}.desktop)

sha256sums=('2c5bcd1764829abc260d5c9328088a09307c00f0e15c3cc12a08ba3584d549c7'
            '4b50547648612e9091536205402a4da9ddea9c18c0f71e5d6cd30b2226f206d9'
            '70be00968f2477e368f75393f193e76f366fff2dadab869c855e92048060cf29')

# Add missing dependencies if needed
[[ $_build_voip == true ]] && depends=(${depends[@]} 'speex')

# Replace qt4 dependency with qt if qt4 is not yet available in the repos
_qmake='qmake4'
if ! pacman -Si qt4 &>/dev/null ; then
	depends[0]='qt'
	_qmake='qmake'
fi

build() {
	cd "${srcdir}"

	msg "Compiling OpenPGP-SDK..."
	cd "${srcdir}/trunk/openpgpsdk/src"
	$_qmake
	make

	msg "Compiling libbitdht..."
	cd "${srcdir}/trunk/libbitdht/src"
	$_qmake
	make

	msg "Compiling libretroshare..."
	cd "${srcdir}/trunk/libretroshare/src"
	$_qmake
	make

	msg "Compiling retroshare-gui..."
	cd "${srcdir}/trunk/retroshare-gui/src"
	$_qmake
	make

	if [[ "$_build_voip" == "true" ]] ; then
		msg "Compiling VOIP plugin..."
		cd "${srcdir}/trunk/plugins/VOIP"
		sed -i 's/lessThan.*/true {/' VOIP.pro
		$_qmake
		make
	fi

	if [[ "$_build_linkscloud" == "true" ]] ; then
		msg "Compiling LinksCloud plugin..."
		cd "${srcdir}/trunk/plugins/LinksCloud"
		$_qmake
		make
	fi

	if [[ $_build_nogui == "true" ]] ; then
		msg "Compiling retroshare-nogui..."
		cd "${srcdir}/trunk/retroshare-nogui/src"
		$_qmake
		make
	fi
}

package() {

	# --- Install Files ---

	msg "Installing files to fakeroot-environment..."

	install -D -m 755 \
		"${srcdir}/trunk/retroshare-gui/src/RetroShare" \
		"${pkgdir}/usr/bin/${pkgname}"

	if [[ "$_build_nogui" == "true" ]] ; then
		install -D -m 755 \
			"${srcdir}/trunk/retroshare-nogui/src/retroshare-nogui" \
			"${pkgdir}/usr/bin/${pkgname}-nogui"
	fi

	# Plugins
	if [[ "$_build_linkscloud" == "true" ]] ; then
		install -D -m 755 \
			"${srcdir}/trunk/plugins/LinksCloud/libLinksCloud.so" \
			"${pkgdir}/usr/lib/retroshare/extensions/libLinksCloud.so"
	fi
	if [[ "$_build_voip" == "true" ]] ; then
		install -D -m 755 \
			"${srcdir}/trunk/plugins/VOIP/libVOIP.so" \
			"${pkgdir}/usr/lib/retroshare/extensions/libVOIP.so"
	fi

	# Icons
	install -D -m 644 \
		"${srcdir}/trunk/retroshare-gui/src/gui/images/retrosharelogo2.png" \
		"${pkgdir}/usr/share/pixmaps/retroshare.png"

	# Desktop File
	install -D -m 644 \
		"${srcdir}/${pkgname}.desktop" \
		"${pkgdir}/usr/share/applications/${pkgname}.desktop"

	# bdboot (needed to bootstrap the DHT)
	install -D -m 644 \
		"${srcdir}/trunk/libbitdht/src/bitdht/bdboot.txt" \
		"${pkgdir}/usr/share/RetroShare/bdboot.txt"

	# Skins
	cp -r "${srcdir}/trunk/retroshare-gui/src/qss" "${pkgdir}/usr/share/RetroShare/"
	find "${pkgdir}/usr/share/RetroShare/" -depth -type d -name ".svn" -exec rm -r {} \;
}
