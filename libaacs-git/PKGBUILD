# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libaacs-git
pkgver=20120508
pkgrel=1
pkgdesc="Advanced Access Content System (GIT version)"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.videolan.org/developers/libaacs.html"
depends=('libgcrypt')
makedepends=('git' 'flex' 'bison')
options=('!libtool')
provides=('libaacs')
conflicts=('libaacs')

_gitroot="git://git.videolan.org/libaacs.git"
_gitname="libaacs"

build() {
    msg "Connecting to GIT server..."

    if [ -d "${srcdir}"/"${_gitname}" ] ; then
        cd "${_gitname}" && git pull
    else
        git clone --depth=1 "${_gitroot}"
    fi
    
    [ -d "${srcdir}"/"${_gitname}"-build ] && rm -fr "${srcdir}"/"${_gitname}"-build
    cp -R "${srcdir}"/"${_gitname}" "${srcdir}"/"${_gitname}"-build
    
    msg "GIT checkout done or server timeout"
    msg "Starting build..."

    cd "${srcdir}"/"${_gitname}"-build
    ./bootstrap
    ./configure --prefix=/usr
    make
}
package() {
    cd "${srcdir}"/"${_gitname}"-build
    make DESTDIR="${pkgdir}" install
}
