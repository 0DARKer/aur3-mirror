# Maintainer: jason ryan <jasonwryan@gmail.com>
# Contributor: Andrew Kopplin <revrollic@gmail.com>

pkgname=surfraw-git
pkgver=20111102
pkgrel=1
pkgdesc="Shell Users' Revolutionary Front Rage Against the Web"
arch=('any')
url="http://surfraw.alioth.debian.org/"
license=('custom')
depends=('perl')
install=$pkgname.install
provides=('surfraw')
conflicts=('surfraw')
backup=('etc/xdg/surfraw/conf' 'etc/xdg/surfraw/bookmarks')

_gitroot="git://git.debian.org/surfraw/surfraw.git"
_gitname="surfraw"

build() {

  msg "Connecting to GIT server..."
  
  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi
    msg "GIT checkout done or server timeout"
    msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  msg "Creating build directory..."
  cp -R $srcdir/$_gitname $srcdir/$_gitname-build
  cd "$srcdir/$_gitname-build"

  msg "Starting make..."

  ./prebuild 
  ./configure --prefix=/usr --sysconfdir=/etc --disable-opensearch
  make 
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

# license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING 
}
