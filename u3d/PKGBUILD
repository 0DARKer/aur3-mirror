# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: oslik <oslik@mail.ru>

pkgname=u3d
pkgver=1.4.2
pkgrel=2
pkgdesc="Set of libraries to manipulate U3D-formatted data. IDTF to U3D converter."
url="http://sourceforge.net/projects/mathgl/files/U3D%20tools/"
arch=('i686' 'x86_64')
license=('custom:APACHE')
depends=('libpng' 'libjpeg')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/mathgl/U3D%20tools/$pkgname%20$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('5f127279aeb75dc4ab1fd6dca45039d7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # libpng 1.5.x fix
  find . -name CIFXImageTools.cpp -exec \
    sed -e "s/png_ptr->io_ptr = \([^;]*\)/png_init_io(png_ptr, (png_voidp)(\1))/" \
        -e "s/png_ptr->io_ptr/png_get_io_ptr(png_ptr)/g" -i {} \;

  CXXFLAGS+=" -fpermissive" \
  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}