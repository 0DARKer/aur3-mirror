# Maintainer: Jorge Israel Pe√±a <jorge.israel.p@gmail.com>

pkgname=syncplay-git
pkgver=1.2.7rc3.r11.gaf5bba6
pkgrel=1
pkgdesc="synchronize watching movies on mplayer2, vlc, mpv, and mpc-hc on many computers"
arch=('any')
url="http://syncplay.pl/"
license=('custom')
depends=('python2' 'python2-pyside' 'twisted')
makedepends=('git')
conflicts=('syncplay')
source=("$pkgname"::'git://github.com/Uriziel/syncplay.git')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$pkgname"

  for file in syncplayClient.py syncplayServer.py; do
    sed -i 's_#!/usr/bin/env python_&2_' $file
  done
}

package() {
  cd "$srcdir/$pkgname"

  make PREFIX="$pkgdir" install
}
