# Maintainer: Jorge Israel Pe√±a <jorge.israel.p@gmail.com>

pkgname=syncplay-git
pkgver=1.2.9a.r0.g18e424d
pkgrel=1
pkgdesc="synchronize watching movies on mplayer2, vlc, mpv, and mpc-hc on many computers"
arch=('any')
url="http://syncplay.pl/"
license=('custom')
depends=('python2' 'python2-pyside' 'python2-twisted')
makedepends=('git')
provides=('syncplay')
conflicts=('syncplay')
source=("$pkgname"::'git://github.com/Uriziel/syncplay.git'
        syncplay.service)
md5sums=('SKIP'
         '29424eefe02f6f86c71d5846176a36c7')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$pkgname"

  for file in syncplayClient.py syncplayServer.py; do
    sed -i 's_#!/usr/bin/env python_&2_' $file
  done
}

package() {
  cd $srcdir

  # systemd
  install -Dm644 syncplay.service ${pkgdir}/usr/lib/systemd/system/syncplay.service

  cd $pkgname

  # actual program
  make PREFIX="$pkgdir" install

}
