# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Henri Valta <cg at jakorasia dot info>

pkgname=critterding
pkgver=beta13.1
pkgrel=2
pkgdesc='A Petri dish universe in 3D that demonstrates evolving artificial life'
arch=('i686' 'x86_64')
url='http://critterding.sourceforge.net/'
license=('GPL')
depends=('freetype2' 'sdl' 'libgl' 'ftgl' 'boost-libs')
makedepends=('cmake' 'gendesk' 'boost')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/sources/${pkgname}-${pkgver}.tar.bz2")
md5sums=('0969011e13a61a08b1feaf1cc5bf45e3')
_categories=Game

build() {
  cd "${srcdir}"
  gendesk # Generate desktop file 
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="-Ofast" # optional
  cmake . -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Copy desktop file and icon
  install -Dm644 ../${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
