# Mantenedor: Felipe Facundes <felipe.facundes@gmail.com>

# Para portar esta PKGBUILD para outro idioma de tor-browser você 
# tem que mudar $pkgname, $_language, $pkgdesc e $url em PKGBUILD
# E, a primeira linha do arquivo .install.

pkgname='tor-browser-pt'
pkgver='4.0.3'
_realpkgver='4.0.3'
_realpkgver_i686='4.0.3'
_language='pt-PT'
pkgrel=1
pkgdesc='Tor Browser Bundle: Anonimato na internet usando o firefox e a rede tor'
url='https://www.torproject.org/projects/torbrowser.html'
arch=('x86_64' 'i686')
license=('GPL')
depends=('gtk2' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
         'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'icu' 'libevent' 'nss' 'hunspell' 'sqlite')
optdepends=('zenity: simples caixa de diálogo'
			'kdebase-kdialog: KDE caixa de diálogo'
            'gst-plugins-good: h.264 vídeo'
            'gst-libav: h.264 vídeo'
            'libpulse: PulseAudio áudio driver'
			'libnotify: Gnome caixa de diálogo')
install="${pkgname}.install"

# instalação em diferente versões dependendo da arquiterura do seu processador
if [[ "$CARCH" == 'i686' ]]; then
	_realpkgver=${_realpkgver_i686}
	_pkgarch='32'
else
	_pkgarch='64'
fi

source=("https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz"
		"${pkgname}.desktop"
		"${pkgname}.png"
		"${pkgname}.sh")

if [[ "$CARCH" == 'x86_64' ]]; then
   sha256sums=('4c73d2e7ad3cd1a8823eb5dea88f0336df751daff53f58af489a330df9b12761')
else
   sha256sums=('SKIP')
fi
sha256sums+=('b0683d77d6eb183c0c950e7b578197d349e13ce56e64b60330764d96dd6e64a0'
			'17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			'256720e56710737ac2445b0e69eb090be12d1a7343d958dbf7d2d583334848bb')

noextract=("tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz")

package() {
	cd "${srcdir}"

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.sh
	sed -i "s/REPL_VERSION/${_realpkgver}/g"	${pkgname}.sh
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.sh

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.desktop
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.desktop
	sed -i "s/REPL_COMMENT/${pkgdesc}/g"		${pkgname}.desktop

	install -Dm 644 ${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 ${pkgname}.png		${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 ${pkgname}.sh		${pkgdir}/usr/bin/${pkgname}

	install -Dm 644 tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz ${pkgdir}/opt/${pkgname}/tor-browser-linux${_pkgarch}-${_realpkgver}_${_language}.tar.xz
}

# vim:set ts=2 sw=2 et:
