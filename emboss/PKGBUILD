# Maintainer: Michael Schubert <mschu.dev at gmail>
# Contributor: moostik <mooostik_at_gmail.com>
# Contributor: mick elliot <micke@sfu.ca>

pkgname=emboss
_pkgname=EMBOSS
pkgver=6.5.7
pkgrel=1
pkgdesc="A collection of molecular biology applications"
arch=('x86_64' 'i686')
url="http://emboss.sourceforge.net/"
depends=('gd' 'libx11' 'pcre' 'java-runtime' 'libmysqlclient' 'postgresql-libs')
options=('!libtool')
license=('GPL2')
source=("ftp://emboss.open-bio.org/pub/EMBOSS/$_pkgname-$pkgver.tar.gz")
md5sums=('6a2cb3f93d5e9415c74ab0f6b1ede5f0')

build() {
  cd "${srcdir}/${_pkgname}-$pkgver"

  ./configure --prefix=/usr --enable-large --with-x

  sed -i 's|$(bindir)/embossupdate|:|' Makefile
  JAVA_CFLAGS="-I$JAVA_HOME/include -I$JAVA_HOME/include/linux" make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/include/{pcre,pcreposix}.h"
}
