# Maintainer: Hilton Medeiros <medeiros.hilton@gmail.com>

pkgname=oalwrapper-git
pkgver=20120614
pkgrel=1
pkgdesc="A wrapper library for OpenAL. Makes using OpenAL a lot easier."
arch=('i686' 'x86_64')
url="http://github.com/FrictionalGames/OALWrapper"
license=('ZLIB')
depends=('freealut' 'libvorbis' 'sdl')
makedepends=('git' 'cmake')
provides=('oalwrapper')
conflicts=('oalwrapper')
source=(CMakeLists.txt)
md5sums=('f340b05c860da98cab5ac2faf896f101')

_gitroot="git://github.com/FrictionalGames/OALWrapper"
_gitname="OALWrapper"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i '/efx-creative.h/d' include/OALWrapper/OAL_Types.h
  sed -i '1i#include "AL/alut.h"' include/OALWrapper/OAL_WAVSample.h
  rm -fr include/AL
  cp -f ../CMakeLists.txt .

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install
  install -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

