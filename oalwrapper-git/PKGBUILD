# Maintainer: Hilton Medeiros <medeiros.hilton AT gmail DOT com>

pkgname=oalwrapper-git
pkgver=20100527
pkgrel=1
pkgdesc="A wrapper library for OpenAL. Makes using OpenAL a lot easier."
arch=('i686' 'x86_64')
url="http://github.com/FrictionalGames/OALWrapper"
license=('ZLIB')
depends=('freealut' 'libvorbis' 'sdl')
makedepends=('git' 'cmake')
provides=('oalwrapper')
conflicts=('oalwrapper')
source=(CMakeLists.txt)
md5sums=('7cc8b02b9829c62016b1022b107fa22b')

_gitroot="git://github.com/FrictionalGames/OALWrapper"
_gitname="OALWrapper"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i '/efx-creative.h/d' include/OALWrapper/OAL_Types.h
  rm -fr include/AL
  cp -f ../CMakeLists.txt .

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir/" install
  install -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

