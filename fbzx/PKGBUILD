# Contributor: rabyte <rabyte*gmail>
# Maintainer: Sausageandeggs <s_stoakley at hotmail.co.uk>
pkgname=fbzx
pkgver=2.5.0
pkgrel=1
pkgdesc="A Sinclair ZX Spectrum Emulator, designed to work both in framebuffer and X"
arch=('i686' 'x86_64')
url="http://www.rastersoft.com/fbzx.html"
license=('GPL3' 'custom')
depends=('sdl' 'alsa-lib' 'pulseaudio')
source=(http://www.rastersoft.com/descargas/$pkgname-$pkgver.tar.bz2)

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 fbzx $pkgdir/usr/bin/fbzx
  mkdir -p $pkgdir/usr/share/{fbzx,applications,pixmaps,doc/fbzx}
  cp -rf spectrum-roms/ $pkgdir/usr/share/
  install -m644 keymap.bmp $pkgdir/usr/share/fbzx/
  install -m644 fbzx.desktop $pkgdir/usr/share/applications/
  install -m644 fbzx.svg $pkgdir/usr/share/pixmaps/
  install -m644 AMSTRAD CAPABILITIES COPYING FAQ README README.TZX VERSIONS \
	$pkgdir/usr/share/doc/fbzx/

  install -Dm644 AMSTRAD $pkgdir/usr/share/licenses/$pkgname/AMSTRAD
}

md5sums=('c3abdfe41a9d6829c50ea62d25c4526e')
