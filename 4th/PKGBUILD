# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: scj <scj archlinux us>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=4th
pkgver=3.61.0
pkgrel=3
pkgdesc="A Forth Compiler"
arch=('i686' 'x86_64')
url="http://www.xs4all.nl/~thebeez/4tH/index.html"
license=('LGPL')
depends=('glibc')
install=4th.install
source=("http://www.xs4all.nl/~thebeez/4tH/$pkgname-$pkgver-unix.tar.gz" 
        x86_64.fix.patch x86_64_editor.fix.patch)
md5sums=('0b835b622d15e8e2d85022987c91cb04'
         '0ee629ceacdb1f573fb7770b81674b6f'
         'a3cd14e46f15824f7981bea8157e834c')

build() {
  cd "$srcdir/$pkgname-$pkgver-unix/sources"

  if [ "$CARCH" == "x86_64" ] ; then
    patch -R <$srcdir/x86_64.fix.patch
  fi

  make

  if [ "$CARCH" == "x86_64" ] ; then
    cd ..
    patch -p1 -R <$srcdir/x86_64_editor.fix.patch
    sources/4th cvsq examples/editor.4th editor.hx
    sources/4th cxq examples/bin2h.4th EmbeddedHX editor.hx sources/editor.h
    cd sources
    make mostlyclean
    make
  fi
}

package() {
  cd "$srcdir/$pkgname-$pkgver-unix/sources"
  mkdir -p $pkgdir/usr/{lib/4th,bin,include,share/{doc/4th,man/man1}}
  make BINARIES=$pkgdir/usr/bin LIBRARIES=$pkgdir/usr/lib install
  cd ..
  gzip -c9 documentation/4tHmanual.txt > $pkgdir/usr/share/doc/4th/4tHmanual.txt.gz
  install -m644 documentation/4th.1 $pkgdir/usr/share/man/man1
  install -m644 sources/4th.h $pkgdir/usr/include  

  # A lot of the library files requires that they are located in $DIR4TH/lib. 
  # To be able to keep the files in /usr/lib/4th, we need to rewrite them.
  # NOTE: this means that all 'includes lib/foo.4th' and '[NEEDS lib/foo.4th]'
  # will be '[NEEDS foo.4th]' instead, etc., as long as you have the
  # environment variable DIR4TH=/usr/lib/4th
  for file in lib/*.4th; do
    sed -e 's#\(include\|\[NEEDS\) lib/#\1 #gi' $file > $pkgdir/usr/lib/4th/$(basename $file) 
  done;
}
