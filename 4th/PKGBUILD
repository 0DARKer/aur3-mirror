#Contributor: scj <scj archlinux us>
#Maintainer: jorge_barroso <jorge.barroso.11@gmail.com
pkgname=4th
pkgver=3.61.5
pkgrel=1
pkgdesc="A Forth Compiler"
arch=('i686' 'x86_64')
url="http://www.xs4all.nl/~thebeez/4tH/index.html"
license=('LGPL')
depends=('glibc')
install=
source=("http://4th.googlecode.com/files/${pkgname}-${pkgver}-unix.tar.gz"
        "Makefile.patch")
md5sums=('34b6a6a5feb4749444f3c13ba301c413'
         '124ded30fea1a6378bd034b100b2405a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-unix/sources"
  patch -uNi ${srcdir}/Makefile.patch
  if [ "$CARCH" == "x86_64" ]; then
    cp include/* . ;
  fi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-unix/sources"
  install -d ${pkgdir}/usr/{share/{doc,man}/4th,{lib,bin}}
  make BINARIES=${pkgdir}/usr/bin \
       LIBRARIES=${pkgdir}/usr/lib \
       DOCDIR=${pkgdir}/usr/share/doc \
       MANDIR=${pkgdir}/usr/share/man install
}
