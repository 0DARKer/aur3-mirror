# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: rabyte <rabyte.at.pen.dot.tj>

pkgname=hugo
pkgver=2.12
pkgrel=1
pkgdesc="A TurboGrafx-16/PC Engine emulator"
arch=('i686')
url="http://www.zeograd.com/parse.php?src=hugof&path=0,1,"
license=('GPL2' 'Modified BSD license')
depends=('sdl_net' 'sdl_mixer' 'gtk2')
source=(http://www.zeograd.com/download/$pkgname-$pkgver.tar.gz
	$pkgname-$pkgver-gcc42.patch
	LICENSE)
md5sums=('1256ab47592413d12789166da23c00fc' '38434092a096f44763bc34f85b928382'
	 '7e56048f4feeb36d2e9b8084fc07d600')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -Np1 -i ../$pkgname-$pkgver-gcc42.patch
  ./configure --prefix=/usr --with-sdl-mixer --with-ogg-support
  make
}

package () {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m644 -D ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
