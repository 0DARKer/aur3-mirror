# Maintainer:  Lucky <aur.archlinux.org [at] lucky.take0ver [dot] net>
# Contributor: Jonny Gerold <jonny@fsk141.com>
# Contributor: Daenyth <Daenyth [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

pkgname=rtorrent-git
_pkgname="${pkgname%-*}"
pkgver=20111013
pkgrel=1
pkgdesc="Ncurses BitTorrent client based on libTorrent"
url="http://libtorrent.rakshasa.no"
license=("GPL")
arch=("i686" "x86_64")
depends=("curl" "libtorrent-git>=20111013" "xmlrpc-c")
makedepends=("cppunit" "git")
conflicts=("rtorrent")
provides=("rtorrent")
install=${_pkgname}.install
source=()
md5sums=()

_gitname="${_pkgname}"
_gitroot="git://github.com/rakshasa/${_pkgname}.git"

build() {
  cd "${srcdir}"

  #GIT {{{
  msg "Connecting to GIT server..."
  if [ -d "${_gitname}" ]; then
    (cd "${_gitname}" && git pull origin)
    msg "The local repository was updated."
  else
    git clone "${_gitroot}" "${_gitname}"
    msg "The remote repository was cloned."
  fi
  msg "GIT sync done or server timeout."
  cd "${srcdir}"
  if [ -d "${_gitname}-build" ]; then
    rm -rf "${_gitname}-build"
  fi
  cp -rf "${_gitname}" "${_gitname}-build"
  cd "${_gitname}-build"
  #}}}

  ./autogen.sh
  CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing" \
  ./configure --prefix=/usr --disable-debug --with-xmlrpc-c
  make
}

package() {
  cd "${_gitname}-build"

  make DESTDIR="${pkgdir}" install
}
# vim:set ts=2 sw=2 et:
