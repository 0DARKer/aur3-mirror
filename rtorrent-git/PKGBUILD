# Maintainer:   Lucky <archlinux@builds.lucky.li>
# Based on rtorrent [community]

pkgname=rtorrent-git
_pkgname="${pkgname%-*}"
pkgver=902.5acfd59
pkgrel=1
pkgdesc="Ncurses BitTorrent client based on libTorrent"
url="http://libtorrent.rakshasa.no"
license=("GPL")
arch=("i686" "x86_64")
depends=("curl" "libtorrent-git" "xmlrpc-c")
makedepends=("cppunit" "git")
conflicts=("${_pkgname}")
provides=("${_pkgname}")
install=${_pkgname}.install
source=("git://github.com/rakshasa/${_pkgname}.git")
md5sums=("SKIP")

pkgver() {
  cd "${_pkgname}"
  #git describe --always | sed "s|^${_pkgname}-||g;s|-|.|g"
  echo $(git rev-list --count master).$(git rev-parse --short master)
  #date +%Y%m%d
}

build() {
  cd "${srcdir}/${_pkgname}"

  ./autogen.sh
  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
  ./configure \
    --prefix=/usr \
    --enable-debug \
    --with-xmlrpc-c \

  make
}

package() {
  cd "${srcdir}/${_pkgname}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 doc/faq.xml      "${pkgdir}/usr/share/doc/rtorrent/faq.xml"
  install -Dm644 doc/old/rtorrent.1   "${pkgdir}/usr/share/man/man1/rtorrent.1"
  install -Dm644 doc/rtorrent.rc  "${pkgdir}/usr/share/doc/rtorrent/rtorrent.rc"
}
