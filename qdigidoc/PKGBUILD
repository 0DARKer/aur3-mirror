# Maintainer: kevku <kevku@gmx.com>
pkgname=qdigidoc
pkgver=3.8.0.1233
pkgrel=1
pkgdesc="Qt based UI application for verifying and signing digital signatures (Official version by AS Sertifitseerimiskeskus)"
arch=('x86_64' 'i686')
url="http://www.id.ee/"
license=('LGPL')
depends=('libdigidoc' 'libdigidocpp' 'qt5-base' 'libldap' 'shared-mime-info' 'esteidcerts' 'ccid' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'xsd' 'opensc' 'qt5-tools' 'qt5-translations')
conflicts=('qdigidoc-svn' 'sk-qdigidoc-svn')
install=qdigidoc.install
source=("https://installer.id.ee/media/sources/$pkgname-$pkgver.tar.gz"
        "https://installer.id.ee/media/sources/$pkgname-$pkgver.tar.gz.asc")
sha256sums=('9f8b015f2672407e23108b7ee5899de84dc1f7b502e850a1186f49115a52f0b6'
            'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR="lib" -DCMAKE_INSTALL_SYSCONFDIR="/etc"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
