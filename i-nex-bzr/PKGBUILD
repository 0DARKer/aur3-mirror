# Maintainer: crazyelf5

pkgname=i-nex-bzr
pkgver=57
pkgrel=1
pkgdesc="Provides CPU information similar to CPU-Z"
arch=('any')
url="https://launchpad.net/i-nex"
license=('GPL3')
depends=('gambas3-devel' 'gambas3-runtime' 'gambas3-gb-image' 'gambas3-gb-gtk' 'gambas3-gb-form' 'gambas3-gb-desktop'
         'gambas3-gb-settings' 'gambas3-gb-form-mdi' 'gambas3-gb-form-stock' 'grep' 'util-linux' 'procps' 'xorg-server-utils' 'gksu' 'lsb-release')
makedepends=('bzr')

_bzrtrunk=lp:i-nex
_bzrmod=i-nex

build() {
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ]; then
    cd $_bzrmod && bzr pull $_bzrtrunk -r $pkgver && cd ..
    msg "The local files are updated."
  else
    bzr branch $_bzrtrunk $_bzrmod -r $pkgver
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd "$_bzrmod-build/src/i-nex"

  gbc3 -eagtpmv
  gba3
}

package() {
  cd "$srcdir/$_bzrmod-build/"

  sed -i 's|Exec=/usr/bin/i-nex.gambas|Exec=/usr/bin/i-nex|' debian/i-nex.desktop
  sed -i 's|Comment|Comment[pl]|' debian/i-nex.desktop
  echo "Comment=View various information about your CPU" >> debian/i-nex.desktop

  install -dm755 "$pkgdir"/usr/{bin,share/{applications,pixmaps}}
  install -m755 src/i-nex/i-nex.gambas "$pkgdir/usr/bin/i-nex"
  install -m644 src/i-nex/i-nex.png "$pkgdir/usr/share/pixmaps"
  install -m644 debian/i-nex.desktop "$pkgdir/usr/share/applications"
} 
