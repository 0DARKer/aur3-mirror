# Maintainer: Lubomir Krajcovic <lubomir.krajcovic@gmail.com>
# Contributor: Vladimir Kutyavin <vlkut@bk.ru>
pkgname=xtables-addons-multikernel
pkgver=1.32
pkgrel=1
pkgdesc="Successor to patch-o-matic(-ng). Additional extensions for iptables, ip6tables, etc. CHAOS, TARPIT, TEE, DELUDE and other targets; condition, geoip, ipp2p and other matches. Includes ipset package. Multikernel version."
arch=('i686' 'x86_64')
license=('GPL2')
url="http://xtables-addons.sourceforge.net/"
depends=('iptables>=1.4.3', 'kernel26>=2.6.17' 'glibc')
makedepends=('kernel26-headers')
conflicts=(ipset xtables-addons)
replaces=(ipset xtables-addons)
provides=(ipset xtables-addons)
source=(http://download.sourceforge.net/project/xtables-addons/Xtables-addons/$pkgver/xtables-addons-$pkgver.tar.xz)
md5sums=('978a04e3e532ef0414ae1dd6a405304d')

build() {
	cd $srcdir/xtables-addons-$pkgver
	# build the modules for all installed kernels
	for kern in /lib/modules/*; do
		kern_ver=$(basename $kern)
		kern_headers="$kern/build"
		echo -e "\n\n\n>>> building for kernel: $kern_ver\n\n\n"
		sleep 3
		if [ ! -d "$kern_headers" ]; then
			echo "!!! SKIPPING build for kernel: $kern_ver"
			echo "!!! could not locate directory: $kern_headers"
			echo "!!! probably missing kernel headers for $kern_ver ?"
			continue
		fi
		./configure \
			--prefix=/usr \
			--libexecdir=/usr/lib/iptables \
			--sysconfdir=/etc \
			--with-xtlibdir=/usr/lib/iptables \
			--mandir=/usr/share/man \
			--with-kbuild="$kern_headers"
		make || return 1
		make DESTDIR=$pkgdir install || return 1 
	done
	chmod a-x $pkgdir/usr/lib/iptables/*.so
}
