# Contributor: Anton Leontiev <bunder /at/ t-25.ru>
pkgname=perl-test-class
pkgver=0.36
pkgrel=1
pkgdesc="Perl module to easily create test classes in an xUnit/JUnit style"
arch=("any")
url="http://search.cpan.org/dist/Test-Class/"
license=("GPL" "PerlArtistic")
depends=("perl>=5.6.1" "perl-mro-compat") # 'perl' provides 'perl-attribute-handlers>=0.77', 'perl-test-simple'
checkdepends=("perl-test-exception")
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/A/AD/ADIE/Test-Class-$pkgver.tar.gz)
md5sums=("9d33a80215754b4e5234e9116088f403")

build() {
  cd "$srcdir/Test-Class-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Test-Class-$pkgver"
  make test
}  

package() {
  cd "$srcdir/Test-Class-$pkgver"
  make install DESTDIR="$pkgdir"
}
