# Maintainer : eolianoe <eolianoe At GoogleMAIL DoT CoM>
# Contributor: Michele Mocciola <mickele>
# Contributor: Guillaume Doll√© < dolle dot guillaume at gmail dot com >

pkgname=mumps-seq
pkgver=4.10.0
pkgrel=1
pkgdesc="Sparse solver library using Gaussian elimination (sequential version)"
url="http://mumps.enseeiht.fr"
license=('custom')
depends=('lapack' 'blas')
provides=('mumps')
conflicts=('mumps')
arch=('i686' 'x86_64')
source=(http://mumps.enseeiht.fr/MUMPS_${pkgver}.tar.gz Makefile.inc)
md5sums=('959e9981b606cd574f713b8422ef0d9f'
         '0523a8f957af96045cee576dbe2ea590')

prepare(){
  cd "${srcdir}/MUMPS_${pkgver}"

  cp -f "${srcdir}/Makefile.inc" .
}

build() {
  cd "${srcdir}/MUMPS_${pkgver}"

  make -j1 all
}

package(){
  # Install all headers
  cd "${srcdir}/MUMPS_${pkgver}/include"
  install -m 755 -d "${pkgdir}/usr/include"
  install -D -m644 -- *.h "${pkgdir}/usr/include"

  # Install all libraries
  cd "${srcdir}/MUMPS_${pkgver}/lib" 
  install -m 755 -d "${pkgdir}/usr/lib" 
  _libs=$(find . -maxdepth 1 -regex ".*\.a" | xargs | sed "s|\.a||g")
  for _FILE in ${_libs}; do
    ld -Bshareable -o ${_FILE}_seq.so.${pkgver} -x -soname ${_FILE}_seq.so --whole-archive ${_FILE}.a
    install -m 755 ${_FILE}_seq.so.${pkgver} ${pkgdir}/usr/lib
    ln -sf ${_FILE}_seq.so.${pkgver} ${pkgdir}/usr/lib/${_FILE}_seq.so.${pkgver:0:1}
  done

  # Install mpiseq headers
  cd "${srcdir}/MUMPS_${pkgver}/libseq"
  install -m 755 -d "${pkgdir}/usr/include/mpiseq"
  install -D -m644 -- *.h "${pkgdir}/usr/include/mpiseq"
  # Install mpiseq libraries
  ld -Bshareable -o libmpiseq.so.${pkgver} -x -soname libmpiseq.so --whole-archive libmpiseq.a
  install -m 755 libmpiseq.so.${pkgver} ${pkgdir}/usr/lib
  ln -sf libmpiseq.so.${pkgver} ${pkgdir}/usr/lib/libmpiseq.so.${pkgver:0:1}

  # Install license
  install -D -m644 "${srcdir}/MUMPS_${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
