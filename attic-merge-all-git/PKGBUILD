# Maintainer: Beneldr <beneldr (dot) aur (at) gmail (dot) com>

pkgname=attic-merge-all-git
pkgver=r790.fc7e489
pkgrel=1
pkgdesc="A deduplicating backup program for efficient and secure backups. Merge-all branch with additional features and bugfixes."
arch=('i686' 'x86_64')
url="https://attic-backup.org/"
license=('BSD')
depends=('openssl' 'python-msgpack')
optdepends=('python-llfuse: Mounting backups as a FUSE filesystem')
makedepends=('git' 'cython' 'python-sphinx')
provides=('attic')
conflicts=('attic')
source=("$pkgname"::'git://github.com/attic/merge.git#branch=merge-all')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname/docs"
  PYTHONPATH=.. make man
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m644 "docs/_build/man/attic-deduplicatingarchiver.1" "$pkgdir/usr/share/man/man1/attic.1"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  python3 setup.py -q install --root="$pkgdir" --optimize=1
}
