# Maintainer: AlexanderR <alexander r at gmx com>
pkgname=xf86-video-sis671
pkgver=0.10.3
pkgrel=1
pkgdesc="SiS 671 video driver (derivate of xf86-sisimedia, latest tested version). Module name is 'sisimedia'."
arch=('i686' 'x86_64')
url="https://gitorious.org/$pkgname"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel>=1.11.0' 'xf86dgaproto' 'libdrm' 'xf86driproto' 'mesa' 'glproto')
provides=('xf86-video-sisimedia')
conflicts=('xorg-server<1.12.0' 'xf86-video-sisimedia')
options=('!libtool')
source=("https://gitorious.org/$pkgname/sis-671-fix/archive-tarball/$pkgname-$pkgver")

build() {
  cd "$srcdir/$pkgname-sis-671-fix"

  autoreconf -vi
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-sis-671-fix"

  make DESTDIR="$pkgdir/" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

sha256sums=('82590213a25e95e31b3ba032c9e49955dc5a24418415cb23a1fdd8f69e98e148')
