# Maintainer: AlexanderR <alexander r at gmx com>
pkgname=xf86-video-sis671
pkgver=0.10.3
pkgrel=1
pkgdesc="SiS 671 video driver (derivate of xf86-sisimedia, latest tested version). Module name is 'sisimedia'."
arch=('i686' 'x86_64')
url='https://gitorious.org/sis-671-fix'
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel>=1.11.0' 'xf86dgaproto' 'libdrm' 'xf86driproto' 'mesa' 'glproto')
provides=('xf86-video-sisimedia')
conflicts=('xorg-server<1.12.0' 'xf86-video-sisimedia')
options=('!libtool')
source=("https://gitorious.org/sis-671-fix/sis-671-fix/archive-tarball/xf86-video-sisimedia-$pkgver")

build() {
  cd "$srcdir/sis-671-fix-sis-671-fix"

  autoreconf -vi
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/sis-671-fix-sis-671-fix"

  make DESTDIR="$pkgdir/" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

sha256sums=('3a64b233262a193572db38f08442a237376e3686c41e5a26c6024f1f5867b84d')
