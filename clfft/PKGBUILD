# Maintainer: jdarch <jda -dot- cloud -plus- archlinux -at- gmail -dot- com>

pkgname=clfft
pkgver=2.0
pkgrel=1
pkgdesc="A software library containing FFT functions written in OpenCL"
arch=("i686" "x86_64")
url="https://github.com/clMathLibraries/clFFT"
license=('APACHE')
depends=("amdapp-sdk")

source=("https://github.com/clMathLibraries/clFFT/archive/v${pkgver}.zip"
	"compiler_detection.patch")
md5sums=('559866094b16e88271336139473db7de'
         '034773932802c4b2a292fa0fbdcdcf8b')
sha512sums=('26c116792547b332c975e1f6dc32b9cf167c721bb503a1bec8103d5de2dc13b3cc814b6e40ff38da5f2b941ee8d11f860193794e863cf9d5c142b2b38d5b533c'
            '238166f8e735055c4b93fa516dfaf630e2372fa9cdf121cfe22edabe769057cfb897482e723f6834866b97fdc46e509edcea7b5de00d31927485e6bb011f4ef9')

if [ "$CARCH" == "x86_64" ]; then
    _bits=64
elif [ "$CARCH" == "i686" ]; then
    _bits=32
fi

prepare() {
	cd "${srcdir}/clFFT-${pkgver}/src"
	patch <"${srcdir}/compiler_detection.patch"
}

build() {
	export DESTDIR="${pkgdir}"
	cd "${srcdir}/clFFT-${pkgver}/src"
	cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ./
	make clFFT
}

package() {
	cd "${srcdir}/clFFT-${pkgver}/src"
	make install
	rm -rf "${pkgdir}/usr/bin${_bits}"
	mv "${pkgdir}/usr/lib${_bits}" "${pkgdir}/usr/lib"
	rm "${pkgdir}/usr/lib/libStatTimer.so"*
	mkdir "${pkgdir}/usr/include/clFFT"
	mv -t "${pkgdir}/usr/include/clFFT/" "${pkgdir}"/usr/include/*.h
}
