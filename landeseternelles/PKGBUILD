# Maintainer: Tosh (duretsimon73 <at> gmail <dot> com)

pkgname=landeseternelles
pkgver=1.8.1
pkgrel=1
pkgdesc="Landes Eternelles is a frensh role playing game (mmorpg), forked from Eternal Lands."
arch=('i686' 'x86_64')
url="http://www.landes-eternelles.com"
license=(custom:'eternal_lands_license')
depends=('sdl_net' 'sdl_image' 'cal3d' 'mesa' 'openal' 'libxslt' 'libvorbis' 'gtk2')
install='landeseternelles.install'
source=(
    http://landes-eternelles.com/client/1810/Client_Sources-1.8.1.tar.bz2
    http://www.landes-eternelles.com/client/1810/Editeur_Sources-1.8.1.tar.bz2
    http://landes-eternelles.com/client/1810/linux/LandesEternellesLinux-1.8.1.tar.bz2
    landeseternelles.desktop
    landeseternelles.png
)
md5sums=(
    '3b25e7134a7d6504e6af8f19a78e37f5'
    'a8f4e12860f6826ed239a1b8bf7be762'
    'b89e3c4758070a662e6b69498e802d21'
    '1dd4e7b92ac9949db19b851071475512'
    '410fed06fca941a95b0427c30cb697c5'
)

build() {
    cd "$srcdir/Client_Sources"
    for fic in Makefile.linux *.h *.c io/*.h io/*.c eye_candy/*.h eye_candy/*.cpp
    do
	sed -e 's#client_sources#Client_Sources#g' -i $fic
	sed -e 's#editeur_sources#Editeur_Sources#g' -i $fic
    done 

    sed '/PFNGLBLENDCOLORPROC\|PFNGLBLENDEQUATIONPROC/d' -i $srcdir/Client_Sources/load_gl_extensions.h
    sed '/PFNGLBLENDCOLORPROC\|PFNGLBLENDEQUATIONPROC/d' -i $srcdir/Client_Sources/load_gl_extensions.c

    make -f Makefile.linux || return 1

    cd "$srcdir/Editeur_Sources"
    for fic in Makefile.linux *.h *.c *.cpp
    do
	sed -e 's#editeur_sources#Editeur_Sources#g' -i $fic
	sed -e 's#client_sources#Client_Sources#g' -i $fic
    done 
    make -f Makefile.linux || return 1
}

package() {
    mkdir -p $pkgdir/usr/{bin,share/{$pkgname,licenses/$pkgname,pixmaps,applications}}
    install -m644 $srcdir/landeseternelles.desktop $pkgdir/usr/share/applications/landeseternelles.desktop
    install -m644 $srcdir/landeseternelles.png $pkgdir/usr/share/pixmaps/landeseternelles.png
    if [ $CARCH == "x86_64" ];then
	cd "$srcdir/Client_Sources"
	install -m755 le.x86_64.linux.bin $pkgdir/usr/bin/le.$CARCH.linux.bin
	cd "$srcdir/Editeur_Sources"
	install -m755 mapedit.x86_64.linux.bin $pkgdir/usr/bin/mapedit.$CARCH.linux.bin
    else
	cd "$srcdir/Client_Sources"
	install -m755 le.x86.linux.bin $pkgdir/usr/bin/le.x86.linux.bin
	cd "$srcdir/Editeur_Sources"
	install -m755 mapedit.x86.linux.bin $pkgdir/usr/bin/mapedit.x86.linux.bin
    fi

    cd "$srcdir/LandesEternellesLinux"
    rm -f *.bin icon.bmp
    mv -f Licence.txt $pkgdir/usr/share/licenses/$pkgname/
    mv -f * $pkgdir/usr/share/$pkgname/
}

