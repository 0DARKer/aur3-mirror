# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=lv2-c++-tools
pkgver=1.0.4
pkgrel=3
pkgdesc="Tools and libraries that may come in handy when writing LV2 plugins."
arch=('i686' 'x86_64')
url="http://ll-plugins.nongnu.org/hacking.html"
license=('GPL3')
makedepends=('boost')
options=('staticlibs')
source=("http://download.savannah.nongnu.org/releases-noredirect/ll-plugins/$pkgname-$pkgver.tar.bz2"
        "$pkgname-boost-1.50.patch")
md5sums=('2468f8750bae1d57300853479846e5ed'
         '1dd1152e9ab8be15248b54546716c8c9')

prepare() {
  cd $pkgname-$pkgver

  # boots 1.50 patch
  patch -p1 -i ../${source[1]}

  # do not call ldconfig
  sed -i '/ldconfig/d' Makefile.template
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}