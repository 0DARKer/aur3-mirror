# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: TryA <tryagainprod {at} gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_pkgbasename=elfutils
pkgname=lib32-elfutils
pkgver=0.152
pkgrel=1
pkgdesc="Collection of libraries for working with ELF object files and DWARF debugging information (32-bit)"
arch=('x86_64')
url="https://fedorahosted.org/elfutils/"
license=('GPL2')
depends=('lib32-bzip2' 'lib32-zlib')
makedepends=('gcc-multilib')
provides=('lib32-libelf')
replaces=('lib32-libelf')
conflicts=('lib32-libelf')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2)
md5sums=('39739ed58a0fa1862eff8735f111fe5c')

build() {
  export CC="${CC:-gcc} -m32"
  export CXX="${CXX:-g++} -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
