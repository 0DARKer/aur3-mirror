# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Thomas Dziedzic <gostrc@@@gmail>
# Contributor: Antti "Tera" Oja <antti.bofh@@@gmail.com>

_pkgname=dri2proto
pkgname=$_pkgname-git
pkgver=2.8.3.gddc1287
pkgrel=1
pkgdesc="X11 DRI extension wire protocol."
url="http://xorg.freedesktop.org/"
arch=("any")
license=("custom")
makedepends=("xorg-util-macros" "git")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git://git.freedesktop.org/git/xorg/proto/$_pkgname")
md5sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s|dri2proto.||g;s|-|.|g"
}

build() {
	cd "$srcdir/$_pkgname"

	./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$_pkgname"

	make DESTDIR="$pkgdir" install
	install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
