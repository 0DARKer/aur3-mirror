# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-linux-inotify2
_lastauthor=M/ML/MLEHMANN
_pkgname=Linux-Inotify2
pkgver=1.22
pkgrel=3
pkgdesc="scalable directory/file change notification"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-common-sense')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('bc9d253f4079935a78b9c071721a5da5c4cb51fb79874e7a28cc4cd5d13521d1')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  if [ -f "Build.PL" ]; then
    perl Build.PL
    perl Build
  elif [ -f "Makefile.PL" ]; then
    perl Makefile.PL
    make
  else
    return 1
  fi
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build test
  elif [ -f "Makefile.PL" ]; then
    make test
  fi
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  if [ -f "Build.PL" ]; then
    perl Build install
  elif [ -f "Makefile.PL" ]; then
    make install
  fi

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
