# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname='perl-linux-inotify2'
_lastauthor='M/ML/MLEHMANN'
_pkgname='Linux-Inotify2'
pkgver='1.22'
pkgrel='2'
pkgdesc="scalable directory/file change notification"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-common-sense')
url='http://search.cpan.org/dist/Linux-Inotify2/'
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('bc9d253f4079935a78b9c071721a5da5c4cb51fb79874e7a28cc4cd5d13521d1')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  export PERL_MM_USE_DEFAULT=1
  export PERL_AUTOINSTALL='--skipdeps'
  perl Makefile.PL INSTALLDIRS=vendor
  make
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
