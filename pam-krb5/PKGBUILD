# Maintainer: Mantas MikulÄ—nas <grawity@gmail.com>
# $Id: PKGBUILD 78535 2012-10-20 10:30:17Z ebelanger $
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Ryan Corder <ryanc@greengrey.org>

pkgname=pam-krb5
pkgver=4.6
pkgrel=1
pkgdesc='A PAM module providing Kerberos v5 support.'
arch=('i686' 'x86_64' 'armv6h')
url='http://www.eyrie.org/~eagle/software/pam-krb5/'
license=('custom')
options=('!libtool')
depends=('krb5' 'pam')
source=(http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz{,.asc})
sha1sums=('d96b018eda3bd269dcb57f4ecee4898afb5c4369'
          'SKIP')
validpgpkeys=('E784364E8DDE7BB370FBD9EAD15D313882004173')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --enable-reduced-depends \
    --libdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
