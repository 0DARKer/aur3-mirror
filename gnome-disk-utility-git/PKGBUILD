# Maintainer: Marti Raudsepp <marti@juffo.org>
# Original author: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility-git
pkgver=20120426
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
groups=('gnome-extra')
depends=('gtk3' 'udisks2' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-common')
# Note: automatically detects and adds dependency on libsystemd
provides=('gnome-disk-utility=3.4.2')
conflicts=('gnome-mount' 'gnome-disk-utility')
options=('!libtool' '!emptydirs')
install=gnome-disk-utility.install
source=()

_gitroot="git://git.gnome.org/gnome-disk-utility"
_gitname="gnome-disk-utility"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}
