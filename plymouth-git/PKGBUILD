# Contributor: Det <nimetonmaili at gmail dot com>
# Contributor: Martin Lee <hellnest.fuah@gmail.com>
# Contributor: Ricardo Funke
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: lh <jarryson#gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgname=plymouth-git
pkgdesc="A Linux boot screen implementation"
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
arch=('i686' 'x86_64')
pkgver=20110729
pkgrel=1
depends=('initscripts' 'libdrm' 'gtk2')
makedepends=('git')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
provides=('plymouth')
conflicts=('plymouth')
source=(arch-logo.png
        plymouth-encrypt.initcpio_hook
        plymouth-encrypt.initcpio_install
        plymouth.functions
        plymouth.initcpio_hook
        plymouth.initcpio_install
        plymouth-update-initrd.patch
        system-release)
sha1sums=('3bc168dd06ff3e2790f26c131209a7fb4a5ccbd7'
           'c69466cc8b8d8e0da7c7df4ca5a5f7fb94bc6c8f'
           '489fe3a754df3711abd5ad0f905650473c5e64f0'
           '91a24dcc30c0fd325cf184855f3a3886b185f491'
           '39a27b5a049dc620b7820b62f351b6cb1d2a48c5'
           'c74e5d25c2eadd28da4b93d86a146cea8bbe3cff'
           'cfe120862d4d793a6b5e5db8b0123d1ebc85b421'
           '4c31cbbfbdeb48593641ff600c8d4fb7bda2b01c')
backup=('etc/plymouth/plymouthd.conf')
_gitroot='git://git.freedesktop.org/git/plymouth'
_gitname='plymouth'

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done. Preparing sources..."

  rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

  msg "Starting make..."
  cd "${srcdir}/${_gitname}-build"

  msg "Applying Patch..."
  patch -Np1 -i "${srcdir}/plymouth-update-initrd.patch"

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' -i src/libply-splash-graphics/ply-image.c

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --enable-tracing \
    --with-system-root-install \
    --enable-gdm-transition \
    --with-gdm-autostart-file=no \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D

  make
}

package() {
  cd ${_gitname}-build

  make DESTDIR="${pkgdir}" install

  cd "${pkgdir}"

  rm -rf `find . -name "*.orig"`

  install -D -m644 "${srcdir}/plymouth.initcpio_install" "${pkgdir}/lib/initcpio/install/plymouth"
  install -D -m644 "${srcdir}/plymouth.initcpio_hook" "${pkgdir}/lib/initcpio/hooks/plymouth"
  install -D -m644 "${srcdir}/plymouth-encrypt.initcpio_install" "${pkgdir}/lib/initcpio/install/plymouth-encrypt"
  install -D -m644 "${srcdir}/plymouth-encrypt.initcpio_hook" "${pkgdir}/lib/initcpio/hooks/plymouth-encrypt"
  install -D -m644 "${srcdir}/plymouth.functions" "${pkgdir}/etc/rc.d/functions.d/plymouth.functions"
  install -D -m644 "${srcdir}/arch-logo.png" "${pkgdir}/usr/share/plymouth/arch-logo.png"
  install -D -m644 "${srcdir}/system-release" "${pkgdir}/etc/system-release"
}