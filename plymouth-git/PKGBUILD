# Maintainer: -
# Contributors: Zephyr, Det, Christian Autermann, Biginoz, Martin Lee, Ricardo Funke, PirateJonno, lh, Cilyan Olowen, Shaffer, Brcha, Lyle Putnam

pkgname=plymouth-git
pkgver=20120713
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support (Git version)"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('systemd' 'libdrm' 'pango')
makedepends=('git')
options=('!libtool' '!emptydirs')
install=$pkgname.install
provides=('plymouth')
conflicts=('plymouth')
backup=('etc/plymouth/plymouthd.conf')
source=('arch-logo.png'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-$CARCH/encrypt_hook"
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-$CARCH/encrypt_install"
        'encrypt_hook.patch'
        'encrypt_install.patch'
        'plymouthd.conf'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-update-initrd.patch'
        'system-release'
        'kdm-plymouth.service'
        'gdm-plymouth.service'
        'lxdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
        'systemd-unit-dir.patch')

sha1sums=('3bc168dd06ff3e2790f26c131209a7fb4a5ccbd7'  # arch-logo.png
          'f39177bcfe423d03b362888025d48903f6dea75c'  # encrypt_hook
          '26fb380d56e64cc777ac32e2c124c41ac0769ff5'  # encrypt_install
          '3ed84239e26a85ce2020dc98f70cb80e2232d117'  # encrypt_hook.patch
          'f12156b7ab71164f7245d997bfcadebedf2b5602'  # encrypt_install.patch
          '01235a179d8f991a37035c15cdcc0733b581afd4'  # plymouthd.conf
          'f5ed30777674bee7f20b6bf2ae9c7cc09483f0b3'  # plymouth.functions
          '332bb6a59e0a58e75684c4545e7a645ca9007bd9'  # plymouth.initcpio_hook
          '5352033523558ab1e6c49dc57f984f7c66d83610'  # plymouth.initcpio_install
          '019663ae0070163e8d6cf268e7efeaaef474e725'  # plymouth-update-initrd.patch
          '4c31cbbfbdeb48593641ff600c8d4fb7bda2b01c'  # system-release
          '44c03d1cc71f7b9fa6ef7022584f0f81c9c17faf'  # kdm-plymouth.service
          '92b2ea373991af921d8e2c8a8d06c31b4f9e7313'  # gdm-plymouth.service
          '31eb516da0e78d9c185dfb9ee5b94468f713ff1c'  # lxdm-plymouth.service
          'c80522e4fe29f8e4cd958e1fbb8407c53c5f4388'  # kdm-unpatched-plymouth.service
          '9e8ad4d68588a7445e764740c1dcdfe81a482ca5') # systemd-unit-dir.patch

_gitroot='git://git.freedesktop.org/git/plymouth'
_gitname='plymouth'

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 $_gitroot $_gitname
  fi
  msg "GIT checkout done. Preparing sources..."

  cd "$srcdir"
  rm -rf ${_gitname}-build
  cp -r $_gitname ${_gitname}-build

  msg "Applying Patches..."

  patch -p0 -i encrypt_hook.patch
  patch -p0 -i encrypt_install.patch

  cd ${_gitname}-build
  patch -p0 -i "$srcdir/plymouth-update-initrd.patch"

  # Change SYSTEMD_UNIT_DIR to /usr/lib/systemd/system
  patch -p0 -i "$srcdir/systemd-unit-dir.patch"

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c

  msg "Starting make..."

  ./autogen.sh --prefix=/usr --exec-prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib --libexecdir=/usr/lib \
    --enable-tracing \
    --without-rhgb-compat-link \
    --with-gdm-autostart-file=yes \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D \
    --enable-systemd-integration \
    --without-system-root-install

  make
}

package() {
  cd ${_gitname}-build

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"
  install -Dm644 "$srcdir/plymouth.initcpio_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "$srcdir/encrypt_install" "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "$srcdir/encrypt_hook" "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "$srcdir/plymouth.functions" "$pkgdir/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 "$srcdir/arch-logo.png" "$pkgdir/usr/share/plymouth/arch-logo.png"
  install -Dm644 "$srcdir/system-release" "$pkgdir/etc/system-release"
  install -Dm644 "$srcdir/plymouthd.conf" "$pkgdir/etc/plymouth/plymouthd.conf"

  # Plymouth<->systemd integration, units for DMs
  install -Dm644 "$srcdir/kdm-plymouth.service" "$pkgdir/usr/lib/systemd/system/kdm-plymouth.service"
  install -Dm644 "$srcdir/gdm-plymouth.service" "$pkgdir/usr/lib/systemd/system/gdm-plymouth.service"
  install -Dm644 "$srcdir/lxdm-plymouth.service" "$pkgdir/usr/lib/systemd/system/lxdm-plymouth.service"
  install -Dm644 "$srcdir/kdm-unpatched-plymouth.service" "$pkgdir/usr/lib/systemd/system/kdm-unpatched-plymouth.service"
}

# vim: set ts=2 sw=2 ft=sh noet: