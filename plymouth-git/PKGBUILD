# Maintainer: Det <nimetonmaili at gmail dot com>
# Contributor: Christian Autermann <christian@autermann.org>
# Contributor: Martin Lee <hellnest.fuah@gmail.com>
# Contributor: Ricardo Funke
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: lh <jarryson#gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgname=plymouth-git
pkgdesc="A Linux boot screen implementation"
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
arch=('i686' 'x86_64')
pkgver=20110731
pkgrel=1
depends=('initscripts' 'libdrm' 'gtk2')
makedepends=('git')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
provides=('plymouth')
conflicts=('plymouth')
source=('arch-logo.png'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-${CARCH}/encrypt_hook"
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-${CARCH}/encrypt_install"
        'encrypt_hook.patch'
        'encrypt_install.patch'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-update-initrd.patch'
        'plymouthd.conf'
        'system-release')
sha1sums=('3bc168dd06ff3e2790f26c131209a7fb4a5ccbd7'
          '2d3dfa82eabe51f1addf7472f65a7588324e1c0a'
          '2ddf6061162b481f343ce883083b6ab3b64342e0'
          'a26793fcf7ba74a4d038b162cf4f1550cb410f8b'
          '08dc7cdc5873634042052014b5250d552158dc4e'
          '5b505947db59c6267c5a284d24b110c595f0322c'
          '39a27b5a049dc620b7820b62f351b6cb1d2a48c5'
          'c74e5d25c2eadd28da4b93d86a146cea8bbe3cff'
          '97d6208c6e6cfe77087b9862f12b969534924d2f'
          '01235a179d8f991a37035c15cdcc0733b581afd4'
          '4c31cbbfbdeb48593641ff600c8d4fb7bda2b01c')

backup=('etc/plymouth/plymouthd.conf')
_gitroot='git://git.freedesktop.org/git/plymouth'
_gitname='plymouth'

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done. Preparing sources..."

  cd "${srcdir}"
  rm -rf ${_gitname}-build
  cp -r ${_gitname} ${_gitname}-build

  msg "Applying Patches..."

  patch -p0 -i encrypt_hook.patch
  patch -p0 -i encrypt_install.patch
  patch -p0 -i plymouth-update-initrd.patch

  cd ${_gitname}-build

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c

  msg "Starting make..."

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --enable-tracing \
    --with-system-root-install \
    --enable-gdm-transition \
    --with-gdm-autostart-file=no \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D

  make
}

package() {
  cd ${_gitname}-build

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/plymouth.initcpio_install" "${pkgdir}/lib/initcpio/install/plymouth"
  install -Dm644 "${srcdir}/plymouth.initcpio_hook" "${pkgdir}/lib/initcpio/hooks/plymouth"
  install -Dm644 "${srcdir}/encrypt_install" "${pkgdir}/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "${srcdir}/encrypt_hook" "${pkgdir}/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "${srcdir}/plymouth.functions" "${pkgdir}/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 "${srcdir}/arch-logo.png" "${pkgdir}/usr/share/plymouth/arch-logo.png"
  install -Dm644 "${srcdir}/system-release" "${pkgdir}/etc/system-release"
  install -Dm644 "${srcdir}/plymouthd.conf" "${pkgdir}/etc/plymouth/plymouthd.conf"
}

# vim: set ts=2 sw=2 ft=sh noet: