# Maintainer: Martin Lee <hellnest.fuah@gmail.com>
# Contributor: Ricardo Funke
# Contributor: PirateJonno <j@skurvy.no-ip.org>
# Contributor: lh <jarryson#gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgname=plymouth-git
pkgdesc="A Linux boot screen implementation"
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
arch=('i686' 'x86_64')
pkgver=20110617
pkgrel=4
depends=('initscripts' 'libdrm' 'gtk2')
makedepends=('git')
options=('!libtool' '!emptydirs')
install=plymouth.install
provides=('plymouth')
conflicts=('plymouth')
source=(plymouth.functions
        plymouth.initcpio_hook
        plymouth.initcpio_install
        plymouth-encrypt.initcpio_hook
        plymouth-encrypt.initcpio_install
        plymouth-update-initrd.patch
        arch-logo.png
        system-release)
backup=('etc/plymouth/plymouthd.conf')
_gitroot='git://git.freedesktop.org/git/plymouth'
_gitname='plymouth'
_gitbranch='master'

sha256sums=('435ef98224566b94429dd2d02b59f929b5e3085882d70f61d1a75ccd97751533'
            '1a4604d84387e0373c899866c3fd475e0ab2ebd0e58afc7ee69bbd2e631220bc'
            '60b970740a4fbaad62c4c096ead3a58c15ace13a8759d3f102c9ed24e723b1e4'
            '12e387eb0a279f9ec9e7600b65fd2dd068a608f8f651de3ff0e14b694fa9e022'
            '8ef0ab3ab0028bd1cb7003fc3ac510b275ae08e4191b1388a77d6b3e1af1bf21'
            '94abcdb4f31e57ead5fef1a39228f313609a12a3c9694f008d02c56368b6c912'
            '9bac679d2494d9b60b288be87021f1d7b85a9503ebbdce93d6e37c0fc07568ae'
            '4ba2e16e7c6ed9c89e4999bd1ce5dd6e39e2db7447b5e43be384d0fbced67828')      

build() {
  cd "$srcdir"
  
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d "${srcdir}/${_gitname}" ] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi
  msg "GIT checkout done. Preparing sources..."

  [ -d "${srcdir}/${_gitname}-build" ] && rm -rf "${srcdir}/${_gitname}-build"
  cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

  msg "Starting make..."
  cd "${srcdir}/${_gitname}-build"
  
  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' -i src/libply-splash-graphics/ply-image.c
  
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --enable-tracing \
    --with-system-root-install \
    --disable-tests \
    --enable-gdm-transition \
    --without-rhgb-compat-link \
    --with-gdm-autostart-file=no \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D
  
  make || return 1
}

package() {
  cd "${srcdir}/${_gitname}-build"
  
  # Set program version automatically from autotools
  _providever="$(echo "@VERSION@" | ./config.status --file=-)"
  provide=('plymouth' "plymouth=$_providever")
  
  make DESTDIR="$pkgdir" install || return 1
  
  cd "$pkgdir"
  
  patch -Np0 -i "$srcdir/plymouth-update-initrd.patch" || return 1

  rm -rf `find . -name "*.orig"`

  install -D -m644 $srcdir/plymouth.initcpio_install ${pkgdir}/lib/initcpio/install/plymouth
  install -D -m644 $srcdir/plymouth.initcpio_hook ${pkgdir}/lib/initcpio/hooks/plymouth
  install -D -m644 $srcdir/plymouth-encrypt.initcpio_install ${pkgdir}/lib/initcpio/install/plymouth-encrypt
  install -D -m644 $srcdir/plymouth-encrypt.initcpio_hook ${pkgdir}/lib/initcpio/hooks/plymouth-encrypt
  install -D -m644 $srcdir/plymouth.functions ${pkgdir}/etc/rc.d/functions.d/plymouth.functions
  install -D -m644 "$srcdir/arch-logo.png" ${pkgdir}/usr/share/plymouth/arch-logo.png
  install -D -m644 "$srcdir/system-release" ${pkgdir}/etc/system-release
}

