# Maintainer: Ricardo Funke <ricardo dot funke at gmail dot com>
# Contributor: Det <nimetonmaili at gmail dot com>
# Contributor: Biginoz <biginoz at free dot fr>
# Contributor: PirateJonno <j at skurvy dot no-ip dot org>
# Contributor: Christian Autermann <christian at autermann dot org>
# Contributor: Martin Lee <hellnest dot fuah at gmail dot com>
# Contributor: lh <jarryson at gmail dot com>
# Contributor: Cilyan Olowen <gaknar at gmail dot com>
# Contributor: Shaffer <benjamin dot arnoult at yahoo dot fr>
# Contributor: Brcha <brcha at gna dot org>
# Contributor: Lyle Putnam <lcputnam at gmail dot com>

pkgname=plymouth-git
pkgver=20120606
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support (Git version)"
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/Plymouth"
license=('GPL')
depends=('libdrm' 'pango')
optdepends=('initscripts' 'systemd')
makedepends=('git' 'autoconf')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
backup=('etc/plymouth/plymouthd.conf')
provides=('plymouth')
conflicts=('plymouth')
source=('arch-logo.png'
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-${CARCH}/encrypt_hook"
        "http://projects.archlinux.org/svntogit/packages.git/plain/cryptsetup/repos/core-${CARCH}/encrypt_install"
        'plymouthd.conf'
        'plymouth.functions'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'system-release'
        'plymouth-halt.service'
        'plymouth-kexec.service'
        'plymouth-poweroff.service'
        'plymouth-quit-wait.service'
        'plymouth-quit.service'
        'plymouth-read-write.service'
        'plymouth-reboot.service'
        'plymouth-start.service'
        'systemd-ask-password-plymouth.path'
        'systemd-ask-password-plymouth.service'
        'kdm-plymouth.service'
        'gdm-plymouth.service'
        'lxdm-plymouth.service'
        'kdm-unpatched-plymouth.service'
        'plymouth-buildfixes.patch')

_gitroot='git://git.freedesktop.org/git/plymouth'
_gitname='plymouth'

build() {
  msg "Connecting to git.freedesktop.org GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin && cd ..
    msg "The local files are updated."
  else
    git clone --depth 1 ${_gitroot} ${_gitname}
  fi
  msg "GIT checkout done. Preparing sources..."

  cd "${srcdir}"
  rm -rf ${_gitname}-build
  cp -r ${_gitname} ${_gitname}-build

  msg "Applying Patches..."

  cd ${_gitname}-build

  # Remove GTK+ dependency and X11 Renderer, Fix initrd.
  patch -p0 -i "${srcdir}/plymouth-buildfixes.patch"

  sed -e 's:png_set_gray_1_2_4_to_8:png_set_expand_gray_1_2_4_to_8:g' \
       -i src/libply-splash-graphics/ply-image.c

  msg "Starting make..."

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --enable-tracing \
    --with-system-root-install \
    --enable-gdm-transition \
    --with-gdm-autostart-file=yes \
    --with-logo=/usr/share/plymouth/arch-logo.png \
    --with-background-start-color-stop=0x000000 \
    --with-background-end-color-stop=0x4D4D4D 
#    --enable-systemd-integration

  make
}

package() {
  cd ${_gitname}-build

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}/plymouth.initcpio_install" "${pkgdir}/usr/lib/initcpio/install/plymouth"
  install -Dm644 "${srcdir}/plymouth.initcpio_hook" "${pkgdir}/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "${srcdir}/encrypt_install" "${pkgdir}/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "${srcdir}/encrypt_hook" "${pkgdir}/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "${srcdir}/plymouth.functions" "${pkgdir}/etc/rc.d/functions.d/plymouth.functions"
  install -Dm644 "${srcdir}/arch-logo.png" "${pkgdir}/usr/share/plymouth/arch-logo.png"
  install -Dm644 "${srcdir}/system-release" "${pkgdir}/etc/system-release"
  install -Dm644 "${srcdir}/plymouthd.conf" "${pkgdir}/etc/plymouth/plymouthd.conf"

  # Plymouth<->systemd integration, copied from systemd --enable-plymouth installation
  install -Dm644 "${srcdir}/plymouth-halt.service" "${pkgdir}/usr/lib/systemd/system/plymouth-halt.service"
  install -Dm644 "${srcdir}/plymouth-kexec.service" "${pkgdir}/usr/lib/systemd/system/plymouth-kexec.service"
  install -Dm644 "${srcdir}/plymouth-poweroff.service" "${pkgdir}/usr/lib/systemd/system/plymouth-poweroff.service"
  install -Dm644 "${srcdir}/plymouth-quit-wait.service" "${pkgdir}/usr/lib/systemd/system/plymouth-quit-wait.service"
  install -Dm644 "${srcdir}/plymouth-quit.service" "${pkgdir}/usr/lib/systemd/system/plymouth-quit.service"
  install -Dm644 "${srcdir}/plymouth-read-write.service" "${pkgdir}/usr/lib/systemd/system/plymouth-read-write.service"
  install -Dm644 "${srcdir}/plymouth-reboot.service" "${pkgdir}/usr/lib/systemd/system/plymouth-reboot.service"
  install -Dm644 "${srcdir}/plymouth-start.service" "${pkgdir}/usr/lib/systemd/system/plymouth-start.service"
  install -Dm644 "${srcdir}/plymouth-start.service" "${pkgdir}/usr/lib/systemd/system/plymouth-start.service"
  install -Dm644 "${srcdir}/systemd-ask-password-plymouth.path" "${pkgdir}/usr/lib/systemd/system/systemd-ask-password-plymouth.path"
  install -Dm644 "${srcdir}/systemd-ask-password-plymouth.service" "${pkgdir}/usr/lib/systemd/system/systemd-ask-password-plymouth.service"
  install -Dm644 "${srcdir}/kdm-plymouth.service" "${pkgdir}/usr/lib/systemd/system/kdm-plymouth.service"
  install -Dm644 "${srcdir}/gdm-plymouth.service" "${pkgdir}/usr/lib/systemd/system/gdm-plymouth.service"
  install -Dm644 "${srcdir}/lxdm-plymouth.service" "${pkgdir}/usr/lib/systemd/system/lxdm-plymouth.service"
  install -Dm644 "${srcdir}/kdm-unpatched-plymouth.service" "${pkgdir}/usr/lib/systemd/system/kdm-unpatched-plymouth.service"
  # Hook up the units so systemd uses them, copied from systemd --enable-plymouth installation
  mkdir -p ${pkgdir}/usr/lib/systemd/system/{halt.target.wants,kexec.target.wants,multi-user.target.wants,poweroff.target.wants,reboot.target.wants,sysinit.target.wants}
  ln -s "/usr/lib/systemd/system/plymouth-halt.service" "${pkgdir}/usr/lib/systemd/system/halt.target.wants/"
  ln -s "/usr/lib/systemd/system/plymouth-kexec.service" "${pkgdir}/usr/lib/systemd/system/kexec.target.wants/"
  ln -s "/usr/lib/systemd/system/plymouth-quit-wait.service" "${pkgdir}/usr/lib/systemd/system/multi-user.target.wants/"
  ln -s "/usr/lib/systemd/system/plymouth-quit.service" "${pkgdir}/usr/lib/systemd/system/multi-user.target.wants/"
  ln -s "/usr/lib/systemd/system/plymouth-poweroff.service" "${pkgdir}/usr/lib/systemd/system/poweroff.target.wants/"
  ln -s "/usr/lib/systemd/system/plymouth-reboot.service" "${pkgdir}/usr/lib/systemd/system/reboot.target.wants/"
  ln -s "/usr/lib/systemd/system/plymouth-read-write.service" "${pkgdir}/usr/lib/systemd/system/sysinit.target.wants/"
  ln -s "/usr/lib/systemd/system/plymouth-start.service" "${pkgdir}/usr/lib/systemd/system/sysinit.target.wants/"
}

sha256sums=('9bac679d2494d9b60b288be87021f1d7b85a9503ebbdce93d6e37c0fc07568ae'
            'e0cbcabb81233b4d465833dca0faf1e762dc3cb6611597a25fe24e5d7209f316'
            'cfe465bdad3d958bb2332a05e04f2e1e884422a5714dfd1a0a3b9b74bf7dc6ae'
            '83ffd4aa52ca6a77423ab7edc2c2369808c7abffe68d97e134176c4592a81cc6'
            '19c14656385444f51ce5ad2f3b768b0c0e9b090ccd62841677b63090ebc05d7f'
            '91781cf1088e97cfc90bbe3be49bbdcf3a9b72de887e30f14f45efec8741ec91'
            '46f9174d6a2a0ec00c2741bef2b95c1a2c7d0edc2a7797e1eea1f8828eb00b63'
            '4ba2e16e7c6ed9c89e4999bd1ce5dd6e39e2db7447b5e43be384d0fbced67828'
            '3d6295ad7ac2f734d16d76a61952f1b920691bde1651df66f21724515b2f1584'
            'e1f64956b4ff54cacda5cc81fed9793639e9d8ab95890bb0bccd46113c67ecfa'
            'c2970cbd4d2c3a1b711c49d10d05c8e3d53892b615f382d1378583970860aea0'
            'f562a1cf427e767777f2b420954816c67c5e1af27f50b3c3c7bf1b80dc253ad9'
            '626a11f826a8faea597bc3c7ae637e37120bf56a74ae3581b132512921c2ecb5'
            '68ee92a7dfab2bc1e7c4505f1b7f5abce890f94e35736e24b8eae4f0faac5fcf'
            '296712c5a1ea8d2bc03ed7a7af746e24e1c3c1649d63cca30baa967eac191680'
            '2268929466022f51f3379c16536ec2d0153cc1a5c30f00fdcfbec44725bf0176'
            '44b671c0663b9f61f0e8758b2138c6dd447323f898a476c77c74fd401362bfaf'
            '45f9029c0857a6a93f9eb5fe4ad2656b8e849248a1b20651a28a2b5deb303d7e'
            '6fff44e4a8a3379c474634aecee6669dfc3bc29490ca37e4a2f86f4992d0d5e3'
            'ed62c3ce3b738f49e4a7dc185920045d850887cd1efbdde210023cd0eca06dd1'
            '93c7a792976525edd7203979c081e03e2d1194b38a4ae7a62965915a0dc1e9ae'
            '3c8fdedfed7b78d6e214b068146417efc7e2d452a7b361ba4d12474144a42ae2'
            '879a749f04ddfd4758322f8085fe32e7f23b0fb67aa40d1e43ef9fef64c4d23c')

# vim: set ts=2 sw=2 ft=sh noet:
