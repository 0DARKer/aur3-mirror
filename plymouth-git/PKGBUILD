#
# Maintainer: Jorge Barroso <jorge.barroso.11 at gmail dot com>
#
# Contributors: Abbradar
#               Zephyr
#               Christian Autermann
#               Biginoz
#               Martin Lee
#               Ricardo Funke
#               PirateJonno
#               lh
#               Cilyan Olowen
#               Shaffer
#               Brcha
#               Lyle Putnam
#               Det
#               Boohbah
#               Lara Maia
#		Padfoot

pkgname=plymouth-git

pkgver=0.9.0.12.g84eb438
pkgrel=1
pkgdesc="A graphical boot splash screen with kernel mode-setting support (Git version)"
url="http://www.freedesktop.org/wiki/Software/Plymouth/"

arch=('i686' 'x86_64')
license=('GPL')

depends=('libdrm' 'pango' 'systemd')
makedepends=('git' 'docbook-xsl')
optdepends=('ttf-dejavu')

options=('!libtool' '!emptydirs')

provides=('plymouth')
conflicts=('plymouth')
backup=('etc/plymouth/plymouthd.conf')

source=('git://anongit.freedesktop.org/plymouth' 
        'arch-logo.png'
        'plymouth.encrypt_hook'
        'plymouth.encrypt_install'
        'gdm-plymouth.service'
        'kdm-plymouth.service'
	    'lxdm-plymouth.service'
        'lightdm-plymouth.service'
        'slim-plymouth.service'
        'plymouth.initcpio_hook'
        'plymouth.initcpio_install'
        'plymouth-quit.service.in.patch'
        'plymouth-set-default-theme.in.patch'
        'plymouth-update-initrd.patch')

sha512sums=('SKIP'
            'fb1608179b51c7d50da71ec6bca358f0803b654062da0b0f1c9bad6d34f95b6943f1f890c256a1364e9aca6d3ba19dc5113361cc6cf0d8d76f064a8e38b42ca5'
            '0bc705106279dd8a4f612523c952167409185ae812fd3d2c3b48ee34912d49f79355fae73498858358caa447fcafef33a72906cb31f930d6dd9934a974da1f0a'
            '915a20606f83085e518b7cc7812f224af07bd96fc340db456802923dd2a9a7dafc9b6ab1f471ff1a742a50f6d5a5a225888adb69f2bfb8fcdefd390e20ce8f9f'
            '6203aed0eee2d9bfc6e31d7fc8fe9e3e3293ea30eb634f40ee687c96e887eea13571b3d852eec6179a70d929a1640ef36e107601a85eb5c55dc785fa63b5f37e'
            '3a219ab8663001b325c1108bddd2fd5624284d836c0e2c7c4f1ea9046b5d0aa4ed50e274d17ef4d87da1f9833a2d3d870773b6e5da5b9b25fa92718a65fc9aa7'
            '123e48527423d542fc838e3171e9130df7d87f054fc66be87b1a539a6c31b64edbf467cfc07901716845ba5c8250b39ac0a2ac2e7f3e0e563639867815ff0514'
            'f4dbd03e6f1d65d99ad5b521e24168ba92f6f6477fc2db5aa13bd6ce379959bce1a6a8647a91e7918937fb6acca3649fcf7b203b8cdd2fc8011a4d57a0db03f9'
            '7c735d3e3d6839c0bdea71e468720214d480c877363cd9078fbd5ba0c0bad42e2c3db1fb58baf9497b49a1389a8b248434f5d3e4a06f819994914abc5aadd49b'
            'ca95efa66dd7e97d9e61add750cddf152321a049638790c370807d2d8438b522349a104b43e9216e79e760dce070d009afcd8001d8d1e5f1e7e37b06d0f68984'
            '55edca629f3bedb76e97135950f452fa221a5a23e5994edf7bd67ad615e64259a19fe5f4ed2234912a44c19b3a01524b9be7d670f171cb9d2961bf487b39e574'
            'ffd4254ac218a80bd2559060d9cc611b6a8b82af0fa6e9790f15942d9d4ceff818bfa642ebb24280b99c4dfff03db8abdc76a9e5234630c8c88bd727bfe74cd8'
            'a51556ddbc8b563b08468e6bcdc2f5d9e90b3588522d1f72749e2e81235559b9f28dddfffdf49e792e8d9935400d1f6a80e597eadf2338398a2a1b4b7189dc3e'
            '64e6327299d360e58404e15764f2dbe6d390d55032e6ed6377ff14f73335efbb9f0d07c4808c626c3a52cb67d45ec5103336e8ba9bae0d7474e455a6438429bb')
        
pkgver() {
	cd "$srcdir"/${pkgname%%-*}
	git describe --always | sed 's/-/./g'
}

prepare() {
	cd "$srcdir"/${pkgname%%-*}
	
	patch -p1 -i $srcdir/plymouth-set-default-theme.in.patch
	patch -p1 -i $srcdir/plymouth-update-initrd.patch
	patch -p1 -i $srcdir/plymouth-quit.service.in.patch
}

build() {
	cd "$srcdir"/${pkgname%%-*}

	test -f Makefile && make distclean

	LDFLAGS="$LDFLAGS -ludev" ./autogen.sh --prefix=/usr \
		--exec-prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--sbindir=/usr/bin \
		--enable-systemd-integration \
		--enable-drm \
		--enable-tracing \
		--enable-pango \
		--enable-gtk=no \
		--with-release-file=/etc/os-release \
		--with-logo=/usr/share/plymouth/arch-logo.png \
		--with-background-color=0x000000 \
		--with-background-start-color-stop=0x000000 \
		--with-background-end-color-stop=0x4D4D4D \
		--without-rhgb-compat-link \
		--without-system-root-install

	make
}

package() {
  cd "$srcdir"/${pkgname%%-*}

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/arch-logo.png"             "$pkgdir/usr/share/plymouth/arch-logo.png"
  install -Dm644 "$srcdir/plymouth.encrypt_hook"     "$pkgdir/usr/lib/initcpio/hooks/plymouth-encrypt"
  install -Dm644 "$srcdir/plymouth.encrypt_install"  "$pkgdir/usr/lib/initcpio/install/plymouth-encrypt"
  install -Dm644 "$srcdir/plymouth.initcpio_hook"    "$pkgdir/usr/lib/initcpio/hooks/plymouth"
  install -Dm644 "$srcdir/plymouth.initcpio_install" "$pkgdir/usr/lib/initcpio/install/plymouth"

for i in {gdm,kdm,lxdm,slim,lightdm}-plymouth.service; do
  install -Dm644 "$srcdir/$i" "$pkgdir/usr/lib/systemd/system/$i"
done

cp "$pkgdir/usr/share/plymouth/plymouthd.defaults" "$pkgdir/etc/plymouth/plymouthd.conf"
}
