# Maintainer: Karel Louwagie <karel@louwagie.net>
# Contributor: Idares <idares@seznam.cz>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>
# Contributor: Vadym Abramchuk <abramm@gmail.com>
# Contributor: karol_007 <karol.blazewicz@gmail.com>

pkgname=zabbix-proxy-sqlite
pkgver=2.2.1
pkgrel=1
pkgdesc="Software for monitoring of your applications, network and servers."
arch=('i686' 'x86_64')
url="http://www.zabbix.com"
license=('GPL')
depends=('sqlite3' 'openipmi' 'libxml2')
backup=('etc/zabbix/zabbix_proxy.conf'
		)
install="zabbix-proxy.install"
source=("http://downloads.sourceforge.net/project/zabbix/ZABBIX%20Latest%20Stable/$pkgver/zabbix-$pkgver.tar.gz"
        "zabbix-proxy.install"
	"zabbix-proxy.service"
	"zabbix-proxy.tmpfiles"
	)

build() {
	cd $srcdir/zabbix-$pkgver
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/zabbix \
		--enable-proxy \
		--enable-ipv6 \
		--with-sqlite3 \
		--with-ssh2 \
		--with-net-snmp \
		--with-openipmi \
		--with-libxml2 \
		--with-libcurl

	make
}

package() {
	cd $srcdir/zabbix-$pkgver
	make DESTDIR=$pkgdir install

	install -d -m0750 $pkgdir/var/log/zabbix

	install -D -m0644 $srcdir/zabbix-proxy.service $pkgdir/usr/lib/systemd/system/zabbix-proxy.service

	# change pid file location
	sed -i 's:# PidFile=.*:PidFile=/run/zabbix/zabbix_proxy.pid:' $pkgdir/etc/zabbix/zabbix_proxy.conf

	# tmpfile
	install -D -m 0644 $srcdir/zabbix-proxy.tmpfiles $pkgdir/usr/lib/tmpfiles.d/zabbix-proxy.conf
}

md5sums=('558497839b13a548f533eb2fc951a4e3'
         '03a770db9cfaa2ae29f52035e6dbbf0f'
         '859debf8351561655532fcef05c28325'
         '9ce692356b4ac0a71595ce55fe3b44c1')
sha1sums=('2f07bfa08123f341ff16df88590d154282ce704f'
          'ca05eedf61a31a6787e6282c5f80b856004cac78'
          '4f4c4d878bcd454d3859df6b5165ef792e087666'
          '8926befcb944732fd59a34c89b569d3fbef1ca9d')

