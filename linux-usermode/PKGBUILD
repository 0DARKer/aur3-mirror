pkgname=linux-usermode
_basekernel=3.4
pkgver=3.4.4
pkgrel=1
_kernelname=-usermodelinux
pkgdesc="User mode Linux kernel and modules"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://user-mode-linux.sourceforge.net/"
depends=('coreutils')
source=(ftp://ftp.kernel.org/pub/linux/kernel/v3.0/linux-$pkgver.tar.bz2
	config)
md5sums=('6b8420c24e6f25c56824ce085d194687'
         'fbd3cd9253f86d0f3a0d37286abd13e2')

build() {
  cd "${srcdir}/linux-$pkgver"

  unset LDFLAGS
  sed -i '1,1i#include <sys/resource.h>' arch/um/os-Linux/start_up.c

  cp $srcdir/config .config
  if [ "${_kernelname}" != "" ]; then
    sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"${_kernelname}\"|g" ./.config
    sed -i "s|CONFIG_LOCALVERSION_AUTO=.*|CONFIG_LOCALVERSION_AUTO=n|g" ./.config
  fi
  make ARCH=um vmlinux modules -j5
}

package() {
  cd "${srcdir}/linux-$pkgver"
  unset LDFLAGS
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/kernel26-usermode"
  make ARCH=um INSTALL_MOD_PATH="${pkgdir}/usr" modules_install
  cp System.map ${pkgdir}/usr/share/kernel26-usermode/System.map
  cp vmlinux ${pkgdir}/usr/bin/
  rm -f $pkgdir/usr/lib/modules/${_basekernel}/{source,build}
}
