# AUR workaround
pkgname=linux-usermode
#pkgname=(linux-usermode linux-usermode-modules)

pkgbase=linux-usermode
_kernelname=-usermodelinux
pkgver=3.4.5
pkgrel=1
pkgdesc="User mode Linux kernel and modules"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://user-mode-linux.sourceforge.net/"
depends=('coreutils')
makedepends=('vde2')
source=(ftp://ftp.kernel.org/pub/linux/kernel/v3.0/linux-$pkgver.tar.bz2
	config)
md5sums=('59a21893b7a410a83328d38e094cd386'
         'fbd3cd9253f86d0f3a0d37286abd13e2')

build() {
  cd "${srcdir}/linux-$pkgver"

  unset LDFLAGS
  sed -i '1,1i#include <sys/resource.h>' arch/um/os-Linux/start_up.c

  cp $srcdir/config .config
  if [ "${_kernelname}" != "" ]; then
    sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"${_kernelname}\"|g" ./.config
    sed -i "s|CONFIG_LOCALVERSION_AUTO=.*|CONFIG_LOCALVERSION_AUTO=n|g" ./.config
  fi
  make ARCH=um vmlinux modules
}

package_linux-usermode() {
  cd "${srcdir}/linux-$pkgver"
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/kernel26-usermode"
  cp System.map ${pkgdir}/usr/share/kernel26-usermode/System.map
  cp vmlinux ${pkgdir}/usr/bin/
}

package_linux-usermode-modules() {
  cd "${srcdir}/linux-$pkgver"
#  make ARCH=um INSTALL_MOD_PATH="${pkgdir}/usr" modules_install
  make ARCH=um INSTALL_MOD_PATH="${pkgdir}/usr" _modinst_
  rm -f $pkgdir/usr/lib/modules/${pkgver}${_kernelname}/{source,build}
}
