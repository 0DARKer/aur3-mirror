pkgname=linux-usermode
_basekernel=3.3
pkgver=3.3.4
pkgrel=1
_kernelname=-usermodelinux
pkgdesc="User mode Linux kernel and modules"
arch=(i686 x86_64)
license=('GPL2')
url="http://user-mode-linux.sourceforge.net/"
depends=('coreutils')
source=(ftp://ftp.kernel.org/pub/linux/kernel/v3.0/linux-$pkgver.tar.bz2)
md5sums=('113baeccd2a3341ecc62ae5e73c2ed1d')
conflicts=('kernel26-usermode')

build() {
  cd "${srcdir}/linux-$pkgver"

  make ARCH=um defconfig

  if [ "${_kernelname}" != "" ]; then
    sed -i "s|CONFIG_LOCALVERSION=.*|CONFIG_LOCALVERSION=\"${_kernelname}\"|g" ./.config
    sed -i "s|CONFIG_LOCALVERSION_AUTO=.*|CONFIG_LOCALVERSION_AUTO=n|g" ./.config
  fi

  LDFLAGS="" make ARCH=um vmlinux modules
}

package() {
  cd "${srcdir}/linux-$pkgver"

  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/kernel26-usermode"

  LDFLAGS="" make ARCH=um INSTALL_MOD_PATH="${pkgdir}/usr" modules_install

  cp System.map ${pkgdir}/usr/share/kernel26-usermode/System.map && \
  cp vmlinux ${pkgdir}/usr/bin/ && \
  rm -f $pkgdir/usr/lib/modules/${_basekernel}/{source,build}
}
