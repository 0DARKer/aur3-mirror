# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-fba-neogeo-git
pkgver=784.da63555
pkgrel=1
pkgdesc="libretro implementation of Final Burn Alpha's NeoGeo core (FBA). (Neo Geo)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/fba-libretro"
license=('custom:FB Alpha License')
makedepends=('git')

_gitname=fba-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/fba_cores_neo_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}/svn-current/trunk/fbacores/neogeo"
  make -f makefile.libretro
}

package() {
  install -Dm644 "${_gitname}/svn-current/trunk/src/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
  install -Dm644 "${_gitname}/svn-current/trunk/fbacores/neogeo/fba_cores_neo_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-fba-neogeo.so"
  install -Dm644 "fba_cores_neo_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-fba-neogeo.info"
}
