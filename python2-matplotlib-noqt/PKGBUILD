# $Id: PKGBUILD 40554 2011-02-25 11:55:09Z stephane $
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: John McDonnell <john.v.mcdonnell@gmail.com> (removed qt dependency)

pkgname=python2-matplotlib-noqt
pkgver=1.0.1
pkgrel=2
pkgdesc="A python plotting library, making publication quality plots. This version does not depend on qt."
arch=('i686' 'x86_64')
url="http://matplotlib.sourceforge.net/"
backup=(usr/lib/python2.7/site-packages/matplotlib/mpl-data/matplotlibrc)
depends=('python-dateutil' 'python2-pytz' 'python2-numpy' 'python2-pyparsing' 'pygtk' )
optdepends=('tk: used by the TkAgg backend'
            'wxpython: for use with the WXAgg backend'
            'python2-qt: for use with the QT backend')
makedepends=('pygtk' 'tk' 'wxpython')
provides=('matplotlib' "python-matplotlib=${pkgver}")
replaces=('python-matplotlib'
          'python2-matplotlib')
conflicts=('python-matplotlib'
           'python2-matplotlib')
source=("http://downloads.sourceforge.net/matplotlib/matplotlib-${pkgver}.tar.gz"
        'setup.cfg'
        'tkinter.patch')
sha1sums=('c7a832f28a66817626e7a8af21e14ea0e15f4008'
          'ed08c3761dc291d92805a652123207bc896f5b6f'
          'c8276d3700249be34757223375109420a21aa149')
license=('custom')

build() {
   patch -p0 < tkinter.patch
   
   cd "${srcdir}/matplotlib-${pkgver}"
   cp ../setup.cfg .
    
   # python2 fix
   for file in $(find . -name '*.py' -print); do
      sed -i -e "s|^#!.*/usr/bin/python|#!/usr/bin/python2|" \
             -e "s|^#!.*/usr/bin/env *python|#!/usr/bin/env python2|" ${file}
   done
    
   python2 setup.py build
}

package() {
   cd "${srcdir}/matplotlib-${pkgver}"
   python2 setup.py install -O1 --skip-build --root "${pkgdir}" --prefix=/usr
    
   install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
   mv license "${pkgdir}/usr/share/licenses/${pkgname}"
}
