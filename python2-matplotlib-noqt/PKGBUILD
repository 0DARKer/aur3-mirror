# $Id$
# Maintainer: Pedro Martinez-Julia <pedromj@um.es>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: John McDonnell <john.v.mcdonnell@gmail.com> (removed qt dependency)

pkgname=python2-matplotlib-noqt
pkgver=1.1.1
pkgrel=1
pkgdesc="A python plotting library, making publication quality plots. This version depends on gtk rather than qt."
arch=('i686' 'x86_64')
url="http://matplotlib.sourceforge.net/"
backup=()
depends=('python2-dateutil' 'python2-pytz' 'python2-numpy' 'python2-pyparsing' 'pygtk' 'libpng')
optdepends=('tk: used by the TkAgg backend'
            'wxpython: for use with the WXAgg backend'
            'python2-qt: for use with the QT backend')
makedepends=('pygtk' 'tk' 'wxpython')
provides=('matplotlib' "python-matplotlib=${pkgver}")
replaces=('python-matplotlib'
          'python2-matplotlib')
conflicts=('python-matplotlib'
           'python2-matplotlib')
source=("http://downloads.sourceforge.net/matplotlib/matplotlib-${pkgver}.tar.gz"
         setup.cfg)
sha1sums=('e47ecb0c651961dbaba57fd172e3b770da197e84'
          'ed08c3761dc291d92805a652123207bc896f5b6f')
license=('custom')

build() {
   cd "${srcdir}/matplotlib-${pkgver}"

   cp ../setup.cfg .

   # remove internal copy of pyparsing
   rm lib/matplotlib/pyparsing.py
   sed -i "s/matplotlib.pyparsing/pyparsing/g" lib/matplotlib/{mathtext,fontconfig_pattern}.py

   # python2 fix
   for file in $(find . -name '*.py' -print); do
      sed -i -e "s|^#!.*/usr/bin/python|#!/usr/bin/python2|" \
             -e "s|^#!.*/usr/bin/env *python|#!/usr/bin/env python2|" ${file}
   done

   python2 setup.py build
}

package() {
   cd "${srcdir}/matplotlib-${pkgver}"
   python2 setup.py install -O1 --skip-build --root "${pkgdir}" --prefix=/usr
    
   install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
   install -m 644 doc/users/license.rst "${pkgdir}/usr/share/licenses/${pkgname}"
}
