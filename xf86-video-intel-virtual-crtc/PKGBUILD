# Maintainer: Gordin <gordin @t gmail dot com>

pkgname=xf86-video-intel-virtual-crtc
_pkgname=xf86-video-intel
pkgver=2.19.0
pkgrel=1
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers, with virtual crtc patch"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('intel-dri' 'libxvmc' 'xcb-util>=0.3.9' 'udev>=183')
makedepends=('xorg-server-devel>=1.12.0' 'libx11' 'libdrm' 'xf86driproto' 'glproto' 'mesa' 'libxvmc' 'libxrender')
conflicts=('xorg-server<1.12.0' 'xf86-video-i810' 'xf86-video-intel-legacy' 'xf86-video-intel')
provides=('xf86-video-intel')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2
        'https://raw.github.com/gordin/patches/master/hacks/xserver-xorg-video-intel-2.19.0_virtual_crtc.patch')
sha1sums=('131934cf5e90bd48c883804cc644c4cb8b1de100'
          '41a5e43279b8c05815f37a583b1744e7db614758')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p1 < ${srcdir}/xserver-xorg-video-intel-2.19.0_virtual_crtc.patch
  ./configure --prefix=/usr --enable-dri
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
