# Maintainer: Gordin <gordin @t gmail dot com>

pkgname=xf86-video-intel-virtual-crtc
_pkgname=xf86-video-intel
pkgver=2.20.2
pkgrel=1
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
install=$pkgname.install
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers, with virtual crtc patch"
depends=('intel-dri' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util>=0.3.9' 'libxfixes' 'systemd-tools')
makedepends=('xorg-server-devel>=1.12.0' 'libx11' 'libdrm' 'xf86driproto' 'glproto' 'mesa' 'libxvmc' 'libxrender')
replaces=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
provides=('xf86-video-intel' 'xf86-video-intel-uxa' 'xf86-video-intel-sna')
conflicts=('xf86-video-intel-sna' 'xf86-video-intel-uxa' 'xorg-server<1.12.0' 'xf86-video-i810' 'xf86-video-intel-legacy' 'xf86-video-intel')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${_pkgname}-${pkgver}.tar.bz2
        fix_gen4.diff
        'http://store.lisk.in/tmp/xserver-xorg-video-intel-2.20.2_virtual_crtc.patch')
        # 'https://raw.github.com/gordin/patches/master/hacks/xserver-xorg-video-intel-2.20.2_virtual_crtc.patch')
sha1sums=('253360dfc5dad3e6d620ce4726e0ce8a050c0e1a'
          '2edafa8c535643d9586f5e80b7caeee8cd68addb'
          'c7be399c6789219d02af2b22e5f3a8c248c9a257')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/fix_gen4.diff
  patch -Np1 -i ${srcdir}/xserver-xorg-video-intel-2.20.2_virtual_crtc.patch
  ./configure --prefix=/usr \
    --enable-dri
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
