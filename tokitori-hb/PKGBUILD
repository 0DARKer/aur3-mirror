# Maintainer: Vianney le Clément <vleclement AT gmail · com>
pkgname=tokitori-hb
pkgver=20120208
pkgrel=1
pkgdesc="A highly entertaining puzzle platformer (Humble Bundle copy required)"
arch=('i686' 'x86_64')
url="http://www.tokitori.com/"
license=('custom:commercial')
groups=('humblebundleandroid')
depends=('libgl' 'libxft')
makedepends=('tar')
options=(!strip)
source=("TokiTori-HIB-3.sh"::"file://TokiTori-HIB-3.sh"
        tokitori.desktop)
md5sums=('22fdec410051088aab8cf32d6cf78200'
         '1f829157625accf6c1ff8917edd148c4')
#PKGEXT='.pkg.tar'

_carch=${CARCH/i686/x86}

package() {
  cd "$srcdir"

  sh TokiTori-HIB-3.sh --tar xf
  _lzma="bin/linux/$_carch/libc.so.6/lzma-decode"
  $_lzma subarch - | tar x

  mkdir "$pkgdir/opt"

  $_lzma "instarchive_all" - | tar x -C "$pkgdir/opt/"
  $_lzma "instarchive_all_$_carch" - | tar x -C "$pkgdir/opt/"
  chown -R root:root "$pkgdir/opt/TokiTori/"

  install -d "$pkgdir/usr/bin"
  ln -s /opt/TokiTori/TokiTori.bin.$_carch "$pkgdir/usr/bin/tokitori"

  install -Dm644 tokitori.desktop "$pkgdir/usr/share/applications/tokitori.desktop"
}

# vim:set ts=2 sw=2 et:
