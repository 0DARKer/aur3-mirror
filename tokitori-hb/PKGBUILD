# Maintainer: Vianney le Clément <vleclement AT gmail · com>
pkgname=tokitori-hb
pkgver=20130705
_pkgver=2013-07-05
pkgrel=1
pkgdesc="A highly entertaining puzzle platformer (Humble Bundle copy required)"
arch=('i686' 'x86_64')
url="http://www.tokitori.com/"
license=('custom:commercial')
groups=('humblebundleandroid')
depends=('libgl' 'libxft')
makedepends=('tar')
options=(!strip)
source=("TokiTori-Linux-$_pkgver.sh"::"file://TokiTori-Linux-$_pkgver.sh"
        tokitori.desktop)
md5sums=('6ea3506e0ea9182d642f34c2340efab4'
         'd39aebb24d37a682b59351d153e860d1')
#PKGEXT='.pkg.tar'

_carch=${CARCH/i686/x86}

prepare() {
  cd "$srcdir"
  sh TokiTori-Linux-$_pkgver.sh --tar xf
}

package() {
  cd "$srcdir"
  _lzma="bin/linux/$_carch/libc.so.6/lzma"
  $_lzma d subarch - | tar x

  mkdir -p "$pkgdir/opt/tokitori"

  $_lzma d "instarchive_all" - | tar x --no-same-owner -C "$pkgdir/opt/tokitori/"
  $_lzma d "instarchive_all_$_carch" - | tar x --no-same-owner -C "$pkgdir/opt/tokitori/"

  install -d "$pkgdir/usr/bin"
  ln -s /opt/tokitori/TokiTori.bin.$_carch "$pkgdir/usr/bin/tokitori"

  install -Dm644 tokitori.desktop "$pkgdir/usr/share/applications/tokitori.desktop"
}

# vim:set ts=2 sw=2 et:
