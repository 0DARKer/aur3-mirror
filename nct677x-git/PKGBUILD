# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname=nct677x-git
pkgver=20120829
pkgrel=1
pkgdesc="Nuvoton lm-sensors module (NCT6775F/NCT6776F/NCT6779D) for motherboards such as the Asus P8Z77 series."
arch=('i686' 'x86_64')
url="https://github.com/groeck/nct6775"
license=('GPL')
makedepends=('git' 'linux-headers')
depends=('linux>3.5' 'linux<3.6')
install=nct677x.install

_extramodules=extramodules-3.5-ARCH
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
_gitroot=https://github.com/groeck/nct6775
_gitname=nct6775

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # fix up Makefile for Arch Linux
  sed -i -e 's/headers-//' -i -e 's/ko/ko.gz/' Makefile
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  gzip -9 nct6775.ko
  install -Dm644 nct6775.ko.gz "$pkgdir"/usr/lib/modules/$_kernver/kernel/drivers/hwmon/nct6775.ko.gz
}
