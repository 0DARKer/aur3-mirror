# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname=nct677x-git
_pkgname=nct6775
pkgver=nct6775.v1.1.20.g6d58723
pkgrel=2
pkgdesc="Nuvoton module for nct677x series of chips not yet supported in mainline kernel."
arch=('i686' 'x86_64')
url="https://github.com/groeck/nct6775"
license=('GPLv2')
depends=('linux>=3.17' 'linux<3.18')
makedepends=(linux-headers)
install=nct677x.install
md5sums=('SKIP')

source=("git://github.com/groeck/nct6775.git")

_extramodules="extramodules-3.17-ARCH"
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

pkgver() {
	cd ${_pkgname}
	git describe --always | sed 's|-|.|g'
}

prepare() {
# headers location and remove shell dependency on running kernel to figure kern ver
	sed -i -e '/^KERNEL_BUILD/ s,src/linux-headers-,lib/modules/,' \
		-i -e '/^KERNEL_BUILD/ s,TARGET),TARGET)/build,' "$_pkgname/Makefile" 
}

build() {
	cd "$_pkgname"
	make
}

package() {
	cd "$_pkgname"
	gzip -9 nct6775.ko
	#install -Dm644 nct6775.ko.gz "$pkgdir"/usr/lib/modules/$_kernver/kernel/drivers/hwmon/nct6775.ko.gz
	install -Dm644 nct6775.ko.gz "$pkgdir"/usr/lib/modules/$_kernver/updates/nct6775.ko.gz
}
