# Maintainer: Alexander De Sousa <archaur.xandy21@spamgourmet.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gtk-engine-murrine-git
pkgver=20120514
pkgrel=1
pkgdesc="GTK2 Murrine engine, development code."
arch=('i686' 'x86_64')
license=('LGPLv3')
depends=('gtk2')
makedepends=('git' 'intltool')
url="http://cimitan.com/murrine/"
conflicts=('gtk-engine-murrine')
provides=('gtk-engine-murrine')
options=(!libtool)
source=(automake-1.12.patch)
md5sums=('e3b5930627f1e1773a020219e0880dc0')
sha1sums=('bc41109e303c6c0eefbcf9efc05ffee6af133b24')

_gitroot="git://git.gnome.org/murrine"
_gitname="gtk-engine-murrine"

build() {
  cd "$srcdir"

  msg2 "Connecting to GIT server..."
  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg2 "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi
  msg2 "GIT checkout done or server timeout."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  msg2 "Patching..."
  patch -Nup1 -i ../automake-1.12.patch
  msg2 "Starting build..."
  ./autogen.sh \
   --prefix=/usr \
   --enable-animation \
   --enable-animationrtl
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}

# vim: set ft=sh ts=2 sw=2 et:
