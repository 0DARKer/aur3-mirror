# Maintainer: Alexander De Sousa <archaur.xandy21@spamgourmet.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gtk-engine-murrine-git
_gitname=murrine
pkgver=403.e32517b
pkgrel=1
pkgdesc="GTK2 Murrine engine, development code."
arch=('i686' 'x86_64')
license=('LGPLv3')
depends=('gtk2')
makedepends=('git' 'intltool')
url="http://cimitan.com/murrine/"
conflicts=('gtk-engine-murrine')
provides=('gtk-engine-murrine')
options=(!libtool)
source=("git://git.gnome.org/murrine"
        automake-1.12.patch
        automake-1.13.patch)
md5sums=('SKIP'
        'e3b5930627f1e1773a020219e0880dc0'
        '9d3409060ced1b970323c59f379b4bdd')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir"

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  msg2 "Patching..."
  patch -Nup1 -i ../automake-1.12.patch
  patch -Nup1 -i ../automake-1.13.patch
  msg2 "Starting build..."
  ./autogen.sh \
   --prefix=/usr \
   --enable-animation \
   --enable-animationrtl
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install
}

# vim: set ft=sh ts=2 sw=2 et:
