pkgname=mingw-w64-tinyxml
pkgver=2.6.2
pkgrel=1
pkgdesc="Simple, small, C++ XML parser that can be easily integrated into other programs (mingw-w64)"
arch=(any)
url="http://www.grinninglizard.com/tinyxml"
license=("zlib")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt)
options=(!strip !buildflags staticlibs)
source=("http://downloads.sourceforge.net/tinyxml/tinyxml_${pkgver//./_}.tar.gz"
"entity.patch"
"tinyxml-2.5.3-stl.patch"
"tinyxml.pc")
sha256sums=('15bdfdcec58a7da30adc87ac2b078e4417dbe5392f3afb719f9ba6d062645593'
            'ef493209b0a51160171fd834a7ecdddd02679463b85fb89a2ea254213e47f99b'
            '3baf2c4dbc2c8f54a151dac8860113d2f549174f83ed85d552b094dfaebb52af'
            '3a658c2d84d461cd64f48aa489baf78bcf06ad193d18455f3e9048a52a1f3b9d')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	cd "$srcdir/tinyxml"
  patch -p0 -i "$srcdir"/entity.patch
  patch -p1 -i "$srcdir"/tinyxml-2.5.3-stl.patch
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cp -r "${srcdir}/tinyxml/"* "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    for file in $(ls *.cpp | grep -v xmltest); do
			${_arch}-g++ -c -Wall -Wno-unknown-pragmas -Wno-format -O3 $file
		done
		${_arch}-ar rcs -o libtinyxml.a $(ls *.o)
    ${_arch}-g++ -shared -o libtinyxml-0.dll -Xlinker --out-implib -Xlinker libtinyxml.dll.a $(ls *.o)
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    install -Dm644 libtinyxml.a "$pkgdir/usr/${_arch}/lib/libtinyxml.a"
    install -Dm644 libtinyxml-0.dll "$pkgdir/usr/${_arch}/bin/libtinyxml-0.dll"
    install -m644 libtinyxml.dll.a "$pkgdir/usr/${_arch}/lib/"
    install -Dm644 "$srcdir/tinyxml.pc" "$pkgdir/usr/${_arch}/lib/pkgconfig/tinyxml.pc"
    install -Dm644 tinyxml.h "$pkgdir/usr/${_arch}/include/tinyxml.h"
    install -m644 tinystr.h "$pkgdir/usr/${_arch}/include/"
    sed -i "s,@ARCH@,${_arch}," "$pkgdir/usr/${_arch}/lib/pkgconfig/tinyxml.pc"
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
