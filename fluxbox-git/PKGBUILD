# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Devin Cofer <ranguvar@archlinux.us>
# Contributor: Cain√£ Costa <sryche@archlinux-br.org>
# Contributor: Patrick Bartels <p4ddy.b@gmail.com>

_pkgname=fluxbox
pkgname=$_pkgname-git
pkgdesc="Lightweight stacking WM with tabbing, dockapps, etc."
pkgver=20130318
pkgrel=1
arch=(i686 x86_64)
url=http://www.fluxbox.org/
license=(MIT)
depends=(fribidi imlib2 libxft libxinerama libxpm libxrandr)
makedepends=(git)
optdepends=('xorg-xmessage: for using the fbsetbg and fluxbox-generate_menu utilities')
provides=($_pkgname)
conflicts=($_pkgname)
options=(!libtool)
install=$_pkgname.install
source=($_pkgname.desktop)
sha256sums=('32ed3d444c938a3778520a641d5e0196a0fdf2984390d69a6546f8b86805c841')
sha512sums=('4c46bb22745849982b2e1ea02cc84f77bf81eeac827b10574eabcb5f2375805fe8b204a58aed15555b2477b1b9cc3f9e0aa4575a92ad9315495735a8e7ba40d1')

_gitroot=git://git.fluxbox.org/$_pkgname.git
_gitname=$_pkgname

build() {
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone --depth 1 $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    autoreconf -fi
    ./configure --prefix=/usr --enable-nls --enable-gnome
    make
}

package() {
    cd $_gitname-build/
    make DESTDIR="$pkgdir" install
    desktop-file-install ../$_pkgname.desktop --dir "$pkgdir"/usr/share/applications/
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
