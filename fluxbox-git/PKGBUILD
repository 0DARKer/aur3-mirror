# Maintainer:  Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Cain√£ Costa <sryche@archlinux-br.org>
# Contributor: Patrick Bartels <p4ddy.b@gmail.com>

pkgname=fluxbox-git
_pkgname=fluxbox
pkgdesc="Lightweight stacking WM with tabbing, dockapps, etc. (Dev version)"
pkgver=20091012
pkgrel=1
arch=('i686' 'x86_64')
license=('MIT')
url="http://www.fluxbox.org/"

makedepends=('git' 'pkgconfig')
depends=('libxft' 'libxpm' 'libxrandr' 'imlib2' 'libxinerama')
conflicts=('fluxbox')
provides=('fluxbox')

source=("$_pkgname.desktop")
sha256sums=('e1e6cc6a7e55df1981ff5df1bd345221e657ed68ac12583553b951e454695d67')

backup=('usr/bin/startfluxbox')

_gitroot="git://git.fluxbox.org/fluxbox.git"
_gitname="fluxbox"


options=('!libtool')
build() {
	cd "$srcdir"
	msg "Performing source checkout..."
	if [ -d "$_gitname" ]; then
		cd "$_gitname"
		git pull origin || return 1
		cd ..
	else
		git clone "$_gitroot" || return 1
	fi
	msg "Source checkout finished."
	rm -rf "$_gitname-build"
	git clone "$_gitname" "$_gitname-build" || return 1
	cd "$_gitname-build"

	./autogen.sh || return 1
	./configure --prefix=/usr --enable-xft --enable-imlib2 --enable-nls \
	            --enable-xinerama --enable-gnome || return 1
	make || return 1
}
package() {
	cd "$srcdir/$_gitname-build"

	make DESTDIR="$pkgdir" install || return 1

	install -Dm644 "$srcdir/$_pkgname.desktop" \
	               "$pkgdir/etc/X11/sessions/$_pkgname.desktop" || return 1
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING" || return 1
}
