# Contributor: Kaiting Chen (kaiting <dot> chen <at> kiwilight <dot> com)
# Contributor: Gaetan Bisson (bisson <at> archlinux <dot> org)
# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)

pkgname='selinux-cronie'
_origname='cronie'
pkgver=1.4.7
pkgrel=9
pkgdesc="Daemon that runs specified programs at scheduled times and related tools with SELinux support"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/cronie/"
license=('custom:BSD')
groups=('selinux' 'selinux-system-utilities')
depends=('selinux-pam' 'bash' 'run-parts' 'selinux-usr-libselinux')
optdepends=('smtp-server: sending cron job output via email')
conflicts=('cron' "${_origname}")
provides=('cron' "${_origname}=${pkgver}-${pkgrel}")
backup=('etc/crontab'
        'etc/anacrontab'
        'etc/conf.d/crond'
        'etc/pam.d/crond'
        'etc/cron.deny')
source=("https://fedorahosted.org/releases/c/r/${_origname}/${_origname}-${pkgver}.tar.gz"
        'cron.deny'
        'crontab'
        'pam.d'
        'rc.d')
sha1sums=('c6644ba0e58bcb14e0bb3f925e3e8cc3f0d47a7f'
          '0f279b8fb820340267d578dc85511c980715f91e'
          '4059bc4ccb75f08b0d4970940799e5d9722b339f'
          '5eff7fb31f6bc0a924243ff046704726cf20c221'
          'c08c040ed5cb12bc4fd15639a5242d31ec247ef5')

build() {
	cd "${srcdir}/${_origname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-anacron \
    --without-audit \
    --with-selinux \
		--with-inotify \
		--with-pam \

	make
}

package() {
	cd "${srcdir}/${_origname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/etc/cron.{d,hourly,daily,weekly,monthly}
	install -d "${pkgdir}"/var/spool/{ana,}cron
	chmod u+s "${pkgdir}"/usr/bin/crontab

	install -Dm755 ../rc.d "${pkgdir}"/etc/rc.d/crond
	install -Dm644 ../pam.d "${pkgdir}"/etc/pam.d/crond
	install -Dm644 ../crontab "${pkgdir}"/etc/crontab
	install -Dm644 ../cron.deny "${pkgdir}"/etc/cron.deny
	install -Dm644 crond.sysconfig "${pkgdir}"/etc/conf.d/crond
	install -Dm644 contrib/0hourly "${pkgdir}"/etc/cron.d/0hourly
	install -Dm755 contrib/0anacron "${pkgdir}"/etc/cron.hourly/0anacron
	install -Dm644 contrib/anacrontab "${pkgdir}"/etc/anacrontab

	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/cronie/COPYING
}
