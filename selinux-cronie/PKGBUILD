# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Kaiting Chen (kaiting <dot> chen <at> kiwilight <dot> com)

pkgname='selinux-cronie'
_origname='cronie'
pkgver=1.4.7
pkgrel=1
pkgdesc="Fedora fork of vixie-cron with PAM and SELinux support"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/cronie/"
license=('custom:ISC')
groups=('selinux' 'selinux-system-utilities')
depends=('selinux-pam' 'glibc' 'selinux-usr-libselinux')
conflicts=('vixie-cron' 'cronie' 'dcron')
provides=('cron' 'dcron' 'cronie=1.4.5')
backup=(etc/crontab etc/pam.d/crond)
source=(crontab crond.pam.d crond.rc.d run-cron 
	https://fedorahosted.org/releases/c/r/${_origname}/${_origname}-${pkgver}.tar.gz)
md5sums=('c7f1341eb31cb6be05313e735d3d6404'
         'd688904def891b8037dc3a06ab047f03'
         '9b415c8a1faa44aa0538dcea88b432ec'
         '00ede56aadf073c839e600033fbd6cb4'
         'dfc26c47756d0c40ee27ae3c7ee98e0d')


build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-anacron \
    --without-audit \
    --with-selinux \
    --with-pam \
    --with-inotify
  make
}

package(){
  cd "${srcdir}/${_origname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Must set suid bit on file manually
  chmod u+s "${pkgdir}/usr/bin/crontab"

  # Replaced Fedora PAM configuration
  install -Dm644 "${srcdir}/crond.pam.d" "${pkgdir}/etc/pam.d/crond"

  # Install crond initscript
  install -Dm755 "${srcdir}/crond.rc.d" "${pkgdir}/etc/rc.d/crond"

  # Install `run-cron` script
  install -Dm755 "${srcdir}/run-cron" "${pkgdir}/usr/bin/run-cron"

  # Install system crontab and directories
  install -Dm644 "${srcdir}/crontab" "${pkgdir}/etc/crontab"
  install -d "${pkgdir}/etc/cron.d"
  install -d "${pkgdir}/etc/cron.hourly"
  install -d "${pkgdir}/etc/cron.daily"
  install -d "${pkgdir}/etc/cron.weekly"
  install -d "${pkgdir}/etc/cron.monthly"

  # Install custom:ISC license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/cronie/COPYING"
}
