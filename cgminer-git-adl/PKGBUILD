# Maintainer: deusstultus <deusstultus@gmail.com>
# Contributor: deusstultus < deusstultus@gmail.com >
# Derived from cgminer-git package http://aur.archlinux.org/packages.php?ID=58194

_pkgname=cgminer
pkgname=cgminer-git-adl
pkgver=20120709
pkgrel=1
pkgdesc="Multi-threaded multi-pool CPU and GPU miner for bitcoin, forked from cpuminer with AMD ADL extension."
url='http://forum.bitcoin.org/index.php?topic=28402.0'
license=('GPL3' 'custom')
#Note license was GPL2 prior to Tue Feb 21 22:23:07 2012 +1100 if playing with old versions with this PKGBUILD
arch=('i686' 'x86_64')
depends=('ncurses' 'curl' 'libcl')
conflicts=('cgminer' 'cgminer-git')
makedepends=('opencl-headers' 'git' 'unzip')
optdepends=('opencl-nvidia: OpenCL implementation for NVIDIA'   \
            'intel-opencl-sdk: OpenCL implementation for Intel' \
            'amdstream: OpenCL implementation for AMD')
source=("$_pkgname.conf.d"
        "$_pkgname"
	"http://download2-developer.amd.com/amd/GPU/zip/ADL_SDK_4.0.zip")
noextract=('ADL_SDK_4.0.zip')
backup=("etc/conf.d/$_pkgname" "etc/$_pkgname.conf")
md5sums=('fe4a243fabe24608f5c05e40f0f118f6'
         'ee39698273671fee0e98d4af16014c36'
	 '5cf94cda3853cabdd6c40a0c2cffe40c')
	 
_gitroot="https://github.com/ckolivas/cgminer.git"
_gitname="cgminer-git"

[ $CARCH == 'x86_64' ] && makedepends+=('yasm')

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd "$srcdir"/$_gitname
  
  #copy in ADMD ADL headers
  mkdir $srcdir/AMD_ADL_SDK
  bsdtar -x -C "$srcdir"/AMD_ADL_SDK -f "$srcdir"/ADL_SDK_4.0.zip --exclude Sample/* --exclude Sample-Managed/* --exclude adlutil/* --exclude ADL_SDK.html --exclude Public-Documents/html/*
  mv "$srcdir"/AMD_ADL_SDK/include/*.h ADL_SDK

  # Use in-tree jansson since it is not compatible with jansson 2.0
  #sed -e 's/^AC_CHECK_LIB(jansson, json_loads, request_jansson=false, request_jansson=true)$/request_jansson=true/' -i configure.ac

  ./autogen.sh || return 1
  # Here you may want to use custom CFLAGS
  #export CFLAGS="-O2 -march=native -mtune=native -msse2"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "$srcdir"/$_gitname

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/AMD_ADL_SDK/Public-Documents/ADL\ SDK\ EULA.rtf "$pkgdir"/usr/share/licenses/"$pkgname"/AMD_ADL_SDK_EULA.rtf
  install -Dm755 "$srcdir"/$_pkgname "$pkgdir"/etc/rc.d/$_pkgname
  install -Dm644 "$srcdir"/$_pkgname.conf.d "$pkgdir"/etc/conf.d/$_pkgname.conf.d
  sed 's#/usr/local/bin#/usr/bin#g' example.conf > $_pkgname.conf
  install -Dm644 $_pkgname.conf "$pkgdir"/etc/$_pkgname.conf
}
