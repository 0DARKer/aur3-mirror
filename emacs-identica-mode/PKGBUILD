# Contributor: Frikilinux <frikilinux@frikilinux.com.ar>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=emacs-identica-mode
_realname=identica-mode
pkgver=1.2.1
pkgrel=3
pkgdesc="GNU Emacs mode for laconi.ca microblogging service."
arch=('any')
url="http://blog.nethazard.net/identica-mode-for-emacs/"
license=('GPL')
depends=('emacs' 'curl' 'wget')
makedepends=('texinfo')
install=${_realname}.install
source=("http://git.savannah.gnu.org/cgit/${_realname}.git/snapshot/${_realname}-$pkgver.tar.gz")
md5sums=('c2f762f09cd5076835c6aaac24395742')

build() {
  cd $srcdir/${_realname}-$pkgver
  emacs -batch -q -f batch-byte-compile ${_realname}.el
  cd $srcdir/${_realname}-$pkgver/doc
  makeinfo fdl.texi
  makeinfo ${_realname}.texi
}
package() {
  install -Dm644 $srcdir/${_realname}-$pkgver/${_realname}.el \
    $pkgdir/usr/share/emacs/site-lisp/${_realname}.el
  install -Dm644 $srcdir/${_realname}-$pkgver/${_realname}.elc \
    $pkgdir/usr/share/emacs/site-lisp/${_realname}.elc
  install -Dm644 $srcdir/${_realname}-$pkgver/doc/fdl.info \
    $pkgdir/usr/share/info/fdl.info
  install -Dm644 $srcdir/${_realname}-$pkgver/doc/${_realname}.info \
    $pkgdir/usr/share/info/${_realname}.info
}

