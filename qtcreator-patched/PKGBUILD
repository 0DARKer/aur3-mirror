# Maintainer: m0rph (m0rph.mailbox@gmail.com)

pkgname="qtcreator-patched"
pkgver=2.3.1
pkgrel=1
pkgdesc='QtCreator with separated POD-type highlighting'
arch=('i686' 'x86_64')
url='http://qt.nokia.com/products/developer-tools'
license=('LGPL')
depends=('qt>=4.7.4')
makedepends=('qt-private-headers>=4.7.4' 'git')
provides=('qtcreator')
conflicts=('qtcreator')
options=('docs')
source=("http://get.qt.nokia.com/qtcreator/qt-creator-${pkgver}-src.tar.gz"
        'add_pod_highlighting.patch'
        'qtcreator.desktop')
md5sums=('8aa296ed9034f847bd53ee0424e5ef08'
         'a2253b62d83f4d00e368bdb7a824672d'
         'f1f4680b9838ff0b8f4cf11eb850d662')

build() {
  cd ${srcdir}

  patch -p2 -i ../add_pod_highlighting.patch

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  qmake ${srcdir}/qt-creator-${pkgver}-src/qtcreator.pro
  make
}

package() {
  optdepends=('qt-doc: for the integrated Qt documentation'
              'gdb: for the debugger'
              'cmake: for cmake project suppport'
              'openssh-askpass: for ssh support'
              'git: for git support'
              'mercurial: for mercurial support'
              'bzr: for bazaar support')
  
  cd ${srcdir}/build

  make INSTALL_ROOT="${pkgdir}/usr/" install

  install -Dm644 ${srcdir}/qtcreator.desktop \
    ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator-${pkgver}-src/LGPL_EXCEPTION.TXT \
    ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}
