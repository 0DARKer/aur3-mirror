# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=StormLib
pkgbase=${_pkgname,,}
pkgname=$pkgbase-git
pkgver=v8.22.43.gfd430b9
pkgrel=1
pkgdesc="A C/C++ API to read and write MPQ files with support for merged archives, patch MPQs and more."
arch=("i686" "x86_64")
url="http://www.zezula.net/en/mpq/stormlib.html"
license=("MIT")
depends=("bzip2" "zlib")
makedepends=("git" "cmake")
provides=("$pkgbase")
conflicts=("$pkgbase")
source=("git://github.com/ladislav-zezula/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
