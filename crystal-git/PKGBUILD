# Maintainer: Mikkel Kroman <mk@maero.dk>
pkgname=crystal-git
pkgver=0.1
pkgrel=5
pkgdesc="The Crystal Programming Language"
arch=('x86_64' 'i686')
url="http://crystal-lang.org"
license=('Apache')
depends=('libunwind' 'llvm' 'pcre' 'clang')
makedepends=('git')
install=crystal-git.install
if [[ $CARCH = "x86_64" ]]; then
  source=('https://s3.amazonaws.com/crystal-lang/crystal-linux64-latest.gz'
          'git+git://github.com/manastech/crystal.git'
          'crystal.sh')
elif [[ $CARCH = "i686" ]]; then
  source=('https://s3.amazonaws.com/crystal-lang/crystal-linux32-latest.gz'
          'git+git://github.com/manastech/crystal.git'
          'crystal.sh')
fi
sha1sums=('SKIP'
          'SKIP'
          '3961cd4c406cd3b23d7c95ad19e10fd9f316fcce')

pkgver() {
  cd "${srcdir}/crystal"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}"

  # Set up links to existing libraries so that ld will be happy
  if [ ! -d libs ]; then
    mkdir libs
  fi

  if [ ! -L libs/libpcre.so.3 ]; then
    ln -s /usr/lib/libpcre.so libs/libpcre.so.3
  fi

  if [ ! -L libs/libunwind.so.7 ]; then
    ln -s /usr/lib/libunwind.so libs/libunwind.so.7
  fi

  cd crystal

  git reset --hard

  # Make the `crystal` shell script happy
  [ ! -d deps ] && mkdir deps

  # Use the pre-compiled compiler
  mv ../crystal-linux64-latest deps/crystal
  chmod +x deps/crystal

  LD_LIBRARY_PATH="${srcdir}/libs:${LD_LIBRARY_PATH}" \
    make release=1 crystal
}

package() {
  # Copy the executable
  install -D "${srcdir}/crystal/.build/crystal" "${pkgdir}/usr/bin/crystal"

  # Copy the profile.d shell script - it exports CRYSTAL_PATH
  install -Dm755 ../crystal.sh "${pkgdir}/etc/profile.d/crystal.sh"

  # Copy the source to /usr/share/crystal/src
  mkdir -p "${pkgdir}/usr/share/crystal/src"
  cp -dpr --no-preserve=ownership "${srcdir}/crystal" "${pkgdir}/usr/share/crystal/src/"

  # Remove (LARGE!) compiled files
  for _dir in .build .crystal deps; do
    rm -rf "${pkgdir}/usr/share/crystal/src/crystal/${_dir}"
  done
}

# vim:set ts=2 sw=2 et:
