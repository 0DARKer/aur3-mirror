# Maintainer: Patrick Burroughs (Celti) <celticmadman@gmail.com>

pkgname=devtodo-git
pkgver=2.1.r17.gbcb7a91
pkgrel=1
pkgdesc="A hierarchical command-line task manager."
arch=('i686' 'x86_64')
url="http://swapoff.org/devtodo.html"
license=('Apache')
makedepends=('git' 'go')
source=('git+https://github.com/alecthomas/devtodo2.git')
sha256sums=('SKIP')

pkgver() {
	cd devtodo2
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
	GOPATH="$srcdir" go get github.com/droundy/goopt
}

build() {
	cd devtodo2
	GOPATH="$srcdir" make
}

package() {
	cd devtodo2
	make PREFIX="$pkgdir/usr" install
}
