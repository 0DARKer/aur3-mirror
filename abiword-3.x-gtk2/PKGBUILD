# Maintainer: Richard Jackson <rj@iinet.net.au>
pkgname=abiword-3.x-gtk2
_pkgname=abiword
pkgver=3.0.0
pkgrel=2
pkgdesc='Fully-featured word processor, GTk2, No plugins, Lite version'
arch=('i686' 'x86_64')
license=('GPL')
depends=('fribidi' 'wv' 'librsvg' 'enchant' 'desktop-file-utils' 'gtk2' 'libxslt')
makedepends=('pkgconfig' 'boost')
install=$pkgname.install
conflicts=('abiword' 'abiword-plugins')
url='http://www.abisource.com'
options=('!makeflags')
source=(http://abisource.com/downloads/$_pkgname/$pkgver/source/$_pkgname-$pkgver.tar.gz)
md5sums=('8d9c41cff3a8fbef8d0c835c65600e65')

build() {
  cd $_pkgname-$pkgver

  ./configure --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-gtk2 \
    --enable-clipart \
    --enable-templates \
    --without-redland \
    --without-libical \
    --disable-builtin-plugins \
    --disable-default-plugins \
    --disable-collab-backend-service \
    --disable-collab-backend-tcp
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}