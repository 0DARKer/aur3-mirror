# Maintainer: Artem Vorotnikov <artem@vorotnikov.me>
# Contributor: egore911
# Contributor: Falcata
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=caelum-ogre1.8
pkgver=0.6.1
pkgrel=2
pkgdesc="Library for OGRE targeted to help create nice-looking atmospheric effects. Built against OGRE 1.8."
arch=('i686' 'x86_64')
url="http://www.ogre3d.org/tikiwiki/Caelum"
depends=('ogre-1.8' 'cegui' 'doxygen' 'boost')
makedepends=('cmake')
provides=('caelum')
license=('LGPL')
source=(http://caelum.googlecode.com/files/caelum-${pkgver}.zip caelumplugin.patch libname.patch)
md5sums=('865e38353e84a7c0f81fcaf365642f0f'
         'b136fa52fa8a941c4db315ba8e0a14c9'
         '222f6235c3b87c6b563e1ff4cd6a2be4')

build() {
  export PKG_CONFIG_PATH="/opt/OGRE-1.8/lib/pkgconfig:$PKG_CONFIG_PATH"
  export LD_LIBRARY_PATH="/opt/OGRE-1.8/lib:$LD_LIBRARY_PATH"
  cd ${srcdir}/caelum-${pkgver}
  patch -p0 < ${srcdir}/caelumplugin.patch main/src/CaelumPlugin.cpp
  patch -p0 < ${srcdir}/libname.patch
  
  # get a clean build dir
  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/opt/Caelum-OGRE1.8

  make
}

package() {
  cd ${srcdir}/caelum-${pkgver}/build
  make DESTDIR=${pkgdir} install
  rm -r ${pkgdir}/opt/Caelum-OGRE1.8/doc
}
