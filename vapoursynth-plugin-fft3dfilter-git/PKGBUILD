# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=fft3dfilter
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=r2.cce2813
_pkgver="$(echo ${pkgver} | cut -d '.' -f1 | tr -d r)"
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="https://github.com/VFR-maniac/VapourSynth-FFT3DFilter"
license=('GPL')
depends=('vapoursynth' 'fftw')
makedepends=('git')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/VFR-maniac/VapourSynth-FFT3DFilter.git")
md5sums=('SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "r$(git rev-list HEAD 2> /dev/null | wc -l | sed 's/ //g').$(git describe --always)"
}

prepare() {
  rm "${_gitname}/VapourSynth.h"
}

build() {
  cd "${_gitname}"
  ./configure --prefix=/usr --extra-cxxflags="$(pkg-config --cflags vapoursynth)"
  make
}

package(){
  cd "${_gitname}"
  install -Dm755 "vsfft3dfilter.so.${_pkgver}" "${pkgdir}/usr/lib/vapoursynth/libvsfft3dfilter.so.${_pkgver}"
  ln -s "libvsfft3dfilter.so.${_pkgver}" "${pkgdir}/usr/lib/vapoursynth/libvsfft3dfilter.so"
#  install -Dm644 README "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README"
}
