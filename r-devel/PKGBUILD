# $Id$
# Contributor: Florian Breitwieser <florian.bw@gmail.com>

pkgname=r-devel
pkgver=20131124
_pkgver=2013-11-24
pkgrel=1
pkgdesc="Language and environment for statistical computing and graphics - development version"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.r-project.org/')
depends=('blas' 'lapack' 'bzip2'  'libpng' 'libjpeg' 'libtiff'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs'
         'tk' 'libxt' 'libxmu' 'pango' 'xz')
makedepends=('openjdk6' 'gcc-fortran')
backup=('etc/R/Makeconf' 'etc/R/Renviron' 'etc/R/ldpaths' 'etc/R/repositories')
options=('!makeflags')
source=("ftp://ftp.stat.math.ethz.ch/pub/Software/R/R-devel_${_pkgver}.tar.gz"
	'r-devel.desktop'
	'r-devel.png')
install=r-devel.install

build() {
   cd ${srcdir}/R-devel
   sed -i 's|#define NeedFunctionPrototypes 0|#define NeedFunctionPrototypes 1|g' src/modules/X11/dataentry.c
   ./configure  --prefix=/opt/r-devel \
		--enable-R-shlib \
                --with-lapack \
		--with-blas \
                F77=gfortran \
		LIBnn=lib
   make
}

package() {
   cd ${srcdir}/R-devel
   make -j1 DESTDIR=${pkgdir} install

  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/r-devel.desktop ${pkgdir}/usr/share/applications/r-devel.desktop
  install -Dm644 ${srcdir}/r-devel.png ${pkgdir}/usr/share/pixmaps/r-devel.png

  # move the config directory to /etc and create symlinks
  install -d ${pkgdir}/etc/R-devel
  cd ${pkgdir}/opt/r-devel/lib/R/etc
  for i in *; do
    mv -f ${i} ${pkgdir}/etc/R-devel
    ln -s /etc/R-devel/${i} ${i}
  done

  # links
  install -d ${pkgdir}/usr/bin
  ln -s /opt/r-devel/bin/R ${pkgdir}/usr/bin/R-devel
  ln -s /opt/r-devel/bin/Rscript ${pkgdir}/usr/bin/Rscript-devel
}

md5sums=('a1e8b422fc244d223e75bb53e3ff5bee'
         'db4aaeb54dec28f831e3fac64705d4be'
         '8862f998939a07a2391840f99ab4a4f6')
