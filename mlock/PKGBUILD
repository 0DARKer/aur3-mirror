# Maintainer: hbdee <hbdee.arch@gmail.com>

pkgname=('mlock' 'mlock-gui')
pkgbase='mlock'
pkgver=0.3
pkgrel=2
pkgdesc="A fast native implementation of the minilock file format that can read and write encrypted miniLock files."
arch=('i686' 'x86_64')
url="http://m-lock.sourceforge.net/"
license=('GPL3')
depends=('openssl' 'libsodium')
makedepends=('cmake' 'qt5-declarative' 'qt5-quickcontrols' 'libsodium')
source=("http://sourceforge.net/projects/m-lock/files/$pkgbase-$pkgver.tar.bz2"
	'mlock-gui.desktop')
md5sums=('6acf3c81ef3ccb92eba7300addc98fff'
	 '4df430f21ffdb13b0ba0b00fbb985ab8')
# will add once I get public key from developer
#validpgpkeys=()

build() {
  cd "$pkgbase-$pkgver"

  make 
  
  make gui
}

package_mlock() {
  provides=('mlock')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install
}

package_mlock-gui() {
  pkgdesc="Qt5 GUI for mlock"
  depends=('qt5-declarative' 'qt5-quickcontrols' 'libsodium')
  install="mlock-gui.install"
  provides=('mlock-gui')

  mkdir -p -m 755 ${pkgdir}/usr/share/applications/
  install -Dm644 mlock-gui.desktop ${pkgdir}/usr/share/applications/

  cd "$pkgbase-$pkgver"
  mkdir -p -m 755 ${pkgdir}/usr/bin
  make DESTDIR="$pkgdir" install-gui
}

# vim:set ts=2 sw=2 et: