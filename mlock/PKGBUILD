# Maintainer: hbdee <hbdee.arch@gmail.com>

pkgname=('mlock' 'mlock-gui')
pkgbase='mlock'
pkgver=0.3
pkgrel=3
pkgdesc="A fast native implementation of the minilock file format that can read and write encrypted miniLock files."
arch=('i686' 'x86_64')
url="http://m-lock.sourceforge.net/"
license=('GPL3')
depends=('openssl' 'libsodium')
makedepends=('cmake' 'qt5-declarative' 'qt5-quickcontrols' 'libsodium')
install="mlock-gui.install"
source=(http://sourceforge.net/projects/m-lock/files/$pkgbase-$pkgver.tar.bz2{,.asc}
	'mlock-gui.desktop')
sha256sums=('68e61086267fd9bdd509acbcdee37a268aa39711d6d7854a53395b88a64128bb'
	    '07f0545ef09a2a1a9fe8b28071d150c5760f07687ab5ed5a1a62f34e10624ea2'
	    '2c4f59a7b84762bcc0f479d7726fdd3d479e7e18e0f4ecb005cb11a5a9fcb3b1')
validpgpkeys=('B8C55574187F49180EDC763750FE0279D805A7C7')

build() {
  cd "$pkgbase-$pkgver"

  make 
  
  make gui
}

package_mlock() {
  provides=('mlock')

  cd "$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install
}

package_mlock-gui() {
  pkgdesc="Qt5 GUI for mlock"
  depends=('mlock' 'qt5-declarative' 'qt5-quickcontrols' 'libsodium')
  install="mlock-gui.install"
  provides=('mlock-gui')

  mkdir -p -m 755 ${pkgdir}/usr/share/applications/
  install -Dm644 mlock-gui.desktop ${pkgdir}/usr/share/applications/

  cd "$pkgbase-$pkgver"
  mkdir -p -m 755 ${pkgdir}/usr/bin
  make DESTDIR="$pkgdir" install-gui
}

# vim:set ts=2 sw=2 et: