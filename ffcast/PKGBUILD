# Maintainer: lolilolicon <lolilolicon@gmail.com>

_name=FFcast
_ver=2.0.0-rc7
pkgname=ffcast
epoch=1
pkgver=${_ver//-/}
pkgrel=1
pkgdesc="screencast one or more interactively selected region or window"
arch=(i686 x86_64)
url="https://github.com/lolilolicon/FFcast"
license=(GPL3)
depends=('bash>=4.3' ffmpeg libx11 xorg-xdpyinfo xorg-xprop xorg-xwininfo)
makedepends=(perl)
source=("https://github.com/lolilolicon/$_name/archive/$_ver.tar.gz")
md5sums=('a01b6f21ec2063c0cd1a399859a73fd1')

build() {
  cd "$_name-$_ver"
  make
}

package() {
  cd "$_name-$_ver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim:st=2:sw=2:et:
