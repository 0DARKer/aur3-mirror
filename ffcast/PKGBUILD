# Maintainer: lolilolicon <lolilolicon@gmail.com>

pkgname=ffcast
_name=FFcast2
epoch=1
pkgver=1.1
pkgrel=1
pkgdesc="screencast one or more interactively selected region or window"
arch=(i686 x86_64)
url="https://github.com/lolilolicon/FFcast2"
license=(GPL3)
depends=('bash>=4.2' ffmpeg libx11 xorg-xdpyinfo xorg-xprop xorg-xwininfo)
makedepends=(perl)
source=("https://github.com/lolilolicon/$_name/archive/$pkgver.tar.gz")
md5sums=('f4a08fa427fc9fe31bb444ca4d7589f0')

build() {
  cd "$_name-$pkgver"
  make
}

package() {
  cd "$_name-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -D -m644 README.asciidoc "$pkgdir/usr/share/doc/$pkgname"/README.txt
}

# vim:st=2:sw=2:et:
