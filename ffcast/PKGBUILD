# Maintainer: lolilolicon <lolilolicon@gmail.com>

pkgname=ffcast
_name=FFcast
epoch=1
pkgver=1.1.1
pkgrel=2
pkgdesc="screencast one or more interactively selected region or window"
arch=(i686 x86_64)
url="https://github.com/lolilolicon/FFcast"
license=(GPL3)
depends=('bash>=4.2' ffmpeg libx11 xorg-xdpyinfo xorg-xprop xorg-xwininfo)
makedepends=(perl)
source=("https://github.com/lolilolicon/$_name/archive/$pkgver.tar.gz")
md5sums=('3693d0d385a4ba1323ed4940457d0c9b')

build() {
  cd "$_name-$pkgver"
  make
}

package() {
  cd "$_name-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

# vim:st=2:sw=2:et:
