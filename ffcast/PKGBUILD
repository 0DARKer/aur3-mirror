# Maintainer: lolilolicon <lolilolicon@gmail.com>

pkgname=ffcast
epoch=1
pkgver=1.0
pkgrel=1
pkgdesc="screencast one or more interactively selected region or window"
arch=(i686 x86_64)
url="https://github.com/lolilolicon/FFcast2"
license=(GPL3)
depends=('bash>=4.2' ffmpeg libx11 xorg-xdpyinfo xorg-xwininfo)
makedepends=(perl)
source=("https://github.com/downloads/lolilolicon/FFcast2/$pkgname-$pkgver.tar.gz")
md5sums=('078ce91f1d5b29e94b941f1299c4bdfe')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -D -m644 README.asciidoc "$pkgdir/usr/share/doc/$pkgname"/README.txt
}

# vim:st=2:sw=2:et:
