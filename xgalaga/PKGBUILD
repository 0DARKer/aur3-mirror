# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Loui Chang <louipc.ist@gmail.com>

pkgname=xgalaga
pkgver=2.0.34
pkgrel=6
pkgdesc="An open source remake of the classic arcade game Galaga"
arch=('i686' 'x86_64')
url="http://rumsey.org/xgal.html"
license=('GPL')
depends=('libxmu' 'libxpm')
backup=(var/lib/$pkgname/scores)
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname.patch
        $pkgname.png
        $pkgname.desktop)
md5sums=('9f7ee685e9c4741b5f0edc3f91df9510'
         '9c5f1ddf017d3618bb4c794f26140b2f'
         '3168c3fec9bd8345946ac93c6b9d7b32'
         'e8fbde4e6049202a087ebc7daf0f0170')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -Np1 -i "$srcdir"/$pkgname.patch

  LDFLAGS='' ./configure --prefix=/usr/share/$pkgname --exec-prefix=/usr/bin --with-xpm-lib=/usr/lib

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make prefix="$pkgdir"/usr/share/$pkgname exec_prefix="$pkgdir"/usr/bin scoredir="$pkgdir"/var/lib/$pkgname install

  install -Dm644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -Dm644 README "$pkgdir"/usr/share/doc/$pkgname/README
}
