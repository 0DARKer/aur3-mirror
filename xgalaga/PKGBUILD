# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Loui Chang <louipc.ist@gmail.com>

pkgname=xgalaga
pkgver=2.0.34
pkgrel=4
pkgdesc="Remake of the classic arcade game Galaga for Unix/X11"
arch=('i686' 'x86_64')
url="http://rumsey.org/xgal.html"
license=('GPL')
depends=('libxmu' 'libxpm')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-altmask.patch"
        "${pkgname}-includes.patch"
        "${pkgname}.png"
        "${pkgname}.desktop")
md5sums=('9f7ee685e9c4741b5f0edc3f91df9510'
         '94fac9d26483683b0c1565f67cc78db8'
         'b528e42e20b1e07aae8385fe56c51cd0'
         '734904d5a43ef92032b3299e87da345a'
         'bb57ecee02657bf378986a2c107686ea')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply patches
  patch -p1 -i "../${pkgname}-altmask.patch"
  patch -p1 -i "../${pkgname}-includes.patch"

  # Compile
  LDFLAGS='' ./configure --prefix=/usr/share/xgalaga \
                         --exec-prefix=/usr/bin \
                         --with-xpm-lib=/usr/lib || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install game files
  make prefix="${pkgdir}/usr/share/${pkgname}" \
       exec_prefix="${pkgdir}/usr/bin" install || return 1

  # Install pixmap, .desktop and readme
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
