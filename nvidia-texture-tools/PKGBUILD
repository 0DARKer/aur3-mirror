pkgname=nvidia-texture-tools
pkgver=2.0.8
pkgrel=2
pkgdesc="GPU-accelerated Texture Tools with support for DirectX 10 texture formats"
arch=('i686' 'x86_64')
url="http://code.google.com/p/nvidia-texture-tools/"
license=('MIT')
depends=('libpng' 'libjpeg' 'libtiff' 'openexr')
makedepends=('cmake')
source=(http://nvidia-texture-tools.googlecode.com/files/${pkgname}-${pkgver}-1.tar.gz
		nvidia-texture-tools-2.0.8-add-pthread.patch
		issue139.patch
		r1157.patch)
md5sums=('7449c95ca1583b512561c83c5a5f401c'
		'55d011f08ef9c924a7fd5b2c8b9e2c85'
		'c67927632740fbea5efb02969b97b52c'
		'a044c7cae45e8c9946b4349e34793656')

build() {
	cd ${srcdir}/${pkgname}

	# add -pthread to linker, r1211
	patch -p1 -i ${srcdir}/nvidia-texture-tools-2.0.8-add-pthread.patch
	# Fix aliasing bug, r1167
	patch -p1 -i ${srcdir}/issue139.patch
	# patch for CUDA 3.0, this pkg doesn't require cuda, but be nice
	patch -p2 -i ${srcdir}/r1157.patch

	./configure --prefix=/usr --release
	make
}

package() {
	cd ${srcdir}/${pkgname}
	make DESTDIR=${pkgdir} install
}
