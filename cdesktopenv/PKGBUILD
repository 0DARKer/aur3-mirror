# Maintainer: Ecmel Ercan <ecmel dot ercan at gmail dot com>
# Contributor: Vain <aurmaint1 on host: uninformativ dot de>
pkgname=cdesktopenv
pkgver=2.2.0b
pkgrel=1
pkgdesc="Common Desktop Environment"
url="http://sourceforge.net/projects/cdesktopenv/"
arch=('i686')
license=('LGPL2')
options=(!strip !zipman)
depends=(openmotif xbitmaps rpcbind mksh ncurses)
makedepends=(tcl ncompress)
optdepends=('cups: for printing')
source=("http://downloads.sourceforge.net/$pkgname/cde-src-$pkgver-alpha.tar.gz"
        'f0a60e.patch'
        'dtlogin.service'
        'fonts.alias')
md5sums=('435a5fe43d839ed3a2050f9fd365db70'
         '2eed55352db902e2f78f76b0a29b5f13'
         '7fb98986c3c8f89ad254acd331100b1f'
         '9ec9f36e48581433e7772060f0960d24')

build() {
  cd "$srcdir/cde-$pkgver/cde/"

  patch -Np2 -i "$srcdir/f0a60e.patch"
  cp "$srcdir/fonts.alias" programs/fontaliases/linux/C/

  mkdir -p imports/x11/include
  ln -sf /usr/include/X11 imports/x11/include/

  cat >> config/cf/site.def <<EOF
#define KornShell /bin/mksh
#define CppCmd cpp
#define YaccCmd bison -y
#define HasTIRPCLib YES
#define HasZlib YES
#define DtLocalesToBuild
EOF

  LANG=C IMAKECPP=cpp make -j1 World

  sed -e "s:mkProd -D :&$pkgdir:" \
      -i admin/IntegTools/dbTools/installCDE
}

package() {
  export INSTALL_LOCATION="$pkgdir/usr/dt"
  export CONFIGURE_LOCATION="$pkgdir/etc/dt"
  export LOGFILES_LOCATION="$pkgdir/var/dt"

  cd "$srcdir/cde-$pkgver/cde/admin/IntegTools/dbTools/"
  ./installCDE -s "$srcdir/cde-$pkgver/cde"

  cd "$pkgdir/usr/dt/"
  chmod 2555 bin/dtmail
  
  cd "$pkgdir/var/dt/"
  chmod 755 .
  chown bin .
  chgrp bin .
  mkdir -p appconfig/appmanager
  mkdir -p tmp
  chmod -R 755 *
  chown -R bin *
  chgrp -R bin *

  cd "$pkgdir/etc/dt/"
  mkdir -p appconfig/appmanager/C
  mkdir -p appconfig/help/C
  mkdir -p appconfig/icons/C
  mkdir -p appconfig/types/C
  mkdir -p config/Xsession.d
  chmod -R 755 *

  install -Dm644 "$srcdir/dtlogin.service" \
                 "$pkgdir/usr/lib/systemd/system/dtlogin.service" 
  install -Dm755 "$srcdir/cde-$pkgver/cde/contrib/rc/linux/dtlogin" \
                 "$pkgdir/etc/rc.d/dtlogin"
}

