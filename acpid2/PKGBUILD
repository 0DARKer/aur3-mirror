# Maintainer: xduugu
# Contributor: Manolis Tzanidakis
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jonathan Schmidt <j.schmidt@archlinux.us>

pkgname=acpid2
_realname=acpid
pkgver=2.0.10
pkgrel=1
pkgdesc="A daemon for delivering ACPI power management events with netlink support"
arch=('i686' 'x86_64')
url="http://www.tedfelix.com/linux/acpid-netlink.html"
license=('GPL')
depends=(bash)
provides=("acpid=$pkgver")
replaces=(acpid)
conflicts=(acpid)
source=(http://www.tedfelix.com/linux/$_realname-$pkgver.tar.gz
		acpid
		anything
		handler.sh)
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything')
md5sums=('d5dd88bcfaa4a0bf51905e95115da6df'
         '6758e9ffa9844fecfd18f490b914c278'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '662d1208db40948ffc3913707f860a10')
sha256sums=('ef7e4ed2f3594dd02a50a92fc47dc0e1fea80c07fab8a5f3d1040ee76fae0acb'
            'e988b949e914fba608ae233e3ce07892cadca6799d4cd7a866b928f4ac33cf50'
            'eb5230affb9dba7653890655b94e83f377c689a39131b6b13c8202ba2382c275'
            'e4df87605b5b8bfb9442a2f62e425cc479ba2fe598637e5530842b5a587883ee')

build() {
	cd "$srcdir/$_realname-$pkgver"
	make
}

package() {
	cd "$srcdir/$_realname-$pkgver"
	make DESTDIR="$pkgdir" install

	# install our supplementary scripts
	install -Dm0755 "$srcdir/acpid"      "$pkgdir/etc/rc.d/acpid"
	install -Dm0644 "$srcdir/anything"   "$pkgdir/etc/acpi/events/anything"
	install -Dm0755 "$srcdir/handler.sh" "$pkgdir/etc/acpi/handler.sh"

	# set world readable bit on the acpid binary
	chmod 755 "$pkgdir/usr/sbin/acpid"

	# remove unnecessary files
	local f
	for f in COPYING Changelog README TESTPLAN TODO; do
		rm -f "$pkgdir/usr/share/doc/acpid/$f"
	done
}
