# Maintainer: xduugu
# Contributor: Manolis Tzanidakis
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jonathan Schmidt <j.schmidt@archlinux.us>

pkgname=acpid2
_realname=acpid
pkgver=2.0.8
pkgrel=1
pkgdesc="A daemon for delivering ACPI power management events with netlink support"
arch=('i686' 'x86_64')
url="http://www.tedfelix.com/linux/acpid-netlink.html"
license=('GPL')
depends=(bash)
provides=("acpid=$pkgver")
replaces=(acpid)
conflicts=(acpid)
source=(http://www.tedfelix.com/linux/$_realname-$pkgver.tar.gz
		acpid
		anything
		handler.sh)
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything')
md5sums=('7e5ede75e37ccaaf75536bc44cf214fc'
         '15903690f1bae4b3b0625b369de65521'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '0e8dd13793b1baa79a745f4034888367')
sha256sums=('af1ebddef17412fc3f00994a624fbb912ebe885437c13a7e87f3142f8339ce8c'
			'f098639475578906116fd398f4cc4a3d1b1d065cc37158f854d1967c20c81b34'
			'eb5230affb9dba7653890655b94e83f377c689a39131b6b13c8202ba2382c275'
			'6ae2b153c4ef9cbfd32c4428406fc63d931ea7fdfb2e45529f89b51f4d7ac17a')

build() {
	cd "$srcdir/$_realname-$pkgver"
	make
}

package() {
	cd "$srcdir/$_realname-$pkgver"
	make DESTDIR="$pkgdir" install

	# install our supplementary scripts
	install -Dm0755 "$srcdir/acpid"      "$pkgdir/etc/rc.d/acpid"
	install -Dm0644 "$srcdir/anything"   "$pkgdir/etc/acpi/events/anything"
	install -Dm0755 "$srcdir/handler.sh" "$pkgdir/etc/acpi/handler.sh"

	# set world readable bit on the acpid binary
	chmod 755 "$pkgdir/usr/sbin/acpid"

	# remove unnecessary files
	local f
	for f in COPYING Changelog README TESTPLAN TODO; do
		rm -f "$pkgdir/usr/share/doc/acpid/$f"
	done
}
