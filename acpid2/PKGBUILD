# Contributor: Manolis Tzanidakis
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Maintainer: Thayer Williams <thayer@archlinux.org>
# Maintainer: Jonathan Schmidt <j.schmidt@archlinux.us>

pkgname=acpid2
realname=acpid
pkgver=2.0.5
pkgrel=1
pkgdesc="A daemon for delivering ACPI power management events with netlink support"
arch=('i686' 'x86_64')
url="http://www.tedfelix.com/linux/acpid-netlink.html"
license=('GPL')
depends=(bash)
provides=("acpid=$pkgver")
replaces=(acpid)
conflicts=(acpid)
source=(http://www.tedfelix.com/linux/$realname-$pkgver.tar.gz
        acpid
        anything
        handler.sh)
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything')
md5sums=('796b99ca935e248c3be1f222a38f5ee1'
         '15903690f1bae4b3b0625b369de65521'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '0e8dd13793b1baa79a745f4034888367')

build() {
    cd $srcdir/$realname-$pkgver
    make || return 1
    install -d -m0755 $pkgdir/usr/bin || return 1
    make DESTDIR=$pkgdir install || return 1

    # install our supplementary scripts
    install -D -m0755 $srcdir/acpid $pkgdir/etc/rc.d/acpid || return 1
    install -D -m0644 $srcdir/anything $pkgdir/etc/acpi/events/anything || return 1
    install -D -m0755 $srcdir/handler.sh $pkgdir/etc/acpi/handler.sh || return 1

    # set world readable bit on the acpid binary
    chmod 755 $pkgdir/usr/sbin/acpid || return 1
}
