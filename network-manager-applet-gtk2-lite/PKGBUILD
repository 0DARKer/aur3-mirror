# Based on: Brian Bidulock <bidulock@openss7.org>
# Based on: Aljosha Papsch <papsch.al@gmail.com>
# Based on: Benjamin Wild <benwild@gmx.de>
# Manteiner: Black_Codec <orso.f.regna@gmail.com>

pkgname=network-manager-applet-gtk2-lite
_pkgname=network-manager-applet
pkgver=0.9.10.0
pkgrel=1
pkgdesc="GNOME frontends to NetWorkmanager compiled for gtk2 with minimal dependencies"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=0.9.8" libsecret gtk2 libnotify
         mobile-broadband-provider-info iso-codes)
makedepends=(intltool gobject-introspection modemmanager gnome-icon-theme)
optdepends=('gnome-icon-theme: icon support if not provided by theme')
options=('!emptydirs')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
install=network-manager-applet.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/0.9/$_pkgname-$pkgver.tar.xz)
sha256sums=('f5c26c692f538dc0145dc7d46d3d390754fad73d0ff06861c074c61f3dc54eca')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -e 's,Gtk-3.0,Gtk-2.0,' -i src/libnm-gtk/Makefile.in
}

build() {
  cd ${_pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-static \
      --disable-more-warnings \
      --disable-maintainer-mode \
      --disable-migration \
      --without-bluetooth \
      --with-modem-manager-1 \
      --with-gtkver=2

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make V=0
}

package() {
  cd ${_pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
