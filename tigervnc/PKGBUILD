# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=tigervnc
pkgver=1.1.0
pkgrel=6
_xorgver=1.11.2
pkgdesc="TigerVNC is a suite of VNC servers and clients that have a focus on performance and remote display functionality. Originally this software was based on the (never released) VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('zlib' 'libjpeg-turbo' 'freetype2' 'libsm' 'libxtst' 'libxi' 'libxfont' 'libxmu' 'libxkbfile' 'libxrender' 'libgl' 'pixman' 'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config' 'xorg-fonts-alias' 'xorg-fonts-misc')
makedepends=('nasm' 'xorg-util-macros' 'xorg-font-util' 'inputproto' 'xextproto' 'xproto' 'bigreqsproto' 'damageproto' 'dri2proto' 'fixesproto' 'fontsproto' 'randrproto' 'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xineramaproto' 'xf86driproto' 'xf86vidmodeproto' 'glproto' 'renderproto' 'mesa' 'libpciaccess' 'xtrans')
conflicts=('tightvnc')
source=(http://downloads.sourceforge.net/tigervnc/${pkgname}-${pkgver}.tar.gz
        ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2
        color-cursor.patch
        xorg111.patch
        fd.patch
        keyboard_focus.patch)
md5sums=('1a5598b4a2ac530fb51411438959e11e'
         '8796fff441e5435ee36a72579008af24'
         '1dac0d89ea6f2240ca955398ddc47f0b'
         'b680b55f7f9f569cd26ac0b330012a8b'
         '543e9951ed0fbcce38c033ea3adc8c95'
         'f43034dab4bd47474c9dcff473219ff7')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver
  for p in ${srcdir}/*.patch; do patch -p1 < $p; done

  autoreconf -fiv
  ./configure --prefix=/usr --without-included-zlib --with-system-jpeg \
              --disable-pam --disable-gnutls
  make

  cd unix/xserver
  patch -p1 < ../xserver110.patch
  autoreconf -fiv
  ./configure --prefix=/usr \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-static --disable-xinerama --disable-composite \
	--disable-config-dbus --disable-config-hal --disable-config-udev \
	--disable-unit-tests --disable-devel-docs --disable-dri \
	--enable-dri2 --enable-glx --enable-glx-tls
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd unix/xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/xorg/modules/extensions/libvnc.la
}
