# Maintainer: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=tigervnc
pkgver=1.1.0
pkgrel=2
_xorgver=1.10.4
pkgdesc="TigerVNC is a suite of VNC servers and clients that have a focus on performance and remote display functionality. Originally this software was based on the (never released) VNC 4 branch of TightVNC."
arch=('i686' 'x86_64')
url="http://www.tigervnc.org"
license=('GPL')
depends=('pam' 'gnutls' 'zlib' 'libjpeg-turbo' 'freetype2' 'libsm' 'libxtst' 'libxi' 'libxfont' 'libxmu' 'libxkbfile' 'libxrender' 'libgl' 'pixman' 'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config' 'xorg-fonts-alias' 'xorg-fonts-misc')
makedepends=('nasm' 'xorg-util-macros' 'xorg-font-util' 'inputproto' 'xextproto' 'xproto' 'bigreqsproto' 'compositeproto' 'damageproto' 'dri2proto' 'fixesproto' 'fontsproto' 'randrproto' 'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xineramaproto' 'xf86driproto' 'xf86vidmodeproto' 'glproto' 'renderproto' 'mesa' 'libpciaccess' 'xtrans')
conflicts=('tightvnc')
source=(http://downloads.sourceforge.net/tigervnc/${pkgname}-${pkgver}.tar.gz
        ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2
        color-cursor.patch)
md5sums=('1a5598b4a2ac530fb51411438959e11e'
         '94f23d30a77d63e27dd209a57fccfebf'
         '1dac0d89ea6f2240ca955398ddc47f0b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver
  patch -Np1 -i ${srcdir}/color-cursor.patch

  autoreconf -fiv
  cd unix
  patch -Np0 -i xserver110.patch
  cd xserver
  autoreconf -fiv

  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --without-included-zlib --with-system-jpeg
  make

  cd unix/xserver
  ./configure --prefix=/usr \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-static --disable-xinerama --disable-composite \
	--disable-config-dbus --disable-config-hal --disable-config-udev \
	--enable-dri2 --enable-glx --enable-glx-tls
  make
  cd ..

  make DESTDIR=${pkgdir} install
  cd xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/xorg/modules/extensions/libvnc.la
}
