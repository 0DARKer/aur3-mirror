# Maintainer: Christian Sturm <reezer@reezer.org>

pkgname='perl-mojolicious'
pkgver='3.31'
pkgrel='1'
pkgdesc="Real-time web framework"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
optdepends=('perl-ev' 'perl-io-socket-ip' 'perl-io-socket-ssl' 'perl-plack')
url='http://mojolicio.us/'
source=("http://cpan.metacpan.org/authors/id/S/SR/SRI/Mojolicious-$pkgver.tar.gz")
md5sums=('27e14d48edca66b9fc6719911883cef7')
sha512sums=('7c1e1d66b95910c7f8939a650147669c092f32191bc1fdb29729d1a1f1d12df3ca8053b8354ca0027a7c5c1910ab4a8350d8dd6e895a6818599294467e2eab3a')
_distdir="${srcdir}/Mojolicious-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd "$_distdir"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
