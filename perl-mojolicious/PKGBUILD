# Maintainer: Christian Sturm <reezer@reezer.org>

pkgname='perl-mojolicious'
pkgver='3.32'
pkgrel='1'
pkgdesc="Real-time web framework"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
optdepends=('perl-ev' 'perl-io-socket-ip' 'perl-io-socket-ssl' 'perl-plack')
url='http://mojolicio.us/'
source=("http://cpan.metacpan.org/authors/id/A/AM/AMS/Mojolicious-$pkgver.tar.gz")
md5sums=('b1242aa22e6f26582b187c2b8971e2ea')
sha512sums=('2b0afffa3434648de7dc71711205feab87607d1c7174d94945ab483364bfbf161ca493d38cd94b884717c3c70f076be37a3ddb995145119a55133364a577e2d9')
_distdir="${srcdir}/Mojolicious-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd "$_distdir"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
