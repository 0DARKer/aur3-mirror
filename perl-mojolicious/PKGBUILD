# Maintainer: Christian Sturm <reezer@reezer.org>

pkgname='perl-mojolicious'
pkgver='3.13'
pkgrel='1'
pkgdesc="Real-time web framework"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
optdepends=('perl-ev' 'perl-io-socket-ip' 'perl-io-socket-ssl')
url='http://mojolicio.us/'
source=("http://cpan.metacpan.org/authors/id/S/SR/SRI/Mojolicious-$pkgver.tar.gz")
md5sums=('0b926b3b2bee1c5f2ef5140373975ab5')
sha512sums=('bdcc22c1654690a4c0614240d27516be68cf8144d1d9b4e508421640b168796d3e0231d321efeabcd142e221e8a1e2081e3ede0b2b51b9a7defd665812c6c4e8')
_distdir="${srcdir}/Mojolicious-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$_distdir"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
