# Maintainer: Christian Sturm <reezer@reezer.org>

pkgname='perl-mojolicious'
pkgver='3.65'
pkgrel='1'
pkgdesc="Real-time web framework"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
optdepends=('perl-ev' 'perl-io-socket-ip' 'perl-io-socket-ssl' 'perl-plack')
url='http://mojolicio.us/'
source=("http://cpan.metacpan.org/authors/id/S/SR/SRI/Mojolicious-$pkgver.tar.gz")
md5sums=('11a1a069ef0c8d0451eeb05be9b6ace2')
sha512sums=('3f94dfa7f49ea794525a7e781275155dda1cffcd6af99d2b18f3fc225c78c39bbfca5aea0ef8e9fa392d98bc48089a9c7bb58a66ef858cbd7248942b068499d4')
_distdir="${srcdir}/Mojolicious-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd "$_distdir"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
