# Maintainer: Christian Sturm <reezer@reezer.org>

pkgname='perl-mojolicious'
pkgver='3.54'
pkgrel='1'
pkgdesc="Real-time web framework"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
optdepends=('perl-ev' 'perl-io-socket-ip' 'perl-io-socket-ssl' 'perl-plack')
url='http://mojolicio.us/'
source=("http://cpan.metacpan.org/authors/id/S/SR/SRI/Mojolicious-$pkgver.tar.gz")
md5sums=('36d1c685406c4b3ff68ef62f84a54415')
sha512sums=('6f2c23f8f39bf8958d79f3fc075b77c6c7134c5fbfc903057151c1254eb0b8945b3d5cdc59ed362ac15beb14a8453f4c229e121eb12c37dc8c92a2855f1b0da8')
_distdir="${srcdir}/Mojolicious-$pkgver"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

package() {
  cd "$_distdir"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
