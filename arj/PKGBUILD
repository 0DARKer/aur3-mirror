# Maintainer:
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Gergely Tamas <dice@mfa.kfki.hu>

pkgname=arj
pkgver=3.10.22
pkgrel=9
pkgdesc='Free and portable clone of the ARJ archiver'
url='http://arj.sourceforge.net/'
arch=('x86_64' 'i686')
license=('GPL')
depends=('glibc')
options=('!makeflags')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        001_arches_align.patch
        002_no_remove_static_const.patch
        003_64_bit_clean.patch
        arj-3.10.22-custom-printf.patch)
md5sums=('f263bf3cf6d42a8b7e85b4fb514336d3'
         '550bc972d825036f17f202a2b11b35c2'
         '395dfa8cc500ffae648777f8f241be88'
         '56b3cf96ec485b0d824761457417fcc0'
         '4630a7254dfc6c8d8c7e696aa4a58f4a')

prepare() {
  cd "$pkgname-$pkgver"

  # Add gentoo patches
  patch -p1 -i "$srcdir/001_arches_align.patch"
  patch -p1 -i "$srcdir/002_no_remove_static_const.patch"
  patch -p1 -i "$srcdir/003_64_bit_clean.patch"
  patch -p1 -i "$srcdir/arj-3.10.22-custom-printf.patch"

  cd gnu
  aclocal
  autoconf
  rm -f config.{guess,sub}
  cp /usr/share/automake-1.14/config.{guess,sub} .
}

build() {
  cd "$pkgname-$pkgver/gnu"

  ./configure --prefix=/usr
  cd ..
  make prepare
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
