pkgname=mingw-w64-libmikmod
pkgver=3.1.12
pkgrel=1
pkgdesc="A portable sound library (mingw-w64)"
arch=(any)
url="http://sourceforge.net/projects/mikmod/"
license=("LGPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-zlib)
options=(!libtool !strip !buildflags)
source=("http://downloads.sourceforge.net/sourceforge/mikmod/libmikmod%20%28source%29/$pkgver/libmikmod-$pkgver.tar.gz")
md5sums=('9f3c740298260d5f88981fc0d51f6f16')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/info"
    rm -r "$pkgdir/usr/${_arch}/man"
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}