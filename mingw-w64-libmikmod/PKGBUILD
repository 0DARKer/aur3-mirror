pkgname=mingw-w64-libmikmod
pkgver=3.2.0
pkgrel=1
pkgdesc="A portable sound library (mingw-w64)"
arch=(any)
url="http://mikmod.shlomifish.org"
license=("GPL" "LGPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-zlib)
options=(!libtool !strip !buildflags)
source=("$url/files/libmikmod-$pkgver.tar.gz"
"libmikmod-3.1.12-loopingvolume-fix.diff"
"libmikmod-3.2.0-64bit-fix.diff")
md5sums=('96e9820d72a41fe27ff304071739696c'
         'a837fd876cbd2ac27419b802504489db'
         '0bb09aac6e83e0a7fd6535961b3cff4c')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/libmikmod-$pkgver"
  patch -Np1 -i $srcdir/libmikmod-3.2.0-64bit-fix.diff
  patch -Np1 -i $srcdir/libmikmod-3.1.12-loopingvolume-fix.diff
  sed -i "s,#define HAVE_SSE2,,g" "include/mikmod_internals.h"
  sed -i "s,typedef unsigned int    ULONG;,typedef unsigned long   ULONG;,g" "include/mikmod.h"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    export CFLAGS="$CFLAGS -DDLL_EXPORTS"
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-threads=win32
    sed -i "s,-Dunix,-DWIN32,g" "libmikmod/Makefile"
    sed -i "s,#define DRV_PIPE 1,,g" "config.h"
    sed -i "s,typedef unsigned int    ULONG;,typedef unsigned long   ULONG;,g" "include/mikmod.h"
    sed -i "s,typedef unsigned int    ULONG;,typedef unsigned long   ULONG;,g" "include/mikmod_build.h"
    make
    ${_arch}-gcc -shared libmikmod/.libs/*.o -o libmikmod.dll -Xlinker --out-implib -Xlinker libmikmod.dll.a
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -m644 "libmikmod.dll" "$pkgdir/usr/${_arch}/bin/"
    install -m644 "libmikmod.dll.a" "$pkgdir/usr/${_arch}/lib/"
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}