pkgname=mingw-w64-libmikmod
pkgver=3.1.12
pkgrel=2
pkgdesc="A portable sound library (mingw-w64)"
arch=(any)
url="http://sourceforge.net/projects/mikmod"
license=("LGPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-zlib)
options=(!libtool !strip !buildflags)
source=("http://downloads.sourceforge.net/sourceforge/mikmod/libmikmod%20%28source%29/$pkgver/libmikmod-$pkgver.tar.gz"
"libmikmod-3.1.12-64bit-fix.diff::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/libmikmod-3.1.12-64bit-fix.diff?h=packages/libmikmod"
"libmikmod-3.1.12-exitcrash-fix.diff::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/libmikmod-3.1.12-exitcrash-fix.diff?h=packages/libmikmod"
"libmikmod-3.1.12-loopingvolume-fix.diff::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/libmikmod-3.1.12-loopingvolume-fix.diff?h=packages/libmikmod"
"libmikmod-3.1.12-md_sngchn-fix.diff::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/libmikmod-3.1.12-md_sngchn-fix.diff?h=packages/libmikmod"
"libmikmod-CVE-2009-0179.patch::https://projects.archlinux.org/svntogit/packages.git/plain/trunk/libmikmod-CVE-2009-0179.patch?h=packages/libmikmod")
md5sums=('9f3c740298260d5f88981fc0d51f6f16'
         'dc7ffd8d6d355e9d6ec671b7f2b2adc7'
         '03a4f5bfcecddf5f515672d6d477b7f7'
         'a837fd876cbd2ac27419b802504489db'
         '076d39de19de36b880ed90297f1ee0d1'
         'fa91f4bc17164be32bec0ea7a73f2aaa')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/libmikmod-$pkgver"
  patch -Np1 -i $srcdir/libmikmod-3.1.12-64bit-fix.diff
  patch -Np1 -i $srcdir/libmikmod-3.1.12-exitcrash-fix.diff
  patch -Np1 -i $srcdir/libmikmod-3.1.12-loopingvolume-fix.diff
  patch -Np1 -i $srcdir/libmikmod-3.1.12-md_sngchn-fix.diff
  patch -Np1 -i $srcdir/libmikmod-CVE-2009-0179.patch
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/"{info,man,share}
  done
}