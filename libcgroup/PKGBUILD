# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=libcgroup
pkgver=0.38
pkgrel=1
pkgdesc="Library that abstracts the control group file system in Linux"
arch=('i686' 'x86_64')
url="http://libcg.sourceforge.net"
license=(LGPL)
backup=('etc/conf.d/cgred' 'etc/conf.d/cgconfig' 'etc/cgconfig.conf' 'etc/cgrules.conf')
options=('!emptydirs' '!libtool')
install=libcgroup.install
source=("http://downloads.sourceforge.net/libcg/${pkgname}-${pkgver}.tar.bz2"
        'cgred' 'cgconfig' 'cgconfig.conf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" pkgconfigdir="/usr/lib/pkgconfig" install

  install -Dm644 samples/cgred.conf "${pkgdir}/etc/conf.d/cgred"
  install -Dm644 samples/cgconfig.conf "${pkgdir}/etc/cgconfig.conf"
  install -Dm644 samples/cgrules.conf "${pkgdir}/etc/cgrules.conf"
  install -Dm644 samples/cgsnapshot_blacklist.conf "${pkgdir}/etc/cgsnapshot_blacklist.conf"

  rm -f ${pkgdir}/usr/lib/security/pam_cgroup.{la,so,so.0}
  mv ${pkgdir}/usr/lib/security/pam_cgroup.so.0.0.0 ${pkgdir}/usr/lib/security/pam_cgroup.so

  rm -rf ${pkgdir}/etc/rc.d
  install -Dm755 "${srcdir}/cgred" ${pkgdir}/etc/rc.d/cgred
  install -Dm755 "${srcdir}/cgconfig" ${pkgdir}/etc/rc.d/cgconfig
  install -Dm644 "${srcdir}/cgconfig.conf" ${pkgdir}/etc/conf.d/cgconfig

  # Make cgexec setgid cgred
  chown root:160 ${pkgdir}/usr/bin/cgexec
  chmod 2755 ${pkgdir}/usr/bin/cgexec
}

sha256sums=('5d36d1a48b95f62fe9fcdf74a5a4089512e5e43e6011aa1504fd6f2a0909867f'
            '149ff59c45ef929e559a340509e3ac43ac8f281b27195cc47139ae75875cc294'
            '673ee77e327765bb3cba6eae9ada91b13a03675d0379921c79df3f1693bc76b4'
            '72c39ce0160c30d714f1a7187036757bf9a8c8649f0818404e3bc45f4ae76e6b')
