# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=libcgroup
pkgver=0.39rc1
pkgrel=1
pkgdesc="Library that abstracts the control group file system in Linux"
arch=('i686' 'x86_64')
url="http://libcg.sourceforge.net"
license=(LGPL)
backup=('etc/conf.d/cgred' 'etc/conf.d/cgconfig' 'etc/cgconfig.conf' 'etc/cgrules.conf')
options=('!emptydirs' '!libtool')
install=libcgroup.install
source=("http://downloads.sourceforge.net/libcg/${pkgname}-${pkgver/rc/.rc}.tar.bz2"
        'cgred' 'cgconfig' 'cgconfig.conf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver/rc/.rc}"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver/rc/.rc}"

  make DESTDIR="${pkgdir}" pkgconfigdir="/usr/lib/pkgconfig" install

  install -Dm644 samples/cgred.conf "${pkgdir}/etc/conf.d/cgred"
  install -Dm644 samples/cgconfig.conf "${pkgdir}/etc/cgconfig.conf"
  install -Dm644 samples/cgrules.conf "${pkgdir}/etc/cgrules.conf"
  install -Dm644 samples/cgsnapshot_blacklist.conf "${pkgdir}/etc/cgsnapshot_blacklist.conf"

  rm -f ${pkgdir}/usr/lib/security/pam_cgroup.{la,so,so.0}
  mv ${pkgdir}/usr/lib/security/pam_cgroup.so.0.0.0 ${pkgdir}/usr/lib/security/pam_cgroup.so

  rm -rf ${pkgdir}/etc/rc.d
  install -Dm755 "${srcdir}/cgred" ${pkgdir}/etc/rc.d/cgred
  install -Dm755 "${srcdir}/cgconfig" ${pkgdir}/etc/rc.d/cgconfig
  install -Dm644 "${srcdir}/cgconfig.conf" ${pkgdir}/etc/conf.d/cgconfig

  # Make cgexec setgid cgred
  chown root:160 ${pkgdir}/usr/bin/cgexec
  chmod 2755 ${pkgdir}/usr/bin/cgexec
}

sha256sums=('9d27e4b16b92fb9917d0807934023b8cc691f3b5258c4ee581957645fa7ba143'
            'f68aadcacca0bbc22b9b89e1f2d9a1d607d1a39890d444eeef7b5574beda138d'
            '40fa009e4beb576728eaab7165e824826cbed46e534e3a25503c6b9d7ed5f640'
            '72c39ce0160c30d714f1a7187036757bf9a8c8649f0818404e3bc45f4ae76e6b')
