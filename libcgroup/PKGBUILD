# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=libcgroup
pkgver=0.39rc1
pkgrel=4
pkgdesc="Library that abstracts the control group file system in Linux"
arch=('i686' 'x86_64')
url="http://libcg.sourceforge.net"
license=(LGPL)
backup=('etc/conf.d/cgred' 'etc/conf.d/cgconfig' 'etc/cgconfig.conf' 'etc/cgrules.conf')
options=('!emptydirs' '!libtool')
install=libcgroup.install
source=("http://downloads.sourceforge.net/libcg/${pkgname}-${pkgver/rc/.rc}.tar.bz2"
	'cgconfig.service::http://libcg.git.sourceforge.net/git/gitweb.cgi?p=libcg/libcg;a=blob_plain;f=dist/cgconfig.service;hb=HEAD'
	'cgrules.service'
        'cgred'
	'cgconfig'
	'cgconfig.conf')

build() {
	cd "${srcdir}/${pkgname}-${pkgver/rc/.rc}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-opaque-hierarchy=name=systemd

	make

	sed -i "s|/sbin/|/usr/sbin/|g" ${srcdir}/cgconfig.service
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver/rc/.rc}"

	make DESTDIR="${pkgdir}" pkgconfigdir="/usr/lib/pkgconfig" install

	install -D -m644 samples/cgred.conf "${pkgdir}/etc/conf.d/cgred"
	install -D -m644 samples/cgconfig.conf "${pkgdir}/etc/cgconfig.conf"
	install -D -m644 samples/cgrules.conf "${pkgdir}/etc/cgrules.conf"
	install -D -m644 samples/cgsnapshot_blacklist.conf "${pkgdir}/etc/cgsnapshot_blacklist.conf"

	install -D -m644 ${srcdir}/cgconfig.service "${pkgdir}/usr/lib/systemd/system/cgconfig.service"
	install -D -m644 ${srcdir}/cgrules.service "${pkgdir}/usr/lib/systemd/system/cgrules.service"

	rm -f ${pkgdir}/usr/lib/security/pam_cgroup.{la,so,so.0}
	mv ${pkgdir}/usr/lib/security/pam_cgroup.so.0.0.0 ${pkgdir}/usr/lib/security/pam_cgroup.so

	rm -rf ${pkgdir}/etc/rc.d
	install -Dm755 "${srcdir}/cgred" ${pkgdir}/etc/rc.d/cgred
	install -Dm755 "${srcdir}/cgconfig" ${pkgdir}/etc/rc.d/cgconfig
	install -Dm644 "${srcdir}/cgconfig.conf" ${pkgdir}/etc/conf.d/cgconfig

	# Make cgexec setgid cgred
	chown root:160 ${pkgdir}/usr/bin/cgexec
	chmod 2755 ${pkgdir}/usr/bin/cgexec
}

sha256sums=('9d27e4b16b92fb9917d0807934023b8cc691f3b5258c4ee581957645fa7ba143'
            '3bdaadce18a482d8e42590aefa9bdd78407a67dd8860377e07d0e69242de2aca'
            '06bf192a9d13e10e245ea58d8405d1719fb781232439dbaa3f3bccd249529da6'
            'f68aadcacca0bbc22b9b89e1f2d9a1d607d1a39890d444eeef7b5574beda138d'
            '40fa009e4beb576728eaab7165e824826cbed46e534e3a25503c6b9d7ed5f640'
            '72c39ce0160c30d714f1a7187036757bf9a8c8649f0818404e3bc45f4ae76e6b')
