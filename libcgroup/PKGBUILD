# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=libcgroup
pkgver=0.39rc1
pkgrel=5
pkgdesc="Library that abstracts the control group file system in Linux"
arch=('i686' 'x86_64')
url="http://libcg.sourceforge.net"
license=(LGPL)
backup=('etc/cgconfig.conf'
        'etc/cgrules.conf'
	'etc/cgsnapshot_blacklist.conf')
options=('!emptydirs' '!libtool')
install=libcgroup.install
source=("http://downloads.sourceforge.net/libcg/${pkgname}-${pkgver/rc/.rc}.tar.bz2"
	'cgconfig.service::http://libcg.git.sourceforge.net/git/gitweb.cgi?p=libcg/libcg;a=blob_plain;f=dist/cgconfig.service;hb=HEAD'
	'cgrules.service')

build() {
	cd "${srcdir}/${pkgname}-${pkgver/rc/.rc}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--enable-opaque-hierarchy=name=systemd

	make

	sed -i "s|/sbin/|/usr/bin/|g" ${srcdir}/cgconfig.service
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver/rc/.rc}"

	make DESTDIR="${pkgdir}" pkgconfigdir="/usr/lib/pkgconfig" install

	install -D -m644 samples/cgconfig.conf "${pkgdir}/etc/cgconfig.conf"
	install -D -m644 samples/cgrules.conf "${pkgdir}/etc/cgrules.conf"
	install -D -m644 samples/cgsnapshot_blacklist.conf "${pkgdir}/etc/cgsnapshot_blacklist.conf"

	install -D -m644 ${srcdir}/cgconfig.service "${pkgdir}/usr/lib/systemd/system/cgconfig.service"
	install -D -m644 ${srcdir}/cgrules.service "${pkgdir}/usr/lib/systemd/system/cgrules.service"

	rm -f ${pkgdir}/usr/lib/security/pam_cgroup.{la,so,so.0}
	mv ${pkgdir}/usr/lib/security/pam_cgroup.so.0.0.0 ${pkgdir}/usr/lib/security/pam_cgroup.so

	rm -rf ${pkgdir}/etc/rc.d

	# Make cgexec setgid cgred
	chown root:160 ${pkgdir}/usr/bin/cgexec
	chmod 2755 ${pkgdir}/usr/bin/cgexec
}

sha256sums=('9d27e4b16b92fb9917d0807934023b8cc691f3b5258c4ee581957645fa7ba143'
            '3bdaadce18a482d8e42590aefa9bdd78407a67dd8860377e07d0e69242de2aca'
            '6b1340ff6717f55e5e57dacc72accc0bfaed7e50ef31439271b6ddc893cbf671')
