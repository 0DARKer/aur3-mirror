# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=libcgroup
pkgver=0.37.1
pkgrel=1
pkgdesc="Library that abstracts the control group file system in Linux"
arch=(i686 x86_64)
url="http://libcg.sourceforge.net"
license=(LGPL)
backup=(etc/conf.d/cgred etc/conf.d/cgconfig etc/cgconfig.conf etc/cgrules.conf)
options=('!emptydirs' '!libtool')
install=libcgroup.install
source=("http://downloads.sourceforge.net/libcg/$pkgname-$pkgver.tar.bz2"
        cgred cgconfig cgconfig.conf)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --exec_prefix=/ \
    --sysconfdir=/etc \
    --localstatedir=/var

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" pkgconfigdir="/usr/lib/pkgconfig" install

  install -Dm644 samples/cgred.conf "$pkgdir/etc/conf.d/cgred"
  install -Dm644 samples/cgconfig.conf "$pkgdir/etc/cgconfig.conf"
  install -Dm644 samples/cgrules.conf "$pkgdir/etc/cgrules.conf"
  install -Dm644 samples/cgsnapshot_blacklist.conf "$pkgdir/etc/cgsnapshot_blacklist.conf"

  cd "$pkgdir"

  mv lib/security/pam_cgroup.so.*.*.* lib/security/pam_cgroup.so
  rm -f lib/security/pam_cgroup.so.*

  rm -rf etc/rc.d
  install -Dm755 "$srcdir/cgred" etc/rc.d/cgred
  install -Dm755 "$srcdir/cgconfig" etc/rc.d/cgconfig
  install -Dm644 "$srcdir/cgconfig.conf" etc/conf.d/cgconfig

  # Make cgexec setgid cgred
  chown root:160 "$pkgdir/bin/cgexec"
  chmod 2755 "$pkgdir/bin/cgexec"
}

md5sums=('24a41b18de112e8d085bb1f7d9e82af7'
         '72abd2fd52853579b91e1ca6ea87197a'
         '8e2ee870398dd62d3c4730330bbf6c21'
         'cf4244ed9a413fa888be66bf0bc6d055')
