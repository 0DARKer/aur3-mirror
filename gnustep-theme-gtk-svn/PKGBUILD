# Maintainer: spider-mario <spidermario@free.fr>
pkgname=gnustep-theme-gtk-svn
pkgver=28908
pkgrel=4
pkgdesc="GNOME theme for GNUstep"
arch=(i686 x86_64)
url="http://www.gnustep.org/"
license=('LGPL')
depends=('gnustep-gui' 'gtk2' 'gdk-pixbuf2' 'gconf')
makedepends=('subversion')
conflicts=('gnustep-theme-gnome-svn')
replaces=('gnustep-theme-gnome-svn')
install=gnustep-theme-gtk.install

_svntrunk=http://svn.gna.org/svn/gnustep/modules/plugins
_svnmod=gnustep

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  . /etc/profile.d/GNUstep.sh
  cd themes/Gtk
  make
}

package() {
  cd "$srcdir/$_svnmod-build/themes/Gtk"
  . /etc/profile.d/GNUstep.sh
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
