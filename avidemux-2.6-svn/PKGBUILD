# Contributor: fredbezies <fredbezies@gmail.com>
# Based on avidemux-svn and Xemertix PKBGUILD.
# Code source used is the one which will give Avidemux 2.6
# Also using avidemux-2.5-i18n patch from official avidemux package in
# Archlinux extra repository.

pkgname=avidemux-2.6-svn
pkgver=5635
pkgrel=1
pkgdesc="A graphical tool to edit video(filter/re-encode/split).SVN version."
arch=(i686 x86_64)
url="http://www.avidemux.org/"
license=('GPL2')
depends=('libxv' 'libxml2' 'sdl')
makedepends=('cmake' 'libxslt' 'gtk2' 'qt' 'jack-audio-connection-kit' 'esound' 'libvorbis' \
'alsa-lib' 'lame' 'xvidcore' 'faad2' 'faac' 'x264' 'libsamplerate' 'subversion')
optdepends=('gtk2: for using the GTK GUI' \
'qt: for using the QT4 GUI' \
'lame, faac: for the corresponding audio encoder plugin' \
'faad2 : for the corresponding audio decoder plugin' \
'esound, jack-audio-connection-kit: for the corresponding audio device plugin' \
'x264, xvidcore: for the corresponding video encoder plugin')
provides=('avidemux')
conflicts=('avidemux' 'avidemux-svn')
source=('avidemux-2.5-i18n.patch')
md5sums=('0adb7cee81e06bfc454baf1d8fbcdd64')

_svntrunk=svn://svn.berlios.de/avidemux/branches/avidemux_2.6_branch_mean
_svnmod=avidemux

build() {
cd "$srcdir"

if [ -d $_svnmod/.svn ]; then
(cd $_svnmod && svn up -r $pkgver)
else
svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
fi

msg "SVN checkout done or server timeout"
msg "Starting make..."

cd "${srcdir}/avidemux"
patch -p1 < ../avidemux-2.5-i18n.patch || return 1
mkdir build
cd build
cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -D NO_GTK=0 -D NO_QT4=0 .. || return 1
make || return 1
make DESTDIR="${pkgdir}" install || return 1


# plugin build expects libraries to be already installed; we fake a prefix
# in build/ by symlinking all libraries to build/lib/
mkdir -p lib
cd lib
find ../avidemux -name '*.so*' | xargs ln -sft . || return 1
cd ../../plugins
mkdir build
cd build
cmake -D CMAKE_INSTALL_PREFIX=/usr -D AVIDEMUX_SOURCE_DIR=${srcdir}/avidemux \
-D AVIDEMUX_CORECONFIG_DIR=${srcdir}/avidemux/build/config \
-D AVIDEMUX_INSTALL_PREFIX=${srcdir}/avidemux/build -D CMAKE_BUILD_TYPE=Release .. || return 1
make || return 1
make DESTDIR="${pkgdir}" install || return 1

ln -s /usr/lib/ADM_plugins/videoEncoder/libADM_vidEnc_xvid.so "${pkgdir}/usr/lib/libADM_vidEnc_xvid.so"
ln -s /usr/lib/ADM_plugins/videoEncoder/libADM_vidEnc_x264.so "${pkgdir}/usr/lib/libADM_vidEnc_x264.so"


# freedesktop thingies
install -D -m644 ../../avidemux_icon.png "${pkgdir}/usr/share/pixmaps/avidemux.png" || return 1
install -D -m644 ../../avidemux2.desktop "${pkgdir}/usr/share/applications/avidemux.desktop" || return 1
# install -D -m644 ../../avidemux2-gtk.desktop "${pkgdir}/usr/share/applications/avidemux.desktop" || return 1
install -D -m644 ../../man/avidemux.1 "${pkgdir}/usr/share/man/man1/avidemux.1" || return 1

}
