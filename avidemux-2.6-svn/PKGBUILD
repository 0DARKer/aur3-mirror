#Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=avidemux-2.6-svn
pkgver=7848
pkgrel=1
pkgdesc="A graphical tool to edit video(filter/re-encode/split).svn version."
arch=('i686' 'x86_64')
url="http://www.avidemux.org/"
license=('GPL2')
depends=('libpulse' 'libdca' 'faac' 'lame' 'xvidcore' 'opencore-amr' 'libvpx' 'faad2' 'jack' 'aften' 'x264' 'mesa' 'libva' 'dcaenc')
makedepends=('cmake' 'svn' 'yasm')
provides=('avidemux2.6')
options=('!makeflags')

_svntrunk=svn://svn.berlios.de/avidemux/branches/avidemux_2.6_branch_mean
_svnmod=avidemux-2.6

# Enable CORE
_CORE=1
# Enable CLI
_CLI=1
# Enable GTK
_GTK=0
# Enable QT
_QT=1
# Enable Plugins
_PLG=1

if [ "${_CORE}" == "1" ]; then
  _CORE="--with-core"
else
  _CORE="--without-core"
fi
if [ "${_CLI}" == "1" ]; then
  _CLI="--with-cli"
else
  _CLI="--without-cli"
fi
if [ "${_GTK}" == "1" ]; then
  depends+=('gtk2')
  _GTK="--with-gtk"
else
  _GTK="--without-gtk"
fi
if [ "${_QT}" == "1" ]; then
  depends+=('qt')
  _QT="--with-qt4"
else
  _QT="--without-qt4"
fi
if [ "${_PLG}" == "1" ]; then
  _PLG="--with-plugins"
else
  _PLG="--without-plugins"
fi

build() {
  cd "${srcdir}"

  msg "Connecting to SVN server...."

  if [ -d "${srcdir}"/"${_svnmod}" ] ; then
    cd "${_svnmod}" && svn update
  else
    svn co "${_svntrunk}" "${_svnmod}"
  fi

  [ -d "${srcdir}"/"${_svnmod}"-build ] && rm -fr "${srcdir}"/"${_svnmod}"-build
  cp -R "${srcdir}"/"${_svnmod}" "${srcdir}"/"${_svnmod}"-build

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cd  "${srcdir}"/"${_svnmod}"-build

  bash bootStrap.bash ${_CORE} ${_CLI} ${_GTK} ${_QT} ${_PLG}
}

package() {
cp -R "${srcdir}"/"${_svnmod}"-build/install/* "${pkgdir}"/
}
