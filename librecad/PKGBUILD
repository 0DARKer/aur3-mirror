# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mickele <mimocciola at yahoo dot com> (librecad-git PKGBUILD)
# Contributor: Ilmari Repo <ilmari at gmail dot com> (librecad-svn PKGBUILD)
# Contributor: GazJ Gary James <garyjames82 at gmail dot com> (CADuntu PKGBUILD)

pkgname=librecad
pkgver=1.0.2
_pkgtag=a5c34b2
pkgrel=2
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad."
arch=('i686' 'x86_64')
url="http://www.librecad.org/"
license=('GPL')
depends=('qt' 'boost' 'muparser')
conflicts=('librecad-git')
replaces=('librecad-svn' 'caduntu' 'caduntu-svn')
source=("${pkgname}-${pkgver}.zip::https://nodeload.github.com/LibreCAD/LibreCAD/zipball/v${pkgver}"
	'librecad.desktop')

build() {
  cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"

  # this script lacks execution permission...
  chmod +x scripts/postprocess-unix.sh

  qmake
  make
}

package() {
  cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"
  install -D -m 755 unix/librecad ${pkgdir}/usr/bin/librecad
  install -D -m 644 "${srcdir}/librecad.desktop" "${pkgdir}/usr/share/applications/librecad.desktop"
  install -D -m 644 res/main/librecad.png "${pkgdir}/usr/share/pixmaps/librecad.png"
  mkdir -p "${pkgdir}/usr/share/librecad/"
  cp -r unix/resources/{library,patterns,fonts,qm} "${pkgdir}/usr/share/librecad/"
}

sha256sums=('391365f5ec25ea8d79e18b34c8f3ece4c51b6dd91750dcc66bce08c95f11ef54'
            '1396d5a23dd289e42609b368fd81b305c4528de21852cae319a213c8f8bb6f2d')
