# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mickele <mimocciola at yahoo dot com> (librecad-git PKGBUILD)
# Contributor: Ilmari Repo <ilmari at gmail dot com> (librecad-svn PKGBUILD)
# Contributor: GazJ Gary James <garyjames82 at gmail dot com> (CADuntu PKGBUILD)

pkgname=librecad
pkgver=2.0.0beta5
_pkgtag=dc0661a
pkgrel=3
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad"
arch=('i686' 'x86_64')
url="http://www.librecad.org/"
license=('GPL')
depends=('qt4' 'muparser')
makedepends=('boost' 'imagemagick')
conflicts=('librecad-git')
replaces=('librecad-svn' 'caduntu' 'caduntu-svn')
install=librecad.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/LibreCAD/LibreCAD/tarball/${pkgver}"
	'0001-bug-3611536-Line-width-changes-bad-saved.patch'
	'0002-arc-trim-debugging.patch'
	'0003-arc-trimming-point-more-work-needed.patch'
	'0004-detect-zooming-overflow-bug-3608502.patch'
	'0005-correct-angle-in-circle-hatch.patch'
	'0006-remove-qt3support-in-nsis-script.patch'
	'0007-more-correct-save-of-dims-in-R12.patch'
	'0008-bug-409-fixed-an-infinite-loop.patch')

build() {
	cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"

	# fix version string
	sed -i "/^SCMREVISION/c SCMREVISION=\"${pkgver}\"" librecad/src/src.pro

	# some critical stuff, already in master for 2.0
	for PATCH in ${srcdir}/*.patch; do
		patch -Np1 < ${PATCH}
	done

	export CPPFLAGS="-std=c++0x"
	export PATH="/usr/lib/qt4/bin:${PATH}"
	qmake-qt4 librecad.pro
	make
}

package() {
	cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"

	install -D -m 755 unix/librecad "${pkgdir}/usr/bin/librecad"
	install -D -m 755 unix/ttf2lff "${pkgdir}/usr/bin/ttf2lff"

	install -D -m 644 desktop/librecad.desktop "${pkgdir}/usr/share/applications/librecad.desktop"
	install -D -m 644 desktop/librecad.1 "${pkgdir}/usr/share/man/man1/librecad.1"

	for SIZE in 16 24 32 48 64 96 128; do
		convert -scale ${SIZE} \
			desktop/graphics_icons_and_splash/Icon\ LibreCAD/Icon_Librecad.svg \
			${srcdir}/librecad.png
		install -D -m 644 ${srcdir}/librecad.png "${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/librecad.png"
	done
	install -D -m 644 desktop/graphics_icons_and_splash/Icon\ LibreCAD/Icon_Librecad.svg "${pkgdir}/usr/share/icons/hicolor/scalable/apps/librecad.svg"

	mkdir -p "${pkgdir}/usr/share/librecad/"
	cp -r unix/resources/{library,patterns,fonts,qm} "${pkgdir}/usr/share/librecad/"
}

sha256sums=('adbeca5191ac4c84900570a14ba4ccb3be48301f4a7475a772c0bfcfa5f5e6c3'
            'f2397f0b4fd9208bbcda15ec2d426b3e66cdd815ffdfd0d84fdff26bc3c88692'
            '58417216a6b3a0f4bda807856c2d3c7123d3fc7ad0fffc061929d3d6be9e6daa'
            '3c075021357c41a860e2656b307e020c8acc543ed789dabb245fbed379f3f3e6'
            'd31ce4aaa9e56cde7ecaaffac26f526cedd1d923a49d9bf7356934aee85fa696'
            '88ac34f9a233ab316c8060e20a7964d6eddf33195477e1c9e4f236c3b53aea9a'
            'e7492e7f62a00542d603d0aa72f3a683fe4c18f91dc56772b2445ac626bf0962'
            '18a516f73768142d05ced955da67a06d9aac6def85ce333cb2cdad1e31a3769d'
            '9333d3ca6c91f6d61e9d9afcd77c2bdd6e8a49f33f1f2a7ad15fa7f7028cbfea')
