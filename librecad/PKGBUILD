# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mickele <mimocciola at yahoo dot com> (librecad-git PKGBUILD)
# Contributor: Ilmari Repo <ilmari at gmail dot com> (librecad-svn PKGBUILD)
# Contributor: GazJ Gary James <garyjames82 at gmail dot com> (CADuntu PKGBUILD)

pkgname=librecad
pkgver=1.0.0rc4
_pkgtag=df9e745
pkgrel=1
pkgdesc="A 2D CAD drawing tool based on the community edition of QCad."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/librecad/"
license=('GPL')
depends=('qt' 'boost')
conflicts=('librecad-git')
replaces=('librecad-svn' 'caduntu' 'caduntu-svn')
source=("${pkgname}-${pkgver}.zip::https://nodeload.github.com/LibreCAD/LibreCAD/zipball/${pkgver}"
	'librecad.desktop')

build() {
  cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"
  qmake
  make
}

package() {
  cd "${srcdir}/LibreCAD-LibreCAD-${_pkgtag}"
  install -D -m 755 unix/librecad ${pkgdir}/usr/bin/librecad
  install -D -m 644 "${srcdir}/librecad.desktop" "${pkgdir}/usr/share/applications/librecad.desktop"
  install -D -m 644 res/main/librecad.png "${pkgdir}/usr/share/pixmaps/librecad.png"
  mkdir -p "${pkgdir}/usr/share/librecad/"
  cp -r unix/resources/{library,patterns,fonts,qm} "${pkgdir}/usr/share/librecad/"
}
md5sums=('86b90caa052a6a6fcb83a7b3f359e321'
         '19dcb83a3fcdb3752439095b118ac6d3')
