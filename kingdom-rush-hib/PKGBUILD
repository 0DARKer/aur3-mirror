# Maintainer: Yaohan Chen <yaohan.chen@gmail.com>

pkgname=kingdom-rush-hib
pkgver=1396303607
pkgrel=1
pkgdesc='An epic fantasy action defense game'
arch=('i686' 'x86_64')
url='http://www.kingdomrush.com/home.html'
license=('Commercial')
depends=('libglapi')
if [ "$CARCH" = 'x86_64' ]; then
  depends=('lib32-libglapi')
fi
makedepends=('hib-dlagent')
source=("hib://KingdomRush_linux_${pkgver}.zip"
        'kingdom-rush.desktop')
md5sums=('6dce4a2534d8d1946fbe99fa7c5599d0'
         'a4e933da1ca5dcb75210bdf59b23d4e5')
options=('!strip' '!upx')

package() {
  _installdir='/opt/kingdom-rush'

  cd "$srcdir/linux"
  install -Dm755 'Kingdom Rush.x86' "$pkgdir/$_installdir/kingdom-rush"
  install -d "$pkgdir/usr/bin/"
  ln -s "$_installdir/kingdom-rush" "$pkgdir/usr/bin/"

  _datadir="$srcdir/linux/Kingdom Rush_Data"
  install -Dm644 "$_datadir/Resources/UnityPlayer.png" \
          "$pkgdir/usr/share/pixmaps/kingdom-rush.png"
  # the data dir must be named <executable basename>_Data
  mv "$_datadir" "$pkgdir/$_installdir/kingdom-rush_Data"

  install -Dm644 "$srcdir/kingdom-rush.desktop" \
          "$pkgdir/usr/share/applications/kingdom-rush.desktop"
}
