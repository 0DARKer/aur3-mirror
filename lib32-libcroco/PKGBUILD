# Maintainer : SpepS <dreamspepser at yahoo dot it>

_pkg=libcroco
pkgname=lib32-$_pkg
pkgver=0.6.2
pkgrel=3
pkgdesc="GNOME CSS2 parsing and manipulation toolkit"
arch=('x86_64')
url="http://www.gnome.org/"
license=('LGPL')
depends=("$_pkg" 'lib32-glib2' 'lib32-libxml2')
makedepends=('gcc-multilib')
source=("http://ftp.gnome.org/pub/gnome/sources/${_pkg}/0.6/${_pkg}-${pkgver}.tar.bz2")
options=('!libtool')
md5sums=('1429c597aa4b75fc610ab3a542c99209')
sha256sums=('be24853f64c09b63d39e563fb0222e29bae1a33c3d9f6cbffc0bc27669371749')

build() {
    cd "${srcdir}/${_pkg}-${pkgver}"

    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    ./configure \
	--prefix=/usr \
	--libdir=/usr/lib32 \
	--disable-static

    make
}

package() {
    cd "${srcdir}/${_pkg}-${pkgver}"

    make DESTDIR="${pkgdir}" install

    # Removing unneeded files
    rm -rf "${pkgdir}"/usr/{bin,include}
}
