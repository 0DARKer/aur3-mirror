# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from xfce4-mixer package)
# Contributor: Tobias Kieslich <tobias@archlinux.org>

_pkgname=xfce4-mixer
pkgname=$_pkgname-git
pkgver=20121111
pkgrel=1
pkgdesc="The volume control plugin for the Xfce panel"
arch=(i686 x86_64)
license=(GPL2)
url=http://git.xfce.org/apps/$_pkgname/tree/README
groups=(xfce4-git)
depends=(xfce4-panel gstreamer0.10-base libunique)
makedepends=(git xfce4-dev-tools)
optdepends=('gstreamer0.10-base-plugins:  to support basic audio hardware'
    'gstreamer0.10-good-plugins: well supported hardware'
    'gstreamer0.10-bad-plugins: not so well supported hardware'
    'gstreamer0.10-ugly-plugins: might contain questionable license hardware')
provides=($_pkgname=4.10.1)
conflicts=($_pkgname)
options=(!libtool)
install=$_pkgname.install

_gitroot=git://git.xfce.org/apps/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh  \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --disable-static \
        --disable-debug

    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
    sed -e 's:Name=Mixer:& Settings:g' \
        -e '/Categories=X-XFCE;/ s|Audio;Mixer;|Settings;DesktopSettings;|g' \
        -i "$pkgdir"/usr/share/applications/$_pkgname.desktop
}
