# Maintainer: SpepS <dreamspepser at yahoo dot it>

_p=boar-daily
_v=11-Jul-2011
pkgname=boar
pkgver=0.11.07.2011
pkgrel=1
pkgdesc="Simple version control and backup for photos, videos and other binary files."
arch=('any')
url="http://code.google.com/p/boar/"
license=('GPL')
depends=('python2')
source=("http://boar.googlecode.com/files/$_p.$_v.tar.gz")
md5sums=('ff806dd15208466e7530b04487a2cda4')

build() {
  cd "$srcdir/$pkgname"

  # python2 fixes
  sed -i "s|\(env python\).*|\12|" `grep -rl "env python" .`
  sed -i "s|\(python\) |\12 |" run_tests.sh

  # remove unneeded imports
  sed -i "38,41d" workdir.py
}

package() {
  cd "$srcdir/$pkgname"

  # data
  install -d "$pkgdir"/usr/{bin,share/$pkgname}
  cp -a *[^bat] "$pkgdir/usr/share/$pkgname"

  # boar bin
  cat << EOF >> "$pkgdir/usr/bin/$pkgname"
#!/bin/bash
cd /usr/share/$pkgname && python2 $pkgname \$@
EOF

  # boarmount bin
  cat << EOF >> "$pkgdir/usr/bin/${pkgname}mount"
#!/bin/bash
cd /usr/share/$pkgname && python2 ${pkgname}mount \$@
EOF

  # exec mod
  chmod +x "$pkgdir/usr/bin/"*
}

# vim:set ts=2 sw=2 et:
