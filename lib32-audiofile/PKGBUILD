# $Id: PKGBUILD 26514 2010-09-15 22:28:30Z bluewind $
# Maintainer: peace4all <markspost@rocketmail.com>
_pkgbasename=audiofile
pkgname=lib32-$_pkgbasename
pkgver=0.3.3
pkgrel=1
pkgdesc="Silicon Graphics Audio File Library (32-bit)"
arch=('x86_64')
url="http://www.68k.org/~michael/audiofile/"
license=('LGPL')
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
source=(http://ftp.acc.umu.se/pub/gnome/sources/${_pkgbasename}/0.3/${_pkgbasename}-${pkgver}.tar.xz)
sha256sums=('f5229c6d5a0dc1126e0afae04f03cbda6d3d5bc985588ee88ad66561bf4cadc5')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
