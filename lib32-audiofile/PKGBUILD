# $Id: PKGBUILD 26514 2010-09-15 22:28:30Z bluewind $
# Maintainer: peace4all <markspost at rocketmail dot com>
_pkgbasename=audiofile
pkgname=lib32-$_pkgbasename
pkgver=0.3.4
pkgrel=1
pkgdesc="Silicon Graphics Audio File Library (32-bit)"
arch=('x86_64')
url="http://www.68k.org/~michael/audiofile/"
license=('LGPL')
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
source=(http://ftp.acc.umu.se/pub/gnome/sources/${_pkgbasename}/0.3/${_pkgbasename}-${pkgver}.tar.xz)
md5sums=('3ef35d77945a096957627f7b270f1981')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
