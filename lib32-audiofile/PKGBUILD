# Maintainer: peace4all <markspost at rocketmail dot com>

_pkgbasename=audiofile
pkgname=lib32-$_pkgbasename
pkgver=0.3.5
pkgrel=1
pkgdesc="Silicon Graphics Audio File Library (32-bit)"
arch=('x86_64')
url="http://www.68k.org/~michael/audiofile/"
license=('LGPL')
depends=('lib32-gcc-libs' 'audiofile' 'lib32-alsa-lib')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://audiofile.68k.org/$_pkgbasename-$pkgver.tar.gz")
md5sums=('795d7f5b323876c14c43d2b650b16abd')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
