# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>

pkgname=ultra-flat-icons
pkgver=1.2
pkgrel=1
pkgdesc="A simple flat icon theme, derived from Flattr icon theme."
arch=(
	"any"
)
url="http://gnome-look.org/content/show.php?content=167477"
license=(
	"CCPL:cc-by-nc-sa-4.0"
)

depends=()
optdepends=()
makedepends=()
provides=(
	"${pkgname}=${pkgver}"
)
conflicts=(
	"${pkgname}"
)
replaces=(
	"${pkgname}"
)

# Original source <https://www.dropbox.com/s/dktl70lh4lnhp0a/ultra-flat-icons.tar.gz>
source=(
	"https://www.dropbox.com/s/zclmiiqc1q229hk/${pkgname}-${pkgver}-${pkgrel}.tar.gz"
)
md5sums=(
	"904bc2db1f924b596cde14cf3d85ba67"
)
sha512sums=(
	"2470080a4ee72fc322508a61b161e0a7d3e150a80d491da771181c31ca543fbd0ada653debcccc967baa575d86edd853cd194f94fe46dc29e97f5bb540b2b448"
)

_iconset=UltraFlatIcons

package() {
	# Create base directories
	install -dm755 ${pkgdir}/usr/share/icons
	
	# Copy icon set
	install -dm755 ${pkgdir}/usr/share/icons/${_iconset}
	cp -R ${srcdir}/${pkgname}/* ${pkgdir}/usr/share/icons/${_iconset}
	
	# Change current working directory
	cd ${pkgdir}/usr/share/icons/${_iconset}
	
	# Education category fix
	ln -fsr apps/scalable/scholar.svg categories/scalable/applications-science.svg
	
	# Multimedia category fix
	rm -f apps/scalable/applications-multimedia.svg
	ln -fsr apps/scalable/openshot.svg categories/scalable/applications-multimedia.svg
	
	# Office category fix
	ln -fsr apps/scalable/libreoffice34-startcenter.svg categories/scalable/applications-office.svg
	
	# Volume icons fix (dark theme)
	ln -fsr status/scalable\ white\ panel/audio-volume-muted-panel.svg status/scalable/audio-volume-muted.svg
	ln -fsr status/scalable\ white\ panel/audio-output-none-panel.svg status/scalable/audio-output-none.svg
	ln -fsr status/scalable\ white\ panel/audio-volume-low-panel.svg status/scalable/audio-volume-low.svg
	ln -fsr status/scalable\ white\ panel/audio-volume-medium-panel.svg status/scalable/audio-volume-medium.svg
	ln -fsr status/scalable\ white\ panel/audio-volume-high-panel.svg status/scalable/audio-volume-high.svg
	
	# Wicd icon fix
	install -dm755 ${pkgdir}/usr/share/icons/${_iconset}/extras/pixmaps/wicd
	ln -fsr apps/scalable/wicd-gtk.svg extras/pixmaps/wicd/wicd-gtk.svg
	ln -fsr apps/scalable/wicd-qt.svg extras/pixmaps/wicd/wicd-qt.svg
}