# Maintainer: Virgil Dupras <hsoft@hardcoded.net>
pkgname=dupeguru-me
pkgver=6.5.1
pkgrel=1
pkgdesc="Find duplicate songs on your system"
arch=(any)
url="http://www.hardcoded.net/dupeguru_me/"
license=('BSD')
depends=('python' 'pyqt')
makedepends=(
  'python-distribute'
  'python-pip'
  'python-virtualenv'
  'python-sphinx'
  'gcc'
)
source=(http://download.hardcoded.net/$pkgname-src-$pkgver.tar.gz
        dupeguru-me.desktop)
md5sums=('83b0685c515479f592828be4d121dfee'
         '227d5f8ba25a57367d748633f8a75372')

build() {
cd "$srcdir/$pkgname-src-$pkgver"
  if [[ ! -d "env" ]]; then
    msg "Creating virtualenv"
    virtualenv --system-site-packages env
  fi
  
  msg "Installing dependencies"
  source env/bin/activate
  pip install -r requirements.txt
  
  msg "Starting build..."
  python configure.py --edition me
  python build.py --clean
}

package() {
  cd "$srcdir"
  mkdir -p "${pkgdir}/usr/share/applications"
  cp ${pkgname}.desktop "${pkgdir}/usr/share/applications"
  
  cd "$pkgname-src-$pkgver"
  python package.py
  cd "build/${pkgname}-arch"
  
  mkdir -p "$pkgdir/usr/share/${pkgname}"
  cp -a * "$pkgdir/usr/share/${pkgname}/"
  chmod a+x "$pkgdir/usr/share/${pkgname}/run.py"
  
  mkdir -p "$pkgdir/usr/bin"
  ln -s ../share/${pkgname}/run.py "$pkgdir/usr/bin/${pkgname}"
}
