# Maintainer: Virgil Dupras <hsoft@hardcoded.net>
pkgname=dupeguru-me
pkgver=6.8.0
pkgrel=1
pkgdesc="Find duplicate songs on your system"
arch=(any)
url="http://www.hardcoded.net/dupeguru_me/"
license=('BSD')
depends=('python' 'python-pyqt5')
makedepends=(
    python-sphinx
    gcc
)
source=(
    http://download.hardcoded.net/$pkgname-src-$pkgver.tar.gz
)
md5sums=(
    '73647d64844eb6488b5e196273a92e2c'
)

build() {
  cd "$srcdir"
  ./bootstrap.sh
  source env/bin/activate
  msg "Starting build..."
  python configure.py --edition me
  python build.py --clean
}

package() {
  cd "$srcdir"
  
  python package.py
  cd "build/${pkgname}-arch"

  mkdir -p "${pkgdir}/usr/share/applications"
  mv ${pkgname}.desktop "${pkgdir}/usr/share/applications"
  
  mkdir -p "$pkgdir/usr/share/${pkgname}"
  cp -a * "$pkgdir/usr/share/${pkgname}/"
  chmod a+x "$pkgdir/usr/share/${pkgname}/run.py"
  
  mkdir -p "$pkgdir/usr/bin"
  ln -s ../share/${pkgname}/run.py "$pkgdir/usr/bin/${pkgname}"
}
