# Maintainer : Andr√© Fettouhi <a.fettouhi@gmail.com>
# Contributor: mirandir <mirandir[at)orange(dot]com>

pkgname=gog-the-witcher-2-assassins-of-kings
pkgver=1.0.0.4
#patchver=1.0.0.4
pkgrel=3
pkgdesc="The second installment in the RPG saga about the Witcher, Geralt of Rivia. GOG version (Linux installer, English)."
arch=("i686" "x86_64")

if [[ $CARCH == i686 ]]; then
  depends=(sdl2 freetype2 sdl2_image gtk2)
else
  depends=(lib32-sdl2 lib32-freetype2 lib32-sdl2_image lib32-gtk2)
fi

url="http://www.gog.com/game/the_witcher_2"
license=("custom")
groups=("games")
source=("gog_the_witcher_2_assassins_of_kings_${pkgver}.tar.gz" "gog-the-witcher-2-assassins-of-kings" "libcurl.so.4" "configurator" "gog-the-witcher-2-configurator.desktop")
sha256sums=('6a37aa3810fcdd2f3e8ff78168699e814215a4ff294d5145e677fc1dd45ebce7'
            'd276f5b31bd53d4a8a5a374cb6f51d07a66d36ddadce6f36a0c10f6265f48231'
            'c59269b471437df2bded9b116b2319cb83a0b46dccac3001fe3c211046a78fd4'
            'dc0225d6272e41ec7f6500ac6a5fb0f99df54b0f35dcdb6f83191f5b94898b12'
            '8548b45672e5b9a972fe828d7a4035401d165574a92b1a77b2e4ac719c2973eb')
#options=('!strip')
PKGEXT=.pkg.tar

package() {
  mkdir -p "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/
  cp -r "${srcdir}"/The\ Witcher\ 2\ Assassins\ Of\ Kings/* "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/
# cp -r "${srcdir}"/The\ Witcher\ 2\ Assassins\ Of\ Kings\ Patch\ ${patchver}/content/* "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/
  
  # workaround for https://github.com/KillaW0lf04/The-Witcher-2-Issues/issues/75
  mkdir -p "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/tw2tmp/
  mv "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/bin "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/tw2tmp/
  mv "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/eONprecompiledShaders32.dat "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/tw2tmp/
  ln -s /tmp/tw2tmp/eONprecompiledShaders32.dat "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/eONprecompiledShaders32.dat
  ln -s /tmp/tw2tmp/bin "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/bin
  mv "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/configurator "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/configurator_
  install -Dm755 "${srcdir}/configurator" "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/configurator
  
  install -Dm644 "${srcdir}"/libcurl.so.4 "${pkgdir}"/opt/gog/the-witcher-2-assassins-of-kings/game/
  install -Dm644 "${srcdir}"/The\ Witcher\ 2\ Assassins\ Of\ Kings/support/gog-the-witcher-2-assassins-of-kings-primary.desktop "${pkgdir}"/usr/share/applications/gog-the-witcher-2-assassins-of-kings.desktop
  install -Dm644 "${srcdir}"/The\ Witcher\ 2\ Assassins\ Of\ Kings/support/gog-the-witcher-2-assassins-of-kings.png "${pkgdir}"/usr/share/pixmaps/gog-the-witcher-2-assassins-of-kings.png
  # install .desktop file for the configurator
  install -Dm644 "${srcdir}"/gog-the-witcher-2-configurator.desktop "${pkgdir}"/usr/share/applications/gog-the-witcher-2-configurator.desktop
  install -Dm644 "${srcdir}"/The\ Witcher\ 2\ Assassins\ Of\ Kings/docs/End\ User\ License\ Agreement.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -Dm755 "${srcdir}/gog-the-witcher-2-assassins-of-kings" "${pkgdir}/usr/bin/gog-the-witcher-2-assassins-of-kings"
}
