# Maintainer: Michael Schubert <mschu.dev at gmail>

pkgname=mgltools
pkgver=1.5.6
pkgrel=2
pkgdesc="Visualization and analysis of molecular structures; includes AutoDockTools, Vision and PythonMoleculeViewer"
arch=('any')
url="http://mgltools.scripps.edu/"
license=('custom:popup')
depends=('swig' 'tk' 'python-imaging' 'python-pmw' 'glut' 'zsi' 'simpy')
source=("http://mgltools.scripps.edu/downloads/tars/releases/REL${pkgver}/${pkgname}_source_${pkgver}.tar.gz")
md5sums=('ea43cdd08347772bf37fca8d0c46a751')

build() {
  cd "$srcdir/${pkgname}_source_$pkgver"
  sed -i "1s:python2.5:python2:" InstTools/install

  # replace path by os.path where needed
  sed -i "s:path.exists:os.path.exists:; \
    s:os.os.path.exists:os.path.exists:; \
    s:path.join:os.path.join:; \
    s:os.os.path.join:os.path.join:" InstTools/install

  # do not install pmw since it is dependecy already
  sed -i 's:getYesNo(msg, "Do you wish to install Pmw:False #:' InstTools/install
  # fix include for strcmp in geomutils
  sed -i "239i\    if (pkgname == \"geomutils\"): os.popen(\"sed -i '3i#include\
    <string.h>' src/geomAlgorithms/objfile.cpp\")" InstTools/install
  # fix opengltk to work with tcl/tk 8.5
  sed -i "240i\    if (pkgname == \"opengltk\"): \
    os.popen(\"sed -i 's/stub8.4/stub8.5/g' setup.py\")" InstTools/install
}

package() {
  cd "$srcdir/${pkgname}_source_$pkgver"

  # let python know about the new modules
  pydir=`python2 -c "from distutils.sysconfig import get_python_lib; \
    print get_python_lib()"`
  mkdir -p "$pkgdir/$pydir"
  ./startInstallation --quiet --instDir="$pkgdir/$pydir"

  # link to bin directory
  install -Dm755 "$pkgdir/$pydir/runAdt" "$pkgdir/usr/bin/adt"
  install -Dm755 "$pkgdir/$pydir/runPmv" "$pkgdir/usr/bin/pmv"
  install -Dm755 "$pkgdir/$pydir/runCADD" "$pkgdir/usr/bin/CADD"
  install -Dm755 "$pkgdir/$pydir/runVision" "$pkgdir/usr/bin/vision"

  echo "MGLToolsPckgs" > "$pkgdir/$pydir/mgltools.pth"

  # fix some python 2.7 issues in the package
  sed -i "/self.__debug__ = 0/d" \
    "$pkgdir/$pydir/MGLToolsPckgs/ViewerFramework/VF.py"

  # remove VCS entries
  rm -rf `find "$pkgdir" -type d -name "CVS"`

  # change interpreter from python to python2
  find "$pkgdir/$pydir/MGLToolsPckgs" -name "*.py" | xargs sed -i "1s/python$/python2/"
}
