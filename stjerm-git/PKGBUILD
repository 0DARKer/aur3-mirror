# Mantainer: Diego <cdprincipeat gmaildot com>
# Contributor: Stjepan Glavina <stjepang@gmail.com>
# Contributor: Markus Gross <xsdnyd@gmail.com>
# Contributor: Andrea Fagiani <andfagiani _at_ gmail dot com>
pkgname=stjerm-git
pkgver=0.16_7_g086916a
pkgrel=1
pkgdesc="Drop-down terminal emulator."
arch=('i686' 'x86_64')
url="http://code.google.com/p/stjerm-terminal"
license=('GPL')
depends=('vte')
makedepends=('autoconf' 'git')
source=($pkgname::git://github.com/mrkrstphr/stjerm.git)
md5sums=(SKIP)

pkgver() {
  cd $pkgname
  git describe --always | sed 's#-#_#g;s#v##'
}

build() {
  cd $srcdir/$pkgname
  touch README
  ./autogen.sh
  export LIBS=-lXinerama
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname
  make DESTDIR="$pkgdir/" install
}
