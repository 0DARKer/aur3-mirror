# Maintainer: JokerBoy <jokerboy at punctweb dot ro>

pkgname=thingmenu
pkgver=0.4
pkgrel=1
pkgdesc="A simple X11 menu"
arch=('i686' 'x86_64')
url="http://git.r-36.net/thingmenu/"
license=('MIT')
depends=('libx11')
source=("http://git.r-36.net/thingmenu/snapshot/$pkgname-$pkgver.zip"
        'config.h')
sha256sums=('b49062270d266ae788e85acf93bd3ab3685309e1f99c1a6936ab9bc65608e550'
            '568654308045c9352e1ce72dfafd3d4143227d81af5048a2c5c46abef3056e20')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp "$srcdir/config.h" config.h

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS = -g/CFLAGS += /g' config.mk
  sed -i 's/^LDFLAGS = -g/#LDFLAGS += -g/g' config.mk
  sed -i 's/^#LDFLAGS = -s/LDFLAGS += -s/g' config.mk

  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README.md "$pkgdir/usr/share/doc/$pkgname/README"
}
