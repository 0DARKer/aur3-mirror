# Maintainer: JokerBoy <jokerboy at punctweb dot ro>

pkgname=thingmenu
pkgver=0.7
pkgrel=1
pkgdesc="A simple X11 menu"
arch=('i686' 'x86_64')
url="http://git.r-36.net/thingmenu/"
license=('MIT')
depends=('libx11')
source=("http://git.r-36.net/thingmenu/snapshot/$pkgname-$pkgver.zip"
        'config.h')
sha256sums=('d6b92b1403498f2f90ab8982a9af153f74324f85ebc09aae46be8692ef13d965'
            'c4eb237ea2d61acb312f933b7ad19e853c1854f01e356c6ab2d0f83e523ebcb3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp "$srcdir/config.h" 'config.h'
  sed -i 's/^CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/^CFLAGS =/CFLAGS +=/g' config.mk
  sed -i 's/^LDFLAGS =/LDFLAGS +=/g' config.mk
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D 'LICENSE' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
