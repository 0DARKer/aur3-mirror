# Maintainer: Jesse Spangenberger <azulephoenix@gmail.com>
pkgname=private-internet-access-vpn
pkgver=0.3
pkgrel=1
pkgdesc="Installs VPN profiles for Private Internet Access Service"
arch=('any')
url="https://www.privateinternetaccess.com/"
license=('GPL')
depends=('openvpn')

sha256sums=('edbf08f15ee016c0348ffdbf1dda6c2064da4892d5c0733af10b3089f9492f4f'
            '7f4a5ee1fb8ea4d0e69ed2a8217c575cf335f21e90082f6e423c769eca4a7a46'
            '246fc4dc3218f56b4c70014df6801b10fc2a573d6545962b7fce05f16908c54e')

source=("https://www.privateinternetaccess.com/openvpn/openvpn.zip"
        "vpn.sh"
        "restart.conf")
noextract=("openvpn.zip")

prepare() {
  cd "${srcdir}"
  mkdir "vpn-configs"
  bsdtar -xf openvpn.zip -C "vpn-configs"

  cd "vpn-configs"

  # Switch .ovpn file extensions to .conf.  This is what the openvpn systemd
  # service expects
  for file in *.ovpn
  do
    mv "$file" "${file/%.ovpn/.conf}"
  done

  # Swap spaces in filenames for underscores to be more command-line friendly
  for file in *\ *
  do
    mv "$file" "${file// /_}"
  done
}

package() {
  cd "${srcdir}"

  install -dm 755 "${pkgdir}/etc/openvpn/"
  install -dm 755 "${pkgdir}/usr/lib/system/systemd/system-sleep/"
  install -dm 755 "${pkgdir}/usr/lib/system/systemd/openvpn@.service.d"

  install -Dm 644 vpn-configs/*.* "${pkgdir}/etc/openvpn/"
  install -Dm 755 "vpn.sh" "${pkgdir}/usr/lib/system/systemd/system-sleep/"
  install -Dm 644 "restart.conf" "${pkgdir}/usr/lib/system/systemd/openvpn@.service.d/restart.conf"
}
