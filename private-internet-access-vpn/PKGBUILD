# Maintainer: Jesse Spangenberger <azulephoenix@gmail.com>
pkgname=private-internet-access-vpn
pkgver=1
pkgrel=1
pkgdesc="Installs VPN profiles for Private Internet Access Service"
arch=('any')
url="https://www.privateinternetaccess.com/"
license=('GPL')
depends=('python')

sha256sums=('8182a659edfb120bd5f3eee20dba50da889ba28cb1aa76caa0b4d5199f338522'
            '6d3bdc9531f16cc1ad199913a71554a0b50aea87e140b28d079c4ab4c0b8c51b'
            '8be463f28d56cb5140c36c23b99b89f79fd4f6fefb9ac140071764215a85c02c'
            '7ee83f40344abceefba936554108b2a66de580bbca90ba27d18f563e16e5c20e')

source=("https://www.privateinternetaccess.com/openvpn/openvpn.zip"
        "https://raw.githubusercontent.com/masterkorp/openvpn-update-resolv-conf/master/update-resolv-conf.sh"
		"source.gz"
		"pia-auto-login.py")
		
noextract=("openvpn.zip"
           "source.gz")

prepare() {
  cd "${srcdir}"
  mkdir "vpn-configs"
  bsdtar -xf openvpn.zip -C "vpn-configs"

  cd "vpn-configs"

  # Switch .ovpn file extensions to .conf.  This is what the openvpn systemd
  # service expects
  for file in *.ovpn
  do
    mv "$file" "${file/%.ovpn/.conf}"
  done

  # Swap spaces in filenames for underscores to be more command-line friendly
  for file in *\ *
  do
    mv "$file" "${file// /_}"
  done
  
  # Attempt to ensure that we use PIA DNS servers
  for file in *.conf
  do
    echo "script-security 2" >> "$file"
    echo "up /etc/openvpn/update-resolv-conf.sh" >> "$file"
    echo "down /etc/openvpn/update-resolv-conf.sh" >> "$file"
  done
}

package() {
  cd "${srcdir}"
  
  bsdtar -xf source.gz -C "${pkgdir}"

  install -dm 755 "${pkgdir}/etc/openvpn/"
  install -dm 755 "${pkgdir}/usr/bin/"

  install -Dm 644 vpn-configs/*.* "${pkgdir}/etc/openvpn/"
  install -m 755 update-resolv-conf.sh "${pkgdir}/etc/openvpn"
  install -m 755 pia-auto-login.py "${pkgdir}/usr/bin"
}
