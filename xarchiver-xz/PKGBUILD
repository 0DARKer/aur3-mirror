# Maintainer: Ivan Puntiy <ivan.puntiy at gmail>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=xarchiver-xz
pkgver=0.5.2
pkgrel=3
pkgdesc="GTK+ frontend to various command line archivers, with xz support patch"
arch=('i686' 'x86_64')
url="http://xarchiver.sourceforge.net"
license=('GPL')
groups=('xfce4-goodies')
depends=('gtk2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('tar: TAR support'
            'gzip: GZIP support'
            'bzip2: BZIP2 support'
            'zip: ZIP support'
            'unzip: ZIP support'
            'unrar: RAR support'
            'p7zip: 7z support'
            'arj: ARJ support'
            'lha: LHA support'
            'lzma-utils: LZMA support'
            'lzop: LZOP support')
provides=("xarchiver=$pkgver")
conflicts=('xarchiver')
options=('!libtool')
install=xarchiver.install
source=(http://downloads.sourceforge.net/xarchiver/xarchiver-$pkgver.tar.bz2
        xarchiver-$pkgver-add_xz_support.patch
	xarchiver-$pkgver-xz-extract.patch)
md5sums=('2bc7f06403cc6582dd4a8029ec9d038d'
         '9facca6efd1c0760ce2072ea458c51cb'
         'b5e3417ba748387b939a114899991692')

build() {
  cd "$srcdir/xarchiver-$pkgver"

  # Slakware's XZ support patch
  patch -uNp1 -i ../xarchiver-$pkgver-add_xz_support.patch
  # ... and extraction support (missing in slak patch)
  patch -uNp1 -i ../xarchiver-$pkgver-xz-extract.patch

  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4
  make
}

package(){
  cd "$srcdir/xarchiver-$pkgver"

  make DESTDIR="$pkgdir" install
}
