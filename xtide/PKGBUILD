# Maintainer: Beej Jorgensen <beej@beej.us>
pkgname=xtide
pkgver=2.11
pkgrel=3
pkgdesc="Harmonic tide clock and tide predictor"
arch=('i686' 'x86_64')
url="http://www.flaterco.com/xtide/xtide.html"
license=('GPL')
depends=('libxaw' 'zlib' 'libxpm' 'libpng' 'libtcd')
optdepends=('gpsd')
backup=("etc/$pkgname/$pkgname.conf")
source=('ftp://ftp.flaterco.com/xtide/harmonics-dwf-20110410-free.tar.bz2'
  'ftp://ftp.flaterco.com/xtide/harmonics-dwf-20110410-nonfree.tar.bz2'
  'ftp://ftp.flaterco.com/xtide/wvs.tar.bz2'
  "ftp://ftp.flaterco.com/xtide/$pkgname-$pkgver.tar.bz2"
  "$pkgname-$pkgver-aur.patch"
  "$pkgname.conf")
noextract=('harmonics-dwf-20110410-free.tar.bz2'
  'harmonics-dwf-20110410-nonfree.tar.bz2'
  'wvs.tar.bz2')

md5sums=('985c2f69eea343782142fa459fc34128'
         '7e25432d92674992e3f817573dc9bf17'
         '56325c8105c7137ced73396f2f2d8221'
         '1d85890bb13b3345154e0f68831b7ebd'
         '16a37c21fc8bee1a3485325d735e62bd'
         '9965593e086a5b53457dc01d9f161784')

build() {
  # install data files
  install -d -m0755 "$pkgdir/usr/share/$pkgname"
  cd "$pkgdir/usr/share/$pkgname"
  tar xvf "$srcdir/harmonics-dwf-20110410-free.tar.bz2" -o
  tar xvf "$srcdir/harmonics-dwf-20110410-nonfree.tar.bz2" -o
  tar xvf "$srcdir/wvs.tar.bz2" -o

  # config file
  install -d -m0755 $pkgdir/etc/$pkgname
  install -m0644 $srcdir/$pkgname.conf $pkgdir/etc/$pkgname

  cd "$srcdir/$pkgname-$pkgver"

  # patch to look for conf in /etc/xtide
  patch -Np1 < ../$pkgname-$pkgver-aur.patch

  ./configure --prefix=/usr
  make
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
