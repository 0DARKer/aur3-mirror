# TODO: wvs in other package
# TODO: icon
# TODO: desktop file
# Maintainer: Beej Jorgensen <beej@beej.us>
pkgname=xtide
pkgver=2.13
pkgrel=1
pkgdesc="Harmonic tide clock and tide predictor"
arch=('i686' 'x86_64')
url="http://www.flaterco.com/xtide/xtide.html"
license=('GPL')
depends=('libxaw' 'zlib' 'libxpm' 'libpng' 'libtcd')
optdepends=(
  'gpsd: for automatic location finding'
  'xtide-wvs: for hi-res vector coastlines'
)
backup=("etc/$pkgname/$pkgname.conf")
source=('ftp://ftp.flaterco.com/xtide/harmonics-dwf-20121224-free.tar.bz2'
  "ftp://ftp.flaterco.com/xtide/$pkgname-$pkgver.tar.bz2"
  "${pkgname}-$pkgver-aur.patch"
  "${pkgname}.conf"
  "${pkgname}.desktop"
)
md5sums=('f9a8c5414aa14bd6b08d1be4e6a3eea7'
         'd208dcceddf73731e978ff2aaab5b576'
         '6e3a065e41473470919696d5e6205f6c'
         'cdcd2380d148525c6117c03621d10383'
         'eb5ebe8432103880ff3331f3236b3e74')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # patch to look for conf in /etc/xtide
  patch -Np1 < ../$pkgname-$pkgver-aur.patch

  ./configure --prefix=/usr
  make
}

package() {
  local hver=20121224
  local hdir=harmonics-dwf-${hver}
  local hfile=${hdir}-free.tcd

  # install harmonics file
  install -D -m0644 $srcdir/$hdir/$hfile "$pkgdir/usr/share/$pkgname/$hfile"

  # config file
  install -D -m0644 $srcdir/${pkgname}.conf $pkgdir/etc/$pkgname/${pkgname}.conf

  # desktop file
  install -D -m0644 $srcdir/${pkgname}.desktop $pkgdir/usr/share/applications/${pkgname}.desktop

  # binary
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # icon
  install -D -m0644 iconsrc/icon_48x48_orig.png $pkgdir/usr/share/pixmaps/${pkgname}.png
}

# vim:set ts=2 sw=2 et:
