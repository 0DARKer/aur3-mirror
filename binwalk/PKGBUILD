# Maintainer: Emmanuel Gras <gras.emmanuel@gmail.com>
pkgname=binwalk
pkgver=0.3.4
pkgrel=1
pkgdesc="A tool for searching a given binary image for embedded files"
arch=('i686' 'x86_64')
url="http://code.google.com/p/binwalk/"
license=('MIT')
depends=('curl')
provides=('binwalk')
source=(http://binwalk.googlecode.com/files/$pkgname-$pkgver.tar.gz
        binwalk.license)
md5sums=('86d0095e7185e67d27a07da9e8f22c76'
         '544998fb9b84e6dd6b3808965f9c8866')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr

  # workaround for version 0.3.4: remove #include <curl/types.h>,
  # which has been removed from curl.
  # to be fixed in next release
  sed -e '/curl\/types.h/d' update.c > tmp
  mv tmp update.c

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -m 755 -D $pkgname $pkgdir/usr/bin/$pkgname
  install -m 644 -D magic.binwalk $pkgdir/etc/$pkgname/magic.binwalk
  install -m 644 -D magic.bincast $pkgdir/etc/$pkgname/magic.bincast
  install -m 644 -D ../binwalk.license $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
