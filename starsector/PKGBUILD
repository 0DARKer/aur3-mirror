# Maintainer: Erdbeerkaese <erbeerkaese underscore arch at yahoo dot de>

pkgname=starsector
pkgver=0.54.1a_RC2
pkgrel=1
pkgdesc="A single-player sandbox style space role-playing game with strategic elements."
arch=('i686' 'x86_64')
url="http://fractalsoftworks.com/"
license=('custom')
depends=('java-runtime' 'openal' 'bash')
install='starsector.install'
replaces=('starfarer')
source=(
http://s3.amazonaws.com/fractalsoftworks/starsector/starsector_linux-${pkgver//_/-}.zip
http://www.fractalsoftworks.com/starfarer/docs/StarfarerManual.pdf
starsector.sh
starsector.desktop)
md5sums=('5013c755020d40b6db6d7a967da17e4c'
         'd677fdea9b9e8c42e6407e2d739be4f8'
         'f2856a6c2d6a473965b695f21ccce07e'
         '2ef5c56c150506f0a3b0ac3d7ffe09a5')
package() {
  install -d ${pkgdir}/usr/share/${pkgname} \
    ${pkgdir}/usr/share/doc/${pkgname} \
    ${pkgdir}/usr/share/java/${pkgname} \
    ${pkgdir}/usr/lib/${pkgname}

  cd ${srcdir}
  install -Dm644 StarfarerManual.pdf \
    ${pkgdir}/usr/share/doc/${pkgname}/${pkgname}.pdf
  install -Dm755 ${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
  install -Dm644 ${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop

  cd ${srcdir}/${pkgname}
  find . -type f -print | xargs chmod 644

  install -Dm644 *.jar ${pkgdir}/usr/share/java/${pkgname}
  if [[ "$CARCH" == x86_64 ]]; then
    install -Dm644 native/linux/*64.so ${pkgdir}/usr/lib/${pkgname}/
  else
    install -Dm644 native/linux/*[!"64"].so ${pkgdir}/usr/lib/${pkgname}/
  fi
  cp -R data graphics sounds ${pkgdir}/usr/share/${pkgname}/

  install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
# vim:set ts=2 sw=2 et:
