# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [testing]'s xorg-server

pkgbase=xorg-server
pkgname=('xorg-server-dev')
true && pkgname=('xorg-server-dev' 'xorg-server-common-dev' 'xorg-server-devel-dev' 'xorg-server-xdmx-dev' 'xorg-server-xephyr-dev' 'xorg-server-xnest-dev' 'xorg-server-xvfb-dev')
pkgver=1.11.99.2
pkgrel=1
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org"
makedepends=('bigreqsproto' 'compositeproto' 'dmxproto' 'dri2proto' 'fontsproto' 'glproto' 'inputproto-git' 'libdmx' 'libgcrypt' 'libgl' 'libpciaccess-git' 'libx11' 'libxaw' 'libxfont' 'libxi' 'libxkbfile' 'libxmu' 'libxrender' 'libxres' 'libxtst' 'libxv' 'mesa' 'pixman' 'randrproto' 'recordproto' 'renderproto' 'resourceproto' 'scrnsaverproto' 'udev' 'videoproto' 'xcmiscproto' 'xextproto' 'xf86dgaproto' 'xf86driproto' 'xineramaproto' 'xorg-font-util' 'xorg-util-macros' 'xorg-xkbcomp' 'xtrans')
options=('!libtool')
source=($url/releases/individual/xserver/$pkgbase-$pkgver.tar.bz2
        autoconfig-sis.patch
        revert-trapezoids.patch
        xvfb-run
        xvfb-run.1
        10-quirks.conf)
md5sums=('6814b7d84fb74abdb05dab900971ba00'
         '1fd33226f76591f3ff5c7ca493f9cabb'
         'cb7ed5403510861efa8b1b94f166b374'
         '52fd3effd80d7bc6c1660d4ecf23d31c'
         '376c70308715cd2643f7bff936d9934b'
         'd4f7dfc6be4ef4e2c6dd7632a9d88abe')

build() {
  cd $pkgbase-$pkgver

  msg2 "Use unofficial imedia SiS driver for supported SiS devices"
  patch -Np0 -i ../autoconfig-sis.patch

  msg2 "Revert commit that causes huge slowdowns with binary nVidia driver"
  patch -Np1 -i ../revert-trapezoids.patch

  autoreconf
  ./configure --prefix=/usr \
      --enable-ipv6 \
      --enable-dri \
      --enable-dmx \
      --enable-xvfb \
      --enable-xnest \
      --enable-composite \
      --enable-xcsecurity \
      --enable-xorg \
      --enable-xephyr \
      --enable-glx-tls \
      --enable-kdrive \
      --enable-install-setuid \
      --enable-config-udev \
      --disable-config-dbus \
      --enable-record \
      --disable-xfbdev \
      --disable-xfake \
      --disable-static \
      --sysconfdir=/etc/X11 \
      --localstatedir=/var \
      --with-xkb-path=/usr/share/X11/xkb \
      --with-xkb-output=/var/lib/xkb \
      --with-fontrootdir=/usr/share/fonts
  make

  # Disable subdirs for make install rule to make splitting easier
  sed -e 's/^DMX_SUBDIRS =.*/DMX_SUBDIRS =/' \
      -e 's/^XVFB_SUBDIRS =.*/XVFB_SUBDIRS =/' \
      -e 's/^XNEST_SUBDIRS =.*/XNEST_SUBDIRS = /' \
      -e 's/^KDRIVE_SUBDIRS =.*/KDRIVE_SUBDIRS =/' \
      -i hw/Makefile
}

package_xorg-server-common-dev() {
  pkgdesc="Xorg server common files - Bleeding edge ones"
  depends=('xkeyboard-config' 'xorg-fonts-misc' 'xorg-setxkbmap' 'xorg-xkbcomp')
  provides=("xorg-server-common=$pkgver")
  conflicts=('xorg-server-common')

  cd $pkgbase-$pkgver
  make -C xkb DESTDIR="$pkgdir" install-data

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/xorg-server-common/COPYING"
  install -Dm644 man/Xserver.1 "$pkgdir/usr/share/man/man1/Xserver.1"
  install -Dm644 dix/protocol.txt "$pkgdir/usr/lib/xorg/protocol.txt"
}

package_xorg-server-devel-dev() {
  pkgdesc="Development files for the Bleeding edge X.Org X server"
  depends=('dri2proto' 'fontsproto' 'inputproto-git' 'kbproto' 'libpciaccess-git' 'pixman' 'randrproto' 'renderproto' 'videoproto' 'xextproto' 'xineramaproto' 'xorg-util-macros' 'xproto')
  provides=("xorg-server-devel=$pkgver")
  conflicts=('xorg-server-devel')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"{/usr/{bin,share/{man,doc,X11},lib/xorg},var}
  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-devel"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-devel/COPYING"
}

package_xorg-server-xdmx-dev() {
  pkgdesc="Distributed Multihead X Server and utilities - Bleeding edge ones"
  depends=('libdmx' 'libgcrypt' 'libxaw' 'libxfixes' 'libxfont' 'libxi' 'libxrender' 'pixman' 'xorg-server-common')
  provides=("xorg-server-xdmx=$pkgver")
  conflicts=('xorg-server-xdmx')

  cd $pkgbase-$pkgver/hw/dmx
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xdmx"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xdmx/COPYING"
}

package_xorg-server-xephyr-dev() {
  pkgdesc="A nested X server that runs as an X application - Bleeding edge version"
  depends=('libgcrypt' 'libgl' 'libxfont' 'libxv' 'pixman' 'xorg-server-common')
  provides=("xorg-server-xephyr=$pkgver")
  conflicts=('xorg-server-xephyr')

  cd $pkgbase-$pkgver/hw/kdrive
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xephyr"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xephyr/COPYING"
}

package_xorg-server-xnest-dev() {
  pkgdesc="A nested X server that runs as an X application - Bleeding edge version"
  depends=('libgcrypt' 'libxext' 'libxfont' 'pixman' 'xorg-server-common')
  provides=("xorg-server-xnest=$pkgver")
  conflicts=('xorg-server-xnest')

  cd $pkgbase-$pkgver/hw/xnest
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xnest"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xnest/COPYING"
}

package_xorg-server-xvfb-dev() {
  pkgdesc="Virtual framebuffer X server - Bleeding edge version"
  depends=('libgcrypt' 'libxau' 'libxdmcp' 'libxfont' 'pixman' 'xorg-server-common')
  provides=("xorg-server-xvfb=$pkgver")
  conflicts=('xorg-server-xvfb')

  cd $pkgbase-$pkgver/hw/vfb
  make DESTDIR="$pkgdir" install

  install -m755 "$srcdir/xvfb-run" "$pkgdir/usr/bin/"
  cp "$srcdir/xvfb-run.1" "$pkgdir/usr/share/man/man1/"

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server-xvfb"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server-xvfb/COPYING"
}

package_xorg-server-dev() {
  pkgdesc="Xorg X server - Bleeding edge version"
  depends=('libdrm' 'libgcrypt' 'libpciaccess-git' 'libxau' 'libxdmcp' 'libxfont' 'pixman' 'udev' 'xf86-input-evdev' 'xorg-server-common')
  backup=('etc/X11/xorg.conf.d/10-evdev.conf' 'etc/X11/xorg.conf.d/10-quirks.conf')
  provides=('x-server' "xorg-server=$pkgver")
  conflicts=('xorg-server')
  groups=('xorg')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir -p "${pkgdir}/etc/X11"
  mv "${pkgdir}/usr/share/X11/xorg.conf.d" "${pkgdir}/etc/X11/"
  cp ../10-quirks.conf "${pkgdir}/etc/X11/xorg.conf.d/"

  # Needed for non-mesa drivers, libgl will restore it
  mv "$pkgdir"/usr/lib/xorg/modules/extensions/libglx.{so,xorg}

  mkdir -p "$pkgdir/usr/share/licenses/xorg-server"
  ln -sf ../xorg-server-common/COPYING "$pkgdir/usr/share/licenses/xorg-server/COPYING"

  rm -rf "$pkgdir"/usr/{share/{man/man1/Xserver.1,aclocal,X11},lib/{xorg/protocol.txt,pkgconfig},include}
}