# Maintainer: Michael Schubert <mschu.dev at gmail>

pkgname=python2-pythonmagick
pkgver=0.9.5
pkgrel=1
pkgdesc="Object-oriented Python2 bindings for the ImageMagick library"
arch=('i686' 'x86_64')
url="http://www.imagemagick.org/"
license=('custom')
depends=('boost' 'python2' 'imagemagick' 'libstdc++5')
makedepends=('perl' 'automake')
options=('!libtool')
source=("http://www.imagemagick.org/download/python/PythonMagick-$pkgver.tar.gz")
md5sums=('c119e2b735dbf130549a8dd751b221d2')

build() {
  cd "$srcdir/PythonMagick-$pkgver"
  ./autogen.pl

  CPPFLAGS="`python2-config --includes`" \
  PYTHON_LIB="`python2-config --libs`" \
  ./configure --prefix=/usr

  pydir=`python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()"`

  sed -i 's:-l$(PYTHON_LIB):$(PYTHON_LIB):' Makefile
  make || return 1
  make DESTDIR="$pkgdir" pyexecdir="$pydir" pythondir="$pydir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-pythonmagick/LICENSE"
}
