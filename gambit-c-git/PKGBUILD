# Maintainer: Louis-Guillaume Gagnon <louis.guillaume.gagnon@gmail.com>
pkgname=gambit-c-git
pkgver=20140209.29a0d8c
pkgrel=1
pkgdesc="Scheme R5RS interpreter and compiler (via C) - git version"
arch=('i686' 'x86_64')
url="http://gambitscheme.org"
license=('Apache' 'LGPL2.1')
makedepends=('git')
provides=('gambit-c')
conflicts=('gambit-c')
install="$pkgname.install"
source=(git+https://github.com/feeley/gambit.git)
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/gambit"
    git log -1 --format='%cd.%h' --date=short | tr -d -
}

build() {
	cd "$srcdir/gambit"
	
        ./configure \
            --prefix=/usr \
            --docdir=/usr/share/gambit-c \
            --infodir=/usr/share/info \
            --libdir=/usr/lib/gambit-c \
            --enable-single-host \
            --enable-c-opt \
            --enable-gcc-ots \
            --enable-compiler-name=gambitc
	
        make from-scratch
}

package() {
	cd "$srcdir/gambit"
	make DESTDIR="$pkgdir/" install
        ln -sf /usr/bin/gambitc "$pkgdir/usr/bin/gsc-script"
}
