# Maintainer: Taylor Venable <taylor@metasyntax.net>

pkgname='gambit-c-git'
pkgver=20120801
pkgrel=1
pkgdesc="Scheme R5RS interpreter and compiler (into portable C)"
arch=('i686' 'x86_64')
url='http://dynamo.iro.umontreal.ca/~gambit/'
license=('LGPL' 'APACHE')
makedepends=('gcc')
conflicts=('gambit-c')
provides=('gambit-c')
install='gambit-c.install'

_gitroot='https://github.com/feeley/gambit.git'
_gitname='gambit'

build() {
  cd "$srcdir"

  if [ -d $_gitname ] ; then
    msg 'Updating Git checkout...'
    cd "$_gitname" && git pull origin
  else
    msg 'Cloning Git repository...'
    git clone "$_gitroot"
  fi

  msg 'Copying to build directory...'

  rm -rf "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  CFGOPTS='--prefix=/usr
    --docdir=/usr/share/doc/gambit-c
    --infodir=/usr/share/info
    --libdir=/usr/lib/gambit-c
    --enable-gcc-opts
    --enable-single-host'

  ./configure $CFGOPTS
  make bootstrap
  make bootclean
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="$pkgdir" install

  # gsc conflicts with the one provided by ghostscript...
  cd "$pkgdir/usr/bin"
  mv gsc gambitc
  rm gsc-script
  ln -s gambitc gsc-script
}
