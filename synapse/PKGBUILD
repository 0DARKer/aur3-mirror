# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Sven-Hendrik Haase <sh at lutzhaase dot com>

pkgname=synapse
pkgver=0.2.4.1
_pkgmainver=0.2
pkgrel=2
pkgdesc="A semantic file launcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/synapse-project"
license=('GPL3')
depends=('desktop-file-utils' 'gtkhotkey' 'hicolor-icon-theme' \
         'json-glib' 'libgee' 'libnotify' 'libunique' 'libzeitgeist')
makedepends=('intltool' 'vala')
optdepends=('banshee: for banshee plugin'
            'bc: for calculator plugin'
            'devhelp: for documentation plugin'
            'gnome-screensaver: for screensaver plugin'
            'gnome-utils: for dictionary plugin'
            'pastebinit: for pastebin plugin'
            'rhythmbox: for rhythmbox plugin'
            'zeitgeist: for zeitgeist plugin')
install=${pkgname}.install
source=(http://launchpad.net/${pkgname}-project/${_pkgmainver}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
md5sums=('f8692bb67aff43b55b8421d1ab3d09c9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

