# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=synapse
pkgver=0.2.8.2
pkgrel=3
pkgdesc="A semantic file launcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/synapse-project"
license=('GPL3')
depends=('desktop-file-utils' 'dbus-glib' 'gtkhotkey' \
         'hicolor-icon-theme' 'json-glib' 'libgee' \
         'libnotify' 'libunique' 'libzeitgeist')
makedepends=('intltool' 'vala')
optdepends=('banshee: banshee plugin'
            'bc: calculator plugin'
            'devhelp: documentation plugin'
            'gnome-screensaver: screensaver plugin'
            'gnome-utils: dictionary plugin'
            'openssh: ssh plugin'
            'pastebinit: pastebin plugin'
            'rhythmbox: rhythmbox plugin'
            'zeitgeist: zeitgeist engine'
            'zeitgeist-datahub: zeitgeist logging daemon')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}-project/0.2/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        "fix-vala-0.14.patch"
        "fix-suspend-hibernate.patch")
md5sums=('06d038397cb06079162e6adb2050e7a6'
         'cc9c890aaee9c0b1c6a553327a51f4d9'
         'bc479f4fed1074e3394e8cbe70315c1d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # vala 0.14 fix
  patch -Np1 -i "${srcdir}/fix-vala-0.14.patch"
  patch -Np1 -i "${srcdir}/fix-suspend-hibernate.patch"

  # DSO fix
  export LDFLAGS="${LDFLAGS} -ldl -lm"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
