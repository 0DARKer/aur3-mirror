# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Sven-Hendrik Haase <sh at lutzhaase dot com>

pkgname=synapse
pkgver=0.2.8.2
_pkgmainver=0.2
pkgrel=1
pkgdesc="A semantic file launcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/synapse-project"
license=('GPL3')
depends=('dbus-glib' 'gtkhotkey' 'hicolor-icon-theme' \
         'json-glib' 'libgee' 'libnotify' 'libunique' \
         'libzeitgeist')
makedepends=('intltool' 'vala')
optdepends=('banshee: banshee plugin'
            'bc: calculator plugin'
            'devhelp: documentation plugin'
            'gnome-screensaver: screensaver plugin'
            'gnome-utils: dictionary plugin'
            'openssh: ssh plugin'
            'pastebinit: pastebin plugin'
            'rhythmbox: rhythmbox plugin'
            'zeitgeist: zeitgeist engine'
            'zeitgeist-datahub: zeitgeist logging daemon')
install=${pkgname}.install
source=("http://launchpad.net/${pkgname}-project/${_pkgmainver}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"
        "fix-vala-014.patch")
md5sums=('06d038397cb06079162e6adb2050e7a6'
         '1facf5afeb3ae0d4514d10c5ef99cedb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # vala 0.14 fix
  patch -Np1 -i "${srcdir}/fix-vala-014.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
