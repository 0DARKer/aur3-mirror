# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=synapse
pkgver=0.2.10
pkgrel=1
pkgdesc="A semantic file launcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/synapse-project"
license=('GPL3')
depends=('desktop-file-utils' 'gtkhotkey' 'hicolor-icon-theme' \
         'json-glib' 'libgee' 'libnotify' 'libunique')
makedepends=('intltool' 'vala')
optdepends=('banshee: banshee plugin'
            'bc: calculator plugin'
            'devhelp: documentation plugin'
            'gnome-screensaver: screensaver plugin'
            'gnome-utils: dictionary plugin'
            'openssh: ssh plugin'
            'pastebinit: pastebin plugin'
            'rhythmbox: rhythmbox plugin'
            'xnoise: xnoise plugin'
            'zeitgeist-datahub: zeitgeist plugin')
install="$pkgname".install
source=("https://launchpad.net/$pkgname-project/0.2/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        "fix-check-desktop.patch")
sha256sums=('d94f724be51f20e2085ead203495d398c5fece24071ffed7e805108be2219540'
            'fab21a94644d898ddc8bc9ba0db81976c41ccff5c72a2575fd7e223a46f036e9')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # no zeitgeist dep
  sed -i '/--pkg zeitgeist-1.0 \\/d' src/ui/Makefile.am

  # fix check XDG_CURRENT_DESKTOP
  patch -Np1 -i "$srcdir"/fix-check-desktop.patch

  # DSO fix
  export LDFLAGS="$LDFLAGS -ldl -lm"

  ./configure --prefix=/usr --disable-zeitgeist
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
