# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=android-qt5-qtxmlpatterns-git
_basever="5.2.0"
pkgver=5.2.0_219.3606f28
pkgrel=1
pkgdesc="Qt 5 for Android: qtxmlpatterns module"
groups=('qt-android' 'qt5-android')
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/Qt5ForAndroid"
license=('LGPL')
depends=('android-qt5-qtbase-git')
makedepends=('git')
options=('!libtool' '!strip')
source=("${pkgname}::git://gitorious.org/qt/qtxmlpatterns.git#branch=dev")
md5sums=('SKIP')

pkgver() {
  cd ${srcdir}/${pkgname}
  echo ${_basever}_$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
  #git describe --always | sed 's|-|_|g' | sed 's|^v||'
}

build() {
  cd ${srcdir}/${pkgname}

  /opt/android-qt5/bin/qmake
  make
}

package() {
  cd ${srcdir}/${pkgname}

  make INSTALL_ROOT="${pkgdir}" install
  
  # Strip by hand, system strip doesn't work on arm binaries

  # arm dynamic libs in lib
  /opt/android-ndk/toolchains/arm-linux-androideabi-4.7/prebuilt/linux-x86*/bin/arm-linux-androideabi-strip ${STRIP_SHARED} \
    ${pkgdir}/opt/android-qt5/lib/*.so*
}
