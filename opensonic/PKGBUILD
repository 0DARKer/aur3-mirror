# Contributor: Dany Martineau <dany.luc.martineau gmail com>
# Maintainer: Anton Larionov <diffident dot cat at gmail dot com>


pkgname=opensonic
pkgver=0.1.4
pkgrel=3
pkgdesc="Game based on the Sonic the Hedgehog universe."
arch=('i686' 'x86_64')
url="http://opensnc.sourceforge.net/"
license=('GPL')
depends=('allegro4')
# Dependency 'libpng' provides by 'allegro4'
makedepends=('cmake')
install=(opensonic.install)

source=("http://downloads.sourceforge.net/project/opensnc/Open Sonic/${pkgver}/opensnc-src-${pkgver}.tar.gz"
        'opensonic.desktop'
        'opensonic.sh')

md5sums=('069fa14f43251b218b4fd81378d1cd78'
         '29ba0a89203cd59cef11ad465ef7145f'
         '145a9da698f74ee3a9decc125132a4be')


build() {
  cd "${srcdir}/opensnc-src-${pkgver}/"
  unset LDFLAGS
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/opensnc-src-${pkgver}/"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -m644 icon.png "${pkgdir}/usr/share/${pkgname}/"
  install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
