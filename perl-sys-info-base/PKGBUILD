# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

_perlmod=Sys-Info-Base
_modnamespace=Sys
pkgname=perl-sys-info-base
pkgver=0.7803
pkgrel=2
pkgdesc="Sys::Info::Base - Base class for Sys::Info"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('afe93cf07edc4edf2f1ea4267f60d9acb4ce712282a04ac41476e595ae389ae59545611d388aa9f0e7a2c87a3b40c03d75e736d8ea98dd0ec0ca96f7a8c68de6')

build() {
	cd "$srcdir/${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "$srcdir/${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "$srcdir/${_perlmod}-${pkgver}"
	make install DESTDIR="$pkgdir/"
}
