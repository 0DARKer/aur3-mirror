# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname=rtl8192du-git
_pkgname=rtl8192du
pkgver=f2630cd
pkgrel=1
_extramodules="extramodules-3.17-ARCH"
pkgdesc="Kernel module for Realtek RTL8192DU USB wireless devices"
arch=('x86_64' 'i686')
url="https://github.com/lwfinger/rtl8192du"
license=('GPL')
depends=('linux>=3.17' 'linux<3.18')
makedepends=('linux-headers>=3.17' 'git')
source=("enable_install_prefix.patch"
"git://github.com/lwfinger/$_pkgname.git")
sha256sums=('7f7c2a0c5770a2d6b91d30d1bd9e6f44dc594a84f3fc507f3dfa180e6f039293'
            'SKIP')
install=readme.install

pkgver() {
	cd "$_pkgname"
	git describe --always | sed 's|-|.|g'
}

prepare() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

	cd "$_pkgname"
	# pull request 28 submitted on 01/12/2015
	# https://github.com/lwfinger/rtl8192du/pull/28
	# allows an install prefix so pacman 4.2 will install
	# to /usr/lib/... not to the symlink
	patch -Np1 -i "$srcdir/enable_install_prefix.patch"

	# needed to 1) reference the static kernel ver not the running one
	# 2) remove the depmod command which we do in the postinstall
	# script and 3) prefix the install to /usr
	sed -i -e "/^KSRC/ s,\$(KVER),$_kernver," \
		-i -e '/depmod/d' \
		-i -e '/^INSTALL_PREFIX/ s,=,= /usr,' Makefile
}

build() {
	cd "$_pkgname"
	make
}

package() {
	cd "$_pkgname"
	make DESTDIR="$pkgdir/" install
	#install -d "$pkgdir/usr/lib/modules/${_extramodules}/kernel/drivers/net/wireless"
	#install -m644 "$srcdir/$_pkgname/8192du.ko" \
		#	"$pkgdir/usr/lib/modules/${_extramodules}/kernel/drivers/net/wireless/8192du.ko"

	#install -d "$pkgdir/usr/lib/firmware/rtlwifi"
	#install -m644 "$srcdir/$_pkgname/rtl8192dufw.bin" \
		#	"$pkgdir/usr/lib/firmware/rtlwifi/rtl8192dufw.bin"
	#install -m644 "$srcdir/$_pkgname/rtl8192dufw_wol.bin" \
		#	"$pkgdir/usr/lib/firmware/rtlwifi/rtl8192dufw_wol.bin"

	#gzip -9 "$pkgdir/usr/lib/modules/${_extramodules}/kernel/drivers/net/wireless/8192du.ko"
}
