# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname=rtl8192du-git
_pkgname=rtl8192du
pkgver=e627694
pkgrel=1
_extramodules="extramodules-3.14-ARCH"
pkgdesc="Kernel module for Realtek RTL8192DU USB wireless devices"
arch=('x86_64' 'i686')
url="git://github.com/lwfinger/rtl8192du"
license=('GPL')
depends=('linux>=3.14' 'linux<3.15')
makedepends=('linux-headers>=3.14' 'git')
source=("git://github.com/lwfinger/$_pkgname.git")
md5sums=('SKIP')
install=readme.install

pkgver() {
	cd "$_pkgname"
	git describe --always | sed 's|-|.|g'
}

prepare() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

	cd "$_pkgname"

	# needed to reference the static kernel ver not the running one
	# also need to remove the depmod command which we do in the postinstall
	# script
	sed -i -e "/^KSRC/ s,\$(KVER),$_kernver," \
		-i -e '/depmod/d' Makefile
}

build() {
	cd "$_pkgname"
	make
}

package() {
	cd "$_pkgname"
	make DESTDIR="$pkgdir/" install
}
