# Maintainer: graysky <graysky AT archlinux DOT us>

pkgname=rtl8192du-git
_pkgname=rtl8192du
pkgver=085e206
pkgrel=2
_extramodules="extramodules-3.14-ARCH"
pkgdesc="Kernel module for Realtek RTL8192DU USB wireless devices"
arch=('x86_64' 'i686')
url="git://github.com/lwfinger/rtl8192du"
license=('GPL')
depends=('linux>=3.14' 'linux<3.15')
makedepends=('linux-headers>=3.14' 'git')
source=("git://github.com/lwfinger/$_pkgname.git"
"unfuck_the_Makefile.patch")
md5sums=('SKIP'
'b00312f292610fac82220c5cd8d56def')
install=readme.install

pkgver() {
	cd "$_pkgname"
	git describe --always | sed 's|-|.|g'
}

prepare() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

	cd "$_pkgname"
	patch -Np1 -i "$srcdir/unfuck_the_Makefile.patch"

	# this line is needed to reference the static kernel ver not the running one
	sed -i -e "/^KSRC/ s,\$(KVER),$_kernver," Makefile
}

build() {
	cd "$_pkgname"
	make
}

package() {
	cd "$_pkgname"
	make DESTDIR="$pkgdir/" install
}
