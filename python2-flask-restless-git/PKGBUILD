# Maintainer: Dennis Fink <the_metalgamer@hackerspace.lu>
# Submitter: Zoran Zeric <zz@zoranzaric.de>

pkgname=python2-flask-restless-git
pkgver=20130211
pkgrel=2
pkgdesc="A Flask extension for easy ReSTful API generation"
arch=('any')
url="http://pypi.python.org/pypi/Flask-Restless"
license=('BSD' 'AGPL3')
depends=('python2-flask>=0.7' 'python2-sqlalchemy' 'python2-dateutil')
optdepends=('python2-simplejson: For python2 version under 2.6'
            'python2-flask-sqlalchemy')
makedepends=('python2-distribute' 'git') 
provides=('python2-flask-restless')
conflicts=('python2-flask-restless')

_gitroot="git://github.com/jfinkels/flask-restless.git"
_gitname="flask-restless"

build() {

  cd "$srcdir"
  msg "Connecting to GIT server..."
  if [ -d "$srcdir/$_gitname" ]; then
    cd "$srcdir/$_gitname" && git pull origin
    cd "$srcdir"
    msg "The localfiles are updates."
  else
    git clone --depth=1 "$_gitroot" "$_gitname"
  fi
  msg "GIT checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  mkdir "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname" && ls -A | grep -v .git | xargs -d '\n' cp -r -t ../$_gitname-build
  cd "$srcdir/$_gitname-build"

  python2 setup.py build
}

package() {

  cd "$srcdir/$_gitname-build"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 $srcdir/$_gitname-build/LICENSE.BSD "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
