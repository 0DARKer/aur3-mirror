# Maintainer: Bill Kolokithas <kolokithas.b@gmail.com>

_gitname=j4-dmenu-desktop
pkgname=$_gitname-git
pkgver=35.d8a3d0a
pkgrel=2
pkgdesc="A rewrite of i3-dmenu-desktop, which is much faster"
arch=('i686' 'x86_64' 'armv7h')
url="https://github.com/enkore/j4-dmenu-desktop"
license=('GPL')
depends=('dmenu')
makedepends=('git' 'cmake')
options=('strip')
source=("git://github.com/enkore/j4-dmenu-desktop"
		'0001-check-return-value-of-strchr.patch')
md5sums=('SKIP'
         '2bcc1f6dbb907a58bcf2443f7aedae02')

pkgver() {
	cd $_gitname
	echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
	cd $_gitname
	patch -p1 -i $srcdir/0001-check-return-value-of-strchr.patch
}

build() {
	cd $_gitname
	cmake -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd $_gitname
	make DESTDIR=$pkgdir/ install
}
