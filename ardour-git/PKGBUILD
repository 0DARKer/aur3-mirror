# Maintainer: Boohbah <boohbah at gmail.com>
# Contributor: SpepS <dreamspepser at yahoo.it>
# Contributor: Bernardo Barros <bernardobarros at gmail.com>
# Contributor: Uli Armbruster <uli_armbruster at web.de>

pkgname=ardour-git
pkgver=20130314
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('i686' 'x86_64')
url="http://ardour.org/"
license=('GPL')
depends=('libgnomecanvasmm>=2.12.0' 'liblrdf>=0.4.0' 'liblo' 'aubio'
         'libsmf' 'lilv<=1.0.0' 'suil<=1.0.0' 'desktop-file-utils')
makedepends=('git' 'python2' 'boost' 'cppunit>=1.12.0' 'doxygen' 'graphviz')
provides=('ardour')
conflicts=('ardour' 'ardour3-svn')
source=('ardour.desktop')
md5sums=('2736c648e452ae45c703e48cf64f75e7')

_gitroot=git://git.ardour.org/ardour/ardour.git
_gitname=ardour

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  python2 waf configure \
    --prefix=/usr \
    --configdir=/etc \
    --docs \
    --freedesktop \
    --lv2 \
    --noconfirm

  python2 waf build
}

package() {
  cd "$srcdir/$_gitname-build"

  python2 waf --destdir="$pkgdir/" install

  # install some freedesktop.org compatibility
  install -Dm644 "$srcdir/ardour.desktop" \
    "$pkgdir/usr/share/applications/ardour.desktop"
}

# vim:set ts=2 sw=2 et:
