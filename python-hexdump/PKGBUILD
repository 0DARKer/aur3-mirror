# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>

pkgname=python-hexdump
_pyname=hexdump
_hghash=e49c14742006
pkgver=2.0
pkgrel=2
pkgdesc='Library and tool to work with hex and binary data'
arch=('any')
url='https://bitbucket.org/techtonik/hexdump'
license=('Public Domain')
depends=('python')
source=(https://bitbucket.org/techtonik/${_pyname}/get/${pkgver}.tar.bz2)
sha512sums=('dae5769e28729f4cee948735e06cc81f4b2fce6376de7cc471d2ba38318bcd8ec2208e41b21bb92c81487f6da83e3405853d1ff38013056cf4a7623a9fc19432')

check() {
  cd "techtonik-${_pyname}-${_hghash}"
  python hexdump.py --test test.txt
  diff -u3 hextest.txt test.txt
}

package() {
  cd "techtonik-${_pyname}-${_hghash}"
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
}

# vim: ts=2 sw=2 et:
