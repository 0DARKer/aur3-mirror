# Maintainer: flu
# Contributor: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/

_gitname="spacefm"
pkgname="$_gitname-git"
pkgver=20140117.960
pkgrel=1
pkgdesc="A multi-panel tabbed file manager - git branch"
arch=('i686' 'x86_64')
url=("http://ignorantguru.github.com/$_gitname/")
license=('GPL3')

makedepends=('intltool' 'gettext')
depends=('gtk3' 'shared-mime-info' 'desktop-file-utils' 'udev')
optdepends=('lsof: device processes'
            'eject: eject media'
            'wget: plugin download'
            'ktsuss: perform as root functionality'
            'gksu: perform as root functionality'
            'udevil-git: mount as non-root user and mount networks'
            'pmount: mount as non-root user'
            'udisks: mount as non-root user'
            'udisks2: mount as non-root user'
            'startup-notification')
conflicts=("$_gitname" "$_gitname-gtk2")
provides=("$_gitname")
source=(git://github.com/IgnorantGuru/$_gitname.git)
install="$_gitname.install"
md5sums=(SKIP)

# Set USE_GDB=1 to build for gdb debugging, otherwise set USE_GDB=0
USE_GDB=1

if (( USE_GDB == 1 )); then
  options=('!strip')
fi

pkgver() {
  cd   "$srcdir"/"$_gitname"
  echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-list --count master)"
}

build() {
  cd "$srcdir/$_gitname"

  # NOTE: To add a custom su program (mysu in this example), add:
  # --with-preferable-sudo="mysu"  to configure line below.

  if (( USE_GDB == 1 )); then
    CFLAGS+="-g" ./configure --prefix=/usr --with-gtk3
    CFLAGS+="-g" make -s
  else
                 ./configure --prefix=/usr --with-gtk3
                 make -s
  fi
}

package() {
  cd "${srcdir}/$_gitname"
  make DESTDIR="${pkgdir}/" install
}
