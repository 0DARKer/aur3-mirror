# Maintainer: Daniel Riedemann <daniel.riedemann [at] googlemail [dot] com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=boinc-svn
pkgver=19336
pkgrel=1
pkgdesc="Berkeley Open Infrastructure for Network Computing -- SVN Version"
arch=('i686' 'x86_64')
url="http://boinc.berkeley.edu/"
license=('LGPL')
depends=('curl' 'wxgtk')
conflicts=('boinc')
provides=('boinc')
makedepends=('libxslt' 'pkgconfig' 'perl-xml-sax' 'subversion')
install=${pkgname}.install
source=(${pkgname}.rc ${pkgname}.desktop)

md5sums=('2fa7bcfaf4b03b118afe9e26b50f3a38'
         'b07690ccdae4e1353020a69df0ba6c96')
sha256sums=('209fee4dfc98bf00e8d84c59ec0ac2dd3a101bbfebfe566a6998188c7277446d'
            'ebc5d2d1cc624e20a5ce40043dd80d67bda9728d38570649337f1661b7ce884c')

_svntrunk="http://boinc.berkeley.edu/svn/trunk"
_svnmod="boinc"

build() {
  msg "Connecting to boinc.berkeley.edu SVN server..."

  if [ -d ${_svnmod}/.svn ]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk}/${_svnmod} --config-dir ./ -r ${pkgver}
  fi

  msg "SVN checkout done or server timeout."
  msg "Copying source files to build directory..."

  if [ -d "${srcdir}/${_svnmod}-build" ]; then
    rm -rf "${srcdir}/${_svnmod}-build"
  fi

  cp -r "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  msg "Done."
  msg "Compiling sources..."

  #configure
  ./_autosetup || return 1
  ./configure --prefix=/usr --disable-server --enable-client \
              --enable-libraries --enable-manager \
              --enable-optimize --with-wxdir=/usr/lib \
              --enable-unicode --with-x --with-ssl \
              --with-wx-config=$(which wx-config) || return 1

  make || return 1

  msg "Done."
  msg "Installing binaries..."

  make DESTDIR="${pkgdir}" install || return 1

  #install rc-script
  install -D -m755 "${srcdir}/${pkgname}.rc" "${pkgdir}/etc/rc.d/${pkgname}.rc"

  #install bash-completion
  install -D -m644 "${srcdir}/${_svnmod}-build/client/scripts/boinc.bash" "${pkgdir}/etc/bash_completion.d/${pkgname}"

  #install .desktop file
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  #killing /etc/init.d directory
  rm -rf "${pkgdir}/etc/init.d" || return 1
}
