# Maintainer: ponsfoot <cabezon dot hashimoto at gmail dot com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

_root=trunk
_mod=boinc
#_root=tags
#_mod=boinc_core_release_6_12_34

_apply_patch="yes"

_svntrunk="http://boinc.berkeley.edu/svn/${_root}/${_mod}"
_svnmod="${_mod}"

pkgname=boinc-svn
pkgver=25025
pkgrel=1
arch=('i686' 'x86_64')
url="http://boinc.berkeley.edu/"
license=('LGPL')
pkgdesc="Berkeley Open Infrastructure for Network Computing"
depends=('curl' 'wxgtk' 'libnotify' 'sqlite3')
makedepends=('libxslt' 'pkg-config' 'perl-xml-sax' 'subversion')
provides=('boinc')
conflicts=('boinc' 'boinc-nox')
options=('!libtool')
install=boinc.install
changelog=ChangeLog
source=(boinc.rc
        boinc.bash
        boinc.desktop
        6.12.26-libnotify-0.7.patch
        6.12.26-fix_subdirs.patch
        r24240-ignore_ret_value.patch
)
md5sums=('43605168e310f50cf426d2f9a7b39847'
         '05ed267db973ef7cbaf1118bb20bf9ce'
         '17969d849f3cf27c2100b20a7b7a7e64'
         '7d774000a1c69c84e71704f42129bbd3'
         'c7da3bb2a697b49cb79ab009ba4e75c8'
         '68bb3dfac41c20251b543665659f0281')

build() {
  cd "${srcdir}"

  msg "Connecting to boinc.berkeley.edu SVN server..."
  if [[ -d ${_svnmod}/.svn ]]; then
    (cd ${_svnmod} ; svn update)
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done or server timeout."

  msg "Starting make..."
  rm -rf "${srcdir}/${_svnmod}-build"
  cp -r "${srcdir}/$_svnmod" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  if [[ "$_apply_patch" = "yes" ]]; then
    if [[ "$_mod" = "boinc_core_release_6_12_34" ]]; then
      # Patches by Gentoo
      # http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/sci-misc/boinc/files/
      patch -p1 -i "${srcdir}/6.12.26-fix_subdirs.patch"
      patch -p1 -i "${srcdir}/6.12.26-libnotify-0.7.patch"
    else
      patch -p1 -i "${srcdir}/r24240-ignore_ret_value.patch"
    fi
  fi

  _inclds_gtk2="$(pkg-config --cflags --libs gtk+-2.0)"
  CFLAGS+=" $_inclds_gtk2 -lX11"
  CXXFLAGS+=" $_inclds_gtk2 -lX11"

  #configure
  LC_ALL=C ./_autosetup # Possibility to fail ver. check depending on the localization.
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-server \
              --enable-unicode \
              --with-x \
              --with-ssl \
              --with-wxdir=/usr/lib \
              --with-wx-config=$(which wx-config) \
              --enable-dynamic-client-linkage \
              --disable-static
  make
}

package() {
  cd "${srcdir}/${_svnmod}-build"

  make DESTDIR=${pkgdir} install

  #install rc-script
  install -D -m755 "${srcdir}/boinc.rc" ${pkgdir}/etc/rc.d/boinc

  #install bash-completion
  install -D -m644 "${srcdir}/boinc.bash" ${pkgdir}/etc/bash_completion.d/boinc

  #install .desktop File
  install -D -m644 "${srcdir}/boinc.desktop" \
    ${pkgdir}/usr/share/applications/boinc.desktop

  #install icons
  install -D -m644 "${srcdir}/${_svnmod}-build/clientgui/res/boincmgr.48x48.png" \
    ${pkgdir}/usr/share/pixmaps/boinc.png

  #killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d
}
