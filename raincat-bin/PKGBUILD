# Maintainer: cuihao <cuihao dot leo at gmail dot com>

pkgname=raincat-bin
_pkgname=raincat
_insdir="opt/${_pkgname}"
pkgver=20100129
pkgrel=1
pkgdesc="A simple but nice game written in Haskell. Guide the fuzzy cat to safety!"
url="http://raincat.bysusanlin.com/"
license=('custom', 'CCPL')
arch=('x86_64' 'i686')
if [ "$CARCH" = "x86_64" ]; then
    depends=('lib32-sdl_mixer' 'lib32-sdl_image' 'lib32-freeglut' 'lib32-gmp')
elif [ "$CARCH" = "i686" ]; then
    depends=('sdl_mixer' 'sdl_image' 'freeglut' 'gmp')
fi
conflicts=('raincat')

source=('http://www.contrib.andrew.cmu.edu/~gchin/downloads/Raincat.tar.gz')
md5sums=('0999c6d4563b9c824f1db65cad10a0be')

package() {
    cd "${srcdir}/${_pkgname}"

    # data and executable files
    mkdir -p "${pkgdir}/${_insdir}/data"
    cp -rT data "${pkgdir}/${_insdir}/data"
    cp {${_pkgname},README} "${pkgdir}/${_insdir}"
    # license
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

    cd "${pkgdir}"
    
    # shell script as a wrapper to start the program
    mkdir -p "usr/bin"
    echo -e "#!/bin/sh" >> "usr/bin/${_pkgname}"
    echo -e "cd /${_insdir}" >> "usr/bin/${_pkgname}"
    echo -e "LD_LIBRARY_PATH+=/${_insdir} exec /${_insdir}/${_pkgname}" \
            >> "usr/bin/${_pkgname}"
    chmod a+x "usr/bin/${_pkgname}"

    # dependence on old libgmp
    if [ "$CARCH" = "x86_64" ]; then
        ln -s /usr/lib32/libgmp.so "${_insdir}/libgmp.so.3"
    elif [ "$CARCH" = "i686" ]; then
        ln -s /usr/lib/libgmp.so "${_insdir}/libgmp.so.3"
    fi
}
