# Maintainer: Milan Knížek <knizek@volny.cz>
pkgname=x2goagent
pkgver=3.5.0.9
pkgrel=1
pkgdesc="X proxy server for x2go using NX libs"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.x2go.org/"
depends=("nxagent>=${pkgver}")
conflicts=('x2goagent-git')
groups=('x2go' 'alts')
source=("http://code.x2go.org/releases/source/nx-libs/nx-libs_${pkgver}-full.tar.gz"
        'copyright.DEBIAN'
        'rgb')
md5sums=('be272307cf477f756aef702a6d4ed2ae'
         '29004bd2f64cbf26d207f700563134cc'
         '09ee098b83d94c7c046d6b55ebe84ae1')

package() {
  cd "$srcdir/nx-libs_${pkgver}"

  install -m 755 -D "bin/x2goagent" "${pkgdir}/usr/bin/x2goagent"

  install -m 755 -d "${pkgdir}/usr/lib/x2go"
  ln -s "../nx/nxagent" "${pkgdir}/usr/lib/x2go/x2goagent"

  install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 "doc/changelog" "${pkgdir}/usr/share/doc/${pkgname}/changelog.DEBIAN"
  install -m 644 "doc/README.NX-redistributed" \
                 "${pkgdir}/usr/share/doc/${pkgname}/README.NX-redistributed"

  install -m 755 -d "${pkgdir}/usr/share/x2go"
  install -m 644 "../rgb" "${pkgdir}/usr/share/x2go/rgb"

  install -m 755 -d "${pkgdir}/usr/share/pixmaps"
  install -m 644 "nx-X11/programs/Xserver/hw/nxagent/x2go.xpm" \
                 "${pkgdir}/usr/share/pixmaps/x2go.xpm"

  install -m 755 -d "${pkgdir}/usr/share/man/man1"
  install -m 644 "nx-X11/programs/Xserver/hw/nxagent/man/x2goagent.1" \
                 "${pkgdir}/usr/share/man/man1/x2goagent.1"
  gzip -f "${pkgdir}/usr/share/man/man1/x2goagent.1"
}

