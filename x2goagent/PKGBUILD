# Contributor: Gerhard Brauer <gerbra@archlinux.de>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Marc Cousin <cousinmarc@gmail.com>
pkgname=x2goagent
pkgver=3.4.0_5_1
_dashes=${pkgver//_/-}
pkgrel=5
pkgdesc="X proxy server for x2go using NX libs"
arch=(i686 x86_64)
license=('GPL')
url="http://www.x2go.org/"
depends=(nxcomp nxcompshad nxcompext nxproxy)
groups=('x2go' 'alts')
source=(http://x2go.obviously-nice.de/deb/pool-lenny/${pkgname}/${pkgname}_${_dashes%-*}.orig.tar.gz
        http://x2go.obviously-nice.de/deb/pool-lenny/${pkgname}/${pkgname}_${_dashes}.diff.gz
        configure
        x2go.xpm)
md5sums=('b550163ea5b2fd66486c0c7ef4814263'
         'acdaf3d3466877d084cb4b566c79988d'
         '4394a29762289ae3d4d8b80bf36f1810'
         'd05088dbd75790aa1d2cfaf797ce09f4')

build() {
  cd "$srcdir/$pkgname-${_dashes%-*}"
  patch -Np1 <../${pkgname}_${_dashes}.diff

  cp ../x2go.xpm programs/Xserver/hw/nxagent/

  find ./ -type f | xargs sed -i 's/_XLIB_H_/_X11_XLIB_H_/g'

  # fake a recompile of nxcomp nxcompext and nxcompshad
  install -D -m 555 $srcdir/configure $srcdir/nxcomp/configure
  install -D -m 555 $srcdir/configure $srcdir/nxcompext/configure
  install -D -m 555 $srcdir/configure $srcdir/nxcompshad/configure
  cp -a /usr/include/nxcomp/* $srcdir/nxcomp
  cp -a /usr/lib/libXcomp.so* $srcdir/nxcomp
  cp -a /usr/include/nxcompext/* $srcdir/nxcompext
  cp -a /usr/lib/libXcompext.so* $srcdir/nxcompext
  cp -a /usr/include/nxcompshad/* $srcdir/nxcompshad
  cp -a /usr/lib/libXcompshad.so* $srcdir/nxcompshad

  make World

  install -d ${startdir}/pkg/usr/lib/x2go
  cp -a lib/X11/lib* ${startdir}/pkg/usr/lib/x2go
  cp -a lib/Xext/lib* ${startdir}/pkg/usr/lib/x2go
  cp -a lib/Xrender/lib* ${startdir}/pkg/usr/lib/x2go
  install -Dm755 programs/Xserver/nxagent ${startdir}/pkg/usr/bin/x2goagent
}
