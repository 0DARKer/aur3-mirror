# Contributor: Jonathan Liu <net147@gmail.com>
pkgname=gradm
pkgver=2.2.1
_timestamp=201012301333
pkgrel=4
pkgdesc="Administrative interface for the grsecurity Role Based Access Control system"
arch=('i686' 'x86_64')
url="http://www.grsecurity.net/"
license=('GPL2')
depends=('pam')
source=("http://www.grsecurity.net/stable/${pkgname}-${pkgver}-${_timestamp}.tar.gz")
md5sums=('7ab5e5f57525c7a4a399e3616faf5629')

build() {
  cd "${srcdir}/${pkgname}2"
  sed -i -e "s/^CFLAGS :=/CFLAGS +=/" "Makefile"
  make
}

package() {
  cd "${srcdir}/${pkgname}2"
  make DESTDIR="${pkgdir}/" INSTALL=/bin/install install
  rm "${pkgdir}"/dev/grsec > /dev/null
  rmdir "${pkgdir}"/dev > /dev/null
}

# vim:set ts=2 sw=2 et:
