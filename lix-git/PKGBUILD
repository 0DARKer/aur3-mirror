# Maintainer: Carl Reinke <mindless2112 gmail com>

pkgname=lix-git
pkgver=20111224
pkgrel=1
pkgdesc="an action-puzzle game in the IRS (Interactive Rodent Simulation) genre (like Lemmings)"
url="http://asdfasdf.ethz.ch/~simon/"
license=('custom')
source=('lix.launcher'
        'lixd.launcher'
        'lix.png'
        'lix.desktop')
md5sums=('55a17a99eed2ca312176bfb330e3c3f1'
         '766cfcdb7e9c861c57153bbf86e15c7b'
         '19cede8ac55382151127c22ad80dc47f'
         'a0a5d058c60d3ea48d09c010699600b5')
arch=('i686' 'x86_64')
depends=('allegro4' 'enet')
makedepends=('git')

_gitroot=git://github.com/SimonN/Lix.git
_gitname=Lix

build()
{
	cd "$srcdir"
	
	msg "Connecting to $_gitroot..."
	if [[ -d "$srcdir/$_gitname" ]]; then
		cd "$_gitname" && git pull origin
	else
		git clone "$_gitroot"
	fi
	
	rm -rf "$srcdir/$_gitname-build"
	git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	
	cd "$srcdir/$_gitname-build"
	
	msg "Starting make..."
	make
}

package()
{
	cd "$srcdir/$_gitname-build"
	
	install -dm755 -ggames  "$pkgdir/opt/lix"
	install -Dm755 bin/lix  "$pkgdir/opt/lix/lix"
	install -Dm755 bin/lixd "$pkgdir/opt/lix/lixd"
	cp -R bitmap/ data/ doc/ levels/ replay/ "$pkgdir/opt/lix/"
	
	install -Dm755 "$srcdir/lix.launcher"  "$pkgdir/usr/bin/lix"
	install -Dm755 "$srcdir/lixd.launcher" "$pkgdir/usr/bin/lixd"
	install -Dm644 "$srcdir/lix.png"       "$pkgdir/usr/share/pixmaps/lix.png"
	install -Dm644 "$srcdir/lix.desktop"   "$pkgdir/usr/share/applications/lix.desktop"
	
	cd "$pkgdir"
	
	chown -R :games opt/lix/*
	chmod -R 644 opt/lix/doc
	chmod -R 664 opt/lix/{bitmap,data,levels,replay}
	find opt/lix/ -type d -exec chmod a+x \{\} \;
	
	chmod 777 opt/lix/data/bitmap  # work-around
}
