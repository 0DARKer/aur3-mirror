# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>
# Maintainer: Nathan Owe <ndowens.aur at gmail dot com>
pkgname=gadmin-rsync
pkgver=0.1.5
pkgrel=2
pkgdesc="An easy to use GTK+ frontend for the rsync backup client and server."
arch=("i686" "x86_64")
url="http://www.gadmintools.org/"
license=("GPL")
depends=("gtk2" "rsync" "openssh")
options=(!emptydirs)
optdepends=('gksu: Graphical SU Support')
source=("http://mange.dynalias.org/linux/${pkgname}/${pkgname}-${pkgver}.tar.gz" "${pkgname}-menu" "${pkgname}.desktop")
md5sums=('cbb77713c159568835d6f012e0450dc3'
         'd251c6fd24dc651ca14aa4571cbdbe5b'
         '1236e288d86316fc205a21dd0e1ab7b1')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/sbin 
	make 
}
package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install 
	install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm755 ${srcdir}/${pkgname}-menu ${pkgdir}/usr/sbin/${pkgname}-menu
}
