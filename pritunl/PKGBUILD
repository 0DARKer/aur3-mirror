# Maintainer: Pritunl <contact@pritunl.com>

pkgname=pritunl
pkgver=1.0.464.77
pkgrel=1
pkgdesc="Enterprise VPN Server"
arch=("any")
license=("custom")
url="https://github.com/${pkgname}/${pkgname}"
depends=(
    "python"
    "python2"
    "python2-flask"
    "python2-pyopenssl"
    "python2-pymongo"
    "net-tools"
    "iproute2"
    "openvpn"
)
optdepends=(
    "mongodb"
)
makedepends=(
    "python2-distribute"
    "python2-flask"
    "python2-pyopenssl"
    "python2-pymongo"
)
provides=("${pkgname}")
conflicts=("${pkgname}")
install=${pkgname}.install
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=("aec52206c24e9fc9a0daf27dbd95fd960901a779430b054ce66b4d2040558ea2")
options=("emptydirs")
backup=(
    "etc/${pkgname}.conf"
    "var/lib/${pkgname}/${pkgname}.db"
    "var/log/${pkgname}.log"
    "var/log/${pkgname}.log.1"
)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p "${pkgdir}/var/lib/${pkgname}"
    python2 setup.py install --root="${pkgdir}" --prefix=/usr --no-upstart
}
