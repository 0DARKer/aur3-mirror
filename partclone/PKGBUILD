# Maintainer: jsteel <jsteel at vorx dot com>
# Contrinutor: Dan Serban
# Contributor: Franz Burgmann
# Contributor: Todd Partridge (Gen2ly)
# Contributor: Ivan Sichmann Freitas

pkgname=partclone
pkgver=0.2.26
pkgrel=2
pkgdesc="Back up and restore used-blocks of a partition"
arch=('i686' 'x86_64')
url="http://partclone.org"
license=(GPL)
depends=(e2fsprogs-git progsreiserfs dosfstools ncurses)
conflicts=(partclone-testing partclone-git)
source=(https://downloads.sourceforge.net/project/$pkgname/stable/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('da44b85a7d3706e373d0c12da8625ae7')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-extfs --enable-reiserfs --enable-fat \
    --enable-hfsp --enable-btrfs --enable-ncursesw --enable-ntfs
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

