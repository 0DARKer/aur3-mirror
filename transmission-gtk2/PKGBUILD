# Maintainer: Ivan Turkin <somebody2112 at yandex.ru>
# Contributor: hadzhimurad ustarkhan - hmurad dot ust at gmail dot com

pkgbase=transmission
pkgname=transmission-gtk2
pkgver=2.50
pkgrel=3
pkgdesc="Fast, easy, and free BitTorrent client (GTK2 GUI)"
arch=('i686' 'x86_64')
url="http://www.transmissionbt.com/"
license=('MIT')
depends=('curl' 'libevent' 'gtk2' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('notification-daemon: Desktop notification support' 'transmission-cli: daemon and web support')
makedepends=('gtk2' 'intltool' 'curl' 'libevent')
provides=('transmission-gtk=$pkgver')
conflicts=('transmission-gtk')
install="${pkgname}.install"
source=(http://mirrors.m0k.org/transmission/files/${pkgbase}-${pkgver}.tar.xz
        0001-gtk-conf-set-correct-download-dir-default.patch)
md5sums=('07606789fab2f9730fa454cfc7a04b2d'
         '4bd9f3cc39e7eb76f5ff45aea832999b')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  patch -Np1 -i "$srcdir/0001-gtk-conf-set-correct-download-dir-default.patch"

  ./configure --prefix=/usr --disable-cli --disable-daemon --with-gtk=2

  make
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  make -C gtk DESTDIR="${pkgdir}" install

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/transmission-gtk/COPYING"
}
