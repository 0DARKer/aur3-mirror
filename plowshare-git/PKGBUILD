# Maintainer: speps <speps at aut dot archlinux dot org>

pkgname=plowshare-git
pkgver=1.0.0.r16.g2c3393e
pkgrel=1
pkgdesc="Command-line downloader and uploader for Rapidshare, Mediafire and other file sharing websites."
arch=('any')
url="http://code.google.com/p/plowshare/"
license=('GPL3')
depends=('curl' 'recode' 'js185')
makedepends=('git')
optdepends=('bash-completion: enable bash auto completion'
            'libcaca: ascii display for no X server'
            'tiv: ascii display for no X server'
            'aview: ascii display for no X server')
provides=('plowshare')
conflicts=('plowshare')
replaces=('plowshare-svn')
source=("$pkgname::git+https://code.google.com/p/plowshare/")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long --tags | sed 's/.//;s/-/.r/;s/-/./'
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir/" PREFIX=/usr make
}

# vim:set ts=2 sw=2 et:
