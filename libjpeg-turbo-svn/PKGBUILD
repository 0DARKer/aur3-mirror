# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libjpeg-turbo-svn
pkgver=537
pkgrel=1
pkgdesc="A high-speed version of libjpeg for x86 and x86-64 processors which uses SIMD instructions"
arch=('i686' 'x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('custom:wxWindows Library Licence')
makedepends=('subversion' 'nasm')
provides=('libjpeg=8.3.0')
conflicts=('libjpeg' 'libjpeg-turbo')
options=('!libtool')

_svntrunk=https://libjpeg-turbo.svn.sourceforge.net/svnroot/libjpeg-turbo/trunk
_svnmod=libjpeg-turbo

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # BUILD
  autoreconf -fiv
  ./configure --with-jpeg8 --prefix=/usr
  make
}
package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
  install -D $srcdir/$_svnmod-build/LICENSE.txt $pkgdir/usr/share/licenses/libjpeg-turbo/LICENSE
}

