# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Baurzhan Muftakhidinov <baurthefirst@gmail.com>

pkgname=xfce4-settings-git
_pkgname=xfce4-settings
pkgver=4.11.3.r29.g6a812b2
pkgrel=1
pkgdesc="Xfce's settings."
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org"
depends=('exo-git' 'gtk2>=2.14.0' 'glib2>=2.18.0' 'libwnck' 'hicolor-icon-theme' 'libxklavier')
#depends=('exo' 'gtk2>=2.14.0' 'glib2>=2.18.0' 'libwnck' 'hicolor-icon-theme')
makedepends=('intltool' 'git' 'xfce4-dev-tools-git' 'libxfce4ui-git' 'xfconf-git')
#makedepends=('intltool' 'git' 'xfce4-dev-tools' 'libxfce4ui' 'xfconf')
conflicts=('xfce4-settings')
provides=('xfce4-settings')
options=('!libtool')
source=("${_pkgname}::git://git.xfce.org/xfce/xfce4-settings.git")
md5sums=("SKIP")

pkgver() {
	cd "${_pkgname}"

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd $srcdir/$_pkgname
    
	./autogen.sh  --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--enable-debug=minimum \
		--enable-maintainer-mode \
		--enable-libxklavier \
		--enable-sound-settings \
		--enable-pluggable-dialogs
	make
}

package() {
	cd $srcdir/$_pkgname
	make DESTDIR=${pkgdir} install
}