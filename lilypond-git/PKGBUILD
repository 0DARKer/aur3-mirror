# Maintainer: Patrick McCarty <pnorcks at gmail dot com>

pkgname=lilypond-git
pkgver=2.17.16.1.34.gecc41fd
pkgrel=1
pkgdesc="An automated music engraving system (Git snapshot)"
arch=('i686' 'x86_64')
url="http://lilypond.org/"
license=('GPL')
depends=('fontconfig'
         'freetype2'
         'ghostscript'
         'guile'
         'pango'
         'python2')
makedepends=('fontforge'
             'git'
             'gsfonts'
             't1utils'
             'texlive-core>=2011.23170')
optdepends=('netpbm: building HTML documentation'
            'imagemagick: building HTML documentation'
            'ttf-kochi-substitute: building HTML documentation'
            'texi2html>=1.82: building HTML documentation'
            'rsync: installing HTML documentation')
provides=('lilypond')
conflicts=('lilypond' 'lilypond-devel')
source=(git://git.sv.gnu.org/lilypond.git)
sha256sums=('SKIP')

pkgver() {
  cd lilypond/
  git describe | sed -e 's|release/\(.*\)|\1|' -e 's|-|.|g'
}

build() {
  cd lilypond/

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  export PYTHON="python2"
  export PYTHON_CONFIG="python2-config"

  ./autogen.sh \
          --prefix=/usr \
          --disable-documentation

  make all
}

package() {
  cd lilypond/
  make DESTDIR="$pkgdir/" vimdir="/usr/share/vim/vimfiles" install

  rm -rf "$pkgdir/usr/share/man"
}

# vim:set ts=2 sw=2 et:
