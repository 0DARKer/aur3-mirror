# Author: NAKASHIMA, Makoto <makoto.nksm@gmail.com>

pkgname=rust-incoming-git
pkgver=20121104
pkgrel=1
pkgdesc="A safe, concurrent, practical language from Mozilla."
arch=(i686 x86_64)
url="http://www.rust-lang.org/"
license=('MIT')
depends=('gcc-libs')
makedepends=('git' 'gcc'
             'libffi' 'python2'  # for LLVM
            )
optdepends=('pandoc: to build rust.pdf'
            'llnextgen: for build-time grammar verification'
            'naturaldocs: to build library doc')

_gitroot="git://github.com/mozilla/rust.git"
_gitname="rust"
_gitbranch="incoming"

build() {
  cd "$srcdir"
  msg "Connecting to git server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    git submodule sync
    git submodule update --init --recursive
    git submodule foreach --recursive git clean -dxf
    git submodule foreach --recursive git checkout .
    msg "The local files are updated."
  else
    git clone --recursive $_gitroot $_gitname --branch $_gitbranch --depth=1
    cd $_gitname
  fi

  msg "git checkout done or server timeout"

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./configure --prefix=/usr

msg "Starting make..."
  make || return 1
}

check() {
  cd "$srcdir/$_gitname-build"

  msg "Starting check..."
  make check || return 1

}

package() {
  mkdir "$pkgdir/usr"
  cd "$srcdir/$_gitname-build"
  make install DESTDIR="$pkgdir"

  _docdir=$pkgdir/usr/share/doc/rust
  mkdir -p "$_docdir"
  for _doc in rust.pdf rust.html tutorial.html rust.css core std rust.md ; do
    if ! [ -e "doc/$_doc" ] ; then continue ; fi
    cp -r "doc/$_doc" "$_docdir/"
    chmod -R 644 "$_docdir/$_doc"
    chown -R root:root "$_docdir/$_doc"
  done
  find "$_docdir" -type d -exec chmod 755 '{}' ';'
}

