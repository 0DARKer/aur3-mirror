# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-rose-datetime
pkgver=0.538
pkgrel=1
pkgdesc="DateTime helper functions and objects"
arch=(any)
license=('PerlArtistic')
url="http://search.cpan.org/dist/Rose-DateTime/"
depends=(perl-datetime perl-rose-object)
makedepends=(perl-test-pod)
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/J/JS/JSIRACUSA/Rose-DateTime-${pkgver}.tar.gz)
md5sums=('8dd64d0111374d271fa86c81bf85b2e4')
sha512sums=('3351a22728b2ac3005986c8dd05d5897014f0a4605c5b5dc07c8803e660ac032f3fde212f8054826335644a597b7bc177e7170ec16cb0a20967496051b362b96')

build() {
  cd ${srcdir}/Rose-DateTime-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/Rose-DateTime-${pkgver}
  make test
}

package() {
  cd ${srcdir}/Rose-DateTime-${pkgver}
  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} '(' -name perllocal.pod -o -name .packlist ')' -delete
}
