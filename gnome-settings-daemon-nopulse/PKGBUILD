# Maintainer: trya <tryagainprod@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgbasename=gnome-settings-daemon
pkgname=gnome-settings-daemon-nopulse
pkgver=3.4.1
pkgrel=2
pkgdesc="The GNOME Settings daemon, without PulseAudio support"
arch=('i686' 'x86_64')
license=('GPL')
depends=('colord' 'dconf' 'gnome-desktop' 'gsettings-desktop-schemas'
         'hicolor-icon-theme' 'libcanberra' 'libgnomekbd' 'libnotify'
         'libsystemd' 'libwacom' 'nss' 'upower')
makedepends=('intltool' 'gtk-doc' 'gnome-desktop' 'gnome-common' 'xf86-input-wacom')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
provides=('gnome-settings-daemon')
conflicts=('gnome-settings-daemon')
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/3.4/$_pkgbasename-$pkgver.tar.xz
        systemd-fallback.patch
        evironment.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=9d7ea1d0b8b90b55f916751858a210a2e02a06b6
        arguments.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=00681573ad6ebeb127ec72ac61d30d69be6459f8
        mem-leak.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=053f48b3b1b9a3b2ab0714d470da4354735d0c4b
        glist-leak.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=214a832820f6448f07d438f6686267eaf8301dd1
        mouse-mem-leak.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=09c215b42312ca5bb63b215c2f95231613b3103d)
sha256sums=('d5d4a270ec5692cdc56a58cfb57bc7f8fa93374566912a65e78c1c3db7faf4ed'
            '3af86d4b341d5ca7c94cc814b250b122309d38f48be0bf0581d0c77e763a8008'
            '1ba33f6eadac4c54f3f08f4bce075d2bef8124f857f5af22eaee010c8d38bc3b'
            '884a959b334f864f75dcb4eec8c137785f587c7c2ae57906f8b4e60e04c29246'
            '3a3f8b2964406b9b3c85c05f25e677e8c9eaa221779b95826ffbc0474740decc'
            'af3bda801befdbd3d318723ae6de3aaf7df1e16bccd84e13f15b57f2dceaefcd'
            '43b7397e622a2c19091b3665822e9461067fb6a497b9507418a3bbb100a69e1b')

build() {
  cd $_pkgbasename-$pkgver

  patch -Np1 -i ../systemd-fallback.patch
  patch -Np1 -i ../evironment.patch
  patch -Np1 -i ../arguments.patch
  patch -Np1 -i ../mem-leak.patch
  patch -Np1 -i ../glist-leak.patch
  patch -Np1 -i ../mouse-mem-leak.patch

  gnome-autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-settings-daemon --disable-static \
      --enable-systemd --disable-pulse --disable-packagekit

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
}
