# Maintainer: trya <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=gnome-settings-daemon
pkgname=gnome-settings-daemon-nopulse
pkgver=3.2.1
pkgrel=1
pkgdesc="The GNOME Settings daemon, without PulseAudio support"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd' 'gnome-desktop' 'libnotify' 'hicolor-icon-theme' 'libcanberra'
         'gsettings-desktop-schemas' 'nss' 'gconf' 'dconf' 'upower')
makedepends=('intltool' 'gtk-doc')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
provides=('gnome-settings-daemon')
conflicts=('gnome-settings-daemon')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/3.2/${_pkgbasename}-${pkgver}.tar.xz
        upower.patch)
sha256sums=('1015b8a12483e91ddc67e891b66207a70339a341a051507aad9a6a15fbfcb78d'
            'f7a66e9ff7f415be34873fd6d03843a20732158bd65b382b114d100aaddeb223')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # Fix suspend via key
  patch -Np1 -i "$srcdir/upower.patch"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libexecdir=/usr/lib/gnome-settings-daemon \
              --disable-static \
              --disable-pulse \
              --disable-packagekit \
              --with-pnpids=/usr/share/libgnome-desktop-3.0/pnp.ids
  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
