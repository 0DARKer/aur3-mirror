# Maintainer: TryA <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=gnome-settings-daemon
pkgname=gnome-settings-daemon-nopulse
pkgver=3.0.3
pkgrel=1
pkgdesc="The GNOME Settings daemon, without PulseAudio support"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd' 'gnome-desktop' 'libnotify' 'hicolor-icon-theme' 
         'gsettings-desktop-schemas' 'nss' 'gconf' 'dconf')
makedepends=('intltool' 'gtk-doc')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
provides=(gnome-settings-daemon)
conflicts=(gnome-settings-daemon)
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/3.0/${_pkgbasename}-${pkgver}.tar.xz)
sha256sums=('9c6e3bb5c4b3a98ab83a917b7be15578d11831330f7f2deeecfbb625568e3e03')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libexecdir=/usr/bin \
              --disable-static \
              --disable-pulse \
              --disable-packagekit
  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
