# Maintainer: TryA <tryagainprod@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=gnome-settings-daemon
pkgname=gnome-settings-daemon-nopulse
pkgver=3.0.2
pkgrel=2
pkgdesc="The GNOME Settings daemon, without PulseAudio support"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd' 'gnome-desktop' 'libnotify' 'hicolor-icon-theme' 
         'gsettings-desktop-schemas' 'nss' 'gconf' 'dconf' 'libsm' 'libgcrypt')
makedepends=('intltool' 'gtk-doc')
options=('!emptydirs' '!libtool')
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
provides=(gnome-settings-daemon)
conflicts=(gnome-settings-daemon)
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/3.0/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('f304aa54c59ab23f7ef55bfeefeecfd6f7a95cabbeed2ea802c94463d4234eec')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libexecdir=/usr/bin \
              --disable-static \
              --disable-pulse \
              --disable-packagekit
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
