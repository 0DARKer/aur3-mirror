# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=airspy-git
pkgver=20140208
_commit=e510c2fee0
pkgrel=1
pkgdesc="Host code for Airspy SDR."
arch=('i686' 'x86_64')
url="http://www.airspy.com"
license=('GPL2')
depends=('libusb>=1.0')
makedepends=('git' 'cmake')
source=("git+https://github.com/airspy/host#commit=$_commit")
md5sums=('SKIP')

_gitname="host"

pkgver() {
  cd "$srcdir/$_gitname"
  git show -s --format="%ci" HEAD | sed -e 's/-//g' -e 's/ .*//'
}

build() {
  cd "$srcdir/$_gitname"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -Wno-dev ../
  make
}

package() {
  cd "$srcdir/$_gitname/build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
