# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

########[ OPTIONS ]########################################
# Comment out if you want to build all language translations
export LG2=en

# Download sources from within i2p
_i2p_fetch=0
###########################################################

pkgname=i2p
pkgver=0.9.5
pkgrel=2
pkgdesc="A distributed anonymous network"
url="http://www.i2p2.de"
license=('GPL2')
arch=('any')
depends=('java-runtime<=7')
makedepends=('apache-ant' 'java-environment')
optdepends=('gtk2: for rrd graphs'
            'robert: I2P BitTorrent client')
conflicts=('i2p-bin' 'i2p-dev' 'i2p-portable' 'i2p-portable-source')
backup=('opt/i2p/wrapper.config')
install='i2p.install'
source=("https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2"
        "https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2.sig"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2psource_${pkgver}.tar.bz2"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2psource_${pkgver}.tar.bz2.sig"
        'tmpfiles.d' 'i2prouter.service' 'i2prouter.sh')

[[ "$_i2p_fetch" = 1 ]] && {
    export http_proxy=127.0.0.1:4444
    source=("http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2"
            "http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2.sig"
            'tmpfiles.d' 'i2prouter.service' 'i2prouter.sh')
}
sha256sums=('441eac3cb7f25c03e7711c7d516626dc86ba6ad373145ab802d150848262c7c2'
            'e4715c5adc4cb982ff34c1699307772904eab93759333707e150f17c382dc06f'
            '875a979992c789d8191ecc17c9e43063d5dd90215afd7101c5019e3fdc3fc8a4'
            'bfa4de5936b0a0ff08c20307b0739ab6a65bee325e82fe22662ccf2062f35666'
            '25b227bc33a4b76e022d4ab40cd1eb1037f1439000d96258de90eda506ecb620')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    source /etc/profile.d/apache-ant.sh
    source /etc/profile.d/jdk.sh 2>/dev/null || source /etc/profile.d/openjdk6.sh

    ant preppkg-linux-only
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    install -dm700   ${pkgdir}/run/i2p
    install -dm755   ${pkgdir}/usr/bin
    install -dm755   ${pkgdir}/opt/i2p

    cp -r pkg-temp/* ${pkgdir}/opt/i2p

    install -Dm755 ${srcdir}/i2prouter.sh         "${pkgdir}/opt/i2p/i2prouter"
    install -Dm644 ${srcdir}/i2prouter.service    "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${srcdir}/tmpfiles.d           "${pkgdir}/usr/lib/tmpfiles.d/i2prouter.conf"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1 "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/LICENSE.txt  "${pkgdir}/usr/share/licenses/i2p/LICENSE"
    mv ${pkgdir}/opt/i2p/licenses/*               "${pkgdir}/usr/share/licenses/i2p/"

    wrapperpath="$pkgdir/opt/i2p/lib/wrapper/linux"
    [[ "$CARCH" = 'x86_64' ]] && wrapperpath+=64
    [[ "$CARCH" = 'armv6h' ]] && wrapperpath+=-armv6
    install -m644 installer/lib/wrapper/all/wrapper.jar "${pkgdir}/opt/i2p/lib/wrapper.jar"
    install -m644 $wrapperpath/libwrapper.so "${pkgdir}/opt/i2p/lib/libwrapper.so"
    install -m755 $wrapperpath/i2psvc        "${pkgdir}/opt/i2p/i2psvc"

    chmod +x ${pkgdir}/opt/i2p/eepget
    ln -s /opt/i2p/{eepget,i2prouter} ${pkgdir}/usr/bin/

    rm -r ${pkgdir}/opt/i2p/{osid,postinstall.sh,runplain.sh,INSTALL-headless.txt,LICENSE.txt,licenses,man,lib/wrapper}

    sed -i $pkgdir/opt/i2p/{eepget,wrapper.config} \
        -e 's:%INSTALL_PATH:/opt/i2p:g' \
        -e 's:$INSTALL_PATH:/opt/i2p:' \
        -e 's:wrapper.signal.mode.usr1=.*:wrapper.signal.mode.usr1=RESTART:'
    sed -i $pkgdir/opt/i2p/clients.config \
        -e "s:clientApp.4.startOnLoad=.*:clientApp.4.startOnLoad=false:"
    echo 'router.updateDisabled=true' >${pkgdir}/opt/i2p/router.config
}
