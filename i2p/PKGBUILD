# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

########[ OPTIONS ]########################################
# Build multi language translations
_multilang=0

# Download sources from within i2p
_i2p_fetch=0
###########################################################

pkgname=i2p
pkgver=0.9.4
pkgrel=1
pkgdesc="A distributed anonymous network"
url="http://www.i2p2.de"
license=('GPL2')
arch=('x86_64' 'i686' 'armv6h')
depends=('java-runtime' 'bash' 'libxtst')
[[ "$_multilang" = 1 ]] && depends+=('gettext')
makedepends=('apache-ant' 'java-environment')
optdepends=('gtk2: for rrd graphs'
            'robert: I2P BitTorrent client')
conflicts=('i2p-bin' 'i2p-dev' 'i2p-portable' 'i2p-portable-source')
install='i2p.install'
source=("https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2"
        "https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2.sig"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2psource_${pkgver}.tar.bz2"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2psource_${pkgver}.tar.bz2.sig"
        'i2prouter.service')

[[ "$_i2p_fetch" = 1 ]] && {
    export http_proxy=127.0.0.1:4444
    source=("http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2"
            "http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2.sig"
            "i2prouter.service")
}
sha256sums=('1b7d9695555ed42142b04ad6bcda083cd1a064f6354b639ad2aef4d9cd474e06'
            '90ddc2ef4c177907a8fc575b4ca432e272844d1f9b6b5935996b07b30f803e47'
            'b6939bb58275e67ecc23990d4bc4062987861da482e4b7a92f8be186ca5383f1')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    [[ "$_multilang" = 0 ]] && export LG2=en
    source /etc/profile.d/apache-ant.sh
    ant installer-linux
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    echo "INSTALL_PATH=${pkgdir}/opt/i2p" >install.properties
    java -jar i2pinstall_${pkgver}*.jar -options install.properties

    sed -i $pkgdir/opt/i2p/{i2prouter,wrapper.config,runplain.sh,eepget} \
        -e "s:$pkgdir/opt/i2p:/opt/i2p:g"
    sed -i $pkgdir/opt/i2p/i2prouter \
        -e "s:#RUN_AS_USER=.*:RUN_AS_USER=i2p:" \
        -e "s:I2P_CONFIG_DIR=.*:I2P_CONFIG_DIR=\"\$I2P/.i2p\":"
    echo 'router.updateDisabled=true' >${pkgdir}/opt/i2p/router.config

    install -d "${pkgdir}/usr/share/licenses/i2p/"
    install -Dm644 ${srcdir}/i2prouter.service       "${pkgdir}/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 ${pkgdir}/opt/i2p/man/eepget.1    "${pkgdir}/usr/share/man/man1/eepget.1"
    install -Dm644 ${pkgdir}/opt/i2p/man/i2prouter.1 "${pkgdir}/usr/share/man/man1/i2prouter.1"
    mv ${pkgdir}/opt/i2p/LICENSE.txt                 "${pkgdir}/usr/share/licenses/i2p/"
    mv ${pkgdir}/opt/i2p/licenses/*                  "${pkgdir}/usr/share/licenses/i2p/"

    rm -r ${pkgdir}/opt/i2p/{Uninstaller,.installationinformation,INSTALL-headless.txt,licenses,man}
    chmod -R 755 ${pkgdir}/opt/i2p
    chmod -x ${pkgdir}/opt/i2p/*.config
}
