# Maintainer: veox <veox at wre dot ath dot cx>
# Contributor: Rorschach <r0rschach@lavabit.com>
# Contributor: Cedric Chabanois <cchabanois@gmail.com>

pkgname=i2p
pkgver=0.8.13
pkgrel=1
pkgdesc="A distributed anonymous network"
url="http://www.i2p2.de"
license=('GPL')
depends=('bash' 'java-runtime')
makedepends=('apache-ant' 'java-environment')
conflicts=('i2p-bin')
replaces=('i2p-source')
arch=('i686' 'x86_64')
install=i2p.install
source=("http://mirror.i2p2.de/i2psource_${pkgver}.tar.bz2" 
        'i2prouter' 'i2prouter.service')

build() {
  # add apache ant to PATH
  . /etc/profile.d/apache-ant.sh
  
  cd $srcdir/${pkgname}-${pkgver}
  cat > install.properties << EOC
INSTALL_PATH=$pkgdir/opt/i2p
EOC
  # yes, download jetty (version 5)
  echo "y" | ant pkg
}

package() {
  cd $srcdir/${pkgname}-${pkgver}

  java -jar install.jar -options install.properties

  sed -i "s|$pkgdir/opt/i2p|/opt/i2p|g" \
    $pkgdir/opt/i2p/i2prouter \
    $pkgdir/opt/i2p/wrapper.config \
    $pkgdir/opt/i2p/runplain.sh \
    $pkgdir/opt/i2p/eepget
  sed -i 's/#RUN_AS_USER=/RUN_AS_USER=i2p/' \
    $pkgdir/opt/i2p/i2prouter

  rm $pkgdir/opt/i2p/.installationinformation
  rm -r $pkgdir/opt/i2p/Uninstaller
  
  install -Dm755 $srcdir/i2prouter $pkgdir/etc/rc.d/i2prouter
  install -Dm644 $srcdir/i2prouter.service $pkgdir/lib/systemd/system/i2prouter.service
}
md5sums=('5598656bf265b263c679ffe7f12215ab'
         'ac9fa57b32f786f371bdf4dbd4931105'
         '57352bccc56bb4b109f969806a0bcd32')
