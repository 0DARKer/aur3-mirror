# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

########[ OPTIONS ]########################################
# Comment out if you want to build all language translations
export LG2=en

# Download sources from within i2p
#_i2p_fetch=1
###########################################################

pkgname=i2p
pkgver=0.9.5
pkgrel=2
pkgdesc="A distributed anonymous network"
url="http://www.i2p2.de"
license=('GPL2')
arch=('any')
depends=('java-runtime')
makedepends=('apache-ant' 'java-environment' 'java-service-wrapper')
optdepends=('robert: I2P BitTorrent client') #'gtk2: for rrd graphs'
conflicts=('i2p-bin' 'i2p-dev' 'i2p-portable' 'i2p-portable-source')
backup=('opt/i2p/wrapper.config')
install='i2p.install'
source=("https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2"
        "https://i2p.googlecode.com/files/i2psource_${pkgver}.tar.bz2.sig"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2psource_${pkgver}.tar.bz2"
        #"https://launchpad.net/i2p/trunk/${pkgver}/+download/i2psource_${pkgver}.tar.bz2.sig"
        'i2prouter.service' 'i2prouter.sh')

[[ $_i2p_fetch ]] && {
    export http_proxy=127.0.0.1:4444
    source=("http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2"
            "http://echelon.i2p/${pkgver}/i2psource_${pkgver}.tar.bz2.sig"
            'i2prouter.service' 'i2prouter.sh')
}
sha256sums=('441eac3cb7f25c03e7711c7d516626dc86ba6ad373145ab802d150848262c7c2'
            'e4715c5adc4cb982ff34c1699307772904eab93759333707e150f17c382dc06f'
            '05741e9dcbe004ff79ecd5c8bfbcf13ead4a9351f5c5cc3185bd464038805e5f'
            '65c989cdf206aed8d1863f9d69aa1463517e5c06a0833b1ceb65bcda0d0c378e')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    source /etc/profile.d/apache-ant.sh
    source /etc/profile.d/jdk.sh

    ant preppkg-linux-only
}

package() {
    cd "$srcdir/$pkgname-$pkgver"

    install -dm755 "$pkgdir/usr/lib/tmpfiles.d"
    install -dm700 "$pkgdir/run/i2p"
    install -dm755 "$pkgdir/usr/bin"
    install -dm755 "$pkgdir/opt/i2p"

    cp -r pkg-temp/* "$pkgdir/opt/i2p"

    install -Dm755 "$srcdir/i2prouter.sh"         "$pkgdir/opt/i2p/i2prouter"
    install -Dm644 "$srcdir/i2prouter.service"    "$pkgdir/usr/lib/systemd/system/i2prouter.service"
    install -Dm644 "$pkgdir/opt/i2p/man/eepget.1" "$pkgdir/usr/share/man/man1/eepget.1"
    install -Dm644 "$pkgdir/opt/i2p/LICENSE.txt"  "$pkgdir/usr/share/licenses/i2p/LICENSE"
    mv "$pkgdir"/opt/i2p/licenses/*               "$pkgdir/usr/share/licenses/i2p/"

    ln -s /opt/i2p/{eepget,i2prouter} "$pkgdir/usr/bin/"
    chmod +x "$pkgdir"/opt/i2p/{eepget,i2prouter}

    echo 'router.updateDisabled=true' >"$pkgdir/opt/i2p/router.config"
    echo 'd /run/i2p 0700 i2p i2p'    >"$pkgdir/usr/lib/tmpfiles.d/i2prouter.conf"

    sed -i "$pkgdir"/opt/i2p/{eepget,wrapper.config} \
        -e 's:%INSTALL_PATH:/opt/i2p:g' \
        -e 's:$INSTALL_PATH:/opt/i2p:g' \
        -e 's:$SYSTEM_java_io_tmpdir:/opt/i2p:g' \
        -e 's:wrapper.signal.mode.usr1=.*:wrapper.signal.mode.usr1=RESTART:'
    
    sed -i "$pkgdir"/opt/i2p/clients.config \
        -e "s:clientApp.4.startOnLoad=.*:clientApp.4.startOnLoad=false:"
    
    sed -i "$pkgdir"/opt/i2p/wrapper.config \
        -e '/wrapper.java.classpath.1/a \wrapper.java.classpath.2=/usr/share/java/wrapper.jar' \
        -e '/wrapper.java.library.path.2/a \wrapper.java.library.path.3=/usr/lib/java-service-wrapper'

    rm -r "$pkgdir"/opt/i2p/{osid,postinstall.sh,runplain.sh,INSTALL-headless.txt,LICENSE.txt,licenses,man,lib/wrapper*}
}
