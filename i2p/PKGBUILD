# Maintainer: veox <veox at wre dot ath dot cx>
# Contributor: Rorschach <r0rschach@lavabit.com>
# Contributor: Cedric Chabanois <cchabanois@gmail.com>

pkgname=i2p
pkgver=0.8.4
pkgrel=1
pkgdesc="A distributed anonymous network, compile from source"
url="http://www.i2p2.de"
license=('GPL')
depends=('bash' 'java-runtime')
makedepends=('apache-ant')
arch=('i686' 'x86_64')
install=i2p.install
source=("http://mirror.i2p2.de/i2psource_${pkgver}.tar.bz2" 'i2prouter')
md5sums=('4fb739db6bcf5931715198de9338d175' 'eaeae5b6778a085d7647bbc0d49cf2e8')

build() {
  # add apache ant to PATH
  . /etc/profile.d/apache-ant.sh
  
  cd $srcdir/${pkgname}-${pkgver}
  cat > install.properties << EOC
INSTALL_PATH=$pkgdir/opt/i2p
EOC
  # yes, download jetty (version 5)
  echo "y" | ant pkg
}

package() {
  cd $srcdir/${pkgname}-${pkgver}

  java -jar install.jar -options install.properties || return 1

  sed -i "s|$pkgdir/opt/i2p|/opt/i2p|g" $pkgdir/opt/i2p/i2prouter $pkgdir/opt/i2p/wrapper.config $pkgdir/opt/i2p/runplain.sh $pkgdir/opt/i2p/eepget || return 1
  sed -i 's/#RUN_AS_USER=/RUN_AS_USER=i2p/' $pkgdir/opt/i2p/i2prouter || return 1

  rm $pkgdir/opt/i2p/.installationinformation || return 1
  rm -r $pkgdir/opt/i2p/Uninstaller || return 1
  install -Dm755 $srcdir/i2prouter $pkgdir/etc/rc.d/i2prouter || return 1
}
