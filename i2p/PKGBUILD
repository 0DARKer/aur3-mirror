# Maintainer: veox <veox at wre dot ath dot cx>
# Contributor: Rorschach <r0rschach@lavabit.com>
# Contributor: Cedric Chabanois <cchabanois@gmail.com>

pkgname=i2p
pkgver=0.9
pkgrel=1
pkgdesc="A distributed anonymous network"
arch=('x86_64' 'i686')
url="http://www.i2p2.de"
license=('GPL')
depends=('bash' 'java-runtime')
makedepends=('apache-ant' 'java-environment')
conflicts=('i2p-bin')
replaces=('i2p-source')
install=i2p.install
source=("http://mirror.i2p2.de/i2psource_$pkgver.tar.bz2"
        "http://mirror.i2p2.de/i2psource_$pkgver.tar.bz2.sig"
        "i2prouter"
        "i2prouter.service")
sha256sums=('8a3654a13781a9aacf9db94081e057be73322f88db2931eba4f2cfa467ead429'
            'a806a4a9de7549fba4aea41d5fd40ac71a90493e5d68c087ebf81d08671801d6'
            'c882ea377740ee464af322b6fc086ab5f8921707913437ef3dbfa3400cf6ea40'
            'dee85e9a856b18bac257cd06e70f42e88edce3f48b92762410359a69d5c4284f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ant pkg
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
    cat > install.properties <<- EOC
	INSTALL_PATH=$pkgdir/opt/i2p
		EOC

    java -jar install.jar -options install.properties
    
    sed -i "s|$pkgdir/opt/i2p|/opt/i2p|g" \
      $pkgdir/opt/i2p/i2prouter \
      $pkgdir/opt/i2p/wrapper.config \
      $pkgdir/opt/i2p/runplain.sh \
      $pkgdir/opt/i2p/eepget
      
    sed -i 's/#RUN_AS_USER=/RUN_AS_USER=i2p/' \
      $pkgdir/opt/i2p/i2prouter

    rm $pkgdir/opt/i2p/.installationinformation
    rm -r $pkgdir/opt/i2p/Uninstaller
  
    install -Dm755 $srcdir/i2prouter $pkgdir/etc/rc.d/i2prouter
    install -Dm644 $srcdir/i2prouter.service $pkgdir/usr/lib/systemd/system/i2prouter.service
}

# vim:set ts=2 sw=2 et:
