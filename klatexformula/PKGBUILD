# Maintainer: Federico Cerruti <druidofdarkforest@gmail.com>

pkgname=klatexformula
pkgver=3.2.9
pkgrel=1
pkgdesc="A program to easily get an image from a LaTeX formula"
url="http://klatexformula.sourceforge.net/"
arch=("i686" "x86_64")
license=('GPL')
depends=('kdebase-runtime' 'texlive-core' 'ghostscript')
makedepends=('cmake' 'automoc4' 'help2man' 'doxygen')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('b4dedf7d5c4033f7b647469da9ab471d')

build() {
  mkdir build
  cd build
  export PATH=/usr/lib/qt4/bin:$PATH
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="-lX11 ${CXXFLAGS}" \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
