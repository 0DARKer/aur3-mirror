# Creator: Chipster
# Maintainer: FoolEcho <foolecho@gmail.com>

pkgname=qelectrotech-svn
pkgver=1312
pkgrel=1
pkgdesc="SVN version of an electric diagram editor"
arch=('i686' 'x86_64')
url="http://qelectrotech.tuxfamily.org/"
install='qelectrotech.install'
license=('GPL2')
depends=('qt' 'shared-mime-info')
makedepends=('subversion')
_svntrunk=(svn://svn.tuxfamily.org/svnroot/qet/qet/branches/0.3)
_svnmod=qelectrotech

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  #for the Arch packaging standards
  sed 's#/usr/local/#/usr/#g' -i qelectrotech.pro
  sed 's#../share/#./share/#g' -i qelectrotech.pro
  sed 's#doc/qelectrotech/#share/doc/qelectrotech/#g' -i qelectrotech.pro
#  sed 's#man/#share/man/#' -i qelectrotech.pro
  #effet de bord... :-\
#  sed 's#share/man/compress_man_pages.sh#man/compress_man_pages.sh#' -i qelectrotech.pro
  qmake 
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make INSTALL_ROOT="$pkgdir/" install
  mv ${pkgdir}/usr/man ${pkgdir}/usr/share/
}
