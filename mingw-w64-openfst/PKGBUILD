# Maintainer: Christoph Drexler <chrdr at gmx dot at>
# Contributor: Benoit Favre <benoit.favre@lif.univ-mrs.fr>

pkgname=mingw-w64-openfst
_pkgname=openfst
pkgver=1.4.1
pkgrel=1
pkgdesc="Library for constructing, combining, optimizing, and searching weighted finite-state transducers (mingw-w64)"
arch=('any')
url="http://www.openfst.org/"
license=('APACHE')
makedepends=(mingw-w64-configure mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-dlfcn mingw-w64-mman-win32-svn)
options=(!strip !buildflags staticlibs)
source=("http://openfst.cs.nyu.edu/twiki/pub/FST/FstDownload/${_pkgname}-${pkgver}.tar.gz"
        ../getpagesize.patch)
md5sums=('ca8f1730b9b9b281e515611fa9ae23c0'
         '392eba5c5a5487242a79bccf6f9cc652')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    patch -p1 < ../getpagesize.patch
}

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    for _arch in ${_architectures}; do
        mkdir -p build-${_arch} && pushd build-${_arch}
        # Options according to http://openfst.cs.nyu.edu/twiki/bin/view/FST/ReadMe
        OPTIONS="--prefix=/usr --disable-dependency-tracking"
        OPTIONS+=" --enable-bin"            # Enable fst::script and command-line binaries;   Default: yes
        OPTIONS+=" --enable-compact-fsts"   # Enable all CompactFst classes;                  Default: no
        OPTIONS+=" --enable-const-fsts"     # Enable all ConstFst classes;                    Default: no
        OPTIONS+=" --enable-far"            # Enable FAR (FST Archive) extension;             Default: no
        OPTIONS+=" --enable-linear-fsts"    # Enable Linear{Tagger,Classifier}Fst extensions; Default: no
        OPTIONS+=" --enable-lookahead-fsts" # Enable LookAheadFst classes;                    Default: no
        OPTIONS+=" --enable-pdt"            # Experimental push-down transducer extensions;   Default: no
        ${_arch}-configure --build="$CHOST" $OPTIONS LDFLAGS=-lmman
        make
        popd
    done
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    for _arch in ${_architectures}; do
        pushd build-${_arch}
        make DESTDIR=${pkgdir} install
        echo ${pkgdir}
        mv ${pkgdir}/usr/bin ${pkgdir}/usr/${_arch}/bin
        find ${pkgdir}/usr/${_arch} -name \*.exe | xargs ${_arch}-strip
        find ${pkgdir}/usr/${_arch} -name \*.a | xargs ${_arch}-strip -g
        #find ${pkgdir}/usr/${_arch} -name \*.dll | xargs ${_arch}-strip --strip-unneeded
        popd
    done
}
