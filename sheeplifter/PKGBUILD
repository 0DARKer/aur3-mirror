# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>
pkgname=sheeplifter
pkgver=2
pkgrel=2
pkgdesc="Control a wolf in a spaceship and abduct sheep in 3D"
url="http://code.google.com/p/sheeplifter/"
license=('BSD')
arch=('x86_64' 'i686')
depends=('java-runtime')
source=('http://sheeplifter.googlecode.com/files/Sheeplifter_GC_2.zip'
        'sheeplifter.desktop'
        'sheeplifter.png')
md5sums=('12fec5e9429aa65d5d11ed3321aecd36'
         '3d07c5c621fecfae9693c8090341862f'
         '448db2c63a36d093558fb74043ad44c6')

build() {
  cd "$srcdir/$pkgname"
  msg2 "Creating wrapperscript..."
  echo "#!/bin/sh" > sheeplifter.sh
  echo "cd '/usr/share/sheeplifter/' && sh run_game_linux.sh" >> sheeplifter.sh
}

package() {
  cd "$srcdir/$pkgname"
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/"{pixmaps,applications,sheeplifter}

  msg2 "Packaging game content..."
  cp -R jme "$pkgdir/usr/share/sheeplifter"
  cp -R jorbis "$pkgdir/usr/share/sheeplifter"
  cp -R lwjgl "$pkgdir/usr/share/sheeplifter"
  find $pkgdir/usr/share/sheeplifter -type f -exec chmod 644 {} \;
  rm -rf "$pkgdir/usr/share/sheeplifter/lwjgl/native/macosx"
  rm -rf "$pkgdir/usr/share/sheeplifter/lwjgl/native/win32"
  if [ "$CARCH" = "x86_64" ]; then
    find "$pkgdir/usr/share/sheeplifter/lwjgl/native/linux" -type f | grep -v 64 | xargs rm
  else
    find "$pkgdir/usr/share/sheeplifter/lwjgl/native/linux" -type f -name "*64.*" | xargs rm
  fi
  install -Dm644 readme.txt sheeplifter.jar $pkgdir/usr/share/sheeplifter
  install -Dm755 run_game_linux.sh $pkgdir/usr/share/sheeplifter

  msg2 "Packaging license..."
  tail -24 readme.txt > license.txt
  install -Dm644 license.txt $pkgdir/usr/share/licenses/$pkgname/COPYING

  msg2 "Packaging wrapperscript..."
  install -Dm755 sheeplifter.sh "$pkgdir/usr/bin/sheeplifter"

  msg2 "Packaging menu entry and icon..."
  install -Dm644 ../sheeplifter.desktop $pkgdir/usr/share/applications
  install -Dm644 ../sheeplifter.png $pkgdir/usr/share/pixmaps
}

# vim:set ts=2 sw=2 et:
