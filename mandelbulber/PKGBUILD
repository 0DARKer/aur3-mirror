# Maintainer: Wilfried Fauvel <wilfried.fauvel at gmail>
# Contributor: Martti KÃ¼hne <mysatyre at gmail dot com>

pkgname=mandelbulber
pkgver=0.98
pkgrel=2
pkgdesc='Easy to use, handy but experimental application designed to help you render 3D Mandelbrot fractals called Mandelbulb and some other kind of 3D fractals like Mandelbox, Bulbbox, Juliabulb, Menger Sponge.'
arch=('i686' 'x86_64')
url='http://sites.google.com/site/mandelbulber/'
license=('GPL')
depends=('gtk2' 'libjpeg' 'libpng12')
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}${pkgver}.tar.gz )
install=mandelbulber.install
md5sums=('6a684ce3f416657bccc39f9d520ad57a')

build() {
  cd "${srcdir}/${pkgname}${pkgver}/makefiles"

  # Clean up pre-compiled binaries
  make clean

  # Build
  make all
}

package()
{
  # Install
  install -m755 -d "${pkgdir}/usr/bin"
  install -Dm755 "${srcdir}/${pkgname}${pkgver}/makefiles/${pkgname}" \
                 "${pkgdir}/usr/bin/"

  install -m755 -d "${pkgdir}/usr/share/${pkgname}"
  install -m755 -d "${pkgdir}/usr/share/${pkgname}/.${pkgname}"
  mv "${srcdir}/${pkgname}${pkgver}/.${pkgname}" "${pkgdir}/usr/share/${pkgname}"
  chmod -R 755 "${pkgdir}/usr/share/${pkgname}/.${pkgname}/"
 }

