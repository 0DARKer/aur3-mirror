# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sigmund Lahn <sigmund@lahn.no>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=rethinkdb
pkgver=1.4.5
pkgrel=1
pkgdesc="An open-source distributed database built with love."
arch=('x86_64')
url="http://www.rethinkdb.com/"
license="AGPL"
depends=('boost-libs' 'protobuf' 'v8' 'zlib')
makedepends=('boost' 'curl' 'ctags' 'python2' 'ruby_protobuf' 'java-runtime' 'libunwind' 'coffee-script')
install=rethinkdb.install
source=(
  http://download.rethinkdb.com/dist/rethinkdb-1.4.5.tgz
  rethinkdb-tmpfile.conf
  rethinkdb.service
)
sha256sums=('efb246258863aa522a3b855c979f997c8cc2a5e8f97723c8e08002839fe33eb4'
            '656d3a42e75d087e723f71aa320fdd91cbbb82071ef72eb11fd3e4a619b429a4'
            '3fad80da77a2cc3d2c20f8e9b4155ae7defc345b988bbccbc95be8d4996b595b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  #Change python invocations to python2
  sed -e 's/python -c/python2 -c/g' \
      -i src/Makefile

  # TODO: why there are no static libs for libprotobuf?
  sed -e 's/libprotobuf.a/libprotobuf.so/g' \
      -e 's/libprotoc.a/libprotoc.so/g' \
      -i external/protobuf-plugin-closure/Makefile

  find "$srcdir/$pkgname-$pkgver" -type f -exec sed -i '1 s/python$/python2/' {} +
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
 
  # TODO: create package for libtcmalloc_minimal
  ./configure --without-tcmalloc --enable-precompiled-web --prefix=/usr
  make
}


package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  cd "$pkgdir"

  # TODO: move etc files ti /etc and handle it correctly, including systemd service
  rm -rf etc var

  cd usr/share/rethinkdb
  install -Dm644 etc/bash_completion.d/rethinkdb.bash "$pkgdir/usr/share/bash-completion/completions/rethinkdb"
  install -Dm755 scripts/rdb_migrate "$pkgdir/usr/bin/rdb_migrate"
  rm -rf etc scripts

  install -Dm644 "$srcdir/rethinkdb-tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/rethinkdb.conf"
  install -Dm644 "$srcdir/rethinkdb.service" "$pkgdir/usr/lib/systemd/system/rethinkdb.service"
}
