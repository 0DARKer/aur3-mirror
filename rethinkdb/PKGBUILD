# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sigmund Lahn <sigmund@lahn.no>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=rethinkdb
pkgver=1.4.5
pkgrel=2
pkgdesc='An open-source distributed database built with love.'
arch=('x86_64')
url='http://www.rethinkdb.com/'
license='AGPL'
depends=('boost-libs' 'protobuf' 'v8' 'zlib')
makedepends=('boost' 'curl' 'ctags' 'python2' 'ruby_protobuf' 'java-runtime' 'libunwind' 'coffee-script')
install=rethinkdb.install
source=(
  http://download.rethinkdb.com/dist/rethinkdb-1.4.5.tgz
  rethinkdb-tmpfile.conf
  rethinkdb@.service
)
sha256sums=('efb246258863aa522a3b855c979f997c8cc2a5e8f97723c8e08002839fe33eb4'
            '656d3a42e75d087e723f71aa320fdd91cbbb82071ef72eb11fd3e4a619b429a4'
            'e56bffa2b9ebc3a00ef566ab2be0719a633c89d961a2461dfa2d9ffdb258c1a2')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Change python invocations to python2
  sed -e 's/python -c/python2 -c/g' \
      -i src/Makefile

  # TODO: why there are no static libs for libprotobuf?
  sed -e 's/libprotobuf.a/libprotobuf.so/g' \
      -e 's/libprotoc.a/libprotoc.so/g' \
      -i external/protobuf-plugin-closure/Makefile

  find "$srcdir/$pkgname-$pkgver" -type f -exec sed -i '1 s/python$/python2/' {} +
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
 
  # TODO: create package for libtcmalloc_minimal
  ./configure --without-tcmalloc --enable-precompiled-web --prefix=/usr --sysconfdir=/etc
  make
}


package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/rethinkdb-tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/rethinkdb.conf"
  install -Dm644 "$srcdir/rethinkdb@.service" "$pkgdir/usr/lib/systemd/system/rethinkdb@.service"

  # create 'default' database instance
  cp "$pkgdir/etc/rethinkdb/default.conf.sample" "$pkgdir/etc/rethinkdb/instances.d/default.conf"
  sed -i 's|# bind=127.0.0.1|bind=all|' "$pkgdir/etc/rethinkdb/instances.d/default.conf"
  # TODO: maybe move following values to systemd config file?
  sed -i 's|# directory=/var/lib/rethinkdb|directory=/var/lib/rethinkdb|' "$pkgdir/etc/rethinkdb/instances.d/default.conf"
  sed -i 's|# pid-file=/var/run/rethinkdb/rethinkdb.pid|pid-file=/var/run/rethinkdb/default.pid|' "$pkgdir/etc/rethinkdb/instances.d/default.conf"

  # Arch uses systemd, no need for init.d scripts
  rm -rf "$pkgdir/etc/init.d"
}
