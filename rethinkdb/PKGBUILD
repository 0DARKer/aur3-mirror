# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sigmund Lahn <sigmund@lahn.no>

pkgname=rethinkdb
pkgver=1.5.1
pkgrel=1
pkgdesc='An open-source distributed database built with love.'
arch=('i686' 'x86_64')
url='http://www.rethinkdb.com/'
license='AGPL'
depends=('protobuf' 'v8' 'zlib')
makedepends=('boost' 'python' 'libunwind' 'gperftools')
install=rethinkdb.install
source=(
  http://download.rethinkdb.com/dist/rethinkdb-$pkgver.tgz
  rethinkdb-tmpfile.conf
  rethinkdb@.service
  build_python3.diff
  donot_build_docs.diff
)
sha256sums=('61e71c0ba1711e1eefa8fec050cfad12b731538e03dcfa8394c6a05df3f8d9ad'
            '656d3a42e75d087e723f71aa320fdd91cbbb82071ef72eb11fd3e4a619b429a4'
            'e56bffa2b9ebc3a00ef566ab2be0719a633c89d961a2461dfa2d9ffdb258c1a2'
            '0e1ee3a6bb780647f7bbd4cfdddf7d2c0a612298b1ede1160cf2e75bcfbc57dd'
            '1ed6de1e612c8a4716c3f8060c8c5274156b69fe89d3ee5a3656f0996b09da72')

# TODO add check function for fast integration tests

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugs.archlinux.org/task/35257
  sed -e 's/libprotobuf.a/libprotobuf.so/g' \
      -e 's/libprotoc.a/libprotoc.so/g' \
      -i external/protobuf-plugin-closure/Makefile

  # https://github.com/rethinkdb/rethinkdb/pull/815
  patch -p1 < ../build_python3.diff

  # this is part of 1.5.1-1 release
  patch -p1 < ../donot_build_docs.diff
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --enable-drivers --enable-precompiled-web --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/rethinkdb-tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/rethinkdb.conf"
  install -Dm644 "$srcdir/rethinkdb@.service" "$pkgdir/usr/lib/systemd/system/rethinkdb@.service"

  # create 'default' database instance
  cp "$pkgdir/etc/rethinkdb/default.conf.sample" "$pkgdir/etc/rethinkdb/instances.d/default.conf"
  sed -e 's|# bind=127.0.0.1|bind=all|' \
      -e 's|# directory=/var/lib/rethinkdb|directory=/var/lib/rethinkdb|' \
      -e 's|# pid-file=/var/run/rethinkdb/rethinkdb.pid|pid-file=/var/run/rethinkdb/default.pid|' \
      -i "$pkgdir/etc/rethinkdb/instances.d/default.conf"

  # Arch uses systemd, no need for init.d scripts
  rm -rf "$pkgdir/etc/init.d"
}
