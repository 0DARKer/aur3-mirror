# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Gerhard Brauer <gerhard.brauer@web.de>
pkgname=libcdk
pkgver=5.0.20110517
pkgrel=1
pkgdesc="Curses Development Kit - widget library, enhanced version"
arch=('x86_64' 'i686')
url="http://invisible-island.net/cdk/"
license=('custom')
depends=('bash')
options=(!emptydirs)
source=("ftp://invisible-island.net/cdk/cdk.tar.gz")
md5sums=('5b55943f16c7110d91d4d59073abdcac')

build() {
  cd "$srcdir/cdk-`echo $pkgver | sed 's/\.2/-2/'`"

  msg2 "Configuring..."
  ./configure --prefix=/usr

  msg2 "Compiling..."
  make
}
  
package() {
  cd "$srcdir/cdk-`echo $pkgver | sed 's/\.2/-2/'`"

  msg2 "Packaging..."
  make \
    DOCUMENT_DIR="$pkgdir/usr/share/doc/cdk" \
    MAN_DIR="$pkgdir/usr/share/man" \
    DESTDIR="$pkgdir/" \
    install

  msg2 "Packaging license..."
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
