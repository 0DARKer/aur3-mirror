# Maintainer: Rich Lindsley <richli.ff at gmail dot com>
pkgname=python2-fftw
_py_pkgname=pyFFTW # The pypi package name
pkgver=0.7.0
pkgrel=1
pkgdesc="A pythonic wrapper around FFTW"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/pyFFTW/"
license=('GPL3')
depends=('python2-numpy' 'fftw')
options=(!emptydirs)
source=(http://pypi.python.org/packages/source/p/$_py_pkgname/$_py_pkgname-$pkgver.tar.gz)
md5sums=('538117261f735e42265575173d27eaee')
sha1sums=('670b09da5c998290b99eb29c8fd4b0d8c36b33f4')
sha256sums=('dc67128d2d684e82b2ffa8febfccbd3b47ba1659e0076672fb888fe23d85283d')

build() {
  cd "$srcdir/$_py_pkgname-$pkgver"

  # Convert the Windows newlines to Unix so the sed script below works
  sed -i -e 's/$//' $(find ${srcdir} -name '*.py')

  # We enforce this to be a strictly python2 package
  sed -i -e "s|^#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|^#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|^#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
             $(find ${srcdir} -name '*.py')

}

package() {
  cd "$srcdir/$_py_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1

}

# vim:set ts=2 sw=2 et:
