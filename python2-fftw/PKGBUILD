# Maintainer: Rich Lindsley <richli.ff at gmail dot com>
pkgname=python2-fftw
_py_pkgname=pyFFTW # The pypi package name
pkgver=0.8.0
pkgrel=1
pkgdesc="A pythonic wrapper around FFTW"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/pyFFTW/"
license=('GPL3')
depends=('python2-numpy' 'fftw')
options=(!emptydirs)
source=(http://pypi.python.org/packages/source/p/$_py_pkgname/$_py_pkgname-$pkgver.tar.gz)
md5sums=('9162d59f048d277f05967ad342743229')
sha1sums=('66de175df9b0f3c07252a49fe64433f0523a228e')
sha256sums=('4a30a0782ed5451c5f5fc9fd60edd9cd7a1eab99f261a4bcf0e8de356f9656be')

build() {
  cd "$srcdir/$_py_pkgname-$pkgver"

  # Convert the Windows newlines to Unix so the sed script below works
  sed -i -e 's/$//' $(find ${srcdir} -name '*.py')

  # We enforce this to be a strictly python2 package
  sed -i -e "s|^#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|^#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|^#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
             $(find ${srcdir} -name '*.py')

}

package() {
  cd "$srcdir/$_py_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1

}

# vim:set ts=2 sw=2 et:
