# Maintainer: Rich Lindsley <richli.ff at gmail dot com>
pkgname=python2-fftw
_py_pkgname=pyFFTW # The pypi package name
pkgver=0.8.2
pkgrel=1
pkgdesc="A pythonic wrapper around FFTW"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/pyFFTW/"
license=('GPL3')
depends=('python2-numpy' 'fftw')
options=(!emptydirs)
source=(http://pypi.python.org/packages/source/p/$_py_pkgname/$_py_pkgname-$pkgver.tar.gz)
md5sums=('d5e55665688a4b51f0b65d9508fe8f3d')
sha1sums=('0e42afe79a7518b916e448361df7e87dba4ee383')
sha256sums=('da3ff754d251c063e753c861c99d750fe7a1f44f00de1bef04b4d59e2be98b54')

build() {
  cd "$srcdir/$_py_pkgname-$pkgver"

  # Convert the Windows newlines to Unix so the sed script below works
  sed -i -e 's/$//' $(find ${srcdir} -name '*.py')

  # We enforce this to be a strictly python2 package
  sed -i -e "s|^#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|^#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|^#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
             $(find ${srcdir} -name '*.py')

}

package() {
  cd "$srcdir/$_py_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1

}

# vim:set ts=2 sw=2 et:
