# Maintainer: Rich Lindsley <richli.ff at gmail dot com>
pkgname=python2-fftw
_py_pkgname=pyFFTW # The pypi package name
pkgver=0.8.1
pkgrel=1
pkgdesc="A pythonic wrapper around FFTW"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/pyFFTW/"
license=('GPL3')
depends=('python2-numpy' 'fftw')
options=(!emptydirs)
source=(http://pypi.python.org/packages/source/p/$_py_pkgname/$_py_pkgname-$pkgver.tar.gz)
md5sums=('b7673125e89d73612944c08d32d9489d')
sha1sums=('eef4622ea6fe77a4bdad3e54421316972fb02c28')
sha256sums=('7e6bde3c865694aa479f5ee2a62872af9818f341cbe4361819d2a66818e8004d')

build() {
  cd "$srcdir/$_py_pkgname-$pkgver"

  # Convert the Windows newlines to Unix so the sed script below works
  sed -i -e 's/$//' $(find ${srcdir} -name '*.py')

  # We enforce this to be a strictly python2 package
  sed -i -e "s|^#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|^#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
         -e "s|^#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
             $(find ${srcdir} -name '*.py')

}

package() {
  cd "$srcdir/$_py_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1

}

# vim:set ts=2 sw=2 et:
