# Maintainer: Thomas Dziedzic < gostrc at gmail >

_pkgname=mock
pkgname=mock-git
pkgver=1.1.30
pkgrel=1
epoch=1
pkgdesc='A simple chroot build environment manager for building RPMs.'
url='https://fedorahosted.org/mock/'
arch=('i686' 'x86_64')
license=('GPL')
install=mock-git.install
depends=('python2' 'usermode')
makedepends=('git')
source=('git://git.fedorahosted.org/git/mock.git')
md5sums=('SKIP')


pkgver() {
  cd $_pkgname
  git describe --always | sed -e 's|-|.|g' -e 's|mock.||'
}

build() {
  cd $srcdir/$_pkgname

  export PYTHON=python2

  ./autogen.sh

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd ${_pkgname}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/bin
  ln -s /usr/bin/consolehelper ${pkgdir}/usr/bin/mock

  mkdir -p ${pkgdir}/var/{lib,cache}/mock
  chown -R root:mock ${pkgdir}/var/{lib,cache}/mock
  chmod -R 02755 ${pkgdir}/var/{lib,cache}/mock

  # fix incorrect shebang
  sed -i 's_#!/usr/bin/python_&2_' ${pkgdir}/usr/sbin/mock
}
