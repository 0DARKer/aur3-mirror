# Submitter:   Wessel Dirksen "p-we" <wdirksen at gmail dot com>

pkgname=tbs-unofficial-dvb-drivers-git
pkgver=1
pkgrel=1
pkgdesc="Unofficial TBS DVB drivers from L.J. Alves + firmware"
url="https://github.com/ljalves/linux_media/wiki"
arch=('i686' 'x86_64')
license=('GPL')
depends=('v4l-utils')
makedepends=('git' 'patchutils' 'perl-proc-processtable' 'linux-headers')
conflicts=('ffdecsawrapper' 'tbs-linux-drivers' 'tbs-dvb-drivers')
provides=('tbs-dvb-drivers')
install='tbs-dvb-drivers.install'

_tbsver=v140425

source=('git://linuxtv.org/media_build.git' \
	'git://github.com/ljalves/linux_media.git#branch=latest' \
	"http://www.tbsdtv.com/download/document/common/tbs-linux-drivers_$_tbsver.zip" \
	'tbs-dvb-drivers.install')

sha256sums=('SKIP'
            'SKIP'
            'fc8701fc2548a2413a385198c486bc79d25cc90e38f45f85c099ddcae4025143'
            'cc8be5c5704970d913c1ab1a825281827d93f49ac0e91042bd818edd434a187c')

pkgver() {

	_kernel=`uname -r | sed -r 's/-/_/g'`

	cd $srcdir/linux_media
 	_gitlinux_media=`git describe --always | sed 's|-|.|g'`
	cd $srcdir/media_build
 	_gitmedia_build=`git describe --always | sed 's|-|.|g'`

	echo "$_gitmedia_build"_"$_gitlinux_media"_"$_kernel"
}


prepare() {

	cd $srcdir/media_build
	sed -i 's/system ("make") == 0 or die "build failed";/#system ("make") == 0 or die "build failed";/' build
	./build

	cd $srcdir/media_build
	make dir DIR=../linux_media
	make distclean
	make
}

package() {

	mkdir -p $pkgdir/usr/lib/modules/`uname -r`/updates/v4l-tbs
	mkdir -p $pkgdir/usr/lib/firmware

	install -m0644 $srcdir/*tbs*.fw  $pkgdir/usr/lib/firmware
	find "$srcdir/media_build" -name '*.ko' -exec cp {} $pkgdir/usr/lib/modules/`uname -r`/updates/v4l-tbs \;

	echo ""
	msg "Compressing modules, this will take awhile..."
	echo ""
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

	chmod 755 -R $pkgdir/usr/lib/modules/`uname -r`/updates
}
