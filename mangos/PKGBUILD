# Maintainer: inteliboy <inteliboy@gmail.com>

pkgname=mangos
pkgver=20090915
pkgrel=1
pkgdesc="The MaNGOS project is a full featured World of Warcraft server suite, including servers for authentication, client updates, world content serving, and battlegrounds. Additional features include tools to build and develop game content"
arch=('x86_64' 'i686')
url="http://getmangos.com"
license=('GPL')
depends=('mysql' 'openssl' 'libmysqlclient' 'mysql-clients' 'zlib')
makedepends=('subversion' 'git' 'libtool' 'patch' 'gcc' 'make' 'autoconf' 'automake' 'bin86' 'bison' 'ed' 'fakeroot' 'flex' 'm4' 'pkgconfig' 'ace')
options=('!libtool')
source=()
md5sums=()
conflicts=(mangos-git)

_gitroot="git://github.com/mangos/mangos.git"
_gitname="mangos"

build() 
{
  cd "$srcdir"
# checkout or clone
  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
  else
    git clone $_gitroot
  fi
# cleanup
  rm -rf "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname"
# SD2 checkout
  [ -d src/bindings/ScriptDev2 ] || mkdir src/bindings/ScriptDev2 || return 1
  svn co https://scriptdev2.svn.sourceforge.net/svnroot/scriptdev2/ src/bindings/ScriptDev2 || return 1
# prepare build dir
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build" || return 1
  cd "$srcdir/$_gitname-build"
# SD2 patch
  git apply src/bindings/ScriptDev2/patches/MaNGOS-8273-ScriptDev2.patch || return 1 
# configure
  autoreconf --install --force || return 1
  aclocal || return 1
  autoheader || return 1
  autoconf || return 1
  automake --add-missing || return 1
  automake src/bindings/ScriptDev2/Makefile || return 1

  [ -d objdir ] || mkdir objdir
  cd objdir
  ../configure --prefix=/usr --sysconfdir=/usr/etc --enable-cli --disable-builtin-ace --enable-ra --datadir=/usr/share/mangos || return 1
# build  
  make || return 1
  make DESTDIR="$pkgdir/" install
# remove SD2 conf duplicate
  rm -rf "$pkgdir/usr/etc/scriptdev2.conf"
} 
