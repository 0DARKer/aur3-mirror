# Maintainer: graysky <graysky AT archlinux DOT us>
pkgname=mailsend-dev
_pkgname=mailsend
pkgver=1.17b9
pkgrel=3
pkgdesc='Simple command line program to send mail via SMTP protocol.'
arch=('i686' 'x86_64')
url='https://code.google.com/p/mailsend/'
license=('GPL')
depends=('openssl')
provides=('mailsend')
conflicts=('mailsend')
source=("https://mailsend.googlecode.com/files/$_pkgname$pkgver.tar.gz")
sha256sums=('8ae3974167b2cb8348230423e7c098a59b90daf99aa5a82213d867daf14bd658')

build() {
	cd "$_pkgname$pkgver"
	./configure --with-openssl=/usr
	make clean

	# the dev code seems to have issues with the jobserver so force 1 thread
	make -j1
}


package() {
	cd "$_pkgname$pkgver"
	# DESTDIR is not supported by upsteam so do it ourselves
	# make DESTDIR="$pkgdir/" install

	install -Dm755 $_pkgname	"$pkgdir/usr/bin/$_pkgname"
	install -Dm644 doc/$_pkgname.1	"$pkgdir/usr/share/man/man1/$_pkgname.1"
}
