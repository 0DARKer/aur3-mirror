# Maintainer: graysky <graysky AT archlinux DOT us>
pkgname=mailsend-dev
_pkgname=mailsend
pkgver=1.17b10
pkgrel=1
pkgdesc='Simple command line program to send mail via SMTP protocol.'
arch=('i686' 'x86_64')
url='https://code.google.com/p/mailsend/'
license=('GPL')
depends=('openssl')
provides=('mailsend')
conflicts=('mailsend')
source=("https://mailsend.googlecode.com/files/$_pkgname-$pkgver.tar.gz")
sha256sums=('063ff2a04053f69379956624e9180030fa3d17fa1fb7add83efb9bdc766640a7')

build() {
	cd "$_pkgname-$pkgver"
	./configure --with-openssl=/usr
	make
}


package() {
	cd "$_pkgname-$pkgver"
	# DESTDIR is not supported by upsteam so do it ourselves
	# make DESTDIR="$pkgdir/" install

	install -Dm755 $_pkgname	"$pkgdir/usr/bin/$_pkgname"
	install -Dm644 doc/$_pkgname.1	"$pkgdir/usr/share/man/man1/$_pkgname.1"
}
