# Maintainer: Danilo Kuehn <dk at nogo-software dot de>
# Git: https://github.com/nogo/archlinux-pkgbuild

_buildtype="Release"
_sync=ocsync
_syncver=0.80.0
_client=mirall

pkgname=owncloud-client-ngs
pkgver=1.3.0
pkgrel=3
pkgdesc="ownCloud client based on mirall"
arch=('i686' 'x86_64')
url="http://owncloud.org/"
license=('GPL2')
depends=('sqlite3' 'iniparser' 'neon' 'smbclient' 'libssh' 'qt4')
optdepends=('qtkeychain')
makedepends=('cmake')
provides=('mirall' 'owncloud-client')
conflicts=('mirall-git' 'ocsync' 'ocsync-ngs')
install=${pkgname}.install
options=(!strip)
backup=('etc/owncloud-client-ngs/sync-exclude.lst')
source=("http://download.owncloud.com/download/${_sync}-${_syncver}.tar.bz2"
        "http://download.owncloud.com/download/${_client}-${pkgver}.tar.bz2")
md5sums=('db46cdb4c710a607dfc062ed0a413b35'
         'ee2e7bafc714399f2027fefed853f968')

# Clean options array to strip pkg if release buildtype is chosen
if [[ ${_buildtype} == "Release" ]] || [[ ${_buildtype} == "release" ]]; then
  options=()
fi

build() {
  if [[ -e ${srcdir}/${_sync}-${_syncver}-build ]]; then rm -rf ${srcdir}/${_sync}-${_syncver}-build; fi
  mkdir ${srcdir}/${_sync}-${_syncver}-build
  cd ${srcdir}/${_sync}-${_syncver}-build

  cmake -DCMAKE_BUILD_TYPE=${_buildtype} \
        ../${_sync}-${_syncver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSCONF_INSTALL_DIR=/etc
  make


  if [[ -e ${srcdir}/${_client}-${pkgver}-build ]]; then rm -rf ${srcdir}/${_client}-${pkgver}-build; fi
  mkdir ${srcdir}/${_client}-${pkgver}-build
  cd ${srcdir}/${_client}-${pkgver}-build

  cmake -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        ../${_client}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCSYNC_BUILD_PATH=${srcdir}/${_sync}-${_syncver}-build \
        -DCSYNC_INCLUDE_PATH=${srcdir}/${_sync}-${_syncver}/src \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/${pkgname}
  make
}

package() {
  cd ${srcdir}/${_sync}-${_syncver}-build
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/${_client}-${pkgver}-build
  make DESTDIR=${pkgdir} install

}

