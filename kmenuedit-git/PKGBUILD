# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kmenuedit-git
pkgver=r714.deacfd5
pkgrel=1
pkgdesc='KDE menu editor'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('khotkeys-git')
makedepends=('extra-cmake-modules' 'git' 'kf5umbrella-git')
conflicts=('kmenuedit' 'kde-workspace<0')
provides=('kmenuedit')
source=('git://anongit.kde.org/kmenuedit.git')
md5sums=('SKIP')

pkgver() {
  cd kmenuedit
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kmenuedit/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # fix conflicts
  rm ${pkgdir}/opt/kf5/share/doc/HTML/en/doc/index.*
}
