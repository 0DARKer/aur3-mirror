# Maintainer:  Gustavo Alvarez <sl1pkn07@gmail.com>

_plug=eedi2
pkgname=vapoursynth-plugin-${_plug}-git
pkgver=r3.0.ga349c90
pkgrel=1
pkgdesc="Plugin for Vapoursynth: ${_plug} (GIT version)"
arch=('i686' 'x86_64')
url="http://forum.doom9.org/showthread.php?t=171136"
license=('GPL')
depends=('vapoursynth')
makedepends=('git')
provides=("vapoursynth-plugin-${_plug}")
conflicts=("vapoursynth-plugin-${_plug}")
source=("${_plug}::git+https://github.com/HomeOfVapourSynthEvolution/VapourSynth-EEDI2.git")
md5sums=('SKIP')
_gitname="${_plug}"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  rm -fr "${_gitname}/EEDI2"/{VapourSynth,VSHelper}.h
}

build() {
  cd "${_gitname}/EEDI2"
  echo "all:
	  g++ -shared ${CXXFLAGS} -std=c++11 -fPIC -o lib${_plug}.so EEDI2.cpp $(pkg-config --cflags vapoursynth)" > Makefile
  make
}

package(){
  cd "${_gitname}/EEDI2"
  install -Dm755 "lib${_plug}.so" "${pkgdir}/usr/lib/vapoursynth/lib${_plug}.so"
  install -Dm644 ../README.md "${pkgdir}/usr/share/doc/vapoursynth/plugins/${_plug}/README.md"
}
