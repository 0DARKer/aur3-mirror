pkgname=mingw-w64-libjpeg-turbo
pkgver=1.3.1
pkgrel=4
arch=(any)
pkgdesc="JPEG image codec with accelerated baseline compression and decompression (mingw-w64)"
license=("custom, GPL")
depends=(mingw-w64-crt)
makedepends=(nasm mingw-w64-gcc mingw-w64-cmake)
provides=(mingw-w64-libjpeg)
conflicts=(mingw-w64-libjpeg)
options=(staticlibs !strip !buildflags)
url="http://libjpeg-turbo.virtualgl.org"
source=("http://downloads.sourceforge.net/libjpeg-turbo/libjpeg-turbo-$pkgver.tar.gz"
"libjpeg-turbo-match-autoconf-behavior.patch"
"libjpeg-turbo-r1237.patch"
"jstdhuff.patch"
"overrun.patch")
sha1sums=('5fa19252e5ca992cfa40446a0210ceff55fbe468'
          '0cd7f6b2b21fdc5d363cb8556265eea6b9fbb9e4'
          '405282fa6fc1bf3d00d6906115e207816cbb4c21'
          'f459890257df430dc4e2d8bd3060173a1be7516e'
          '438aba2241b19857bab090c6f61bd3f249f27591')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	cd "$srcdir/libjpeg-turbo-$pkgver"
	patch -p1 -i ../libjpeg-turbo-match-autoconf-behavior.patch
	patch -p1 -i ../libjpeg-turbo-r1237.patch
	patch -p0 -i ../jstdhuff.patch # FS#42747
	patch -p1 -i ../overrun.patch # FS#42922
}

build() {
	cd "$srcdir/libjpeg-turbo-$pkgver"
	export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
	export CXXFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
	unset LDFLAGS
	for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir "build-${_arch}" && pushd "build-${_arch}"
    ${_arch}-cmake \
      -DCMAKE_BUILD_TYPE=Release \
      ..
    make
    popd
  done
}

package() {
	for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname#mingw-w64-}-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/doc"
  done
}
