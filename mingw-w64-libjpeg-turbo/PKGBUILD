# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-libjpeg-turbo
_pkgname=libjpeg-turbo
pkgver=1.2.0
pkgrel=2
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression (mingw-w64)"
arch=('any')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('mingw-w64-crt')
makedepends=('nasm' 'mingw-w64-gcc' 'cmake')
provides=('mingw-w64-libjpeg=8.0.2')
conflicts=('mingw-w64-libjpeg')
replaces=('mingw-w64-libjpeg')
options=('!libtool' '!strip' '!buildflags')
source=("http://sourceforge.net/projects/$_pkgname/files/$pkgver/$_pkgname-$pkgver.tar.gz"
        "Toolchain-i686-w64-mingw32.cmake"
        "Toolchain-x86_64-w64-mingw32.cmake")
md5sums=('5329fa80953938cb4f097afae55059e2'
         'da10ad00a161800e0b011a77c3efbf76'
         'adea9ccdeea1784c7cd30af195f2a8cb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd $srcdir/$_pkgname-$pkgver

  unset LDFLAGS

  for _arch in ${_architectures}; do
    mkdir build-${_arch} && pushd build-${_arch}
    PATH="/usr/${_arch}/bin:$PATH" cmake \
      -DCMAKE_VERBOSE_MAKEFILE=ON \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr/${_arch} \
      -DCMAKE_INSTALL_LIBDIR:PATH=/usr/${_arch}/lib \
      -DINCLUDE_INSTALL_DIR:PATH=/usr/${_arch}/include \
      -DLIB_INSTALL_DIR:PATH=/usr/${_arch}/lib \
      -DSYSCONF_INSTALL_DIR:PATH=/usr/${_arch}/etc \
      -DSHARE_INSTALL_PREFIX:PATH=/usr/${_arch}/share \
      -DBUILD_SHARED_LIBS:BOOL=ON \
      -DCMAKE_TOOLCHAIN_FILE=${srcdir}/Toolchain-${_arch}.cmake \
      ..
    make
    popd
  done
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make DESTDIR=$pkgdir/ install
    popd
  
    # Remove manpages
    rm -rf ${pkgdir}/usr/${_arch}/share/man

    # Remove dovs
    rm -rf ${pkgdir}/usr/${_arch}/doc

    # Remove programs (we need lib, not .exe files)
    rm -f ${pkgdir}/usr/${_arch}/bin/*.exe

    # Strip
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/bin/*.dll
    mv ${pkgdir}/usr/${_arch}/lib/*.dll.a ${pkgdir}/
    ${_arch}-strip --strip-unneeded ${pkgdir}/*.dll.a # Dynamic lib loaders
    ${_arch}-strip --strip-debug ${pkgdir}/usr/${_arch}/lib/*.a # Static libs
    mv ${pkgdir}/*.dll.a ${pkgdir}/usr/${_arch}/lib/
  done
}
