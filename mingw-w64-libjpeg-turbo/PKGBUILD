# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-libjpeg-turbo
_pkgname=libjpeg-turbo
pkgver=1.2.0
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression (mingw-w64)"
arch=('any')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('mingw-w64-crt')
makedepends=('nasm' 'mingw-w64-gcc')
provides=('mingw-w64-libjpeg=8.0.2')
conflicts=('mingw-w64-libjpeg')
replaces=('mingw-w64-libjpeg')
options=('!libtool' '!strip' '!buildflags')
source=(http://sourceforge.net/projects/$_pkgname/files/$pkgver/$_pkgname-$pkgver.tar.gz)
md5sums=('5329fa80953938cb4f097afae55059e2')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd $srcdir/$_pkgname-$pkgver

  # put documentation in a reasonable directory...
  sed -i "/docdir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in
  sed -i "/exampledir = /s#/doc#/doc/libjpeg-turbo#" Makefile.in

  unset LDFLAGS

  for _arch in ${_architectures}; do
    mkdir -p ${srcdir}/${_pkgname}-build-${_arch}
    cd ${srcdir}/${_pkgname}-build-${_arch}

    ${srcdir}/${_pkgname}-${pkgver}/configure --host=${_arch} --prefix=/usr/${_arch} --with-jpeg8 --mandir=/usr/share/man
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/${_pkgname}-build-${_arch}
    make DESTDIR=$pkgdir/ install
  
    cd $srcdir/$_pkgname-$pkgver

    # provide jpegint.h as it is required by various software
    install -m644 jpegint.h $pkgdir/usr/${_arch}/include/
  
    # do not distributre libturbojpeg as it is unversioned
    rm $pkgdir/usr/${_arch}/bin/libturbojpeg.dll
    rm $pkgdir/usr/${_arch}/lib/libturbojpeg.{dll.a,a}
    rm $pkgdir/usr/${_arch}/include/turbojpeg.h

    rm -r $pkgdir/usr/share
    rm -r $pkgdir/usr/${_arch}/share
  done
}
