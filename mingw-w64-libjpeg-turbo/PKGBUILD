# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-libjpeg-turbo
_pkgname=libjpeg-turbo
pkgver=1.2.90
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression (mingw-w64)"
arch=('any')
url="http://libjpeg-turbo.virtualgl.org/"
license=('GPL' 'custom')
depends=('mingw-w64-crt')
makedepends=('nasm' 'mingw-w64-gcc' 'mingw-w64-cmake')
provides=('mingw-w64-libjpeg=8.0.2')
conflicts=('mingw-w64-libjpeg')
replaces=('mingw-w64-libjpeg')
options=('!libtool' '!strip' '!buildflags')
source=("http://downloads.sourceforge.net/$_pkgname/$_pkgname-$pkgver.tar.gz")
md5sums=('ea53fc1e8a00c26e2f501385f887f1f6')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd $srcdir/$_pkgname-$pkgver

  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  unset LDFLAGS

  for _arch in ${_architectures}; do
    mkdir build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake ..
    make
    popd
  done
}

package() {
  cd $srcdir/$_pkgname-$pkgver

  for _arch in ${_architectures}; do
    pushd build-${_arch}
    make DESTDIR=$pkgdir/ install
    popd
  
    # Remove manpages
    rm -rf ${pkgdir}/usr/${_arch}/share/man

    # Remove dovs
    rm -rf ${pkgdir}/usr/${_arch}/doc

    # Remove programs (we need lib, not .exe files)
    rm -f ${pkgdir}/usr/${_arch}/bin/*.exe

    # Strip
    ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/bin/*.dll
    mv ${pkgdir}/usr/${_arch}/lib/*.dll.a ${pkgdir}/
    ${_arch}-strip --strip-unneeded ${pkgdir}/*.dll.a # Dynamic lib loaders
    ${_arch}-strip --strip-debug ${pkgdir}/usr/${_arch}/lib/*.a # Static libs
    mv ${pkgdir}/*.dll.a ${pkgdir}/usr/${_arch}/lib/
  done
}
