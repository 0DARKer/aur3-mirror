# Maintainer: Moritz Maxeiner <moritz@ucworks.org>
# Contributor: Sebastien <sebcactus@gmail.com>

pkgname=sptk
pkgver=3.5
pkgrel=1
pkgdesc="A suite of speech signal processing tools."
arch=('i686' 'x86_64')
url="http://sp-tk.sourceforge.net/"
license=('BSD')
depends=('glibc')
optdepends=('libx11: for XY-plotter' 'tcsh: for helper scripts')
source=(http://downloads.sourceforge.net/sp-tk/SPTK-$pkgver.tar.gz ${pkgname}.sh)
md5sums=('37d775c2ca1f0672e001de414615c263'
         '852880da62e38e47bf16ef7c304098f5')

build() {
  cd "${srcdir}/SPTK-$pkgver"
  ./configure --prefix=/opt/$pkgname
  make
}

package() {
  cd "${srcdir}/SPTK-$pkgver"
  make prefix="${pkgdir}/opt/$pkgname" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -D -m755 ../${pkgname}.sh "${pkgdir}/etc/profile.d/${pkgname}.sh"
}
