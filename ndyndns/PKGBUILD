# Maintainer: Jens Adam (byte/jra) <j_adam@web.de>

pkgname=ndyndns
pkgver=2.1
pkgrel=1
pkgdesc="Update client for the dynamic DNS services from DynDNS and Namecheap"
url="http://code.google.com/p/ndyndns/"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('curl')
install=${pkgname}.install
backup=('etc/ndyndns.conf')
source=("http://${pkgname}.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        'ldflags-asneeded.patch'
        'ndyndns.conf'
        'ndyndns')
md5sums=('0cdb4dcba5f8c1664f53884b3c2179e9'
         '87839c15c14838ca54e7894485cca698'
         'f0c69eb41db16a228ebf62c1561c89d6'
         '525e890ba29c929170c45fbe07d7628b')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  patch < ../ldflags-asneeded.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  install -d "${pkgdir}"/{etc/rc.d,usr/{sbin,share/man/man{1,5}}}
  install -m600 ../ndyndns.conf "${pkgdir}"/etc
  install ../ndyndns "${pkgdir}"/etc/rc.d
  install ndyndns "${pkgdir}"/usr/sbin
  gunzip ndyndns.{1,conf.5}.gz
  install -m644 ndyndns.1 "${pkgdir}"/usr/share/man/man1
  install -m644 ndyndns.conf.5 "${pkgdir}"/usr/share/man/man5
  install -d "${pkgdir}"/{usr/share/doc,var/lib}/ndyndns
  install -m644 CHANGES DESIGN README "${pkgdir}"/usr/share/doc/ndyndns
}
