# Maintainer: thebastl
# Contributor: Det
# Contributor: Achilleas Pipinellis <axilleas@archlinux.info>
# Contributor: speed145a <speed145a(at)hotmail(dot)com>
# Contributor: Schnouki

pkgname=firefox-beta-bin-de
#_basever=33.0
#_rc=1
pkgver=33.0b3
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org - Beta"
arch=('i686' 'x86_64')
url="https://www.mozilla.org/de/firefox/"
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'libxt' 'mime-types' 'dbus-glib' 'alsa-lib' 'desktop-file-utils' 'hicolor-icon-theme'
         'libvpx' 'icu' 'libevent' 'nss' 'sqlite')
optdepends=('networkmanager: Location detection via available WiFi networks'
            'gstreamer0.10-base-plugins: vorbis decoding, ogg demuxing'
            'gstreamer0.10-good-plugins: webm and mp4 demuxing'
            'gstreamer0.10-bad-plugins: aac, vp8 and opus decoding'
            'gstreamer0.10-ugly-plugins: h.264 and mp3 decoding'
            'gstreamer0.10-ffmpeg: more decoders'
            'libpulse: PulseAudio driver')
_arch=x86_64  # Workaround for mkaurball: https://bugs.archlinux.org/task/40711
[[ "$CARCH" = "i686" ]] && _arch=i686
install=$pkgname.install
#source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/candidates/$_basever-candidates/build$_rc/linux-$_arch/de/firefox-$_basever.tar.bz2"
source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/linux-$_arch/de/firefox-$pkgver.tar.bz2"
        "$pkgname.desktop"
        "$pkgname-safe.desktop")
sha1sums=('584ed009d5cb9f39b30d587a672bcf8031e71f73'
          '5874219e4ee5625a73b7cd199bb6f019085718b7'
          'e5388d596454b13ba8d61a73b4a994ad473b86be')
          
package() {
  # Create directories
  mkdir -p "$pkgdir"/{usr/{bin,share/{applications,icons/hicolor/128x128/apps}},opt}

  # Install
  cp -r firefox/ "$pkgdir"/opt/$pkgname-$pkgver

  # /usr/bin link
  ln -s /opt/$pkgname-$pkgver/firefox "$pkgdir"/usr/bin/$pkgname

  # Desktops
  install -m644 *.desktop "$pkgdir"/usr/share/applications/

  # Icons
  for i in 16 32 48; do
    install -d "$pkgdir"/usr/share/icons/hicolor/${i}x$i/apps/
    ln -s /opt/$pkgname-$pkgver/browser/chrome/icons/default/default$i.png \
          "$pkgdir"/usr/share/icons/hicolor/${i}x$i/apps/$pkgname.png
  done
  ln -s /opt/$pkgname-$pkgver/browser/icons/mozicon128.png \
        "$pkgdir"/usr/share/icons/hicolor/128x128/apps/$pkgname.png
}
