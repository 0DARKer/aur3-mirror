# Maintainer: Taylor Venable <taylor@metasyntax.net>

pkgname='stklos-hg'
pkgver=20111003
pkgrel=1
pkgdesc='A free Scheme system compliant with the languages features defined in R5RS'
arch=('i686' 'x86_64')
url='http://www.stklos.net/index.html'
license=('GPL')
depends=('gc' 'gmp' 'libffi' 'pcre')
makedepends=('mercurial')
conflicts=('stklos')
provides=('stklos')

_hgroot='http://www.stklos.net/hg/hgweb.cgi'
_hgname='stklos'

build() {
  msg 'Connecting to Mercurial server....'

  if [ -d "$_hgname" ] ; then
    cd "$_hgname"
    hg pull -u
    msg 'The local files are updated.'
  else
    hg clone "$_hgroot" "$_hgname"
  fi

  msg 'Mercurial checkout done or server timeout'
  msg 'Starting make...'

  cd "$srcdir"

  rm -rf "${_hgname}-build"
  hg clone "$_hgname" "${_hgname}-build"

  cd "${_hgname}-build"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_hgname}-build"
  make DESTDIR="$pkgdir" install
}
