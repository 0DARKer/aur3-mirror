# Maintainer: TDY <tdy@archlinux.info>

pkgname=autopoweroff
pkgver=2.9.1
pkgrel=1
pkgdesc="A daemon to shutdown the computer at specifc times under specific conditions"
arch=('i686' 'x86_64')
url="http://autopoweroff.sourceforge.net/"
license=('GPL')
depends=('python2')
optdepends=('pygtk: GUI configurator')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname.service)
sha256sums=('6bd2f797b1c8c3103d326ba2c9bd7ad9bc016b7c8356dfbff15c1d6cf22d87f4'
            '8b7c6b1e18800f20bfd253e8e9faad08d41e3256aedb1d30b018370dca65fbce')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '2739s|=.*|=/var/run|; 2740s|=.*|=/var/log|; 2744s|/init|/rc|' configure
  sed -i '/postinstall$/d' Makefile.in
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  ln -sf ../pixmaps/$pkgname.png "$pkgdir/usr/share/glade/$pkgname.png"
  install -Dm644 ../$pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 et:
