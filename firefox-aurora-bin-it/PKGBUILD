# Maintainer: Simone Sclavi 'Ito' <darkhado@gmail.com>
#
# based on the firefox-aurora PKGBUILD
pkgname=firefox-aurora-bin-it
_mypkgn=firefox-aurora
pkgdesc='Aurora channel is where users can test the latest features and innovations in Firefox web browser'
url='http://www.mozilla.com/firefox/channel/'
pkgver=5.0a2
pkgrel=1
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
source=('firefox-aurora.desktop' 'firefox-aurora-safe.desktop'
        http://ftp.mozilla.org/pub/mozilla.org/firefox/nightly/latest-mozilla-aurora-l10n/firefox-${pkgver}.it.linux-${CARCH}.tar.bz2)
sha1sums=('879ef74d3d1d558851c95a8fd2aef2a496f1a0d8'
          '4cb4e97c195d70f1e1b801829b03f5636289797f'
          '5de02e8c6e7a0d613e2e632b419d6eea65e259b7')
[[ "$CARCH" == "x86_64" ]] && sha1sums[2]="0d1ac2f5c444103b4ec1d4e810c2f70822524cb4"         
depends=('desktop-file-utils' 'xulrunner' 'gnome-vfs')
conflicts=('firefox-aurora')
install=firefox.install

package() {

  mkdir -p "${pkgdir}"/{usr/{bin,share/{applications,pixmaps}},opt}
  cp -r firefox "${pkgdir}/opt/${_mypkgn}"

  ln -s /opt/${_mypkgn}/firefox "${pkgdir}/usr/bin/firefox-aurora"
  install -m644 "${srcdir}"/{firefox-aurora.desktop,firefox-aurora-safe.desktop} "${pkgdir}/usr/share/applications/"
  install -m644 "${srcdir}/firefox/icons/mozicon128.png" "${pkgdir}/usr/share/pixmaps/${_mypkgn}-icon.png"
}
