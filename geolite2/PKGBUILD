# Maintainer: felix (fstirlitz)
pkgbase=geolite2
pkgname=(
	geolite2-country
	geolite2-city
)
epoch=
pkgver=20140807
pkgrel=1
pkgdesc="MaxMind GeoLite2 GeoIP database - split package"
license=('CCPL:cc-by-sa-3.0')
arch=(any)
url="http://dev.maxmind.com/geoip/geoip2/downloadable/"
depends=()
makedepends=()
checkdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
source=()
sha512sums=()
noextract=()

_wanted() {
	for _item in "${pkgname[@]}"; do
		[[ "$_item" = "$1" ]] && return 0
	done
	return 1
}

if _wanted geolite2-country; then
	source+=("https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country.mmdb.gz")
	sha512sums+=('2b08f3fd9bf819793077a2224c935fd2089c36cffa77cabd6169adcbf6d16717089ddb256daf29150a4397093c6bba04211938097997ed127b0624317c276a52')
fi

if _wanted geolite2-city; then
	source+=("https://geolite.maxmind.com/download/geoip/database/GeoLite2-City.mmdb.gz")
	sha512sums+=('2c9c490104e989c0fcb7a90f66b8537756e3bc47ed55e0af8fd461559413175ff469c0ea51ce86c1a16cc091c9dbe8c2b4356d6ee4e219f92d2502a1979cefc7')
fi

build() {
	: # nothing needed
}

check() {
	cd "$srcdir"
	if _wanted geolite2-country; then
		msg2 "Validating the GeoLite2 Country database"
		echo "$(curl https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country.md5) GeoLite2-Country.mmdb" | md5sum -c
	fi

	if _wanted geolite2-city; then
		msg2 "Validating the GeoLite2 City database"
		echo "$(curl https://geolite.maxmind.com/download/geoip/database/GeoLite2-City.md5) GeoLite2-City.mmdb" | md5sum -c
	fi
}

package_geolite2-country() {
	pkgdesc="MaxMindDB GeoLite2 Country GeoIP database"

	cd "$srcdir"
	install -D -m0644 GeoLite2-Country.mmdb \
		"$pkgdir/usr/share/GeoIP/GeoLite2-Country.mmdb"
}

package_geolite2-city() {
	pkgdesc="MaxMindDB GeoLite2 City GeoIP database"

	cd "$srcdir"
	install -D -m0644 GeoLite2-City.mmdb \
		"$pkgdir/usr/share/GeoIP/GeoLite2-City.mmdb"
}
