# Maintainer: RibShark <ribshark at outlook dot com>

pkgname=multimc5
pkgver=0.1.1
pkgrel=1
pkgdesc="Minecraft launcher with ability to manage multiple instances (stable version)"
arch=('i686' 'x86_64')
url="http://multimc.org/"
license=('Apache')
depends=('qt5-base' 'qt5-x11extras' 'qt5-svg' 'java-runtime')
makedepends=('git' 'cmake' 'qt5-tools' 'java-environment')
source=("$pkgname"::'git://github.com/MultiMC/MultiMC5.git#tag=0.1.1'
        'multimc5.desktop'
        'multimc5.sh')
md5sums=('SKIP'
         '79b5d284ae801b60276518d9ec92281c'
         'a679adad5fd4f5deeb9ae5423284e49d')

build() {
  cd $srcdir/$pkgname
  
  sed -i '/add_subdirectory(mmc_updater)/d' CMakeLists.txt
  mkdir build
  cd build
  cmake $srcdir/$pkgname
  make
}

package() {
  cd $srcdir/$pkgname/build
  install -D MultiMC $pkgdir/usr/lib/multimc5/MultiMC5
  install -D $srcdir/multimc5.sh $pkgdir/usr/bin/multimc5
  install -D $srcdir/$pkgname/resources/icons/multimc.svg $pkgdir/usr/share/pixmaps/multimc5.svg
  install -D $srcdir/multimc5.desktop $pkgdir/usr/share/applications/multimc5.desktop
  install -D depends/launcher/MultiMCLauncher.jar $pkgdir/usr/lib/multimc5/jars/MultiMCLauncher.jar
  install -D depends/javacheck/JavaCheck.jar $pkgdir/usr/lib/multimc5/jars/JavaCheck.jar
}
