# Maintainer: Michael Mansell <michael.mansell@gmail.com>
# Contributor: Joe Davison <joedavison.davison@gmail.com>

pkgname=wxlauncher
_pkgtar=wxlauncher
pkgver=0.9.4
pkgrel=5

source=("http://wxlauncher.googlecode.com/files/${_pkgtar}-$pkgver.tar.gz"
	"wxlauncher_homedir.patch"
	"wxlauncher.png"
	"wxlauncher.desktop")
sha256sums=('6afab46e3bea5e428fce8318cc82ac80cb9c5f42e51867a26a1e3672846603a5'
            '6de402c653f63e1515377977cfdde0e761d19ed43a1e390ac27a92ac714dd0c9'
            'e3bf9cbe7c61378026331a21313fccd891b051e949448715625d1cfd3ae72576'
            '8de688171c12fdf273ece4f33177fd773395204c81cf212f8c4293f0ddd16582')

pkgdesc="wxLauncher is a cross-platform launcher for the FreeSpace 2 Open engine"
url="http://www.hard-light.net/forums/index.php?topic=67950.0"
arch=(i686 x86_64)
license=(GPL2)
# change below if you want to modify the resources intall directory.  The default installs it along side fs2_open resources.
resourcesdir=/opt/fs2_open/wxlauncher

depends=('wxpython2.8' 'python2' 'python2-markdown' 'openal')
optdepends=('fs2_open')
makedepends=(cmake)

prepare() {
	# prepare environment
	cd $srcdir/${_pkgtar}-$pkgver

	# needed to fix linking bug mentioned in Issue 93 (http://code.google.com/p/wxlauncher/issues/detail?id=93)
	# Thanks to Samsagax for pointing me in the direction of this patch
	patch -Np3 < $srcdir/wxlauncher_homedir.patch
}


build() {
	cd $srcdir/${_pkgtar}-$pkgver

		if [ ! -d build ]; then
			mkdir build
		fi

	cd build/
	
	# commence build		

	#cmake -DPYTHON_EXECUTABLE=python2 -DUSE_OPENAL=1 -DRESOURCES_PATH=$resourcesdir -DCMAKE_INSTALL_PREFIX=/usr ../
	# Make sure to build against wxpython2.8
	cmake -DPYTHON_EXECUTABLE=python2 -DUSE_OPENAL=1 -DRESOURCES_PATH=$resourcesdir -DCMAKE_INSTALL_PREFIX=/usr ../ -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8' -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8'
	make
		

}

package() {
	cd "$srcdir/$pkgname-$pkgver/build"
	make DESTDIR="$pkgdir/" install
	# need to install the .desktop file and icon
	cd "$srcdir"
	install -Dm644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
	install -Dm644 "$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
