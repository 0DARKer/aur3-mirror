# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Vladimir Ermakov <vooon341@gmail.com>

pkgname=gazebo
pkgver=1.0.1
pkgrel=1
pkgdesc="A multi-robot simulator for outdoor environments"
arch=('i686' 'x86_64')
url="http://gazebosim.org/"
license=('Apache')
depends=('boost' 'cegui' 'intel-tbb' 'libltdl' 'libxml2' 'ogre' 'protobuf' 'qt' 'tinyxml')
makedepends=('cmake')
install="${pkgname}.install"
source=("http://gazebosim.org/assets/distributions/${pkgname}-${pkgver}.tar.bz2"
        "cmake.patch")
md5sums=('7caa4bff87959184e09f19660619e542'
         'a346c6d54b72ed7f17267518a9db11ed')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply a patch to fix cmake issues
  patch -Np1 -i ../cmake.patch

  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
