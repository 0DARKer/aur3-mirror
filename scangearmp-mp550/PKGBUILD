pkgname=scangearmp-mp550
true && pkgname=('scangearmp-common' 'scangearmp-mp550')
_pkgname=scangearmp
pkgver=1.40
pkgrel=3
_pkgver=1.40-1
url="http://support-sg.canon-asia.com/contents/SG/EN/0100237102.html"
arch=('i686')
license=('custom')
makedepends=('rpmextract')
source=("http://gdlp01.c-wss.com/gds/1/0100002371/01/${_pkgname}-mp550series-${_pkgver}-i386-rpm.tar.gz")
md5sums=('3bff6b1bc7cfae467b2806486c70db10')

build() {
  mkdir ${srcdir}/${_pkgname}-common
  mkdir ${srcdir}/${_pkgname}-mp550
  cd ${srcdir}
  tar -xzf ${_pkgname}-mp550series-${_pkgver}-i386-rpm.tar.gz
  cd ${_pkgname}-mp550series-${_pkgver}-i386-rpm/packages
  mv ${_pkgname}-common-${_pkgver}.i386.rpm ${srcdir}/${_pkgname}-common
  mv ${_pkgname}-mp550series-${_pkgver}.i386.rpm ${srcdir}/${_pkgname}-mp550
  cd ${srcdir}/${_pkgname}-common
  rpmextract.sh ${_pkgname}-common-${_pkgver}.i386.rpm
  cd ${srcdir}/${_pkgname}-mp550
  rpmextract.sh ${_pkgname}-mp550series-${_pkgver}.i386.rpm
}

package_scangearmp-common() {
  pkgdesc="Canon Scanner Driver (Common package)"
  depends=('sane' 'gimp' 'libpng' 'gtk2' 'libusb' 'libpng12')
  cd ${srcdir}/${_pkgname}-common
  install -Dm 644 etc/udev/rules.d/80-canon_mfp.rules ${pkgdir}/etc/udev/80-canon_mfp.rules
  install -d ${pkgdir}/usr/lib
  install -d ${pkgdir}/usr/lib/bjlib
  install -Dm 755 -t ${pkgdir}/usr/lib usr/lib/*.*
  chmod 644 ${pkgdir}/usr/lib/libsane-canon_mfp.a
  install -Dm 644 -t ${pkgdir}/usr/lib/bjlib usr/lib//bjlib/*
  install -Dm 755 usr/local/bin/${_pkgname} ${pkgdir}/usr/bin/${_pkgname}
  install -d ${pkgdir}/usr/share/licenses/${_pkgname}
  install -Dm 644 -t ${pkgdir}/usr/share/licenses/${_pkgname} usr/share/doc/${_pkgname}-common-${pkgver}/*
  install -d ${pkgdir}/usr/share/${_pkgname}/pixmaps
  install -Dm 644 -t ${pkgdir}/usr/share/${_pkgname}/pixmaps usr/local/share/${_pkgname}/pixmaps/*
  for locale in $(ls usr/local/share/locale); do
    install -Dm 644 usr/local/share/locale/${locale}/LC_MESSAGES/${_pkgname}.mo ${pkgdir}/usr/share/locale/${locale}/LC_MESSAGES/${_pkgname}.mo
  done
}

package_scangearmp-mp550() {
  pkgdesc="Canon Scanner Driver (For Multifunction MP550 series)"
    depends=('gcc-libs' 'scangearmp-common=1.40')
  cd ${srcdir}/${_pkgname}-mp550
  install -d ${pkgdir}/usr/lib
  install -d ${pkgdir}/usr/lib/bjlib
  install -Dm 755 -t ${pkgdir}/usr/lib usr/lib/*.*
  install -Dm 755 -t ${pkgdir}/usr/lib/bjlib usr/lib//bjlib/*
}

pkgdesc="Canon Scanner Driver (For Multifunction MP550 series)"
depends=('gimp' 'gtk2' 'libusb' 'pango' 'libpng12' 'zlib')