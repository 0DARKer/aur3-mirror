# Maintainer: Jacob Courtneay <jacob@sporkexec.com>
pkgname=sscrotwm-git
pkgver=20120228
pkgrel=2
pkgdesc="Shriveled scrotwm: A minimalist fork of scrotwm/spectrwm."
arch=('i686' 'x86_64')
url="http://github.com/sporkexec/sscrotwm"
license=('custom:ISC')
depends=('libxrandr')
backup=(etc/sscrotwm.conf)
conflicts=('spectrwm')
makedepends=('git')
optdepends=('scrot: screenshots' 'xlockmore: screenlocking' 'dmenu: program launching')
source=()
md5sums=()

_gitroot="git://github.com/sporkexec/sscrotwm.git"
_gitname="sscrotwm"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."

  if [ -d ${srcdir}/${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone --depth 1 ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd "$srcdir/$_gitname"

  # it is like a patch, only less fragile
  #sed -i 's|setenv(\"LD_PRELOAD\", SWM_LIB, 1);|//setenv(\"LD_PRELOAD\", SWM_LIB, 1);|' "$srcdir/sscrotwm/sscrotwm.c"
  sed -i 's|\"/usr/local/lib/libswmhack.so\"|\"libswmhack.so\"|' "$srcdir/sscrotwm/sscrotwm.c"

  cd linux
  ln -s ../version.h version.h
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$_gitname/linux"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  install -Dm644 sscrotwm.desktop "$pkgdir/usr/share/xsessions/sscrotwm.desktop"
  cd ..
  install -Dm644 sscrotwm.conf "$pkgdir/etc/sscrotwm.conf"
  install -Dm755 screenshot.sh "$pkgdir/usr/share/sscrotwm/screenshot.sh"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  unlink "$pkgdir/usr/bin/scrotwm"

  # These are the conflicts. There should be a way to avoid these kinds of things.
  ln -s /usr/lib/libswmhack.so.0.0 "$pkgdir/usr/lib/libswmhack.so.0"
  ln -s /usr/lib/libswmhack.so.0.0 "$pkgdir/usr/lib/libswmhack.so"

}
