# Maintainer: Vivien Maisonneuve <v dot maisonneuve at gmail dot com>
# Category: science
pkgname='barvinok'
pkgver=0.36.r50.g6ec31f4
pkgrel=2
pkgdesc='A library for counting the number of integer points in parametric and non-parametric polytopes'
arch=('i686' 'x86_64')
url='http://freecode.com/projects/barvinok'
license=('GPL2')
provides=("$pkgname=$pkgver")
depends=('ntl' 'isl' 'polylib')
source=("git://repo.or.cz/$pkgname.git#commit=6ec31f49a95d98c866e8f9cea0eccbd7ef8c622f")
    # last version to work with isl 0.2.12
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$pkgname"
    git describe --long | sed -r 's/^barvinok-//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/$pkgname"
    sh autogen.sh
    ./configure --prefix=/usr --with-isl=system --with-polylib=system
    make
}

check() {
    cd "$srcdir/$pkgname"
    make check
}

package() {
    cd "$srcdir/$pkgname"
    make prefix="$pkgdir"/usr install
}
