# vim:set ts=2 sw=2 et ft=sh tw=100: expandtab
# Maintainer: Piotr Rogoża <rogoza dot piotr at gmail dot com>
# Contributor: Piotr Rogoża <rogoza dot piotr at gmail dot com>

pkgname=perl-wx
pkgver=0.9922
_author=M/MD/MDOOTSON
_perlmod=Wx
pkgrel=2
pkgdesc="Wx - interface to the wxWidgets GUI toolkit"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Wx"
license=('GPL' 'PerlArtistic')
depends=(
perl
wxgtk
)
makedepends=(
wxgtk
perl-alien-wxwidgets
perl-extutils-xspp
#ExtUtils::MakeMaker    => perl
#ExtUtils::ParseXS      => perl
#File::Spec::Functions  => perl
#Test::More             => perl
#Test::Harness          => perl
)
#{{{ provides
provides=(
perl-wx-aui
perl-wx-app
perl-wx-artprovider
perl-wx-calendar
perl-wx-dnd
perl-wx-dataview
perl-wx-datetime
perl-wx-docview
perl-wx-dropsource
perl-wx-event
perl-wx-fs
perl-wx-grid
perl-wx-help
perl-wx-html
perl-wx-locale
perl-wx-mdi
perl-wx-media
perl-wx-menu
perl-wx-mini
perl-wx-overload-driver
perl-wx-overload-handle
perl-wx-perl-carp
perl-wx-perl-splashfast
perl-wx-perl-textvalidator
perl-wx-print
perl-wx-radiobox
perl-wx-richtext
perl-wx-stc
perl-wx-socket
perl-wx-timer
perl-wx-xrc
perl-wx-build-makemaker
perl-wx-build-makemaker-any_os
perl-wx-build-makemaker-any_wx_config
perl-wx-build-makemaker-core
perl-wx-build-makemaker-hacks
perl-wx-build-makemaker-macosx_gcc
perl-wx-build-makemaker-win32
perl-wx-build-makemaker-win32_msvc
perl-wx-build-makemaker-win32_mingw
perl-wx-build-options
perl-wx-build-utils
perl-build-wx-xsp-enum
perl-build-wx-xsp-event
perl-build-wx-xsp-overload
perl-build-wx-xsp-virtual
) #}}}
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/$_author/$_perlmod-$pkgver.tar.gz")

build(){
  cd "$srcdir"/$_perlmod-$pkgver
  
  # Setting these env variables overwrites any command-line-options we don't want...
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  # If using Makefile.PL
  if [ -r Makefile.PL ]; then
    /usr/bin/perl Makefile.PL
    make
  # If using Build.PL
  elif [ -r Build.PL ]; then
    /usr/bin/perl Build.PL
    perl Build
  fi
}
check(){
  cd "$srcdir"/$_perlmod-$pkgver

  # If using Makefile.PL
  if [ -r Makefile.PL ]; then
    make test
  # If using Build.PL
  elif [ -r Build.PL ]; then
    perl Build test
  fi
}
package(){
  cd "$srcdir"/$_perlmod-$pkgver
  
  # If using Makefile.PL
  if [ -r Makefile.PL ]; then
    make install
  # If using Build.PL
  elif [ -r Build.PL ]; then
    perl Build install
  fi

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
md5sums=('672a97d618ae64814bfd3572187ab929')
