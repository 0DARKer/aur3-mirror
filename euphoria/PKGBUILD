# Contributor: Jeremy Cowgar <cowgar.com: jeremy>
# Maintainer:  Jesse Jaara   <gmail.com:  jesse.jaara>

pkgname=euphoria
pkgver=4.0.4
pkgrel=1
pkgdesc="Fast interpreted or compiled general purpose programming language"
url="http://openeuphoria.org"
license=('custom:"OpenEuphoria License"')
source=(http://downloads.sourceforge.net/rapideuphoria/Euphoria/4.0.4/Linux/${pkgname}-LINUX-${pkgver}.tar.gz)
arch=('i686' 'x86_64')

if [ "$CARCH" = "x86_64" ]; then
  depends=('lib32-glibc')
  makedepends=('gcc-multilib')
fi

build() {
  ### We have a dirty tarball at the moment :(
  cd "${srcdir}/${pkgname}-LINUX-${pkgver}/euphoria-LINUX-d2d529ba243f/source"

  ./configure --prefix=/usr
  make
  make tools
}

package() {
  cd "${srcdir}/${pkgname}-LINUX-${pkgver}/euphoria-LINUX-d2d529ba243f/source"

  ## The installer cretes the bin dir too late :'( ##
  mkdir -p "${pkgdir}/usr/bin"
  # Base and Tool install
  make DESTDIR="${pkgdir}" install install-tools

  # Install our license file per ArchLinux spec
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "${srcdir}/${pkgname}-LINUX-${pkgver}/euphoria-LINUX-d2d529ba243f/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Make an initial eu.cfg
  cat > eu.cfg <<EOF
[all]
-i /usr/share/${pkgname}/include
EOF
  mkdir -p "${pkgdir}/etc/${pkgname}"
  install -m644 eu.cfg "${pkgdir}/etc/${pkgname}"
}

md5sums=('d99b7439e14c6f49e2e99dcef1e8531f')
