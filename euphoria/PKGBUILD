# Contributor: Jeremy Cowgar <jeremy@cowgar.com>
pkgname=euphoria
pkgver=4.0.2
pkgrel=2
pkgdesc="Fast interpreted or compiled general purpose programming language"
url="http://openeuphoria.org"
license=('custom:"OpenEuphoria License"')
source=(http://sourceforge.net/projects/rapideuphoria/files/Euphoria/$pkgver/Linux/${pkgname}-LINUX-${pkgver}.tar.gz
	be_callc.patch)
arch=('i686' 'x86_64')

if [ "$CARCH" = "x86_64" ]; then
  depends=('lib32-glibc')
  makedepends=('gcc-multilib')
fi

build() {
  cd "${srcdir}/${pkgname}-LINUX-${pkgver}/source"

  patch -p2 -i ../../be_callc.patch

  ./configure --prefix=/usr
  make
  make tools
}

package() {
  cd "${srcdir}/${pkgname}-LINUX-${pkgver}/source"

  ## The installer cretes the bin dir too late :'( ##
  mkdir -p "${pkgdir}/usr/bin"
  # Base and Tool install
  make DESTDIR="${pkgdir}" install install-tools

  # Install our license file per ArchLinux spec
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "${srcdir}/${pkgname}-LINUX-${pkgver}/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Make an initial eu.cfg
  cat > eu.cfg <<EOF
[all]
-i /usr/share/${pkgname}/include
EOF
  mkdir -p "${pkgdir}/etc/${pkgname}"
  install -m644 eu.cfg "${pkgdir}/etc/${pkgname}"
}

md5sums=('cc21ee0d002b4bae60194c257d3a0769'
         'f1a45c31ed0bc201b9e19c301d97cd4a')
