pkgname=atvclient-git
pkgver=20101209
pkgrel=1
pkgdesc="Background application implementing the functionality of the AppleTV OS HID driver"
arch=('i686' 'x86_64')
url="http://wiki.github.com/Evinyatar/atvclient/"
license=('GPL2')
depends=('libusb-compat')
makedepends=('git')
source=(atvclient.rc
	atvclient.conf)
changelog=$pkgname.changelog
install=$pkgname.install
_gitroot='git://github.com/Evinyatar/atvclient.git'
_gitname='atvclient'

build() {

    cd ${srcdir}

    msg "Connecting to GIT server...."

    if [ -d ${srcdir}/${_gitname} ] ; then
        cd ${_gitname} && git pull origin
        msg "The local files are updated."
    else
        git clone ${_gitroot}
        cd ${_gitname}
    fi
    
    autoreconf
    ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
    make

}

package() {

    cd ${srcdir}/${_gitname}
    make install DESTDIR=${pkgdir}
    
    install -d -m 755 ${pkgdir}/etc/{rc.d,conf.d}/
    install -m 755 ${srcdir}/${source[0]} ${pkgdir}/etc/rc.d/atvclientd
    install -m 644 ${srcdir}/${source[1]} ${pkgdir}/etc/conf.d/atvclientd

    install -d -m 755 ${pkgdir}/usr/share/doc/${pkgname}/
    install -m 644 {AUTHORS,NEWS,README,COPYING,ChangeLog} ${pkgdir}/usr/share/doc/${pkgname}/

}

sha1sums=('9a26ad09b9394b36b449f2bb0cdf7f3501f1cfae'
          '19f073c1dc4b96766148b59311ce0eb214eaa520')
