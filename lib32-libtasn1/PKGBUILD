_pkgbasename=libtasn1
pkgname=lib32-$_pkgbasename
pkgver=2.9
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS (32 bit)"
arch=(i686 x86_64)
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('lib32-glibc')
makedepends=('gcc-multilib')
options=('!libtool')
source=(ftp://ftp.gnu.org/gnu/libtasn1/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('f4f4035b84550100ffeb8ad4b261dea9')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32'
  make
}

check() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/"{bin,include,share}
}
