    # Contributor: Neverth <t-r-a-y@mbnet.fi>
    # Contributor: fettouhi <A.Fettouhi@gmail.com>
    # Contributor: Marvn
     
    pkgname=moodbar
    pkgver=0.1.2
    pkgrel=8
    pkgdesc="moodbar binaries and script for Amarok"
    arch=(i686 x86_64)
    url="http://pwsp.net/~qbob/"
    license=('GPL')
    depends=('gstreamer0.10' 'fftw' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-good-plugins' 'gstreamer0.10-ugly-plugins' 'gstreamer0.10-ffmpeg' 'gstreamer0.10-base-plugins')
    makedepends=('pkgconfig' 'make')
    source=("http://pwsp.net/~qbob/${pkgname}-${pkgver}.tar.gz"
            "analyzer.patch")
    md5sums=('28c8eb65e83b30f71b84be4fab949360'
             '1a6a5a7e22c4c9381779f99a52cd2246')
     
    build() {
      cd ${srcdir}/${pkgname}-${pkgver}
     
      patch -p1 -i ../analyzer.patch
      ./configure --prefix=`pkg-config --variable=prefix gstreamer-0.10`
     
      make
    }
     
    package() {
      cd ${srcdir}/${pkgname}-${pkgver}
     
      make DESTDIR="$pkgdir" install
    }
