#pkgbase="touchegg-svn"
#pkgname=('touchegg-svn' 'touchegg-gui-svn')
pkgname=touchegg-svn
true && pkgname=('touchegg-svn' 'touchegg-gui-svn') #Darn you, AUR!
pkgver=137
pkgrel=7
arch=('i686' 'x86_64')
url="https://code.google.com/p/touchegg/"
license=('GPL')
depends=('qt' 'utouch-geis')
makedepends=('subversion')
source=()
sha512sums=()

_svntrunk=http://touchegg.googlecode.com/svn
_svnmod=touchegg-svn

build() {
  cd "${srcdir}"
  msg "Connecting to SVN server..."

  if [[ -d "${_svnmod}/.svn" ]]; then
    (cd "${_svnmod}" && svn up -r "${pkgver}")
  else
    svn co "${_svntrunk}" --config-dir ./ -r "${pkgver}" "${_svnmod}"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  cd "${srcdir}/${_svnmod}/${pkgname[0]%-*}"
  qmake
  make

  cd "${srcdir}/${_svnmod}/${pkgname[1]%-*}"
  qmake
  make
}

package_touchegg-svn() {
  pkgdesc="Multitouch gesture recognizer"
  provides=('touchegg')
  conflicts=('touchegg')

  cd "${srcdir}/${_svnmod}/${pkgname[0]%-*}"
  make INSTALL_ROOT="${pkgdir}" DESTDIR="${pkgdir}" install
}

package_touchegg-gui-svn() {
  pkgdesc="Multitouch gesture recognizer (GUI)"
  provides=('touchegg-gui')
  conflicts=('touchegg-gui')

  cd "${srcdir}/${_svnmod}/${pkgname[1]%-*}"
  make INSTALL_ROOT="${pkgdir}" DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
