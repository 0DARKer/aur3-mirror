# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=touchegg-svn
pkgver=187
pkgrel=1
pkgdesc="Multitouch gesture recognizer"
arch=('i686' 'x86_64')
url="https://code.google.com/p/touchegg/"
license=('GPL')
depends=('qt4' 'geis')
makedepends=('subversion')
provides=('touchegg')
conflicts=('touchegg')
source=('touchegg::svn+http://touchegg.googlecode.com/svn')
sha512sums=('SKIP')

pkgver() {
  cd "$SRCDEST/touchegg"
  svnversion | tr -d [A-z]
}

build() {
  cd "${srcdir}/touchegg/touchegg"
  qmake-qt4
  make
}

package() {
  cd "${srcdir}/touchegg/touchegg"
  make INSTALL_ROOT="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
