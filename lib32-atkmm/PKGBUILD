_pkgbasename=atkmm
pkgname=lib32-$_pkgbasename
pkgver=2.22.4
pkgrel=1
pkgdesc="C++ bindings for atk (32 bit, library only)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-atk' 'lib32-glibmm' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/2.22/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('c47ac8866fd0be695b6ba54a7cda5ab94676295ec5b2c22bd159bb8212aca438')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,lib32/atkmm*}
}
