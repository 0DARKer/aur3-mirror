_pkgbasename=atkmm
pkgname=lib32-$_pkgbasename
pkgver=2.22.2
pkgrel=2
pkgdesc="C++ bindings for atk (32 bit, library only)"
arch=('x86_64')
depends=('lib32-atk>=1.12.0' 'lib32-glibmm>=2.24.0' 'lib32-libsigc++' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://www.gtkmm.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/2.22/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('b94666ae0acebea53241fa0b2444468ff66e8a7790dfc7491489a9ad2eeefa34')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{include,lib32/atkmm*}
}
