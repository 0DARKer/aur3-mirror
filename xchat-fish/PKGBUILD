# Maintainer: skydrome <skydrome@i2pmail.org>
# Contributor: skydrome <skydrome@i2pmail.org>

pkgname=xchat-fish
pkgver=0.98
pkgrel=1
pkgdesc='Blowfish encryption plugin compatible to original blowcrypt script'
url='http://fish.secure.la'
license=('Copyleft' 'AGPL3')
arch=('i686' 'x86_64')
depends=('xchat')
makedepends=('unzip')
source=("ftp://ftp.dinoex.de/pub/FreeBSD/distfiles/FiSH-XChat.v${pkgver}-source.zip"
        "http://www.certivox.com/wp-content/themes/certivox/res/miracl.zip"
        "bling.patch"
        "xfish_makefile")
noextract=('miracl.zip')
sha256sums=('a065ace34c5a59cf51c194d9ac6aa5c8f00241488df38345e88174f5f345ecd6'
            'ed10f49d05ce80c5e59fba2b6db365fd88ee05be77a34671b03f6f43b732a679'
            '1fa56875bef9be6690735069b45c6ffdb170b64f41e969d6d3e2baaf1baf6028'
            '9cc579cdb284e7a4978ef81d5a792cb17a97961006622a16eb093c221bdff73a')

if [[ "$CARCH" = 'x86_64' ]]; then
    _buildcmd=linux64
    _CFLAGS="-fPIC"
else
    _buildcmd=linux
fi

build_miracl() {
    cd "$srcdir"
    unzip -qo -j -aa -L miracl.zip -d "${srcdir}/miracl"
    cd "${srcdir}/miracl"
    msg "compiling miracl math library..."
    sed "s|-O2|-O2 $_CFLAGS|" -i $_buildcmd
    sh $_buildcmd
    cp miracl.a ..
}

build() {
    [[ ! -f miracl.a ]] && build_miracl
    cd "$srcdir"
    msg "compiling xfish plugin..."
    rm Makefile && cp xfish_makefile Makefile
    #patch -Np1 -i "${srcdir}/bling.patch"
    make $_buildcmd
}
package() {
    cd "$srcdir"
    install -d "${pkgdir}/usr/lib/xchat/plugins"
    install -Dm644 "${srcdir}/xfish.so" "${pkgdir}/usr/lib/xchat/plugins/"
}
