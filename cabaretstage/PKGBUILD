# Maintainer: TDY <tdy@gmx.com>

pkgname=cabaretstage
pkgver=5.1.2
pkgrel=1
pkgdesc="A powerful, flexible PDF suite"
arch=('i686' 'x86_64')
url="http://www.cabaret-solutions.com/en/"
license=('custom')
depends=('desktop-file-utils' 'gtk2' 'java-runtime' 'swt')
optdepends=('cups: for printing support')
install=$pkgname.install
source=(http://www.cabaret-solutions.com/misc/downloads/${pkgname}_${pkgver}_linux-gtk-x86_64.tar.gz
        http://www.cabaret-solutions.com/misc/standards/EULA_en.pdf
        $pkgname.desktop certs.md5)
md5sums=('e2d6be0006849a567bd7e6deb352bbf3'
         '5b40ef1e8075f75d32dae154129fb62d'
         '930faa7f427bac6e75c3576e7ebc751a'
         'b2d10f87483f72f815edd337d821b356')
[[ $CARCH = i686 ]] && source[0]=${source[0]/_64} \
                    && md5sums[0]='b91158fe090f33a19a7b993fb930eea6'

build() {
  cd "$srcdir/$pkgname-$pkgver"
  install -dm755 "$pkgdir"/usr/{bin,lib,share/{java,pixmaps,licenses/$pkgname}}
  sed -i "4s,=.*,=/usr/share/java/$pkgname," bin/$pkgname.sh

  # certificates.tbz is a workaround for bsdtar pkg.tar.gz encoding issues
  tar cjf certificates.tbz certificates
  cp -r . "$pkgdir"/usr/share/java/$pkgname
  cp -r bin "$pkgdir"/usr/lib/$pkgname
  install -m444 ../certs.md5 "$pkgdir"/usr/share/java/$pkgname/certs.md5
  rm -rf "$pkgdir"/usr/share/java/$pkgname/certificates

  find "$pkgdir" -type d -exec chmod 755 '{}' \;
  find "$pkgdir" -type f -exec chmod 644 '{}' \;
  find "$pkgdir" -type f \( -name '*.s[oh]' -o -name "$pkgname" \) \
    -exec chmod 755 '{}' \;

  ln -sf ../share/java/$pkgname/bin/$pkgname.sh "$pkgdir"/usr/bin/$pkgname
  ln -sf ../java/$pkgname/cabaret.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -Dm644 ../EULA_en.pdf "$pkgdir"/usr/share/licenses/$pkgname/EULA.pdf
}
