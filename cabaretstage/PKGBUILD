# Maintainer: TDY <tdy@gmx.com>

pkgname=cabaretstage
pkgver=4.2.1
pkgrel=1
pkgdesc="A powerful, flexible PDF suite"
arch=('i686' 'x86_64')
url="http://www.cabaret-solutions.com/en/"
license=('custom')
depends=('desktop-file-utils' 'gtk2' 'java-runtime')
optdepends=('cups: for printing support')
install=$pkgname.install
source=(http://www.cabaret-solutions.com/misc/downloads/${pkgname}_${pkgver}_linux-gtk.tar.gz
        http://www.cabaret-solutions.com/misc/standards/EULA_en.pdf
        $pkgname.desktop certs.md5)
md5sums=('e91f99bbdd38637fcd0ad4b50aa21283'
         '5b40ef1e8075f75d32dae154129fb62d'
         '930faa7f427bac6e75c3576e7ebc751a'
         '1d89f1b79a62d95964cf83998c558f0f')
[[ $CARCH = x86_64 ]] && source[0]=${source[0]/-gtk/-gtk-$CARCH} \
                      && md5sums[0]='d2c632bd78fbdf2eb04080f5d562945f'

build() {
  cd "$srcdir/$pkgname-$pkgver"
  install -dm755 "$pkgdir"/usr/{bin,lib,share/{java,pixmaps,licenses/$pkgname}}
  sed -i "s#=bin#=/usr/lib/$pkgname#; 3 c\cd /usr/share/java/$pkgname" \
    $pkgname.sh

  # certificates.tbz is a workaround for bsdtar pkg.tar.gz encoding issues
  tar cjf certificates.tbz certificates
  cp -r . "$pkgdir"/usr/share/java/$pkgname
  cp -r bin "$pkgdir"/usr/lib/$pkgname
  install -m444 ../certs.md5 "$pkgdir"/usr/share/java/$pkgname/certs.md5
  rm -rf "$pkgdir"/usr/share/java/$pkgname/{bin,certificates}

  find "$pkgdir" -type d -exec chmod 755 '{}' \;
  find "$pkgdir" -type f -exec chmod 644 '{}' \;
  find "$pkgdir" -type f \( -name '*.s[oh]' -o -name "$pkgname" \) \
    -exec chmod 755 '{}' \;

  ln -sf ../share/java/$pkgname/$pkgname.sh "$pkgdir"/usr/bin/$pkgname
  ln -sf ../java/$pkgname/cabaret.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -Dm644 ../EULA_en.pdf "$pkgdir"/usr/share/licenses/$pkgname/EULA.pdf
}
