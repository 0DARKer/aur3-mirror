# Contributor: Wintershade <Wintershade[no_spam]<AT>gmail[no_spam]<DOT>com>

pkgname=util-linux-ng-git
pkgver=20100218
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux - the GIT version"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
arch=('i686' 'x86_64')
license=('GPL2')
groups=()
depends=('bash' 'ncurses>=5.7' 'zlib')
makedepends=('git')
provides=('linux32' 'util-linux' 'util-linux-ng=2.17.1')
replaces=('linux32' 'util-linux' 'util-linux-ng=2.17.1')
conflicts=('linux32' 'util-linux' 'util-linux-ng')
options=('!libtool')
install=util-linux-ng.install
source=()
optdepends=('perl: for chkdupexe support')
noextract=()
md5sums=()

_gitroot="git://git.kernel.org/pub/scm/utils/util-linux-ng/util-linux-ng.git"
_gitname="util-linux-ng"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  
  msg "A little housekeeping..."
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  msg "Starting configure..."
  ./autogen.sh 
  ./configure --enable-arch --enable-write --enable-raw --disable-wall --enable-rdev --enable-partx || return 1
  
  msg "Configure finished. Starting make. Good luck!"
  make HAVE_SLN=yes ADD_RAW=yes || return 1
  make HAVE_SLN=yes ADD_RAW=yes DESTDIR="${pkgdir}" install || return 1
  # remove files
  rm -f "${pkgdir}/bin/kill"
  rm -f "${pkgdir}/usr/share/man/man1/kill.1"
  rm -f "${pkgdir}/usr/share/man/man5/nfs.5"
  rm -f "${pkgdir}/usr/share/info/dir"
} 
