# Mantainer: Diego Principe <cdprincipeat gmaildot com>
# Contributor: Silvio Knizek <killermoehre@gmx.net>

pkgname=xfce4-volumed-git
pkgver=27.0ebbdc3
pkgrel=2
pkgdesc="A volume keys control daemon for Xfce"
arch=('any')
license=('GPL2')
url="http://git.xfce.org/apps/xfce4-volumed/"
depends=('gstreamer0.10-base' 'libkeybinder2' 'libnotify' 'xfconf')
optdepends=('gstreamer0.10-base-plugins: for sound card support'
'xfce4-notifyd: for OSD notifications')
makedepends=('git')
provides=('xfce4-volumed')
conflicts=('xfce4-volumed' 'xfce4-volumed-pulse')
source=('git://git.xfce.org/apps/xfce4-volumed')
md5sums=('SKIP')

_gitname=xfce4-volumed
_gitroot=http://git.xfce.org/apps/xfce4-volumed

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname

  ./autogen.sh
  
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install
}