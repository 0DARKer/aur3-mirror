# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=wp2latex  
pkgver=3.56
pkgrel=1
pkgdesc="Converts WordPerfect document formats to LaTeX"
url="http://www.penguin.cz/~fojtik/$pkgname/$pkgname.htm"
arch=('i686' 'x86_64')
license=('GPL')
source=(http://www.penguin.cz/~fojtik/$pkgname/$pkgname-${pkgver}.zip \
  gcc4.4.1.patch)
md5sums=('41fdccdbb1934cb3233b6289a77ee5ca'
         '4ce85f2032a0f02fa27ae3a72a18ad84')
depends=('libjpeg' 'gcc-libs')
install=$pkgname.install

build() {
  cd ${srcdir}/$pkgname.${pkgver}
  autoconf
  ./configure --prefix=/usr --without-png
  patch -p1 < ${srcdir}/gcc4.4.1.patch
  make x2latex
}
package() {
  cd ${srcdir}/$pkgname.${pkgver}
  install -Dm755 ${srcdir}/$pkgname.${pkgver}/linux/$pkgname \
    $pkgdir/usr/bin/$pkgname 
  for _i in cs de
  do
    install -Dm644 \
    $srcdir/$pkgname.${pkgver}/doc/locale/${_i}/lc_messages/$pkgname.mo \
      $pkgdir/usr/share/locale/${_i}/LC_MESSAGES/$pkgname.mo
  done 
  install -Dm644 $srcdir/$pkgname.${pkgver}/doc/$pkgname.1 \
    $pkgdir/usr/share/man/man1/$pkgname.1 
  install -d $pkgdir/usr/share/$pkgname/doc
  cp -R $srcdir/$pkgname.${pkgver}/doc/program.man/* \
    $pkgdir/usr/share/$pkgname/doc
  install -d $pkgdir/usr/share/texmf/tex/latex/$pkgname
  cp $srcdir/$pkgname.${pkgver}/styles.tex/* \
    $pkgdir/usr/share/texmf/tex/latex/$pkgname
}
