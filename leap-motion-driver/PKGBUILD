# Maintainer: M0Rf30 <morfeo89 [at] hotmail [dot] it>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>

pkgname=leap-motion-driver
pkgver=1.0.9.8411
pkgrel=1
pkgdesc="The Leap Motion Driver"
arch=('i686' 'x86_64')
url="https://developer.leapmotion.com/downloads"
depends=('mesa' 'libxxf86vm')
conflicts=('leap')
license=('custom')
source=("https://dl.dropboxusercontent.com/u/4772389/Leap_Packages_1.0.9%2B8411_Linux.tgz"
	LICENSE
	leapd.service
  leap-sdk.install)
install=leap-sdk.install

package() {
  cd ${srcdir}/Leap_Packages_1.0.9+8411_Linux

  if [ "$CARCH" == 'x86_64' ]; then
    bsdtar xf Leap-1.0.9+8411-x64.deb
  else
    bsdtar xf Leap-1.0.9+8411-x86.deb
  fi

  tar xf data.tar.gz

  # Copy binaries, headers, examples & libraries
  cp -r usr ${pkgdir}
  cp -r lib ${pkgdir}/usr/

  # Install systemd service
  install -Dm644 "${srcdir}/leapd.service" "${pkgdir}/usr/lib/systemd/system/leapd.service"

  # Copy license
  install -D -m644 "${srcdir}"/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

md5sums=('70c07089d1c5ec87545bc7def67d0f39'
         '78a4f0934b105397d1f7b17d06e4717c'
         '07287e65a3c2a4e2a956b7ba9038d816'
         '7dcbd917193007746310130fb76e53eb')
