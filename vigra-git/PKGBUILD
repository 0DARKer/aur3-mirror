# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=vigra-git
pkgver=20120317
pkgrel=1
pkgdesc="Computer vision library"
arch=('i686' 'x86_64')
url="http://hci.iwr.uni-heidelberg.de/vigra/"
license=('custom:MIT')
depends=('libpng' 'libtiff' 'openexr' 'hdf5')
optdepends=('python2: vigra-config'
            'boost-libs: python interface' 'python2: python interface' 'fftw: python interface')
makedepends=('git' 'cmake' 'doxygen' 'fftw' 'python2-numpy' 'boost')
checkdepends=('python2-nose')
provides=('vigra' 'vigra-hg')
conflicts=('vigra' 'vigra-hg')
replaces=('vigra-hg')
source=()
md5sums=()

_gitroot="git://github.com/ukoethe/vigra.git"
_gitname="vigra"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  mkdir "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DWITH_OPENEXR=true \
        -DDOCINSTALL=share/doc \
        -DCMAKE_SHARED_LINKER_FLAGS="-lpthread" \
        ../$_gitname
  make
}

check() {
  cd "$srcdir/$_gitname-build"
  make check
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

  # fix for python2
  sed -i 's|python$|python2|' "$pkgdir/usr/bin/vigra-config"

  # license
  install -D -m644 "$srcdir/$_gitname/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
} 
