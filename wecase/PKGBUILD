# Contributor: Star Brilliant <echo bTEzMjUzQGhvdG1haWwuY29tCg== | base64 -d>

pkgname=wecase
pkgver=0.02
pkgrel=1
pkgdesc="The Linux Sina Weibo Client"
arch=('any')
url="https://github.com/WeCase/WeCase"
license=('GPL3')
depends=('python>=3' 'pyqt>=4' 'python-dateutil' 'python-notify2')
_sdkver=2823b40c5293e342fb04fa1a3c7421beeb307498
source=("WeCase-${pkgver}.tar.gz::https://github.com/WeCase/WeCase/archive/${pkgver}.tar.gz"
        "sinaweibopy-${_sdkver}.tar.gz::https://github.com/WeCase/sinaweibopy/archive/${_sdkver}.tar.gz"
        'launcher.sh' 'wecase.desktop')
md5sums=('12a70b9131c3ae4773f92334c741601b'
         'f4d2a6e82d5f26b1c59c629de353a994'
         'f77938cd93864dcdb9e8f29634e51beb'
         '83bdc0d830e0bac97c600487885bcf5e')

package() {
  cd "$srcdir/WeCase-$pkgver"
  mkdir -p "$pkgdir/usr/lib/"
  cp -R src "$pkgdir/usr/lib/WeCase"
  cp "$srcdir/sinaweibopy-$_sdkver/weibo.py" "$pkgdir/usr/lib/WeCase/"
  rm "$pkgdir/usr/lib/WeCase/ui" && cp -R ui "$pkgdir/usr/lib/WeCase/"
  install -Dm0755 "$srcdir/launcher.sh" "$pkgdir/usr/bin/wecase"
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s "/usr/lib/WeCase/ui/img/WeCase 854.png" "$pkgdir/usr/share/pixmaps/WeCase.png"
  install -D "$srcdir/wecase.desktop" "$pkgdir/usr/share/applications/wecase.desktop"
}
