# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: breakdown <breakdown[at]archlinux[dot]us>
# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: William Heinbockel <wheinbockel[at]gmail[dot]com>

pkgname=xf86-video-intel-git
pkgver=20110212
pkgrel=1
pkgdesc='X.org Intel i810/i830/i915/945G/G965+ video drivers'
url='http://xorg.freedesktop.org/'
arch=('i686' 'x86_64')
license=('custom')
depends=('intel-dri' 'libpciaccess' 'libdrm-git' 'libxvmc' 'xcb-util')
makedepends=('git' 'xorg-server' 'xf86driproto' 'glproto'
             'mesa' 'xineramaproto' 'xorg-util-macros' 'xorg-server-devel')
conflicts=('xf86-video-intel')
provides=('xf86-video-intel')
options=('!libtool')
source=('LICENSE')
md5sums=('b924480931774dec14515e270db4924d')

_gitroot='git://anongit.freedesktop.org/xorg/driver/xf86-video-intel'
_gitname='xf86-video-intel'

build() {
  msg 'Connecting to git.freedesktop.org GIT server....'

  if [ -d ${_gitname} ] ; then
    pushd ${_gitname}
    git pull origin
    popd
    msg 'The local files are updated.'
  else
    git clone ${_gitroot}
  fi

  msg 'GIT checkout done or server timeout'

  rm -rf build
  cp -r ${_gitname} build
  cd build

  msg 'Starting make...'

  ./autogen.sh \
    --prefix=/usr \
    --enable-dri

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
