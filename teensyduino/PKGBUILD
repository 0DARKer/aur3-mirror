# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
# Contributor: PyroPeter <googlemail.com@abi1789>
# Contributor: darkapex <me@jailuthra.in>
# Contributor: tty0 <vt.tty0[d0t]gmail.com>

pkgname=teensyduino
pkgver=1.17
_arduino=1.0.5
_loader=2.1
pkgrel=8
pkgdesc="Arduino SDK with Teensyduino"
arch=('i686' 'x86_64')
url="http://www.pjrc.com/teensy/teensyduino.html"
options=(!strip staticlibs)
license=('GPL' 'LGPL' 'custom')
depends=('arm-none-eabi-gcc' 'arm-none-eabi-binutils' 'avr-gcc' 'avr-libc' 'avrdude'
         'libusb-compat' 'libusb' 'java-runtime' 'java-rxtx')
makedepends=('xorg-server-xvfb' 'libxft' 'xdotool')
optdepends=('gtk2: Arduino IDE and Teensy Loader GUI'
            'libpng12: Teensy Loader GUI')
provides=('arduino')
conflicts=('arduino' 'teensy-loader-cli')
install="arduino.install"
source=("http://arduino.googlecode.com/files/arduino-${_arduino}-linux32.tgz"
        'arduino-gcc-4.8-no-use-cxa-atexit.patch'
        'arduino'
        'arduino.png'
        'arduino.desktop'
        "http://www.pjrc.com/teensy/td_${pkgver//./}/teensyduino.32bit"
        'teensyduino.sh'
        'teensy.desktop'
        "http://www.pjrc.com/teensy/teensy_loader_cli.${_loader}.zip"
        "http://www.pjrc.com/teensy/49-teensy.rules"
        'LICENSE')
md5sums=('ca6a87052a83e9500d6555c1f54c793b'
         '57a444a1fe8328b56563388c015b5e2f'
         '551304dc658224c867513f6099075f0e'
         '9e36d33891d5e68d38ec55d1494499a5'
         'eebc4d6495864bea99ad057af801afb9'
         'a861e4f1b8e9e2748138d816ffc24a2a'
         '938d0061610c2e6cee5537ca4890a8cc'
         '8716606687b10d470300d27aaaec2c98'
         'c27a8a970f32b60e5bb12b70cb52dd42'
         '9f0593b4f3dab6d9a32ebc993d6aedc3'
         '97ce5af4d697a059f2933ddf93146926')

if [ "$CARCH" == 'x86_64' ]; then
  source[0]="http://arduino.googlecode.com/files/arduino-${_arduino}-linux64.tgz"
  source[5]="http://www.pjrc.com/teensy/td_${pkgver//./}/teensyduino.64bit"
  md5sums[0]='47bc3292f96a9d0e1a31f7c707c9acef'
  md5sums[5]='8026f33bb72d5b4c9f2d79501b865911'

  _bits=64
elif [ "$CARCH" == 'i686' ]; then
  _bits=32
fi

prepare() {
  cd "$srcdir/arduino-$_arduino"

  patch -Np1 <"$srcdir/arduino-gcc-4.8-no-use-cxa-atexit.patch"
}

build() {
  msg2 "Running Teensyduino installer (takes around 40 seconds)"
 
  cd "$srcdir"

  chmod +x "teensyduino.${_bits}bit"
  xvfb-run ./teensyduino.sh "./teensyduino.${_bits}bit" "$srcdir/arduino-$_arduino"

  msg2 "Building Teensy Loader command line"

  cd teensy_loader_cli
  make
}

package() {
  cd "$srcdir/arduino-$_arduino"

  # arduino excutable should accept arguments
  sed -i 's/^java .* processing.app.Base$/\0 "$*"/' arduino

  mkdir -p $pkgdir/usr/{bin,share/{doc,applications,pixmaps,licenses/teensyduino}}
  mkdir -p "$pkgdir/usr/lib/udev/rules.d"

  # use Arch's avr toolchain
  mkdir -p "$pkgdir/usr/arm-none-eabi/lib"
  install -m644 hardware/tools/arm-none-eabi/arm-none-eabi/lib/libarm_cortexM4l_math.a "$pkgdir/usr/arm-none-eabi/lib/"
  rm -rf hardware/tools/{arm-none-eabi,avr}
  rm -f hardware/tools/{avrdude,avrdude64,avrdude.conf,readme.txt}

  # copy the whole SDK to /usr/share/arduino/
  cp -a . "$pkgdir/usr/share/arduino"

  # use system's RXTX library
  ln -sf /usr/lib/librxtxSerial.so "$pkgdir/usr/share/arduino/lib/librxtxSerial.so"
  ln -sf /usr/lib/librxtxSerial.so "$pkgdir/usr/share/arduino/lib/librxtxSerial64.so"
  ln -sf /usr/share/java/rxtx/RXTXcomm.jar "$pkgdir/usr/share/arduino/lib/RXTXcomm.jar"

  # at least support the FHS a little bit
  install -m755 "$srcdir/arduino" "$pkgdir/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "$pkgdir/usr/share/doc/arduino"

  # desktop icon
  install -m644 "$srcdir/arduino.desktop" "$pkgdir/usr/share/applications/"
  install -m644 "$srcdir/arduino.png" "$pkgdir/usr/share/pixmaps/"

  # install custom PJRC license
  install -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/teensyduino/"

  # install teensy loader files
  install -m644 "$srcdir/49-teensy.rules" "$pkgdir/usr/lib/udev/rules.d"
  ln -s /usr/share/arduino/hardware/tools/teensy "$pkgdir/usr/bin/teensy-loader"
  install -m644 "$srcdir/teensy.desktop" "$pkgdir/usr/share/applications/"

  # install command-line teensy loader
  install -m755 "$srcdir/teensy_loader_cli/teensy_loader_cli" "$pkgdir/usr/bin/teensy-loader-cli"
}
