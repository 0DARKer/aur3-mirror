# Contributor: rich_o <rich_o@lavabit.com>
# Maintainer: rich_o <rich_o@lavabit.com>

_pkgname="ncmpcpp"
pkgname="${_pkgname}-git"
pkgver=1510.786ffe9
pkgrel=1
pkgdesc="An almost exact clone of ncmpc with some new features." 
arch=('i686' 'x86_64')
url="http://unkart.ovh.org/ncmpcpp/"
license=('GPL2')
depends=('ncurses' 'libmpdclient>=2.8' 'boost-libs')
makedepends=('git' 'boost')
optdepends=('curl: fetch lyrics'
    'taglib: tag editor'
    'fftw: frequency spectrum mode visualization'
    )
conflicts=('ncmpcpp' 'ncmpcpp-xdg-config' 'ncmpcpp-xdg-config-git')
install=${pkgname}.install
source=("git+git://repo.or.cz/${_pkgname}.git")
md5sums=('SKIP')

pkgver() {
  cd "${SRCDEST}/${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/${_pkgname}"
  ./autogen.sh BOOST_LIB_SUFFIX='' \
  --prefix=/usr \
  --enable-clock \
  --enable-outputs \
  --enable-visualizer
  make
} 

package() {
  cd "${srcdir}/${_pkgname}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  find "${pkgdir}" -type d -name .git -exec rm -r '{}' +
}
