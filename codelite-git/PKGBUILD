# Maintainer: Pyro Devil <p dot devil at gmail dot com>
pkgname=codelite-git
_gitname=codelite
pkgver=5.4.25.gb432de4
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages"
url="http://codelite.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('wxgtk>=3.0.0' 'clang' 'libssh')
makedepends=('git' 'cmake' 'webkitgtk2')
optdepends=('graphviz: callgraph visualization')
conflicts=('codelite')
replaces=()
provides=('codelite')
backup=()
install=
source=("${_gitname}::git+https://github.com/eranif/codelite.git")
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git describe --tags | sed 's|-|.|g'
}

build() {
  cd "$_gitname"
  
  cmake -G "Unix Makefiles" -DPREFIX="/usr" -DCMAKE_BUILD_TYPE=Release -DENABLE_CLANG=1 -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config -DwxWidgets_wxrc_EXECUTABLE=/usr/bin/wxrc-3.0 .
  make
}

package() {
  cd "${_gitname}"

  make -j1 DESTDIR=${pkgdir} install
  install -m 644 -D "${srcdir}/${_gitname}/LICENSE" "${pkgdir}/usr/share/licenses/${_gitname}/LICENSE"
}
