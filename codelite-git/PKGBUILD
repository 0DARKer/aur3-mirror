#
# codelite PKGBUILD
#
# Maintainer: Pyro Devil <p dot devil at gmail dot com>
# Contributor: Jefferson Gonzalez <jgmdev@gmail.com>
#

pkgname=codelite-git
_gitname=codelite-master
pkgver=7.1
pkgrel=1
pkgdesc="Open-source, cross platform IDE for the C/C++ programming languages"
arch=('i686' 'x86_64')
url="http://www.codelite.org/"
license=('GPL')
depends=('wxgtk' 'curl' 'webkitgtk2' 'libssh' 'xterm' 'python2' 'libedit' 'ncurses' 'valgrind')
makedepends=('pkgconfig' 'cmake')
optdepends=(
    'graphviz: callgraph visualization'
    'lldb-svn: debugger used by the lldb plugin'
)
conflicts=('codelite' 'codelite-bin' 'codelite4-svn')
replaces=('codelite' 'codelite-bin' 'codelite4-svn')
provides=('codelite')

source=(
    ${_gitname}::https://github.com/eranif/codelite/archive/master.zip
    http://repos.codelite.org/wxCrafterLibs/wxgui.zip
)

md5sums=(
    'SKIP'
    'SKIP'
)

noextract=('wxgui.zip')

build() {
    if [ -e "${srcdir}/${pkgname}" ]; then
        rm -rf "${srcdir}/${pkgname}"
    fi
    
    cp -R "$_gitname" "${srcdir}/${pkgname}"
    cd "${srcdir}/${pkgname}"
    
    CXXFLAGS="${CXXFLAGS} -fno-devirtualize"

    mkdir -p build
    cd build
    cmake -G "Unix Makefiles" -DCMAKE_BUILD_TYPE=Release -DENABLE_CLANG=1 -DCMAKE_INSTALL_LIBDIR=lib ..
    make
}

package() {
    cd "${srcdir}/${pkgname}/build"
    
    make -j1 DESTDIR="${pkgdir}" install
    install -m 644 -D "${srcdir}/wxgui.zip" "${pkgdir}/usr/share/codelite/wxgui.zip"
    install -m 644 -D "${srcdir}/${pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/codelite/LICENSE"
}
