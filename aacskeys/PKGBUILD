# Submitter: Paul Burton <paulburton89@gmail.com>
# Maintainer: Zebulon <zeb@zebulon.org.uk>
pkgname=aacskeys
pkgver="0.4.0e"
pkgrel=1
pkgdesc="A library and program to retrieve decryption keys for HD discs"
arch=('i686' 'x86_64')
url="http://forum.doom9.org/showthread.php?t=123311"
license=('custom')
depends=('openssl')
makedepends=('java-environment' 'premake3')
source=(http://debian-multimedia.org/pool/main/a/aacskeys/${pkgname}_0.4.0c.orig.tar.gz
	http://ysk.orz.hm/BD/aacskey/${pkgname}-${pkgver}.zip)
sha1sums=('c2fd4df47d3aa2e39d7fe5e44991c00361898283'
	  'fd18f6a7f21d497a134841e35d8b4cf18d885c83')

build() {

  #cd "${srcdir}"
  cp "${srcdir}/$pkgname-$pkgver/aacs_ecdsa.cpp" "$pkgname-0.4.0c/src/"
  cp "${srcdir}/$pkgname-$pkgver/aacskeys.cpp" "$pkgname-0.4.0c/src/"
  cp "${srcdir}/$pkgname-$pkgver/ProcessingDeviceKeysSimple.txt" "$pkgname-0.4.0c"
  cp "${srcdir}/$pkgname-$pkgver/HostKeyCertificate_PS3.txt" "$pkgname-0.4.0c/HostKeyCertificate.txt"

  cd "${srcdir}/$pkgname-0.4.0c"

  # Paranoia: remove binaries, make sure they aren't used
  rm -rf bin lib

  # If java-environement is installed for the first time
  # we need to source profile to set JAVA_HOME
  source /etc/profile

  sed -i 's|/usr/local/ssl/include|/usr/include|' premake.lua
  sed -i 's|/usr/local/ssl/lib|/usr/lib|' premake.lua
  sed -i "s|/usr/lib/jvm/java-6-sun/include|$JAVA_HOME/include|" premake.lua

  make || return 1
}

package() {
  cd "${srcdir}/$pkgname-0.4.0c"

  # Install lib
  mkdir -p ${pkgdir}/usr/lib
  cp lib/linux/libaacskeys.so ${pkgdir}/usr/lib/ || return 1

  # Install program
  mkdir -p ${pkgdir}/usr/bin
  cp bin/linux/aacskeys ${pkgdir}/usr/bin || return 1

  # Install resources
  mkdir -p ${pkgdir}/usr/share/$pkgname
  cp HostKeyCertificate.txt ${pkgdir}/usr/share/$pkgname/ || return 1
  cp ProcessingDeviceKeysSimple.txt ${pkgdir}/usr/share/$pkgname/ || return 1
}
