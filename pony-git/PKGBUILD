pkgname=pony-git
pkgver=0.1.5.r119.g112d776
pkgrel=1

pkgdesc='An actor model, capabilities, high performance programming language.'
url='http://ponylang.org/'
arch=('x86_64')
license=('BSD')

options=('staticlibs')

depends=('zlib' 'ncurses' 'gcc-libs')
makedepends=('git' 'llvm')

optdepends=('pcre2: Needed for the regex package.')

provides=('pony')
conflicts=('pony')

source=('git://github.com/CausalityLtd/ponyc')
install='pony-git.install'

md5sums=('SKIP')

pkgver() {
    cd ponyc
    git describe --tags | sed 's/-/.r/; s/-/./'
}

build() {
    cd ponyc
    make config=release prefix=/usr
}

check() {
    cd ponyc
    make config=release prefix=/usr test
}

package() {
    cd ponyc

    # XXX: Currently this attempts to create a symlink to root owned locations.
    # Due to this I can trivially replicate the installation in a more
    # controlled manner.
    #make config=release prefix=/usr destdir="$pkgdir" install
    _version="$(git describe --always --tags)"

    install -Dm755 build/release/ponyc "$pkgdir"/usr/lib/pony/"$_version"/ponyc
    install -Dm644 build/release/libponyrt.a "$pkgdir"/usr/lib/libponyrt.a
    install -Dm644 build/release/libponyc.a "$pkgdir"/usr/lib/libponyc.a
    install -Dm644 src/libponyrt/pony.h "$pkgdir"/usr/include/pony.h

    # Ponyc is designed to read its standard packages from the same PWD.
    install -dm755 "$pkgdir"/usr/bin
    ln -sf /usr/lib/pony/"$_version"/ponyc "$pkgdir"/usr/bin/ponyc

    cp -dr --no-preserve=ownership packages/* "$pkgdir"/usr/lib/pony/"$_version"

    install -dm755 "$pkgdir"/usr/share/doc/pony
    cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
