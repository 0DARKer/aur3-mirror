pkgname=pony-git
pkgver=0.1.2.r13.g9bab5fc
pkgrel=1

pkgdesc='An actor model, capabilities, high performance programming language.'
url='http://ponylang.org/'
arch=('x86_64')
license=('BSD')

options=('staticlibs')

depends=('zlib' 'ncurses' 'gcc-libs')
makedepends=('git' 'llvm')

provides=('pony')
conflicts=('pony')

source=('git://github.com/CausalityLtd/ponyc')
install='pony-git.install'

md5sums=('SKIP')

pkgver() {
    cd ponyc
    git describe --tags | sed 's/-/.r/; s/-/./'
}

build() {
    cd ponyc
    make
}

check() {
    cd ponyc
    make test
}

package() {
    cd ponyc
    install -Dm755 build/debug/ponyc "$pkgdir"/usr/bin/ponyc
    install -Dm644 build/debug/libponyrt.a "$pkgdir"/usr/lib/libponyrt.a
    install -Dm644 build/debug/libponyc.a "$pkgdir"/usr/lib/libponyc.a
    install -Dm644 src/libponyrt/pony.h "$pkgdir"/usr/include/pony.h
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE

    install -dm755 "$pkgdir"/usr/lib/pony
    cp -dr --no-preserve=ownership packages/* "$pkgdir"/usr/lib/pony

    install -dm755 "$pkgdir"/usr/share/doc/pony
    cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony
}
