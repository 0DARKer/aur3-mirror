# Maintainer: udeved <udeved@openrc4arch.site40.net>
# Contributor: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Ivailo Monev <xakepa10@gmail.com>

_udevver=212

pkgname=eudev
pkgver=1.7
pkgrel=1
pkgdesc="The userspace dev tools (udev) forked by Gentoo"
arch=('i686' 'x86_64')
url="http://dev.gentoo.org/~blueness/eudev"
license=('GPL')
groups=('eudev-base')
depends=('glib2' 'hwids' 'kbd' 'kmod' 'util-linux')
makedepends=('gobject-introspection'
			'gperf'
			'gtk-doc'
			'intltool')
optdepends=('eudev-openrc: eudev-postmount script'
			'dbus-nosystemd: dbus without systemd'
			'upower-nosystemd: upower without systemd'
			'udisks2-nosystemd: udisks2 without systemd'
			'libpulse-nosystemd: libpulse without systemd'
			'cups-nosystemd: cups without systemd')
provides=("udev=${_udevver}"
		"systemd=${_udevver}"
		"libsystemd=${_udevver}"
		"systemd-tools=${_udevver}")
conflicts=('systemd'
		'libsystemd'
		'systemd-tools')
backup=('etc/udev/udev.conf')
options=(!makeflags !libtool)
source=("${url}/${pkgname}-${pkgver}.tar.gz"
		'initcpio_hooks'
		'initcpio_install')
md5sums=('80649a0350ff9620fc2da9562d9f2a6a'
         'e99e9189aa2f6084ac28b8ddf605aeb8'
         '88849afefcef229b8f79be7bbf05c3a2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --with-rootprefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib \
    --sbindir=/usr/bin \
    --with-firmware-path=/usr/lib/firmware/updates:/usr/lib/firmware \
    --with-modprobe=/usr/bin/modprobe \
    --with-html-dir=/usr/share/doc/${pkgname}-${pkgver}/html \
    --enable-gtk-doc \
    --enable-gudev \
    --enable-introspection \
    --enable-split-usr \
    --enable-rule-generator
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  for i in "${pkgdir}/usr/lib/udev/rules.d/"*.rules; do
    sed -i -e 's#GROUP="dialout"#GROUP="uucp"#g;
		s#GROUP="tape"#GROUP="storage"#g;
		s#GROUP="cdrom"#GROUP="optical"#g' "${i}"
  done

  install -Dm644 "${srcdir}/initcpio_hooks" "${pkgdir}/usr/lib/initcpio/hooks/udev"
  install -Dm644 "${srcdir}/initcpio_install" "${pkgdir}/usr/lib/initcpio/install/udev"
}

