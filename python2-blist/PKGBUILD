# Maintainer: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Chris Baker <baker.chris.3@gmail.com>

pkgname=python2-blist
pkgver=1.3.4
pkgrel=2
pkgdesc="A list-like type with better asymptotic performance"
arch=(i686 x86_64)
url="http://pypi.python.org/pypi/blist/"
license=('BSD')
depends=('python2')
makedepends=('python2-distribute')
changelog=Changelog
source=(http://pypi.python.org/packages/source/b/blist/blist-$pkgver.tar.gz)
md5sums=(02e8bf33cffec9cc802f4567f39ffa6f)

build() {
  cd "$srcdir/blist-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/blist-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

