# Maintainer: Aaron Chen <nextAaron at gmail.com>
# Contributor: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>

_pkgname=core
pkgname=ocaml-$_pkgname
pkgver=109.15.01
_pkgdir=${pkgver/%.??/.00}
pkgrel=2
pkgdesc="Standard library overlay"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ocaml>=3.12' 'ocaml-res' 'ocaml-sexplib>=109.15.00' 'ocaml-bin_prot>=109.15.00' 'ocaml-fieldslib>=109.15.00' 'ocaml-variantslib>=109.15.00' 'ocaml-herelib>=109.15.00' 'ocaml-comparelib>=109.15.00' 'ocaml-pa_ounit>=109.15.00' 'ocaml-pipebang>=109.15.00')
makedepends=('ocaml-findlib')
options=(!strip !makeflags)
url="http://janestreet.github.com"
source=("https://ocaml.janestreet.com/ocaml-core/$_pkgdir/individual/$_pkgname-$pkgver.tar.gz")
md5sums=('e18c22112252250e6248fec7cbac95f9')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --enable-docs --disable-debug --prefix /usr --destdir "$pkgdir"
  make all
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  export OCAMLFIND_DESTDIR="$pkgdir$(ocamlfind printconf destdir)"
  export OCAMLFIND_COMMANDS="ocamldoc=ocamldoc -thread"
  install -dm 755 "$OCAMLFIND_DESTDIR/stublibs"
  make install
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
