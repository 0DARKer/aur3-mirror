# Maintainer: Raimar Buehmann (raimar)

pkgname=lxsession-git
pkgver=0.5.1.r894.20141031
pkgrel=1
epoch=1
pkgdesc='Lightweight X11 session manager for LXDE'
arch=('i686' 'x86_64')
url="http://lxde.org/"
license=('GPL2')
groups=('lxde')
depends=('gtk2' 'polkit' 'dbus-glib')
makedepends=('pkgconfig' 'intltool' 'docbook-xml' 'docbook-xsl' 'vala')
provides=('lxsession' 'lxsession-edit' 'lxpolkit' 'lxde-settings-daemon' 'lxsession-lite')
conflicts=('lxsession' 'lxsession-edit' 'lxpolkit' 'lxde-settings-daemon' 'lxsession-lite')
source=(
	$pkgname::git+git://git.lxde.org/git/lxde/lxsession.git
	Create-lock-file-to-prevent-more-than-one-logout-dialog.patch
	Added-ESCAPE-key-to-close-the-lxsession-logout-menu.patch
)
sha256sums=(
	'SKIP'
	757b0857fa4156ced4eb2d7b689d851fef5b9d63021f226e4e01342b47132371
	b1bb557caab61eb9421c959cd17e05fb06a4e800a989ba4ae778163e4fe4389c
)

pkgver() {
	# remove ".r*.*" from package version
	_pkgverTriple=$(echo $pkgver | sed "s/\.r.*//g")
	cd $pkgname
	# get number of last git commit
	_commitCount=$(git rev-list --count HEAD)
	# get time of last git commit
	_commitTime=$(git show -s --format="%ci" | grep -o "....-..-.." | sed "s/-//g")
	# add ".r*.*" from package version
	echo "$_pkgverTriple.r$_commitCount.$_commitTime"
}

prepare() {
	cd $pkgname
	patch -Np1 -i ../Create-lock-file-to-prevent-more-than-one-logout-dialog.patch
	patch -Np1 -i ../Added-ESCAPE-key-to-close-the-lxsession-logout-menu.patch
}

build() {
	cd $pkgname
	./autogen.sh
	./configure --prefix=/usr --sysconfdir=/etc --disable-buildin-clipboard --disable-buildin-polkit
	make
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install 
}
