# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>

pkgname=libretro-catsfc-git
pkgver=401.6097277
pkgrel=1
pkgdesc="libretro implementation of CATSFC. (Super Nintendo Entertainment System)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/CATSFC-libretro"
license=('custom')
makedepends=('git')

_gitname=CATSFC-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/catsfc_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm644 "${_gitname}/catsfc_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-catsfc.so"
  install -Dm644 "catsfc_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-catsfc.info"
  install -Dm644 "${_gitname}/copyright" "${pkgdir}/usr/share/licenses/$pkgname/license.txt"
}
