# Maintainer: jonnyb <zer0ne at gmx.net>

pkgname=magarena
pkgver=1.43
pkgrel=1
pkgdesc="Fantasy card strategy game against computer opponent"
arch=('any')
url="http://code.google.com/p/magarena/"
license=('GPL3')
depends=('java-runtime' 'groovy' 'sh')
makedepends=('unzip')
source=(http://magarena.googlecode.com/files/Magarena-$pkgver.zip
        $pkgname.sh
	$pkgname.png
	$pkgname.desktop)
noextract=(Magarena-$pkgver.zip)
sha1sums=('c9925863095b33862aba9bf92927d2a038ee660a'
          'ad4e412856ed4b919d0d13cea26a3c5de52fed43'
          '79cfc023362ed1133de3fa62a0b4e7da6d66cdb5'
          'f10867c4c96ebc02e31b51becee75825397481ef')
prepare() {
  cd "$srcdir"
  unzip -q "Magarena-$pkgver.zip"
  cd "Magarena-$pkgver"
  mv Magarena.exe ../$pkgname.jar
}

package() {
  cd "$srcdir"
  install -dm755 "$pkgdir/usr/share/java/$pkgname/"
  install -Dm644 $pkgname.jar "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -dm755 "$pkgdir/usr/share/$pkgname-base/"
  cp -r "Magarena-$pkgver/Magarena/" "$pkgdir/usr/share/$pkgname-base/"
}
