# Maintainer: jonnyb <zer0ne at gmx net>

pkgname=magarena
pkgver=1.44
pkgrel=1
pkgdesc="Fantasy card strategy game against computer opponent"
arch=('any')
url="http://code.google.com/p/magarena/"
license=('GPL3')
depends=('java-environment' 'bash')
makedepends=('unzip')
source=(http://magarena.googlecode.com/files/Magarena-$pkgver.zip
        $pkgname.sh
        $pkgname.png
        $pkgname.desktop)
noextract=(Magarena-$pkgver.zip)
sha1sums=('258eb88e39d7951d2e86e09c4115af92cce71cf0'
          '6acdd2fe3bf2006b02e6a44c48f9c3c6006ee950'
          '79cfc023362ed1133de3fa62a0b4e7da6d66cdb5'
          '1844a0246512997b17ead106d696810be9f5c41c')
prepare() {
  cd "$srcdir"
  unzip -q "Magarena-$pkgver.zip"
  cd "Magarena-$pkgver"
  mv Magarena.exe ../$pkgname.jar
  mv README.txt Magarena/README.txt
  echo "$pkgver-$pkgrel" > Magarena/VERSION
}

package() {
  cd "$srcdir"
  install -Dm644 $pkgname.jar "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  for _libfile in $srcdir/Magarena-$pkgver/lib/*.jar ; do
    _libname=$(basename $_libfile)
    install -Dm644  $srcdir/Magarena-$pkgver/lib/$_libname "$pkgdir/usr/share/java/$pkgname/lib/$_libname"
  done

  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -dm755 "$pkgdir/usr/share/$pkgname-base/"
  cp -rp "Magarena-$pkgver/Magarena/" "$pkgdir/usr/share/$pkgname-base/"
}
