# Maintainer: jonnyb <zer0ne at gmx net>

pkgname=magarena
pkgver=1.43
pkgrel=2
pkgdesc="Fantasy card strategy game against computer opponent"
arch=('any')
url="http://code.google.com/p/magarena/"
license=('GPL3')
depends=('java-runtime' 'groovy' 'sh')
makedepends=('unzip')
source=(http://magarena.googlecode.com/files/Magarena-$pkgver.zip
        $pkgname.sh
        $pkgname.png
        $pkgname.desktop)
noextract=(Magarena-$pkgver.zip)
sha1sums=('c9925863095b33862aba9bf92927d2a038ee660a'
          'de690be3b0437f8089657a663aa0be945fdbdddf'
          '79cfc023362ed1133de3fa62a0b4e7da6d66cdb5'
          'f10867c4c96ebc02e31b51becee75825397481ef')
prepare() {
  cd "$srcdir"
  unzip -q "Magarena-$pkgver.zip"
  cd "Magarena-$pkgver"
  mv Magarena.exe ../$pkgname.jar
  mv README.txt Magarena/README.txt
  echo "$pkgver-$pkgrel" > Magarena/VERSION
}

package() {
  cd "$srcdir"
  install -Dm644 $pkgname.jar "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -dm755 "$pkgdir/usr/share/$pkgname-base/"
  cp -rpf "Magarena-$pkgver/Magarena/" "$pkgdir/usr/share/$pkgname-base/"
}
