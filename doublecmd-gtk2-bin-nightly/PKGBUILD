# $Id:$
# Contributor: (sirocco AT ngs.ru)
# Maintainer: (sirocco AT ngs.ru)

pkgname=doublecmd-gtk2-bin-nightly
_pkgname=doublecmd
pkgver=0.7.0
_pkgver=0+svn5772
pkgrel=772
pkgdesc="Double Commander is a cross platform open source file manager with two panels side by side. It is inspired by Total Commander and features some new ideas."
arch=('i686' 'x86_64')
url='http://doublecmd.sourceforge.net'
# https://launchpad.net/~alexx2000/+archive/doublecmd-svn
depends=('gtk2')
optdepends=('lua: scripting' 'p7zip: support for 7zip archives' 'libunrar: support for rar archives'
            'pmount: mount removable devices' 'imagemagick: speed up thumbnail view')
install="doublecmd.install"
provides=('doublecmd')
conflicts=('doublecmd-gtk2' 'doublecmd-svn')
license=('GPL2')
options=('!strip')

# temporary to enforce upgrade to pacman 4.2
makedepends=('pacman>=4.2')

_bldarch=amd64
[[ $CARCH = i686 ]] && _bldarch=i386

_url=http://download.opensuse.org/repositories/home:/Alexx2000:/doublecmd-svn/xUbuntu_14.10
source=(${_url}/all/${_pkgname}-common_${pkgver}~${_pkgver}_all.deb)
source_i686+=(${_url}/i386/${_pkgname}-gtk_${pkgver}~${_pkgver}_i386.deb
        ${_url}/i386/${_pkgname}-plugins_${pkgver}~${_pkgver}_i386.deb)
source_x86_64+=(${_url}/amd64/${_pkgname}-gtk_${pkgver}~${_pkgver}_amd64.deb
        ${_url}/amd64/${_pkgname}-plugins_${pkgver}~${_pkgver}_amd64.deb)
sha256sums=('0ecf0f9e62e0dad53383bb09102073a424712970f57b7dafc711c60d1490bdfd')
sha256sums_i686=('15dbba8754185107fcec956c664b8f9c4254ea322bf3fcd2934ffe0ccfc2c5c6'
                 '5b675755d405130393bba813c32c14887a6d1dc2356d920b3991a49d8f4cb5e3')
sha256sums_x86_64=('8584d605b18b8117d1221d5300c8bd5f38d2e570da21a0e3413341dd5bbb1c76'
                   '72df033f41452f0b552d0e33973bc361abc2aca25a718be9fc313a0a1455034d')

package() {
    msg2 "Extracting files..."
    cd "$srcdir"
    ar x ${_pkgname}-gtk_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-plugins_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-common_${pkgver}~${_pkgver}_all.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
}

