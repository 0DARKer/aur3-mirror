# $Id:$
# Contributor: (sirocco AT ngs.ru)
# Maintainer: (sirocco AT ngs.ru)

pkgname=doublecmd-gtk2-bin-nightly
_pkgname=doublecmd
pkgver=0.7.0
_pkgver=0+svn5873
pkgrel=873
pkgdesc="Double Commander is a cross platform open source file manager with two panels side by side. It is inspired by Total Commander and features some new ideas."
arch=('i686' 'x86_64')
url='http://doublecmd.sourceforge.net'
# https://launchpad.net/~alexx2000/+archive/doublecmd-svn
depends=('gtk2')
optdepends=('lua: scripting' 'p7zip: support for 7zip archives' 'libunrar: support for rar archives'
            'pmount: mount removable devices' 'imagemagick: speed up thumbnail view')
install="doublecmd.install"
provides=('doublecmd')
conflicts=('doublecmd-gtk2' 'doublecmd-svn')
license=('GPL2')
options=('!strip')

# temporary to enforce upgrade to pacman 4.2
makedepends=('pacman>=4.2')

_bldarch=amd64
[[ $CARCH = i686 ]] && _bldarch=i386

_url=http://download.opensuse.org/repositories/home:/Alexx2000:/doublecmd-svn/xUbuntu_14.10
source=(${_url}/all/${_pkgname}-common_${pkgver}~${_pkgver}_all.deb)
source_i686+=(${_url}/i386/${_pkgname}-gtk_${pkgver}~${_pkgver}_i386.deb
        ${_url}/i386/${_pkgname}-plugins_${pkgver}~${_pkgver}_i386.deb)
source_x86_64+=(${_url}/amd64/${_pkgname}-gtk_${pkgver}~${_pkgver}_amd64.deb
        ${_url}/amd64/${_pkgname}-plugins_${pkgver}~${_pkgver}_amd64.deb)
sha256sums=('d76236c3fc6868144a2053bf0ad6776efe8b9f03f71ed1067abf7a5334c917d8')
sha256sums_i686=('22cf3fa0a6c3952a60cdaf204ed0528f8ad735e17e626091cf6d5f15852c8b7c'
                 'd708da24e07abd81ea680e7de7fd59e23032082ac07f9db4dc7329b26f55d6b1')
sha256sums_x86_64=('949d4e7dea2fd733cf1ecbc59563b7f5341ad558aa3c647d473def2f79706f0b'
                   'bd6a0d8646988f95b323f198c3f2060654b72b79675decedc42336508344574f')

package() {
    msg2 "Extracting files..."
    cd "$srcdir"
    ar x ${_pkgname}-gtk_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-plugins_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-common_${pkgver}~${_pkgver}_all.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
}

