# $Id:$
# Contributor: (sirocco AT ngs.ru)
# Maintainer: (sirocco AT ngs.ru)

pkgname=doublecmd-gtk2-bin-nightly
_pkgname=doublecmd
pkgver=0.7.0.svn.r6072
_pkgver=0.7.0~0+svn6072
pkgrel=1
pkgdesc="Double Commander is a cross platform open source file manager with two panels side by side. It is inspired by Total Commander and features some new ideas."
arch=('i686' 'x86_64')
url='http://doublecmd.sourceforge.net'
# https://launchpad.net/~alexx2000/+archive/doublecmd-svn
depends=('gtk2')
optdepends=('lua: scripting' 'p7zip: support for 7zip archives' 'libunrar: support for rar archives'
            'pmount: mount removable devices' 'imagemagick: speed up thumbnail view')
install="doublecmd.install"
provides=('doublecmd')
conflicts=('doublecmd-gtk2' 'doublecmd-svn')
license=('GPL2')
options=('!strip')

# temporary to enforce upgrade to pacman 4.2
makedepends=('pacman>=4.2')

_bldarch=amd64
[[ $CARCH = i686 ]] && _bldarch=i386

_url=http://download.opensuse.org/repositories/home:/Alexx2000:/doublecmd-svn/xUbuntu_15.04
source=(${_url}/all/${_pkgname}-common_${_pkgver}_all.deb)
source_i686+=(${_url}/i386/${_pkgname}-gtk_${_pkgver}_i386.deb
        ${_url}/i386/${_pkgname}-plugins_${_pkgver}_i386.deb)
source_x86_64+=(${_url}/amd64/${_pkgname}-gtk_${_pkgver}_amd64.deb
        ${_url}/amd64/${_pkgname}-plugins_${_pkgver}_amd64.deb)
sha256sums=('933190cbadbc26ec5a352167d2f2ed04fad12afe0b4b25280b361c61c69cd830')
sha256sums_i686=('0aea8257f2c55c478e521e92a8bd7aa7464d34571da382c1c3f21830568e9d68'
                 'af977b0d58d7ef025a208e5fde7a97e2c3112491877b6869fbb5ea96bb063a56')
sha256sums_x86_64=('91bd2df1c7db31129d2e5a0eb9061aa6c7730c6b211b860eeec3add503c35abf'
                   'b8e899619d4f546141ded792f5fd27c84c4c673cf00f1f266c0af7da3ccce331')

package() {
    msg2 "Extracting files..."
    cd "$srcdir"
    ar x ${_pkgname}-gtk_${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-plugins_${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-common_${_pkgver}_all.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz

}

