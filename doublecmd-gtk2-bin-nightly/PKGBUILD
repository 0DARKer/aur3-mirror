# $Id:$
# Contributor: (sirocco AT ngs.ru)
# Maintainer: (sirocco AT ngs.ru)

pkgname=doublecmd-gtk2-bin-nightly
_pkgname=doublecmd
pkgver=0.7.0
_pkgver=0+svn5967
pkgrel=967
pkgdesc="Double Commander is a cross platform open source file manager with two panels side by side. It is inspired by Total Commander and features some new ideas."
arch=('i686' 'x86_64')
url='http://doublecmd.sourceforge.net'
# https://launchpad.net/~alexx2000/+archive/doublecmd-svn
depends=('gtk2')
optdepends=('lua: scripting' 'p7zip: support for 7zip archives' 'libunrar: support for rar archives'
            'pmount: mount removable devices' 'imagemagick: speed up thumbnail view')
install="doublecmd.install"
provides=('doublecmd')
conflicts=('doublecmd-gtk2' 'doublecmd-svn')
license=('GPL2')
options=('!strip')

# temporary to enforce upgrade to pacman 4.2
makedepends=('pacman>=4.2')

_bldarch=amd64
[[ $CARCH = i686 ]] && _bldarch=i386

_url=http://download.opensuse.org/repositories/home:/Alexx2000:/doublecmd-svn/xUbuntu_14.10
source=(${_url}/all/${_pkgname}-common_${pkgver}~${_pkgver}_all.deb)
source_i686+=(${_url}/i386/${_pkgname}-gtk_${pkgver}~${_pkgver}_i386.deb
        ${_url}/i386/${_pkgname}-plugins_${pkgver}~${_pkgver}_i386.deb)
source_x86_64+=(${_url}/amd64/${_pkgname}-gtk_${pkgver}~${_pkgver}_amd64.deb
        ${_url}/amd64/${_pkgname}-plugins_${pkgver}~${_pkgver}_amd64.deb)
sha256sums=('951a59595cc618be2e24eb1be38d8af8baed7c500b17c5f4f72e874a8f875422')
sha256sums_i686=('f777bd15fd0b2c994267430aa3719419f862019b2fc23f038c7e79534eb85baf'
                 '09670794ce740576f87051a42d37b3404df3fe8e9b2c290a42f2741f1d34a228')
sha256sums_x86_64=('7f08ec7adbe4102a0c9d6a6b60b36c50ea82f0b69a4a73e3f267ad38baf1a9f5'
                   '3fe7435ee6bd05d1a22907c46fc5e47e18d75227d0c78dc03fe4052468af5a58')

package() {
    msg2 "Extracting files..."
    cd "$srcdir"
    ar x ${_pkgname}-gtk_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-plugins_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-common_${pkgver}~${_pkgver}_all.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
}

