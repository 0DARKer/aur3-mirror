# $Id:$
# Contributor: (sirocco AT ngs.ru)
# Maintainer: (sirocco AT ngs.ru)

pkgname=doublecmd-gtk2-bin-nightly
_pkgname=doublecmd
pkgver=0.7.0
_pkgver=0+svn5790
pkgrel=790
pkgdesc="Double Commander is a cross platform open source file manager with two panels side by side. It is inspired by Total Commander and features some new ideas."
arch=('i686' 'x86_64')
url='http://doublecmd.sourceforge.net'
# https://launchpad.net/~alexx2000/+archive/doublecmd-svn
depends=('gtk2')
optdepends=('lua: scripting' 'p7zip: support for 7zip archives' 'libunrar: support for rar archives'
            'pmount: mount removable devices' 'imagemagick: speed up thumbnail view')
install="doublecmd.install"
provides=('doublecmd')
conflicts=('doublecmd-gtk2' 'doublecmd-svn')
license=('GPL2')
options=('!strip')

# temporary to enforce upgrade to pacman 4.2
makedepends=('pacman>=4.2')

_bldarch=amd64
[[ $CARCH = i686 ]] && _bldarch=i386

_url=http://download.opensuse.org/repositories/home:/Alexx2000:/doublecmd-svn/xUbuntu_14.10
source=(${_url}/all/${_pkgname}-common_${pkgver}~${_pkgver}_all.deb)
source_i686+=(${_url}/i386/${_pkgname}-gtk_${pkgver}~${_pkgver}_i386.deb
        ${_url}/i386/${_pkgname}-plugins_${pkgver}~${_pkgver}_i386.deb)
source_x86_64+=(${_url}/amd64/${_pkgname}-gtk_${pkgver}~${_pkgver}_amd64.deb
        ${_url}/amd64/${_pkgname}-plugins_${pkgver}~${_pkgver}_amd64.deb)
sha256sums=('e38b71968d72eafb537ffb04bbb5cddcb90f2f44a4aedd5f50a30bc2db5508ce')
sha256sums_i686=('675a383534c727802f84b12510d86c2566adc1fa3518090b35b578a34dd07bfe'
                 'b9e180f3be0541823e5ee879b55a49e8f504b11f25224899404a4f32ba54916f')
sha256sums_x86_64=('67b899ff9c2b8d3d17191df79189f1fece1d844ee5f22a422b41833d2b6ae15e'
                   '534fcbfc2f9a0fb6e5c4ee5cf633139e1dcc22c4686032aad3bf4c211633fa09')

package() {
    msg2 "Extracting files..."
    cd "$srcdir"
    ar x ${_pkgname}-gtk_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-plugins_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-common_${pkgver}~${_pkgver}_all.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
}

