# $Id:$
# Contributor: (sirocco AT ngs.ru)
# Maintainer: (sirocco AT ngs.ru)

pkgname=doublecmd-gtk2-bin-nightly
_pkgname=doublecmd
pkgver=0.6.0
_pkgver=0+svn5521
pkgrel=521
#_pkgsuf=~trusty
_pkgsuf=~utopic
pkgdesc="Double Commander is a cross platform open source file manager with two panels side by side. It is inspired by Total Commander and features some new ideas."
arch=('i686' 'x86_64')
url='http://doublecmd.sourceforge.net'
# https://launchpad.net/~alexx2000/+archive/doublecmd-svn
depends=('gtk2')
optdepends=('lua: scripting' 'p7zip: support for 7zip archives' 'libunrar: support for rar archives'
            'pmount: mount removable devices' 'imagemagick: speed up thumbnail view')
install="doublecmd.install"
provides=('doublecmd')
conflicts=('doublecmd-gtk2' 'doublecmd-svn')
license=('GPL2')
options=('!strip')

case ${CARCH} in
  i686)
    _bldarch='i386'
    sha1sums=('cc8f15f6c7ddf25b9b61f2acc2f66014b15cf404'
              '8dd68d111bb7111f91972fa97f8556b2fccd1b4b'
              '2d0360e525d9db3d65132598e1de7cfd2df94507')
    ;;
  x86_64)
    _bldarch='amd64'
    sha1sums=('cc8f15f6c7ddf25b9b61f2acc2f66014b15cf404'
              'f384b542a7af7763ccbdaf0c3ca190532331888e'
              'd44117b18634f1915d5330ef11467a64407cc550')
    ;;
esac

_url=https://launchpad.net/~alexx2000/+archive/doublecmd-svn/+files
source=(${_url}/${_pkgname}-common_${pkgver}-${_pkgver}${_pkgsuf}_all.deb
        ${_url}/${_pkgname}-gtk_${pkgver}-${_pkgver}${_pkgsuf}_${_bldarch}.deb
        ${_url}/${_pkgname}-plugins_${pkgver}-${_pkgver}${_pkgsuf}_${_bldarch}.deb)


package() {
    msg2 "Extracting files..."
    cd "$srcdir"
    ar x ${_pkgname}-gtk_${pkgver}-${_pkgver}${_pkgsuf}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-plugins_${pkgver}-${_pkgver}${_pkgsuf}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-common_${pkgver}-${_pkgver}${_pkgsuf}_all.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
}

