# $Id:$
# Contributor: (sirocco AT ngs.ru)
# Maintainer: (sirocco AT ngs.ru)

pkgname=doublecmd-gtk2-bin-nightly
_pkgname=doublecmd
pkgver=0.7.0
_pkgver=0+svn5960
pkgrel=960
pkgdesc="Double Commander is a cross platform open source file manager with two panels side by side. It is inspired by Total Commander and features some new ideas."
arch=('i686' 'x86_64')
url='http://doublecmd.sourceforge.net'
# https://launchpad.net/~alexx2000/+archive/doublecmd-svn
depends=('gtk2')
optdepends=('lua: scripting' 'p7zip: support for 7zip archives' 'libunrar: support for rar archives'
            'pmount: mount removable devices' 'imagemagick: speed up thumbnail view')
install="doublecmd.install"
provides=('doublecmd')
conflicts=('doublecmd-gtk2' 'doublecmd-svn')
license=('GPL2')
options=('!strip')

# temporary to enforce upgrade to pacman 4.2
makedepends=('pacman>=4.2')

_bldarch=amd64
[[ $CARCH = i686 ]] && _bldarch=i386

_url=http://download.opensuse.org/repositories/home:/Alexx2000:/doublecmd-svn/xUbuntu_14.10
source=(${_url}/all/${_pkgname}-common_${pkgver}~${_pkgver}_all.deb)
source_i686+=(${_url}/i386/${_pkgname}-gtk_${pkgver}~${_pkgver}_i386.deb
        ${_url}/i386/${_pkgname}-plugins_${pkgver}~${_pkgver}_i386.deb)
source_x86_64+=(${_url}/amd64/${_pkgname}-gtk_${pkgver}~${_pkgver}_amd64.deb
        ${_url}/amd64/${_pkgname}-plugins_${pkgver}~${_pkgver}_amd64.deb)
sha256sums=('c02bae0440ed552d3cde4e6ec79ace18ee7047425f3e29151c304ed1406930c7')
sha256sums_i686=('9625bf3bbec3572672c0a18205c719d99eb9d632f6b77a92c1ffa7fe012c2e56'
                 '4ca7a89515ac1220e6d6390aa8c352e67f6a386de87e7bd745200c21465d4e47')
sha256sums_x86_64=('cde54b30abd1b59ad2e8815b2a38dd94822348305f4f735fc1132e437a07a36a'
                   '4ae0748785c4085da7dd93c4116da88decb18aacfec9ea3cbc826508cdc7601d')

package() {
    msg2 "Extracting files..."
    cd "$srcdir"
    ar x ${_pkgname}-gtk_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-plugins_${pkgver}~${_pkgver}_${_bldarch}.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
    ar x ${_pkgname}-common_${pkgver}~${_pkgver}_all.deb
    tar xf data.tar.xz --exclude lintian -C $pkgdir
    rm -f data.tar.xz
}

