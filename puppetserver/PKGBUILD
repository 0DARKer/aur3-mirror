# puppet-server: Installer: Arch
# Maintainer: Niels Abspoel <aboe76 (at) Gmail (dot) com>

# RC style, reserved for later use
##pkgname=puppet-server
#_rc=1
#_pkgver=2.0.0
#pkgver=${_pkgver}_rc${_rc}

pkgname=puppetserver
pkgver=1.0.0
pkgrel=3
pkgdesc="Puppet master Server"
arch=("any")
url="https://docs.puppetlabs.com/puppetserver/latest/services_master_puppetserver.html"
license=("APACHE")
depends=("ruby" "puppet" "jre7-openjdk" "ruby-rgen" "logrotate")
provides=("puppetserver" "puppet-server")
backup=('etc/puppetserver/conf.d/puppetserver.conf'
        'etc/puppetserver/conf.d/webserver.conf'
        'etc/puppetserver/conf.d/os-settings.conf'
        'etc/puppetserver/conf.d/global.conf')
install="puppetserver.install"
source=("http://downloads.puppetlabs.com/puppet/$pkgname-${pkgver//_/-}.tar.gz")
md5sums=('fc425c62e115ce6a93662b289d370377')
package () {
  cd "${srcdir}/${pkgname}-${pkgver//_/-}"

  #params for build and configure:
  unset DESTDIR
  unset prefix
  unset puppet_libdir
  export prefix=/usr
  export DESTDIR=${pkgdir}
  puppet_libdir=`ruby -rrbconfig -e 'puts RbConfig::CONFIG["vendorlibdir"]'`

  # install puppet-server 
  make -e install-puppetserver

  # os-settings.conf
  echo "os-settings: {" > "${pkgdir}/etc/${pkgname}/conf.d/os-settings.conf"
  echo "    ruby-load-path: [${puppet_libdir}]" >> "${pkgdir}/etc/${pkgname}/conf.d/os-settings.conf"
  echo "}" >> "${pkgdir}/etc/${pkgname}/conf.d/os-settings.conf"
  
  #create logdir:
  mkdir -p "${pkgdir}/var/log/${pkgname}"

  #create logrotate files
  mkdir -p "${pkgdir}/etc/logrotate.d"
  install -D -m644 "${srcdir}/${pkgname}-${pkgver//_/-}/ext/puppetserver.logrotate.conf" "${pkgdir}/etc/logrotate.d/puppetserver"

  #create jruby-gems
  mkdir -p "$pkgdir"/var/lib/puppet/jruby-gems

  #archlinux specific systemd because of java binary in other place.
  install -D -m644 "${srcdir}/${pkgname}-${pkgver//_/-}/ext/redhat/puppetserver.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  install -D -m644 "${srcdir}/${pkgname}-${pkgver//_/-}/ext/default" "${pkgdir}/etc/sysconfig/${pkgname}"

  # remove $pkgdir/var/run because it is provided by filesystem
  rmdir "$pkgdir/var/run"

}
# vim: set ft=sh ts=2 sw=2 et:
