# Maintainer: Luka Perkov <luka.perkov@sartura.hr>

pkgname=ubus-lua-git
_gitname=ubus
pkgver=r193.f688c7a
# commit 1d5ac421a5b3dca60562e876ba70d0c2fe46b3d2
pkgrel=1
pkgdesc='An embedded bus system developed for OpenWrt. It is like dbus but simple and small.'
url='http://nbd.name/gitweb.cgi?p=luci2/ubus.git'
arch=('i686' 'x86_64')
license=('LGPLv2.1')
depends=('libubox-lua-git' 'json-c' 'lua51')
makedepends=('git' 'cmake' 'gcc' 'make' 'patch' 'pkg-config')
conflicts=('ubus' 'ubus-git')
provides=('ubus')
source=('git://nbd.name/luci2/ubus.git'
	'001-lua-version.patch')
md5sums=('SKIP'
	'34e0767e6d1df56d188b1e18d91ef46f')

pkgver() {
	cd "$srcdir/$_gitname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_gitname"

	patch -p1 -i "$srcdir/001-lua-version.patch"
}

build() {
	cd "$srcdir/$_gitname"

	cmake CMakeLists.txt \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_EXAMPLES=OFF \
		-DBUILD_LUA=ON

	make
}

package() {
	cd "$srcdir/$_gitname"

	make DESTDIR="$pkgdir" install
}
