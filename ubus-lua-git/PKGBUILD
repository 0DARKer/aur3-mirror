# Maintainer: Luka Perkov <luka.perkov@sartura.hr>

pkgname=ubus-lua-git
_gitname=ubus
pkgver=r168.29d7092
# commit 29d7092252a10c11ac137e38c871e301e26a8c11
pkgrel=1
pkgdesc='An embedded bus system developed for OpenWrt. It is like dbus but simple and small.'
url='http://nbd.name/gitweb.cgi?p=luci2/ubus.git'
arch=('i686' 'x86_64')
license=('LGPLv2.1')
depends=('libubox-lua-git' 'json-c' 'lua51')
makedepends=('git' 'cmake' 'gcc' 'make')
conflicts=('ubus' 'ubus-git')
provides=('ubus')
source=('git://nbd.name/luci2/ubus.git'
	'001-lua-version.patch'
	'0001-ubus-lua-allow-methods-with-no-arguments.patch'
	'0001-ubus-lua-fix-whitespaces-and-typo.patch')
md5sums=('SKIP'
	'34e0767e6d1df56d188b1e18d91ef46f'
	'2d9d1ead7b9a7eefed07b404a4fcbf64'
	'47fe8d4d30d07235448caabdc4d30a9b')

pkgver() {
	cd "$srcdir/$_gitname"

	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "$srcdir/$_gitname"

	patch -p1 -i "$srcdir/001-lua-version.patch"
	patch -p1 -i "$srcdir/0001-ubus-lua-allow-methods-with-no-arguments.patch"
	patch -p1 -i "$srcdir/0001-ubus-lua-fix-whitespaces-and-typo.patch"
}

build() {
	cd "$srcdir/$_gitname"

	cmake CMakeLists.txt \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_LUA=ON

	make
}

package() {
	cd "$srcdir/$_gitname"

	make DESTDIR="$pkgdir" install
}
