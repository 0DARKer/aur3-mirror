# Maintainer: Daniel Kirchner <daniel at ekpyron dot org>

pkgname=mingw32-assimp
_basename=assimp
pkgver=3.0.1270
pkgrel=1
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner (mingw32)"
arch=('i686' 'x86_64')
license=('BSD')
depends=('mingw32-runtime' 'mingw32-boost')
makedepends=('cmake' 'mingw32-gcc')
url=('http://assimp.sourceforge.net/index.html')
source=("http://downloads.sourceforge.net/project/assimp/assimp-3.0/assimp--${pkgver}-source-only.zip")
options=(!strip !buildflags)
sha1sums=('e80a3a4326b649ed6585c0ce312ed6dd68942834')

_targetarch=i486-mingw32

build() 
{
	unset LDFLAGS
	cd ${srcdir}

	rm -rf build
	mkdir build
	cd build

    echo "SET(CMAKE_SYSTEM_NAME Windows)" > win32.cmake
    echo "SET(CMAKE_C_COMPILER ${_targetarch}-gcc)" >> win32.cmake
    echo "SET(CMAKE_CXX_COMPILER ${_targetarch}-g++)" >> win32.cmake
    echo "SET(CMAKE_RC_COMPILER ${_targetarch}-windres)" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH /usr/${_targetarch})" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)" >> win32.cmake
    echo "SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)" >> win32.cmake	
	
	cmake ../${_basename}--${pkgver}-source-only \
		-DCMAKE_TOOLCHAIN_FILE=win32.cmake \
		-DCMAKE_INSTALL_PREFIX="/usr/${_targetarch}" \
		-DBUILD_ASSIMP_TOOLS=NO -DBUILD_STATIC_LIB=ON \
		-DCMAKE_BUILD_TYPE=RELEASE
	make 
}

package() 
{
	cd ${srcdir}/build
	make DESTDIR=$pkgdir install
	cd ${srcdir}/${_basename}--${pkgver}-source-only
	install -Dm644 LICENSE ${pkgdir}/usr/${_targetarch}/share/licenses/${_basename}/LICENSE
}
