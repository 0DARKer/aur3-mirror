# Maintainer: Tom Richards <tom@tomrichards.net>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=telegram-git
pkgver=r240.4b6870f
pkgrel=1
pkgdesc="Telegram messenger CLI"
arch=('i686' 'x86_64')
url="https://github.com/vysheng/tg"
license=('GPL2')
depends=('libconfig' 'lua')
makedepends=('git')
conflicts=('telegram')
provides=('telegram')

source=("$pkgname"::'git+https://github.com/vysheng/tg')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/bin --sysconfdir=/etc --sharedstatedir=/etc --localstatedir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname"
  install -Dm755 telegram ${pkgdir}/usr/bin/telegram
}
