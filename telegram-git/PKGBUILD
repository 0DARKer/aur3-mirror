# Maintainer: Tom Richards <tom@tomrichards.net>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=telegram-git
pkgver=r305.c5333c0
pkgrel=1
pkgdesc="Telegram messenger CLI"
arch=('i686' 'x86_64')
url="https://github.com/vysheng/tg"
license=('GPL2')
depends=('libconfig' 'lua')
makedepends=('git')
conflicts=('telegram')
provides=('telegram')

source=("$pkgname"::'git+https://github.com/vysheng/tg' 'server.pub')
sha256sums=('SKIP'
           'a01e4c76de063717763dad04b6fb9234a31a54facf8caf9b70811111fb18c24b')

pkgver() {
  cd "$srcdir/$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$pkgname"
  ./configure --prefix=/usr --sbindir=/usr/bin --libexecdir=/usr/bin --sysconfdir=/etc --sharedstatedir=/etc --localstatedir=/etc
  make
}

package() {
  install -Dm644 server.pub ${pkgdir}/etc/telegram/server.pub

  cd "$srcdir/$pkgname"
  install -Dm755 telegram ${pkgdir}/usr/bin/telegram
}
