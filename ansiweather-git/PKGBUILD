# Maintainer: Andrew Gallant <jamslam@gmail.com>
_pkgname=ansiweather
pkgname=$_pkgname-git
pkgver=r96.747f268
pkgrel=1
pkgdesc="Weather in your terminal, with ANSI colors and Unicode symbols."
arch=('any')
url="https://github.com/fcambus/ansiweather"
license=('BSD')
depends=('jq' 'bc')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+https://github.com/fcambus/ansiweather")
md5sums=('SKIP')

package() {
  cd "$_pkgname"
  install -D -m 755 ansiweather "$pkgdir/usr/bin/ansiweather"
  for f in AUTHORS ChangeLog LICENSE README.md; do
    install -D -m 644 "$f" "$pkgdir/usr/share/doc/ansiweather/$f"
  done
}

pkgver() {
  cd "$_pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

# vim:set ts=2 sw=2 et:
