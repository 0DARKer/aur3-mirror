# Maintainer  : Fernando "Firef0x" G.P. da Silva <firefgx { aT ) gmail [ d0t } com>
# Contributor : Fernando "Firef0x" G.P. da Silva <firefgx { aT ) gmail [ d0t } com>

pkgname=autoshadower
pkgver=0.1.1.20150208.b0fcde9
pkgrel=1
pkgdesc="Find many free shadowsocks accounts for you"
arch=('i686' 'x86_64')
url='https://github.com/yangyangwithgnu/autoshadower'
license=('MIT')
depends=('curl' 'shadowsocks')
makedepends=('cmake' 'git')
changelog=README.md

source=("git+https://github.com/yangyangwithgnu/${pkgname}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  ( set -o pipefail
  printf "0.1.1.%s.%s" "$(git log -1 --pretty=format:%cd --date=short | sed 's/-//g')" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd "${pkgname}/build"

  cmake .
  make
}

package() {
  cd "${pkgname}/build"

  # Install bin file
  make DESTDIR="${pkgdir}" install

  cd ..

  # Install documentation
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"

  # Install license file
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sts=2 sw=2 ts=2 et:
