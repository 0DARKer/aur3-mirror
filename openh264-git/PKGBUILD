# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=openh264-git
pkgver=20131227.71e4821
pkgrel=1
pkgdesc='Open Source H.264 Codec'
arch=('i686' 'x86_64')
url='http://www.openh264.org/'
license=('BSD')
depends=('gcc-libs')
makedepends=('git' 'nasm')
options=('staticlibs')
source=(git://github.com/cisco/openh264.git)
md5sums=('SKIP')

pkgver() {
  cd openh264
  echo $(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-parse --short HEAD)
}

build() {
  cd openh264

  if [[ $CARCH == x86_64 ]]; then
    make all ENABLE64BIT=Yes
  else
    make all
  fi
}

package() {
  cd openh264

  install -Dm755 h264dec "$pkgdir"/usr/bin/h264dec
  install -Dm755 h264enc "$pkgdir"/usr/bin/h264enc
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  for lib in *.a; do
    install -Dm644 $lib "$pkgdir"/usr/lib/$lib
  done
}
