# Maintainer: Felix E. xelif@icqmail.com

# Upstream name of extension:
_extname=Math
# Variant valid as package name:
_extpkgname=math

pkgname=mediawiki-$_extpkgname-git
pkgver=r728.726edfc
pkgrel=3
pkgdesc="A MediaWiki extension for math typesetting."
install=mediawiki-math-git.install
source=("git+https://gerrit.wikimedia.org/r/mediawiki/extensions/$_extname"
        "mediawiki-math-git.excludelist")
md5sums=("SKIP"
         "441fd0e6c2d37ac939e686b8b2b2478c")
arch=(i686 x86_64)
url="http://www.mediawiki.org/wiki/Extension:$_extname"
license=("GPL")
depends=(mediawiki texvc)
optdepends=()
makedepends=(git make ocaml rsync)
provides=("mediawiki-$_extpkgname")
conflicts=()

pkgver() {
  cd $_extname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  # Target extension directory of MediaWiki:
  _extdir="$pkgdir/usr/share/webapps/mediawiki/extensions/$_extname"
  mkdir -p "$_extdir"
  rsync -a "$_extname/" "$_extdir/" --exclude /.git --exclude-from "mediawiki-math-git.excludelist"
}

build() {
  cd "$srcdir/$_extname/texvccheck"
  make
}