# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>

pkgname=ps3remote-bluez
pkgver=4.96
pkgrel=2
pkgdesc="Libraries and tools for the Bluetooth protocol stack with kitlaan's ps3remote patch applied."
url="http://www.bluez.org/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dbus-core')
makedepends=('gstreamer0.10-base' 'libusb-compat' 'libnl' 'libsndfile')
optdepends=("gstreamer0.10-base: bluetooth GStreamer support"
	"alsa-lib: Audio bluetooth devices support"
	"dbus-python: to run bluez-simple-agent"
	"pygobject: to run bluez-simple-agent"
	"libusb-compat: USB adapters support"
	"cups: CUPS backend"
	"libnl: netlink plugin")
conflicts=('bluez' 'bluez-libs' 'bluez-utils')
provides=('bluez=4.96' 'bluez-libs' 'bluez-utils')
replaces=('bluez' 'bluez-libs' 'bluez-utils')
options=('!libtool' 'emptydirs')
backup=(etc/bluetooth/{main,rfcomm,audio,network,input,serial}.conf
        'etc/conf.d/bluetooth' 'etc/dbus-1/system.d/bluetooth.conf')
#source=("http://www.kernel.org/pub/linux/bluetooth/${pkgname}-${pkgver}.tar.bz2"
source=('http://www.kernel.org/pub/linux/bluetooth/bluez-4.96.tar.bz2'
        'http://kitlaan.twinaxis.com/projects/bluez-ps3remote/bluez_ps3remote_4.91.diff'
        'bluetooth.conf.d'
        'rc.bluetooth')
md5sums=('255c3dda8b93210ab0dcf8d04ddaf69f'
         'f4993b84d3ec9a391e811449e69774e1'
         '7412982b440f29fa7f76a41a87fef985'
         '8f9498707f809506928b2e480d3b6789')

build() {
  cd "${srcdir}/bluez-4.96"
  
  patch -p1 -i $srcdir/bluez_ps3remote_4.91.diff

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/lib \
    --enable-gstreamer \
    --enable-alsa \
    --enable-usb \
    --enable-tools \
    --enable-bccmd \
    --enable-dfutool \
    --enable-hid2hci \
    --enable-hidd \
    --enable-pand \
    --enable-dund \
    --enable-cups \
    --enable-wiimote
  make
}

package() {
  cd ${srcdir}/bluez-4.96
  make DESTDIR=${pkgdir} install

  install -Dm755 ${srcdir}/rc.bluetooth ${pkgdir}/etc/rc.d/bluetooth
  
  install -d ${pkgdir}/etc/bluetooth
  install -m644 network/network.conf \
                input/input.conf \
                audio/audio.conf \
                serial/serial.conf \
    ${pkgdir}/etc/bluetooth/
  
  install -Dm644 ${srcdir}/bluetooth.conf.d \
    ${pkgdir}/etc/conf.d/bluetooth

  # FS#27630
  install -Dm755 test/simple-agent "${pkgdir}"/usr/bin/bluez-simple-agent
  install -Dm755 test/test-device "${pkgdir}"/usr/bin/bluez-test-device
  install -Dm755 test/test-input "${pkgdir}"/usr/bin/bluez-test-input
  sed -i 's#/usr/bin/python#/usr/bin/python2#' \
    "${pkgdir}"/usr/bin/bluez-simple-agent \
    "${pkgdir}"/usr/bin/bluez-test-device \
    "${pkgdir}"/usr/bin/bluez-test-input

  # http://mailman.archlinux.org/pipermail/arch-general/2011-April/019787.html
  rm "${pkgdir}"/lib/udev/rules.d/97-bluetooth.rules
}

