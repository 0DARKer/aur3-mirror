pkgname=mingw-w64-tools
pkgver=3.1.0
pkgrel=1
pkgdesc="MinGW-w64 gendef and genidl tools"
arch=(i686 x86_64)
url="http://mingw-w64.sourceforge.net"
license=("custom")
groups=(mingw-w64)
options=(!libtool !emptydirs)
source=("http://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v${pkgver}.tar.bz2")
md5sums=('df0e7657f46cbd59ed9cbe2a50c66e15')

build() {
  cd ${srcdir}
  msg "Building MinGW-w64-tools"
  mkdir -p "${srcdir}"/gendef-build && cd "${srcdir}"/gendef-build
    "${srcdir}"/mingw-w64-v${pkgver}/mingw-w64-tools/gendef/configure --prefix=/usr
    make
  mkdir -p "${srcdir}"/genidl-build && cd "${srcdir}"/genidl-build
    "${srcdir}"/mingw-w64-v${pkgver}/mingw-w64-tools/genidl/configure --prefix=/usr
    make
}

package() {
  cd "${srcdir}/gendef-build"
  make DESTDIR="${pkgdir}" install
  cd ../genidl-build
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/mingw-w64-v${pkgver}/mingw-w64-tools/gendef/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.gendef"
  install -Dm644 "${srcdir}/mingw-w64-v${pkgver}/mingw-w64-tools/genidl/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING.genidl"
}

