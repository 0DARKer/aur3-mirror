# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
_gemname=fetcher
pkgname=ruby-fetcher
pkgver=0.4.4
pkgrel=1
pkgdesc="Fetch Text Documents or Binary Blobs via HTTP, HTTPS"
arch=(any)
url="https://github.com/rubylibs/fetcher"
license=('custom')
depends=(ruby)
makedepends=(rubygems)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem LICENSE)
noextract=($_gemname-$pkgver.gem)
sha256sums=('92e765e6eef8dc96e9d86be9b619cde02233c207bef8b5832a5c6dbd87712bf9'
            '4fde9916d36bdc2120a09693d9015af0f0d0b970635b7516476a3dbeb52b55ff')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

