# $Id$
# Maintainer: Joschka Thurner <Joschka.Thurner@gmail.com>
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>


pkgname=ipython-git
pkgver=20130317
pkgrel=1
pkgdesc="An enhanced Interactive Python 3 shell in its current version from github."
arch=('any')
url="http://ipython.org"
license=('Modified BSD')
makedepends=('python' 'git') # for setup.py
depends=('python' 'sqlite' 'python-distribute')
optdepends=("wxpython: needed for ipythonx and ipython-wx"
            "python-nose: if you want to run IPython's test suite"
            "pyqt: for ipython-qtconsole"
            "sip: for ipython-qtconsole"
            "python-pygments: for ipython-qtconsole"
            "python-pyzmq: for ipython-qtconcole"
            "python-tornado: for ipython notebook")
conflicts=('ipython3' 'ipython')
provides=('ipython3' 'ipython')

_gitroot="git://github.com/ipython/ipython.git"
_gitname="ipython"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
  find "$pkgdir/" -type d -empty -delete

  install -Dm644 docs/source/about/license_and_copyright.txt "$pkgdir/usr/share/licenses/ipython/LICENSE"

  cd "$pkgdir/usr/bin/"
  mv ipcluster3        ipcluster
  mv ipcontroller3     ipcontroller
  mv ipengine3         ipengine
  mv iplogger3         iplogger
  mv iptest3           iptest
  mv ipython3          ipython
  mv irunner3          irunner
  mv pycolor3          pycolor 
}
