# Maintainer: SJ_UnderWater

pkgname=mdnsresponder
true && pkgname=('mdnsresponder' 'nss-mdnsresponder')
pkgver=320.10.80
pkgrel=0
pkgdesc="Apple's official implementation of mDNS/DNS-SD/Bonjour/Zeroconf"
arch=('i686' 'x86_64')
url='https://developer.apple.com/opensource/'
license=('APACHE')
depends=('glibc')
options=('!libtool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(mdnsd nss-mdnsresponder.install http://www.opensource.apple.com/tarballs/mDNSResponder/mDNSResponder-$pkgver.tar.gz)
md5sums=('30269a992ca9f5c3c9f0036e903de0ee'
	'fa87aeb60f79a34fe26d570c16494844'
	'2f88bd0edae86ff2442827182dd1a394')

build() {
	if [ "$(echo $srcdir|grep -E '[ "]')" ];then
	error 'Spaces found in path, this will break the Makefile';exit 1;fi
	cd "$srcdir"/mDNSResponder-$pkgver/mDNSPosix
	if [ ! -f link ];then
	msg2 'Fixing Makefile...'
	sed -i 's:bin/sh:bin/bash:' mdnsd.sh
	sed -i 's/cp \$/$(CP) $/' Makefile
	sed -i 's;NSSINSTPATH := ;NSSINSTPATH := $(DESTDIR)/usr;' Makefile
	sed -i 's/INSTBASE?=/INSTBASE?=$(DESTDIR)/' Makefile
	sed -i 's/STARTUPSCRIPTNAME?=mdns/STARTUPSCRIPTNAME?=mdnsd/' Makefile
	sed -i 's/MANPATH := /MANPATH := $(DESTDIR)/' Makefile
	sed -i 's:STARTUPSCRIPTDIR = $(INSTBASE)/etc/rc.d:STARTUPSCRIPTDIR = $(DESTDIR)/etc/rc.d:' Makefile
	sed -i 's:/etc/nss_mdns.conf:$(DESTDIR)/etc/nss_mdns.conf:' Makefile
	sed -i 's/cp -f/#cp -f/' Makefile
	sed -i 's/sed -e/#sed -e/' Makefile
	sed -i 's/\tldconfig/\t$(LDCONFIG)/' Makefile
	sed -i 's/\$@ start/#$@ start/' Makefile
	echo -e '\n$(STARTUPSCRIPTDIR): $(DESTDIR)\n\tmkdir -p $<' >>Makefile
	echo -e '#!/bin/bash\nmkdir -p `dirname $2`\ncp $1 $2' >copy
	echo -e '#!/bin/bash\npushd `dirname $1`\nln -s -f `basename $1` `basename $2`\npopd' >link;fi
	msg2 'Making...'
	make os=linux
}
package_mdnsresponder() {
	conflicts=()
	cd "$srcdir"/mDNSResponder-$pkgver/mDNSPosix
	msg2 'Building...'
	make os=linux CP=". $PWD/copy" LN=". $PWD/link" LDCONFIG= DESTDIR="$pkgdir" OPTINSTALL= install
	install -m755 "$srcdir"/mdnsd "$pkgdir"/etc/rc.d
}
package_nss-mdnsresponder() {
	provides=('nss-mdns')
	conflicts=('nss-mdns')
	true && pkgdesc="Apple's official implementation of mdns host name resolution"
	install=$pkgname.install
	cd "$srcdir"/mDNSResponder-$pkgver/mDNSPosix
	msg2 'Building...'
	make os=linux CP=". $PWD/copy" LN=". $PWD/link" LDCONFIG= DESTDIR="$pkgdir" InstalledNSS
}
