# Maintainer: VojtÄ›ch Aschenbrenner <v@asch.cz>

_gemname=sup
pkgname=sup
pkgver=0.17.0
pkgrel=1
pkgdesc="Sup is a console-based email client for people with a lot of email."
arch=(any)
url="http://supmua.org/"
license=('GPL')
depends=('ruby-chronic-old' 'ruby-highline' 'ruby-locale' 'ruby-lockfile' 'ruby-mime-types-1' 'ruby-ncursesw' 'ruby-rmail-sup' 'ruby-trollop' 'ruby-unicode' 'ruby-xapian-ruby')
optdepends=('ruby-gpgme: PGP support')
conflicts=(sup-git)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha512sums=('71306e442c77a75bd61b5498897c44da180fe104917a291fbc0fa8eff19d8ba1186db3894be53e0329043d9a9ecc4c2435fdf27c6102cd80ae50ef222da3de46')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"
}

# vim:set ts=2 sw=2 et:
