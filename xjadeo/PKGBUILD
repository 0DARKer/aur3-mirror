# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: N30N <archlinux@alunamation.com>

pkgname=xjadeo
pkgver=0.6.0_rc7
pkgrel=1
pkgdesc="A simple video player that is synchronized to jack transport."
arch=('i686' 'x86_64')
url="http://xjadeo.sourceforge.net"
license=('GPL')
depends=('ffmpeg' 'imlib2' 'jack' 'liblo' 'libxpm' 'portmidi')
makedepends=('imlib' 'qt')
optdepends=('qt: for the GUI (qjadeo)')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver/_/-}.tar.gz" \
	"qjadeo.desktop")
install="$pkgname.install"
md5sums=('cfcd7c05e3ab466f5c1b7715487eb738'
         '315e2ab44ce3edf4068ff6db48942908')

build() {
  cd "${srcdir}/${pkgname}-${pkgver/_/-}"

  ./configure --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver/_/-}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 doc/xjadeorc "${pkgdir}/etc/xjadeorc"
  install -Dm644 doc/xjadeo.png "${pkgdir}/usr/share/pixmaps/qjadeo.png"
  install -Dm644 ../qjadeo.desktop "${pkgdir}/usr/share/applications/qjadeo.desktop"
}

# vim: set noet ff=unix: