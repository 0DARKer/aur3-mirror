# Maintainer: Patryk Szalanski <patryk.szalanski+aur@gmail.com>

_name=meld
pkgname=$_name-git
pkgver=3.11.0.21.g1ac8ecc
pkgrel=1
pkgdesc='Visual diff and merge tool'
arch=('any')
url='http://meldmerge.org/'
license=('GPL')
depends=('pygtk'
		'hicolor-icon-theme'
		'desktop-file-utils'
		'dconf'
		'gtksourceview3')
makedepends=('intltool' 'gnome-doc-utils' 'git' 'gnome-common' 'itstool')
optdepends=('python2-dbus: open a new tab in an already running instance'
            'python2-gconf: gnome integration')
provides=($_name)
conflicts=($_name)
install=meld.install
options=('!emptydirs')
source=("git://git.gnome.org/$_name")
sha256sums=('SKIP')

pkgver() {
  cd $_name
  git describe --tags | sed s/-/./g
}

build() {
  cd $_name
  python2 setup.py build
}

package() {
  cd $_name
  NO_COMPILE_SCHEMAS=1 python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  
  # Missing files; required
  install -d "${pkgdir}"/usr/share/{glib-2.0/schemas,icons}
  cp -r data/icons/hicolor data/icons/HighContrast "${pkgdir}/usr/share/icons"
  cp data/org.gnome.meld.gschema.xml "${pkgdir}/usr/share/glib-2.0/schemas"
}
