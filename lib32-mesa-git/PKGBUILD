# Maintainer: Jesse Jaara <gmail.com: jesse.jaara>
# Maintainer: Tavian Barnes <tavianator@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>


pkgbase=lib32-mesa-git
pkgname=lib32-mesa-git
true && pkgname=('lib32-mesa-git' 'lib32-osmesa-git' 'lib32-libgl-git'
                 'lib32-libglapi-git' 'lib32-libgles-git' 'lib32-libegl-git'
                 'lib32-ati-dri-git' 'lib32-intel-dri-git'
                 'lib32-nouveau-dri-git' 'lib32-svga-dri-git')
pkgver=20130119
_mesaver=9.1
pkgrel=1
arch=(i686 x86_64)
makedepends=('git' 'glproto' 'lib32-libdrm' 'lib32-libxxf86vm'
             'lib32-libxdamage' 'lib32-expat' 'lib32-libx11'
             'lib32-libxt' 'gcc-multilib' 'dri2proto' 'python2'
             'lib32-libxml2' 'imake' 'lib32-talloc' 'lib32-llvm'
             'lib32-llvm-amdgpu-git' 'lib32-vdpau' 'patchelf')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=(!libtool)

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export LLVM_CONFIG="/usr/bin/llvm-config32"

  msg 'Connecting to git.freedesktop.org GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
  else
    git clone ${_gitroot} --depth=1
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd ${srcdir}

  # Cleanup and prepare the build dir
  rm -rf build
  cp -r ${_gitname} build
  cd build

##Disable opencl for now. We need up-to-date and buildale version of this.
##And the clang lib problem has to be solved 1st.

  ./autogen.sh --prefix=/usr \
    --with-dri-driverdir=/usr/lib32/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --enable-gallium-llvm \
    --enable-r600-llvm-compiler \
    --enable-gallium-egl\
    --enable-shared-glapi \
    --enable-glx-tls \
    --enable-gles1 \
    --enable-gles2 \
    --enable-egl \
    --enable-dri \
    --enable-glx \
    --enable-xa \
    --enable-osmesa \
    --enable-gbm \
    --enable-texture-float \
    --enable-vdpau \
    --enable-32-bit  \
    --libdir=/usr/lib32

  make
}

package_lib32-libgl-git() {
  depends=('lib32-libdrm-git' 'lib32-libxxf86vm' 'lib32-libxdamage' 'lib32-expat' \
	   'lib32-libglapi-git' 'lib32-gcc-libs')
  conflicts=('lib32-libgl')
  provides=("lib32-libgl=${_mesaver}")
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"

  cd "${srcdir}"/build
  install -m755 -d "${pkgdir}/usr/lib32"
  install -m755 -d "${pkgdir}/usr/lib32/xorg/modules/extensions"

  install lib32/libGL.so* "${pkgdir}/usr/lib32/"
  install lib32/libdricore* "${pkgdir}/usr/lib32/"

  cd src/mesa/drivers/dri
  make -C "${srcdir}"/build/src/gallium/targets/dri-swrast DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/gallium/targets/vdpau-softpipe DESTDIR="${pkgdir}" install
  ln -s libglx.xorg "${pkgdir}/usr/lib32/xorg/modules/extensions/libglx.so"
}

package_lib32-osmesa-git() {
  depends=('lib32-mesa-git')
  conflicts=('lib32-osmesa')
  provides=("lib32-osmesa=${_mesaver}")
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  pkgdesc="Mesa 3D off-screen rendering library"

  cd "${srcdir}/build/"

  mkdir -p "${pkgdir}/usr/lib32/pkgconfig"
  cp src/mesa/drivers/osmesa/.libs/libOSMesa.so.8.0.0 lib32/
  patchelf --set-rpath "" lib32/libOSMesa.so.8.0.0
  install lib32/libOSMesa.so* "${pkgdir}/usr/lib32/"
  install src/mesa/drivers/osmesa/osmesa.pc "${pkgdir}/usr/lib32/pkgconfig"
}


package_lib32-libglapi-git() {
  depends=('lib32-glibc' 'libglapi-git')
  conflicts=('lib32-libglapi')
  provides=("lib32-libglapi=${_mesaver}")
  pkgdesc="free implementation of the GL API -- shared library. The Mesa GL API module is responsible for dispatching all the gl* functions"

  cd "${srcdir}"/build
  install -m755 -d "${pkgdir}/usr/lib32"
  install lib32/libglapi.so* "${pkgdir}/usr/lib32"
}

package_lib32-libgles-git() {
  depends=('lib32-libglapi-git' 'khrplatform-devel-git')
  conflicts=('lib32-libgles')
  provides=("lib32-libgles=${_mesaver}")
  pkgdesc="Mesa GLES libraries and headers"

  cd "${srcdir}"/build
  install -m755 -d "${pkgdir}/usr/lib32"
  install lib32/libGLESv* "${pkgdir}/usr/lib32/"
}

package_lib32-libegl-git() {
  depends=('lib32-libglapi-git' 'lib32-libdrm-git' 'lib32-libxext' 'lib32-libxfixes'
           'lib32-systemd' 'libegl-git')
  conflicts=('lib32-libegl')
  provides=("lib32-libegl=${_mesaver}")
  pkgdesc="Mesa libEGL libraries and headers"

  cd "${srcdir}"/build
#  make -C src/gallium/targets/egl-static DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib32"
  install -m755 -d "${pkgdir}/usr/lib32/egl"
  install lib32/libEGL.so* "${pkgdir}/usr/lib32/"
  install lib32/egl/* "${pkgdir}/usr/lib32/egl/"
}

package_lib32-ati-dri-git() {
  depends=('lib32-libgl-git' 'ati-dri-git' 'lib32-llvm-amdgpu-git' 'lib32-vdpau')
  conflicts=('lib32-ati-dri')
  provides=("lib32-ati-dri=${_mesaver}")
  pkgdesc="Mesa DRI + Gallium3D r300 drivers for AMD/ATI Radeon"

  # Fix libtool relink issue
  install -m755 -d "${pkgdir}/usr/lib32"
  cp "${srcdir}"/build/lib32/libdricore* "${pkgdir}/usr/lib32/"

  cp "${srcdir}"/build/src/gallium/drivers/radeon/.libs/libllvmradeon9.1.0.so "${srcdir}/build/lib32/"
  sed "s|-ldl -lm|-L${srcdir}/build/lib32/ -ldl -lm|" -i "${srcdir}"/build/src/gallium/targets/dri-r600/r600_dri.la
  sed "s|-ldl -lm|-L${srcdir}/build/lib32/ -ldl -lm|" -i "${srcdir}"/build/src/gallium/targets/dri-radeonsi/radeonsi_dri.la
  sed "s|-ldl -lm|-L${srcdir}/build/lib32/ -ldl -lm|" -i "${srcdir}"/build/src/gallium/targets/vdpau-r600/libvdpau_r600.la
  sed "s|-ldl -lm|-L${srcdir}/build/lib32/ -ldl -lm|" -i "${srcdir}"/build/src/gallium/targets/vdpau-radeonsi/libvdpau_radeonsi.la

  make -C "${srcdir}"/build/src/mesa/drivers/dri/radeon DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/mesa/drivers/dri/r200 DESTDIR="${pkgdir}" install
  # classic mesa driver for r200 r200_dri.so
  # gallium3D driver for r300-shouthern islands
  make -C "${srcdir}"/build/src/gallium/targets/dri-r300 DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/gallium/targets/dri-r600 DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/gallium/targets/dri-radeonsi DESTDIR="${pkgdir}" install
  # vdpau driver
  make -C "${srcdir}"/build/src/gallium/targets/vdpau-r300 DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/gallium/targets/vdpau-r600 DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/gallium/targets/vdpau-radeonsi DESTDIR="${pkgdir}" install

  # Remove libdricore
  rm "${pkgdir}"/usr/lib32/libdricore*
  # Copy libllvmradeon to the package
  mv "${srcdir}/build/lib32/libllvmradeon9.1.0.so" "${pkgdir}/usr/lib32/"
}

package_lib32-intel-dri-git() {
  depends=('lib32-libgl-git' 'intel-dri-git')
  conflicts=('lib32-intel-dri')
  provides=("lib32-intel-dri=${_mesaver}")
  pkgdesc="Mesa DRI drivers for Intel"

  # Fix libtool relink issue
  install -m755 -d "${pkgdir}/usr/lib32"
  cp "${srcdir}"/build/lib32/libdricore* "${pkgdir}/usr/lib32/"

  make -C "${srcdir}"/build/src/mesa/drivers/dri/i915 DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/mesa/drivers/dri/i965 DESTDIR="${pkgdir}" install

  # Remove libdricore
  rm "${pkgdir}"/usr/lib32/libdricore*
}

package_lib32-nouveau-dri-git() {
  depends=('lib32-libgl-git' 'nouveau-dri-git')
  conflicts=('lib32-nouveau-dri')
  provides=("lib32-nouveau-dri=${_mesaver}")
  pkgdesc="Mesa classic DRI + Gallium3D drivers for Nouveau"

  # Fix libtool relink issue
  install -m755 -d "${pkgdir}/usr/lib32"
  cp "${srcdir}"/build/lib32/libdricore* "${pkgdir}/usr/lib32/"

  # classic mesa driver for nv10 , nv20 nouveau_vieux_dri.so
  make -C "${srcdir}"/build/src/mesa/drivers/dri/nouveau DESTDIR="${pkgdir}" install
  # gallium3D driver for nv30 - nv40 - nv50 nouveau_dri.so
  make -C "${srcdir}"/build/src/gallium/targets/dri-nouveau DESTDIR="${pkgdir}" install
  make -C "${srcdir}"/build/src/gallium/targets/vdpau-nouveau DESTDIR="${pkgdir}" install
  # Remove libdricore
  rm "${pkgdir}"/usr/lib32/libdricore*
}

package_lib32-svga-dri-git() {
 depends=("lib32-mesa-git")
 conflicts=('lib32-svga-dri')
 provides=("lib32-svga-dri=${_mesaver}")
 pkgdesc="Gallium3D VMware guest GL driver"

 make -C "${srcdir}"/build/src/gallium/targets/dri-vmwgfx DESTDIR="${pkgdir}" install
}

package_lib32-mesa-git() {
  depends=('lib32-libgl-git' 'lib32-gcc-libs'
           'dri2proto' 'glproto' 'mesa-git')
  conflicts=('lib32-mesa')
  provides=("lib32-mesa=${_mesaver}")
  pkgdesc="Mesa 3-D graphics libraries and include files"

  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  rm -f  "${pkgdir}/usr/lib32/libGL.so"*
  rm -f  "${pkgdir}/usr/lib32/libglapi.so"*
  rm -f  "${pkgdir}/usr/lib32/libgbm.so"*
  rm -f  "${pkgdir}/usr/lib32/libGLESv"*
  rm -f  "${pkgdir}/usr/lib32/libEGL"*
  rm -f  "${pkgdir}/usr/lib32/libOSMesa"*
  rm -f  "${pkgdir}/usr/lib32/libdricore"*
  rm -rf "${pkgdir}/usr/lib32/egl"
  rm -f  "${pkgdir}"/usr/lib32/pkgconfig/{glesv1_cm.pc,glesv2.pc,egl.pc,osmesa.pc,gbm.pc}
  rm -rf "${pkgdir}/usr/lib32/xorg"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/etc"
}
