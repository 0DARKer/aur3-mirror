# Maintainer: David Campbell <davekong@archlinux.us>
# Contributor: Sean Escriva <sean.escriva@gmail.com>
pkgname=xapian-ruby-bindings
pkgver=1.2.5
pkgrel=1
pkgdesc="Bindings allowing Xapian to be used from Ruby"
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.xapian.org'
depends=('xapian-core=1.2.5' 'ruby' 'util-linux-ng')
provides=('ruby-xapian-full')
conflicts=('ruby-xapian-full')
source=(http://www.oligarchy.co.uk/xapian/$pkgver/xapian-bindings-$pkgver.tar.gz)
md5sums=('24b379d5b95eebbec7a685ff63321bdd')

build() {
  cd "$srcdir/xapian-bindings-$pkgver"

  ./configure --prefix=/usr/ --with-ruby
  make
  make check
}

package() {
  cd "$srcdir/xapian-bindings-$pkgver"
  make DESTDIR="$pkgdir/" install
}
