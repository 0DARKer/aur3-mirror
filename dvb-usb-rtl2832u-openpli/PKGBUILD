# Maintainer: Mauro Santos <registo.mailling@gmail.com>
# Contributor: Benjamin van der Burgh <benjaminvdb@gmail.com>
# Contributor: Leif Warner <abimelech@gmail.com>
# Contributor: Christophe Gueret <christophe.gueret@gmail.com>
pkgname=dvb-usb-rtl2832u-openpli
pkgver=20120405
pkgrel=3
pkgdesc="Linux module for the RTL2832U DVB-T USB2.0 device"
arch=('i686' 'x86_64')
url="http://openpli.org/forums/topic/20899-rtl2832u-chipset-support-proposal"
license=('GPL')
depends=('linux')
makedepends=('git' 'linux-headers')
install="${pkgname}.install"
provides=('dvb-usb-rtl2832u')
source=('driver_src.patch::http://openpli.git.sourceforge.net/git/gitweb.cgi?p=openpli/openembedded;a=blob_plain;f=recipes/linux/linux-etxx00/dvb-usb-rtl2832.patch;hb=323b73fa116d99ff069e501376f5e81797ea654a'
        'Makefile'
        'usbpidwinfast.patch'
        'dvb-usb-rtl28xxu.conf'
        'Makefile.patch'
        'rtl2832u.c.patch'
        'rtl2832u_ioctl.h.patch')
md5sums=('f18ad8730f2e6d3367ef1ea399dc5dba'
         'f6d784a7676dab74435ac6cf8db7e2c7'
         '599d236b29bd167a9a3b0dd2d43c1020'
         '063be4e255e010899574fc3cdec67120'
         '4a1298b973f55c3f830fd7838659cd9e'
         '7898aac0c83126950ba5937dd7dd110e'
         '95e880064c94491e0988b401dc7c90b8')

_extramodules="extramodules-$(uname -r | cut -f-2 -d.)-$(uname -r | cut -f3 -d-)"
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
_kernveri="$(echo $_kernver | tr -d . | cut -b1-3)"

build() {
  cd "$srcdir"
  
  [[ -e build_dir ]] && rm -r build_dir
  mkdir  build_dir
  cd build_dir

  cp "$srcdir/Makefile" .
  
  # Get the sources from OpenPLi kernel patch
  patch -p5 < "$srcdir/driver_src.patch" &> /dev/null && return 1
  
  ## Patches go here ##
  patch -p1 < "$srcdir/usbpidwinfast.patch"

  ## Patches for kernel >= 3.7.3
  if [ $_kernveri -ge 373 ]
  then
    patch -p1 < "$srcdir/Makefile.patch"
    patch -p1 < "$srcdir/rtl2832u.c.patch"
    patch -p1 < "$srcdir/rtl2832u_ioctl.h.patch"
  fi

  export KBUILD_SRC="/usr/src/linux-${_kernver}"
  export KVER="${_kernver}"
  make
}

package() {
  install -D -m644 "${srcdir}/dvb-usb-rtl28xxu.conf" "${pkgdir}/etc/modprobe.d/dvb-usb-rtl28xxu.conf"
  install -D -m644 "${srcdir}/build_dir/dvb-usb-rtl2832u.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/dvb-usb-rtl2832u.ko"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/${install}"
} 
