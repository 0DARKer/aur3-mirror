# Maintainer: Mantas MikulÄ—nas <grawity@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gmp-mpbsd
# 5.0.5 was the last version to have mpbsd
pkgver=5.0.5
pkgrel=1
pkgdesc="BSD multi-precision arithmetic compatibility libraries from GMP"
arch=('i686' 'x86_64')
url="http://gmplib.org/"
depends=('gcc-libs' 'sh')
provides=('bsd-mp')
license=('LGPL3')
options=(!libtool)
source=(ftp://ftp.gmplib.org/pub/gmp-${pkgver}/gmp-${pkgver}.tar.xz{,.sig})
sha1sums=('4e006dd5e473f21d414b88997630122662ddc2be'
          'e57c645521d1109314263dbeacba073d9c729819')

build() {
  cd "${srcdir}/gmp-${pkgver}"
  
  [[ ${CARCH} = "i686" ]] && export ABI="32"
  ./configure --build=${CHOST} \
    --prefix=/usr --enable-mpbsd
  make
}

check() {
  cd "${srcdir}/gmp-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/gmp-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir/usr/include/gmp.h" \
         "$pkgdir/usr/lib/libgmp."* \
         "$pkgdir/usr/share"
}
