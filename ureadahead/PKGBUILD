# ureadahead
# Maintainer: ying <Jinoto Systems>
# Contributor: ying <Jinoto Systems>

pkgname=ureadahead
pkgver=0.100.0
pkgrel=9
pkgdesc="ureadahead is used during boot to read files in advance. readahead-list and sreadahead are out-of-date. Needs patched kernel!"
license=('GPL2')
arch=('i686' 'x86_64')
url="https://launchpad.net/ureadahead"
depends=('e2fsprogs' 'libnih' 'dbus-core')
options=(!libtool)
source=(https://launchpad.net/ubuntu/natty/+source/$pkgname/$pkgver-$pkgrel/+files/${pkgname}_$pkgver.orig.tar.gz
        https://launchpad.net/ubuntu/natty/+source/$pkgname/$pkgver-$pkgrel/+files/${pkgname}_$pkgver-$pkgrel.diff.gz
        ureadahead)
md5sums=('086d3f3584b7c54a0c8647c1fba6cec7'
         '3bf9468c098bbb6128c2ff2cd251f084'
         'f92b6f0822e02999e7d906bb9ebc48d2')

build() {
  cd "$srcdir"
  patch -Np0 -i ${pkgname}_$pkgver-$pkgrel.diff

  cd $pkgname-$pkgver
  ./configure --prefix /usr
 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/var/lib/ureadahead/debugfs"
  rm -r "$pkgdir/usr/etc"

  install -Dm644 "$srcdir/ureadahead" "$pkgdir/etc/rc.d/functions.d/ureadahead"
}
