# $Id: PKGBUILD 119517 2011-04-11 20:41:11Z ibiru $
# Maintainer : Vaca

pkgname=nvidia-sascng
pkgver=270.41.06
_kernver='2.6.38-sascng'
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26-sascng."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-sascng>=2.6.38' 'kernel26-sascng<2.6.39' "nvidia-utils=${pkgver}")
makedepends=('kernel26-headers-sascng>=2.6.38' 'kernel26-headers-sascng<2.6.39')
conflicts=('nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('4e3b54ee55c6cfa4437c12a8bd9a4250')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
        md5sums=('d62243b91d0dc8f2c4b2ab646b72ca65')
fi

build() {
	cd $srcdir
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
 	make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
	install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
		$pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko
        install -d -m755 $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install
        # gzip -9 all modules to safe 100MB of space
        find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
