pkgname=firefox-theme-fxchrome
_pkgname=fxchrome
pkgver=17.4
pkgrel=1
pkgdesc="Chrome/Chromium like theme for Firefox"
url="https://addons.mozilla.org/en-US/firefox/addon/fxchrome/"
arch=('any')
license=('MPL')
depends=("firefox")
makedepends=('unzip')
source=("https://addons.cdn.mozilla.net/storage/public-staging/287749/${_pkgname}-${pkgver}-fx-linux.xpi")
md5sums=('51c80179234aa4841f218947f35079ea')

build() {
  cd "$srcdir"

  [[ -d "${pkgname}.extract" ]] || \
    mkdir "${pkgname}.extract"
  cd "${pkgname}.extract"
  unzip ../${_pkgname}-${pkgver}-fx-linux.xpi
}

package() {
  cd "$srcdir/${pkgname}.extract"

  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  local dstdir=$pkgdir/usr/lib/firefox/extensions/${emid}
  install -d $dstdir
  #sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>10.*</em:maxVersion>#' install.rdf

  cp -R * ${dstdir}
  find ${pkgdir} -type d -exec chmod 0755 {} \;
  find ${pkgdir} -type f -exec chmod 0644 {} \;
}

# vim:set ts=2 sw=2 et:
