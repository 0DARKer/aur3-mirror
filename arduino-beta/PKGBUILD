# Maintainer: Lauri Hakko <lauri dot hakko at gmail dot com>
# Contributor: PyroPeter <googlemail.com@abi1789>
# Contributor: darkapex <me@jailuthra.in>

pkgname=arduino-beta
epoch=1
pkgver=1.5.8
pkgrel=4
pkgdesc="Arduino SDK Beta release"
arch=('i686' 'x86_64')
url="http://arduino.cc/en/Main/Software"
options=(!strip staticlibs)
license=('GPL')
depends=('avr-libc' 'libusb-compat' 'java-runtime')
install="arduino.install"
conflicts=('arduino' 'arduino-toolchain')
provides=('arduino')
source=("http://arduino.cc/download.php?f=/arduino-${pkgver//_/-}-linux32.tgz"
	'arduino'
	'arduino.png'
	'arduino.desktop')
md5sums=('289b1e3ad64f29db3ed13b06880dce40'
         '5e385c8cba80ca1b4227b162e4cad5cd'
         '9e36d33891d5e68d38ec55d1494499a5'
         'eebc4d6495864bea99ad057af801afb9')


if [ "$CARCH" == 'x86_64' ]; then
  source[0]="http://arduino.cc/download.php?f=/arduino-${pkgver//_/-}-linux64.tgz"
  md5sums[0]='beffea19c77cca7669cb151f50a1aaaf'
fi

package() {
  cd "$srcdir/arduino-${pkgver//_/-}"

  # arduino excutable should accept arguments
  sed -i 's/^java .* processing.app.Base$/\0 "$*"/' arduino

  mkdir -p "$pkgdir"/usr/{bin,share/{doc,applications,pixmaps}}

  # copy the whole SDK to /usr/share/arduino/
  cp -r . "$pkgdir/usr/share/arduino"

  # at least support the FHS a little bit:
  install -m755 "$srcdir/arduino" "$pkgdir/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "$pkgdir/usr/share/doc/arduino"

  # desktop icon
  install -m644 "$srcdir/arduino.desktop" "$pkgdir/usr/share/applications/"
  install -m644 "$srcdir/arduino.png" "$pkgdir/usr/share/pixmaps/"
}
