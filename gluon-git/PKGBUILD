# Maintainer: Harley Laue <losinggeneration@gmail.com>

pkgname=gluon-git
pkgver=20120702
pkgrel=1
pkgdesc="A Free and Open Source framework for creating and distributing games"
arch=('i686' 'x86_64')
url="http://gluon.gamingfreedom.org/"
license=('LGPL')
depends=('kdelibs' 'alure' 'assimp')
makedepends=('git' 'cmake' 'automoc4' 'mesa')
provides=('gluon')
conflicts=('gluon')
install=gluon.install

_gitroot="git://anongit.kde.org/gluon.git"
_gitname="gluon"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull --depth=1 origin
    msg "The local files are updated."
  else
    git clone --depth=1 "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  [ -d "$srcdir/$_gitname-build" ] && rm -rf "$srcdir/$_gitname-build"
  mkdir "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr "$srcdir/$_gitname"
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
