# Maintainer: Markus Opitz <mastero23 at gmail dot com>

pkgname=x2goserver-git
pkgver=20110724
pkgrel=1
pkgdesc="Open source terminal server"
arch=('i686' 'x86_64')
url="http://www.x2go.org/"
license=('GPL')
depends=('openssh' 'perl-config-simple' 'perl-dbd-sqlite' 'python')
options=(emptydirs)
install=x2goserver.install
source=('x2goserver.rc.d')
md5sums=('8083819ac423401440c0ad5feef1a128')

_gitname="${pkgname%-git}"
_gitroot="git://code.x2go.org/$_gitname.git"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir/" install

  rm -rf "$pkgdir/versions"
  chmod 755 "$pkgdir"/usr/lib/x2go/*
  sed -i 's/$err ne "ok"/$err ne ""/' "$pkgdir/usr/lib/x2go/x2godbwrapper.pm"

  for i in "$pkgdir"/usr/lib/x2go/*; do
    ln -s "/usr/lib/x2go/${i##*/}" "$pkgdir/usr/bin/"
  done

  install -D -m 755 "$srcdir/x2goserver.rc.d" "$pkgdir/etc/rc.d/x2goserver"
}
