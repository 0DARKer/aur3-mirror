# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: cute.tec@gmail.com

pkgname=xfwm4-git
pkgver=20121206
pkgrel=1
pkgdesc="Xfce window manager"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://git.xfce.org/xfce/xfwm4"
groups=('xfce4-git')
depends=('libxfce4ui-git' 'hicolor-icon-theme' 'libwnck') #'libxfce4ui' does also work
optdepends=()
makedepends=('git' 'xfce4-dev-tools') #xfce4-dev-tools-git
provides=('xfwm4')
conflicts=('xfwm4')
source=()
md5sums=()

_gitname=xfwm4
_gitroot=git://git.xfce.org/xfce/xfwm4

build() {
	cd $srcdir
	msg "Getting sources..."

	if [[ -d $_gitname ]] ; then
		cd $_gitname && git pull origin
		msg "The local files are updated." && cd ../
	else
		git clone $_gitroot $_gitname
	fi

	[ -d $_gitname-build ] && rm -r $_gitname-build 
	cp $_gitname $_gitname-build -r
	cd $_gitname-build

	msg "Starting build..."

	./autogen.sh
	./configure --enable-maintainer-mode \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-static \
		--enable-compositor \
		--enable-xsync \
		--disable-debug
	make
}

package() {
	cd $srcdir/$_gitname-build
	make DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
