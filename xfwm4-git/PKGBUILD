# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: cute.tec@gmail.com

_pkgname=xfwm4
pkgname=xfwm4-git
pkgver=4.10.0.132.g34327bc
pkgrel=1
pkgdesc="Xfce window manager"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://git.xfce.org/xfce/xfwm4"
groups=('xfce4-git')
depends=('libxfce4ui-git' 'hicolor-icon-theme' 'libwnck')
makedepends=('git' 'xfce4-dev-tools-git')
provides=('xfwm4')
conflicts=('xfwm4')
source=('git://git.xfce.org/xfce/xfwm4#branch=master')
md5sums=('SKIP')


pkgver() {
	cd $_pkgname
	git describe --always | sed -e 's|-|.|g' -e 's/xfwm4.//'
}

build() {
	cd $srcdir/$_pkgname

	./autogen.sh
	./configure --enable-maintainer-mode \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib \
		--localstatedir=/var \
		--disable-static \
		--enable-compositor \
		--enable-xsync \
		--disable-debug
	make
}

package() {
	cd $srcdir/$_pkgname
	make DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et: