#Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase="wxwidgets2.8-light"
pkgname=('wxbase2.8-light' 'wxgtk2.8-light' )
pkgver=2.8.12.1
pkgrel=3
pkgdesc="wxWidgets suite for Base and GTK2 toolkits (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
makedepends=('git' 'glu')
source=("wxwidgets::git+https://github.com/wxWidgets/wxPython.git#tag=wxPy-${pkgver}"
        'wxGTK-2.8-collision.patch')
sha1sums=('SKIP'
          '75d2292a0058570aa6071b4bee6eef69e47f1208')
_gitname=wxwidgets

prepare() {
  cd "${_gitname}"
  patch -p1 -i ../wxGTK-2.8-collision.patch
  cd ..
  rm -fr "${_gitname}"-{base,gtk}
  cp -R "${_gitname}" "${_gitname}-base"
  cp -R "${_gitname}" "${_gitname}-gtk"
}


build() {
  pushd "${_gitname}-base" &> /dev/null
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-regex=builtin \
              --enable-unicode \
              --disable-{precomp-headers,visibility,gui}
  make -C locale allmo

  popd &> /dev/null
  pushd "${_gitname}-gtk" &> /dev/null
  ./configure --prefix=/usr --libdir=/usr/lib \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,regex=builtin,opengl,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode} \
              --disable-{mediactrl,precomp-headers,webview,visibility}
  make
  popd &> /dev/null
}

package_wxbase2.8-light() {
  pkgdesc="wxWidgets Base (GNOME/GStreamer free!)"
  depends=('bash' 'expat' 'zlib')
  provides=("wxbase2.8=${pkgver}")
  conflicts=('wxbase2.8')
  options=('!emptydirs')

  make -C "${_gitname}-base" DESTDIR="${pkgdir}" install
  install -D -m644 "${_gitname}-base/docs/licence.txt" "${pkgdir}/usr/share/licenses/wxbase2.8-light/LICENSE"
}

package_wxgtk2.8-light() {
  pkgdesc="wxWidgets with GTK2 Toolkit (GNOME/GStreamer free!)"
  depends=('wxbase2.8-light' 'gtk2' 'libsm' 'sdl' 'libgl' 'libmspack')
  provides=("wxgtk2.8=${pkgver}")
  conflicts=('wxgtk2.8')
  options=('!emptydirs')

  make -C "${_gitname}-gtk" DESTDIR="${pkgdir}" install
  install -D -m644 "${_gitname}-gtk/docs/licence.txt" "${pkgdir}/usr/share/licenses/wxgtk2.8-light/LICENSE"

  rm -fr "${pkgdir}/usr/bin/wx-config-2.8"
  rm -fr "${pkgdir}/usr/lib/"*baseu*
  rm -fr "${pkgdir}/usr/share/"{aclocal,bakefile}
  for i in $(diff -srq "${pkgdir}/usr/include/wx-2.8/wx/" "${pkgdir}/../wxbase2.8-light/usr/include/wx-2.8/wx/" | grep identical | awk '{print $2}'); do rm -fr ${i}; done
}
