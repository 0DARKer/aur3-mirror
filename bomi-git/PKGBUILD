# Maintainer: Ben Reedy <breed808 *AT* breed808 *DOT* com>
# Contributor: xylosper <darklin20@gmail.com>

_pkgbase=bomi

pkgname=$_pkgbase-git
pkgver=0.9.3.r59.g7ab3267
pkgrel=2
pkgdesc="Powerful and easy-to-use GUI multimedia player based on mpv (git version)"
arch=('i686' 'x86_64')
url="http://bomi-player.github.io"
license=('GPL')
install=$_pkgbase.install
depends=('qt5-base' 'qt5-declarative' 'qt5-x11extras' 'qt5-quickcontrols'
         'libdvdread' 'libdvdnav' 'libcdio-paranoia' 'libcdio' 'smbclient'
         'alsa-lib' 'libpulse' 'jack' 'libchardet' 'libbluray'
         'libva' 'libgl' 'fribidi' 'libass' 'ffmpeg')
makedepends=('git' 'mesa' 'gcc' 'pkg-config' 'python' 'qt5-tools')
optdepends=('libaacs: AACS decryption for Blu-ray support'
            'libbdplus: BD+ decryption for Blu-ray support'
            'youtube-dl: streaming website support including YouTube')
provides=('bomi')
conflicts=('cmplayer' 'bomi')
source=(git+https://github.com/xylosper/${_pkgbase}.git)
md5sums=('SKIP')
#options=(debug !strip)

pkgver() {
    cd "$srcdir/$_pkgbase"
    echo $(git describe --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g' | cut -c2-)
}

build() {
    cd "$srcdir/$_pkgbase"
    ./configure --prefix=/usr --enable-jack --enable-pulseaudio --enable-cdda
    make
}

package() {
    cd "$srcdir/$_pkgbase"
    make DEST_DIR="$pkgdir" install
}
