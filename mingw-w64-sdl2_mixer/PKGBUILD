# Maintainer: edubart <edub4rt@gmail.com>
pkgname=mingw-w64-sdl2_mixer
pkgver=2.0.0
pkgrel=9
pkgdesc="SDL2 mixer libraries (mingw-w64)"
arch=(any)
url="http://www.libsdl.org/projects/SDL_mixer"
license=("MIT")
depends=(mingw-w64-sdl2 mingw-w64-smpeg2 mingw-w64-libvorbis mingw-w64-libmodplug mingw-w64-flac)
makedepends=(mingw-w64-configure)
options=(staticlibs !strip !buildflags)
source=("http://www.libsdl.org/projects/SDL_mixer/release/SDL2_mixer-$pkgver.tar.gz")
md5sums=('65f6d80df073a1fb3bb537fbda031b50')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/SDL2_mixer-${pkgver}"
  export CFLAGS="-I${srcdir}/SDL2_mixer-${pkgver}/external/libmodplug-0.8.8.4/src"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure \
      --disable-sdltest \
      --disable-smpegtest \
      --disable-music-ogg-shared \
      --with-smpeg-prefix=/usr/${_arch}
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/SDL2_mixer-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
