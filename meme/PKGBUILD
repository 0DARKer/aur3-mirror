#Contributor: allencch <allencch at hotmail dot com>
#Contributor: Charles E. Vejnar
pkgname=meme
pkgver=4.10.0_1
pkgrel=3
pkgdesc="The MEME suite provides tools for discovering and using protein and DNA sequence motifs"
arch=("i686" "x86_64")
url="http://meme.sdsc.edu/meme/intro.html"
license=("custom")
depends=(perl-html-template tcsh python2 perl-xml-parser openmpi)
source=("http://ebi.edu.au/ftp/software/MEME/4.10.0/${pkgname}_${pkgver}.tar.gz"
        "python27.patch")
sha1sums=("47db8717a61b9fb11cf6069ab33b17e1bbe58085"
          "737b1ef7cb277412c2fff692a2e9b2a261e1b532")

prepare() {
    cd $srcdir/${pkgname}_${pkgver%_*}

    patch -p1 -i "${srcdir}/python27.patch"
}

build() {
    cd $srcdir/${pkgname}_${pkgver%_*}

    ./configure --prefix=/usr --program-prefix="meme-" --docdir='${prefix}'/usr/share/doc/meme --sysconfdir='${prefix}'/../etc/meme --with-python=/usr/bin/python2.7

    make
}

package() {
        cd "${srcdir}/${pkgname}_${pkgver%_*}"

    make prefix="${pkgdir}/usr" install

    # Move Python modules to a more standard location
    install -dm755 ${pkgdir}/usr/lib/python2.7/site-packages
    for fname in $(ls -1 ${pkgdir}/usr/lib/python2.7/); do
        if [[ $fname = *.py ]]; then
            mv ${pkgdir}/usr/lib/python2.7/$fname ${pkgdir}/usr/lib/python2.7/site-packages/${fname#meme-}
        fi
    done

    # Rename Perl modules
    for fname in $(ls -1 ${pkgdir}/usr/lib/perl/); do
        if [[ $fname = meme-*.pm ]]; then
            mv ${pkgdir}/usr/lib/perl/$fname ${pkgdir}/usr/lib/perl/${fname#meme-}
        fi
    done
}
