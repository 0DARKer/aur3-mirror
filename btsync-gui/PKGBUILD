# Maintainer: Martin Wimpress <code@flexion.org>

pkgname=btsync-gui
pkgver=0.7.2
_pkgupd=1
pkgrel=2
pkgdesc="Unofficial Bittorrent Sync GUI for Linux desktops."
arch=('any')
url="http://forum.bittorrent.com/topic/28106-debian-and-ubuntu-desktop-gui-unofficial-packages-for-bittorrent-sync/"
license=('GPL2')
depends=('btsync' 'hicolor-icon-theme' 'python2-gobject' 'python2-requests' 'python2-qrencode' 'pygtk' )
conflicts=('btsyncindicator' 'btsync-autoconfig')
source=("https://github.com/tuxpoldo/btsync-deb/archive/${pkgname}-${pkgver}-${_pkgupd}.tar.gz"
        "${pkgname}.key"
        "http://syncapp.bittorrent.com/18TDE4IPRO/BitTorrentSyncUserGuide.pdf")
sha1sums=('d9d7a29e596237a869618941e593b0e0901aea46'
          '2a0f2cc3004e248b3f3548f325e1b03cb384bc20'
          'ec72a4a9f85b079a9bceb4db1275413412b18ece')
install="${pkgname}.install"

prepare() {
    cd "${srcdir}/btsync-deb-${pkgname}-${pkgver}-${_pkgupd}/${pkgname}"
    
    # Arch Linux compatibility patches
    sed -i 's|env python|env python2|' ${pkgname}
    sed -i 's|/usr/lib/btsync-common/btsync-core|/usr/bin/btsync|' btsyncagent.py
    sed -i 's|/usr/share/doc/btsync-common/BitTorrentSyncUserGuide.pdf.gz|/usr/share/doc/btsync-gui/BitTorrentSyncUserGuide.pdf|' btsyncstatus.py
    
    # These are being removed from upstream soon.
    if [ -d icons/36x36 ]; then
        rm -rf icons/36x36
    fi
}

package() {
    cd "${srcdir}/btsync-deb-${pkgname}-${pkgver}-${_pkgupd}/${pkgname}"
    
    install -d -m 0755 "${pkgdir}/usr/bin"
    install -m 0755 ${pkgname} "${pkgdir}/usr/bin/"

    install -d -m 0755 "${pkgdir}/usr/lib/${pkgname}"
    install -m 0755 *.py "${pkgdir}/usr/lib/${pkgname}"
    install -m 0644 *.glade "${pkgdir}/usr/lib/${pkgname}"
    install -m 0644 "${srcdir}/${pkgname}.key" "${pkgdir}/usr/lib/${pkgname}"

    install -d -m 0755 "${pkgdir}/usr/share/applications"
    install -m 0644 ${pkgname}.desktop "${pkgdir}/usr/share/applications/"

    install -d -m 0755 "${pkgdir}/usr/share/man/man7"
    install -m 0644 ${pkgname}.7 "${pkgdir}/usr/share/man/man7/"

    install -d -m 0755 "${pkgdir}/usr/share/doc/${pkgname}"
    install -m 0644 *.md "${pkgdir}/usr/share/doc/${pkgname}"
    install -m 0644 "${srcdir}/BitTorrentSyncUserGuide.pdf" "${pkgdir}/usr/share/doc/${pkgname}"

    cd "${srcdir}/btsync-deb-${pkgname}-${pkgver}-${_pkgupd}/${pkgname}/icons"
    for ICON in *
    do
        if [ -d ${ICON}/apps ]; then
            install -d -m 0755 "${pkgdir}/usr/share/icons/hicolor/${ICON}/apps"
            install -m 0644 ${ICON}/apps/* "${pkgdir}/usr/share/icons/hicolor/${ICON}/apps/"
        fi
        if [ -d ${ICON}/status ]; then
            install -d -m 0755 "${pkgdir}/usr/share/icons/hicolor/${ICON}/status"
            install -m 0644 ${ICON}/status/* "${pkgdir}/usr/share/icons/hicolor/${ICON}/status/"
        fi
    done
}
