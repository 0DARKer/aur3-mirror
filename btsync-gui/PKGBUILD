# Maintainer: Martin Wimpress <code@flexion.org>

pkgname=btsync-gui
pkgver=0.8.1
_pkgupd=1
pkgrel=1
pkgdesc="Unofficial Bittorrent Sync GUI for Linux desktops."
arch=('any')
url="http://www.yeasoft.com/site/projects:btsync-deb:btsync-gui"
license=('GPL2')
depends=('btsync' 'hicolor-icon-theme' 'python2-gobject' 'python2-requests' 'python2-qrencode' 'pygtk')
makedepends=('gettext')
conflicts=('btsyncindicator' 'btsync-autoconfig')
source=("https://github.com/tuxpoldo/btsync-deb/archive/${pkgname}-${pkgver}-${_pkgupd}.tar.gz"
        "${pkgname}.key"
        "http://syncapp.bittorrent.com/18TDE4IPRO/BitTorrentSyncUserGuide.pdf")
sha1sums=('aa06e16f1adaf5e7d7064333c9af441e47f89e19'
          '2a0f2cc3004e248b3f3548f325e1b03cb384bc20'
          'ec72a4a9f85b079a9bceb4db1275413412b18ece')
install="${pkgname}.install"

prepare() {
    cd "${srcdir}/btsync-deb-${pkgname}-${pkgver}-${_pkgupd}/${pkgname}"
    # Arch Linux compatibility patches
    sed -i 's|env python|env python2|' ${pkgname}
    sed -i 's|/usr/lib/btsync-common/btsync-core|/usr/bin/btsync|' btsyncagent.py
    sed -i 's|/usr/share/doc/btsync-common/BitTorrentSyncUserGuide.pdf.gz|/usr/share/doc/btsync-gui/BitTorrentSyncUserGuide.pdf|' btsyncstatus.py
}

package() {
    cd "${srcdir}/btsync-deb-${pkgname}-${pkgver}-${_pkgupd}/${pkgname}"
    install -d -m 0755 "${pkgdir}/usr/bin"
    install -m 0755 ${pkgname} "${pkgdir}/usr/bin/"

    install -d -m 0755 "${pkgdir}/usr/lib/${pkgname}"
    install -m 0755 *.py "${pkgdir}/usr/lib/${pkgname}"
    install -m 0644 *.glade "${pkgdir}/usr/lib/${pkgname}"
    install -m 0644 "${srcdir}/${pkgname}.key" "${pkgdir}/usr/lib/${pkgname}"

    install -d -m 0755 "${pkgdir}/usr/share/applications"
    install -m 0644 ${pkgname}.desktop "${pkgdir}/usr/share/applications/"

    install -d -m 0755 "${pkgdir}/usr/share/man/man7"
    install -m 0644 ${pkgname}.7 "${pkgdir}/usr/share/man/man7/"

    install -d -m 0755 "${pkgdir}/usr/share/doc/${pkgname}"
    install -m 0644 *.md "${pkgdir}/usr/share/doc/${pkgname}"
    install -m 0644 "${srcdir}/BitTorrentSyncUserGuide.pdf" "${pkgdir}/usr/share/doc/${pkgname}"

    cd "${srcdir}/btsync-deb-${pkgname}-${pkgver}-${_pkgupd}/${pkgname}/icons"
    for ICON in *
    do
        if [ -d ${ICON}/apps ]; then
            install -d -m 0755 "${pkgdir}/usr/share/icons/hicolor/${ICON}/apps"
            install -m 0644 ${ICON}/apps/* "${pkgdir}/usr/share/icons/hicolor/${ICON}/apps/"
        fi
        if [ -d ${ICON}/status ]; then
            install -d -m 0755 "${pkgdir}/usr/share/icons/hicolor/${ICON}/status"
            install -m 0644 ${ICON}/status/* "${pkgdir}/usr/share/icons/hicolor/${ICON}/status/"
        fi
    done
    
    cd "${srcdir}/btsync-deb-${pkgname}-${pkgver}-${_pkgupd}/${pkgname}/po"
    for PO in *.po
    do
        LANG_CODE=`basename ${PO} .po`
        install -d -m 0755 "${pkgdir}/usr/share/locale/${LANG_CODE}/LC_MESSAGES"
        msgfmt -c ${PO} -o "${pkgdir}/usr/share/locale/${LANG_CODE}/LC_MESSAGES/btsync-gui.mo" 2>/dev/null
    done
}
