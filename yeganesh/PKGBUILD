# Maintainer: David J. Haines <djhaines@gmx.com>
_hkgname=yeganesh
pkgname=yeganesh
pkgver=2.4
pkgrel=1
pkgdesc="dmenu with a sense of history"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc' 'haskell-strict>=0.3' 'haskell-xdg-basedir>=0.2')
optdepends=('dmenu-path-c: useful if you wish to use the -f option')
depends=('dmenu')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
md5sums=('2b52a9f3ac2b9f5d4b74b5566ce9ab8d')
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname} -O
    runhaskell Setup build
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
# vim:set ts=2 sw=2 et:
