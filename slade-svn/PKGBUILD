# Contributor: Jan Cholasta <grubber@grubber.cz>

pkgname=slade-svn
pkgver=1004
pkgrel=1
pkgdesc="SLADE3 Doom editor."
arch=('i686' 'x86_64')
url="http://slade.mancubus.net/"
license=('GPL')
depends=('bzip2' 'fluidsynth' 'freeimage' 'sfml' 'wxgtk-2.9' 'zlib')
makedepends=('cmake' 'subversion')
conflicts=('slade')
provides=('slade')
source=($pkgname-cmake.patch
        $pkgname-linux.patch
        slade.desktop
        slade.png)
md5sums=('458375ab68c863c5d0f30d5c5c2924b8'
         'bbadc55b603a18fd8a4d97fcd83512c1'
         '2c7337f8f918009b8633f6650a583efd'
         '85e440a64d8185fb903b1cec4f72c234')

_svntrunk="http://mancubus.net/svn/hosted/slade/trunk/"
_svnmod="slade"

build() {
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf $_svnmod-build
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  patch -p1 <"$srcdir"/$pkgname-cmake.patch
  patch -p1 <"$srcdir"/$pkgname-linux.patch

  cmake -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.9 -DwxWidgets_wxrc_EXECUTABLE=/usr/bin/wxrc-2.9 .
  make
}

package() {
  install -Dm755 $_svnmod-build/slade "$pkgdir"/usr/bin/slade
  install -Dm644 $_svnmod-build/dist/slade.pk3 "$pkgdir"/usr/share/slade3/slade.pk3

  install -Dm644 slade.desktop "$pkgdir"/usr/share/applications/slade.desktop
  install -Dm644 slade.png "$pkgdir"/usr/share/pixmaps/slade.png
}
