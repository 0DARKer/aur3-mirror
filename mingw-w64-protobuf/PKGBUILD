pkgname=mingw-w64-protobuf
pkgver=2.5.0
pkgrel=1
pkgdesc="A way of encoding structured data in an efficient yet extensible format (mingw-w64)"
arch=(any)
url="http://code.google.com/p/protobuf"
license=("APACHE")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config protobuf python)
depends=(mingw-w64-crt mingw-w64-zlib)
options=(!libtool !strip !buildflags)
source=("http://protobuf.googlecode.com/files/protobuf-$pkgver.tar.bz2")
md5sums=('a72001a9067a4c2c4e0e836d0f92ece4')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --target=${_arch} \
      --with-protoc=/usr/bin/protoc
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}