# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-rose-db
pkgver=0.770
pkgrel=1
pkgdesc="A DBI wrapper and abstraction layer"
arch=(any)
license=('PerlArtistic')
url="http://search.cpan.org/dist/Rose-DB/"
depends=(perl-bit-vector
         perl-clone-pp
         perl-dbi
         perl-datetime
         perl-datetime-format-mysql
         perl-datetime-format-oracle
         perl-datetime-format-pg
         perl-rose-datetime
         perl-rose-object
         perl-sql-reservedwords
         perl-time-clock)
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/J/JS/JSIRACUSA/Rose-DB-${pkgver}.tar.gz)
md5sums=('af1ef992207c5017a111513176728bf6');
sha1sums=('71f93bcbb5bd1b68056031f1d0781b431ad55fd6')

build() {
  cd ${srcdir}/Rose-DB-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd ${srcdir}/Rose-DB-${pkgver}
  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} '(' -name perllocal.pod -o -name .packlist ')' -delete
}
