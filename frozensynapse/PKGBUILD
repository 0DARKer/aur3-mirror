# Maintainer: wido <widowild [ta] myopera [tod] com>

pkgname=frozensynapse
pkgver=1
pkgrel=1
pkgdesc="A single player and multiplayer simultaneous-turn-based tactical game. (The Humble Frozen Synapse Bundle)"
url="http://www.frozensynapse.com/"
license=('custom: "commercial"')
arch=('i686' 'x86_64')
makedepends=('unzip')
source=(${pkgname}.sh ${pkgname}.desktop)
md5sums=('c8c0ca0850f2bc790019c738b4d23712'
         'bcd554f457d4ddd95b7c33922ddd70fc')

depends=('mesa' 'glibc')
_depends64=('lib32-mesa' 'lib32-glibc')

if [[ $CARCH == x86_64 ]]; then
  depends=(${_depends64[@]})
fi

_gamepkg="${pkgname}-${pkgver}-linux-bin"

build() {
  cd ${srcdir}

  msg "You need a full copy of this game in order to install it"
  msg "Searching for ${_gamepkg} in dir: $(readlink -f `pwd`/..)"
  if [[ -f "../${_gamepkg}" ]]; then
    msg "Found game package, installing..."
    ln -fs "../${_gamepkg}" .
    unzip -o ${_gamepkg} || true
  else
    error "Game package not found, please type absolute path to ${_gamepkg}:"
    read pkgpath
    if [[ -f "${pkgpath}/${_gamepkg}" ]]; then
      msg "Found game package, installing..."
      ln -fs "${pkgpath}/${_gamepkg}" .
      unzip -o ${_gamepkg} || true
    else
      error "Unable to find game package."
      return 1
    fi
  fi
}

package(){

  # directories
  install -d ${pkgdir}/opt/${pkgname}
  cd ${srcdir}
  cp -r data guis meta scripts ${pkgdir}/opt/${pkgname}/
  #fix permissions
  find ${pkgdir}/opt/${pkgname} -type d -exec chmod 755 {} \;
  find ${pkgdir}/opt/${pkgname}/{data,guis,meta,scripts} -type f -exec chmod 644 {} \;
  chmod 755 ${pkgdir}/opt/${pkgname}/data/FrozenSynapse


  # startup scripts
  install -Dm755 ${srcdir}/${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}

  # desktop entry
  install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
