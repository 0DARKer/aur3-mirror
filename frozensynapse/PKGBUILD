#Maintainer: Gadget3000 <gadget3000 at msn dot com>
#Contributor: wido <widowild [ta] myopera [tod] com>

pkgname=frozensynapse
pkgver=1
pkgrel=5
pkgdesc="A single player and multiplayer simultaneous-turn-based tactical game. (The Humble Frozen Synapse Bundle)"
url="http://www.frozensynapse.com/"
groups=('humblefsbundle' 'humblebundles')
license=('custom: "commercial"')
arch=('i686' 'x86_64')
makedepends=('unzip')
optdepends=('lib32-nvidia-utils: If you have nvidia graphics'
            'lib32-catalyst-utils: If you have ATI graphics')
source=(${pkgname}.desktop)
md5sums=('eccdc9f63917fdb761d45c5621853485')

if [[ $CARCH == x86_64 ]]; then
	depends=('lib32-glibc' 'lib32-gcc-libs' 'lib32-openal' 'lib32-sdl' 'lib32-mesa')
else
	depends=('glibc' 'gcc-libs' 'openal' 'sdl' 'mesa')
fi

_archive="${pkgname}-${pkgver}-linux-bin"
_archive_md5="ef8f7c64af2adac39a35b7b24a54fb37"

build() {
  cd ${srcdir}

  if [ ! -f ${_fsarchivelocation}${_archive} ]; then
	  if [ ! -f ${_archive} ] && [ -n "${_humblefsbundlekey}" ]; then
		rm -f ${_archive}* index.html\?key\=${_humblefsbundlekey}*
		wget http://www.humblebundle.com/?key=${_humblefsbundlekey}
		wget $(cat index.html\?key\=${_humblefsbundlekey} | grep "${_archive}" | cut -d "'" -f 10)
		mv ${_archive}* ${_archive}
	  elif [ -z "${_humblefsbundlekey}" ]; then
		echo You can automate the download of the archive using the _humblefsbundlekey bash variable.
		echo Just add \'export _humblefsbundlekey\=\<Your key here\>\' to \.bashrc
		echo
		echo Otherwise please just place ${_archive} into $(pwd)/
		echo Press Enter to continue
		read -a _unused
	  fi
  fi

  if [ ! -f ${_fsarchivelocation}${_archive} ]; then
    echo "${_fsarchivelocation}${_archive} not found!"
    return 1
  fi  

  if ! echo "${_archive_md5}  ${_fsarchivelocation}${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_fsarchivelocation}${_archive}"
    return 1
  fi
}

package(){

  # directories
  install -d ${pkgdir}/opt/${pkgname}
  cd ${srcdir}
  yes A | unzip -qqo ${_fsarchivelocation}${_archive} -x guis/* meta/* scripts/* -d ${srcdir}/extractedInstaller/ || true
  mv ${srcdir}/extractedInstaller/data/* ${pkgdir}/opt/${pkgname}/

  # Clean librairy
  rm ${pkgdir}/opt/${pkgname}/libgcc_s.so.1
  rm ${pkgdir}/opt/${pkgname}/libopenal.so
  rm ${pkgdir}/opt/${pkgname}/libSDL-1.2.so.0
  rm ${pkgdir}/opt/${pkgname}/libstdc++.so.6

  #fix permissions
  find ${pkgdir}/opt/${pkgname} -type d -exec chmod 755 {} \;
  find ${pkgdir}/opt/${pkgname} -type f -exec chmod 644 {} \;
  chmod 755 ${pkgdir}/opt/${pkgname}/FrozenSynapse

  # startup scripts
  install -Dd ${pkgdir}/usr/bin
  echo \#\!/bin/bash > ${pkgdir}/usr/bin/frozensynapse
  echo cd /opt/frozensynapse/ >> ${pkgdir}/usr/bin/frozensynapse
  echo ./FrozenSynapse \$@ >> ${pkgdir}/usr/bin/frozensynapse
  chmod +x ${pkgdir}/usr/bin/frozensynapse

  # desktop entry
  install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
