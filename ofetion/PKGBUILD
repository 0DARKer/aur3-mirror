# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=ofetion
_PKGALIAS=openfetion
pkgver=2.1.0
pkgrel=6
pkgdesc="Fetion client for linux"
arch=('i686' 'x86_64')
url="http://basiccoder.com/openfetion"
license=('GPL')
depends=('libxml2' 'openssl' 'sqlite3' 'gtk2')
makedepends=('cmake')
optdepends=('libnotify: notification support'
            'gstreamer0.10: multimedia support'
            'libxss: idle detection support'
            'networkmanager: auto-reconnect support')
conflicts=('openfetion' 'openfetion-cli' 'openfetion-all')
install=$pkgname.install
source=("http://$pkgname.googlecode.com/files/$_PKGALIAS-all-$pkgver.tar.gz"
        "$pkgname.install")
md5sums=('8fd4f863e38294d0abd0d4a3032d76be'
         '64a9334ea6ae8c1c23ed6183f8038ecb')

_ods=()
_tmpfile="$pkgdir/.tmp"

add_ods()
{
    _hit=0
    pkg-config $1 || _hit=1
    if [ $_hit -eq 0 ]
    then
        echo "$3" >> "$_tmpfile"
    else
        _ods=("${_ods[@]}" "$2")
    fi
}

build()
{
    rm -rf "$srcdir/$pkgname-build"
    mkdir "$srcdir/$pkgname-build"
    cd "$srcdir/$pkgname-build"

    add_ods libnotify "-DWITH_LIBNOTIFY=OFF" libnotify
    add_ods gstreamer-0.10 "-DWITH_GSTREAMER=OFF" gstreamer0.10
    add_ods xscrnsaver "-DWITH_LIBXSS=OFF" libxss
    add_ods NetworkManager "-DWITH_NETWORKMANAGER=OFF" networkmanager

    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
        "${_ods[@]}" "$srcdir/$_PKGALIAS-all-$pkgver"
    make
}

package()
{
    cd "$srcdir/$pkgname-build"

    make DESTDIR="$pkgdir" install
    pkg-config gstreamer-0.10 ||
        rm -f "$pkgdir/usr/share/openfetion/resource/newmessage.wav"

    while read _ch
    do
        depends=("${depends[@]}" "$_ch")
        for (( _idx = 0; _idx < 4; _idx++ ))
        do
            if [[ "${optdepends[$_idx]}" =~ ^$_ch:* ]]
            then
                unset optdepends[$_idx]
                break
            fi
        done
    done < "$_tmpfile"
    rm -f "$_tmpfile"
}
