# Maintainer: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=ofetion
_PKGALIAS=openfetion
pkgver=2.1.0
pkgrel=4
pkgdesc="Fetion client for linux, based on GTK+ 2"
arch=('i686' 'x86_64')
url="http://basiccoder.com/openfetion"
license=('GPL')
depends=('libxml2' 'openssl' 'sqlite3' 'gtk2')
makedepends=('cmake')
optdepends=('libnotify: notification support'
            'gstreamer0.10: multimedia support'
            'libxss: idle detection support'
            'networkmanager: auto-reconnect support')
conflicts=('openfetion')
source=(http://$pkgname.googlecode.com/files/$_PKGALIAS-all-$pkgver.tar.gz)
md5sums=('8fd4f863e38294d0abd0d4a3032d76be')

build()
{
    _ODS=()
    pkg-config libnotify || _ODS=("${_ODS[@]}" -DWITH_LIBNOTIFY=OFF)
    pkg-config gstreamer-0.10 || _ODS=("${_ODS[@]}" -DWITH_GSTREAMER=OFF)
    pkg-config xscrnsaver || _ODS=("${_ODS[@]}" -DWITH_LIBXSS=OFF)
    pkg-config NetworkManager || _ODS=("${_ODS[@]}" -DWITH_NETWORKMANAGER=OFF)

    rm -rf "$srcdir/$pkgname-build"
    mkdir "$srcdir/$pkgname-build"
    cd "$srcdir/$pkgname-build"

    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release \
        ${_ODS[@]} "$srcdir/$_PKGALIAS-all-$pkgver"
    make
}

package()
{
    cd "$srcdir/$pkgname-build"

    make DESTDIR=$pkgdir install
    pkg-config gstreamer-0.10 ||
        rm -f $pkgdir/usr/share/openfetion/resource/newmessage.wav
}
