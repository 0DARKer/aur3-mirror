# Contributor: Andre Klitzing <andre () incubo () de>

pkgname=epson-inkjet-printer-stylus-office-tx510fn-series
_pkgname_filter=epson-inkjet-printer-filter
pkgver=1.0.0
pkgrel=1
pkgdesc="Linux printer driver for Epson (BX310FN, TX510FN, TX515FN, see: avasys url)"
arch=('i686' 'x86_64')
url="http://avasys.jp/eng/linux_driver/download/lsb/epson-inkjet/escp/"
license=('GPL' 'LGPL' 'custom:AVASYS Public Licence')
depends=('cups')
makedepends=('rpmextract' 'libtool')
source=(http://linux.avasys.jp/drivers/lsb/epson-inkjet/stable/SRPMS/$pkgname-$pkgver-1lsb3.2.src.rpm)

build() {
  cd "$startdir/src"
  rpmextract.sh $pkgname-$pkgver-1lsb3.2.src.rpm || return 1
  tar xzf $pkgname-$pkgver.tar.gz || return 1
  tar xzf $_pkgname_filter-$pkgver.tar.gz || return 1

  cd $_pkgname_filter-$pkgver
  aclocal
  libtoolize
  chmod +x configure
  # if you have runtime problems: add "--enable-debug" and look into /tmp/epson-inkjet-printer-filter.txt
  ./configure LDFLAGS="$LDFLAGS -Wl,--no-as-needed" --prefix=/opt/$pkgname || return 1
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "$pkgdir/opt/$pkgname/"
  cp -r lib "$pkgdir/opt/$pkgname/"
  cp -r resource "$pkgdir/opt/$pkgname/"
  cp -r watermark "$pkgdir/opt/$pkgname/"
  install -d "$pkgdir/usr/share/cups/model/$pkgname"
  install -m 644 ppds/* "$pkgdir/usr/share/cups/model/$pkgname"

  cd "$srcdir/$_pkgname_filter-$pkgver"
  install -d "$pkgdir/opt/$pkgname/cups/lib/filter/"
  install -m 755 src/epson_inkjet_printer_filter "$pkgdir/opt/$pkgname/cups/lib/filter/epson_inkjet_printer_filter"
}
md5sums=('29b656343b5172c87f80dc29c08dc25b')
