# Contributor: darkcoder <mario_vazq.hotmail.com>

pkgname=os-prober
pkgver=1.46
pkgrel=2
pkgdesc="Utility to detect other OSes on a set of drives"
url="http://joey.kitenet.net/code/os-prober/"
arch=('i686' 'x86_64')
license=('GPL')
depends=()
optdepends=()
makedepends=('gcc' 'sed')
install=
source=(ftp://ftp.us.debian.org/debian/pool/main/o/${pkgname}/${pkgname}_${pkgver}.tar.gz)
md5sums=('b49d98e33da4c2c2534fff6badc2013c')
sha512sums=('a95a2f879affeb1b893d3a0baa1934f468c1948352f397d5ede449bcb0c83f992fa36991242c4272bac9fe2dafd25e39ab784dc4a02475bf54f58f052967dcd9')

build() {
  cd $srcdir/${pkgname}-${pkgver}

  # adjust lib dir to allow detection of 64-bit distros
  sed -i -e "s:/lib/ld\*\.so\*:/lib*/ld*.so*:g" os-probes/mounted/common/90linux-distro  || die "sed failed on 90linux-distro"

  make

  install -Dm755 linux-boot-prober $pkgdir/usr/bin/linux-boot-prober
  install -Dm755 os-prober $pkgdir/usr/bin/os-prober
  install -Dm755 newns $pkgdir/usr/lib/os-prober/newns

  install -Dm755 linux-boot-probes/common/50mounted-tests $pkgdir/usr/lib/linux-boot-probes/50mounted-tests
  install -Dm755 linux-boot-probes/mounted/x86/40grub $pkgdir/usr/lib/linux-boot-probes/mounted/40grub
  install -Dm755 linux-boot-probes/mounted/x86/50lilo $pkgdir/usr/lib/linux-boot-probes/mounted/50lilo
  install -Dm755 linux-boot-probes/mounted/common/40grub2 $pkgdir/usr/lib/linux-boot-probes/mounted/40grub2
  install -Dm755 linux-boot-probes/mounted/common/90fallback $pkgdir/usr/lib/linux-boot-probes/mounted/90fallback

  install -Dm755 os-probes/common/50mounted-tests $pkgdir/usr/lib/os-probes/50mounted-tests
  install -Dm755 os-probes/init/common/10filesystems $pkgdir/usr/lib/os-probes/init/10filesystems
  for f in os-probes/mounted/x86/* ; do 
    install -Dm755 $f $pkgdir/usr/lib/os-probes/mounted/`basename $f`
  done
  install -Dm755 os-probes/mounted/common/40lsb $pkgdir/usr/lib/os-probes/mounted/40lsb
  install -Dm755 os-probes/mounted/common/90linux-distro $pkgdir/usr/lib/os-probes/mounted/90linux-distro

  install -Dm755 common.sh $pkgdir/usr/share/os-prober/common.sh  
  install -Dm644 debian/copyright $pkgdir/usr/share/doc/os-prober/copyright  
  
  # create a empty labels file, will be used by os-prober at execution
  mkdir -p $pkgdir/var/lib/os-prober
  touch $pkgdir/var/lib/os-prober/labels 
  chmod 644 $pkgdir/var/lib/os-prober/labels
}
