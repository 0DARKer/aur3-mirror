pkgname=mingw-w64-libwebp
pkgver=0.4.0
pkgrel=2
pkgdesc="WebP library (mingw-w64)"
arch=(any)
url="https://developers.google.com/speed/webp"
license=("BSD")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt
mingw-w64-libjpeg-turbo
mingw-w64-libpng
mingw-w64-libtiff
mingw-w64-freeglut)
options=(staticlibs !strip !buildflags)
source=("http://webp.googlecode.com/files/libwebp-$pkgver.tar.gz"
"libwebp-0.4.0-giflib-5.1.0.patch")
sha256sums=('31913577e96386556855b41d210736449445fe96cfbe9289014e9b8afa944d69'
            '466fb04887b7fba1be95bb409bd01c23c2855dc2b9471cf5cd6de1822e55ed86')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/libwebp-$pkgver"
  patch -p1 -i "$srcdir"/libwebp-0.4.0-giflib-5.1.0.patch
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-swap-16bit-csp \
      --enable-experimental \
      --enable-libwebpmux \
      --enable-libwebpdemux \
      --enable-libwebpdecoder
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
