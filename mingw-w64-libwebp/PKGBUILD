pkgname=mingw-w64-libwebp
pkgver=0.1.99
pkgrel=3
pkgdesc="WebP library and conversion tools (mingw-w64)"
arch=(any)
url="http://code.google.com/intl/en/speed/webp/"
license=("BSD")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-libjpeg-turbo mingw-w64-libpng mingw-w64-libtiff)
options=(!libtool !strip !buildflags)
source=("http://webp.googlecode.com/files/libwebp-$pkgver.tar.gz")
md5sums=('aca4d43f3e8d1d9f87136d116acfb68d')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/libwebp-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.a' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
