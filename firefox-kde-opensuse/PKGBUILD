# Contributor: Weng Xuetian <wengxt@gmail.com>

# PGO might not build for you. And it takes very long time to compile.
_enable_pgo="n"
# JEMALLOC not works for me, at least. You can just try, but might waste time.
_enable_jemalloc_pgo="n"


pkgname=firefox-kde-opensuse
pkgver=6.0
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'gcc-libs' 'libidl2' 'mozilla-common' 'nss>=3.12.10' 'libxt'
         'libxrender' 'hunspell' 'startup-notification' 'mime-types' 'dbus-glib'
         'alsa-lib' 'libevent' 'sqlite3>=3.7.4' 'libnotify' 'desktop-file-utils'
         'libvpx' 'lcms' 'nspr>=4.8.8' 'libevent' 'libpng' 'cairo' 'kmozillahelper>=0.6' )
makedepends=('zip' 'pkg-config' 'diffutils' 'python2' 'wireless_tools' 'yasm'
             'mesa' 'autoconf2.13')
# this is a charka CCR feature, AUR user can ignore it.
screenshot="http://img864.imageshack.us/img864/5116/firefoxm.png"

if [[ $_enable_pgo == "y" ]]; then
    if [ ! -f '/etc/chakra-release' ]; then
        makedepends+=('xorg-server-xvfb')
    fi
fi
provides=("firefox=${pkgver}")
conflicts=('firefox')
install=firefox.install
url="http://www.mozilla.org/projects/firefox"
#source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
source=(ftp://ftp.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
        mozconfig
        mozconfig-pgo
        firefox.desktop
        mozilla-firefox-1.0-lang.patch
        kde.js
        firefox-cross-desktop.patch
        firefox-kde.patch
        firefox-browser-css.patch
        firefox-kde-114.patch
        mozilla-nongnome-proxies.patch
        mozilla-kde.patch
)


build() {
  cd "${srcdir}/mozilla-release"
  patch -Np1 -i "${srcdir}/mozilla-firefox-1.0-lang.patch"

  rm -f browser/components/shell/src/nsKDEShellService.cpp \
        browser/components/shell/src/nsKDEShellService.h \
        browser/components/shell/src/nsUnixShellService.cpp \
        browser/components/shell/src/nsUnixShellService.h \
        browser/base/content/browser-kde.xul
  rm -f toolkit/xre/nsKDEUtils.cpp \
        toolkit/xre/nsKDEUtils.h \
        uriloader/exthandler/unix/nsCommonRegistry.cpp \
        uriloader/exthandler/unix/nsCommonRegistry.h \
        uriloader/exthandler/unix/nsKDERegistry.cpp \
        uriloader/exthandler/unix/nsKDERegistry.h \
        toolkit/content/widgets/dialog-kde.xml \
        toolkit/content/widgets/preferences-kde.xml

  install -m 644 "${srcdir}/kde.js" browser/app/profile/kde.js
  patch -Np1 -i "${srcdir}/firefox-cross-desktop.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-kde.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-browser-css.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-kde-114.patch" || return 1
  patch -Np1 -i "${srcdir}/mozilla-nongnome-proxies.patch" || return 1
  patch -Np1 -i "${srcdir}/mozilla-kde.patch" || return 1

  if [[ $_enable_pgo == "y" ]]; then
      msg "PGO is enabled"
      cp "${srcdir}/mozconfig-pgo" .mozconfig
  else
      msg "PGO is disabled"
      cp "${srcdir}/mozconfig" .mozconfig
  fi

  # Kill @PRE_RELEASE_SUFFIX@ from browser.xul because it
  # gets set to \177 for an unknown reason
  sed -i 's/@PRE_RELEASE_SUFFIX@//g' \
      browser/base/content/browser.xul
  
  # Don't generate startup cache. Unbreaks build
  sed -i 's/^GENERATE_CACHE .*/GENERATE_CACHE = true/' \
      toolkit/mozapps/installer/packager.mk
  
  export LDFLAGS="-Wl,-rpath,/usr/lib/firefox-$pkgver -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"
  export PYTHON="/usr/bin/python2"
  
  # PGO
  if [[ $_enable_pgo == "y" ]]; then
      if [[ $_enable_jemalloc_pgo == "y" ]]; then
        sed -i '/^NO_PROFILE_GUIDED_OPTIMIZE = 1$/d' \
            memory/jemalloc/Makefile.in
      fi
      echo 'LDFLAGS += -lX11 -lXrender' \
          >> layout/build/Makefile.in

      if [ "${CARCH}" = "x86_64" ]; then
        make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
      fi

      LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
      LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
      kill $! || true
  else
      make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  fi

}

package() {
  cd "${srcdir}/mozilla-release"
  make -j1 DESTDIR="${pkgdir}" -f client.mk install

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 browser/branding/official/default${i/x*/}.png \
          "$pkgdir/usr/share/icons/hicolor/$i/apps/firefox.png"
  done
  
  install -Dm644 "$srcdir/firefox.desktop" \
      "$pkgdir/usr/share/applications/firefox.desktop"
  
  # Remove included dictionaries, add symlink to system myspell path
  # Note: this will cause file conflicts when users have installed dictionaries in the old location
  rm -rf "$pkgdir/usr/lib/firefox-$pkgver/dictionaries"
  ln -sf /usr/share/myspell/dicts \
      "$pkgdir/usr/lib/firefox-$pkgver/dictionaries"
  
  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/firefox-devel-$pkgver,share/idl}

}
md5sums=('1840185865a1a8975df4a3db59080ddc'
         'dc3dc3ddcc91286350b21f89721e718b'
         'c4e62becbe5e63abeb1272e76eba86af'
         'ba96924ece1d77453e462429037a2ce5'
         'bd5db57c23c72a02a489592644f18995'
         '75df0f88cc7a7fa7d522459e4ff82cc5'
         'ef2ec32d2c98fbbdc0304801a3d6413d'
         '056408c7c538c269f74bae3488c6d00f'
         '36868e98e05c27c9cb5b982d386b6dea'
         '038666a4be4e56c1ea25265fa4a12dd2'
         'ea37f26534d1bab452da7945695e2b32'
         'fa79dc366d9a450c0c7df0abcc84dec9')
