# Contributor: Weng Xuetian <wengxt@gmail.com>

pkgname=firefox-kde-opensuse
pkgver=3.6.14
pkgrel=1
_xulver=1.9.2.14
pkgdesc="Standalone web browser from mozilla.org with OpenSUSE patch, integrate better with KDE"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=("xulrunner-kde-opensuse=${_xulver}" 'desktop-file-utils')
makedepends=('zip' 'pkg-config' 'diffutils' 'python2' 'wireless_tools' 'autoconf2.13')
provides=("firefox=${pkgver}")
conflicts=('firefox')
replaces=('firefox3')
install=firefox.install
url="http://www.mozilla.org/projects/firefox"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}.source.tar.bz2
        mozconfig
        firefox.desktop
        firefox-safe.desktop
        mozilla-firefox-1.0-lang.patch
        browser-defaulturls.patch
        firefox-version.patch
        firefox-agent.patch
        python2.7.patch
        kde.js
        firefox-cross-desktop.patch
        firefox-kde.patch
        firefox-no-gnomevfs.patch
        firefox-browser-css.patch)
md5sums=('ab0d00cd33e6b2388429dda1c01abd01'
         '8148bd06f49f3fe2edff56f7539c23a6'
         'ba96924ece1d77453e462429037a2ce5'
         '6f38a5899034b7786cb1f75ad42032b8'
         'bd5db57c23c72a02a489592644f18995'
         '1807651225b021e043154f8bba715a19'
         '92c11c66dd69b03f214002fededd1fc8'
         'f437e94acff8f810991271ef4677d859'
         'ab3dc9aecae7f08b9492fb3c00a5fd28'
         '75df0f88cc7a7fa7d522459e4ff82cc5'
         '92b6abfae8cf61be8f0e3429c58b64f1'
         '73749673894c4058a451742a2b5539a2'
         'c0a7c799a5a32353fcf4f33aa99cd000'
         '444da995d2fa30bdbf2a5dfe4c846d49')

build() {
  cd "${srcdir}/mozilla-1.9.2"
  patch -Np1 -i "${srcdir}/mozilla-firefox-1.0-lang.patch"
  patch -Np1 -i "${srcdir}/browser-defaulturls.patch"
  patch -Np1 -i "${srcdir}/firefox-version.patch"
  patch -Np1 -i "${srcdir}/firefox-agent.patch"
  patch -Np0 -i "${srcdir}/python2.7.patch"

  rm -f browser/components/shell/src/nsKDEShellService.cpp \
        browser/components/shell/src/nsKDEShellService.h \
        browser/components/shell/src/nsUnixShellService.cpp \
        browser/components/shell/src/nsUnixShellService.h \
        browser/base/content/browser-kde.xul
  
  install -m 644 "${srcdir}/kde.js" browser/app/profile/kde.js
  patch -Np1 -i "${srcdir}/firefox-cross-desktop.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-kde.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-no-gnomevfs.patch" || return 1
  patch -Np1 -i "${srcdir}/firefox-browser-css.patch" || return 1

  cp "${srcdir}/mozconfig" .mozconfig
  unset CFLAGS
  unset CXXFLAGS

  export LDFLAGS="-Wl,-rpath,/usr/lib/firefox-3.6"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  make -j1 DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}/usr/lib/firefox-3.6/libjemalloc.so

  install -m755 -d ${pkgdir}/usr/share/{applications,pixmaps}
  install -m644 ${srcdir}/mozilla-1.9.2/other-licenses/branding/firefox/default48.png ${pkgdir}/usr/share/pixmaps/firefox.png || return 1
  install -m644 ${srcdir}/firefox.desktop ${pkgdir}/usr/share/applications/
  install -m644 ${srcdir}/firefox-safe.desktop ${pkgdir}/usr/share/applications/
}
