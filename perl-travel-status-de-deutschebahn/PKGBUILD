# Maintainer: Jens Adam (byte/jra) <j_adam@web.de>

_perlmod=Travel-Status-DE-DeutscheBahn
pkgname=perl-travel-status-de-deutschebahn
pkgver=1.00
pkgrel=1
pkgdesc='Interface to the DeutscheBahn online departure monitor'
url='http://finalrewind.org/projects/Travel-Status-DE-DeutscheBahn/'
license=('GPL' 'PerlArtistic')
arch=('any')
depends=('perl-class-accessor' 'perl-www-mechanize' 'perl-xml-libxml')
makedepends=('perl-file-slurp' 'perl-test-compile' 'perl-test-pod')
options=('!emptydirs')
source=("http://finalrewind.org/projects/${_perlmod}/${_perlmod}-${pkgver}.tar.gz")
md5sums=('cc6e2c088877e442ba4d5f44feb9cbb0')

build() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  perl Build.PL installdirs=vendor destdir="${pkgdir}"
  ./Build
}

check() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  ./Build test
}

package() {
  cd "${srcdir}"/${_perlmod}-${pkgver}
  ./Build install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
