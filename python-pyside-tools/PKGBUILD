# Maintainer: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
pkgname=python-pyside-tools
_pkgrealname=pyside-tools
pkgver=0.2.14
pkgrel=1
_pyver=3.2
pkgdesc="UI Compiler (pyside-uic) plus Qt Resource Compiler (pyside-rcc4) for PySide."
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.pyside.org"
depends=('python-pyside>=1.1.1' 'python')
replaces=('pyside-tools')
makedepends=('cmake' 'automoc4' 'python-shiboken>=1.1.1')
source=("http://www.pyside.org/files/${_pkgrealname}-$pkgver.tar.bz2")
md5sums=('0a683e1dc426df5834cb1e540d0c6e3f')

build(){
    cd $srcdir/${_pkgrealname}-$pkgver
    mkdir -p build && cd build
    _pyverflags=$(python -c 'import sysconfig; print(sysconfig.get_config_var("SOABI"))')
    cmake ../ -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_BUILD_TYPE=Release \
	      -DSHIBOKEN_PYTHON_SUFFIX=.${_pyverflags}
    make
}

package(){
    cd $srcdir/${_pkgrealname}-$pkgver/build
    make DESTDIR=$pkgdir install
}

