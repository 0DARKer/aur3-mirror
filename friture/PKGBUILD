# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=friture
pkgver=0.4
pkgrel=1
pkgdesc="An application to visualize and analyze live audio data in real-time."
arch=(i686 x86_64)
url="http://tlecomte.github.com/friture/"
license=('GPL3')
depends=('pyqwt' 'python2-scipy' 'python2-psutil' 'python2-pyaudio')
optdepends=('jack: for JACK I/O support')
makedepends=('cython2')
source=("http://pypi.python.org/packages/source/f/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('884fd19e512ad12eeb9e71a083e1d7af')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/"

  # python2, CRLF fix
  sed -i 's/\r//;s/env python/&2/' `find "$pkgdir" -name "*.py"` \
    "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
