# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Tom Burdick <thomas.burdick@wrightwoodtech.com>

pkgname=erlang-r15b
pkgver=R15B
pkgrel=1
pkgdesc="Temporary erlang R15B until included in extra"
arch=(i686 x86_64)
url="http://www.erlang.org"
license=('custom')
depends=('ncurses' 'glibc')
provides=('erlang')
conflicts=('erlang')
makedepends=('java-environment' 'perl' 'wxgtk' 'openssl' 'unixodbc>=2.3.1' 'mesa')
optdepends=('wxgtk: for wx support' 'unixodbc: database' 'mesa: gl support')

options=('!makeflags')
source=(http://www.erlang.org/download/otp_src_${pkgver/./-}.tar.gz
	http://www.erlang.org/download/otp_doc_man_${pkgver/./-}.tar.gz)
md5sums=('dd6c2a4807551b4a8a536067bde31d73'
         '9738da523737712a9db87db0dee05338')

build() {
  cd "$srcdir/otp_src_${pkgver/./-}"
  export ERTSVERSION=$(sed -n 's/^VSN[ ]*=[ ]*\([0-9.]\)/\1/p' < erts/vsn.mk)
  export ERLINTERFACEVERSION=$(sed -n 's/^EI_VSN[ ]*=[ ]*\([0-9.]\)/\1/p' < lib/erl_interface/vsn.mk)

  sed -i '/SSL_DYNAMIC_ONLY=/s:no:yes:' erts/configure

  CFLAGS="${CFLAGS} -fno-strict-aliasing" ./configure --prefix=/usr --enable-smp-support --enable-threads -enable-native-libs
  make
}

# please check these deps reported by namcap when you do the next build:
#erlang E: Dependency java-environment detected and not included (found class files usr/lib/erlang/lib/jinterface-1.5.4/priv/OtpErlang.jar, usr/lib/erlang/lib/ic-4.2.27/priv/ic.jar)
#erlang E: Dependency mesa detected and not included (libraries ['usr/lib/libGLU.so.1'] needed in files ['usr/lib/erlang/lib/wx-0.99/priv/erl_gl.so'])

package() {
  cd "$srcdir/otp_src_${pkgver/./-}"
  make INSTALL_PREFIX="$pkgdir" install

# fix prefix
  cd "$pkgdir"
  for i in usr/lib/erlang/bin/erl \
      usr/lib/erlang/bin/start \
      usr/lib/erlang/erts-${ERTSVERSION}/bin/erl \
      usr/lib/erlang/erts-${ERTSVERSION}/bin/start \
      usr/lib/erlang/releases/RELEASES ;do
    sed -i "s#$pkgdir##" $i
  done

# fix symlinks
  cd "$pkgdir/usr/lib/erlang/bin"
  ln -sf ../erts-${ERTSVERSION}/bin/epmd

  cd "$pkgdir/usr/bin"
  for file in *; do
    ln -sfv ../lib/erlang/bin/$file
  done

  ln -s ../lib/erlang/lib/erl_interface-${ERLINTERFACEVERSION}/bin/erl_call "$pkgdir/usr/bin/erl_call"

# install documentation
  install -d "$pkgdir/usr/share/doc/erlang"
  install -D -m644 "$srcdir"/{COPYRIGHT,PR.template,README} "$pkgdir/usr/share/doc/erlang"
  cp -r "$srcdir/man" "$pkgdir/usr/lib/erlang"

  install -Dm644 "$srcdir/otp_src_${pkgver/./-}/EPLICENCE" "$pkgdir/usr/share/licenses/$pkgname/EPLICENCE"

}

