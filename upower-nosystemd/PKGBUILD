# Maintainer: Alexey D. <lq07829icatm@rambler.ru>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=upower-nosystemd
pkgver=0.9.19
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('libusb' 'polkit-consolekit' 'pm-utils' 'dbus-glib' 'libimobiledevice' 'udev')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection')
provides=("upower=${pkgver}")
conflicts=('upower')
replaces=('upower')
options=('!libtool')
source=($url/releases/upower-$pkgver.tar.xz
        lg-unifying-fix-K750-features-name-use-1-second-interval.patch)
md5sums=('f96955ff1a2e4f006937d6b5ea95afb8'
         '04b2a6141e80e422a9fe2820b5b3a613')

build() {
  cd "upower-$pkgver"

  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure

  patch -Np1 -i ../lg-unifying-fix-K750-features-name-use-1-second-interval.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/upower \
    --disable-static
  make
}

package() {
  cd "upower-$pkgver"
  make DESTDIR="$pkgdir" install
}
