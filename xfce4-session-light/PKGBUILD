# Maintainer: megadriver <megadriver at gmx dot com>
# Based on xfce4-session from [extra]

pkgname=xfce4-session-light
_pkgname=xfce4-session
pkgver=4.8.2
pkgrel=2
pkgdesc="A session manager for Xfce (GNOME free!)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
# upower and consolekit are needed for reboot/shutdown/hibernate/suspend
depends=('xfce4-panel' 'libwnck' 'libsm' 'xorg-iceauth' 'upower' 'consolekit' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('fortune-mod: for xfce4-tips')
replaces=('xfce4-toys')
conflicts=('xfce4-session')
provides=('xfce4-session')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.8/$_pkgname-$pkgver.tar.bz2)
md5sums=('48780cbcf784ab64debc9312f17765f2')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-hal \
    --enable-session-screenshots \
    --enable-upower \
    --enable-consolekit \
    --enable-polkit \
    --enable-panel-plugin \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
