# Maintainer: megadriver <megadriver at gmx dot com>
# Based on xfce4-session from [testing]

pkgname=xfce4-session-light
_pkgname=xfce4-session
pkgver=4.10.0
pkgrel=1
pkgdesc="A session manager for Xfce (GNOME and *kit free!)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('libxfce4ui' 'libwnck' 'libsm' 'xorg-iceauth' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('fortune-mod: for xfce4-tips')
replaces=('xfce-utils')
conflicts=('xfce4-session')
provides=('xfce4-session')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.10/$_pkgname-$pkgver.tar.bz2)
md5sums=('4768e1a41a0287af6aad18b329a0f230')


build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-upower \
    --disable-consolekit \
    --disable-polkit \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
