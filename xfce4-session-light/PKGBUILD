# Maintainer: megadriver <megadriver at gmx dot com>
# Based on xfce4-session from [extra]

pkgname=xfce4-session-light
_pkgname=xfce4-session
pkgver=4.8.3
pkgrel=1
pkgdesc="A session manager for Xfce (GNOME and *kit free!)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('xfce4-panel' 'libwnck' 'libsm' 'xorg-iceauth' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('fortune-mod: for xfce4-tips')
replaces=('xfce4-toys')
conflicts=('xfce4-session')
provides=('xfce4-session')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.8/$_pkgname-$pkgver.tar.bz2)
md5sums=('461cc38bbd37cab881adbdf943f9a402')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-hal \
    --enable-session-screenshots \
    --disable-upower \
    --disable-consolekit \
    --disable-polkit \
    --enable-panel-plugin \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
