# Mantainer: Franco Tortoriello

# http://download.opensuse.org/repositories/mozilla/SUSE_Factory/src/
# http://download.opensuse.org/repositories/mozilla:/Factory/openSUSE_Factory/src/

pkgname=seamonkey-kde
pkgver=2.11
pkgrel=1
pkgdesc="SeaMonkey internet suite with OpenSUSE patches for better KDE integration"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('kdebase-runtime' 'gtk2' 'libevent' 'libnotify' 'libvpx' 'mime-types'
	 'mozilla-common' 'startup-notification' 'kmozillahelper>=0.6'
	 'nss>=3.13.3')
makedepends=('unzip' 'zip' 'python2' 'yasm' 'wireless_tools' 'mesa'
	     'autoconf2.13' 'imake')
optdepends=('wireless_tools: Location detection via available WiFi networks')
provides=("seamonkey=$pkgver")
conflicts=('seamonkey')
options=(!emptydirs)
install=seamonkey.install
url="http://www.seamonkey-project.org/"
source=("http://releases.mozilla.org/pub/mozilla.org/seamonkey/releases/$pkgver/source/seamonkey-$pkgver.source.tar.bz2"
	'seamonkey.desktop' 'mozconfig' 'vendor.js' 'install-dir.patch'
	# seamonkey opensuse patches
	'mozilla-nongnome-proxies.patch' 'mozilla-ntlm-full-path.patch'
	'mozilla-shared-nss-db.patch' 'seamonkey-shared-nss-db.patch'
	# firefox opensuse patches
	'browser-css.patch' 'browser-kde.patch' 'mozilla-kde.patch' 'kde.js'
	'glibc216.patch')
md5sums=('807e8993f8fe4a3a42e8f63ecfb0f15d'
	 '7ad044d30f196c55587f416578ea58f1'
	 '663302bacc82b811ec6189a2698a2e2e'
	 'd2be4366b24b97c7aecb7ec66ba1e86b'
	 'a45806db2a84aa513b03c53f95edb923'
	 '8b0ecfdf697485d7b7dd26291c0dc478'
	 '78afb7bce349fbbae9cb92e096daddd7'
	 'b800fc5f090dd34a30e99119484e1720'
	 '9646b7682d0ce02e7ebb2e13ffe50157'
	 'df34733c7b43225d85453ebbb87521c6'
	 'f865e0a028227020c6d8cf7104bb3737'
	 '5f5df76b04b1368763f96053ca8e970a'
	 '75df0f88cc7a7fa7d522459e4ff82cc5'
	 '878f5aa3c2cfa1a1d91e72c992f50dfd')

build() {
  cd "$srcdir/comm-release/mozilla"

  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-ntlm-full-path.patch"
  patch -Np1 -i "$srcdir/mozilla-shared-nss-db.patch"
  patch -Np1 -i "$srcdir/browser-css.patch"
  patch -Np1 -i "$srcdir/browser-kde.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  patch -Np1 -i "$srcdir/glibc216.patch"
  install -m644 "$srcdir/kde.js" browser/app/profile/kde.js

  cd ..
  patch -Np1 -i "$srcdir/install-dir.patch"
  patch -Np1 -i "$srcdir/seamonkey-shared-nss-db.patch"
  cp ../mozconfig .mozconfig

  # Don't exit with error when some libs are missing which we have in
  # system.
  sed '/^MOZ_PKG_FATAL_WARNINGS/s@= 1@= 0@' \
      -i suite/installer/Makefile.in

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/seamonkey"
  export PYTHON="/usr/bin/python2"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  # Enable PGO
  export MOZ_PGO=1
  export DISPLAY=:99
  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

  make -f client.mk build
  kill $! || true
}

package() {
  cd "$srcdir/comm-release"

  make -j1 -f client.mk DESTDIR="$pkgdir" install

  install -m644 "$srcdir/vendor.js" "$pkgdir/usr/lib/seamonkey/defaults/pref"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/seamonkey/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/seamonkey/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/seamonkey/hyphenation"

  install -m755 -d "$pkgdir/usr/share/applications"
  install -m755 -d "$pkgdir/usr/share/pixmaps"
  install -m644 suite/branding/nightly/icons/gtk/seamonkey.png \
                "$pkgdir/usr/share/pixmaps/"
  install -m644 "$srcdir/seamonkey.desktop" "$pkgdir/usr/share/applications/"

  rm -r "$pkgdir"/usr/{include,lib/seamonkey-devel,share/idl}
}
