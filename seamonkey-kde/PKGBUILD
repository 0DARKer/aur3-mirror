# Mantainer: Franco Tortoriello
# based on the seamonkey and firefox packages from community and extra

pkgname=seamonkey-kde
pkgver=2.17
pkgrel=1
pkgdesc="SeaMonkey internet suite with OpenSUSE patches for better KDE integration"
arch=(i686 x86_64)
license=(MPL GPL LGPL)
depends=(gtk2 libevent libnotify nss libjpeg-turbo libvpx mime-types
	 mozilla-common startup-notification kmozillahelper)
makedepends=(unzip zip python2 yasm mesa autoconf2.13 imake)
optdepends=('networkmanager: Location detection via available WiFi networks')
provides=("seamonkey=$pkgver")
conflicts=(seamonkey)
options=(!emptydirs)
install=seamonkey.install
url="http://www.seamonkey-project.org"
_patchurl=https://api.opensuse.org/public/source/mozilla:Factory
source=(http://ftp.mozilla.org/pub/mozilla.org/seamonkey/releases/$pkgver/source/seamonkey-$pkgver.source.tar.bz2
	seamonkey.desktop mozconfig vendor.js prefs.patch system-cairo.patch
	# seamonkey opensuse patches
	$_patchurl/seamonkey/mozilla-nongnome-proxies.patch
	$_patchurl/seamonkey/mozilla-shared-nss-db.patch
	$_patchurl/seamonkey/mozilla-ntlm-full-path.patch
	$_patchurl/seamonkey/mozilla-gstreamer-760140.patch
	$_patchurl/seamonkey/seamonkey-shared-nss-db.patch
	# firefox opensuse patches
	$_patchurl/MozillaFirefox/mozilla-kde.patch
	$_patchurl/MozillaFirefox/firefox-browser-css.patch
	$_patchurl/MozillaFirefox/firefox-kde.patch
	$_patchurl/MozillaFirefox/firefox-712763.patch)
md5sums=('931f027dd6e2aac3e36e788f6d86979b'
	 '8a0cd54ed487e8603d852ee1749a30a9'
	 'a0fe28e00b0e24b44cbaeed42cbe13ee'
	 '9b3b71cec2cb4ab6eb098c3a1c56df10'
	 '2d1d8fcb1e579f1c7a334d0f33797314'
	 '183d71a79c0f02eaa1b52b8c71690883'
	 '79f113b56057e17ca2466cd0ac578bb3'
	 '0c680eaf5e2035e992488723ce5ce853'
	 'f3b657384513254c70866286701c6947'
	 '9f4e6467284a5c82fbba0c0afe862f31'
	 'c0ac87a79abdc48242d0093d766522f1'
	 '8e833c5abff5ddd3d20bc81eb9bfc028'
	 'df34733c7b43225d85453ebbb87521c6'
	 '194da028eba1fbc316cf37dd586c4112'
	 '7303a96e92e600a46dd6a2cf9af5ada5')

build() {
  cd comm-release/mozilla

  msg "WARNING: If you have a Mozilla based browser installed,"
  msg "you may need to uninstall it before packaging"

  msg2 "Patching"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-shared-nss-db.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  patch -Np1 -i "$srcdir/mozilla-ntlm-full-path.patch"
  patch -Np1 -i "$srcdir/mozilla-gstreamer-760140.patch"
  patch -Np1 -i "$srcdir/firefox-browser-css.patch"
  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/firefox-712763.patch"

  cd ..
  patch -Np1 -i "$srcdir/seamonkey-shared-nss-db.patch"
  patch -Np1 -i "$srcdir/prefs.patch"
  patch -Np1 -i "$srcdir/system-cairo.patch"
  cp ../mozconfig .mozconfig

  # Change install dir
  sed -e 's/-$(MOZ_APP_VERSION)//g' -i {.,mozilla/js/src}/config/baseconfig.mk

  # Fix PRE_RELEASE_SUFFIX
  sed '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    -i mozilla/browser/base/Makefile.in

  # Fix issue with repackaging (don't fail if files are already removed)
  sed 's/xargs rm$/xargs rm -f/' -i mozilla/toolkit/mozapps/installer/packager.mk

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/seamonkey"
  export PYTHON="/usr/bin/python2"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  # Tests fail with CPPFLAGS="-D_FORTIFY_SOURCE=2"
  unset CPPFLAGS

  msg2 "Starting build"
  # Enable PGO
  export MOZ_PGO=1
  export DISPLAY=:99
  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &
  _fail=0

  make -f client.mk build || _fail=1

  kill $! || true
  return $_fail

  # Build without PGO
  #make -f client.mk build
}

package() {
  cd comm-release

  make -j1 -f client.mk DESTDIR="$pkgdir" install
  install -Dm644 ../vendor.js "$pkgdir/usr/lib/seamonkey/defaults/preferences/vendor.js"

  # Use system-provided dictionaries
  rm -r "$pkgdir"/usr/lib/seamonkey/dictionaries
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/seamonkey/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/seamonkey/hyphenation"

  # Desktop files
  install -Dm644 suite/branding/nightly/icons/gtk/seamonkey.png \
	"$pkgdir/usr/share/pixmaps/seamonkey.png"
  install -Dm644 ../seamonkey.desktop \
	"$pkgdir/usr/share/applications/seamonkey.desktop"

  # man page
  install -Dm644 obj-*/mozilla/dist/man/man1/seamonkey.1 "$pkgdir/usr/share/man/man1/seamonkey.1"

  # Remove development stuff
  rm -r "$pkgdir"/usr/{include,lib/seamonkey-devel,share/idl}

  # Remove a reference to srcdir
  sed -e "\|$srcdir|d" -i "$pkgdir/usr/lib/seamonkey/defaults/pref/channel-prefs.js"

  msg2 "The lightning, calendar-timezones and gdata-provider extensions are in"
  msg2 "$srcdir/comm-release/obj-$CHOST/mozilla/dist/xpi-stage"
  msg2 "You can install them manually from there"
}

