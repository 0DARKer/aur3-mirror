# Mantainer: Franco Tortoriello
# based on the seamonkey and firefox packages from community and extra

pkgname=seamonkey-kde
pkgver=2.15.1
pkgrel=1
pkgdesc="SeaMonkey internet suite with OpenSUSE patches for better KDE integration"
arch=(i686 x86_64)
license=(MPL GPL LGPL)
depends=(gtk2 libevent libnotify nss libjpeg-turbo libvpx mime-types
	 mozilla-common startup-notification kmozillahelper)
makedepends=(unzip zip python2 yasm mesa autoconf2.13
	     imake ccache)
optdepends=('networkmanager: Location detection via available WiFi networks')
provides=("seamonkey=$pkgver")
conflicts=(seamonkey)
options=(!emptydirs)
install=seamonkey.install
url="http://www.seamonkey-project.org"
_patchurl=https://api.opensuse.org/public/source/mozilla:Factory
source=(http://ftp.mozilla.org/pub/mozilla.org/seamonkey/releases/$pkgver/source/seamonkey-$pkgver.source.tar.bz2
	seamonkey.desktop mozconfig vendor.js prefs.patch system-cairo.patch
	# seamonkey opensuse patches
	$_patchurl/seamonkey/mozilla-nongnome-proxies.patch
	$_patchurl/seamonkey/mozilla-shared-nss-db.patch
	$_patchurl/seamonkey/mozilla-gstreamer-803287.patch
	$_patchurl/seamonkey/mozilla-gstreamer-760140.patch
	$_patchurl/seamonkey/mozilla-ntlm-full-path.patch
	$_patchurl/seamonkey/seamonkey-shared-nss-db.patch
	# firefox opensuse patches
	$_patchurl/MozillaFirefox/mozilla-kde.patch
	$_patchurl/MozillaFirefox/firefox-browser-css.patch
	$_patchurl/MozillaFirefox/firefox-kde.patch
	)
md5sums=('883757087d041e0a991c1ed96d161fa1'
	 'cc2c6837430ffbf5bb734fee5ce8f1df'
	 '3131ced721c046fd1d2f42e602eb40ce'
	 '9b3b71cec2cb4ab6eb098c3a1c56df10'
	 '2d1d8fcb1e579f1c7a334d0f33797314'
	 '183d71a79c0f02eaa1b52b8c71690883'
	 '79f113b56057e17ca2466cd0ac578bb3'
	 'e07f9ace785e867791fed541759951ee'
	 'bcba525db7fdf3c3df5088f3ae3d1c64'
	 '8aec3e82b656522bfcfb546e869257e1'
	 'f3b657384513254c70866286701c6947'
	 'c0ac87a79abdc48242d0093d766522f1'
	 '8554d4185d1683e31d3db39059caba4b'
	 'df34733c7b43225d85453ebbb87521c6'
	 '64e612ead2a6b8d1cd8b90a2eebb86f0')

build() {
  cd "$srcdir/comm-release/mozilla"

  msg "WARNING: If you have a Mozilla based browser installed,"
  msg "you may need to uninstall it before the packaging stage"

  msg2 "Patching ..."
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-shared-nss-db.patch"
  patch -Np1 -i "$srcdir/mozilla-gstreamer-803287.patch"
  patch -Np1 -i "$srcdir/mozilla-gstreamer-760140.patch"
  patch -Np1 -i "$srcdir/mozilla-ntlm-full-path.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  patch -Np1 -i "$srcdir/firefox-browser-css.patch"
  patch -Np1 -i "$srcdir/firefox-kde.patch"

  cd ..
  patch -Np1 -i "$srcdir/seamonkey-shared-nss-db.patch"
  patch -Np1 -i "$srcdir/prefs.patch"
  patch -Np1 -i "$srcdir/system-cairo.patch"
  cp ../mozconfig .mozconfig

  # Change install dir
  sed -e 's/-$(MOZ_APP_VERSION)//g' -i {.,mozilla/js/src}/config/baseconfig.mk

  # Fix PRE_RELEASE_SUFFIX
  sed '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    -i mozilla/browser/base/Makefile.in

  # Don't exit with error when some libs are missing which we have in
  # system.
  sed '/^MOZ_PKG_FATAL_WARNINGS/s@= 1@= 0@' \
      -i suite/installer/Makefile.in

  # Fix issue with repackaging (don't fail if files are already removed)
  sed 's/xargs rm$/xargs rm -f/' -i mozilla/toolkit/mozapps/installer/packager.mk

  # WebRTC build tries to execute "python" and expects Python 2
  #mkdir "$srcdir/python2-path"
  #ln -s /usr/bin/python2 "$srcdir/python2-path/python"
  #export PATH="$srcdir/python2-path:$PATH"

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/seamonkey"
  export PYTHON="/usr/bin/python2"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  msg2 "Starting build ..."
  make -f client.mk build
}

package() {
  cd "$srcdir/comm-release"

  make -j1 -f client.mk DESTDIR="$pkgdir" install
  install -Dm644 ../vendor.js "$pkgdir/usr/lib/seamonkey/defaults/preferences/vendor.js"

  # Use system-provided dictionaries
  rm -r "$pkgdir"/usr/lib/seamonkey/dictionaries
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/seamonkey/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/seamonkey/hyphenation"

  # Desktop files
  install -Dm644 suite/branding/nightly/icons/gtk/seamonkey.png \
	"$pkgdir/usr/share/pixmaps/seamonkey.png"
  install -Dm644 ../seamonkey.desktop \
	"$pkgdir/usr/share/applications/seamonkey.desktop"

  # man page
  install -Dm644 obj-*/mozilla/dist/man/man1/seamonkey.1 "$pkgdir/usr/share/man/man1/seamonkey.1"
  gzip -9 "$pkgdir/usr/share/man/man1/seamonkey.1"

  # Remove development stuff
  rm -r "$pkgdir"/usr/{include,lib/seamonkey-devel,share/idl}

  # Remove a reference to srcdir
  sed -e "\|$srcdir|d" -i "$pkgdir/usr/lib/seamonkey/defaults/pref/channel-prefs.js"

  msg2 "The Lightning calendar extensions are in $(echo $srcdir/comm-release/obj-*/mozilla/dist/xpi-stage)"
  msg2 "You can install them manually from there if you need them"
}

