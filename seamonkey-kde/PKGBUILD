# Mantainer: Franco Tortoriello
# based on the seamonkey and firefox packages from community and extra

# Profile-guided optimizatons (takes longer to compile)
_pgo=y

pkgname=seamonkey-kde
pkgver=2.17
pkgrel=2
pkgdesc="SeaMonkey internet suite with OpenSUSE patches for better KDE integration"
arch=(i686 x86_64)
license=(MPL GPL LGPL)
depends=(alsa-lib desktop-file-utils dbus-glib gtk2 hunspell libevent libvpx
	 mime-types mozilla-common nss startup-notification gstreamer0.10-base
	 kmozillahelper)
makedepends=(unzip zip mesa python2 python yasm autoconf2.13)
[ $_pgo = y ] && makedepends+=(xorg-server-xvfb)
optdepends=('networkmanager: Location detection via available WiFi networks')
provides=("seamonkey=$pkgver")
conflicts=(seamonkey)
options=(!emptydirs)
install=seamonkey.install
url="http://www.seamonkey-project.org"
_patchurl=https://api.opensuse.org/public/source/mozilla:Factory
source=(http://ftp.mozilla.org/pub/mozilla.org/seamonkey/releases/$pkgver/source/seamonkey-$pkgver.source.tar.bz2
	seamonkey.desktop mozconfig vendor.js system-cairo.patch
	# seamonkey opensuse patches
	$_patchurl/seamonkey/mozilla-nongnome-proxies.patch
	$_patchurl/seamonkey/mozilla-gstreamer-760140.patch
	# firefox opensuse patches
	$_patchurl/MozillaFirefox/mozilla-kde.patch
	$_patchurl/MozillaFirefox/firefox-browser-css.patch
	$_patchurl/MozillaFirefox/firefox-kde.patch
	$_patchurl/MozillaFirefox/firefox-712763.patch
	$_patchurl/MozillaFirefox/kde.js)
md5sums=(931f027dd6e2aac3e36e788f6d86979b
	 8a0cd54ed487e8603d852ee1749a30a9
	 a0fe28e00b0e24b44cbaeed42cbe13ee
	 c8b5dc4252223f3bd71cb357adce9106
	 183d71a79c0f02eaa1b52b8c71690883
	 79f113b56057e17ca2466cd0ac578bb3
	 9f4e6467284a5c82fbba0c0afe862f31
	 8e833c5abff5ddd3d20bc81eb9bfc028
	 df34733c7b43225d85453ebbb87521c6
	 194da028eba1fbc316cf37dd586c4112
	 7303a96e92e600a46dd6a2cf9af5ada5
	 75df0f88cc7a7fa7d522459e4ff82cc5)

build() {
  cd comm-release/mozilla

  msg "WARNING: If you have a Mozilla based browser installed,"
  msg "you may need to uninstall it before packaging"

  msg2 "Patching"
  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  patch -Np1 -i "$srcdir/mozilla-gstreamer-760140.patch"
  patch -Np1 -i "$srcdir/firefox-browser-css.patch"
  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/firefox-712763.patch"

  cd ..
  patch -Np1 -i "$srcdir/system-cairo.patch"

  # disable libmozgnome
  sed -e 's/MOZ_ENABLE_GNOME_COMPONENT=1/MOZ_ENABLE_GNOME_COMPONENT=/' \
      -i {.,mozilla}/configure{,.in}

  cp ../mozconfig .mozconfig

  # Change install dir
  sed -e 's/-$(MOZ_APP_VERSION)//g' -i {.,mozilla/js/src}/config/baseconfig.mk

  # Fix PRE_RELEASE_SUFFIX
  sed '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    -i mozilla/browser/base/Makefile.in

  # Fix issue with repackaging (don't fail if files are already removed)
  sed 's/xargs rm$/xargs rm -f/' -i mozilla/toolkit/mozapps/installer/packager.mk

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/seamonkey"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  # Tests fail with CPPFLAGS="-D_FORTIFY_SOURCE=2"
  unset CPPFLAGS

  msg2 "Starting build"
  if [ $_pgo = y ]; then

    msg2 "PGO enabled"
    export MOZ_PGO=1
    export DISPLAY=:99
    Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &
    _fail=0

    make -f client.mk build || _fail=1

    kill $! || true
    return $_fail

  else

    make -f client.mk build

  fi
}

package() {
  cd comm-release

  make -j1 -f client.mk DESTDIR="$pkgdir" install
  install -m644 ../{vendor,kde}.js "$pkgdir/usr/lib/seamonkey/defaults/pref/"

  # Use system-provided dictionaries
  rm -r "$pkgdir"/usr/lib/seamonkey/dictionaries
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/seamonkey/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/seamonkey/hyphenation"

  # Desktop files
  install -Dm644 suite/branding/nightly/icons/gtk/seamonkey.png \
	"$pkgdir/usr/share/pixmaps/seamonkey.png"
  install -Dm644 ../seamonkey.desktop \
	"$pkgdir/usr/share/applications/seamonkey.desktop"

  # man page
  install -Dm644 obj-*/mozilla/dist/man/man1/seamonkey.1 "$pkgdir/usr/share/man/man1/seamonkey.1"

  # Remove development stuff
  rm -r "$pkgdir"/usr/{include,lib/seamonkey-devel,share/idl}

  # Remove a reference to srcdir
  sed -e "\|$srcdir|d" -i "$pkgdir/usr/lib/seamonkey/defaults/pref/channel-prefs.js"

  msg2 "The lightning, calendar-timezones and gdata-provider extensions are in"
  msg2 "$srcdir/comm-release/obj-$CHOST/mozilla/dist/xpi-stage"
  msg2 "You can install them manually from there"
}

