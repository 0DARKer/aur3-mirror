# Mantainer: Franco Tortoriello

pkgname=seamonkey-kde
pkgver=2.12.1
pkgrel=1
pkgdesc="SeaMonkey internet suite with OpenSUSE patches for better KDE integration"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('kdebase-runtime' 'gtk2' 'libevent' 'libnotify' 'libvpx' 'mime-types'
	 'mozilla-common' 'startup-notification' 'kmozillahelper>=0.6.3'
	 'nss>=3.13.3')
makedepends=('unzip' 'zip' 'python2' 'yasm' 'wireless_tools' 'mesa'
	     'autoconf2.13' 'imake')
optdepends=('wireless_tools: Location detection via available WiFi networks')
provides=("seamonkey=$pkgver")
conflicts=('seamonkey')
options=(!emptydirs)
install=seamonkey.install
url="http://www.seamonkey-project.org/"
_sourceurl="https://api.opensuse.org/public/source/mozilla:Factory"
source=("ftp://ftp.mozilla.org/pub/mozilla.org/seamonkey/releases/$pkgver/source/seamonkey-$pkgver.source.tar.bz2"
	'seamonkey.desktop' 'mozconfig' 'vendor.js' 'install-dir.patch'
	# seamonkey opensuse patches
	$_sourceurl/seamonkey/{mozilla-{gstreamer,nongnome-proxies,ntlm-full-path,shared-nss-db},seamonkey-shared-nss-db}.patch
	# firefox opensuse patches
	$_sourceurl/MozillaFirefox/{{firefox-{browser-css,kde},mozilla-kde}.patch,kde.js}
	'glibc216.patch')
md5sums=('89bc1360aa1eb59c5b4177373bb16502'
	 '219ad4600af34f4dc9301372ae4f4752'
	 '663302bacc82b811ec6189a2698a2e2e'
	 '7b06d28c84ecb1dbcc23598ff8c42bc7'
	 'a45806db2a84aa513b03c53f95edb923'
	 '177c659f374c7d15911ed5112fb61276'
	 '8b0ecfdf697485d7b7dd26291c0dc478'
	 '78afb7bce349fbbae9cb92e096daddd7'
	 'd0e4986d466d75edd5b04d0d6a76e467'
	 '9646b7682d0ce02e7ebb2e13ffe50157'
	 'df34733c7b43225d85453ebbb87521c6'
	 '528852aec6eada56d29aae281a7d7c47'
	 '7783cf3420b5ae4e7939afe35c3faff1'
	 '75df0f88cc7a7fa7d522459e4ff82cc5'
	 '878f5aa3c2cfa1a1d91e72c992f50dfd')

build() {
  cd "$srcdir/comm-release/mozilla"

  patch -Np1 -i "$srcdir/mozilla-nongnome-proxies.patch"
  patch -Np1 -i "$srcdir/mozilla-ntlm-full-path.patch"
  patch -Np1 -i "$srcdir/mozilla-shared-nss-db.patch"
  patch -Np1 -i "$srcdir/firefox-browser-css.patch"
  patch -Np1 -i "$srcdir/firefox-kde.patch"
  patch -Np1 -i "$srcdir/mozilla-kde.patch"
  patch -Np1 -i "$srcdir/glibc216.patch"
  install -m644 "$srcdir/kde.js" browser/app/profile/kde.js

  cd ..
  patch -Np1 -i "$srcdir/install-dir.patch"
  patch -Np1 -i "$srcdir/seamonkey-shared-nss-db.patch"
  cp ../mozconfig .mozconfig

  # Don't exit with error when some libs are missing which we have in
  # system.
  sed '/^MOZ_PKG_FATAL_WARNINGS/s@= 1@= 0@' \
      -i suite/installer/Makefile.in

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/seamonkey"
  export PYTHON="/usr/bin/python2"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  # Enable PGO
  export MOZ_PGO=1
  export DISPLAY=:99
  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

  make -f client.mk build
  kill $! || true
}

package() {
  cd "$srcdir/comm-release"

  make -j1 -f client.mk DESTDIR="$pkgdir" install
  install -m644 "$srcdir/vendor.js" "$pkgdir/usr/lib/seamonkey/defaults/pref"

  # Use system-provided dictionaries
  rm -r "$pkgdir"/usr/lib/seamonkey/dictionaries
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/seamonkey/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/seamonkey/hyphenation"

  install -m755 -d "$pkgdir/usr/share/applications"
  install -m755 -d "$pkgdir/usr/share/pixmaps"
  install -m644 suite/branding/nightly/icons/gtk/seamonkey.png \
                "$pkgdir/usr/share/pixmaps/"
  install -m644 "$srcdir/seamonkey.desktop" "$pkgdir/usr/share/applications/"

  rm -r "$pkgdir"/usr/{include,lib/seamonkey-devel,share/idl}
}

