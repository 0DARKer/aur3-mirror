# Maintainer: Ner0

pkgname=feedler-bzr
pkgver=314
pkgrel=1
pkgdesc="RSS feeds Reader from the Elementary Project."
arch=('i686' 'x86_64')
url="https://launchpad.net/feedler"
license=('GPL3')
depends=('libwebkit3' 'gtk3' 'sqlheavy' 'libsoup' 'libindicate' 'libunity'
	 'libxml2' 'libnotify' 'granite-bzr' 'dconf' 'libdbusmenu-gtk3' 'contractor-bzr')
makedepends=('bzr' 'vala' 'cmake')
options=('!makeflags')
install=feedler.install

_bzrtrunk=lp:feedler
_bzrmod=feedler

build() {
  msg "Connecting to Bazaar server...."

  if [ -d "$_bzrmod" ]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r "$pkgver" && cd ..
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r "$pkgver"
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_bzrmod-build"
  cp -rf "$_bzrmod" "$_bzrmod-build"
  cd "$_bzrmod-build"

  sed -i '/this.store.remove (folder_iter)/d' src/ui/sidebar.vala
  sed -i '/this.store.remove (channel_iter)/d' src/ui/sidebar.vala

  rm -rf build/
  mkdir build
  cd build
  CFLAGS="$CFLAGS -Wno-deprecated-declarations -lm"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
