# Maintainer: crazyelf5

pkgname=feedler-bzr
pkgver=55
pkgrel=1
pkgdesc="RSS feeds Reader from the Elementary Project."
arch=('i686' 'x86_64')
url="https://launchpad.net/feedler"
license=('GPL3')
depends=('libwebkit3' 'gtk3' 'sqlheavy-git' 'libsoup'
	 'libxml2' 'libnotify' 'granite-bzr' 'dconf')
makedepends=('bzr' 'vala' 'cmake')
install=${pkgname}.install

_bzrtrunk=lp:${pkgname%-*}
_bzrmod=${pkgname%-*}

build() {
  cd "${srcdir}"

  msg "Connecting to Bazaar server...."

  if [ -d ${_bzrmod} ]; then
    bzr up ${_bzrmod}
    msg "The local files are updated."
  else
    bzr branch ${_bzrtrunk} ${_bzrmod} -r ${pkgver}
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d ${_bzrmod}-build ] && rm -rf ${_bzrmod}-build
  cp -r ${_bzrmod} ${_bzrmod}-build
  cd ${_bzrmod}-build

  sed -i 's|sqlheavy-0.1|sqlheavy-0.2|' CMakeLists.txt

  [ -d build ] && rm -rf build
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_bzrmod}-build/build"
  make DESTDIR="${pkgdir}" install
  rm -rf "${srcdir}/${_bzrmod}-build"
}
