# Maintainer: Alexander Drozdov <adrozdoff@gmail.com>
pkgname=vera++
pkgver=1.1.1
pkgrel=1

_boostname=boost
_boostver=1_43_0

pkgdesc="A programmable tool for verification, analysis and transformation of C++ source code"
arch=(i686 x86_64)
url="http://www.inspirel.com/vera/"
license=('custom:boost')
depends=('sh' 'gcc-libs' 'tcl>=8.5' 'tcl<8.6')

makedepends=()
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=

source=(http://www.inspirel.com/vera/files/$pkgname-$pkgver.tar.gz
        http://downloads.sourceforge.net/sourceforge/${_boostname}/${_boostname}_${_boostver}.tar.gz
        vera++)

noextract=()

build() {
  cd "$srcdir/$pkgname-$pkgver"

  BOOST_DIR=${srcdir}/${_boostname}_${_boostver}
  TCL_LIB=tcl8.5
  sed -i 's|^\(BOOST_DIR[ \t]*=[ \t]*\).*$|\1'$BOOST_DIR'|;
          s|^\(TCL_LIB[ \t]*=[ \t]*\).*$|\1'$TCL_LIB'|' src/Make.common
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -d ${pkgdir}/usr/bin
  install -d ${pkgdir}/usr/lib/${pkgname}
  install -d ${pkgdir}/usr/share/doc/${pkgname}
  install -d ${pkgdir}/usr/share/licenses/${pkgname}

  install -m 755 vera++           ${pkgdir}/usr/lib/${pkgname}/vera++
  install -m 755 ${srcdir}/vera++ ${pkgdir}/usr/bin/vera++
  install -m 644 LICENSE_1_0.txt  ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE_1_0.txt
  
  cp -a profiles scripts ${pkgdir}/usr/lib/${pkgname}/
  cp -a doc CHANGES      ${pkgdir}/usr/share/doc/${pkgname}/
}

md5sums=('1077f55265e67c53dccaba200fbb8668'
         '734565ca4819bf04bd8e903e116c3fb1'
         'ca32269270ea3fa61079e05630cdbb46')
