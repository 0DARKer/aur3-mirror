# Maintainer: James Reed <supplantr at archlinux dot info>

_pkgname=xss-lock
pkgname=$_pkgname-git
pkgver=20140302.82
pkgrel=1
pkgdesc='use external locker as X screen saver'
arch=('i686' 'x86_64')
url='https://bitbucket.org/raymonad/xss-lock'
license=('MIT')
depends=('xcb-util' 'systemd')
makedepends=('cmake' 'python-docutils')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+https://bitbucket.org/raymonad/xss-lock")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$_pkgname"
	echo "$(git log -1 --format="%cd" --date=short | sed 's|-||g').$(git rev-list --count master)"
}

build() {
	cd "$srcdir/$_pkgname"
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
	make
}

package() {
	cd "$srcdir/$_pkgname"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
