# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Alessandro Nakamuta <alessandro dot ufms at gmail dot com>
# Contributor: 3ED <krzysztof1987 at gmail dot com>

pkgname=nautilus-wallpaper
pkgver=0.1
pkgrel=5
pkgdesc="A nautilus extension to set your desktop wallpaper from the context menu"
url="http://www.gnome.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('nautilus2')
makedepends=('intltool')
options=('!libtool')
source=("${pkgname}-0.1.tar.gz"
        "01_add_gconf_pkgconfig.patch"
        "02_move_to_extensions-2.patch"
        "03_fix_doc_prefix.patch")
md5sums=('e47350626509c85dd9339d0b98900dd6'
         '1b05fa6983061df794c4d462b2d8ed28'
         '7601e33bbf4cff550f0b974f6bc0e062'
         '25569f0797abf01aa6596b7fa37ba8a1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i ${srcdir}/01_add_gconf_pkgconfig.patch
  patch -Np1 -i ${srcdir}/02_move_to_extensions-2.patch
  patch -Np1 -i ${srcdir}/03_fix_doc_prefix.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

