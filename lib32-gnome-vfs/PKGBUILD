# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=gnome-vfs
pkgname=lib32-$_pkgbasename
pkgver=2.24.4
pkgrel=5
pkgdesc="The GNOME Virtual File System (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-avahi' 'lib32-bzip2' 'lib32-gconf'
	 'lib32-gnutls' 'lib32-hal' 'lib32-krb5'
	 "$_pkgbasename")
makedepends=('intltool' 'gtk-doc' 'gnome-common'
	     'lib32-dbus-glib' 'gnome-mime-data'
	     'lib32-keyutils')
options=('!libtool' '!emptydirs' '!docs')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/${pkgver%.*}/$_pkgbasename-${pkgver}.tar.bz2
        hal-show-volume-names.patch
        gnutls-config.patch)
sha256sums=('62de64b5b804eb04104ff98fcd6a8b7276d510a49fbd9c0feb568f8996444faa'
            'b38b86d6c966dcf64f9140d800aebe8a14845bb8ce7c045f52d4df483cacd23b'
            '66c7cfb12995c0dd94a2caea95c7e3c55981993f05a79c585d60915ff131955d')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  #Archlinux patch (b.g.o #321498)
  patch -Np1 -i "${srcdir}/hal-show-volume-names.patch"

  #Fix build with new gnutls
  patch -Np1 -i "${srcdir}/gnutls-config.patch"
  libtoolize --force
  gtkdocize
  aclocal
  autoconf
  automake

  CC='gcc -m32' CXX='g++ -m32' \
  ./configure --prefix=/usr \
  	      --sysconfdir=/etc \
  	      --localstatedir=/var \
  	      --disable-static \
  	      --libdir=/usr/lib32 \
  	      --libexecdir=/usr/lib32 \
  	      --enable-avahi \
  	      --enable-gnutls \
  	      --enable-hal \
  	      --disable-howl \
  	      --disable-openssl \
  	      --enable-samba
  	      
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include,share} ${pkgdir}/etc
}
