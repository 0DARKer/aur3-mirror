# Maintainer: Slash <demodevil5 [at] yahoo [dot] com>

pkgname=maraschinoproject-git
pkgver=20120407
pkgrel=1
pkgdesc="Web-based frontend for XBMC, Sickbeard, Sabnzbd, and more."
arch=('any')
url="http://www.maraschinoproject.com/"
license=('GPL')
depends=('python2')
makedepends=('git')
backup=('etc/conf.d/maraschino'
        'opt/maraschino/settings.py'
        'opt/maraschino/maraschino.db')
install='maraschino.install'
source=('maraschino.sh' 'maraschino.rc' 'maraschino.confd')
md5sums=('195ed78ae6fe35ca369f3a224d0d76b6'
         'ea6b0e7a15204586ead39142b760d92c'
         '0c1da3d4c518dd4fadd27ddaba68a7fd')

_gitroot=https://github.com/mrkipling/maraschino
_gitname=maraschino

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting build..."
}

package() {
    # Make destination directory
    install -d ${pkgdir}/opt/

    # Move Maraschino files to destintion directory
    cp -r ${srcdir}/${_gitname} ${pkgdir}/opt/maraschino

    # Copy Default Settings Example
    install -m644 "${pkgdir}/opt/maraschino/settings_example.py" \
        "${pkgdir}/opt/maraschino/settings.py" \

    # Set Default Database Path
    sed -i 's@/path/to/@/opt/maraschino/@g' \
        "${pkgdir}/opt/maraschino/settings.py"

    # Install Launcher Script
    install -D -m755 "${srcdir}/maraschino.sh" \
        "${pkgdir}/usr/bin/maraschino"

    # Install Daemon Script
    install -D -m755 "${srcdir}/maraschino.rc" \
        "${pkgdir}/etc/rc.d/maraschino"

    # Install Daemon Configuration
    install -D -m644 "${srcdir}/maraschino.confd" \
        "${pkgdir}/etc/conf.d/maraschino"
}

