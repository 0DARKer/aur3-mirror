# Maintainer: Que Quotion <quequotion@mailinator.com>
# Contributor: Andrew Crerar <asc9003 [at] rit [dot] edu>
# Contributor: RKA KriK <rka_krik@mail.ru>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: Charles Bos <charlesbos1 AT gmail>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: hbdee <hbdee.arch@gmail.com>

pkgname=gtk3-git-ubuntu
_gitname=gtk+
pkgver=3.12.0.47.g46802d3
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (git version) with Ubuntu patches from bzr"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' \
	'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' \
	'shared-mime-info' 'colord' 'at-spi2-atk' 'wayland-git' 'libxkbcommon' \
	'glib2-git>=2.39.0' 'gobject-introspection-git')
makedepends=('gobject-introspection' 'bzr')
provides=('gtk3=$pkgver' 'gtk3-ubuntu')
conflicts=('gtk3')
backup=('etc/gtk-3.0/settings.ini')
options=('!libtool')
install=gtk3-git.install
source=('git://git.gnome.org/gtk+' 'settings.ini' 'repatch-018_gdkenumtypes.c_location.patch' 'repatch-043_ubuntu_menu_proxy.patch' 'repatch-062_ubuntu-set-grab-add.patch' 'repatch-075_expose_gdkwidget_for_gtkrange.patch' 'repatch-071_fix-installation-of-HTML-images.patch' 'repatch-101_revert_symbolic_icon_search.patch' 'repatch-ubuntu_gtk_custom_menu_items.patch')
md5sums=('SKIP'
         'fd06f6b71336389ffcc97b5df818fbf9'
         'c8098c1ec0ef74fb0a80df5724e56db4'
         '455aba365d39f1ac781fbf4f19299528'
         'a23da0f7e6ccf95e0a51562aaa483f86'
         'a9aa0bed05b0c1f81b7d1b1d5583fb38'
         'bb7de9f7164c9126cba6c87f45c39b07'
         '836ece078bb5266c431af75c1b57e103'
         'f2ee8749a69fb37c1e2123d430ea6f19')

pkgver() {
	cd $_gitname
	git describe --always | sed 's/-/./g'
}

build() {

	cd $srcdir

	#Pull latest Ubuntu patchset (minus git cherry-picks)
	bzr cat lp:ubuntu/gtk+3.0/debian/patches/series > series
	for i in $(cat series) ; do
		if [[ ! $i == *git* ]]; then
			bzr cat lp:ubuntu/gtk+3.0/debian/patches/$i > $i
			#Apply available repatches
			if [ -f "repatch-$i" ]; then
				msg "Updating ${i} ..." 
				patch -i "repatch-$i"
			fi
		fi
	done

	cd $_gitname

	#Apply Ubuntu patches
	for i in $(cat $srcdir/series) ; do
		if [[ ! $i == *git* ]]; then
			msg "Applying ${i} ..."
			patch -Np1 -i  "$srcdir/$i"
		fi
	done

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-gtk2-dependency \
		--disable-schemas-compile \
		--enable-x11-backend \
		--enable-broadway-backend \
		--enable-wayland-backend

	#https://bugzilla.gnome.org/show_bug.cgi?id=655517
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make
}

package() {
	cd $_gitname
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}

