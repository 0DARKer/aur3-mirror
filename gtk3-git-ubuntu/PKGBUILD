# Maintainer: Que Quotion <quequotion@mailinator.com>
# Contributor: Andrew Crerar <asc9003 [at] rit [dot] edu>
# Contributor: RKA KriK <rka_krik@mail.ru>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: Charles Bos <charlesbos1 AT gmail>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: hbdee <hbdee.arch@gmail.com>

pkgname=gtk3-git-ubuntu
_gitname=gtk+
_bzrdir=~ubuntu-desktop/gtk/ubuntugtk3
pkgver=3.13.1.103.gf4d02bd
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (git version with Ubuntu patches from bzr)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' \
	'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' \
	'shared-mime-info' 'colord' 'at-spi2-atk' 'wayland-git' 'libxkbcommon' \
	'glib2-git' 'gobject-introspection-git')
makedepends=('gobject-introspection' 'bzr')
provides=("gtk3=${pkgver}" "gtk3-ubuntu=${pkgver}" "gtk3-git=${pkgver}")
conflicts=('gtk3')
backup=('etc/gtk-3.0/settings.ini')
options=('!libtool')
install=gtk3-git.install
source=('git://git.gnome.org/gtk+'
        'settings.ini'
        'repatch-071_fix-installation-of-HTML-images.patch'
        'repatch-x-canonical-accel.patch'
	'repatch-0001-Add-a-setting-for-dialog-headers.patch')
sha512sums=('SKIP'
            '3ee323c287ad4354d5521f584d06149bbe6bb3c6ed4f409e7544c9ac59892a89d7b52e4b7807e6ed7e3a3e064372dc61422153153f67acb97cc371fe1d8200f2'
            '67fb57a124d8ee4a1675bc87255ad4cd5ae327a930444a39803122d7bbc3edfd5e2f513dda150d2f70c202a0c979c3729c9dd2e575292cdd190f568548cf450d'
            'b6e218e57288128d4d6cb58982c3b2e1f2f573e3884bac94aa00d3bf0f667114170659098b5f36a1b63db5bcec0bf44da3179dbf84ff38d28769fbaf7d3799ac'
            '3e654e70ebf826168cf925429dfb59bcebb3fc66220b3a503d4399eb48f12f751bbf0ac1e38abad7d72a694be71eba0028685d0dd86d7afb235f2188d247687b')

pkgver() {
	cd "${_gitname}"
	git describe --always | sed 's/-/./g'
}

prepare() {

	if [ ! -d "${srcdir}/../ubuntu/" ]; then
		mkdir "${srcdir}/../ubuntu/"
		cd "${srcdir}/../ubuntu/"
		#Pull latest Ubuntu patchset (minus git cherry-picks)
		bzr cat lp:"${_bzrdir}/debian/patches/series" > series
		while read i; do
    			if [[ "${i}" =~ ^# || -z "${i}" || "${i}" == *git* ]]; then
				continue # Skip comments, newlines, and git patches
			else
				msg "Downloading ${i} ..."
				bzr cat -q lp:"${_bzrdir}/debian/patches/${i}" > "${i}"
				#Apply available repatches
				if [ -f "../repatch-${i}" ]; then
					msg "Updating ${i} ..." 
					patch -si "../repatch-${i}"
				fi
			fi
		done < series
			
	fi
	ln -s "${srcdir}/../ubuntu/"* "${srcdir}/"
}

build() {

	cd "${_gitname}"

	#Apply Ubuntu patches
	while read i; do
    		if [[ "${i}" =~ ^# || -z "${i}" || "${i}" == *git* ]]; then
			continue
		else
			msg "Applying ${i} ..."
			patch -Nsp1 -i  "${srcdir}/${i}"
		fi
	done < "${srcdir}/series"

	./autogen.sh --prefix=/usr --sysconfdir=/etc \
		--localstatedir=/var \
		--enable-gtk2-dependency \
		--disable-schemas-compile \
		--enable-x11-backend \
		--enable-broadway-backend \
		--enable-wayland-backend

	#https://bugzilla.gnome.org/show_bug.cgi?id=655517
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd "${_gitname}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 "${srcdir}/settings.ini" "${pkgdir}/etc/gtk-3.0/settings.ini"
}

