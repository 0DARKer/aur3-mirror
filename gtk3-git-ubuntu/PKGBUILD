# Maintainer: Que Quotion <quequotion@mailinator.com>
# Contributor: Andrew Crerar <asc9003 [at] rit [dot] edu>
# Contributor: RKA KriK <rka_krik@mail.ru>
# Contributor: Boohbah <boohbah at gmail.com>
# Contributor: Charles Bos <charlesbos1 AT gmail>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: hbdee <hbdee.arch@gmail.com>

pkgname=gtk3-git-ubuntu
_gitname=gtk+
pkgver=3.12.0.55.gfee33b1
pkgrel=1
pkgdesc="GObject-based multi-platform GUI toolkit (git version with Ubuntu patches from bzr)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('atk' 'cairo' 'gtk-update-icon-cache' 'libcups' 'libxcursor' \
	'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'pango' \
	'shared-mime-info' 'colord' 'at-spi2-atk' 'wayland-git' 'libxkbcommon' \
	'glib2-git>=2.39.0' 'gobject-introspection-git')
makedepends=('gobject-introspection' 'bzr')
provides=('gtk3=$pkgver' 'gtk3-ubuntu')
conflicts=('gtk3')
backup=('etc/gtk-3.0/settings.ini')
options=('!libtool')
install=gtk3-git.install
source=('git://git.gnome.org/gtk+'
        'settings.ini'
        'repatch-018_gdkenumtypes.c_location.patch'
        'repatch-043_ubuntu_menu_proxy.patch'
        'repatch-062_ubuntu-set-grab-add.patch'
        'repatch-075_expose_gdkwidget_for_gtkrange.patch'
        'repatch-071_fix-installation-of-HTML-images.patch'
        'repatch-101_revert_symbolic_icon_search.patch'
        'repatch-ubuntu_gtk_custom_menu_items.patch') # TODO: implement custom menu items
sha512sums=('SKIP'
            '3ee323c287ad4354d5521f584d06149bbe6bb3c6ed4f409e7544c9ac59892a89d7b52e4b7807e6ed7e3a3e064372dc61422153153f67acb97cc371fe1d8200f2'
            'c3583dccbbe0742fa1a22da56a9f7c4860ac85f6ee5470e4413c0c7d797ef22931c69f46c6eaf0d4d300700874073bc066ce0bfdb758ce9db4eccb23ac796998'
            '7465c6e3f96d73c0a5995326c2a32242135f411ce16d828953e6d46d190b4bc54e2dcdbf2838111d8a252fb633a80a34c09366b4aca206a559d4f1b4de6dd02e'
            '5a9a0e92e6904918aa3a401a45f439a7e97f1dd5253a4116e90d7e0d645cc0ed8745c8877c2106cdfb863bde9e296d9c99b1ca78fc7d3bda360e04a192efbf4e'
            '2098aee750a5ce1fc8b54a8adab378c3c929968f9461cae47be78034a4ceea565da7160d2d9463382c528c2c1a985185e73a714e7c9024257a46d6497241d3fc'
            'dee4dff82edc1c6900f9e41332747dec6ffe1175a11cc0c4c0633558fcad4a9dce7ed5949088079ab7f83c59602d65ccbf73878e2bf876f3aed2c7d314ca18d2'
            'f616fa298fa559f7b83cf41dbc89d126adae833401c3e35616e199bc46a62d8d4f20a0a64a5b5d7e09c0b3fe8cf69838820095afc8b28e3b2a35105dde52b7f0'
            '0c0677b1b879e73ddd8821df1d64b43bb3d8fe2bbc2f3a512ce8ffe2d1f1522bb9053095ddf069fcf5708a5c16d3a72a2efd0fed7c4ad5c237b4498a68123d4d')

pkgver() {
	cd $_gitname
	git describe --always | sed 's/-/./g'
}

build() {

	cd $srcdir

	#Pull latest Ubuntu patchset (minus git cherry-picks)
	bzr cat lp:ubuntu/gtk+3.0/debian/patches/series > series
	while read i; do
    		if [[ ${i} =~ ^# || -z ${i} || ${i} == *git* ]]; then
			continue
		else
			msg "Downloading ${i} ..."
			bzr cat -q lp:ubuntu/gtk+3.0/debian/patches/${i} > ${i}
			#Apply available repatches
			if [ -f "repatch-${i}" ]; then
				msg "Updating ${i} ..." 
				patch -si "repatch-${i}"
			fi
		fi
	done < series

	cd $_gitname

	#Apply Ubuntu patches
	while read i; do
    		if [[ ${i} =~ ^# || -z ${i} || ${i} == *git* ]]; then
			continue
		else
			msg "Applying ${i} ..."
			patch -Nsp1 -i  "$srcdir/${i}"
		fi
	done < $srcdir/series

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-gtk2-dependency \
		--disable-schemas-compile \
		--enable-x11-backend \
		--enable-broadway-backend \
		--enable-wayland-backend

	#https://bugzilla.gnome.org/show_bug.cgi?id=655517
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make
}

package() {
	cd $_gitname
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/settings.ini" "$pkgdir/etc/gtk-3.0/settings.ini"
}

