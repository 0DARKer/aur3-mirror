pkgname=amule-git
pkgver=10834
pkgrel=1
pkgdesc='Client for the eD2k and Kad networks'
arch=(i686 x86_64)
url=http://amule.org
license=(GPL2)
depends=(crypto++ gd geoip libupnp wxgtk2.8)
makedepends=(git)
provides=(amule)
conflicts=(amule)
source=(git://repo.or.cz/amule.git)
md5sums=(SKIP)

_confopts=(
  --disable-debug
  --enable-alcc
  --enable-amule-daemon
  --enable-amulecmd
  --enable-cas
  --enable-ccache
  --enable-geoip
  --enable-mmap
  --enable-optimize
  --enable-webserver
  --enable-xas
  --mandir=/usr/share/man
  --prefix=/usr
  --with-wx-config=wx-config-2.8
)

pkgver() {
  cd amule

  cat .svn-revision
}

build() {
  cd amule

  ./configure ${_confopts[@]}

  make
}

package() {
  cd amule

  make DESTDIR=$pkgdir install

  install -m644 *.txt docs/{*.dia,AUTHORS,COPYING,README.*} README* $pkgdir/usr/share/doc/amule
}
