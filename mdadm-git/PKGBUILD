# Contributor: Mathias Buren <mathias.buren@gmail.com>
pkgname=mdadm-git
pkgver=20110701
pkgrel=3
pkgdesc="A tool for managing/monitoring Linux md device arrays, also known as Software RAID. -git version"
arch=(i686 x86_64)
license=('GPL')
url="http://neil.brown.name/blog/mdadm/"
conflicts=('mkinitcpio<0.5.99' 'mdadm')
makedepends=('git')
depends=('glibc')
backup=('etc/mdadm.conf')
source=(mdadm-git
        mdadm.conf
        mdadm_install
        mdadm_hook)
install=mdadm.install
replaces=('raidtools')

_gitroot="git://neil.brown.name/mdadm"
_gitname="mdadm"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname --depth=1
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  make || return 1
  # build static mdassemble for Arch's initramfs
  make MDASSEMBLE_AUTO=1 mdassemble

}

package() {
   cd "$srcdir/$_gitname-build"
   make INSTALL=/bin/install DESTDIR=$pkgdir install
   install -D -m644 $srcdir/mdadm.conf $pkgdir/etc/mdadm.conf
   install -D -m755 $srcdir/mdadm-git $pkgdir/etc/rc.d/mdadm
   install -D -m644 $srcdir/mdadm_install $pkgdir/lib/initcpio/install/mdadm
   install -D -m644 $srcdir/mdadm_hook $pkgdir/lib/initcpio/hooks/mdadm
   # symlink for backward compatibility
   ln -sf /lib/initcpio/hooks/mdadm  $pkgdir/lib/initcpio/hooks/raid
   install -D -m755 mdassemble $pkgdir/sbin/mdassemble
}
md5sums=('6df172c8f77b280018cf87eb3d313f29'
         '00cbed931db4f15b6ce49e3e7d433966'
         '865c3d39e5f5dae58388160b563981f1'
         '1a3eb63832cecd6550f5b0a21d58cfdb')
