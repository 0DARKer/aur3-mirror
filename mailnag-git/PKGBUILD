# Maintainer: Kevin MacMartin <prurigro@gmail.com>
# Contributor: Patrick Ulbrich <zulu99 at gmx . net>
# Contributor: rasmus <rasmus . pank at gmail . com>

_pkgname=mailnag
pkgname=$_pkgname-git
pkgver=20150201.r486.9f393a1
pkgrel=1
pkgdesc='An extendable mail notification daemon'
arch=('any')
url='https://github.com/pulb/mailnag'
license=('GPL')
depends=('python2-gobject' 'python2-httplib2' 'libgnome-keyring' 'gnome-keyring' 'python2-xdg' 'python2-dbus' 'libnotify' 'gst-plugins-base' 'gtk3' 'gdk-pixbuf2')
optdepends=('mailnag-gnome-shell: for tighter GNOME 3 integration'
            'mailnag-goa-plugin-git: for GNOME online accounts integration')
makedepends=('gettext')
source=("git+$url")
sha512sums=('SKIP')
install="$pkgname.install"
conflicts=("$_pkgname")
provides=("$_pkgname")

pkgver() {
  cd $_pkgname
  printf "%s.r%s.%s" "$(git show -s --format=%ci master | sed 's/\ .*//g;s/-//g')" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd $_pkgname
  python2 setup.py install --root="$pkgdir"
}
