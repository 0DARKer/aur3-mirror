# Maintainer: Ner0

pkgname=granite-bzr
pkgver=555
pkgrel=1
pkgdesc="A development library for elementary development"
arch=('i686' 'x86_64')
url="https://launchpad.net/granite"
license=('LGPL3')
groups=('pantheon')
depends=('glib2' 'gobject-introspection' 'gtk3' 'libgee' 'hicolor-icon-theme')
makedepends=('bzr' 'cmake' 'vala')
conflicts=('granite')
provides=('granite')
install=granite.install
source=("granite::bzr+https://code.launchpad.net/~elementary-pantheon/granite/granite")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/granite"
  bzr revno
}

build() {
  cd "$srcdir/granite"

  rm -rf build/
  mkdir build
  cd build

  CFLAGS="${CFLAGS} -Wno-deprecated-declarations"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DICON_UPDATE=OFF
  make
}

package() {
  cd "$srcdir/granite/build"
  make DESTDIR="$pkgdir/" install
}
