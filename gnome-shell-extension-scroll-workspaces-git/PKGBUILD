# Maintainer: XZS <d.f.fischer at web dot de>

pkgname="gnome-shell-extension-scroll-workspaces-git"
_dirname="gnome-shell-scroll-workspaces/scroll-workspaces"
pkgdesc="Switch workspaces by scrolling in the top panel"
pkgver=19.b8358ae
pkgrel=1
arch=(any)
url="https://github.com/gfxmonk/gnome-shell-scroll-workspaces/"
license=(GPLv2)
depends=('gnome-shell>=3.8')
makedepends=('git')
source=('git+https://github.com/gfxmonk/gnome-shell-scroll-workspaces/')
md5sums=('SKIP')

pkgver() {
  cd gnome-shell-scroll-workspaces
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  # gnome-shell 3.12 is not officially supported.
  cd "$_dirname"
  sed -i '5s/]/, "3.12"]/' metadata.json
}

package () {
  cd "$_dirname"
  destdir="$pkgdir/usr/share/gnome-shell/extensions/scroll-workspaces@gfxmonk.net"
  install -d "$destdir"
  mv * "$destdir"
}
