# Maintainer : Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

_pkgname="mactel-boot"

_ver="0.9"
_rpm_rel="1"
_fedora_rel="fc16"

_pkgver="${_ver}"

pkgname="${_pkgname}"
pkgver="${_pkgver}"

pkgrel=1
pkgdesc="Utility to bless EFI bootloaders in Apple-Intel Macs"
url="https://bugzilla.redhat.com/show_bug.cgi?id=755093"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=()
depends=('util-linux')
options=(!strip)

source=("http://www.codon.org.uk/~mjg59/mactel-boot/mactel-boot-${_ver}-${_rpm_rel}.${_fedora_rel}.src.rpm"
        'mactel-boot-install.sh')

noextract=("mactel-boot-${_ver}-${_rpm_rel}.${_fedora_rel}.src.rpm")

sha256sums=('9ee86469664448e0a906b5a5fdcf158fe8434c231e7d623b1c44b9075cd322cb'
            'a4f1039a07eac4ac20c686c5e976a19db03b6a9a9609d8fda832ed8a23027766')

_extract_src_rpm() {
	
	cd "${srcdir}/"
	
	bsdtar xf "${srcdir}/mactel-boot-${_ver}-${_rpm_rel}.${_fedora_rel}.src.rpm"
	
	bsdtar xf "${srcdir}/mactel-boot-${_ver}.tar.bz2"
	
}

build() {
	
	_extract_src_rpm
	
	cd "${srcdir}/mactel-boot-${_ver}"
	
	CFLAGS="" make
	echo
	
}

package() {
	
	cd "${srcdir}/mactel-boot-${_ver}"
	
	make DESTDIR="${pkgdir}/" install
	echo
	
	install -D -m0755 "${srcdir}/mactel-boot-install.sh" "${pkgdir}/usr/sbin/mactel-boot-install"
	
	install -d "${pkgdir}/usr/share/mactel-boot"
	install -D -m0644 "${srcdir}/mactel-boot-${_ver}/SystemVersion.plist" "${pkgdir}/usr/share/mactel-boot/SystemVersion.plist"
	
}
