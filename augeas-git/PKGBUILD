# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Jon Nordby <jononor@gmail.com>

_pkgname=augeas
pkgname=$_pkgname-git
pkgver=1.3.0.r28.g92f8738
pkgrel=1
pkgdesc="program for editing configuration files and a configuration API - git version"
arch=('i686' 'x86_64')
url="http://augeas.net"
license=('LGPL')
depends=('readline')
makedepends=('git')
provides=($_pkgname)
conflicts=($_pkgname)
source=(git://github.com/hercules-team/$_pkgname.git)
md5sums=('SKIP')


pkgver() {
  cd "$_pkgname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^release.//'
}

build() {
  cd "$_pkgname"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir/" install
}
