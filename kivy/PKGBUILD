# Maintainer: SpepS <dreamspepser at yahoo dot it>

_name=Kivy
pkgname=kivy
pkgver=1.2.0
pkgrel=1
pkgdesc="A python module for developing multi-touch enabled media rich applications."
arch=(i686 x86_64)
url="http://kivy.org/"
license=('LGPL')
depends=('python-pygame' 'python-opengl' 'python-imaging'
         'gstreamer0.10-python' 'cython2' 'mtdev')
optdepends=('twisted: networking framework integration')
source=("http://pypi.python.org/packages/source/K/$_name/$_name-$pkgver.tar.gz")
md5sums=('468da8a353c2ea4936eb92d71403c960')

build() {
  cd "$srcdir/$_name-$pkgver"

  # cython 0.16 fix
  sed -i "s/&self/self/" kivy/graphics/fbo.pyx

  python2 setup.py build
}

package() {
  cd "$srcdir/$_name-$pkgver"
  python2 setup.py install --root="$pkgdir"

  # python2 fixes
  sed -i "s/\#\!.*python/&2/" \
    `grep -rl "\#\!.*python" "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
