# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=kivy
pkgver=1.6.0
pkgrel=1
pkgdesc="A python module for developing multi-touch enabled media rich applications."
arch=(i686 x86_64)
url="http://kivy.org/"
license=('LGPL')
depends=('python2-pygame' 'python2-opengl' 'python2-imaging'
         'gstreamer0.10-python' 'cython2' 'libgl' 'mtdev')
makedepends=('mesa')
optdepends=('twisted: networking framework integration')
source=("http://pypi.python.org/packages/source/K/Kivy/Kivy-$pkgver.tar.gz")
md5sums=('951941c03c4ce6c066ee46c79489e7d7')

build() {
  cd "$srcdir/Kivy-$pkgver"

  # strip /local
  sed -i "s|/local||" setup.py

#  export USE_GLEW=0 \
#         USE_SDL=1 \
#         USE_MESAGL=1

  python2 setup.py build
}

package() {
  cd "$srcdir/Kivy-$pkgver"
  python2 setup.py install --root="$pkgdir"

  # python2 fixes
  sed -i "s/\#\!.*python/&2/" \
    `grep -rl "\#\!.*python" "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
