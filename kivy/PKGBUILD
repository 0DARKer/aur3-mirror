# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=kivy
pkgver=1.7.0
pkgrel=1
pkgdesc="A python module for developing multi-touch enabled media rich applications."
arch=(i686 x86_64)
url="http://kivy.org/"
license=('LGPL')
depends=('python2-pygame' 'python2-opengl' 'python2-imaging'
         'gstreamer0.10-python' 'cython2' 'libgl')
makedepends=('mesa')
optdepends=('twisted: networking framework integration'
            'python2-pigments: syntax highligh'
            'python2-docutils: reStructuredText renderer')
source=("http://pypi.python.org/packages/source/K/Kivy/Kivy-$pkgver.tar.gz")
md5sums=('f0d87c417f4c5d03780f1fcdc4208d9b')

build() {
  cd "$srcdir/Kivy-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/Kivy-$pkgver"
  python2 setup.py install --root="$pkgdir"

  # python2 fixes
  sed -i "s/\#\!.*python$/&2/" \
    `grep -rl "\#\!.*python$" "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
