# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=kivy
pkgver=1.4.1
pkgrel=3
pkgdesc="A python module for developing multi-touch enabled media rich applications."
arch=(i686 x86_64)
url="http://kivy.org/"
license=('LGPL')
depends=('python2-pygame' 'python2-opengl' 'python2-imaging'
         'gstreamer0.10-python' 'cython2' 'mtdev')
optdepends=('twisted: networking framework integration')
source=("http://pypi.python.org/packages/source/K/Kivy/Kivy-$pkgver.tar.gz"
        "https://github.com/kivy/kivy/commit/5f0a2c4504114f6e95097c048887363af0e7738c.patch")
md5sums=('94bba894269e4bdecc7881f256367e01'
         '3e26d8b0c774743f1c1e1ea823e45cfc')

build() {
  cd "$srcdir/Kivy-$pkgver"

  # cython 0.17.2 temporary fix
  patch -p1 -i "$srcdir/${source[1]##*/}"

  python2 setup.py build
}

package() {
  cd "$srcdir/Kivy-$pkgver"
  python2 setup.py install --root="$pkgdir"

  # python2 fixes
  sed -i "s/\#\!.*python/&2/" \
    `grep -rl "\#\!.*python" "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
