# Maintainer: SpepS <dreamspepser at yahoo dot it>

_pkg=Kivy
pkgname=kivy
pkgver=1.0.6
pkgrel=1
pkgdesc="A python module for developing multi-touch enabled media rich applications."
arch=(i686 x86_64)
url="http://kivy.org/"
license=('LGPL')
depends=('python-pygame' 'python-opengl' 'gstreamer0.10-python' 'python-imaging' 'cython')
source=("http://pypi.python.org/packages/source/K/$_pkg/$_pkg-$pkgver.tar.gz")
md5sums=('6cdb972817f62f8bded3cf43c8d9364e')

build() {
  cd "$srcdir/$_pkg-$pkgver"

  # Python 2 fixes
  export PYTHON="python2"
  sed -i "s|\(env python\).*|\12|;s|\(bin/python\).*|\12|" \
      `grep -Erl "(env python|bin/python)" .`

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkg-$pkgver"

  python2 setup.py install --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
