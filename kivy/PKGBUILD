# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=kivy
pkgver=1.0.3_alpha
_pkg=${pkgname/k/K}
_ver=${pkgver/_/-}
pkgrel=1
pkgdesc="A python module for developing multi-touch enabled media rich applications."
arch=(i686 x86_64)
url="http://kivy.org/"
license=('GPL')
depends=('python-pygame' 'python-opengl' 'gstreamer0.10-python' 'python-imaging' 'cython')
source=("http://pypi.python.org/packages/source/K/$_pkg/$_pkg-$_ver.tar.gz")
md5sums=('dbd113ef52f7430ec92053a8f6f61588')

build() {
  cd "$srcdir/$_pkg-$_ver"

  # Python 2 fixes
  export PYTHON="python2"
  sed -i "s|\(env python\).*|\12|;s|\(bin/python\).*|\12|" \
      `grep -Erl "(env python|bin/python)" .`

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkg-$_ver"

  python2 setup.py install --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
