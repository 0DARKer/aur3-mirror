pkgname=mingw-w64-libuecc
pkgver=3
pkgrel=1
pkgdesc="Very small Elliptic Curve Cryptography library (mingw-w64)"
arch=(any)
url="http://git.universe-factory.net/libuecc"
license=("BSD")
makedepends=(mingw-w64-gcc cmake)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("https://projects.universe-factory.net/attachments/download/42/libuecc-3.tar.xz"
"Toolchain-i686-w64-mingw32.cmake"
"Toolchain-x86_64-w64-mingw32.cmake")
md5sums=('3c45ffecc7709ea929892993808e218e'
         'da10ad00a161800e0b011a77c3efbf76'
         'adea9ccdeea1784c7cd30af195f2a8cb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/libuecc-$pkgver"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir "build-${_arch}" && pushd "build-${_arch}"
    PATH="/usr/${_arch}/bin:$PATH" cmake \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr/${_arch} \
      -DCMAKE_TOOLCHAIN_FILE=${srcdir}/Toolchain-${_arch}.cmake \
      ..
    make
    ${_arch}-gcc -shared src/CMakeFiles/uecc.dir/*.obj -o libuecc.dll -Xlinker --out-implib -Xlinker libuecc.dll.a
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/libuecc-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -Dm644 libuecc.dll "$pkgdir/usr/${_arch}/bin/libuecc.dll"
    install -m644 libuecc.dll.a "$pkgdir/usr/${_arch}/lib/"
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
