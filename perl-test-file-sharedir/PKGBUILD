# Contributor: : Kent Fredric <kentnl@cpan.org>
# Maintainer : Kars Wang <jaklsy AT gmail.com>

pkgname='perl-test-file-sharedir'
_dist='Test-File-ShareDir'
pkgver='1.001001'
pkgrel='1'
pkgdesc='Create a Fake ShareDir for your modules for testing'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs' 'purge')
optdepends=()
url="https://metacpan.org/release/${_dist}"
depends=('perl>=5.6' 'perl-file-copy-recursive>=0' 'perl-file-sharedir>=1.00' 'perl-class-tiny>=0' 'perl-scope-guard>=0')
optdepends=('perl-path-tiny>=0.058')
makedepends=('perl-extutils-makemaker>=0')
checkdepends=('perl-test-fatal' 'perl-test-simple>=0.98')
source=("http://search.cpan.org/CPAN/authors/id/K/KE/KENTNL/${_dist}-${pkgver}.tar.gz")
sha256sums=('c8815ee3644949ad6419616c2706a04b3c22d709bfb610ad5c4cfe78884a8658')

build() (
  cd "$srcdir/$_dist-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  /usr/bin/perl Makefile.PL
  make
)

check() (
  cd "$srcdir/$_dist-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
)

package() (
  cd "$srcdir/$_dist-$pkgver"
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)
