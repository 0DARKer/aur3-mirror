# Maintainer: xzy3186 <xzy3186(at)gmail>
# Contributors: Bob Fanger <bfanger(at)gmail>, Filip <fila pruda com>, Det <nimetonmaili(at)gmail>
# Based on [community-testing]'s r8168: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/r8168

pkgname=r8168-all
_basename=r8168
pkgver=8.038.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards (for all kernels)"
arch=('i686' 'x86_64')
url="http://www.realtek.com.tw"
license=('GPL')
makedepends=('linux-headers')
conflicts=('r8168' 'r8168-lts')
provides=('r8168')
#source=("https://r8168.googlecode.com/files/$_basename-$pkgver.tar.bz2")
source=("http://r8168dl.appspot.com/files/r8168-8.038.00.tar.bz2")
install=$pkgname.install
sha256sums=('1384bdf4d33b219f937bf1a600c72b22962baf7796e562b08e4da3c000f49072')

prepare() {
   cd $_basename-$pkgver

   # Use separate source directories
   for _kernver in $(cat /usr/lib/modules/extramodules-*/version); do
      cp -r src src-$_kernver
   done
}

build() {
   # Build for all kernels
   for _kernver in $(cat /usr/lib/modules/extramodules-*/version); do
      cd "$srcdir/$_basename-$pkgver/src-$_kernver"

      # Build
      msg2 "Building module for $_kernver"
      make -C /usr/lib/modules/$_kernver/build \
         SUBDIRS="$srcdir/$_basename-$pkgver/src-$_kernver" \
         EXTRA_CFAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
         modules
   done
}

package() {
   # Install for all kernels
   for _extramod in $(find /usr/lib/modules/extramodules-*/version -printf '%h\n'); do
      _kernver=$(cat $_extramod/version)

      # Install
      install -Dm644 $_basename-$pkgver/src-$_kernver/$_basename.ko "$pkgdir/$_extramod/$_basename.ko"

      # Compress
      gzip "$pkgdir/$_extramod/$_basename.ko"
   done
}
