# Maintainer: xzy3186 <xzy3186(at)gmail>
# Contributors: Bob Fanger <bfanger(at)gmail>, Filip <fila pruda com>, Det <nimetonmaili(at)gmail>
# Based on [community-testing]'s r8168: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/r8168

pkgname=r8168-all
_basename=r8168
pkgver=8.031.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards (for all kernels)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux')
conflicts=('r8168' 'r8168-ck' 'r8168-pf')
makedepends=('linux-headers')
source=(https://r8168.googlecode.com/files/$_basename-$pkgver.tar.bz2)
install=$_basename.install
sha256sums=('5c617b3c08aca18d1eb24d33f77df40020eb64fb32c8e4008265e08b7ffe5779')

_MODULEDIRS=`find /lib/modules -name version | sed 's|/lib/modules/||; s|/version||'`
_KERNELS=`file /boot/vmlinuz* | sed 's/.*version \([^ ]\+\).*/\1/'`

build() {
  # Loop through all detected kernels
  for _kernver in $_KERNELS; do
    cd "$srcdir/$_basename-$pkgver"
    cp -r src src-$_kernver
    cd src-$_kernver
    msg2 "Building module for $_kernver"
    make clean BASEDIR=/lib/modules/$_kernver modules
  done
}

package() {
  # Loop through all detected kernels
  for _kernver in $_KERNELS; do
    cd "$srcdir/$_basename-$pkgver"
    # Loop through all detected extramodules directories
    for _extramodules in $_MODULEDIRS; do
      # Check which extramodules directory corresponds with the built module
      if [ `cat "/lib/modules/$_extramodules/version"` = $_kernver ]; then
        install -Dm644 src-$_kernver/$_basename.ko "$pkgdir/lib/modules/$_extramodules/$_basename.ko"
      fi
    done
  done
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}
