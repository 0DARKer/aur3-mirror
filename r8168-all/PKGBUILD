# Maintainer: xzy3186 <xzy3186(at)gmail>
# Contributors: Bob Fanger <bfanger(at)gmail>, Filip <fila pruda com>, Det <nimetonmaili(at)gmail>
# Based on [community-testing]'s r8168: https://projects.archlinux.org/svntogit/community.git/tree/trunk?h=packages/r8168

pkgname=r8168-all
_basename=r8168
pkgver=8.036.00
pkgrel=2
pkgdesc="A kernel module for Realtek 8168 network cards (for all kernels)"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux' 'linux-headers')
conflicts=('r8168' 'r8168-ck' 'r8168-pf' 'r8168-dkms')
#makedepends=('linux-headers')
source=(https://r8168.googlecode.com/files/$_basename-$pkgver.tar.bz2 linux-3.10.patch)
install=$_basename.install
sha256sums=('937fd5d7d4cbfc95cf2859d8c3beb15a8536dc4e41752f7a53f2a25f07cdcd33'
            '1fb115b4d1b6b058d23322b89fad52d29748ce75b2774815e72c679a235d0e87')

_MODULEDIRS=`find /usr/lib/modules -name version | sed 's|/usr/lib/modules/||; s|/version||'`
_KERNELS=`cat $(find /usr/lib/modules -name version)`

build() {
   # Loop through all detected kernels
   for _kernver in $_KERNELS; do
      _kernver_short=$(echo ${_kernver} | sed "s/^\([0-9]\.[0-9]*\).*/\1/")
      cd "$srcdir/$_basename-$pkgver"
      cp -r src src-$_kernver
      cd src-$_kernver
      if [[ ${_kernver_short} = '3.10' ]]; then
         cp ../../linux-3.10.patch .
         patch -p1 -i "linux-3.10.patch"
      fi
      msg2 "Building module for $_kernver"
      #make clean BASEDIR=/lib/modules/$_kernver modules
      make -C /usr/lib/modules/$_kernver/build \
         SUBDIRS="$srcdir/$_basename-$pkgver/src-$_kernver" \
         EXTRA_CFAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
         modules
   done
}

package() {
   # Loop through all detected kernels
   for _kernver in $_KERNELS; do
      cd "$srcdir/$_basename-$pkgver"
      # Loop through all detected extramodules directories
      for _extramodules in $_MODULEDIRS; do
         # Check which extramodules directory corresponds with the built module
         if [ `cat "/usr/lib/modules/$_extramodules/version"` = $_kernver ]; then
            install -Dm644 src-$_kernver/$_basename.ko "$pkgdir/usr/lib/modules/$_extramodules/$_basename.ko"
         fi
      done
   done
   find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}
