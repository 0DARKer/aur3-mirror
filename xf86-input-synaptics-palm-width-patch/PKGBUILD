# $Id: PKGBUILD 218280 2014-07-29 10:09:27Z lcarlier $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski  <tpowa@archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xf86-input-synaptics-palm-width-patch
pkgver=1.8.0
pkgrel=4
pkgdesc="Synaptics driver for notebook touchpads"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxtst' 'mtdev' 'libevdev')
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=21' 'libxi' 'libx11' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.16' 'X-ABI-XINPUT_VERSION<21' 'X-ABI-XINPUT_VERSION>=22')
replaces=('synaptics')
provides=('synaptics' 'xf86-input-synaptics')
conflicts=('synaptics')
groups=('xorg-drivers' 'xorg')
backup=('etc/X11/xorg.conf.d/50-synaptics.conf')
source=(http://xorg.freedesktop.org/releases/individual/driver/xf86-input-synaptics-${pkgver}.tar.bz2
        add_tapbuttons.diff
        fix_palm_detect.patch
        fix_build.patch)
sha256sums=('9bf27632aaa6c5e62621ca9c2ca00f9b309c85b039ee33cd592b189fc872c37a'
            '5298d3e871db3768b34cf8516d3f53f9291202e2ea5de3300256e1cd9119a1ec'
            '8b8e802a510e5f6380e91aeca48b917d38b460baa4a5da6c1dbb83555653bc37'
            '3f875cbc77f9e3173a57a0a8e6202dff9bedf4285615e4300e9c062a3d65c79f')

build() {
  cd xf86-input-synaptics-${pkgver}
  
  # * upstream disabled tapping by default if a physical left button is present - see FS#33282
  # * enable MatchDevicePath directive by default - FS#33291
  patch -Np1 -i ${srcdir}/add_tapbuttons.diff

  # FIX palm detection on my x240
  patch -Np1 -i ${srcdir}/fix_palm_detect.patch

  # FIX build on recent glibc (http://cgit.freedesktop.org/xorg/driver/xf86-input-synaptics/commit/?id=96e60a4ea242d2decf109835981ae186cc36f642) 
  patch -Np1 -i ${srcdir}/fix_build.patch
  
  ./configure --prefix=/usr
  make
}

package() {
  cd xf86-input-synaptics-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 conf/50-synaptics.conf "${pkgdir}/etc/X11/xorg.conf.d/"
  install -m755 -d "${pkgdir}/usr/share/licenses/xf86-input-synaptics"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/xf86-input-synaptics/"

  rm -rf "${pkgdir}/usr/share/X11"
}
