# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=vegastrike-svn
_pkgname=vegastrike
pkgver=13192
pkgrel=1
pkgdesc="3D action-space-sim allowing players to trade and bounty hunt in a vast universe"
arch=('i686' 'x86_64')
url="http://vegastrike.sourceforge.net/"
license=('GPL')
depends=('gcc-libs' 'freetype2' 'freeglut' 'gtk2' 'openal' 'sdl' 'python2' 'libvorbis' 'ffmpeg' 'openal' 'boost-libs' 'vegastrike-data-svn')
makedepends=('boost' 'subversion')
provides=('vegastrike')
conflicts=('vegastrike')
source=()
md5sums=()

_svntrunk=https://vegastrike.svn.sourceforge.net/svnroot/vegastrike/trunk/vegastrike
_svnmod=vegastrike

build() {
  cd ${srcdir}

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co --ignore-externals $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi
  msg "Vegastrike BIN SVN checkout done or server timeout"

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  msg "Starting make..."

  ./bootstrap-sh
  ./configure \
      --disable-boost \
      --prefix=/usr \
      --with-boost=system \
      --with-data-dir=/usr/share/vegastrike
  make
}

package() {
  cd ${srcdir}

  cd $_svnmod-build
  make DESTDIR=${pkgdir} install

  # move objconv dir to right path + avoiding conflict with mysql's 'replace' file
  install -d ${pkgdir}/usr/share/${_pkgname}
  mv -f ${pkgdir}/usr/objconv ${pkgdir}/usr/share/${_pkgname}/objconv
  mv ${pkgdir}/usr/bin/replace ${pkgdir}/usr/bin/vsreplace

  # remove unneeded
  rm -rf `find ${pkgdir}/usr/share -type d -name .svn`
}
