pkgname=mingw-w64-sdl2_net
pkgver=2.0.0
pkgrel=3
pkgdesc="A small sample cross-platform networking library (mingw-w64)"
arch=(any)
url="http://libsdl.org/projects/SDL_net"
license=("zlib")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt mingw-w64-sdl2)
options=(!libtool !strip !buildflags)
source=("http://www.libsdl.org/tmp/SDL_net/release/SDL2_net-$pkgver.tar.gz")
md5sums=('f2a2c2c69e871f9f4eb3cdbfffa7fdcf')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/SDL2_net-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-sdltest
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
