# Maintainer: spider-mario <spidermario@free.fr>
pkgname=pgocaml
pkgver=1.4
pkgrel=1
pkgdesc="Interface to PostgreSQL databases for OCaml applications"
arch=('i686' 'x86_64')
url="http://$pkgname.berlios.de/"
license=('custom:LGPL')
depends=('ocaml' 'ocaml-extlib' 'ocaml-calendar' 'ocaml-csv' 'pcre-ocaml')
makedepends=('ocaml-findlib')
source=(http://download.berlios.de/$pkgname/$pkgname-$pkgver.tgz)
md5sums=('9132620652796f03b35aa4b86df58c09')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  echo "DESTDIR := \"$pkgdir/\"" >> Makefile.config
  make depend
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make install
  install --directory "$pkgdir/usr/share/licenses/$pkgname/"
  install COPYING.LIB "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
