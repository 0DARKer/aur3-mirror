pkgname=mutt-sidebar-trash
_pkgname=mutt
pkgver=1.5.21
pkgrel=1
pkgdesc='Small but very powerful text-based mail client. Patched with trash folder and sidebar support.'
url='http://www.mutt.org/'
license=('GPL')
backup=('etc/Muttrc')
arch=('i686' 'x86_64')
optdepends=('smtp-forwarder: to send mail')
depends=('gpgme' 'ncurses' 'openssl' 'libsasl' 'gdbm' 'libidn' 'mime-types' 'krb5')
provides=('mutt')
conflicts=('mutt')
source=("ftp://ftp.mutt.org/mutt/devel/${_pkgname}-${pkgver}.tar.gz"
        "trash_folder.patch"
        "sidebar.patch"
        "build.patch")
sha1sums=('a8475f2618ce5d5d33bff85c0affdf21ab1d76b9'
          '403555370cda8946016be1aa1b0a4561cabe2eb4'
          '1f0cd50650f0aa331dba88a65d4cec5e47445f0d'
          '649aa0564023c2fa3eade075a26f45025b62757f')

install=install

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
        patch -p1 -i "${srcdir}/sidebar.patch"
        patch -p1 -i "${srcdir}/build.patch"
        patch -p1 -i "${srcdir}/trash_folder.patch"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-gpgme \
		--enable-pop \
		--enable-imap \
		--enable-smtp \
		--enable-hcache \
		--with-curses=/usr \
		--with-regex \
		--with-gss=/usr \
		--with-ssl=/usr \
		--with-sasl \
		--with-idn \

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm "${pkgdir}"/usr/bin/{flea,muttbug}
	rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
	rm "${pkgdir}"/etc/mime.types{,.dist}
	install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
