pkgname=opentk
pkgver=2013.11.12
pkgrel=1
pkgdesc="An advanced, cross-platform, C-sharp OpenGL, OpenAL and OpenCL wrapper for Mono/.Net."
arch=(i686 x86_64)
license=("MIT")
depends=(mono sdl2 openal)
makedepends=(dos2unix doxygen texlive-core ghostscript)
options=(!emptydirs)
url="http://www.opentk.com"
source=("http://downloads.sourceforge.net/opentk/opentk-${pkgver//./-}.zip"
"opentk.pc.in"
"opentk-glcontrol.pc.in")
sha1sums=('c00fdf967509fe891a10c583e2a22ff7dbd1b8f2'
          '8a2c53b2ba8e0b753ad30e7106e92cdf8c3ecdcc'
          '662500d60cefc93927426215e50b2039e6e35fb2')
         
prepare() {
	cd "${srcdir}/opentk-${pkgver//./-}"
	find . -type f -exec dos2unix {} \;
}
         
build() {
	cd "${srcdir}/opentk-${pkgver//./-}"
	xbuild OpenTK.sln
}

package() {
	cd "${srcdir}/opentk-${pkgver//./-}/Binaries/OpenTK/Debug"
	find . -name '*.config' -o -name '*.mdb' -o -name '*.dll' -o -name '*.xml' |
		xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/opentk/"{}
	cp -r Data "$pkgdir/usr/lib/opentk"
	rm "$pkgdir/usr/lib/opentk/"{Bind,Convert}.exe.mdb
	rm "$pkgdir/usr/lib/opentk/Build.UpdateVersion.exe.mdb"
	install -m755 Examples.exe "$pkgdir/usr/lib/opentk/"
	cd ../../../Documentation
	install -Dm644 "License.txt" "$pkgdir/usr/share/licenses/$pkgname/License.txt"
	doxygen
	mkdir -p "$pkgdir/usr/share/doc/opentk/"
	cp -r Source/* "$pkgdir/usr/share/doc/opentk/"
	find "$pkgdir/usr/lib/opentk/" -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
	find "$pkgdir/usr/lib/opentk/" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
	install -Dm644 "$srcdir/opentk.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk.pc"
	install -m644 "$srcdir/opentk-glcontrol.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk-glcontrol.pc"
}
