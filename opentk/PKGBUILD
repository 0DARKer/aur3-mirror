pkgname=opentk
epoch=2
pkgver=1.1_1
pkgrel=3
pkgdesc="Open source game development toolkit for .Net/Mono."
depends=(mono sdl2 openal)
arch=(any)
license=("MIT")
makedepends=(doxygen texlive-core ghostscript)
options=(!emptydirs)
url="http://www.opentk.com"
source=("https://github.com/opentk/opentk/archive/${pkgver//_/-}.tar.gz"
"opentk.pc.in"
"opentk.glcontrol.pc.in"
"opentk.compatibility.pc.in")
sha1sums=('f991dc0a84ff8305452e2002c3b84b76ed18102c'
          '1af9b1b5edd2b56023bf85c7aa79d6867fa6c94c'
          'b30246f657626e2223c252fbf9d176f32e56fb18'
          '933814784c3f73f007ed899abfea16e990c2836b')

prepare() {
	cd "$srcdir/opentk-${pkgver//_/-}/Source/Examples"
	sed -i "s,debug.log,/tmp/opentk_debug.log," ExampleBrowser.cs
	sed -i -e "s,debug.log,/tmp/opentk_debug.log,g" -e "s,trace.log,/tmp/opentk_trace.log,g" Main.cs
}

build() {
	cd "$srcdir/opentk-${pkgver//_/-}/Source/Build.UpdateVersion"
	xbuild Build.UpdateVersion.csproj /p:Configuration=Release
	cd ../..
	xbuild OpenTK.sln /p:Configuration=Release
	cd Documentation
	doxygen
}

package() {
	cd "$srcdir/opentk-${pkgver//_/-}/Binaries/OpenTK/Release"
	rm *.xml
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/lib/opentk/"{} \;
	find "$pkgdir" -name 'OpenTK*.dll' -exec gacutil -i {} -root "$pkgdir/usr/lib" \;
	cd ../../../Documentation
	install -Dm644 "License.txt" "$pkgdir/usr/share/licenses/$pkgname/License.txt"
	install -Dm644 "$srcdir/opentk.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk.pc"
	install -m644 "$srcdir/opentk.glcontrol.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk.glcontrol.pc"
	install -m644 "$srcdir/opentk.compatibility.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk.compatibility.pc"
	find "$pkgdir/usr/lib/pkgconfig" -type f -exec sed -i "s|@VERSION@|${pkgver//_/-}|" {} \;
	
	cd Source
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/doc/opentk/"{} \;
}
