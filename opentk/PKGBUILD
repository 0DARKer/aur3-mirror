pkgname=opentk
pkgver=2012.03.15
pkgrel=5
pkgdesc="An advanced, cross-platform, C-sharp OpenGL, OpenAL and OpenCL wrapper for Mono/.Net."
arch=(i686 x86_64)
license=("MIT")
depends=(mono)
makedepends=(mono dos2unix)
url="http://www.opentk.com"
conflicts=(opentk-svn)
source=("http://downloads.sourceforge.net/opentk/opentk-${pkgver//./-}.zip"
"opentk.pc.in")
md5sums=('6d72ba716bd161e8975b463d29c2d219'
				 '3d65b532d4b089daf24b6bf25806e970')

build() {
	cd "${srcdir}/opentk"
	find . -type f -exec dos2unix {} \;
	xbuild OpenTK.sln
}

package() {
	cd "${srcdir}/opentk/Binaries/OpenTK/Debug"
	find . -name '*.config' -o -name '*.mdb' -o -name '*.dll' -o -name '*.xml' |
		xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/opentk/"{}
	cp -r Data "$pkgdir/usr/lib/opentk/"
	rm "$pkgdir/usr/lib/opentk/"{Bind,Convert}.exe.mdb
	rm "$pkgdir/usr/lib/opentk/Build.UpdateVersion.exe.mdb"
	install -m755 Examples.exe "$pkgdir/usr/lib/opentk/"
	cd "$srcdir/opentk/Documentation"
	install -Dm644 "License.txt" "$pkgdir/usr/share/licenses/$pkgname/License.txt"
	for file in `find . -name '*.txt' -o -name '*.pdf'`; do
		install -Dm644 $file "$pkgdir/usr/share/doc/opentk/$file"
	done
	find "$pkgdir/usr/lib/opentk/" -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
	find "$pkgdir/usr/lib/opentk/" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
	install -Dm644 "$srcdir/opentk.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk.pc"
	sed -i "s|@VERSION@|$pkgver|" "$pkgdir/usr/lib/pkgconfig/opentk.pc"
}
