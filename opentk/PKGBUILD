pkgname=opentk
epoch=2
pkgver=1.1
pkgrel=2
pkgdesc="Open source game development toolkit for .Net/Mono."
depends=(mono sdl2 openal)
arch=(any)
license=("MIT")
makedepends=(doxygen texlive-core ghostscript)
options=(!emptydirs)
url="http://www.opentk.com"
source=("https://github.com/opentk/opentk/archive/${pkgver//_/-}.tar.gz"
"opentk.pc.in"
"opentk.glcontrol.pc.in"
"opentk.compatibility.pc.in")
sha1sums=('886cb0c44ded38268420fd39bd0ceccf4f661425'
          '3582cb9e1b339ab7ee7ee5ca06a00c125bf37fce'
          '6d0318f77d2f8cbe4bee1b76de8df6df6a1da715'
          '7abcd193622390a16dcc771a7ac9a50e3a9cd4e2')

prepare() {
	cd "$srcdir/opentk-${pkgver//_/-}/Source/Examples"
	sed -i "s,debug.log,/tmp/opentk_debug.log," ExampleBrowser.cs
	sed -i -e "s,debug.log,/tmp/opentk_debug.log,g" -e "s,trace.log,/tmp/opentk_trace.log,g" Main.cs
}

build() {
	cd "$srcdir/opentk-${pkgver//_/-}/Source/Build.UpdateVersion"
	xbuild Build.UpdateVersion.csproj /p:Configuration=Release
	cd ../..
	xbuild OpenTK.sln /p:Configuration=Release
	cd Documentation
	doxygen
}

package() {
	cd "$srcdir/opentk-${pkgver//_/-}/Binaries/OpenTK/Release"
	rm *.xml
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/lib/opentk/"{} \;
	find "$pkgdir" -name 'OpenTK*.dll' -exec gacutil -i {} -root "$pkgdir/usr/lib" \;
	cd ../../../Documentation
	install -Dm644 "License.txt" "$pkgdir/usr/share/licenses/$pkgname/License.txt"
	install -Dm644 "$srcdir/opentk.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk.pc"
	install -m644 "$srcdir/opentk.glcontrol.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk.glcontrol.pc"
	install -m644 "$srcdir/opentk.compatibility.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk.compatibility.pc"
	find "$pkgdir/usr/lib/pkgconfig" -type f -exec sed -i "s|@VERSION@|${pkgver//_/-}|" {} \;
	
	cd Source
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/doc/opentk/"{} \;
}
