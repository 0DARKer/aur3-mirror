pkgname=opentk
epoch=1
pkgver=1.1_b2
pkgrel=4
arch=(i686 x86_64)
license=("MIT")
makedepends=(doxygen texlive-core ghostscript monodevelop)
options=(!makeflags)
url="http://www.opentk.com"
source=("https://github.com/opentk/opentk/archive/${pkgver//_/-}.tar.gz")
sha1sums=('1e52dc31b38f02d704e6d943a7330fd07ef4e5da')

prepare() {
	cd "${srcdir}/opentk-${pkgver//_/-}"
	mdtool generate-makefiles -s -d:Debug OpenTK.sln
	cd Source/Examples
	sed -i "s,debug.log,/tmp/opentk_debug.log," ExampleBrowser.cs
	sed -i -e "s,debug.log,/tmp/opentk_debug.log,g" -e "s,trace.log,/tmp/opentk_trace.log,g" Main.cs
}

build() {
	cd "${srcdir}/opentk-${pkgver//_/-}"
	./configure --prefix=/usr
	find . -name 'Makefile' -exec sed -i "s,../GlobalAssemblyInfo.cs,Properties/AssemblyInfo.cs," {} \;
	make
	cd Documentation
	doxygen
}

package() {
	pkgdesc="Open source game development toolkit for .Net/Mono."
	depends=(mono sdl2 openal)
	
	cd "${srcdir}/opentk-${pkgver//_/-}"
	make DESTDIR="$pkgdir" install
	cd Documentation
	install -Dm644 "License.txt" "$pkgdir/usr/share/licenses/$pkgname/License.txt"
	find "$pkgdir" -name '*.dll' -exec gacutil -i {} -root "$pkgdir/usr/lib" \;
	#find "$pkgdir" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
	
	cd Source
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/doc/opentk/"{} \;
}

# package_opentk-docs() {
# 	pkgdesc="OpenTK API reference"
# 	
# 	cd "${srcdir}/opentk-${pkgver//_/-}/Documentation/Source"
# 	find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/doc/opentk/"{} \;
# }
