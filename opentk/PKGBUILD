pkgname=opentk
epoch=1
pkgver=1.1_b1
pkgrel=1
arch=(i686 x86_64)
license=("MIT")
makedepends=(doxygen texlive-core ghostscript)
options=(!emptydirs)
url="http://www.opentk.com"
source=("https://github.com/opentk/opentk/archive/${pkgver//_/-}.tar.gz"
"opentk.pc.in"
"opentk-glcontrol.pc.in")
sha1sums=('6949277fcdcd85a83f47857e24314f1e672ddc93'
          '83aad571f99ad4ff91d3cd61dc09000b1a1b1464'
          '7a50bdf0c4ce061e8ccff9687cddb868d236f7ea')

prepare() {
	cd "${srcdir}/opentk-${pkgver//_/-}/Source/Examples"
	sed -i "s,debug.log,/tmp/debug.log," ExampleBrowser.cs
	sed -i -e "s,debug.log,/tmp/debug.log,g" -e "s,trace.log,/tmp/trace.log,g" Main.cs
}

build() {
	cd "${srcdir}/opentk-${pkgver//_/-}"
	xbuild OpenTK.sln
	cd Documentation
	doxygen
}

package() {
	pkgdesc="An advanced, cross-platform, C-sharp OpenGL, OpenAL and OpenCL wrapper for Mono/.Net."
	depends=(mono sdl2 openal)
	
	cd "${srcdir}/opentk-${pkgver//_/-}/Binaries/OpenTK/Debug"
	find . -name '*.config' -o -name '*.mdb' -o -name '*.dll' -o -name '*.xml' |
		xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/opentk/"{}
	cp -r Data "$pkgdir/usr/lib/opentk"
	rm "$pkgdir/usr/lib/opentk/"{Bind,Convert}.exe.mdb
	rm "$pkgdir/usr/lib/opentk/Build.UpdateVersion.exe.mdb"
	install -m755 Examples.exe "$pkgdir/usr/lib/opentk/"
	cd ../../../Documentation
	install -Dm644 "License.txt" "$pkgdir/usr/share/licenses/$pkgname/License.txt"
	find "$pkgdir/usr/lib/opentk/" -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
	find "$pkgdir/usr/lib/opentk/" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
	install -Dm644 "$srcdir/opentk.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk.pc"
	install -m644 "$srcdir/opentk-glcontrol.pc.in" "$pkgdir/usr/lib/pkgconfig/opentk-glcontrol.pc"
	sed -i "s,@VERSION@,${pkgver//_/}," "$pkgdir/usr/lib/pkgconfig/opentk.pc"
	sed -i "s,@VERSION@,${pkgver//_/}," "$pkgdir/usr/lib/pkgconfig/opentk-glcontrol.pc"
	
	cd "${srcdir}/opentk-${pkgver//_/-}/Documentation/Source"
	find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/doc/opentk/"{} \;
}

# package_opentk-docs() {
# 	pkgdesc="OpenTK API reference"
# 	
# 	cd "${srcdir}/opentk-${pkgver//_/-}/Documentation/Source"
# 	find . -type f -exec install -Dm644 {} "$pkgdir/usr/share/doc/opentk/"{} \;
# }
