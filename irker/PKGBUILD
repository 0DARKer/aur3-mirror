# Maintainer: Josh Cartwright <joshc@eso.teric.us>
pkgname=irker
pkgver=1.15
pkgrel=1
pkgdesc="An IRC client that runs as a daemon accepting notification requests as JSON objects presented to a listening socket."
arch=('i686' 'x86_64')
url="http://www.catb.org/esr/irker/"
license=('BSD')
depends=('python2' 'python2-irc')
makedepends=('xmlto' 'docbook-xml' 'docbook-xsl')
source=("http://www.catb.org/~esr/irker/$pkgname-$pkgver.tar.gz")
md5sums=('65d3c6b0f472bb7aaef040e8c124a9dc')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i '1s|python|python2|' irkerhook.py irkerd
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
	install -Dm 755 irkerhook.py "$pkgdir/usr/bin/irkerhook.py"
	install -Dm 644 COPYING "$pkgdir/usr/share/licenses/irker/LICENSE"
}
