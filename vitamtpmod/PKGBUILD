# Maintainer: codestation <codestation404@gmail.com>

pkgname=vitamtpmod
_pkgname=vitamtp
pkgver=2.5.5
pkgrel=1
_soname=4
pkgdesc="Library to interact with Vita's USB MTP protocol (fork)"
arch=("i686" "x86_64")
url="https://github.com/codestation/vitamtp"
license=('GPL')
depends=('libusb' 'libxml2')
conflicts=('vitamtp')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/codestation/${_pkgname}/archive/v${pkgver}.tar.gz")
options=('!libtool')
sha1sums=('a6fca9757175710d60ea18a9fbbd16f01254dfa8')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 debian/vitamtp${_soname}.udev "$pkgdir/usr/lib/udev/rules.d/80-psvita.rules"
}
