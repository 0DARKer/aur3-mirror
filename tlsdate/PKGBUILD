# Maintainer: skydrome <skydrome@tormail.org>

pkgname=tlsdate
pkgver=0.0.4
pkgrel=2
pkgdesc="A secure parasitic rdate replacement to update local time over HTTPS"
arch=('i686' 'x86_64')
url="https://github.com/ioerror/tlsdate"
license=('BSD')
depends=('openssl')
options=(!libtool)
backup=('etc/tlsdate/ca-roots/tlsdate-ca-roots.conf'
        'etc/conf.d/tlsdate')
source=("https://github.com/ioerror/tlsdate/archive/tlsdate-${pkgver}.tar.gz"
        'tlsdate.conf.d'
        'tlsdate.service')
sha256sums=('2336533aa8d76251ab371a8fd38774b3eb0a3e6f9351bc43e2d500a12c7f567d'
            'eb4c95bf4ac31460392bcd140b4922c2dcbb843844c382bba90975c6a0ea8fc6'
            'ffc5c7b73fdf8ab1b93f47fa502a80d713ecc204020f8ca9516dcfcd26002c07')
build() {
    cd "$srcdir/$pkgname-$pkgname-$pkgver"

    sed -i 's|nogroup|nobody|' src/tlsdate-helper.h
    ./autogen.sh
    ./configure --prefix=/usr --sysconfdir=/etc
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgname-$pkgver"

    make DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/tlsdate/LICENSE"

    # Not sure if `tlsdated` behaves like an actual daemon, so I leave these here for someone to make correct.
    #install -Dm644 "$srcdir/tlsdate.conf.d"  "$pkgdir/etc/conf.d/tlsdate"
    #install -Dm644 "$srcdir/tlsdate.service" "$pkgdir/usr/lib/systemd/system/tlsdate.service"
}
