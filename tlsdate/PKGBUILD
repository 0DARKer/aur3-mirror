# Maintainer: skydrome <skydrome@tormail.org>

pkgname=tlsdate
pkgver=0.0.6
pkgrel=2
pkgdesc="A secure rdate replacement to update local time over HTTPS"
arch=('i686' 'x86_64' 'armv6l' 'armv6h')
url="https://github.com/ioerror/tlsdate"
license=('BSD')
depends=('openssl' 'ca-certificates' 'dbus' 'zlib')
options=(!libtool emptydirs)
install=tlsdate.install
backup=('etc/conf.d/tlsdate'
        'etc/tlsdate/tlsdated.conf')
source=("https://github.com/ioerror/tlsdate/archive/tlsdate-${pkgver}.tar.gz"
        'tlsdate.conf.d'
        'tlsdate.service')
sha256sums=('3121ed70ecfff7d569a04f6c5caf32ff5241f738dfc2e1de6dcc170f336217ed'
            '1498a74913feb66c6e2e7d982f43b07fc48881947543969668a75ef4323503aa'
            '299da7368e151da167f226bd45e58038ab6b9a2a615f90b6a24dc3841feacc3d')

build() {
    cd "$srcdir/$pkgname-$pkgname-$pkgver"

    ./autogen.sh
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --with-dbus-group=tlsdate \
        --with-unpriv-group=tlsdate \
        --with-unpriv-user=tlsdate
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgname-$pkgver"

    make DESTDIR="$pkgdir" install
    rm -rf "$pkgdir/usr/lib/libtlsdate_compat.a"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/tlsdate/LICENSE"
    install -Dm644 "$srcdir/tlsdate.conf.d"  "$pkgdir/etc/conf.d/tlsdate"
    install -Dm644 "$srcdir/tlsdate.service" "$pkgdir/usr/lib/systemd/system/tlsdate.service"
}
