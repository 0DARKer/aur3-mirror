# Maintainer: skydrome <skydrome@tormail.org>

pkgname=tlsdate
pkgver=0.0.5
pkgrel=2
pkgdesc="A secure parasitic rdate replacement to update local time over HTTPS"
arch=('i686' 'x86_64')
url="https://github.com/ioerror/tlsdate"
license=('BSD')
depends=('openssl' 'ca-certificates' 'dbus' 'zlib')
options=(!libtool emptydirs)
install=tlsdate.install
backup=('etc/conf.d/tlsdated'
        'etc/tlsdate/tlsdated.conf')
source=("https://github.com/ioerror/tlsdate/archive/tlsdate-${pkgver}.tar.gz"
        'tlsdated.conf.d'
        'tlsdated.service')
sha256sums=('8f3d28c466de71cb3c3a23bd51c24460d5fa7c1f429142b4b53e87a97b61b2b0'
            '1498a74913feb66c6e2e7d982f43b07fc48881947543969668a75ef4323503aa'
            '1469ea10c136974c82670bdb1523ec5a2e675828d5c3cdda236547704a5582da')

build() {
    cd "$srcdir/$pkgname-$pkgname-$pkgver"

    ./autogen.sh
    ./configure --prefix=/usr --sysconfdir=/etc \
        --with-dbus-group=tlsdate --with-unpriv-group=tlsdate
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgname-$pkgver"

    make DESTDIR="$pkgdir" install
    rm -rf "$pkgdir/usr/lib/libtlsdate_compat.a"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/tlsdate/LICENSE"
    install -Dm644 "$srcdir/tlsdated.conf.d"  "$pkgdir/etc/conf.d/tlsdated"
    install -Dm644 "$srcdir/tlsdated.service" "$pkgdir/usr/lib/systemd/system/tlsdated.service"
}
