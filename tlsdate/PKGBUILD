# Maintainer: skydrome <skydrome@tormail.org>

pkgname=tlsdate
pkgver=0.0.11
pkgrel=1
pkgdesc="A secure rdate replacement to update local time over HTTPS"
arch=('i686' 'x86_64' 'armv6l' 'armv6h')
url="https://github.com/ioerror/tlsdate"
license=('BSD')
depends=('openssl' 'ca-certificates' 'dbus' 'zlib')
options=(emptydirs)
install=tlsdate.install
backup=('etc/conf.d/tlsdate'
        'etc/tlsdate/tlsdated.conf')
source=("tlsdate-$pkgver.zip::https://github.com/ioerror/tlsdate/archive/master.zip"
        #"https://github.com/ioerror/tlsdate/archive/tlsdate-${pkgver}.tar.gz"
        'tlsdate.conf.d'
        'tlsdate.service')

sha256sums=('SKIP'
            #'db8c2704b5fd5d70702d774476b941450cfd424fa7dfd01ba8a9a6f00ef54176'
            '1498a74913feb66c6e2e7d982f43b07fc48881947543969668a75ef4323503aa'
            '4f26258284522036975f04f41094257953a8e34e767df456caa2939df4d053fb')

prepare() {
    #cd "$srcdir/$pkgname-$pkgname-$pkgver"
    cd "$srcdir/tlsdate-master"
    ./autogen.sh
}

build() {
    #cd "$srcdir/$pkgname-$pkgname-$pkgver"
    cd "$srcdir/tlsdate-master"

    #--disable-seccomp-filter \
    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-dbus-group=tlsdate \
        --with-unpriv-group=tlsdate \
        --with-unpriv-user=tlsdate
    make
}

package() {
    #cd "$srcdir/$pkgname-$pkgname-$pkgver"
    cd "$srcdir/tlsdate-master"

    make DESTDIR="$pkgdir" install
    rm -rf "$pkgdir/usr/lib/libtlsdate_compat.a"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/tlsdate/LICENSE"
    install -Dm644 "$srcdir/tlsdate.conf.d"  "$pkgdir/etc/conf.d/tlsdate"
    install -Dm644 "$srcdir/tlsdate.service" "$pkgdir/usr/lib/systemd/system/tlsdate.service"
}
