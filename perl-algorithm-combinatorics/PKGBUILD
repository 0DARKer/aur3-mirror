# Contributor: Anton Leontiev <bunder /at/ t-25.ru>
pkgname=perl-algorithm-combinatorics
pkgver=0.26
pkgrel=1
pkgdesc="Perl module for efficient generation of combinatorial sequences"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/Algorithm-Combinatorics/"
license=("GPL" "PerlArtistic")
source=(http://search.cpan.org/CPAN/authors/id/F/FX/FXN/Algorithm-Combinatorics-$pkgver.tar.gz)
depends=("perl")   # provides 'perl-scalar-list-utils', 'perl-xsloader'
makedepends=()     # provides 'perl-extutils-makemaker'
checkdepends=()    # provides 'perl-test-simple'
options=(!emptydirs)
md5sums=('b4432f7048c22c916e8e57593fedfbbb')

build() {
  cd "$srcdir/Algorithm-Combinatorics-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Algorithm-Combinatorics-$pkgver"
  make test
}

package() {
  cd "$srcdir/Algorithm-Combinatorics-$pkgver"
  make install DESTDIR="$pkgdir"
}
