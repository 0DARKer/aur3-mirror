# Maintainer: Beej Jorgensen <beej@beej.us>
# Contributor: Beej Jorgensen <beej@beej.us>

pkgname=trn
pkgver=4.0test77
pkgrel=1
pkgdesc="Text-based threaded Usenet newsreader"
arch=('i686' 'x86_64')
url="http://trn.sourceforge.net/"
license=('custom')
depends=('heimdal' 'ncurses')
makedepends=('patch' 'expect' 'bison')
optdepends=('aspell: spellcheck support'
  'sendmail: (or any outgoing mailer) outgoing email support')
source=('http://downloads.sourceforge.net/trn/trn-4.0-test77.tar.gz' 
        'trn-4.0-test77-aur.patch' 'configbot')
md5sums=('e0680ae4e6062747f88ba982326ac4c0'
         'ff8cb720292c477cc89a482f57c5b43d'
         '71212d74def969dc4ef398258d72425f')

build() {
  cd "$srcdir/trn-4.0-test77"

  # fixes in include order to allow builds, and a fix to allow aspell
  # to be used as the speller:
  patch -Np1 < ${srcdir}/trn-4.0-test77-aur.patch

  # the Configure script is very very interactive, so we
  # let expect handle it.  This expect script runs the
  # Configure script:
  expect -f $srcdir/configbot || return 1

  # build it!
  make || return 1

  # install, forcing the paths to what was set in Configure:
  make \
    rnbin=${pkgdir}/usr/bin \
    rnlib=${pkgdir}/usr/lib/trn \
    inewsbin=${pkgdir}/usr/bin \
    mansrc=${pkgdir}/usr/share/man/man1 \
    install || return 1

  # custom license install
  install -m644 -D LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}

# vim:set ts=2 sw=2 et:

