# Maintainer: Miroslav Koskar (http://miroslavkoskar.com/)

# Based on bitlbee-3.2.2-1:
# * Contributor: FUBAR <mrfubar@gmail.com>
# * Contributor: simo <simo@archlinux.org>
# * Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# * Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# * Contributor: Gaetan Bisson <bisson@archlinux.org>
# * Maintainer: Dave Reisner <dreisner@archlinux.org>

_pkgname='bitlbee'
pkgname="$_pkgname-mkoskar"
pkgver='3.2.2'
_pkgnamever="$_pkgname-$pkgver"
pkgrel=1
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC (mkoskar patchset)'
url='https://github.com/mkoskar/pkgbuilds/tree/master/bitlbee-mkoskar'
license=('GPL')
arch=('i686' 'x86_64')
provides=('bitlbee')
conflicts=('bitlbee')
depends=('gnutls' 'glib2' 'libevent')
optdepends=('skype4py: to use skyped' 'libotr: for OTR encryption support')
makedepends=('asciidoc' 'libotr')

source=("http://get.bitlbee.org/src/$_pkgnamever.tar.gz"
        'bitlbee.tmpfiles'
        'skype-displayname.patch'
        'twitter-account-tag-for-nick-and-channel.patch')
sha1sums=('7e3cfe2b6bf4e8e603c74e7587307a6f5d267e9c'
          '3695ed2fe22436c4d0fc3ead829f7d1f89bc491c'
          '92dd4f9cd84caa964b984082f9cf2776d31211c2'
          'df091eceecb46ffd7cf5346c9aa2d456001fbdf1')
backup=('etc/bitlbee/bitlbee.conf'
        'etc/bitlbee/motd.txt')
install="$_pkgname.install"

prepare() {
    cd "$_pkgnamever"

    patch -p0 -i "$srcdir"/skype-displayname.patch
    patch -p0 -i "$srcdir"/twitter-account-tag-for-nick-and-channel.patch
}

build() {
    cd "$_pkgnamever"

    ./configure \
        --prefix=/usr \
        --etcdir=/etc/bitlbee \
        --sbindir=/usr/bin \
        --pidfile=/run/bitlbee/bitlbee.pid \
        --ipcsocket=/run/bitlbee/bitlbee.sock \
        --systemdsystemunitdir=/usr/lib/systemd/system \
        --ssl=gnutls \
        --events=libevent \
        --strip=0 \
        --otr=plugin \
        --skype=plugin

    make
}

package() {
    cd "$_pkgnamever"

    make DESTDIR="$pkgdir" install{,-etc,-dev,-systemd}

    install -d -o 65 -g 65 -m 770 "$pkgdir"/var/lib/bitlbee
    install -D -m 644 "$srcdir"/bitlbee.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/bitlbee.conf
}
