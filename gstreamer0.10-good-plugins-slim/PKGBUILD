# Maintainer: Ananda Samaddar <ananda@samaddar.co.uk>

pkgname=gstreamer0.10-good-plugins-slim
pkgver=0.10.28
pkgrel=1
arch=('i686' 'x86_64')
url="http://gstreamer.freedesktop.org/"
license=('LGPL')
depends=("gstreamer0.10-good=${pkgver}" 'libavc1394' 'libiec61883' 'libshout' 'libdv' 'flac' 'wavpack' 'libpng' 'libjpeg')
makedepends=('intltool' 'pkgconfig' "gstreamer0.10-good=${pkgver}" 'libavc1394' 'libiec61883' 'libshout' 'libdv' 'flac' 'wavpack' 'bzip2')
pkgdesc="GStreamer Multimedia Framework Good Plugins (gst-plugins-good) with reduced and no GNOME dependecies"
provides=("gstreamer0.10-good-plugins=${pkgver}")
conflicts=('gstreamer0-10-good-plugins')
groups=('gstreamer0.10-plugins')
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.bz2)
sha256sums=('adfbce68b9fbadb7a7aeda2227af6afe1928ef025af4158726617b9d6834b028')

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --disable-schemas-install \
    --disable-esd \
    --disable-oss \
    --disable-aalib \
    --disable-gconf \
    --disable-libcaca \
    --with-package-name="GStreamer Good Plugins Slim" \
    --with-package-origin="Psychedelic Desi"

  make
  sed -e 's/gst sys ext/gst/' -i Makefile
}

package() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make -C sys DESTDIR="${pkgdir}" install
  make -C ext DESTDIR="${pkgdir}" install
}

