# Maintainer: M0Rf30
pkgname=webcamstudio-svn
pkgver=483
pkgrel=1
pkgdesc="WebcamStudio helps you create a virtual webcam that can show your desktop, overlays, effects, and more."
arch=('any')
url="http://sourceforge.net/projects/webcamstudio/"
license=('GPL')
depends=('java-runtime' 'ffmpeg' 'dvgrab' 'jna')
optdepends=('vloopback-svn: Provides V4L loopback support'
	'v4l2loopback: Provides V4L2 loopback support')
makedepends=('subversion' 'apache-ant' 'junit')
conflicts=('webcamstudio')
replaces=('webcamstudio')
source=('svn+http://webcamstudio.googlecode.com/svn/trunk/'
	'webcamstudio.desktop')

pkgver() {
  cd "$SRCDEST/trunk"
  svnversion | tr -d [A-z]
}

build() {
  cd "$srcdir/trunk"

#Clean build directory. Specify proper location of jna.jar. Build jar.
	ant clean 
	sed -i 's|/usr/share/java/jna.jar|/usr/share/java/jna/jna.jar|g' nbproject/project.properties
	ant jar
## Build libwebcamstudio. Does NOT build vloopback, despite the directory name.
	cd "vloopback"
	gcc -c -fPIC libwebcamstudio.c -o libwebcamstudio.o
	gcc -shared -Wl,-o,libwebcamstudio.so.1.0.1 libwebcamstudio.o
}


package() {
##Copy .jar and libraries to /usr/share/java/webcamstudio/
  cd "$srcdir/trunk"
	install -Dm755 dist/WebcamStudio.jar "$pkgdir"/usr/share/java/webcamstudio/WebcamStudio.jar
	mkdir $pkgdir/usr/share/java/webcamstudio/lib
	cp libraries/* "$pkgdir"/usr/share/java/webcamstudio/lib
	ln -s /usr/share/java/jna/jna.jar "$pkgdir"/usr/share/java/webcamstudio/lib/jna.jar
##Copy animations and widgets from trunk/package to install dir 
##Copy libwebcamstudio and create lib links
	install -Dm755 vloopback/libwebcamstudio.so.1.0.1 "$pkgdir"/usr/lib/libwebcamstudio.so.1.0.1
	ln -s /usr/lib/libwebcamstudio.so.1.0.1 "$pkgdir"/usr/lib/libwebcamstudio.so.1 
	ln -s /usr/lib/libwebcamstudio.so.1 "$pkgdir"/usr/lib/libwebcamstudio.so
##Create /usr/bin/webcamstudio and .desktop entry for ease of startup
	mkdir -p "$pkgdir"/usr/{share/{applications,pixmaps},bin}
	echo -e '#!/bin/sh\n"$JAVA_HOME"/bin/java -cp "/usr/share/java/webcamstudio/lib/*" -jar /usr/share/java/webcamstudio/WebcamStudio.jar' > "$pkgdir"/usr/bin/webcamstudio
	chmod 755 "$pkgdir"/usr/bin/webcamstudio
	cp ../webcamstudio.desktop "$pkgdir"/usr/share/applications/webcamstudio.desktop
	cp debian/webcamstudio.png "$pkgdir"/usr/share/pixmaps/webcamstudio.png
}

md5sums=('SKIP'
	 '4c00bb07cda746ef51dc38c29c2c980b')
