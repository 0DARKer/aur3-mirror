# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: Greg von Nessi <greg.vonnessi@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=jfsrec-svn
pkgver=20130405
pkgrel=1
pkgdesc='Read only extraction of files and directories from a damaged jfs filesystem'
url='http://jfsrec.sourceforge.net/'
arch=('i686' 'x86_64' 'armv7h')
license=('GPL')
depends=('boost')
optdepends=('dd_rhelp')
makedepends=('subversion')
source=('svn+https://jfsrec.svn.sourceforge.net/svnroot/jfsrec'
        'recent.patch')
sha1sums=('SKIP'
          '4abae30d417f34f3afc89799c5e6126644f1fb52')

conflicts=('jfsrec')
provides=('jfsrec')

prepare() {
	cd "${srcdir}/jfsrec"
	patch -p1 -i ../recent.patch
}

build() {
	cd "${srcdir}/jfsrec"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/jfsrec"
	make DESTDIR="${pkgdir}" install
}
