# Maintainer: PedsXing <pedsxing at gmx dot net>
# Contributer: Hiroe <ashadocat at gmail dot com>

pkgname=doom3-darkmod
pkgver=1.05
pkgrel=2
pkgdesc="DOOM III mod based on the Thief series by Looking Glass Studios"
arch=('i686' 'x86_64')
url="http://www.thedarkmod.com/"
license=('CCPL')

[ "$arch" == i686   ] && depends=('doom3' 'glew' 'libpng' 'libjpeg')
[ "$arch" == i686   ] && makedepends=('p7zip' 'scons')

[ "$arch" == x86_64 ] && depends=('doom3' 'lib32-glew' 'lib32-libpng' 'lib32-libjpeg')
[ "$arch" == x86_64 ] && makedepends=('p7zip' 'scons' 'binutils-multilib' 'gcc-multilib' 'libtool-multilib')

install="$pkgname".install
source=(http://www.bloodgate.com/mirrors/tdm/pub/pk4/tdm_update_linux.zip
        http://www.bloodgate.com/mirrors/tdm/pub/thedarkmod."$pkgver".src.7z
        patch.diff
        doom3-darkmod
        "$pkgname".desktop
        "$pkgname".png)

build() {
  # Obtain data
  cd "$startdir"
  [ ! -e darkmod ] && mkdir darkmod
  cd darkmod

  chmod 755 "$srcdir"/tdm_update.linux
  "$srcdir"/tdm_update.linux || true
  mv tdm_update.log "$srcdir"
  rm -f crc_info.txt tdm_version_info.txt

  msg "Validating sources downloaded with tdm_update.linux: if building fails"
  msg "  here delete the directory 'darkmod' and try again."
  msg "If it continues to fails then the data are changed and the package"
  msg "  needs to be updated. Please mark it as out-of-date in the AUR."

  echo 'b7acd2413c34c6fbd5b89c06e04d9217  -' >"$srcdir"/md5sources
  find . -type f -print0 | sort -z | xargs -0 cat | md5sum --status -c "$srcdir"/md5sources
  msg2 "Done"

  cd "$srcdir"
  ln -fs "$startdir"/darkmod darkmod

  7z x thedarkmod."$pkgver".src.7z
  cd thedarkmod."$pkgver".src
  patch -p0 <"$srcdir"/patch.diff
  scons BUILD="release" BUILD_GAMEPAK="1"
}

package() {
  cd "$srcdir"/darkmod

  # Create destination directories
  install -m755 -d "$pkgdir"/opt/doom3/darkmod/
  install -m755 -d "$pkgdir"/usr/bin/
  install -m755 -d "$pkgdir"/usr/share/{applications,icons,licenses/"$pkgname"}

  # Install: game folder
  cp -r * "$pkgdir"/opt/doom3/darkmod/
  find "$pkgdir"/opt/doom3/darkmod/ -type d -exec chmod 755 {} \;
  find "$pkgdir"/opt/doom3/darkmod/ -type f -exec chmod 644 {} \;
  chmod 755 "$pkgdir"/opt/doom3/darkmod/tdmlauncher.linux
  install -m644 "$srcdir"/thedarkmod."$pkgver".src/tdm_game02.pk4 "$pkgdir"/opt/doom3/darkmod/
  install -m755 "$srcdir"/doom3-darkmod "$pkgdir"/usr/bin/

  # license
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/"$pkgname"/license.txt

  # desktop file, icon
  install -Dm644 "$srcdir"/"$pkgname".png "$pkgdir"/usr/share/icons/
  install -Dm644 "$srcdir"/"$pkgname".desktop "$pkgdir"/usr/share/applications/
}

md5sums=('b780883f41191812b0dd742f865f23f6'
         'eef2536f7a4e48d481c3b7ca671aa790'
         '546739496f589148cd1861cfca9484b8'
         'dda9bbec61a79bd2511451a1ab2ba872'
         '02d3ab8c0f402c717d266f514a2502a9'
         '5309e528ce22f28ecc0e9781e43379e0')
