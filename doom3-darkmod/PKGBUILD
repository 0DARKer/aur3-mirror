# Maintainer: PedsXing <pedsxing at gmx dot net>
# Contributer: Hiroe <ashadocat at gmail dot com>

pkgname=doom3-darkmod
pkgver=1.06
pkgrel=1
pkgdesc="DOOM III mod based on the Thief series by Looking Glass Studios"
arch=('i686' 'x86_64')
url="http://www.thedarkmod.com/"
license=('CCPL')

[ "$CARCH" == i686   ] && depends=('doom3' 'glew' 'libpng' 'libjpeg-turbo')
[ "$CARCH" == i686   ] && makedepends=('p7zip' 'scons')

[ "$CARCH" == x86_64 ] && depends=('doom3' 'lib32-glew' 'lib32-libpng' 'lib32-libjpeg-turbo')
[ "$CARCH" == x86_64 ] && makedepends=('p7zip' 'scons' 'binutils-multilib' 'gcc-multilib' 'libtool-multilib')

install="$pkgname".install
source=(http://www.bloodgate.com/mirrors/tdm/pub/thedarkmod."$pkgver".src.7z
        patch.diff
        doom3-darkmod
        "$pkgname".desktop
        "$pkgname".png)

build() {
  # Obtain data
  cd "$srcdir"
  7z x -y thedarkmod."$pkgver".src.7z

  cd thedarkmod."$pkgver".src
  patch -p0 <"$srcdir"/patch.diff

  cd tdm_update
  scons

  cd "$startdir"
  [ ! -e darkmod ] && mkdir darkmod
  cd darkmod
  kill -9 `pidof tdm_update.linux` || true
  cp "$srcdir"/thedarkmod."$pkgver".src/tdm_update/tdm_update.linux .
  chmod 755 ./tdm_update.linux
  ./tdm_update.linux || true
  if [ -e _tdm_update.linux ] ;then
    mv _tdm_update.linux tdm_update.linux
    chmod 755 tdm_update.linux
    ./tdm_update.linux || true
  fi
  mv tdm_update.log "$srcdir"
  rm -f crc_info.txt tdmlauncher.log tdm_mirrors.txt tdm_version_info.txt

  echo '44818fe2fcbf1bd89b53b02518b954e7  -' >"$srcdir"/tdm_hash
  find . -type f -print0 | sort -z | xargs -0 cat | md5sum --status -c "$srcdir"/tdm_hash \
    || error "File checksum mismatch!"

  cd "$srcdir"
  ln -fs "$startdir"/darkmod darkmod
  cd thedarkmod."$pkgver".src
  scons BUILD="release" BUILD_GAMEPAK="1"
  kill -9 `pidof tdm_update.linux` || true
}

package() {
  cd "$srcdir"/darkmod

  # Create destination directories
  install -m755 -d "$pkgdir"/opt/doom3/darkmod/
  install -m755 -d "$pkgdir"/usr/bin/
  install -m755 -d "$pkgdir"/usr/share/{applications,icons,licenses/"$pkgname"}

  # Install: game folder
  cp -r * "$pkgdir"/opt/doom3/darkmod/
  find "$pkgdir"/opt/doom3/darkmod/ -type d -exec chmod 755 {} \;
  find "$pkgdir"/opt/doom3/darkmod/ -type f -exec chmod 644 {} \;
  chmod 755 "$pkgdir"/opt/doom3/darkmod/tdmlauncher.linux
  install -m644 "$srcdir"/thedarkmod."$pkgver".src/tdm_game02.pk4 "$pkgdir"/opt/doom3/darkmod/
  install -m755 "$srcdir"/doom3-darkmod "$pkgdir"/usr/bin/

  # license
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/"$pkgname"/license.txt

  # desktop file, icon
  install -Dm644 "$srcdir"/"$pkgname".png "$pkgdir"/usr/share/icons/
  install -Dm644 "$srcdir"/"$pkgname".desktop "$pkgdir"/usr/share/applications/
}

md5sums=('738dcf3328f43575369e9a69ac1cf7e4'
         'b176039ddfe0861e180f38fcc6053519'
         'dda9bbec61a79bd2511451a1ab2ba872'
         '02d3ab8c0f402c717d266f514a2502a9'
         '5309e528ce22f28ecc0e9781e43379e0')
