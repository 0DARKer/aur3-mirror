# Maintainer: Tom Billiet <mouse256@gmail.com>
# Contributor: Olivier Duclos <olivier.duclos@gmail.com>

pkgname=cnijfilter-mp620
pkgver=2.80
pkgrel=1
pkgdesc="Canon drivers for the MP620 printer/scanner with improved PPD files"
arch=('i686' 'x86_64')
url="http://support-asia.canon-asia.com"
license=('custom')
if [ "${CARCH}" = 'x86_64' ]; then
	depends=('lib32-libcups' 'cups' 'lib32-popt' 'ghostscript')
	makedepends=('gcc-multilib')
else
	depends=('cups' 'popt' 'ghostscript')
fi
conflicts=('ppd-mp620-630' 'cnijfilter-common')
optdepends=("cups-bjnp: network printing support")
install=mp620.install
source=(http://gdlp01.c-wss.com/gds/1/0100000841/01/cnijfilter-common-$pkgver-1.tar.gz \
	http://downloads.sourceforge.net/mp610linux/ppdMP620-630en-1.5.tar.gz)
md5sums=('1319f320f9f6651b43e43c0b09af5b73'
         '4635702d2bade156030ebf45fec2de18')

build() {
	if [ "${CARCH}" = 'x86_64' ]; then
		export CC="gcc -m32"
		export CXX="g++ -m32"
		export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
		LIBDIR="--libdir=/usr/lib32"
	fi

	cd $srcdir/cnijfilter-common-$pkgver/libs
	./autogen.sh --prefix=/usr $LIBDIR || return 1

	cd $srcdir/cnijfilter-common-$pkgver/cngpij
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin $LIBDIR || return 1

	cd $srcdir/cnijfilter-common-$pkgver/pstocanonij
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin $LIBDIR || return 1

	cd $srcdir/cnijfilter-common-$pkgver/backend
	./autogen.sh --prefix=/usr --enable-progpath=/usr/bin $LIBDIR || return 1

	cd $srcdir/cnijfilter-common-$pkgver
	make || return 1
 
	cd $srcdir/cnijfilter-common-$pkgver/cnijfilter
	./autogen.sh --prefix=/usr --program-suffix=mp610 $LIBDIR --enable-libpath=/usr/lib/bjlib --enable-binpath=/usr/bin || return 1
	make || return 1
}

package() {
	cd $srcdir/cnijfilter-common-$pkgver
	make install DESTDIR=$pkgdir || return 1

	if [ "${CARCH}" = 'x86_64' ]; then
		#hack
		mkdir -p $pkgdir/usr/lib/cups/filter
		mv $pkgdir/usr/lib32/cups/filter/pstocanonij $pkgdir/usr/lib/cups/filter/pstocanonij
		rmdir $pkgdir/usr/lib32/cups/filter
		rmdir $pkgdir/usr/lib32/cups/
		rmdir $pkgdir/usr/lib32
	fi
	
	install -d $pkgdir/usr/lib/bjlib
	install 327/database/*.tbl $pkgdir/usr/lib/bjlib
	install 327/libs_bin/*.so.* $pkgdir/usr/lib
	install -D LICENSE-cnijfilter-2.80E.txt $pkgdir/usr/share/licenses/${pkgname}/license.txt
	
	cd $srcdir/cnijfilter-common-$pkgver/cnijfilter
	make install DESTDIR=$pkgdir || return 1
	
	# Now we install the updated PPDs from http://mp610.blogspot.com
	cd $srcdir/ppdMP620-630en-1.5
	install -d $pkgdir/usr/share/cups/model
	install canonmp620-630en.ppd $pkgdir/usr/share/cups/model/
	install cifmp610.conf $pkgdir/usr/lib/bjlib/
}

