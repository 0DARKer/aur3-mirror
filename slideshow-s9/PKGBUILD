# Maintainer: Niels Martign√®ne <niels.martignene@gmail.com>
_gemname=slideshow
pkgname=slideshow-s9
pkgver=1.2.3
pkgrel=1
pkgdesc="The Slide Show (S9) Ruby gem lets you create slide shows and author slides in plain text"
arch=(any)
url="http://slideshow.rubyforge.org/"
license=('custom')
depends=(ruby ruby-activesupport ruby-markdown ruby-pakman ruby-props ruby-redcloth ruby-textutils)
makedepends=(rubygems)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem LICENSE)
noextract=($_gemname-$pkgver.gem)
md5sums=('189dd79ba6d0b884297bf93fd87cc51d'
         '80034b242bc563e2faed3ee9c5d98490')

package() {
    cd "$srcdir"
    # _gemdir is defined inside package() because if ruby[gems] is not installed on
    # the system, makepkg will exit with an error when sourcing the PKGBUILD.
    local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

    gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
        "$_gemname-$pkgver.gem"

    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

