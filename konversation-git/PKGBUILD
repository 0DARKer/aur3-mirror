# Maintainer: Julien Humbert <julroy67 at gmail dot com>
# Contributor: Charly <charlyghislain at gmail dot com>
# Contributor: Nick B <Shirakawasuna at gmail dot com>

pkgname=konversation-git
pkgver=20110326
pkgrel=1
pkgdesc='A user friendly IRC client for KDE, latest development version'
arch=('i686' 'x86_64')
url="http://konversation.kde.org/"
license=('GPL2' 'FDL')
depends=('kdebase-runtime' 'kdepimlibs')
makedepends=('git' 'automoc4' 'cmake' 'pkgconfig' 'docbook-xml' 'docbook-xsl')
conflicts=('konversation')
provides=('konversation')

_gitroot='git://anongit.kde.org/konversation'
_gitname='konversation'

build() {
	msg "Connecting to the GIT server...." 
	cd "${srcdir}"
	
	if [[ -d ${srcdir}/$_gitname ]] ; then
		cd "$_gitname"
		git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot
	fi
	
	msg "GIT checkout done"
	msg "Starting make..."
	
	if [[ -d ${srcdir}/build ]] ; then
		rm -Rf "${srcdir}/build"
		mkdir -p "${srcdir}/build" || return 1
	else
		mkdir -p "${srcdir}/build" || return 1
	fi
	
	cd "${srcdir}/build" || return 1
	sed 's#/usr/bin/env python#/usr/bin/python2#' \
		-i ../"$_gitname"/data/scripts/media
	cmake ../"$_gitname" \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_USE_KIDLETIME=ON \
		-DCMAKE_SKIP_RPATH=ON || return 1
	make || return 1
}

package() {
	cd "${srcdir}/build"
	make DESTDIR=${pkgdir} install || return 1
}
