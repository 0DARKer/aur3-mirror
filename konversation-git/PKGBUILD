# Maintainer : Antonio Rojas <arojas@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>
# Contributor: Charly <charlyghislain at gmail dot com>
# Contributor: Nick B <Shirakawasuna at gmail dot com>
# Contributor: Julien Humbert <julroy67 at gmail dot com>

pkgname=konversation-git
pkgver=r8003.4b1b3c3
pkgrel=1
pkgdesc="A user friendly IRC client for KF5 Frameworks. (GIT Version)"
arch=('x86_64' 'i686')
url="http://konversation.kde.org"
makedepends=('git' 'extra-cmake-modules' 'kdoctools' 'python')
depends=('knotifyconfig' 'kemoticons' 'kparts' 'kidletime' 'qca-qt5')
optdepends=('python: python scripting support')
conflicts=('konversation' 'konversation-frameworks-git')
provides=('konversation')
replaces=('konversation-frameworks-git')
license=('GPL2' 'FDL')
install=konversation-git.install
source=('git://anongit.kde.org/konversation')
sha1sums=('SKIP')
_gitname=konversation

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  rm -fr build
  mkdir -p build
  sed 's|find_package(Qca 2.1.0)|find_package(Qca-qt5 2.1.0)|g' -i "${_gitname}/CMakeLists.txt"
}

build() {
  cd build

  cmake "../${_gitname}" \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DLIB_INSTALL_DIR=lib \
      -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
      -DQca-qt5_DIR=/usr/lib/cmake/Qca
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
