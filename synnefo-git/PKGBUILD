# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=synnefo-git
pkgver=0.9.5.164.g3ea8955
pkgrel=1
pkgdesc="A Qt based front end for the ICC based Oyranos Color Management System. (GIT version)"
arch=('i686' 'x86_64')
url="http://www.oyranos.org/synnefo/"
license=('BSD')
depends=('oyranos-git')
makedepends=('git' 'cmake')
provides=('synnefo')
conflicts=('synnefo')
source=("synnefo::git+https://gitorious.org/synnefo/bekus-synnefo.git"
        'COPYING')
sha1sums=('SKIP'
          '37cb5e34ad08d853124a42d06e273b2c6f33fe4b')
_gitname="synnefo"
install=synnefo.install

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

prepare() {
  rm -fr build
  mkdir -p build
}

build() {
  cd build
  cmake "../${_gitname}" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
