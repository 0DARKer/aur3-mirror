# Contributor: Alexander 'hatred' Drozdov <adrozdoff@gmail.com>
# Maintainer: Erico Nunes <nunes dot erico at gmail>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

_target=arm-linux-gnueabi
pkgname=${_target}-linux-api-headers
pkgver=3.0.40
_basever=3.0
pkgrel=2
pkgdesc="Kernel headers sanitized for use in userspace (${_target})"
arch=(any)
url="http://www.gnu.org/software/libc"
license=('GPL2')
provides=("arm-linux-gnueabi-linux-api-headers=${_basever}" "${_target}-linux-api-headers30")
conflicts=("${_target}-linux-api-headers26")
source=("http://www.kernel.org/pub/linux/kernel/v3.x/linux-${_basever}.tar.xz"
        "http://www.kernel.org/pub/linux/kernel/v3.x/patch-${pkgver}.xz")
md5sums=('ecf932280e2441bdd992423ef3d55f8f'
         '04f02b45faaf5760e83226b6d0652c78')


build() {
  cd ${srcdir}/linux-${_basever}

  [[ $pkgver != $_basever ]] && patch -p1 -i ${srcdir}/patch-${pkgver}

  make ARCH=arm mrproper
  make ARCH=arm headers_check
}

package() {
  cd ${srcdir}/linux-${_basever}

  make INSTALL_HDR_PATH=${pkgdir}/usr/${_target}/ ARCH=arm V=0  headers_install

  # use headers from libdrm
  #rm -rf ${pkgdir}/usr/include/drm
  # clean-up unnecessary files generated during install
  find ${pkgdir} -name .install -or -name ..install.cmd | xargs rm -f

  #
  #mkdir -p ${pkgdir}/usr/${_target}/usr
  #(
  # cd ${pkgdir}/usr/${_target}/usr
  # ln -s ../{include,lib} .
  #)
}

