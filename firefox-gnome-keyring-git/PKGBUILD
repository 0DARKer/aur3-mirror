# Maintainer: Maciej Szeptuch <neverous@neverous.info>
pkgname='firefox-gnome-keyring-git'
pkgver=20110401
pkgrel=1
_extver=0.5.0
pkgdesc="Gnome-keyring integration for Firefox"
arch=('i686' 'x86_64')
url='http://github.com/nougad/firefox-gnome-keyring'
license=('GPL')
depends=('firefox>=4.0' 'gnome-keyring')
makedepends=('git' 'zip' 'unzip' 'xulrunner')
provides=('firefox-gnome-keyring')
source=(Makefile.x86_64
		Makefile.i686)
sha256sums=('3e4bb62b540e2ccfcbaa8734454653ba8046f84e1b6cddd7a3f9bf948a03d435'
			'3b4835db88b8fa3046b4c94357bada6cb077ca9a6a0bc71673cb126df02126d9')

_gitroot="git://github.com/nougad/firefox-gnome-keyring.git"
_gitname="firefox-gnome-keyring"

build()
{
	cd ${srcdir}
	msg "Connecting to GIT server...."
	if [ -d ${srcdir}/${_gitname} ]; then
		cd ${srcdir}/${_gitname}
		git pull origin
		msg2 "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi

	msg "GIT checkout done or server timeout."
	msg "Starting make ${pkgname} for ${CARCH}..."
	if [ -d ${srcdir}/${_gitname}-build ]; then
		msg2 "Cleaning build directory..."
		rm -rf ${srcdir}/${_gitname}-build
	fi

	git clone ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
	cd ${srcdir}/${_gitname}-build
	msg "Patching Makefile"
	patch -Np1 -i ../Makefile.${CARCH}

	msg "Building extension..."
	make
}

package()
{
	cd ${srcdir}/${_gitname}-build

	mkdir -p ${pkgdir}/usr/lib/firefox-4.0/extensions
	unzip ${srcdir}/${_gitname}-build/gnome-keyring_password_integration-${_extver}.xpi -d ${pkgdir}/usr/lib/firefox-4.0/extensions/{6f9d85e0-794d-11dd-ad8b-0800200c9a66}
}
