# Maintainer: bzt <unmacaque@gmail.com>

pkgname=firefox-gnome-keyring-git
pkgver=20120216
pkgrel=1
pkgdesc="Gnome-keyring integration for Firefox"
arch=('i686' 'x86_64')
url='http://github.com/infinity0/mozilla-gnome-keyring'
license=('GPL')
depends=('gnome-keyring' 'firefox>=10.0')
makedepends=('zip' 'unzip' 'xulrunner>=10.0' 'git')
provides=('firefox-gnome-keyring')
conflicts=('firefox-gnome-keyring')
replaces=('firefox-gnome-keyring')

_uuid={6f9d85e0-794d-11dd-ad8b-0800200c9a66}

_gitroot=git://github.com/infinity0/mozilla-gnome-keyring.git
_gitname=mozilla-gnome-keyring

build()
{
  cd ${srcdir}
  msg "Connecting to GIT server...."
  if [ -d ${srcdir}/${_gitname} ]; then
    cd ${srcdir}/${_gitname}
    git pull origin
    msg2 "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout."
  msg "Starting make ${pkgname} for ${CARCH}..."
  if [ -d ${srcdir}/${_gitname}-build ]; then
    msg2 "Cleaning build directory..."
    rm -rf ${srcdir}/${_gitname}-build
  fi

  git clone ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
  cd ${srcdir}/${_gitname}-build

  msg "Building extension..."
  make
}

package()
{

  cd ${srcdir}

  mkdir -p ${pkgdir}/usr/lib/firefox/extensions
  unzip ${srcdir}/${_gitname}-build/gnome-keyring_password_integration-*.xpi -d ${pkgdir}/usr/lib/firefox/extensions/${_uuid}
}

