# Maintainer: bzt <unmacaque@gmail.com>

pkgname=firefox-gnome-keyring-git
true && pkgname=('firefox-gnome-keyring-git' 'thunderbird-gnome-keyring-git')
pkgver=20120203
pkgrel=1
arch=('i686' 'x86_64')
url='http://github.com/infinity0/firefox-gnome-keyring'
license=('GPL')
depends=('gnome-keyring')
makedepends=('zip' 'unzip' 'xulrunner>=10.0' 'git')

_uuid={6f9d85e0-794d-11dd-ad8b-0800200c9a66}

_gitroot=git://github.com/infinity0/firefox-gnome-keyring.git
_gitname=firefox-gnome-keyring

build()
{
  cd ${srcdir}
  msg "Connecting to GIT server...."
  if [ -d ${srcdir}/${_gitname} ]; then
    cd ${srcdir}/${_gitname}
    git pull origin
    msg2 "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout."
  msg "Starting make ${pkgname} for ${CARCH}..."
  if [ -d ${srcdir}/${_gitname}-build ]; then
    msg2 "Cleaning build directory..."
    rm -rf ${srcdir}/${_gitname}-build
  fi

  git clone ${srcdir}/${_gitname} ${srcdir}/${_gitname}-build
  cd ${srcdir}/${_gitname}-build

  msg "Building extension..."
  make
}

package_firefox-gnome-keyring-git()
{
  pkgdesc="Gnome-keyring integration for Firefox"
  depends=('firefox>=10.0')
  provides=('firefox-gnome-keyring')
  conflicts=('firefox-gnome-keyring')
  replaces=('firefox-gnome-keyring')

  cd ${srcdir}

  mkdir -p ${pkgdir}/usr/lib/firefox/extensions
  unzip ${srcdir}/${_gitname}-build/gnome-keyring_password_integration-*.xpi -d ${pkgdir}/usr/lib/firefox/extensions/${_uuid}
}

package_thunderbird-gnome-keyring-git()
{
  pkgdesc="Gnome-keyring integration for Thunderbird"
  depends=('thunderbird>=10.0')
  provides=('thunderbird-gnome-keyring')
  conflicts=('thunderbird-gnome-keyring')
  replaces=('thunderbird-gnome-keyring')

  cd ${srcdir}

  mkdir -p ${pkgdir}/usr/lib/thunderbird/extensions
  unzip ${srcdir}/${_gitname}-build/gnome-keyring_password_integration-*.xpi -d ${pkgdir}/usr/lib/thunderbird/extensions/${_uuid}
}

