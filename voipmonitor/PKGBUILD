# Maintainer: Andrey Mivrenik <gim at fastmail dot fm>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>
pkgname=voipmonitor
pkgver=10.1.7
pkgrel=5
pkgdesc="Live network packet sniffer and call recorder which analyzes SIP/RTP/RTCP protocols."
arch=('i686' 'x86_64')
url="http://www.voipmonitor.org/"
license=('GPL2')
depends=('libogg' 'libpcap' 'libvorbis' 'snappy' 'libssh' 'json-c' 'zlib' 'unixodbc' 'rrdtool' 'mysql++')
backup=('etc/voipmonitor.conf')
source=("http://downloads.sourceforge.net/project/$pkgname/${pkgver%.*}/$pkgname-$pkgver-src.tar.gz"
        "$pkgname.service"
        "makefilein.patch"
        "json-configure.patch"
        "json-tools.patch")
install="$pkgname.install"
sha256sums=('19275d163e51ee0546ec6bc0f6623f75f141358c318f2c6b3fac59dca64eb469'
            'ddc6861d016a901f54bc94fbbb2058cc94e7a65adb1c1f45eb0e3a1aaeb7f5ad'
            '576a8eef1be41da95a5136cca335d820777b21d4d9b062ed747bf8894f83ffdf'
            'd4aa7650055540a6d73c8f5800b25f0671e4aca3c054af02e5642c210007c85c'
            'fec1760f1fb40a8aae57eefa9e76a68b98d312efbc9fdb115f4d1e7a8b824d28')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}-src"
  patch -p0 -i "$srcdir/makefilein.patch"
  patch -p0 -i "$srcdir/json-configure.patch"
  patch -p0 -i "$srcdir/json-tools.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-src"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-src"

  # installing voipmonitor
  install -d "$pkgdir/usr/bin/"
  install -Dm755 "voipmonitor" "$pkgdir/usr/bin/"

  # installing config file
  install -d "$pkgdir/etc/"
  install -Dm644 "config/voipmonitor.conf" "$pkgdir/etc/"
  
  # installing service file
  install -d "$pkgdir/usr/lib/systemd/system/"
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  
  # installing additional directories
  install -d "$pkgdir/var/spool/voipmonitor"
}

# vim:set ts=2 sw=2 et:
