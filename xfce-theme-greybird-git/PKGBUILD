# Contributor: Alessio Sergi <asergi at archlinux dot us>
pkgname=xfce-theme-greybird-git
pkgver=1.3.4.3.gc7c961b
pkgrel=1
pkgdesc="Desktop theme suite from Xubuntu, with support for Xfce, Metacity and Unity"
arch=('any')
url="https://github.com/shimmerproject/Greybird"
license=('GPL' 'CCPL')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('elementary-xfce-icons: matching icon theme'
            'emerald: emerald theme'
            'lightdm-gtk2-greeter: lightdm GTK+2 theme'
            'lightdm-gtk3-greeter: lightdm GTK+3 theme'
            'lightdm-unity-greeter: lightdm unity theme')
provides=('xfce-theme-greybird')
conflicts=('xfce-theme-greybird')
source=('git+https://github.com/shimmerproject/Greybird.git')
sha1sums=('SKIP')

pkgver() {
	cd Greybird
	git describe --always | sed 's|v||;s|-|.|g'
}

package() {
	cd "${srcdir}/Greybird"

	# install emerald theme
	install -d -m 755 "${pkgdir}"/usr/share/emerald/themes/Greybird
	tar zxf Greybird.emerald -C "${pkgdir}"/usr/share/emerald/themes/Greybird/

	# create theme dirs
	install -d -m 755 "${pkgdir}"/usr/share/themes/Greybird{,-compact/xfwm4}

	# install compact theme
	install -m 644 xfwm4_compact/* "${pkgdir}"/usr/share/themes/Greybird-compact/xfwm4/

	# clean up
	rm -rf {.git,.gitignore,Greybird.emerald,LICENSE.{CC,GPL},README,xfwm4_compact}

	# install theme
	cp -r . "${pkgdir}"/usr/share/themes/Greybird/
}