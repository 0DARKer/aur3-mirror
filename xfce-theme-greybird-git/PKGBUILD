# Maintainer: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Alex Griffin <griffin dot aj at gmail dot com>

pkgname=xfce-theme-greybird-git
_realname=xfce-theme-greybird
pkgver=20111002
pkgrel=1
pkgdesc="Desktop suite for Xfce"
arch=('any')
url="https://github.com/shimmerproject/Greybird"
license=('GPL3')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('elementary-icons: for matching icon set'
            'emerald: for emerald theme')
provides=(${_realname})
conflicts=(${_realname})

_gitroot="git://github.com/shimmerproject/Greybird.git"
_gitname="Greybird"

package() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot} ${_gitname}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"

  # install wallpaper
  install -d -m755 "${pkgdir}/usr/share/xfce4/backdrops/"
  install -m644 backdrops/greybird-wall-1920x1200.png \
    "${pkgdir}/usr/share/xfce4/backdrops/greybird.png"

  # install emerald theme
  install -d -m755 "${pkgdir}/usr/share/emerald/themes/Greybird/"
  tar zxf Greybird.emerald -C "${pkgdir}/usr/share/emerald/themes/Greybird/"

  # cleaning up
  rm -rf {.git,.gitignore,backdrops,Greybird.emerald}

  # install theme
  install -d -m755 "${pkgdir}/usr/share/themes/Greybird/"
  cp -r . "${pkgdir}/usr/share/themes/Greybird/"

  # fix permission and ownership
  find "${pkgdir}"/ -type d -exec chmod 755 '{}' \;
  find "${pkgdir}"/ -type f -exec chmod 644 '{}' \;
  chown -R root.root "${pkgdir}"/*
}

