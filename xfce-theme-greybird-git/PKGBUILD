# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=xfce-theme-greybird-git
_pkgname=xfce-theme-greybird
pkgver=20121014
pkgrel=1
pkgdesc="Desktop suite for Xfce"
arch=('any')
url="https://github.com/shimmerproject/Greybird"
license=('GPL2' 'CCPL')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('elementary-xfce-icons: matching icon theme'
            'emerald: emerald theme'
            'lightdm-gtk-greeter: lightdm theme'
            'lightdm-unity-greeter: lightdm unity theme')
provides=($_pkgname)
conflicts=($_pkgname)

_gitroot="git://github.com/shimmerproject/Greybird.git"
_gitname="Greybird"

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
}

package() {
  cd "$srcdir/$_gitname-build"

  # install emerald theme
  install -d -m 755 "$pkgdir"/usr/share/emerald/themes/Greybird
  tar zxf Greybird.emerald -C "$pkgdir"/usr/share/emerald/themes/Greybird/

  # create theme dirs
  install -d -m 755 "$pkgdir"/usr/share/themes/Greybird{,-compact/xfwm4}

  # install compact theme
  install -m 644 xfwm4_compact/* "$pkgdir"/usr/share/themes/Greybird-compact/xfwm4/

  # clean up
  rm -rf {.git,.gitignore,Greybird.emerald,LICENSE.{CC,GPL},README,xfwm4_compact}

  # install theme
  cp -r . "$pkgdir"/usr/share/themes/Greybird/

  # fix permission and ownership
  find "$pkgdir"/usr/share/emerald/themes/Greybird -type f -exec chmod 644 {} \;
  chown -R root:root "$pkgdir"/usr/share/emerald/themes/Greybird
}

# vim:set ts=2 sw=2 et:
