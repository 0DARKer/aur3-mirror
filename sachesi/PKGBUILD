# Maintainer: L <alaviss at gmail dot c o m>

pkgname=sachesi
_pkgname=Sachesi #no uppercase letter in pkgname
pkgver=1.9.4.r1.g680831d
pkgrel=1
pkgdesc="Firmware, extractor, searcher and installer for Blackberry 10"
arch=('i686' 'x86_64')
url="https://github.com/xsacha/Sachesi"
license=('GPL3')
depends=('qt5-declarative' 'quazip' 'libusb' 'lzo')
makedepends=('git' 'gendesk' 'qt5-quickcontrols' 'qt5-tools')
source=(git+https://github.com/xsacha/Sachesi.git)
sha1sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgname}"
  git describe --tags --long | sed 's/-/.r/;s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  qmake-qt5 CONFIG+="shared_lzo2 shared_quazip"
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${_pkgname}"
  install -Dm755 Sachesi "${pkgdir}/usr/bin/Sachesi"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
  # Generate desktop file
  gendesk --pkgname=${_pkgname} --pkgdesc="${pkgdesc}" -n
  install -Dm644 ${_pkgname}.desktop "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
  install -Dm644 "assets/${pkgname}-114.png" "$pkgdir/usr/share/pixmaps/${_pkgname}.png"
}
