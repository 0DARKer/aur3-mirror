# Maintainer: polyzen <polycitizen@gmail.com>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: Fergus Symon <fergofrog at fergofrog dot com>
# Maintainer: polyzen <polycitizen@gmail.com>

pkgname=razercfg
pkgver=0.25
pkgrel=2
pkgdesc="Next generation Razer device configuration tool"
arch=('any')
url="http://dual.bues.ch/cms/hacking/razercfg.html"
license=('GPL')
depends=('python2' 'libusb')
makedepends=('cmake')
optdepends=('python-pyside: For qrazercfg')
backup=('etc/razer.conf')
source=("http://bues.ch/$pkgname/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop"
        "$pkgname-0.17-python2.patch"
        'razer.svg::http://upload.wikimedia.org/wikipedia/en/0/00/Razer_Snake_Logo.svg'
        'tmpfile.conf')
install="$pkgname.install"
md5sums=('cfaca3d1bca46e0809d049a8738e7f25'
         'd7813fd5cc96054433de769b3e4d4cd7'
         '3c7d7a60b31339f048f6c0ec069d116d'
         'b069094e1fc60ceb37bab31551477a01'
         '15b463d4a3b2843b7390ea37ac0167d5')
         
prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "$srcdir/$pkgname-0.17-python2.patch"
}

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
        
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/razer.svg" "$pkgdir/usr/share/pixmaps/razer.svg"
  install -Dm644 "razer.conf" "$pkgdir/etc/razer.conf"
  install -Dm644 "$srcdir/tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/razerd.conf"
}

# vim:set ts=2 sw=2 et:
