# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: Fergus Symon <fergofrog at fergofrog dot com>

pkgname=razercfg
pkgver=0.17
pkgrel=1
pkgdesc="Next generation Razer device configuration tool"
arch=('any')
url="http://bu3sch.de/joomla/index.php/razer-nextgen-config-tool"
license=('GPL')
depends=('python2' 'libusb')
makedepends=('diffutils' 'patch' 'cmake')
optdepends=('python2-qt: for the graphical qrazercfg tool')
source=("http://bu3sch.de/$pkgname/$pkgname-$pkgver.tar.bz2"
        'razercfg.desktop'
        'razerd'
        'razercfg-0.17-python2.patch'
				'razer.svg::http://upload.wikimedia.org/wikipedia/en/0/00/Razer_Snake_Logo.svg')
md5sums=('e26064a5a02fa9d7e94bff151a5b9a52'
         '1cf8494fc25ae16ddab4cbd4804f507d'
         '73574642df956f0c6848cb1096d45897'
         '3c7d7a60b31339f048f6c0ec069d116d'
				 'b069094e1fc60ceb37bab31551477a01')
install=('razercfg.install')
         
build() {
	cd "$pkgname-$pkgver"
	patch -p1 < "$srcdir/razercfg-0.17-python2.patch"
	cmake -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	
	install -Dm 755 "$srcdir/razerd" "$pkgdir/etc/rc.d/razerd"
	install -Dm 644 "$srcdir/razercfg.desktop" "$pkgdir/usr/share/applications/razercfg.desktop"
	install -Dm 644 "$srcdir/razer.svg" "$pkgdir/usr/share/pixmaps/razer.svg"
}
