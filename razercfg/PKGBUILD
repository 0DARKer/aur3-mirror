# Maintainer: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: Fergus Symon <fergofrog at fergofrog dot com>

pkgname=razercfg
pkgver=0.19
pkgrel=1
pkgdesc="Next generation Razer device configuration tool"
arch=('any')
url="http://dual.bues.ch/cms/hacking/razercfg.html"
license=('GPL')
depends=('python2' 'libusb')
makedepends=('diffutils' 'patch' 'cmake')
optdepends=('python2-qt: for the graphical qrazercfg tool')
backup=('etc/razer.conf')
source=("http://bues.ch/$pkgname/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop"
        'razerd.service'
        "$pkgname-0.17-python2.patch"
        'razer.svg::http://upload.wikimedia.org/wikipedia/en/0/00/Razer_Snake_Logo.svg'
        'tmpfile.conf')
md5sums=('6b686274fd084be512877edd893937ca'
         '1cf8494fc25ae16ddab4cbd4804f507d'
         'fa10feaec06fe57485cef5799c7ac42b'
         '3c7d7a60b31339f048f6c0ec069d116d'
         'b069094e1fc60ceb37bab31551477a01'
         '15b463d4a3b2843b7390ea37ac0167d5')
install="$pkgname.install"
         
build() {
	cd "$pkgname-$pkgver"
	patch -Np1 -i "$srcdir/razercfg-0.17-python2.patch"
	cmake -DCMAKE_INSTALL_PREFIX=/usr .
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	
	install -Dm 644 "$srcdir/razercfg.desktop" "$pkgdir/usr/share/applications/razercfg.desktop"
	install -Dm 644 "$srcdir/razer.svg" "$pkgdir/usr/share/pixmaps/razer.svg"
    install -Dm 644 "$srcdir/razerd.service" "$pkgdir/usr/lib/systemd/system/razerd.service"
	install -Dm 644 "razer.conf" "$pkgdir/etc/razer.conf"
    install -Dm644 "$srcdir/tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/razerd.conf"
}
