# Maintainer: Emmanuel GRAS <gras.emmanuel@gmail.com>
pkgname=line6linux-svn
pkgver=746
pkgrel=2
pkgdesc="A collection of software to use Line6 devices under linux"
arch=('i686' 'x86_64')
url="http://www.tanzband-scream.at/line6/"
license=('GPL')
depends=('perl')
makedepends=('subversion' 'kernel26-headers' 'lyx')
provides=('line6linux')
install='line6usb.install'
source=('driverdocs.pdf')
md5sums=('8229eaa7e5986951452995d20dbc0a60')

_svntrunk=https://line6linux.svn.sourceforge.net/svnroot/line6linux/driver/trunk
_svnmod=line6usb

build() {
  cd "$srcdir"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # remove depmod and modprobe in install build target.
  # installs in /lib/module/[VERSION]/updates, to avoid conflicts with existing line6usb.ko.
  # (see https://bbs.archlinux.org/viewtopic.php?id=104324)
  sed -e '/depmod/d' -e '/modprobe/d' -e 's/^MODDIR.*$/MODDIR  := $(MODROOT)\/updates/' -e '/-delete/d' GNUmakefile > tmp
  rm -f GNUmakefile
  mv tmp GNUmakefile
  make
}

package() {
  cp driverdocs.pdf "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install
}
