# Maintainer:  jyantis <yantis@yantis.net>

pkgname=autoenv-git
pkgver=r100.f8346c1
pkgrel=1
pkgdesc='Directory based enviroments'
arch=('any')
url='https://github.com/kennethreitz/autoenv'
license=('custom')
source=('git+https://github.com/kennethreitz/autoenv.git')
sha256sums=('SKIP')
depends=('bash')
optdepends=('zsh: if you prefer zsh over bash')
install='autoenv.install'

pkgver() {
  cd autoenv
  set -o pipefail
  git describe --long | sed -r 's/([^-]*-g)/r/;s/-/./g' ||
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd autoenv

  install -D -m755 activate.sh "${pkgdir}/usr/share/${pkgname}/activate.sh"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
