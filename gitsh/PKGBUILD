# Maintainer: Tommaso Sardelli <lacapannadelloziotom [at] gmail [dot] com>
# Contributor: Pat Brisbin <pbrisbin@gmail.com>
pkgname='gitsh'
pkgver=0.5
pkgrel=1
pkgdesc='An interactive shell for git'
arch=('any')
url="https://github.com/thoughtbot/gitsh"
license=('custom')
depends=('ruby>=1.9.3')
source=("http://thoughtbot.github.io/gitsh/gitsh-$pkgver.tar.gz"
        "build.patch")
sha1sums=('1221ab0c0eeaed7544abf91f9a9851f477b00bea'
          'dbbb5b5ee64e9333d2c0efa83a14ee9c1f45f93a')

prepare() {
  patch -p1 < $srcdir/build.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
     --disable-debug \
     --disable-dependency-tracking \
     --disable-silent-rules \
     --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
