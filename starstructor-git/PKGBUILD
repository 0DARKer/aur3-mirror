pkgname=starstructor-git
pkgver=4
pkgrel=1
pkgdesc="Starstructor is a graphical .dungeon and .structure (ship) editor"
arch=(i686 x86_64)
url="http://community.playstarbound.com/index.php?resources/starstructor-the-starbound-toolset.152/"
license=("GPLv2")
depends=(mono)
makedepends=(git)
source=("${pkgname%-*}::git+https://github.com/cstamford/Starstructor.git"
"starstructor.png"
"starstructor.desktop"
"starstructor.sh")
md5sums=('SKIP'
         '10202fa6b47e1d79ba1f59e52b8590f7'
         '6a4acb581089c52d2e13a4adb5d36505'
         'b4da7d7b139618e6ea4b0d8e6c12c718')

pkgver () {
	cd "$srcdir/${pkgname%-*}"
	echo $(git rev-list --count master)
}

prepare() {
	cd "$srcdir/${pkgname%-*}"
	sed -i "s,log.txt,/tmp/starstructor.log," DungeonEditor/Editor.cs
	sed -i 's,string path = AppDomain.CurrentDomain.BaseDirectory + "settings.json",string path = Environment.GetFolderPath(Environment.SpecialFolder.Personal) + "/.starstructor.json",g' DungeonEditor/Editor.cs
}

build() {
	cd "${srcdir}/${pkgname%-*}"
	xbuild DungeonEditor.sln
}

package() {
	cd "${srcdir}/${pkgname%-*}/DungeonEditor/bin/Debug"
	pdb2mdb Newtonsoft.Json.dll
	find . -name '*.config' -o -name '*.mdb' -o -name '*.exe' -o -name '*.dll' -o -name '*.xml' |
		xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/starstructor/"{}
	find "$pkgdir/usr/lib/starstructor/" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
	install -Dm755 "$srcdir/starstructor.sh" "$pkgdir/usr/bin/starstructor"
	install -Dm644 "$srcdir/starstructor.png" "$pkgdir/usr/share/icons/starstructor.png"
	install -Dm644 ../../../LICENSE "$pkgdir/usr/share/licenses/starstructor/LICENSE"
	install -m644 ../../../JSONNETLICENSE "$pkgdir/usr/share/licenses/starstructor/JSONNETLICENSE"
	install -Dm644 "$srcdir/starstructor.desktop" "$pkgdir/usr/share/applications/starstructor.desktop"
} 
