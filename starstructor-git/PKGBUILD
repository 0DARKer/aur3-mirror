pkgname=starstructor-git
pkgver=58
pkgrel=2
pkgdesc="Starstructor is a graphical .dungeon and .structure (ship) editor"
arch=(any)
url="http://community.playstarbound.com/index.php?resources/starstructor-the-starbound-toolset.152/"
license=("GPLv2")
depends=(json.net)
makedepends=(git mono)
source=("${pkgname%-*}::git+https://github.com/cstamford/Starstructor.git"
"dungeoneditor.png"
"dungeoneditor.desktop"
"dungeoneditor.sh")
md5sums=('SKIP'
         '10202fa6b47e1d79ba1f59e52b8590f7'
         '53bd874e44a5ab92cbc91a0fe5c22f66'
         '9d424f5b7e453d5b49cad798b9a25c4e')

pkgver () {
	cd "$srcdir/${pkgname%-*}"
	echo $(git rev-list --count master)
}

prepare() {
	cd "$srcdir/${pkgname%-*}"
	sed -i "s,log.txt,/tmp/starstructor.log," DungeonEditor/Editor/Editor.cs
	sed -i 's,string path = AppDomain.CurrentDomain.BaseDirectory + "settings.json",string path = Environment.GetFolderPath(Environment.SpecialFolder.Personal) + "/.starstructor.json",g' DungeonEditor/Editor/Editor.cs
}

build() {
	cd "${srcdir}/${pkgname%-*}"
	xbuild DungeonEditor.sln /p:Configuration=Release
}

package() {
	cd "${srcdir}/${pkgname%-*}/DungeonEditor/bin/Release"
	find . -name '*.config' -o -name '*.mdb' -o -name '*.exe' |
		xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/dungeoneditor/"{}
	install -Dm755 "$srcdir/dungeoneditor.sh" "$pkgdir/usr/bin/dungeoneditor"
	install -Dm644 "$srcdir/dungeoneditor.png" "$pkgdir/usr/share/icons/dungeoneditor.png"
	install -Dm644 ../../../LICENSE "$pkgdir/usr/share/licenses/starstructor/LICENSE"
	install -Dm644 "$srcdir/dungeoneditor.desktop" "$pkgdir/usr/share/applications/dungeoneditor.desktop"
} 
