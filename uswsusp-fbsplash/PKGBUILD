
# Maintainer: Kurt J. Bosch <kjb-temp-2009 at alpenjodel.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Kaos < gianlucaatlas at gmail dot com >

_pkgname=uswsusp
pkgname=${_pkgname}-fbsplash
pkgver=0.8
pkgrel=24
pkgdesc="Userspace software suspend aka suspend-utils - with Fbsplash support"
arch=('i686' 'x86_64')
url="http://suspend.sourceforge.net"
license=('GPL')
depends=('libgcrypt' 'libx86' 'lzo2'
	'pciutils>=2.2.4' 'zlib'
	'fbsplash'
	'mkinitcpio>=0.6' # busybox pidof
)
optdepends=( 'pm-utils: Utilities and scripts for power management' )
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}=${pkgver}")
backup=('etc/suspend.conf')
install=${_pkgname}.install
changelog=Changelog
source=(
	"http://downloads.sourceforge.net/suspend/suspend-${pkgver}.tar.gz"
	'uresume-hook'
	'uresume-install'
	'suspend-overflow-gentoo.patch'
	'fbsplash-type-fix.patch'
	'config.patch'
	# optional patches
	's2ram-chvt63.patch'
	's2disk-no-chvt1.patch'
	'fbsplash-no-pre-snapshoot-progress.patch' #### Fix final progress bar
	'fbsplash-no-fade-effects.patch'           #### Disable fadein/fadeout effects
)
md5sums=('c81817b2ba2be899cdb32c00de016bff'
         'c2b1202d8c26fd97673ecf4ba9e02d30'
         '662673eee5922eb264e5d2866e720185'
         '1bedade3d5a61e9ec02f9efaff23a20d'
         'ba73824e616ccf50f5e8ebd95be007a0'
         'e0ac8b02017fe21adcced38aa565091b'
         '37c2be33e2c363a3a62e16814ace48a7'
         '9bce66efb4a8ff08039973be6e45e7ed'
         '3d7aca2afffb7b34fc975bf1e9c897bc'
         'ddfec99ea3266e53c2b16218b9e5641c')

build() {
	cd "${srcdir}"/suspend-${pkgver}

	# Fix http://bugs.gentoo.org/238511
	patch -Np1 -i ../suspend-overflow-gentoo.patch

	# Fix the splash type: Use 'suspend' and 'resume' instead of 'bootup'
	patch -Np1 -i ../fbsplash-type-fix.patch

	# Make the config file a bit more nice
	( cd conf && patch -Np0 -i ../../config.patch )

	# optional: Show a black screen instead of uggly console
	patch -Np2 -i ../s2ram-chvt63.patch
	patch -Np1 -i ../s2disk-no-chvt1.patch

	# optional: Fix initial suspend progress bar shown again after resume (0% = 100% resume in reverse)
	patch -Np2 -i ../fbsplash-no-pre-snapshoot-progress.patch

	# optional: Disable fadein/fadeout effects (kernel cmdline isn't parsed)
	#patch -Np2 -i ../fbsplash-no-fade-effects.patch

	./configure \
		--prefix=/usr \
		--enable-compress \
		--enable-encrypt \
		--disable-resume-static \
		--disable-static \
		--sysconfdir=/etc \
		--enable-fbsplash
	make
}

package() {
	cd "${srcdir}"/suspend-${pkgver}

	make DESTDIR="${pkgdir}" install
	rmdir "${pkgdir}"/dev

	install -D -m 644 "${srcdir}"/uresume-hook    "${pkgdir}"/lib/initcpio/hooks/uresume
	install -D -m 644 "${srcdir}"/uresume-install "${pkgdir}"/lib/initcpio/install/uresume
}
