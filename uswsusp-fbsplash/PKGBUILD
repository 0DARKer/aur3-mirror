
# Maintainer: Kurt J. Bosch <kjb-temp-2009 at alpenjodel.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Kaos < gianlucaatlas at gmail dot com >

_pkgname=uswsusp
pkgname=${_pkgname}-fbsplash
pkgver=1.0
pkgrel=3
pkgdesc="Userspace software suspend aka suspend-utils - with Fbsplash support"
arch=('i686' 'x86_64')
url="http://suspend.sourceforge.net"
license=('GPL')
depends=('libgcrypt' 'libx86' 'lzo2'
	'pciutils>=2.2.4' 'zlib'
	'fbsplash'
	'mkinitcpio>=0.7' # busybox pidof, build()
)
optdepends=( 'pm-utils: Utilities and scripts for power management' )
provides=("${_pkgname}=${pkgver}")
conflicts=("${_pkgname}")
backup=('etc/suspend.conf')
install=${_pkgname}.install
changelog=Changelog
source=(
	"http://downloads.sourceforge.net/project/suspend/suspend/suspend-${pkgver}/suspend-utils-${pkgver}.tar.bz2"
	'suspend-0.9_pre0-errno.patch'
	'uresume-hook'
	'uresume-install'
	'suspend.conf.patch'
	's2ram-chvt63.patch'
	's2disk-no-chvt1.patch'
	'fbsplash-no-pre-snapshoot-progress.patch'
	'fbsplash-no-fade-effects.patch'
)

build() {
	cd "${srcdir}"/suspend-utils-${pkgver}

	# Fix build error - patch from http://bugs.gentoo.org/339759
	patch -Np0 -i ../suspend-0.9_pre0-errno.patch

	# Make the config file a bit more nice
	patch -Np2 -i ../suspend.conf.patch

	# optional: Show a black screen instead of uggly console
	patch -Np2 -i ../s2ram-chvt63.patch
	patch -Np2 -i ../s2disk-no-chvt1.patch

	# optional: Fix initial suspend progress bar shown again after resume (0% = 100% resume in reverse)
	patch -Np2 -i ../fbsplash-no-pre-snapshoot-progress.patch

	# optional: Disable fadein/fadeout effects (kernel cmdline isn't parsed)
	patch -Np2 -i ../fbsplash-no-fade-effects.patch

	./configure \
		--prefix=/usr \
		--enable-compress \
		--enable-encrypt \
		--enable-threads \
		--disable-resume-static \
		--disable-static \
		--sysconfdir=/etc \
		--enable-fbsplash
	make
}

package() {
	cd "${srcdir}"/suspend-utils-${pkgver}

	make DESTDIR="${pkgdir}" install
	rmdir "${pkgdir}"/dev

	install -D -m 644 "${srcdir}"/uresume-hook    "${pkgdir}"/lib/initcpio/hooks/uresume
	install -D -m 644 "${srcdir}"/uresume-install "${pkgdir}"/lib/initcpio/install/uresume
}

md5sums=('02f7d4b679bad1bb294a0efe48ce5934'
         '511f2309fccc4c6ec411104d1bcd371e'
         'c2b1202d8c26fd97673ecf4ba9e02d30'
         'd4a4b7c2511a64f07245a0081eddad15'
         '700b8a3fd0954ef66d540b55542f592c'
         '95111769efe97103f1ab5cfe9a408545'
         '4927a0bfa0056918ab58c47e8f837ef9'
         '917d079ea7cd9a02d19361e71b0b3ac7'
         'a479dc5f19d1ee9c8315f3e63217a65a')
