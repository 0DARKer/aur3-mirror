# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=lib32-wxgtk-light
pkgver=3.0.2
_pkgver="$(echo ${pkgver} | tr . _)"
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!) (lib32)"
arch=('x86_64')
url="http://wxwidgets.org/"
license=('custom:wxWindows')
depends=('lib32-gtk2' 'lib32-libsm' 'lib32-sdl' 'lib32-libxxf86vm')
makedepends=('git' 'lib32-mesa' 'lib32-glu' 'lib32-libxt')
provides=('lib32-wxgtk=3.0.0')
conflicts=('lib32-wxgtk')
source=("wxwidgets::git+https://github.com/wxWidgets/wxWidgets.git#tag=WX_${_pkgver}"
        'lib32-wxGTK-collision.patch')
sha1sums=('SKIP'
          '480ea546d8378e1cfde5332636fd689c3c2624b0')
_gitname=wxwidgets

prepare() {
  cd "${_gitname}"
  patch -Np1 -i ../lib32-wxGTK-collision.patch
}

build() {
  cd "${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,regex=builtin,opengl,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode,stl} \
              --disable-{mediactrl,precomp-headers,webview,visibility}
  make
  make -C locale allmo
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/licence.txt"
}
