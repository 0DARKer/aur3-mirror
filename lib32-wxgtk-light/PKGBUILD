# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Based on lib32-wxgtk from [multilib]

pkgname=lib32-wxgtk-light
pkgver=2.8.12.1
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!) (lib32)"
arch=('x86_64')
url="http://wxwidgets.org/"
license=('custom:wxWindows')
depends=('lib32-gtk2' 'lib32-libsm' 'lib32-sdl' 'wxgtk')
makedepends=('lib32-gtk2' 'lib32-mesa' 'lib32-glu' 'lib32-libxt')
conflicts=('lib32-wxgtk')
provides=('lib32-wxgtk')
#source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2")
source=("http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2")

sha1sums=('05688dc03d61631750f5904273122bb40a2115f5')

build() {
  rm -fr "${srcdir}/wxPython-build"
  cp -R "${srcdir}/wxPython-src-${pkgver}" "${srcdir}/wxPython-build"
  cd "${srcdir}/wxPython-build"

# Multilib
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  ./configure --prefix=/usr --libdir=/usr/lib32 --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,opengl,regex=builtin,sdl} --enable-{graphics_ctx,unicode} --disable-{mediactrl,optimize,precomp-headers}
  make
  make -C locale allmo
  make -C contrib/src
}

package() {

cd "${srcdir}/wxPython-build"

# Install
  make DESTDIR="${pkgdir}" install
  make -C contrib/src DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin/wxrc}
  mv "${pkgdir}"/usr/bin/wx-config{,32}
  mv "${pkgdir}"/usr/bin/wxrc{,32}-2.8

# License
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/licence.txt"
}
