# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=lib32-wxgtk-light
pkgver=3.0.0
pkgrel=2
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!) (lib32)"
arch=('x86_64')
url="http://wxwidgets.org/"
license=('custom:wxWindows')
depends=('lib32-gtk2' 'lib32-libsm' 'lib32-sdl' 'lib32-libxxf86vm')
makedepends=('lib32-gtk2' 'lib32-mesa' 'lib32-glu' 'lib32-libxt')
provides=('lib32-wxgtk=3.0.0')
conflicts=('lib32-wxgtk')
source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2"
        'wxGTK-collision.patch')
sha1sums=('756a9c54d1f411e262f03bacb78ccef085a9880a'
          '480ea546d8378e1cfde5332636fd689c3c2624b0')

prepare() {
  rm -fr build
  cp -R "wxWidgets-${pkgver}" build
  cd build
  patch -Np1 -i ../wxGTK-collision.patch
}

build() {
  cd "build"

# Multilib
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  ./configure --prefix=/usr --libdir=/usr/lib32 \
              --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,regex=builtin,opengl,sdl} \
              --without-{libnotify,gnomevfs,gnomeprint} \
              --enable-{graphics_ctx,unicode,stl} \
              --disable-{mediactrl,precomp-headers,webview}
  make
  make -C locale allmo
}

package() {
  cd build

# Install
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}

# License
  install -Dm644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/licence.txt"
}
