# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-logger-qt-git
_gitname='telepathy-logger-qt'
pkgver=0.6.0.9.g5142bd7
pkgrel=1
epoch=1
pkgdesc="Qt bindings for telepathy logger"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kdereview/telepathy-logger-qt"
license=('GPL')
depends=('telepathy-qt' 'qt-gstreamer' 'telepathy-logger')
makedepends=('git' 'automoc4' 'cmake' 'python2')
provides=('telepathy-logger-qt')
conflicts=('telepathy-logger-qt')
source=('git://anongit.kde.org/telepathy-logger-qt')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g;s|v||'
}

build() {
  cd "$srcdir"

  msg "Starting build..."

  rm -rf "$srcdir/build" && mkdir "$srcdir/build"
  cd "$srcdir/build"
  
  
  cmake ../$_gitname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DQT_MOC_EXECUTABLE=/usr/bin/moc-qt4 \
    -DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
    -DQT_UIC_EXECUTABLE=/usr/bin/uic-qt4 \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
  
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
