# Maintainer: Christian Hesse <mail@eworm.de
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>

pkgname=squeezelite-git
pkgver=1.6.3.r0.gdeb5f6b
pkgrel=1
pkgdesc='Lightweight headless squeezebox emulator'
arch=('i686' 'x86_64' 'armv6h')
url='http://code.google.com/p/squeezelite/'
license=(GPL3)
depends=('alsa-lib')
makedepends=('git' 'alsa-lib' 'faad2' 'flac' 'libmad' 'libvorbis' 'mpg123'
	'portaudio')
optdepends=('faad2: Support for decoding AAC'
	'flac: Support for decoding FLAC'
	'libmad: Support for decoding MP3'
	'libvorbis: Support for decoding Ogg Vorbis'
	'mpg123: Support for decoding MP3'
	'portaudio: Use of binary using portaudio for output')
provides=('squeezelite')
conflicts=('squeezelite')
source=('git+https://code.google.com/p/squeezelite/')

pkgver() {
	cd squeezelite/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd squeezelite/

	echo "OPTS += -DPORTAUDIO" > Makefile.archpa
	echo "CFLAGS += -Wall -fPIC -O2 \$(OPTS)" >> Makefile.archpa
	echo "LDFLAGS += -lportaudio -lasound -lpthread -ldl -lrt" >> Makefile.archpa
	echo "EXECUTABLE = squeezelite-pa" >> Makefile.archpa
	echo "include Makefile" >> Makefile.archpa
	make -f Makefile.archpa

	make clean

	echo "CFLAGS += -Wall -fPIC -O2" > Makefile.arch
	echo "LDFLAGS += -lasound -lpthread -ldl -lrt" >> Makefile.arch
	echo "EXECUTABLE = squeezelite" >> Makefile.arch
	echo "include Makefile" >> Makefile.arch
	make -f Makefile.arch
}

package() {
	cd squeezelite/

	# Main binary
	install -m0755 -D squeezelite "${pkgdir}/usr/bin/squeezelite"
	install -m755 -D squeezelite-pa "${pkgdir}/usr/bin/squeezelite-pa"
}

sha256sums=('SKIP')
