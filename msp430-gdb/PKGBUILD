# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=msp430-gdb
pkgver=7.2
pkgrel=4
pkgdesc="GNU debugger for MSP430"
arch=('i686' 'x86_64')
url="http://mspgcc4.sourceforge.net/"
license=('GPL')
depends=('gcc-msp430' 'python2')
makedepends=('gcc-msp430')

_mspgcc_ver=20110612
_gdb_patch_ver=20110103
_gnu_mirror="http://ftpmirror.gnu.org"

source=("http://sourceforge.net/projects/mspgcc/files/mspgcc/mspgcc-${_mspgcc_ver}.tar.bz2"
        "${_gnu_mirror}/gdb/gdb-${pkgver}.tar.bz2")
sha1sums=('b00fff4f9c6311a9b467b4b428efc0c41822b6f6'
          'cae138dee0c11778c471a1d5e4b09e0ae08f9e9d')

_builddir="${srcdir}/build"

build() {
  _patch_name="msp430-gdb-${pkgver}-${_gdb_patch_ver}.patch"
  (cd "${srcdir}/gdb-${pkgver}" &&
    patch -p1 < "${srcdir}/mspgcc-${_mspgcc_ver}/${_patch_name}")

  rm -frv ${_builddir}
  mkdir -p ${_builddir} && cd ${_builddir}
  "${srcdir}/gdb-${pkgver}/configure" \
    --target=msp430 \
    --prefix=/usr \
    --program-prefix="msp430-" \
    --enable-languages=c,c++ \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --with-python=python2 \
    --disable-nls
  make
}

package() {
  cd ${_builddir}
  make DESTDIR=${pkgdir} install

  rm -rf ${pkgdir}/usr/share/info/dir
  rm -fr ${pkgdir}/usr/lib/libiberty.a
  rm -fr ${pkgdir}/usr/share/info/annotate.info
  rm -fr ${pkgdir}/usr/share/info/bfd.info
  rm -fr ${pkgdir}/usr/share/info/configure.info
  rm -fr ${pkgdir}/usr/share/info/gdb.info*
  rm -fr ${pkgdir}/usr/share/info/gdbint.info*
  rm -fr ${pkgdir}/usr/share/info/stabs.info
  rm -fr ${pkgdir}/usr/share/info/standards.info

  rm -fr ${pkgdir}/usr/share/gdb/syscalls/amd64-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/gdb-syscalls.dtd
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/i386-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/ppc-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/ppc64-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/sparc-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/sparc64-linux.xml
}

# vim:set sts=2 ts=2 sw=2 et:
