# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=msp430-gdb
pkgver=7.2
pkgrel=3
pkgdesc="GNU debugger for MSP430"
arch=('i686' 'x86_64')
url="http://mspgcc4.sourceforge.net/"
license=('GPL')
depends=('gcc-msp430' 'msp430-libc' 'ncurses' 'expat')

_mspgcc4_release=20110312
_mspgcc4_path=mspgcc4-${_mspgcc4_release}
_gnu_mirror="http://ftpmirror.gnu.org"
_gdb_patch_folder="gdb-6-and-7"
_gdb_version=${pkgver}

source=("${_mspgcc4_path}.tar.bz2::http://sourceforge.net/projects/mspgcc4/files/mspgcc4/${_mspgcc4_path}.tar.bz2/download"
        "${_gnu_mirror}/gdb/gdb-${_gdb_version}.tar.bz2")
sha1sums=('8883f7932e395809ca32592c54112132ca140f9c'
          'cae138dee0c11778c471a1d5e4b09e0ae08f9e9d')
md5sums=('333a9b58d7a1db2138fb95015ab06a4f'
         '64260e6c56979ee750a01055f16091a5')

build() {
  msg "Building gdb"

  cd ${srcdir}/gdb-${_gdb_version}
  cp -rf ${srcdir}/${_mspgcc4_path}/ports/${_gdb_patch_folder}/* .
  if [[ -e ${srcdir}/${_mspgcc4_path}/gdb-${_gdb_version}.patch ]] ; then
    patch -N -p1 < ${srcdir}/${_mspgcc4_path}/gdb-${_gdb_version}.patch
  fi

  cd ${srcdir}/gdb-${_gdb_version}
  rm -fr build
  mkdir build
  cd build
  ../configure --target=msp430 \
               --prefix=/usr \
               --program-prefix="msp430-" \
               --enable-languages=c,c++ \
               --mandir=/usr/share/man \
               --infodir=/usr/share/info \
               --disable-nls
  make
}

package() {
  msg "Begin packaging"

  cd ${srcdir}/gdb-${_gdb_version}/build
  make DESTDIR=${pkgdir} install

  cd .. && rm -fr build
  rm -rf ${pkgdir}/usr/share/info/dir
  rm -fr ${pkgdir}/usr/lib/libiberty.a
  rm -fr ${pkgdir}/usr/share/info/annotate.info
  rm -fr ${pkgdir}/usr/share/info/bfd.info
  rm -fr ${pkgdir}/usr/share/info/configure.info
  rm -fr ${pkgdir}/usr/share/info/gdb.info*
  rm -fr ${pkgdir}/usr/share/info/gdbint.info*
  rm -fr ${pkgdir}/usr/share/info/stabs.info
  rm -fr ${pkgdir}/usr/share/info/standards.info

  rm -fr ${pkgdir}/usr/share/gdb/syscalls/amd64-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/gdb-syscalls.dtd
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/i386-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/ppc-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/ppc64-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/sparc-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/sparc64-linux.xml
}

# vim:set sts=2 ts=2 sw=2 et:
