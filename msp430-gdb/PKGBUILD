# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=msp430-gdb
pkgver=7.2
pkgrel=1
pkgdesc="GNU debugger for MSP430"
arch=('i686' 'x86_64')
url="http://mspgcc4.sourceforge.net/"
license=('GPL')
depends=('gcc-msp430=4.4.5-2' 'msp430-libc=20110130-1' 'ncurses' 'expat')

_mspgcc4_release=20110130
_mspgcc4_path=mspgcc4-${_mspgcc4_release}
_gnu_mirror="http://ftpmirror.gnu.org"
_gdb_patch_folder="gdb-6-and-7"
_gdb_version=${pkgver}

source=("${_mspgcc4_path}.tar.bz2::http://sourceforge.net/projects/mspgcc4/files/mspgcc4/${_mspgcc4_path}.tar.bz2/download"
        "${_gnu_mirror}/gdb/gdb-${_gdb_version}.tar.bz2")

md5sums=('2d9e1b1c9c85c64083529b13993e59e1'
         '64260e6c56979ee750a01055f16091a5')

build() {
  msg "Building gdb"

  cd ${srcdir}/gdb-${_gdb_version}
  cp -rf ${srcdir}/${_mspgcc4_path}/ports/${_gdb_patch_folder}/* .
  if [[ -e ${srcdir}/${_mspgcc4_path}/gdb-${_gdb_version}.patch ]] ; then
    patch -N -p1 < ${srcdir}/${_mspgcc4_path}/gdb-${_gdb_version}.patch
  fi

  cd ${srcdir}/gdb-${_gdb_version}
  rm -fr build
  mkdir build
  cd build
  ../configure --target=msp430 \
               --prefix=/usr \
               --program-prefix="msp430-" \
               --enable-languages=c,c++ \
               --mandir=/usr/share/man \
               --infodir=/usr/share/info \
               --disable-nls
  make
}

package() {
  msg "Begin packaging"

  cd ${srcdir}/gdb-${_gdb_version}/build
  make DESTDIR=${pkgdir} install

  cd .. && rm -fr build
  rm -rf ${pkgdir}/usr/share/info/dir
  rm -fr ${pkgdir}/usr/lib/libiberty.a
  rm -fr ${pkgdir}/usr/share/info/annotate.info
  rm -fr ${pkgdir}/usr/share/info/bfd.info
  rm -fr ${pkgdir}/usr/share/info/configure.info
  rm -fr ${pkgdir}/usr/share/info/gdb.info*
  rm -fr ${pkgdir}/usr/share/info/gdbint.info*
  rm -fr ${pkgdir}/usr/share/info/stabs.info
  rm -fr ${pkgdir}/usr/share/info/standards.info

  rm -fr ${pkgdir}/usr/share/gdb/syscalls/amd64-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/gdb-syscalls.dtd
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/i386-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/ppc-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/ppc64-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/sparc-linux.xml
  rm -fr ${pkgdir}/usr/share/gdb/syscalls/sparc64-linux.xml
}

# vim:set sts=2 ts=2 sw=2 et:
