# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# based on:  TDY <tdy@gmx.com>
# Contributor: bunder <budner : t-25 : ru>
# Note: wxgtk-2.9 can coexist with extra/wxgtk (a la extra/wxgtk-2.6)

pkgname=wxgtk-wout-gconf
pkgver=2.9.2
pkgrel=1
pkgdesc="A GTK+ implementation of wxWidgets API for GUI. WITHOUT GCONF"
arch=('i686' 'x86_64')
url="http://wxwidgets.org/"
license=('custom:wxWindows')
depends=('gstreamer0.10-base' 'gtk2>=2.10' 'mesa')
makedepends=('gstreamer0.10-good-plugins' 'pkgconfig>=0.9.0')
provides=('wxgtk-2.9')
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-$pkgver.tar.bz2
        configure-wout-gconf.patch)
md5sums=('d6cec5bd331ba90b74c1e2fcb0563620'
         'cbd5b36ec3ddbe6f7a7ce19bbe93eb4a')

build() {
  cd "$srcdir/wxWidgets-$pkgver"
  patch -p0 < ../configure-wout-gconf.patch
  sed -i 's,/usr/$wx_cv_std_libpath,& /usr/lib,' configure
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --without-gnomeprint --enable-graphics_ctx --enable-mediactrl --disable-optimise
  make
  make -C locale allmo
}

package() {
  cd "$srcdir/wxWidgets-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 docs/licence.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # provide compatibility with extra/wxgtk
  ln -sf /usr/lib/wx/config/gtk2-unicode-2.9 "$pkgdir/usr/bin/wx-config-2.9"
  rm -f "$pkgdir/usr/bin/wx-config"
  rm -f "$pkgdir/usr/bin/wxrc"
  rm -rf "$pkgdir/usr/share/bakefile"
  find "$pkgdir/usr/share/locale" -name wxmsw.mo -delete
  for _mo in "$pkgdir"/usr/share/locale/*/LC_MESSAGES/wxstd.mo; do
    mv $_mo ${_mo/wxstd.mo/compat-wxstd29.mo}
  done
  mv "$pkgdir/usr/share/aclocal/wxwin.m4" "$pkgdir/usr/share/aclocal/wxwin-2.9.m4"
}
