# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Arto Puranen <purcher@gmail.com>

pkgname=arno-iptables-firewall
pkgver=2.0.1d
pkgrel=1
pkgdesc="A secure stateful firewall for both single and multi-homed machine"
arch=('any')
url=(http://rocky.eld.leidenuniv.nl/joomla/)
license=('GPL')
depends=('iptables' 'iproute2')
makedepends=('patch')
backup=(etc/${pkgname}/firewall.conf
        etc/${pkgname}/custom-rules
        etc/${pkgname}/plugins/dmz-dnat.conf
        etc/${pkgname}/plugins/dsl-ppp-modem.conf
        etc/${pkgname}/plugins/dyndns-host-open.conf
        etc/${pkgname}/plugins/ids-protection.conf
        etc/${pkgname}/plugins/ipsec-vpn.conf
        etc/${pkgname}/plugins/ipv6-over-ipv4.conf
        etc/${pkgname}/plugins/linux-upnp-igd.conf
        etc/${pkgname}/plugins/mac-address-filter.conf
        etc/${pkgname}/plugins/multiroute.conf
        etc/${pkgname}/plugins/sip-voip.conf
        etc/${pkgname}/plugins/ssh-brute-force-protection.conf
        etc/${pkgname}/plugins/traffic-accounting.conf
        etc/${pkgname}/plugins/traffic-shaper.conf
        etc/${pkgname}/plugins/transparent-dnat.conf
        etc/${pkgname}/plugins/transparent-proxy.conf)
source=(http://rocky.eld.leidenuniv.nl/${pkgname}/${pkgname}_${pkgver}.tar.gz \
        ${pkgname}.script \
        ${pkgname}.patch \
        ${pkgname}.service)
sha256sums=('177343362063125985e8b0008fe69bc6ca8d3ba252cfa35a316e708f52fef9c6'
            'b8b2c3edaf3c2d4bf9ee617b01808bb8035c76f5eac5381c777f67c4ca6a1a20'
            '90ba06504b503fa34c78ebd8f2b83ee404f34580e47a8691318d9996171c62e3'
            '520915c51d247ec88e9d494f1dfd0b10161373a1c859b363599d0f73cb4432ce')

package() {
  cd ${srcdir}/${pkgname}_${pkgver}

# patch
  patch -Np0 -i ${srcdir}/${pkgname}.patch

# conf files
  install -d -m 0755 etc/${pkgname}/plugins/ ${pkgdir}/etc/${pkgname}/plugins/
  for i in `find etc/${pkgname} -type f`; do install -T -m 0600 $i ${pkgdir}/$i ;done
  install -D -m 0644 share/${pkgname}/environment ${pkgdir}/usr/share/${pkgname}/environment

# plugins
  install -d -m 0755 share/${pkgname}/plugins/ ${pkgdir}/usr/share/${pkgname}/plugins/
  for i in share/${pkgname}/plugins/*plugin; do install -T -m 0644 $i ${pkgdir}/usr/$i ;done
  install -D -m 0744 share/${pkgname}/plugins/dyndns-host-open-helper \
    ${pkgdir}/usr/share/${pkgname}/plugins/dyndns-host-open-helper
  install -D -m 0744 share/${pkgname}/plugins/traffic-accounting-helper \
    ${pkgdir}/usr/share/${pkgname}/plugins/traffic-accounting-helper
  install -D -m 0744 share/${pkgname}/plugins/traffic-accounting-log-rotate \
    ${pkgdir}/usr/share/${pkgname}/plugins/traffic-accounting-log-rotate
  install -D -m 0744 share/${pkgname}/plugins/traffic-accounting-show \
    ${pkgdir}/usr/share/${pkgname}/plugins/traffic-accounting-show

# binary
  install -D -m 0744 bin/${pkgname} ${pkgdir}/usr/sbin/${pkgname}
  install -D -m 0744 bin/arno-fwfilter ${pkgdir}/usr/sbin/arno-fwfilter
  install -D -m 0744 contrib/adsl-failover ${pkgdir}/usr/sbin/adsl-failover

# man files
  install -D -m 0644 share/man/man1/arno-fwfilter.1 ${pkgdir}/usr/share/man/man1/arno-fwfilter.1 
  install -D -m 0644 share/man/man8/${pkgname}.8 ${pkgdir}/usr/share/man/man8/${pkgname}.8

# daemon script
  install -D -m 0744 ${srcdir}/${pkgname}.script ${pkgdir}/etc/rc.d/${pkgname}
# systemd script
  install -D -m 0744 ${srcdir}/${pkgname}.service ${pkgdir}/usr/lib/systemd/${pkgname}.service
}
