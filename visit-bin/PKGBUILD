# Contributor: Dmitriy Morozov <archlinux@foxcub.org>
pkgname=visit-bin
_pkgname=visit
pkgver=2.8.0
_pkgver=2_8_0
pkgrel=1
pkgdesc="Interactive parallel visualization and graphical analysis tool"
url="https://wci.llnl.gov/simulation/computer-codes/visit"
arch=('x86_64')
license=('BSD')
provides=('visit')
conflicts=('visit' 'visit-svn')
depends=('libpng' 'glu')
makedepends=('cpio')
source=(http://portal.nersc.gov/svn/${_pkgname}/trunk/releases/${pkgver}/${_pkgname}${_pkgver}.linux-x86_64-ubuntu11.tar.gz
        http://portal.nersc.gov/svn/${_pkgname}/trunk/releases/${pkgver}/${_pkgname}-install${_pkgver}
        visit-bin.sh
        visit.patch)
md5sums=('efdafb0b8822a2c9a4a201e0f3c5d966'
         '6b1efa51561ad1681f5689522a7f9fc5'
         'eda322f9745ff8ace1cfbec7536929df'
         '1e3e007983fe50f42c405b42cca8102b')

package() {
  cd ${srcdir}

  install -dm755 ${pkgdir}/opt/${_pkgname}
  
  mv ${_pkgname}${_pkgver}.linux-x86_64-ubuntu11.tar.gz ${_pkgname}${_pkgver}.linux-x86_64.tar.gz
  sh ${_pkgname}-install${_pkgver} -c none ${pkgver} linux-x86_64 ${pkgdir}/opt/${_pkgname}

  patch ${pkgdir}/opt/visit/bin/frontendlauncher < ${srcdir}/visit.patch

  install -dm755 ${pkgdir}/etc/profile.d
  install -m755  visit-bin.sh ${pkgdir}/etc/profile.d/
}
