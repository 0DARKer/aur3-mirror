pkgname=mldonkey-ed2kad-daemon-git
pkgver=3.1.4.239.gfbc2cfe
pkgrel=1
pkgdesc='A multi-network P2P client. Daemon function, only ED2K/KAD (GIT Version)'
arch=('i686' 'x86_64')
url="http://mldonkey.sourceforge.net/"
license=('GPL')
depends=('bzip2' 'miniupnpc' 'libnatpmp' 'gcc-libs' 'file')
makedepends=('ocaml' 'git')
conflicts=('mldonkey')
provides=('mldonkey')
install="mldonkey-daemon.install"
source=('git+http://repo.or.cz/r/mldonkey.git#branch=next'
        'mldonkey.logrotate'
        'mldonkey.service'
        'mldonkey@.service'
        'mldonkey.tmpfiles')
sha1sums=('SKIP'
          '489d9cd16885e85036b51103f56ea1413f293a1d'
          'dd782135c64386bc377460d7c0de56220a474105'
          'e4f7cfe248e930e3237a54ec1d576d79817aeec0'
          'c6e4bb5dbca6e3e0a2942e6e2483b0d688f7ecdb')
options=('!makeflags')
_gitname=mldonkey

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - . | sed 's|release.||g')"
}

build() {
  cd "${_gitname}"
  ./configure --prefix=/usr --enable-minimum --enable-upnp-natpmp --enable-bzip2 --enable-magic --enable-donkeysui
  make depend
  make
  make utils
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -Dm755 mld_hash "${pkgdir}/usr/bin/ed2k_hash"

  install -Dm644 ../mldonkey.service "${pkgdir}/usr/lib/systemd/system/mldonkey.service"
  install -Dm644 ../mldonkey@.service "${pkgdir}/usr/lib/systemd/system/mldonkey@.service"
  install -Dm644 ../mldonkey.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/mldonkey.conf"

  install -d "${pkgdir}/var/log/mldonkey"
  install -Dm644 ../mldonkey.logrotate "${pkgdir}/etc/logrotate.d/mldonkey"
}
