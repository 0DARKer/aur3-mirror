# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: David Pretty <david.pretty@gmail.com> 

pkgname=python2-pyml
pkgver=0.7.8
pkgrel=1
pkgdesc='An interactive object oriented framework for machine learning written in Python'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://pyml.sourceforge.net/'
depends=('python2-numpy' 'python2-matplotlib')
source=("http://downloads.sourceforge.net/pyml/PyML-${pkgver}.tar.gz")
md5sums=('a706766a7369668d5b6681d58a7bbcde')

build() {
  cd PyML-${pkgver}

  # the following 2 seds fix https://sourceforge.net/tracker/?func=detail&aid=3361193&group_id=115057&atid=670031
  # fix PyML/classifiers/ext/libsvm_wrap.cpp:2856:13: error: ‘ptrdiff_t’ does not name a type
  sed \
    -e '14i#include <cstddef>' \
    -i PyML/classifiers/ext/libsvm_wrap.cpp
  # fix PyML/utils/ext/arrayWrap_wrap.cpp:2641:13: error: ‘ptrdiff_t’ does not name a type
  sed \
    -e '13i#include <cstddef>' \
    -i PyML/utils/ext/arrayWrap_wrap.cpp

  python2 setup.py build
}

package() {
  cd PyML-${pkgver}

  python2 setup.py install \
    --root=${pkgdir} \
    --optimize=1
}
