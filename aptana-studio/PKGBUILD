# Contributor: Bruno Galeotti <bravox87 at gmail dot org>
# Maintainer: Jorge Pizarro Callejas <jpizarro at inf dot utfsm dot cl>
pkgname=aptana-studio
_pkgname=aptana
pkgver=2.0.5
pkgrel=1
pkgdesc="JavaScript-focused IDE for building dynamic web applications."
arch=("i686" "x86_64")
url="http://www.aptana.com/"
license=("EPL")
depends=("gtk2" "java-environment" "unzip" "xulrunner")
replaces=("aptana")
conflicts=("aptana")

if [ "$CARCH" = "i686" ]; then
    source=("http://download.aptana.org/tools/studio/standalone/linux/Aptana_Studio_Setup_Linux_x86_2.0.5.zip"
	    "aptana.sh"
	    "aptana.desktop"
	    "aptana-execfiles.txt")
    md5sums=("d7b9118168549f5ee1c540370dec1461"
	     "52a5970330ab0d77238468269b481c0b"
	     "b59a7b242f51b66aa5b8f5d83bffc159"
	     "3fba080dbf04edd31ff4e16685054f4d")
    noextract=("Aptana_Studio_Setup_Linux_x86_$pkgver.zip")
    _zipname="Aptana_Studio_Setup_Linux_x86_$pkgver.zip"
elif [ "$CARCH" = "x86_64" ]; then
    source=("http://download.aptana.org/tools/studio/standalone/linux/Aptana_Studio_Setup_Linux_x86_64_2.0.5.zip"
	    "aptana.sh"
	    "aptana.desktop"
	    "aptana-execfiles.txt")
    md5sums=("2c391b2a83c501c5afe1244638d32b87"
	     "52a5970330ab0d77238468269b481c0b"
	     "b59a7b242f51b66aa5b8f5d83bffc159"
	     "3fba080dbf04edd31ff4e16685054f4d")
    noextract=("Aptana_Studio_Setup_Linux_x86_64_$pkgver.zip")
    _zipname="Aptana_Studio_Setup_Linux_x86_64_$pkgver.zip"
fi

build() {
    jar xf $_zipname

    local _instpath="/usr/share"
    local _file="none"

    cd "$srcdir" || return 1

    mv "$srcdir/Aptana Studio ${pkgver%*.*}" "$srcdir/$_pkgname" || return 1

    # Install Dirs
    find "$_pkgname" -type d -exec install -d "{}" "$pkgdir/$_instpath/{}" \; || return 1

    # Install Files
    install -m755 -d "$pkgdir/$_instpath" || return 1
    find "$_pkgname" -type f \
	    $(cat "$startdir/$_pkgname-execfiles.txt" | sed -e "s/^/-not -path /" | sed -e "N;s:\n: :g") \
	    -exec install -Dm644 "{}" "$pkgdir/$_instpath/{}" \; || return 1

    # Install Executables
    for _file in $(cat "$startdir/$_pkgname-execfiles.txt"); do
	    install -Dm755 "$_file" "$pkgdir/$_instpath/$_file" || return 1
    done || return 1

    # install misc
    install -d "$pkgdir/usr/bin" "$pkgdir/$_instpath"/{applications,pixmaps} || return 1
    install -m755 "$startdir/$_pkgname.sh" "$pkgdir/usr/bin/$_pkgname" || return 1
    install -m644 "$startdir/aptana.desktop" "$pkgdir/$_instpath/applications/" || return 1
    ln -s "$_instpath/$_pkgname/icon.xpm" "$pkgdir/$_instpath/pixmaps/$_pkgname.xpm" || return 1
}
