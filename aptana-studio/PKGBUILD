# Maintainer: Sarkasper <echo a2FzcGVyLm1lbnRlbkBnbXguY29tCg==|base64 -d>
# Contributor: Bruno Galeotti <bravox87 at gmail dot org>
# Contributor: Jonathan Basniak <basniakgm112 at gmail dot com>
# Upgrade to 3.1.1: Sean Burau <seanburau at gmail dot com>
# Upgrade to 3.0.1: Simon Luijk <simon dot luijk gmail dot com>
# Upgrade to 3.2.0: Saulius Lukauskask <luksaulius at gmail dot com>
pkgname=aptana-studio
_pkgname=aptana
pkgver=3.4.0
pkgrel=1
pkgdesc="The professional, open source development tool for the open web."
arch=("i686" "x86_64")
url="http://www.aptana.com/"
license=("GPLv3")
depends=("gtk2" "java-runtime" "libjpeg-turbo" "libpng12" "net-tools" "unzip")
optdepends=("xulrunner192: Legacy XULRunner interface")
#makedepends=("unzip") #gm112 note: This has been replaced with "unzip" as there is a package in extra/ for this.
options=(!strip)
replaces=("aptana")
conflicts=("aptana")
backup=("usr/share/aptana/AptanaStudio3.ini")

if [ "$CARCH" = "i686" ]; then
source=("http://download.aptana.com/studio3/standalone/$pkgver/linux/Aptana_Studio_3_Setup_Linux_x86_$pkgver.zip"
"aptana.sh"
"aptana.desktop"
"aptana-execfiles.txt")    
md5sums=('f20b036dd4e4082c2d9ab6276406d822'
         '6b7b28fd865cdaffb66ef5f2a3e175d3'
         'e297d32ab375b84eaaabfca7bc012752'
         '859173402304e8bfa02eb4981c81d3d9')
noextract=("Aptana_Studio_3_Setup_Linux_x86_$pkgver.zip")
_zipname="Aptana_Studio_3_Setup_Linux_x86_$pkgver.zip"

elif [ "$CARCH" = "x86_64" ]; then
source=("http://download.aptana.com/studio3/standalone/$pkgver/linux/Aptana_Studio_3_Setup_Linux_x86_64_$pkgver.zip"
"aptana.sh"
"aptana.desktop"
"aptana-execfiles.txt")
md5sums=('9d7c973f42905eb13d6a8458cadf9c3a'
         '6b7b28fd865cdaffb66ef5f2a3e175d3'
         'e297d32ab375b84eaaabfca7bc012752'
         '859173402304e8bfa02eb4981c81d3d9')
noextract=("Aptana_Studio_3_Setup_Linux_x86_64_$pkgver.zip")
_zipname="Aptana_Studio_3_Setup_Linux_x86_64_$pkgver.zip"
fi

package() {
echo "Extracting $_zipname"
unzip -q $_zipname

local _instpath="/usr/share"
local _file="none"

cd "$srcdir" || return 1
mv "$srcdir/Aptana_Studio_${pkgver%*.*.*}" "$srcdir/$_pkgname" || return 1

# Patch the .ini file for the proper xulrunner version
#echo "-Dorg.eclipse.swt.browser.XULRunnerPath=/usr/lib/xulrunner-1.9.2" >> "$srcdir/$_pkgname/AptanaStudio3.ini"

# Install Dirs
find "$_pkgname" -type d -exec install -d "{}" "$pkgdir/$_instpath/{}" \; || return 1

# Install Files
install -m755 -d "$pkgdir/$_instpath" || return 1
find "$_pkgname" -type f \
$(cat "$startdir/$_pkgname-execfiles.txt" | sed -e "s/^/-not -path /" | sed -e "N;s:\n: :g") \
-exec install -Dm644 "{}" "$pkgdir/$_instpath/{}" \; || return 1

# Install Executables
for _file in $(cat "$startdir/$_pkgname-execfiles.txt"); do
install -Dm755 "$_file" "$pkgdir/$_instpath/$_file" || return 1
done || return 1

# install misc
install -d "$pkgdir/usr/bin" "$pkgdir/$_instpath"/{applications,pixmaps} || return 1
install -m755 "$startdir/$_pkgname.sh" "$pkgdir/usr/bin/$_pkgname" || return 1
install -m644 "$startdir/aptana.desktop" "$pkgdir/$_instpath/applications/" || return 1
ln -s "$_instpath/$_pkgname/icon.xpm" "$pkgdir/$_instpath/pixmaps/$_pkgname.xpm" || return 1
}
