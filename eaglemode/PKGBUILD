# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=eaglemode
pkgver=0.80.0
pkgrel=1
pkgdesc="Visit almost everything in your PC simply by zooming in"
arch=('i686' 'x86_64')
url="http://eaglemode.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libtiff' 'libpng' 'librsvg' 'xdg-utils' 'xine-lib' 'xz')
makedepends=('perl>=5.8')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2 \
        $pkgname.desktop \
        $pkgname.sh)
md5sums=('f26c705241edf1fb583f4faa2bfbcecd'
         '8c57916be2b1e7e771aabed9373a09af'
         '541a9bfd70e015dc2712721db72b09aa')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  install -d ${pkgdir}/opt

  perl make.pl build
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  perl make.pl install dir=${pkgdir}/opt/$pkgname
  install -D -m755 ${srcdir}/$pkgname.sh ${pkgdir}/usr/bin/$pkgname

# .desktop + icon file
  install -D -m644 ${srcdir}/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop
  install -D -m644 ${srcdir}/$pkgname-$pkgver/res/icons/${pkgname}32.png \
    ${pkgdir}/usr/share/icons/hicolor/32x32/apps/$pkgname.png
  install -D -m644 ${srcdir}/$pkgname-$pkgver/res/icons/${pkgname}48.png \
    ${pkgdir}/usr/share/icons/hicolor/48x48/apps/${pkgname}.png
}
