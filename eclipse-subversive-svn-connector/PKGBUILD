# Maintainer: Benjamin Hedrich <kiwisauce(at)pagenotfound(dot)de>

pkgname=eclipse-subversive-svn-connector
pkgver=2.2.2.I20101203_1700
pkgrel=1
pkgdesc="Subversive SVN Connectors"
arch=('i686' 'x86_64')
url="http://www.polarion.com/products/svn/subversive.php"
license=('EPL')
depends=('eclipse')
source=("$pkgname-$pkgver-feature.jar::http://community.polarion.com/projects/subversive/download/eclipse/2.0/update-site/features/org.polarion.eclipse.team.svn.connector_${pkgver//_/-}.jar" \
"$pkgname-$pkgver-plugin.jar::http://community.polarion.com/projects/subversive/download/eclipse/2.0/update-site/plugins/org.polarion.eclipse.team.svn.connector_${pkgver//_/-}.jar")
noextract=($pkgname-$pkgver-{feature,plugin}.jar)

md5sums=('ace6eea9251e1205148b0f01212c0324'
         '424cf2e67e48ae6c757ecdd16573e95f')


build() {
  _dest="$pkgdir/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse"
  # Feature(s)
  install -d -m755 $_dest/features/org.polarion.eclipse.team.svn.connector_${pkgver//_/-}
  cd $_dest/features/org.polarion.eclipse.team.svn.connector_${pkgver//_/-}
  jar xf $srcdir/$pkgname-$pkgver-feature.jar || return 1
  # Plugin(s)
  install -D -m644 $srcdir/$pkgname-$pkgver-plugin.jar \
    $_dest/plugins/org.polarion.eclipse.team.svn.connector_${pkgver//_/-}.jar || return 1
}
