# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>
# Contributor:

_pkgbase=gconf
pkgname=lib32-${_pkgbase}
pkgver=3.2.3
pkgrel=1
pkgdesc="A configuration database system (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-libxml2' 'lib32-polkit' 'lib32-libldap'
	 'lib32-dbus-glib' 'lib32-gtk3' "$_pkgbase")
makedepends=('intltool' 'gtk-doc' 'gcc-multilib')
options=('!libtool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/GConf/3.2/GConf-${pkgver}.tar.xz
        gconf-reload.patch
        01_xml-gettext-domain.patch)
sha256sums=('9eb55dd1d009b6eba9c32e3fae87d3aa45a33629a5e2031ed9f5acb05efaa149'
            '567b78d8b4b4bbcb77c5f134d57bc503c34867fcc6341c0b01716bcaa4a21694'
            'c883dec2b96978874a53700cfe7f26f24f8296767203e970bc6402b4b9945eb8')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/GConf-${pkgver}"
  
  # Patch from fedora - reloads gconf after installing schemas
  patch -Np1 -i "${srcdir}/gconf-reload.patch"
  # http://bugzilla.gnome.org/show_bug.cgi?id=568845
  patch -Np1 -i "${srcdir}/01_xml-gettext-domain.patch"
  # Python2 fix
  sed -i '1s|#!/usr/bin/env python$|&2|' gsettings/gsettings-schema-convert

  ./configure \
  	--prefix=/usr \
  	--sysconfdir=/etc \
  	--localstatedir=/var \
  	--libdir=/usr/lib32 \
  	--libexecdir=/usr/lib32/GConf \
  	--enable-defaults-service \
  	--enable-introspection=no \
  	--with-gtk=3.0 \
  	--disable-static \
  	--disable-orbit

  make
}

package() {
  cd "${srcdir}/GConf-${pkgver}"  
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}

