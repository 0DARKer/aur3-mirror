# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=gconf
pkgname=lib32-${_pkgbase}
pkgver=3.2.5
pkgrel=1
pkgdesc="A configuration database system (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-libxml2' 'lib32-polkit' 'lib32-libldap' 'lib32-dbus-glib' 'lib32-gtk3' "$_pkgbase")
makedepends=('intltool' 'gtk-doc' 'gcc-multilib')
options=('!libtool')
url="http://www.gnome.org"
_ver=${pkgver%.*}
source=(http://ftp.gnome.org/pub/gnome/sources/GConf/${_ver}/GConf-${pkgver}.tar.xz
        gconf-reload.patch
        01_xml-gettext-domain.patch)
sha256sums=('4ddea9503a212ee126c5b46a0a958fd5484574c3cb6ef2baf38db02e819e58c6'
            '567b78d8b4b4bbcb77c5f134d57bc503c34867fcc6341c0b01716bcaa4a21694'
            'c883dec2b96978874a53700cfe7f26f24f8296767203e970bc6402b4b9945eb8')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "GConf-${pkgver}"
  patch -Np1 -i "${srcdir}/gconf-reload.patch"
  patch -Np1 -i "${srcdir}/01_xml-gettext-domain.patch"
  sed -i '1s|#!/usr/bin/env python$|&2|' gsettings/gsettings-schema-convert

  ./configure \
  	--prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--enable-defaults-service --enable-introspection=no \
  	--disable-static --disable-orbit --with-gtk=3.0 \
  	--libdir=/usr/lib32 --libexecdir=/usr/lib32/GConf

  make pkglibdir=/usr/lib32/GConf
}

package() {
  cd "GConf-${pkgver}"  
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}

