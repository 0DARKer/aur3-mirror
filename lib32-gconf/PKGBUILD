# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>
# Contributor:

_pkgbasename=gconf
pkgname=lib32-${_pkgbasename}
pkgver=2.32.4
pkgrel=1
pkgdesc="A configuration database system (32 bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-dbus-glib' 'lib32-gtk3' 'lib32-libxml2' 'lib32-orbit2'
	 'lib32-polkit' "${_pkgbasename}>=${pkgver}")
makedepends=('gcc-multilib' 'intltool' 'gtk-doc')
options=('!libtool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/GConf/2.32/GConf-${pkgver}.tar.xz
        gconf-reload.patch
        01_xml-gettext-domain.patch)
sha256sums=('f36f4a01a5ac898b5ec49a04a43b051d3809f07c3d0841188fd6a5c512389025'
            '567b78d8b4b4bbcb77c5f134d57bc503c34867fcc6341c0b01716bcaa4a21694'
            'c883dec2b96978874a53700cfe7f26f24f8296767203e970bc6402b4b9945eb8')

build() {
  cd "${srcdir}/GConf-${pkgver}"
  
  # Patch from fedora - reloads gconf after installing schemas
  patch -Np1 -i "${srcdir}/gconf-reload.patch"
  # http://bugzilla.gnome.org/show_bug.cgi?id=568845
  patch -Np1 -i "${srcdir}/01_xml-gettext-domain.patch"

  # Python2 fix
  sed -i '1s|#!/usr/bin/env python$|&2|' gsettings/gsettings-schema-convert

  ./configure \
  	--prefix=/usr \
  	--sysconfdir=/etc \
  	--localstatedir=/var \
  	--libdir=/usr/lib32 \
  	--libexecdir=/usr/lib32/GConf \
  	--enable-defaults-service \
  	--enable-introspection=no \
  	--with-gtk=3.0 \
  	--disable-static \
  	CC="gcc -m32" CXX="g++ -m32" \
  	PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  make
}

package() {
  cd "${srcdir}/GConf-${pkgver}"  
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}
