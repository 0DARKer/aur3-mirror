# Maintainer: XZS <d.f.fischer at web dot de>
pkgname=firefox-extension-omnisidebar-git
pkgver=1.2.2
pkgrel=1
pkgdesc='A firefox add-on designed to provide more control over the behavior of the sidebar.'
url='https://github.com/Quicksaver/OmniSidebar'
arch=('any')
license=('MPLv2')
depends=('firefox')
source=("$pkgname::git+https://github.com/Quicksaver/OmniSidebar")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  sed -n 's/.*<em:version>\(.*\)<\/em:version>.*/\1/;T;p;q' install.rdf
}

package() {
  cd "$srcdir/$pkgname"
  local emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  local dstdir=$pkgdir/usr/lib/firefox/browser/extensions/$emid
  install -d $dstdir
  mv * $dstdir
}
