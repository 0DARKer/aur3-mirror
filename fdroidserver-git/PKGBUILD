# Maintainer Daniel Landau <daniel.landau@iki.fi>
# Contributor: Prurigro
# Ported from the package by AlexanderR <alexander r at gmx com>

_pkgname=fdroidserver
pkgname=${_pkgname}-git
pkgver=r2570.d104f2e
pkgrel=1
pkgdesc="F-Droid repository management tools"
url="https://gitorious.org/f-droid/$_pkgname"
license=('GPL3')
depends=('python2' 'java-runtime' 'android-sdk-build-tools')
makedepends=('git' 'python2-setuptools' 'python2-magic' 'python2-pillow' 'java-environment')
optdepends=('android-sdk'
     'android-ndk'
     'maven'
     'gradle'
     'svn'
     'mercurial'
     'bzr'
     'git'
     'python2-magic'
     'python2-pillow'
     )
arch=('any')
options=(!emptydirs)
source=("git://gitorious.org/f-droid/fdroidserver.git")
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "$srcdir/${_pkgname}"

    python2 setup.py install --root="$pkgdir/" --optimize=1 --install-data="/tmp" || true
    rm -rf "$pkgdir/tmp"

    cd ${_pkgname}/getsig
    javac getsig.java
    mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/${_pkgname}/getsig/"
    install getsig.class "$pkgdir/usr/lib/python2.7/site-packages/${_pkgname}/getsig/getsig.class"
    cd -

    mkdir -p "$pkgdir/usr/bin"
    install "fdroid" "$pkgdir/usr/bin"
    install "fd-commit" "$pkgdir/usr/bin"

    install -D completion/bash-completion "$pkgdir/usr/share/bash-completion/completions/fdroidserver"

    install -D "examples/config.py" "$pkgdir/usr/share/$_pkgname/config.sample.py"
}
