# Maintainer Daniel Landau <daniel.landau@iki.fi>
# Contributor: Prurigro
# Ported from the package by AlexanderR <alexander r at gmx com>

_pkgname=fdroidserver
pkgname=${_pkgname}-git
pkgver=cf85201
pkgrel=1
pkgdesc="F-Droid repository management tools"
url="https://gitorious.org/f-droid/$_pkgname"
license=('GPL3')
depends=('python2' 'java-runtime' 'android-sdk-build-tools')
makedepends=('git' 'python2-setuptools' 'python2-pillow' 'python2-magic' 'java-environment')
optdepends=('android-sdk' 'android-ndk' 'maven' 'svn' 'mercurial' 'bzr')
arch=('any')
options=(!emptydirs)
source=("git://gitorious.org/f-droid/fdroidserver.git" 'config.py')
sha256sums=('SKIP'
            '2f915c79e592203c23b580bbe87d8952b81fc078af12c152bf0cf994ed6a53c1')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git describe --always
}

prepare() {
    find "${srcdir}/${_pkgname}" -type f -exec sed -i 's@/usr/bin/python\s*$@/usr/bin/python2@' {} \+
}

package() {
    cd "$srcdir/${_pkgname}"

    python2 setup.py install --root="$pkgdir/" --optimize=1 --install-data="/tmp" || true
    rm -rf "$pkgdir/tmp"

		cd ${_pkgname}/getsig
		javac getsig.java
    mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/${_pkgname}/getsig/"
    install getsig.class "$pkgdir/usr/lib/python2.7/site-packages/${_pkgname}/getsig/getsig.class"
		cd -

		mkdir -p "$pkgdir/usr/bin"
		install "fdroid" "$pkgdir/usr/bin"
    install -D "$srcdir/config.py" "$pkgdir/usr/share/$_pkgname/config.sample.py"
}
