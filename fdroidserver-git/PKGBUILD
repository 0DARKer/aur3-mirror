# Maintainer: Prurigro
# Ported from the package by AlexanderR <alexander r at gmx com>

_pkgname=fdroidserver
pkgname=${_pkgname}-git
pkgver=2d8ccbb
pkgrel=1
pkgdesc="F-Droid repository management tools"
url="https://gitorious.org/f-droid/$_pkgname"
license=('GPL3')
depends=('python2')
makedepends=('git')
optdepends=('android-sdk' 'android-ndk' 'maven' 'svn' 'mercurial' 'bzr')
arch=('any')
options=(!emptydirs)
source=("git://gitorious.org/f-droid/fdroidserver.git" 'config.py')
sha256sums=('SKIP' 'd04e0d6f9c2c7e9d7fe54050498342f16457f627b8fc920434dd693d911a5a6a')

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git describe --always
}

prepare() {
    find "${srcdir}/${_pkgname}" -type f -exec sed -i 's@/usr/bin/python\s*$@/usr/bin/python2@' {} \+
}

package() {
    cd "$srcdir/${_pkgname}"

    python2 setup.py install --root="$pkgdir/" --optimize=1 --install-data="/tmp" || true
    rm -rf "$pkgdir/tmp"
    install -D "$srcdir/config.py" "$pkgdir/usr/share/$_pkgname/config.sample.py"
}
