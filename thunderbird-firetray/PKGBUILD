# Contributor: Vasily Ryabov
# Maintainer: Sebastien Duthil <duthils@free.fr>

pkgname=thunderbird-firetray
pkgver=0.3.5
pkgrel=1
pkgdesc="A system tray extension for linux. Installs globally for all users. Useful as package when home mounted with noexec, because extension requires execute permission."
arch=('any')
url="http://code.google.com/p/firetray/"
license=('GPL')
depends=('thunderbird')
source=(http://firetray.googlecode.com/files/firetray-$pkgver.xpi)
md5sums=('fc311801344fafb794b01f598664ba3f')
options=(!strip)
install="$pkgname.install"

package() {

    # Looking for thhe install path
    TB_DIRS="$(ls /usr/lib | grep thunderbird)"
    TB_FINAL_DIR=""
    for TB_DIR in $TB_DIRS ; do
        if [ -d "/usr/lib/$TB_DIR/extensions" ] ; then
            # Checking if there are multiple possible paths
            if [ -z "$TB_FINAL_DIR" ] ; then
                TB_FINAL_DIR="$TB_DIR"
            else
                error "There are multiple thunderbird folders in /usr/lib."
                msg "Please specify the Thunderbird folder containing the extensions directory in the _tb_firetray_install variable."
                return 1
            fi
        fi
    done

    if [ -z "$TB_FINAL_DIR" ] ; then
        # Specified path ?
        if [ -z "$_tb_firetray_install" ] ; then
            error "No thunderbird path found."
            msg "Please specify the Thunderbird folder containing the extensions directory in the _tb_firetray_install variable."
            return 1
        else
            TB_FINAL_DIR="$_tb_firetray_install"
        fi
    fi

    EXT_ID=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf) || return 1
    DEST_DIR="$pkgdir/usr/lib/$TB_FINAL_DIR/extensions/$EXT_ID/"
    mkdir -p "$DEST_DIR"
    cp -a "$srcdir/"* "$DEST_DIR"
    rm "$DEST_DIR"/*.xpi
}
