# Maintainer: Marti Raudsepp <marti@juffo.org>
# Contributor: Lucas De Marchi <lucas.de.marchi@gmail.com>

pkgname=perf-util
pkgver=2.6.39rc7
_dist_pkgver=v${pkgver/rc/-rc}
pkgrel=2
pkgdesc="Performance monitoring tools using the Linux performance events infrastructure"
arch=(i686 x86_64)
license=('GPL')
url="http://www.kernel.org/"
depends=('elfutils' 'zlib' 'perl' 'python2')
# NB! This is detected build-time only
optdepends=(
  'libnewt: enable interactive console UI'
)
# necessary for building man pages -- 'perf help' relies on these
makedepends=('asciidoc' 'xmlto' 'libdwarf')
source=(http://www.politreco.com/files/$pkgname-$_dist_pkgver.tar.bz2)

build() {
  cd $srcdir/$pkgname-$_dist_pkgver/tools/perf

  sed -i s/python-config/python2-config/ Makefile

  # CFLAGS to work around warning in Perl 5 headers with GCC 4.6.x
  make CFLAGS="-Wno-error=switch-default" prefix=/usr \
      perfexecdir=lib/perf-core all man

  make CFLAGS="-Wno-error=switch-default" prefix=/usr \
      perfexecdir=lib/perf-core DESTDIR=$pkgdir/ install install-man

  install -dm 755 $pkgdir/usr/share/doc/$pkgname
  cp *.txt Documentation/*.txt $pkgdir/usr/share/doc/$pkgname
}
md5sums=('e92bdb2eff530b003cf7ed03a6e357d3')
