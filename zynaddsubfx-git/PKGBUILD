# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=zynaddsubfx-git
pkgver=2.4.3.r63.gae6824c
pkgrel=1
pkgdesc="A powerful realtime, multi-timbral software synthesizer."
arch=('i686' 'x86_64')
url="http://zynaddsubfx.sourceforge.net"
license=('GPL')
depends=('fltk' 'ntk-git' 'fftw' 'portaudio' 'lash' 'mxml' 'liblo')
makedepends=('git' 'cmake' 'dssi')
optdepends=('dssi: dssi plugin')
provides=('zynaddsubfx')
conflicts=('zynaddsubfx')
options=('!emptydirs')
install="$pkgname.install"
source=("$pkgname::git+git://git.code.sf.net/p/zynaddsubfx/code"
        "http://zynaddsubfx.sourceforge.net/doc/instruments/unsortedzynaddsubfxParameters_20131122.zip"
        "http://zynaddsubfx.sourceforge.net/doc/instruments/banks20090520.zip"
        "http://rekkerd.org/bin/presets/folderol_zynaddsubfx_Collection.zip"
        "zynaddsubfx-jack.desktop"
        "zynaddsubfx-alsa.desktop"
        "zynaddsubfx.svg")
noextract=("${source[1]##*}/" "${source[2]##*}/" "${source[3]##*/}")
md5sums=('SKIP'
         'f282369eebc5cc7a7b242424177b9369'
         '7d7974e877b818fb562cc870d5886fc5'
         '271ca88e262d3d3378f8d695a7151d1b'
         '137baa3407ca0a9ce3d7f4644723978f'
         '9825fcb4efc641ce1806d58cf1389aa9'
         '6f7e9c3ce3947088a10c99c46a65431f')

prepare() {
  cd $pkgname
  [ -d b ] || mkdir b

  # install dssi plugin in /usr/lib
  sed -i 's/lib64/lib/' src/CMakeLists.txt

  # extract instruments
  cd instruments && mkdir -p banks/Experimental
  bsdtar --strip-components 1 -zxf ${srcdir}/${source[1]##*/} -C banks/Experimental
  bsdtar --strip-components 1 -zxf ${srcdir}/${source[2]##*/} -C banks
  bsdtar -zxf ${srcdir}/${source[3]##*/} -C banks
}

pkgver() {
  cd $pkgname
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd $pkgname/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DGuiModule=ntk
  make

  # build external programs
  cd ../ExternalPrograms/Spliter && make
  cd ../Controller && make
}

package() {
  cd $pkgname/b
  make DESTDIR="$pkgdir/" install

  # external programs
  install -Dm644 ../ExternalPrograms/Spliter/spliter \
                 "$pkgdir/usr/bin/spliter"
  install -Dm644 ../ExternalPrograms/Controller/controller \
                 "$pkgdir/usr/bin/controller"

  # spliter doc
  install -Dm644 ../ExternalPrograms/Spliter/readme.txt \
                 "$pkgdir/usr/share/doc/zynaddsubfx/SPLITER.txt"

  # icon and desktop file
  install -Dm644 "$srcdir/zynaddsubfx-jack.desktop" \
                 "$pkgdir/usr/share/applications/zynaddsubfx-jack.desktop"
  install -Dm644 "$srcdir/zynaddsubfx-alsa.desktop" \
                 "$pkgdir/usr/share/applications/zynaddsubfx-alsa.desktop"
  install -Dm644 "$srcdir/zynaddsubfx.svg" \
                 "$pkgdir/usr/share/pixmaps/zynaddsubfx.svg"
}
