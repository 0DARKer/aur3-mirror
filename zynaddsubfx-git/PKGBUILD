# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=zynaddsubfx-git
pkgver=2.5.0.r0.g39f0392
pkgrel=1
pkgdesc="A powerful realtime, multi-timbral software synthesizer."
arch=('i686' 'x86_64')
url="http://zynaddsubfx.sourceforge.net"
license=('GPL')
depends=('fltk' 'ntk-git' 'fftw' 'portaudio' 'lash' 'mxml' 'liblo')
makedepends=('git' 'cmake') # 'dssi')
#optdepends=('dssi: dssi plugin') # DSSI temporary disabled
provides=('zynaddsubfx')
conflicts=('zynaddsubfx')
options=('!emptydirs')
install="$pkgname.install"
source=("$pkgname::git+git://git.code.sf.net/p/zynaddsubfx/code"
        "http://zynaddsubfx.sourceforge.net/doc/instruments/unsortedzynaddsubfxParameters_20140402.zip")
noextract=("${source[1]##*/}")
md5sums=('SKIP'
         '83073c967e7bb5bf176b592cc990187f')

prepare() {
  cd $pkgname
  [ -d b ] || mkdir b

  # pull rtosc and instruments
  git submodule update --init

  # does not build with --as-needed
  LDFLAGS=${LDFLAGS//,--as-needed}
}

pkgver() {
  cd $pkgname
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd $pkgname/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_INSTALL_LIBDIR=/usr/lib \
           -DGuiModule=ntk
  make

  # build external programs
  cd ../ExternalPrograms/Spliter && make
  cd ../Controller && make
}

package() {
  cd $pkgname/b
  make DESTDIR="$pkgdir/" install

  # external programs
  install -Dm644 ../ExternalPrograms/Spliter/spliter \
                 "$pkgdir/usr/bin/spliter"
  install -Dm644 ../ExternalPrograms/Controller/controller \
                 "$pkgdir/usr/bin/controller"

  # spliter doc
  install -Dm644 ../ExternalPrograms/Spliter/readme.txt \
                 "$pkgdir/usr/share/doc/zynaddsubfx/SPLITER.txt"

  # additional parameters
  install -d "$pkgdir/usr/share/zynaddsubfx/parameters"
  bsdtar --strip-components 1 --uid 0 --gid 0 -zxf \
                 ${srcdir}/${source[1]##*/} -C \
                 "$pkgdir/usr/share/zynaddsubfx/parameters"
}
