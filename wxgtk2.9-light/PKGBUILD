# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

# Based on wxgtk2.9 from [extra]

pkgname=wxgtk2.9-light
pkgver=2.9.4
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI (GNOME/GStreamer free!)"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'libgl' 'libxxf86vm' 'libsm' 'sdl')
makedepends=('mesa' 'ccache' 'glu' 'libxt')
provides=('wxgtk2.9')
conflicts=('wxgtk2.9')
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2
        "wxGTK-2.9.4-collision.patch")
sha1sums=('5a34ddf19d37c741f74652ee847df9568a8b81e1'
          'bb3a8021a90d5457f9ece0da88577d87d5e0bf53')
options=('ccache')  # Required to build libwx_gtk2u_mmedia

build() {
  rm -fr "${srcdir}/wxWidgets-build"
  cp -R "${srcdir}/wxWidgets-${pkgver}" "${srcdir}/wxWidgets-build"
  cd "${srcdir}/wxWidgets-build"

  patch -p1 -i ../wxGTK-2.9.4-collision.patch

  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --disable-optimize --disable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-sdl --disable-precomp-headers
  make
  make -C locale allmo
}

package() {
  cd "${srcdir}/wxWidgets-build"
  make DESTDIR="${pkgdir}" install
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  rm "${pkgdir}/usr/share/locale/it/LC_MESSAGES/wxmsw.mo"
}
