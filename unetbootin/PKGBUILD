# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>

pkgname=unetbootin
pkgver=502
pkgrel=3
pkgdesc="Allows the creation of bootable Live USB drives for Linux/BSD distributions"
url="http://unetbootin.sourceforge.net/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('syslinux' 'p7zip' 'qt' 'mtools' 'libpng')
optdepends=('gksu: for graphically requesting the root password'
	    'kdebase-runtime: for graphically requesting the root password')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-source-${pkgver}.tar.gz)
md5sums=('72af23407d0478439bd0e9fb1b0b0293')

build() {
  sed -i '/^RESOURCES/d' unetbootin.pro
  lupdate unetbootin.pro
  lrelease unetbootin.pro
  qmake "DEFINES += NOSTATIC" "RESOURCES -= unetbootin.qrc"
  make
}

package() {
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  install -m644 ${pkgname}_*.qm "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 ${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 ../${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  echo -e Icon=${pkgname} >> "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}