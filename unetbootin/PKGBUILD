# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: Godane aka Christopher Rogers <slaxemulator@gmail.com>

pkgname=unetbootin
pkgver=494
pkgrel=1
pkgdesc="Allows the creation of bootable Live USB drives for Linux/BSD distributions"
url="http://unetbootin.sourceforge.net/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('syslinux' 'p7zip' 'qt' 'mtools' 'libpng')
optdepends=('gksu: for graphically requesting the root password'
	    'kdebase-runtime: for graphically requesting the root password')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-source-${pkgver}.tar.gz)
md5sums=('e3f01fd88f3aa5d918c60ba0be608e86')

build() {
  sed -i '/^RESOURCES/d' unetbootin.pro
  lupdate unetbootin.pro
  lrelease unetbootin.pro
  qmake "DEFINES += NOSTATIC" "RESOURCES -= unetbootin.qrc"
  make
}

package() {
  install -Dm755 ${pkgname} "${pkgdir}/usr/sbin/${pkgname}"
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  install -m644 ${pkgname}_*.qm "${pkgdir}/usr/share/${pkgname}/"
  install -Dm644 ${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
#   sed -i 's|Exec=/usr/bin/unetbootin|Exec=su -c /usr/sbin/unetbootin|g' "${pkgdir}/usr/share/applications/${pkgname}.desktop"
#   sed -i 's|Terminal=false|Terminal=true|g' "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}