# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=gap-math
pkgver=4.7.2
pkgrel=1
pkgdesc="A system for computational discrete algebra"
arch=('i686' 'x86_64')
url="http://www.gap-system.org/"
license=('GPL')
makedepends=('chrpath' 'rsync')
options=('!libtool' '!emptydirs' '!makeflags')
source=(http://www.gap-system.org/pub/gap/gap47/tar.bz2/gap4r7p2_2013_12_01-10_17.tar.bz2
        gap.sh)
sha256sums=('ebdf852ff86e2f92e8c9006640d8ebd0125ded029d42d6a42f87e3f0e63435f4'
            'd90a6ac0610b0eea1657297e6d18a3bfcbdee8ca5c180079b55ae7c1af57cca2')

build() {
  cd "$srcdir/gap4r7"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"
  install -dm755 "$pkgdir/opt/gap/"
  rsync -prv --exclude='*.exe' --exclude='*.dll' gap4r7/* "$pkgdir/opt/gap/"
  chrpath -d $(find "$pkgdir" ! -name '*.o' -exec file '{}' \;|grep ELF|cut -d: -f1)
  install -Dm755 gap.sh "$pkgdir/usr/bin/gap"
}

# vim:set ts=2 sw=2 et:
