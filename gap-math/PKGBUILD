# Maintainer:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=gap-math
pkgver=4.5.7
pkgrel=1
pkgdesc="A system for computational discrete algebra"
arch=('i686' 'x86_64')
url="http://www.gap-system.org/"
license=('GPL')
makedepends=('chrpath' 'rsync')
options=('!libtool' '!emptydirs')
source=(ftp://ftp.gap-system.org/pub/gap/gap4/tar.bz2/gap4r5p7_2012_12_14-17_45.tar.bz2
        gap.sh)
md5sums=('1d10705f4156a06e88f87ae6da364e97'
         '3deb5c878b9695bc7c87474a8f8be788')

build() {
  cd "$srcdir/gap4r5"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"
  install -dm755 "$pkgdir/opt/gap/"
  rsync -prv --exclude='*.exe' --exclude='*.dll' gap4r5/* "$pkgdir/opt/gap/"
  chrpath -d $(find "$pkgdir" ! -name '*.o' -exec file '{}' \;|grep ELF|cut -d: -f1)
  install -Dm755 gap.sh "$pkgdir/usr/bin/gap"
}

# vim:set ts=2 sw=2 et:
