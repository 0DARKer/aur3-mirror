# Maintainer: Michael Schubert <mschu.dev at gmail>

pkgname=libsbml
pkgver=5.5.0
pkgrel=1
pkgdesc="XML-based description language for computational models in systems biology"
url="http://sbml.org/Software/libSBML"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'libxml2')
optdepends=('bzip2' 'python2' 'perl' 'ruby' 'java-runtime' 'octave' 'mono')
builddepends=('swig' 'python2' 'perl' 'java-sdk')
options=('!libtool')
source=("http://sourceforge.net/projects/sbml/files/$pkgname/$pkgver/stable/$pkgname-$pkgver-src.tar.gz")
md5sums=('d33b0029ac8172885caade4053ebfdd3')

build() {
  mkdir -p "$srcdir/build" && cd "$srcdir/build"

  cmake -DCMAKE_BUILD_TYPE=release -DWITH_PYTHON=1 -DWITH_JAVA=1 \
    -DWITH_OCTAVE=0 -DWITH_CSHARP=0 -DWITH_PERL=1 -DWITH_RUBY=0 \
    -DWITH_MATLAB=0 -DCMAKE_INSTALL_PREFIX=/usr "../$pkgname-$pkgver"

  find . -type f -exec sed -i "s:/usr/bin/python:/usr/bin/python2:" {} \;
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
}
