# Contributor: Lex Rivera aka x-demon <aur@x-demon.org>                
# Maintainer: Lex Rivera aka x-demon <aur@x-demon.org>
# Maintainer: Mantas M. <grawity@gmail.com>

pkgname=binkd
pkgver=0.9.11
pkgrel=1
pkgdesc="Binkley protocol daemon for transferring files between Fidonet systems"
arch=('i686' 'x86_64')
url="http://2f.ru/binkd/"
license=('GPL')
source=("ftp://happy.kiev.ua/pub/fidosoft/mailer/binkd/binkd09b.zip")
sha1sums=('fbecc523259ca597e06fe467607f39834a716f3d')

build() {
  cd "$srcdir/$pkgname"
  cp mkfls/unix/{Makefile*,configure*,install-sh,mkinstalldirs} .
  ./configure \
    --prefix=/usr           \
    --sysconfdir=/etc       \
    --localstatedir=/var    \
    --with-https            \
    --with-ntlm             \
    ;
  touch banner # shut up post-make text
  make
}

package() {
  cd "$srcdir/$pkgname"
  make PREFIX="$pkgdir/usr" install
  ln -sf "binkd-$pkgver" "$pkgdir/usr/sbin/binkd"
  mkdir -p "$pkgdir/usr/share"
  mv "$pkgdir/usr/man" "$pkgdir/usr/share/man"
}

# vim: ts=2:sw=2:et
