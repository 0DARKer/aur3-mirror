# Contributor: Lex Rivera aka x-demon <aur@x-demon.org>                
# Maintainer: Lex Rivera aka x-demon <aur@x-demon.org>
# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=binkd
pkgver=1.0.1
pkgrel=1
pkgdesc="Binkley protocol daemon for transferring files between Fidonet systems"
arch=('i686' 'x86_64')
url="http://2f.ru/binkd/"
license=('GPL')
backup=("etc/binkd/binkd.conf")
source=("ftp://happy.kiev.ua/pub/fidosoft/mailer/$pkgname/$pkgname-$pkgver.tar.gz"
        "binkd.service"
        "binkd@.service"
        "binkd.socket"
        "binkd.tmpfiles")
install="binkd.install"
sha1sums=('cef53e315ef9c5d761774a9cf5388c0f63b59a12'
          '39ccffc23e1f4f1ec1dcd21ef8f9b53f9e61bc3d'
          'e0040b854b8c46750f7109336647a41d13815460'
          '6fb4bc935010e2b068b6d50c83a30279b5f66c38'
          '47851c1aafda0472ad816aec8978f368a9227c66')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp mkfls/unix/{Makefile*,configure*,install-sh,mkinstalldirs} .
  ./configure \
    --prefix=/usr           \
    --sbindir=/usr/bin      \
    --sysconfdir=/etc       \
    --mandir=/usr/share/man \
    --localstatedir=/var    \
    --with-https            \
    --with-ntlm             \
    ;
  touch banner # shut up post-make text
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"

  ln -sf "binkd-$pkgver" "$pkgdir/usr/bin/binkd"

  install -dm0755 "$pkgdir/etc/binkd"
  mv "$pkgdir/etc/binkd.conf-dist" "$pkgdir/etc/binkd/binkd.conf"

  for dir in inbound{,-temp,-unsecure} outbound/fidonet longbox personalboxes nodelist; do
    mkdir -p "$pkgdir/var/spool/ftn/$dir"
  done

  cd "$srcdir"
  install -Dm0644 binkd.service   "$pkgdir/usr/lib/systemd/system/binkd.service"
  install -Dm0644 binkd@.service  "$pkgdir/usr/lib/systemd/system/binkd@.service"
  install -Dm0644 binkd.socket    "$pkgdir/usr/lib/systemd/system/binkd.socket"
  install -Dm0644 binkd.tmpfiles  "$pkgdir/usr/lib/tmpfiles.d/binkd.conf"
}

# vim: ts=2:sw=2:et
