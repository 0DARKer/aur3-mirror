# Maintainer: rtfreedman (rob<d0t>til<d0t>freedman<aT>gmail<d0t>com
# 
# This build script uses (persistent) git repositories (ffmpeg-git, mpv-git and libass-git)
# located where PKGBUILD resides - clang is used by default, but can be changed to gcc easily.
#
# It is heavily commented and configured without seldom used features by default
#
# use 'makepkg &>mpv.log' to get a log file of the build
#
# if you need features not autodetected, use 
# 'src/mpv-build/mpv/configure --help' and
# 'src/mpv-build/ffmpeg/configure --help' 
#

pkgname=mpv-player-git
_gitname="mpv-build"
pkgver=34924
pkgrel=1
pkgdesc="Video player based on MPlayer/mplayer2 (uses statically linked ffmpeg-git & libass-git)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://mpv.io/"
#
# check log file for auto detected libs; you can use --disable-xxx to disable auto detection
#
depends=('desktop-file-utils' 'enca' 'fribidi' 'freetype2' 'fontconfig' 'lcms2' 'libbluray' 'libcdio-paranoia' 
		'lame' 'libgl' 'libmng' 'libdvdread' 'libpulse' 'libquvi' 'libva' 'libvdpau' 'libxinerama' 
		'libxkbcommon' 'libvpx' 'libxss' 'libxv' 'libxxf86vm' 'mpg123' 'x264')
#
# for using gcc instead of clang, remove 'clang' here
# and replace it with gcc further down in CC=gcc and --cc=gcc
# or reverse it later again
#
makedepends=('clang' 'git' 'mesa' 'python-docutils' 'yasm')
#
conflicts=('mpv' 'mpv-git' 'mpv-build-git')
install="mpv-player.install"
#
# git repositories outside of src/ are persistent and allows for faster rebuilds ;)
#
source=('git://github.com/mpv-player/mpv-build.git'
        'git://github.com/mpv-player/mpv.git'
        'git://source.ffmpeg.org/ffmpeg.git'
        'git+https://code.google.com/p/libass'
        'mpv.desktop')
md5sums=(SKIP SKIP SKIP SKIP
         '0ff83929010f5a71bda77772141ab347')

pkgver() {
  cd mpv
  # get a plain number
  echo $(git rev-list --count master) 
}

prepare() {
  cd "${_gitname}"
  rm -fr mpv ffmpeg libass ffmpeg_build build_libs
  # pacman puts them into src/, we move them into place
  mv ../{mpv,ffmpeg,libass} .
  #
  # mpv build flags are mostly autodetected
  # remove --disable-xxx to enable auto detection, see 'src/mpv-build/mpv/configure --help'
  # use --disable-encoding (and remove --enable-xxx in ffmpeg) for playing only & reduced foot print
  #
  sed 's|OPTIONS="|OPTIONS="--prefix=/usr --confdir=/etc/mpv --extra-cflags=-I/usr/include/samba-4.0 \
 --disable-rsound --disable-vstream --disable-wayland --disable-ladspa --disable-jack --disable-smb \
 --disable-sdl --disable-sdl2 --disable-caca --disable-lirc --disable-lircc --disable-portaudio|' \
	-i scripts/mpv-config
  #
  # ffmpeg build flags -- please take notice of default '--enable-nonfree' in ffmpeg
  # --enable-xxx enables additional codecs, see [no] labeled options in 'src/mpv-build/ffmpeg/configure --help' 
  #
  # enable: VP8 and VP9 de/encoding via libvpx, H.264 encoding via x264, MP3 encoding via libmp3lame
  # disable building ffmpeg programs not used by mpv-player
  # we need to set the compiler for ffmpeg explicitly: --cc=clang|gcc
  #
  sed 's|OPTIONS="|OPTIONS="--cc=clang --disable-programs --enable-libx264 --enable-libvpx --enable-libmp3lame |' \
	-i scripts/ffmpeg-config
}

build() {
  cd "${_gitname}"
  # either use CC=clang|gcc
  export CC=clang
  make
}

package() {
  cd "${_gitname}/mpv"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/mpv.desktop" "${pkgdir}/usr/share/applications/mpv.desktop"
  install -Dm644 etc/mplayer.xpm "${pkgdir}/usr/share/pixmaps/mpv.xpm"

  # media file identification script 
  install -m755 TOOLS/mpv_identify.sh "${pkgdir}/usr/bin"
  
  # sample config files
  install -d "${pkgdir}"/{etc/mpv,usr/share/doc/mpv}
  install -Dm644 etc/input.conf "${pkgdir}/etc/mpv/input.conf-example"
  install -Dm644 etc/example.conf "${pkgdir}/etc/mpv/mpv.conf-example"
  install -Dm644 etc/encoding-example-profiles.conf "${pkgdir}/etc/mpv/encoding-profiles.conf"
  
  # fix up to get consistent documentation
  sed -i 's|/usr/local/etc/mpv.conf|/etc/mpv.conf|' "${pkgdir}/etc/mpv/mpv.conf-example"
  
  # either copy /etc/mpv/mpv.conf-example to ~/.mpv/mpv.conf or rename it to /etc/mpv/mpv.conf
  # in any case /etc/mpv/encoding-profiles.conf gets included by default
  sed -i 's|/path/to/the/file/you/want/to/include|/etc/mpv/encoding-profiles.conf|' \
	"${pkgdir}/etc/mpv/mpv.conf-example"
  
  install -Dm644 Copyright DOCS/tech-overview.txt "${pkgdir}/usr/share/doc/mpv"
}
