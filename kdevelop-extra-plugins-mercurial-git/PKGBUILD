# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=kdevelop-extra-plugins-mercurial-git
pkgver=20110605
pkgrel=1
pkgdesc="Mercurial support for KDevelop"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/devtools/plugins/kdev-mercurial"
license=('GPL')
depends=('kdevelop-git' 'mercurial')
makedepends=('cmake' 'automoc4' 'git')
source=(adapt_to_API.diff update_version.diff)
md5sums=('e385b989f3c4bf265749f45564b834b1'
         '4c4e9787409726c6f0f9b388c93b74ef')

_gitroot="git://anongit.kde.org/kdev-mercurial"
_gitname="mercurial"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."
  if [ -d $_gitname ] ; then
    cd $_gitname
    # Change remote url to anongit
    if [ -z $(git branch -v | grep anongit) ] ; then
      git remote set-url origin ${_gitroot}
    fi
    git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  cd "$srcdir"/$_gitname
  patch -Np1 < "$srcdir"/adapt_to_API.diff || true
  patch -Np1 < "$srcdir"/update_version.diff || true

  if [ -e "$srcdir"/$_gitname-build ]; then
        rm -rf "$srcdir"/$_gitname-build
  fi

  mkdir -p "$srcdir"/$_gitname-build
  cd "$srcdir"/$_gitname-build

  cmake ../$_gitname -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir"/$_gitname-build
  make DESTDIR="$pkgdir" install
}
