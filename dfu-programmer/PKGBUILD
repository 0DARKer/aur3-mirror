# Contributor: Eduardo Martins Lopes < edumlopes at gmail dot com >
# Contributor: Poco <poco@atelo.org> (Patched 0.5.4 with atmega16u2 support)
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=dfu-programmer
pkgver=0.5.4
pkgrel=2
pkgdesc='Programmer for Atmel chips with a USB bootloader'
arch=('i686' 'x86_64')
url='http://dfu-programmer.sourceforge.net/'
license=('GPL')
depends=('libusbx')
source=(
  "http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
  "atmega16u2.diff")
sha256sums=(
  'c0fb08fead3d4379368d0e4af5a5aa99c7acee32fa8696cd3e27d2f7856a20de'
  '7ec83daaaf7601559a62ccd0ef184a115d9ff8ce8b6f9448a9c1a0cc858f7f5c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p0 -i "$srcdir/atmega16u2.diff"
  # Hack before upstream fix
  export LDFLAGS="${LDFLAGS//,--as-needed}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
