# Contributor: Eduardo Martins Lopes < edumlopes at gmail dot com >
# Contributor: Poco <poco@atelo.org> (Patched 0.5.4 with atmega16u2 support)
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=dfu-programmer
pkgver=0.6.1
pkgrel=1
pkgdesc='Programmer for Atmel chips with a USB bootloader'
arch=('i686' 'x86_64')
url='http://dfu-programmer.sourceforge.net/'
license=('GPL')
depends=('libusbx')
source=(
  "http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha1sums=('dbbf81a7fd47df7b214358e46e422b6215ae53f0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Rebuild configure to use PKG_CHECK_MODULES due to bug 3599159.
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac
  ./bootstrap.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
