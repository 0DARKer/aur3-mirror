# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgbase=lua51-sql-sqlite
pkgname=('lua51-sql-sqlite')
pkgver=2.2.0
pkgrel=3
arch=('i686' 'x86_64')
url='http://www.keplerproject.org/luasql/'
license=('MIT')
makedepends=('lua51' 'sqlite')
source=("https://github.com/downloads/keplerproject/luasql/luasql-$pkgver.tar.gz")
md5sums=('59d72db444c6369c936d4f03694db6b7')

build() {
  cd luasql-$pkgver
  sed -i '24s,$,/lua5.1,' config
  msg2 'Building sqlite support'
  make T=sqlite3 PREFIX=/usr DRIVER_LIBS='-lsqlite3' DRIVER_INCS='-fPIC -std=c99'
}

package_lua51-sql-sqlite() {
  pkgdesc='SQLite module for Lua'
  depends=('lua51' 'sqlite')

  cd luasql-$pkgver
  install -Dm644 src/sqlite3.so "$pkgdir/usr/lib/lua/5.1/luasql/sqlite3.so"
  install -Dm644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

# vim:set ts=2 sw=2 et:
