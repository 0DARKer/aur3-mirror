pkgname=mingw-w64-nettle
pkgver=2.7.1
pkgrel=4
pkgdesc="A low-level cryptographic library (mingw-w64)"
arch=(any)
url="http://www.lysator.liu.se/~nisse/nettle"
license=("LGPL")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt mingw-w64-gmp)
options=(staticlibs !strip !buildflags)
source=("http://www.lysator.liu.se/~nisse/archive/nettle-$pkgver.tar.gz"
"nettle-2.7.1-remove-ecc-testsuite.patch"
"nettle-2.7.1-tmpalloc.patch")
md5sums=('003d5147911317931dd453520eb234a5'
         'f3b10a962dae1dbff215adc26a522625'
         'f40dd21708f3b885d948dd573012b39a')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	cd "$srcdir/nettle-$pkgver"
	patch -p1 -i ../nettle-2.7.1-remove-ecc-testsuite.patch
	patch -p1 -i ../nettle-2.7.1-tmpalloc.patch
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-shared
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
