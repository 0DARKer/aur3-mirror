# Submitter: Anthony Cornehl <accornehl[at]gmail[dot]com>
# Maintainer: Mike Dacre <mike@dacre.me>
# https://github.com/twinshadow/abs
pkgname=libmlx4
pkgver=1.0.6
pkgrel=2
pkgdesc="Userspace driver for Mellanox ConnectX InfiniBand HCAs"
groups=('ofed')
url="http://www.openfabrics.org"
arch=('x86_64' 'i686')
license=('GPL2' 'custom:"Open Fabrics Alliance BSD"')
depends=('libibverbs')
source=("https://www.openfabrics.org/downloads/mlx4/${pkgname}-${pkgver}.tar.gz")
# cksum start
md5sums=('bbc1a9293a550250fb9ed8860824fad3')
sha512sums=('f1eea0be2b7b5b3a536dcfa6dc2d348487ba6e9e75b8fe70488d7aa7a62aaeaba36b8aed5bcaa83b5bb483d4be6386d87f79ec48ab14a2efd07e69de4ee51cd1')
# cksum end

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
