# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=tslib
pkgver=1.0.r112.g158ee49
pkgrel=1
pkgdesc="tslib TouchScreen Library"
arch=('arm' 'i686' 'x86_64')
license=('custom')
url="http://tslib.berlios.de/"
depends=('xorg-server')
makedepends=('git')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=('git+https://github.com/kergoth/tslib.git')
#source=("http://download.berlios.de/${pkgname}/${pkgname}-${pkgver}.tar.bz2")

pkgver() {
	cd tslib/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd tslib/
	
	./autogen.sh
	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd tslib/
	
	make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP')
