# $Id: PKGBUILD 100347 2010-11-22 23:25:14Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=avogadro-git
pkgver=20101219
pkgrel=2
pkgdesc="An advanced molecular editor based on Qt"
arch=('i686' 'x86_64')
url="http://avogadro.openmolecules.net/wiki/Main_Page"
license=('GPL2')
depends=('eigen' 'openbabel' 'python2-qt' 'boost-libs' 'glew' 'python-numpy')
makedepends=('cmake' 'boost' 'git')
conflicts=('avogadro')
provides=('avogadro')
source=('sip-4.11.patch' 'fix-pycapsule.patch')
md5sums=('e78c3f96d0dc702947f75de1c094f580'
         '45b0f3be5913575311e92b74a5520966')

_gitroot="git://avogadro.git.sourceforge.net/gitroot/avogadro/avogadro"
_gitname="avogadro"


build() {
  cd "$srcdir"

  if [ -d "$srcdir/$_gitname" ] ; then
    cd "$_gitname" && git pull origin
   else
    git clone "$_gitroot"
  fi

  cd "${srcdir}/$_gitname"
  patch -Np1 -i ${srcdir}/sip-4.11.patch || true
  patch -Np1 -i ${srcdir}/fix-pycapsule.patch || true

  mkdir -p "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"


  cmake "../${_gitname}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
}
