# Maintainer: Dennis Hamester <dennis [dot] hamester [at] gmail [dot] com>

pkgname=ftl
pkgver=1.01
pkgrel=4
pkgdesc="A spaceship simulation real-time roguelike-like (requires full copy of the game)"
license=('custom:ftl')
arch=('i686' 'x86_64')
url="http://www.ftlgame.com/"
depends=('mesa')
source=('file://FTL.Linux.1.01.tar.gz'
        'ftl'
        'ftl.desktop'
        'ftl.png')
sha1sums=('8ae2e98623bdd7a8297bfab3f932dc06e06b69a5'
          '51101e86e6e75d5d0414fd5b5346d003e38160e7'
          'ebaaca2051c536893079fd47275be9456ab39d14'
          '37a0cd78eb94b5ea3d72da03ef36166c4d63ee44')

package() {
  install -d "${pkgdir}"/opt

  cp -r "${srcdir}"/FTL "${pkgdir}"/opt/

  # fix file permissions
  find "${pkgdir}"/opt/FTL -type d -exec chmod 755 {} +
  find "${pkgdir}"/opt/FTL -type f -exec chmod 644 {} +
  chmod 755 "${pkgdir}"/opt/FTL/FTL
  chmod 755 "${pkgdir}"/opt/FTL/data/FTL

  if [[ $CARCH != x86_64 ]]; then
    rm -rf "${pkgdir}"/opt/FTL/data/amd64
    chmod 755 "${pkgdir}"/opt/FTL/data/x86/bin/FTL
  else
    rm -rf "${pkgdir}"/opt/FTL/data/x86
    chmod 755 "${pkgdir}"/opt/FTL/data/amd64/bin/FTL
  fi

  install -Dm755 "${srcdir}"/ftl "${pkgdir}"/usr/bin/ftl

  install -Dm644 "${srcdir}"/ftl.desktop "${pkgdir}"/usr/share/applications/ftl.desktop
  install -Dm644 "${srcdir}"/ftl.png "${pkgdir}"/usr/share/pixmaps/ftl.png
}

