# Maintainer: Dennis Hamester <dennis [dot] hamester [at] gmail [dot] com>

pkgname=ftl
pkgver=1.02.6
pkgrel=1
pkgdesc="A spaceship simulation real-time roguelike-like (requires full copy of the game)"
license=('custom:commercial')
arch=('i686' 'x86_64')
url="http://www.ftlgame.com/"
depends=('glu')
_gamepkg="ftl-linux-$pkgver-1350405106.tar.gz"
source=("$_gamepkg"::"file://$_gamepkg}"
        $pkgname.sh
        $pkgname.desktop
        $pkgname.png)
PKGEXT='.pkg.tar'

package() {
  install -d "$pkgdir"/opt

  cp -r "$srcdir"/FTL "$pkgdir"/opt/

  # fix file permissions
  find "$pkgdir"/opt/FTL -type d -exec chmod 755 {} +
  find "$pkgdir"/opt/FTL -type f -exec chmod 644 {} +
  chmod 755 "$pkgdir"/opt/FTL/FTL
  chmod 755 "$pkgdir"/opt/FTL/data/FTL

  if [[ $CARCH != x86_64 ]]; then
    rm -rf "$pkgdir"/opt/FTL/data/amd64
    chmod 755 "$pkgdir"/opt/FTL/data/x86/bin/FTL
  else
    rm -rf "$pkgdir"/opt/FTL/data/x86
    chmod 755 "$pkgdir"/opt/FTL/data/amd64/bin/FTL
  fi

  install -Dm755 "$srcdir"/$pkgname.sh "$pkgdir"/usr/bin/$pkgname

  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -Dm644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}

md5sums=('7a5f301656a5683f7ecc33773dc35a89'
         'd90ab053f44278026fd961ad02865a85'
         'c7dd5f9976bf0cc7efc99ecde0d0cd18'
         '100600bfdf5bf4ff18569a0e170c7b8e')
