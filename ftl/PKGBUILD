# Maintainer: Dennis Hamester <dennis [dot] hamester [at] gmail [dot] com>

pkgname=ftl
pkgver=1.01
pkgrel=2
pkgdesc="A spaceship simulation real-time roguelike-like (requires full copy of the game)"
license=('custom:ftl')
arch=('i686' 'x86_64')
url="http://www.ftlgame.com/"
depends=('devil-ilut' 'libbass' 'libbassmix' 'freetype2')
source=('file://FTL.Linux.1.01.tar.gz'
        'ftl'
        'ftl.desktop'
        'ftl.png')
sha1sums=('8ae2e98623bdd7a8297bfab3f932dc06e06b69a5'
          'b9c0480ed09f6e52b8b7cb96c3c487df6996928d'
          'e1451d8ab53402250f24bde3cad95d68ac216c35'
          '37a0cd78eb94b5ea3d72da03ef36166c4d63ee44')

package() {
  install -Dm644 "${srcdir}"/FTL/FTL_README.html "${pkgdir}"/usr/share/doc/ftl/FTL_README.html

  install -Dm644 "${srcdir}"/FTL/data/exe_icon.bmp "${pkgdir}"/usr/share/ftl/exe_icon.bmp
  install -Dm644 "${srcdir}"/FTL/data/resources/resource.dat "${pkgdir}"/usr/share/ftl/resources/resource.dat
  install -Dm644 "${srcdir}"/FTL/data/resources/data.dat "${pkgdir}"/usr/share/ftl/resources/data.dat

  if [[ $CARCH == x86_64 ]]; then
    install -D "${srcdir}"/FTL/data/amd64/bin/FTL "${pkgdir}"/usr/share/ftl/FTL
  else
    install -D "${srcdir}"/FTL/data/x86/bin/FTL "${pkgdir}"/usr/share/ftl/FTL
  fi

  install -Dm755 "${srcdir}"/ftl "${pkgdir}"/usr/bin/ftl

  install -Dm644 "${srcdir}"/ftl.desktop "${pkgdir}"/usr/share/applications/ftl.desktop
  install -Dm644 "${srcdir}"/ftl.png "${pkgdir}"/usr/share/pixmaps/ftl.png
}

