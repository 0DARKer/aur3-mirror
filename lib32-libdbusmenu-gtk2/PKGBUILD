# Maintainer: Jameson Pugh <imntreal@gmail.com>

_pkgbase=libdbusmenu
_pkgbasename=libdbusmenu-gtk2
pkgname=lib32-libdbusmenu-gtk2
pkgver=12.10.2
pkgrel=9
pkgdesc="Small little library that passes a menu structure across DBus (GTK+ 2 library)"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('LGPL')
depends=('lib32-gtk2' 'lib32-libdbusmenu' "$_pkgbasename")
makedepends=('gcc-multilib' 'intltool' 'gnome-doc-utils' 'gobject-introspection' 'vala' 'lib32-json-glib')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=(http://launchpad.net/dbusmenu/${pkgver%.*}/$pkgver/+download/$_pkgbase-$pkgver.tar.gz)
md5sums=('e30fc986b447f62513d61225fa573a70')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "$srcdir/$_pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/dbusmenu-bench

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --libdir=/usr/lib32 --disable-static --with-gtk=2
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"

  make -j1 -C libdbusmenu-glib DESTDIR="$pkgdir/" install
  make -j1 -C libdbusmenu-gtk DESTDIR="$pkgdir/" install
  make -j1 -C libdbusmenu-glib DESTDIR="$pkgdir/" uninstall

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
