# Maintainer: Valentin V. Bartenev <ne@vbart.ru> http://vbart.info/

pkgname=ctpp2
pkgver=2.7.2
pkgrel=1
pkgdesc='CTPP (or CT++) is the fastest template engine which completely written in C++'
arch=(i686 x86_64)
url='http://ctpp.havoc.ru/en/'
optdepends=(
  'iconv: iconv support'
  'md5: md5 support'
)
makedepends=(cmake make)
source=("http://ctpp.havoc.ru/download/ctpp2-$pkgver.tar.gz")
license=('custom:BSD-like')
md5sums=(09d9cef5703eb8f71c9c20bcea689bef)
sha256sums=(51d3b04a9bc5bc20ea95c0a1aa9ef183be114d895ec38ee9e105b6a64d995cb4)
_srcdir="$srcdir/$pkgname-$pkgver"

build() {
  cd "$_srcdir"
  mkdir build
  cd build
  cmake -Wno-dev -DCMAKE_INSTALL_PREFIX=/usr -DSKIP_RELINK_RPATH:BOOL=ON ..
  make VERBOSE=1
}

check() {
  cd "$_srcdir/build"
  env LD_LIBRARY_PATH=".:$LD_LIBRARY_PATH" make test
}

package() {
  cd "$_srcdir/build"
  make DESTDIR="$pkgdir" install
  install -Dm 644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# Enjoy! ;)