# Maintainer: mareex <marcus.behrendt.86[at]gmail[dot]com>
# Contributor: Joost Bremmer <toost dot b at gmail dot com>

pkgname=i-nex
_prgname=I-Nex
pkgver=0.6.4
pkgrel=4
pkgdesc="System information tool like hardinfo, sysinfo"
arch=('any')
url="https://github.com/eloaders/I-Nex"
license=('LGPL3')
depends=('libcpuid-git' 
		 'gambas3-gb-gtk'
		 'gambas3-gb-desktop'
		 'gambas3-gb-settings'
		 'gambas3-gb-form-dialog'
		 'gambas3-gb-qt4'
		 'gambas3-gb-form-mdi'
		 'gambas3-gb-form-stock'
		 'xorg-server-utils'
		 'lsb-release'
		 'mesa-demos')
makedepends=('gambas3-devel')
conflicts=('i-nex-bzr')
source=("https://github.com/eloaders/I-Nex/archive/0.6.4.tar.gz"
	"i-nex.patch")
sha256sums=('593c47be390cb505b0e0e380b984f3df3c27d633d07ff8718803c07e895a9b13'
            '1aae01efa3101e1879ec07b9bd5e78b3a430dad88377dae0dd2466657ecbec6f')

prepare()
{
	cd "${srcdir}/${_prgname}-${pkgver}"
	patch -p1 -i  "${srcdir}/i-nex.patch"
}


build() {
  cd "${srcdir}/${_prgname}-${pkgver}/src/i-nex"
  echo "Starting make..."
  gbc3 -eagtpmv
  gba3
  cd "../../JSON"
  make 
}

package() {
  cd "${srcdir}/${_prgname}-${pkgver}/JSON"
  make DESTDIR=${pkgdir} install
  cd ..
  sed -i 's|i-nex.gambas|i-nex|' debian/i-nex.desktop
  install -Dm755 src/i-nex/i-nex.gambas "$pkgdir/usr/bin/i-nex"
  install -Dm644 src/i-nex/logo/i-nex.0.4.x.png "$pkgdir/usr/share/pixmaps/i-nex.0.4.x.png"
  install -Dm644 debian/i-nex.desktop "$pkgdir/usr/share/applications/i-nex.desktop"
  install -dm755 "$pkgdir/usr/share/i-nex/pastebinit"
  cp -r pastebin.d pastebinit{,.xml} release.conf "$pkgdir/usr/share/i-nex/pastebinit"
  sed -i 's|python$|python2|' "$pkgdir/usr/share/i-nex/pastebinit/pastebinit"
}
