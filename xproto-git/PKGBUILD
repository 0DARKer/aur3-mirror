# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: Matt Parnell/ilikenwf <parwok@gmail.com>
# Based on [extra]'s xproto

_pkgname=xproto
pkgname=$_pkgname-git
pkgver=20130329
pkgrel=1
pkgdesc="X11 core wire protocol and auxiliary headers - Git version"
arch=('any')
url="http://cgit.freedesktop.org/xorg/proto/$_pkgname/"
license=('custom')
makedepends=('git' 'xorg-util-macros')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("git://anongit.freedesktop.org/xorg/proto/$_pkgname")
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git log -1 --format="%cd" --date=short | sed 's/-//g'
}

build() {
  cd $_pkgname
  msg2 "Starting autogen.sh..."
  ./autogen.sh --prefix=/usr
  msg2 "Starting make..."
  make
}

package(){
  cd $_pkgname
  provides=$([ -d .git ] && git describe | sed 's/-/=/3')
  msg2 "Starting make install..."
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}