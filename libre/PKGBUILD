# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Luka Perkov <archlinux <at> lukaperkov <dOt> net>

_name=re
pkgname=libre
pkgver=0.4.0
pkgrel=1
pkgdesc="A library for real-time communications with async IO support, a complete SIP stack with support for SDP, RTP/RTCP, STUN/TURN/ICE and DNS Client"
arch=('i686' 'x86_64')
url="http://www.creytiv.com/"
license=('BSD')
depends=('openssl')
source=("http://www.creytiv.com/pub/$_name-$pkgver.tar.gz")
md5sums=('77469714212b40ec7f224ad8648f91d2')

build() {
  cd "$srcdir/$_name-$pkgver"
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 docs/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # fix permission for static lib
  chmod 644 "$pkgdir/usr/lib/$pkgname.a"

  # add missing include
  sed -i '7a#include <stdint.h>' \
    "$pkgdir/usr/include/re/re_types.h"
}
