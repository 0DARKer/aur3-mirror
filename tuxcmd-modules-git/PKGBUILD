# Contributor: xduugu
pkgname=tuxcmd-modules-git
pkgver=20100227
pkgrel=1
pkgdesc="Modules for Tux Commander"
arch=('i686' 'x86_64')
url="http://tuxcmd.sourceforge.net/"
license=('GPL')
depends=('tuxcmd')
makedepends=('git' 'sharutils')
optdepends=('gvfs: for gvfs module')
provides=('tuxcmd-modules')
conflicts=('tuxcmd-modules')
options=('!docs' '!emptydirs')
source=()
md5sums=()

_gitroot="git://git.bzatek.net/tuxcmd-modules/"
_gitname="tuxcmd-modules"

build() {
	cd "$srcdir"
	msg "Connecting to GIT server...."

	if [ -d $_gitname ] ; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "$srcdir/$_gitname-build"
	cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
	cd "$srcdir/$_gitname-build"

	# fix Makefiles
	sed -i 's|$(INSTALL) \(.*\) $(DESTDIR)/lib$(LIB_SUFFIX)/tuxcmd/|$(INSTALL) -D \1 $(DESTDIR)/lib/tuxcmd/\1|' */Makefile

	make -j1 || return 1
}

package() {
	cd "$srcdir/$_gitname-build"
	make DESTDIR="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 noet:
