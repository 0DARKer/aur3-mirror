# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: xduugu

pkgname=tuxcmd-modules-git
pkgver=20121107
pkgrel=1
pkgdesc="Modules for Tux Commander - git checkout"
arch=('i686' 'x86_64')
url="http://tuxcmd.sourceforge.net/"
license=('GPL')
depends=('tuxcmd')
makedepends=('git' 'sharutils')
optdepends=('gvfs: for gvfs module')
provides=('tuxcmd-modules')
conflicts=('tuxcmd-modules')
options=('!docs' '!emptydirs')

_gitroot="git://git.bzatek.net/tuxcmd-modules/"
_gitname="tuxcmd-modules"

build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."

	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot}
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "${srcdir}/${_gitname}-build"
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
	cd "${srcdir}/${_gitname}-build"

	# fix Makefiles
	for MAKEFILE in $(find -name Makefile); do
		sed -i 's|$(INSTALL) \(.*\) $(DESTDIR)/lib$(LIB_SUFFIX)/tuxcmd/|$(INSTALL) -D \1 $(DESTDIR)/lib/tuxcmd/\1|' ${MAKEFILE}
	done

	make -j1
}

package() {
	cd "$srcdir/$_gitname-build"

	make DESTDIR="$pkgdir/usr" install
}

