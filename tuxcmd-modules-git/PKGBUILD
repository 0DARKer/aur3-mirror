# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: xduugu

pkgname=tuxcmd-modules-git
pkgver=0.6.74.10.gbc2840a
pkgrel=1
pkgdesc="Modules for Tux Commander - git checkout"
arch=('i686' 'x86_64')
url="http://tuxcmd.sourceforge.net/"
license=('GPL')
depends=('tuxcmd')
makedepends=('git' 'sharutils')
optdepends=('gvfs: for gvfs module')
provides=('tuxcmd-modules')
conflicts=('tuxcmd-modules')
options=('!docs' '!emptydirs')
source=('git://git.bzatek.net/tuxcmd-modules/')

pkgver() {
	cd tuxcmd-modules/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd tuxcmd-modules/

	# fix Makefiles
	for MAKEFILE in $(find -name Makefile); do
		sed -i 's|$(INSTALL) \(.*\) $(DESTDIR)/lib$(LIB_SUFFIX)/tuxcmd/|$(INSTALL) -D \1 $(DESTDIR)/lib/tuxcmd/\1|' ${MAKEFILE}
	done

	make -j1
}

package() {
	cd tuxcmd-modules/

	make DESTDIR="$pkgdir/usr" install
}

sha256sums=('SKIP')
