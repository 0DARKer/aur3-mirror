# Maintainer: Jens Pranaitis <jens@jenux.homelinux.org>
# Contributor: Techlive Zheng <techlivezheng [at] gmail [dot] com>

pkgname=airgraph-ng-svn
pkgdesc="Graphing tool for the aircrack-ng suite"
pkgver=1854
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.aircrack-ng.org"
license=('GPLv2')
depends=('python2' 'graphviz')
optdepends=("psyco: JIT compiler for python")
makedepends=('subversion')
conflicts=('airgraph-ng' 'aircrack-ng-scripts')
source=()
install=airgraph-ng-svn.install

_svnmod=airgraph-ng
_svntrunk="http://trac.aircrack-ng.org/svn/trunk/scripts/airgraph-ng"

build() {
  cd ${srcdir}

  msg "Connecting to SVN server...."
  if [ -d ${_svnmod}/.svn ]; then
    msg "Updating airdrop-ng SVN..."
    svn up ${_svnmod} -r ${pkgver}
  else
    msg "Checking out airdrop-ng SVN..."
    svn co ${_svntrunk} -r ${pkgver} ${_svnmod}
  fi
  msg "SVN checkout done or server timeout."

  msg "Starting make..."

  if [ -d ${_svnmod}-build/ ]; then
    msg "Build exists, cleaning it up... "
    rm -rf ${_svnmod}-build || return 1
  fi

  msg "Creating build folder... "
  svn export ${_svnmod} ${_svnmod}-build

  grep -rl python ${_svnmod}-build | xargs sed -i 's|python|python2|g' || return 1
}

package() {
  cd ${srcdir}

  install -D -m755 ${_svnmod}-build/dump-join               ${pkgdir}/usr/bin/dump-join                      || return 1
  install -D -m755 ${_svnmod}-build/airgraph-ng             ${pkgdir}/usr/bin/airgraph-ng                    || return 1
  install -D -m644 ${_svnmod}-build/lib/lib_Airgraphviz.py  ${pkgdir}/usr/lib/airgraph-ng/lib_Airgraphviz.py || return 1
  install -D -m644 ${_svnmod}-build/man/dump-join.1         ${pkgdir}/usr/share/man/man1/dump-join.1         || return 1
  install -D -m644 ${_svnmod}-build/man/airgraph-ng.1       ${pkgdir}/usr/share/man/man1/airgraph-ng.1       || return 1
  install -D -m644 ${_svnmod}-build/README                  ${pkgdir}/usr/share/doc/airgraph-ng/README       || return 1

  #Remove build directory
  rm -rf ${_svnmod}-build || return 1
}

md5sums=()
