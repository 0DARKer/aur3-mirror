# Maintainer: Lev Lybin <lev.lybin@gmail.com>
# Contributor: Vadym Abramchuk <abramm@gmail.com>
# Contributor: John Gerritse <reaphsharc@gmail.com>

pkgname=gearman
pkgver=1.1.6
pkgrel=7
pkgdesc="Gearman provides a generic application framework to farm out work to other machines or processes that are better suited to do the work. It allows you to do work in parallel, to load balance processing, and to call functions between languages."
arch=('i686' 'x86_64')
depends=('libevent' 'boost' 'boost-libs')
url="http://gearman.org/"
license=('Simplified BSD License')
install=$pkgname.install
backup=('etc/gearmand.conf')
source=(https://launchpad.net/gearmand/1.2/${pkgver}/+download/gearmand-${pkgver}.tar.gz
        gearman.service
        gearmand.conf)
md5sums=('9358f6c97b47ac53a0fe095c2f7da546'
         '073806ec64cf655ca09d8968163d8f99'
         'b4b8d4f8703bd15fd3ca15f6f5fcbe2d')

build() {
  cd "${srcdir}/${pkgname}d-${pkgver}"
  LIBS="-lboost_system" ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/run
  make CFLAGS+=-std=gnu99 || return 1
}

package() {
  cd "${srcdir}/${pkgname}d-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 $srcdir/gearman.service $pkgdir/usr/lib/systemd/system/gearman.service
  install -Dm644 $srcdir/gearmand.conf $pkgdir/etc/gearmand.conf
}
