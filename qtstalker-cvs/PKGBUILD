# $Id:$
# Contributor: Ameysh <trader9@gmail.com>
# Maintainer: Balwinder S "bsd" Dheeman (bdheeman AT gmail.com)

pkgname=qtstalker-cvs
_realname=${pkgname%-*}
pkgver=20110430
pkgrel=1
pkgdesc="Commodity/Stock Charting and Technical Analysis Platform"
arch=('i686' 'x86_64')
url=http://qtstalker.sourceforge.net/
license=('GPL')
depends=('qt>=4.6.0' 'qwt>=5.2.1' 'ta-lib-svn>=1542')
optdepends=('perl-libwww: for updation of quote/other data')
conflicts=('qtstalker<=0.36' 'qtstalker-qt4')
replaces=('qtstalker<=0.36' 'qtstalker-qt4')
provides=(${_realname})
changelog='ChangeLog'
source=('qtstalker.desktop')

_cvsroot=":pserver:anonymous@${_realname}.cvs.sourceforge.net:/cvsroot/${_realname}"
_cvsmod=${_realname}

build() {
    msg "Connecting to ${_cvsroot#*@} CVS server..."
    cd "$srcdir"
    if [ -d ${_cvsmod}/CVS ]; then
	(cd ${_cvsmod} && cvs -z3 update -d)
    else
	cvs -z3 -d ${_cvsroot} co -f ${_cvsmod}
    fi
    msg "CVS checkout done or server timeout"

    msg "Preparing builddir..."
    rsync -av --delete --exclude 'CVS' --exclude '.cvs*' $srcdir/${_cvsmod}/ $srcdir/${_realname}-build
    cd $srcdir/${_realname}-build || return $?

    msg "Running ./configure..."
    sed 's|/usr/local|/usr|' -i ${_realname}.config
    ./configure || return $?

    msg "Starting make all install..."
    make || return $?
    make INSTALL_ROOT=$pkgdir install || return $?

    msg "Making it nice..."
    mkdir -p $pkgdir/usr/share/applications
    install -m 644 ../../${_realname}.desktop $pkgdir/usr/share/applications
    mkdir -p $pkgdir/usr/share/pixmaps
    install -m 644 pics/${_realname}.xpm $pkgdir/usr/share/pixmaps
}

# vim:set ts=4 sw=4 et:
md5sums=('9d4f011bd7e01bc49d2b6641f8323bf4')
