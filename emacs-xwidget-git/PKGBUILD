# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=emacs-xwidget-git
pkgver=25.0.50.r119844
pkgrel=1
pkgdesc="The famous editor from xwidget branch"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/"
license=('GPL3')
depends=('alsa-lib' 'gpm' 'desktop-file-utils' 'hicolor-icon-theme'
	 'webkitgtk3' 'm17n-lib' 'giflib' 
	 'libdbus' 'gobject-introspection' 'imagemagick' 'librsvg' 'gconf')
makedepends=('git' 'texinfo' 'glproto')
options=('docs' '!emptydirs' '!makeflags')
provides=('emacs')
conflicts=('emacs')
install=emacs.install
source=("git://git.savannah.gnu.org/emacs.git#branch=xwidget")
md5sums=('SKIP')
_gitname="emacs"

pkgver() {
  cd "$srcdir/$_gitname"
  printf "%s.r%s" \
    "$(grep AC_INIT configure.ac | sed -e 's/^.\+\ \([0-9]\+\.[0-9]\+\.[0-9]\+\).\+$/\1/')" \
    "$(git rev-list --count HEAD)"
}

build() {
  cd "$srcdir"/"$_gitname"

  ./autogen.sh && \
      LDFLAGS="`pkg-config --libs MagickWand `" \
	     ac_cv_lib_gif_EGifPutExtensionLast=yes ./configure \
	     --prefix=/usr \
	     --with-xwidgets \
	     --with-x-toolkit=gtk3 \
	     --with-sound \
	     --libexecdir=/usr/lib \
	     --localstatedir=/var \
	     --with-rsvg \
	     --with-gconf \
	     --with-gif
  make bootstrap 
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR=$pkgdir install
  # adding the READMEs. 
  install -Dm644 $srcdir/${_gitname}/README \
	  $pkgdir/usr/share/doc/$pkgname/README
  install -Dm644 $srcdir/${_gitname}/README.xwidget \
	  $pkgdir/usr/share/doc/$pkgname/README.xwidget
  # remove conflict with ctags package
  mv "$pkgdir"/usr/bin/{ctags,ctags.emacs}
  mv "$pkgdir"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1.gz}
  # remove conflict with the texinfo-package
  rm $pkgdir/usr/share/info/info.info.gz
  # Adjust permissions
  find "${pkgdir}"/usr/share/emacs -type d -exec chmod 755 {} \;
  find "${pkgdir}"/usr/share/emacs -exec chown root.root {} \;
  
  # fix perms on /var/games
  chgrp -R games "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games
  chmod 775 "$pkgdir"/var/games/emacs
  chmod 664 "$pkgdir"/var/games/emacs/*
}
