# Maintainer: ponsfoot <cabezon dot hashimoto at gmail dot com>

## Until AUR support split packages, this won't packed as split package
## due to dependency issue (with libkkc).

## bindings
_perl="yes"
_python2="yes"
_ruby="yes"

pkgname=marisa
#pkgname=('marisa' 'marisa-perl' 'marisa-python2' 'marisa-ruby')
pkgver=0.2.4
pkgrel=1
pkgdesc="Static and space-efficient trie data structure library"
arch=('i686' 'x86_64')
url="https://code.google.com/p/marisa-trie/"
license=('BSD' 'LGPL')
options=(!libtool)
#depends=()
#makedepends=()
source=(https://marisa-trie.googlecode.com/files/marisa-${pkgver}.tar.gz)
sha1sums=('fb0ed7d993e84dff32ec456a79bd36a00022629d')

[[ "$_perl"    == "yes" ]] && depends+=('perl')
[[ "$_python2" == "yes" ]] && depends+=('python2')
[[ "$_ruby"    == "yes" ]] && depends+=('ruby')
[[ "$_perl" == "yes" || "$_python2" == "yes" || "$_ruby" == "yes" ]] && \
  makedepends+=('swig')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
  sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool
  make

  if [[ "$_perl" == "yes" ]]; then
    cd bindings/perl
    perl Makefile.PL INC="-I${srcdir}/${pkgname}-${pkgver}/lib" LIBS="-L${srcdir}/${pkgname}-${pkgver}/lib/.libs"
    cd ~-
  fi
  if [[ "$_python2" == "yes" ]]; then
    cd bindings/python
    python2 setup.py build_ext --include-dirs="${srcdir}/${pkgname}-${pkgver}/lib" --library-dirs="${srcdir}/${pkgname}-${pkgver}/lib/.libs"
    python2 setup.py build
    cd ~-
  fi
  if [[ "$_ruby" == "yes" ]]; then
    cd bindings/ruby
    ruby extconf.rb --with-opt-include="${srcdir}/${pkgname}-${pkgver}/lib" --with-opt-lib="${srcdir}/${pkgname}-${pkgver}/lib/.libs" --vendor
    make
    cd ~-
  fi
}

package_marisa() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m 644 docs/* AUTHORS COPYING README "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/"

  if [[ "$_perl" == "yes" ]]; then
    cd bindings/perl
    make DESTDIR="$pkgdir"
    cd ~-
  fi

  if [[ "$_python2" == "yes" ]]; then
    cd bindings/python
    python2 setup.py install --root="$pkgdir"
    cd ~-
  fi

  if [[ "$_ruby" == "yes" ]]; then
    cd bindings/ruby
    _hdrdir=`pkg-config --variable=rubyhdrdir ruby-2.0`
    _arch=`pkg-config --variable=arch ruby-2.0`
    make DESTDIR="$pkgdir" install hdrdir="$_hdrdir" arch_hdrdir="${_hdrdir}/${_arch}" rubyhdrdir="$_hdrdir"
    cd ~-
  fi
}
