pkgname=yabause-qt-svn
pkgver=3100
pkgrel=2
pkgdesc="A Sega Saturn emulator with QT interface. (svn)"
arch=(i686 x86_64)
license=("GPL")
depends=(qt4 sdl freeglut mini18n-svn)
makedepends=(subversion cmake)
url="http://yabause.org"
conflicts=(yabause yabause-qt yabause-gtk-svn yabause-git yabause-gtk)
provides=(yabause-qt yabause-git yabause-gtk-svn)
source=("rwx.patch")
md5sums=('2f6491ed2cbe88d78253617cfa68b22b')

_svntrunk="https://yabause.svn.sourceforge.net/svnroot/yabause/trunk"
_svnmod="yabause"

build() {
  cd "${srcdir}"

  if [ -d "${_svnmod}/.svn" ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver $_svnmod
  fi

  msg 'SVN checkout done or server timeout'

  rm -rf "${_svnmod}-build"
  cp -r "$_svnmod" "${_svnmod}-build"
  cd "${_svnmod}-build/yabause"
  
  patch -Np1 -i "${srcdir}"/rwx.patch
  sed -i "s/\(#define SETTINGS_H\)/\1\n#include \"QtYabause.h\"/" src/qt/Settings.h
  sed -i "s/\(#define QTYABAUSE_H\)/\1\n#define PACKAGE \"${pkgname%-qt}\"/" src/qt/QtYabause.h
  sed -i "s/\(#define QTYABAUSE_H\)/\1\n#define VERSION \"${pkgver}\"/" src/qt/QtYabause.h
  LDFLAGS+=",-z,noexecstack"
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DYAB_PORTS=qt \
    -DYAB_MULTIBUILD=OFF \
    -DYAB_NETWORK=ON \
    -DYAB_OPTIMIZED_DMA=on \
    -DYAB_PERKEYNAME=ON
  make
}

package() {
  cd "${srcdir}/${_svnmod}-build/yabause"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
