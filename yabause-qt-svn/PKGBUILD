pkgname=yabause-qt-svn
pkgver=3105
pkgrel=1
pkgdesc="A Sega Saturn emulator with QT interface. (svn)"
arch=(i686 x86_64)
license=("GPL")
depends=(qt4 sdl freeglut mini18n-svn)
makedepends=(subversion cmake)
url="http://yabause.org"
conflicts=(yabause yabause-qt yabause-gtk-svn yabause-git yabause-gtk)
provides=(yabause-qt yabause-git yabause-gtk-svn)
source=("${pkgname%-*}::svn+https://yabause.svn.sourceforge.net/svnroot/yabause/trunk/yabause"
"rwx.patch")
md5sums=('SKIP' '2f6491ed2cbe88d78253617cfa68b22b')

pkgver() {
	cd "$SRCDEST/${pkgname%-*}"
	svnversion
}

build() {
	cd "${srcdir}/${pkgname%-*}"

	patch -Np1 -i "${srcdir}"/rwx.patch
	LDFLAGS+=",-z,noexecstack"
	cmake \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DYAB_PORTS=qt \
		-DYAB_MULTIBUILD=OFF \
		-DYAB_NETWORK=ON \
		-DYAB_OPTIMIZED_DMA=on \
		-DYAB_PERKEYNAME=ON
	make
}

package() {
	cd "${srcdir}/${pkgname%-*}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
