# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=yabause-qt-svn
pkgver=0.9.13.3234
pkgrel=1
pkgdesc="A Sega Saturn emulator. (SVN version)"
arch=('x86_64' 'i386')
url="http://yabause.org/"
license=('GPL')
conflicts=('yabause')
provides=('yabause')
depends=('mini18n-git' 'openal' 'sdl' 'freeglut' 'qt5-base')
makedepends=('subversion' 'cmake')
source=("svn+https://svn.code.sf.net/p/yabause/code/trunk/yabause/")
_svnmodule="yabause"
md5sums=('SKIP')

pkgver() {
  cd "${_svnmodule}"
  _major_v="$(cat CMakeLists.txt | grep -num1 YAB_VERSION_MAJOR | tr -d ')' | cut -d ' ' -f2)"
  _minor_v="$(cat CMakeLists.txt | grep -num1 YAB_VERSION_MINOR | tr -d ')' | cut -d ' ' -f2)"
  _patch_v="$(cat CMakeLists.txt | grep -num1 YAB_VERSION_PATCH | tr -d ')' | cut -d ' ' -f2)"
  echo "$(echo -n ${_major_v}.${_minor_v}.${_patch_v}).$(svnversion)"
}


build() {
  cd "${_svnmodule}"

  LDFLAGS+=",-z,noexecstack"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DYAB_PORTS=qt -DYAB_NETWORK=ON
  make
}

package() {
  cd "${_svnmodule}"
  make DESTDIR="${pkgdir}/" install
}
