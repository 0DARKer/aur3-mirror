# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>

pkgname=phc-k8
pkgver=0.4.4
pkgrel=4
pkgdesc="frequency driver for AMD CPUs with undervolting feature"
url="http://www.linux-phc.org"
arch=('any')
license=('GPL')
provides=('linux-phc')
backup=(etc/phc-k8.conf etc/modprobe.d/phc-k8.conf)
install=phc-k8.install
source=(phc-k8-$pkgver.tar.gz::$url/forum/download/file.php?id=124
        phc-k8.{conf,sh,sleep,system-sleep} linux3.patch)
sha256sums=('19744a7ae94f19c1327e3e5542522f9ab5f978e4466f1973b7518e1a83e91edd'
            '352a97272390ed428cc1ebb886ebb044e7b54c145aa7a4c0d96862ad7a7fd46a'
            '68e8978836ae990d7d287c8f7c84b1075d25f6cec1ca61689943d9f3c277cee4'
            '8619514df4b931705f713d61c1288b6e6960d578067523604b75960dcbc6e778'
            '74ae72d992d0205babd6c9428e637ca407fb26461bfc68da7f331945126b810a'
            'db7394ca3e446b8bb3c92954c4b9a203dd70cab1e2b23e415de2e074c70087bf')

build() {
	cd phc-k8_v$pkgver
	patch -Np2 -i ../linux3.patch
	sed -e 's,lib/modules/$(KERNELVERSION)/updates/kernel/arch/x86/kernel/cpu/cpufreq,usr/lib/modules/$(KERNELVERSION)/extramodules,'\
		-e 's,^\tinstall -m 644 -o root -g root phc-k8.modprobe,#\tinstall -m 644 -o root -g root phc-k8.modprobe,' -i Makefile
	sed -i 's,/sbin/modprobe phc-k8 |,/sbin/modprobe phc-k8 \&\& /usr/bin/phc-k8 set |,' phc-k8.modprobe
}

package() {
	install -Dm644 phc-k8.conf "$pkgdir/etc/phc-k8.conf"
	install -Dm755 phc-k8.sh "$pkgdir/usr/bin/phc-k8"
	install -Dm755 phc-k8.sleep "$pkgdir/usr/lib/pm-utils/sleep.d/00phc-k8"
	install -Dm755 phc-k8.system-sleep "$pkgdir/usr/lib/systemd/system-sleep/phc-k8"

	cd phc-k8_v$pkgver
	install -Dm644 phc-k8.modprobe "$pkgdir/etc/modprobe.d/phc-k8.conf"
	install -d "$pkgdir/usr/src/phc-k8/" "$pkgdir/usr/share/doc/phc-k8/"
	install -m644 Changelog README "$pkgdir/usr/share/doc/phc-k8/"
	install -m644 Makefile mperf.? phc-k8.? "$pkgdir/usr/src/phc-k8/"
}
