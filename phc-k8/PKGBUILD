# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>

pkgname=phc-k8
pkgver=0.4.4
pkgrel=3
pkgdesc="frequency driver for AMD K8 with undervolting feature"
url="http://www.linux-phc.org"
arch=('any')
license=('GPL')
provides=('linux-phc')
backup=(etc/conf.d/phc-k8 etc/modprobe.d/phc-k8.conf)
install=phc-k8.install
source=(phc-k8-$pkgver.tar.gz::$url/forum/download/file.php?id=124
	phc-k8.{conf,rc,sleep} linux3.patch)
sha256sums=('19744a7ae94f19c1327e3e5542522f9ab5f978e4466f1973b7518e1a83e91edd'
            '352a97272390ed428cc1ebb886ebb044e7b54c145aa7a4c0d96862ad7a7fd46a'
            '2d27eacf04a93ff8668df9e8deafffeb93b7e12f19911cc112d80b3b304f15c6'
            '7e05315587f54c36ec756ae53de90459eae1f41bd3f14404596d2412a9a999aa'
            'db7394ca3e446b8bb3c92954c4b9a203dd70cab1e2b23e415de2e074c70087bf')

build() {
	cd phc-k8_v$pkgver
	patch -Np2 -i ../linux3.patch
	sed -e 's,updates/kernel/arch/x86/kernel/cpu/cpufreq,extramodules,'\
		-e 's,^\tinstall -m 644 -o root -g root phc-k8.modprobe,#\tinstall -m 644 -o root -g root phc-k8.modprobe,' -i Makefile
	sed -i 's,/sbin/modprobe phc-k8 |,/sbin/modprobe phc-k8 \&\& /etc/rc.d/phc-k8 set |,' phc-k8.modprobe
}

package() {
	install -Dm644 phc-k8.conf "$pkgdir/etc/conf.d/phc-k8"
	install -Dm755 phc-k8.rc "$pkgdir/etc/rc.d/phc-k8"
	install -Dm755 phc-k8.sleep "$pkgdir/usr/lib/pm-utils/sleep.d/00phc-k8"

	cd phc-k8_v$pkgver
	install -Dm644 phc-k8.modprobe "$pkgdir/etc/modprobe.d/phc-k8.conf"
	install -d "$pkgdir/usr/src/phc-k8/" "$pkgdir/usr/share/doc/phc-k8/"
	install -m644 Changelog README "$pkgdir/usr/share/doc/phc-k8/"
	install -m644 Makefile mperf.? phc-k8.? "$pkgdir/usr/src/phc-k8/"
}
