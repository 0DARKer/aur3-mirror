# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>

pkgname=phc-k8
pkgver=0.4.3
pkgrel=2
pkgdesc="frequency driver for AMD K8 with undervolting feature"
url="http://www.linux-phc.org"
arch=('any')
license=('GPL')
depends=('kernel26' 'kernel26-headers')
provides=('linux-phc')
backup=(etc/conf.d/phc-k8 etc/modprobe.d/phc-k8.conf)
install=phc-k8.install
source=(phc-k8-$pkgver.tar.gz::$url/forum/download/file.php?id=111
phc-k8.{conf,rc,sleep}
2.6.38.patch)
md5sums=('1e0dc8218ec3850369db8d824d88a551'
         '22d603c31ad2e14154de5bf3bd79ce77'
         '829967d57521d87d70e4ae89c22324bd'
         '5c646e9106f8534006915e550b89be01'
         '6af6288200c69c0db119d9680baa0c45')

build() {
	cd phc-k8_v$pkgver
	patch phc-k8.c ../2.6.38.patch
	sed -i 's|^\tinstall -m 644 -o root -g root phc-k8.modprobe|#\tinstall -m 644 -o root -g root phc-k8.modprobe|' Makefile
	sed -i 's,/sbin/modprobe phc-k8 |,/sbin/modprobe phc-k8 \&\& /etc/rc.d/phc-k8 set |,' phc-k8.modprobe
}

package() {
	install -Dm644 phc-k8.conf "$pkgdir/etc/conf.d/phc-k8"
	install -Dm755 phc-k8.rc "$pkgdir/etc/rc.d/phc-k8"
	install -Dm755 phc-k8.sleep "$pkgdir/usr/lib/pm-utils/sleep.d/00phc-k8"
	install -d "$pkgdir/usr/src/phc-k8" "$pkgdir/usr/share/doc/phc-k8/"

	cd phc-k8_v$pkgver
	install -Dm644 phc-k8.modprobe "$pkgdir/etc/modprobe.d/phc-k8.conf"
	install -m644 {Changelog,README} "$pkgdir/usr/share/doc/phc-k8/"
	cp {Makefile,mperf.?,phc-k8.?} "$pkgdir/usr/src/phc-k8/"
}
