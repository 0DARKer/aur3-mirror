# Contributor: fs4000 <matthias_dienstbier[at]yahoo[dot]de>

pkgname=('phc-k8-misc' 'phc-k8-kernel26')
pkgbase='phc-k8'
pkgver=0.4.3
pkgrel=1
pkgdesc="frequency driver for AMD K8 with undervolting feature"
arch=('i686' 'x86_64')
url="http://www.linux-phc.org"
license=('GPL')
provides=('linux-phc')
options=(!makeflags)
install=$pkgbase.install
source=($pkgbase-$pkgver.tar.gz::$url/forum/download/file.php?id=107)
md5sums=('1e0dc8218ec3850369db8d824d88a551')

build() {
	cd ${pkgbase}_v$pkgver
	sed -i 's|`uname -r`|$(KERNELVERSION)|' Makefile
	sed -i 's|^KERNELVERSION =|#KERNELVERSION =|' Makefile
	sed -i 's|^\tinstall -m 644 -o root -g root phc-k8.modprobe|#\tinstall -m 644 -o root -g root phc-k8.modprobe|' Makefile
	sed -i 's|\([^=]\) /lib/modules/|\1 ${DESTDIR}/lib/modules/|' Makefile
	sed -i 's|^\tdepmod $(KERNELVERSION) -a|#\tdepmod $(KERNELVERSION) -a|' Makefile
}

package_phc-k8-misc() {
	pkgdesc="misc files for $pkgbase"
	arch=('any')
	unset provides install
	backup=(etc/modprobe.d/$pkgbase.conf)
	cd ${pkgbase}_v$pkgver
	install -Dm644 $pkgbase.modprobe "$pkgdir"/etc/modprobe.d/$pkgbase.conf
	install -d "$pkgdir/usr/share/doc/$pkgbase/"
	install -m644 {Changelog,README} "$pkgdir/usr/share/doc/$pkgbase/"
}

package_phc-k8-kernel26() {
	# name of the kernel package
	local kernel='kernel26'
	# minor version of kernel
	local kernver=36
	# kernel release (uname -r)
	local kernrel=2.6.$kernver-ARCH

	depends=($kernel'>=2.6.'$kernver $kernel'<2.6.'$((kernver+1)) 'phc-k8-misc')
	makedepends=($kernel'-headers>=2.6.'$kernver $kernel'-headers<2.6.'$((kernver+1)))

	sed "s/depmod.*/depmod $KERNELVERSION/" -i ../$install
	cd ${pkgbase}_v$pkgver
	export KERNELVERSION=$kernrel
	make
	make DESTDIR="$pkgdir" install
}

package_phc-k8-kernel26-lts() {
	local kernel='kernel26-lts'
	local kernver=32
	local kernrel=2.6.$kernver-lts

	depends=($kernel'>=2.6.'$kernver $kernel'<2.6.'$((kernver+1)) 'phc-k8-misc')
	makedepends=($kernel'-headers>=2.6.'$kernver $kernel'-headers<2.6.'$((kernver+1)))

	sed "s/depmod.*/depmod $KERNELVERSION/" -i ../$install
	cd ${pkgbase}_v$pkgver
	export KERNELVERSION=$kernrel
	make
	make DESTDIR="$pkgdir" install
}
