# Maintainer: Brian Petersen <me@brianrpetersen.com>
# Based on the PKGBUILD for libmcrypt

pkgname=mcrypt
pkgver=2.6.8
pkgrel=1
pkgdesc="A replacement for the old unix crypt(1)."
url="http://mcrypt.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libmcrypt' 'mhash' 'zlib')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/mcrypt/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5145aa844e54cca89ddab6fb7dd9e5952811d8d787c4f4bf27eb261e6c182098')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
