# Maintainer: Romain Schmitz <slopjong .at. slopjong .dot. de>

pkgbase=python-sphinx_rtd_theme
pkgname=('python-sphinx_rtd_theme' 'python2-sphinx_rtd_theme')
_pkgname=('sphinx_rtd_theme')
pkgdesc='Python Sphinx Read The Docs Theme'
pkgver=0.1.6
pkgrel=1
arch=('any')
url='https://github.com/snide/sphinx_rtd_theme'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/s/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")

md5sums=('8df56a300c8c69f17e1ffbee194b2a26')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  # remove old build directory 
  rm -rf build

  # python builds
  python  setup.py build --build-lib=build/python
  python2 setup.py build --build-lib=build/python2
}

package_python-sphinx_rtd_theme() {
  depends=('python')

  cd "$srcdir/${_pkgname}-${pkgver}"
  python setup.py build --build-lib=build/python \
    install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-sphinx_rtd_theme() {
  depends=('python2')

  cd "$srcdir/${_pkgname}-${pkgver}"
  python2 setup.py build --build-lib=build/python2 \
    install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 ft=sh et:
