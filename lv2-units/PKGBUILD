# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=lv2-units
pkgver=5.2
pkgrel=1
pkgdesc="Lv2 Units extension"
arch=(any)
url="http://lv2plug.in/"
license=('custom:ISC')
groups=('lv2extensions')
depends=('lv2core')
source=("$url/spec/$pkgname-$pkgver.tar.bz2")
md5sums=('5636ffaae03d9be415295e204d6b0860')

build() {
  return 0

  # nothing to do here
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -d "$pkgdir/usr/lib/lv2/${pkgname/lv2-}.lv2"
  install -Dm644 *.ttl "$pkgdir/usr/lib/lv2/${pkgname/lv2-}.lv2"

  # generate headers
  _ns=`grep "^<http" manifest.ttl \
      | sed "s|<http://\(.*\)>|\1|"`
  _name="${_ns/*\/}"
  _path="$pkgdir/usr/include/lv2/${_ns%/*}"
  [ -d "$_path" ] || install -d "$_path"
  ln -s "/usr/lib/lv2/$_name.lv2" "$_path/$_name"

  # license
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n 15 ${pkgname/lv2-}.ttl > \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
