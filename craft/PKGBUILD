# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=craft
pkgver=3.5
pkgrel=1
pkgdesc="Strategy game similar to Warcraft and Dune II"
arch=('i686' 'x86_64')
url="http://happypenguin.org/show?Craft"
license=('custom')
depends=('libx11' 'gcc-libs')
source=("http://www.ibiblio.org/pub/linux/games/strategy/craftcc35.tar.Z"
        "COPYING"
        "${pkgname}.patch"
        "${pkgname}.sh")
md5sums=('e55d412d330f28798d10d13ab28596c0'
         '27b587f927a4d2159398148736389181'
         '28a9404c791d0efa63a594500118937c'
         '72c195d88008549f85000f2a5836ba87')

build() {
  cd "${srcdir}"

  # A few tricky substitutions
  mv getline.h xgetline.h
  sed -i 's/getline/xgetline/g' *.h *.hc
  sed -i 's/xgetline_/getline_/g' *.h *.hc

  # Fix game speed on modern computers
  sed -i 's/min_cycle_time  50/min_cycle_time  2000/g' craft.hc

  # Compile
  patch -p0 -i "${pkgname}.patch"
  ./install
}

package() {
  cd "${srcdir}"

  # Install data files
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -rf buttons/ "${pkgdir}/opt/${pkgname}/"
  cp -rf hcraft/ "${pkgdir}/opt/${pkgname}/"

  # Set file permissions
  find "${pkgdir}/opt/${pkgname}" -type d -exec chmod 755 '{}' \;
  find "${pkgdir}/opt/${pkgname}" -type f -exec chmod 644 '{}' \;
  chmod 755 "${pkgdir}/opt/${pkgname}/hcraft/edit/cmedit"

  # Install binary
  install -Dm755 "${pkgname}" "${pkgdir}/opt/${pkgname}/${pkgname}"
  install -Dm644 ".windefaults.params" "${pkgdir}/opt/${pkgname}/.windefaults.params"

  # Install launcher and license file  
  install -Dm755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 "../COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  # Install manuals
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  cp -rf html/* "${pkgdir}/usr/share/doc/${pkgname}/"
  find "${pkgdir}/usr/share/doc/${pkgname}" -type f -exec chmod 644 '{}' \;
}
