# Maintainer: TryA <tryagainprod {at} gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=raine
pkgver=0.51.9
pkgrel=1
pkgdesc="A multiple arcade emulator focused on 680x0 machines"
url="http://rainemu.swishparty.co.uk"
license=('unknown')
arch=('i686' 'x86_64')
if [ "${CARCH}" == "x86_64" ]; then
  depends=('lib32-sdl_ttf' 'lib32-sdl_image' 'lib32-muparser')
  makedepends=('nasm' 'gcc-multilib')
else
  depends=('sdl_ttf' 'sdl_image' 'muparser')
  makedepends=('nasm')
fi
optdepends=('raine-emudx: improved graphic and sound files for some classic games')
source=("http://trya.alwaysdata.net/linux/raines-${pkgver}.tar.bz2")
md5sums=('43e08bacbc5eea481d1304fb519dab37')

build() {
  cd ${srcdir}/raine-${pkgver}

  # fix system directories in Makefile for the install target
  sed -e 's/\$(prefix)\/games/\$(prefix)\/bin/' \
      -e 's|/usr/bin/install|/bin/install|' \
      -i makefile

  # we don't build NeoRaine
  sed -e 's|NEO=1|# NEO=1|' -i makefile

  # -O3 optimizations cause segfaults, use -O2 instead
  sed -e 's|CFLAGS = -O3|CFLAGS = -O2|' -i makefile

  make
}

package() {
  cd ${srcdir}/raine-${pkgver}
  make DESTDIR=${pkgdir} install
}
