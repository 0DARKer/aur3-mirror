# Maintainer: trya <tryagainprod@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=raine
pkgver=0.61.1
_gitver=70075ab
pkgrel=1
pkgdesc="A multiple arcade emulator focused on 680x0 machines"
url="http://rainemu.swishparty.co.uk"
license=('unknown')
arch=('i686' 'x86_64')
if [ "${CARCH}" == "x86_64" ]; then
  depends=('lib32-sdl_ttf' 'lib32-sdl_image' 'lib32-muparser')
  makedepends=('nasm' 'gcc-multilib')
else
  depends=('sdl_ttf' 'sdl_image' 'muparser')
  makedepends=('nasm')
fi
optdepends=('raine-emudx: improved graphic and sound files for some classic games')
source=("raine-$pkgver.tar.gz"::"http://rainemu.swishparty.co.uk/cgi-bin/gitweb.cgi?p=raine;a=snapshot;h=70075abd4c6a2217d1dfe96b267056eb2a677e9e;sf=tgz"
        "http://rainemu.swishparty.co.uk/html/archive/debian/dists/unstable/main/binary-amd64/raine_${pkgver}_amd64.deb")
md5sums=('f75bf83cf9531cac7bbdf2d82128f968'
         '1e14c287f20aba4298458593db1063bf')

build() {
  cd "$srcdir"
  mkdir -p raine-bin
  bsdtar xf data.tar.gz -C raine-bin

  cd "raine-$_gitver"

  # copy bitmaps and fonts from raine's deb package
  cp -r "$srcdir/raine-bin/usr/share/games/raine/bitmaps" .
  cp -r "$srcdir/raine-bin/usr/share/games/raine/fonts" .

  # fix system directories in Makefile for the install target
  sed -e 's|\$(prefix)\/games|\$(prefix)\/bin|' \
      -e 's|INSTALL = @install|INSTALL = install|' \
      -i makefile

  # -O3 optimizations cause segfaults, use -O2 instead
  sed -e 's|CFLAGS = -O3|CFLAGS = -O2|' -i makefile

  make
}

package() {
  cd "$srcdir/raine-$_gitver"
  make DESTDIR="$pkgdir" install
}
