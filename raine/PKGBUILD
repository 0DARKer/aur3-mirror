# Maintainer: trya <tryagainprod@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=raine
pkgver=0.62.6
_gitver=b5e3b10
pkgrel=1
pkgdesc="A multiple arcade emulator focused on 680x0 machines"
url="http://rainemu.swishparty.co.uk"
license=('unknown')
arch=('i686' 'x86_64')
if [ "${CARCH}" == "x86_64" ]; then
  depends=('lib32-sdl_ttf' 'lib32-sdl_image' 'lib32-sdl_sound' 'lib32-muparser')
  makedepends=('nasm' 'gcc-multilib')
else
  depends=('sdl_ttf' 'sdl_image' 'sdl_sound' 'muparser')
  makedepends=('nasm')
fi
optdepends=('raine-emudx: improved graphic and sound files for some classic games')
provides=('neoraine')
conflicts=('neoraine')
replaces=('neoraine')
source=("raine-$pkgver.tar.gz"::"http://rainemu.swishparty.co.uk/cgi-bin/gitweb.cgi?p=raine;a=snapshot;h=b5e3b105e5b6fd6520e282173c57e5d44072bc20;sf=tgz"
        "http://rainemu.swishparty.co.uk/html/archive/debian/dists/unstable/main/binary-i386/raine_${pkgver}_i386.deb")
md5sums=('1c752e271adb878a59b0d91dc4022771'
         '5972f43a0e368f809dd3539a42975897')

build() {
  cd "$srcdir"
  mkdir -p raine-bin
  bsdtar xf data.tar.xz -C raine-bin

  cd "raine-$_gitver"

  # copy bitmaps and fonts from raine's deb package
  cp -r "$srcdir/raine-bin/usr/share/games/raine/bitmaps" .
  cp -r "$srcdir/raine-bin/usr/share/games/raine/fonts" .

  # fix system directories in Makefile for the install target
  sed -e 's|\$(prefix)\/games|\$(prefix)\/bin|' \
      -e 's|INSTALL = @install|INSTALL = install|' \
      -i makefile

  # -O3 optimizations cause segfaults, use -O2 instead
  sed -e 's|CFLAGS = -O3|CFLAGS = -O2|' -i makefile

  make
}

package() {
  cd "$srcdir/raine-$_gitver"
  make DESTDIR="$pkgdir" install
}
