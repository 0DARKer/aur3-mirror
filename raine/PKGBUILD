# Maintainer: trya <tryagainprod@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>
# Contributor: carstene1ns <arch carsten-teibes de>

pkgname=raine
pkgver=0.63.10
_gitver=a0375f2
pkgrel=2
pkgdesc="A multiple arcade emulator focused on 680x0 machines"
url="http://rainemu.swishparty.co.uk"
license=('unknown')
arch=('i686' 'x86_64')
if [ "$CARCH" == "x86_64" ]; then
  depends=('lib32-sdl_ttf' 'lib32-sdl_image' 'lib32-sdl_sound' 'lib32-muparser' 'lib32-glu' 'lib32-mesa')
  makedepends=('nasm' 'gcc-multilib')
else
  depends=('sdl_ttf' 'sdl_image' 'sdl_sound' 'muparser' 'glu' 'mesa')
  makedepends=('nasm')
fi
optdepends=('raine-emudx: improved graphic and sound files for some classic games'
            'arcade-history-dat: database with various information about the loaded rom')
provides=('neoraine')
conflicts=('neoraine')
replaces=('neoraine')
source=("raine-$pkgver.tar.gz"::"http://rainemu.swishparty.co.uk/cgi-bin/gitweb.cgi?p=raine;a=snapshot;h=a0375f23a561af2cd48d7216c9ab85e84d7f02c7;sf=tgz"
        "http://rainemu.swishparty.co.uk/html/archive/debian/dists/unstable/main/binary-i386/raine_${pkgver}_i386.deb")
md5sums=('84d5981f928cd073f3482bba97d6f8e4'
         'af905c9993ae99ad0e3f884b3ef99e17')

prepare() {
  cd "$srcdir"
  mkdir -p raine-bin
  bsdtar xf data.tar.xz -C raine-bin

  cd "raine-$_gitver"

  # copy bitmaps and fonts from raine's deb package
  cp -r "$srcdir/raine-bin/usr/share/games/raine/bitmaps" .
  cp -r "$srcdir/raine-bin/usr/share/games/raine/fonts" .

  # adapt folder structure to arch standards in Makefile for the install target
  sed -e 's|$(prefix)/games|\$(prefix)/bin|' \
      -e 's|$(prefix)/share/games|\$(prefix)/share|' \
      -e 's|INSTALL = @install|INSTALL = install|' \
      -i makefile

  # adapt folder structure to arch standards in source files
  sed 's|share/games/raine|share/raine|' -i source/sdl/dialogs/about.cpp source/raine.c

  # -O3 optimizations cause segfaults, use -O2 instead
  sed -e 's|CFLAGS = -O3|CFLAGS = -O2|' -i makefile

  # link to the dynamic library of SDL_sound
  sed -e 's|LIBS += /usr/local/lib/libSDL_sound.a -lFLAC -logg -lspeex -lmikmod -lvorbisfile -lmodplug|LIBS += -lSDL_sound|' -i makefile
}

build() {
  cd "$srcdir/raine-$_gitver"
  make
}

package() {
  cd "$srcdir/raine-$_gitver"
  make DESTDIR="$pkgdir" install
  # symlink neoraine, both projects have been merged
  ln -s raine "$pkgdir"/usr/bin/neoraine
}
