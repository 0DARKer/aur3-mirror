# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Marco Maso <demind@gmail.com>
# Contributor: Ian Taylor <ian@lorf.org>

_name=freeglut
pkgname=lib32-$_name
pkgver=2.8.0
pkgrel=3
pkgdesc="Provides functionality for small OpenGL programs. Multilib."
arch=('x86_64')
url="http://freeglut.sourceforge.net/"
license=('custom:MIT')
depends=("$_name" 'lib32-mesa' 'lib32-libxi' 'lib32-libxrandr' 'lib32-libxxf86vm')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://downloads.sourceforge.net/freeglut/$_name-$pkgver.tar.gz")
md5sums=('5db8651af306bc403fbfd36934a20e1d')

build() {
  cd "$srcdir/$_name-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  # mesa 9.0.1 fix
  sed -i '/PFNGLSHADERSOURCEPROC/s/ \*\*string/\* const \*string/' \
    progs/demos/smooth_opengl3/smooth_opengl3.c

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_name-$pkgver"

  make DESTDIR="$pkgdir/" install

  # rm unneeded header files
  rm -rf "$pkgdir/usr/include"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
