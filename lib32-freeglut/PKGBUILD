# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Marco Maso <demind@gmail.com>
# Contributor: Ian Taylor <ian@lorf.org>

_pkgname32=freeglut
pkgname=lib32-$_pkgname32
pkgver=2.6.0
pkgrel=2
pkgdesc="Provides functionality for small OpenGL programs. Multilib."
arch=('x86_64')
url="http://freeglut.sourceforge.net/"
license=('MIT')
depends=("$_pkgname32" 'lib32-mesa' 'lib32-libxi')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://downloads.sourceforge.net/freeglut/${_pkgname32}-${pkgver}.tar.gz" \
        "2.6.0-GFX_radeon.patch")
md5sums=('39f0f2de89f399529d2b981188082218'
         '6d0a018fe4f0bc9ace2b244ca59514d3')

build() {

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${srcdir}/${_pkgname32}-${pkgver}"
# fixes an issue with the radeon[hd] ATI opensource drivers
# ref: http://bugs.gentoo.org/show_bug.cgi?id=295163
  patch -p0 -i "${srcdir}/2.6.0-GFX_radeon.patch"

  ./configure \
	--prefix=/usr \
	--libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgname32}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Removing unneeded header files
  rm -rf "$pkgdir/usr/include"
}
