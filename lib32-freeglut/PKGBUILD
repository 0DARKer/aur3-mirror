# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Marco Maso <demind@gmail.com>
# Contributor: Ian Taylor <ian@lorf.org>

pkgname=lib32-freeglut
pkgver=2.8.1
pkgrel=1
pkgdesc="Provides functionality for small OpenGL programs. Multilib."
arch=('x86_64')
url="http://freeglut.sourceforge.net/"
license=('custom:MIT')
depends=('lib32-libxi' 'lib32-libxrandr' 'lib32-libgl' 'lib32-libxxf86vm')
makedepends=('gcc-multilib' 'lib32-mesa' 'lib32-glu')
source=("http://downloads.sourceforge.net/freeglut/freeglut-$pkgver.tar.gz")
md5sums=('918ffbddcffbac83c218bc52355b6d5a')

prepare() {
  cd freeglut-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  # smooth_opengl3 demo doesn't build with recent mesa, Gentoo bug #428382
  sed -i "s/smooth_opengl3 //" progs/demos/Makefile.*
}

build() {
  cd freeglut-$pkgver
  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package() {
  cd freeglut-$pkgver/src
  make DESTDIR="$pkgdir/" install

  # license
  install -Dm644 ../COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
