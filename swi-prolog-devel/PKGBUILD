# Maintainer: Nikolaos Bezirgiannis <bezeria@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog-devel
pkgver=7.1.20
pkgrel=1
pkgdesc='Prolog environment [development version]'
arch=('x86_64' 'i686')
url='http://www.swi-prolog.org/'
license=('GPL' 'LGPL')
depends=('gmp' 'readline' 'openssl' 'libarchive')
makedepends=('libxft' 'libjpeg' 'unixodbc' 'libxpm' 'libxinerama')
optdepends=('unixodbc: for using the odbc4pl library'
            'libjpeg: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxft: for using the pl2xpce library')
#options=('!makeflags')
source=("http://swi-prolog.org/download/devel/src/pl-7.1.20.tar.gz")
sha256sums=('03198c5c2658a665e203799da2834d6d3395c5272f2ed2d3ef4add7bd995ee9f')
conflicts=('swi-prolog')
provides=('swi-prolog')

build() {
  cd "pl-$pkgver"

  ./configure --prefix=/usr --with-world
  cd src
  ./configure --enable-readline --prefix=/usr
  cd ..
  make
}

check() {
  make -C "pl-$pkgver" check || true
}

package() {
  make -C "pl-$pkgver" DESTDIR="$pkgdir" install

  # Fix for FS#20873
  chmod +x "$pkgdir/usr/lib/swipl-$pkgver/library/dialect/sicstus/swipl-lfr.pl"
}

