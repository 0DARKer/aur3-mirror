# Maintainer: speps <speps at aur dot archlinux dot org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=buzztrax
pkgver=0.7.0
pkgrel=1
pkgdesc="A modular, free, open source music studio that is conceptually based on Buzz (former Buzztard)"
arch=('i686' 'x86_64')
url="http://www.buzztrax.org"
license=('LGPL')
depends=('gst-buzztrax>=0.7.0' 'libgsf' 'libgnomecanvas' 'gstreamer0.10-base' 'gconf'
         'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'gtk-doc')
provides=("buzztard=$pkgver")
conflicts=('buzztard')
replaces=('buzztard')
options=('!libtool' '!emptydirs')
#options+=('!strip')
install="$pkgname.install"
#source=("http://downloads.sourceforge.net/sourceforge/buzztard/$pkgname-$pkgver.tar.gz"
source=("https://github.com/Buzztrax/buzztrax/archive/RELEASE_0_7.tar.gz"
        "https://github.com/Buzztrax/buzztrax/commit/b6236502c764025294a5644cdedcec5572290e38.patch"
        "$pkgname-null-ptr.patch")
md5sums=('d4dd5fb5e43ba096c98f16b982a70f76'
         '66d18cb95bc9d007fea62dd35443da3d'
         '359799495a7bbe23510b7c17bddd85bd')

build() {
  cd "$srcdir/$pkgname-RELEASE_0_7"

  # null pointer patch
  patch -p1 -i ../${source[1]##*/}
  patch -p1 -i ../${source[2]}

  # deprecated glib
  sed -i '/g_type_init ();/d' `grep -rl 'g_type_init ();'`

  ./autogen.sh 
  ./configure --prefix=/usr \
              --enable-deprecated=no \
              --enable-static=no \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
#              --enable-debug \
  make
}

package() {
  cd "$srcdir/$pkgname-RELEASE_0_7"
  make DESTDIR="$pkgdir/" install

  # Deleting unneeded files
  rm -f "$pkgdir/usr/share/applications/mimeinfo.cache"
  find "$pkgdir/usr/share/mime" -maxdepth 1 -type f -exec rm {} \;
}
