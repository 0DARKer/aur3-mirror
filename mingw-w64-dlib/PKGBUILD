# Maintainer: pingplug <pingplug@foxmail.com>

pkgname=mingw-w64-dlib
_pkgname=dlib
pkgver=18.10
pkgrel=1
pkgdesc="Dlib is a general purpose cross-platform C++ library designed using contract programming and modern C++ techniques. (mingw-w64)"
arch=('any')
url="http://www.dlib.net/"
license=('Boost Software License')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc' 'mingw-w64-cmake')
optdepends=('mingw-w64-fftw: for FFTW support'
            'mingw-w64-blas: for BLAS and LAPACK support'
            'mingw-w64-libjpeg-turbo: for JPEG support'
            'mingw-w64-libpng: for PNG support'
            'mingw-w64-sqlite3: for sqlite support')
options=('!buildflags' '!strip' 'staticlibs')
source=(http://jaist.dl.sourceforge.net/project/dclib/$_pkgname/v$pkgver/$_pkgname-$pkgver.tar.bz2
        dlib-18.10.patch)
md5sums=('ede77c5a1fb8c249b13987728424be74'
         '0d56a0d8e17d3b09f95e913898c72aa6')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
    cd $srcdir

    # fix the windres flag
    # fix can't find blas and lapack
    # fix a linking error while build a static binary with libpng
    patch -Np0 -i dlib-18.10.patch
}

build() {
    export CPPFLAGS=""
    export CFLAGS="-O2 -pipe -static"
    export CXXFLAGS="-O2 -pipe -static"
    export LDFLAGS="-Wl,-O1"

    for _arch in ${_architectures}; do
        mkdir -p "${srcdir}/${_pkgname}-build-${_arch}"
        cd "${srcdir}/${_pkgname}-build-${_arch}"
        ${_arch}-cmake "../${_pkgname}-$pkgver/${_pkgname}"
        make
    done
}

package() {
    for _arch in ${_architectures}; do
        cd "$srcdir/${_pkgname}-$pkgver"
        install -Dm755 -d "$pkgdir/usr/${_arch}/include"
	    cp -a ${_pkgname} "$pkgdir/usr/${_arch}/include"
        cd "${srcdir}/${_pkgname}-build-${_arch}"
        install -Dm755 -d "$pkgdir/usr/${_arch}/lib"
        install -Dm644 libdlib.a "$pkgdir/usr/${_arch}/lib"
    done
}
