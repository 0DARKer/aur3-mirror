# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=mkinitcpio-uname
pkgver=0.1.0.r0.g46c6cef
pkgrel=1
pkgdesc="Add uname output to early boot process"
arch=('any')
url="http://www.eworm.de/"
makedepends=('git')
license=('GPL')
install=mkinitcpio-uname.install
source=('git://github.com/eworm-de/mkinitcpio-uname.git')
 
pkgver() {
	cd mkinitcpio-uname/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

package() {
	cd mkinitcpio-uname/

	install -D -m644 hook/uname ${pkgdir}/usr/lib/initcpio/hooks/uname
	install -D -m644 install/uname ${pkgdir}/usr/lib/initcpio/install/uname
}

sha256sums=('SKIP')
