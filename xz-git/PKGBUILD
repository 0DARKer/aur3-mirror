# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Pierre Schmitz <pierre@archlinux.de> ([core] package)
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz-git
pkgver=5.1.2alpha.22.g5ea900c
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files - git checkout'
arch=('i686' 'x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('sh')
makedepends=('git')
provides=('lzma' 'lzma-utils' 'xz-utils' 'xz=5.1.2')
replaces=('lzma' 'lzma-utils' 'xz-utils')
conflicts=('lzma' 'lzma-utils' 'xz-utils' 'xz')
options=('!libtool')
source=('git+http://git.tukaani.org/xz.git')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

pkgver() {
	cd xz/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd xz/
  
	./autogen.sh
	./configure --prefix=/usr \
		--disable-rpath \
		--enable-werror
	make
}

check() {
	cd xz/

	make check
}

package() {
	cd xz/

	make DESTDIR=${pkgdir} install
	install -d -m755 ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/doc/xz/COPYING ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/licenses/common/GPL2/license.txt ${pkgdir}/usr/share/doc/xz/COPYING.GPLv2
}

sha256sums=('SKIP')
