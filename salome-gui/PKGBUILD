# Maintainer: Michele Mocciola <mickele>

pkgname=salome-gui
pkgver=7.4.0
pkgrel=3
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation. GUI Module."
url="http://www.salome-platform.org"
depends=('salome-kernel' 'python2' 'qt4' 'python2-pyqt4' 'boost-libs' 'opencascade>=6.7.1' 'qwt' 'omniorb' 'omniorbpy' 'hdf5' 'paraview-salome' 'libxml2' 'cppunit')
makedepends=('doxygen' 'swig' 'boost' 'optipng' 'python2-sphinx')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
source=("http://files.salome-platform.org/Salome/Salome${pkgver}/src${pkgver}.tar.gz" "${pkgname}.profile" "salome.desktop" "salome.png" "broken-png-list" "salome")

_source=GUI_SRC_${pkgver}
_installdir=/opt/salome/gui
_paraviewrootdir=/usr
_paraviewver=4.1

prepare(){
  cd "$srcdir/$_source"

  # error "sip: Q_PID is undefined"
  sed -e 's|from PyQt4 import pyqtconfig;|from PyQt4 import QtCore;|' \
      -i adm_local/cmake_files/FindPyQt4.cmake
  sed -e "s|pyqtconfig.Configuration().pyqt_sip_flags|QtCore.PYQT_CONFIGURATION['sip_flags']|" \
      -i adm_local/cmake_files/FindPyQt4.cmake

  # DESTDIR
  sed -e 's|\\"\${CMAKE_INSTALL_PREFIX}/\\\${INSTALL_TS_DIR}\\"|\\"\\\$ENV{DESTDIR}\${CMAKE_INSTALL_PREFIX}/\\\${INSTALL_TS_DIR}\\"|' -i adm_local/cmake_files/UseQt4Ext.cmake

  # python -> python2
  for _FILE in `grep -Rl "/usr/bin/env python" * `
  do
	sed -e "s|/usr/bin/env python|/usr/bin/env python2|" -i ${_FILE}
  done
  for _FILE in `grep -Rl "python -c" * `
  do
	sed -e "s|python -c|python2 -c|" -i ${_FILE}
  done

  # pyuic4 -> python2-pyuic4
  sed -e 's|pyuic4|python2-pyuic4|' \
      -i adm_local/cmake_files/FindPyQt4.cmake
      
  # now "-py2" is default for pyrcc4
  #  sed -e 's|${PYQT_PYRCC_EXECUTABLE}|${PYQT_PYRCC_EXECUTABLE}" -py2"|' \
  #      -i adm_local/cmake_files/FindPyQt4.cmake
}

build() {
  source /etc/salome/profile.d/salome-kernel.sh

  cd "$srcdir/$_source"

  # flags to use python2 instead of python which is 3.x.x on archlinux
  local cmake_system_python_flags="-DPYTHON_EXECUTABLE=/usr/bin/python2"

  cmake . \
     -DCMAKE_INSTALL_PREFIX=${_installdir} \
     -DOPENGL_ROOT_DIR=/usr \
     -DVTK_ROOT_DIR=/usr \
     -DQWT_ROOT_DIR=/usr \
     -DCAS_ROOT_DIR=/opt/opencascade \
     -DVTK_DIR="${_paraviewrootdir}/lib/cmake/paraview-${_paraviewver}" \
     -DSPHINX_EXECUTABLE=/usr/bin/sphinx-build2 \
     ${cmake_system_python_flags}
  make
}

package() {
  cd "$srcdir/$_source"

  make DESTDIR="$pkgdir" install

  # install profile
  install -D -m755 "${srcdir}/${pkgname}.profile" "${pkgdir}/etc/salome/profile.d/${pkgname}.sh"

  # install menu entry
  install -D -m 644 "${srcdir}/salome.png" "${pkgdir}/usr/share/pixmaps/salome.png"
  desktop-file-install --dir="${pkgdir}/usr/share/applications" "${srcdir}/salome.desktop"

  cd "${pkgdir}"
  cat "${srcdir}/broken-png-list" | xargs -n 1 -P 3 optipng -quiet -force -fix

  install -D -m755 "${srcdir}/salome" \
                   "${pkgdir}/usr/bin/salome"

  sed -e "s|GEOM,SMESH,HEXABLOCK,MED,YACS,PARAVIS|GEOM,SMESH,HEXABLOCK,MED,YACS,PARAVIS,EFICAS,ASTER|" -i ${pkgdir}${_installdir}/share/salome/resources/gui/SalomeApp.xml
}
md5sums=('18055915a14e0921563072f830614e7f'
         'ec9fa9d689892de5a47b34d5d2ba484c'
         'a102063b779e332914ef0b73843e928a'
         'faab453234c365a3243944e3b9af131b'
         '24f95b42919e4fc47f6e7516025b2f74'
         'c47b2bb9e51120089bd3169c2298abf3')
