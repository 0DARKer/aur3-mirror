# Maintainer: Michele Mocciola <mickele>

pkgname=salome-gui
pkgver=5.1.4
pkgrel=1
pkgdesc="SALOME provides a generic platform for Pre and Post-Processing for numerical simulation. GUI Module."
url="http://www.salome-platform.org"
depends=('salome-kernel' 'python' 'qt' 'sip' 'pyqt' 'boost' 'opencascade' 'qwt' 'omniorb' 'omniorbpy' 'omninotify' 'swig' 'hdf5' 'vtk>=5.6' 'graphviz' 'libxml2' 'cppunit' 'flex' 'bison' 'sed')
makedepends=('gcc' 'automake' 'autoconf' 'libtool' 'make' 'doxygen' 'docutils' 'desktop-file-utils')
arch=('i686' 'x86_64')
conflicts=()
provides=()
license=('LGPL')
source=("http://files.opencascade.com/Salome/Salome${pkgver}/src${pkgver}.tar.gz" "${pkgname}.profile" "salome.desktop" "salome.png" "salome")
options=('libtool')

_source=src${pkgver}/GUI_SRC_${pkgver}
_installdir=/opt/salome/gui
_pythonver=2.6
_vtkver=5.6

build() {
  source /etc/profile.d/salome-kernel.sh || return 1

  cd ${srcdir}/${_source} || return 1

  # Builds and install
  ./clean_configure
  ./build_configure || return 1

  ./configure --prefix=${_installdir} \
	      --with-python-site=${_installdir}/lib/python-${_pythonver}/site-packages/salome \
	      --with-python-site-exec=${_installdir}/lib/python-${_pythonver}/site-packages/salome \
	      --with-qt=/usr \
	      --with-vtk=/usr \
	      --with-vtk-version=-${_vtkver} \
       	      --with-qwt=/usr \
	      --with-qwt_inc=/usr/include/qwt \
	      --disable-debug \
	      --enable-production || return 1

  make || return 1
}

package() {
  cd ${srcdir}/${_source} || return 1

  make DESTDIR="${pkgdir}" install || return 1

  install -D -m755 "${srcdir}/${pkgname}.profile" \
                   "${pkgdir}/etc/profile.d/${pkgname}.sh" || return 1

  install -D -m 644 "${srcdir}/salome.png" "${pkgdir}/usr/share/pixmaps/salome.png" || return 1

  desktop-file-install \
    --dir="${pkgdir}/usr/share/applications" \
    "${srcdir}/salome.desktop" || return 1

  install -D -m755 "${srcdir}/salome" \
                   "${pkgdir}/usr/bin/salome" || return 1
}

md5sums=('ec363b2ff2dbdb889c973e9e19b2970f'
         'cef74d7e140d6bef45c1503ad2a2e282'
         'a102063b779e332914ef0b73843e928a'
         '88e83a7034ec959a13c432f8eae3fe1c'
         'fd65925f524c628aa1ee8d62cceebe31')
