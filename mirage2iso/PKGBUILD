# Contributor: ezzetabi <ezzetabi@gawab.com>
# Contributor: seschwar -- contact via Arch Linux forum or AUR

pkgname=mirage2iso
pkgver=0.3.1
pkgrel=2
pkgdesc="libmirage-based CD/DVD image file converter"
arch=(i686 x86_64)
url="https://bitbucket.org/mgorny/mirage2iso"
license=(BSD3)
depends=(libassuan libmirage)
source=(https://bitbucket.org/mgorny/$pkgname/downloads/$pkgname-$pkgver.tar.bz2
        https://bitbucket.org/mgorny/$pkgname/downloads/$pkgname-0.3-tests.tar.xz)
noextract=("$pkgname-0.3-tests.tar.xz")
md5sums=('be2535e1a8dd1bedad6e353e2dd3551f'
         '2c2d3611a02efdbf0015cd6b4c1cd3ae')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr
    make LIBS+=-lgobject-2.0
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    bsdtar xf "../$pkgname-0.3-tests.tar.xz" --strip-components 1
    make -k check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING.txt"
}

