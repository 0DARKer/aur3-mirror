# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Sevenseven

pkgname=libunity-bzr
_pkgname=libunity
pkgver=309
pkgrel=1
pkgdesc="A library for instrumenting- and integrating with all aspects of the Unity shell."
arch=('i686' 'x86_64')
url="https://launchpad.net/libunity"
license=('LGPL')
depends=('dbus-glib' 'dee-bzr' 'gtk3' 'libdbusmenu-glib' 'libgee06' 'python2')
makedepends=('bzr' 'gobject-introspection' 'intltool' 'vala')
options=('!libtool')
install=$pkgname.install
provides=('libunity')
conflicts=('libunity')
source=('libunity::bzr+lp:~unity-team/libunity/trunk')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  bzr revno
}

build() {
  cd "$srcdir/$_pkgname"
  
  export PYTHON='/usr/bin/python2'
  ./autogen.sh --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --disable-static
  make 
}

package() { 
  cd "$srcdir/$_pkgname"
  
  make DESTDIR="${pkgdir}" install
}
