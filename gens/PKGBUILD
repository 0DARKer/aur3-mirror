# Maintainer: TryA <tryagainprod {at} gmail.com>

pkgname=gens
pkgver=2.15.5
pkgrel=4
pkgdesc='An emulator of Sega Genesis, Sega CD and 32X, built from the official Gens for Linux tree'
url='http://gens.sourceforge.net'
license=('GPL')
arch=('i686' 'x86_64')
if [[ $CARCH == "x86_64" ]]; then
  depends=('lib32-gtk2' 'lib32-sdl' 'lib32-libgl')
  makedepends=('nasm' 'gcc-multilib')
else
  depends=('gtk2' 'sdl' 'libgl')
  makedepends=('nasm' 'gcc')
fi
conflicts=('gens-gs' 'bin32-gens' 'bin32-gens-gs')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('1496a49be056d65bf76b52de62e932d7')

build() {
  if [ $CARCH == "x86_64" ]; then
    export CC="gcc -m32"
    export CXX="g++ -m32"
  fi
  
  unset CFLAGS
  unset LDFLAGS  
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
