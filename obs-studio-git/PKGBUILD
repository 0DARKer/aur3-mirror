# Maintainer: ledti <antegist at gmail.com>

pkgname=obs-studio-git
_pkgname=obs-studio
pkgver=992.c2eebd8
pkgrel=1
pkgdesc="Free and open source software for video recording and live streaming."
arch=('i686' 'x86_64')
url="https://github.com/jp9000/obs-studio"
license=('GPL2')
depends=('ffmpeg' 'libgl' 'libxinerama' 'qt5-base' 'qt5-x11extras')
makedepends=('cmake' 'git' 'qt5-tools' 'x264')
provides=('obs-studio')
conflicts=('obs-studio')
source=("$_pkgname::git://github.com/jp9000/obs-studio.git#branch=master")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  cd "$srcdir/$_pkgname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}

