# Maintainer: Claudio Kozicky <claudiokozicky@gmail.com>

pkgname=bindingofisaac-wrathoflamb
pkgver=1.48
pkgrel=1
pkgdesc="The Binding of Isaac + Wrath of the Lamb DLC is a randomly generated action RPG shooter with heavy Rogue-like elements."
arch=(i686 x86_64)
url="http://www.bindingofisaac.com/"
license=(custom)
[ $CARCH = x86_64 ]\
    && depends=(lib32-gtk2 lib32-libidn lib32-libxt lib32-sqlite)
[ $CARCH = i686 ]\
    && depends=(gtk2 libidn libxt sqlite)
options=(!strip) # otherwise game crashes
source=(hib://the_binding_of_isaac_wrath_of_the_lamb-linux.tar.gz
        $pkgname.desktop
        $pkgname.png)
md5sums=(f56410cdbed698cb984745e898585087
         d50b435a012f42ffeaff1a451093e7a6
         bdde316318e2f5cb54afc1db3558369e)

package() {
    cd "$srcdir"

    # bin
    install -Dm755 "Binding of Isaac/The Binding of Isaac + Wrath of the Lamb" "$pkgdir"/usr/bin/$pkgname

    # desktop integration
    install -Dm644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
    install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
