# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=seafile-shared
pkgver=2.2.0
pkgrel=1
pkgdesc="Shared components of seafile: seafile-daemon, libseafile, libseafile python bindings, manuals, and icons"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/haiwen/seafile/"
license=('GPL')
depends=('ccnet>=1.4.1' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'fuse')
makedepends=('vala' 'intltool')
optdepends=()
options=('!libtool' '!emptydirs')
install=seafile-shared.install
source=("https://github.com/haiwen/seafile/archive/v${pkgver}.tar.gz"
        "0002-Add-autoconfiguration-for-libjansson.patch")

prepare () {
        cd "$srcdir/seafile-${pkgver}"
        patch -p1 -i $srcdir/0002-Add-autoconfiguration-for-libjansson.patch
}


build ()
{
	cd "$srcdir/seafile-${pkgver}"
	./autogen.sh
	./configure --disable-server --disable-console --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package ()
{
	cd "${srcdir}/seafile-${pkgver}"
	make DESTDIR="${pkgdir}/" install

	# Remove seafile binary
	rm -rf "${pkgdir}/usr/bin/seafile"
	# Remove cli client binary
	rm -rf "${pkgdir}/usr/bin/seaf-cli"

	install -D -m644 ${srcdir}/seafile-${pkgver}/LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE.txt
}
sha256sums=('f2c7c8b9d283390ea02ed8cea08a0ceda04e6dbd04f35a485bf82994e2eb4033'
            '55d7812bf962743a151f9d0ffe870061e098307311e707afe1f9ceb8aab6865e')
