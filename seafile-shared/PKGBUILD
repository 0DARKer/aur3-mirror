# Maintainer: Moritz Maxeiner <moritz@ucworks.org>

pkgname=seafile-shared
pkgver=2.1.1
pkgrel=6
pkgdesc="Shared components of seafile: seafile-daemon, libseafile, libseafile python bindings, manuals, and icons"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://github.com/haiwen/seafile/"
license=('GPL')
depends=('ccnet>=1.4.0' 'hicolor-icon-theme' 'fuse')
makedepends=('vala' 'intltool')
optdepends=()
options=('!libtool' '!emptydirs')
install=seafile-shared.install
source=("https://github.com/haiwen/seafile/archive/v${pkgver}.tar.gz"
        "0002-Add-autoconfiguration-for-libjansson.patch")
md5sums=('f205f7e4088341a1805a725305b4cd37'
         '86357d9fee615ec3931cd2399fdcd4de')
sha256sums=('f07b09ab1eb1cb33f92cee74e499d0350941624396910a4c1a7d79abb45acaa6'
            '55d7812bf962743a151f9d0ffe870061e098307311e707afe1f9ceb8aab6865e')

prepare () {
        cd "$srcdir/seafile-${pkgver}"
        patch -p1 -i $srcdir/0002-Add-autoconfiguration-for-libjansson.patch
}


build ()
{
	cd "$srcdir/seafile-${pkgver}"
	./autogen.sh
	./configure --disable-server --disable-console --prefix=/usr PYTHON=/usr/bin/python2
	make -j1
}

package ()
{
	cd "${srcdir}/seafile-${pkgver}"
	make DESTDIR="${pkgdir}/" install

	# Remove seafile binary
	rm -rf "${pkgdir}/usr/bin/seafile"
	# Remove cli client binary
	rm -rf "${pkgdir}/usr/bin/seaf-cli"

	install -D -m644 ${srcdir}/seafile-${pkgver}/LICENCE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENCE.txt
}
