# Contributor: Jonathan Liu <net147@hotmail.com>
pkgname=dahdi
pkgver=2.5.0
_realpkgver=${pkgver/_/-}
pkgrel=1
pkgdesc="DAHDI drivers for Asterisk"
arch=('i686' 'x86_64')
url="http://www.asterisk.org/"
license=('GPL2')
depends=('kernel26' 'libusb' 'perl')
makedepends=('kernel26-headers')
conflicts=('zaptel')
install="${pkgname}.install"
source=("http://downloads.asterisk.org/pub/telephony/dahdi-linux/releases/dahdi-linux-${_realpkgver}.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-oct6114-064-1.05.01.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-oct6114-128-1.05.01.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-tc400m-MR6.12.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-hx8-2.06.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fw-vpmoct032-1.8.0.tar.gz"
        "http://downloads.digium.com/pub/telephony/firmware/releases/dahdi-fwload-vpmadt032-1.25.0.tar.gz"
        "Makefile.patch")
md5sums=('449f6591ccedb68937b5b30cbd37cea3'
         '88db9b7a07d8392736171b1b3e6bcc66'
         'c1f1a18d3e20d283f42c71e580a64b5a'
         '2ea860bb8a9d8ede2858b9557b74ee3c'
         'a7f3886942bb3e9fed349a41b3390c9f'
         'e75ef6065a07275b127458c3a8654ab1'
         '19a660c2073749641072ca644e983cbc'
         '5f27b1a1005d41bd74ba9bdfe88223bb')

build() {
  cd "${srcdir}/dahdi-linux-${_realpkgver}"

  ln -sf "${srcdir}"/dahdi-fw*.tar.gz "drivers/dahdi/firmware/"
  mv "${srcdir}"/dahdi-fw*.bin "drivers/dahdi/firmware/"
  patch -Np1 -i "${srcdir}/Makefile.patch"

  make DESTDIR="${pkgdir}" modules
}

package() {
  cd "${srcdir}/dahdi-linux-${_realpkgver}"
  make DYNFS=1 DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
