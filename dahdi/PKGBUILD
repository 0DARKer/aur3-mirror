# Maintainer : Xavier Devlamynck <magicrhesus@ouranos.be>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=dahdi
pkgver=2.8.0.1
pkgver_tools=2.8.0
pkgrel=1
pkgdesc="DAHDI drivers for Asterisk"
arch=('i686' 'x86_64')
url="http://www.asterisk.org/"
license=('GPL2')
depends=('linux' 'libusb' 'perl')
makedepends=('linux-headers')
conflicts=('zaptel')
install="${pkgname}.install"
source=("http://downloads.asterisk.org/pub/telephony/dahdi-linux-complete/releases/dahdi-linux-complete-${pkgver}+${pkgver_tools}.tar.gz"
        "Makefile.patch"
        "dahdi.service")
sha1sums=('ab6533c6161a897ddcf708b69fad311f8f4ddd61'
          '2c32963f71423a3591602b1863c714d92e2afcc5'
          'd37e37c67726cc64ce8b668217db625e2e9d9e9e')
build() {
  cd "${srcdir}/dahdi-linux-complete-${pkgver}+${pkgver_tools}"
  patch -p0 -i "${srcdir}/Makefile.patch" "${srcdir}/dahdi-linux-complete-${pkgver}+${pkgver_tools}/Makefile"
  make DESTDIR="${pkgdir}" all
}

package() {
  cd "${srcdir}/dahdi-linux-complete-${pkgver}+${pkgver_tools}"
  make DYNFS=1 DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
