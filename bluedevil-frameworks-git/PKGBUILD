# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=bluedevil-frameworks-git
pkgver=r1445.d629cb1
pkgrel=1
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/base/bluedevil'
license=('GPL2')
depends=('bluez' 'libbluedevil-frameworks-git' 'kdelibs4support-git' 'kcmutils-git')
makedepends=('extra-cmake-modules-git' 'git')
provides=('bluedevil-frameworks')
conflicts=('bluedevil-frameworks')
source=("git://anongit.kde.org/bluedevil.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
  cd bluedevil
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  sed -i 's|KF5 CONFIG REQUIRED|KF5 REQUIRED|' bluedevil/CMakeLists.txt
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"
  export CMAKE_PREFIX_PATH="/opt/kf5"
 
  cd build
  cmake ../bluedevil \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
