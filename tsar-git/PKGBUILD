# $Id: PKGBUILD 85687 2013-03-05 05:00:24Z fyan $
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=tsar-git
_gitname=tsar
pkgver=0.27.deb8698
pkgrel=1
pkgdesc="Taobao System Activity Reporter"
arch=('i686' 'x86_64')
url="https://github.com/alibaba/tsar"
license=('Apache')
depends=('glibc')
makedepends=('git')
source=("git://github.com/alibaba/$_gitname.git"
	"fix_module_path.patch")
md5sums=('SKIP'
	 '69eb27bc486d4ab6d3002b8475525063')

pkgver() {
  cd "$srcdir/$_gitname"
  echo "0.$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd "$srcdir/${_gitname}"
  patch -Np1 -i "$srcdir/fix_module_path.patch"
  make  # --prefix=/usr Not supported, WTF
}

package() {
  cd "$srcdir/${_gitname}"

  install -d "$pkgdir/usr/lib/tsar/modules"
  cp modules/*.so "$pkgdir/usr/lib/tsar/modules"/
  
  install -Dm755 src/tsar "$pkgdir/usr/bin/tsar"

  install -Dm644 conf/tsar.conf "$pkgdir/etc/tsar/tsar.conf"
  install -Dm644 conf/tsar.logrotate "$pkgdir/etc/logrotate.d/tsar"
  install -Dm644 conf/tsar.cron "$pkgdir/etc/cron.d/tsar"

  install -Dm644 conf/tsar.8 "$pkgdir/usr/share/man/man8/tsar.8"
}
