# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>
# Maintainer: ava1ar <mail@ava1ar.info>

pkgname=kdeplasma-applets-networkmanagement
epoch=1
pkgver=0.9.0.10
pkgrel=1
pkgdesc="KDE control panel and widget network connections"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/base/networkmanagement'
license=('GPL')
depends=('kdebase-workspace' 'networkmanager')
conflicts=('kdeplasma-applets-plasma-nm')
makedepends=('cmake' 'python2' 'automoc4')
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers'
            'openconnect: Cisco AnyConnect compatible VPN client')
install=${pkgname}.install
source=("http://download.kde.org/unstable/networkmanagement/${pkgver}/src/networkmanagement-${pkgver}.tar.xz")
sha256sums=('dce8d080256f1c2a6b70d61cb46e623839f6c0a0f260a6901d9ad68746ce7dfc')

build() {
  rm -rf build && mkdir build
  cd build
  cmake ../networkmanagement-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
