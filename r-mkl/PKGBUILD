# $Id: PKGBUILD 122820 2011-05-06 10:48:44Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=r-mkl
pkgver=2.13.0
pkgrel=1
pkgdesc="R is a language and environment for statistical computing and graphics"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.r-project.org/')
depends=('intel-mkl' 'lapack' 'bzip2'  'libpng' 'libjpeg' 'libtiff'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs'
         'tk' 'libxt' 'libxmu' 'pango')
#makedepends=('openjdk6' 'gcc-fortran')
makedepends=('java-environment=6' 'gcc-fortran')
options=('!makeflags')
source=("http://cran.r-project.org/src/base/R-2/R-${pkgver}.tar.gz"
	'r.desktop'
	'r.png')
provides=(r)
conflicts=(r)

build() {
   cd ${srcdir}/R-${pkgver}
   sed -i 's|#define NeedFunctionPrototypes 0|#define NeedFunctionPrototypes 1|g' src/modules/X11/dataentry.c

   if [[ $CARCH == x86_64 ]]; then
      MKLPATH="${MKLROOT}/lib/intel64"
      MKLSUFFIX="_lp64"
   else
      MKLPATH="${MKLROOT}/lib/ia32"
      MKLSUFFIX=""
   fi
   MKL="-L$MKLPATH $MKLPATH/libmkl_solver$MKLSUFFIX.a -Wl,--start-group -lmkl_gf$MKLSUFFIX -lmkl_gnu_thread -lmkl_core -Wl,--end-group -fopenmp -lpthread"
   LDFLAGS="-lpthread"
   ./configure  --prefix=/usr \
		--libdir=/usr/lib \
		--datarootdir=/usr/share \
		  rsharedir=/usr/share/R/ \
		  rincludedir=/usr/include/R/ \
		  rdocdir=/usr/share/R/docs/ \
                --with-gnu-ld \
		--with-readline \
                --with-x \
                --with-zlib \
                --with-bzlib  \
                --with-pcre \
		--enable-R-shlib \
		--with-blas="$MKL" \
                --with-lapack \
                F77=gfortran \
		LIBnn=lib
   make
}

package() {
   cd ${srcdir}/R-${pkgver}
   make -j1 DESTDIR=${pkgdir} install

   #  Fixup R wrapper scripts.
   sed -i "s|${pkgdir} ||" ${pkgdir}/usr/bin/R
   rm ${pkgdir}/usr/lib/R/bin/R
   cd ${pkgdir}/usr/lib/R/bin
   ln -s ../../../bin/R

  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/r.desktop \
	${pkgdir}/usr/share/applications/r.desktop
  install -Dm644 ${srcdir}/r.png \
	${pkgdir}/usr/share/pixmaps/r.png
 }
md5sums=('ecfb928067cfd932e75135f8b8bba3e7'
         'f23f172a35249321c4ccecb712397e74'
         '00659f39e90627fe87f1645df5d4e3a7')
