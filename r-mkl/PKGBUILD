# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# based on package from extra
pkgname=r-mkl
pkgver=2.12.1
pkgrel=1
pkgdesc="R is a language and environment for statistical computing and graphics"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.r-project.org/')
depends=('bzip2'  'libpng' 'libjpeg' 'libtiff'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs'
         'tk' 'libxt' 'libxmu' 'pango' 'intel-mkl')
makedepends=('openjdk6' 'gcc-fortran')
options=('!makeflags')
source=(http://cran.r-project.org/src/base/R-2/R-${pkgver}.tar.gz
        r.desktop
        r.png)
provides=(r)
conflicts=(r)

md5sums=('078e8d1179fc9a762e326e6da2725468'
         'f6d54d32e510d90c748a0d16d2abcbdc'
         '00659f39e90627fe87f1645df5d4e3a7')

build() {
  cd ${srcdir}/R-${pkgver}
  if [[ $CARCH == x86_64 ]]; then
    MKLPATH="${MKLROOT}/lib/intel64"
    MKLSUFFIX="_lp64"
  else
    MKLPATH="${MKLROOT}/lib/ia32"
    MKLSUFFIX=""
  fi
  MKL="-L$MKLPATH $MKLPATH/libmkl_solver$MKLSUFFIX.a -Wl,--start-group -lmkl_gf$MKLSUFFIX -lmkl_gnu_thread -lmkl_core -Wl,--end-group -fopenmp -lpthread"
  LDFLAGS="-lpthread"
  ./configure --prefix=/usr \
    --libdir=/usr/lib \
    --datarootdir=/usr/share \
      rsharedir=/usr/share/R/ \
      rincludedir=/usr/include/R/ \
      rdocdir=/usr/share/R/docs/ \
    --enable-memory-profiling \
    --with-system-zlib \
    --with-system-pcre \
    --with-system-bzlib \
    --with-x \
    --enable-R-shlib \
    --with-blas="$MKL" \
    --with-lapack \
    F77=gfortran \
    LIBnn=lib
  make || return 1
  make check || return 1
  make DESTDIR=${pkgdir} install || return 1

  #  Fixup R wrapper scripts.
  sed -i "s|${pkgdir} ||" ${pkgdir}/usr/bin/R
  rm ${pkgdir}/usr/lib/R/bin/R
  cd ${pkgdir}/usr/lib/R/bin
  ln -s ../../../bin/R

  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/r.desktop \
  ${pkgdir}/usr/share/applications/r.desktop || return 1
  install -Dm644 ${srcdir}/r.png \
  ${pkgdir}/usr/share/pixmaps/r.png || return 1
}
