# Maintainer: Lone_Wolf <lonewolf@xs4all.nl>
pkgname=oolite
pkgver=1.75.2
pkgrel=1
pkgdesc="A space-sim based on the classic Elite"
arch=('i686' 'x86_64')
url="http://oolite-linux.berlios.de"
license=('GPL2')
groups=()
depends=('gnustep-gui' 'sdl_mixer' 'mesa' 'espeak' 'nspr')
makedepends=('gcc-objc' 'gnustep-make')
conflicts=(oolite-test)
source=(oolite http://download2.berlios.de/oolite-linux/oolite-dev-source-${pkgver}.tar.bz2)
noextract=()
md5sums=('653a862453d74f64c3d206656c6291e8'
         '926720981d94fd09c9e2824dbc102d5e')

build() {
    cd $srcdir
    cp -r ${srcdir}/oolite-dev-source-${pkgver} ${srcdir}/${pkgname}-build

# running 'make -f libjs.make debug=no' and/or 'make -f Makefile release'
# works from commandline, but gives errors that cause makepkg to abort
    cd ${srcdir}/${pkgname}-build/deps/Cocoa-deps/scripts
    ./update-mozilla.sh
    mkdir -p ${srcdir}/${pkgname}-build/deps/Cross-platform-deps/mozilla/js/src/build-release
    cd ${srcdir}/${pkgname}-build/deps/Cross-platform-deps/mozilla/js/src/build-release
    ../configure --disable-shared-js --enable-threadsafe --with-system-nspr --disable-tests --enable-trace-jscalls
# end of lines needed to avoid makepkg aborting
    cd ${srcdir}/${pkgname}-build
    make -f libjs.make debug=no
    make -f Makefile release
}

package() {
    mkdir -p ${pkgdir}/usr/bin
    mkdir -p ${pkgdir}/usr/share/{oolite.app,applications,pixmaps,doc/oolite}
    cd ${srcdir}/${pkgname}-build
    cp -r oolite.app/* ${pkgdir}/usr/share/oolite.app/
#install shellscript and icon
    install -D -m755 ../oolite ${pkgdir}/usr/bin/oolite
    install -D -m644 installers/FreeDesktop/oolite-icon.png ${pkgdir}/usr/share/pixmaps/oolite-icon.png
# copy  .desktop file
    install -D -m644 installers/FreeDesktop/oolite.desktop ${pkgdir}/usr/share/applications/oolite.desktop
# documentation
    install -D -m644 Doc/AdviceForNewCommanders.pdf Doc/OoliteReadMe.pdf Doc/OoliteRS.pdf ${pkgdir}/usr/share/doc/oolite/
 }
# vim:set ts=2 sw=2 et:
