# Maintainer: dx <dx@dxzone.com.ar>
# Contributor: Trevor Mosey <uberushaximus AT gmail DOT com>
# Contributor: Scott Sinclair <scott@nullis.net>

pkgname='bitlbee-steam-git'
pkgver=1.0.1.r0.g89375b9
pkgrel=1
pkgdesc='A bitlbee plugin for the steam message protocol'
arch=('i686' 'x86_64')
url='https://github.com/jgeboski/bitlbee-steam'
license=('GPL')
depends=('bitlbee')
makedepends=('git' 'bitlbee')
provides=('bitlbee-steam')
conflicts=('bitlbee-steam')
options=('!libtool')
source=("$pkgname::git://github.com/jgeboski/bitlbee-steam.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g; s/^v//'
}

build() {
  cd "$srcdir/$pkgname"
  mkdir m4
  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
