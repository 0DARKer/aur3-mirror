# Maintainer: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>
pkgname="teamspeak3-server"
pkgver="beta30"
pkgrel=1
pkgdesc="A flexible, powerful, scalable software which enables people to speak with one another over the Internet"
arch=('i686' 'x86_64')
url="http://www.teamspeak.com/"
license=('custom')
backup=('etc/conf.d/teamspeak3-server')
install="teamspeak3-server.install"
source=("http://ftp.4players.de/pub/hosted/ts3/releases/beta-30/teamspeak3-server_linux-x86-3.0.0-${pkgver}.tar.gz"
        "http://ftp.4players.de/pub/hosted/ts3/releases/beta-30/teamspeak3-server_linux-amd64-3.0.0-${pkgver}.tar.gz"
        'teamspeak3-server.conf.d'
        'teamspeak3-server.rc.d'
        'teamspeak3-server.sh')

md5sums=('f42e830bc2cb57ed8ef9513d368cd63d'
         '41e4fb5386f8d00b928b68308377df01'
         'e742aa2f8091a0a61c80ca9c06d40db9'
         '19c2ea8347953f55e566592904f789db'
         '2b33b2826d998eae74ef9bce5fd68985')

build() {
    
    _tsarch="x86"

    cd "$srcdir"
    
    #create folders
    install -d "$pkgdir/usr/share/licenses/$pkgname" || return 1
    install -d "$pkgdir/opt/" || return 1
    install -d "$pkgdir/etc/conf.d/" || return 1
    install -d "$pkgdir/etc/rc.d/" || return 1
    install -d -m 0750 "$pkgdir/var/lib/$pkgname" || return 1
    install -d -m 0755 "$pkgdir/var/log/$pkgname" || return 1
    install -d "$pkgdir/usr/bin/" || return 1
    
    # change startscripts to correct arch
    if [ $CARCH == "x86_64" ]; then
        sed -i 's/x86/amd64/' 'teamspeak3-server.sh' || return 1
        sed -i 's/x86/amd64/' 'teamspeak3-server.rc.d' || return 1

        _tsarch="amd64"
    fi
    
    #copy files 
    install -T -m 644 "teamspeak3-server_linux-${_tsarch}/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
    install -T -m 644 "teamspeak3-server.conf.d" "$pkgdir/etc/conf.d/teamspeak3-server" || return 1
    install -T -m 755 "teamspeak3-server.rc.d" "$pkgdir/etc/rc.d/teamspeak3-server" || return 1
    install -T -m 755 "teamspeak3-server.sh" "$pkgdir/usr/bin/teamspeak3-server" || return 1
    mv "${pkgname}_linux-${_tsarch}" "$pkgdir/opt/teamspeak3-server" || return 1
}
