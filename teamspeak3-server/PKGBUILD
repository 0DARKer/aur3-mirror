# Maintainer: Malte Rabenseifner <mail@malte-rabenseifner.de>

pkgname="teamspeak3-server"
pkgver="3.0.6.1"
pkgrel=1
pkgdesc="A proprietary VoIP conference software"
arch=('i686' 'x86_64')

if [ "${CARCH}" == "x86_64" ]; then
  _TSARCH='amd64'
  _TSMD5='0a7983a1d0cc646d3e69cc2b652495a1'
else
  _TSARCH='x86'
  _TSMD5='a624179f68f57c05ba51940e26eabd71'
fi

url="http://www.teamspeak.com/"
license=('custom')
depends=('bash')
backup=('etc/conf.d/teamspeak3-server')
install="${pkgname}.install"
source=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/${pkgname}_linux-${_TSARCH}-${pkgver}.tar.gz"
        'teamspeak3-server.conf.d'
        'teamspeak3-server.rc.d'
        'teamspeak3-server.sh')
md5sums=(${_TSMD5}
         'e742aa2f8091a0a61c80ca9c06d40db9'
         '19c2ea8347953f55e566592904f789db'
         '2b33b2826d998eae74ef9bce5fd68985')

build() {
    cd "${srcdir}"

    # Change startscripts to correct arch
    if [ "${CARCH}" == "x86_64" ]; then
      sed -i 's/x86/amd64/' "${pkgname}.sh"
      sed -i 's/x86/amd64/' "${pkgname}.rc.d"
    fi
}

package() {
    cd "${srcdir}"
    
    # Create folders
    install -d "${pkgdir}/opt/${pkgname}/sql"
    install -d "${pkgdir}/usr/share/doc/${pkgname}"
    install -dm 0750 "${pkgdir}/var/lib/${pkgname}"
    install -dm 0750 "${pkgdir}/var/log/${pkgname}"
    
    # Copy files 
    install -Dm 644 "${pkgname}.conf.d" "${pkgdir}/etc/conf.d/${pkgname}"
    install -Dm 755 "${pkgname}.rc.d" "${pkgdir}/etc/rc.d/${pkgname}"
    install -Dm 755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
    install -Dm 644 "${pkgname}_linux-${_TSARCH}/libts3db_mysql.so" "${pkgdir}/opt/${pkgname}/libts3db_mysql.so"
    install -Dm 644 "${pkgname}_linux-${_TSARCH}/libts3db_sqlite3.so" "${pkgdir}/opt/${pkgname}/libts3db_sqlite3.so"
    install -Dm 755 "${pkgname}_linux-${_TSARCH}/ts3server_linux_${_TSARCH}" "${pkgdir}/opt/${pkgname}/ts3server_linux_${_TSARCH}"

    cp -a "${pkgname}_linux-${_TSARCH}/sql/"* "${pkgdir}/opt/${pkgname}/sql"
    cp -a "${pkgname}_linux-${_TSARCH}/doc/"* "${pkgdir}/usr/share/doc/${pkgname}"

    install -Dm 644 "${pkgname}_linux-${_TSARCH}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
