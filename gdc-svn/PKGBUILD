# Contributor: Selman Ulug <selman.ulug@gmail.com>
pkgname=gdc-svn
pkgver=243
_gccver=4.1.2
pkgrel=3
pkgdesc="GDC, Digital Mars D Programing Language (DMD) frontend for GCC"
arch=(i686 x86_64)
url="http://dgcc.sourceforge.net"
license="GPL"
provides=('gdc')
depends=('gcc>=4.1.2')
makedepends=('subversion')
conflicts=('gdc')
options=('!libtool' '!emptydirs')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${_gccver}/gcc-core-${_gccver}.tar.bz2
        gcc_pure64.patch gdc-stdlib)
md5sums=('2af3fb599635219171c6ae1f3034888a'
         '1ceaa49e3a1d9f984ecc2893c43f7425'
         'c74fed004a7000616fef7b4cf5047333')

_svntrunk=https://svn.sourceforge.net/svnroot/dgcc/trunk
_svnmod=dgcc

build() {
  rm -rf $startdir/src/$_svnmod-build

  cd $startdir/src

  svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod

  msg "SVN checkout done or server timeout"
  msg "Starting make..."
    
  cp -a $startdir/src/gcc-${_gccver} $_svnmod-build
  cp -a $startdir/src/dgcc/d $_svnmod-build/gcc
  cd $_svnmod-build

  # make patches in dgcc-build if necessary
  # apply gdc patches
  ./gcc/d/setup-gcc.sh

  export MAKEFLAGS="-j1"

  # Don't install libiberty 
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  if [ "${CARCH}" = "x86_64" ]; then
     # Note that gcc 4.1.2 does not support as many arches as more recent versions
      export CFLAGS=$(echo $CFLAGS | sed 's/-m\(arch\|tune\)=[^ ]*//g')
      export CXXFLAGS=$(echo $CFLAGS | sed 's/-m\(arch\|tune\)=[^ ]*//g')
      patch -Np1 -i ../gcc_pure64.patch || return 1
  fi

  # Don't run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  mkdir ../gcc-build
  cd ../gcc-build
  ../$_svnmod-build/configure --prefix=/usr \
       --enable-languages=d --enable-threads  --enable-__cxa_atexit \
       --disable-multilib --libdir=/usr/lib --libexecdir=/usr/lib \
       --disable-shared
  make all-target-libphobos || return 1
  make DESTDIR=${startdir}/pkg MKDIRPROG="mkdir -p" install-target-libphobos || return 1
  cd gcc
  make DESTDIR=${startdir}/pkg MKDIRPROG="mkdir -p" install-libgcc || return 1
  make DESTDIR=${startdir}/pkg lang.install-normal lang.install-common lang.install-man

  install -Dm 755 cc1d $startdir/pkg/usr/lib/gcc/${CHOST}/${_gccver}/

  cd $startdir/pkg/usr/lib
  mv libgphobos.a libgphobos.a.phobos
  ln -s libgphobos.a.phobos libgphobos.a
  cd $startdir/pkg/usr/include/d/${_gccver}
  mv object.d object.d.phobos
  ln -s object.d.phobos object.d
  mv std std.phobos
  ln -s std.phobos std

  install -Dm 755 $startdir/gdc-stdlib $startdir/pkg/usr/bin
}
