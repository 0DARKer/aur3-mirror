# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Maintainer: GordonGR <ntheo1979@gmail.com>

_pkgname=libdvdnav
pkgname=lib32-${_pkgname}
pkgver=4.2.1
pkgrel=1
pkgdesc="The library for xine-dvdnav plugin (32 bit)"
arch=('x86_64')
license=('GPL')
url="http://dvdnav.mplayerhq.hu/"
depends=('lib32-libdvdread' "${_pkgname}")
source=("http://dvdnav.mplayerhq.hu/releases/${_pkgname}-${pkgver}.tar.xz")
md5sums=('8a1b2ebfe81daecb590893cb9093f832')


build() {
export CC='gcc -m32'
export CXX='g++ -m32'
export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
cd "${_pkgname}-${pkgver}"  
./autogen.sh --prefix=/usr --libdir=/usr/lib32
make
}

package() {
cd "${_pkgname}-${pkgver}"
make DESTDIR="${pkgdir}" install
rm -rf "${pkgdir}/usr"/{bin,include,share}
}
