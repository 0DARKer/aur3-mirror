pkgname=lucene++
pkgver=3.0.4
pkgrel=1
pkgdesc="C++ port of the popular Java Lucene library"
arch=(i686 x86_64)
url="https://github.com/luceneplusplus/LucenePlusPlus"
license=('GPL' 'Apache')
depends=(boost-libs gcc-libs)
makedepends=(boost clang)
source=($pkgname-$pkgver.tar.gz::$url/archive/rel_$pkgver.tar.gz)
md5sums=('64c34aa66b3870cc015c5d084a1a2b4e')

prepare() {
    cd "$srcdir/LucenePlusPlus-rel_$pkgver"

    #https://github.com/luceneplusplus/LucenePlusPlus/pull/48
    #@vsladik: Fix accidental use of operator + instead of operator <<.
    sed -i '/\" documents will be lost/s/+/<</' src/core/index/CheckIndex.cpp

    mkdir build || return 0
}

build() {
	cd "$srcdir/LucenePlusPlus-rel_$pkgver/build"
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_DESTINATION=/usr/lib \
        -DBoost_LIBRARY_DIR=/usr/lib
        
	make
}

package() {
	cd "$srcdir/LucenePlusPlus-rel_$pkgver/build"
	make DESTDIR="$pkgdir/" install
}
