# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=mkinitcpio-chkeymap
pkgver=0.1.0.r0.geb3abca
pkgrel=1
pkgdesc="Set keymap and timezone for main system inside initrd"
arch=('any')
makedepends=('git')
url="http://www.eworm.de/"
license=('GPL')
install=mkinitcpio-chkeymap.install
source=('git://github.com/eworm-de/mkinitcpio-chkeymap.git')
    
pkgver() {
	cd mkinitcpio-chkeymap/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

package() {
	cd mkinitcpio-chkeymap/

	install -D -m644 hook/chkeymap ${pkgdir}/usr/lib/initcpio/hooks/chkeymap
	install -D -m644 install/chkeymap ${pkgdir}/usr/lib/initcpio/install/chkeymap

	install -D -m644 etc/vconsole.conf ${pkgdir}/etc/vconsole.conf
	install -D -m644 etc/05-keymap.conf ${pkgdir}/etc/X11/xorg.conf.d/05-keymap.conf
}

sha256sums=('SKIP')
