# Maintainer: josephgbr <rafael.f.f1@gmail.com>

# How to update?
# 'pkgver' and 'pkgrel' has to be same as in 'smbclient'
# Run 'pacman -Si smbclient' to verify the version
# e.g.: smbclient 4.1.8-1 means pkgver=4.1.8 and pkgrel=1

pkgbase=lib32-smbclient
pkgname=(lib32-libwbclient lib32-smbclient)
pkgver=4.1.9
pkgrel=1
pkgdesc="Tools to access a server's filespace and printers via SMB (32 bits)"
arch=('x86_64')
url="http://www.samba.org"
license=('GPL3')
source=("ftp://ftp.archlinux.org/extra/os/i686/smbclient-$pkgver-$pkgrel-i686.pkg.tar.xz"
        "ftp://ftp.archlinux.org/extra/os/i686/libwbclient-$pkgver-$pkgrel-i686.pkg.tar.xz")
noextract=(smbclient-$pkgver-$pkgrel-i686.pkg.tar.xz
           libwbclient-$pkgver-$pkgrel-i686.pkg.tar.xz)
md5sums=('eb77e19786fd159ecfd63c1f938c7384'
         'f28f59e22ce3aa31665c40802e3b9f81')

package_lib32-libwbclient() {
  pkgdesc="Samba winbind client library (32 bits)"
  depends=('lib32-libbsd' "libwbclient>=$pkgver")
  
  rm -rf libwbclient && mkdir libwbclient
  cd libwbclient  
  tar xf ../libwbclient-$pkgver-$pkgrel-i686.pkg.tar.xz usr/lib
  
  mkdir -p "$pkgdir"/usr/lib32
  cp -rPf usr/lib/* "$pkgdir"/usr/lib32
  sed -i 's#/lib#/lib32#' "$pkgdir"/usr/lib32/pkgconfig/wbclient.pc
}


package_lib32-smbclient() {
  pkgdesc="Tools to access a server's filespace and printers via SMB (32 bits)"
  depends=('lib32-tdb' 'lib32-gnutls' 'lib32-talloc' 'lib32-libcap'
           'lib32-libwbclient' 'lib32-libgcrypt' 'lib32-libcups'
           'lib32-pam' 'lib32-avahi' "smbclient>=$pkgver")
         
  rm -rf smbclient && mkdir smbclient
  cd smbclient  
  tar xf ../smbclient-$pkgver-$pkgrel-i686.pkg.tar.xz usr/lib
  
  mkdir -p "$pkgdir"/usr/lib32
  cp -rPf usr/lib/* "$pkgdir"/usr/lib32
  sed -i 's#/lib#/lib32#'\
    "$pkgdir"/usr/lib32/pkgconfig/netapi.pc \
    "$pkgdir"/usr/lib32/pkgconfig/smbclient.pc \
    "$pkgdir"/usr/lib32/pkgconfig/smbclient-raw.pc
                           
  rm -rf "$pkgdir"/usr/lib32/cups # not a lib
}
