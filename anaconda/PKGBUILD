# Maintainer : Immae <ismael.bouya@normalesup.org>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=anaconda
pkgver=1.8.0
pkgrel=3
_pkgarch=`uname -m`
pkgdesc="Completely free enterprise-ready Python distribution for large-scale data processing, predictive analytics, and scientific computing."
url="https://store.continuum.io/cshop/anaconda/"
license=("custom")
arch=('i686' 'x86_64')
source=("http://09c8d0b2229f813c1b93-c95ac804525aac4b6dba79b00b39d1d3.r79.cf1.rackcdn.com/Anaconda-${pkgver}-Linux-x86_64.sh"
        "installer_sh_x86_64.patch"
        "installer_sh_x86.patch"
        "conda_install.patch")
options=(!strip)
md5sums=('398d4b7ddc5c0a16c556c415b2444266'
         '06427acef4a4c8ec9a674a03881e8007'
         'd30f5aec2d9dfbed969eed0acc75edab'
         '7966366a5ca6a3a548e36ddb6454631b')

if [ "$CARCH" == "i686" ]; then
    _pkgarch="x86"
    md5sums[0]='5028bf0aa7ff8a071d5532b8f8ec924c'
    source[0]="http://09c8d0b2229f813c1b93-c95ac804525aac4b6dba79b00b39d1d3.r79.cf1.rackcdn.com/Anaconda-${pkgver}-Linux-${_pkgarch}.sh"
fi

prepare() {
    patch --follow-symlinks -p1 < installer_sh_${_pkgarch}.patch
}

package() {
    chmod +x $srcdir/Anaconda-${pkgver}-Linux-${_pkgarch}.sh
    cd ${srcdir}
    prefix=${pkgdir}/opt/${pkgname}
    ${srcdir}/Anaconda-${pkgver}-Linux-${_pkgarch}.sh -b -p $prefix 2>/dev/null
    cd $prefix
    patch -p1 < $srcdir/conda_install.patch
    CONDA_INSTALL="$prefix/pkgs/conda-2.0.4-py27_0/lib/python2.7/site-packages/conda/install.py"
    $prefix/pkgs/python-2.7.5-3/bin/python -E $CONDA_INSTALL --prefix=$prefix --instdir=/opt/${pkgname} --pkgs-dir=$prefix/pkgs --link-all || exit 1
}
