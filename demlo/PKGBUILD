# Maintainer: Pierre Neidhardt <ambrevar at gmail dot com>

_pkgname=demlo
pkgname=$_pkgname
_arcdir=ambrevar-$_pkgname-70448e2eda71
pkgver=1.5
pkgrel=1
pkgdesc="A dynamic and extensible music library organizer"
url="http://ambrevar.bitbucket.org/$_pkgname/"
arch=('any')
license=('MIT')
depends=('ffmpeg' 'lua' 'lua-dkjson' 'lua-filesystem' 'lua-llthreads2' 'lua-penlight' 'slnunicode-git')
optdepends=('chromaprint: Internet tagging')
source=(https://bitbucket.org/ambrevar/$_pkgname/get/v$pkgver.tar.bz2)
sha1sums=('c2c5f952c6eaed8fdd9eb39e85162e3e8ec79ed4')

package() {
    cd "${srcdir}/$_arcdir"
    install -D -m755 $_pkgname.lua "${pkgdir}/usr/bin/$_pkgname"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"

    install -D -m644 ${_pkgname}rc "${pkgdir}/usr/share/${_pkgname}/${_pkgname}rc"

    for i in scripts/*; do
        install -D -m644 "$i" "${pkgdir}/usr/share/${_pkgname}/$i"
    done

    for i in $_pkgname/*; do
        install -D -m644 "$i" "${pkgdir}/usr/share/lua/5.2/$i"
    done

    ## Man page
    install -D -m755 -d "${pkgdir}/usr/share/man/man1/"
    gzip -c $_pkgname.1 > "${pkgdir}/usr/share/man/man1/$_pkgname.1.gz" && \
        chmod 644 "${pkgdir}/usr/share/man/man1/$_pkgname.1.gz"
}
