# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>

pkgname=libretro-ume-git
pkgver=31958.63b2b6a
pkgrel=1
arch=('i686' 'x86_64' 'arm' 'armv6h')
pkgdesc="libretro implementation of UME 2014. (Arcade)"
url="https://github.com/libretro/mame"
license=('custom:MAME License')
makedepends=('git')

_gitname=mame
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/mame_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make -f Makefile.libretro TARGET=ume
}

package() {
  install -Dm644 "${_gitname}/ume_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-ume.so"

  sed -i "s/MAME 2014/UME 2014/g" "mame_libretro.info"
  install -Dm644 "mame_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-ume.info"

  install -Dm644 "${_gitname}/docs/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
