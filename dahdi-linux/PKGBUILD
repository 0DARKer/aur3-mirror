# Maintainer: Maik Broemme <mbroemme@libmpq.org>
pkgname="dahdi-linux"
pkgdesc="DAHDI drivers for Asterisk (Digium, OpenVox, Allo and Yeastar cards)"
pkgver=2.10.1
pkgrel=1
arch=("i686" "x86_64")
url="http://www.asterisk.org/"
license=("LGPLv2")
depends=("linux>=3.19" "linux<4.0")
makedepends=("linux-headers>=3.19" "linux-headers<4.0")
conflicts=("dahdi")
install="${pkgname}.install"
source=(
  "http://downloads.asterisk.org/pub/telephony/${pkgname}/${pkgname}-${pkgver}.tar.gz"
  "oslec.h::http://git.kernel.org/cgit/linux/kernel/git/stable/linux-stable.git/plain/drivers/misc/echo/oslec.h?id=refs/tags/v3.19"
  "dahdi-linux-2.10.1-allo.patch"
  "dahdi-linux-2.10.1-openvox.patch"
  "dahdi-linux-2.10.1-yeastar.patch"
)
sha256sums=(
  "94c532e190fc6372f9731df71f8c590fc6f6f184d5394339ce892ac6788843aa"
  "dc8b86ded9963ad825ea7a18bdbcfb0a4758bbc9dcac5fc9cd9bcf58074a0748"
  "626c63d2baad6d7236e98904002d447eefc055b051a87264c4e0175446be7625"
  "853a5d16647fdf4471480fe64b3d072cf21586891c513580635ab53119fceb4d"
  "1830ed6696f6c28f61943d5f037e2f6306237a4a7ba0b6b1c2d5b801378413b8"
)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # enable additional drivers.
  patch -Np1 -i "${srcdir}/dahdi-linux-2.10.1-allo.patch"
  patch -Np1 -i "${srcdir}/dahdi-linux-2.10.1-openvox.patch"
  patch -Np1 -i "${srcdir}/dahdi-linux-2.10.1-yeastar.patch"

  # enable dahdi oslec with upstream echo module.
  mkdir -p drivers/staging/echo
  cp "${srcdir}/oslec.h" drivers/staging/echo/oslec.h
  sed 's|ifneq (,$(wildcard $(src)/../staging/echo/echo.c))|ifneq (,$(wildcard $(src)/../staging/echo/oslec.h))|' -i drivers/dahdi/Kbuild
  sed '\|obj-m += ../staging/echo/echo.o|d' -i drivers/dahdi/Kbuild

  # fix wrong installation paths.
  sed 's,$(DESTDIR)/lib/firmware,$(DESTDIR)/usr/lib/firmware,g' -i drivers/dahdi/firmware/Makefile
  sed 's,$DESTDIR/lib/firmware,$DESTDIR/usr/lib/firmware,g' -i build_tools/install_firmware

  # compile.
  make -j1 DESTDIR="${pkgdir}" all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install-firmware
  make DESTDIR="${pkgdir}" install-include
  make DESTDIR="${pkgdir}" install-xpp-firm
  cd drivers
  find . -name "*.ko" -exec gzip "{}" \; -exec install -D -m 0644 "{}.gz" "${pkgdir}/usr/lib/modules/extramodules-3.19-ARCH/{}.gz" \;
}
