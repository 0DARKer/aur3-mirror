# Maintainer: Maik Broemme <mbroemme@libmpq.org>
pkgname="dahdi-linux"
pkgdesc="DAHDI drivers for Asterisk (Digium, OpenVox, Allo and Yeastar cards)"
pkgver=2.10.0.1
pkgrel=1
arch=("i686" "x86_64")
url="http://www.asterisk.org/"
license=("LGPLv2")
depends=("linux>=3.18" "linux<3.19")
makedepends=("linux-headers>=3.18" "linux-headers<3.19")
conflicts=("dahdi")
install="${pkgname}.install"
source=(
  "http://downloads.asterisk.org/pub/telephony/${pkgname}/${pkgname}-${pkgver}.tar.gz"
  "dahdi-linux-2.10.0.1-allo.patch"
  "dahdi-linux-2.10.0.1-openvox.patch"
  "dahdi-linux-2.10.0.1-yeastar.patch"
)
sha256sums=(
  "7b129903c517fbcb74e9600cd2baaf1721d8684c53525314620db7c0410055fe"
  "626c63d2baad6d7236e98904002d447eefc055b051a87264c4e0175446be7625"
  "853a5d16647fdf4471480fe64b3d072cf21586891c513580635ab53119fceb4d"
  "1830ed6696f6c28f61943d5f037e2f6306237a4a7ba0b6b1c2d5b801378413b8"
)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/dahdi-linux-2.10.0.1-allo.patch"
  patch -Np1 -i "${srcdir}/dahdi-linux-2.10.0.1-openvox.patch"
  patch -Np1 -i "${srcdir}/dahdi-linux-2.10.0.1-yeastar.patch"
  sed 's,smp_mb__after_clear_bit,smp_mb__after_atomic,g' -i drivers/dahdi/wcte12xp/base.c
  sed 's,smp_mb__after_clear_bit,smp_mb__after_atomic,g' -i drivers/dahdi/wcaxx-base.c
  sed 's,smp_mb__after_clear_bit,smp_mb__after_atomic,g' -i drivers/dahdi/wcte13xp-base.c
  sed 's,smp_mb__after_clear_bit,smp_mb__after_atomic,g' -i drivers/dahdi/wcte43x-base.c
  sed 's,$(DESTDIR)/lib/firmware,$(DESTDIR)/usr/lib/firmware,g' -i drivers/dahdi/firmware/Makefile
  sed 's,$DESTDIR/lib/firmware,$DESTDIR/usr/lib/firmware,g' -i build_tools/install_firmware
  make -j1 DESTDIR="${pkgdir}" all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install-firmware
  make DESTDIR="${pkgdir}" install-include
  make DESTDIR="${pkgdir}" install-xpp-firm
  cd drivers
  find . -name "*.ko" -exec install -D -m 0644 {} "${pkgdir}/usr/lib/modules/extramodules-3.18-ARCH/{}" \;
}
