# Maintainer: Claudio Kozicky <claudiokozicky@gmail.com>
# Contributor: Yann Kaiser <kaiser.yann@gmail.com>

pkgname=jamestown
pkgver=1.0.1.164
_pkgver=1_0_1
_timestamp=1324610248
pkgrel=1
pkgdesc="A neo-classical top-down shooter for up to four players set on 17th-century British Colonial Mars."
arch=(i686 x86_64)
url="http://www.finalformgames.com/jamestown/"
license=(custom)
depends=(libgl openal sdl)
source=(hib://jtownlinux_"$_pkgver"_$_timestamp.zip
        jamestown.desktop)
md5sums=(cc8ae55f3231ff7659c3e2ffbb524b7b
         9ea819a8c77b79088a5b983886d7056d)
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')
PKGEXT=.pkg.tar

prepare()
{
    # extract data dir from installer
    cd $srcdir
    bsdtar -xf JamestownInstaller_"$_pkgver"-bin data

    # cleanup
    cd data
    rm -r x86 amd64 Distribution.txt LICENSES.TXT README-linux.txt
    [ $CARCH = x86_64 ] \
        && rm Jamestown-x86 \
        || rm Jamestown-amd64
}

package()
{
    # data
    cd "$srcdir"/data
    find Archives -type f -exec install -Dm644 {} "$pkgdir"/opt/$pkgname/{} \;
    install -m755 -t "$pkgdir"/opt/$pkgname Jamestown Jamestown-*

    # doc
    install -Dm644 README-linux-generic.txt "$pkgdir"/usr/share/doc/$pkgname/README

    # legal
    install -Dm644 Jamestown_EULA.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    # launcher
    install -d "$pkgdir"/usr/bin
    ln -s /opt/$pkgname/Jamestown "$pkgdir"/usr/bin/$pkgname

    # desktop integration
    install -Dm644 $pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
    install -Dm644 ../$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}

# vim:et:sw=4:sts=4
