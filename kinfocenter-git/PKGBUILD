# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kinfocenter-git
pkgver=r812.d7bc706
pkgrel=1
pkgdesc='KInfocenter'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('kdelibs4support-git' 'kcmutils-git' 'plasma-framework-git' 'pciutils' 'glu' 'libraw1394')
makedepends=('extra-cmake-modules' 'git' 'kf5umbrella-git' 'kdoctools-git')
conflicts=('kinfocenter' 'kde-workspace<0')
provides=('kinfocenter')
source=('git://anongit.kde.org/kinfocenter.git')
md5sums=('SKIP')

pkgver() {
  cd kinfocenter
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../kinfocenter/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  rm ${pkgdir}/opt/kf5/etc/xdg/menus/kde-information.menu
  rm ${pkgdir}/opt/kf5/share/desktop-directories/kde-information.directory
}

