# Maintainer: Michael Hansen <zrax0111 gmail com>
# Contributor: ngoonee <ngoonee.talk@gmail.com>
# Contributor: Adam Russell <adamlr6+arch@gmail.com>
pkgname=openchange
_codename=BORG
_dlid=180
pkgver=1.0
pkgrel=7
pkgdesc="Library for MAPI connectivity with Microsoft Exchange. This package was SPECIFICALLY created to support evolution-mapi and may not work for any other purpose."
arch=('i686' 'x86_64')
url="http://www.openchange.org"
license=('GPL3')
depends=('samba>=4.0.0-2' 'libical' 'sqlite3' 'file' 'boost' 'python2')
makedepends=('ccache' 'python2' 'subversion' 'docbook-xsl' 'libxslt'
             'flex' 'bison' 'autoconf' 'automake' 'patch')
options=(!makeflags)
# Releases are mirrored at http://tracker.openchange.org/projects/openchange/files
source=("http://tracker.openchange.org/attachments/download/${_dlid}/${pkgname}-${pkgver}-${_codename}.tar.gz"
        "bugzilla_682449.patch")

md5sums=('9b57a325e90e5ad3a702ddc6e8762ab6'
         '7521f89f8ffaa733ed72532334951b90')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}-${_codename}

	patch -i ../bugzilla_682449.patch

	SAVEIFS=${IFS}
	IFS=$(echo -en "\n\b")
	PYTHON_CALLERS="$(find ${srcdir}/${pkgname}-${pkgver}-${_codename} -name '*.py')
$(find ${srcdir}/${pkgname}-${pkgver}-${_codename}/  -type f)
$(find ${srcdir}/${pkgname}-${pkgver}-${_codename}/ -name 'configure.ac')"
	sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
	       -e "s|python-config$|python2-config|" \
	       -e "s|bin/python$|bin/python2|" \
	    ${PYTHON_CALLERS}

	# Patch for samba4-alpha19+
	SRC_FILES="$(find ${srcdir}/${pkgname}-${pkgver}-${_codename} -name '*.c')"
	sed -i -e "s|#include <samba/popt.h>|#include <popt.h>|" ${SRC_FILES}
	IFS=${SAVEIFS}

	export PYTHON=/usr/bin/python2

	export PKG_CONFIG_PATH="/usr/samba/lib/pkgconfig:/usr/lib/pkgconfig"
	export BOOST_LIB_SUFFIX="-mt"
	./autogen.sh
	./configure --prefix=/usr
	make FLEX=/usr/bin/flex BISON=/usr/bin/bison || return 1
}

package() {
	_pyver=`python2 -c 'import sys; print(sys.version[:3])'`

	cd ${srcdir}/${pkgname}-${pkgver}-${_codename}
	make DESTDIR="$pkgdir/" install

	cd ${pkgdir}/usr/lib/
	ln -s libmapi.so libmapi.so.0
	ln -s libocpf.so libocpf.so.0

	find ${pkgdir}/usr/lib/python${_pyver}/site-packages/ -name '*.py' | \
	     xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
}
