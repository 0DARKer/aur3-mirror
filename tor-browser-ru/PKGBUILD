# Maintainer: Kovivchak Evgen <oneonfire@gmail.com>

pkgname='tor-browser-ru'
pkgver='2.2.35_rc7.2'
_realpkgver='2.2.35-7.2-dev'
_language='ru'
pkgrel='1'
pkgdesc='Lets you use Tor with Firefox (+ Torbutton, NoScript, HTTPS-Everywhere) without the need to configure anything. Russian version.'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-environment' 'xulrunner')
install='tor-browser-ru.install'
if [[ "$CARCH" == 'i686' ]]; then
	source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")
	sha256sums=('e3a4f64e25a396cd68f0a0afa74093ee70eb7524c32c4e33ab88c543ce144051')
else
	source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")
	sha256sums=('c632aafa3d7a57f24a03cd5ad6445ca07fb7417499379d56643753bf878888d6')
fi
source+=("${pkgname}.desktop"
		"${pkgname}.png"
		"${pkgname}.sh")
sha256sums+=('f504d644bfe386fe1abe05486ee1c8054d3fafbc9845f40816b512b665e21a13'
			'17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			'41491ffb657362214bf0f5101c149b9bb6bcaf24dde9882554b30d12ad0a2874')
options=('!strip' 'emptydirs')

package() {
	cd ${srcdir}/tor-browser_ru

	install -d ${pkgdir}/opt/${pkgname}/ ${pkgdir}/usr/bin/

	# Any way to use install here?
	cp -a * ${pkgdir}/opt/${pkgname}/

	# Other files
	install -Dm644 ../${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm644 ../${pkgname}.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm755 ../${pkgname}.sh ${pkgdir}/usr/bin/${pkgname}
}

# vim:set ts=2 sw=2 et:
