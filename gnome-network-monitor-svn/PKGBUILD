# Maintainer: JSH <jsh6789 at gmail dot com>
# Contributor: fancris3 <fancris3 at aol.com>
# Contributor: Lucas Sali√©s Brum <lucas@archlinux.com.br>

pkgname=gnome-network-monitor-svn
pkgver=0.9.1.r125
pkgrel=2
pkgdesc="A Network Monitor for GNOME"
arch=('any')
url="http://gnetworkmonitor.sourceforge.net/"
license=('GPL')
depends=('pygtk' 'python2-pysqlite' 'usermode>=1.36')
makedepends=('intltool' 'gettext' 'python2' 'desktop-file-utils')
options=(!emptydirs)
install=$pkgname.install
source=("$pkgname::svn+https://svn.code.sf.net/p/gnetworkmonitor/code/trunk")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  local ver="$(awk '/Version:/ { print $2 }' ${pkgname%%-svn}.spec)"
  local rev="$(svnversion)"
  printf "%s.r%s" "$ver" "${rev//[[:alpha:]]}"
}

prepare() {
  cd "$pkgname"
  
  # python2 fix
  sed -i 's|\(\W\)python|\1python2|g' Makefile
  sed -i 's|^#!/usr/bin/env python|#!/usr/bin/env python2|g' gnome-network-monitor gnm/*.py
}

#check() {
#  cd "$pkgname/gnm"
#  
#  python2 unittesting.py
#}

package() {
  cd "$pkgname"
  
  make DESTDIR="$pkgdir" install
}