# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Luiz Ribeiro <luizribeiro@gmail.com>

pkgname=luagl
pkgver=1.6
pkgrel=1
pkgdesc="A library that provides access to all of the OpenGL (up to version 1.3) functionality from Lua."
arch=('i686' 'x86_64')
url="http://luagl.sourceforge.net"
license=('MIT')
depends=('lua' 'glut' 'libxmu')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-${pkgver}_Sources.tar.gz")
md5sums=('c2562f844bf3757124b63613b3521057')

build() {
  cd "$srcdir/$pkgname"

  make
}

package() {
  cd "$srcdir/$pkgname"

  # libs
  install -d "$pkgdir/usr/lib/lua/5.1"
  install -Dm755 src/*.so "$pkgdir/usr/lib/lua/5.1"
  install -d "$pkgdir/usr/lib"
  install -Dm755 lib/*/*.so "$pkgdir/usr/lib"
  ln -s /usr/lib/lua/5.1/$pkgname.so "$pkgdir/usr/lib"

  # headers
  install -d "$pkgdir/usr/include"
  install -Dm644 include/* "$pkgdir/usr/include"

  # license
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname"
}