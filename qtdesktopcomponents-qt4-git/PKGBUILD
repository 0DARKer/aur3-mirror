# Maintainer: spider-mario <spidermario@free.fr>
pkgname=qtdesktopcomponents-qt4-git
pkgver=20130322
pkgrel=1
pkgdesc="QtÂ Quick components styled with QStyle"
arch=('i686' 'x86_64')
url="http://qt-project.org/wiki/QtDesktopComponents"
license=('custom:BSD')
depends=('qt4')
makedepends=('git')
provides=('qtdesktopcomponents-qt4' 'qt-components-desktop-qt4' 'qt-desktop-components-qt4')
conflicts=('qtdesktopcomponents-qt4' 'qt-components-desktop-qt4' 'qt-desktop-components-qt4')

_gitname=qtdesktopcomponents
_gitroot=https://git.gitorious.org/qt/qtquickcontrols.git

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git checkout qt4 && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname" && git checkout qt4
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  qmake-qt4
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make INSTALL_ROOT="$pkgdir/" install

  install --directory "$pkgdir/usr/bin"
  install qmldesktopviewer/qmldesktopviewer "$pkgdir/usr/bin/"

  install --directory "$pkgdir/usr/share/licenses/$pkgname"
  cp header.BSD "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
