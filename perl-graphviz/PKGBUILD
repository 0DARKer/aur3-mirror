# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: antoniolucas <antoniolucasnobar at gmail dot com>

_perlmod=GraphViz
pkgname=perl-graphviz
pkgver=2.14
pkgrel=1
pkgdesc="GraphViz - Interface to AT&T's GraphViz. Deprecated. See GraphViz2"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl-file-which>=1.09'
         'perl-ipc-run>=0.6'
         'perl-parse-recdescent>=1.965001'
         'perl-xml-twig>=3.38'
         'perl-xml-xpath>=1.13')
makedepends=('perl-module-build>=0.3800' 'perl-test-pod>=1.44')
source=("http://cpan.perl.org/modules/by-module/$_perlmod/$_perlmod-${pkgver}.tgz")
sha512sums=('f0a9c14dcb4456fd426e6c8adaa2757a057800fad8022f9f6e43b275b9cc8b4a31319e83bf659b4d0d608e400caa3268fb884fc7db437f068d2b96111a5c134e')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	perl Build.PL installdirs=vendor destdir="$pkgdir/"
	perl Build
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	perl Build test
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	perl Build install
}
