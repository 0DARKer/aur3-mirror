# Maintainer: antoniolucas <antonioluccas at gmail dot com>
 
pkgname=perl-graphviz
_realname=GraphViz
pkgver=2.04
pkgrel=1
pkgdesc="Perl/CPAN GraphViz Module"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
license=('GPL' 'Artistic')
depends=('perl>=5.10.0' 'perl-ipc-run' 'graphviz')
source=("http://www.cpan.org/authors/id/L/LB/LBROCARD/${_realname}-${pkgver}.tar.gz")
md5sums=("4ef406a3deca074c2a2b9237e4ae54b0")
options=(!emptydirs)
 
build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # Install module into the vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}
 
package() {
  cd "${srcdir}/${_realname}-${pkgver}"
 
  make DESTDIR="${pkgdir}" install
 
  # Remove .packlist and perllocal.pod files.
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name 'perllocal.pod' -delete
}