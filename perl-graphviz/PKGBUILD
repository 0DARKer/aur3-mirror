# Maintainer: antoniolucas <antoniolucasnobar at gmail dot com>
 
pkgname=perl-graphviz
_realname=GraphViz
pkgver=2.08
pkgrel=1
pkgdesc="Perl/CPAN GraphViz Module"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
license=('GPL' 'Artistic')
depends=('perl>=5.10.0' 'perl-file-which>=1.09' 'perl-ipc-run' 'graphviz')
source=("http://search.cpan.org/CPAN/authors/id/R/RS/RSAVAGE/${_realname}-${pkgver}.tgz")
md5sums=("28523a39bfbc9e63bb451ee27718d918")
options=(!emptydirs)
 
build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # Install module into the vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}
 
package() {
  cd "${srcdir}/${_realname}-${pkgver}"
 
  make DESTDIR="${pkgdir}" install
 
  # Remove .packlist and perllocal.pod files.
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name 'perllocal.pod' -delete
}