# Maintainer: Florian LÃ©ger <florian6 dot leger at laposte dot net>
# Contributor: Vic Demuzere <vic@demuzere.be>

pkgname=minecraft-server
pkgver=1.4.6
pkgrel=2
epoch=1
pkgdesc="Official Minecraft server"
arch=(any)
license=('custom')
url="http://www.minecraft.net/"
depends=('java-runtime-headless' 'expect' 'detachtty')
optdepends=('java-runtime: for GUI support')
source=("https://s3.amazonaws.com/MinecraftDownload/launcher/minecraft_server.jar"
        "minecraft-server"
        "minecraft-server-rc"
        "minecraft-server.conf")
md5sums=('b0700fa969dbf0ff085082cde87384d2'
         '40194f9c13854a803fe318b97b4f82bb'
         '75cf675e7d217b113a6287b1496faefd'
         '415134c2d9beb74fbf509dada83d0ff8')
noextract=("minecraft_server.jar")
options=(emptydirs)
install="minecraft-server.install"

package() {
  install -Dm755 "${srcdir}/minecraft-server" "${pkgdir}/usr/bin/minecraft-server"
  install -Dm644 "${srcdir}/minecraft_server.jar" "${pkgdir}/usr/share/java/minecraft-server/minecraft-server.jar"
  install -Dm755 "${srcdir}/minecraft-server-rc" "${pkgdir}/etc/rc.d/minecraft-server"
  install -Dm644 "${srcdir}/minecraft-server.conf" "${pkgdir}/usr/lib/tmpfiles.d/minecraft-server.conf"

  install -m755 -d "${pkgdir}/srv/minecraft"
  install -m755 -d "${pkgdir}/run/minecraft-server"
  install -m750 -d "${pkgdir}/var/log/minecraft-server"
}
