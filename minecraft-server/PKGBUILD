# Maintainer: Philip Abernethy <chais.z3r0@gmail.com>

pkgname=minecraft-server
pkgver=1.8.3
pkgrel=1
pkgdesc="Minecraft server unit files, script, and jar"
arch=(any)
url="http://minecraft.net/"
license=('custom')
depends=('java-runtime-headless' 'systemd' 'screen' 'expect')
conflicts=('minecraft-server-systemd' 'minecraft-canary')
options=(emptydirs)
install=minecraft-server.install
backup=('etc/conf.d/minecraft')
source=(https://s3.amazonaws.com/Minecraft.Download/versions/${pkgver}/minecraft_server.${pkgver}.jar
        minecraftd
        minecraftd-diag
        minecraftd.service
		minecraftctl
		conf.minecraft)
noextract=("minecraft_server.${pkgver}.jar")  
sha256sums=('a26751b18ccc80ceef488da645c3b785aa528e2ae20a6a6dbd46f6dc754e62bd'
            '4dd347fd864511e8e3dc0256fd0416263ce3d59a167639c63b4d8eb3e830eba0'
            'de03317d02668bb71ec160fff497c7fbfe8b03ffff2950c6a0bcfb39f69cd214'
            '6a28d6a04feee4eb055b9d2d302645660f863291bd1f36e3a7986330131adb81'
            '472e06e1bd63838e88ace0bd2f971f72f2efd9e541cc0956599a44c324fd49ac'
            '738e10185df160d190a5f4f0226de110d67ba023877e4999a6d3ef41c9918ed6')

package() {
  install -Dm755 "${srcdir}/minecraftd" "${pkgdir}/usr/bin/minecraftd"
  install -Dm755 "${srcdir}/minecraftd-diag" "${pkgdir}/usr/bin/minecraftd-diag"
  install -Dm755 "${srcdir}/minecraftctl" "${pkgdir}/usr/bin/minecraftctl"
  
  install -Dm644 "${srcdir}/minecraft_server.${pkgver}".jar "${pkgdir}/srv/minecraft/minecraft_server.${pkgver}".jar
  ln -s "minecraft_server.${pkgver}.jar" "${pkgdir}/srv/minecraft/minecraft_server.jar"
  install -Dm644 "${srcdir}/minecraftd.service" "${pkgdir}/usr/lib/systemd/system/minecraftd.service"
  install -Dm644 "${srcdir}/conf.minecraft" "${pkgdir}/etc/conf.d/minecraft"

  install -d "${pkgdir}/srv/minecraft/backup"
}
