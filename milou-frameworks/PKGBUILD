# Contributor: Antonio Rojas

pkgname=milou-frameworks
pkgver=4.96.0
pkgrel=1
pkgdesc="A dedicated search application built on top of Baloo"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/base/milou/'
license=('LGPL')
depends=('baloo-frameworks' 'qt5-webkit')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/milou-$pkgver.tar.xz")
md5sums=('552caae6639502b1387c9da9a6be665f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../milou-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
