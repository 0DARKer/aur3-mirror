# Maintainer: Tom Richards <tom@tomrichards.net>
# https://github.com/t-richards/aur-mcserver-git

pkgname=mcserver-git
pkgver=r7469.bcb839d
pkgrel=1
pkgdesc='A performant C++ Minecraft Server'
arch=('x86' 'x86_64')
url='http://mc-server.org'
license=('Apache')
makedepends=('cmake')
install="$pkgname.install"
source=("$pkgname"::'git+https://github.com/mc-server/MCServer'
        'service')
sha256sums=('SKIP'
            '38b848d71d3bd1630bd29c257619dec88b353b75beafeaeed9b95e33c898d862')

pkgver() {
    cd "$srcdir/$pkgname"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$srcdir/$pkgname"
    git submodule init
    git submodule update
}

build() {
    cd "$srcdir/$pkgname"
    cmake . -DBUILD_TOOLS=1 -DSELF_TEST=1
    make
}

check() {
    cd "$srcdir/$pkgname"
    make test
}

package() {
    cd "$srcdir/$pkgname"
    install -d "$pkgdir"/srv/mcserver
    cp -R MCServer/* "$pkgdir"/srv/mcserver/.

    install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/mcserver.service
}
