# Maintainer:  Devin Cofer <ranguvar@archlinux.us>
# Contributor: wantilles <wantilles@adslgr.com>
# Contributor: blasse <koralik(at)gmail(dot)com>

pkgname=firefox-pgo-beta
_mozver=2.0
_ffmajorver=4.0
_ffver=${_ffmajorver}rc1
_build=build1
pkgver=${_ffver}${_build}
pkgrel=1
pkgdesc="Mozilla Firefox customizable web browser (XULRunner independent, PGO optimized, 64-bit TraceMonkey, beta)"
url="http://www.mozilla.org/projects/firefox"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')

_soundsystem='alsa-lib'  # 'alsa-lib' for ALSA, 'oss' for OSS

makedepends=('autoconf2.13' 'gcc>=4.4' 'zip' 'pkgconfig' 'diffutils'
             'libgnomeui>=2.24.1' 'python2' 'wireless_tools' 'yasm>=0.7.1'
             'mesa' 'xorg-server-xvfb')
depends=('gtk2>=2.18.0' 'gcc-libs>=4.4' 'libidl2>=0.8.13' 'mozilla-common'
         'nss>=3.12.4' 'libxt' 'hunspell>=1.2.8' 'startup-notification>=0.10'
         'libnotify>=0.4' 'mime-types' 'dbus-glib>=0.82' 'desktop-file-utils'
         'cairo-tee>=1.10' 'libpng>=1.4.0' 'libevent>=2.0' ${_soundsystem})
provides=("firefox=$pkgver" "firefox-pgo=$pkgver" "firefox-beta=$pkgver")
conflicts=('firefox' 'firefox-pgo' 'firefox-beta' 'firefox-pgo-minefield' 'firefox-pgo-minefield-smp')

source=("ftp://ftp.mozilla.org/pub/mozilla.org/firefox/nightly/${_ffver}-candidates/${_build}/source/firefox-${_ffver}.source.tar.bz2"
        'mozconfig'
        'firefox.desktop'
        'firefox-safe.desktop'
        'firefox.install'
        'fix-mozilla-launcher.patch'
        'mozilla-firefox-1.0-lang.patch'
        'ldflags-namespec.patch'
	'jemalloc-enable-pgo.patch')
sha256sums=('8bae4af5d364c6caa624ad106f532f046f23e6187281cf87d8d4ed5d899f100f'
            '6df78c5882627b7c1d990ce947c21637c7bbd2bf9717e24c08a52c7ed806f6e6'
            '83a14017a6a09491db22b9d6972e0bd39f528c6a79fe8695d6fc5f9350c8e293'
            '4b6de45753856a890f4482055666e77f9b01bdfb7e0df08bafaa3a4d9937eed3'
            '4fa604c579cb90ceedd6385e7757a56187f9696f0c62ff415e7ef7aec7b92179'
            'd4948cc5878b2100b4d19b0fbc09119c34377593c5847678d5788db2b4e0fe43'
            '0ca095ff2af57297f615877a7e79ddc84d1a3f62509a8af6ca50aad7a8671f6a'
            '0e9631fdad5efa3fd7a95b59171f5d15420d10aa61748b920cc994ee9227915c'
	    'e6b8345215eb0c595cadfd6b1abb3a12a1cad8b8b1f3528e6affc58900695215')
install='firefox.install'


build() {
	# 93 length = 80 on output
	msg "* Note: If the build fails, try again, try without jemalloc PGO, and try without"
	msg "* PGO at all. PGO can be very temperamental -- it can sometimes take two or"
	msg "* three builds before you'll get a good one."
	sleep 5
	cd "$srcdir"/mozilla-$_mozver

	cp "$srcdir"/mozconfig .mozconfig
	# Don't strip if the user doesn't want us to... ;p
	if [ "$(check_option strip)" = "n" ]; then
		sed -i 's/--enable-strip/--disable-strip/' .mozconfig
		sed -i 's/--enable-install-strip/--disable-install-strip/' \
			.mozconfig
	fi

	if [ "$_soundsystem" = "oss" ]; then
		msg2 "Using OSS instead of ALSA."
		sed -i 's/sydney_audio_alsa/sydney_audio_oss/' \
		       media/libsydneyaudio/src/Makefile.in
		# Get rid of ALSA config system stuff (requires autoconf rerun)
		sed -i '/alsa\//d' config/system-headers
		sed -i '/alsa\//d' js/src/config/system-headers
		sed -i '/LIB(asound/d' configure.in
	fi


	msg2 "Patching source."
	msg2 "Patches from distro packages..."
	# Fix stub launcher - Arch
	patch -Np0 -i "$srcdir"/fix-mozilla-launcher.patch
	# Use LANG environment variable to choose locale
	patch -Np1 -i "$srcdir"/mozilla-firefox-1.0-lang.patch


	msg "Patches from this package..."
	# PGO compilation LDFLAGS fix
	patch -Np1 -i "$srcdir"/ldflags-namespec.patch
	# Enable experimental PGO for jemalloc (speed).
	# See Mozilla bugs #418866 and #419470.
	patch -Np0 -i "$srcdir"/jemalloc-enable-pgo.patch


	msg2 "Setting up build."
	# Changing the user's optimization flags is justified, because this is
	# a package specifically for an optimized software build, and because of
	# the official branding, binaries can't be redistributed anyways.
	# These flags just set guidelines for the build, they are overridden in
	# most compile job pieces by Firefox's better judgement.
	export CFLAGS="-march=native -O2 -pipe"
	export CXXFLAGS="-march=native -O2 -pipe"
	# The hash-style and as-needed flags are in Arch defaults anyways,
	# and the other optimization flags are almost definitely safe.
	export LDFLAGS="-Wl,-rpath,/usr/lib/firefox-$_ffmajorver -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"
	export PYTHON=python2

	autoconf-2.13


	msg2 "Actual build."
	# Yes, all this is SMP -- MOZ_MAKE_FLAGS takes care of it.
	# Compile a non-PGO build first to reduce chance of error in PGO build.
	MOZ_MAKE_FLAGS="$MAKEFLAGS" make -j1 -f client.mk build
	# Comment out remaining lines for a non-PGO build.
	msg2 "Profiled build now."
	# Don't let PGO disrupt the user, use a virtual framebuffer.
	LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX :99 &
	XPID=$!  # Store the PID of the framebuffer
	DISPLAY=:99 LD_PRELOAD="" MOZ_MAKE_FLAGS="$MAKEFLAGS" \
		make -j1 -f client.mk profiledbuild \
		PYTHONPATH='$(OBJDIR)/_profile/pgo'
#	RC=$?  # Store the make return code
#	kill -9 $XPID  # Kill the framebuffer
#	if [ $RC != 0 ]; then  # Build did not succeed, fail
#		return 1
#	fi
}
package() {
	cd "$srcdir"/mozilla-$_mozver

	make -j1 DESTDIR="$pkgdir" -C ff-pgo install

	install -Dm644 "$srcdir"/mozilla-$_mozver/other-licenses/branding/firefox/mozicon128.png \
		"$pkgdir"/usr/share/pixmaps/firefox.png
	install -Dm644 "$srcdir"/firefox.desktop \
		"$pkgdir"/usr/share/applications/firefox.desktop
	install -Dm644 "$srcdir"/firefox-safe.desktop \
		"$pkgdir"/usr/share/applications/firefox-safe.desktop

	# Remove devel stuff.
	rm -rf "$pkgdir"/usr/include/
	rm -rf "$pkgdir"/usr/lib/firefox-devel-$_ffmajorver/
	rm -rf "$pkgdir"/usr/share/idl/
}
