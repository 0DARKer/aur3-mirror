# Maintainer: Rudolf Polzer <divVerent@xonotic.org>
pkgname=kxstitch-kde4
pkgver=0.9.2
pkgrel=1
pkgdesc="The program that lets you create cross stitch patterns and charts."
arch=('i686' 'x86_64')
depends=('kdebase-runtime' 'imagemagick')
makedepends=('cmake' 'automoc4' 'doxygen' 'patch')
url="http://kxstitch.sourceforge.net/"
license=('GPL')
source=(http://downloads.sourceforge.net/project/kxstitch/kxstitch/${pkgver}/kxstitch-${pkgver}-KDE4.tar.gz)
md5sums=('04e25211687835cf61e3ba7e56af54ef')
install=kxstitch-kde4.install
conflicts=('kxstitch')

build() {
    cd "$srcdir/kxstitch-${pkgver}-KDE4"
    rm -rf build
    mkdir build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=`kde4-config --prefix` .. \
        -DImageMagick_Magick++_INCLUDE_DIR:PATH=/usr/include/ImageMagick-6 \
        -DImageMagick_Magick++_LIBRARY:FILEPATH=/usr/lib/libMagick++-6.Q16.so \
        -DImageMagick_MagickCore_INCLUDE_DIR:PATH=/usr/include/ImageMagick-6 \
        -DImageMagick_MagickCore_LIBRARY:FILEPATH=/usr/lib/libMagickCore-6.Q16.so \
        -DCMAKE_CXX_FLAGS:STRING="-DMAGICKCORE_HDRI_ENABLE=0 -DMAGICKCORE_QUANTUM_DEPTH=16"
        # ^^^ workaround for broken cmake, remove once cmake is fixed
        #     see: https://bugs.archlinux.org/task/35280
    make
}

package() {
    cd "$srcdir/kxstitch-${pkgver}-KDE4/build"
    make DESTDIR="${pkgdir}" install

    # this one is unnecessary and namcap hates this dangling link
    rm "${pkgdir}/usr/share/doc/kde/html/en/kxstitch/common"
}
