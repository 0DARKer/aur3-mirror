pkgname=geckofx-hg
pkgver=75
pkgrel=1
pkgdesc="Windows Forms control that embeds the Mozilla Gecko browser control in any Windows Forms Application. (hg)"
url="https://bitbucket.org/hindlemail"
arch=(any)
license=("MPL 1.1")
depends=(gtk-sharp-2 mono)
makedepends=(mono mercurial)
options=(!strip)
_hgroot=$url
_hgrepo=geckofx

build() {
  cd $srcdir
  msg "Connecting to hg server..."
  if [[ -d "$_hgrepo/.hg" ]]; then
    msg "pull"
    ( cd $_hgrepo && hg pull -u )
  else
    msg "clone"
    hg clone "${_hgroot}/${_hgrepo}"
  fi
  cd "${_hgrepo}"

  msg "Mercurial checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_hgrepo}-build"
  cp -r "${srcdir}/${_hgrepo}" "${srcdir}/${_hgrepo}-build"
  cd "${srcdir}/${_hgrepo}-build/Skybound.Gecko"
  xbuild Skybound.Gecko.sln
}

package() {
  cd "$srcdir/${_hgrepo}-build/Skybound.Gecko/bin/x86/Debug_Linux"
  install -Dm644 Skybound.Gecko.dll "$pkgdir/usr/lib/GeckoFX/Skybound.Gecko.dll"
  gacutil -i Skybound.Gecko.dll -root "$pkgdir/usr/lib"
}
