# Maintainer:  Martin C. Doege <mdoege at compuserve dot com>

pkgname=gophervr
pkgver=0.4.1
pkgrel=8
pkgdesc="A 3-D interface to Gopherspace"
arch=('i686' 'x86_64')
url="gopher://gopher.floodgap.com/1/gophervr"
license=('GPL')
depends=('openmotif')
makedepends=('curl' 'unzip' 'imake' 'autoconf')
optdepends=('xterm: for telnet connections'
	    'inetutils: for telnet connections'
	    'xv: for image display'
	    'sox: for audio playback'
	    'lynx: for HTML files')
source=("Makefile.patch"
        "configure.patch"
        "helpdiag.h.patch"
        "vogl.h.patch"
        "hershey_path.patch"
        "gopherto3d.c.patch"
        "gophervr.desktop"
        "gophervr_icon.png")
md5sums=('acbb60dc127d4294d0ae404be6fd865b'
         '556cb62f09b9ffd6010b49062c33d639'
         '1143af9faa2a53355f480382b6a2b0e0'
         'bf0bd46b2015cf678e3264ce8cd077d0'
         '1985131a840223fea895f46803650c8c'
         'eec1e950b57ca896ff17726e40f09de9'
         '357365c3e59262d92e1d0382cd842e53'
         '05f51b00d10f6dac04ca5d03b62e88e1')

build() {
  cd $srcdir
  curl -O "gopher://gopher.floodgap.com/5/gophervr/all-versions/gophervr-"$pkgver"-src.zip"
  unzip "gophervr-"$pkgver"-src.zip"
  cd $srcdir/gophervr/gopher
  patch configure.in $srcdir/configure.patch
  autoconf configure.in > configure
  cd $srcdir/gophervr
  patch gophervr/Makefile $srcdir/Makefile.patch
  patch gophervr/gopherto3d.c $srcdir/gopherto3d.c.patch
  patch gophervr/helpdiag.h $srcdir/helpdiag.h.patch
  patch libvogl/src/vogl.h $srcdir/vogl.h.patch
  patch libvogl/Makefile $srcdir/hershey_path.patch
  make linux
}

package() {
  cd "$srcdir/gophervr/gophervr"
  mkdir -p $pkgdir/usr/bin
  cp gophervr $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/share/hershey
  cp futura.hfont $pkgdir/usr/share/hershey
  mkdir -p $pkgdir/usr/share/doc/gophervr
  cp gophervr.help $pkgdir/usr/share/doc/gophervr
  cd $srcdir
  mkdir -p $pkgdir/usr/share/icons
  cp gophervr_icon.png $pkgdir/usr/share/icons
  mkdir -p $pkgdir/usr/share/applications
  cp gophervr.desktop $pkgdir/usr/share/applications
}
