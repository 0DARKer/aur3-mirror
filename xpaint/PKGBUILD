# Contributor: erm67
# Contributor: Stefan Husmann <Stefan-Husmann@t-online.de>
pkgname=xpaint
pkgver=2.9.9.4
pkgrel=2
pkgdesc="A color image editing tool which features most standard \
paint program options."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/sf-xpaint/"
license=('GPL')
depends=('libxaw3dxft' 'openjpeg' 'libpng' 'libtiff' 'libxft')
makedepends=('imake')
optdepends=('gv: external viewer for PostScript output' 'imagemagick: external viewer for pixel graphics')
source=(xpaint.desktop \
http://downloads.sourceforge.net/sourceforge/sf-$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('3c532dc3bb0b66afc022cbb58db609af'
         'c9d14260a095b96f82b1aa6292816db6')

build() {
  cd $srcdir/$pkgname-$pkgver
  [ -L xaw_incdir ] && rm xaw_incdir
  ln -s /usr/include/X11/Xaw3dxft xaw_incdir 
  ./configure --prefix=/usr 
  make 
}
package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm644 XPaintIcon.xpm $pkgdir/usr/share/pixmaps/XPaintIcon.xpm
  install -Dm644 $srcdir/$pkgname.desktop \
    $pkgdir/usr/share/applications/$pkgname.desktop
  rm $pkgdir/usr/share/xpaint/XPaintIcon.xpm
}
