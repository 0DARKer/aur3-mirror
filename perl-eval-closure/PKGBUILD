# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-eval-closure
_lastauthor=D/DO/DOY
_pkgname=Eval-Closure
pkgver=0.10
pkgrel=1
pkgdesc="safely and cleanly create closures via string eval"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-try-tiny' 'perl-sub-exporter')
checkdepends=('perl-test-requires' 'perl-test-fatal')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('df8a0e15408032630c4c8159da1b6dc3b093c341924720aa0653f8d11e32716b')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Makefile.PL
  make
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install
}
