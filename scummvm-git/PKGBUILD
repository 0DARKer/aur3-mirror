# Maintainer: Thomas Krug <phragment@lavabit.com>
# Contributor: Joe Davison <joedavison.davison@gmail.com>

pkgname=scummvm-git
_gitname=scummvm
pkgver=20130501
pkgrel=3
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('i686' 'x86_64')
url="http://www.scummvm.org"
license=('GPL')
depends=('sdl' 'libmad' 'fluidsynth' 'libgl' 'libtheora' 'faad2')
makedepends=('git')
conflicts=('scummvm')
provides=('scummvm')
source=('git://github.com/scummvm/scummvm.git')
md5sums=('SKIP')
install=${pkgname}.install
 
pkgver() {
  cd $_gitname

  date +%Y%m%d
}
 
build() {
  cd $_gitname
 
  ./configure --backend=sdl \
              --with-flac-prefix=/usr \
              --prefix=/usr \
              --enable-all-engines || return 1

  make || return 1
}
 
package() {
  cd $_gitname
 
  make DESTDIR=${pkgdir} install

  install -D -m 644 dists/scummvm.desktop ${pkgdir}/usr/share/applications/scummvm.desktop
}

# vim:set ts=2 sw=2 et:
