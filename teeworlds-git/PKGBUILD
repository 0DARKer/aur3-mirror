# Maintainer: Lari Tikkanen <lartza@wippies.com>
pkgname=teeworlds-git
pkgver=20121207
pkgrel=1
pkgdesc="A multiplayer 2D shooter (Git version)"
arch=('i686' 'x86_64')
url="http://teeworlds.com"
license=('custom')
depends=('mesa' 'sdl')
makedepends=('python' 'git' 'bam')
replaces=('teewars')
conflicts=('teeworlds')
source=(teeworlds.desktop
        teeworlds.png)
md5sums=('3944aaf7a8c19a83510b2b39c4cddf5f'
         '44f39e398409232a0e0a431ed46d8cc8')

_gitroot="git://github.com/teeworlds/teeworlds.git"
_gitname="teeworlds"

build() {
	cd ${srcdir}
	
	if [[ -d $srcdir/$_gitname ]] ; then
		cd $_gitname
		git pull origin
	else
		git clone $_gitroot $_gitname
	fi

	cd ${srcdir}/${_gitname}
	bam release
}

package() {
	cd ${srcdir}/${_gitname}

	mkdir -p ${pkgdir}/usr/share/${_gitname}/data
	cp -r ${srcdir}/${_gitname}/data/* \
		${pkgdir}/usr/share/${_gitname}/data

	install -Dm755 ${srcdir}/${_gitname}/${_gitname} \
		${pkgdir}/usr/bin/${_gitname}
	install -Dm755 ${srcdir}/${_gitname}/${_gitname}_srv \
		${pkgdir}/usr/bin/${_gitname}_srv

	install -Dm644 ${srcdir}/${_gitname}.desktop \
		${pkgdir}/usr/share/applications/${_gitname}.desktop
	install -Dm644 ${srcdir}/${_gitname}.png \
		${pkgdir}/usr/share/pixmaps/${_gitname}.png
	install -Dm644 ${srcdir}/${_gitname}/license.txt \
		${pkgdir}/usr/share/licenses/${_gitname}/license.txt
}
