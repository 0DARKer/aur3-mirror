# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=allegro
pkgname=lib32-${_pkgbase}
pkgver=5.0.6
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming (32 bit)"
arch=('x86_64')
url="http://alleg.sourceforge.net/"
license=('custom')
depends=('lib32-libxcursor' 'lib32-libpng' 'lib32-libgl' 'lib32-libjpeg'
	 'lib32-alsa-lib' 'lib32-freetype2' 'lib32-libvorbis' 'lib32-flac'
	 "${_pkgbase}")
makedepends=('cmake' 'gcc-multilib' 'lib32-mesa' 'lib32-jack'
	  'lib32-libxxf86vm' 'lib32-libxxf86dga' 'lib32-libxpm')
options=('!makeflags')
source=(http://downloads.sourceforge.net/alleg/${_pkgbase}-${pkgver}.tar.gz)
sha256sums=('d981a1cac937085e6636e597c492ddb743066516c1d215c2a39e4049bd70dc24')

build() {
  cd "${srcdir}"
  
  rm -rf build
  mkdir build && cd build

  cmake "../${_pkgbase}-${pkgver}" \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	-DWANT_DOCS=OFF \
  	-DLIB_SUFFIX="32" \
	-DCMAKE_C_FLAGS="$CFLAGS -m32" \
	-DCMAKE_CXX_FLAGS="$CXXFLAGS -m32" \
	-DWANT_PULSEAUDIO=OFF \
	-DWANT_NATIVE_DIALOG=OFF \
	-DWANT_X11_XINERAMA=OFF \
	-DWANT_X11_XRANDR=OFF \
	-DWANT_PHYSFS=OFF \
	-DWANT_OPENAL=OFF
	
  # The lastest 'OFF' parameters were added to this lib32 package to avoid
  # extra dependencies if compared to package 'allegro' (ex: lib32-libpulse)

  make
}

package() {
  cd "${srcdir}/build"
  
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/include"

  install -d "${pkgdir}/usr/share/licenses"
  ln -s ${_pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"
}

