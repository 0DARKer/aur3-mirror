_pkgbasename=allegro
pkgname=lib32-$_pkgbasename
pkgver=4.4.1.1
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming (32 bit, library only)"
arch=('x86_64')
url="http://alleg.sourceforge.net/"
license=('custom')
depends=('lib32-jack' 'lib32-libxpm' 'lib32-libxxf86vm' 'lib32-libxxf86dga'
		 'lib32-libxcursor' 'lib32-libpng' "$_pkgbasename>=$pkgver")
makedepends=('cmake' 'gcc-multilib')
options=('!makeflags')
source=(http://downloads.sourceforge.net/alleg/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('0f1cfff8f2cf88e5c91a667d9fd386ec')

build() {
  cd "${srcdir}"
  mkdir build && cd build

  cmake "../${_pkgbasename}-${pkgver}" \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DLIB_SUFFIX="32" \
	-DCMAKE_C_FLAGS="-m32" \
	-DCMAKE_CXX_FLAGS="-m32" \
	-DWANT_DOCS=OFF

  make
}

package() {
  cd "${srcdir}/build"
  
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include,lib32/*.a}
  mkdir -p ${pkgdir}/usr/share/licenses
  ln -fs ${_pkgbasename} ${pkgdir}/usr/share/licenses/${pkgname}
}
