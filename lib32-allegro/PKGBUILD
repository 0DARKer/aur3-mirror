_pkgbasename=allegro
pkgname=lib32-$_pkgbasename
pkgver=5.0.0
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming (32 bit, library only)"
arch=('x86_64')
url="http://alleg.sourceforge.net/"
license=('custom')
depends=('lib32-jack' 'lib32-libxpm' 'lib32-libxxf86vm' 'lib32-libxxf86dga'
		 'lib32-libxcursor' 'lib32-libpng' 'lib32-libpulse' 'lib32-openal'
		 'lib32-physfs' "$_pkgbasename")
makedepends=('cmake' 'gcc-multilib')
options=('!makeflags')
source=(http://downloads.sourceforge.net/alleg/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('99ef472e2f99972d5e833794bf2f57bf')

build() {
  cd "${srcdir}"
  mkdir build && cd build

  cmake "../${_pkgbasename}-${pkgver}" \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DLIB_SUFFIX="32" \
	-DCMAKE_C_FLAGS="$CFLAGS -m32" \
	-DCMAKE_CXX_FLAGS="$CXXFLAGS -m32" \
	-DWANT_DOCS=OFF

  make
}

package() {
  cd "${srcdir}/build"
  
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include}
  mkdir -p ${pkgdir}/usr/share/licenses
  ln -sf ${_pkgbasename} ${pkgdir}/usr/share/licenses/${pkgname}
}
