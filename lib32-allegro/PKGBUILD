# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=allegro
pkgname=lib32-$_pkgbasename
pkgver=4.4.2
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming (32 bit)"
arch=('x86_64')
url="http://alleg.sourceforge.net/"
license=('custom:allegro')
depends=('lib32-jack' 'lib32-libxcursor' 'lib32-libxxf86vm'
	 'lib32-libxxf86dga' 'lib32-alsa-lib' 'lib32-libxpm')
makedepends=('cmake' 'gcc-multilib')
options=('!makeflags')
conflicts=('lib32-allegro5')
source=(http://downloads.sourceforge.net/alleg/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('4db71b0460fc99926ae91d223199c2e6')

build() {
  cd "${srcdir}"
  
  rm -rf build
  mkdir build && cd build
  cmake "../${_pkgbasename}-${pkgver}" \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DLIB_SUFFIX="32" \
	-DCMAKE_C_FLAGS="$CFLAGS -m32" \
	-DCMAKE_CXX_FLAGS="$CXXFLAGS -m32" \
	-DWANT_DOCS=OFF
  make
}

package() {
  cd "${srcdir}/build"
  
  make DESTDIR="${pkgdir}" install

  rm -rf ${pkgdir}/usr/{bin,include}
  install -Dm644 "../${_pkgbasename}-${pkgver}/docs/txt/license.txt" \
  		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
