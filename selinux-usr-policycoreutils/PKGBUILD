# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)                 
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)

pkgname=selinux-usr-policycoreutils
_origname=policycoreutils
_release=20101221
pkgver=2.0.85
pkgrel=2
pkgdesc="SELinux userspace (policycoreutils)"
arch=('i686' 'x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
groups=('selinux' 'selinux-userspace')
depends=('selinux-usr-libsemanage>=2.0.46' 'selinux-usr-libselinux>=2.0.98' 'python2' 'libcap-ng')
options=(!emptydirs)
source=(http://userspace.selinuxproject.org/releases/${_release}/devel/${_origname}-${pkgver}.tar.gz)
md5sums=('170451cfe4613642b6abf13517a40b30')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  sed -i -e "s/shell python -c/shell python2 -c/" "semanage/Makefile"

  sed -i 's|/usr/bin/install|install|g' po/Makefile
  sed -i 's|-Werror||g' setfiles/Makefile

  make
}

package(){
  cd "${srcdir}/${_origname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Following are python2 scripts
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/audit2allow"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/audit2why"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/chcat"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/sepolgen-ifgen"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/sbin/semanage"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/lib/python2.7/site-packages/seobject.py"
}
