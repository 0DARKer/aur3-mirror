# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)                 
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)

pkgname=selinux-usr-policycoreutils
_origname=policycoreutils
_release=20120216
pkgver=2.1.10
pkgrel=1
pkgdesc="SELinux userspace (policycoreutils)"
arch=('i686' 'x86_64')
url="http://userspace.selinuxproject.org"
license=('GPL')
groups=('selinux' 'selinux-userspace')
depends=('selinux-usr-libsemanage>=2.1.0' 'selinux-usr-libselinux>=2.1.0' 'python2' 'libcap-ng' 'libcgroup')
options=(!emptydirs)
source=(http://userspace.selinuxproject.org/releases/${_release}/${_origname}-${pkgver}.tar.gz
        policycoreutils-seunshare.diff
        restorecond)
sha256sums=('8bbbc36b7d375edff891503932da93e37553f0dd7bdceded7ce9a45c80bec3d1'
            '10e1dfcd1db7fb1c2efa26151f37ede7d3da6cada393e186ffdeac234a8635fc'
            '8b207474f1a195fe848442c271c8af7e9a1c77e52b43b19165445d68d7d79888')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  sed -i -e "s/shell python -c/shell python2 -c/" "semanage/Makefile"

	patch -Np1 -i ../${_origname}-seunshare.diff
  make
}

package(){
  cd "${srcdir}/${_origname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Move daemons to correct locations
  mv "${pkgdir}/etc/rc.d/init.d/sandbox" \
	"${pkgdir}/etc/rc.d/"
  rm -rf "${pkgdir}/etc/rc.d/init.d/"
  install -m755 "${srcdir}/restorecond" "${pkgdir}/etc/rc.d/"

  # Following are python2 scripts
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/audit2allow"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/audit2why"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/chcat"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/bin/sepolgen-ifgen"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/sbin/semanage"
  sed -i -e "s/python -E/python2 -E/" \
        "${pkgdir}/usr/lib/python2.7/site-packages/seobject.py"
}

