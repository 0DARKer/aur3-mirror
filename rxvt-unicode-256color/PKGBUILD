# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributer: N30N <archlinux@alunamation.com>
# Contributor: Florian Pritz <f-p@gmx.at>
# Contributor: RayNbow <ShinNoNoir@gmail.com>
# Contributor: Oliver Mader <dotb52@gmail.com>

pkgname="rxvt-unicode-256color"
pkgver=9.09
pkgrel=2
pkgdesc="a unicode enabled rxvt-clone terminal emulator (urxvt), with 256 colour support"
arch=("i686" "x86_64")
depends=("gcc-libs" "libxft")
makedepends=("ncurses" "perl" "pkgconfig")
optdepends=("gtk2-perl: for urxvt-tabbed usage")
provides=("rxvt-unicode")
conflicts=("rxvt-unicode")
url="http://software.schmorp.de/pkg/rxvt-unicode.html"
license=("GPL2")
install=rxvt-unicode-256color.install
source=("http://dist.schmorp.de/rxvt-unicode/rxvt-unicode-$pkgver.tar.bz2"
        "rxvt-unicode.desktop"
        "rxvt-unicode.png"
        "font-width-fix.patch"
        "rxvt-hints.patch")
md5sums=('3505887adae710382edee90ed5538a01'
         'ef2dfa44a86cae36a60f45559d8ad783'
         '84328cada91751df07324d95f8be4d1b'
         'df0c3a8b6bb0578d1b91e4081c47881c'
         '3318e0f54eea97069987232476c5732d')

build() {
  cd "$srcdir"/rxvt-unicode-$pkgver

  patch -p0 < "$srcdir"/font-width-fix.patch
  patch -p0 < "$srcdir"/rxvt-hints.patch

  ./configure --prefix=/usr \
    --with-terminfo=/usr/share/terminfo \
    --enable-smart-resize \
    --enable-xim \
    --disable-iso14755 \
    --enable-256-color

  make
}

package() {
  cd "$srcdir"/rxvt-unicode-$pkgver
  install -d "$pkgdir"/usr/share/terminfo
  TERMINFO="$pkgdir"/usr/share/terminfo \
  make DESTDIR="$pkgdir" install

  # install the tabbing wrapper
  sed -i 's/\"rxvt\"/"urxvt"/' doc/rxvt-tabbed
  install -D -m755 doc/rxvt-tabbed "$pkgdir"/usr/bin/urxvt-tabbed

  # install freedesktop menu and icon ( icon from cvs checkout )
  install -D -m644 "$srcdir"/rxvt-unicode.desktop \
    "$pkgdir"/usr/share/applications/rxvt-unicode.desktop
  install -Dm644 "$srcdir"/rxvt-unicode.png \
    "$pkgdir"/usr/share/pixmaps/rxvt-unicode.png
}
