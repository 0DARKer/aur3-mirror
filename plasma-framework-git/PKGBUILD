# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework-git
pkgver=0.r11895.946286
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/libs/plasma-framework'
license=('LGPL')
depends=('kactivities-frameworks-git' 'qca')
makedepends=('extra-cmake-modules-git' 'git')
source=('git://anongit.kde.org/plasma-framework')
md5sums=('SKIP')

pkgver() {
  cd plasma-framework
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export LD_LIBRARY_PATH=/opt/kf5/lib
  export PKG_CONFIG_PATH=/opt/kf5/lib

  cd build
  cmake ../plasma-framework \
    -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
