# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework-git
pkgver=r12605.e59a86d
pkgrel=1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/libs/plasma-framework'
license=('LGPL')
depends=('threadweaver-git' 'kdeclarative-git' 'kactivities-frameworks-git' 'ktexteditor-git' 'kwallet-git' 
'qt5-quickcontrols')
makedepends=('extra-cmake-modules' 'git' 'qt5-tools')
optdepends=('kde-workspace-git: provide a Plasma shell')
conflicts=('plasma-framework')
provides=('plasma-framework')
source=('git://anongit.kde.org/plasma-framework.git')
md5sums=('SKIP')

pkgver() {
  cd plasma-framework
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export CMAKE_PREFIX_PATH="/opt/kf5"

  cd build
  cmake ../plasma-framework \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
