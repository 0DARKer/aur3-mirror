# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=bluefish-svn
pkgver=4057
pkgrel=1
pkgdesc="SVN version of the programmer's HTML editor written using GTK."
arch=('i686' 'x86_64')
url="http://bluefish.openoffice.nl"
license=('GPL')
depends=('aspell>=0.60.4-2' 'libxml2' 'gtk2' 'desktop-file-utils')
makedepends=('intltool' 'perlxml' 'pkgconfig' 'subversion')
optdepends=('python: for self written extensions')
provides=('bluefish')
conflicts=('bluefish' 'bluefish-devel' 'bluefish-cvs')
install=$pkgname.install
source=()
md5sums=()

_svntrunk=https://bluefish.svn.sourceforge.net/svnroot/bluefish/trunk
_svnmod=bluefish

build() {
  cd "${srcdir}"

  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "${srcdir}/$_svnmod-build"
  cp -r "${srcdir}/$_svnmod/$_svnmod" "${srcdir}/$_svnmod-build"
  cd "${srcdir}/$_svnmod-build"

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --exec-prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-freedesktop_org-menu=/usr/share/applications \
    --with-freedesktop_org-mime=/usr/share/mime \
    --without-gnome2_4-mime \
    --without-gnome2_4-appreg \
    --with-icon-path=/usr/share/pixmaps \
    --disable-update-databases \
    --disable-xml-catalog-update

  make || return 1
  make DESTDIR="${pkgdir}" install

  rm -rf "${srcdir}/$_svnmod-build" || return 1
}
