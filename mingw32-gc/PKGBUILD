pkgname=mingw32-gc
pkgver=7.2alpha6 # 7.1 fails compile with undefined references
pkgrel=3
pkgdesc="A garbage collector for C and C++ (mingw32)"
arch=(any)
license=('GPL')
depends=(mingw32-runtime)
makedepends=(mingw32-gcc)
options=(!strip !buildflags !libtool)
source=("http://www.hpl.hp.com/personal/Hans_Boehm/gc/gc_source/gc-$pkgver.tar.gz")
url='http://www.hpl.hp.com/personal/Hans_Boehm/gc/'
sha256sums=('ebf425b254773d565e1690287641ed177716a78e952c96fec917edbc64535c16')

build() {
  cd "${srcdir}/gc-${pkgver}"

  export PKG_CONFIG_LIBDIR="/usr/i486-mingw32/lib/pkgconfig"
  unset LDFLAGS PKG_CONFIG_PATH
  mkdir build && cd build
  ../configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --enable-threads=win32
  make
}

package() {
  cd "${srcdir}/gc-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  
  i486-mingw32-strip -x $pkgdir/usr/i486-mingw32/bin/*.dll
  i486-mingw32-strip -g $pkgdir/usr/i486-mingw32/lib/*.a
  rm -rf "$pkgdir/usr/i486-mingw32/share"
}
