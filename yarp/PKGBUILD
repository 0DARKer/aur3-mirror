# Contributor: Victor NoÃ«l

pkgname=yarp
pkgver=2.3.15
pkgrel=1
pkgdesc="Yet Another Robot Platform"
url="http://eris.liralab.it/yarp/"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('cmake')
depends=('ace' 'gtkmm' 'gsl')
options=(!libtool)
source=(http://downloads.sourceforge.net/yarp0/$pkgname-$pkgver.tar.gz)

build() {
  cd $srcdir/$pkgname-$pkgver/

  cd $srcdir

  rm -rf build-$pkgname-$pkgver
  mkdir build-$pkgname-$pkgver
  cd build-$pkgname-$pkgver

  # missing include
  sed -i ../$pkgname-$pkgver/src/yarpmanager/libymanager/src/localbroker.cpp -f - <<'==='
/<stdlib.h>/ a \
#include <unistd.h>
===

  cmake ../$pkgname-$pkgver -DCMAKE_INSTALL_PREFIX=/usr -DCREATE_GUIS=TRUE -DCREATE_SHARED_LIBRARY=TRUE -DCREATE_LIB_MATH=TRUE 
  make
}

package() {
  cd $srcdir/build-$pkgname-$pkgver

  make DESTDIR=$pkgdir install
}

md5sums=('58912d7d1a6ed3347fc15ef7236899e1')
