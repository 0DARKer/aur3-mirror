# Maintainer:  TDY <tdy@gmx.com>
# Contributor: Loui Chang <louipc dot ist at gmail company>
# Contributor: rxvt <artwithin@gmail.com>

pkgname=skippy
pkgver=0.5.0
pkgrel=4
pkgdesc="A full-screen task switcher for X11, similar to Apple Expose"
url="http://thegraveyard.org/skippy.php"
license=('GPL')
arch=('i686' 'x86_64')
depends=('imlib2' 'libxft' 'libxinerama')
install=skippy.install
source=(http://cdn.thegraveyard.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2
        README)
md5sums=('c7b5c7f0dd870513494916afd8581323'
         'd480f2481d87900488aa340a11bd4b2e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '261s,CARD32,void*,' wm.c
  export CFLAGS+=" -DDEBUG -DXINERAMA"
  export LDFLAGS+=" -lXext -lXinerama"
  make PREFIX=/usr X11PREFIX=/usr/lib
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 skippyrc-default "$pkgdir/usr/share/$pkgname/skippyrc"
  install -Dm644 ../README "$pkgdir/usr/share/$pkgname/README"
}
