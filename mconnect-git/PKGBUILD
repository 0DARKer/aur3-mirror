# Maintainer: Suhaimi Ghazali <serdotlinecho@gmail.com>

pkgname=mconnect-git
pkgver=r127.15b0907
pkgrel=1
pkgdesc="KDE Connect protocol implementation in Vala/C"
arch=('i686' 'x86_64')
url="https://github.com/bboozzoo/mconnect"
license=('GPL')
depends=('vala' 'glib2' 'json-glib' 'openssl' 'libnotify' 'libgee')
makedepends=('git')
source=("${pkgname}::git+https://github.com/bboozzoo/mconnect.git")
md5sums=('SKIP')

pkgver() {
    cd "${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${pkgname}"
    autoreconf -fi
    ./configure --sysconfdir=/etc --prefix=/usr
    make
}

package() {
    cd "${pkgname}"
    make DESTDIR="${pkgdir}" install
}
