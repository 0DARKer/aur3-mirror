# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=python2-guiqwt
pkgver=2.2.1
pkgrel=1
pkgdesc="Python library providing efficient 2D data-plotting features"
arch=('i686' 'x86_64')
url="http://code.google.com/p/guiqwt/"
license=('custom:CeCIL')
depends=('python2-guidata' 'pyqwt' 'python2-scipy' 'python-imaging')
optdepends=('python2-pydicom: for DICOM files I/O features'
            'spyder: for Sift embedded Python console')
makedepends=('python2-distribute' 'unzip' 'gcc-fortran')
options=(!emptydirs)

source=(http://guiqwt.googlecode.com/files/guiqwt-$pkgver.zip)
md5sums=('d7fc08078d6d41e063596a04050c1217')

build() {
  cd "$srcdir"/guiqwt-$pkgver

  unset LDFLAGS
  python2 setup.py build
}

package() {
  cd "$srcdir"/guiqwt-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -Dm644 Licence_CeCILL_V2-en.txt "$pkgdir/usr/share/licenses/python2-guiqwt/LICENSE"
}

