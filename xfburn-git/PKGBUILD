# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
#
# (Added from xfburn package)
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

_pkgname=xfburn
pkgname=$_pkgname-git
pkgver=20121111
pkgrel=1
pkgdesc="A simple CD/DVD burning tool based on libburnia libraries"
arch=(i686 x86_64)
url=http://www.xfce.org/projects/$_pkgname
license=(GPL2)
groups=(xfce4-goodies)
depends=(desktop-file-utils exo-git gstreamer0.10-base hicolor-icon-theme libburn libisofs libxfce4util-git)
makedepends=(git xfce4-dev-tools)
provides=($_pkgname=0.4.3)
conflicts=($_pkgname)
install=$_pkgname.install

_gitroot=git://git.xfce.org/apps/$_pkgname
_gitname=$_pkgname

build() {
    cd "$srcdir"
    msg "Connecting to the GIT server..."
    if [[ -d $_gitname/.git ]]; then
        pushd $_gitname && git pull
        msg2 "The local files are updated."
        popd
    else
        git clone $_gitroot
    fi
    msg2 "GIT checkout done or server timeout"

    rm -rf $_gitname-build/
    cp -r $_gitname/ $_gitname-build/
    cd $_gitname-build/

    msg "Building..."
    ./autogen.sh  \
        --prefix=/usr \
        --enable-gstreamer \
        --enable-dbus \
        --disable-hal
    make
}

package() {
    cd "$srcdir"/$_gitname-build/
    make DESTDIR="$pkgdir" install
}
