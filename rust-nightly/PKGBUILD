# Contributor: Isak Karlsson <isak.karlsson@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=rust-nightly
pkgver=0.13.0_2015.01.27
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A safe, concurrent, practical language'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=('shared-mime-info')
makedepends=('libffi' 'perl' 'python2' 'curl' 'clang' 'texlive-core')
source=("http://static.rust-lang.org/dist/rust-nightly-src.tar.gz")
sha256sums=('995dcf7f033f72767e4e715e2888f47c7d538d16735a4291ca9368035b040b33')
install=rust.install
options=('staticlibs' '!strip')
conflicts=('rust')
provides=('rust')

build() {
  cd rust-nightly

  ./configure --prefix=/usr --enable-docs --disable-rpath --disable-verify-install

  # avoid python makedepend (force fallback to python2)
  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules
  make
 }

package() {
  cd rust-nightly
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/share/vim" "$pkgdir/usr/share/licenses/$pkgname"

  install -Dm644 src/etc/zsh/_rust "$pkgdir/usr/share/zsh/site-functions/_rust"

  install -m644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname"

  cd "$pkgdir/usr/lib"
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/* .
  find $pkgdir/usr/share/doc/rust -type f -exec chmod o+r {} \;
}
