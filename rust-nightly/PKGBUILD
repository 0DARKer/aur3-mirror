# Contributor: Isak Karlsson <isak.karlsson@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=rust-nightly
pkgver=20150118
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A safe, concurrent, practical language'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=('shared-mime-info')
makedepends=('libffi' 'perl' 'python2' 'curl' 'clang')
source=("http://static.rust-lang.org/dist/rust-nightly-src.tar.gz")
sha256sums=('930e34270a604b497db19ee01f89953c292a556b51cfd1f32961ca67d6f12cc5')
install=rust.install
options=('staticlibs')
conflicts=('rust')
provides=('rust')

build() {
  cd rust-nightly

  ./configure --prefix=/usr --disable-docs --disable-rpath --disable-verify-install

  # avoid python makedepend (force fallback to python2)
  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules
  make
  cd src/etc/emacs
  emacs --eval '(byte-recompile-directory "." 0)' --quick --batch 2> /dev/null
}

#check() {
  #cd rust-$_pkgver
  #make check
#}

pkgver() {
  date +%Y%m%d
}

package() {
  cd rust-nightly
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/share/vim" "$pkgdir/usr/share/licenses/$pkgname"

  cp -a src/etc/vim "$pkgdir/usr/share/vim/vimfiles"
  find "$pkgdir/usr/share/vim" -type f -exec chmod 644 {} +
  find "$pkgdir/usr/share/vim" -type d -exec chmod 755 {} +

  install -Dm644 src/etc/zsh/_rust "$pkgdir/usr/share/zsh/site-functions/_rust"
  install -Dm644 src/etc/kate/rust.xml "$pkgdir/usr/share/apps/katepart/syntax/rust.xml"
  install -Dm644 src/etc/gedit/share/mime/packages/rust.xml \
    "$pkgdir/usr/share/mime/packages/rust.xml"
  install -Dm644 src/etc/gedit/share/gtksourceview-3.0/language-specs/rust.lang \
    "$pkgdir/usr/share/gtksourceview-3.0/language-specs/rust.lang"

  install -m644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname"

  cd src/etc/emacs
  install -Dm644 rust-mode.el "$pkgdir/usr/share/emacs/site-lisp/rust-mode.el"
  install -Dm755 run-rust-emacs-test.sh \
	  "$pkgdir/usr/share/emacs/site-lisp/run-rust-emacs-test.sh"
  install -Dm644 README.md "$pkgdir/usr/share/emacs/site-lisp/README.md"
  install -Dm644 rust-mode-tests.el \
	  "$pkgdir/usr/share/emacs/site-lisp/rust-mode-tests.el"
  
  cd "$pkgdir/usr/lib"
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/* .
}
