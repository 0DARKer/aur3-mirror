# Maintainer: Simon Thorpe <simon@hivetechnology.com.au>
pkgname=sekaiju
pkgver=4.4
pkgrel=2
pkgdesc="Sequencer for editing MIDI files"
arch=('i686' 'x86_64')
url="http://openmidiproject.sourceforge.jp/Sekaiju_en.html"
license=('LGPL')
depends=('wine')
makedepends=('gendesk' 'imagemagick')
optdepends=('bash-completion: tab completion support')
source=("http://sourceforge.jp/frs/redir.php?m=jaist&f=%2Fopenmidiproject%2F62602%2FSekaiju4.4.zip")
sha256sums=('930606a4c9c425171821ead897d9121c613192675914ef21d0a5dc949881da1e')

prepare(){
  convert "$srcdir/Sekaiju4.4/res/SekaijuType.ico" "$srcdir/icon.png"
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --name='Sekaiju' \
    --mimetype='audio/midi' \
    --categories 'Audio;Sequencer;Midi;Music;'
}

package(){
  mkdir -p $pkgdir/usr/share
  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/share/bash-completion/completions
  cp -R $srcdir/Sekaiju4.4 $pkgdir/usr/share/$pkgname
  echo -e '#!/bin/bash\n[[ "$1" == "" ]] && wine /usr/share/sekaiju/Sekaiju.exe\n[[ "$1" != "" ]] && wine /usr/share/sekaiju/Sekaiju.exe "$(winepath -w "$1")"'>$pkgdir/usr/bin/sekaiju
  echo -e '[Language]\nUserInterface=English' >$pkgdir/usr/share/$pkgname/Sekaiju.ini
  chmod +x $pkgdir/usr/bin/$pkgname
  install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  
  echo "_$pkgname() { local cur prev words cword split; _init_completion -s || return; _filedir '@(skj|chy|mid|csv|vsq)'; } && complete -F _$pkgname $pkgname" >$pkgdir/usr/share/bash-completion/completions/sekaiju
}