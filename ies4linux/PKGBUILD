#Maintainer: Jarek Sedlacek <jareksedlacek@gmail.com>

pkgname=ies4linux
pkgver=2.99.0.1
pkgrel=3
pkgdesc="The simpler way to run Microsoft Internet Explorer on Wine"
arch=(any)
url="http://www.tatanka.com.br/ies4linux/page/Main_Page"
license=(GPL)
depends=(cabextract pygtk python2 wine unzip)
source=(${url/pa*}/downloads/$pkgname-$pkgver.tar.gz
        remove-wineprefixcreate.patch
   	wine-version.patch)

md5sums=(5dbf1091bb157b12db20ca409cc980ff
         5f35abcdf193ae82d84caf4d19c888b2
         3356b9024f349c7fbac2e9efc091e5eb)

build() {
  cd "$srcdir"
  patch -p0 < "$srcdir/remove-wineprefixcreate.patch"
  patch -p0 < "$srcdir/wine-version.patch"
  sed -i 's|IES4LINUX=`dirname "$0"`|IES4LINUX=/usr/lib/ies4linux|' ies4linux-$pkgver/ies4linux
  sed -i 's/python -c/python2 -c/'  ies4linux-$pkgver/ies4linux
  sed -i 's/python/python2/'  ies4linux-$pkgver/ui/pygtk/python-gtk.sh
}

package(){
  cd "$srcdir"
  mkdir -p "$pkgdir/usr/lib"
  # place the executable in bin
  install -D -m0755 ies4linux-$pkgver/ies4linux "$pkgdir/usr/bin/ies4linux"
  # the executable doesn't need to be in lib, we can remove it
  rm ies4linux-$pkgver/ies4linux
  # copy everything else to lib
  cp -r -a ies4linux-$pkgver "$pkgdir/usr/lib/ies4linux"
}
