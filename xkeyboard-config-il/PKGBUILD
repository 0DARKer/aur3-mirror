# $Id: PKGBUILD 118460 2011-04-07 07:07:47Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# il - new SI1452: Yoel Lion <yoel3ster@gmail.com>
pkgname=xkeyboard-config-il
pkgbasename=xkeyboard-config
pkgver=2.3
pkgrel=1
pkgdesc="X keyboard configuration files with new SI1452 layout patch"
arch=(any)
license=('custom')
url="http://www.freedesktop.org/wiki/Software/XKeyboardConfig"
depends=('xorg-xkbcomp')
makedepends=('intltool')
provides=('xkeyboard-config' 'xkbdata')
replaces=('xkeyboard-config' 'xkbdata')
conflicts=('xkeyboard-config' 'xkbdata')
source=(http://xorg.freedesktop.org/releases/individual/data/${pkgbasename}/${pkgbasename}-${pkgver}.tar.bz2 il.patch)
sha1sums=('a49c4cce9cfb7003aa04a32114f369c6bd7a9077' 'c5ec8bd471990c39a11d67a805b80536edf1f46f')

build() {
  cd "${srcdir}/${pkgbasename}-${pkgver}"
  patch -p1 < ${srcdir}/il.patch
  ./configure --prefix=/usr \
      --with-xkb-base=/usr/share/X11/xkb \
      --with-xkb-rules-symlink=xorg \
      --enable-compat-rules=yes
  make
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/share/X11/xkb/compiled"
  install -m755 -d "${pkgdir}/var/lib/xkb"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgbasename}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgbasename}/"
}
