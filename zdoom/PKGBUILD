# Contributor: Jan Cholasta <grubber@grubber.cz>
# Maintainer: Jan Cholasta <grubber@grubber.cz>

pkgname=zdoom
pkgver=2.6.0
pkgrel=1
pkgdesc="An enhanced Doom port with additional support for Heretic, Hexen and Strife."
arch=('i686' 'x86_64')
url="http://www.zdoom.org/"
license=('custom')
depends=('bzip2' 'gtk2' 'gxmessage' 'sdl')
makedepends=('nasm' 'cmake' 'imagemagick' 'p7zip')
optdepends=('doom1-wad: Doom shareware IWAD (game data)'
            'harmony-wad: Harmony IWAD (game data)'
            'heretic1-wad: Heretic shareware IWAD (game data)'
            'hexen1-wad: Hexen 1 Demo IWAD (game data)'
            'strife0-wad: Strife shareware IWAD (game data)'
            'urbanbrawl-wad: Urban Brawl: Action Doom 2 IWAD (game data)')
source=(http://zdoom.org/files/zdoom/2.6/${pkgname}-${pkgver}-src.7z \
        http://www.fmod.org/index.php/release/version/fmodapi43818linux.tar.gz \
        ${pkgname}-${pkgver}-sharedir.patch \
        ${pkgname}.desktop)
noextract=(${pkgname}-${pkgver}-src.7z)
md5sums=('26318282e8b28fe06f8a9d446d058b8c'
         '9b1af4ae3352fbe147c72c8430df74cd'
         '4a3d58c667d19573eef907c838a574c7'
         '83e47fdae2768da78cd4ac151ec92ad1')

_fmodver=4.38.18
_fmod64=

if [ "$CARCH" = "x86_64" ]; then
  _fmod64=64
  makedepends[0]='yasm'
  source[1]=http://www.fmod.org/index.php/release/version/fmodapi${_fmodver//./}linux${_fmod64}.tar.gz
  md5sums[1]='619f32636aa2c1d9417fc8cdf52cef37'
fi

build() {
  msg2 "Extracting ${pkgname}-${pkgver}-src.7z with 7z"
  7z x -y -o"$srcdir"/${pkgname}-${pkgver} "$srcdir"/${pkgname}-${pkgver}-src.7z &> /dev/null

  cd "$srcdir"/${pkgname}-${pkgver}

  cp ../fmodapi${_fmodver//./}linux${_fmod64}/api/lib/libfmodex${_fmod64}-${_fmodver}.so ../libfmodex-${pkgname}.so

  convert src/win32/icon1.ico'[2]' ../${pkgname}.png

  patch -p1 < ../${pkgname}-${pkgver}-sharedir.patch

  cmake -DFMOD_INCLUDE_DIR=../fmodapi${_fmodver//./}linux${_fmod64}/api/inc -DFMOD_LIBRARY=../libfmodex-${pkgname}.so -DCMAKE_SKIP_RPATH=TRUE .
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  install -m755 -D zdoom "$pkgdir"/usr/bin/zdoom
  install -m644 -D zdoom.pk3 "$pkgdir"/usr/share/games/zdoom/zdoom.pk3
  install -m644 -D docs/BUILDLIC.TXT "$pkgdir"/usr/share/licenses/${pkgname}/BUILDLIC.TXT
  install -m644 -D docs/doomlic.txt "$pkgdir"/usr/share/licenses/${pkgname}/doomlic.txt

  install -m755 -D ../libfmodex-${pkgname}.so "$pkgdir"/usr/lib/libfmodex-${pkgname}.so
  install -m644 -D ../${pkgname}.png "$pkgdir"/usr/share/pixmaps/zdoom.png
  install -m644 -D ../${pkgname}.desktop "$pkgdir"/usr/share/applications/zdoom.desktop
}
