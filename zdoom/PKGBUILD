# Maintainer: Jan Cholasta <grubber at grubber cz>

_fmodver=42636
_fmodarch=linux
[ "$CARCH" = x86_64 ] && _fmodarch=linux64

pkgname=zdoom
pkgver=2.6.1
pkgrel=1
pkgdesc="An enhanced Doom port with additional support for Heretic, Hexen and Strife."
arch=('i686' 'x86_64')
url="http://www.zdoom.org/"
license=('custom')
depends=('fluidsynth' 'gxmessage' 'sdl')
makedepends=('nasm' 'cmake' 'imagemagick' 'p7zip')
optdepends=('blasphemer: Blasphemer (free Heretic) game data'
            'chexquest3-wad: Chex Quest 3 game data'
            'doom1-wad: Doom shareware game data'
            'freedoom: FreeDoom game data'
            'hacx-wad: HacX game data'
            'harmony-wad: Harmony game data'
            'heretic1-wad: Heretic shareware game data'
            'hexen1-wad: Hexen demo game data'
            'strife0-wad: Strife shareware game data'
            'urbanbrawl-wad: Urban Brawl: Action Doom 2 game data')
source=(http://zdoom.org/files/zdoom/2.6/zdoom-$pkgver-src.7z \
        http://www.fmod.org/index.php/release/version/fmodapi${_fmodver}${_fmodarch}.tar.gz \
        doom-share-dir.patch \
        stack-noexec.patch \
        zdoom.desktop)
noextract=(zdoom-${pkgver}-src.7z)
md5sums=('49d9f3b7587ce1d5799f7ba4411d43bb'
         '220a54f330bdf3056d6207a0facf2096'
         'c8a3905f172f6d9de548e70fa660a300'
         'e1e1160df8dcf969d9acae76dab27bed'
         '83e47fdae2768da78cd4ac151ec92ad1')

if [ "$CARCH" = x86_64 ]; then
  makedepends[0]='binutils'
  md5sums[1]='355cba00a34eb5f7d027da68b452f6d9'
fi

build() {
  msg2 "Extracting zdoom-$pkgver-src.7z with 7z"
  7z x -y -o"$srcdir/$pkgname-$pkgver" "$srcdir/zdoom-$pkgver-src.7z" &>/dev/null

  cd "$srcdir/$pkgname-$pkgver"

  local _fmodlibver=${_fmodver:0:1}.${_fmodver:1:2}.${_fmodver:3:2}
  local _fmodlibname=libfmodex
  [ "$CARCH" = x86_64 ] && _fmodlibname=libfmodex64

  cp "$srcdir/fmodapi${_fmodver}${_fmodarch}/api/lib/$_fmodlibname-$_fmodlibver.so" libfmodex-$pkgname.so

  convert "src/win32/icon1.ico[2]" zdoom.png

  patch -p1 <"$srcdir/doom-share-dir.patch"
  patch -p1 <"$srcdir/stack-noexec.patch"

  cmake -DFMOD_INCLUDE_DIR="$srcdir/fmodapi${_fmodver}${_fmodarch}/api/inc" -DFMOD_LIBRARY=libfmodex-$pkgname.so -DCMAKE_SKIP_RPATH=TRUE .
  make CFLAGS="$CFLAGS -DSHARE_DIR=/usr/share/games/zdoom/"
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  install -Dm755 zdoom "$pkgdir/usr/bin/zdoom"
  install -Dm644 zdoom.pk3 "$pkgdir/usr/share/games/zdoom/zdoom.pk3"
  install -Dm644 docs/BUILDLIC.TXT "$pkgdir/usr/share/licenses/$pkgname/BUILDLIC.TXT"
  install -Dm644 docs/doomlic.txt "$pkgdir/usr/share/licenses/$pkgname/DOOMLIC.TXT"

  install -Dm755 libfmodex-$pkgname.so "$pkgdir/usr/lib/libfmodex-$pkgname.so"
  install -Dm644 zdoom.png "$pkgdir/usr/share/pixmaps/zdoom.png"
  install -Dm644 "$srcdir/zdoom.desktop" "$pkgdir/usr/share/applications/zdoom.desktop"

  ln -s /usr/share/doom/doom.wad "$pkgdir/usr/share/games/zdoom/freedoomu.wad"
  ln -s /usr/share/doom/doom2.wad "$pkgdir/usr/share/games/zdoom/freedoom.wad"
  ln -s /usr/share/doom/hexen.wad "$pkgdir/usr/share/games/zdoom/hexendemo.wad"
}
