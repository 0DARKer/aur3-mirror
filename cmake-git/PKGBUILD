# Contributor: Roman Naumann
pkgname=cmake-git
pkgver=20100326
pkgrel=1
pkgdesc="Cross-Platform Make"
arch=(i686 x86_64)
url="http://www.cmake.org"
license=('GPL')
depends=('expat' 'xmlrpc-c' 'gcc-libs')
makedepends=('qt' 'git')
provides=(cmake)
conflicts=(cmake)

_gitroot="git://cmake.org/cmake.git"
_gitname="cmake"

build() {
  cd "$srcdir"
  msg "Connecting to server...."
  if [ -d $_gitname/.git ]; then
    cd $_gitname
    git pull
  else
    git clone $_gitroot
    cd $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/${_gitname}-build"
  cp -r "$srcdir/${_gitname}" "$srcdir/${_gitname}-build"
  cd "$srcdir/${_gitname}-build"


  ./bootstrap --prefix=/usr \
         --mandir=/share/man \
          --docdir=/share/doc/cmake \
          --system-libs \
          --qt-gui
  make || return 1
  make DESTDIR=$pkgdir install

  install -D -m644 Docs/cmake-indent.vim $pkgdir/usr/share/vim/indent/cmake-indent.vim
  install -D -m644 Docs/cmake-syntax.vim $pkgdir/usr/share/vim/syntax/cmake-syntax.vim
  install -D -m644 Docs/cmake-mode.el $pkgdir/usr/share/emacs/site-lisp/cmake-mode.el
  install -D -m644 Copyright.txt $pkgdir/usr/share/licenses/$pkgname/Copyright.txt
}
