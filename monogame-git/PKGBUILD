pkgname=monogame-git
pkgver=v3.0.0.0.1009
pkgrel=1
pkgdesc="XNA Implementation for Mono based platforms (git)"
arch=(i686 x86_64)
license=("Microsoft Public License")
depends=(mono "opentk>=2012.03.15")
makedepends=(nant git)
conflicts=(monogame tao-framework tao-framework-svn)
provides=(monogame)
url="http://monogame.codeplex.com"
source=("${pkgname%-*}::git+https://github.com/mono/MonoGame.git"
"MonoGame.snk")
md5sums=(SKIP 'c1da425a06132c94d678f4c0ccf82399')

pkgver () {
	cd "$srcdir/${pkgname%-*}"
	git describe | sed 's|\(.*-.*\)-.*|\1|;s|-|.|'
}

build() {
	cd "${srcdir}/${pkgname%-*}"
	nant buildlinux
  cd "MonoGame.Framework/bin/Linux/Release"
  monodis Lidgren.Network.dll --output=Lidgren.Network.il
  monodis MonoGame.Framework.dll --output=MonoGame.Framework.il
  find . -name '*.il' | xargs -rtl1 -I {} ilasm /dll /key:$srcdir/MonoGame.snk {}
}

package() {
	cd "${srcdir}/${pkgname%-*}/MonoGame.Framework/bin/Linux/Release"
	find . -name '*.dll' -o -name '*.mdb' -o -name '*.mgfxo' -o -name '*.config' |
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/monogame/"{}
  install -Dm644 "${srcdir}/${pkgname%-*}/LICENSE.txt" "$pkgdir/usr/share/licenses/monogame/LICENSE.txt"
  gacutil "$pkgdir/usr/lib/monogame/MonoGame.Framework.dll" -i  -root "$pkgdir/usr/lib"
  gacutil "$pkgdir/usr/lib/monogame/Tao.Sdl.dll" -i  -root "$pkgdir/usr/lib"
  find "$pkgdir/usr/lib/monogame/" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
}
