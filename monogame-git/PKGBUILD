pkgname=monogame-git
pkgver=20130123
pkgrel=1
pkgdesc="XNA Implementation for Mono based platforms (git)"
arch=(any)
license=("Microsoft Public License")
depends=(mono "opentk>=2012.03.15")
makedepends=(mono nant git)
options=(!strip)
conflicts=(monogame)
replaces=(monogame)
provides=("monogame=3.0.0.0-3")
url="http://monogame.codeplex.com"
_gitroot="https://github.com/mono/MonoGame.git"
_gitname="MonoGame"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]
  then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."
  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  nant buildlinux
  sn -k 1024 "$srcdir/MonoGame.snk"
  cd "MonoGame.Framework/bin/Linux/Release"
  monodis Lidgren.Network.dll --output=Lidgren.Network.il
  monodis MonoGame.Framework.dll --output=MonoGame.Framework.il
  find . -name '*.il' | xargs -rtl1 -I {} ilasm /dll /key:$srcdir/MonoGame.snk {}
  rm Tao.Sdl.dll{,.config}
  cd "$srcdir/$_gitname-build/ProjectTemplates/MonoDevelop/MonoDevelop.MonoGame"
  nant
}

package() {
  cd "$srcdir/$_gitname-build/MonoGame.Framework/bin/Linux/Release"
  find . -name '*.dll' -o -name '*.mdb' -o -name '*.mgfxo' -o -name '*.config' |
    xargs -rtl1 -I {} install -Dm644 {} "$pkgdir/usr/lib/monogame/"{}
  find "$pkgdir/usr/lib/monogame" -name '*.dll' | xargs -rtl1 -I {} gacutil -i {} -root "$pkgdir/usr/lib"
  cd "$srcdir/$_gitname-build/ProjectTemplates/MonoDevelop/MonoDevelop.MonoGame/MonoDevelop.MonoGame"
  install -Dm644 "bin/Release/MonoDevelop.MonoGame.dll" "$pkgdir/usr/lib/monodevelop/AddIns/MonoDevelop.MonoGame/MonoDevelop.MonoGame.dll"
  cp -r icons "$pkgdir/usr/lib/monodevelop/AddIns/MonoDevelop.MonoGame/"
  cp -r templates "$pkgdir/usr/lib/monodevelop/AddIns/MonoDevelop.MonoGame/"
  install -Dm644 "$srcdir/$_gitname-build/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  mkdir -p "$pkgdir/usr/share/$pkgname"
  cp -r "$srcdir/$_gitname-build/Graphics" "$pkgdir/usr/share/$pkgname/"
}
