# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kf5-kdelibs-git
pkgver=v4.10.3.4220.g045efae
pkgrel=1
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('kf5-strigi-git' 'kf5-attica-git' 'kf5-libdbusmenu-qt-bzr'
        'kf5-phonon-git' 'qttools-git' 'qtwebkit-git' 'libxcursor'
        'libxslt' 'libjpeg' 'giflib' 'docbook-xsl' 'hunspell'
        'kf5-polkit-qt-git' 'udisks2' 'libutempter' 'media-player-info'
        'qtscript-git')
makedepends=('cmake' 'extra-cmake-modules-git' 'hspell' 'avahi')
conflicts=('kf5-kdelibs')
provides=('kf5-kdelibs')
install=${pkgname}.install
options=('debug')
source=('git://anongit.kde.org/kdelibs#branch=frameworks'
	    'kde-applications-menu.patch'
        'archlinux-menu.patch')
md5sums=('SKIP'
         '1322a27d8137878fa7abc37bc7656490'
         '078c43bcec25e6ff3d28ebb1c054c1c2')

pkgver() {
  cd kdelibs
  git describe --always | sed 's|-|.|g'
}

prepare() {
  mkdir -p build
  
  cd kdelibs
  patch -p1 -i "${srcdir}"/kde-applications-menu.patch
  patch -p1 -i "${srcdir}"/archlinux-menu.patch
}

build() {
  export PKG_CONFIG_PATH=/opt/kf5/lib/pkgconfig:/opt/qt-git/lib/pkgconfig
  export LD_LIBRARY_PATH=/opt/kf5/lib:/opt/qt-git/lib

  cd build
  cmake ../kdelibs \
    -DCMAKE_PREFIX_PATH=/opt/qt-git/lib/cmake \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_SKIP_RPATH=ON \
    -DKDE_DISTRIBUTION_TEXT='Arch Linux' \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DKDE_DEFAULT_HOME='.kf5' \
    -DQt5_DIR=/opt/qt-git/lib/cmake/Qt5 \
    -DHUPNP_ENABLED=FALSE \
    -DWITH_SOLID_UDISKS2=ON \
    -DLIB_INSTALL_DIR=lib \
    -DStrigi_DIR=/opt/kf5/lib/cmake/Strigi \
    -DPolkitQt-1_DIR=/opt/kf5/lib/cmake/PolkitQt-1
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install

  rm -f "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
  ln -sf /etc/ssl/certs/ca-certificates.crt \
    "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
}
