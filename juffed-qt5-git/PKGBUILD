# Maintainer: Alfredo Ramos <alfredo dot ramos at yandex dot com>

_pkgname=juffed
pkgname=${_pkgname}-qt5-git
pkgver=0.10.r63.gdf5faca
pkgrel=1
pkgdesc="A lightweight cross-platform text editor. Qt5 build. Development version."
arch=(
	"$CARCH"
)
url="http://juffed.com/"
license=(
	"GPL2"
)

depends=(
	"enca"
	"qscintilla-qt5"
	"desktop-file-utils"
)
optdepends=()
makedepends=(
	"git"
	"cmake"
)
provides=(
	"${_pkgname}=${pkgver}"
	"${_pkgname}-plugins=${pkgver}"
)
conflicts=(
	"${_pkgname}"
	"${_pkgname}-git"
)
replaces=(
	"${_pkgname}"
	"${_pkgname}-git"
)

install=${pkgname}.install

source=(
	"git+https://github.com/Mezomish/${_pkgname}.git"
	"${pkgname}.install"
)
sha384sums=(
	"SKIP"
	"eab0dfacf015dae1f81b4cbf0afcec65632c225ff6368a711c444a3c4d048e5b354ffd0e1995997f9a24c3dafff6cf81"
)
sha512sums=(
	"SKIP"
	"995dd08f218ebe1bfd1030e7456e4bd70c4b7d8e6cf323d7f0b99b08496da16f4f6f28ace27d616f0d452698ce6e266d342a9129f66df9d2a2224c0a30267308"
)

pkgver() {
	# Updating package version
	cd ${srcdir}/${_pkgname}
	(
		set -o pipefail
		git describe --long --tags | sed -r 's/^juffed-//;s/([^-]*-g)/r\1/;s/-/./g' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

prepare() {
	# Make build directory
	mkdir -p ${srcdir}/build
}

build() {
	# Number of jobs
	declare -i njobs=$(nproc)
	
	if [[ ${njobs} -ge 8 ]]; then
		njobs=$(( ${njobs} - 2 ))
	fi
	
	# Building package
	cd ${srcdir}/build
	cmake ../${_pkgname} \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_QT5=ON \
		-DUSE_ENCA=ON
	make -j${njobs}
}

package() {
	# Installing package
	cd ${srcdir}/build
	make DESTDIR=${pkgdir} install
}