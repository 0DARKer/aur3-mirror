# Maintainer: Timofey Titovets <nefeli4mag@gmail.com>

pkgname=systemd-git
pkgver=219.50.g00d053d
pkgrel=1
pkgdesc="system and service manager"
arch=('any')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('acl' 'curl' 'elfutils' 'glib2' 'hwids' 'kbd' 'kmod' 'libcap' 'libseccomp' 'libgcrypt' 'libidn' 'lz4' 'pam' 'python' 'util-linux' 'xz')
makedepends=('docbook-xsl' 'git' 'gobject-introspection' 'gperf' 'gtk-doc' 'intltool' 'libxslt' 'linux-api-headers')
provides=('systemd' 'libsystemd' 'nss-myhostname' 'systemd-sysvcompat' 'systemd-tools' "udev=$pkgver")
conflicts=('systemd' 'libsystemd' 'nss-myhostname' 'systemd-sysvcompat' 'systemd-tools' 'sysvinit' 'initscripts' 'udev')
groups=('systemd')
backup=(etc/dbus-1/system.d/org.freedesktop.systemd1.conf
        etc/dbus-1/system.d/org.freedesktop.hostname1.conf
        etc/dbus-1/system.d/org.freedesktop.login1.conf
        etc/dbus-1/system.d/org.freedesktop.locale1.conf
        etc/dbus-1/system.d/org.freedesktop.machine1.conf
        etc/dbus-1/system.d/org.freedesktop.timedate1.conf
        etc/pam.d/systemd-user
        etc/systemd/bootchart.conf
        etc/systemd/coredump.conf
        etc/systemd/journal-upload.conf
        etc/systemd/journald.conf
        etc/systemd/logind.conf
        etc/systemd/resolved.conf
        etc/systemd/system.conf
        etc/systemd/timesyncd.conf
        etc/systemd/user.conf
        etc/udev/udev.conf)
install='systemd.install'
source=('git://anongit.freedesktop.org/systemd/systemd.git'
        'initcpio-hook-udev'
        'initcpio-install-udev'
        'initcpio-install-systemd')
md5sums=('SKIP'
         '90ea67a7bb237502094914622a39e281'
         'bde43090d4ac0ef048e3eaee8202a407'
         'c9db3010602913559295de3481019681')

pkgver() {
  cd 'systemd'
  git describe | sed 's/^v//;s/-/./g'
}

build() {
  cd 'systemd'

  local timeservers=({0..3}.arch.pool.ntp.org)

  ./autogen.sh
  ./configure \
      --libexecdir=/usr/lib \
      --localstatedir=/var \
      --sysconfdir=/etc \
      --enable-introspection \
      --enable-gtk-doc \
      --enable-lz4 \
      --enable-compat-libs \
      --enable-kdbus \
      --disable-audit \
      --disable-ima \
      --with-sysvinit-path= \
      --with-sysvrcnd-path= \
      --with-ntp-servers="${timeservers[*]}"

  make
}

package() {
  make -C "systemd" DESTDIR="$pkgdir" install

  rm -r "$pkgdir/usr/lib/rpm/macros.d"

  for tool in runlevel reboot shutdown poweroff halt telinit; do
    ln -s 'systemctl' "$pkgdir/usr/bin/$tool"
  done
  ln -s '../lib/systemd/systemd' "$pkgdir/usr/bin/init"

  # fix .so links in manpage stubs
  find "$pkgdir/usr/share/man" -type f -name '*.[[:digit:]]' \
      -exec sed -ri '1s|^\.so (.*)\.([0-9]+)|.so man\2/\1.\2|' {} +

  # don't write units to /etc by default
  rm "$pkgdir/etc/systemd/system/getty.target.wants/getty@tty1.service" \
      "$pkgdir/etc/systemd/system/multi-user.target.wants/systemd-networkd.service" \
      "$pkgdir/etc/systemd/system/multi-user.target.wants/systemd-resolved.service" \
      "$pkgdir/etc/systemd/system/sysinit.target.wants/systemd-timesyncd.service" \
      "$pkgdir/etc/systemd/system/network-online.target.wants/systemd-networkd-wait-online.service"
  rmdir "$pkgdir/etc/systemd/system/getty.target.wants" \
      "$pkgdir/etc/systemd/system/network-online.target.wants"

  # Replace dialout/tape/cdrom group in rules with uucp/storage/optical group
  sed -i 's#GROUP="dialout"#GROUP="uucp"#g;
          s#GROUP="tape"#GROUP="storage"#g;
          s#GROUP="cdrom"#GROUP="optical"#g' "$pkgdir"/usr/lib/udev/rules.d/*.rules
  sed -i 's/dialout/uucp/g;
          s/tape/storage/g;
          s/cdrom/optical/g' "$pkgdir"/usr/lib/sysusers.d/basic.conf

  # ensure proper permissions for /var/log/journal
  chown root:systemd-journal "$pkgdir/var/log/journal"
  chmod 2755 "$pkgdir/var/log/journal"

  # ship default policy to leave services disabled
  echo 'disable *' >"$pkgdir"/usr/lib/systemd/system-preset/99-default.preset

  # fix systemd-user PAM
  sed -i 's|system-auth|system-login|g' "$pkgdir/etc/pam.d/systemd-user"

  # add mkinitcpio hooks
  install -Dm644 "$srcdir/initcpio-install-systemd" "$pkgdir/usr/lib/initcpio/install/systemd"
  install -Dm644 "$srcdir/initcpio-install-udev" "$pkgdir/usr/lib/initcpio/install/udev"
  install -Dm644 "$srcdir/initcpio-hook-udev" "$pkgdir/usr/lib/initcpio/hooks/udev"

  # include MIT license, since it's technically custom
  install -Dm644 "$srcdir/systemd/LICENSE.MIT" \
      "$pkgdir/usr/share/licenses/systemd-git/LICENSE.MIT"
}
