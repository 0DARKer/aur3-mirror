# Maintainer: Michael Lass <bevan@bi-co.net>

pkgname=openafs-modules-dkms
_srcname=openafs
pkgver=1.6.10
pkgrel=4
pkgdesc="Kernel module for OpenAFS (dkms)"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=('dkms' 'openafs')
provides=("openafs-modules=$pkgver")
conflicts=('openafs-features-libafs' 'openafs-modules' 'openafs<1.6.6-2')
options=(!emptydirs)
source=(http://openafs.org/dl/openafs/${pkgver}/${_srcname}-${pkgver}-src.tar.bz2
        dkms.conf
        0001-Linux-3.17-No-more-typedef-for-ctl_table.patch
        0002-Linux-3.17-Deal-with-d_splice_alias-errors.patch
        0003-Linux-3.18-d_invalidate-can-no-longer-return-an-erro.patch
        0004-Linux-3.18-key_type-no-longer-has-a-match-op.patch
        0005-LINUX-Avoid-check-for-key_type.match-existence.patch
        0006-Linux-Move-code-to-reset-the-root-to-afs-LINUX.patch
        0007-Linux-d_alias-becomes-d_u.d_alias.patch
        0008-Linux-d_splice_alias-may-drop-inode-reference-on-err.patch)
install=openafs-modules-dkms.install
md5sums=('f5ecfe357f6c56053f7bbab40644496e'
         '1b10cb73e18c11abbd8cb12ce72ed4aa'
         '0a45b08af617d8e3b733c93fa019980e'
         '0eb82ff62749aa17296949926cf6e4ab'
         'dbd25ee8dc285bfc793a9ccdceca99c9'
         '18a72cde91893bea28fcd1c7bc266776'
         'cd80bd2bbfb4de24d590e043112f562c'
         '188b81171de40a97ccad2c7fa5232418'
         '834f269b5ec5d7a5f9b7cfed031e67a1'
         '48cac6ad221c86b60b27736b484f0aeb')

build() {
  cd ${srcdir}/${_srcname}-${pkgver}

  sysname=i386_linux26
  [ "$CARCH" == "x86_64" ] && sysname=amd64_linux26

  # patches needed for linux 3.17
  patch -p1 < ${srcdir}/0001-Linux-3.17-No-more-typedef-for-ctl_table.patch
  patch -p1 < ${srcdir}/0002-Linux-3.17-Deal-with-d_splice_alias-errors.patch

  # patches needed for linux 3.18
  patch -p1 < ${srcdir}/0003-Linux-3.18-d_invalidate-can-no-longer-return-an-erro.patch
  patch -p1 < ${srcdir}/0004-Linux-3.18-key_type-no-longer-has-a-match-op.patch

  # fix a possible regression from patches above
  patch -p1 < ${srcdir}/0005-LINUX-Avoid-check-for-key_type.match-existence.patch

  # structural changes required for the following patches
  # (still in upstream's review process)
  patch -p1 < ${srcdir}/0006-Linux-Move-code-to-reset-the-root-to-afs-LINUX.patch

  # patch needed for linux 3.18.1
  # (still in upstream's review process)
  patch -p1 < ${srcdir}/0007-Linux-d_alias-becomes-d_u.d_alias.patch

  # fix used after freed inode reference on linux 3.17.3 and newer
  # (still in upstream's review process)
  patch -p1 < ${srcdir}/0008-Linux-d_splice_alias-may-drop-inode-reference-on-err.patch

  # Only needed when changes to configure were made
  ./regen.sh -q

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --libexecdir=/usr/bin \
              --disable-fuse-client \
              --disable-kernel-module \
              --with-afs-sysname=$sysname

  make only_libafs_tree
}

package() {

  # install license
  install -Dm644 ${srcdir}/${_srcname}-${pkgver}/src/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # install sources
  install -dm755 ${pkgdir}/usr/src/${_srcname}-${pkgver}
  mv ${srcdir}/${_srcname}-${pkgver}/libafs_tree/* ${pkgdir}/usr/src/${_srcname}-${pkgver}
  sed "s/__VERSION__/$pkgver/" ${srcdir}/dkms.conf > ${pkgdir}/usr/src/${_srcname}-${pkgver}/dkms.conf

}
