# Maintainer: Michael Lass <bevan@bi-co.net>

pkgname=openafs-modules-dkms
_srcname=openafs
pkgver=1.6.11pre2
pkgrel=1
pkgdesc="Kernel module for OpenAFS (dkms)"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=('dkms' 'openafs')
provides=("openafs-modules=$pkgver")
conflicts=('openafs-features-libafs' 'openafs-modules' 'openafs<1.6.6-2')
options=(!emptydirs)
source=(http://openafs.org/dl/candidate/${pkgver}/${_srcname}-${pkgver}-src.tar.bz2
        dkms.conf)
install=openafs-modules-dkms.install
sha256sums=('fcd68eb6bbfa16bd4ae0d35086f37073b173cdf431c9ecd5e1c40eb51e504e67'
            '674f86573fd3b3e26c677d9afdd40d08ac3099c6240553bc8f8476531fd68986')

build() {
  cd ${srcdir}/${_srcname}-${pkgver}

  sysname=i386_linux26
  [ "$CARCH" == "x86_64" ] && sysname=amd64_linux26

  # Only needed when changes to configure were made
  #./regen.sh -q

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --libexecdir=/usr/bin \
              --disable-fuse-client \
              --disable-kernel-module \
              --with-afs-sysname=$sysname

  make only_libafs_tree
}

package() {

  # install license
  install -Dm644 ${srcdir}/${_srcname}-${pkgver}/src/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # install sources
  install -dm755 ${pkgdir}/usr/src/${_srcname}-${pkgver}
  mv ${srcdir}/${_srcname}-${pkgver}/libafs_tree/* ${pkgdir}/usr/src/${_srcname}-${pkgver}
  sed "s/__VERSION__/$pkgver/" ${srcdir}/dkms.conf > ${pkgdir}/usr/src/${_srcname}-${pkgver}/dkms.conf

}
