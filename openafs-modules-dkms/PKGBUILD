# Maintainer: Michael Lass <bevan@bi-co.net>

pkgname=openafs-modules-dkms
_srcname=openafs
pkgver=1.6.10
pkgrel=1
pkgdesc="Kernel module for OpenAFS (dkms)"
arch=('i686' 'x86_64')
url="http://www.openafs.org"
license=('custom:"IBM Public License Version 1.0"')
depends=('dkms' 'openafs')
provides=("openafs-modules=$pkgver")
conflicts=('openafs-features-libafs' 'openafs-modules' 'openafs<1.6.6-2')
options=(!emptydirs)
source=(http://openafs.org/dl/openafs/${pkgver}/${_srcname}-${pkgver}-src.tar.bz2
        dkms.conf
        0001-Linux-3.17-No-more-typedef-for-ctl_table.patch
        0002-Linux-3.17-Deal-with-d_splice_alias-errors.patch)
install=openafs-modules-dkms.install
md5sums=('f5ecfe357f6c56053f7bbab40644496e'
         '1b10cb73e18c11abbd8cb12ce72ed4aa'
         '0ce2490c8c6e70cf40568e730a26bcb5'
         '6286d93f5daea5922d96e46e4868683a')

build() {
  cd ${srcdir}/${_srcname}-${pkgver}

  sysname=i386_linux26
  [ "$CARCH" == "x86_64" ] && sysname=amd64_linux26

  # patches needed for linux 3.17
  patch -p1 < ${srcdir}/0001-Linux-3.17-No-more-typedef-for-ctl_table.patch
  patch -p1 < ${srcdir}/0002-Linux-3.17-Deal-with-d_splice_alias-errors.patch

  # Only needed when changes to configure were made
  ./regen.sh -q

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --sbindir=/usr/bin \
              --libexecdir=/usr/bin \
              --disable-fuse-client \
              --disable-kernel-module \
              --with-afs-sysname=$sysname

  make only_libafs_tree
}

package() {

  # install license
  install -Dm644 ${srcdir}/${_srcname}-${pkgver}/src/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # install sources
  install -dm755 ${pkgdir}/usr/src/${_srcname}-${pkgver}
  mv ${srcdir}/${_srcname}-${pkgver}/libafs_tree/* ${pkgdir}/usr/src/${_srcname}-${pkgver}
  sed "s/__VERSION__/$pkgver/" ${srcdir}/dkms.conf > ${pkgdir}/usr/src/${_srcname}-${pkgver}/dkms.conf

}
