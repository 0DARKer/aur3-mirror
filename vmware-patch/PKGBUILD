# Maintainer: Det <nimetonmaili g-mail>
# Contributors: Igor Duarte Cardoso, haagch, Olivier MÃ©doc

pkgname=vmware-patch
pkgver=11.0.0
pkgrel=3
pkgdesc="A post-install configuration solution for VMware Workstation and Player (Plus)"
arch=('i686' 'x86_64')
url="https://wiki.archlinux.org/index.php/VMware%23Configuration"
license=('GPL')
depends=('fuse' 'gtkmm' 'linux-headers')
options=('!emptydirs')
install=$pkgname.install
source=('vmware-patch' 'vmware-unpatch' 'common-functions.sh' 'vmware.service' 'vmware-usbarbitrator.service' 'vmware-workstation.service'
        # Workstation 10.0.5 / Player (Plus) 6.0.5
        'vmci-10.0.5-3.14.patch'
        'vmmon-10.0.5-3.14.patch'
        'vmnet-10.0.5-3.17.patch'
        'vsock-10.0.5-3.14.patch'
        'vsock-10.0.5-3.15.patch'
        # Workstation 10.0.4 / Player (Plus) 6.0.4
		'vmci-10.0.4-3.14.patch'
		'vmmon-10.0.4-3.14.patch'
		'vmnet-10.0.4-3.14.patch'
		'vsock-10.0.4-3.14.patch'
        # Workstation 10.0.1 / Player (Plus) 6.0.1
        'vmnet-10.0.1-3.13.patch')

package() {
    # Directories
    install -d "$pkgdir"/etc/init.d
    install -d "$pkgdir"/usr/bin
    install -d "$pkgdir"/usr/lib/systemd/system
    install -d "$pkgdir"/usr/lib/vmware/modules/patches
    install -d "$pkgdir"/usr/share/vmware-patch

    # Patch scripts
    install -m755 vmware-{un,}patch "$pkgdir"/usr/bin/

    # Common functions
    install -m755 common-functions.sh "$pkgdir"/usr/share/vmware-patch/

    # Patches
    install -m644 *.patch "$pkgdir"/usr/lib/vmware/modules/patches/

    # Main service (in case it doesn't exist or owned by us)
    if [[ ! -f /usr/lib/systemd/system/vmware.service ]] || pacman -Qo /usr/lib/systemd/system/vmware.service &>/dev/null; then
        install -m644 vmware.service "$pkgdir"/usr/lib/systemd/system/
    else
        warning "Not overwriting /usr/lib/systemd/system/vmware.service"
    fi

    # USB Arbitrator/Workstation Server services
    install -m644 vmware-{usbarbitrator,workstation}.service "$pkgdir"/usr/lib/systemd/system/
}

# Generated using 'updpkgsums'
md5sums=('bac9aaf69a3e0ede6edcce301cf11427'
         'ba054b375308442d43a408dbae5e9401'
         '4afaf90bd1cce3b3aebb3695fbf5855f'
         '03f7b0d6a304557e30c111b88fad69c6'
         'ea3817fb7952932707bfedcf33a70697'
         '56f7f642683e54250372bb57faaf4e95'
         '9e2c8d4d91d54edb249da24031de6985'
         '298e1b59a9e729525cc3fa9d06f127eb'
         '43dee79cc5f5980372e16a20962b3105'
         'fdf3d3876533af9ffc4fcdb27f0f957b'
         '1893542f8144aa0bb96f5433b9c34158'
         'aa4e4c353558f6ea0a160eb714633d02'
         '4eca49060d2373c259a0c045520de9f9'
         'acd25c0daccb62af8abf750e6369ae25'
         '639ed104e10c838e264c0314d2485f36'
         'd980b5092aeee55edc6dbafb843d886f')