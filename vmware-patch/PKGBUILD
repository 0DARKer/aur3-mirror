# Maintainer: Det
# Contributors: Igor Duarte Cardoso <igordcard@gmail.com>, haagch, Olivier MÃ©doc <o_medoc@yahoo.fr>

pkgname=vmware-patch
pkgver=10.0.2
pkgrel=3
pkgdesc="A post-install configuration solution for VMware Workstation and Player (Plus)"
arch=('i686' 'x86_64')
url="https://wiki.archlinux.org/index.php/Vmware%23Configuration"
license=('GPL')
depends=('fuse' 'gtkmm' 'linux-headers')
options=('!emptydirs')
install=$pkgname.install
source=('vmware-patch' 'vmware-unpatch' 'vmware.service' 'vmware-usbarbitrator.service' 'vmware-workstation.service'
        # Workstation 10.0.2 / Player (Plus) 6.0.2
        'vmci-10.0.2-10.0.1-10.0.0-6.0.2-6.0.1-6.0.0-3.15-3.14.patch'
        'vmmon-10.0.2-10.0.1-10.0.0-6.0.2-6.0.1-6.0.0-3.15-3.14.patch'
        'vsock-10.0.2-10.0.1-10.0.0-6.0.2-6.0.1-6.0.0-3.15.patch'
        'vsock-10.0.2-10.0.1-10.0.0-6.0.2-6.0.1-6.0.0-3.15-3.14.patch'
        # Workstation 10.0.1 / Player (Plus) 6.0.1
        'vmnet-10.0.1-10.0.0-6.0.1-6.0.0-3.15-3.14-3.13.patch')

package() {
    # Directories
    install -d "$pkgdir"/{etc/init.d,usr/{bin,lib/{systemd/system,vmware/modules/patches}}}

    # Patch scripts
    install -m755 vmware-{un,}patch "$pkgdir"/usr/bin/

    # Patches
    install -m644 *.patch "$pkgdir"/usr/lib/vmware/modules/patches/

    # Main service (in case it doesn't exist or owned by us)
    if [[ ! -f /usr/lib/systemd/system/vmware.service || $(pacman -Qo /usr/lib/systemd/system/vmware.service 2>&-) ]]; then
        install -m644 vmware.service "$pkgdir"/usr/lib/systemd/system/
    else
        warning "Not overwriting /usr/lib/systemd/system/vmware.service"
    fi

    # USB Arbitrator/Workstation Server services
    install -m644 vmware-{usbarbitrator,workstation}.service "$pkgdir"/usr/lib/systemd/system/
}

# Generated using 'updpkgsums' (a pacman-contrib binary merged in pacman 4.1)
md5sums=('c4fcf194eb8182bffc27214e6c18bf7c'
         '52423254228097515ecae4165ea9665e'
         '03f7b0d6a304557e30c111b88fad69c6'
         'ea3817fb7952932707bfedcf33a70697'
         '56f7f642683e54250372bb57faaf4e95'
         '9228a5d7adf3b0b6663e821807e5c911'
         '56a25fee00705466e22767426e76f868'
         '1893542f8144aa0bb96f5433b9c34158'
         'd6086a3289f20f17e6779d5c0a65c2c8'
         '39bd3ee62b652d0a9a90179dfa24784b')
