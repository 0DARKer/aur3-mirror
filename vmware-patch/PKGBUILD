# Maintainer: Det <nimetonmaili g-mail>
# Contributors: Igor Duarte Cardoso, haagch, Olivier MÃ©doc

pkgname=vmware-patch
pkgver=10.0.4
pkgrel=1
pkgdesc="A post-install configuration solution for VMware Workstation and Player (Plus)"
arch=('i686' 'x86_64')
url="https://wiki.archlinux.org/index.php/VMware%23Configuration"
license=('GPL')
depends=('fuse' 'gtkmm' 'linux-headers')
options=('!emptydirs')
install=$pkgname.install
source=('vmware-patch' 'vmware-unpatch' 'common-functions.sh' 'vmware.service' 'vmware-usbarbitrator.service' 'vmware-workstation.service'
        # Workstation 10.0.4 / Player (Plus) 6.0.4
        'vmci-10.0.4-3.14.patch'
        'vmmon-10.0.4-3.14.patch'
        'vmnet-10.0.4-3.14.patch'
        'vmnet-10.0.4-3.17.patch'
        'vsock-10.0.4-3.14.patch'
        'vsock-10.0.4-3.15.patch'
        # Workstation 10.0.1 / Player (Plus) 6.0.1
        'vmnet-10.0.1-3.13.patch')

package() {
    # Directories
    install -d "$pkgdir"/etc/init.d
    install -d "$pkgdir"/usr/bin
    install -d "$pkgdir"/usr/lib/systemd/system
    install -d "$pkgdir"/usr/lib/vmware/modules/patches
    install -d "$pkgdir"/usr/share/vmware-patch

    # Patch scripts
    install -m755 vmware-{un,}patch "$pkgdir"/usr/bin/

    # Common functions
    install -m755 common-functions.sh "$pkgdir"/usr/share/vmware-patch/

    # Patches
    install -m644 *.patch "$pkgdir"/usr/lib/vmware/modules/patches/

    # Main service (in case it doesn't exist or owned by us)
    if [[ ! -f /usr/lib/systemd/system/vmware.service ]] || pacman -Qo /usr/lib/systemd/system/vmware.service &>/dev/null; then
        install -m644 vmware.service "$pkgdir"/usr/lib/systemd/system/
    else
        warning "Not overwriting /usr/lib/systemd/system/vmware.service"
    fi

    # USB Arbitrator/Workstation Server services
    install -m644 vmware-{usbarbitrator,workstation}.service "$pkgdir"/usr/lib/systemd/system/
}

# Generated using 'updpkgsums' (a pacman-contrib binary merged in pacman 4.1)
md5sums=('d4d6c440c34c36b45b3a54ffd726bf69'
         'ba054b375308442d43a408dbae5e9401'
         '4afaf90bd1cce3b3aebb3695fbf5855f'
         '03f7b0d6a304557e30c111b88fad69c6'
         'ea3817fb7952932707bfedcf33a70697'
         '56f7f642683e54250372bb57faaf4e95'
         '9228a5d7adf3b0b6663e821807e5c911'
         '56a25fee00705466e22767426e76f868'
         'acd25c0daccb62af8abf750e6369ae25'
         '43dee79cc5f5980372e16a20962b3105'
         'd6086a3289f20f17e6779d5c0a65c2c8'
         '1893542f8144aa0bb96f5433b9c34158'
         'd980b5092aeee55edc6dbafb843d886f')
