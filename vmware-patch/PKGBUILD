# Maintainer: Det
# Contributors: Igor Duarte Cardoso <igordcard@gmail.com>, haagch, Olivier MÃ©doc <o_medoc@yahoo.fr>

pkgname=vmware-patch
pkgver=10.0.3
pkgrel=3
pkgdesc="A post-install configuration solution for VMware Workstation and Player (Plus)"
arch=('i686' 'x86_64')
url="https://wiki.archlinux.org/index.php/Vmware%23Configuration"
license=('GPL')
depends=('fuse' 'gtkmm' 'linux-headers')
options=('!emptydirs')
install=$pkgname.install
source=('vmware-patch' 'vmware-unpatch' 'vmware.service' 'vmware-usbarbitrator.service' 'vmware-workstation.service'
        # Workstation 10.0.2 / Player (Plus) 6.0.2
        'vmci-10.0.3-10.0.2-10.0.1-10.0.0-6.0.3-6.0.2-6.0.1-6.0.0-3.16-3.15-3.14.patch'
        'vmmon-10.0.3-10.0.2-10.0.1-10.0.0-6.0.3-6.0.2-6.0.1-6.0.0-3.16-3.15-3.14.patch'
        'vmnet-10.0.3-10.0.2-10.0.1-10.0.0-6.0.3-6.0.2-6.0.1-6.0.0-3.16-3.15-3.14.patch'
        'vsock-10.0.3-10.0.2-10.0.1-10.0.0-6.0.3-6.0.2-6.0.1-6.0.0-3.16-3.15.patch'
        'vsock-10.0.3-10.0.2-10.0.1-10.0.0-6.0.3-6.0.2-6.0.1-6.0.0-3.16-3.15-3.14.patch'
        # Workstation 10.0.1 / Player (Plus) 6.0.1
        'vmnet-10.0.1-10.0.0-6.0.1-6.0.0-3.16-3.15-3.14-3.13.patch')

package() {
    # Directories
    install -d "$pkgdir"/{etc/init.d,usr/{bin,lib/{systemd/system,vmware/modules/patches}}}

    # Patch scripts
    install -m755 vmware-{un,}patch "$pkgdir"/usr/bin/

    # Patches
    install -m644 *.patch "$pkgdir"/usr/lib/vmware/modules/patches/

    # Main service (in case it doesn't exist or owned by us)
    if [ ! -f /usr/lib/systemd/system/vmware.service ] || pacman -Qo /usr/lib/systemd/system/vmware.service &>/dev/null; then
        install -m644 vmware.service "$pkgdir"/usr/lib/systemd/system/
    else
        warning "Not overwriting /usr/lib/systemd/system/vmware.service"
    fi

    # USB Arbitrator/Workstation Server services
    install -m644 vmware-{usbarbitrator,workstation}.service "$pkgdir"/usr/lib/systemd/system/
}

# Generated using 'updpkgsums' (a pacman-contrib binary merged in pacman 4.1)
md5sums=('8864169c0e88403e3ec715224a4c010a'
         '2dff3a48d8c7649c72b3f7e6b6b04c79'
         '03f7b0d6a304557e30c111b88fad69c6'
         'ea3817fb7952932707bfedcf33a70697'
         '56f7f642683e54250372bb57faaf4e95'
         '9228a5d7adf3b0b6663e821807e5c911'
         '56a25fee00705466e22767426e76f868'
         'acd25c0daccb62af8abf750e6369ae25'
         '1893542f8144aa0bb96f5433b9c34158'
         'd6086a3289f20f17e6779d5c0a65c2c8'
         'd980b5092aeee55edc6dbafb843d886f')
