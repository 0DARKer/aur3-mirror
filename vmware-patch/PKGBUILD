# Maintainer: Det
# Contributors: Igor Duarte Cardoso <igordcard@gmail.com>, haagch, Olivier MÃ©doc <o_medoc@yahoo.fr>
pkgname=vmware-patch
pkgver=9.0.2
pkgrel=4
pkgdesc="Several scripts to apply patches to VMware Workstation and Player modules"
arch=('i686' 'x86_64')
url="https://www.vmware.com"
license=('GPL')
depends=('linux-headers')
options=('!emptydirs')
install=${pkgname}.install
source=('vmware-patch' 'vmware-unpatch' 'vmware.service' 'vmware-usbarbitrator.service' 'vmware-workstation.service'
        # Workstation 9/Player 5
        'vmblock-9.0.2-5.0.2-9.0.1-5.0.1-9.0.0-5.0.0-3.9-3.8-3.7-3.6.patch'
        'vmci-9.0.1-5.0.1-3.8.patch'
        'vmmon-9.0.0-5.0.0-3.7-3.6-3.5.patch'
        # Workstation 8/Player 4
        'vmblock-8.0.4-4.0.4-8.0.3-4.0.3-3.6-3.5-3.4.patch' 'vmnet-8.0.4-4.0.4-8.0.3-4.0.3-8.0.2-4.0.2-3.6-3.5-3.4-3.3-3.2.patch'
        # Workstation 7/Player 3
        'vmblock-7.1.5-3.1.5-3.4.patch' 'vmmon-7.1.5-3.1.5-3.4-3.3-3.2.patch' 'vmnet-7.1.5-3.1.5-3.4-3.3-3.2.patch'
        'vmblock-7.1.5-3.1.5-3.3-3.2-3.1-3.0.patch' 'vsock-7.1.5-3.1.5-3.4-3.3-3.2-3.1-3.0.patch'
        'vmnet-7.1.5-3.1.5-3.1.patch' 'vmmon-7.1.5-3.1.5-3.1-3.0-2.6.39.patch'
        'vmblock-7.1.5-3.1.5-2.6.39.patch' 'vmci-7.1.5-3.1.5-2.6.39.patch' 'vmnet-7.1.5-3.1.5-3.0-2.6.39.patch' 'vsock-7.1.5-3.1.5-2.6.39.patch'
        'vmci-7.1.5-3.1.5-2.6.37.patch' 'vmmon-7.1.5-3.1.5-2.6.37.patch' 'vmnet-7.1.5-3.1.5-2.6.37.patch' 'vsock-7.1.5-3.1.5-2.6.37.patch'
        'vmmon-7.1.5-3.1.5-2.6.36.patch'
        'vmmon-7.1.5-3.1.5-2.6.35.patch' 'vsock-7.1.5-3.1.5-2.6.35.patch')

package() {
    # Create directories
    install -d "${pkgdir}"/{etc/init.d,usr/{bin,lib/{systemd/system,vmware/modules/patches}},sbin}

    # Patch scripts
    install -m755 vmware-{un,}patch "${pkgdir}"/usr/bin/

    # Patches
    install -m644 *.patch "${pkgdir}"/usr/lib/vmware/modules/patches/

    # Install the main service, if necessary (doesn't exist or owned by us)
    if [[ ! -f /usr/lib/systemd/system/vmware.service || $(pacman -Qo /usr/lib/systemd/system/vmware.service 2>&-) ]]; then
        install -m644 vmware.service "${pkgdir}"/usr/lib/systemd/system/
    else
        warning "Not overwriting /usr/lib/systemd/system/vmware.service"
    fi

    # USB Arbitrator and Workstation Server service
    install -m644 vmware-{usbarbitrator,workstation}.service "${pkgdir}"/usr/lib/systemd/system/

    # Create /sbin symlinks, if necessary (they don't exist or owned by us)
    for i in insmod lsmod rmmod modinfo; do
        if [[ ! -h /sbin/${i} || $(pacman -Qo /sbin/${i} 2>&-) ]]; then
            ln -s /usr/bin/${i} "${pkgdir}"/sbin/
        else # Already there
            warning "Not overwriting /sbin/${i}"
        fi
    done

    # Symlink version.h, if necessary (1* A VMware product actually exists)
    if [ -f /usr/bin/vmware-installer ]; then
        # Use separate lines for simplicity (2* installed Workstation/Player less than 9.0.2/5.0.2)
        if [[ ! $(vmware-installer -l |& egrep "(player|workstation)" | grep -Po "(\d\.){2}\d") = [59].0.[2-6] ]]; then
            for i in /usr/src/linux-*; do
                # 3* Kernel version 3.5 or above (another way to deal with the numbers)
                if [ $(echo $i | cut -d "-" -f2 | cut -d "." -f-2 | tr -d '.') -ge 35 ]; then
                    # 4* No version.h or owned by us
                    if [[ ! -h ${i}/include/linux/version.h || $(pacman -Qo ${i}/include/linux/version.h 2>&-) ]]; then
                        install -d "${pkgdir}"/${i}/include/linux/
                        ln -s ${i}/include/generated/uapi/linux/version.h "${pkgdir}"/${i}/include/linux/
                    else
                        warning "Not overwriting ${i}/include/generated/uapi/linux/version.h"
                    fi
                fi
            done
        fi
    fi
}

# Generated using 'updpkgsums' (a pacman-contrib binary merged in pacman 4.1)
md5sums=('6bf221b23a0634e661b74b614cb6040f'
         'ae58ab62507210074a1b8a96c43f7b09'
         '03f7b0d6a304557e30c111b88fad69c6'
         'ea3817fb7952932707bfedcf33a70697'
         '56f7f642683e54250372bb57faaf4e95'
         'a6b96ba687e8ece29fa130c619996dc9'
         'c95ff54cc002921fdbd32735dc5d6510'
         '4eb0d38133bd4c019ce45a78e23233fe'
         'a8d9b98570344514eda185a8115b2c32'
         'edea711dca63399b7f47b53e50444c09'
         '45c79503e541f0cad53b2c564deabffa'
         'b8d90cee99489b8b4c9ab5d0f86fd618'
         'b16f5e552d364419b6671ae76c6054f3'
         'd1c9773adc7ec14392f2ac7e7610afec'
         'adb689206fe85cc0e19ac4b3e50cd656'
         '827b32cec681045a3d2ef761631aa2b2'
         '2e24ef4fa122afd023685d521d533ffc'
         '0c317d09aaed3b10de4a35a97c605f8d'
         'e3efe95c6f90e87bb8fe78ccceb7c4c4'
         'f2cc9e4042c43f6075207f3612d72f21'
         'adb689206fe85cc0e19ac4b3e50cd656'
         '289fe3df6aa21e53f0071079800e5754'
         'a4d7bcc5a23382b0c6dd5040723b4d0b'
         'f07e53526dd47a248dec7eacd54121a8'
         '0bbfdfe33dd5e14d96b9719456b0b49c'
         'f0a138dcfb01a12b54bd4268f23fcc2b'
         '0ebdad5df51c600a2219a9fdf5339096'
         '98fbca6f17795df309accad565a35bf4')
