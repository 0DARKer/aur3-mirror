# Maintainer: Det
# Contributors: Igor Duarte Cardoso <igordcard@gmail.com>, haagch, Olivier MÃ©doc <o_medoc@yahoo.fr>
pkgname=vmware-patch
pkgver=10.0.1
pkgrel=7
pkgdesc="A post-install configuration solution for VMware Workstation and Player (Plus)"
arch=('i686' 'x86_64')
url="https://wiki.archlinux.org/index.php/Vmware%23Configuration"
license=('GPL')
depends=('linux-headers')
optdepends=("fuse: for the 'vmware-vmblock-fuse' client instead of the 'vmblock.ko' module")
options=('!emptydirs')
install=${pkgname}.install
source=('vmware-patch' 'vmware-unpatch' 'vmware.service' 'vmware-usbarbitrator.service' 'vmware-workstation.service'
        # Workstation 10 / Player (Plus) 6
        'vmblock-10.0.1-10.0.0-6.0.1-6.0.0-3.14.patch'
        'vmci-10.0.1-10.0.0-6.0.1-6.0.0-3.14.patch'
        'vmmon-10.0.1-10.0.0-6.0.1-6.0.0-3.14.patch'
        'vmnet-10.0.1-10.0.0-6.0.1-6.0.0-3.14-3.13.patch'
        'vsock-10.0.1-10.0.0-6.0.1-6.0.0-3.14.patch'
        # Workstation 9 / Player 5
        'vmblock-9.0.2-5.0.2-3.13-3.12.patch'
        'vmblock-9.0.2-5.0.2-3.13-3.12-3.11.patch'
        'vmblock-9.0.2-5.0.2-3.13-3.12-3.11-3.10.patch'
        'vmnet-9.0.2-5.0.2-3.13-3.12-3.11-3.10.patch')

package() {
    # Directories
    install -d "${pkgdir}"/{etc/init.d,usr/{bin,lib/{systemd/system,vmware/modules/patches}}}

    # Patch scripts
    install -m755 vmware-{un,}patch "${pkgdir}"/usr/bin/

    # Patches
    install -m644 *.patch "${pkgdir}"/usr/lib/vmware/modules/patches/

    # Main service (in case it doesn't exist or owned by us)
    if [[ ! -f /usr/lib/systemd/system/vmware.service || $(pacman -Qo /usr/lib/systemd/system/vmware.service 2>&-) ]]; then
        install -m644 vmware.service "${pkgdir}"/usr/lib/systemd/system/
    else
        warning "Not overwriting /usr/lib/systemd/system/vmware.service"
    fi

    # USB Arbitrator/Workstation Server services
    install -m644 vmware-{usbarbitrator,workstation}.service "${pkgdir}"/usr/lib/systemd/system/
}

# Generated using 'updpkgsums' (a pacman-contrib binary merged in pacman 4.1)
md5sums=('554efb6bc1b441eb514d6daadbccdc32'
         '09fa916e891c04ae53c5b3ce3dc8bbde'
         '03f7b0d6a304557e30c111b88fad69c6'
         'ea3817fb7952932707bfedcf33a70697'
         '56f7f642683e54250372bb57faaf4e95'
         'ef9f9f846d9cf28ad89cdc5235ec9c8d'
         '9228a5d7adf3b0b6663e821807e5c911'
         '56a25fee00705466e22767426e76f868'
         '39bd3ee62b652d0a9a90179dfa24784b'
         'd6086a3289f20f17e6779d5c0a65c2c8'
         '28c37d1cc61a7a4004e028fa574aeb44'
         'f56140753fed23554af954d1106f1455'
         'ce885089247ec49fc4d86b8d7f33b6f6'
         '6dc136e002d86382b66b287e419e2680')
