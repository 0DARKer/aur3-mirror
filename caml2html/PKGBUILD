# Maintainer: Baptiste Jonglez <zerstorer@free.fr>
pkgname=caml2html
pkgver=1.4.1
pkgrel=5
pkgdesc="A tool to generate colorized html source from OCaml source code, and much more : LaTeX output, type display... (standalone binary)"
arch=('i686' 'x86_64')
url="http://martin.jambon.free.fr/caml2html.html"
license=('GPL')
depends=()
makedepends=('ocaml')
optdepends=()
options=('!strip')
source=("http://martin.jambon.free.fr/$pkgname-$pkgver.tar.bz2"
	'caml2html.1.gz')
noextract=('caml2html.1.gz')
md5sums=('cecc0b3191a99611609a0fb1957c15b6'
         '9286b5894e7f2b3ad1156f31b0cc2ff1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # only making binary (default target)
  # see `caml2html-full' for the OCaml library
  PREFIX=/usr make
  
  # fix a version number that hasn't been updated
  sed -i -e "s/1.3.1/1.4.1/" META
 
  # add `|| true' to prevent make from exiting with error code 1
  sed -i -e '38c\
	test -f caml2html.cma -o -f caml2html.cmxa && $(MAKE) libinstall || true' Makefile
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p "$pkgdir/usr/bin"
  PREFIX="$pkgdir/usr" make install

  cd "$srcdir"
  mkdir -p "$pkgdir/usr/share/man/man1"
  install -m 0644 caml2html.1.gz "$pkgdir/usr/share/man/man1"
}
