_pkgbasename=ppl
pkgname=lib32-$_pkgbasename
pkgver=0.11.2
pkgrel=3
pkgdesc="A modern library for convex polyhedra and other numerical abstractions. (32 bit)"
arch=('x86_64')
url="http://www.cs.unipr.it/ppl"
license=('GPL3')
depends=('lib32-gmp' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
options=('!docs' '!libtool')
source=(http://bugseng.com/products/ppl/download/ftp/releases/$pkgver/ppl-$pkgver.tar.bz2)
md5sums=('c24429e6c3bc97d45976a63f40f489a1')

build() {
  cd $srcdir/ppl-$pkgver
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS="$LDFLAGS -I/usr/lib32/gmp" #force lib32-gmp headers

  ./configure --prefix=/usr --enable-interfaces="c,cxx" \
		--libdir=/usr/lib32 --libexecdir=/usr/lib32
  make
}

package() {
  cd $srcdir/ppl-$pkgver
  make DESTDIR=$pkgdir install
  rm -Rf ${pkgdir}/usr/{bin,include,share}
}
