# Maintained by Score_Under <seejay.11@gmail.com>
_pkgname=flvlib
pkgname=python2-$_pkgname
pkgver=0.1.13
pkgrel=1
pkgdesc="Python library for parsing, manipulating and indexing FLV files"
arch=(any)
url="https://wulczer.org/flvlib/"
license=('MIT')
depends=('python2')
source=("https://wulczer.org/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2")
sha256sums=(fa6ba5be85046e8f9862337472dd89bc3c7a3fdca8ab2a8887bd66a9e5feadea)

prepare() {
  cd "${_pkgname}-${pkgver}"
  set_python2
}

package() {
  cd "${_pkgname}-${pkgver}"
  python2 ./setup.py install --root="$pkgdir/"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# Taken from python-potr's pkgbuild
# To use python2 instead of default python v3.
set_python2() {
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' "${file}"
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' "${file}"
  done
}
