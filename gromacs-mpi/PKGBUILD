# $Id: PKGBUILD 57440 2011-10-27 20:16:15Z lcarlier $
# Maintainer: Hector <hsearaDOTatDOTgmailDOTcom>
# Contributor: Eduardo Martins Lopes "duca" <edumlopes@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Ricardo <rikardo.horo@gmail.com>

pkgname=gromacs-mpi
_pkgname=gromacs
pkgver=5.0.4
pkgrel=1
pkgdesc='A versatile package to perform molecular dynamics, i.e. simulate the Newtonian equations of motion for systems with hundreds to millions of particles.'
url='http://www.gromacs.org/'
license=("GPL")
arch=('i686' 'x86_64')
depends=('gromacs' 'fftw' 'lesstif' 'perl' 'libxml2' 'libsm' 'libx11' 'openmpi')
makedepends=('cmake')
options=('!libtool')
source=(ftp://ftp.gromacs.org/pub/gromacs/${_pkgname}-${pkgver}.tar.gz)
sha1sums=('faeeab8486475b26bede1ab4ff762f23cd973b1e')

build() {
  mkdir -p ${srcdir}/{single,double}

  ###### CMAKE OPTIONS DISABLE BY DEFAULT ###########
  # Add them to the cmake commands if you need themÂ #
  #        -DGMX_SIMD=AVX_256 \                     #  
  #        -DREGRESSIONTEST_DOWNLOAD=ON \           #
  ###################################################
  msg2 "Building the double precision files"
  cd ${srcdir}/double	
  cmake ../${_pkgname}-${pkgver}/ \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_SHARED_LIBS=ON \
        -DGMX_X11=ON \
        -DGMX_MPI=YES \
        -DGMX_OPENMP=ON \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DGMX_DOUBLE=ON \
        -DGMX_SIMD=AVX_256 \
        -DGMX_LIBS_SUFFIX=_d
  make

  msg2 "Building the single precision files"
  cd ${srcdir}/single
  cmake ../${_pkgname}-${pkgver}/ \
        -DCMAKE_INSTALL_PREFIX=/usr/ \
        -DBUILD_SHARED_LIBS=ON \
        -DGMX_X11=ON \
        -DGMX_MPI=YES \
        -DGMX_OPENMP=ON \
        -DGMX_SIMD=AVX_256 \
        -DCMAKE_INSTALL_LIBDIR=lib
  make
}

check () {
  ########################################
  #   Currently tests fail. This is the  #
  #   reason they are commented out.     #
  #   This is normal so far.             #
  ######################################## 
  msg2 "Testing double precision compilation"
  cd ${srcdir}/double
  #make test
  msg2 "Testing single precision compilation"
  cd ${srcdir}/single
  #make test
}

package() {

  msg2 "Making the single precision executables"
  cd ${srcdir}/single
  make  DESTDIR=${pkgdir} install

  # Cleaning up, kept the csh completion at default location
  msg2 "Making the double precision executables"
  cd ${srcdir}/double
  make DESTDIR=${pkgdir} install

  # Cleaning to combine with gromacs package:
  rm -rf ${pkgdir}/usr/include #Already in gromacs package
  rm -rf ${pkgdir}/usr/share  #Already in gromacs package
  rm -rf ${pkgdir}/usr/bin/GMXRC* #Already in gromacs package
  rm -rf ${pkgdir}/usr/bin/demux.pl #Already in gromacs package
  rm -rf ${pkgdir}/usr/bin/demux.pl #Already in gromacs package
  rm -rf ${pkgdir}/usr/bin/xplor2gmx.pl #Already in gromacs package
  rm -f ${pkgdir}/usr/bin/completion.* #Already in gromacs package
  rm -f ${pkgdir}/usr/bin/gmx-completion.bash #Already in gromacs package
}
