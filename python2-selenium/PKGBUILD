# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=python2-selenium
_realpkgname=selenium
pkgver=2.31.0
pkgrel=2
pkgdesc="Python binding for Selenium Remote Control"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/selenium"
license=('Apache')
depends=('python2')
makedepends=('python2-distribute')
provides=('python-selenium')
conflicts=('python-selenium')
optdepends=('lib32-glibc: Firefox x86 WebDriver support for x86_64')
source=("http://pypi.python.org/packages/source/s/selenium/selenium-${pkgver}.tar.gz")
md5sums=('8ed4e8e1907dd06b0d9801b4d6b512ad')
options=('!strip')

build() {
  cd "${srcdir}/${_realpkgname}-${pkgver}"
  find "${srcdir}" -name '*.py' -exec sed -i -e 's|^\(#!.*\)\bpython$|\1python2|' {} +
  python2 setup.py build
}

package() {
  cd "${srcdir}/${_realpkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" -O1
}

# vim:set ts=2 sw=2 et:
