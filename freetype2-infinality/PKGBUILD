# Contributor: JIN Xiao-Yong <jinxiaoyong@gmail.com>

pkgname=freetype2-infinality
pkgver=2.4.4
pkgrel=3
pkgdesc="TrueType font rendering library with infinality patch"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib')
conflicts=('freetype2')
provides=("freetype2=$pkgver")
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        infinality-settings
        freetype-2.2.1-enable-valid.patch
        http://www.infinality.net/files/freetype-add-subpixel-hinting-infinality-20101114-1.patch
        http://www.infinality.net/files/freetype-enable-subpixel-hinting-infinality-20100909-1.patch
        http://www.infinality.net/files/freetype-entire-infinality-patchset-20101114-1.patch
        freetype2-infinality-protect_null_pointer-goddesse.patch)

build() {
  cd "${srcdir}/freetype-${pkgver}"
  local patch_file
  for patch_file in "${source[@]:2}"; do
      patch -Np1 -i "${srcdir}/${patch_file##*/}"
  done

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -T "${srcdir}/infinality-settings" "${pkgdir}/etc/profile.d/infinality-settings.sh"
}

md5sums=('b3e2b6e2f1c3e0dffa1fd2a0f848b671'
         '416154ff9eb8dfb27e46080f678785b8'
         '214119610444c9b02766ccee5e220680'
         '799a2d229c2997a5bb2d5b7c9f92fdba'
         'a88b1a66da44a90c1fdcdeaa39a2dc09'
         '6725774b17820f5413cb6a3cf7547f53'
         '316d2986f9a08e7ceb51fb9d5a5cab38')
