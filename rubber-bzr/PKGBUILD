# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=rubber-bzr
pkgver=420
pkgrel=2
pkgdesc="A wrapper for LaTeX and friends, latest bzr version."
arch=('any')
url="https://launchpad.net/rubber"
license=('GPL')
depends=('python2' 'texlive-core')
install=rubber.install
makedepends=('bzr' 'texinfo')
provides=('rubber')
conflicts=('rubber')
replaces=('rubber-darcs')
source=('rubber::bzr+http://bazaar.launchpad.net/~rubber-devs/rubber/main/'
        'http://sources.debian.net/data/main/r/rubber/1.1+20100306-6/debian/patches/02_rubber_bibtext_20120807.diff'
        'install.patch')
md5sums=('SKIP'
         'df12e94475a376c60e1914c6dad14ad4'
         '29b6260060f620237fd3a429c8e752ec')

pkgver() {
  cd rubber
  bzr revno
}

prepare() {
  cd rubber
  patch -p1 < ../02_rubber_bibtext_20120807.diff # https://bugs.debian.org/cgi-bin/bugreport.cgi?bug=684228
  patch -p1 < ../install.patch
}

build() {
  cd rubber

  ./configure --prefix=/usr
  make
}

package() {
  cd rubber
  make DESTDIR="$pkgdir"/ install
  cd "$pkgdir"/usr
  mv info man share/
  rm share/rubber/modules/etex.rub # fixes http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=592865
}

