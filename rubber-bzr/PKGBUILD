# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=rubber-bzr
pkgver=417
pkgrel=2
pkgdesc="A wrapper for LaTeX and friends, latest bzr version."
arch=('any')
url="https://launchpad.net/rubber"
license=('GPL')
depends=('python2' 'texlive-core')
install=rubber.install
makedepends=('bzr' 'texinfo')
provides=('rubber')
conflicts=('rubber')
replaces=('rubber-darcs')
source=('rubber::bzr+http://bazaar.launchpad.net/~rubber-devs/rubber/main/'
        'install.patch')
md5sums=('SKIP'
         '29b6260060f620237fd3a429c8e752ec')

pkgver() {
  cd "$srcdir/rubber"
  bzr revno
}

prepare() {
  cd "$srcdir/rubber"
  patch -p1 < ../install.patch
}

build() {
  cd "$srcdir/rubber"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/rubber"
  make DESTDIR="$pkgdir/" install
  cd "$pkgdir/usr"
  mv info man share/
  rm share/rubber/modules/etex.rub # fixes http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=592865
}

