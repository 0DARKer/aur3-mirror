# Maintainer: spider-mario <spidermario@free.fr>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=rubber-bzr
pkgver=417
pkgrel=2
pkgdesc="A wrapper for LaTeX and friends, latest bzr version."
arch=('any')
url="https://launchpad.net/rubber"
license=('GPL')
depends=('python2' 'texlive-core')
install=rubber.install
makedepends=('bzr' 'texinfo')
provides=('rubber')
conflicts=('rubber')
replaces=('rubber-darcs')
source=('install.patch')
md5sums=('29b6260060f620237fd3a429c8e752ec')

_bzrtrunk="https://code.launchpad.net/~rubber-devs/rubber/main"
_bzrmod="$pkgname"

build() {
  cd "$srcdir"
  msg "Connecting to Bazaar server...."

  if [ -d $_bzrmod ] ; then
    cd ${_bzrmod} && bzr --no-plugins pull ${_bzrtrunk} -r ${pkgver}
    msg "The local files are updated."
  else
    bzr --no-plugins branch ${_bzrtrunk} ${_bzrmod} -q -r ${pkgver}
  fi

  msg "Bazaar checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_bzrmod-build"
  cp -r "$srcdir/$_bzrmod" "$srcdir/$_bzrmod-build"
  cd "$srcdir/$_bzrmod-build"
  patch -p1 < ../install.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_bzrmod-build"
  make DESTDIR="$pkgdir/" install
  cd "$pkgdir/usr"
  mv info man share/
  rm share/rubber/modules/etex.rub # fixes http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=592865
}

