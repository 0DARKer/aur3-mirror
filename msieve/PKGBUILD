# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=msieve
pkgver=1.50
pkgrel=1
pkgdesc='Library for factoring large integers'
url='http://sourceforge.net/projects/msieve/'
license=('custom')
arch=('i686' 'x86_64')
depends=('zlib' 'gmp-ecm')
source=("http://downloads.sourceforge.net/project/msieve/msieve/Msieve%20v${pkgver}/msieve${pkgver/./}src.tar.gz")
sha1sums=('ba50c8835909e576caf278f072878535705d8a24')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -e 's/^CFLAGS = /CFLAGS += /' -i Makefile
	CFLAGS+=' -fopenmp'

	make ${CARCH/i686/x86} ECM=1
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -Dm755 msieve "${pkgdir}"/usr/bin/msieve
	install -Dm544 Readme "${pkgdir}"/usr/share/doc/msieve/Readme
	install -d "${pkgdir}"/usr/share/licenses/msieve; ln -s /usr/share/doc/msieve/Readme "${pkgdir}"/usr/share/licenses/msieve/Readme
}
