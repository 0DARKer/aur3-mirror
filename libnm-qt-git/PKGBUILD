# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libnm-qt-git
pkgver=1a1bda4
pkgrel=1
pkgdesc='Qt-only wrapper for NetworkManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libnm-qt'
license=('LGPL')
depends=('libmm-qt' 'networkmanager')
makedepends=('cmake' 'git')
provides=('libnm-qt')
conflicts=('libnm-qt')
source=('git://anongit.kde.org/libnm-qt')
md5sums=('SKIP')

pkgver() {
  cd libnm-qt
  git describe --always | sed 's|-|.|g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libnm-qt \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
