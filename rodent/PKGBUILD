# Maintainer: Anntoin Wilkinson <anntoin gmail com>
# Contributor: IÃ±igo Alvarez<alvarezviu@gmail.com>
pkgname=rodent
pkgver=5.0.3
pkgrel=2
pkgdesc="Lightweight, yet full of options filemanager, compatible with console commands"
arch=('i686' 'x86_64')
url="http://xffm.org/"
license=('GPL3')
depends=('pkg-config' 'libzip' 'intltool' 'file' 'gawk' 'librsvg' 'gtk3' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('python2: for rfm-simple-agent')
options=('!libtool')
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/xffm/$pkgver/$pkgname-$pkgver.tar.bz2)
md5sums=('538f5323f871e939774fc1b9d6683ad9')

prepare()
{
    # Fix doc path
    sed -i 's/@RC_DIR@/@PACKAGE_NAME@/' $srcdir/$pkgname-$pkgver/Build/share/Makefile.in

    # Specify python2
    sed -i 's/python/python2/' $srcdir/$pkgname-$pkgver/libs/rfm/plugins/filesystem_user_space/rfm-simple-agent
}

build()
{
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --with-gtk3 --prefix=/usr
    make
}

package()
{
    cd "$srcdir/$pkgname-$pkgver"
    make install DESTDIR=$pkgdir
}

