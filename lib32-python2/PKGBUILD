# Maintainer: Jameson Pugh <imntreal@gmail.com>

_pkgbasename=python2
pkgname=lib32-$_pkgbasename
pkgver=2.7.3
pkgrel=1
_pybasever=2.7
pkgdesc="A high-level scripting language (32 bit, libraries only)"
arch=('x86_64')
license=('PSF')
url="http://www.python.org/"
depends=('lib32-db' 'lib32-bzip2' 'lib32-gdbm' 'lib32-openssl' 'lib32-zlib'
                 'lib32-expat' 'lib32-sqlite3' 'lib32-libffi' "$_pkgbasename>=$pkgver")
makedepends=('tk' 'gcc-multilib')
optdepends=('tk: for IDLE')
conflicts=('python<3')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2)
sha256sums=('726457e11cb153adc3f428aaf1901fc561a374c30e5e7da6742c0742a338663c')
 
build() {
  cd "${srcdir}/Python-${pkgver}"
 
  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c
 
  # Enable built-in SQLite3 module to load extensions (fix FS#22122)
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py
 
  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*
 
  export OPT="${CFLAGS}"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  ./configure --prefix=/usr --enable-shared --with-threads --enable-ipv6 \
              --enable-unicode=ucs4 --with-system-expat --with-system-ffi \
              --libdir=/usr/lib32 --libexecdir=/usr/lib32 CC='gcc -m32' \
              CXX='g++ -m32'
 
  # Force /usr/lib to /usr/lib32
  sed -i -e 's#$(prefix)/lib#$(prefix)/lib32#' Makefile
 
  make
}
 
package() {
  cd "${srcdir}/Python-${pkgver}"
 
  make DESTDIR=${pkgdir} altinstall
 
  ln -sf ../../libpython${_pybasever}.so \
    ${pkgdir}/usr/lib32/python${_pybasever}/config/libpython${_pybasever}.so
 
  mv ${pkgdir}/usr/bin/smtpd.py $pkgdir/usr/lib32/python${_pybasever}/
 
  # some useful "stuff"
  install -dm755 ${pkgdir}/usr/lib32/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py \
    ${pkgdir}/usr/lib32/python${_pybasever}/Tools/i18n/
  install -m755 Tools/scripts/{README,*py} \
    ${pkgdir}/usr/lib32/python${_pybasever}/Tools/scripts/
 
  # clean up #!s
  find ${pkgdir}/usr/lib32/python2.7/ -name '*.py' | \
    xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"
 
  # clean-up reference to build directory
  sed -i "s#${srcdir}/Python-${pkgver}:##" \
    ${pkgdir}/usr/lib32/python${_pybasever}/config/Makefile
 
  # Clean up, only libraries needed
  rm -rf ${pkgdir}/usr/{bin,include,share}
}

# vim:set ts=2 sw=2 et:
