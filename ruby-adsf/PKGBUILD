# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: carlf <carlf at photocarl dot org>
# Contributor: oslik <oslik at mail dot ru>
#
# namcap warns that dependency ruby-rack is not needed. This is untrue.

pkgname=ruby-adsf
_pkgname="${pkgname#ruby-}"
pkgver=1.2.0
pkgrel=2
pkgdesc='A tiny web server that you can launch instantly in any directory.'
arch=(any)
url='http://stoneship.org/software/adsf/'
license=(MIT)
depends=(ruby-rack)
options=(!emptydirs)
source=("https://rubygems.org/downloads/${_pkgname}-${pkgver}.gem" LICENSE)
noextract=("${_pkgname}-${pkgver}.gem")
sha256sums=('a92d7310c3c27418ee5f6126df9cd20a95d3e4c277c1f7f24a4cc7102e720b62'
            'ec8e8027e23305f99d35f6f8ee9ad1a5343bd563588b0e2779a440abe148df45')

package() {
  cd "${srcdir}"

  # install gem
  HOME=/tmp gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}$(ruby -rubygems -e 'puts Gem.default_dir')" \
    --bindir "${pkgdir}/usr/bin" \
    "${_pkgname}-${pkgver}.gem"

  # install license
  install -Dm 644 \
    LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
