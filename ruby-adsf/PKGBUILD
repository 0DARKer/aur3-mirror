# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: carlf <carlf at photocarl dot org>
# Contributor: oslik <oslik at mail dot ru>

pkgname=ruby-adsf
_gemname="${pkgname#ruby-}"
pkgver=1.2.0
pkgrel=1
pkgdesc="A tiny web server that you can launch instantly in any directory."
url="http://stoneship.org/software/adsf/"
license=("MIT")
arch=("any")
depends=("ruby-rack")
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem", 'LICENSE')
noextract=("${_gemname}-${pkgver}.gem")
md5sums=('fddcdc6e55951b0f327d0e32adbc46df'
         '835cfe9539760796ef14e84d7dd55c21')
sha1sums=('bd871217c767402cbdf127d7059d76f766ba3317'
          '7dae1aac6bae6ebb430363f6f49c4bdb941fc97e')

package() {
  cd "${srcdir}"
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  HOME=/tmp gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"
  install -Dm 644 \
    "LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
