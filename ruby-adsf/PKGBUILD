# Maintainer: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: carlf <carlf at photocarl dot org>
# Contributor: oslik <oslik at mail dot ru>

pkgname=ruby-adsf
_gemname="${pkgname#ruby-}"
pkgver=1.1.1
pkgrel=1
pkgdesc="A tiny web server that you can launch instantly in any directory."
url="http://stoneship.org/software/adsf/"
license=("MIT")
arch=("any")
depends=("ruby" "ruby-rack")
source=("http://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
md5sums=('dbfe8b279fe9b66f2326dd1665f5025f')
sha1sums=('54744e36ef7aa96715ccb220e04ec742ac05a4bf')

package() {
  cd "${srcdir}"
  local _gemdir="$(ruby -rubygems -e 'puts Gem.default_dir')"
  HOME=/tmp gem install \
    --no-user-install \
    --ignore-dependencies \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"
}

# vim:set ts=2 sw=2 et:
