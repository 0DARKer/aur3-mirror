# Maintainer: Moritz Lipp <mlq@pwmt.org>
pkgname=asix-ax88179-dkms
pkgver=1.8.0
pkgrel=1

_dirname=AX88179_178A_LINUX_DRIVER_v${pkgver}_SOURCE
_modname=ax88179_178a

pkgdesc="Driver for USB ASIX Ethernet model AX88179"
arch=('i686' 'x86_64')
url="http://www.asix.com.tw/products.php?op=pItemdetail&PItemID=131;71;112&PLine=71"
license=('GPL')
depends=('dkms' 'linux-headers')
install=${_modname}-dkms.install
source=("http://www.asix.com.tw/FrootAttach/driver/AX88179_178A_LINUX_DRIVER_v${pkgver}_SOURCE.tar.bz2"
    "dkms.conf"
    "blacklist-${_modname}.conf"
    "${_modname}-dkms.conf"
    "${_modname}-dkms.install"
)
md5sums=('6b882a300315e757fcfe6c24b8ba0242'
         '7108f5ea2fa8e0ae9506848404b15873'
         'd6005a9e529a254e622b52c17134013f'
         '8a22aef60e449f7952d668cabc68e216'
         '428714f9c5e7fe2d8c2fc83b490bfb1b')

package() {
  # Install files
	installDir="$pkgdir/usr/src/$_modname-dkms-$pkgver"
	
	install -dm755 "$installDir"
	install -m644 "$srcdir/dkms.conf" "$installDir"
	install -dm755 "$pkgdir/etc/modprobe.d"
	install -dm755 "$pkgdir/etc/modules-load.d"
	install -m644 "$srcdir/$_modname-dkms.conf" \
    "$pkgdir/etc/modules-load.d/$_modname-dkms.conf"
	install -m644 "$srcdir/blacklist-$_modname.conf" \
    "$pkgdir/etc/modprobe.d/blacklist-$_modname.conf"

  # Install module source
	cd "$srcdir/$_dirname"
 	
 	for d in `find . -type d`; do
		install -dm755 "$installDir/$d"
	done
	
	for f in `find . -type f`; do
	  install -m644  "$srcdir/$_dirname/$f" "$installDir/$f"
	done
}
