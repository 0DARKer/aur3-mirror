# Contributor: Antonio Rojas

pkgname=libkscreen2
pkgver=1.71
plasmaver=4.96.0
pkgrel=1
pkgdesc='KDE screen management software'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libkscreen'
license=('LGPL')
depends=('qt5-x11extras')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/unstable/plasma/$plasmaver/src/$pkgname-$pkgver.tar.xz")
md5sums=('4af1bd12a56c4f7993047cc331cadc53')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
