pkgname=latexdraw
pkgver=2.0.8
pkgrel=1
pkgdesc='Create svg graphics and/or pstricks code usable in latex documents.'
arch=('any')
url="http://latexdraw.sourceforge.net"
license=('GPL')
makedepends=('java-environment>=6')
depends=('java-runtime>=6')
optdepends=('texlive-pstricks: use generated code in latex documents')
install="${pkgname}.install"
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/LaTeXDraw${pkgver}_src.zip
        http://downloads.sourceforge.net/project/${pkgname}/releaseNote/release_note${pkgver}.txt
        ${pkgname}.sh
        ${pkgname}.desktop
        ${pkgname}.MANIFEST.MF)
md5sums=('7d4c51c34a522e51d25fe60dd994df6e' '76085542ee39f327f80e1fa8965cec2b'\
         '8f7e3de38675797b824c5a8878e4c694' 'd3c711ba1a872bc6fb5fb1069f7dbf3f'\
         '323daf0cb6eab9f4c4964967d2e1644d')

build() {
    cd ${srcdir}/LaTeXDraw${pkgver}_src
    
    cp -r 'src lib/net' .
    cp -r 'src lib/org' .
    
    # the compilescript is broken so do it manually
    javac \
        org/sourceforge/jlibeps/epsgraphics/*.java \
        net/sourceforge/jiu/codecs/*.java \
        net/sourceforge/jiu/codecs/jpeg/*.java \
        net/sourceforge/jiu/codecs/tiff/*.java \
        net/sourceforge/jiu/color/*.java \
        net/sourceforge/jiu/color/conversion/*.java \
        net/sourceforge/jiu/data/*.java \
        net/sourceforge/jiu/gui/awt/*.java \
        net/sourceforge/jiu/ops/*.java \
        net/sourceforge/jiu/util/*.java || return 1
    javac \
        latexDraw/figures/*.java \
        latexDraw/figures/properties/*.java \
        latexDraw/filters/*.java \
        latexDraw/lang/*.java \
        latexDraw/parsers/*.java \
        latexDraw/parsers/pstricks/*.java \
        latexDraw/psTricks/*.java \
        latexDraw/ui/*.java \
        latexDraw/ui/components/*.java \
        latexDraw/ui/dialog/*.java \
        latexDraw/ui/listeners/*.java \
        latexDraw/util/*.java  || return 1
        
    # create the exectuable jar file
    install -d ${pkgdir}/usr/share/java/${pkgname}
    
    jar cvmf ${srcdir}/${pkgname}.MANIFEST.MF \
      ${pkgdir}/usr/share/java/${pkgname}/LaTeXDraw.jar \
      latexDraw/ res/ lang/ || return 1
    
    cp -r help lib lang templates ${pkgdir}/usr/share/java/${pkgname} || return 1
    
    # NB readme stipulates release_note.txt license.txt must be installed
    # in same folder as executable jarfile
    install -m644 -t ${pkgdir}/usr/share/java/${pkgname} \
            release_note.txt license.txt || return 1
    
    # install sh wrapper script
    install -Dm755 ${srcdir}/${pkgname}.sh \
      ${pkgdir}/usr/bin/${pkgname} || return 1
    # install menu entry & icons
    install -Dm644 ${srcdir}/${pkgname}.desktop \
      ${pkgdir}/usr/share/applications/${pkgname}.desktop || return 1
    install -Dm644 res/LaTeXDrawIcon.png \
      ${pkgdir}/usr/share/pixmaps/${pkgname}.png || return 1

}
