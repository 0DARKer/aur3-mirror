# Contributor: Francois Boulogne fboulogne at april dot org

pkgname=glances
pkgver=1.3.7
pkgrel=2
pkgdesc="CLI curses-based monitoring tool"
arch=('any')
url="https://github.com/nicolargo/glances"
license=('LGPL')
depends=('pystatgrab')
source=(https://github.com/downloads/nicolargo/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=("d3ba87e0b336b909b7f305b9431ea0f2")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i \
    -e 's/env python/&2/' \
    -e 's/glances.py/glances/' src/glances.py

  # doc path fix
  sed -i 's/${prefix}/${datadir}/' Makefile.am

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/usr/bin/glances{.py,}
}

# vim:ts=2:sw=2:et:
