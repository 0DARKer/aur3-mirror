# Maintainer: revel <revelΘmuub·net>
# Contributor: Stephen Caraher <moskvax@gmail.com>
# Contributor: Uli Armbruster <uli_armbrusterΘweb·de>
# Contributor: Robert Gregor <gimpΘpop·de>

pkgname=libvorbis-aotuv
pkgver=b6.03
pkgrel=3
_releasedate=20110424
pkgdesc="A fork of libvorbis intended to provide better quality sound at low to medium bitrates."
url="http://www.geocities.jp/aoyoume/aotuv"
license=("custom")
depends=("libogg>=1.0")
makedepends=("pkgconfig")
conflicts=(libvorbis)
provides=(libvorbis=1.3.2)
#install=("aotuv.install")
source=("http://www.geocities.jp/aoyoume/aotuv/source_code/${pkgname}_${pkgver}.tar.bz2"
	"libvorbis-1.3.2_1.3.4.diff")
arch=('i686' 'x86_64')
#options=(!libtool)

prepare() {
	cd $srcdir/aotuv-${pkgver}_${_releasedate}
	patch -Np1 -i ../libvorbis-1.3.2_1.3.4.diff
	chmod +x configure
}
build() {
	cd $srcdir/aotuv-${pkgver}_${_releasedate}
	./configure --prefix=/usr --disable-oggtest
	make
}
package() {
	cd $srcdir/aotuv-${pkgver}_${_releasedate}
	make DESTDIR=$pkgdir install
	install -v -m 644 -D $srcdir/aotuv-${pkgver}_${_releasedate}/COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

md5sums=('586d2ac0fa13f32cba78be5db4a16330'
         '087a38076a6dd861362ae8d99a0661e5')
sha256sums=('95455420f07e4b3abdf32bda9f5921e9ed3f1afdc3739098dc090150a42fd7fd'
            '10075232433076e5d7cb8db65ec07b35ae446f8f7adeaa6dbc59001dd3408ccc')
