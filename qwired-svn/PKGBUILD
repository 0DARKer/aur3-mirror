pkgname=qwired-svn
pkgver=470
pkgrel=5
pkgdesc="Multi-platform client for the Wired protocol. It is written in C++ and using the Qt4 framework, providing a very good multi-platform experience for the user."
arch=(i686 x86_64)
license=("GPL v2")
depends=(lua qt4 qtwebkit)
makedepends=(subversion)
url="http://qwired-suite.blogspot.com"
source=("qwired.desktop")
md5sums=('cf4d69f4d9701ef6d9c3878d9ba0b65e')

_svntrunk="http://qwired-suite.googlecode.com/svn/trunk/"
_svnmod="qwired"

_optimal_make_jobs() {
  if [ -r /proc/cpuinfo ]; then
    local core_count=$(grep -Fc processor /proc/cpuinfo)
  else
    local core_count=0
  fi
  if [ $core_count -gt 1 ]; then
    echo -n $[$core_count-1]
  else
    echo -n 1
  fi
}

build() {
  cd "${srcdir}"

  if [ -d "${_svnmod}/.svn" ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver $_svnmod
  fi

  msg 'SVN checkout done or server timeout'

  rm -rf "${_svnmod}-build"
  cp -r "$_svnmod" "${_svnmod}-build"
  cd "${_svnmod}-build/qw_library"
  
  qmake-qt4 qw_library.pro
  make -j$(_optimal_make_jobs)
  cd "../qw_client"
  qmake-qt4 qw_client.pro
  make -j$(_optimal_make_jobs)
}

package() {
  cd "${srcdir}/${_svnmod}-build/"
  install -Dm755 "bin/qwired" "$pkgdir/usr/bin/qwired"
  install -Dm644 "contrib/QwiredLogo.svg" "$pkgdir/usr/share/icons/$pkgname.svg"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 "$srcdir/qwired.desktop" "$pkgdir/usr/share/applications/qwired.desktop"
}
