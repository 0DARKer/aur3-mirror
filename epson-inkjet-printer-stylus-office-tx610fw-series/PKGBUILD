# Contributor: Andre Klitzing <andre () incubo () de>

pkgname=epson-inkjet-printer-stylus-office-tx610fw-series
_pkgname_filter=epson-inkjet-printer-filter
pkgver=1.0.0
pkgrel=6
pkgdesc="Epson printer driver (Stylus Office BX610FW, TX610FW / Stylus SX610FW / WorkForce 610, 615)"
arch=('i686' 'x86_64')
url="http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX"
license=('LGPL' 'custom:Epson Licence Agreement')
depends=('cups')
makedepends=('rpmextract' 'libtool' 'make' 'automake' 'autoconf')
source=(http://download.ebz.epson.net/dsc/op/stable/SRPMS/${pkgname}-${pkgver}-1lsb3.2.src.rpm)

build() {
  cd "$srcdir"
  rpmextract.sh $pkgname-$pkgver-1lsb3.2.src.rpm || return 1
  tar xzf $pkgname-$pkgver.tar.gz || return 1
  FILTER_FILE=`ls $_pkgname_filter*.tar.gz`
  tar xzf $FILTER_FILE || return 1

  cd "${FILTER_FILE%.tar.gz}"
  aclocal
  libtoolize
  chmod +x configure
  # if you have runtime problems: add "--enable-debug" and look into /tmp/epson-inkjet-printer-filter.txt
  ./configure LDFLAGS="$LDFLAGS -Wl,--no-as-needed" --prefix=/opt/$pkgname || return 1
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "$pkgdir/opt/$pkgname/"
  if [ "$CARCH" = "x86_64" ]; then
     cp -r lib64 "$pkgdir/opt/$pkgname/"
  else
     cp -r lib "$pkgdir/opt/$pkgname/"
  fi
  cp -r resource "$pkgdir/opt/$pkgname/"

  if [ -e "watermark" ]; then
      cp -r watermark "$pkgdir/opt/$pkgname/"
  fi
  install -d "$pkgdir/usr/share/cups/model/$pkgname"
  install -m 644 ppds/* "$pkgdir/usr/share/cups/model/$pkgname"

  cd "$srcdir"
  FILTER_FILE=`ls $_pkgname_filter*.tar.gz`
  cd "${FILTER_FILE%.tar.gz}"
  install -d "$pkgdir/opt/$pkgname/cups/lib/filter/"
  install -m 755 src/epson_inkjet_printer_filter "$pkgdir/opt/$pkgname/cups/lib/filter/epson_inkjet_printer_filter"
}
sha256sums=('f85b07603aba73b183f814d782451951226132a9db5cd0b5377c3f2cf1a5956c')
