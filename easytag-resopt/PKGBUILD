# $Id: PKGBUILD 71340 2010-03-06 18:44:25Z andrea $
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=easytag-resopt
_realname=easytag
pkgver=2.1.6
pkgrel=1
pkgdesc="Utility for viewing, editing and writing ID3 tags of your MP3 files, with resolution optimization patch applied for use with small screens."
arch=('i686' 'x86_64')
license=('GPL')
url="http://easytag.sourceforge.net/"
depends=('id3lib' 'libid3tag' 'gtk2' 'libvorbis' 'flac' 'libmp4v2' 'speex' 'wavpack')
provides=('easytag')
conflicts=('easytag')
install=${_realname}.install
source=("http://downloads.sourceforge.net/${_realname}/${_realname}-${pkgver}.tar.bz2"
        'mp4v2-1.9.patch'
        'resolution_optimize.02.patch')
md5sums=('6c5b9dc2bf1f3b0a11bd4efc81aaa9ee'
         '912d9baa362c4d9e5aee0436b4ccb5a2'
         '3fe96025f854639a0d9ea02b9c735abc')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  patch -Np1 -i ${srcdir}/mp4v2-1.9.patch
  patch -Np0 -i ${srcdir}/resolution_optimize.02.patch
  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Remove directory mimetype
  sed '/^MimeType=/ s@;x-directory/normal;@;@' \
    -i ${pkgdir}/usr/share/applications/easytag.desktop
}
