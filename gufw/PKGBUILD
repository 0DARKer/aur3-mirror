# $Id: PKGBUILD 66507 2012-02-25 19:38:41Z jlichtblau $
# Maintainer: 
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>

pkgname=gufw
pkgver=12.04.1
pkgrel=1
pkgdesc="An easy, intuitive way to manage your Linux firewall"
arch=('any')
url="http://gufw.tuxfamily.org/index.html"
license=('GPL3')
depends=('dbus' 'dbus-python' 'gksu' 'gtk3' 'hicolor-icon-theme' 'python2-gobject' 'polkit' 'ufw')
makedepends=('patch' 'python-distutils-extra')
optdepends=('gnome-python')
source=(https://launchpad.net/ubuntu/+archive/primary/+files/gui-ufw_${pkgver}.orig.tar.gz
        $pkgname-$pkgver-execute.patch)
install=$pkgname.install
sha256sums=('48238b999d695732f5696873f6c9957e17a3b3fd68ea5d44adb906912b4e653a'
            '5bf4a6678182244f028d0bf602d59389452a7a7464a02be638d2426da6ca83d6')

package() {
  cd ${srcdir}/gui-ufw-${pkgver}

  # python2 fix
  for file in $(find . -name '*.py' -print); do
      sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
      sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py build install --root=${pkgdir}

  cd ${pkgdir}
  patch -Np0 -i ${srcdir}/$pkgname-$pkgver-execute.patch
}
