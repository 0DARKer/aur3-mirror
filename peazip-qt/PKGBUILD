# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: TuxSpirit <tuxspirit@archlinux.fr>

pkgname=peazip-qt
pkgver=5.4.1
pkgrel=1
pkgdesc="QT archiver utility"
arch=(i686 x86_64)
url=http://peazip.sourceforge.net/peazip-linux.html
license=(Artistic2.0 LGPL2.1)
depends=(balz desktop-file-utils lib32-{curl,gmp4,libx11,ncurses,qt4pas} p7zip unace upx zpaq)
[[ $CARCH == "i686" ]] && depends=(${depends[@]/lib32-/})
optdepends=(quad)
provides=(${pkgname%-*})
conflicts=(${pkgname%-*}-gtk2)
install=${pkgname%-*}.install
source=($pkgname-$pkgver.tgz::http://downloads.sourceforge.net/project/${pkgname%-*}/$pkgver/${pkgname%-*}-$pkgver.LINUX.Qt.tgz
    ${pkgname%-*}.desktop)
noextract=($pkgname-$pkgver.tgz)
sha256sums=('5b6b38ca86966225d6bdf2c381d5ba1c0408bfde27bbf96c0b0f7b2cd50327a0'
            'c3ca0fa3e89626447acc376e2392fdd4e452c2b8f0685324eb978ab63c217fce')
sha512sums=('0aab653f538f550cea0a6defe00d603f360c202ef9387ea351a9c898a28b1f5de9a04c8db4f9ca7c1dc5ed696481445bedd19b319d36b538974c51dd60b962b0'
            '0fb2e432f3b403df55134ebd57aacd2fb1eea75ef96520a5e4d7827ec319ca16a45c6e147504af0472704872fc601273b38d8f45ebfb8f68328266d240a1f71f')

package() {
    _resdir="$pkgdir"/usr/lib/${pkgname%-*}/res/

    install -d $pkgname/
    cd $pkgname/
    bsdtar -xf ../$pkgname-$pkgver.tgz
    cd usr/local/share/PeaZip/res/

    install -Dm755 ../${pkgname%-*} "$pkgdir"/usr/lib/${pkgname%-*}/${pkgname%-*}

    for i in pea pealauncher rnd; do
        install -Dm755 $i "$_resdir"/$i
    done
    for i in arc/{arc,*.sfx}; do
        install -Dm755 $i "$_resdir"/$i
    done
    for i in altconf.txt lang/* themes/{{nographic,seven}-embedded/*,*.7z} arc/arc.{ini,groups}; do
        install -Dm644 $i "$_resdir"/$i
    done

    install -d "$_resdir"/7z/Codecs/
    for i in 7z{,a,r,.so,Con.sfx,.sfx} Codecs/Rar29.so; do
        ln -sf /usr/lib/p7zip/$i "$_resdir"/7z/$i
    done
    for i in quad/{balz,quad} unace/unace upx/upx lpaq/lpaq8 paq/paq8o zpaq/zpaq; do
        install -d "$_resdir"/$(dirname $i)/
        ln -sf /usr/bin/$(basename $i) "$_resdir"/$i
    done
    install -d "$pkgdir"/usr/bin/
    for i in /usr/lib/${pkgname%-*}/{${pkgname%-*},res/pea,res/pealauncher}; do
        ln -sf $i "$pkgdir"/usr/bin/$(basename $i)
    done

    install -Dm644 "$srcdir"/$pkgname/usr/local/share/icons/${pkgname%-*}.png "$pkgdir"/usr/share/pixmaps/${pkgname%-*}.png
    desktop-file-install "$srcdir"/${pkgname%-*}.desktop --dir "$pkgdir"/usr/share/applications/
}
