# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: Matt Parnell /ilikenwf <parwok@gmail.com>
# Based on [extra]'s recordproto

_pkgname=recordproto
pkgname=$_pkgname-git
pkgver=1.14.2.2.g0fd4f8e
pkgrel=1
pkgdesc="X11 Record extension wire protocol - Git version"
arch=('any')
url="http://cgit.freedesktop.org/xorg/proto/$_pkgname/"
license=('custom')
makedepends=('git' 'xorg-util-macros')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("git://anongit.freedesktop.org/xorg/proto/$_pkgname")
md5sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --always | cut -d "-" -f2- | tr - .
}

build() {
  cd $_pkgname
  msg2 "Starting autogen.sh..."
  ./autogen.sh --prefix=/usr
  msg2 "Starting make..."
  make
}

package(){
  cd $_pkgname
  msg2 "Starting make install..."
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_pkgname/COPYING"
}