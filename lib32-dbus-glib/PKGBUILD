# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=dbus-glib
pkgname=lib32-$_pkgbasename
pkgver=0.92
pkgrel=3
pkgdesc="GLib bindings for DBUS (32 bit)"
arch=('x86_64')
license=('GPL' 'custom:AFL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
depends=('lib32-glib2' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://dbus.freedesktop.org/releases/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('b595b36890c4f9f8f5d5dec131c495f8')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
   
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--libdir=/usr/lib32 --enable-static=no --enable-bash-completion=no \
	CC='gcc -m32' CXX='g++ -m32'
  make
}
package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include,share}
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
