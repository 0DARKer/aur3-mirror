# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbasename=dbus-glib
pkgname=lib32-$_pkgbasename
pkgver=0.94
pkgrel=1
pkgdesc="GLib bindings for DBUS (32 bit)"
arch=('x86_64')
license=('GPL' 'custom:AFL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
depends=('lib32-dbus-core' 'lib32-glib2' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://dbus.freedesktop.org/releases/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz
'fix_network_manager.patch')
md5sums=('e1f1506a6f4941e67bffd614b1ad5af6'
	 '2a3598342dd25722d452e7beaf21b5f3')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  patch -Np1 -i "${srcdir}/fix_network_manager.patch" 
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--enable-static=no --enable-bash-completion=no \
	--libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include,share}
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
