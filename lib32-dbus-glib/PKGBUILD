# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

_pkgbase=dbus-glib
pkgname=lib32-$_pkgbase
pkgver=0.98
pkgrel=1
pkgdesc="GLib bindings for DBUS (32 bit)"
arch=('x86_64')
license=('GPL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
depends=('lib32-dbus-core' 'lib32-glib2' "$_pkgbase")
makedepends=('gcc-multilib')
options=('!libtool' '!emptydirs')
source=(http://dbus.freedesktop.org/releases/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz)
md5sums=('3f7f117dc7c8d899d9acfdcbf9253fb8')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--enable-static=no --enable-bash-completion=no \
	--libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"  
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{bin,include,share}
}
