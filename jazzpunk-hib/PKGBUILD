# Maintainer:  ainola <opp310@alh.rqh> (ROT13)

pkgname=jazzpunk-hib
pkgver=July6_2014
pkgrel=1
pkgdesc="Jazzpunk is an adventure comedy game and poorly made word processor (Humble Bundle Version)."
arch=('i686' 'x86_64')
url="http://jazzpunk.net/"
license=('custom: commercial')
depends=('glu' 'libxext' 'libxcursor')
source=("hib://Jazzpunk-${pkgver/_/-}-Linux.zip"
        "${pkgname}.png"
        "${pkgname}.desktop")
sha256sums=("9e6f29d87295292cf60a0173b09ccc5a5c429ec084fe2f0c7ab7732851fdf3b7"
            "c2ddb1cff681814ab56aa88cf5b64664c277fd3a9b452573c3401a08c019ffe1"
            "8f42bfbb22dfaded91a47416edae581d4b7ff3d17b77262243248ad2dd6220fc")
# Prevent compressing final package
PKGEXT='.pkg.tar'

# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download the file manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf"; echo "Read this PKGBUILD for more info."; exit 1')

package() {
    # Launcher and Data
    install -Dm755 "${srcdir}/Jazzpunk.${CARCH}" "${pkgdir}/opt/${pkgname}/Jazzpunk.${CARCH}"
    mv "${srcdir}/Jazzpunk_Data" "${pkgdir}/opt/${pkgname}/"
    find "${pkgdir}/opt/${pkgname}/Jazzpunk_Data" -type d -exec chmod 755 {} \;
    find "${pkgdir}/opt/${pkgname}/Jazzpunk_Data" -type f -exec chmod 644 {} \;

    # Install Binaries/Launchers
    mkdir -p "$pkgdir/usr/bin"
    ln -s "/opt/${pkgname}/Jazzpunk.${CARCH}"  "${pkgdir}/usr/bin/${pkgname}"

    # Desktop Integration
    install -Dm644 "${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
