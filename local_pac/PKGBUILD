# Maintainer: Max Meyer <dev@fedux.org>
pkgname=local_pac
pkgver=0.0.5
pkgrel=2
pkgdesc="local pacfile serving server"
arch=(i686 x86_64)
url="https://github.com/dg-vrnetze/${pkgname}"
license=('MIT')
depends=(ruby)
makedepends=(rubygems filegen)
source=(http://gems.rubyforge.org/gems/$pkgname-$pkgver.gem)
noextract=($pkgname-$pkgver.gem)
sha256sums=('aa2edfd0ae50bced4b9f53ad60262a82dd2d016ceba2895e6f5bdbe1e9aea0c1')

package() {
  cd "$srcdir"

  _library_dir=/usr/lib/${pkgname}
  _share_dir="${_library_dir}/gems/${pkgname}-${pkgver}/share"
  _systemd_dir=/usr/lib/systemd/system
  _bin_dir=/usr/bin

  install -d ${pkgdir}/$_library_dir 
  install -d ${pkgdir}/$_bin_dir 
  install -d ${pkgdir}/$_systemd_dir 

  gem install --env-shebang --wrappers --no-ri --no-rdoc --install-dir ${pkgdir}/${_library_dir} $pkgname puma

  SOFTWARE_BINARY=$_library_dir/gems/${pkgname}-${pkgver}/bin/${pkgname} SOFTWARE_LIB=/usr/lib/local_pac filegen  ${pkgdir}${_share_dir}/archlinux/startup.erb > ${pkgdir}${_bin_dir}/${pkgname}

  chmod a+x ${pkgdir}/${_bin_dir}/${pkgname}
  rm -r ${pkgdir}/${_library_dir}/cache
  rm -r ${pkgdir}/${_library_dir}/{build_info,doc}

  install -D -m644 ${pkgdir}/$_share_dir/systemd/${pkgname}.service ${pkgdir}/$_systemd_dir
  install -D -m644 ${pkgdir}/$_share_dir/systemd/${pkgname}@.service ${pkgdir}/$_systemd_dir
  install -D -m644 ${pkgdir}/$_share_dir/systemd/${pkgname}.socket ${pkgdir}/$_systemd_dir

  install -D -m644 ${pkgdir}${_share_dir}/../LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
