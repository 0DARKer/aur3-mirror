pkgname=vera
pkgver=1.21
pkgrel=1
pkgdesc="A list of computer related acronyms (Info format)"
arch=(any)
url="http://home.snafu.de/ohei/vera/vueber-e.html"
license=('FDL')
install=vera.install
depends=('texinfo')
source=(http://ftp.gnu.org/gnu/vera/vera-$pkgver.tar.gz{,.sig}
        vera.patch)
md5sums=('b3ac74d4f5336512996142fc00e3e144'
         'SKIP'
         'cc9099babf1fc8332a8eff20c7a2f110')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../vera.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  makeinfo --no-split --force vera.texi
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D -m 644 vera.info "$pkgdir"/usr/share/info/vera.info
}
