pkgname=vera
pkgver=1.20
pkgrel=1
pkgdesc="V.E.R.A. --- Virtual Entity of Relevant Acronyms (GNU Texinfo format)"
arch=(any)
url="http://home.snafu.de/ohei/vera/vueber-e.html"
license=('FDL')
install=vera.install
depends=('texinfo')
source=(http://ftp.gnu.org/gnu/vera/vera-1.20.tar.gz{,.sig}
        vera.patch)
md5sums=('6b31d08020fb28af147f98a1bee69805'
         'SKIP'
         '4269c1fef6dd306db18dab284598b027')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../vera.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  makeinfo --no-split --force vera.texi
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D -m 644 vera.info "$pkgdir"/usr/share/info/vera.info
}
