# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>

pkgname=vit
pkgver=1.2
pkgrel=1
pkgdesc="A terminal interface for Taskwarrior with Vim key bindings and colorization support"
arch=('any')
url="http://taskwarrior.org/projects/taskwarrior/wiki/Vit"
license=('GPL3')
depends=('perl-curses' 'task')
source=("http://taskwarrior.org/download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('a78dee573130c8d6bc92cf60fafac0abc78dd2109acfba587cb0ae202ea5bbd0')

prepare() {
  cd ${pkgname}-${pkgver}

  # use /usr/share/vit instead of /etc/vit
  sed -i "s|/etc|/share/vit|" Makefile.in vit.pl

  # use /usr/share/man instead of /usr/man
  sed -i "s|/man/|/share/man/|" Makefile.in vit.pl

  # use DESTDIR and do NOT use sudo for install
  sed -i "s|sudo ||;s| \(@prefix\)| \$(DESTDIR)/\1|" Makefile.in
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
