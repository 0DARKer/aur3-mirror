# Maintainer: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>

pkgname=vit
pkgver=1.1
pkgrel=2
pkgdesc="A full-screen terminal interface for Taskwarrior with Vi key bindings and complete support for native Taskwarrior colorization."
arch=('any')
url="http://taskwarrior.org/projects/taskwarrior/wiki/Vit"
license=('GPL3')
depends=('perl-curses' 'task')
source=("http://taskwarrior.org/attachments/606/$pkgname-$pkgver.tar.gz")
md5sums=('9bee5b22324d83a003c63b630ca2e912')

prepare(){
  cd "$srcdir/$pkgname-$pkgver"

  # use /usr/share/vit instead of /etc/vit
  sed -i "s|/etc|/share/vit|" Makefile.in vit.pl

  # use DESTDIR and do NOT use sudo for install
  sed -i "s|sudo ||;s| \(@prefix\)| \$(DESTDIR)/\1|" Makefile.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
