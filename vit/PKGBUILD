# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>

pkgname=vit
pkgver=1.2beta1
_pkgver=1.2.beta1
pkgrel=1
pkgdesc="A terminal interface for Taskwarrior with Vim key bindings and colorization support"
arch=('any')
url="http://taskwarrior.org/projects/taskwarrior/wiki/Vit"
license=('GPL3')
depends=('perl-curses' 'task')
source=("http://taskwarrior.org/download/${pkgname}-${_pkgver}.tar.gz")
sha256sums=('2946bb647b9603d1bb582adb0f72481321bc2c3635f84894a7573f57a867bec5')

prepare() {
  cd ${pkgname}-${_pkgver}

  # use /usr/share/vit instead of /etc/vit
  sed -i "s|/etc|/share/vit|" Makefile.in vit.pl
  
  # use /usr/share/man instead of /usr/man
  sed -i "s|/man/|/share/man/|" Makefile.in vit.pl

  # use DESTDIR and do NOT use sudo for install
  sed -i "s|sudo ||;s| \(@prefix\)| \$(DESTDIR)/\1|" Makefile.in
}

build() {
  cd ${pkgname}-${_pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${_pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
