# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Aaron Griffin (aaron <at> archlinux <dot> org)
# Contributor: judd (jvinet <at> zeroflux <dot> org)

pkgname=selinux-openssh
_origname=openssh
pkgver=5.6p1
pkgrel=1
pkgdesc="A Secure SHell server/client with SELinux support"
arch=('i686' 'x86_64')
url="http://www.openssh.org/portable.html"
license=('custom')
groups=('selinux' 'selinux-system-utilities')
depends=('openssl' 'zlib' 'selinux-pam' 'tcp_wrappers' 'heimdal' 'selinux-usr-libselinux')
provides=('openssh=5.6p1')
conflicts=('openssh')
backup=(etc/ssh/ssh_config
	etc/ssh/sshd_config
	etc/pam.d/sshd)
source=(ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${_origname}-${pkgver}.tar.gz
        sshd sshd.confd sshd.pam)
md5sums=('e6ee52e47c768bf0ec42a232b5d18fb0'
         '17b1b1bf0f578a55945ee204bd4462af'
         'e2cea70ac13af7e63d40eb04415eacd5'
         '31ff9888206c73d64ea917c63cba29af')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  #NOTE we disable-strip so that makepkg can decide whether to strip or not
  ./configure --prefix=/usr --libexecdir=/usr/lib/ssh \
    --sysconfdir=/etc/ssh --with-tcp-wrappers --with-privsep-user=nobody \
    --with-md5-passwords --with-pam --with-mantype=man --mandir=/usr/share/man \
    --with-xauth=/usr/bin/xauth --with-kerberos5=/usr --with-ssl-engine \
    --disable-strip --with-selinux
  make
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm755 "${srcdir}/sshd" "${pkgdir}/etc/rc.d/sshd"

  install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${_origname}/LICENCE"
  install -Dm644 "${srcdir}/sshd.pam" "${pkgdir}/etc/pam.d/sshd"
  install -Dm644 "${srcdir}/sshd.confd" "${pkgdir}/etc/conf.d/sshd"

  rm "${pkgdir}/usr/share/man/man1/slogin.1"
  ln -sf ssh.1.gz "${pkgdir}/usr/share/man/man1/slogin.1.gz"

  #additional contrib scripts that we like
  install -Dm755 contrib/findssl.sh "${pkgdir}/usr/bin/findssl.sh"
  install -Dm755 contrib/ssh-copy-id "${pkgdir}/usr/bin/ssh-copy-id"
  install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}/usr/share/man/man1/ssh-copy-id.1"

  # sshd_config
  sed -i \
    -e 's|^#ListenAddress 0.0.0.0|ListenAddress 0.0.0.0|g' \
    -e 's|^#UsePAM no|UsePAM yes|g' \
    -e 's|^#ChallengeResponseAuthentication yes|ChallengeResponseAuthentication no|g' \
    "${pkgdir}/etc/ssh/sshd_config"
  echo "HashKnownHosts yes" >> "${pkgdir}/etc/ssh/ssh_config"
  echo "StrictHostKeyChecking ask" >> "${pkgdir}/etc/ssh/ssh_config"

  #ssh_config
  sed -i \
    -e 's|^# Host \*|Host *|g' \
    "${pkgdir}/etc/ssh/ssh_config"
}
