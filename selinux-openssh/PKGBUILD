# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Aaron Griffin (aaron <at> archlinux <dot> org)
# Contributor: judd (jvinet <at> zeroflux <dot> org)

pkgname=selinux-openssh
_origname=openssh
pkgver=5.8p1
pkgrel=1
pkgdesc="Free version of the SSH connectivity tools with SELinux support"
arch=('i686' 'x86_64')
url="http://www.openssh.org/portable.html"
license=('custom:BSD')
groups=('selinux' 'selinux-system-utilities')
depends=('libedit' 'selinux-pam' 'tcp_wrappers' 'heimdal' 'selinux-usr-libselinux')
provides=('openssh=5.6p1')
conflicts=('openssh')
backup=(etc/ssh/ssh_config
	etc/ssh/sshd_config
	etc/pam.d/sshd
	etc/conf.d/sshd)
source=(ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${_origname}-${pkgver}.tar.gz
	sshd.confd 
	sshd.pam
        sshd
	${_origname}-${pkgver}-selinux.patch)
sha1sums=('adebb2faa9aba2a3a3c8b401b2b19677ab53f0de'
          'ec102deb69cad7d14f406289d2fc11fee6eddbdd'
          '800ded1a0ce5900de9024324c98590493ece6ae8'
          '6b7f8ebf0c1cc37137a7d9a53447ac8a0ee6a2b5'
	  '65f47ccd1546c43d413902a703b2a3d288d662a8')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"
  #Patch so that build --with-selinux does not crash
  patch -Np1 -i "${srcdir}/${_origname}-${pkgver}-selinux.patch"

  #NOTE we disable-strip so that makepkg can decide whether to strip or not
  ./configure --prefix=/usr --libexecdir=/usr/lib/ssh \
    --sysconfdir=/etc/ssh --with-tcp-wrappers --with-privsep-user=nobody \
    --with-md5-passwords --with-pam --with-mantype=man --mandir=/usr/share/man \
    --with-xauth=/usr/bin/xauth --with-kerberos5=/usr --with-ssl-engine \
    --with-libedit=/usr/lib --disable-strip --with-selinux
  make
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm755 "${srcdir}/sshd" "${pkgdir}/etc/rc.d/sshd"

  install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${_origname}/LICENCE"
  install -Dm644 "${srcdir}/sshd.pam" "${pkgdir}/etc/pam.d/sshd"
  install -Dm644 "${srcdir}/sshd.confd" "${pkgdir}/etc/conf.d/sshd"

  rm "${pkgdir}/usr/share/man/man1/slogin.1"
  ln -sf ssh.1.gz "${pkgdir}/usr/share/man/man1/slogin.1.gz"

  #additional contrib scripts that we like
  install -Dm755 contrib/findssl.sh "${pkgdir}/usr/bin/findssl.sh"
  install -Dm755 contrib/ssh-copy-id "${pkgdir}/usr/bin/ssh-copy-id"
  install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}/usr/share/man/man1/ssh-copy-id.1"

  # PAM is a common, standard feature to have
  sed -i  -e '/^#ChallengeResponseAuthentication yes$/c ChallengeResponseAuthentication no' \
          -e '/^#UsePAM no$/c UsePAM yes' \
          "${pkgdir}/etc/ssh/sshd_config"
}
