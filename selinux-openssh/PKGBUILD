# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Gaetan Bisson <bisson <at> archlinux <dot> org>
# Contributor: Aaron Griffin (aaron <at> archlinux <dot> org)
# Contributor: judd (jvinet <at> zeroflux <dot> org)

pkgname=selinux-openssh
_origname=openssh
pkgver=5.8p2
pkgrel=8
pkgdesc="Free version of the SSH connectivity tools with SELinux support"
arch=('i686' 'x86_64')
url="http://www.openssh.org/portable.html"
license=('custom:BSD')
groups=('selinux' 'selinux-system-utilities')
depends=('libedit' 'selinux-pam' 'tcp_wrappers' 'krb5' 'openssl' 'selinux-usr-libselinux')
provides=("${_origname}=${pkgver}-${pkgrel}")
conflicts=("${_origname}")
backup=(etc/ssh/ssh_config
        etc/ssh/sshd_config
        etc/pam.d/sshd
        etc/conf.d/sshd)
source=(ftp://ftp.openbsd.org/pub/OpenBSD/OpenSSH/portable/${_origname}-${pkgver}.tar.gz
        sshd.confd 
        sshd.pam
        sshd
        authfile.c.patch)
sha1sums=('64798328d310e4f06c9f01228107520adbc8b3e5'
          'ec102deb69cad7d14f406289d2fc11fee6eddbdd'
          '62a5589d9cc9a3f75ecb9f3edb0b3b19edbfc4ff'
          '6b7f8ebf0c1cc37137a7d9a53447ac8a0ee6a2b5'
          '3669cb5ca6149f69015df5ce8e60b82c540eb0a4')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  # fix FS#24693 using http://anoncvs.mindrot.org/index.cgi/openssh/authfile.c?revision=1.95
  patch -p1 -i ../authfile.c.patch

  #NOTE we disable-strip so that makepkg can decide whether to strip or not
  ./configure \
      --prefix=/usr \
      --libexecdir=/usr/lib/ssh \
      --sysconfdir=/etc/ssh \
      --with-tcp-wrappers \
      --with-privsep-user=nobody \
      --with-md5-passwords \
      --with-pam \
      --with-mantype=man \
      --mandir=/usr/share/man \
      --with-xauth=/usr/bin/xauth \
      --with-kerberos5=/usr \
      --with-ssl-engine \
      --with-libedit=/usr/lib \
      --disable-strip \
      --with-selinux

  make
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm755 "${srcdir}/sshd" "${pkgdir}/etc/rc.d/sshd"

  install -Dm644 LICENCE "${pkgdir}/usr/share/licenses/${_origname}/LICENCE"
  install -Dm644 "${srcdir}/sshd.pam" "${pkgdir}/etc/pam.d/sshd"
  install -Dm644 "${srcdir}/sshd.confd" "${pkgdir}/etc/conf.d/sshd"

  rm "${pkgdir}/usr/share/man/man1/slogin.1"
  ln -sf ssh.1.gz "${pkgdir}/usr/share/man/man1/slogin.1.gz"

  #additional contrib scripts that we like
  install -Dm755 contrib/findssl.sh "${pkgdir}/usr/bin/findssl.sh"
  install -Dm755 contrib/ssh-copy-id "${pkgdir}/usr/bin/ssh-copy-id"
  install -Dm644 contrib/ssh-copy-id.1 "${pkgdir}/usr/share/man/man1/ssh-copy-id.1"

  # PAM is a common, standard feature to have
  sed -i  -e '/^#ChallengeResponseAuthentication yes$/c ChallengeResponseAuthentication no' \
          -e '/^#UsePAM no$/c UsePAM yes' \
          "${pkgdir}/etc/ssh/sshd_config"
}
