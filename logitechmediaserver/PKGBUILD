# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=logitechmediaserver
pkgver=7.7.0
pkgrel=1
pkgdesc='Streaming audio server supported by Logitech'
url='http://www.mysqueezebox.com/download'
license=('GPL' 'custom')
depends=('perl')
arch=('i686' 'x86_64')
makedepends=('nasm' 'yasm')
depends=('perl>5.13' 'perl<5.15') # 'perl-dbi' too when building self cpan
source=("http://downloads.slimdevices.com/LogitechMediaServer_v${pkgver}/logitechmediaserver-${pkgver}.tgz"
        'perl-5.14.patch'
        'scanner.patch'
        'conf.d'
        'rc.d')
backup=('etc/conf.d/logitechmediaserver')
sha1sums=('d3d78ddcee3867a2fcf22cd4f79fd034e945e44e'
          '7b3f84a5af4ff7daf210b251e5239bb1f2863166'
          '241d93fdebbc13f7fe734f2165e87bd3fad7bc69'
          '80d3658796ba7241b497e5fce93813423c57e06e'
          'b21c6e2aabc41c2ce1e731b7366727d09d711285')

replaces=('squeezecenter' 'squeezeboxcenter' 'squeezebox-center')
install=install

build_cpan() {
	svnurl=svn.slimdevices.com/repos/slim/7.6/trunk/vendor/CPAN/
	wget -r --no-parent http://${svnurl} --reject 'mysql-*'
	pushd ${svnurl}
	wget http://search.cpan.org/CPAN/authors/id/F/FL/FLORA/Sub-Name-0.05.tar.gz
	sed \
		-e '/build DBI/d' \
		-e '/build_module DBI/d' \
		-e '/build DBD::mysql/d' \
		-e '/RUN_TESTS=1/c RUN_TESTS=0' \
		-e 's/Sub-Name-0.04/Sub-Name-0.05/g' \
		-e 's/perl5.12.3/perl/g' \
		-e 's/5.12.3/5.14.1/g' \
		-e 's/5.12/5.14/g' \
		-e 's/512/514/g' \
		-i buildme.sh
	chmod +x buildme.sh
	./buildme.sh
	popd
	sed \
		-e '/Sub::Name 0.04/c Sub::Name 0.05' \
		-e '/Audio::Scan 0.88/c Audio::Scan 0.9' \
		-i modules.conf
	cp -r ${svnurl}build/5.14/lib/perl5/*-linux-thread-multi/* CPAN/
	cp -r ${svnurl}build/arch CPAN/
	rm -fr svn.slimdevices.com
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"-*
	#build_cpan
	patch -p1 -i ../perl-5.14.patch
	patch -p1 -i ../scanner.patch
	rm -r CPAN/arch/5.{8,10,12}
	rm -r CPAN/arch/5.14/{arm-linux-gnueabi-thread-multi-64int,i386-linux-thread-multi-64int,powerpc-linux-thread-multi-64int}
	[[ ${CARCH} = i686   ]] && rm -r CPAN/arch/5.14/x86_64-linux-thread-multi || true
	[[ ${CARCH} = x86_64 ]] && rm -r CPAN/arch/5.14/i386-linux-thread-multi || true
	rm -r Bin/{arm-linux,darwin,i386-freebsd-64int,powerpc-linux}
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"-*
	install -d "${pkgdir}"/{opt,usr/share/licenses}/"${pkgname}"
	ln -s /opt/"${pkgname}"/License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	cp -a * "${pkgdir}/opt/${pkgname}/"
	install -D -m644 ../conf.d "${pkgdir}/etc/conf.d/${pkgname}"
	install -D -m755 ../rc.d "${pkgdir}/etc/rc.d/${pkgname}"
}
