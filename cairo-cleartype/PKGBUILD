# Maintainer: Estevao Valadao <estevao@archlinux-br.org>
# Contributor: Sven Goericke <sgoericke@member.fsf.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>
# Contributor: Nagy Gabor <ngaba@@bibl..u-szeged..hu>

pkgname=cairo-cleartype
pkgver=1.12.2
pkgrel=1
pkgdesc="Cairo vector graphics library with ClearType patch from Gentoo"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng>=1.4.0' 'libxrender' 'fontconfig' 'pixman' 'glib2' 'sh')
makedepends=('librsvg' 'poppler-glib' 'libspectre' 'gtk-doc')
optdepends=('xcb-util: for XCB backend') # really needed?
options=('!libtool')
conflicts=('cairo' 'cairo-xcb')
provides=("cairo=${pkgver}" "cairo-xcb")
source=(http://cairographics.org/releases/cairo-$pkgver.tar.xz
        cairo-1.10.0-buggy_gradients.patch
        cairo-1.12.2-cleartype.patch)
md5sums=('87649eb75789739d517c743e94879e51'
         '9b323790dab003e228c6955633cb888e'
         'd5d3e9ad3aae3dbeeb514154c39c8fef')

build() {
  cd "${srcdir}/cairo-${pkgver}"
  patch -Np1 -i "${srcdir}/cairo-1.10.0-buggy_gradients.patch"
  patch -Np1 -i "${srcdir}/cairo-1.12.2-cleartype.patch"
  autoreconf -vfi
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-tee \
	--disable-xlib-xcb
  make
}

package() {
  cd "$srcdir/cairo-$pkgver"
  make DESTDIR="$pkgdir" install
}
