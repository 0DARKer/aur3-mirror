# Maintainer: Daniel Kirchner <daniel@ekpyron.org>
# Based on mingw32-boost

pkgname=mingw-w64-boost
pkgver=1.49.0
_boostver=${pkgver//./_}
pkgrel=1
pkgdesc="Free peer-reviewed portable C++ source libraries (mingw-w64 version)"
arch=(any)
_arch_target=x86_64-w64-mingw32
url="http://www.boost.org/"
license=('custom')
depends=('mingw-w64-gcc')
# Depends on native Boost so we can use its bjam instead of building it ourself first
makedepends=('boost' 'bzip2')
options=(!strip)
source=(http://downloads.sourceforge.net/sourceforge/boost/boost_${_boostver}.tar.bz2)
md5sums=('0d202cb811f934282dea64856a175698')

build() {
	cd ${srcdir}/boost_${_boostver}

	cat > user-config.jam << EOF
using gcc : 4.6 : ${_arch_target}-g++
	:
	<rc>${_arch_target}-windres
	<archiver>${_arch_target}-ar
;
EOF

	/usr/bin/bjam \
		toolset=gcc \
		target-os=windows \
		variant=release \
		threading=multi \
		threadapi=win32 \
		link=shared,static \
		runtime-link=shared \
		--prefix=${pkgdir}/usr/${_arch_target} \
		--user-config=user-config.jam \
		--without-mpi \
		--without-python \
		-sNO_BZIP2=1 \
		-sNO_ZLIB=1 \
		${MAKEFLAGS} \
		--layout=tagged install

	# --layout=tagged will add the "-mt" suffix for multithreaded libraries
	# and installs includes in /usr/include/boost.
	# --layout=system no longer adds the -mt suffix for multi-threaded libs.

#  find ${pkgdir}/usr/x86_64-w64-mingw32 -name *.a | xargs x86_64-w64-mingw32-ranlib
	${_arch_target}-strip -x $pkgdir/usr/${_arch_target}/lib/*.dll
	${_arch_target}-strip -g $pkgdir/usr/${_arch_target}/lib/*.a
}
