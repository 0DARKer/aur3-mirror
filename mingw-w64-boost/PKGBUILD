# Maintainer: Daniel Kirchner <daniel@ekpyron.org>
# Based on mingw32-boost

pkgname=mingw-w64-boost
pkgver=1.57.0
_boostver=${pkgver//./_}
pkgrel=2
pkgdesc="Free peer-reviewed portable C++ source libraries (mingw-w64)"
arch=('any')
url="http://www.boost.org/"
license=('custom')
depends=('mingw-w64-crt' 'mingw-w64-zlib' 'mingw-w64-bzip2')
# Depends on native Boost so we can use its bjam instead of building it ourself first
makedepends=('mingw-w64-gcc' 'boost' 'bzip2')
options=(!strip !buildflags staticlibs)
source=(http://downloads.sourceforge.net/sourceforge/boost/boost_${_boostver}.tar.bz2 boost-mingw.patch)
_architectures="32:i686-w64-mingw32 64:x86_64-w64-mingw32"
md5sums=('1be49befbdd9a5ce9def2983ba3e7b76'
         '01f5f0d6b915d3b04dbabfd6db40f1e9')

package() {
    cd "${srcdir}"

    for _arch in ${_architectures}; do
        pushd "build-${_arch:3}"

        /usr/bin/bjam \
	        toolset=gcc-mingw64 \
    		target-os=windows \
		    variant=release \
		    threading=multi \
		    threadapi=win32 \
		    link=shared,static \
		    runtime-link=shared \
		    --prefix=${pkgdir}/usr/${_arch:3} \
		    --user-config=user-config.jam \
		    --without-mpi \
		    --without-python \
		    cxxflags="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4" \
		    address-model=${_arch:0:2} \
		    architecture=x86 \
		    binary-format=pe \
		    ${MAKEFLAGS} \
		    --layout=tagged install

        mkdir -p $pkgdir/usr/${_arch:3}/bin
        mv "$pkgdir"/usr/${_arch:3}/lib/*.dll "$pkgdir"/usr/${_arch:3}/bin
        ${_arch:3}-strip -x "$pkgdir"/usr/${_arch:3}/bin/*.dll
        ${_arch:3}-strip -g "$pkgdir"/usr/${_arch:3}/lib/*.a

        popd
    done
}

build() {
	unset LDFLAGS CPPFLAGS

	cd "${srcdir}"

    for _arch in ${_architectures}; do
        rm -rf build-${_arch:3}
        cp -r boost_${_boostver} build-${_arch:3}
        pushd build-${_arch:3}
	
        patch -Np0 -i "${srcdir}/boost-mingw.patch"

    	cat > user-config.jam << EOF
using gcc : mingw64 : ${_arch:3}-g++
	:
	<rc>${_arch:3}-windres
	<archiver>${_arch:3}-ar
;
EOF

    	/usr/bin/bjam \
	    	toolset=gcc-mingw64 \
		    target-os=windows \
		    variant=release \
		    threading=multi \
		    threadapi=win32 \
		    link=shared,static \
		    runtime-link=shared \
		    --prefix=${pkgdir}/usr/${_arch:3} \
		    --user-config=user-config.jam \
		    --without-mpi \
		    --without-python \
		    cxxflags=-std=c++11 \
		    address-model=${_arch:0:2} \
		    architecture=x86 \
		    binary-format=pe \
		    ${MAKEFLAGS} \
		    --layout=tagged
        popd
    done
}
