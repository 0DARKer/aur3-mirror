# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Manuel "ekerazha" C. (www.ekerazha.com)

pkgname=pysqlitegui
pkgver=0.0.8RC0
pkgrel=3
pkgdesc="A GUI tool for creating and managing SQLite databases"
arch=('any')
url="http://pysqlitegui.ospdev.net/"
license=('GPL')
depends=('pygtk' 'python-pysqlite' 'python-pysqlite-legacy')
source=(http://mirror.lfeo.ir/$pkgname/$pkgname/$pkgver/pySQLiteGUI-$pkgver.tar.bz2
        $pkgname.desktop
        $pkgname.png)
md5sums=('ee1410da30b4bf9f9994bed4373c184d'
         '33c1f15e6b30ba1be6f00f152a1b0364'
         'a8881ff3bb1bce90035ee4d7c557e78e')

build() {
  cd "$srcdir"/pySQLiteGUI/src

  # fix data path
  sed -i "s,psqlitegui.glade,/usr/share/$pkgname/psqlitegui.glade,g" $pkgname.py

  # install script
  install -Dm755 $pkgname.py "$pkgdir"/usr/bin/$pkgname

  # install data
  install -Dm644 psqlitegui.glade "$pkgdir"/usr/share/$pkgname/psqlitegui.glade
  install -Dm644 psqlitegui.gladep "$pkgdir"/usr/share/$pkgname/psqlitegui.gladep

  # install desktop file and icon
  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -Dm644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
}
