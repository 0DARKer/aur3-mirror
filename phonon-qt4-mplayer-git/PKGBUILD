# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Jekyll Wu <adaptee at gmail dot com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Rick W. Chen <stuffcorpse@archlinux.us>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Marek Kubica <marek@xivilization.net>

pkgname=phonon-qt4-mplayer-git
pkgver=20120714.7217499
pkgrel=1
pkgdesc="Mplayer backend for phonon, the multimedia API of KDE. (Qt4 only) GIT version)"
arch=("i686" "x86_64")
url="https://projects.kde.org/projects/unmaintained/phonon-mplayer"
license=("LGPL")
depends=('phonon-qt4' 'mplayer')
makedepends=('cmake' 'automoc4' 'git')
provides=('phonon-qt4-mplayer' 'phonon-qt4-backend')
conflicts=('phonon-mplayer' 'phonon-qt4-mplayer')
source=('git://anongit.kde.org/phonon-mplayer.git'
        'fix_find_config.patch'
        'CMakeLists.txt.patch')
md5sums=('SKIP'
         '55a20d7146371fbf1f1139a2ed874604'
         'ab4c216d2b66e6763dd90a7160fbaf65')
_gitname="phonon-mplayer"

pkgver() {
  cd "${_gitname}"
  echo "$(git log -1 --format="%cd" --date=short | tr -d '-').$(git log -1 --format="%h")"
}

prepare() {
  [ ${CARCH} = 'x86_64' ] && CXXFLAGS+=" -fPIC"

  cd "${_gitname}"
  patch -Np1 -i "${srcdir}/fix_find_config.patch"
  patch -Np0 -i "${srcdir}/CMakeLists.txt.patch"
}

build() {
  cd "${_gitname}"
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX="$(kde4-config --prefix)" \
          -DCMAKE_SKIP_RPATH=ON
  make
}


package () {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
