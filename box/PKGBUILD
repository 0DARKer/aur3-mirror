# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=box
pkgver=0.2.3
pkgrel=1
pkgdesc="A programming language thought to create graphic figures."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/boxc/"
license=('GPL')
depends=('cairo' 'libtool')
optdepends=('boxer: pygtk GUI')
conflicts=("$pkgname-core")
replaces=("$pkgname-core")
options=('!libtool')
source=("http://switch.dl.sourceforge.net/project/boxc/Box%20-%20the%20compiler/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('b1bfc96ad16e5ce5b7f12db3eca91183')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove insecure rpath
  sed -i "s|\${wl}-rpath.*libdir||g" configure

  ./configure --prefix=/usr \
              --enable-static=no \
              --with-cairo
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # examples
  install -d "$pkgdir/usr/share/$pkgname"
  cp -a examples "$pkgdir/usr/share/$pkgname"
  find "$pkgdir/usr/share/$pkgname" -name "Makefile*" -delete

  # man
  install -Dm644 man/$pkgname.1 "$pkgdir/usr/share/man/man1/$pkgname.1"
}

# vim:set ts=2 sw=2 et:
