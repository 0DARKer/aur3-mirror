# Contributor: Calimero <calimeroteknik at free dot fr>
# Maintainer: Laurent Hofer <laurenth at lavabit dot com>
pkgname=xcas
pkgver=1.0.0
pkgrel=2
pkgdesc="Xcas Computer Algebra System"
arch=('i686' 'x86_64')
url="http://www-fourier.ujf-grenoble.fr/~parisse/giac.html"
license=('GPL3')
depends=('perl' 'libgl' 'fltk-giac' 'mpfr' 'pari23' 'gsl' 'lapack' 'ntl' 'desktop-file-utils' 'cocoalib')
makedepends=('latex2html' 'readline') 
provides=('xcas' 'giac')
conflicts=('xcas-bin')
install=xcas.install
source=("http://www-fourier.ujf-grenoble.fr/~parisse/giac/giac-${pkgver}.tar.bz2") 
md5sums=('c64fbb702bd01457d8b5b9f0d00e2b8f')

build() {
  CXXFLAGS='-fpermissive'
  LDFLAGS='-lX11'
  cd "$srcdir/giac-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/giac-$pkgver"
  make DESTDIR="$pkgdir/" install
}
