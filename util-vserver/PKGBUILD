# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=util-vserver
pkgver=0.30.216_pre2926
_realpkgver=${pkgver/_/-}
pkgrel=1
pkgdesc="Provides tools for kernels with the security context patch"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/util-vserver/"
license=('GPL2')
depends=('beecrypt' 'python2')
makedepends=('autoconf>=2.61' 'automake>=1.10.1' 'dietlibc' 'iproute2' 'iptables' 'vconfig')
#source=("http://ftp.linux-vserver.org/pub/utils/util-vserver/${pkgname}-${_realpkgver}.tar.bz2")
source=("http://people.linux-vserver.org/~dhozac/t/uv-testing/${pkgname}-${_realpkgver}.tar.bz2"
        "python2.patch"
        "vserver-build-pacman.patch"
        "vserver-build.rpm.patch")
md5sums=('236c0ef54b94f8c116afaf7b78396a58'
         '7f32376706c566a80648950a71f3aceb'
         '6cb9bc539e1834590be90bc17a000d45'
         'e7453251105ab2705783723f0ec3b8a1')

build() {
  # expanding path for dietlibc
  export PATH="${PATH}:/opt/diet/bin"
  cd "${srcdir}/${pkgname}-${_realpkgver}"

  patch -Np1 -i "${srcdir}/python2.patch"
  patch -Np1 -i "${srcdir}/vserver-build-pacman.patch"
  patch -Np1 -i "${srcdir}/vserver-build.rpm.patch"
  msg "Running autoreconf..."
  autoreconf -fi 2>/dev/null
  msg "Continuing build()..."
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --with-initrddir=/etc/rc.d --localstatedir=/var --enable-dietlibc --with-crypto-api=beecrypt
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_realpkgver}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-distribution
  # Move the v_* scripts out of the init dir, as Arch does not support SYSV style init scripts
  install -m755 -d "${pkgdir}/usr/libexec/vserver"
  mv "${pkgdir}/etc/rc.d"/v_* "${pkgdir}/usr/libexec/vserver"
  find "${pkgdir}" -name '*.la' -exec rm {} \;
}

# vim:set ts=2 sw=2 et:
