# Maintainer: crazyelf5

pkgname=cinnamon-extension-gpaste
pkgver=2.5.0
pkgrel=1
pkgdesc="A clipboard management extension written in Vala"
arch=('any')
url="https://launchpad.net/~merlwiz79/+archive/cinnamon-ppa"
license=('GPL3')
depends=('cinnamon' 'dconf')
makedepends=('vala')
options=('!emptydirs')
install=$pkgname.install
source=(${url}/+files/gpaste_${pkgver}.orig.tar.gz
        02_add-cinnamon-extension.patch)
md5sums=('e7e0da1a00662fa27d37515abeebcf2d'
         'd50b1642148502fa780f9c4541f444ff')

build() {
  cd "$srcdir/gpaste-$pkgver/"

  patch -p1 -i ../02_add-cinnamon-extension.patch
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --enable-applet --disable-gnome-shell-extension
  make
}

package() {
  cd "$srcdir/gpaste-$pkgver/"

  make DESTDIR="$pkgdir/" install
  sed -i 's|"1.2"|"1.2", "1.3.0", "1.3.1"|' "$pkgdir/usr/share/cinnamon/extensions/GPaste@cinnamon.org/metadata.json"
}

# vim:set ts=2 sw=2 et:
