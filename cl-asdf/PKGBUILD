# Maintainer: DavidK <david_king at softhome dot net>
# Contributor: Alexej Magura <agm2819*gmail*>
# Contributor: revel <revelΘmuub·net>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>

pkgname=cl-asdf
_realname=asdf
pkgver=3.1.2
pkgrel=1
pkgdesc="Common Lisp's Another System Definition Facility (pre-installed in some implementations)"
arch=('any')
url="http://common-lisp.net/project/asdf"
license=('MIT')
depends=('common-lisp') # requires common-lisp for it to be at all useful, but doesn't need it to build.
makedepends=('texinfo')
provides=('asdf=3.1.2')
install=asdf.install
source=("http://common-lisp.net/project/$_realname/archives/$_realname-${pkgver}.tar.gz")
md5sums=('96d2371532b10f58083ac1ab8867dfd7')

prepare() {

    cd "$srcdir"
    
    if [[ -d "$pkgname-$pkgver" ]]; then
	rm -rf "$pkgname-$pkgver";
    fi

    mkdir -p $pkgname-$pkgver

    for dirs in $(ls -a1 $srcdir | sed -r "s:^(\\.\\.|\\.).*$:: ; s:^makefile$:: ; s:^$_realname-$pkgver\\.tar\\.gz$:: ; s:^$pkgname-$pkgver$:: ; s:^.*_patch$:: ; /^$/d")
    do
	mv $dirs $pkgname-$pkgver 
    done

    cd $pkgname-$pkgver 

    rm -rf build

}

build() {
    
    # build asdf.lisp
    cd "$srcdir"/$pkgname-$pkgver

    make

    cd "$srcdir"/$pkgname-$pkgver/doc

    make asdf.info
    mv asdf.info asdf-${pkgver}.info

    cd ..

    # extract the copyright portion of the debian/copyright file; save it to doc/copyright

    cd "$srcdir"/$pkgname-$pkgver/debian

    cat copyright | sed -e :a -e '$q;N;25,$D;ba' > ../doc/copyright

    cd ..

}

package() {
    # What a user needs is nothing other than asdf.lisp.
    # All of the rest of the files are really for developing and debugging ASDF,
    # which require write access to the source directory.
    install -d "${pkgdir}"/usr/share/common-lisp/source/${_realname}
    install -d "${pkgdir}"/usr/share/common-lisp/source/${_realname}/build
    install -d "${pkgdir}"/usr/share/common-lisp/source/${_realname}/uiop
    install -d "${pkgdir}"/usr/share/info
    install -d "${pkgdir}"/usr/share/licenses/${pkgname}

    cd "$srcdir"/$pkgname-$pkgver
    
    cp build/asdf.lisp ./asdf.lisp
    install -m 644 -t "${pkgdir}"/usr/share/common-lisp/source/${_realname} *.lisp *.asd version.*
    install -m 644 -t "${pkgdir}"/usr/share/common-lisp/source/${_realname}/build build/asdf.lisp
    install -m 644 -t "${pkgdir}"/usr/share/common-lisp/source/${_realname}/uiop uiop/*.lisp uiop/*.asd uiop/version.*
    install -m 644 doc/${_realname}-${pkgver}.info "${pkgdir}"/usr/share/info/${_realname}-${pkgver}.info
    install -m 644 doc/copyright "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

    
