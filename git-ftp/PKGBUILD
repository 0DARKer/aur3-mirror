# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=git-ftp
pkgver=0.9.0
pkgrel=1
pkgdesc='Git powered FTP client written as shell script'
url='https://github.com/git-ftp/git-ftp'
arch=('any')
license=('GPL')
depends=('curl' 'git')
makedepends=('ruby-ronn')
source=(https://github.com/git-ftp/git-ftp/archive/$pkgver.tar.gz
        git-ftp-0.9.0-Replace-pandoc-with-ronn.patch)
md5sums=('1a3e764c564c0afd7ef04b8162dedc8b'
         '12463b49d14f3133bed635002a33fa68')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../git-ftp-0.9.0-Replace-pandoc-with-ronn.patch
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install-all

  cd "$pkgdir"
  install -d usr/share/
  mv bin usr/
  mv man usr/share
}
