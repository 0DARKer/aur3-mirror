# Maintainer: David Spicer <azleifel at googlemail dot com>
# Contributor: Leszek Czurylo <leniu_user@wp.pl>

pkgname=vdr-epgsearch
_pluginname=epgsearch
pkgver=1.0.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin providing a replacement schedule with extended functionality"
url="http://winni.vdr-developer.org/epgsearch/"
license=('GPL2')
depends=('vdr>=1.6' 'perl' 'pcre')
backup=('etc/vdr/plugins/epgsearch/epgsearchcats.conf'
        'etc/vdr/plugins/epgsearch/epgsearchcmds.conf'
        'etc/vdr/plugins/epgsearch/epgsearchmenu.conf')
install=${pkgname}.install
source=(http://winni.vdr-developer.org/epgsearch/downloads/${pkgname}-${pkgver}.tgz)
md5sums=('c18d93ceeb4ae5d23b03e786d12c571b')

build() {
  cd "${srcdir}/${_pluginname}-${pkgver}"

  # Assumed VDR directory environment (VDRDIR/Make.config):
  # MANDIR       = /usr/share/man
  # BINDIR       = /usr/bin
  # LOCDIR       = /usr/share/locale
  # PLUGINLIBDIR = /usr/lib/vdr
  # VIDEODIR     = /var/spool/video
  # CONFDIR      = /etc/vdr

  install -d -m755 "${pkgdir}/etc/vdr/plugins/${_pluginname}"
  install -d -m755 "${pkgdir}/usr/bin"
  install -d -m755 "${pkgdir}/usr/lib/vdr"
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  install -d -m755 "${pkgdir}/usr/share/locale"
  install -d -m755 "${pkgdir}/usr/share/man"
  
  make VDRDIR=/usr/include/vdr \
       LIBDIR="${pkgdir}/usr/lib/vdr" \
       LOCALEDIR="${pkgdir}/usr/share/locale" \
       MANDIR="${pkgdir}/usr/share/man" all install-doc

  # Install scripts
  install -m755 scripts/*.{pl,sh} "${pkgdir}/usr/bin"

  # Install configuration files
  install -m644 conf/* "${pkgdir}/etc/vdr/plugins/${_pluginname}"
  install -m644 conf/epgsearchcats.conf-epgdata "${pkgdir}/etc/vdr/plugins/${_pluginname}/epgsearchcats.conf"
  install -m644 scripts/*.conf "${pkgdir}/etc/vdr/plugins/${_pluginname}"

  # Install documents
  for _file in README README.DE HISTORY HISTORY.DE MANUAL; do
    install -m644 ${_file} "${pkgdir}/usr/share/doc/${pkgname}"
  done
}
