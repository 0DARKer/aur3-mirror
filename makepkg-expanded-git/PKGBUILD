# Maintainer: Dominik Fischer <d dot f dot fischer at web dot de>
pkgname=makepkg-expanded-git
pkgver=r23.ed229e8
pkgrel=1
# Do not try to track version changes with git. As the version generated by the pkgver
# function in the git template will in turn reference the commit hash, so commiting
# a version change will change the version, which is an unbreakable race condition.
pkgdesc="builds packages and aurballs expanding templates off-file"
arch=('any')
url="https://github.com/dffischer/makepkg-expanded"
license=('GPL')
depends=('pacman')
makedependa=('perl-ronn')

makedepends+=('git')
source+=("${_gitname:=${pkgname%-git}}::${_giturl:-git+$url}")
md5sums+=('SKIP')
provides+=($_gitname)
conflicts+=($_gitname)

pkgver() {
  cd $_gitname
  git describe --long --tags 2>/dev/null | sed 's/[^[:digit:]]*\(.\+\)-\([[:digit:]]\+\)-g\([[:xdigit:]]\{7\}\)/\1.r\2.g\3/;t;q1'
  [ ${PIPESTATUS[0]} -ne 0 ] && printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}


build() {
  cd $_gitname
  ronn --roff makepkg-expanded.md
  gzip makepkg-expanded.1
}

package() {
  cd $_gitname
  install -Dt "$pkgdir/usr/bin" makepkg-expanded 
  install -Dm644 -t "$pkgdir/usr/share/man/man1" makepkg-expanded.1.gz
}
