# Maintainer: Carl George < arch at cgtx.us >
# Contributor: Zohar Malamant <dotfloat gmail com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Martin Erik Werner ("arand") <martinerikwerner@gmail.com>

pkgname="redeclipse"
pkgver="1.5.0"
pkgrel="4"
pkgdesc="free, casual arena shooter"
arch=("i686" "x86_64")
url="http://redeclipse.net"
license=("custom" "CC0" "CCPL:by" "CCPL:by-sa" "ZLIB" "MIT")
makedepends=("ed")
depends=("sdl_image"  "libgl" "sdl_mixer" "hicolor-icon-theme" "glu")
install="redeclipse.install"
source=("base-${pkgver}.tar.gz::https://github.com/red-eclipse/base/archive/v${pkgver}.tar.gz"
        "data-${pkgver}.tar.gz::https://github.com/red-eclipse/data/archive/v${pkgver}.tar.gz"
        "system-install-fix-binary-name.patch")
sha256sums=('1b860e796d46d8bacf56ac4d15b87c11e547167f3abe659da4139bc1ce922def'
            'db8bf0a1854781b72533d1bf4f646ee2b293985c56a6c36971f18f4bc88cac58'
            '78de3f17c36deac67e411aa8bb46977e648aef1b454f0eaaa6e28bdac51c18c2')

prepare() {
    cd "${srcdir}/base-${pkgver}"
    rmdir data
    mv "${srcdir}/data-${pkgver}" data
    patch -p1 -i "${srcdir}/system-install-fix-binary-name.patch"
}

build() {
    cd "${srcdir}/base-${pkgver}"
    make -C src/ client server
}

package() {
    cd "${srcdir}/base-${pkgver}"
    make -C src/ DESTDIR="${pkgdir}" prefix=/usr system-install
    install -Dm 0644 "${srcdir}/base-${pkgver}/doc/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    install -Dm 0644 "${srcdir}/base-${pkgver}/doc/all-licenses.txt" "${pkgdir}/usr/share/licenses/${pkgname}/all-licenses.txt"
}
