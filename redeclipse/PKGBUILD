# Maintainer: Zohar Malamant <dotfloat gmail com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Martin Erik Werner ("arand") <martinerikwerner@gmail.com>

pkgname=redeclipse
pkgver=1.4
pkgrel=2
pkgdesc="Red Eclipse is a fun-filled new take on the casual first person arena shooter, built as a total conversion of Cube Engine 2"
arch=('i686' 'x86_64')
url="http://redeclipse.net"
license=('custom' 'CCPL:by-sa' 'CCPL:by' 'ZLIB' 'MIT')
depends=('sdl_image'  'libgl' 'sdl_mixer' 'hicolor-icon-theme' 'glu')
makedepends=()
install=redeclipse.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}_${pkgver}_nix.tar.bz2)

build() {
	cd "$srcdir/$pkgname-$pkgver"

	sed -i "s/\"@APPNAME@\"/@APPNAME@/g" "src/install/nix/redeclipse.desktop.am"

	make -C src/ client server icons
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make -C src DESTDIR="$pkgdir" prefix=/usr system-install

	install -Dm644 "$srcdir/$pkgname-$pkgver/doc/all-licenses.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha256sums=('a0dfc527b0f437170a423b55df68a97e1b6e60d8c9fb6c842519b6670c1e69bf')
