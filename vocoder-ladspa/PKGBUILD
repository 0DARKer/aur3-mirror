# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Matt Parnell/ilikenwf <parwok@gmail.com>
# Contributor: Leslie P. Polzer <polzer@stardawn.org>

_name=vocoder
pkgname=$_name-ladspa
pkgver=0.4
pkgrel=4
pkgdesc="Stereo Vocoder LADSPA plugin"
arch=('i686' 'x86_64')
url="http://www.sirlab.de/linux/download_vocoder.html"
license=('GPL')
groups=('ladspa-plugins')
depends=('ladspa')
source=("http://www.gjcp.net/~gordonjcp/vocoder.tar.bz2")
md5sums=('772551a3c57aefcf166f4270c8c0e936')

build() {
  cd "$srcdir/$_name"

  # Right port name fix
  sed -i "/Right/s/L/R/" $_name.c

  make
}

package() {
  install -Dm755 "$srcdir/$_name/$_name.so" \
    "$pkgdir/usr/lib/ladspa/$_name.so"
}
