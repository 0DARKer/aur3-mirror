# Maintainer:  Ainola <opp310@alh.rqh> (ROT13)
# Contributor: <fero dot kiraly at gmail.com>

pkgname=pd-extended
pkgver=0.43.4
pkgrel=4
pkgdesc="An open source visual programming language. Contains an extra assembly of libraries, extensions, and documentation."
url="http://puredata.info/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libdc1394' 'desktop-file-utils' 'libiec61883' 'fftw' \
         'jack' 'tk' 'libquicktime' 'libdv' 'gsl' 'imagemagick' \
         'ftgl' 'lua51' 'ladspa')
makedepends=('dssi' 'libv4l' 'tcllib' 'swig' 'automake')
conflicts=('pdp' 'pd-zexy' 'pd')
provides=('pd' 'pd-gem' 'pdp' 'zexy')
optdepends=('puredata-utils: pdsend and pdreceive for Pure Data')
options=('!makeflags' '!libtool')
install=pd-extended.install
source=("${pkgname}.tar.bz2::http://downloads.sourceforge.net/project/pure-data/pd-extended/${pkgver}/Pd-extended_${pkgver}-source.tar.bz2"
        'change_gem_configure_file.patch'
        's_inter.c.patch'
        'tclpd.Makefile.patch'
        'makefile.am.patch')
sha256sums=('087f23cbc955b308c8e1b9bbc37746d96837f96c6c3c7a199f52ee47d625bf87'
            '0db840450831f8a5745ac21eee876d8f8ba41283d598177c90c60b43d2a0b562'
            '594b614392776df8c2fe8d047ccba40ff1b11f8d192e88634efae6976213ef61' 
            'a4f8af85533bb08403f1a87e356d960e4468fa06e85199a8884e41c81d17713c' 
            'ded38de27b4f843733a078beb96b7fb410906d3899c9f5154749fd5fe33a347f')

prepare(){
  #FIXES -----------------------------------------------------------------------------
  # fix #1 lua
  #patch  $srcdir/$pkgname/externals/loaders/pdlua/src/Makefile ./repair_lua_lib_number.patch
  #fix #2 -- GEM configure.ac
  patch  "${srcdir}/${pkgname}/externals/Gem/configure.ac" ./change_gem_configure_file.patch
  #fix #3 -- repair wish8.6 (for tcl8.6)
  patch  "${srcdir}/${pkgname}/pd/src/s_inter.c" ./s_inter.c.patch
  #fix #4 -- repair version of tcl (8.6)
  patch  "${srcdir}/${pkgname}/externals/loaders/tclpd/Makefile" ./tclpd.Makefile.patch
  #fix #5 -- repair binary name to pd-extended (from pd)
  #patch  $srcdir/$pkgname/packages/Makefile ./program_name.patch 
  patch  "${srcdir}/${pkgname}/pd/src/Makefile.am" ./makefile.am.patch
 
  # 64 bit archutecure----------------------------------------------------------------
  if [ "$CARCH" = "x86_64" ]; then
    # fix -fPIC issue in PDP
    sed -e "s|CFLAGS =|CFLAGS = -fPIC|" \
      -i "${srcdir}/${pkgname}/externals/pdp/opengl/Makefile.config"
    # fix -fPIC issue in pddp
    sed -e "s|DEFINES =|DEFINES = -fPIC|" \
      -i "${srcdir}/${pkgname}/externals/miXed/Makefile.common"
    # setting additional variable
    FPIC_FLAG="-fPIC"
  else
    FPIC_FLAG=""
  fi
}

build() {
  unset CFLAGS
  unset LDFLAGS
  unset INCLUDES
 
  cd "${srcdir}/${pkgname}/packages/linux_make"

  make BUILDLAYOUT_DIR="${srcdir}/${pkgname}/packages" \
       GEM_EXTRA_CXXFLAGS="$FPIC_FLAG" \
       prefix=/usr \
       install
}

package() {
  cp -urv "${srcdir}/${pkgname}/packages/linux_make/build/usr" "$pkgdir/"
  mv "${pkgdir}/usr/share/man/man1/pd.1" "${pkgdir}/usr/share/man/man1/pdextended.1"
  rm "${pkgdir}/usr/share/man/man1/"{pdreceive.1,pdsend.1}
  rm "${pkgdir}/usr/include/m_pd.h"
  rm "${pkgdir}/usr/bin/pd-gui.tcl"
  cd "${srcdir}/${pkgname}/"

# PD License
  install -Dm644 pd/LICENSE.txt "${pkgdir}/usr/share/licenses/pd-extended/LICENSE.txt"
  cd packages/
  install -Dm644 linux_make/default.pdextended "${pkgdir}/usr/lib/pd-extended"

# icons/.desktop
  install -Dm644 linux_make/pd-extended.png       "${pkgdir}/usr/share/icons/hicolor/128x128/apps/pd-extended.png"
  install -Dm644 linux_make/48x48/pd-extended.png "${pkgdir}/usr/share/icons/hicolor/48x48/apps/pd-extended.png"
  install -Dm644 linux_make/pd-extended.desktop   "${pkgdir}/usr/share/applications/pd-extended.desktop"

# mime
  install -Dm644 linux_make/pd-extended.xml     "${pkgdir}/usr/share/mime/packages/pd-extended.xml"
  install -Dm644 linux_make/text-x-puredata.png "${pkgdir}/usr/share/icons/hicolor/128x128/mimetypes/text-x-puredata.png"
   
# files for /etc
  cd "${srcdir}/${pkgname}"

  install -Dm644 scripts/bash_completion/pd "${pkgdir}/etc/bash_completion.d/pd"
# emacs mode for .pd files
  install -Dm644 scripts/pd-mode.el "${pkgdir}/usr/share/emacs/site-lisp/pd-mode.el"
# Pd-related scripts
  install -Dm755 scripts/pd-diff "${pkgdir}/usr/bin/pd-diff"
  install -Dm755 scripts/config-switcher.sh "${pkgdir}/usr/bin/config-switcher.sh"
}

# vim:set ts=2 sw=2 et:
