# Maintainer: Duncan K. <duncank@fastmail.fm>

pkgname=prisonarchitect-hib
pkgver=0_alpha24c
pkgrel=1
pkgdesc='Prison construction and management simulation (Humble Bundle version)'
url='http://www.introversion.co.uk/prisonarchitect/'
arch=('i686' 'x86_64')
license=('custom:commercial')
depends=('libgl' 'glu')
conflicts=('prisonarchitect')
options=('!strip' '!upx')
PKGEXT='.pkg.tar.gz'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

_archive="prisonarchitect-alpha24c-linux"
source=("hib://${_archive}.tar.gz")
md5sums=('960cce6a900a08fddaf7ab6ff828f8a9')

package() {
	cd $srcdir
	_installdir="/opt/prisonarchitect"; _target="${pkgdir}/${_installdir}"

	# Install game files
	mkdir -p $_target
	cp -rT $_archive $_target

	# Point launcher script to the correct directory
	sed -i 's/^base=.*/base="\/opt\/prisonarchitect"/' "${_target}/PrisonArchitect"

	# Link executable
	install -d "${pkgdir}/usr/bin"
	ln -s "${_installdir}/PrisonArchitect" "${pkgdir}/usr/bin/prisonarchitect"
}
