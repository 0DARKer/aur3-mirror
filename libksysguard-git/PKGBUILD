# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=libksysguard-git
pkgver=r1468.8be07e5
pkgrel=1
pkgdesc='Libksysguard'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('kdelibs4support-git' 'plasma-framework-git')
makedepends=('extra-cmake-modules' 'git' 'kf5umbrella-git' 'kdoctools-git')
provides=('libksysguard')
conflicts=('libksysguard' 'kde-workspace<0')
source=('git://anongit.kde.org/libksysguard.git')
md5sums=('SKIP')

pkgver() {
  cd libksysguard
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libksysguard/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
