# Maintainer: Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

# _GNU_EFI_LIB_DIR="/usr/lib"

_actualname="gummiboot"
_pkgname="${_actualname}-efi-x86_64"
pkgname="${_pkgname}"

_pkgver="49d4ae4e4258d7647f221a58df3375ac2957a8dc"
pkgver="6"

pkgrel="1"
pkgdesc="Simple x86_64 UEFI Boot Manager for EFISTUB Kernels"
url="http://freedesktop.org/wiki/Software/gummiboot"
arch=('any')
license=('GPL2')

makedepends=('git' 'gnu-efi')

depends=('dosfstools' 'efibootmgr')
optdepends=('mactel-boot: For bless command in Apple Mac systems')

conflicts=("${_pkgname}")
provides=("${_pkgname}")

backup=('boot/efi/loader/loader.conf'
        'boot/efi/loader/entries/archlinux-core.conf'
        'boot/efi/loader/entries/archlinux-core-fallback.conf')

options=('!strip' 'docs')
install="${_pkgname}.install"

source=("${_actualname}-${pkgver}.tar.gz::http://cgit.freedesktop.org/gummiboot/snapshot/${_actualname}-${_pkgver}.tar.gz"
        'loader.conf'
        'archlinux-core.conf'
        'archlinux-core-fallback.conf')

## Generate a sha256sum for gummiboot-${pkgver}.tar.gz as it deos not stay the same everytime, or store a snapshot locally.
sha256sums=('a051a9c3aa87db3447b0c9de3b8a09a91589e972aaca2a5eaa79988b63f03d4e'
            '9f53946a3efa4005031ec9f8247a874b8857c1363a2401624e0a909436b558ed'
            '4d4a0d59ca3e7ab93428c817a04ca717e5295a1536f13649f4b4007495f3921c'
            '48b20ba6684f65c24508ab7cadd0467201de0b158514c384500b622eaa97efff')

build() {
	
	if [[ "${CARCH}" != "x86_64" ]]; then
		echo "${pkgname} package can be built only in a x86_64 system. Exiting."
		exit 1
	fi
	
	cd "${srcdir}/${_actualname}-${_pkgver}/"
	echo
	
	make clean || true
	echo
	
	make
	echo
	
}

package() {
	
	cd "${srcdir}/${_actualname}-${_pkgver}/"
	
	## Install gummiboot x86_64 UEFI application
	install -d "${pkgdir}/boot/efi/EFI/arch/gummiboot/"
	install -D -m0644 "${srcdir}/${_actualname}-${_pkgver}/gummiboot.efi" "${pkgdir}/boot/efi/EFI/arch/gummiboot/gummibootx64.efi"
	
	## Install gummiboot configuration files
	install -d "${pkgdir}/boot/efi/loader/entries/"
	install -D -m0644 "${srcdir}/loader.conf" "${pkgdir}/boot/efi/loader/loader.conf"
	install -D -m0644 "${srcdir}/archlinux-core.conf" "${pkgdir}/boot/efi/loader/entries/archlinux-core.conf"
	install -D -m0644 "${srcdir}/archlinux-core-fallback.conf" "${pkgdir}/boot/efi/loader/entries/archlinux-core-fallback.conf"
	
}
