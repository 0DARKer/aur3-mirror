# Contributor: wido <widomaker2k7@gmail.com>

pkgname=oilrush
pkgver=0.6
pkgrel=4
pkgdesc="a real-time strategy game based on group control."
arch=('i686' 'x86_64')
depends=('openal' 'mesa')
provides=()
#options=(!strip)
license=(commercial)
url="http://oilrush-game.com/"
_gamepkg="OilRush-$pkgver.run"
source=('oilrush' 'oilrush.desktop' 'OilRush-Logo.png')
md5sums=('273430dd3360e5e2513fb46799dd5d44'
         '5cd89bf2d8432c47933faf93ca377985'
         'bee05cb657ed95ec9ba51276cd8adc32')

build() {
    cd ${srcdir}

  msg "You need a full copy of this game in order to install it"
  msg "Searching for \"${_gamepkg}\"\
  in dir: $(readlink -f $startdir)"
  pkgpath=$startdir

  if [[ ! ( -f "$startdir/${_gamepkg}" ) ]]; then
    error "Game package not found, please type absolute path to game setup package (/home/joe):"
    read pkgpath
    if [[ ! ( -f "${pkgpath}/${_gamepkg}" ) ]] ; then
       error "Unable to find game package." && return 1
   fi
  fi
  msg "Found game package, installing..."

  ln -fs "${pkgpath}/${_gamepkg}" .
  sh ${srcdir}/${_gamepkg} --noexec --keep
}

package() {
    install -d ${pkgdir}/opt/oilrush

    cp -R ${srcdir}/OilRush-${pkgver}/* \
        ${pkgdir}/opt/oilrush/

    if [ "$CARCH" = "x86_64" ]; then
        sed -i s:./bin/OilRush_x86d:./bin/OilRush_x64d: ${srcdir}/$pkgname
        cd ${pkgdir}/opt/
        find . -name '*x86*' -print -exec rm {} \;
    else
        cd ${pkgdir}/opt/
        find . -name '*x64*' -print -exec rm {} \;
    fi

    # Install Launcher
    install -D -m755 ${srcdir}/$pkgname \
        ${pkgdir}/usr/bin/$pkgname

    # Install Icon
    install -D -m644 ${srcdir}/OilRush-Logo.png \
        ${pkgdir}/usr/share/pixmaps/OilRush-Logo.png

    # Install Desktop
    install -D -m644 ${srcdir}/oilrush.desktop \
        ${pkgdir}/usr/share/applications/oilrush.desktop

    # Make save game Directory with user group permissions
    install -d -m775 -g users ${pkgdir}/opt/oilrush/bin/profiles/
    install -d -m775 -g users ${pkgdir}/opt/oilrush/bin/save/
    install -m644 -g users ${srcdir}/OilRush-${pkgver}/oilrush-${pkgver}.cfg ${pkgdir}/opt/oilrush/
}
