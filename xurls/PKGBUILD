# $Id$
# Maintainer: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=xurls
pkgver=0.2
pkgrel=1
pkgdesc="Extract urls from plain text"
url="https://github.com/mvdan/xurls"
license=('BSD')
arch=('i686' 'x86_64')
depends=()
makedepends=('git' 'go')
source=("${pkgname}::git+https://github.com/mvdan/xurls#tag=v${pkgver}")
sha1sums=('SKIP')

build() {
	cd "${srcdir}"
	mkdir -p "src/github.com/mvdan"
	mv "${pkgname}" "src/github.com/mvdan/${pkgname}"
	cd "src/github.com/mvdan/${pkgname}/cmd/${pkgname}"
	GOPATH="${srcdir}" go build
}

package() {
	cd "${srcdir}/src/github.com/mvdan/${pkgname}"
	install -Dm755 "cmd/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
