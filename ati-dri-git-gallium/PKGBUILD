# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Perry3D <perry3d@gmail.com>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
pkgname=ati-dri-git-gallium
pkgver=20100911
pkgrel=1
pkgdesc='Gallium3D-enabled DRI drivers for ATI/AMD videochips from the git master branch.'
arch=('i686' 'x86_64')
url='http://xorg.freedesktop.org/'
license=('MIT')
depends=('libgl-git-gallium' 'expat>=2.0.1' 'mesa-git-gallium' 'xf86-video-ati-git')
makedepends=('glproto-git' 'pkgconfig' 'libxfixes' 'libxdamage' 'libxxf86vm' 'libxext' 'dri2proto-git')
conflicts=('xf86-video-ati<6.9.0-6' 'ati-dri' 'ati-dri-git')
provides=('ati-dri=7.9' 'ati-dri-git')
options=('!libtool')
source=(mesa-7.1-link-shared.patch LICENSE)
md5sums=('5a8d58b6a19f550c7300325e9594e280'
         '7ba429de367a2b3d273d2230cc705771')

_gitroot="git://anongit.freedesktop.org/git/mesa/mesa"
_gitname="mesa"

build() {
	msg "Connecting to git.freedesktop.org GIT server...."

	if [ -d ${srcdir}/$_gitname ] ; then
		cd $_gitname && git pull origin
		msg "The local files are updated."
	else
		git clone $_gitroot
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf ${srcdir}/$_gitname-build
	cp -r ${srcdir}/$_gitname ${srcdir}/$_gitname-build
	cd ${srcdir}/$_gitname-build

	# Any patches you may want to add go here

	#patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
	./autogen.sh --prefix=/usr \
	--with-dri-driverdir=/usr/lib/xorg/modules/dri \
	--with-dri-drivers=radeon,r200,r300,r600 \
	--enable-glx-tls \
	--with-driver=dri \
	--enable-xcb \
	--disable-glu \
	--disable-glut \
	--enable-gallium \
	--enable-gallium-radeon \
  --enable-gallium-r600 \
	--disable-glw
	make
}

package() {
	cd ${srcdir}/$_gitname-build/src/mesa/drivers/dri

	make DESTDIR="${pkgdir}" install

	install -D -m755 "${srcdir}/$_gitname-build/lib/gallium/r300_dri.so" "${pkgdir}/usr/lib/xorg/modules/dri/r300_dri.so"
	install -D -m755 "${srcdir}/$_gitname-build/lib/gallium/r600_dri.so" "${pkgdir}/usr/lib/xorg/modules/dri/r600_dri.so"

	install -D -m755 "${srcdir}/$_gitname-build/lib/r200_dri.so" "${pkgdir}/usr/lib/xorg/modules/dri/r200_dri.so"
	install -D -m755 "${srcdir}/$_gitname-build/lib/radeon_dri.so" "${pkgdir}/usr/lib/xorg/modules/dri/radeon_dri.so"

	install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
	rm -rf "${pkgdir}/usr/include"
	rm -rf "${pkgdir}/usr/lib/pkgconfig"
}

# vim: sw=2:ts=2 et:
