# Contributor: Victor Noel

pkgname=argos3-git
_gitname=argos3
pkgver=3.0.0.beta30.r1.g8d079c4
pkgrel=1
pkgdesc="A parallel, multi-engine simulator for heterogeneous swarm robotics"
url="http://iridia.ulb.ac.be/argos"
license=('MIT')
arch=('i686' 'x86_64')
makedepends=('cmake' 'lua>=5.2' 'git' 'asciidoc')
depends=('gsl' 'freeimage' 'qt4' 'freeglut' 'glu' 'mesa' 'shared-mime-info')
optdepends=('lua: for lua wrapper')
provides=('argos3')
source=('git+https://github.com/ilpincy/argos3.git')
md5sums=('SKIP')
install=$pkgname.install


pkgver() {
  cd "$_gitname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_gitname"

  rm -rf build
  mkdir build
  cd build

  cmake ../src/ -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -Wno-dev

  make
  # needed by make install
  make doc
}

package() {
  cd "$_gitname/build"

  make DESTDIR="$pkgdir" install
}
