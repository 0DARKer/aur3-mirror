# Maintainer: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=gngeo-hg
pkgver=0.8.r95.6ad2a3d1bba3
pkgrel=1
pkgdesc="A portable command-line NeoGeo Emulator (development version)"
arch=('i686' 'x86_64')
url="https://code.google.com/p/gngeo/"
license=('custom GPL')
depends=('zlib' 'sdl')
optdepends=('libgl: OpenGL renderer backend')
makedepends=('mercurial' 'glu')
makedepends_i686=('nasm')
conflicts=("${pkgname%-*}")
provides=("${pkgname%-*}")
install=$pkgname.install
source=(${pkgname%-*}::"hg+$url"
        "${pkgname%-*}.desktop"
        "${pkgname%-*}.png")
sha256sums=('SKIP'
            '5a3bfd83f3617860c28ba7c7a1d04dd34fc6b60eb5a93c28b08adf6d1be3454c'
            'b543425d51bd669fe5081b0b89492f18163c292194ac2a4acff683ba2a60063e')

pkgver() {
  cd ${pkgname%-*}

  # get latest tag, revision count and commit identifier
  printf "%s.r%s.%s" "$(hg parents --template {latesttag} | sed 's/gngeo_//')" \
    "$(hg identify -n)" "$(hg identify -i)"
}

prepare() {
  # change path
  sed 's#games/lib#share#' -i ${pkgname%-*}/sample_gngeorc
}

build() {
  cd ${pkgname%-*}

  ./configure --prefix=/usr
  # force single threaded build
  make -j1
}

package() {
  cd ${pkgname%-*}

  make DESTDIR="$pkgdir/" install

  # install icon, .desktop, sample config and license file
  install -Dm644 ../${pkgname%-*}.png "$pkgdir"/usr/share/pixmaps/${pkgname%-*}.png
  install -Dm644 ../${pkgname%-*}.desktop "$pkgdir"/usr/share/applications/${pkgname%-*}.desktop
  install -Dm644 sample_gngeorc "$pkgdir"/usr/share/${pkgname%-*}/sample_gngeorc
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
