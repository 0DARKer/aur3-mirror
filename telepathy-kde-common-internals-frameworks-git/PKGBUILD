# Maintainer: Antonio Rojas 

pkgname=telepathy-kde-common-internals-frameworks-git
pkgver=r1525.06598ea
pkgrel=1
pkgdesc='Common components for KDE-Telepathy'
arch=('i686' 'x86_64')
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
depends=('kaccounts-integration-frameworks-git' 'knotifyconfig' 'ktexteditor' 'kdelibs4support' 'telepathy-qt5')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
source=("git://anongit.kde.org/ktp-common-internals.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
  cd ktp-common-internals
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ktp-common-internals \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
