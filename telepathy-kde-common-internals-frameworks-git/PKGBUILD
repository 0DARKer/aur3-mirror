# Maintainer: Antonio Rojas 

pkgname=telepathy-kde-common-internals-frameworks-git
pkgver=r1490.819a5c8
pkgrel=1
pkgdesc='Common components for KDE-Telepathy'
arch=('i686' 'x86_64')
url='http://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
depends=('kcmutils' 'knotifyconfig' 'ktexteditor' 'kdelibs4support' 'telepathy-qt5-git')
makedepends=('extra-cmake-modules' 'git')
source=("git://anongit.kde.org/ktp-common-internals.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
  cd ktp-common-internals
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  sed -i 's|KF5 REQUIRED CONFIG|KF5 REQUIRED|' ktp-common-internals/CMakeLists.txt
  sed -i 's|KAboutData::License_LGPL|KAboutLicense::LGPL|' ktp-common-internals/tools/debugger/main.cpp
}

build() {
  cd build
  cmake ../ktp-common-internals \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
