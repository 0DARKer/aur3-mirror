# Maintainer: Gicu Gorodenco <cyclopsihus 'at' gmail 'dot' com>
# Contributor: rafael ff1 (aka josephgbr)
_pkgbasename=libsigc++
pkgname=lib32-$_pkgbasename
pkgver=2.2.9
pkgrel=2
pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2 (32 bit)"
arch=('x86_64')
url="http://libsigc.sourceforge.net/"
license=('LGPL')
depends=("$_pkgbasename")
makedepends=('gcc-multilib')
options=(!libtool !emptydirs)
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/2.2/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('2f4c083e01d9be377669206bd97090d2e95bc05f2e8d95447c9f9ce92bdfbb63')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}
