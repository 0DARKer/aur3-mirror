# Maintainer: Gicu Gorodenco <cyclopsihus 'at' gmail 'dot' com>
# Contributor: rafael ff1 (aka josephgbr)
_pkgbasename=libsigc++
pkgname=lib32-$_pkgbasename
pkgver=2.2.11
pkgrel=1
pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2 (32 bit)"
arch=('x86_64')
url="http://libsigc.sourceforge.net/"
license=('LGPL')
depends=("$_pkgbasename")
makedepends=('gcc-multilib')
options=(!libtool !emptydirs)
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/2.2/${_pkgbasename}-${pkgver}.tar.xz)
sha256sums=('9834045f74f56752c2c6b3cdc195c30ab8314ad22dc8e626d6f67f940f1e4957')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}
