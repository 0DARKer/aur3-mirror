# Maintainer: Presence <dan465 at mail dot com>

pkgname=pyobd
pkgver=0.9.2.2
_real_pkgver=0.9.2
pkgrel=1
pkgdesc="An OBD-II compliant car diagnostic tool"
arch=('any')
url="http://www.obdtester.com/pyobd"
license=('GPL')
depends=('wxpython' 'python2-pyserial' 'hicolor-icon-theme')
install=pyobd.install
source=(http://www.obdtester.com/download/${pkgname}_${_real_pkgver}-2.tar.gz
        fix_dtc_window.patch
        pyobd.png)
sha256sums=('3edb646d2ab70c0ae0fab526d115f969422e20b681762dba1b83abda37aa95f0'
            '1e1d4672395656b5a5113e78356d019343e96aedbad561b5ff7ae673de1b740f'
            '14d0d90dcda38c339dc8397f004923075f6d4fce37b7c8539021f4a77b3a86fd')

prepare() {
	cd "$srcdir/${pkgname}-${_real_pkgver}"

	patch -Np1 -i "$srcdir/fix_dtc_window.patch"
		
	#Fix desktop file
	sed -i -e "s#/usr/share/pyobd/pyobd.gif#$pkgname#" \
		-e "s#python /usr/bin/pyobd#$pkgname#" \
		-e "s#Name=pyOBD: OBD2 Diagnostics#Name=pyOBD\nGenericName=OBD2 Diagnostics#" \
		"pyobd.desktop"

	#Set executable to run with python2
	sed -i "s%#!/usr/bin/env python%#!/usr/bin/python2%" "pyobd"
}

package() {
	cd "$srcdir/${pkgname}-${_real_pkgver}"

	install -Dm644 "pyobd.desktop"      "$pkgdir/usr/share/applications/$pkgname.desktop"
	install -Dm644 "$srcdir/pyobd.png"  "$pkgdir/usr/share/icons/hicolor/32x32/apps/$pkgname.png"
	install -Dm644 "COPYING"            "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	install -Dm755 "pyobd"              "$pkgdir/usr/lib/$pkgname/pyobd"
	install -m644 -t "$pkgdir/usr/lib/$pkgname/" *.py{,c}

	mkdir -p "$pkgdir/usr/bin/"
	ln -sf "../lib/$pkgname/pyobd" "$pkgdir/usr/bin/$pkgname"
}

# vim:set ts=2 sw=2 et:
