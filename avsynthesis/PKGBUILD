# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=avsynthesis
pkgver=34_05_13
pkgrel=1
pkgdesc="Audio video synthesis"
arch=('any')
url="https://docs.google.com/View?id=dfq5sj5w_80f9z8tb"
license=('custom')
depends=('csound' 'java-runtime')
makedepends=('zip')
source=('http://anonfiles.com/cdn/1319728460464.zip'
        'avsynthesis.sh'
        'MANIFEST.MF'
        'config.xml')
md5sums=('405fbe2640a2bad8168c15bbd0940d45'
         '722d6c2cff50b7375bb35c10d0519950'
         'a3a4f4ddcbd6c3c56914c90a4f190efa'
         '7c60330a92e20278dae6417e1ee9fb66')

build() {
  cd "$srcdir/AVSynthesis_$pkgver"

  msg2 "Fixing jar manifest..."
  mkdir -p META-INF
  cp "$srcdir/MANIFEST.MF" META-INF
  zip -q -u AVSynthesis.jar META-INF/MANIFEST.MF

  echo "Public domain?" > LICENSE
}

package() {
  cd "$srcdir/AVSynthesis_$pkgver"

  msg2 "Packaging data and libraries..."
  mkdir -p "$pkgdir/usr/share/$pkgname"
  cp -r data "$pkgdir/usr/share/$pkgname/data"
  cp -r lib "$pkgdir/usr/share/$pkgname/lib"

  msg2 "Installing config.xml to /etc..."
  mkdir -p "$pkgdir/etc/$pkgname"
  install -Dm644 "$srcdir/config.xml" \
    "$pkgdir/etc/$pkgname/config.xml"
  ln -sf "/etc/$pkgname/config.xml" \
    "$pkgdir/usr/share/$pkgname/data/config.xml"

  msg2 "Save files will be placed in /tmp instead of /usr/share..."
  mv "$pkgdir/usr/share/$pkgname/data/save" \
    "$pkgdir/usr/share/$pkgname/data/original_save"
  ln -s "/tmp" "$pkgdir/usr/share/$pkgname/data/save"

  msg2 "Packaging executables..."
  install -Dm644 AVSynthesis.jar \
    "$pkgdir/usr/share/$pkgname/AVSynthesis.jar"
  install -Dm755 ../avsynthesis.sh \
    "$pkgdir/usr/bin/$pkgname"

  install -Dm 644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
