# Maintainer: monson <holymonson@gmail.com>
# Contributor: giniu <gginiu@gmail.com>

pkgname=intel-mkl
pkgver=10.3.4
pkgrel=1
pkgdesc="Intel Math Kernel Library, for non-commercial use."
arch=('i686' 'x86_64')
url="http://software.intel.com/en-us/intel-mkl/"
license=('custom')
makedepends=('cpio')

_build=191
_urlnumber=2153

if [ "$CARCH" == "x86_64" ]; then
    _intel_arch=intel64
    _archive_md5='13014a4c96bb76ce5b374cb71a568665'
elif [ "$CARCH" == "i686" ]; then
    _intel_arch=ia32
    _archive_md5='2a1ea34264853be1304fdc7c548e078f'
fi

_archive=l_mkl_$pkgver.${_build}_${_intel_arch}
_install_prefix=/opt/intel/composerxe

source=("http://registrationcenter-download.intel.com/irc_nas/${_urlnumber}/${_archive}.tgz"
        config.ini)
md5sums=(${_archive_md5}
         '932cdcdf8a791dbad245367a2eec72dc')


build() {
    cd "$srcdir"/${_archive}
    
    msg "Checking licenses..."
    if ! ( [ -f $HOME/intel/licenses/*.lic ] || [ -f /opt/intel/licenses/*.lic ] ); then
        msg "ERROR: NO license file be found.

 ** http://software.intel.com/en-us/articles/non-commercial-software-development/
 ** Before re-building, visit the website above to ask for a non-commercial license,
 ** and place the .lic file in /opt/intel/licenses/ (*) OR \$HOME/intel/licenses/ .
    "
        return 1
    fi
    
    msg "Modifying install prefix..."
    sed -e "s|PSET_INSTALL_DIR=.*|PSET_INSTALL_DIR=$pkgdir${_install_prefix}|g" "$srcdir"/config.ini > ./config.ini
    grep -rlI "\$HOME/intel" . | xargs sed -i -e "s|\$HOME/intel|$pkgdir/opt/intel|g"
    
    msg "Unpacking..."
    mkdir -p "$srcdir"/tmp    
    ./install.sh -t "$srcdir"/tmp -s ./config.ini
    
    msg "Removing extra empty directories..."
    cd "$HOME"
    rmdir --ignore-fail-on-non-empty -p intel/licenses/
}

package() {    
    msg "Fixing paths in files..."
    grep -rlI "$pkgdir/opt/intel" "$pkgdir" | xargs sed -i -e "s|$pkgdir/opt/intel|/opt/intel|g"
    
    msg "Installing documentations..."
    install -d "$pkgdir"/usr/share/licenses/${pkgname}/
    install -D -m 644 "$srcdir"/${_archive}/license "$pkgdir"/usr/share/licenses/${pkgname}/    
    install -d "$pkgdir"/usr/share/doc/
    ln -sf ${_install_prefix}/Documentation/en_US/mkl "$pkgdir"/usr/share/doc/${pkgname}
    
    msg "Installing profile..."
    _mklvars_file=${_install_prefix}/mkl/bin/${_intel_arch}/mklvars_${_intel_arch}.sh    
    sed -i -e "s|^${_install_prefix}|source ${_install_prefix}|" "$pkgdir"${_mklvars_file} # patch for global environment
    install -D -m 755 "$pkgdir"${_mklvars_file}  "$pkgdir"/etc/profile.d/${pkgname}.sh
}
