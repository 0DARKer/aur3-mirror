# Maintainer: Charles Bos <charlesbos1 AT gmail>

pkgname=gnome-flashback-git
_gitname=gnome-flashback
pkgver=3.10.0.r1.g5d7dbb3
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="GNOME Flashback session (development version)"
url="https://git.gnome.org/browse/gnome-flashback"
# Note: Dependencies from gnome-panel onwards are flashback session dependencies and are not required for building the flashback module
depends=('glib2>=2.39.91' 'gnome-desktop>=3.8.0' 'gtk3>=3.10.0' 'libcanberra>=0.13' 'libpulse' 'gnome-panel' 'gnome-screensaver' 'gnome-session' 'gnome-settings-daemon-compat' 'gnome-themes-standard' 'metacity' 'nautilus' 'notification-daemon' 'polkit-gnome')
optdepends=('compiz: required for the GNOME Flashback (Compiz) session')
makedepends=('git' 'gnome-common' 'intltool')
provides=('gnome-flashback=3.10.0')
conflicts=('gnome-flashback')
install=$pkgname.install
source=(git://git.gnome.org/gnome-flashback
        session-modification.patch)
sha256sums=('SKIP'
            '685cd8b4aac2ead00f54069700c760989d604f2c90379f7c6710ec784dee397b')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$_gitname"

  # Rename GNOME Flashback (Metacity) to GNOME Flashback and add extra required components
  patch -Np1 -i "$srcdir/session-modification.patch"
}

build() {
  cd "$srcdir/$_gitname"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/"$_gitname"

  make
}

package() {
  cd "$srcdir/$_gitname"

  make DESTDIR="$pkgdir" install
}

