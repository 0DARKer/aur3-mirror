# Maintainer: Charles Bos <charlesbos1 AT gmail>

pkgname=gnome-flashback-git
_gitname=gnome-flashback
pkgver=r71.471bbf7
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="GNOME Flashback session"
url="https://git.gnome.org/browse/gnome-flashback"
depends=('glib2>=2.39.91' 'gnome-desktop>=3.8.0' 'gtk3>=3.10.0' 'libcanberra>=0.13' 'libpulse')
makedepends=('git' 'gnome-common' 'intltool')
provides=('gnome-flashback=3.10.0')
install=$pkgname.install
source=(git://git.gnome.org/gnome-flashback)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir/$_gitname"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/"$_gitname"

  make
}

package() {
  cd "$srcdir/$_gitname"

  make DESTDIR="$pkgdir" install

  rm -rf {$pkgdir/usr/share/gnome-session,$pkgdir/usr/share/xsessions,$pkgdir/usr/lib}
}

