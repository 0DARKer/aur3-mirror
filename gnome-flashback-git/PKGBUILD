# Maintainer: Charles Bos <charlesbos1 AT gmail>

pkgname=gnome-flashback-git
_gitname=gnome-flashback
pkgver=3.10.0.r18.g4b81aed
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="GNOME Flashback session (development version)"
url="https://git.gnome.org/browse/gnome-flashback"
# Note: Dependencies from gnome-panel onwards are flashback session dependencies and are not required for building the flashback module
depends=('glib2>=2.39.91' 'gnome-desktop>=3.8.0' 'gtk3>=3.10.0' 'libcanberra>=0.13' 'libpulse' 'gnome-panel' 'gnome-screensaver' 'gnome-session' 'gnome-settings-daemon-compat' 'gnome-themes-standard' 'metacity' 'nautilus' 'notification-daemon' 'polkit-gnome')
optdepends=('compiz: required for the GNOME Flashback (Compiz) session')
makedepends=('git' 'gnome-common' 'intltool')
provides=('gnome-flashback=3.10.0')
conflicts=('gnome-flashback')
install=$_gitname.install
source=(git://git.gnome.org/gnome-flashback
        session-components.patch)
sha256sums=('SKIP'
            '4f5e80b2aaade9b6a1ca5fa5f6334cadd61cb79ba296c0ff81a06002cfe07423')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "$srcdir/$_gitname"

  # Add extra components to the Flashback sessions
  patch -Np1 -i "$srcdir/session-components.patch"
}

build() {
  cd "$srcdir/$_gitname"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/"$_gitname"

  make
}

package() {
  cd "$srcdir/$_gitname"

  make DESTDIR="$pkgdir" install
}

