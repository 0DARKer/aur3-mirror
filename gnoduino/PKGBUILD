# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gnoduino
pkgver=0.4.0
pkgrel=1
pkgdesc="An implementation of well-known Arduino IDE for GNOME"
arch=(any)
url="http://gnome.eu.org/index.php/Gnoduino"
license=('GPL')
depends=('avr-libc' 'avrdude' 'binutils-avr' 'gcc-avr' 'pyxdg'
         'pygtksourceview2' 'python2-pyserial'
         'python2-gconf' 'desktop-file-utils')
makedepends=('python2-distribute')
install="$pkgname.install"
source=("http://gnome.eu.org/$pkgname-$pkgver.tar.gz")
md5sums=('33d16c8ec8761fdacd06b6e23210decb')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # do not install gconf schema
  sed -i "/installSchema.*)$/d" setup.py

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir/"

  # gconf schema
  install -Dm644 data/$pkgname.schemas \
    "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas"

  # python2 fix
  sed -i "s|bin/python$|&2|" `find "$pkgdir" -name "*.py"`
}

# vim:set ts=2 sw=2 et:
