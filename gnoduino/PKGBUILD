# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gnoduino
pkgver=0.3.2
pkgrel=1
pkgdesc="An implementation of well-known Arduino IDE for GNOME."
arch=(any)
url="http://gnome.eu.org/index.php/Gnoduino"
license=('GPL')
depends=('avr-libc' 'avrdude' 'binutils-avr' 'gcc-avr'
         'python2-gnomevfs' # <-- comment to get rid of gnomevfs
#         'pyxdg'            # <-- uncomment to use xdg instead
         'pygtksourceview2' 'python2-pyserial'
         'python2-gconf' 'desktop-file-utils')
makedepends=('python2-distribute')
install="$pkgname.install"
source=("http://gnome.eu.org/$pkgname-$pkgver.tar.gz")
md5sums=('b8e593b367d1de9c1d83f02fb0a6c427')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ## If you wanna get rid of the big gnome-vfs dependency,
  #  uncomment next sed lines to replace any gnomevfs call
  #  with the light xdg.
#  sed -e "s/gnomevfs.get_mime_type_for_data/Mime.get_type_by_data/g" \
#      -e "s/import gnomevfs/from xdg import Mime/" \
#      -i src/srcview.py

  # do not install gconf schema
  sed -i "/installSchema$/d" setup.py

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir/"

  # gconf schema
  install -Dm644 data/$pkgname.schemas \
    "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas"

  # python2 fix
  sed -i "s|bin/python$|&2|" `find "$pkgdir" -name "*.py"`
}

# vim:set ts=2 sw=2 et:
