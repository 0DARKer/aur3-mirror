# Maintainer: Paul Cox <pauldcox@gmail.com>
# Contributor: Matthew Bauer (previous maintainer)

pkgname=granola
pkgver=5.0.6
pkgrel=1
pkgdesc="MiserWare intelligent power management for PCs"
arch=('i686' 'x86_64')
  if [ $CARCH = x86_64 ]; then
    _arch=$CARCH
  else
    _arch=i386
  fi
url="http://grano.la/"
license=('custom')
depends=('bash' 'gcc-libs')
optdepends=('python2: MiserWare customer support via the mw-feedback script'
            'granola-connect: daemon to upload energy stats'
            'granola-gui: Granola graphical interface')
backup=('etc/granola.conf')
options=(!strip)
install=$pkgname.install

source=("https://download.miserware.com/linux/tar/$_arch/$pkgname-$pkgver-$_arch.tar.gz"
	'0001-change-to-python2-in-scripts.patch'
        'granola.confd'
        'granola.rcd.sh')

[ $_arch = x86_64 ] && sha1sums=('46936d839ee0d14c89d9fd16a20b54a63179f9b6')
[ $_arch = i386 ] && sha1sums=('d976c1d400a83393c68e0ed1c9ee256501f754fa')
sha1sums+=('914152579a7558e6b4d29820105233490c0ea8e4'
           '0d360a9de96b688a7a2bdfdba45398f25dd28724'
           '1b956ab230f81b09fca92dd1be0120d81e00f7f6')

build() {
  cd "$srcdir/$pkgname-$pkgver-$_arch"

  # change python script headers to python2
  patch -p1 -i ../0001-change-to-python2-in-scripts.patch
}

package() {
  cd "$srcdir/$pkgname-$pkgver-$_arch"

  cp -r usr var "$pkgdir"
  chmod 775 "$pkgdir/var/lib/miserware"
  chmod 664 "$pkgdir/var/lib/miserware"/*
  install -Dm644 etc/granola.conf "$pkgdir/etc/granola.conf"

  # rc.d files
  install -Dm644 "$srcdir/granola.confd" "$pkgdir/etc/conf.d/granola.conf"
  install -Dm755 "$srcdir/granola.rcd.sh" "$pkgdir/etc/rc.d/granola"

  # put license in proper directory
  rm -fr "$pkgdir/usr/share/doc"
  install -Dm644 usr/share/doc/$pkgname-$pkgver/LICENSE.txt \
          "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
