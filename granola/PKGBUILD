# Maintainer: Limao Luo <luolimao@gmail.com>
# Contributor: Paul Cox <pauldcox@gmail.com>
# Contributor: Matthew Bauer

pkgname=granola
pkgver=5.0.11
pkgrel=1
pkgdesc="MiserWare intelligent power management for PCs"
arch=(i686 x86_64)
if [[ $CARCH = "x86_64" ]]; then
    _arch=$CARCH
else
    _arch=i386
fi
url=http://grano.la/
license=(custom)
depends=(bash gcc-libs)
optdepends=('python2: MiserWare customer support via the mw-feedback script'
    'granola-connect: daemon to upload energy stats'
    'granola-gui: Granola graphical interface')
backup=(etc/$pkgname.conf)
options=(!strip)
install=$pkgname.install
source=(https://download.miserware.com/linux/tar/$_arch/$pkgname-$pkgver-$_arch.tar.gz
    $pkgname.confd
    $pkgname.rcd.sh)
if [[ $CARCH = "x86_64" ]]; then
    sha256sums=('8144a196f9de3e4f996d9ec4b0d4f9ac1a09d1de2464151c454da80b4fd7fbf0')
    sha512sums=('09908ac4bf06b231527c9a7ae991e7bbc13006ee45ffda12756fd0761ab1c4b97017e6ece2c7d4d14372123c8a0328e1462e92068981376239742eba4c90632a')
else
    sha256sums=('4a4b2ccea5ce232d59e0a3f756d51f6e8777eb5ae42993fb05d7d98ec76d9846')
    sha512sums=('846f5cfe70cd32a50cb3cb24b30a4109ba32dae8e1f5f0bbf9bc88f7273120b2fe1f4867b3800f0373beac654c7e3e15c00ee4e234d11862e39053ad9efec250')
fi

sha256sums+=('4e1bd0f5c528562e0c1b58c52d1fdf6272d535465a865d847517348aa4123465'
    '32f1960751e039078920761accd3b65eadd633dac31f8c0873d24849c2588914')
sha512sums+=('9f8da2294120e048e8630369e1ea0e55e53b4c4bc393b16e938f0b2d522eed627b0a3a510b5d9b70b302e0761d538bda76324ea13b78261905663abb8bce4b5d'
    '3be6451b99dbdfe910cb2c1e0fff41da11eee5143295d2fc4e234bd0f8c0fb13f2f9209a39924feffd795b4c4aaeb0f83d741821b2711837206fa301765f89ad')

build() {
    sed -i "s:^#!/usr/bin/env python$:&2:" "$srcdir"/$pkgname-$pkgver-$_arch/usr/bin/mw-feedback
}

package() {
    cd "$srcdir"/$pkgname-$pkgver-$_arch/

    cp -r usr var "$pkgdir"
    chmod 755 "$pkgdir"/var/lib/miserware
    chmod 644 "$pkgdir"/var/lib/miserware/*
    install -Dm644 etc/$pkgname.conf "$pkgdir"/etc/$pkgname.conf

    install -Dm644 ../$pkgname.confd "$pkgdir"/etc/conf.d/$pkgname.conf
    install -Dm755 ../$pkgname.rcd.sh "$pkgdir"/etc/rc.d/$pkgname

    rm -rf "$pkgdir"/usr/share/doc/
    install -Dm644 usr/share/doc/$pkgname-$pkgver/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
