# Maintainer: Sara <sara at archlinux dot us>

pkgname=vim-tex_nine
pkgver=1.1.6
_scriptid=17064
pkgrel=4
pkgdesc="a simple and snappy LaTeX plugin for Vim"
arch=('any')
url="http://www.vim.org/scripts/script.php?script_id=3508"
license=('GPL')
depends=('python2' 'vim')
optdepends=('texlive-bin: compile TeX files')
groups=('vim-plugins')
install='vimdoc.install'
source=("${pkgname}::http://www.vim.org/scripts/download_script.php?src_id=${_scriptid}")
md5sums=('481f16ea9b16f65e42726433891680b9')

package(){
  install -d "${pkgdir}"/usr/share/vim/vimfiles/{autoload,compiler,doc,ftplugin,indent,plugin}
  install -d "${pkgdir}"/usr/share/vim/vimfiles/ftplugin/TeX_9/{dictionaries,pymodules,skeleton,snippets}

  cd "${srcdir}/tex_nine-${pkgver}/ftplugin/TeX_9"

  # python2 fix
  sed \
    -e 's_#!/usr/bin/python_#!/usr/bin/python2_' \
    -i pymodules/evince_dbus.py

  install -Dm644 *.vim "${pkgdir}"/usr/share/vim/vimfiles/ftplugin/TeX_9/

  for i in dictionaries pymodules skeleton snippets
  do
    install -Dm644 "${i}"/* "${pkgdir}"/usr/share/vim/vimfiles/ftplugin/TeX_9/"${i}"/
  done

  cd "${srcdir}/tex_nine-${pkgver}"

  install -Dm644 ftplugin/*.vim "${pkgdir}"/usr/share/vim/vimfiles/ftplugin/

  for i in autoload compiler doc indent plugin
  do
    install -Dm644 "${i}"/* "${pkgdir}"/usr/share/vim/vimfiles/"${i}"/
  done

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # updating vim help tags post install 
  rm -f "${pkgdir}"/usr/share/vim/vimfiles/doc/tags
}

# vim:set ts=2 sw=2 et:
