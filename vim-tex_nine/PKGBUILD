pkgname=vim-tex_nine
pkgver=1.3.7
_scriptid=20621
pkgrel=1
pkgdesc="A semi-automatic LaTeX ftplugin with lots of firepower"
arch=('any')
url="http://www.vim.org/scripts/script.php?script_id=3508"
license=('GPL')
depends=('python2' 'vim')
optdepends=('texlive-bin: compile TeX files')
groups=('vim-plugins')
install=vimdoc.install
source=(${pkgname}.tar.gz::"http://www.vim.org/scripts/download_script.php?src_id=${_scriptid}")
sha1sums=('efe512fa4323b66afe4a116c738ad273e906d6eb')

package(){
	installpath="${pkgdir}/usr/share/vim/vimfiles"

	install -d $installpath/{autoload,doc,ftplugin,indent}
	install -d $installpath/ftplugin/tex_nine/{skeleton,snippets}

	cd "${srcdir}/tex_nine-${pkgver}/ftplugin/tex_nine"

	sed \
	  -e 's_#!/usr/bin/python_#!/usr/bin/python2_' \
	  -i evince_dbus.py

	install -Dm644 *.vim *.py *.txt $installpath/ftplugin/tex_nine/

	for i in skeleton snippets
	do
		install -Dm644 "${i}"/* $installpath/ftplugin/tex_nine/"${i}"/
	done

	cd "${srcdir}/tex_nine-${pkgver}"

	install -Dm644 ftplugin/*.vim $installpath/ftplugin/

	for i in autoload doc indent
	do
		install -Dm644 "${i}"/* $installpath/"${i}"/
	done

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
