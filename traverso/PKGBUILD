# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Philipp Überbacher <murks at lavabit dot com>

pkgname=traverso
pkgver=0.49.2
pkgrel=3
pkgdesc="Digital Audio Workstation with an innovative User Interface"
arch=('i686' 'x86_64')
url="http://traverso-daw.org/"
license=('GPL')
depends=('qt' 'jack' 'fftw' 'wavpack' 'redland'
         'lame' 'libmad' 'shared-mime-info')
makedepends=('cmake')
options=('!libtool')
install="$pkgname.install"
source=("http://traverso-daw.org/download/releases/current/$pkgname-$pkgver.tar.gz")
md5sums=('1acc46e2fa17e9a77f069b00307478a0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # raptor2 fixes
  sed -i 's/raptor/&2/' src/$pkgname/CMakeLists.txt
  export CFLAGS+=" -I/usr/include/raptor2"
  export CXXFLAGS+=" -I/usr/include/raptor2"

  # gcc fix
  sed -i '24a#include <unistd.h>' src/core/FileHelpers.cpp \
                                  src/traverso/Main.cpp

  [[ -d bld ]] || mkdir bld && cd bld

  cmake .. -DCMAKE_INSTALL_PREFIX="/usr" \
           -DWANT_MP3_ENCODE=ON \
           -DUSE_SYSTEM_SLV2_LIBRARY=OFF

  # DSO link dirty fix (libdl)
  sed -i 's/jack/& -ldl/' `find src/traverso -name link.txt`

  make
}

package(){
  cd "$srcdir/$pkgname-$pkgver/bld"
  make DESTDIR="$pkgdir/" install

  cd ../resources
  # desktop file
  install -d "$pkgdir/usr/share/applications"
  sed '/Path/d' $pkgname.desktop > \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icons
  install -d "$pkgdir/usr/share/icons/hicolor"
  cp -a freedesktop/icons/* "$pkgdir/usr/share/icons/hicolor"

  # mime
  install -Dm644 x-$pkgname.xml \
    "$pkgdir/usr/share/mime/packages/x-$pkgname.xml"

  # doc
  install -Dm644 help.text \
    "$pkgdir/usr/share/doc/$pkgname/help.txt"
}
