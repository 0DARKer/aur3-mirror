# Maintainer: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-kde-common-internals-git
_gitname="ktp-common-internals"
pkgver=0.6.1.20.g7d5aab3
pkgrel=1
epoch=1
pkgdesc="Common components for Kde-Telepathy."
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/network/telepathy/ktp-common-internals"
license=('LGPL')
depends=('kdebase-runtime' 'telepathy-logger-qt-git')
makedepends=('git' 'cmake' 'automoc4')
install="$pkgname.install"
conflicts=('telepathy-kde-common-internals')
source=("git://anongit.kde.org/ktp-common-internals.git")
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g;s|v||'
}

build() {
  cd "$srcdir"

  msg "Starting build..."

  rm -rf "$srcdir/build" && mkdir "$srcdir/build"
  cd "$srcdir/build"
  
  cmake ../$_gitname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -DQT_MOC_EXECUTABLE=/usr/bin/moc-qt4 \
    -DQT_RCC_EXECUTABLE=/usr/bin/rcc-qt4 \
    -DQT_UIC_EXECUTABLE=/usr/bin/uic-qt4 \
    -DCMAKE_POSITION_INDEPENDENT_CODE=on
   
   make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
