# Maintainer: Jack L. Frost <fbt@fleshless.org>

pkgname='loksh'
pkgver='5.6'
pkgrel=6
pkgdesc="A Linux port of OpenBSD's ksh targeting musl"
arch=( 'i686' 'x86_64' )
url='https://github.com/dimkr/loksh'
license=( 'custom' )
source=(
	"https://github.com/dimkr/loksh/archive/${pkgver}.tar.gz"
	'bin_name.patch'
)

#_flag_static=1
if (( _flag_static )); then
	makedepends+=( 'musl' )
	_makeopts=( 'CC=musl-gcc' 'LDFLAGS=-static' )
else
	depends=( 'libbsd' )
	_makeopts=( 'LDFLAGS=-lbsd' )
fi

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p0 < ../bin_name.patch
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	make ${_makeopts[*]}
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" BIN_NAME='loksh' install
}

sha1sums=('d93804e174725879d9f8b34760a788ba7d38c39f'
          'dedfa504eee7afc9f4f5c31edd5524ed3ae4ac2a')
