pkgname=tano
pkgver=1.0_final
pkgrel=1
pkgdesc="An open-source cross-platform IP TV player based on Qt and libvlc"
arch=('i686' 'x86_64')
url="http://projects.tano.si/en/player"
license=('GPL')
depends=('vlc>=1.1' 'qt>=4.6' 'libvlc-qt>=0.6.1')
makedepends=('cmake>=2.6' 'dbus>=1.4')
_pkgver=${pkgver/_*/}
source=("http://sourceforge.net/projects/$pkgname/files/Tano%20Player/${pkgver/_/-}/${pkgname}_${_pkgver}_src.tar.gz")
md5sums=('719f5c7b56a139dc4ac34fa6a29f8795')

_optimal_make_jobs() {
	if [ -r /proc/cpuinfo ]; then
		local core_count=$(grep -Fc processor /proc/cpuinfo)
	else
		local core_count=0
	fi

	if [ $core_count -gt 1 ]; then
		echo -n $[$core_count-1]
	else
		echo -n 1
	fi
}

build() {
	cd "$srcdir/$pkgname-${_pkgver}"

	msg 'Running cmake...'
	cmake . -DCMAKE_INSTALL_PREFIX=/usr

	msg 'Running make...'
	make -j$(_optimal_make_jobs)
}

package() {
	cd "$srcdir/$pkgname-${_pkgver}"
	make DESTDIR="$pkgdir" install
	install -Dm 644 tano-editor.desktop "$pkgdir/usr/share/applications/tano-editor.desktop"
	install -Dm 644 tano.desktop "$pkgdir/usr/share/applications/tano.desktop"
}
