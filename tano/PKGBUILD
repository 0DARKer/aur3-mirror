pkgname=tano
pkgver=1.0_beta1
pkgrel=1
pkgdesc="An open-source cross-platform IP TV player based on Qt and libvlc"
arch=('i686' 'x86_64')
url="http://projects.tano.si/en/player"
license=('GPL')
depends=('vlc>=1.1' 'qt>=4.6' 'libvlc-qt>=0.6.0')
makedepends=('cmake>=2.6' 'dbus>=1.4')
_pkgver=${pkgver/_/-}
source=("http://sourceforge.net/projects/$pkgname/files/Tano%20Player/$_pkgver/${pkgname}_${_pkgver}_src.tar.gz"
	'unistd.patch')
md5sums=('7ebc65925d8fded9461e541671f5ca5c' '3e075ef7b959bd872b0f3400f206dadf')
_gitcommit='~git20120503~'

_optimal_make_jobs() {
	if [ -r /proc/cpuinfo ]; then
		local core_count=$(grep -Fc processor /proc/cpuinfo)
	else
		local core_count=0
	fi

	if [ $core_count -gt 1 ]; then
		echo -n $[$core_count-1]
	else
		echo -n 1
	fi
}

build() {
	cd "$srcdir/$pkgname-${pkgver/_/$_gitcommit}"

	msg 'Applying patch...'
	patch -Np0 -i "$srcdir/unistd.patch"

	msg 'Running cmake...'
	cmake . -DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_EXE_LINKER_FLAGS='-lX11'

	msg 'Running make...'
	make -j$(_optimal_make_jobs)
}

package() {
	cd "$srcdir/$pkgname-${pkgver/_/$_gitcommit}"
	make DESTDIR="$pkgdir" install
	install -Dm 644 tano-editor.desktop "$pkgdir/usr/share/applications/tano-editor.desktop"
	install -Dm 644 tano.desktop "$pkgdir/usr/share/applications/tano.desktop"
}
