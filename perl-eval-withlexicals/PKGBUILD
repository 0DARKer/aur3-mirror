# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Richard Shaw <richs@archlinux.us>

_perlmod=Eval-WithLexicals
_modnamespace=Eval
pkgname=perl-eval-withlexicals
pkgver=1.003000
pkgrel=1
pkgdesc="Eval::WithLexicals - Pure Perl eval with persistent lexical variables"
arch=('any')
url="https://metacpan.org/pod/Eval::WithLexicals"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl-moo>=0.009006')
checkdepends=('perl-strictures')
source=("http://search.cpan.org/CPAN/authors/id/H/HA/HAARG/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('26b0df50b86d685f61ebac33cede5f7722b6ff0620cc948868270f4db8e9320965c2257476179d37324905618a4a551f9f15169300e699b29eb11bf32a828eba')

build() {
	cd "${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}"
}
