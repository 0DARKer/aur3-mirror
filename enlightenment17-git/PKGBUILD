# Maintainer: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=enlightenment17-git
_pkgname=enlightenment
pkgver=0.17.99.16245
pkgrel=1
pkgdesc="Enlightenment window manager (formerly e17) - Development version"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('elementary-git' 'xcb-util-keysyms' 'hicolor-icon-theme'
         'desktop-file-utils' 'udisks' 'ttf-font')
makedepends=('git')
optdepends=('connman: network module')
provides=('e' 'e-svn' 'enlightenment17==$pkgver' 'enlightenment17-svn' 'notification-daemon')
conflicts=('enlightenment17' 'enlightenment17-svn')
backup=('etc/enlightenment/sysactions.conf'
        'etc/xdg/menus/enlightenment.menu')
options=('!libtool' 'debug')
install=enlightenment17.install
source=("git://git.enlightenment.org/core/$_pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"

  for i in v_maj v_min v_mic; do
    local _$i=$(grep -m 1 $i configure.ac | sed 's/m4//' | grep -o "[[:digit:]]*")
  done

  echo $_v_maj.$_v_min.$_v_mic.$(git rev-list --count HEAD)
}

build() {
  cd "$srcdir/$_pkgname"

  ./autogen.sh \
	--prefix=/usr \
	--sysconfdir=/etc \
	--disable-azy

  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 AUTHORS "$pkgdir/usr/share/licenses/$pkgname/AUTHORS"
}
