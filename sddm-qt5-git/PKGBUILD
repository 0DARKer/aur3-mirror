# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_pkgname=sddm-qt5
pkgname=$_pkgname-git
pkgver=513.c270144
pkgrel=1
pkgdesc="QML based X11 display manager"
arch=('i686' 'x86_64')
url="https://github.com/sddm/sddm"
license=('GPL')
depends=('qt5-declarative' 'upower')
makedepends=('git' 'cmake' 'qt5-tools')
provides=($_pkgname)
conflicts=($_pkgname)
backup=("etc/sddm.conf")
source=("git://github.com/${_pkgname%-qt5}/${_pkgname%-qt5}.git")
sha256sums=('SKIP')
_gitname=${_pkgname%-qt5}


pkgver() {
	cd "$srcdir/$_gitname"
	echo "$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
	cd "$srcdir/$_gitname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr  -DUSE_QT5=true ..
	make
}

package() {
	cd "$srcdir/$_gitname"
	cd build
	make DESTDIR="$pkgdir" install
}
