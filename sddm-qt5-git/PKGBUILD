# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Abdurrahman AVCI <abdurrahmanavci@gmail.com>

_gitname=sddm
_pkgname=$_gitname-qt5
pkgname=$_pkgname-git
pkgver=541.aac8643
pkgrel=1
pkgdesc="QML based X11 display manager"
arch=("i686" "x86_64")
url="https://github.com/davidedmunson/sddm"
license=("GPL")
depends=("qt5-declarative" "upower")
makedepends=("git" "cmake" "qt5-tools")
provides=("$_gitname" "$_pkgname")
conflicts=("$_gitname" "$_pkgname")
backup=("etc/sddm.conf")
source=("git://github.com/$_gitname/$_gitname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_gitname"
	echo "$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
	cd "$srcdir/$_gitname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DUSE_QT5=true ..
	make
}

package() {
	cd "$srcdir/$_gitname"
	cd build
	make DESTDIR="$pkgdir" install
}
