# Maintainer: Abdurrahman AVCI <abdurrahmanavci@gmail.com>

pkgname=sddm-qt5-git
pkgver=02d3a61
pkgrel=1
pkgdesc="QML based X11 display manager"
arch=('i686' 'x86_64')
backup=('etc/sddm.conf')
url="https://github.com/sddm/sddm"
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'git' 'qt5-tools')
provides=('sddm')
conflicts=('sddm')
source=("git://github.com/sddm/sddm.git")
sha256sums=('SKIP')
_gitname="sddm"

pkgver() {
	cd "$srcdir/$_gitname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_gitname"
	mkdir -p build
	cd build
	cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DUSE_QT5=true
	make
}

package() {
	cd "$srcdir/$_gitname"
	cd build
	make DESTDIR="$pkgdir" install
}
