# Maintainer: Abdurrahman AVCI <abdurrahmanavci@gmail.com>

pkgname=sddm-qt5-git
pkgver=r523.c586890
pkgrel=1
pkgdesc="QML based X11 display manager"
arch=('i686' 'x86_64')
url="https://github.com/sddm/sddm"
license=('GPL')
depends=('qt5-declarative')
makedepends=('cmake' 'git' 'qt5-tools')
provides=('sddm')
conflicts=('sddm')
backup=('etc/sddm.conf')
source=("git://github.com/sddm/sddm.git")
md5sums=('SKIP')
_gitname="sddm"

pkgver() {
	cd $_gitname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../$_gitname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT5=TRUE
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
