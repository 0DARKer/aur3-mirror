# Maintainer: Carsten Feuls (DL1CAF) 'Der Techniker' <dl1caf@vfdb.org>
# Contributer: Amateurfunk Station der Hochschule Niederrhein (DF0FN) <df0fn@hs-niederrhein.de>
pkgname=('svxlink')
pkgver="14.08"
pkgrel=5
arch=('i686' 'x86_64' 'armv5h' 'armv6h' 'armv7h')
url="http://sourceforge.net/projects/svxlink/"
license=('GPL')
source=("https://github.com/sm0svx/${pkgname}/archive/${pkgver}.tar.gz"
	"etc_default_svxlink"
	"svxlink.service"
	"remotetrx.service")
sha256sums=('c6007f6e71b8df91c6674ed3ce5805437e3c56c40c00a82a937d9658b0819124'
            'ed577031496f454a1e19b091bac8985bcf49e7dc56675afa293715590e9e4a29'
            '9d9799feddbc9aa8a51b3bac1adf9963c19441a10708a96e68d0ece8856edb5a'
            '49afeb47b0f2549c08b3904168cf409b46be232610a508e3af1e3e695c12a8d9')
depends=('alsa-utils' 'alsa-lib' 'libsigc++' 'gsm' 'libgcrypt' 'popt' 'tcl' 'speex' 'opus')
makedepends=('cmake')
pkgdesc="Echolink Software to provide Ham Radio Voip Operations"
optdepends=('svxlink-sounds-en_us-heather-8k: provides speech sound files in medium quality'
            'svxlink-sounds-en_us-heather-16k: provides speech sound files in high quality')
backup=('etc/svxlink/svxlink.conf'
	'etc/svxlink/TclVoiceMail.conf'
	'etc/svxlink/remotetrx.conf'
	'etc/svxlink/svxlink.d/ModuleDtmfRepeater.conf'
	'etc/svxlink/svxlink.d/ModuleEchoLink.conf'
	'etc/svxlink/svxlink.d/ModuleHelp.conf'
	'etc/svxlink/svxlink.d/ModuleMetarInfo.conf'
	'etc/svxlink/svxlink.d/ModuleParrot.conf'
	'etc/svxlink/svxlink.d/ModulePropagationMonitor.conf'
	'etc/svxlink/svxlink.d/ModuleSelCallEnc.conf'
	'etc/svxlink/svxlink.d/ModuleTclVoiceMail.conf'
	'etc/default/svxlink')
install=svxlink.install

build(){
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  mkdir -p build 
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONF_INSTALL_DIR=/etc -DLOCAL_STATE_DIR=/var -DUSE_QT=NO ..
  make
  make doc
}

package(){
  make -C ${srcdir}/${pkgname}-${pkgver}/src/build DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}/etc/default"
  mkdir -p "${pkgdir}/usr/lib/systemd/system"
  cp ${srcdir}/etc_default_svxlink ${pkgdir}/etc/default/svxlink
  # svxlink systemd service file
  cp ${srcdir}/svxlink.service ${pkgdir}/usr/lib/systemd/system
  cp ${srcdir}/remotetrx.service ${pkgdir}/usr/lib/systemd/system
}
