# Maintainer: Alessio Sergi <asergi at archlinux dot us>

pkgname=kdeplasma-applets-ihatethecashew
pkgver=0.4
pkgrel=2
pkgdesc="A plasmoid that remove the cashew"
arch=('i686' 'x86_64')
url="http://kde-look.org/content/show.php/I+HATE+the+Cashew?content=91009"
license=('GPL2')
depends=('kdelibs')
makedepends=('automoc4' 'cmake')
source=("http://www.kde-look.org/CONTENT/content-files/91009-iHateTheCashew-4.4.tbz"
        "ihtc-fix.patch")
sha1sums=('b91b5e4ab3bd1c8b0d0185c3e852edaf89f06667'
          '23b3681ca0a2b0d027f0beabecc0505afb4d49af')

build() {
  # KDE 4.6+ fix
  patch -Np0 -d iHateTheCashew -i "$srcdir"/ihtc-fix.patch

  rm -rf build
  mkdir build && cd build

  cmake ../iHateTheCashew \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
