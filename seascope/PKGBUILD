# Maintainer: Alain Kalker <a.c.kalker at gmail dot com>
pkgname=seascope
pkgver=0.7
pkgrel=2
pkgdesc="A multi-platform multi-language source code browsing tool"
arch=('any')
url="http://code.google.com/p/seascope/"
license=('BSD')
depends=('python2-qscintilla' 'ctags')
makedepends=('gendesk')
optdepends=('idutils: backend for C, lex, yacc, limited support for C++, Java'
            'cscope: backend for all languages supported by ctags'
            'global: backend for C, C++, Yacc, Java, PHP'
            'graphviz: generate class graphs')
provides=('seascope')
conflicts=('seascope')
options=(!emptydirs)
source=(http://seascope.googlecode.com/files/seascope-0.7.tar.gz
        a40508fcb472-fix.patch
        c7234f9f71f9-fix_typos.patch)
md5sums=('7d9c18b251c033dce95b94d19f87a886'
         'b331f94265a70b1f84d530ec96f67882'
         'e45c31e2a23006d36972865d66ebde97')
_name=('Seascope')
_exec=('Seascope')

build() {
  cd "$srcdir/"
  ln -sf "$pkgname-$pkgver/src/icons/seascope.svg" "$pkgname.svg"
  gendesk -n
  cd "$pkgname-$pkgver"

  # Fix setup.py to make the package buildable, from upstream Mercurial repo.
  patch -p1 -i "$srcdir/a40508fcb472-fix.patch"
  patch -p1 -i "$srcdir/c7234f9f71f9-fix_typos.patch"

  # Use python2
  find -type f -exec sed -i -e '/^#!/s/python$/python2/' {} \;
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  chmod +x "$pkgdir/usr/lib/python2.7/site-packages/Seascope/Seascope.py"
  install -m755 -d "$pkgdir/usr/bin"
  ln -s /usr/lib/python2.7/site-packages/Seascope/Seascope.py "$pkgdir/usr/bin/Seascope"
  install -D -m644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -D -m644 "$srcdir/$pkgname.svg" "$pkgdir/usr/share/pixmaps/$pkgname.svg"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
