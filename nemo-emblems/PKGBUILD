# Maintainer: twa022 <twa022 at gmail dot com>

pkgname=nemo-emblems
pkgver=2.4.3
_mintrel=rebecca
pkgrel=1
pkgdesc="Nemo file emblems extension"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo-extensions"
license=('GPL')
depends=('nemo-python')
makedepends=('python2-distutils-extra')
options=('!libtool' '!emptydirs')
install="$pkgname".install

source=("${pkgname}-${pkgver}.tar.gz::http://packages.linuxmint.com/pool/main/${pkgname:0:1}/${pkgname}/${pkgname}_${pkgver}+${_mintrel}.tar.gz")
sha256sums=('ba9fd27ee4535f07df8dd6fe246c5e75819308af1d845d8573efed362853e990')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}+${_mintrel}
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/env python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
            $(find . -name '*.py')
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}+${_mintrel}
  python2 ./setup.py install --root="${pkgdir}"
}
