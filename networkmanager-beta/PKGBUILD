# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: Petr Nov√°k <n_petr at seznam dot cz>
# Based on the networkmanager package trunk

_pkgbase=networkmanager
pkgname=networkmanager-beta
pkgver=0.8.996
pkgrel=1
pkgdesc="Network Management daemon"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('dbus-glib' 'iproute2' 'libnl' 'nss' 'polkit' 'udev' 'wireless_tools' 'wpa_supplicant' 'ppp' 'dhcpcd')
makedepends=('intltool' 'dhclient' 'iptables')
optdepends=('modemmanager: for modem management service'
            'dhclient: alternative DHCP/DHCPv6 client'
            'iptables: Connection sharing'
            'dnsmasq: Connection sharing'
            'bluez: Bluetooth support')
options=('!libtool')
backup=('etc/NetworkManager/NetworkManager.conf')
install=networkmanager.install
provides=("${_pkgbase}=${pkgver}")
conflicts=("${_pkgbase}")
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager/0.8/NetworkManager-${pkgver}.tar.bz2
        NetworkManager.conf disable_set_hostname.patch)
sha256sums=('3ccca66610a4896c5bc17e653dcde70320ed7c8278aa4419507e7221131d8550'
            '44b048804c7c0b8b3b0c29b8632b6ad613c397d0a1635ec918e10c0fbcdadf21'
            '1e4586991bc96ef004dc520c794047a336c54433e0bd4edc3879fb6e7ab0e553')

build() {
  cd NetworkManager-${pkgver}
  patch -Np1 -i ../disable_set_hostname.patch
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/networkmanager --with-crypto=nss \
    --with-distro=arch --with-dhclient=/usr/sbin/dhclient --with-dhcpcd=/sbin/dhcpcd --with-iptables=/usr/sbin/iptables \
    --disable-static --enable-more-warnings=no --disable-wimax
  make
}

package() {
  cd NetworkManager-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m644 ../NetworkManager.conf "${pkgdir}/etc/NetworkManager/"
}