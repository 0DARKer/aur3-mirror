# Maintainer : dobo <dobo90_at_gmail.com>
# Contributor: Paul N. Maxwell <msg.maxwel@gmail.com>

pkgname=transmission-remote-gui
pkgver=3.1
pkgrel=1
pkgdesc='Cross platform remote GUI for the Transmission daemon'
arch=(i686 x86_64)
url='http://code.google.com/p/transmisson-remote-gui/'
license=(GPL2)
depends=(gtk2 desktop-file-utils)
makedepends=(lazarus unzip)
conflicts=(transmission-remote-gui-svn)
install=transmission-remote-gui.install
source=(http://transmisson-remote-gui.googlecode.com/files/transgui-${pkgver}-src.zip
	transgui.desktop
	r607.patch
	transgui.res)
md5sums=(5ec88dee4319a45ab84c28ff0917c6c6
	cf55fd5a681d9b34a1f9c7d91b74d121
	8768a92028a5d0cd408bbc29aab6062d
	4bd6eb0b16261d3571a5b5a3dab974a1)

build() {
	cd ${srcdir}
	
	# applying patch from r607
	# http://code.google.com/p/transmisson-remote-gui/source/detail?format=side&path=/trunk/about.lfm&r=607&spec=svn607#publish
	patch -p0 < r607.patch
	cp transgui.res TransGUI/transgui.res # binary file from r607
	rm TransGUI/{transgui.lrs,transgui.manifest,transgui.rc} # deleting unnecessary files

	cd ${srcdir}/TransGUI
	lazbuild transgui.lpi
}

package() {
	cd ${srcdir}/TransGUI

	install -D -m 755 units/transgui ${pkgdir}/usr/bin/transgui
	install -D -m 644 readme.txt ${pkgdir}/usr/share/doc/${pkgname}/readme.txt
	install -D -m 644 history.txt ${pkgdir}/usr/share/doc/${pkgname}/history.txt
  	install -D -m 644 LICENSE.txt ${pkgdir}/usr/share/doc/${pkgname}/VERSION.txt
  	install -D -m 644 transgui.png ${pkgdir}/usr/share/pixmaps/transgui.png
	mkdir -p -m 755 ${pkgdir}/usr/share/transgui/lang
	install -D -m 644 lang/transgui.* ${pkgdir}/usr/share/transgui/lang/

  	install -D -m 644 ${srcdir}/transgui.desktop ${pkgdir}/usr/share/applications/transgui.desktop
}
