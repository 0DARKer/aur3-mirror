# Maintainer: Sam S. <smls75@gmail.com>

pkgname=aquaria-hib-git
pkgver=1.1.3+g256.8fbed64
_hibver=1.1.3
pkgrel=2
pkgdesc="A 2D underwater action-adventure game (Humble Bundle version + improved Open Source build of the executable)"
url='http://www.bit-blot.com/'
arch=('i686' 'x86_64')
license=('GPL' 'custom: "commercial"')
depends=('sdl' 'openal' 'libvorbis' 'libpng' 'freetype2' 'gcc-libs')
makedepends=('cmake')
conflicts=('aquaria' 'aquaria-hib-hg')
provides=('aquaria')
options=('!strip' '!upx')
PKGEXT='.pkg.tar.gz'
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')
CFLAGS="${CFLAGS} -O2"  # -O3 causes problems
CXXFLAGS="${CXXFLAGS} -O2"

source=('hib://aquaria-lnx-humble-bundle.mojo.run'
        'git://github.com/fgenesis/Aquaria_clean.git'
        "$pkgname.desktop")
md5sums=('8b24ddeeb553e028bbd501102f891cc2'
         'SKIP'
         '8677297d97cee364ca63652503ed6e62')

_gitname=Aquaria_clean

pkgver() {
  cd $_gitname
  # Automatically generate package version from git commit number
  echo $_hibver+g$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  cmake -DAQUARIA_INTERNAL_LUA=true -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd $srcdir
  _installdir="/opt/Aquaria"; _target="$pkgdir/$_installdir"

  # Install original game files (from Humble Bundle archive)
  mkdir -p $_target
  cp -r data/* $_target

  # Remove bundled binaries/libraries (use compiled or distro versions instead)
  rm "$_target"/{aquaria,xdg-open}
  rm "$_target"/{libSDL-1.2.so.0,libgcc_s.so.1,libopenal.so.1,libstdc++.so.6}

  # Install new/updated game files and binary (from git repo)
  cp -r $_gitname/{game_scripts,files}/* "$_target"
  cp -r $_gitname/aquaria "$_target"

  # Install documentation
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  mv -T data/docs "$pkgdir/usr/share/doc/$pkgname"

  # Install icon
  install -Dm644 data/aquaria.png "$pkgdir/usr/share/pixmaps/aquaria.png"

  # Install desktop entry
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Install launch script
  echo -e "#!/bin/sh\ncd $_installdir && exec ./aquaria \$*" > "launcher.sh"
  install -Dm755 "launcher.sh" "$pkgdir/usr/bin/aquaria"
}
