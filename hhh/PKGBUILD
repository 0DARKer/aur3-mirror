# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Dominika Solarz <dominikasolarz@gmail.com>

pkgname=hhh
pkgver=1.0
pkgrel=2
pkgdesc="Help Hannah's Horse: an arcade game based on Pacman and Fastfood"
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/hannah/"
license=('GPL' 'custom')
depends=('gcc-libs' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
source=("http://downloads.sourceforge.net/hannah/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('526b7fc05029f7b842d7ee89f61f8783'
         'cb9f7b5bb1cafae37cc09482217a21ff')

build() {
  cd "${srcdir}/${pkgname}-2008-03-29"

  # Fix installation paths and compile
  sed -i "s_local/games_share_" Makefile
  sed -i "s_local/bin_bin_" Makefile
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-2008-03-29"

  # Install game files
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  cp -r blue carrot freeze hannah images invert invis maps newanim \
    pink red sounds stop *.{ogg,png,ttf} "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Set files permissions
  find "${pkgdir}/usr/share/${pkgname}" -type d -exec chmod 755 '{}' \;
  find "${pkgdir}/usr/share/${pkgname}" -type f -exec chmod 644 '{}' \;

  # Install licenses and readme
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE Ray* Stans* "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"

  # Install the pixmap and .desktop file
  install -Dm644 hannah/default/hannah_down1.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
