# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Dominika Solarz <dominikasolarz@gmail.com>

pkgname=hhh
pkgver=1.0
pkgrel=1
pkgdesc="Help Hannah's Horse: an arcade game based on Pacman and Fastfood"
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/hannah/"
license=('GPL' 'custom')
depends=('gcc-libs' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
source=(http://downloads.sourceforge.net/hannah/$pkgname-$pkgver.tar.gz
        $pkgname.desktop)
md5sums=('526b7fc05029f7b842d7ee89f61f8783'
         '54505f9fc3c2263c548600a91639210f')

build() {
  cd "$srcdir"/$pkgname-2008-03-29

  # make
  sed \
    -e "s,local/games,share," \
    -e "s,local/bin,bin," \
    -i Makefile
  make || return 1

  # install binary
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname

  # install data
  install -m 755 -d "$pkgdir"/usr/share/$pkgname
  cp -rf \
    blue carrot freeze hannah images invert \
    invis maps newanim pink red sounds stop \
    *.{ogg,png,ttf} \
    "$pkgdir"/usr/share/$pkgname

  # install license files
  install -m755 -d "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 -t "$pkgdir"/usr/share/licenses/$pkgname \
    LICENSE {Ray_Dittmeier_Fonts,StansHandOFL}.txt

  # instll .desktop file
  install -Dm644 ../$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop

  #install icon
  install -Dm644 hannah/default/hannah_down1.png "$pkgdir"/usr/share/pixmaps/$pkgname.png

  # set permissions
  find "$pkgdir"/usr/share/$pkgname -type d -exec chmod 755 {} +
  find "$pkgdir"/usr/share/$pkgname -type f -exec chmod 644 {} +
}
