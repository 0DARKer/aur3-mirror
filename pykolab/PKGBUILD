# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

pkgname=pykolab
pkgver=0.7.0
pkgrel=1
pkgdesc="Kolab Groupware solution meta-package"
arch=(any)
license=('GPL3')
makedepends=('intltool')
depends=('python2' 'python2-icalendar' 'python2-nose' 'python2-ldap' 'python2-augeas' 'python2-cheetah' 'python2-sqlalchemy')
url="http://kolab.org"
install=pykolab.install
source=("http://mirror.kolabsys.com/pub/releases/${pkgname}-${pkgver}.tar.gz"{,.gpg}
	"http://mirror.kolabsys.com/pub/releases/kolab-schema-3.0.tar.gz"{,.gpg}
	"http://mirror.kolabsys.com/pub/releases/kolab-webadmin-3.2.1.tar.gz"{,.gpg})
optdepends=('cyrus-imapd: Only supported IMAP backend so far',
	    'postfix: Mail Transfer Agent',
	    '389-ds-base: Prefered LDAP server',
	    'openldap: Can be used as LDAP server',
	    'roundcubemail>=0.9: Default webclient used by Kolab',
	    'roundcubemail-plugins-kolab>=3.0: Roundcube plugin for Kolab integration'
	    'mariadb: Mysql server backend')
sha512sums=('500cdd28ab43e6136aac0b96068525596db7adcecb321a97828d39e3f7f31e4667d39a1b8f4e5132624bdb676fddf42b08d3a2f670fce816d6c471b668577e7e'
	    '41ae82df7a5c4e9fa08332bce1e17931ba746037eb22bb6bdf84c28831ce5910ffb8e5753fe4132b1c1ae404bf8fad2812238a04f63502e52061855a0b2df88f'
	    'e4d1dde34e9431450e5bc6e4b75564ff462275ee36c7f6f918ecef6ab271f16cba6a4a9814921f4c83227826e63d759b5d938aa158e217d90881275d223b51d3'
	    '15d1b5cec36e508f879c589813802a3f0af292e25093b38bdbe57e463e74c432b8db1c1cc584272084f7555bf620ba8c0bb04bdc29b789fce3f0dff7a7c1c7ce'
	    'a2d6c7324b5d34b34558fb152570686f351b316bd098b28fc2c92ec9448470cb90a9feab2df04a38b99787fc5776dc30a40fb8481c54354c38c447460359bd57'
	    'e1cda4cde9f36ff4fc44e36a1febfa0d50c2c21b7240650e2e9e6aeebfe29f9bc662a5e3ba6288be1a12c6f97951f8e489bd34be2004fac155742f08fcd1df0c')

prepare(){
  cd "$srcdir/${pkgname}-${pkgver}"
  find . -name "*.py" -exec sed -i "s|usr/bin/python|usr/bin/python2|" {} \;
  sed -i 's|sysconfig/kolabd|conf.d/kolabd.conf|' kolabd/kolabd.systemd
  sed -i 's|/usr/sbin|/usr/bin|' kolabd/kolabd.systemd
  sed -i '10iPermissionsStartOnly=true\nExecStartPre=-/usr/bin/mkdir /var/run/kolabd\nExecStartPre=/usr/bin/chown -R kolab:kolab /var/run/kolabd' kolabd/kolabd.systemd
  sed -i 's|sysconfig/kolab-saslauthd|conf.d/kolab-saslauthd.conf|' saslauthd/kolab-saslauthd.systemd
  sed -i 's|/usr/sbin|/usr/bin|' saslauthd/kolab-saslauthd.systemd
  sed -i 's|sysconfig/wallace|conf.d/wallace.conf|' wallace/wallace.systemd
  sed -i 's|/usr/sbin|/usr/bin|' wallace/wallace.systemd
  sed -i '318i\    elif os.path.isfile("/usr/bin/setup-ds.pl"):\n        setup_ds_admin = "/usr/bin/setup-ds.pl"' pykolab/setup/setup_ldap.py
  sed -i '385s/\/usr\/share\/doc/\/usr\/share\/kolab/g' pykolab/setup/setup_ldap.py
  sed -i '124s/\/usr\/share\/doc/\/usr\/share\/kolab/g' pykolab/setup/setup_mysql.py
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  autoreconf -vi
  PYTHON=python2 ./configure \
    	--prefix=/usr \
	--sbindir=/usr/bin \
	--sysconfdir=/etc \
  	--localstatedir=/var
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR=$pkgdir/ install

  install -D "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 "kolabd/kolabd.systemd" "${pkgdir}/usr/lib/systemd/system/kolabd.service"
  install -Dm644 "kolabd/kolabd.tmpfiles.d.conf" "${pkgdir}/usr/lib/tmpfiles.d/kolabd.conf"
  install -Dm644 "kolabd/kolabd.sysconfig" "${pkgdir}/etc/conf.d/kolabd.conf"

  install -Dm644 "saslauthd/kolab-saslauthd.systemd" "${pkgdir}/usr/lib/systemd/system/kolab-saslauthd.service"
  install -Dm644 "saslauthd/kolab-saslauthd.sysconfig" "${pkgdir}/etc/conf.d/kolab-saslauthd.conf"

  install -Dm644 "wallace/wallace.systemd" "${pkgdir}/usr/lib/systemd/system/wallace.service"
  install -Dm644 "wallace/wallace.tmpfiles.d.conf" "${pkgdir}/usr/lib/tmpfiles.d/wallace.conf"
  install -Dm644 "wallace/wallace.sysconfig" "${pkgdir}/etc/conf.d/wallace.conf"

  install -Dm644 doc/kolab.1 "$pkgdir/usr/share/doc/kolab/kolab.1"

  cp ${srcdir}/kolab-schema-3.0/* "${pkgdir}/usr/share/kolab/"
  cp ${srcdir}/kolab-webadmin-3.1.5/doc/kolab_wap-3.1.sql "${pkgdir}/usr/share/kolab/"
}
