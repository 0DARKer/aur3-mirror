# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>

# dep: kolabformat, irony (dav protocol access layer) missing?
# includes: kolab, kolab-ldap, kolab-mta, kolab-imap, kolab-webclient
# it's possible to use dovecot as imap server: http://kolab.org/blog/grote/2013/03/19/using-kolab-dovecot-imap-server

pkgname=pykolab
pkgver=0.6.8
pkgrel=6
pkgdesc="Kolab Groupware solution meta-package"
arch=(any)
license=('GPL3')
depends=('python2' 'python2-icalendar' 'python2-nose' 'python2-ldap' 'python2-augeas' 'python2-cheetah' 'python2-sqlalchemy')
url="http://kolab.org"
install=pykolab.install
source=("http://mirror.kolabsys.com/pub/releases/${pkgname}-${pkgver}.tar.gz"{,.gpg})
optdepends=('cyrus-imapd: Only supported IMAP backend so far',
	    'postfix: Mail Transfer Agent',
	    'openldap: Can be used as LDAP server',
	    'roundcubemail>=0.9: Default webclient used by Kolab',
	    'roundcubemail-plugins-kolab>=3.0: Roundcube plugin for Kolab integration'
	    'mariadb: Mysql server backend')
sha512sums=('b637a0ff7e090ed6fa58d6c7b9de6c1f3a6ef5d3acf24a12de4e370824d9331098cdffb3e09fc94c2c77bcd04c05ba7a89db72398e93b8f8585f164ae7a8e19e'
	    'a45e2148ecb60516e0885477c2c5577d0ed83eb3292d69492db56072f5b6bd4e7068aa0997c0ea6d3f64ca4b8e1ef5d092363b5ac06b9124d30f9ac36820944a')

prepare(){
  cd "$srcdir/${pkgname}-${pkgver}"
  find . -name "*.py" -exec sed -i "s|usr/bin/python|usr/bin/python2|" {} \;
  sed -i 's|sysconfig/kolabd|conf.d/kolabd.conf|' kolabd/kolabd.systemd
  sed -i 's|/usr/sbin|/usr/bin|' kolabd/kolabd.systemd
  sed -i '10iPermissionsStartOnly=true\nExecStartPre=-/usr/bin/mkdir /var/run/kolabd\nExecStartPre=/usr/bin/chown -R kolab:kolab /var/run/kolabd' kolabd/kolabd.systemd
  sed -i 's|sysconfig/kolab-saslauthd|conf.d/kolab-saslauthd.conf|' saslauthd/kolab-saslauthd.systemd
  sed -i 's|/usr/sbin|/usr/bin|' saslauthd/kolab-saslauthd.systemd
  sed -i 's|sysconfig/wallace|conf.d/wallace.conf|' wallace/wallace.systemd
  sed -i 's|/usr/sbin|/usr/bin|' wallace/wallace.systemd
  sed -i '318i\\telif os.path.isfile("/usr/bin/setup-ds.pl"):\n\t\tsetup_ds_admin = "/usr/bin/setup-ds.pl"' pykolab/setup/setup_ldap.py
}

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  autoreconf -vi
  PYTHON=python2 ./configure \
    	--prefix=/usr \
	--sbindir=/usr/bin \
	--sysconfdir=/etc \
  	--localstatedir=/var
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR=$pkgdir/ install

  install -D "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 "kolabd/kolabd.systemd" "${pkgdir}/usr/lib/systemd/system/kolabd.service"
  install -Dm644 "kolabd/kolabd.tmpfiles.d.conf" "${pkgdir}/usr/lib/tmpfiles.d/kolabd.conf"
  install -Dm644 "kolabd/kolabd.sysconfig" "${pkgdir}/etc/conf.d/kolabd.conf"

  install -Dm644 "saslauthd/kolab-saslauthd.systemd" "${pkgdir}/usr/lib/systemd/system/kolab-saslauthd.service"
  install -Dm644 "saslauthd/kolab-saslauthd.sysconfig" "${pkgdir}/etc/conf.d/kolab-saslauthd.conf"

  install -Dm644 "wallace/wallace.systemd" "${pkgdir}/usr/lib/systemd/system/wallace.service"
  install -Dm644 "wallace/wallace.tmpfiles.d.conf" "${pkgdir}/usr/lib/tmpfiles.d/wallace.conf"
  install -Dm644 "wallace/wallace.sysconfig" "${pkgdir}/etc/conf.d/wallace.conf"

  install -Dm644 doc/kolab.1 "$pkgdir/usr/share/doc/kolab/kolab.1"

  cp share/kolab2.schema $pkgdir/usr/share/kolab/
}
