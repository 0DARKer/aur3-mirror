# Maintainer: Javier Torres <javitonino [at] gmail [dot] com>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=pykolab
pkgver=0.7.8
pkgrel=2
pkgdesc='Kolab Python Utilities'
arch=('any')
license=('GPL3')
makedepends=('intltool')
depends=('python2' 'python2-nose' 'python2-ldap' 'python2-augeas' 'python2-cheetah' 'python2-sqlalchemy' 'python2-kolabformat' 'python2-icalendar' 'python2-mysql-connector')
url='http://kolab.org'
install=pykolab.install
backup=('etc/kolab/kolab.conf')
source=("http://mirror.kolabsys.com/pub/releases/${pkgname}-${pkgver}.tar.gz"
        "http://mirror.kolabsys.com/pub/releases/kolab-schema-3.0.tar.gz"
        "systemd-services.patch"
        "setup-script.patch"
        "templates.patch"
        "kolabd.tmpfiles")
optdepends=('cyrus-imapd-ldap: Only supported IMAP backend so far'
            'postfix: Mail Transfer Agent'
            'amavisd-new: Mail scanning'
            'perl-ldap: Amavis integration'
            'spamassassin: Spam detection'
            'clamav: Virus detection'
            '389-ds-base: Prefered LDAP server'
            'mariadb: Mysql server backend'
            'roundcubemail: Default webclient used by Kolab'
            'roundcubemail-plugins-kolab: Roundcube plugin for Kolab integration'
            'roundcubemail-skin-chameleon: Recommended skin for Roundcube'
            'chwala: File Manager'
            'irony: Cal/Card/GroupDAV support'
            'kolab-freebusy: Free/Busy API'
            'kolab-syncroton: ActiveSync support'
            'kolab-webadmin: Administration tool')

sha256sums=('0ded788d58cb3cb15afe42eca1537a1df9e7bd3f03f2dc3526e8fe928714ef70'
            'eeb2c2665aad21388f532c3bc83709785819db29e9f32cc9bd8f16abfbc20f3a'
            '136962c359b85a6c789c0fa0ab95b141ae5079dcc79aba4469da60b83e7dada2'
            'bdc5613f3dd6d8d9bf91e63979beb4641f9cf73c542184f31b3113224753d044'
            '8da77fef87854e976119b474ba514104995c001ac1998971f95d5578ee3e9501'
            '4bbb72695b20b7a4c1c0348a93c62f048f0eef9f3bc7135fa2720c62e98e4634')


prepare(){
    cd "$srcdir/${pkgname}-${pkgver}"
    find . -name "*.py" -exec sed -i "s|usr/bin/python|usr/bin/python2|" {} \;
    patch -p1 < "${srcdir}/systemd-services.patch"
    patch -p1 < "${srcdir}/setup-script.patch"
    patch -p1 < "${srcdir}/templates.patch"
}

build() {
    cd "$srcdir/${pkgname}-${pkgver}"
    autoreconf -vi
    PYTHON=python2 ./configure \
            --prefix=/usr \
            --sbindir=/usr/bin \
            --libexecdir=/usr/lib \
            --sysconfdir=/etc \
            --localstatedir=/var
    make
}

package() {
    cd "$srcdir/${pkgname}-${pkgver}"
    make DESTDIR=$pkgdir/ install

    install -D "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    install -Dm644 "kolabd/kolabd.systemd" "${pkgdir}/usr/lib/systemd/system/kolabd.service"
    install -Dm644 "kolabd/kolabd.tmpfiles.d.conf" "${pkgdir}/usr/lib/tmpfiles.d/kolabd.conf"
    install -Dm644 "kolabd/kolabd.sysconfig" "${pkgdir}/etc/conf.d/kolabd.conf"

    install -Dm644 "saslauthd/kolab-saslauthd.systemd" "${pkgdir}/usr/lib/systemd/system/kolab-saslauthd.service"
    install -Dm644 "saslauthd/kolab-saslauthd.sysconfig" "${pkgdir}/etc/conf.d/kolab-saslauthd.conf"

    install -Dm644 "wallace/wallace.systemd" "${pkgdir}/usr/lib/systemd/system/wallace.service"
    install -Dm644 "wallace/wallace.tmpfiles.d.conf" "${pkgdir}/usr/lib/tmpfiles.d/wallace.conf"
    install -Dm644 "wallace/wallace.sysconfig" "${pkgdir}/etc/conf.d/wallace.conf"

    install -Dm644 doc/kolab.1 "$pkgdir/usr/share/doc/kolab/kolab.1"
    
    install -Dm0644 "${srcdir}/kolabd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/kolabd.conf"

    cp ${srcdir}/kolab-schema-3.0/* "${pkgdir}/usr/share/doc/kolab/"
    
    rm ${pkgdir}/var/run -r
}
