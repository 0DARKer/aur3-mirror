# Maintainer: Lo√Øc Guitaut <flink at belfalas dot eu>

pkgname='terracoin-qt'
pkgver=0.1.2rc2
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.terracoin.org/"
makedepends=('boost' 'automoc4' 'qrencode' 'miniupnpc')
license=('MIT')
pkgdesc="P2P decentralized digital currency with fast confirmation and no central issuer (QT)"
depends=(boost-libs qt4 miniupnpc qrencode)
install=terracoin-qt.install
source=("https://github.com/terracoin/terracoin/archive/v$pkgver.tar.gz"
        "$pkgname.desktop")
sha256sums=('cea0bbbfb6d193ba52b0ca1a35e3e6b1b33930256933b13d4fdbb845bcf7e33b'
            '71f94ea8725bf59e3758b8d246762f7f5b61373c15370e13d54f7d174785456b')

build() {
  cd "$srcdir/terracoin-$pkgver"

  # and make qt gui
  qmake-qt4 USE_QRCODE=1 USE_UPNP=1
  make
}


package() {
  install -Dm644 terracoin-qt.desktop "$pkgdir"/usr/share/applications/terracoin.desktop
  cd "$srcdir/terracoin-$pkgver"
  install -Dm755 terracoin-qt "$pkgdir"/usr/bin/terracoin-qt
  install -Dm644 src/qt/res/icons/bitcoin.png "$pkgdir"/usr/share/pixmaps/terracoin256.png

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

