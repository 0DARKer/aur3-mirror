# Maintainer: Mike Swanson <mikeonthecomputer@gmail.com>
# Contributor: Slash <demodevil5 at yahoo dot com>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>
# Contributor: Christoph Zeiler <rabyte*gmail>

# Based on the old ecm PKGBUILD.

# The pkgdesc is really long and not particularly pleasing as a
# result.  These utilties aren't entirely related to each other, but
# are distributed together.  Suggestions are welcome to making the
# pkgdesc suitably fit in 80 characters (the names of all the
# utilities in it are primarily to aide in searching the AUR for
# them.).

pkgname=cmdpack
pkgver=1.02
pkgrel=2
pkgdesc="Collection of command line utilities, most for emulation or disk images (bin2iso, bincomp, brrrip, byteshuf, byteswap, cdpatch, ecm, fakecrc, hax65816, id3point, rels, screamf, subfile, uips, usfv, vb2rip, wordadd, zerofill)"
arch=('i686' 'x86_64')
url="http://www.neillcorlett.com/cmdpack/"
license=('GPL3')
provides=('ecm' 'unecm')
source=("http://www.neillcorlett.com/downloads/${pkgname}-${pkgver}-src.tar.gz")
sha256sums=('1478d16720cccd585548b975d3bba0209cc0d62e274e67ba7fb0c168ec7fc39f')

_utilties=('bin2iso' 'bincomp' 'brrrip' 'byteshuf' 'byteswap'
  'cdpatch' 'ecm' 'fakecrc' 'hax65816' 'id3point' 'rels' 'screamf'
  'subfile' 'uips' 'usfv' 'vb2rip' 'wordadd' 'zerofill')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}-src/src/

  for elem in ${_utilties[@]}
  do
    gcc $CFLAGS $elem.c -o $elem
  done
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}-src/src/

  for elem in ${_utilties[@]}
  do
    install -Dm755 $elem ${pkgdir}/usr/bin/$elem
  done

  install -Dm644 ../doc/cmdpack.txt ${pkgdir}/usr/share/doc/cmdpack/cmdpack.txt

  # There is a bin2iso package in the main repo.  Rename this so both
  # packages may co-exist (nc is for "Neill Corlett").
  mv ${pkgdir}/usr/bin/bin2iso ${pkgdir}/usr/bin/bin2iso-nc
  ln ${pkgdir}/usr/bin/ecm ${pkgdir}/usr/bin/unecm
}
