# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=cromfs
pkgver=1.5.9.1
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='Compressed read-only filesystem based on FUSE'
url='http://bisqwit.iki.fi/source/cromfs.html'
license=('GPL')
depends=('gcc-libs' 'fuse' 'lzo2')
source=("http://bisqwit.iki.fi/src/arch/${pkgname}-${pkgver}.tar.gz"
        'make-generic.patch'
        'make-static.patch'
        'gcc46.patch')
sha1sums=('48033e515d461a6997421f12043aeaa3d89f0f6d'
          '6f496b78c71678f13826300ca88129412151692d'
          'a6c7605e23379decbc537f7e9d5c6e2404f7ffd8'
          '00754e32313be7bbca45d35bd71754290e104225')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../make-generic.patch
	patch -p1 -i ../make-static.patch
	patch -p1 -i ../gcc46.patch

	./configure
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
	install -d "${pkgdir}"/usr/{bin,share/doc/cromfs}
	install -m755 install/progs/* "${pkgdir}"/usr/bin/
	install -m644 install/docs/* "${pkgdir}"/usr/share/doc/cromfs/
}
