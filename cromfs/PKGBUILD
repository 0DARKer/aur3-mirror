# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=cromfs
pkgver=1.5.10
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Compressed read-only filesystem based on FUSE'
url='http://bisqwit.iki.fi/source/cromfs.html'
license=('GPL')
depends=('gcc-libs' 'fuse' 'lzo2')
source=("http://bisqwit.iki.fi/src/arch/${pkgname}-${pkgver}.tar.gz"
        'make-generic.patch'
        'make-static.patch')
sha1sums=('983975d0e7cf65fda20ce16560b9a0b4c6be8123'
          '6f496b78c71678f13826300ca88129412151692d'
          'a6c7605e23379decbc537f7e9d5c6e2404f7ffd8')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../make-generic.patch
	patch -p1 -i ../make-static.patch

	./configure
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
	install -d "${pkgdir}"/usr/{bin,share/doc/cromfs}
	install -m755 install/progs/* "${pkgdir}"/usr/bin/
	install -m644 install/docs/* "${pkgdir}"/usr/share/doc/cromfs/
}
