# Maintainer: Sial <NAME at cpan dot org>

_perlmod=Compiler-Lexer
pkgname=perl-compiler-lexer
pkgver=0.11
pkgrel=1
pkgdesc="Lexical Analyzer for Perl5"
arch=(any)
url="http://search.cpan.org/~goccy/${_perlmod}-${pkgver}/"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.8.9')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/G/GO/GOCCY/${_perlmod}-${pkgver}.tar.gz)
md5sums=('f47ed957726253c13857c30fd9a408ae')

build() {
  cd "${srcdir}/${_perlmod}-${pkgver}"

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='${pkgdir}'" \
    PERL_MB_OPT="--installdirs vendor --destdir '${pkgdir}'" \
    MODULEBUILDRC=/dev/null
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd ${_perlmod}-${pkgver}
  make test
}

package() {
  cd "${srcdir}/${_perlmod}-${pkgver}"
  make install
}
# vim:set ts=2 sw=2 et:
