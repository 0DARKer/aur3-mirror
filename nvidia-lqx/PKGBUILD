# Maintainer: shivik <>, PGP Key: 761E423C

pkgname=nvidia-lqx
pkgver=313.18
_extramodules=extramodules-3.7-lqx
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=3
pkgdesc="NVIDIA drivers for linux-lqx."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-lqx>=3.7' 'linux-lqx<3.8' "nvidia-utils=${pkgver}")
conflicts=('nvidia-173xx-lqx')
license=('custom')
options=(!strip)
install=nvidia-lqx.install
__mainserver="ftp://download.nvidia.com"
__usserver="http://us.download.nvidia.com"
__currentserver=${__mainserver}

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source=("${__currentserver}/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
		"work-with-linux-3.7.6.patch")
	md5sums=('780c37c28a6e06e9571cafe348b7da64'
		 'b4cbc6778721e5dd7d5f936f76fd936d')	
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source=("${__currentserver}/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
		"work-with-linux-3.7.6.patch")
	md5sums=('fa17a260793a38b4b8ae367db2e03b39'
		 'b4cbc6778721e5dd7d5f936f76fd936d')
fi

build() {
	cd "${srcdir}"
	sh ${_pkg}.run --extract-only
	cd ${_pkg}/kernel
	patch -Np1 -i "${srcdir}/work-with-linux-3.7.6.patch"
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-lqx.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-lqx.install"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
