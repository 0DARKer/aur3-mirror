# Contributor: Meowdib <meowdib at gmail dot com>
# Maintainer: Doug <doug.quaid@gmail.com>

pkgname=nvidia-lqx
pkgver=275.21
_kernver='2.6.39-lqx'
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26-lqx."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=("kernel26-lqx>=2.6.39" "kernel26-lqx<2.6.40" "nvidia-utils=${pkgver}")
conflicts=()
license=('custom')
install=nvidia-lqx.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	sha256sums=('ac38d6bbb4ad9320cae35270dcb31e5b2f45c9f3c1230c1ad7bdf29d7b9a60db')
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	sha256sums=('2eb0bfc03749651c731c24738bd5768bbd0bb1671c4da938f0197c887f56bdfe')
fi

build() {
    cd $srcdir
    sh ${_pkg}.run --extract-only
    cd ${_pkg}/kernel
    make SYSSRC=/lib/modules/${_kernver}/build module || return 1
}

package() {
    install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
        $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko

    # Blacklist nouveau since 2.6.34
    # Blacklisted via kernel-specific file to not conflict with other nvidia drivers
    # Duplicated blacklist entries are not problematic
    install -d -m755 $pkgdir/etc/modprobe.d/ || return 1
    echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist-lqx.conf || return 1

    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia-lqx.install
}
