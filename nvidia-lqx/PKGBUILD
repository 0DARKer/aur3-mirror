# Maintainer: shivik <>, PGP Key: 761E423C

pkgname=nvidia-lqx
pkgver=331.38
_extramodules=extramodules-3.12-lqx
_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
pkgrel=1
pkgdesc="NVIDIA drivers for linux-lqx."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-lqx>=3.12' 'linux-lqx<3.13' "nvidia-libgl" "nvidia-utils=${pkgver}")
conflicts=('nvidia-96xx' 'nvidia-173xx-lqx')
license=('custom')
options=(!strip)
install=nvidia-lqx.install
__mainserver="ftp://download.nvidia.com"
__usserver="http://us.download.nvidia.com"
__currentserver=${__mainserver}

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	source+=("${__currentserver}/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums+=('16aa229f7f118c8cafad6fb3f4ac082e')	
elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	source+=("${__currentserver}/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
	md5sums+=('f2059ae373665cb6c8fb826e1173b04d')
fi

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	cd "${srcdir}"
	sh "${_pkg}.run" --extract-only
	cd "${_pkg}/kernel"
	make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
	install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
	install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
	echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-lqx.conf"
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-lqx.install"
	gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
