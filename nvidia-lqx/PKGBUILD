# Contributor: Meowdib <meowdib at gmail dot com>
# Maintainer: Doug <doug.quaid@gmail.com>

pkgname=nvidia-lqx
pkgver=260.19.36
_kernver='2.6.37-lqx'
pkgrel=5
pkgdesc="NVIDIA drivers for kernel26-lqx."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=("kernel26-lqx>=2.6.37" "kernel26-lqx<2.6.38" "nvidia-utils=${pkgver}")
conflicts=()
license=('custom')
install=nvidia-lqx.install

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('2826484d87827351774449382e45c4a9')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('6ebc5db9066e920b0b0e48377ce5eeb5')
fi

build() {
    cd $srcdir
    sh ${_pkg}.run --extract-only
    cd ${_pkg}/kernel
    make SYSSRC=/lib/modules/${_kernver}/build module || return 1
}

package() {
    install -D -m644 $srcdir/${_pkg}/kernel/nvidia.ko \
        $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko

    # Blacklist nouveau since 2.6.34
    # Blacklisted via kernel-specific file to not conflict with other nvidia drivers
    # Duplicated blacklist entries are not problematic
    install -d -m755 $pkgdir/etc/modprobe.d/ || return 1
    echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist-lqx.conf || return 1

    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia-lqx.install
}
