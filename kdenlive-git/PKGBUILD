# Maintainer: IncredibleLaser
# Maintainer: nous <nous at archlinux.us>
# Submitter: pressh

pkgname=kdenlive-git
pkgver=v0.9.4.r373.g3e4a06e
pkgrel=1.1
pkgdesc="A non-linear video editor for Linux using the MLT video framework."
arch=('i686' 'x86_64')
url="http://http://www.kdenlive.org/"
license=('GPL')
depends=('hicolor-icon-theme' 'kdelibs' 'mlt-git' \
         'qimageblitz' 'qjson' 'shared-mime-info' 'movit')
makedepends=('git' 'cmake' 'automoc4' 'gettext' 'glew')
optdepends=('cdrkit: for creation of DVD ISO images'
            'dvdauthor: for creation of DVD'
            'dvgrab: for firewire capture'
            'ffmpeg: for webcam capture'
            'recordmydesktop: for screen capture'
            'xine-lib: for DVD preview')
provides=('kdenlive')
conflicts=('kdenlive')
#options=(!libtool)
source=("git://anongit.kde.org/kdenlive")
md5sums=('SKIP')
install=kdenlive.install
_gitname="kdenlive"

build() {
  export LDFLAGS="$LDFLAGS -lX11"
  cd "${srcdir}/${_gitname}"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=/usr/lib \
    -DLINK_LIBRARIES=-lX11
  make
}

package() {
  cd "$srcdir/$_gitname/build"
  make DESTDIR="$pkgdir" install
}

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}
