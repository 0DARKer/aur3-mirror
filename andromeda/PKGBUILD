# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=andromeda
pkgver=0.3
pkgrel=1
pkgdesc="Cross-platform file manager, written on Qt"
arch=('i686' 'x86_64')
url="https://gitorious.org/$pkgname"
license=('LGPL')
depends=('qtwebkit' 'poppler-qt')
makedepends=('cmake')
install="$pkgname.install"
#source=("https://gitorious.org/andromeda/andromeda/archive-tarball/v$pkgver")
source=("https://github.com/ABBAPOH/Andromeda/archive/v$pkgver.tar.gz")
md5sums=('514fc54200c9c38445a097a454418fc7')

build() {
  cd "$srcdir/${pkgname^}-$pkgver"

  # x86_64 lib path fix
  sed -i '/LIB_SUFFIX/s/64//' cmake/LibSuffix.cmake

  [ -d bld ] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/${pkgname^}-$pkgver/bld"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
