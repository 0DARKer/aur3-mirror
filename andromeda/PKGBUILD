# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=andromeda
pkgver=0.2
pkgrel=1
pkgdesc="Cross-platform file manager, written on Qt"
arch=('i686' 'x86_64')
url="https://gitorious.org/$pkgname"
license=('LGPL')
depends=('qtwebkit')
makedepends=('cmake')
install="$pkgname.install"
source=("https://gitorious.org/andromeda/andromeda/archive-tarball/v0.2")
md5sums=('ca3ad7576eda9331b61fb30d558e0c05')

build() {
  cd "$srcdir/$pkgname-$pkgname"

  # x86_64 lib path fix
  sed -i '/LIB_SUFFIX/s/64//' cmake/LibSuffix.cmake

  # do not fetch tag commit revision from git
  sed "s/^get_git_revision.*/set( GIT_REVISION "52f1e6f0e9c10fda4c74443a151f5391638db5eb" )/" \
    -i cmake/ProjectInfo.cmake

  # missing unistd.h
  sed -i "60i#include <unistd.h>" src/shared/qtsingleapplication/qtlocalpeer.cpp
  sed -i "18i#include <unistd.h>" src/libs/io/qdrivecontroller_linux.cpp
  sed -i "12i#include <unistd.h>" src/libs/io/qtrash_linux.cpp

  [ -d bld ] || mkdir bld && cd bld
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgname/bld"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
