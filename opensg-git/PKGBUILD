# Maintainer: Rodrigo Bezerra <rodrigobezerra21 at gmail dot com>

pkgname=opensg-git
pkgver=20130329
pkgrel=1
pkgdesc="Open Source scene graph system for interactive 3D graphics applications"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.opensg.org"
depends=('boost-libs' 'freeglut' 'glu' 'mesa' 'openexr' 'python2' 'qt4')
makedepends=('git')
conflicts=('opensg-cvs')
options=('!docs')

_gitroot="git://opensg.git.sourceforge.net/gitroot/opensg/opensg"
_gitname="opensg"

build() {
  cd "${srcdir}"
  msg "Connecting to GIT server...."

  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}" && git pull origin
    msg "The local files are updated."
  else
    git clone "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_gitname}-build"
  git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd "${srcdir}/${_gitname}-build"
  
  mkdir build && cd build
    
  cmake .. \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DCMAKE_BUILD_TYPE=Release \
  -DOSG_ENABLE_C++11=ON \
  -DOSGBUILD_EXAMPLES_SIMPLE=ON \
  -DOSGBUILD_EXAMPLES_ADVANCED=ON \
  -DOSGBUILD_EXAMPLES_TUTORIAL=ON \
  -DOSG_INSTALL_EXAMPLES=ON \
  -DOSG_INSTALL_EXAMPLES_EXE=ON
  
  make
}

package() {
  cd "${srcdir}/${_gitname}-build/build"
  
  make DESTDIR="${pkgdir}/" install
  [ -d "${pkgdir}/usr/lib64" ] && mv "${pkgdir}/usr/lib64" "${pkgdir}/usr/lib" || true
  
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" "${pkgdir}/usr/bin/fcd2code/fcd2code"
  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" "${pkgdir}/usr/bin/osg2-config"
}
