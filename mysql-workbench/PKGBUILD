# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Alexandre Boily <alexandreboily@gmail.com>
# Contributor: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mysql-workbench
pkgver=6.2.4
pkgrel=4
pkgdesc='A cross-platform, visual database design tool developed by MySQL'
arch=('i686' 'x86_64')
url='https://www.mysql.com/products/workbench/'
license=('GPL2')
depends=('python2' 'libzip' 'libmariadbclient' 'lua51' 'gtkmm' 'ctemplate'
	'libgnome-keyring' 'libgl' 'python2-paramiko' 'python2-pexpect' 'gdal'
	'pcre' 'tinyxml' 'libxml2' 'mysql-python' 'python2-pysqlite' 'cairo'
	'python2-cairo' 'hicolor-icon-theme' 'desktop-file-utils' 'libiodbc'
	'mysql-connector-c++>=1.1.5' 'vsqlite++' 'unixodbc' 'freetype2'
	'libantlr3c')
optdepends=('gnome-keyring: store SSH/MySQL passwords in GNOME password manager'
	'python2-pyodbc: database migration')
makedepends=('cmake' 'boost' 'curl' 'mesa' 'imagemagick' 'swig2' 'java-runtime')
install=mysql-workbench.install
source=("http://cdn.mysql.com/Downloads/MySQLGUITools/mysql-workbench-community-${pkgver}-src.tar.gz"{,.asc}
	'http://www.antlr3.org/download/antlr-3.4-complete.jar'
	'mysql-workbench-ctemplate.patch'
	'mysql-workbench-keyring.patch'
	'mysql-workbench-no-check-for-updates.patch'
	'mysql-workbench-paramiko.patch'
	'arch_linux_profile.xml')

prepare() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src/"

	# Disable 'Help' -> 'Check for Updates'
	patch -Np1 < "${srcdir}"/mysql-workbench-no-check-for-updates.patch

	# fix deprecated calls to tpl->ReloadIfChanged
	# http://bugs.mysql.com/72585
	patch -Np1 < "${srcdir}"/mysql-workbench-ctemplate.patch

	# fix GNOME keyring >= 3.14
	# http://bugs.mysql.com/bug.php?id=74771
	patch -Np1 < "${srcdir}"/mysql-workbench-keyring.patch

	# fix SSH tunnel
	patch -Np1 < "${srcdir}"/mysql-workbench-paramiko.patch

	# put antlr into place
	install -D ${srcdir}/antlr-3.4-complete.jar ${srcdir}/linux-res/bin/antlr-3.4-complete.jar

	# fix GDAL
	sed -i '/#include/s|gdal/||' backend/wbpublic/grtui/geom_draw_box.h backend/wbpublic/grt/spatial_handler.h
}

build() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src/"

	cmake \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DSWIG_EXECUTABLE=/usr/bin/swig-2 \
		.

	make
}

package() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src"

	make DESTDIR="${pkgdir}" install

	# generate icons
	for SIZE in 16 24 32 48 64 96 128 160 192 256 384 512; do
		if [ ! -s images/icons/MySQLWorkbench-${SIZE}.png ]; then
			convert -scale ${SIZE} \
				images/icons/MySQLWorkbench-512.png \
				images/icons/MySQLWorkbench-${SIZE}.png
		fi
		install -D -m0644 images/icons/MySQLWorkbench-${SIZE}.png \
			"${pkgdir}/usr/share/icons/hicolor/${SIZE}x${SIZE}/apps/mysql-workbench.png"
	done

	install -D -m 0644 "${srcdir}"/arch_linux_profile.xml "${pkgdir}"/usr/share/mysql-workbench/mysql.profiles/Arch_Linux_\(MariaDB_5.5_Package\).xml
}

sha256sums=('b3e0775b7725fde3be8a63b6b1fd293a41bb6cbbc9dc459924b774fb4ad1afaa'
            'SKIP'
            '9d3e866b610460664522520f73b81777b5626fb0a282a5952b9800b751550bf7'
            'bbf6f3e56847cc012634e262295f34eaaa43ef32d04e1ec291315dabd6613042'
            '683a60e2bb38d8687f7e5228b49f62b374bf2c93488bdb89307205dbf5b3c476'
            '996482e15d88af097f5e7a578885338cad5b8724f8abb4341749d5538801f5c0'
            '9c8d9a2c32ef63f2fd1f1062d42ab0b77f8c28fa77d05ded7b1c0a98be626d71'
            '28724c4b4cec29ce19aada08279df1b086381cd788fef7ae07c1860f7d17af7e')
