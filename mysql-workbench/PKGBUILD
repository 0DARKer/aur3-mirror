# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Alexandre Boily <alexandreboily@gmail.com>
# Contributor: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mysql-workbench
pkgver=6.0.2
pkgrel=1
pkgdesc="A cross-platform, visual database design tool developed by MySQL"
arch=('i686' 'x86_64')
url="https://www.mysql.com/products/workbench/"
license=('GPL2')
depends=('python2' 'libzip' 'libmariadbclient' 'lua51' 'gtkmm' 'ctemplate'
	 'libgnome-keyring' 'libgl' 'python2-paramiko' 'python2-pexpect'
	 'pcre' 'tinyxml' 'libxml2' 'mysql-python' 'python2-pysqlite' 'cairo'
         'python-cairo' 'hicolor-icon-theme' 'desktop-file-utils' 'libiodbc'
	 'mysql-connector-c++' 'vsqlite++')
optdepends=('gnome-keyring: store SSH/MySQL passwords in GNOME password manager')
makedepends=('cmake' 'boost' 'curl' 'mesa')
options=('!libtool !distcc !ccache')
install=mysql-workbench.install
source=("http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQLGUITools/${pkgname}-gpl-${pkgver}-src.tar.gz"
	'Arch_Linux_(MariaDB_5.5_Package).xml')

build() {
	cd "${srcdir}/${pkgname}-gpl-${pkgver}-src"

	cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .

	# fix path to lua include files
	# is this a bug by cmake? (/usr/share/cmake-2.8/Modules/FindLua51.cmake)
	sed -i '/^LUA_INCLUDE_DIR/s|/usr/include|/usr/include/lua5.1|' CMakeCache.txt

	# fix detection of Python
	sed -i 's|python3.3m|python2.7|' CMakeCache.txt

	make
}

package() {
	cd "${srcdir}/${pkgname}-gpl-${pkgver}-src"

	make DESTDIR="${pkgdir}" install

	install -D -m 0644 ${srcdir}/Arch_Linux_\(MariaDB_5.5_Package\).xml ${pkgdir}/usr/share/mysql-workbench/mysql.profiles/Arch_Linux_\(MariaDB_5.5_Package\).xml
}

sha256sums=('2eb80cda1b27e50ac79bfca08281e8bfafb616ec926bc8b5bee32f843531e39a'
            '1aa3cc7bf83470e81eca766114a597fa57542316818732011c8ea67e52197c7e')
