# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Marc poiroud <marci1@archlinux.fr>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>
# Contributor: Laurie Clark-Michalek <bluepeppers@archlinux.us>

pkgname=megamario
pkgver=1.7
pkgrel=1
pkgdesc="A Super Mario Bros clone with better graphics and new levels"
arch=('i686' 'x86_64')
url="http://mmario.sourceforge.net/"
license=('LGPL')
depends=('libgl' 'sdl_image' 'sdl_mixer' 'sdl_ttf')
source=("http://downloads.sourceforge.net/mmario/MegaMario_v${pkgver}_full.zip")
md5sums=('9550b20f7c2754cd8f94ebf309becf62')

build() {
  cd "${srcdir}"
  sed -i "s/PATH_MAX/255/" src/Global.h
  sed -i "s_mp3music/_/usr/share/megamario/sfx/&_" src/{functions,main}.cpp
  make PREFIX=/usr
}

package() {
  cd "${srcdir}"

  # Install game files
  make PREFIX="${pkgdir}/usr" install
  cp -r mp3music "${pkgdir}/usr/share/${pkgname}/sfx"

  # Install a desktop entry
  install -Dm644 "${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 {CONTROLS,fixes_v1.7,readme}.txt "${pkgdir}/usr/share/doc/${pkgname}"
}
