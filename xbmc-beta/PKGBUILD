# Maintainer : graysky <graysky AT archlinux DOT us>
# Contributor: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: DonVla <donvla@users.sourceforge.net>
# Contributor: Ulf Winkelvos <ulf [at] winkelvos [dot] de>
# Contributor: Ralf Barth <archlinux dot org at haggy dot org>
# Contributor: B & monty - Thanks for your hints :)
#
# Original credits go to Edgar Hucek <gimli at dark-green dot com>
# for his xbmc-vdpau-vdr PKGBUILD at https://archvdr.svn.sourceforge.net/svnroot/archvdr/trunk/archvdr/xbmc-vdpau-vdr/PKGBUILD

pkgname=xbmc-beta
pkgver=rc3
_pkgname="xbmc-Frodo_$pkgver"
_prefix='/usr'
pkgrel=3
pkgdesc="A software media player and entertainment hub for digital media. Frodo rc3."
provides=('xbmc')
conflicts=('xbmc' 'xbmc-pulse' 'xbmc-svn' 'xbmc-git')
arch=('i686' 'x86_64')
url="http://xbmc.org"
license=('GPL' 'LGPL')
depends=('hicolor-icon-theme' 'fribidi' 'lzo2' 'smbclient' 'libtiff' 'libva' 'libpng' 'libcdio' 'yajl' 'libmysqlclient' 'libjpeg-turbo' 'libsamplerate' 'glew' 'libssh' 'libmicrohttpd' 'libxrandr' 'sdl_mixer' 'sdl_image' 'python2' 'libass' 'libmpeg2' 'libmad' 'libmodplug' 'jasper' 'rtmpdump' 'unzip' 'mesa-demos' 'xorg-xdpyinfo' 'libbluray' 'libnfs' 'afpfs-ng' 'avahi' 'bluez' 'tinyxml' 'libcap' 'swig' 'taglib' 'java-runtime-headless' 'glu' 'mesa' 'libpulse' 'libcec-git')
makedepends=('boost' 'cmake' 'gperf' 'nasm' 'libxinerama' 'zip' 'libva-vdpau-driver' 'udisks' 'upower')

optdepends=(
'gdb: for meaningful backtraces in case of trouble - STRONGLY RECOMMENDED'
'libcrystalhd: Broadcom CrystalHD kernel module'
'libcec: support for Pulse-Eight USB-CEC adapter'
'libva-vdpau-driver: accelerated video playback for nvidia cards'
'lirc: remote controller support'
'pulseaudio: pulseaudio support'
'udisks: automount external drives'
'upower: used to trigger suspend functionality'
'unrar: access compressed files without unpacking them'
'xvba-video: accelerated video playback for amd cards'
)
install="$pkgname.install"
source=("https://github.com/xbmc/xbmc/archive/Frodo_$pkgver.tar.gz"
'10-xbmc.rules'
'libdvdcss-automake_fix.patch'
'xbmc.service')
sha256sums=('5d2e9d47aad3c84c32d37fd2fe9645a2466b2bc51d7fc4923e66a1aa48013f3d'
            '74d76be5e6a0f44376ab237f7ab597d1f13656879ce6fc1491deaad84e97a4cb'
            'ebde6a550e0ad00598641c4348920c1e30619daa6f2901e4d3551b50fcfeffb3'
            '547e06f3141866cd34b1df34025517e9826992b67543af7621d954bcdfa0ba94')

build() {
	cd "$srcdir/$_pkgname"
	# Configure XBMC
	#
	# Note on external-libs:
	#   - We cannot use external python because Arch's python was built with
	#     UCS2 unicode support, whereas xbmc expects UCS4 support
	#   - According to an xbmc dev using external/system ffmpeg with xbmc is "pure stupid" :D

	patch -Np1 -i "$srcdir/libdvdcss-automake_fix.patch"

	./bootstrap	
	export PYTHON_VERSION=2  # external python v2
	./configure --prefix=$_prefix --exec-prefix=$_prefix \
		--enable-debug \
		--enable-optimizations \
		--enable-gl \
		--enable-sdl \
		--enable-vaapi \
		--enable-vdpau \
		--enable-xvba \
		--enable-joystick \
		--enable-xrandr \
		--enable-rsxs \
		--enable-projectm \
		--enable-x11 \
		--enable-pulse \
		--enable-rtmp \
		--enable-samba \
		--enable-nfs \
		--enable-afpclient \
		--enable-ffmpeg-libvorbis \
		--enable-dvdcss \
		--disable-hal \
		--enable-avahi \
		--enable-webserver \
		--enable-optical-drive \
		--enable-libbluray \
		--enable-texturepacker \
		--enable-udev \
		--enable-libusb \
		--enable-libcec \
		--enable-external-libraries \
		--disable-external-ffmpeg 

	# Now (finally) build
	make
}

package() {
	cd "$srcdir/$_pkgname"
	make DESTDIR="$pkgdir" install

	# run feh with python2
	sed -i -e 's/python/python2/g' ${pkgdir}${_prefix}/bin/xbmc

	# Tools
	install -Dm755 "$srcdir"/$_pkgname/tools/TexturePacker/TexturePacker "$pkgdir"${_prefix}/lib/xbmc/

	# Licenses
	install -dm755 "$pkgdir"$_prefix/share/licenses/$pkgname
	for licensef in LICENSE.GPL copying.txt; do
		mv "$pkgdir"$_prefix/share/doc/xbmc/$licensef "$pkgdir"$_prefix/share/licenses/$pkgname
	done

	# install systemd service
	install -Dm644 "$srcdir"/xbmc.service "$pkgdir"/usr/lib/systemd/system/xbmc.service

	# Polkit rules thanks to dejavu
	# https://bbs.archlinux.org/viewtopic.php?pid=1202676#p1202676
	install -dm700 "$pkgdir"/etc/polkit-1/rules.d
	install -Dm644 "$srcdir"/10-xbmc.rules "$pkgdir"/etc/polkit-1/rules.d/10-xbmc.rules
}
