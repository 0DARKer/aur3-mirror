# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=qgo2-cvs
pkgver=20101210
pkgrel=1
pkgdesc='Go client and full featured SGF editor; snapshot of the next major version'
arch=('i686' 'x86_64')
url='http://qgo.sourceforge.net/'
license=('GPL')
depends=('qt')
makedepends=('cvs')
conflicts=('qgo')

source=('qt-4.7.patch')
sha1sums=('20ac7d885e817bf668aa9533dbac3902c21d6744')

_cvsroot=':pserver:anonymous:@qgo.cvs.sourceforge.net:/cvsroot/qgo'
_cvsmod='qgo2'

build() {
	cd "$srcdir"
	cvs -z3 -d$_cvsroot checkout $_cvsmod
	cp -r $_cvsmod $_cvsmod-build
	cd $_cvsmod-build

	patch -p1 < ../qt-4.7.patch
	ls src/translations/*.ts | sed 's/\.ts/\.qm/' | xargs touch

	qmake QMAKE_CXXFLAGS=-fpermissive -o Makefile qgo2.pro
	make
}

package() {
	cd "$srcdir/$_cvsmod-build"
	QTDIR=/usr make INSTALL_ROOT="$pkgdir/" install
}
