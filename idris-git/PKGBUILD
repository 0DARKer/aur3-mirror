# Maintainer: Leif Warner <abimelech@gmail.com>
pkgname=idris-git
pkgver=2694.2f52264
pkgrel=1
pkgdesc="Functional programming language with dependent types."
url="http://www.idris-lang.org"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('haskell-cabal' 'haskell-binary' 'haskell-bytestring' 'haskell-containers' 'haskell-directory' 'haskell-filepath' 'haskell-haskeline' 'haskell-mtl' 'haskell-parsec' 'haskell-pretty' 'haskell-process' 'haskell-transformers' 'haskell-split' 'haskell-libffi' 'haskell-language-java>=0.2.6' 'haskell-llvm-general' 'haskell-vector-binary-instances' 'gc' 'haskell-ansi-terminal' 'haskell-trifecta>=1.1' 'haskell-xml' 'haskell-network')
depends=('gmp' 'libffi' 'gcc')
optdepends=(
  'gc: dynamically linked from the output of the llvm backend'
  'maven: downloads runtime when using Java backend'
)
options=('staticlibs')
provides=('idris')
conflicts=('idris')
source=('git://github.com/idris-lang/Idris-dev.git')
md5sums=('SKIP')
_gitname=Idris-dev

prepare() {
  cd $_gitname
  # Relax upper bound of parsers
  sed -ie '399s/==/>=/' idris.cabal
  # Relax upper bound of trifecta
  sed -ie '406s/==/>=/' idris.cabal
}

pkgver() {
  cd $_gitname
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}
build() {
  cd $_gitname
  runhaskell Setup configure -f FFI -f LLVM -f GMP --prefix=/usr --docdir=/usr/share/doc/${pkgname} -O
  runhaskell Setup build
}
# The idris tests are not run in-situ just yet; they run against the installed libs.
#check() {
#  cd $_gitname
#  make test
#}
package() {
    cd $_gitname
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
    rmdir ${pkgdir}/usr/share/doc/${pkgname}
}
