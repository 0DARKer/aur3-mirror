# Contributor: Hy Goldsher aka hyness <hyness-at-gmail>                
# Maintainer: Hy Goldsher aka hyness <hyness-at-gmail>
pkgname=davmail
pkgver=4.3.2	
pkgrel=1
pkgdesc="a POP/IMAP/SMTP/Caldav/LDAP gateway for the exchange service"
arch=('i686' 'x86_64')
url="http://davmail.sourceforge.net/"
license=('GPL')
makedepends=('unzip')
depends=('java-runtime')
[ "$CARCH" = "i686" ] && _ARCH=x86
[ "$CARCH" = "x86_64" ] && _ARCH=x86_64
_rev=2138
source=(http://downloads.sourceforge.net/davmail/davmail-linux-${_ARCH}-$pkgver-$_rev.tgz davmail.desktop davmail.sh)
[ "$CARCH" = "i686" ] && md5sums=('fe3a759551a8871d1e994879685251e9')
[ "$CARCH" = "x86_64" ] && md5sums=('a3766ee9671bda2b8c8d5153843ac3d3')
md5sums+=('1df37a6120d88de8df3cb735977336ba' '5f7c1bb0b5a9bc69c7015715d87862ea')
install=davmail.install

package() {
  _src=$srcdir/davmail-linux-${_ARCH}-$pkgver-$_rev
  install -d $pkgdir/{usr/share/java/$pkgname/lib,usr/bin,usr/share/applications} || return 1
  install $_src/davmail.jar $pkgdir/usr/share/java/$pkgname/ || return 1
  install -D $_src/lib/* $pkgdir/usr/share/java/$pkgname/lib || return 1
  install -m755 ${srcdir}/davmail.sh $pkgdir/usr/share/java/$pkgname || return 1
  ln -s /usr/share/java/$pkgname/davmail.sh $pkgdir/usr/bin/davmail || return 1
  install -Dm644 ${srcdir}/davmail.desktop ${pkgdir}/usr/share/applications/ || return 1

  # Create icons
  cd $_src || return 1
  unzip -q davmail.jar tray2.png tray32.png tray48.png || return 1
  install -Dm644 tray2.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/davmail.png || return 1
  install -Dm644 tray32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/davmail.png || return 1
  install -Dm644 tray48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/davmail.png || return 1
}

