# Maintainer: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Ray Kohler <ataraxia@gmail.com>
# Contributor: muflax <muflax@gmail.com>
# Contributor: coolkehon <coolkehon@gmail.com>

_pkgname=task
pkgname=task-devel
pkgver=2.0.0.beta4
pkgrel=1
pkgdesc="A command-line todo list manager - latest unstable release"
arch=('i686' 'x86_64')
url="http://taskwarrior.org/projects/show/taskwarrior/"
license=('MIT')
depends=()
provides=('task=2.0.0')
conflicts=('task')
makedepends=('cmake')
optdepends=('bash-completion: for bash completion', 'python: for python export addon', 'ruby: for ruby export addon', 'perl: for perl export addon')
source=(http://www.taskwarrior.org/download/$_pkgname-$pkgver.tar.gz)
sha1sums=('9b31fa334cc64c2e0e89aa66c5bf2d578325b32f')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/bash/task_completion.sh" "$pkgdir/etc/bash_completion.d/task_completion.sh"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/fish/task.fish" "$pkgdir/usr/share/fish/completions/task.fish"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/zsh/_task" "$pkgdir/usr/share/zsh/site-functions/_task"
  
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/vim/ftdetect/task.vim" "$pkgdir/usr/share/vim/vimfiles/ftdetect/task.vim"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/vim/syntax/taskdata.vim" "$pkgdir/usr/share/vim/vimfiles/syntax/taskdata.vim"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/vim/syntax/taskedit.vim" "$pkgdir/usr/share/vim/vimfiles/syntax/taskedit.vim"
  install -Dm644 "$pkgdir/usr/share/doc/task/scripts/vim/syntax/taskrc.vim" "$pkgdir/usr/share/vim/vimfiles/syntax/taskrc.vim"
}
