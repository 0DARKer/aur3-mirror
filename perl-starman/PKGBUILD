# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname='perl-starman'
_pkgname='Starman'
pkgver='0.3006'
pkgrel='1'
pkgdesc="Starman launcher"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.1' 'perl-data-dump' 'perl-http-date' 'perl-http-parser-xs' 'perl-http-message' 'perl-net-server>=0.91' 'perl-plack>=0.9971')
makedepends=('perl-extutils-makemaker>=6.59' 'perl-test-requires' 'perl-test-tcp')
url="http://search.cpan.org/dist/Starman"
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIYAGAWA/${_pkgname}-${pkgver}.tar.gz")
md5sums=('9fafe7a99ebe753e8a1a5bac7d9b007a')
sha512sums=('b0a5afa6bba831a3d181e98bab168a0bfe7922d6d438412cb3a80336ab4c4652356d19db043a1e0da5cc44f8f1e5b4b62e09ddac897097cce4c86dff360be142')
_distdir="${srcdir}/${_pkgname}-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$_distdir"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
