# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname='perl-starman'
_pkgname='Starman'
pkgver='0.3003'
pkgrel='1'
pkgdesc="Starman launcher"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.1' 'perl-data-dump' 'perl-http-date' 'perl-http-parser-xs' 'perl-http-message' 'perl-net-server>=0.91' 'perl-plack>=0.9971')
makedepends=('perl-extutils-makemaker>=6.59' 'perl-test-requires')
url="http://search.cpan.org/dist/Starman"
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIYAGAWA/${_pkgname}-${pkgver}.tar.gz")
md5sums=('7e954506ecb5d34505c99953ec593c14')
sha512sums=('5a12681277e732590acce72d48afee675d5c19abee340a0115d6ca30861007b8d861a96c929b22641f17b4e6982ff2b3a3207cdb696e926702de4a3a823242e7')
_distdir="${srcdir}/${_pkgname}-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$_distdir"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
