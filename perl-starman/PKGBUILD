# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname='perl-starman'
_pkgname='Starman'
pkgver='0.3005'
pkgrel='1'
pkgdesc="Starman launcher"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8.1' 'perl-data-dump' 'perl-http-date' 'perl-http-parser-xs' 'perl-http-message' 'perl-net-server>=0.91' 'perl-plack>=0.9971')
makedepends=('perl-extutils-makemaker>=6.59' 'perl-test-requires' 'perl-test-tcp')
url="http://search.cpan.org/dist/Starman"
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIYAGAWA/${_pkgname}-${pkgver}.tar.gz")
md5sums=('1b1707ae90bfc8dd12b97ab4dc412eca')
sha512sums=('0f5411b050c2eb6e33479d9bd029165105ec0eaaf27c3904bf32538de6c888c60cb8b43916f0b096b1c2edac4cb383181c3b528860ab4a2b9d9abc990b2c96aa')
_distdir="${srcdir}/${_pkgname}-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$_distdir"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$_distdir"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
