# Maintainer: Olaf Leidinger <oleid@mescharet.de>
_hkgname=yesod-form
pkgname=haskell-yesod-form
pkgver=1.3.4
pkgrel=1
pkgdesc="Form handling support for Yesod Web Framework"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:MIT')
arch=('i686' 'x86_64')
makedepends=()
depends=('ghc' 'haskell-aeson' 'haskell-attoparsec>=0.10' 'haskell-blaze-builder>=0.2.1.4' 'haskell-blaze-html>=0.5' 'haskell-blaze-markup>=0.5.1' 'haskell-bytestring'  'haskell-crypto-api>=0.8' 'haskell-data-default' 'haskell-email-validate>=1.0' 'haskell-hamlet<1.2' 'haskell-network' 'haskell-persistent<1.3' 'haskell-resourcet' 'haskell-shakespeare-css<1.1' 'haskell-shakespeare-js<1.3' 'haskell-template-haskell' 'haskell-text' 'haskell-time' 'haskell-transformers' 'haskell-wai>=1.3' 'haskell-xss-sanitize>=0.3.0.1' 'haskell-yesod-core<1.3' 'haskell-yesod-persistent<1.3')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('4be93214845173197c64efc26860ab09')
build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
