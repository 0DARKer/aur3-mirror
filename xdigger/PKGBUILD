# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=xdigger
pkgver=1.0.10
pkgrel=1
pkgdesc="A Boulderdash clone that was ported from KC85 to Unix/X11"
arch=('i686' 'x86_64')
url="http://www.happypenguin.org/show?XDigger"
license=('GPL')
depends=('libxext')
makedepends=('imake')
source=("http://www.ibiblio.org/pub/Linux/games/arcade/${pkgname}-${pkgver}.tgz"
        "${pkgname}.desktop")
md5sums=('74959b5c3f0ba47d1f25f515438c86ab'
         '3440c5bfc853d6707d010690d7c13309')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "s_/usr/lib/X11/xdigger_/usr/share/xdigger_" configure.h
  sed -i "s_/var/X11R6/scores_/var/lib/xdigger_" configure.h
  xmkmf || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Install binary
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install data files
  mkdir -p "${pkgdir}/usr/share/${pkgname}"
  cp -rf audio "${pkgdir}/usr/share/${pkgname}"
  install -m644 "${pkgname}.level"{,2} "${pkgdir}/usr/share/${pkgname}"
  install -Dm666 "${pkgname}.hiscore" "${pkgdir}/var/lib/${pkgname}/${pkgname}.hiscore"

  # Install docs and man
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -m644 ChangeLog README "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 "${pkgname}.man" "${pkgdir}/usr/share/man/man6/${pkgname}.6"

  # Install pixmap and desktop file
  install -Dm644 "pixmaps/${pkgname}.xpm" "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
