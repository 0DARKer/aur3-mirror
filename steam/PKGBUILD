# $Id: PKGBUILD 79958 2012-11-15 03:14:52Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
pkgname=steam
pkgver=1.0.0.24
pkgrel=4
pkgdesc="Digital distribution client - open beta - bootstrap package"
arch=("i686" "x86_64")
url="http://steampowered.com/"
license=('custom')
depends=(
      'bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
      'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-font' 'zenity'
      )
if [[ "$CARCH" == "x86_64" ]]; then
  depends+=(lib32-libgl lib32-gcc-libs lib32-libx11)
  optdepends=(
  'lib32-ati-dri: for open source ATI driver users'
  'lib32-catalyst-utils: for AMD Catalyst users'
  'lib32-intel-dri: for open source Intel driver users'
  'lib32-nouveau-dri: for Nouveau users'
  'lib32-nvidia-utils: for NVIDIA proprietary blob users'
  'lib32-flashplugin: for flash video, copy to $XDG_DATA_HOME/Steam/ubuntu12_32/plugins/'
  )
fi


install=steam.install

source=("http://repo.steampowered.com/steam/archive/precise/steam_1.0.0.24_i386.deb")
md5sums=('d8b91148f962e1b266a9aa24ff4c19c0')

build() {
  cd "$srcdir"

  bsdtar xf "$srcdir/data.tar.gz"
}


package() {

  cp -dpr --no-preserve=ownership "$srcdir/usr" "$pkgdir"
  # Install license
  
  install -Dm644 "$srcdir/usr/share/doc/steam/steam_install_agreement.txt.gz" "$pkgdir/usr/share/licenses/steam/LICENSE.gz"
  gunzip "$pkgdir/usr/share/licenses/steam/LICENSE.gz"
  

  # blank steamdeps because apt-get
  ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"

}
#vim: set ft=PKGBUILD sw=2 ts=2 et
