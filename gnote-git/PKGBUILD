# Contributor: Andre Klitzing  <aklitzing () online () de>
pkgname=gnote-git
pkgver=20090531
pkgrel=1
pkgdesc="Experimental port of Tomboy to C++ (git version)"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gnote"
license=('GPL')
depends=('gtkmm' 'boost' 'gtkspell' 'libxml2')
makedepends=('intltool' 'gnome-doc-utils' 'git')
source=()
md5sums=()

_gitroot="git://git.gnome.org/gnote"
_gitname="gnote"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/build"
  git clone "$srcdir/$_gitname" "$srcdir/build"
  cd "$srcdir/build"

  ./autogen.sh --prefix=/usr
  make || return 1
  make DESTDIR="$pkgdir/" install || return 1
}

