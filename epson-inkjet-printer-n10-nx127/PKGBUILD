# Contributor: Reynaldi Riva <boyfe98@yahoo.com>
# This PKGBUILD is adapted from epson-inkjet-printer-stylus-nx110-series PKGBUILD by Andre Klitzing with few minor changes

pkgname=epson-inkjet-printer-n10-nx127
_pkgname_filter=epson-inkjet-printer-filter
pkgver=1.0.0
pkgrel=1
pkgdesc="Printer driver for Epson N10, NX127, SX125, T12, etc (see avasys.jp). Also works for Epson L100 (Indonesia)"
arch=('i686' 'x86_64')
url="http://avasys.jp/eng/linux_driver/download/lsb/epson-inkjet/escp/"
license=('GPL' 'LGPL' 'custom:AVASYS Public Licence')
depends=('cups')
makedepends=('rpmextract' 'libtool')
if [ "$CARCH" = "x86_64" ]; then
   source=(http://linux.avasys.jp/drivers/lsb/epson-inkjet/stable/SRPMS/$pkgname-$pkgver-1lsb3.2.src.rpm
           http://linux.avasys.jp/drivers/lsb/epson-inkjet/stable/RPMS/x86_64/$pkgname-$pkgver-1lsb3.2.x86_64.rpm)
   md5sums=('b0ae0112bb93e76f39db4ffec6321622'
            'ff7dacd91f888988be7b4935b7867633')
else
   source=(http://linux.avasys.jp/drivers/lsb/epson-inkjet/stable/SRPMS/$pkgname-$pkgver-1lsb3.2.src.rpm
           http://linux.avasys.jp/drivers/lsb/epson-inkjet/stable/RPMS/i486/$pkgname-$pkgver-1lsb3.2.i486.rpm)
   md5sums=('b0ae0112bb93e76f39db4ffec6321622'
	    '893faa565bf27239648ba0d6601f93bd')

fi

build() {
  cd "$startdir/src"
  rpmextract.sh $pkgname-$pkgver-1lsb3.2.src.rpm || return 1
  tar xzf $pkgname-$pkgver.tar.gz || return 1
  tar xzf $_pkgname_filter-$pkgver.tar.gz || return 1
  if [ "$CARCH" = "x86_64" ]; then
     rpmextract.sh $pkgname-$pkgver-1lsb3.2.x86_64.rpm || return 1
  fi

  cd $_pkgname_filter-$pkgver
  aclocal
  libtoolize
  chmod +x configure
  # if you have runtime problems: add "--enable-debug" and look into /tmp/epson-inkjet-printer-filter.txt
  ./configure LDFLAGS="$LDFLAGS -Wl,--no-as-needed" --prefix=/opt/$pkgname || return 1
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "$pkgdir/opt/$pkgname/"
  if [ "$CARCH" = "x86_64" ]; then
     cp -r $srcdir/opt/$pkgname/lib64/ "$pkgdir/opt/$pkgname/"
  else
     cp -r lib "$pkgdir/opt/$pkgname/"
  fi
  cp -r resource "$pkgdir/opt/$pkgname/"
  cp -r watermark "$pkgdir/opt/$pkgname/"
  install -d "$pkgdir/usr/share/cups/model/$pkgname"
  install -m 644 ppds/* "$pkgdir/usr/share/cups/model/$pkgname"

  cd "$srcdir/$_pkgname_filter-$pkgver"
  install -d "$pkgdir/opt/$pkgname/cups/lib/filter/"
  install -m 755 src/epson_inkjet_printer_filter "$pkgdir/opt/$pkgname/cups/lib/filter/epson_inkjet_printer_filter"
}
