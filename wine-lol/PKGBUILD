 
# $Id: PKGBUILD 32257 2010-11-12 21:09:55Z bluewind $
# Maintainer: James Byrne <compaked@gmail.com> 
# Contributor: Sven-Hendrik <sh@lutzhaase.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>


basename=wine
bename=lol
pkgname=wine-lol
pkgver=1.3.17
pkgrel=1

_pkgbasever=${pkgver/rc/-rc}

source=(http://ibiblio.org/pub/linux/system/emulators/$basename/$basename-$_pkgbasever.tar.bz2
	ole-force-inproc-instead-of-local.patch)

md5sums=('f8d9d1d8cdab27ad311fa3743a1a0bc5'
         '58091d383f16a71fde88fee4eac7b602')

pkgdesc="A compatibility layer for running Windows programs"
url="http://www.winehq.com"
arch=(i686 x86_64)
license=(LGPL)

depends=(
  fontconfig      lib32-fontconfig
  mesa            lib32-mesa 
  libxcursor      lib32-libxcursor
  libxrandr       lib32-libxrandr
  libxdamage      lib32-libxdamage
  libxxf86dga     lib32-libxxf86dga
  alsa-lib        lib32-alsa-lib
)

makedepends=(autoconf ncurses bison perl fontforge flex prelink
  'gcc>=4.5.0-2'  'gcc-multilib>=4.5.0-2'
  giflib          lib32-giflib
  libxpm          lib32-libxpm
  libpng          lib32-libpng
  libxinerama     lib32-libxinerama
  libxcomposite   lib32-libxcomposite
  libxmu          lib32-libxmu
  libxxf86vm      lib32-libxxf86vm
  libxml2         lib32-libxml2
  libxslt         lib32-libxslt
  libldap         lib32-libldap
  lcms            lib32-lcms
  mpg123          lib32-mpg123
  openal          lib32-openal
  jack            lib32-jack
  libcups         lib32-libcups
  gnutls          lib32-gnutls
  v4l-utils       lib32-v4l-utils
)
  
optdepends=(
  giflib          lib32-giflib
  libpng          lib32-libpng
  libldap         lib32-libldap
  lcms            lib32-lcms
  libxml2         lib32-libxml2
  mpg123          lib32-mpg123
  openal          lib32-openal
  jack            lib32-jack
  libcups         lib32-libcups
  gnutls          lib32-gnutls
  v4l-utils       lib32-v4l-utils
)

if [[ $CARCH == i686 ]]; then
  # Strip lib32 etc. on i686
  depends=('fontconfig' 'mesa' 'libxcursor' 'libxrandr' 'libxdamage' 'libxxf86dga' 'alsa-lib' 'lib32-mesa' 'lib32-fontconfig' 'lib32-libxcursor' 'lib32-libxrandr' 'lib32-libxdamage' 'lib32-libxxf86dga' 'lib32-alsa-lib')
  makedepends=(${makedepends[@]/*32-*/})
  makedepends=(${makedepends[@]/*-multilib*/})
  optdepends=(${optdepends[@]/*32-*/})
else
  provides=("bin32-wine=$pkgver")
  conflicts=('bin32-wine')
  replaces=('bin32-wine')
fi

build() {
  cd "$srcdir"

  # Allow ccache to work
  mv $basename-$_pkgbasever wine
  
  msg2 "Patching..."
  patch -Np1 -i "$srcdir/ole-force-inproc-instead-of-local.patch" -d "wine"

  # Get rid of old build dirs
  rm -rf $basename-32-build
  mkdir $basename-32-build

  msg2 "Building Wine-32..."
  cd "$srcdir/$basename-32-build"
  ../$basename/configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-x \
    --with-gnutls \
    "${_wine32opts[@]}"

  make -j`grep 'processor' /proc/cpuinfo | wc -l` 
}

package() {
  msg2 "Packaging Wine-32..."
  cd "$srcdir/$basename-32-build"

  if [[ $CARCH == i686 ]]; then
    make prefix="$pkgdir/usr" install
  else
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib32" \
      dlldir="$pkgdir/usr/lib32/wine" install
  fi
}
