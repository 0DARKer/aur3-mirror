# $Id$
# Maintainer: Egon Ashrafinia <wubbbi@gmx.de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=wine-lol
pkgver=1.3.35
pkgrel=1

_pkgrealname="wine"

source=(http://ibiblio.org/pub/linux/system/emulators/$_pkgrealname/$_pkgrealname-$pkgver.tar.bz2
        ole-force-inproc-instead-of-local.patch)
md5sums=('5833787fcd7fee5a2299fade10a2760d'
         '58091d383f16a71fde88fee4eac7b602')

pkgdesc="A compatibility layer for running Windows programs. This version of Wine includes a patch to run League of Legends."
url="http://www.winehq.com"
arch=(i686 x86_64)
license=(LGPL)
install=wine.install

depends=(
  fontconfig      lib32-fontconfig
  mesa            lib32-mesa 
  libxcursor      lib32-libxcursor
  libxrandr       lib32-libxrandr
  libxdamage      lib32-libxdamage
  libxi           lib32-libxi
  gettext         lib32-gettext
  desktop-file-utils
)

makedepends=(autoconf ncurses bison perl fontforge flex prelink
  'gcc>=4.5.0-2'  'gcc-multilib>=4.5.0-2'
  giflib          lib32-giflib
  libxpm          lib32-libxpm
  libpng          lib32-libpng
  libxinerama     lib32-libxinerama
  libxcomposite   lib32-libxcomposite
  libxmu          lib32-libxmu
  libxxf86vm      lib32-libxxf86vm
  libxml2         lib32-libxml2
  libxslt         lib32-libxslt
  libldap         lib32-libldap
  lcms            lib32-lcms
  mpg123          lib32-mpg123
  openal          lib32-openal
  libcups         lib32-libcups
  v4l-utils       lib32-v4l-utils
  alsa-lib        lib32-alsa-lib
  oss
)
  
optdepends=(
  giflib          lib32-giflib
  libpng          lib32-libpng
  libldap         lib32-libldap
  lcms            lib32-lcms
  libxml2         lib32-libxml2
  mpg123          lib32-mpg123
  openal          lib32-openal
  libcups         lib32-libcups
  v4l-utils       lib32-v4l-utils
  libpulse        lib32-libpulse
  alsa-plugins    lib32-alsa-plugins
  alsa-lib        lib32-alsa-lib
  oss
)

if [[ $CARCH == i686 ]]; then
  # Strip lib32 etc. on i686
  depends=(${depends[@]/*32-*/})
  makedepends=(${makedepends[@]/*32-*/})
  makedepends=(${makedepends[@]/*-multilib*/})
  optdepends=(${optdepends[@]/*32-*/})
  provides=("bin32-wine=$pkgver" "wine=$pkgver")
  conflicts=('bin32-wine' 'wine')
  replaces=('bin32-wine' 'wine')
else
  provides=("bin32-wine=$pkgver" "wine-wow64=$pkgver" "wine=$pkgver")
  conflicts=('bin32-wine' 'wine-wow64' 'wine')
  replaces=('bin32-wine' 'wine')
fi

build() {
  cd "$srcdir"

  # Allow ccache to work
  mv $_pkgrealname-$pkgver $_pkgrealname

  msg2 "Patching..."
  (
    cd $_pkgrealname
    patch -p1 < ../../ole-force-inproc-instead-of-local.patch
  )

  # Get rid of old build dirs
  rm -rf $_pkgrealname-{32,64}-build
  mkdir $_pkgrealname-32-build

  if [[ $CARCH == x86_64 ]]; then
    msg2 "Building Wine-64..."

    mkdir $_pkgrealname-64-build
    cd "$srcdir/$_pkgrealname-64-build"
    ../$_pkgrealname/configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --libdir=/usr/lib \
      --with-x \
      --enable-win64

    make

    _wine32opts=(
      --libdir=/usr/lib32
      --with-wine64="$srcdir/$_pkgrealname-64-build"
    )

    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

  msg2 "Building Wine-32..."
  cd "$srcdir/$_pkgrealname-32-build"
  ../$_pkgrealname/configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-x \
    "${_wine32opts[@]}"

  # These additional CFLAGS solve FS#27560
  make CFLAGS+="-mstackrealign" CXXFLAGS+="-mstackrealign"
}

package() {
  msg2 "Packaging Wine-32..."
  cd "$srcdir/$_pkgrealname-32-build"

  if [[ $CARCH == i686 ]]; then
    make prefix="$pkgdir/usr" install
  else
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib32" \
      dlldir="$pkgdir/usr/lib32/wine" install

    msg2 "Packaging Wine-64..."
    cd "$srcdir/$_pkgrealname-64-build"
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib" \
      dlldir="$pkgdir/usr/lib/wine" install
  fi
}

# vim:set ts=8 sts=2 sw=2 et:
