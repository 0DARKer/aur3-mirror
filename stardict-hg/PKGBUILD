# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=stardict-hg
pkgver=583
pkgrel=1
pkgdesc="International dictionary software"
arch=('i686' 'x86_64')
license=('GPL')
depends=('espeak' 'libsigc++' 'enchant' 'gtk2' 'libsm')
makedepends=('mercurial' 'popt' 'intltool' 'gnome-doc-utils' 'gnome-common')
url="http://www.stardict.org/"
provides=('stardict')
conflicts=('stardict')
options=('!libtool' '!emptydirs')
source=('NetDictRequests.patch'
  'gcc46.patch')
md5sums=('39ff24cf5634d5e7eeb6d93f3467460c'
         '52ff47441c503de07add4b7688510bc5')

_hgroot="https://code.google.com/p/stardict-3"
_hgrepo="stardict"

build() {
  cd $srcdir
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf $srcdir/$_hgrepo-build
  cp -r $srcdir/$_hgrepo $srcdir/$_hgrepo-build
  cd $srcdir/$_hgrepo-build

  # ugly hack to avoid crashing with empty dictionaries
  patch -p1 -i ../NetDictRequests.patch
  patch -p1 -i ../gcc46.patch
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --disable-gnome-support \
    --disable-schemas-install \
    --disable-festival \
    --disable-qqwry \
    --disable-updateinfo \
    --disable-gucharmap \
    --disable-static \
    --disable-tools
  make
}

package() {
  cd $srcdir/$_hgrepo-build
  make DESTDIR=$pkgdir install
}
