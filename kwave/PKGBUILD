# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Panagiotis Papadopoulos (pano_90 <at> gmx <dot> net)

pkgname=kwave
pkgver=0.8.12
pkgrel=2
pkgdesc="A sound editor for KDE"
arch=('i686' 'x86_64')
url="http://kwave.sourceforge.net/"
license=('GPL')
depends=('audiofile>=0.3.0' 'flac' 'fftw' 'kdelibs' 'libvorbis' 'libsamplerate' 'opus')
makedepends=('cmake' 'gettext' 'imagemagick' 'kdesdk-poxml' 'automoc4')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-1.tar.bz2
        ${pkgname}-${pkgver}-doc-fr.patch)
sha256sums=('88c1fb9fd55b6a7fe230d3257268f8c91d5b6ddd6106b4a1d791bb671e6d4b0b'
            'ad59d0ce096b41387af2320cb6be7145d471959c2da3d3a06a2f95d2285f29fc')  

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Documentation build fails with KDE 4.14+ http://sourceforge.net/p/kwave/bugs/40/
  patch -Np0 -i "${srcdir}/${pkgname}-${pkgver}-doc-fr.patch"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
  -DLIB_INSTALL_DIR=/usr/lib \
  -DCMAKE_BUILD_TYPE=Release \
  -DWITH_PULSEAUDIO=OFF \
  -DWITH_PHONON=OFF \
  -DWITH_MP3=OFF 
  #disable Phonon support, since it currently does not work, and only makes Kwave crash 
  #disable MP3 support, since it doesn't compile although dependency id3lib is installed
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
