# Maintainer: Jack L. Frost <fbt@fleshless.org>

pkgname=( vdev-git vdev-libudev-compat-git )
pkgver=r447.36fa5ca
pkgrel=1
pkgdesc='A virtual device manager for *nix'
url='https://github.com/jcnelson/vdev.git'
arch=( 'x86_64' 'i686' )
license=( 'ISC' )
conflicts=( 'vdev' )
provides=( 'vdev' )
makedepends=( 'libpstat' 'fskit' )

source=( "${pkgname}::git+${url}" )
sha1sums=( 'SKIP' )

pkgver() {
	cd "${pkgname}"

	if git_version=$( git describe --long --tags 2>/dev/null ); then
		IFS='-' read last_tag tag_rev commit <<< "$git_version"
		printf '%s.r%s.%s' "$last_tag" "$tag_rev" "$commit"
	else
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	fi
}

build() {
	cd "${pkgname}"
	make PREFIX=/usr
	make -C libudev-compat
}

package_vdev-git() {
	depends=( 'libpstat' 'fskit' 'util-linux' 'kmod' 'iproute2' 'sed' 'grep' 'device-mapper' 'lvm2' 'fuse' )

	cd "$pkgname"

	########### From the makefile ################
	#                                            #
	# PREFIX         ?= /usr/local               #
	# BINDIR         ?= $(PREFIX)/bin            #
	# SBINDIR        ?= $(PREFIX)/sbin           #
	# LIBDIR         ?= $(PREFIX)/lib            #
	# INCLUDEDIR     ?= /usr/local/include       #
	# USRBINDIR      ?= /usr/bin                 #
	# USRSBINDIR     ?= /usr/sbin                #
	# USRSHAREDIR    ?= /usr/share               #
	# PKGCONFIGDIR   ?= $(PREFIX)/lib/pkgconfig  #
	# ETCDIR         ?= /etc                     #
	#                                            #
	##############################################

	rm build/.install.mk
	make PREFIX="${pkgdir}/usr" \
	     INCLUDEDIR="${pkgdir}/usr/include" \
	     SBINDIR="${pkgdir}/usr/bin" \
	     USRSBINDIR="${pkgdir}/usr/bin" \
	     ETCDIR="${pkgdir}/etc" \
	install

	cd "$pkgdir"

	# There is no way to tell the Makefile not to install these.
	rm etc/init.d/vdev
	rmdir etc/init.d
}

package_vdev-libudev-compat-git() {
	provides=( "libudev=${_udevver}" 'libgudev-1.0.so' 'libudev.so' )
	conflicts=( "libudev=${_udevver}" 'libgudev-1.0.so'  'libudev.so'	)

	cd vdev-git
	make PREFIX="${pkgdir}/usr" -C libudev-compat install
}

# vim: noet 
