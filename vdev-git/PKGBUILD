# Maintainer: Jack L. Frost <fbt@fleshless.org>

pkgname=vdev-git
pkgver=r428.9f969a2
pkgrel=2
pkgdesc='A virtual device manager for *nix'
url='https://github.com/jcnelson/vdev.git'
arch=( 'x86_64' 'i686' )
license=( 'ISC' )
depends=( 'libpstat' 'fskit' )
conflicts=( 'vdev' )
provides=( 'vdev' )

source=( "${pkgname}::git+${url}" )
sha1sums=( 'SKIP' )

pkgver() {
	cd "${pkgname}"

	if git_version=$( git describe --long --tags 2>/dev/null ); then
		IFS='-' read last_tag tag_rev commit <<< "$git_version"
		printf '%s.r%s.%s' "$last_tag" "$tag_rev" "$commit"
	else
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	fi
}

build() {
	cd "${pkgname}"
	make

	make -C libvdev
	make -C vdevd
	make -C fs
	make -C example
}

package() {
	cd "${pkgname}"
	make PREFIX="${pkgdir}/usr" -C libvdev install
	make PREFIX="${pkgdir}/usr" -C vdevd install
	make PREFIX="${pkgdir}/usr" -C fs install
	make PREFIX="${pkgdir}" -C example install

	cd "${pkgdir}"

	# BINDIR breaks helpers, so here's a crutch
	mv usr/sbin usr/bin

	# There is no way to tell the Makefile not to install these.
	rm etc/init.d/vdev
	rmdir etc/init.d
}

# vim: noet 
