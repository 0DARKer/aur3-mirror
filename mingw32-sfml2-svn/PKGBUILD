# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sven Schneider <mr_xyz_unknown@web.de>
# Contributor: Dustin Mays <dork.fish.wat@gmail.com>
# Contributor: scj <scj archlinux us>

pkgname=mingw32-sfml2-svn
pkgver=1790
pkgrel=1
pkgdesc="A simple, fast, cross-platform, and object-oriented multimedia API"
arch=('i686' 'x86_64')
url="http://sfml-dev.org"
license=('zlib')
depends=('mingw32-libsndfile' 'libxrandr' 'mingw32-libjpeg' 'mingw32-libpng' 'mingw32-openal' 'mingw32-glew' 'mingw32-freetype')
makedepends=('mesa' 'cmake' 'subversion')
provides=('mingw32-sfml2')
source=(cmake-toolchain.cmake)
md5sums=('9fcb99c1e2504f504b4780c3f503bc3f')

_svntrunk=https://sfml.svn.sourceforge.net/svnroot/sfml/branches/sfml2
_svnmod=sfml2

build() {
  rm -rf ${srcdir}/$_svnmod-build
  
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver && cd ${srcdir})
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi
  
  msg "SVN checkout done or server timeout."
  
  svn export $_svnmod $_svnmod-build
  cd $_svnmod-build
  
  msg "Starting make."
  
  unset LDFLAGS
  
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr/i486-mingw32/ \
    -DCMAKE_TOOLCHAIN_FILE=${srcdir}/cmake-toolchain.cmake
  make
}

package() {
  cd $_svnmod-build
  
  make DESTDIR=${pkgdir} install

  install -Dm 644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -r $pkgdir/usr/share/cmake-2.8
  rm $pkgdir/usr/i486-mingw32/bin/libsndfile-1.dll
  rm $pkgdir/usr/i486-mingw32/bin/openal32.dll
  rm $pkgdir/usr/i486-mingw32/license.txt
  mkdir -p $pkgdir/usr/i486-mingw32/lib
  mv $pkgdir/usr/i486-mingw32/bin/*.dll $pkgdir/usr/i486-mingw32/lib/
}

# vim:ts=2:sw=2
