# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=buddycloud-server
pkgver=0.3.5
pkgrel=1
pkgdesc="A fully distributed social network based on XMPP - server"
arch=(i686 x86_64)
url="https://buddycloud.org/wiki/Main_Page"
license=('Apache')
depends=('nodejs' 'icu' 'expat' 'postgresql-libs')
optdepends=('buddycloud-webclient: the buddycloud web client'
            'prosody: a lightweight and extensible XMPP server'
            'ejabberd: a powerful XMPP server'
            'logrotate: rotate log file automatically'
            "start-stop-daemon: daemon managment if you're not using systemd")
backup=('etc/buddycloud-server/config.js'
        'etc/logrotate.d/buddycloud-server')
install=buddycloud-server.install
source=("https://github.com/downloads/buddycloud/$pkgname/$pkgname-$pkgver.tar.gz"
        'buddycloud-server.rc'
        'buddycloud-server.logrotate'
        'buddycloud-server.service')
md5sums=('911a0bd517781dc46e8a00c41ffa53b3'
         '64ea2b4f6ace15fe057a7029447ded2d'
         '555033f0880c2a40b5e16418b4f2a9f9'
         '652aa13d643a869e040c504236770818')
sha256sums=('923686b7f74075f7cc4af15c798750a48ac916197159469dd0972a6bcd625819'
            '0bbc3ba58abc520552c9fd47d948a7eb4dacd216dd89f437952fc7418b43c1c8'
            '1696cb64db6fe76c5c32cc32637f485f86f8ea79b0d89ecf5ac4d1615c8f9f4c'
            'ed76e58355bf2ce58cb95b808f22648acff58deba04cb2226f8d6bebfa4e2821')
noextrtact=("$pkgname-$pkgver.tar.gz")

build() {
  cd "$srcdir"
  mkdir "$pkgname"
  cd "$pkgname"
  bsdtar -xf "$srcdir/$pkgname-$pkgver.tar.gz"
  ./build-deps
}

package() {
  mkdir -p "$pkgdir/opt"
  cp -R "$srcdir/$pkgname" "$pkgdir/opt/$pkgname"

  cd "$pkgdir/opt/$pkgname"

  install -Dm644 config.js.example "$pkgdir/etc/buddycloud-server/config.js"
  ln -sf ../../etc/buddycloud-server/config.js "$pkgdir/opt/$pkgname/config.js"

  rm config.js.example _etc_init.d_buddycloud-server

  install -Dm755 "$srcdir/buddycloud-server.rc" "$pkgdir/etc/rc.d/buddycloud-server"
  install -Dm644 "$srcdir/buddycloud-server.service" "$pkgdir/usr/lib/systemd/system/buddycloud-server.service"
  install -Dm644 "$srcdir/buddycloud-server.logrotate" "$pkgdir/etc/logrotate.d/buddycloud-server"
}

# vim:set ts=2 sw=2 et:
