# Maintainer: Thomas Jost <schnouki@schnouki.net>
pkgname=buddycloud-server
pkgver=0.3.4
pkgrel=1
pkgdesc="A fully distributed social network based on XMPP - server"
arch=(i686 x86_64)
url="https://buddycloud.org/wiki/Main_Page"
license=('Apache')
depends=('nodejs' 'icu' 'expat' 'postgresql-libs' 'start-stop-daemon')
optdepends=('buddycloud-webclient: the buddycloud web client'
            'prosody: a lightweight and extensible XMPP server'
            'ejabberd: a powerful XMPP server'
            'logrotate: rotate log file automatically')
backup=('etc/buddycloud-server/config.js'
        'etc/logrotate.d/buddycloud-server')
install=buddycloud-server.install
source=("https://github.com/downloads/buddycloud/$pkgname/$pkgname-$pkgver.tar.gz"
        'buddycloud-server.rc'
        'buddycloud-server.logrotate')
md5sums=('4ed638087055144096acd31c83baafaa'
         '64ea2b4f6ace15fe057a7029447ded2d'
         '555033f0880c2a40b5e16418b4f2a9f9')
sha256sums=('07dfba1c972c0a3341cc2c404d0afe28b0f0540665ad8b7c81274556186fcd01'
            '0bbc3ba58abc520552c9fd47d948a7eb4dacd216dd89f437952fc7418b43c1c8'
            '1696cb64db6fe76c5c32cc32637f485f86f8ea79b0d89ecf5ac4d1615c8f9f4c')
noextrtact=("$pkgname-$pkgver.tar.gz")

build() {
  cd "$srcdir"
  mkdir "$pkgname"
  cd "$pkgname"
  bsdtar -xf "$srcdir/$pkgname-$pkgver.tar.gz"
  ./install
}

package() {
  mkdir -p "$pkgdir/opt"
  cp -R "$srcdir/$pkgname" "$pkgdir/opt/$pkgname"

  cd "$pkgdir/opt/$pkgname"

  install -Dm644 config.js.example "$pkgdir/etc/buddycloud-server/config.js"
  ln -sf ../../etc/buddycloud-server/config.js "$pkgdir/opt/$pkgname/config.js"

  rm config.js.example _etc_init.d_buddycloud-server

  install -Dm755 "$srcdir/buddycloud-server.rc" "$pkgdir/etc/rc.d/buddycloud-server"
  install -Dm644 "$srcdir/buddycloud-server.logrotate" "$pkgdir/etc/logrotate.d/buddycloud-server"
}

# vim:set ts=2 sw=2 et:
