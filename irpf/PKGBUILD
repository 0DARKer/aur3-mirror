# Maintainer: Lara Maia <lara@craft.net.br>

pkgname=irpf
pkgver=2013.1.0
pkgrel=2
license='custom'

arch=(any)
pkgdesc='Programa Oficial da Receita para elaboração do IRPF'
url='http://www.receita.fazenda.gov.br'

source=(http://www28.receita.fazenda.gov.br/publico/programas/IRPF/2013/PGDIRPF2013/Java/zip/IRPF2013-1.0.zip
        Copyright
        $pkgname.png
        $pkgname.desktop
        $pkgname.install)
        
md5sums=(611a3aeae33605aa2fe05bc87a515443
         b89b755901fbc7a813e044c077bc4dcf
         43d8c9617118578f03b2c4eeb1c72c57
         5c3f364ed6e30e8aac69ad4394deabba
         66ffa626d31a826c5bb1af465f0829f0)

depends=('java-runtime>=6' 'hicolor-icon-theme' 'desktop-file-utils')
install=$pkgname.install

prepare() {
	cd "$srcdir"/IRPF2013
	sed -i 's|java|cd /usr/share/irpf\njava|' exec.sh
	sed -i 's|irpf.jar|/usr/share/irpf/irpf.jar|' exec.sh
}

package() {
	cd "$srcdir"/IRPF2013
	
	mkdir -p "$pkgdir"/usr/share/{icons/hicolor/128x128/apps,applications,licenses/irpf,irpf}
	
	cp -rf tutorial "$pkgdir"/usr/share/irpf/
	cp -rf lib "$pkgdir"/usr/share/irpf/
	
	install -Dm644 irpf.jar "$pkgdir"/usr/share/irpf/
	install -Dm644 IRPF.chm "$pkgdir"/usr/share/irpf/
	install -Dm644 IRPF2013.ini "$pkgdir"/usr/share/irpf/
	install -Dm644 IRPF2013.acb "$pkgdir"/usr/share/irpf/
	install -Dm755 exec.sh "$pkgdir"/usr/bin/irpf
	
	install -Dm644 "$srcdir"/Copyright "$pkgdir"/usr/share/licenses/irpf/
	
	install -Dm644 "$srcdir"/irpf.png "$pkgdir"/usr/share/icons/hicolor/128x128/apps/
	install -Dm644 "$srcdir"/irpf.desktop "$pkgdir"/usr/share/applications/
}
