# Contributor & Maintainer: machoo02 <ichthyoboy@gmail.com>
# Contributor: mutlu_inek <mutlu_inek@yahoo.de>

pkgname=bibus
pkgver=1.5.1
pkgrel=2
pkgdesc="A bibliographic database that can directly insert references in \
OpenOffice.org and generate the bibliographic index."
url="http://sourceforge.net/projects/bibus-biblio/"
license="GPL"
arch=('any')
depends=('python-pysqlite' 'wxpython' 'pyxml' 'unixodbc' 'hicolor-icon-theme')
optdepends=('openoffice-base')
source=(http://downloads.sourceforge.net/$pkgname-biblio/$pkgname-$pkgver.tar.gz)
md5sums=('9d1808f98556b58b3f709bebfba11a80')
install=${pkgname}.install
backup=(etc/bibus.config)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make -f Setup/Makefile DESTDIR="${pkgdir}/usr" python=/usr/bin/python2 \
        sysconfdir="${pkgdir}/etc" install
  rm "${pkgdir}/usr/bin/bibus"
  ln -s /usr/share/bibus/bibusStart.py "$pkgdir/usr/bin/bibus"
  rm "$pkgdir/usr/share/bibus/Setup/uninstall.sh"
  
  ## Fix .py files for python2
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      $(find $pkgdir -name '*.py')
      
  sed -i -e "s|${pkgdir}||g" "${pkgdir}/usr/share/bibus/bibus.cfg" 
}
