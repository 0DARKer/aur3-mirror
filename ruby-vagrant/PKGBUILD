# Author: Jochen Schalanda <jochen+aur AT schalanda DOT name>
# Maintainer: Olivier Biesmans <o DOT archlinux AT biesmans DOT fr>
# Contributor: Mathieu Clabaut <mathieu DOT clabaut AT gmail DOT com>
# Contributor: helios <aur AT wiresphere DOT de>
_name=vagrant
pkgname=ruby-$_name
pkgver=1.4.0
pkgrel=1
pkgdesc="Tool for building and distributing virtualized development environments"
arch=('i686' 'x86_64')
url='http://vagrantup.com/'
license=('MIT')
depends=('ruby'  'ruby-childprocess>=0.3.7' 'ruby-erubis>=2.7.0' \
         'ruby-i18n>=0.6.0' 'ruby-log4r>=1.1.9'  'ruby-net-ssh==2.6.6' \
         'ruby-net-scp>=1.1.0')
makedepends=('ruby')
provides=('vagrant')
conflicts=('vagrant')
options=(!strip)
source=("https://github.com/mitchellh/${_name}/archive/v${pkgver}.tar.gz")
md5sums=('42e6c220b70de8337164d54e41822882')

build() {
  cd "${srcdir}/${_name}-${pkgver}"
  gem build ${_name}.gemspec
  #bundle install --path=bundle
  #bundle exec rake build
}

package() {
  cd "${srcdir}/${_name}-${pkgver}"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  gem install --no-user-install --ignore-dependencies \
    -i "${pkgdir}${_gemdir}" -n "${pkgdir}"/usr/bin "pkg/${_name}-${pkgver}.gem"
}

# vim:set ts=2 sw=2 sts=2 et:
