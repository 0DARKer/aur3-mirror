# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=xombrero
pkgver=1.0.0
pkgrel=1
pkgdesc="A minimalists web browser, vi-like while maintaining traditional web browser behavior. Formerly xxxterm."
arch=('i686' 'x86_64')
url="https://opensource.conformal.com/wiki/xombrero"
license=('custom:ISC')
depends=('libwebkit' 'gtk2' 'libbsd')
optdepends=()
replaces=('xxxterm')
source=(http://opensource.conformal.com/snapshots/$pkgname/$pkgname-$pkgver.tgz \
        LICENSE)
md5sums=('c5cb56a106f6df395bc1d323b90e2d16'
         'f3eeb6e8b70a3dcccb8ee57daf584c9e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|LIBS= gtk+-2.0|& javascriptcoregtk-1.0|' linux/Makefile
  sed -i 's|/etc/ssl/cert.pem|/etc/ssl/certs/ca-certificates.crt|' xombrero.conf
  # no spam
  sed -i 's|https://www.cyphertite.com|https://archlinux.org|' xombrero.{c,conf}
  sed -i 's|/usr/local/share/xombrero/|/usr/share/xombrero/|' xombrero.{c,conf}
  cd linux
  sed -i 's/gnutls/& libbsd/' Makefile
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver/linux"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  cd ..
  install -Dm644 xombrero.conf "$pkgdir/etc/skel/xombrero.conf"
  install -Dm644 xombrero.desktop "$pkgdir/usr/share/applications/xombrero.desktop"
  cd ../..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

