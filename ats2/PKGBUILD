# Maintainer: Ben Darwin <bcdarwin@gmail.com>
# Contributor: flu

pkgname=ats2
_pkgname=ATS2-Postiats
pkgver=0.0.7
pkgrel=1
pkgdesc="An ML derivative with linear and dependent types for safe systems programming."
arch=('i686' 'x86_64')
url="http://www.ats-lang.org/"
license=('GPL3')
depends=('sh')
optdepends=('gmp')
provides=('ats2')
options=('staticlibs')
source=(
  "http://downloads.sourceforge.net/project/ats2-lang/ats2-lang/ats2-postiats-$pkgver/$_pkgname-$pkgver.tgz")
sha256sums=('dda17a39f79a61e3d8e89fa6b18918827856d79c51004be7f846bae4946267b3')
build() {
  cd "$srcdir/$_pkgname-$pkgver"
  ./configure --prefix=/usr
  make -j1 all
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" -j1 install

  mkdir -p "$pkgdir/etc/profile.d"
  echo "export PATSHOME=/usr/lib/ats2-postiats-$pkgver" > "$pkgdir/etc/profile.d/${pkgname}.sh"
}

# vim:set ts=2 sw=2 et:
