# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Arch Haskell Team <>

pkgname=haskell-clientsession
_hkgname=clientsession
_licensefile=LICENSE
pkgver=0.8.0
pkgrel=1
pkgdesc="Securely store session data in a client-side cookie."
url="http://github.com/snoyberg/clientsession/tree/master"
license=('BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-base64-bytestring'
         'haskell-bytestring'
         'haskell-cereal'
         'haskell-cprng-aes'
         'haskell-crypto-api'
         'haskell-cryptocipher'
         'haskell-directory'
         'haskell-entropy'
         'haskell-skein'
         'haskell-tagged')
options=('strip')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('9318fc543c6c238041e4f605a8b87a59e9406485c55c21128f996f4ae22cedfa')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    
    runhaskell Setup configure -O --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 ${_licensefile} ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/${_licensefile}
}
