_pkgbasename=physfs
pkgname=lib32-$_pkgbasename
pkgver=2.0.2
pkgrel=2
pkgdesc="A library to provide abstract access to various archives (32 bit)"
arch=('x86_64')
url="http://icculus.org/physfs/"
license=('custom:physfs')
depends=('lib32-zlib' "${_pkgbasename}")
makedepends=('cmake' 'doxygen' 'gcc-multilib' 'lib32-readline')
source=("http://icculus.org/physfs/downloads/${_pkgbasename}-${pkgver}.tar.gz"
	'physfs-2.0.2-gcc46.patch')
md5sums=('4e8927c3d30279b03e2592106eb9184a'
         '0c609d5026142a68cf8da78af1f30995')
sha1sums=('2d3d3cc819ad26542d34451f44050b85635344d0'
          'dbd437e0e960bfe6adf17ec32138f98b6b108252')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # http://www.mail-archive.com/opensuse-commit@opensuse.org/msg01328.html
  patch -p0 < "${srcdir}/physfs-2.0.2-gcc46.patch"
  
  rm -rf build
  mkdir build && cd build 
  export CFLAGS="$CFLAGS -m32 -fno-strict-aliasing"
  export CXXFLAGS="$CXXFLAGS -m32 -fno-strict-aliasing"
  cmake .. \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DLIB_SUFFIX=32 \
  	-DPHYSFS_BUILD_WX_TEST=no  	
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}/build"  
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include}
  install -Dm644 ../LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
