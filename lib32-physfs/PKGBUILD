_pkgbasename=physfs
pkgname=lib32-$_pkgbasename
pkgver=2.0.2
pkgrel=1
pkgdesc="A library to provide abstract access to various archives (32 bit)"
arch=('x86_64')
url="http://icculus.org/physfs/"
license=('zlib')
depends=('lib32-zlib' 'lib32-readline' "${_pkgbasename}")
makedepends=('cmake' 'doxygen' 'gcc-multilib')
source=(http://icculus.org/physfs/downloads/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('4e8927c3d30279b03e2592106eb9184a')
sha1sums=('2d3d3cc819ad26542d34451f44050b85635344d0')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  mkdir build && cd build 
  export CFLAGS="$CFLAGS -m32 -fno-strict-aliasing"
  export CXXFLAGS="$CXXFLAGS -m32 -fno-strict-aliasing"
  cmake .. \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DLIB_SUFFIX=32 \
  	-DPHYSFS_BUILD_WX_TEST=no
  	# WX test is not needed for lib32 and it giving me headaches.
  	
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}/build"
  
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{bin,include}
  mkdir -p ${pkgdir}/usr/share/licenses
  ln -s "${_pkgbasename}" "${pkgdir}/usr/share/licenses/${pkgname}"
}
