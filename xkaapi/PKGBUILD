# Maintainer: Kniyl <mathias dot ettinger at gmail dot com>

pkgname=xkaapi
pkgver=1.0.1
pkgrel=1
_fileid=30134
pkgdesc="C++ library that implements work stealing algorithm for multithreaded computation"
url="http://kaapi.gforge.inria.fr/dokuwiki/doku.php"
arch=('i686' 'x86_64')
license=('CeCILL')
depends=('glibc' 'hwloc')
source=("https://gforge.inria.fr/frs/download.php/${_fileid}/$pkgname-$pkgver.tar.gz")
md5sums=('3aec61d9ac4a1bdab7e606bccddd46fa')

build() {
  cd "${srcdir}"
  rm -rf ${pkgname}-build
  cp -r ${pkgname}-${pkgver//_/-} ${pkgname}-build

  msg "Applying patches..."
	sed -i 's|  bool work_queue|  inline bool work_queue|g' \
		${pkgname}-build/api/kastl/kastl/kastl_workqueue.h 
  
  cd ${pkgname}-build

  msg "Starting make..."
  ./configure \
    --prefix=/usr \
    --enable-mode=release \
    --disable-api-kaapif \
    --enable-api-kaapic \
    --enable-api-kaapixx \
    --enable-api-kastl 

  make
}

package() {
  cd ${pkgname}-build

  make DESTDIR="${pkgdir}" install
}
