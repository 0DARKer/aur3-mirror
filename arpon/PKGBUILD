# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=arpon
_realpkgname=ArpON
pkgver=2.1
pkgrel=3
pkgdesc="Arp handler inspectiON - a portable Arp handler"
arch=('i686' 'x86_64')
url="http://arpon.sourceforge.net/"
license=('custom')
depends=('bash' 'libnet' 'libpcap')
makedepends=('cmake' 'libdnet')
backup=('etc/conf.d/arpon')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$_realpkgname-$pkgver.tar.gz"
        "arpon"
        "arpon.conf.d")
md5sums=('c5e958af0d7068a12cb1fea656a759bd'
         '86f43d0040e3947c0509de1ed0902775'
         'f4ae37f8907bb24e3084725c9c66532c')

build() {
  cd "$srcdir/$_realpkgname-$pkgver"
  [ -d build ] || mkdir build
  cd build
  cmake -DPCAP_LIB_DIR=/usr/lib -DPCAP_LIBRARY=/usr/lib/libpcap.so ..
  make
}

package() {
  cd "$srcdir/$_realpkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install
  install -D -m644 "../LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m755 "$srcdir/arpon" "$pkgdir/etc/rc.d/arpon"
  install -D -m644 "$srcdir/arpon.conf.d" "$pkgdir/etc/conf.d/arpon"
}

# vim:set ts=2 sw=2 et:
