# Maintainer: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=1.15
pkgrel=3
pkgdesc="A tool for monitoring webpages for updates"
arch=('any')
url="http://thp.io/2008/urlwatch"
license=('BSD')
depends=('python2' 'python2-futures')
source=("http://thp.io/2008/urlwatch/$pkgname-$pkgver.tar.gz"
        '0001-add-smtp-support.patch'
        '0002-fix-parsing-content-encoding-headers.patch')
md5sums=('a0a739d4ea18f15c121e5f12bb75cbc9'
         '96a0d8308ec74621f92a002748bd481b'
         'b24cbeb3ca9469884ba6b135b8a389bd')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i "$srcdir/0001-add-smtp-support.patch"
    patch -Np1 -i "$srcdir/0002-fix-parsing-content-encoding-headers.patch"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    python2 setup.py build
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir"
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
