# Maintainer: Mathias Anselmann <mathias.anselmann@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >, lainme <lainme993@gmail.com>
# Contributor: Klimov Max <cleemmi@gmail.com>

pkgname=libcgns-paraview
pkgver=3.1.4
pkgrel=1
pkgdesc='General purpose library for the storage and retrieval of computational fluid dynamics analysis data by CGNS standard'
arch=('i686' 'x86_64')
url='http://www.cgns.org'
license=('custom')
conflicts=('libcgns2' 'libcgns')
depends=('tk' 'hdf5' 'libxmu' 'glu')
makedepends=('cmake')
changelog=$pkgname.changelog
source=("http://downloads.sourceforge.net/project/cgns/cgnslib_${pkgver:0:3}/cgnslib_${pkgver}.tar.gz")
md5sums=('6937c7fae34381fd51986bd3d42832cd')

# need to tell cmake when to build 64bit version
[[ "$CARCH" == "i686" ]] && _64bits=OFF
[[ "$CARCH" == "x86_64" ]] && _64bits=ON

build() {

  mkdir -p build
  cd build

  cmake \
  -DCGNS_BUILD_SHARED:BOOL=ON \
  -DCMAKE_BUILD_TYPE:STRING=Release \
  -DCMAKE_INSTALL_PREFIX:PATH=/usr \
  -DCMAKE_SKIP_RPATH:BOOL=ON \
  -DBUILD_CGNSTOOLS:BOOL=ON \
  -DENABLE_64BIT:BOOL=${_64bits} \
  -DENABLE_FORTRAN:BOOL=ON \
  -DENABLE_HDF5:BOOL=ON \
  -DENABLE_SCOPING:BOOL=OFF \
  -DENABLE_TESTS:BOOL=OFF \
  ../cgnslib_${pkgver}

  make
}


package() {
  cd build

  make DESTDIR=${pkgdir} install

  # install license
  install -d ${pkgdir}/usr/share/licenses/libcgns-paraview
  install -m644 ${srcdir}/cgnslib_${pkgver}/license.txt \
    ${pkgdir}/usr/share/licenses/libcgns-paraview
}
