# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>
# Contributor: Flamelab <panosfilip@gmail.com>
# Based on the gnome-shell package trunk

pkgname=gnome-shell
pkgver=2.91.91
pkgrel=1
pkgdesc="The next generation GNOME Shell"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeShell"
license=('GPL2')
depends=('dbus-glib' 'dconf' 'gtk3' 'libffi' 'gnome-desktop-3.0>=2.91'  'libgnomeui' 'cairo' 'librsvg' 'gnome-python' 'python2' 'spidermonkey' 'gnome-menus' 'gnome-control-center-dev>=2.91' 'freetype2' 'gstreamer0.10'
'clutter>=1.6.4' 'mutter>=2.91.90' 'gjs-dev>=0.7.10' 'gsettings-desktop-schemas-dev>=0.1.5' 'mesa-demos' 'gnome-settings-daemon-dev>=2.91' 'evolution-data-server-dev>=2.91' 'polkit-gnome-dev>=0.100' 'telepathy-glib-dev>=0.13.2')
makedepends=('xulrunner' 'gnome-common' 'intltool' 'libpulse' 'gnome-doc-utils>=0.12.0' 'gobject-introspection>=0.10')
install=gnome-shell.install
groups=(gnome)
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.91/${pkgname}-${pkgver}.tar.bz2")
sha256sums=(`wget ${source/.t*}.sha256sum -qO - | grep tar.bz2 | cut -d " " -f1`)
options=(!libtool !emptydirs)

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-shell \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package()  {
  cd ${pkgname}-${pkgver}
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-shell "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
