# Contributor: Anton Shestakov <engored*ya.ru>
# Maintainer: Eric Anderson <ejona86@gmail.com>

pkgname=sdlume
_patch=3
_basever=0148
pkgver=0.148u${_patch}
pkgrel=1
pkgdesc='Universal Machine Emulator combines the features of MAME and MESS into a single multi-purpose emulator'
url='http://mamedev.emulab.it/haze/'
license=('custom:MAME License')
arch=('i686' 'x86_64')
depends=('sdl>=1.2.11' 'sdl_ttf' 'libxinerama' 'gconf' 'zlib' 'expat' 'gtk2' 'alsa-lib')
makedepends=('mesa')
[ "$CARCH" = 'i686' ] && makedepends+=('nasm')
optdepends=('ttf-liberation: recommended UI font')
source=("${pkgname}.sh"
        "mame${_basever}s.zip::http://mamedev.org/downloader.php?file=releases/mame${_basever}s.zip"
        "http://mamedev.org/updates/${_basever}u1_diff.zip"
        "http://mamedev.org/updates/${_basever}u2_diff.zip"
        "http://mamedev.org/updates/${_basever}u3_diff.zip"
        )
md5sums=('e1d320d42f62f3a18532640b015ee29a'
         '38f7727c2961cd31e2ab6aa1814a23ba'
         '01edd53824784f52448f4128f6d52aac'
         '6970189e5cd593dea59aa87579bb4dca'
         '406dff01777ba08a35489b3081d5f644'
         )
install="${pkgname}.install"
noextract=("mame${_basever}s.zip")
PKGEXT='.pkg.tar.gz'

build() {
  mkdir "${srcdir}/${pkgname}-${pkgver}"
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg "Extracting mame${_basever}s.zip"
  bsdtar -xOf "${srcdir}/mame${_basever}s.zip" | bsdtar -xf -

  for (( i=1; i <= ${_patch}; i++ )); do
    msg "Applying patch ${i}"
    patch -p0 -E --binary --quiet < "${srcdir}/${_basever}u${i}.diff"
  done

  msg "Building"
  make TARGET=ume SUFFIX64='' NOWERROR=1 BUILD_ZLIB=0 BUILD_EXPAT=0 \
      ARCHOPTS="$CFLAGS" PYTHON=python2
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Installing the wrapper script
  install -Dm755 "$srcdir/${pkgname}.sh" "$pkgdir/usr/bin/$pkgname"

  # Installing binaries
  install -Dm755 ume "$pkgdir/usr/share/$pkgname/$pkgname"

  # Installing extra bits
  install -d "$pkgdir/usr/share/$pkgname/"{artwork,hash,shader,keymaps}

  install -m644 artwork/* "$pkgdir/usr/share/$pkgname/artwork/"
  install -m644 hash/* "$pkgdir/usr/share/$pkgname/hash/"
  install -m644 src/osd/sdl/shader/glsl*.*h "$pkgdir/usr/share/$pkgname/shader/"
  install -m644 src/osd/sdl/keymaps/* "$pkgdir/usr/share/$pkgname/keymaps/"

  # The license
  install -Dm644 docs/license.txt "$pkgdir/usr/share/licenses/custom/$pkgname/license.txt"
}
