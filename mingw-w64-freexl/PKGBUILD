pkgname=mingw-w64-freexl
pkgver=1.0.0h
pkgrel=1
pkgdesc="Library for reading Excel (.xls) spreadsheets (mingw-w64)"
arch=('any')
license=('MPL')
url="https://www.gaia-gis.it/fossil/freexl/index"
depends=('mingw-w64-crt')
makedepends=('mingw-w64-configure')
options=('!strip' '!buildflags' 'staticlibs')
source=("http://www.gaia-gis.it/gaia-sins/freexl-${pkgver}.tar.gz")
sha1sums=('093d7fbf04818e95f04ca7e0cbfed2e1ac96fdc9')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}/freexl-$pkgver
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure --enable-threads
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/freexl-$pkgver/build-${_arch}"
    make install DESTDIR="${pkgdir}"
    rm -rf "${pkgdir}/usr/${_arch}/share/man"
    chmod a-x "${pkgdir}/usr/${_arch}/lib/"*.dll.a
    ${_arch}-strip -x -g "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip -g "${pkgdir}/usr/${_arch}/lib/"*.a
  done
}
