# Maintainer: Nicol√°s Reynolds <fauno@kiwwwi.com.ar>
# Contributor (Arch): Nathan Owe <ndowens04 at gmail>
pkgname=anubis
pkgver=4.2
pkgrel=1
pkgdesc="An SMTP message submission daemon. "
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/anubis/"
license=('GPL3')
depends=('guile' 'gpgme' 'gnutls' 'gsasl' 'pam' 'pcre')
makedepends=('libmariadbclient' 'postgresql-libs')
source=(ftp://ftp.gnu.org/gnu/anubis/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('3ca1dd378ba72900a432004cb9db935f' SKIP)

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr \
              --with-guile \
              --with-pam \
              --with-mysql \
              --with-postgres \
              --with-pcre \
              --without-tcp-wrappers \
              --with-socks-proxy

  # For some reason the GUILE_INCLUDES variable is set but not used
  # during build.
  make CFLAGS="$CFLAGS $(guile-config compile)"
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir/ install
}

# vim:set ts=2 sw=2 et:
