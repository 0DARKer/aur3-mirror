# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mpie <michael.kyne-phillips1@ntlworld.com>
# Contributor: xduugu
# Maintainer: Daenyth
pkgname=parrot-svn
pkgver=41179
pkgrel=1
pkgdesc="A standalone virtual machine that can be used to execute bytecode compiled dynamic languages"
arch=('i686' 'x86_64')
url="http://www.parrot.org/"
license=('GPL')
depends=('icu' 'openssl')
makedepends=('subversion')
provides=("parrot=$pkgver")
conflicts=("parrot")
options=(!emptydirs !makeflags)

_svntrunk=https://svn.parrot.org/parrot/trunk
_svnmod=parrot

build() {
  if [ -d "$_svnmod/.svn" ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  perl Configure.pl --prefix=/usr --parrot_is_shared

  find -type f -name Makefile | while read F; do
    sed -i "s#-Wl,-rpath=$srcdir/$_svnmod-build/blib/lib##" $F
    sed -i "s#-rpath=$srcdir/$_svnmod-build/blib/lib##" $F
  done

  OLD_LIB_PATH=$LD_LIBRARY_PATH
  export LD_LIBRARY_PATH="$LD_LIBRARY_PATH:$srcdir/$_svnmod-build/blib/lib"

  make all parrot_utils docs html || return 1

  export LD_LIBRARY_PATH=$(pwd)/blib/lib
  make DESTDIR="$pkgdir" install-dev || return 1

  sed -i "s#$srcdir/$_svnmod-build#/usr/src#" "$pkgdir/usr/lib/$_svnmod/"*/tools/lib/Parrot/Config/Generated.pm || return 1

  export LD_LIBRARY_PATH=$OLD_LIB_PATH
}
