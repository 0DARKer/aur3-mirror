# https://wiki.archlinux.org/index.php/VCS_PKGBUILD_Guidelines
# Maintainer: rtfreedman  (rob<d0t>til<d0t>freedman<aT>googlemail<d0t>com
pkgname=argotlunar-git
_gitname=argotlunar
pkgver=20130217
pkgrel=2
pkgdesc="Realtime Granulator VST Plugin."
arch=('i686' 'x86_64')
url="http://argotlunar.info"
license=('GPL2')
provides=('argotlunar')
conflicts=('argotlunar-bin')
makedepends=('git' 'steinberg-vst24')
#Linux users should install ttf-bitstream-vera fonts
optdepends=('ttf-bitstream-vera')
source=("$_gitname::git+https://github.com/mourednik/argotlunar"
		'juce_VST_Wrapper.patch'
		'Makefile.patch'
		'presets.bank'
		'argotlunar2-reference.pdf')
md5sums=('SKIP'
		 'c923454e447396296025e9ab881d43a6'
         '4f86cd0056023c24cdb5858092fa2566'
         '746ea1e061470dda9a4499ba92fe647d'
         '94bc49bd07e2d3eccac696b13c63e2e9')

pkgver() {
  cd "$srcdir/$_gitname"
  git log -1 --format="%cd" --date=short | tr -d '-'
}

prepare() {
  cd "$srcdir/$_gitname"
  patch -p1 -i "$srcdir/Makefile.patch"
  patch -p1 -i "$srcdir/juce_VST_Wrapper.patch"
}

build() {
  cd "$srcdir/$_gitname/Builds/Linux"
  make
}

package() {
  cd "$srcdir/$_gitname"
  install -D -m755 Builds/Linux/build/argotlunar.so "$pkgdir/usr/lib/vst/argotlunar.so"
  install -D -m644 "$srcdir/presets.bank" "$pkgdir/usr/lib/vst/presets.bank"
  install -D -m644 "$srcdir/argotlunar2-reference.pdf"  "$pkgdir/usr/share/doc/$pkgname/argotlunar2-reference.pdf"
}

# vim:set ts=2 sw=2 et:
