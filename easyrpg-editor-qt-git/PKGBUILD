# Maintainer: carstene1ns <url/mail: arch carsten-teibes de>

pkgname=easyrpg-editor-qt-git
pkgver=r231.56e1258
pkgrel=1
pkgdesc="EasyRPG's Game Editor (QT, development version)"
arch=('i686' 'x86_64')
url="https://easy-rpg.org/"
license=('unknown')
conflicts=('easyrpg-editor-qt')
provides=('easyrpg-editor-qt')
makedepends=('git')
depends=('liblcf-git' 'qt5-multimedia')
source=(easyrpg-editor-qt::"git+https://github.com/EasyRPG/editor-qt.git")
md5sums=('SKIP')

pkgver() {
  cd easyrpg-editor-qt
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  # set include folder for liblcf
  sed 's|$$PWD/libs/liblcf/src|/usr/include/liblcf|;/liblcf\/generated/d' \
    -i easyrpg-editor-qt/EasyRPG-Editor.pro
  # reorder libraries
  sed 's|-lexpat -llcf|-llcf -lexpat|' -i easyrpg-editor-qt/EasyRPG-Editor.pro
  # build fixes
  sed 's|qeventwidget.h|QEventWidget.h|' \
    -i easyrpg-editor-qt/src/{main,dialogEvent,tools/{QEventWidget,qeventwidget_init}}.cpp
  sed 's|DialogEvent.h|dialogEvent.h|' \
    -i easyrpg-editor-qt/src/{tools/QGraphicsMapScene,dialogEvent}.cpp
}

build () {
  cd easyrpg-editor-qt

  qmake-qt5
  make
}

package () {
  install -Dm755 easyrpg-editor-qt/bin/EasyRPG-Editor \
    "$pkgdir"/usr/bin/easyrpg-editor-qt
}
