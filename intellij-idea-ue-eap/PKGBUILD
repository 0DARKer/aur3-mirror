# Maintainer: Gauthier P. <kendos.kenlen@gmail.com>

pkgname=intellij-idea-ue-eap
_pkgname=idea-IU
pkgver=14
_buildver=14
pkgrel=1
pkgdesc="Early access version of the upcoming version of Intellij Idea IDE (ultimate version)"
arch=('i686' 'x86_64') # not 'any' because of fsnotifier
url="http://www.jetbrains.com/idea/"
backup=("usr/share/${pkgname}/bin/idea.vmoptions" "usr/share/${pkgname}/bin/idea64.vmoptions")
license=('custom')
depends=('java-environment' 'giflib' 'libxtst')
conflicts=('intellij-idea-ultimate-edition')
provides=('intellij-idea-ultimate-edition')
options=(!strip)
source=(http://download.jetbrains.com/idea/ideaIU-$_buildver.tar.gz)
md5sums=('f72ce091f94cc457caf8b0cbb4b9861c')

package() {
  cd ${srcdir}
  mkdir -p ${pkgdir}/opt/${pkgname}
  cp -R ${srcdir}/${_pkgname}-${_buildver}/* ${pkgdir}/opt/${pkgname}
  if [[ $CARCH = 'i686' ]]; then
     rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so"
     rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
     rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen64.so"
     rm -R -f "${pkgdir}/opt/${pkgname}/lib/libpty/linux/x86_64"
  fi
  if [[ $CARCH = 'x86_64' ]]; then
     rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux.so"
     rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier"
     rm -f "${pkgdir}/opt/${pkgname}/bin/libbreakgen.so"
     rm -R -f "${pkgdir}/opt/${pkgname}/lib/libpty/linux/x86"
  fi

(
cat <<EOF
[Desktop Entry]
Version=${pkgver}
Name=IntelliJ IDEA Ultimate Edition
Icon=idea
GenericName=IDE For Java Developmers
Comment=Early Access Program
Exec=/opt/${pkgname}/bin/idea.sh
Terminal=false
Type=Application
Categories=Development
EOF
) > ${srcdir}/idea-IU.desktop

  mkdir -p "${pkgdir}/usr/bin/"
  mkdir -p "${pkgdir}/usr/share/applications/"
  mkdir -p "${pkgdir}/usr/share/pixmaps/"
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m 644 "${srcdir}/idea-IU.desktop" "${pkgdir}/usr/share/applications/"
  install -m 644 "${pkgdir}/opt/${pkgname}/bin/idea.png" "${pkgdir}/usr/share/pixmaps/idea-IU.png"
  install -m 644 "${srcdir}/${_pkgname}-${_buildver}/license/IDEA_license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/IDEA_license.txt"
  ln -s "/opt/$pkgname/bin/idea.sh" "$pkgdir/usr/bin/ideaIU"
}

# vim:set ts=2 sw=2 et:
