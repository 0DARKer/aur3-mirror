# Maintainer: Gauthier P. <kendos.kenlen@gmail.com>

pkgname=intellij-idea-ue-eap
_pkgname=idea-IU
pkgver=140.2683.2
_buildver=140.2683.2
pkgrel=1
pkgdesc="Early access version of the upcoming version of Intellij Idea IDE (ultimate version)"
arch=('any')
url="http://www.jetbrains.com/idea/"
backup=("usr/share/${pkgname}/bin/idea.vmoptions" "usr/share/${pkgname}/bin/idea64.vmoptions")
license=('Commercial')
depends=('java-environment' 'giflib' 'libxtst')
options=(!strip)
source=(https://download.jetbrains.com/idea/ideaIU-$pkgver.tar.gz \
        intellij-idea-ue-eap.sh \
        intellij-idea-ue-eap.desktop
)
sha256sums=('a20206e5fa7bbf64457846c45ae31f7b0213c6a322df91b842208b5bb3dee802'
            'ce7e9fbb082a9b06f48bc5828b60fa073543f7bd466d9b516a3046c8c8ee50d1'
            '3e0762bcbb2186dd784a346491b4393a88ebe184276aef383582098dac42c270')

package() {
  cd "$srcdir"

  install -d -m755 $pkgdir/usr/{bin,share}
  cp -a "idea-IU-$_buildver" "$pkgdir/usr/share/$pkgname"
  chown -R root:root $pkgdir/usr/share

  find $pkgdir/usr/share/$pkgname -type d -exec chmod 0755 {} ';'
  find $pkgdir/usr/share/$pkgname -type f -exec chmod 0644 {} ';'

  chmod +x $pkgdir/usr/share/$pkgname/bin/idea.sh
  chmod +x "$pkgdir"/usr/share/$pkgname/bin/fsnotifier
  chmod +x "$pkgdir"/usr/share/$pkgname/bin/fsnotifier64

  install -D -m755 $srcdir/intellij-idea-ue-eap.sh $pkgdir/usr/bin/$pkgname
  install -D -m644 $srcdir/intellij-idea-ue-eap.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -D -m644 $srcdir/idea-IU-$_buildver/bin/idea.png $pkgdir/usr/share/pixmaps/$pkgname.png
  install -D -m644 $srcdir/idea-IU-$_buildver/license/IDEA_license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt

  sed -i 's|lcd|on|'  "$pkgdir"/usr/share/$pkgname/bin/*.vmoptions
}


# vim:set ts=2 sw=2 et:
