# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: joel schaerer <joel.schaerer@laposte.net>

pkgname=insight-toolkit
pkgver=3.20.1
pkgrel=1
pkgdesc='Insight Segmentation and Registration Toolkit (ITK).'
arch=('i686' 'x86_64')
url='http://www.itk.org/'
license=('custom:BSD')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/project/itk/itk/${pkgver:0:4}/InsightToolkit-${pkgver}.tar.gz")
md5sums=('90342ffa78bd88ae48b3f62866fbf050')

build() {
  cd InsightToolkit-${pkgver}

  cmake \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DBUILD_TESTING:BOOL=OFF \
    -DBUILD_EXAMPLES:BOOL=OFF \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DITK_USE_REVIEW:BOOL=ON

  make
}

package() {
  cd InsightToolkit-${pkgver}

  make DESTDIR=${pkgdir} install

  # install BSD license
  install -d ${pkgdir}/usr/share/licenses/insight-toolkit
  install -m 644 Copyright.txt \
    ${pkgdir}/usr/share/licenses/insight-toolkit
}
