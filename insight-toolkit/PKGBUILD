# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: joel schaerer <joel.schaerer@laposte.net>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=insight-toolkit
pkgver=4.1.0
pkgrel=1
pkgdesc='Insight Segmentation and Registration Toolkit (ITK).'
arch=('i686' 'x86_64')
url='http://www.itk.org/'
license=('APACHE')
makedepends=('cmake' 'python2' 'swig')
optdepends=('python2: python bindings')
provides=('itk-python')
conflicts=('itk-python')
source=("http://downloads.sourceforge.net/project/itk/itk/${pkgver:0:3}/InsightToolkit-${pkgver}.tar.xz")
md5sums=('5dd05b4aa272f745e0897da2bcf37a34')

build() {
  rm -rf build
  mkdir build
  cd build

  cmake \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DBUILD_TESTING:BOOL=OFF \
    -DBUILD_EXAMPLES:BOOL=OFF \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DITK_USE_REVIEW:BOOL=ON \
    -DITK_USE_SYSTEM_SWIG:BOOL=ON \
    -DITK_WRAPPING:BOOL=ON \
    -DITK_WRAP_PYTHON:BOOL=ON \
    -DPYTHON_EXECUTABLE:PATH=/usr/bin/python2 \
    -DPYTHON_INCLUDE_DIR:PATH=/usr/include/python2.7 \
    -DPYTHON_LIBRARY:PATH=/usr/lib/libpython2.7.so \
    ../InsightToolkit-${pkgver}

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}

