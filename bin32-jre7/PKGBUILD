# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=bin32-jre7
_major=7
_minor=67
_build=b01
_pkg=${_major}u$_minor
pkgver=$_major.$_minor
pkgrel=1
pkgdesc="Java $_major Runtime Environment (32-bit)"
arch=('x86_64')
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
license=('custom')
depends=('lib32-libxrender' 'lib32-libxtst' 'lib32-gtk2') #  'shared-mime-info' 'xdg-utils' 'desktop-file-utils' 'hicolor-icon-theme'
optdepends=('lib32-alsa-lib: sound'
            'ttf-dejavu: fonts')
provides=("java-runtime-32=$_major" "java-runtime-headless-32=$_major" "java-web-start-32=$_major")
conflicts=("${provides[@]}" "${provides[@]/$_major/7}")
backup=('opt/java32/jre/lib/security/java.policy')
#install=jre.install
source=("http://download.oracle.com/otn-pub/java/jdk/$_pkg-$_build/jre-$_pkg-linux-i586.tar.gz"
        'javaws-launcher'
        'jre.csh'
        'jre.sh')
md5sums=('2a256eb2a91f0084e58c612636342c2b'
         '45c15a6b4767288f2f745598455ea2bf'
         '92dec202858f2bf7729c6805b1bd3ae4'
         '85ba1d2b39d5e6efad89ef98d0f19909')
# # Alternative mirrors, if your local one is throttled:
# source[0]="http://uni-smr.ac.ru/archive/dev/java/JRE/oracle/$_major/jre-$_pkg-linux-i586.tar.gz"
# source[0]="http://ftp.wsisiz.edu.pl/pub/pc/pozyteczne%20oprogramowanie/java/jre-$_pkg-linux-i586.tar.gz"

DLAGENTS=('http::/usr/bin/curl -LC - -b "oraclelicense=a" -O')

package() {
  msg2 "Creating required dirs"
  cd jre1.$_major.0_$_minor
  mkdir -p "$pkgdir"/{opt/java32/jre,usr/{lib32/mozilla/plugins,share/licenses/$pkgname},etc/profile.d}

  msg2 "Removing undesired stuff" # desktop file, icons, man pages
  rm -r plugin/ man/ lib/desktop/*

  msg2 "Moving stuff in place"
  mv COPYRIGHT LICENSE *.txt "$pkgdir"/usr/share/licenses/$pkgname/
  mv * "$pkgdir"/opt/java32/jre/

  msg2 "Symlinking the plugin"
  ln -s /opt/java32/jre/lib/i386/libnpjp2.so "$pkgdir"/usr/lib32/mozilla/plugins/

  msg2 "Installing the scripts, confs and .desktops of our own"
  cd "$srcdir"
  install -m755 jre.{,c}sh "$pkgdir"/etc/profile.d/
  install -m755 javaws-launcher "$pkgdir"/opt/java32/jre/bin/
  
  msg2 "Tweaking profile.d files for 32-bit in a 64-bit system"
  sed -i -e '/J2REDIR/d;/JAVA_HOME/d;s#opt/java#&32#' "$pkgdir"/etc/profile.d/*
  
  msg2 "Renaming files for 32-bit in a 64-bit system"
  for bin in $(ls "$pkgdir"/opt/java32/jre/bin/); do
    mv "$pkgdir"/opt/java32/jre/bin/$bin "$pkgdir"/opt/java32/jre/bin/${bin}32
  done
  ln -fs jcontrol32 "$pkgdir"/opt/java32/jre/bin/ControlPanel32
  mv "$pkgdir"/etc/profile.d/{,bin32-}jre.sh
  mv "$pkgdir"/etc/profile.d/{,bin32-}jre.csh  
}
