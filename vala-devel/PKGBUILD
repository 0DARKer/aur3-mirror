# Contributor: Ner0
# Contributor: Damien Churchill <damoxc@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Christer Nissen <christer.nissen@gmail.com>

pkgname=vala-devel
pkgver=0.17.4
pkgrel=1
pkgdesc="Compiler for the GObject type system (development release)"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Vala"
license=('LGPL')
depends=('glib2')
makedepends=('libxslt')
provides=("vala=${pkgver}")
conflicts=('vala' 'vala-git')
options=('!libtool')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname%-*}/${pkgver%.*}/${pkgname%-*}-${pkgver}.tar.xz")
sha256sums=('d49c90afd84b6d58cfa0b841957543cc48790a2a94241473ba873d720e016a01')

build() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  ./configure --prefix=/usr --enable-vapigen
  make ${MAKEFLAGS}
}

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
