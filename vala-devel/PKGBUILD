# Maintainer: Damien Churchill <damoxc@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Christer Nissen <christer.nissen@gmail.com>

pkgname=vala-devel
_pkgname=vala
pkgver=0.14.0
_series=${pkgver%.*}
pkgrel=1
pkgdesc="Compiler for the GObject type system (development release)"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Vala"
license=('LGPL')
depends=('glib2')
makedepends=('libxslt')
options=('!libtool')
provides=("vala=${pkgver}")
conflicts=('vala')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/${_series}/${_pkgname}-${pkgver}.tar.bz2)
sha256sums=('4cbca602e2e2a09803ae33fd7324219bc2c611db5a62a52e733e7d8806acb6f5')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-vapigen
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
