# Maintainer: Damien Churchill <damoxc@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Christer Nissen <christer.nissen@gmail.com>

pkgname=vala-devel
_pkgname=vala
pkgver=0.15.1
_series=${pkgver%.*}
pkgrel=1
pkgdesc="Compiler for the GObject type system (development release)"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Vala"
license=('LGPL')
depends=('glib2')
makedepends=('libxslt')
options=('!libtool')
provides=("vala=${pkgver}")
conflicts=('vala')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/${_series}/${_pkgname}-${pkgver}.tar.xz)
sha256sums=('0db76207eb1ebe2f7685808f53d71e95257ae37a467dd75d679ce74e8f9f38b0')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-vapigen
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
