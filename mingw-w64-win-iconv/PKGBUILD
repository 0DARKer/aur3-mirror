# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-win-iconv
pkgver=0.0.6
pkgrel=1
arch=(any)
pkgdesc="Iconv implementation using Win32 API (mingw-w64)"
depends=(mingw-w64-crt)
makedepends=(mingw-w64-gcc mingw-w64-cmake dos2unix)
options=(!strip !buildflags !libtool)
conflicts=(mingw-w64-libiconv-static mingw-w64-libiconv)
provides=(mingw-w64-libiconv-static mingw-w64-libiconv)
license=("PublicDomain")
url="http://code.google.com/p/win-iconv"
source=("http://win-iconv.googlecode.com/files/win-iconv-${pkgver}.tar.bz2")
md5sums=('1e97ed4d9e7379ff0ee22077256e8c58')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/win-iconv-$pkgver"
  
  dos2unix readme.txt
  dos2unix ChangeLog
  chmod -x readme.txt
  chmod -x ChangeLog

  #export CFLAGS="-O2 -pipe -march=i686 -mms-bitfields"
  unset LDFLAGS
  
  for _arch in ${_architectures}; do
    mkdir -p ../build-${_arch} && pushd ../build-${_arch}
    ${_arch}-cmake -DBUILD_STATIC=1 ../win-iconv-${pkgver}
    make
    popd
  done
}

package() {
  cd "$srcdir"
  for _arch in ${_architectures}; do
    pushd ${srcdir}/build-${_arch}
    make DESTDIR="${pkgdir}" install

    rm -rf ${pkgdir}/usr/${_arch}/bin/*.exe

    ${_arch}-strip --strip-unneeded $pkgdir/usr/${_arch}/bin/iconv.dll
    ${_arch}-strip --strip-unneeded $pkgdir/usr/${_arch}/lib/libiconv.dll.a # dll interface
    ${_arch}-strip --strip-debug ${pkgdir}/usr/${_arch}/lib/libiconv.a # static lib

    popd
  done
}
