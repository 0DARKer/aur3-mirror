# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rabyte <rabyte__gmail>
# Contributor: Lex Black <autumn-wind@web.de>

_pkgname=zsnes
pkgname=$_pkgname-svn
pkgver=5224
pkgrel=1
pkgdesc="A Super Nintendo emulator"
arch=(i686)
url=http://www.$_pkgname.com/
license=(GPL2)
depends=(libao libgl libpng sdl qt)
makedepends=(nasm subversion)
provides=($_pkgname)
conflicts=($_pkgname{,-wip})
source=(path_error.patch)
sha256sums=('afca5aa8bdff91a734926801a3827798ac764eb6fb1cd5dcd90392dc77f3cd1c')
sha512sums=('ea1f39dd05d76af85a22a62e160efb2f4c934eb36558b9ec7bbf0d0d43145871abfb73a2a4e4555e9634a365aa0f8b647657de9643b55e4882115a806cb37ffa')

_svntrunk=http://svn.bountysource.com/$_pkgname/trunk/
_svnmod=zsnes

build() {
    install -d "$srcdir"/$_pkgname/
    cd "$srcdir"/$_pkgname/

    msg "Starting SVN checkout..."
    if [[ -d $_svnmod/.svn ]]; then
        pushd $_svnmod && svn up -r $pkgver
        msg2 "The local files have been updated."
        popd
    else
        svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi
    msg2 "SVN checkout done or server timeout"

    rm -rf $_svnmod-build
    cp -r $_svnmod $_svnmod-build
    cd $_svnmod-build/src/
    msg "Building..."
    patch -Np1 -i "$srcdir"/path_error.patch
    ./autogen.sh --prefix=/usr \
        --enable-libao \
        x_libraries=/usr/lib \
        --enable-release \
        --disable-debugger
    make
}

package() {
    cd $srcdir/$_svnmod-build/src/
    make DESTDIR="$pkgdir" install

    # install icon and desktop file
    install -Dm644 icons/48x48x32.png $pkgdir/usr/share/pixmaps/zsnes.png
    desktop-file-install linux/zsnes.desktop --dir "$pkgdir"/usr/share/applications/

    mv "$pkgdir"/usr/{man,share}
}
