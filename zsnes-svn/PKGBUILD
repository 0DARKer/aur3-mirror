# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: rabyte <rabyte__gmail>
# Contributor: Lex Black <autumn-wind@web.de>

_pkgname=zsnes
pkgname=$_pkgname-svn
pkgver=5224
pkgrel=1
pkgdesc="A Super Nintendo emulator"
arch=(i686)
url=http://www.$_pkgname.com/
license=(GPL2)
if [[ $CARCH == "x86_64" ]]; then
    depends=(lib32-gcc-libs lib32-sdl lib32-libpng lib32-libgl lib32-ncurses)
    optdepends=('lib32-alsa-lib: sound support')
    makedepends=(nasm gcc-multilib gendesk mesa)
else
    depends=(sdl libpng libgl)
    optdepends=('alsa-lib: sound support')
    makedepends=(nasm gcc gendesk mesa)
fi
depends+=(libao qt4)
makedepends+=(nasm subversion)
provides=($_pkgname)
conflicts=($_pkgname{,-wip})
options=(!buildflags !makeflags)
source=(path_error.patch
    $_pkgname.patch
    $_pkgname-1.51-libpng15.patch)
sha256sums=('afca5aa8bdff91a734926801a3827798ac764eb6fb1cd5dcd90392dc77f3cd1c'
    'f54fc255e775551844c93ef02b8d28695ac5824b6bccf4bf49dd31ac39cc4081'
    '4825cb7f3fa158ebf5ea5a6f1ba2a0b6bab0b05adea675dc1edb86230137fc4e')
sha512sums=('ea1f39dd05d76af85a22a62e160efb2f4c934eb36558b9ec7bbf0d0d43145871abfb73a2a4e4555e9634a365aa0f8b647657de9643b55e4882115a806cb37ffa'
    '308ddb054a291e136770c8728a631cffef7cfd60bb20d86a75f5313f609618e5d566c2226cd2f15e774ac721509323542ae36c4c9b2a493953b88f46d28d51cf'
    'dc97bc8b85c9596a9efa3716ecf797d7924763528f1a902f5f6e0ca527e9edef5b6c32ef013e870a80daf81ec81747c00eb0e7f97f714ee695afbc82fd57a9e4')

_svntrunk=http://svn.bountysource.com/$_pkgname/trunk/
_svnmod=zsnes

build() {
    install -d "$srcdir"/$_pkgname/
    cd "$srcdir"/$_pkgname/

    msg "Starting SVN checkout..."
    if [[ -d $_svnmod/.svn ]]; then
        pushd $_svnmod && svn up -r $pkgver
        msg2 "The local files have been updated."
        popd
    else
        svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi
    msg2 "SVN checkout done or server timeout"

    rm -rf $_svnmod-build
    cp -r $_svnmod $_svnmod-build
    cd $_svnmod-build/src/

    if [[ $CARCH == "x86_64" ]]; then
        export CC="gcc -m32"
        export CXX="g++ -m32"
    fi
    msg "Building..."
    patch -Np1 -i "$srcdir"/path_error.patch
    patch -Np1 -i "$srcdir"/$_pkgname.patch
    patch -Np1 -i "$srcdir"/$_pkgname-1.51-libpng15.patch
    ./autogen.sh --prefix=/usr \
        --enable-libao \
        x_libraries=/usr/lib \
        --enable-release \
        --disable-debugger \
        force_arch=i686
    make
}

package() {
    cd "$srcdir"
    gendesk

    cd $_svnmod-build/src/
    make DESTDIR="$pkgdir" install

    # install icon and desktop file
    install -Dm644 icons/48x48x32.png $pkgdir/usr/share/pixmaps/zsnes.png
    desktop-file-install linux/zsnes.desktop --dir "$pkgdir"/usr/share/applications/

    mv "$pkgdir"/usr/{man,share}
}
