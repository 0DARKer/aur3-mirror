# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

pkgname=hamlib
pkgver=1.2.12
pkgrel=2
pkgdesc="HAM radio equipment control libraries"
arch=('i686' 'x86_64')
url="http://hamlib.org"
license=('GPL' 'LGPL')
depends=('perl' 'python2' 'tcl' 'libxml2' 'libusb')
makedepends=('libtool' 'swig')
source=(configure.patch http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
options=('!emptydirs')
changelog=${pkgname}.ChangeLog.markdown

build() {
	export PYTHON=/usr/bin/python2

	cd $srcdir

	patch -p1 < configure.patch

	cd $pkgname-$pkgver

	./configure --prefix=/usr \
		--with-perl-binding \
		--with-python-binding \
		--with-tcl-binding

	make
}

package() {
	cd $srcdir/$pkgname-$pkgver

	make DESTDIR=$pkgdir install

	# fix perl module location
	cd $pkgdir/usr/lib/perl5/site_perl/
	mkdir current/
	mv auto current
	mv Hamlib.pm current
	rm perltest.pl

	/usr/bin/find $pkgdir -name '.packlist' -delete
	/usr/bin/find $pkgdir -name '*.pod' -delete
}

md5sums=('7ff8682f9dacaa04d0126567ca49fd43'
         '914835c0fe4d618d1cd62f1fd0a8dd7d')
sha256sums=('3b59ee6b1f5b4e36bc9c737d9526390455b543ef0f60ac14d5d0f4f89f755a21'
            '735782ecd83de4036cef7dfcc87eac5095420406bd0ac2262d967e808c2d108c')
