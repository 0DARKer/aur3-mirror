# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD

pkgname=hamlib
pkgver=1.2.14
pkgrel=2
pkgdesc="HAM radio equipment control libraries"
arch=('i686' 'x86_64')
url="http://hamlib.org"
license=('GPL' 'LGPL')
depends=('perl' 'python2' 'tcl' 'libxml2' 'libusb')
makedepends=('libtool' 'swig')
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
options=('!emptydirs')
changelog=${pkgname}.ChangeLog.markdown

build() {
	export PYTHON=/usr/bin/python2

	cd $srcdir

	cd $pkgname-$pkgver

	./configure --prefix=/usr \
		--with-perl-binding \
		--with-python-binding \
		--enable-tcl-binding

	make
}

package() {
	cd $srcdir/$pkgname-$pkgver

	make DESTDIR=$pkgdir install

	# fix perl module location
	cd $pkgdir/usr/lib/perl5/site_perl/
	mkdir current/
	mv auto current
	mv Hamlib.pm current
	rm perltest.pl

	/usr/bin/find $pkgdir -name '.packlist' -delete
	/usr/bin/find $pkgdir -name '*.pod' -delete
}

md5sums=('ccd5232d3285a31c29b5acce1c7a3a65')
sha256sums=('4231d431ebdd122cb5dea673e6dd8a3cb59601a770a1b2cc86264725dbe37843')
