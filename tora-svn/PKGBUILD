# Maintainer: Tom Kuther <gimpel@sonnenkinder.org>
pkgname=tora-svn
pkgver=4289
pkgrel=1
pkgdesc="Toolkit for databases with support for Oracle, MySQL and PostgreSQL"
arch=('i686' 'x86_64')
url="http://tora.sourceforge.net"
license=('GPL')
provides=('tora')
replaces=('tora')
conflicts=('tora')
depends=('qscintilla' 'oracle-instantclient-basic')
makedepends=('cmake' 'oracle-instantclient-sdk' 'subversion')
options=()
install=$pkgname.install
source=()
md5sums=()

_svntrunk=http://tora.svn.sourceforge.net/svnroot/tora/trunk/tora
_svnmod=tora

build() {
  cd "${srcdir}"

  msg "Connecting to SVN server...."                                                                                                                                   
                                                                                                                                                                       
  if [[ -d "$_svnmod/.svn" ]]; then                                                                                                                                    
    (cd "$_svnmod" && svn up -r "$pkgver")                                                                                                                             
  else                                                                                                                                                                 
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"                                                                                                        
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$_svnmod-build"/build
  make DESTDIR="${pkgdir}" install

  mkdir -p "${pkgdir}/usr/share/applications"
  mkdir -p "${pkgdir}/usr/share/icons/hicolor/16x16/apps"
  mkdir -p "${pkgdir}/usr/share/icons/hicolor/32x32/apps"
  install -m644 ../src/icons/toramini.xpm "${pkgdir}/usr/share/icons/hicolor/16x16/apps"
  install -m644 ../src/icons/tora.xpm "${pkgdir}/usr/share/icons/hicolor/32x32/apps"
  install -m644 ../rpm/tora.desktop "${pkgdir}/usr/share/applications"
}
