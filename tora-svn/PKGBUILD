# Maintainer: Tom Kuther <gimpel@sonnenkinder.org>

pkgname=tora-svn
_svnname=tora

_tora_branch=trunk/tora
#_tora_branch=branches/tora3  ## interesting stuff, but currently broken..

pkgver=2.1.4.svn.4682    
pkgrel=1
pkgdesc="Toolkit for databases with support for Oracle, MySQL and PostgreSQL"
arch=('i686' 'x86_64')
url="http://tora.sourceforge.net"
license=('GPL')
provides=('tora')
replaces=('tora')
conflicts=('tora')
depends=('qscintilla' 'oracle-instantclient-basic')
makedepends=('cmake' 'oracle-instantclient-sdk' 'subversion' 'boost')
options=()
install=$pkgname.install
source=("tora::svn+http://svn.code.sf.net/p/tora/code/${_tora_branch}"
        'tora.desktop')
md5sums=('SKIP'
         '089754af8d9d5a537d3ef44929f6aa28')

pkgver() {
  cd $_svnname
  _major=`grep 'SET (VERSION_MAJOR' CMakeLists.txt|awk -F'("|")' '{print $2}'`
  _minor=`grep 'SET (VERSION_MINOR' CMakeLists.txt|awk -F'("|")' '{print $2}'`
  _patch=`grep 'SET (VERSION_PATCH' CMakeLists.txt|awk -F'("|")' '{print $2}'`
  _svnrev=`svnversion | tr -d [A-z]`
  echo "${_major}.${_minor}.${_patch}.svn.${_svnrev}"
}

build() {
  cd $_svnname

  # comes with oracle-instantclient-basic package
  test -f /etc/profile.d/oracle.sh && source /etc/profile.d/oracle.sh

  mkdir build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_SUFFIX="" \
    -DBOOST_ROOT=/usr \
    -DBOOST_INCLUDEDIR=/usr/include/boost \
    -DBOOST_LIBRARYDIR=/usr/lib
  make
}

package() {
  cd $_svnname/build
  make DESTDIR="${pkgdir}" install

  # -DLIB_SUFFIX="" breakage, fu...
  test -d "${pkgdir}"/usr/lib64 && mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib

  mkdir -p "${pkgdir}/usr/share/applications"
  mkdir -p "${pkgdir}/usr/share/pixmaps"
  install -m644 "${srcdir}"/tora.desktop "${pkgdir}/usr/share/applications"
  if [ "${_tora_branch}" = "trunk/tora" ]; then
	  install -m644 ../src/icons/tora.xpm "${pkgdir}/usr/share/pixmaps"
  else
	  install -m644 ../src/icons/tora.png "${pkgdir}/usr/share/pixmaps"
  fi
}

# vim:set ts=2 sw=2 et:
