# Contributor: Médéric Boquien <mboquien@free.fr>

pkgname=sofa
pkgver=20131202
pkgrel=1
pkgdesc="Set of algorithms and procedures used in fundamental astronomy"
url="http://www.iausofa.org"
arch=('x86_64' 'i686')
license=('custom')
source=("http://www.iausofa.org/2013_0212_C/sofa_c-${pkgver}_b.tar.gz")
md5sums=('0ef63a452c363a14f1810d29e458ebf9')

build() {
  cd "${srcdir}/${pkgname}/${pkgver}_b/c/src"
  make CFLAGF="-c -O -fPIC"
}

package() {
  cd "${srcdir}/${pkgname}/${pkgver}_b/c/src"
  make INSTALL_DIR="${pkgdir}/usr" install

  cd "${srcdir}/${pkgname}/${pkgver}_b/c/"
  install -Dm644 00READ.ME "$pkgdir/usr/share/doc/$pkgname/00READ.ME"
  install -m644 doc/*.lis "$pkgdir/usr/share/doc/$pkgname/"
}
