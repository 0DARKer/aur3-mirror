# Past Maintainer: Sven-Hendrik Haase <sh at lutzhaase dot com>
# Contributor: Lukas Kropatschek <lukas.krop at gmail dot com>
# Maintainer: Axper Jan <483ken at gmail dot com>
pkgname=horde3d-svn
pkgver=354
pkgrel=3
pkgdesc="A lightweight Next-Generation Graphics Engine"
arch=('i686' 'x86_64')
url="http://www.horde3d.org"
license=('EPL')
depends=('gcc-libs' 'glfw')
makedepends=('subversion' 'cmake')
provides=('horde3d')
conflicts=('horde3d')

_svntrunk=https://horde3d.svn.sourceforge.net/svnroot/horde3d
_svnmod=horde3d

build() {
	cd "$srcdir"
	msg "Connecting to SVN server...."
	
	if [[ -d "$_svnmod/.svn" ]]; then
		(cd "$_svnmod" && svn up)
	else
		svn co "$_svntrunk" "$_svnmod"
	fi

	msg "SVN checkout done or server timeout"
	msg "Starting build..."

  # Copy repository
	if [[ -d "$_svnmod-build" ]]; then
    rm -R "$_svnmod-build"
  fi
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"

  # Build
  cd "$srcdir/$_svnmod-build/trunk"
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$_svnmod-build/trunk"
  make DESTDIR="$pkgdir/" install

  # Docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -a ./Horde3D/Docs/* "$pkgdir/usr/share/doc/$pkgname"
  find "$pkgdir/usr/share/doc/" -type f -exec chmod 644 {} \;

  # ColladaConv
  install -Dm755 ./Horde3D/Binaries/Linux/ColladaConv \
    "$pkgdir/usr/bin/ColladaConv"
}

# vim:set ts=2 sw=2 et:
