# Contributor: zhuqin <zhuqin83@gmail.com>
pkgname=lensfun-git
_gitname=lensfun
pkgver=v0.2.8.261.g7844f76
pkgrel=1
pkgdesc="Library to correct optical lens defects and lens database"
arch=(i686 x86_64)
url="http://lensfun.sourceforge.net"
license=('LGPL3')
depends=('glibc' 'glib2')
makedepends=('python2' 'libpng')
provides=('lensfun')
conflicts=('lensfun')
source=("lensfun::git://git.code.sf.net/p/lensfun/code")
md5sums=(SKIP)

pkgver() {
  cd $_gitname
  git describe --always | sed 's|-|.|g'
}

build() {
    cd $_gitname
    ./configure --prefix=/usr --libdir=/usr/lib
    make all
}

package() {
  cd $_gitname
  make INSTALL_PREFIX="$pkgdir/" install
}
