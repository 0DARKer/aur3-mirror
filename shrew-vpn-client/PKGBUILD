# Maintainer: Christian Hammacher <bmasterc gmail com>
# Contributor: Christian Hammacher <bmasterc gmail com>

pkgname=shrew-vpn-client
pkgver=2.1.7
pkgrel=1
pkgdesc="A portable VPN client for Linux with Qt GUI"
arch=('i686' 'x86_64')
url="http://www.shrew.net/"
license=('osi')
depends=('qt3' 'openssl')
makedepends=('gcc' 'flex' 'libedit' 'bison' 'cmake')
optdepends=('openldap' 'logrotate')
conflicts=('shrew-vpn-client-alpha')
backup=('etc/iked.conf')
source=(http://www.shrew.net/download/ike/ike-$pkgver-release.tbz2)
md5sums=('bc86e101809fc750013e18480c8c1040')

build() {
  cd $srcdir/ike

  # Build the whole package
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DQTGUI=YES -DETCDIR=/etc -DNATT=YES \
        -DPATH_MAN=/usr/share -DQT_QT_LIBRARY=/opt/qt/lib/libqt-mt.so \
        -DQT_INCLUDE_DIR=/opt/qt/include -DQT_UIC_EXECUTABLE=/opt/qt/bin/uic \
        -DQT_MOC_EXECUTABLE=/opt/qt/bin/moc -DMANDIR=/usr/share/man
  make
}

package() {
  cd "$srcdir/ike"
  make DESTDIR="$pkgdir/" install
  
  # Install the daemon script
  install -D -m755 $startdir/iked $pkgdir/etc/rc.d/iked

  # The configuration file is already ready for use; just rename it
  mv $pkgdir/etc/iked.conf.sample $pkgdir/etc/iked.conf

  # Prevent logs from growing if logrotate is installed
  install -D -m644 $startdir/iked.conf $pkgdir/etc/logrotate.d/iked.conf

  # Copy our desktop files
  install -D -m644 $srcdir/ike/source/ikea/png/ikea.png $pkgdir/usr/share/icons/ikea.png
  install -D -m755 $startdir/ikea.desktop $pkgdir/usr/share/applications/ikea.desktop
}


