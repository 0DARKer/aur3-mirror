# Contributor: riverscn<riverscn at gmail.com>
# Contributor: Lee.MaRS<leemars at gmail.com>
pkgname=ibus-table-git
pkgver=20101020
pkgrel=1
pkgdesc="The IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on."
arch=('i686' 'x86_64')
url="http://code.google.com/p/ibus/"
license=('LGPL')
depends=('python-pyenchant' 'ibus' 'aspell-en')
provides=('ibus-table=1.3.0')
conflicts=('ibus-table')
makedepends=('git' 'cvs')
options=('!libtool')

_gitroot="git://github.com/acevery/ibus-table.git"
_gitname="ibus-table"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin master
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  for file in engine/ibus-engine-table.in engine/ibus-table-createdb.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  ./autogen.sh --break-configure || :

  ./configure \
    PYTHON=/usr/bin/python2 \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
} 
