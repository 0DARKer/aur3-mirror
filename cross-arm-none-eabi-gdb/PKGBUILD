# Maintainer: Matias De la Puente <mfpuente.ar@gmail.com>
# based on 'cross-arm-elf-gcc' by Andreas Messer <andi@surveycorner.de>

pkgname=cross-arm-none-eabi-gdb
pkgver=7.2
pkgrel=1
pkgdesc="The GNU Debugger for ARM target"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python')
makedepends=('texinfo')
optdepends=('openocd: for debugging jtag targets')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/gdb/gdb-${pkgver}.tar.bz2)
md5sums=('64260e6c56979ee750a01055f16091a5')

build() {
  cd ${srcdir}/gdb-${pkgver}
  
  ./configure --prefix=/usr \
	--target=arm-none-eabi \
	--host=$CHOST \
	--build=$CHOST \
	--disable-nls --with-system-readline \
	--with-local-prefix=/usr/lib/cross-arm \
	--with-sysroot=/usr/$CHOST/arm-none-eabi

  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # resolve conflicts with binutils / remove unneeded files
  rm -f ${pkgdir}/usr/include/{bfd,dis-asm,symcat,ansidecl,bfdlink}.h
  rm -f ${pkgdir}/usr/lib/{libiberty,libbfd,libopcodes}.a
  rm -rf ${pkgdir}/usr/share/info
  rm -f ${pkgdir}/usr/share/gdb/syscalls/gdb-syscalls.dtd
  rm -f ${pkgdir}/usr/share/gdb/syscalls/{i386,ppc64,amd64,ppc,sparc,sparc64}-linux.xml
}
