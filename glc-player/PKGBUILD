# Maintainer: Jonathan Liu <net147@gmail.com>
pkgname=glc-player
pkgver=2.2.0
pkgrel=2
pkgdesc="Open source 3D model viewer"
arch=('i686' 'x86_64')
url="http://www.glc-player.net/"
license=('GPL2')
depends=('glc-lib')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/GLC_Player_${pkgver}/GLC_Player_src_${pkgver}.zip"
        "glc_player.desktop"
        "glc_player.png"
        "glc_lib-2.1.patch")
md5sums=('9aa479e1c72381f9723e5af3bf68b36d'
         '60cba62de5872f63c0609791a4be7156'
         '7772386e065a8855cf6a0a241d6d9348'
         '5f1fc757c59ae0e895f8da92cf576717')

build() {
  cd "${srcdir}/GLC_Player_src_${pkgver}"
  patch -Np1 -i "${srcdir}/glc_lib-2.1.patch"
  qmake
  make
}

package() {
  cd "${srcdir}/GLC_Player_src_${pkgver}"
  install -D -m755 glc_player "${pkgdir}/usr/bin/glc_player"
  install -D -m644 "${srcdir}/glc_player.desktop" "${pkgdir}/usr/share/applications/glc_player.desktop"
  install -D -m644 "${srcdir}/glc_player.png" "${pkgdir}/usr/share/pixmaps/glc_player.png"
}

# vim:set ts=2 sw=2 et:
