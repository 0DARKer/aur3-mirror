# Maintainer: talavis <aur talavis eu>
pkgname=ugene
pkgver=1.11.1
pkgrel=2
pkgdesc="A free cross-platform genome analysis suite."
arch=('i686' 'x86_64')
url="http://ugene.unipro.ru/"
license=('GPL')
depends=('qt' 'mesa')
source=(${url}downloads/$pkgname-$pkgver.tar.gz)
sha1sums=('66df7ffb7c208725102b362615658f6e4f8f470a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  if [[ $CARCH == "i686" ]]
  then
    qmake PREFIX=/usr -r
  elif [[ $CARCH == "x86_64" ]]
  then
    qmake CONFIG+=x64 PREFIX=/usr -r
  fi
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install
}
