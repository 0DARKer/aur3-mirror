# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-glib2
pkgver=2.28.8
pkgrel=1
arch=(any)
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw32)"
depends=(mingw32-gettext mingw32-zlib)
makedepends=(mingw32-gcc)
license=(LGPL)
options=(!strip)
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/2.28/glib-$pkgver.tar.xz)
sha256sums=('4d7ca95dbde8e8f60ab428c765b0dbb8a44be9eb9316491803ce5ee7b4748353')

build()
{
  cd "$srcdir/glib-$pkgver"

cat > win32.cache << EOF
glib_cv_long_long_format=I64
glib_cv_stack_grows=no
EOF

  export CFLAGS="-O2 -pipe -march=i686 -mms-bitfields"
  export CPPFLAGS="-D_REENTRANT"
  export PKG_CONFIG_LIBDIR=/usr/i486-mingw32/lib/pkgconfig
  unset PKG_CONFIG_PATH
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --cache-file=win32.cache
  make
}

package() {
  cd "$srcdir/glib-$pkgver"
  make install DESTDIR="$pkgdir"

  cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/{share/{gtk-doc,man},lib/charset.alias}
}

