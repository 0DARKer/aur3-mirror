# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-glib2
pkgver=2.26.1
pkgrel=1
arch=(any)
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (mingw32)"
depends=(mingw32-gettext mingw32-zlib)
makedepends=(mingw32-gcc)
license=(LGPL)
options=(!strip)
url="http://www.gtk.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/2.26/glib-$pkgver.tar.bz2)
sha256sums=('7a74ff12b6b9dee1f2d0e520b56b68b621920c4f4250bdf23468e515625c28d5')

build()
{
  cd "$srcdir/glib-$pkgver"

cat > win32.cache << EOF
glib_cv_long_long_format=I64
glib_cv_stack_grows=no
EOF

  export CPPFLAGS="-D_REENTRANT"
  export PKG_CONFIG_LIBDIR=/usr/i486-mingw32/lib/pkgconfig
  unset PKG_CONFIG_PATH
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --cache-file=win32.cache
  make
}

package() {
  cd "$srcdir/glib-$pkgver"
  make install DESTDIR="$pkgdir"

  cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/{share/{gtk-doc,man},lib/charset.alias}
}

