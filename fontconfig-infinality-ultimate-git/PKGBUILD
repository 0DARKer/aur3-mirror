# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: bohoomil <bohoomil@zoho.com>

# Installation order: freetype2-infinality-ultimate, fontconfig-infinality-ultimate, cairo-infinality-ultimate
pkgname=fontconfig-infinality-ultimate-git
_basename=fontconfig
_gitname=fontconfig-ultimate
pkgdesc="A library for configuring and customizing font access (includes generic fontconfig package and ultimate settings by bohoomil; git version)."
url=('http://www.fontconfig.org/release/')
license=('custom' 'CC-BY-SA')
pkgver=2.10.93
pkgrel=1
epoch=1
arch=('i686' 'x86_64')
groups=('infinality-bundle')
depends=('expat' 'freetype2-infinality-ultimate')
options=('!libtool')
provides=('fontconfig=$pkgver' 'fontconfig-infinality' 'fontconfig-infinality-ultimate')
conflicts=('fontconfig' 'fontconfig-infinality' 'fontconfig-infinality-git' 'fontconfig-infinality-ultimate')
backup=('etc/fonts/fonts.conf')
install=fontconfig-ultimate.install

source=(http://www.fontconfig.org/release/${_basename}-${pkgver}.tar.bz2
        git+https://github.com/bohoomil/fontconfig-ultimate.git)

md5sums=('0f8e5c63c2d6931626185c02802375a3'
         'SKIP')

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html

#version() {
#  cd $_gitname
#  git log -1 --format="%cd" --date=short | sed 's|-|.|g'
#}

build() {

  patches=(00-fonts.conf.in.patch
           01-configure.patch
           02-configure.ac.patch
           03-Makefile.in.patch
           04-Makefile.conf.d.patch)

  cd "${srcdir}/${_basename}-${pkgver}"

  cp -r ${srcdir}/fontconfig-ultimate/conf.d.infinality ${srcdir}/${_basename}-${pkgver}/conf.d.infinality
  cp -r ${srcdir}/fontconfig-ultimate/fontconfig_patches/*.patch .

  # infinality & post release fixes
  for patch in "${patches[@]}"; do
    patch -Np1 -i ${patch}
  done

  # make sure there's no rpath trouble and sane .so versioning -
  # FC and Gentoo do this as well
  aclocal
  libtoolize -f
  automake -fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-templateinfdir=/etc/fonts/conf.avail.infinality \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

check() {
  cd "${srcdir}/${_basename}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${_basename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 "${srcdir}/fontconfig-ultimate/fontconfig_patches/29-replace-bitmap-fonts.conf" "${pkgdir}/etc/fonts/conf.avail"

  #Install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${_basename}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${_basename}"

  #install infinality bits
  cd ../${_gitname}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 "${srcdir}/fontconfig-ultimate/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r "${srcdir}/fontconfig-ultimate/doc" "${pkgdir}/usr/share" 

  cp -r "${srcdir}/fontconfig-ultimate/doc" "${pkgdir}/usr/share/"

  install -m755 -d "${pkgdir}/usr/share/doc/${pkgname}/freetype"
  install -m755 "${srcdir}/fontconfig-ultimate/freetype/infinality-settings.sh" "${pkgdir}/usr/share/doc/${pkgname}/freetype/infinality-settings.sh"

  find "${pkgdir}" -type d -name .git -exec rm -r '{}' +
}
