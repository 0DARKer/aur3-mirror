# Maintainer: bohoomil <at zoho dot com>

pkgname=fontconfig-infinality-ultimate-git
_gitname=fontconfig-ultimate
pkgver=2013.05.04
pkgrel=1
epoch=1
pkgdesc="Fontconfig files for use with freetype2-infinality, customized by bohoomil. Standalone git version, incompatible with infinality-bundle."
arch=(any)
license=('CC-BY-SA')
url="https://github.com/bohoomil/fontconfig-ultimate"
depends=('fontconfig>=2.10.1' 'freetype2-infinality')
makedepends=('git')
provides=('fontconfig-infinality')
conflicts=('fontconfig-infinality' 'fontconfig-infinality-git' 'fontconfig-infinality-ultimate')
install='fontconfig.install'
backup=('etc/fonts/infinality/infinality.conf'
        'etc/fonts/conf.d/52-infinality.conf')
source=('git://github.com/bohoomil/fontconfig-ultimate.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  git log -1 --format="%cd" --date=short | sed 's|-|.|g'
}

package() {
  cd $_gitname
 
  mkdir -p ${pkgdir}/etc/fonts/{conf.d,infinality}
  mkdir -p ${pkgdir}/etc/profile.d

  cp -r ${srcdir}/fontconfig-ultimate/etc-fonts/infinality ${pkgdir}/etc/fonts
  cp ${srcdir}/fontconfig-ultimate/etc-profile.d/infinality-settings.sh ${pkgdir}/etc/profile.d/infinality-settings.sh.pacnew
  ln -sd /etc/fonts/infinality/conf.d/52-infinality.conf ${pkgdir}/etc/fonts/conf.d/52-infinality.conf
  install -m644 -D ${srcdir}/fontconfig-ultimate/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
}
