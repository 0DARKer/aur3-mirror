# Maintainer: OK100 <ok100.ok100.ok100@gmail.com>

pkgname=gnome-mplayer-gtk2
pkgver=1.0.8
pkgrel=1
pkgdesc='A simple MPlayer GUI. GTK2 version.'
arch=('i686' 'x86_64')
url='http://gnome-mplayer.googlecode.com/'
license=('GPL')
depends=('mplayer' 'dbus-glib' 'libnotify' 'hicolor-icon-theme' 'dconf' 'gmtk-gtk2')
makedepends=('gnome-power-manager')
provides=(gnome-mplayer=${pkgver})
conflicts=('gnome-mplayer')
install="${pkgname}.install"
source=("http://gnome-mplayer.googlecode.com/files/gnome-mplayer-${pkgver}.tar.gz")

build() {
  cd "${srcdir}/gnome-mplayer-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-install --disable-nautilus --disable-gtk3
  make
}

package() {
  cd "${srcdir}/gnome-mplayer-${pkgver}"

  make DESTDIR="${pkgdir}" install

  sed -i 's@Exec=gnome-mplayer %U@Exec=gnome-mplayer %F@' \
    "${pkgdir}/usr/share/applications/gnome-mplayer.desktop"
}

md5sums=('9225dc0645d050ee31dd877559d7c16b')
