# Maintainer: Ecmel Ercan <ecmel dot ercan at gmail dot com>
# Contributor: David Brazdil <david dot brazdil at gmail dot com>
pkgname=qooxdoo-sdk
pkgver=2.0.2
pkgrel=1
dirname=qooxdoo
pkgdesc="A universal JavaScript framework."
arch=('any')
url="http://qooxdoo.org"
license=('LGPL' 'EGPL')
depends=(python2)
makedepends=(unzip)
optdepends=()
provides=('qooxdoo')
conflicts=('qooxdoo')
options=(!strip)
source=(http://downloads.sourceforge.net/qooxdoo/$dirname-$pkgver-sdk.zip)
noextract=($dirname-$pkgver-sdk.zip)
md5sums=('2048cd76397dbc7028f43733864a5ce9')

build() {
  msg "Extracting source"
  cd "$srcdir"
  unzip -q "$dirname-$pkgver-sdk.zip"

  cd "$srcdir/$dirname-$pkgver-sdk"

  msg "Changing links to python2"

  for x in `find -regex ".*[.]py$"`; do
    line=`head -n 1 "$x" | head -c 2`;
    if [ "$line" = '#!' ]; then
      echo '#!/usr/bin/env python2' > "${x}-edit";
      tail -n +2 $x >> "${x}-edit";
      mv "${x}-edit" $x;
      chmod a+x $x;
    fi;
  done
}

package() {
  mkdir -p "$pkgdir/opt/"
  mv "$srcdir/$dirname-$pkgver-sdk" "$pkgdir/opt/$pkgname"

  msg "Generating: widgetbrowser"
  cd $pkgdir/opt/$pkgname/application/widgetbrowser/
  ./generate.py build

  msg "Generating: playground"
  cd $pkgdir/opt/$pkgname/application/playground/
  ./generate.py build

  msg "Creating link: qx-create-application"
  mkdir -p $pkgdir/usr/bin/

  cat > $pkgdir/usr/bin/qx-create-application <<EOF
#!/bin/sh
/opt/qooxdoo/tool/bin/create-application.py \$@
EOF

  chmod a+x $pkgdir/usr/bin/qx-create-application
}

