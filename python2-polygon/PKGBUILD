#Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python2-polygon
pkgver=2.0.5
pkgrel=1
pkgdesc="Python package that handles polygonal shapes in 2D"
arch=('i686' 'x86_64')
url="http://www.j-raedler.de/projects/polygon/"
depends=('python2-numpy')
license=('LGPL')
options=(!libtool)
source=(https://github.com/downloads/jraedler/Polygon2/Polygon-${pkgver}.zip)
md5sums=('f1034a801b8b599bd117166108128468')

build() {
    cd "$srcdir"/Polygon-$pkgver
    export CFLAGS+=" -I/`python2 -c 'from distutils.sysconfig import get_python_lib; \
	    print get_python_lib()'`/numpy/core/include/"
    export PYTHON="`which python2`"
    python2 setup.py build
}

package() {
    cd "$srcdir"/Polygon-$pkgver
    python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

