# Maintainer: Jens Staal <staal1978@gmail.com>
# Adopted from mingw-w64 package by: Daniel Kirchner <daniel AT ekpyron DOT org>

pkgname=open_watcom-zlib
pkgver=1.2.8
pkgrel=5
pkgdesc="A compression/decompression Library (OpenWatcom (cross-)compilation)"
arch=('any')
license=('custom:zlib')
depends=(open_watcom-v2)
makedepends=(open_watcom-v2)
url="http://www.zlib.net/"
source=("http://zlib.net/zlib-${pkgver}.tar.gz")
provides=('open_watcom-zlib-static')
conflicts=('open_watcom-zlib-static')
options=(!strip !buildflags staticlibs emptydirs)
md5sums=('44d667c142d7cda120332623eab69f40')



build() {
  cd $srcdir/zlib-${pkgver}
  source /opt/watcom/owsetenv.sh
  #we want to move stuff as early as possible to the right location
  install -d $srcdir/build/$WATCOM/{lib286/dos,lib386/{linux,dos,nt},h/nt,lh}
  
  msg "build for linux"
  CC=owcc ./configure --prefix=$WATCOM -static

  make CC=owcc RC=wrc STRIP=wstrip CFLAGS="-O3 -blinux" AR=wlib ARFLAGS="" LD=wlink RANLIB=true
  install -m644 libz.a $srcdir/build/$WATCOM/lib386/linux/zlib.lib
  #cleaning... notice that makepkg -s fails the 2nd time
  install -m644 zconf.h $srcdir/build/$WATCOM/lh/
  rm zconf.h #to make sure that subsequent configures are clean
  make clean
  rm -f *.{o,a}
  
  msg "build for DOS.."
  sed 's|del|rm|g' -i watcom/*.mak
  cp test/example.c example.c
  cp test/minigzip.c minigzip.c
  msg2 "...16-bit"
  export INCLUDE=$WATCOM/h
  C=owcc ./configure --prefix=$WATCOM -static
  wmake -f watcom/watcom_l.mak CFLAGS="-zq -ml -s -bt=dos -oilrtfm -fr=nul -wx -fo=.obj"
  install -m644 zlib_l.lib $srcdir/build/$WATCOM/lib286/dos/zlib.lib
  wmake -f watcom/watcom_l.mak clean
  rm -f *.{obj,a,lib,exe}

  msg2 "...extended 32-bit"
  export INCLUDE=$WATCOM/h
  wmake -f watcom/watcom_f.mak CFLAGS="-zq -mf -3r -fp3 -s -bt=dos -oilrtfm -fr=nul -wx -fo=.obj"
  install -m644 zlib_f.lib $srcdir/build/$WATCOM/lib386/dos/zlib.lib
  wmake -f watcom/watcom_f.mak clean
  install -m644 zconf.h $srcdir/build/$WATCOM/h/
  rm zconf.h #to make sure that subsequent configures are clean
  rm -f *.{obj,a,lib,exe}
  
  msg "build for Win32"
  export INCLUDE=$WATCOM/h:$WATCOM/h/nt
  C=owcc ./configure --prefix=$WATCOM -static #-shared
  sed 's|(RCFLAGS) -o |(RCFLAGS) -r -fo=|g' -i win32/Makefile.gcc
  #sed 's|-o |-fo=|g' -i win32/Makefile.gcc
  #sed 's|win32/zlib.def|-dll -def:win32/zlib.def|g' -i win32/Makefile.gcc
  #temporary fix : remove building of dll
  sed 's|\$(SHAREDLIB) \$(IMPLIB)||g' -i win32/Makefile.gcc
  sed 's|example_d.exe minigzip_d.exe||g' -i win32/Makefile.gcc
  sed 's|libz.a|zlib.lib|g' -i win32/Makefile.gcc
  make -f win32/Makefile.gcc CC=owcc AR=wlib RC=wrc RCFLAGS="-bt=nt" STRIP=true \
  CFLAGS="-O3 -bnt" AR=wlib ARFLAGS="" LD=owcc RANLIB=true
  install -m644 zlib.lib $srcdir/build/$WATCOM/lib386/nt/zlib.lib
  #install -m644 libz1.dll $srcdir/build/opt/watcom/lib386/nt/libz1.dll
  install -m644 zconf.h $srcdir/build/$WATCOM/h/nt/
  make -f win32/Makefile.gcc clean
  rm -f *.{o,a,exe}
    
}

package () {
    cd $srcdir/zlib-${pkgver}
    install -dm755 ${pkgdir}${WATCOM}/{lib286/dos,lib386/{linux,dos,nt},h/nt,lh}
    cp -R $srcdir/build/* $pkgdir
    chmod 644 ${pkgdir}${WATCOM}
    install -m644 zlib.h ${pkgdir}${WATCOM}/{h,lh}/
}
