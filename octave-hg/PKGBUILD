# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Benjamin van der Burgh <benjaminvdb@gmail.com>

pkgname=octave-hg
pkgver=r19590.6d75f1683ce8
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
url="http://www.octave.org"
arch=('i686' 'x86_64')
license=('GPL')
# Some of these may be optional, e.g. arpack, lapack, qhull, but if they
# are installed, octave will be linked against them.
depends=('fftw>=3.2.2' 'curl' 'fltk' 'hdf5' 'glpk' 'arpack' 'gl2ps' 
  'qrupdate' 'qscintilla' 'qhull' 'graphicsmagick')
makedepends=('pcre' 'mercurial' 'gcc-fortran' 'gperf' 'rsync'
	     'suitesparse-octavecompat' 'transfig' 'epstool' 'texlive-core')
optdepends=('texinfo: for help-support in octave'
	    'gnuplot: alternative plotting'
	    'portaudio: for using audiodevinfo.oct')
conflicts=('octave')
install=octave.install
options=('!emptydirs' '!makeflags')
source=(git://git.sv.gnu.org/gnulib hg+http://hg.savannah.gnu.org/hgweb/octave)
md5sums=('SKIP'
         'SKIP')
_hgrepo=octave
_appver=4.1.0+
provides=("octave=$_appver")

pkgver() {
  cd $srcdir/${_hgrepo}
  hg update > /dev/null
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
}

build() {
  cd $srcdir/${_hgrepo}
  ./bootstrap --no-git --gnulib-srcdir=$srcdir/gnulib
  [[ -d .build ]] || mkdir .build
  cd .build
  [[ $CARCH == "x86_64" ]] && _arch=amd64
  [[ $CARCH == "i686" ]] && _arch=i386
  CXX=g++ CC=gcc ../configure --prefix=/usr \
    --libexecdir=/usr/lib --enable-shared --enable-jit \
    --enable-qhull MOC=moc-qt4 UIC=uic-qt4 --with-umfpack \
    --with-quantum-depth=16 --disable-java \
    --with-java-homedir=/usr/lib/jvm/`archlinux-java get` \
    --with-java-includedir=/usr/lib/jvm/`archlinux-java get`/include \
    --with-java-libdir=/usr/lib/jvm/`archlinux-java get`/lib/${_arch}/server 
  make clean
  CXXFLAGS+="-fexceptions `llvm-config --cxxflags`" make
}

package() {
  cd $srcdir/${_hgrepo}/.build
  make DESTDIR=${pkgdir} install
  # add octave library path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${_hgrepo}/${_appver}" > "${pkgdir}/etc/ld.so.conf.d/${_hgrepo}.conf"
}
