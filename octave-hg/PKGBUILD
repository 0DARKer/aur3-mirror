# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Benjamin van der Burgh <benjaminvdb@gmail.com>

pkgname=octave-hg
pkgver=19215.c61deba773ab
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
url="http://www.octave.org"
arch=('i686' 'x86_64')
license=('GPL')
# Some of these may be optional, e.g. arpack, lapack, qhull, but if they
# are installed, octave will be linked against them. If you build with
# clang++, you will also need llvm-libs
depends=('fftw>=3.2.2' 'curl' 'fltk' 'hdf5' 'glpk' 'arpack' 'gl2ps' 
  'qrupdate' 'qscintilla' 'qhull' 'graphicsmagick' 'glu')
makedepends=('pcre' 'mercurial' 'gcc-fortran' 'gperf' 'perl' 'rsync' 
  'transfig' 'epstool' 'suitesparse-solibs')
optdepends=('texinfo: for help-support in octave'
  'gnuplot: alternative plotting')
conflicts=('octave')
install=octave.install
options=('!emptydirs' '!makeflags')
source=(git://git.sv.gnu.org/gnulib hg+http://hg.savannah.gnu.org/hgweb/octave)
md5sums=('SKIP'
         'SKIP')
_hgrepo=octave
_appver=4.1.0+
provides=("octave=$_appver")

pkgver() {
  cd $srcdir/${_hgrepo}
  hg update > /dev/null
  echo $(hg identify -n).$(hg identify -i)
}

prepare () {
  sed -i 's+SUITESPARSE_VER_CODE (4, 3)+4,3+' $srcdir/${_hgrepo}/liboctave/util/oct-sparse.h
}

build() {
  cd $srcdir/${_hgrepo}
  ./bootstrap --no-git --gnulib-srcdir=$srcdir/gnulib
  mkdir .build
  cd .build
  [[ $CARCH == "x86_64" ]] && _arch=amd64
  [[ $CARCH == "i686" ]] && _arch=i386
  CXX=g++ CC=gcc ../configure --prefix=/usr \
    --libexecdir=/usr/lib --enable-shared --enable-jit \
    --enable-qhull --enable-java MOC=moc-qt4 UIC=uic-qt4 \
    --with-quantum-depth=16 --disable-java \
    --with-umfpack
#    --with-java-homedir=/usr/lib/jvm/java-8-openjdk \
#    --with-java-includedir=/usr/lib/jvm/java-8-openjdk/include \
#    --with-java-libdir=/usr/lib/jvm/java-8-openjdk/lib/${_arch}/server \
    
  CXXFLAGS+="-fexceptions `llvm-config --cxxflags`" make
}

package() {
  cd $srcdir/${_hgrepo}/.build
  make DESTDIR=${pkgdir} install
  # add octave library path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${_hgrepo}/${_appver}" > "${pkgdir}/etc/ld.so.conf.d/${_hgrepo}.conf"
}
