# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Benjamin van der Burgh <benjaminvdb@gmail.com>

pkgname=octave-hg
pkgver=18308.c1baf94184af
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
url="http://www.octave.org"
arch=('i686' 'x86_64')
license=('GPL')
# Some of these may be optional, e.g. arpack, lapack, qhull, but if they
# are installed, octave will be linked against them.
depends=('fftw>=3.2.2' 'curl' 'fltk' 'hdf5' 'glpk' 'arpack' 'gl2ps' 
  'qrupdate' 'qscintilla' 'qhull' 'graphicsmagick' 'java-environment' 
  'glu')
makedepends=('pcre' 'mercurial' 'gcc-fortran' 'gperf' 'perl' 'rsync' 
  'transfig' 'epstool')
optdepends=('texinfo: for help-support in octave'
  'gnuplot: alternative plotting')
conflicts=('octave' 'octave3' 'octave-suitsparse')
install=octave.install
options=('!emptydirs')
source=(hg+http://hg.savannah.gnu.org/hgweb/octave llvm-3.4.patch::http://savannah.gnu.org/bugs/download.php?file_id=30296)
md5sums=('SKIP'
         'd5bde91483417b6ad8a3f80335e1f5a0')
_hgrepo=octave
_appver=4.1.0+
provides=("octave=$_appver")

pkgver() {
  cd $srcdir/${_hgrepo}
  hg update > /dev/null
  echo $(hg identify -n).$(hg identify -i)
}

prepare () {
  cd $srcdir/${_hgrepo}
  patch -p1 < $srcdir/llvm-3.4.patch
}

build() {
  cd $srcdir/${_hgrepo}
  export LC_NUMERIC=C 
  
  ./bootstrap
  mkdir .build
  cd .build
  ../configure --prefix=/usr \
    --libexecdir=/usr/lib --enable-shared --enable-jit \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lrt" \
    --enable-qhull --enable-java MOC=moc-qt4 UIC=uic-qt4 \
    --with-java-homedir=/usr/lib/jvm/java-7-openjdk \
    --with-java-includedir=/usr/lib/jvm/java-7-openjdk/include \
    --with-java-libdir=//usr/lib/jvm/java-7-openjdk/lib 
  CXXFLAGS+="-fexceptions `llvm-config --cxxflags`" make
}

package() {
  cd $srcdir/${_hgrepo}/.build
  make DESTDIR=${pkgdir} install
  # add octave library path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${_hgrepo}/${_appver}" > "${pkgdir}/etc/ld.so.conf.d/${_hgrepo}.conf"
}
