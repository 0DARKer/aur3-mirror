# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Benjamin van der Burgh <benjaminvdb@gmail.com>

pkgname=octave-hg
pkgver=16834.7246f7deacbe
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
url="http://www.octave.org"
arch=('i686' 'x86_64')
license=('GPL')

# Some of these may be optional, e.g. arpack, lapack, qhull, but if they
# are installed, octave will be linked against them.
depends=('fftw>=3.2.2' 'curl' 'fltk' 'hdf5' 'glpk' 'arpack' \
  'qrupdate' 'qscintilla' 'qhull' 'graphicsmagick')

makedepends=('pcre' 'mercurial' 'texinfo' 'gcc-fortran' 'gperf' \
  'perl' 'rsync')
# For building the documentation, epstool and fig2dev are searched during
# build. The build works without them, but you may want to install
# transfig from [extra] and epstool from AUR
optdepends=('texinfo: for help-support in octave'
  'gnuplot: alternative plotting')
conflicts=('octave' 'octave3' 'octave-suitsparse')
provides=('octave=3.7.5')
#install=octave.install
options=('!emptydirs')
source=(hg+http://hg.savannah.gnu.org/hgweb/octave)
md5sums=('SKIP')
_hgrepo=octave

pkgver() {
  cd $srcdir/${_hgrepo}
  echo $(hg identify -n).$(hg identify -i)
}

prepare() {
  cd $srcdir/${_hgrepo}
  sed -i s+llvm/Support/IRBuilder.h+llvm/IR/IRBuilder.h+ \
    libinterp/interp-core/jit-typeinfo.cc libinterp/interp-core/pt-jit.cc
  sed -i '43i#include <llvm/IR/Attributes.h>' \
    libinterp/interp-core/jit-typeinfo.cc
  sed -i s+llvm/Value.h+llvm/IR/Value.h+ \
    libinterp/interp-core/jit-util.cc
  sed -i s+llvm/GlobalVariable.h+llvm/IR/GlobalVariable.h+ \
    libinterp/interp-core/jit-typeinfo.cc
  sed -i s+llvm/LLVMContext.h+llvm/IR/LLVMContext.h+ \
    libinterp/interp-core/jit-typeinfo.cc libinterp/interp-core/pt-jit.cc
  sed -i s+llvm/Function.h+llvm/IR/Function.h+ \
    libinterp/interp-core/jit-typeinfo.cc
  sed -i s+llvm/Instructions.h+llvm/IR/Instructions.h+ \
    libinterp/interp-core/jit-typeinfo.cc libinterp/interp-core/jit-ir.cc
  sed -i s+llvm/Intrinsics.h+llvm/IR/Intrinsics.h+ \
    libinterp/interp-core/jit-typeinfo.cc
  sed -i s+llvm/BasicBlock.h+llvm/IR/BasicBlock.h+ \
    libinterp/interp-core/jit-ir.cc
  sed -i s+llvm/Module.h+llvm/IR/Module.h+ \
    libinterp/interp-core/pt-jit.cc
  sed -i s+llvm/Target/TargetData.h+llvm/IR/DataLayout.h+ \
    libinterp/interp-core/pt-jit.cc
  sed -i s+TargetData+DataLayout+g libinterp/interp-core/pt-jit.cc
}

build() {
  cd $srcdir/${_hgrepo}
  ./bootstrap 
  CXXFLAGS+="-fexceptions `lvm-config --cxxflags`" ./configure --prefix=/usr \
    --libexecdir=/usr/lib --enable-shared \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lrt" --enable-jit \
    --enable-qhull --disable-java  MOC=moc-qt4 UIC=uic-qt4
   make all
}

package() {
  cd $srcdir/${_hgrepo}
  make DESTDIR=${pkgdir} install
  # add octave library path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  export _appver=$(${pkgdir}/usr/bin/octave --version | head -1 | sed "s/.*version \([^ ]*\).*$/\1/")
  echo "/usr/lib/${_hgrepo}/${_appver}" > "${pkgdir}/etc/ld.so.conf.d/${_hgrepo}.conf"
}
