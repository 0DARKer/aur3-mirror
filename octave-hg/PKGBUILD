# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=octave-hg  
pkgver=16449
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
url="http://www.octave.org"
arch=('i686' 'x86_64')
license=('GPL')

# Some of these may be optional, e.g. arpack, lapack, qhull, but if they 
# are installed, octave will be linked against them.
depends=('fftw>=3.2.2' 'curl' 'fltk' 'hdf5' 'glpk' 'arpack' \
 'qrupdate' 'qscintilla' 'qhull' 'graphicsmagick')
 
makedepends=('pcre' 'mercurial' 'texinfo' 'gcc-fortran' 'gperf' \
  'perl' 'rsync')
# For building the documentation, epstool and fig2dev are searched during 
# build. The build works without them, but you may want to install 
# transfig from [extra] and epstool from AUR
optdepends=('texinfo: for help-support in octave'
            'gnuplot: alternative plotting')
conflicts=('octave' 'octave3' 'octave-suitsparse')
provides=('octave=3.7.2+')
#install=octave.install
options=('!emptydirs')
source=(hg+http://hg.savannah.gnu.org/hgweb/octave)
md5sums=('SKIP')
_hgrepo=octave

pkgver() {
  cd $srcdir/$_hgrepo
  echo $(hg identify -ni|cut -d " " -f2)
}

build() {
  cd $srcdir/${_hgrepo}
  ./bootstrap --bootstrap-sync
LANG=C  
  ./configure --prefix=/usr --libexecdir=/usr/lib --enable-shared \
    --with-umfpack="-lumfpack -lsuitesparseconfig -lrt" --disable-jit \
    --enable-qhull --disable-java --disable-docs
  make
}

package() {
  cd $srcdir/${_hgrepo}
  make DESTDIR=${pkgdir} install
  # add octave library path to ld.so.conf.d
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${pkgname}/${pkgver}" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
} 
