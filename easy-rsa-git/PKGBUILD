# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=easy-rsa-git
pkgver=2.2.0.r3.gff5bfd1
pkgrel=1
pkgdesc="An easy-to-use, robust, and highly configurable VPN (Virtual Private Network) - git checkout"
arch=('i686' 'x86_64')
url="https://github.com/OpenVPN/easy-rsa"
depends=('openssl')
conflicts=('easy-rsa' 'openvpn')
provides=('easy-rsa')
license=('custom')
source=('git://github.com/OpenVPN/easy-rsa.git')

pkgver() {
	cd easy-rsa/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd easy-rsa/

	# Build easy-rsa
	autoreconf -vi
	./configure \
		--prefix=/usr
	make
}

package() {
	cd easy-rsa/

	# Install easy-rsa
	mkdir -p ${pkgdir}/usr/share/easy-rsa/
	make DESTDIR=${pkgdir} install

	# remove old openssl config
	rm ${pkgdir}/usr/share/easy-rsa/openssl-0.9.{6,8}.cnf
}

sha256sums=('SKIP')
