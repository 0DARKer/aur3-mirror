# Maintainer: Paul Burton <paulburton89@gmail.com>
pkgname=ejtagproxy-svn
pkgver=47
pkgrel=1
pkgdesc="GDB interface utility for PIC32 and other MIPS processors"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ejtagproxy"
license=('BSD')
depends=('libusb')
makedepends=('subversion' 'pcre')

_svntrunk=http://ejtagproxy.googlecode.com/svn/trunk
_svnmod=ejtagproxy

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  # The makefile does an odd read of .svn/entries
  mkdir .svn
  echo "${pkgver}" >.svn/entries

  # ...and has some dodgy OS detection
  sed -i 's|/lib/i386-linux-gnu|/|' makefile

  make
}

package() {
  cd "$srcdir/$_svnmod-build"

  # The makefile installs to a hardcoded path...
  install -Dm755 ejtagproxy "${pkgdir}/usr/bin/ejtagproxy"

  # Extract the license
  offsets=(`pcregrep -M --file-offsets 'Copyright \(C\)(\n|.)*?\*/' \
    target-ejtag.c | head -n 1 | tr ',' ' '`)
  start=${offsets[0]}
  len=${offsets[1]}
  dd if=target-ejtag.c bs=1 skip=${start} count=${len} 2>/dev/null | \
    sed 's|/\*||' | sed 's|\*/||' | sed 's|^ \* ||g' | sed 's|^ \*$||g' \
    >LICENSE
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
