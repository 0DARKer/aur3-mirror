# Maintainer: Gicu Gorodenco <cyclopsihus 'at' gmail 'dot' com>
# Contributor: rafael ff1 <rafael.f.f1 'at' gmail 'dot' com> 
_pkgbasename=pangomm
pkgname=lib32-$_pkgbasename
pkgver=2.27.1
pkgrel=1
pkgdesc="C++ bindings for pango (32 bit, library only)"
arch=('x86_64')
url="http://gtkmm.sourceforge.net/"
license=('LGPL')
depends=('lib32-pango>=1.28.0' 'lib32-glibmm>=2.24.2' 'lib32-cairomm>=1.8.2')
makedepends=('pkgconfig' 'gcc-multilib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/2.27/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('be3a0cf41589dab65e2d0ee3896f025179aa3676be70b87c50e1dd769becc79d')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
  # Cleanup for a libr32 package
  rm -rf ${pkgdir}/usr/{include,lib32/$_pkgbasename*}
  # Install license
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING
}
