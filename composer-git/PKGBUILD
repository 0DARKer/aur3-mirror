# Maintainer: spider-mario <spidermario@free.fr>
pkgname=composer-git
pkgver=20130406
pkgrel=1
pkgdesc="Song editor for karaoke games"
arch=(i686 x86_64)
url="http://performous.org/composer"
license=(GPL)
depends=(phonon ffmpeg)
makedepends=(git)
provides=(composer)
conflicts=(composer)
source=(av.patch
        cmake.patch
        git+https://github.com/performous/composer.git)
sha1sums=('f22d5fc8bc9075cdd1c18a1601410d757f751813'
          '112063f483e01d61d1353519ae528581ac633c4e'
          'SKIP')

prepare() {
  cd "$srcdir/composer/src"

  git apply --3way "$srcdir/av.patch"
  git apply --3way "$srcdir/cmake.patch"
}

build() {
  mkdir "$srcdir/composer/build"
  cd "$srcdir/composer/build"

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    ..
  make
}

package() {
  cd "$srcdir/composer/build"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
