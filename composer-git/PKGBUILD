# Maintainer: spider-mario <spidermario@free.fr>
pkgname=composer-git
pkgver=20120823
pkgrel=1
pkgdesc="Song editor for karaoke games"
arch=(i686 x86_64)
url="http://performous.org/composer"
license=(GPL)
depends=(phonon ffmpeg)
makedepends=(git)
provides=(composer)
conflicts=(composer)
source=(av.patch)
md5sums=('3392c2f2a595144395782d2923aba2b5')

_gitroot=git://git.performous.org/gitroot/performous/editor
_gitname=editor

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git reset --hard && git pull
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi
  git apply --3way "$srcdir/av.patch"

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  mkdir "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "$srcdir/$_gitname/"
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
