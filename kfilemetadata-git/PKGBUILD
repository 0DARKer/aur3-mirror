# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfilemetadata-git
pkgver=r22.db4cdee
pkgrel=1
pkgdesc="A library for extracting file metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/base/kfilemetadata'
license=('LGPL')
depends=('kdelibs' 'poppler-qt' 'taglib' 'ffmpeg' 'ebook-tools')
makedepends=('cmake' 'automoc4' 'git')
provides=('kfilemetadata')
conflicts=('kfilemetadata')
source=('git://anongit.kde.org/kfilemetadata')
md5sums=('SKIP')

pkgver() {
  cd kfilemetadata
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kfilemetadata \
    -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
