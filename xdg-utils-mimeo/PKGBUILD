# Contributor: cantabile <cantabile dot desu at gmail dot com> 

_appname=xdg-utils
_suffix=-mimeo
pkgname=${_appname}${_suffix}
pkgver=1.0.2.20100618
pkgrel=2
pkgdesc='Command line tools that assist applications with a variety of desktop integration tasks. Patched to use mimeo if available.'
arch=('any')
url='http://portland.freedesktop.org/'
depends=('sh')
makedepends=('docbook-xsl' 'lynx' 'xmlto')
optdepends=('xorg-utils: needed if using Xfce'
            'mimeo: if you want xdg-open to use mimeo to open stuff')

conflicts=('xdg-utils')
provides=('xdg-utils')

license=('custom')
source=("ftp://ftp.archlinux.org/other/${_appname}/${_appname}-${pkgver}.tar.xz" 'mimeo.patch')


build() {
	cd ${srcdir}/${_appname}-${pkgver}
   patch -Np1 -i ../mimeo.patch
	./configure --prefix=/usr --mandir=/usr/share/man || return 1
	cd scripts
	make scripts html man || return 1
	cd ..
	make || return 1
}

package() {
	cd ${srcdir}/${_appname}-${pkgver}
	make DESTDIR=${pkgdir} install || return 1
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${_appname}/LICENSE
}

_source() {
	local _current=$(pwd)
	local _tmp=$(mktemp -d)
	cd $_tmp

	cvs -d:pserver:anoncvs@anoncvs.freedesktop.org:/cvs/portland co -d ${_appname}-${pkgver} portland/xdg-utils
	find ${_appname}-${pkgver} -name 'CVS' -type d -exec rm -rf {} \;
	bsdtar cJf ${_appname}-${pkgver}.tar.xz ${_appname}-${pkgver}
	mv ${_appname}-${pkgver}.tar.xz $_current
	cd $_current
	rm -rf $_tmp
}
md5sums=('d032904fe43bfb384563cc2227316301'
         '524278a025f9302f89f27480c225fe8f')
