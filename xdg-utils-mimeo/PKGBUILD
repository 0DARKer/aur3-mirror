# $Id$
# Maintainer: matthewbauer <mjbauer95+aur@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: cantabile <cantabile dot desu at gmail dot com>

_pkgname=xdg-utils

pkgname=${_pkgname}-mimeo
pkgver=1.1.0rc1
pkgrel=5
pkgdesc='Command line tools that assist applications with a variety of desktop integration tasks; patched to use mimeo'
arch=('any')
url='http://portland.freedesktop.org/'
license=('MIT')
depends=('sh' 'xorg-xset' 'mimeo')
makedepends=('docbook-xsl' 'lynx' 'xmlto')
optdepends=('kdebase-runtime: for KDE support in xdg-open'
	'libgnome: for GNOME support in xdg-open'
	'exo: for Xfce support in xdg-open'
	'xorg-xprop: for Xfce support in xdg-open'
	'pcmanfm: for LXDE support in xdg-open'
	'mimeo: for mimeo support in xdg-open'
	'perl-file-mimeinfo: for generic support in xdg-open')
provides=('xdg-utils')
conflicts=('xdg-utils')
source=(#"ftp://ftp.archlinux.org/other/${_pkgname}/${_pkgname}-${pkgver}.tar.xz"
	http://portland.freedesktop.org/download/${_pkgname}-${pkgver/rc1/-rc1}.tar.gz \
	xfce-detection.diff
	mimeo-detection.diff)
md5sums=('fadf5e7a08e0526fc60dbe3e5b7ef8d6'
         '061c85755b9f3ca8a8cc847518b6be3d'
         '20d615042b27cc3b715170d5c6db6a0c')

build() {
	#cd ${srcdir}/${pkgname}-${pkgver}
	cd "${srcdir}/${_pkgname}-${pkgver/rc1/-rc1}"
	patch -p0 -i "${srcdir}"/xfce-detection.diff
	patch -p1 -i "${srcdir}"/mimeo-detection.diff
	./configure --prefix=/usr --mandir=/usr/share/man
	make -C scripts
	make
}

package() {
	#cd ${srcdir}/${pkgname}-${pkgver}
	cd "${srcdir}/${_pkgname}-${pkgver/rc1/-rc1}"
	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"
}

