pkgname=mingw-w64-libarchive
pkgver=3.0.4
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats (mingw-w64)"
arch=(any)
url="http://libarchive.googlecode.com"
license=("BSD")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-expat mingw-w64-openssl mingw-w64-xz mingw-w64-zlib)
options=(!libtool !strip !buildflags)
source=("https://github.com/downloads/libarchive/libarchive/libarchive-${pkgver}.tar.gz"
        "libarchive-3.0.x-fix-mtree-writer.patch")
md5sums=('af443ca9a10ddbcbf00f7ae34ca7fc16'
         '6d36a50a7282db6576bd1fbc23f08055')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/libarchive-$pkgver"
  patch -Np1 -i $srcdir/libarchive-3.0.x-fix-mtree-writer.patch
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --without-bz2lib
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}