# Contributor: Rener Paiva <ren2r@mail.com> 

pkgname=gtk2-dragmenubar
pkgver=2.24.5
pkgrel=2
pkgdesc="The GTK+ Toolkit (v2) patched to support drag window by empty space in menubar and toolbars."
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
makedepends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'krb5' 'gnutls'
             'shared-mime-info' 'cairo' 'libcups' 'gdk-pixbuf2' 'gobject-introspection')
depends=('atk' 'pango' 'libxcursor' 'libxinerama' 'libxrandr' 'libxi' 'libxcomposite' 'libxdamage' 'krb5' 'gnutls' 'shared-mime-info' 'cairo' 'libcups' 'gtk-update-icon-cache')
provides=("gtk2=${pkgver}")
conflicts=('gtk2')
options=('!libtool' '!docs')
license=('LGPL')
install=gtk2.install

source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.24/gtk+-${pkgver}.tar.xz
        xid-collision-debug.patch
        gdk_Make_background_changes_queue_a_repaint.patch
        drag_menubar_and_toolbar.patch)
sha256sums=('d6b30889efbb9fab9aa598092d08887826a7b2a3069b3bd5155dede28d9866f3'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            '3573af25e8db2fb490b264f80cb9e617ee1a36e767e36e7bb1d830c0b715db93'
            '5a13f6c401b12aff895e744afa05d3af07be6a1e7c5445a9b526804f43b94f4b')

build() {
    cd "${srcdir}/gtk+-${pkgver}"
    patch -Np1 -i "${srcdir}/xid-collision-debug.patch"
    patch -NRp1 -i "${srcdir}/gdk_Make_background_changes_queue_a_repaint.patch"
    patch -Np1 -i "${srcdir}/drag_menubar_and_toolbar.patch"

    CXX=/bin/false ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-xinput=yes
    make
}
package() {

    backup=(etc/gtk-2.0/gtkrc)

    cd "${srcdir}/gtk+-${pkgver}"

    make DESTDIR="${pkgdir}" install
    sed -i "s#env python#env python2#" $pkgdir/usr/bin/gtk-builder-convert
    echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc"
    #split this out to use with gtk3 too
    rm ${pkgdir}/usr/bin/gtk-update-icon-cache
}
