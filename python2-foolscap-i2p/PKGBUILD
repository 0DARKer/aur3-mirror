# Maintainer : Skydrome <irc.freenode.net>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: giniu <gginiu@gmail.com>

pkgname=python2-foolscap-i2p
pkgver=0.6.4
pkgrel=3
pkgdesc="RPC protocol for Python and Twisted over the I2P network"
url="http://foolscap.lothar.com"
license=('MIT')
arch=('any')
conflicts=('python2-foolscap')
provides=('python2-foolscap')
replaces=('python-foolscap-i2p')
options=(!emptydirs)

depends=('twisted'
         'python2'
         'python2-pyopenssl')

_patchver="${pkgver}-r1"
source=("http://foolscap.lothar.com/releases/foolscap-${pkgver}.tar.gz"
        "http://foolscap.lothar.com/releases/foolscap-${pkgver}.tar.gz.asc"
        #"0001-proxy-support.patch"
        #"0002-recognize-i2p-dests.patch"
        "http://killyourtv.i2p.us/tahoe-lafs/patches/foolscap-i2p-${_patchver}.patch")

sha256sums=('74b283dd68a3c9d68ebf4e9b4ebf5cfb37480915b6a808ac930fe858a66f8d99'
            'SKIP'
            '7cab26a7b0bf3b368c8e9989cf427147a7f8b7b0698822aa4fd635cf6072099a')

prepare() {
    cd "${srcdir}/foolscap-${pkgver}"
    for p in $(ls ${srcdir}/*.patch); do
        patch -Np1 -i $p
    done
}

build() {
    cd "${srcdir}/foolscap-${pkgver}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/foolscap-${pkgver}"
    python2 setup.py install --prefix=/usr --root="$pkgdir"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
