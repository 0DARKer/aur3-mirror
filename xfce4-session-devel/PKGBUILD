# Maintainer: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=xfce4-session-devel
pkgver=4.8.1
pkgrel=1
pkgdesc="A session manager for Xfce (Development Version)"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4-devel')
 # keep xorg-server-utils for https://bugs.archlinux.org/task/21096  'xorg-server-utils'
 # upower and consolekit for reboot/shutdown/hibernate/suspend
depends=('xfce4-panel-devel' 'gconf' 'libgnome-keyring' 'libwnck' 'libsm' 'xorg-iceauth' 'upower' 'consolekit' 'hicolor-icon-theme' )
makedepends=('intltool')
optdepends=('fortune-mod: for xfce4-tips')
replaces=('xfce4-toys')
backup=(etc/polkit-1/localauthority/50-local.d/org.freedesktop.consolekit.pkla
	etc/polkit-1/localauthority/50-local.d/org.freedesktop.upower.pkla)
conflicts=('xfce4-session' 'xfce4-session-git')
options=('!libtool')
install=xfce4-session.install
source=(http://archive.xfce.org/src/xfce/xfce4-session/4.8/xfce4-session-${pkgver}.tar.bz2
	org.freedesktop.consolekit.pkla
	org.freedesktop.upower.pkla)
md5sums=('478080ff666fdd36786a243829663efd'
         '2e2519950d8c591dc9440ed8957a49ed'
         '5fa270637b5e685f033863d8664795a2')

build() {
  cd ${srcdir}/xfce4-session-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var \
	--disable-static \
	--disable-hal \
	--enable-gnome \
	--enable-libgnome-keyring \
	--enable-session-screenshots \
	--enable-upower \
	--enable-consolekit \
	--enable-polkit \
	--enable-panel-plugin \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/xfce4-session-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # install polkit/consolekit permission file needed for usage without a display manager
  install -dm700 ${pkgdir}/etc/polkit-1/localauthority
  install -dm755 ${pkgdir}/etc/polkit-1/localauthority/50-local.d
  install -m644 ${srcdir}/org.freedesktop.consolekit.pkla ${pkgdir}/etc/polkit-1/localauthority/50-local.d/
  install -m644 ${srcdir}/org.freedesktop.upower.pkla ${pkgdir}/etc/polkit-1/localauthority/50-local.d/
}

