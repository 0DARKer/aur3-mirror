# Maintainer:  cuihao <cuihao dot leo at gmail dot com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

# Original PKGBUILD (extra/xfce4-session):
# $Id: PKGBUILD 150131 2012-02-13 08:12:40Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-session-devel
_pkgname=xfce4-session
pkgver=4.10.0
pkgrel=1
pkgdesc="A session manager for Xfce (Development Version)"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
# keep xorg-server-utils for https://bugs.archlinux.org/task/21096
# upower and consolekit for reboot/shutdown/hibernate/suspend
depends=('libxfce4ui' 'libwnck' 'libsm' 'xorg-iceauth' 'upower' 'consolekit'
         'xorg-xinit' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('gnome-keyring: for keyring support when GNOME compatibility is enabled'
            'xscreensaver: for locking screen with xflock4'
            'gnome-screensaver: for locking screen with xflock4'
            'xlockmore: for locking screen with xflock4')
replaces=('xfce-utils')
options=('!libtool')
install=$_pkgname.install
source=(http://archive.xfce.org/src/xfce/$_pkgname/4.10/$_pkgname-$pkgver.tar.bz2)
sha1sums=('a956b67816fb06c50990c604d0a09c402b20302c')

groups=('xfce4-devel')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname" "$_pkgname-git" "xfce-utils")

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --enable-libgnome-keyring \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
