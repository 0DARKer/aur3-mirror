# Maintainer: Spyhawk
# Contributor: jnbek
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Travis Nickles <ryoohki7@yahoo.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>

pkgname=enemy-territory
pkgver=2.60b
pkgrel=13
pkgdesc="Wolfenstein: Enemy Territory is a completely free, standalone, team-based, multiplayer FPS."
arch=('i686' 'x86_64')
url="http://www.splashdamage.com/wolfet"
license=('custom')
if [[ "$CARCH" == "i686" ]]; then
  depends=('mesa' 'libxext')
else
  depends=('lib32-mesa' 'lib32-libxext' 'lib32-libxdamage')
fi
optdepends=('et-sdl-sound')
makedepends=('unzip')
provides=('enemy-territory-data')
conflicts=('enemy-territory-data')
backup=(usr/share/enemy-territory/etmain/server.cfg
        usr/share/enemy-territory/etmain/punkbuster.cfg)
install=$pkgname.install
source=('http://filebase.trackbase.net/et/full/et260b.x86_full.zip'
        'enemy-territory.desktop'
        'et.sh'
        'etded.sh'
        'etded.service')
md5sums=('6db59b543d42ee096ce263f3df571c08'
         '27532e5ae263d7ed86ca1ab17cb93fbd'
         'd47784d89cc43564f21194c86d6242fa'
         'ab126dd835c910f920bae4ab976e3bf8'
         '88def8a215ad2476a343e119b5d31a6b')

prepare() {
  # extract
  unzip -o et260b.x86_full.zip

  chmod +x et260b.x86_keygen_V03.run
  ./et260b.x86_keygen_V03.run --noexec --nox11 --target $pkgname
}

package() {
  # bin
  mkdir -p $pkgdir/usr/bin
  install -Dm 755 et.sh $pkgdir/usr/bin/et
  install -Dm 755 etded.sh $pkgdir/usr/bin/etded

  # data
  mkdir -p $pkgdir/usr/share/$pkgname
  install -Dm 755 $pkgname/bin/Linux/x86/{et,etded}.x86 $pkgdir/usr/share/$pkgname/
  mv $pkgname/etmain $pkgdir/usr/share/$pkgname/

  # punkbuster
  mv $pkgname/pb $pkgdir/usr/share/$pkgname/
  chmod +x $pkgdir/usr/share/$pkgname/pb/pbweb.x86
  mkdir -p $pkgdir/usr/share/$pkgname/pb/{dll,htm,svlogs,svss}
  install -dm 755 $pkgdir/usr/share/$pkgname/pb $pkgdir/usr/share/$pkgname/pb/{dll,htm,svlogs,svss}

  # etkey generator
  install -Dm 755 $pkgname/etkey.sh $pkgdir/usr/bin/etkey

  # systemd unit
  mkdir -p $pkgdir/usr/lib/systemd/system/
  install -Dm 644 etded.service $pkgdir/usr/lib/systemd/system/
  
  # doc
  mkdir -p $pkgdir/usr/share/doc/$pkgname/pb
  mv $pkgname/Docs/Help $pkgdir/usr/share/doc/$pkgname
  install -Dm 644 $pkgname/Docs/MSR.rtf $pkgdir/usr/share/doc/$pkgname
  install -Dm 644 $pkgname/CHANGES $pkgdir/usr/share/doc/$pkgname
  install -Dm 644 $pkgname/Docs/{PB_EULA,PunkBuster}.rtf $pkgdir/usr/share/doc/$pkgname/pb

  # license
  mkdir -p $pkgdir/usr/share/licenses/enemy-territory
  install -Dm 644 $pkgname/Docs/EULA_Wolfenstein_Enemy_Territory.txt $pkgdir/usr/share/licenses/enemy-territory

  # desktop file and icon
  mkdir -p $pkgdir/usr/share/{pixmaps,applications}
  install -Dm 644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm 644 $pkgname/ET.xpm $pkgdir/usr/share/pixmaps/$pkgname.xpm
}

