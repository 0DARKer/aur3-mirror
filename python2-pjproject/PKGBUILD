# Maintainer : M0Rf30 <morf3089@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_pkg=pjproject
pkgname=python2-$_pkg
pkgver=2.0.1
pkgrel=1
pkgdesc="Open source SIP stack and media stack (also known as pjsip)"
arch=('i686' 'x86_64')
url="http://www.pjsip.org/"
license=('GPL')
depends=('alsa-lib' 'util-linux' 'python2' 'openssl')
makedepends=('e2fsprogs' 'python2-distribute')
source=("http://www.pjsip.org/release/$pkgver/pjproject-$pkgver.tar.bz2")

build() {
   cd "$srcdir/$_pkg-$pkgver"
 
  [ "$CARCH" = 'x86_64' ] && \
  export CFLAGS="$CFLAGS -fPIC" && \
  export CXXFLAGS="$CXXFLAGS -fPIC"

  ./configure --prefix=/usr --disable-debug --enable-ssl
  make dep 
  make -j1

# Build python module
  cd pjsip-apps/src/python/
  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkg-$pkgver"

  make DESTDIR="$pkgdir" install

  cd pjsip-apps/src/python/
  python2 setup.py install --root="$pkgdir"
}


md5sums=('69e68612deaa3c760ec6aac46968c95a')
