# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-frameworks
pkgver=4.96.0
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('xapian-core' 'kfilemetadata-frameworks' 'kidletime' 'kcmutils' 'krunner')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/baloo-$pkgver.tar.xz")
md5sums=('0262135f202a7601724acdf8a5448252')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../baloo-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
