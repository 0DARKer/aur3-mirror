# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-frameworks
pkgver=5.0.1
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('xapian-core' 'kfilemetadata-frameworks' 'kidletime' 'kcmutils' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools')
source=("http://download.kde.org/stable/plasma/$pkgver/baloo-$pkgver.tar.xz")
md5sums=('ac1b9c72fd57b8bbf14d18659d115e8b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../baloo-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# move policy file to /opt
  mv "$pkgdir"/usr/share/polkit-1 "$pkgdir"/opt/kf5/share
  rm -fr "$pkgdir"/usr
}
