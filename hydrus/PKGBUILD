# Configuration:
#####

build_pyc=true
build_pyo=false
remove_py=false

#####
options=(!strip)  # Don't strip libs because there aren't any; this wastes time
DOC_DIRS=(opt/hydrus/help)

pkgname=hydrus
pkgver=149
pkgrel=1
pkgdesc="Danbooru-like image tagging and searching system for the desktop"
arch=(any)
license=(WTFPL)
url=http://hydrusnetwork.github.io/hydrus/
depends=(python2 wxpython opencv python2-beautifulsoup4 python2-yaml
         hsaudiotag python2-pypdf2 python2-pafy python2-lz4 python2-numpy
         python2-twisted python2-pillow python2-potr python2-flvlib)
makedepends=(git)
optdepends=('ffmpeg: show duration and other information on video thumbnails'
            'miniupnpc: automatic port forwarding')
source=("${pkgname}::git+https://github.com/hydrusnetwork/${pkgname}.git#commit=9997b8511f53093a18e57b8f20377283955c7cb2"
        paths-in-opt.patch
        running-the-server.patch
        hydrus-client
        hydrus-server
        hydrus.desktop)
sha256sums=(SKIP
            2a18de754d7bff58e579ebe1f3e14d1090ebfd4db19e0d585a02093550f8a9ee
            4a05646f71e157c11bdcf35bf51171bcb82b698408887bf2d17db1bf0caa9b0b
            39e6cf4fce867939856960415b2d63b42d0cb17334eedf5c6d0ff933df16dcd2
            b03369feac4d21fbddc25230042052921806ba35415b21f8e312c888e0ae2ac8
            9ba3942ac1a37f6b39c98ae6592573402bf08d8376f64554d0696c0fed6fd0e2)
install=hydrus.install

prepare() {
    cd "$pkgname"
    patch -Np1 -i ../paths-in-opt.patch
    patch -Np1 -i ../running-the-server.patch
}

package() {
  cd "$pkgname"

  # Create /opt/hydrus and copy hydrus sources to there
  install -m755 -d "${pkgdir}/opt/hydrus"
  cp -r help include static client.pyw server.pyw "${pkgdir}/opt/hydrus/"

  # Remove unused library
  rm -rf "${pkgdir}/opt/hydrus/include/magic"

  # Fix permissions
  chown -R root:root "${pkgdir}/opt/hydrus"
  chmod a-x "${pkgdir}/opt/hydrus/include/"*.py

  # Compile .py files
  $build_pyc && python2 -m compileall "${pkgdir}/opt/hydrus"
  $build_pyo && python2 -OO -m compileall "${pkgdir}/opt/hydrus"
  $remove_py && find "${pkgdir}/opt/hydrus" -name '*.py' -delete

  # Create and populate /opt/hydrus/bin
  install -d -m755 "${pkgdir}/opt/hydrus/bin"
  ln -s /usr/bin/upnpc "${pkgdir}/opt/hydrus/bin/upnpc_linux"
  ln -s /usr/bin/ffmpeg "${pkgdir}/opt/hydrus/bin/ffmpeg"

  # Install hydrus-client and hydrus-server executables
  install -d -m755 "${pkgdir}/usr/bin"
  install -m755 ../hydrus-{client,server} "${pkgdir}/usr/bin/"

  # Install license files
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Install .desktop shortcut
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -m644 ../hydrus.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
