# Configuration:
#####

build_pyc=true
build_pyo=false
remove_py=false
remove_help=false

#####
options=(!strip)  # Don't strip libs because there aren't any; this wastes time
DOC_DIRS=(opt/hydrus/help)

pkgname=hydrus
pkgver=151
pkgrel=1
pkgdesc="Danbooru-like image tagging and searching system for the desktop"
arch=(any)
license=(WTFPL)
url=http://hydrusnetwork.github.io/hydrus/
depends=(python2 wxpython opencv python2-beautifulsoup4 python2-yaml
         hsaudiotag python2-pypdf2 python2-pafy python2-lz4 python2-numpy
         python2-twisted python2-pillow python2-potr python2-flvlib)
makedepends=(git)
optdepends=('ffmpeg: show duration and other information on video thumbnails'
            'miniupnpc: automatic port forwarding')
source=("${pkgname}::git+https://github.com/hydrusnetwork/${pkgname}.git#commit=13b132ebec8c34a04a00068a2e810edf1f2e6b57"
        paths-in-opt.patch
        running-the-server.patch
        hydrus-client
        hydrus-server
        hydrus.desktop)
sha256sums=(SKIP
            860e9b94d359b3e47a48964b78bec6e0393f66f0b635d146f80b7d9056af5d1e
            466757bc555b4973ae94fdd7473e75ff0799861de285f17aa642054091f2ebff
            39e6cf4fce867939856960415b2d63b42d0cb17334eedf5c6d0ff933df16dcd2
            b03369feac4d21fbddc25230042052921806ba35415b21f8e312c888e0ae2ac8
            9ba3942ac1a37f6b39c98ae6592573402bf08d8376f64554d0696c0fed6fd0e2)
install=hydrus.install

prepare() {
    cd "$pkgname"
    patch -Np1 -i ../paths-in-opt.patch
    patch -Np1 -i ../running-the-server.patch
}

package() {
  cd "$pkgname"

  # Create /opt/hydrus and copy hydrus sources to there
  install -m755 -d "${pkgdir}/opt/hydrus"
  cp -r help include static client.pyw server.pyw "${pkgdir}/opt/hydrus/"

  # Remove unused library
  rm -rf "${pkgdir}/opt/hydrus/include/magic"

  # Remove strange file
  rm -f "${pkgdir}/opt/hydrus/include/pyconfig.h"

  # Remove unit tests
  rm -f "${pkgdir}/opt/hydrus/include/Test"*.py
  rm -rf "${pkgdir}/opt/hydrus/static/testing"

  # Remove help
  $remove_help && rm -rf "${pkgdir}/opt/hydrus/help"

  # Fix permissions
  chown -R root:root "${pkgdir}/opt/hydrus"
  chmod a-x "${pkgdir}/opt/hydrus/include/"*.py

  # Compile .py files
  $build_pyc && python2 -m compileall "${pkgdir}/opt/hydrus"
  $build_pyo && python2 -OO -m compileall "${pkgdir}/opt/hydrus"
  $remove_py && find "${pkgdir}/opt/hydrus" -name '*.py' -delete

  # Create and populate /opt/hydrus/bin
  install -d -m755 "${pkgdir}/opt/hydrus/bin"
  ln -s /usr/bin/upnpc "${pkgdir}/opt/hydrus/bin/upnpc_linux"
  ln -s /usr/bin/ffmpeg "${pkgdir}/opt/hydrus/bin/ffmpeg"

  # Install hydrus-client and hydrus-server executables
  install -d -m755 "${pkgdir}/usr/bin"
  install -m755 ../hydrus-{client,server} "${pkgdir}/usr/bin/"

  # Install license files
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -m644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/"

  # Install .desktop shortcut
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -m644 ../hydrus.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
