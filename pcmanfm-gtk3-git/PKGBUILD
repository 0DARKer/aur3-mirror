# Maintainer: ksj <podhorsky.ksj@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

_gitname=pcmanfm
pkgname=pcmanfm-gtk3-git
pkgver=20140218
pkgrel=1
pkgdesc='Extremely fast and lightweight file manager'
arch=('i686' 'x86_64')
url='http://pcmanfm.sourceforge.net/'
license=('GPL')
depends=('gtk3' 'desktop-file-utils' 'libfm-gtk3-git' 'lxmenu-data')
makedepends=('git' 'intltool' 'pkg-config')
optdepends=('gvfs: for trash support, mounting with udisks and remote filesystems'
            'udisks: alternative for mounting volumes'
            'xarchiver: archive management')
provides=('pcmanfm')
conflicts=('pcmanfm')
install=pcmanfm.install
source=(git://pcmanfm.git.sourceforge.net/gitroot/pcmanfm/pcmanfm)
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_gitname"
    git log -1 --format='%cd' --date=short | tr -d -- '-'
}

build() {
  cd "${srcdir}/${_gitname}"


  msg "Starting make..."
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --with-gtk=3
  make LDFLAGS="-lm ${LDFLAGS}"
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
}
