# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=darkhttpd-git
pkgver=0.r278.g5854227
pkgrel=1
pkgdesc="A small, static webserver - git checkout"
arch=('i686' 'x86_64')
url="http://dmr.ath.cx/net/darkhttpd/"
license=('BSD')
depends=('glibc')
makedepends=('git')
conflicts=('darkhttpd')
provides=('darkhttpd')
source=('git+http://unix4lyfe.org/git/darkhttpd'
	'http://www.eworm.de/download/linux/darkhttpd-get-version-string-from-git.patch')

pkgver() {
	cd darkhttpd/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd darkhttpd/

	patch -Np1 < ${srcdir}/darkhttpd-get-version-string-from-git.patch

	make
}

package() {
	cd darkhttpd/

	install -D darkhttpd ${pkgdir}/usr/bin/darkhttpd

	mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
	sed -n '1,/ \*\//p' < darkhttpd.c > ${pkgdir}/usr/share/licenses/${pkgname}/license

	install -D -m0644 README ${pkgdir}/usr/share/doc/${pkgname}/README
}

sha256sums=('SKIP'
            'd2b99a7010f7ce20a45544289505b09bcccfd03fbdae020d05f7a2d89eb32ffe')
