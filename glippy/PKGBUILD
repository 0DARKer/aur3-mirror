# Maintainer: Shem Valentine <archlinux@valentinenews.net>
pkgname=glippy
pkgver=0.5.1.1
_majorver=0.5
pkgrel=1
pkgdesc="not so simple clipboard manager for GNOME"
arch=('i686' 'x86_64')
url="https://launchpad.net/glippy"
license=('GPL')
depends=('mono')
makedepends=('gtk-sharp-2' 'gnome-sharp' 'gconf-sharp')
options=(!emptydirs)
install=$pkgname.install
source=(http://launchpad.net/${pkgname}/${_majorver}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
md5sums=('c720c9e9aa3352ea736a2e447876159e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" \
    "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
