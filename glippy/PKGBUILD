# Maintainer: Eduardo Reveles <me@osiux.ws>
# Contributor: Shem Valentine <archlinux@valentinenews.net>

pkgname=glippy
pkgver=0.5.4
_majorver=1.0
pkgrel=2
pkgdesc="not so simple clipboard manager for GNOME"
arch=('i686' 'x86_64')
url="https://launchpad.net/glippy"
license=('GPL')
depends=('mono')
makedepends=('automake-1.11' 'gtk-sharp-2' 'gnome-sharp' 'gconf-sharp')
options=(!emptydirs)
install=$pkgname.install
source=(http://launchpad.net/${pkgname}/${_majorver}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
md5sums=('5d46001ca07da2ede33321f92e1f9f09')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" \
    "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}

# vim:set ts=2 sw=2 et:
