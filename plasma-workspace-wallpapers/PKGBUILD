# Contributor: Antonio Rojas

pkgname=plasma-workspace-wallpapers
pkgver=4.98.0
pkgrel=1
pkgdesc='Additional wallpapers for the Plasma Workspace'
arch=('any')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=()
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/unstable/plasma/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('9959b931a21ab5f02d311b4addc73cd2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
