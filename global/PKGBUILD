# Maintainer: Lucas De Marchi <lucas.de.marchi@gmail.com>
# Contributor: Matthias Meulien orontee@gmail.com

pkgname=global
pkgver=6.2.3
pkgrel=1
pkgdesc="A source code tag system"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/global/"
license=('GPL')
depends=('libltdl')
optdepends=('idutils' 'perl' 'ctags')
options=(!emptydirs)
install=global.install
source=(http://tamacom.com/global/$pkgname-$pkgver.tar.gz)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --with-exuberant-ctags=/usr/bin/ctags
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install

	install -d "$pkgdir/usr/share/emacs/site-lisp"
	ln -s ../../gtags/gtags.el "$pkgdir/usr/share/emacs/site-lisp/gtags.el"
}

md5sums=('f789a4e422656ee5a6cd6cb2e776c32f')
sha256sums=('b200b60765f139e6497dfa9fa68999b0f877ea44647d47b5ac2c7c22a48f4e55')
