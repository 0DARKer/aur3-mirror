# Maintainer: Lucas De Marchi <lucas.de.marchi@gmail.com>
# Contributor: Matthias Meulien orontee@gmail.com

pkgname=global
pkgver=6.2.1
pkgrel=1
pkgdesc="A source code tag system"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/global/"
license=('GPL')
depends=('libltdl')
optdepends=('idutils' 'perl' 'ctags')
options=(!emptydirs)
install=global.install
source=(http://tamacom.com/global/$pkgname-$pkgver.tar.gz)

md5sums=('57e9e546bc708dfa5f20217b96e17e1b')
sha256sums=('db744411a6e0e0391239673a8436cb22f1834dbee3aef420cccaee69c86307a5')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --with-exuberant-ctags=/usr/bin/ctags
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install

	install -d "$pkgdir/usr/share/emacs/site-lisp"
	ln -s ../../gtags/gtags.el "$pkgdir/usr/share/emacs/site-lisp/gtags.el"
}
