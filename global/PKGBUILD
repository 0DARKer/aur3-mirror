# Maintainer: Lucas De Marchi <lucas.de.marchi@gmail.com>
# Contributor: Matthias Meulien orontee@gmail.com

pkgname=global
pkgver=6.2.4
pkgrel=1
pkgdesc="A source code tag system"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/global/"
license=('GPL')
depends=('libltdl')
optdepends=('idutils' 'perl' 'ctags')
options=(!emptydirs)
install=global.install
source=(http://tamacom.com/global/$pkgname-$pkgver.tar.gz)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --with-exuberant-ctags=/usr/bin/ctags
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install

	install -d "$pkgdir/usr/share/emacs/site-lisp"
	ln -s ../../gtags/gtags.el "$pkgdir/usr/share/emacs/site-lisp/gtags.el"
}

md5sums=('c778d4e52a30706db7a02070cce1078c')
sha256sums=('e193aa1969038084de54dc2ce3f708c273007a4774feccf5c04b08e8d3bbc4e7')
