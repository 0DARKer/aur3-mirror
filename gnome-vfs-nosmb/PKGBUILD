# Maintainer: Hilton Medeiros <medeiros.hilton at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-vfs-nosmb
_pkgname=gnome-vfs
pkgver=2.24.4
pkgrel=1
pkgdesc="The GNOME Virtual File System (without samba support)."
arch=(i686 x86_64)
license=('LGPL')
depends=('gamin' 'gconf>=2.32.0' 'bzip2' 'avahi>=0.6.27' 'gnome-mime-data>=2.18.0-2' 'heimdal>=1.3.3' 'gnutls>=2.8.6')
makedepends=('intltool' 'gtk-doc' 'gnome-common')
options=('!libtool' '!emptydirs' '!makeflags')
conflicts=('gnome-vfs')
provides=("gnome-vfs=$pkgver")
url="http://www.gnome.org"
install=gnome-vfs.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgname}/2.24/gnome-vfs-${pkgver}.tar.bz2
        gnutls-config.patch)
sha256sums=('62de64b5b804eb04104ff98fcd6a8b7276d510a49fbd9c0feb568f8996444faa'
            '66c7cfb12995c0dd94a2caea95c7e3c55981993f05a79c585d60915ff131955d')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  #Fix build with new gnutls
  patch -Np1 -i "${srcdir}/gnutls-config.patch"

  libtoolize --force
  gtkdocize
  aclocal
  autoconf
  automake
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-vfs-2.0 \
      --disable-samba --disable-hal \
      --enable-avahi --disable-howl \
      --disable-openssl --enable-gnutls

  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_pkgname}.schemas" \
    --domain gnome-vfs-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas

  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
