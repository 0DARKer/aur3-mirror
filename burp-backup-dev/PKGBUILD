# Maintainer: Jason Lenz <Jason at Lenzplace dot org>
# Contributor: Rafa≈Ç Michalski <plum.michalski at gmail dot com>
pkgname="burp-backup-dev"
_pkgname="burp"
pkgver=2.0.18
pkgrel=1
pkgdesc="Burp 2 is a next generation experimental version of the burp backup and restore program which is still under heavy development.  For important data please use the stable version for now."
arch=('i686' 'x86_64')
license=("AGPL3")
depends=('librsync' 'acl' 'openssl')
makedepends=('uthash')
conflicts=('burp-backup' 'burp-backup-git' 'burp-backup14')
install=$_pkgname.install
url='http://burp.grke.org/burp2.html'

source=(
  "http://downloads.sourceforge.net/project/${_pkgname}/${_pkgname}-${pkgver}/${_pkgname}-${pkgver}.tar.bz2"
  "burp.install"
  "burp-server.service"
  "burp-client.service"
  "burp-client.timer"
  "01-runpath_fix.patch"
  "readme-archlinux.txt"
)

sha256sums=(
  02cb95cf1f68c0ab4ed1bda48818aed078e01d2730c3facd3d34f7f2f9d69e66 # burp-2.0.18.tar.bz2
  1d2c97471494dd5924bd0676e817ebfac54465c4ec0e23b49ad1c3c91d36460f # burp.install
  eb5ed9b87270edbd98425397824557e6d80fd429c19e8646f838d61ba3f68962 # burp-server.service
  220829f1a3f6932c23471caa88ac61e0f52a0abaaec0d45ad3b20e15c226d40d # burp-client.service
  26916e62663d7adb136147daae2198fb4e8e382f2e0d1f3451d70b27873bd4b7 # burp-client.timer
  781fcd11df9d8bf928eb08982cc5ed21c93a0661dacb4e184d01e246277ab877 # runpath_fix.patch change /var/run to /run
  602a7ee91363472017afd8b4073a01fb3daeee0acb5e93a570b49f27646bebd2 # arch linux specific instructions
)

backup=(
  "etc/burp/burp.conf"
  "etc/burp/burp-server.conf"
  "etc/burp/CA.cnf"
  "etc/logrotate.d/burp"
)

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../01-runpath_fix.patch
}

build() {
  cd $srcdir/$_pkgname-$pkgver
  ./configure --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Setup logrotate
  mkdir -p $pkgdir/etc/logrotate.d
  cp debian/logrotate $pkgdir/etc/logrotate.d/burp

  # Copy useful user setup files
  mkdir -p $pkgdir/usr/share/burp/
  cp debian/burp.cron.d $pkgdir/usr/share/burp/
  mkdir -p $pkgdir/usr/lib/systemd/system/
  cp $srcdir/burp-server.service $pkgdir/usr/lib/systemd/system/
  cp $srcdir/burp-client.service $pkgdir/usr/lib/systemd/system/
  cp $srcdir/burp-client.timer $pkgdir/usr/lib/systemd/system/

  # Copy archlinux specific documentation 
  mkdir -p $pkgdir/usr/share/doc/burp/
  cp $srcdir/readme-archlinux.txt $pkgdir/usr/share/doc/burp/readme-archlinux.txt

  # Fix permissions
  mkdir -p $pkgdir/var/spool/burp
  chmod 0755 $pkgdir/var/spool
  chmod 0700 $pkgdir/var/spool/burp
  chmod -R go-rwx $pkgdir/etc/burp

  # Move "testclient" example to subfolder so it doesn't automatically get added to backup
  mv $pkgdir/etc/burp/clientconfdir/testclient $pkgdir/etc/burp/clientconfdir/incexc
}

# Helpful packaging references:
# General packaging -> https://wiki.archlinux.org/index.php/Creating_packages
# Generating patches -> https://wiki.archlinux.org/index.php/Patching_in_ABS
# Backing up package config files -> https://wiki.archlinux.org/index.php/Pacnew_and_Pacsave_files

# vim:set ts=2 sw=2 et:
