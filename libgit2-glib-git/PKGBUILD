# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=libgit2-glib-git
pkgver=0.0.2.r1.ga74cade
pkgrel=1
pkgdesc='a glib wrapper library around the libgit2 git access library'
arch=('i686' 'x86_64')
url="http://git.gnome.org/browse/libgit2-glib"
license=('GPL2')
depends=('libgit2')
makedepends=('git' 'cmake' 'gobject-introspection' 'gnome-common')
provides=('libgit2-glib')
conflicts=('libgit2-glib')
source=('git://git.gnome.org/libgit2-glib')

pkgver() {
	cd libgit2-glib/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd libgit2-glib/

	./autogen.sh
	./configure --prefix=/usr
	make
}

package() {
	cd libgit2-glib/

	make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP')
