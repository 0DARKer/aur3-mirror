# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=cqrlogo
pkgver=0.1.3.r0.gac49c6c
pkgrel=1
pkgdesc="CGI QR-Code logo for web services"
arch=('i686' 'x86_64')
url="http://www.eworm.de/"
depends=('gdk-pixbuf2' 'qrencode')
makedepends=('git')
license=('GPL')
source=('git://github.com/eworm-de/cqrlogo.git')

pkgver() {
	cd cqrlogo/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}


build() {
	cd cqrlogo/

	make
}

package() {
	cd cqrlogo/

	install -D -m0755 ${pkgname} ${pkgdir}/usr/share/webapps/${pkgname}/${pkgname}
}

sha256sums=('SKIP')
