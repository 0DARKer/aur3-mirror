# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=cqrlogo
pkgver=0.4.0
pkgrel=1
pkgdesc="CGI QR-Code logo for web services"
arch=('i686' 'x86_64')
url="https://github.com/eworm-de/cqrlogo"
depends=('iniparser' 'libpng' 'qrencode')
makedepends=('fcgi' 'markdown')
optdepends=('apache: for apache web server inclusion'
	'fcgi: for FastCGI version'
	'lighttpd: for lighttpd web server inclusion'
	'mod_fastcgi: for fastcgi in apache web server'
	'mod_fcgid: for fastcgi in apache web server')
conflicts=('cqrlogo-git')
checkdepends=('zbar')
license=('GPL')
install=cqrlogo.install
backup=('etc/cqrlogo.conf'
	'etc/apache/conf/extra/cqrlogo.conf'
	'etc/lighttpd/conf.d/cqrlogo.conf')
source=("http://www.eworm.de/download/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.asc})

build() {
	cd ${pkgname}-${pkgver}/

	make
}

check() {
	cd ${pkgname}-${pkgver}/

	make check
}

package() {
	cd ${pkgname}-${pkgver}/

	make DESTDIR="${pkgdir}" install
}

sha256sums=('eba1717eda980fa33c5ca3ef5330d2c49c16122795220f7a496b6d2e1c3221c4'
            'SKIP')
