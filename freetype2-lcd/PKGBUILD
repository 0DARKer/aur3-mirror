# Contributor: Anonymo <meowdib at gmail dot com>
# Maintainer: Luca Bennati <lucak3 AT gmail DOT com>

pkgname=freetype2-lcd
_realname=freetype
pkgver=2.4.9
pkgrel=3
pkgdesc="TrueType font rendering library, for LCD displays."
arch=('i686' 'x86_64')
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh')
provides=('freetype2')
conflicts=('freetype2')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/${_realname}/${_realname}-${pkgver}.tar.bz2"
	freetype-2.3.0-enable-spr.patch
	freetype-2.2.1-enable-valid.patch
	bug35833.patch
	bug35847.patch
	freetype-2-quantization_fix.patch
	ftoption.h)
md5sums=('77a893dae81fd5b896632715ca041179'
         '816dc8619a6904a7385769433c0a8653'
         '214119610444c9b02766ccee5e220680'
         '69bde731a06448c7e4828e96c74d45ee'
         '41f9c0a5362bf2cd9a4d7557ddf98098'
         '385162f4a3e01653d55ab9a9be0c8808'
         '5a8171040609dc6f67048e955b5b1324')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  patch -Np1 -i "${srcdir}/freetype-2.3.0-enable-spr.patch"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"
  #Upstream bugs fixed in git. Requested in FS#29034
  patch -Np1 -i "${srcdir}/bug35833.patch"
  patch -Np1 -i "${srcdir}/bug35847.patch"

  cp "${srcdir}/ftoption.h" include/freetype/config/ftoption.h
  cd src/autofit
  patch -p0 -i "${srcdir}/freetype-2-quantization_fix.patch"
  cd ../..

  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
