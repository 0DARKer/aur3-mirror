# Maintainer: Michael Williams <draringi@draringi.net>
# Contributor: onny <onny@project-insanity.org>

pkgname=libwebsockets
pkgver=1.3
pkgrel=1
pkgdesc="A c websockets server library."
depends=('openssl')
makedepends=('cmake')
arch=('i686' 'x86_64')
url="http://git.warmcat.com/cgi-bin/cgit/libwebsockets/"
license=('LGPL')
source=("http://git.warmcat.com/cgi-bin/cgit/libwebsockets/snapshot/libwebsockets-1.3-chrome37-firefox30.tar.gz")
md5sums=('63117f35174a5fe4ec062f6baa411638')
_browser_versions='chrome37-firefox30'

prepare() {
	cd "$srcdir/${pkgname}-${pkgver}-${_browser_versions}"
	mkdir build
}

build() {
	cd "$srcdir/${pkgname}-${pkgver}-${_browser_versions}/build"
	cmake ..  -DCMAKE_INSTALL_PREFIX:PATH=/usr
	make
}
package () {
	cd "$srcdir/${pkgname}-${pkgver}-${_browser_versions}/build"
	make DESTDIR="$pkgdir" install
}
