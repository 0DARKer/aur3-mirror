# Maintainer: Tom Wadley <tomwadley@fastmail.fm>
pkgname=manicdigger
pkgver=2011_12_18
pkgrel=1
pkgdesc="A public domain 3d block-building game similar to Minecraft"
arch=('any')
url="http://manicdigger.sourceforge.net"
license=('custom:"public domain"')
depends=('mono' 'openal')
source=("$pkgname-$pkgver.zip::http://manicdigger.sourceforge.net/nightly/ManicDigger2011-12-18Binary.zip"
        "manicdigger"
        "manicdigger-server"
        "manicdigger-monstereditor")
noextract=("$pkgname-$pkgver.zip")
md5sums=('c1105b17d7fd4fe0c5c9a3606a7e99d3'
         '8853a36d497bad45ec57d2839b31617d'
         '5b934df520afccd827f1426ce5ff2a96'
         'f2a61ac984da9c95ee0971d31131720e')

package() {
  mkdir -p "$pkgdir/opt/$pkgname"
  bsdtar -xf "$pkgname-$pkgver.zip" -C "$pkgdir/opt/$pkgname"

  chmod 644 -R "$pkgdir/opt/$pkgname"
  chmod a+X -R "$pkgdir/opt/$pkgname"

  install -D -m755 "$srcdir"/manicdigger "$pkgdir/usr/bin/manicdigger"
  install -D -m755 "$srcdir"/manicdigger-server "$pkgdir/usr/bin/manicdigger-server"
  install -D -m755 "$srcdir"/manicdigger-monstereditor "$pkgdir/usr/bin/manicdigger-monstereditor"

  install -D -m644 "$pkgdir/opt/$pkgname"/credits.txt "$pkgdir/usr/share/licenses/$pkgname/credits.txt"
}

