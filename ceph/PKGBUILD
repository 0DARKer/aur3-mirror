# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>
pkgname=ceph
pkgver=0.27.1
pkgrel=1
pkgdesc="Massively distributed fault-tolerant file system"
arch=('x86_64' 'i686')
url="http://ceph.newdream.net/"
license=('GPL')
depends=('libedit' 'libsigc++' 'gtkmm' 'btrfs-progs-unstable' 'crypto++' 'google-perftools' 'python2')
makedepends=('boost')
optdepends=('fcgi: radosgw - Amazon S3 compatibility'
            'expat: radosgw - Amazon S3 compatibility')
source=("http://ceph.newdream.net/download/$pkgname-$pkgver.tar.gz")
md5sums=('5dc5d11e963b563f53600128edb161dd')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  msg2 "Configuring..."
  ./configure --prefix=/usr --sysconfdir=/etc --without-libatomic-ops --without-fuse
  make clean
  msg2 "Compiling..."
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  msg2 "Packaging..."
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/var/run/ceph" "$pkgdir/var/log/ceph" \
    "$pkgdir/etc/rc.d" "$pkgdir/etc/ceph"
  install -Dm755 "src/init-ceph" "$pkgdir/etc/rc.d/ceph"
  # Move /usr/sbin to /sbin, which is expected by the mount command
  mv "$pkgdir/usr/sbin" "$pkgdir/"
  install -Dm644 "$pkgdir/usr/share/doc/ceph/sample.ceph.conf" \
    "$pkgdir/etc/ceph/ceph.conf.sample"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
