# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
pkgbase=fwts
pkgname=('fwts' 'fwts-efi-runtime-dkms')
pkgver=14.09.00
pkgrel=1
arch=('i686' 'x86_64')
url="https://wiki.ubuntu.com/FirmwareTestSuite/"
license=('GPL2')
options=(!buildflags)
changelog=Changelog
makedepends=('pcre' 'json-c')
source=("http://fwts.ubuntu.com/release/fwts-V${pkgver}.tar.gz" 'json-c.patch')
sha256sums=('17cb79ba79337b4aea04b6ff55d7a7d5585a257e7b10f41eb91b804fad893382'
            'dae690fcf2e101263d0533b53f69f1924f9a81deafd69ae0a5f5ca9e46ffa5f0')

prepare() {
    # not sure if I'm doing it the right way
    cd "${srcdir}/debian"
    sed -i 's/#MODULE_VERSION#/'${pkgver}'/' fwts-efi-runtime-dkms.dkms
    sed -i 's/updates/kernel/' fwts-efi-runtime-dkms.dkms
    cd $srcdir
    patch -p1 < json-c.patch
}

build() {
    cd "${srcdir}"
    autoreconf -ivf
    ./configure --prefix="/usr"
    make
}

package_fwts() {
    pkgdesc="FirmWare Test Suite is a tool to do automatic testing of a PC's firmware"
    depends=('bash' 'glib2' 'pcre' 'json-c')
    conflicts=('fwts-git')
    optdepends=('dialog: fwts-frontend-text - dialog based user interface'
                'fwts-efi-runtime-dkms: UEFI tests'
                'dmidecode: some tests'
                'iasl: some tests'
                'pciutils: some tests')

    cd "${srcdir}"
    make DESTDIR="${pkgdir}" install

    # fwts-collect
    install -m 755 scripts/fwts-collect "${pkgdir}/usr/bin"

    # fwts-frontend-text
    install -m 755 live-image/fwts-frontend-text "${pkgdir}/usr/bin"
    install -m 644 live-image/fwts-live-dialogrc "${pkgdir}/usr/share/${pkgname}"

    # some documentation
    install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
    install -m 644 README* "${pkgdir}/usr/share/doc/${pkgname}"
}

package_fwts-efi-runtime-dkms() {
    pkgdesc="FirmWare Test Suite efi-runtime kernel module sources"
    install=fwts-efi-runtime-dkms.install
    depends=('dkms' 'fwts')

    cd "${srcdir}"
    install -dm755 "${pkgdir}/usr/src/${pkgname}-${pkgver}"
    install -m644 efi_runtime/* "${pkgdir}/usr/src/${pkgname}-${pkgver}"
    install -m644 debian/fwts-efi-runtime-dkms.dkms "${pkgdir}/usr/src/${pkgname}-${pkgver}/dkms.conf"
}
