# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
pkgname=fwts
pkgver=14.03.01
pkgrel=1
pkgdesc="FirmWare Test Suite is a tool to do automatic testing of a PC's firmware"
arch=('i686' 'x86_64')
url="https://wiki.ubuntu.com/FirmwareTestSuite/"
license=('GPL2')
depends=('bash' 'pcre' 'json-c')
optdepends=('dialog: fwts-frontend-text - dialog based user interface'
            'fwts-efi-runtime-dkms: UEFI tests'
            'dmidecode: some tests'
            'iasl: some tests'
            'pciutils: some tests')
conflicts=('fwts-git')
options=(!buildflags)
changelog=Changelog
source=("http://fwts.ubuntu.com/release/fwts-V${pkgver}.tar.gz")
sha256sums=('73d650fbd48d72c2308367cb42f16cd9f316c965699aee70139e84b2a14f17fc')

build() {
    cd "${srcdir}"
    autoreconf -ivf
    ./configure --prefix="/usr"
    make
}

package() {
    cd "${srcdir}"
    make DESTDIR="${pkgdir}" install

    # fwts-collect
    install -m 755 scripts/fwts-collect "${pkgdir}/usr/bin"

    # fwts-frontend-text
    install -m 755 live-image/fwts-frontend-text "${pkgdir}/usr/bin"
    install -m 644 live-image/fwts-live-dialogrc "${pkgdir}/usr/share/${pkgname}"

    # some documentation
    install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
    install -m 644 README* "${pkgdir}/usr/share/doc/${pkgname}"
}
