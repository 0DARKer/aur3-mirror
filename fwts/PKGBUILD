# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
pkgname=fwts
pkgver=0.26.08
pkgrel=1
pkgdesc="FirmWare Test Suite is a tool to do automatic testing of a PC's firmware"
arch=('i686' 'x86_64')
url="https://launchpad.net/fwts"
license=('GPL2')
depends=('bash' 'pcre' 'json-c')
optdepends=('dialog: fwts-frontend-text support'
            'dmidecode: some tests'
	    'iasl: some tests'
            'pciutils: some tests')
makedepends=('git')
conflicts=('fwts-git')
options=('!libtool' '!staticlibs')
changelog=Changelog
install=${pkgname}.install
source=("git://kernel.ubuntu.com/hwe/fwts.git#tag=V${pkgver}")
sha256sums=('SKIP')

prepare() {
	cd ${srcdir}/${pkgname}
	# fix for automake1.12
	sed -i 's/-Wall/-Wall -Wno-extra-portability/' configure.ac
	# fix lspci path
	sed -i 's/bin\/lspci/sbin\/lspci/' src/lib/include/fwts_binpaths.h
}

build() {
	cd ${srcdir}/${pkgname}
	autoreconf -ivf
	./configure --prefix="/usr"
	make 
}

package() {
	cd ${srcdir}/${pkgname}
	make DESTDIR="${pkgdir}" install

	# fwts-collect
	install -m 755 scripts/fwts-collect "${pkgdir}/usr/bin"

	# fwts-frontend-text
	install -m 755 live-image/fwts-frontend-text "${pkgdir}/usr/bin"
	install -m 644 live-image/fwts-live-dialogrc "${pkgdir}/usr/share/${pkgname}"

	# some documentation
	install -m 755 -d "${pkgdir}/usr/share/doc/${pkgname}"
	install -m 644 docs/* README* "${pkgdir}/usr/share/doc/${pkgname}"
}

# TODO: add efi dkms support

