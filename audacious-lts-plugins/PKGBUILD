# Contributor: Maloy <daniel.go.mon@gmail.com>

pkgname=audacious-lts-plugins
pkgver=2.5.4
pkgrel=1
realname=audacious-plugins
pkgdesc="Plugins for Audacious longterm release"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL')
depends=('audacious-lts') 
optdepends=('alsa-lib: Advanced Linux Sound Arch. output'
            'pulseaudio: PulseAudio output'
            'jack: Jack Audio Connection Kit output'
	    'lame: FileWriter MP3 output'
	    'libvorbis: Vorbis input, FileWriter Vorbis output'
	    'flac: FLAC input, FileWriter FLAC output'
	    'faad2: AAC input'
	    'ffmpeg: ffaudio input'
	    'fluidsynth: MIDI FluidSynth backend input'
	    'libcdio: CD Digital Audio input'
	    'libsidplay: Commodore 64 audio input'
	    'wavpack: WavPack input'
	    'libnotify: libnotify OSD'
	    'lirc-utils: LIRC'
	    'curl: AudioScrobbler Client'
	    'libmtp: Upload to MTP device'
	    'neon: neon-based http transport'
	    'libmms: libmms-based mms transport'
	    'libcue: CUE playlist format')
provides=("audacious-plugins=${pkgver}")
conflicts=('audacious-plugins')
replaces=('audacious-plugins')
options=('!libtool')
source=(http://distfiles.atheme.org/${realname}-${pkgver}.tar.gz)
md5sums=('93e8d13f2a17d047a4c24e1e5605fbac')

build() {
  cd "$srcdir/$realname-$pkgver"

  if [[ $CARCH = "i686" ]]; then
    ./configure --prefix=/usr \
      --enable-chardet --enable-amidiplug \
      --disable-adplug --disable-projectm-1.0 --disable-sse2 || return 1
  elif [[ $CARCH = "x86_64" ]]; then
    ./configure --prefix=/usr \
      --enable-chardet --enable-amidiplug \
      --disable-adplug --disable-projectm-1.0 || return 1
  fi

  make || return 1
  make DESTDIR="$pkgdir" install || return 1
}

