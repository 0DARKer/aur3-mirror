
# Maintainer : RunningDroid <rningdrd AT tormail.net>
# Contributor: Samuel Tardieu <sam@rfc1149.net>
# Contributor: Justin Gottula <justin@jgottula.com>

pkgname=electrum-git
pkgver=0.61.r1.1180.g2d613a4
pkgrel=1
pkgdesc="Python-Based Bitcoin Client"
arch=('any')
url="http://electrum-desktop.com/"
license=('GPL3')
depends=('gettext' 'python2-pyqt' 'python2-ecdsa' 'python2-slowaes')
makedepends=('git')
optdepends=('python2-zbar: QRcode reading support')
provides=('electrum')
conflicts=('electrum')
source=(git+https://github.com/spesmilo/electrum.git#branch=master)
md5sums=('SKIP')

_gitname='electrum'

pkgver() {
  cd "$srcdir/$_gitname"
  echo $(git describe --always | sed 's/-/./g')
}

build() {
  cd "$srcdir/$_gitname"

  sed -i -e 's|^#!/usr/bin/env python|#!/usr/bin/env python2|' \
      -e 's|^#!/usr/bin/python|#!/usr/bin/python2|' \
      lib/* gui/* scripts/* electrum make_packages mki18n.py setup.py

  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/icons_rc.py
  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname"
  python2 setup.py install --root=$pkgdir
}
