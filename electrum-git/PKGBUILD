# Maintainer : RunningDroid <rningdrd AT tormail.net>
# Contributor: Samuel Tardieu <sam@rfc1149.net>
# Contributor: Justin Gottula <justin@jgottula.com>
pkgname=electrum-git
pkgver=20130309
pkgrel=1
pkgdesc="Python-Based Bitcoin Client"
arch=('any')
url="http://electrum-desktop.com/"
license=('GPL3')
depends=('gettext' 'python2-pyqt' 'python2-ecdsa' 'python2-slowaes')
makedepends=('git')
optdepends=('python2-zbar: QRcode reading support')
provides=('electrum')
conflicts=('electrum')
source=()

_gitroot='git://github.com/spesmilo/electrum.git'
_gitname='electrum'

# change this if you'd like to build a different branch
_gitbranch='master'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin $_gitbranch
    msg "The local files are updated."
  else
    git clone -b $_gitbranch "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i -e 's|^#!/usr/bin/env python|#!/usr/bin/env python2|' \
      -e 's|^#!/usr/bin/python|#!/usr/bin/python2|' \
      lib/* gui/* scripts/* electrum make_packages mki18n.py setup.py

  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/icons_rc.py
  python2 setup.py build
}

package() {
  cd "$srcdir/$_gitname-build"
  python2 setup.py install --root=$pkgdir
}
