# Maintainer: Xiao-Long chen <chenxiaolong@cxl.epac.to>

pkgname=policykit-desktop-privileges-ubuntu
pkgver=0.10
pkgrel=2
pkgdesc="Policykit rules to allow administrators to Run common desktop actions without password"
arch=('any')
url="https://launchpad.net/ubuntu/+source/policykit-desktop-privileges"
license=('GPL')
depends=('polkit')
conflicts=('desktop-privileges')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver}.tar.gz")
sha512sums=('596d9ba22fa526703b5a3c8a8f7a2a16baf2638e54d1ac04ad8e310ff3e6da9b1b04e450186f5ffea06c0ce3a1d778c2d9cc3a6ad496e6de445727ba612fd867')

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  install -dm700 "${pkgdir}/etc/polkit-1/localauthority/"
  install -dm755 "${pkgdir}/etc/polkit-1/localauthority/50-local.d/"
  install -m644 com.ubuntu.desktop.pkla "${pkgdir}/etc/polkit-1/localauthority/50-local.d/"

  # Add wheel to the groups as the 'admin' and 'sudo' groups aren't present in
  # Arch Linux
  sed -i '/Identity=.*sudo/ s/$/;unix-group:wheel/g' "${pkgdir}/etc/polkit-1/localauthority/50-local.d/com.ubuntu.desktop.pkla"
}

# vim:set ts=2 sw=2 et:
