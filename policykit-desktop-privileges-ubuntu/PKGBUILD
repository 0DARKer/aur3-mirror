# Maintainer: Xiao-Long chen <chenxiaolong@cxl.epac.to>

pkgname=policykit-desktop-privileges-ubuntu
pkgver=0.12
pkgrel=1
pkgdesc="Policykit rules to allow administrators to Run common desktop actions without password"
arch=('any')
url="https://launchpad.net/ubuntu/+source/policykit-desktop-privileges"
license=('GPL')
depends=('polkit')
conflicts=('desktop-privileges')
source=("https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname%-*}_${pkgver}.tar.gz")
sha512sums=('4f9f924446efdbeb3cbdc6eeda6b2aa2103a905de828a99639d895e239fcf9830a3242243424d40bcc968c9871527448ff7d5325cc658b76d619dfc764b2aaf0')

package() {
  cd "${srcdir}/${pkgname%-*}-${pkgver}"
  install -dm700 "${pkgdir}/etc/polkit-1/localauthority/"
  install -dm755 "${pkgdir}/etc/polkit-1/localauthority/50-local.d/"
  install -m644 com.ubuntu.desktop.pkla "${pkgdir}/etc/polkit-1/localauthority/50-local.d/"

  # Add wheel to the groups as the 'admin' and 'sudo' groups aren't present in
  # Arch Linux
  sed -i '/Identity=.*sudo/ s/$/;unix-group:wheel/g' "${pkgdir}/etc/polkit-1/localauthority/50-local.d/com.ubuntu.desktop.pkla"
}

# vim:set ts=2 sw=2 et:
