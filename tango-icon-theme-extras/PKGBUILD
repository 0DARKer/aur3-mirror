# Maintainer: kfgz <kfgz at interia pl>
# Contributor: Thayer Williams <thayer at archlinux dot org>
# Contributor: James Rayner <james at archlinux dot org>
# Contributor: William Rea <sillywilly at gmail dot com>

pkgname=tango-icon-theme-extras
pkgver=0.1.0
pkgrel=4
pkgdesc="Extra icons for Tango"
arch=('any')
url="http://tango.freedesktop.org"
license=('custom:ccpl-attribution-sharealike-2.5')
depends=('tango-icon-theme')
makedepends=('imagemagick' 'icon-naming-utils' 'python')
#source=(http://tango.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz)
source=(https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.orig.tar.gz)
md5sums=('caaceaec7b61f1cbda0db9842f9db281')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-png-creation
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
