# Maintainer: J0k3r <moebius282 e4at gmail D0_T com>

pkgname=hotlinemiami
pkgver=1.0.9a
pkgrel=1
pkgdesc="A 2D top-down action video game"
url="http://superhexagon.com/"
license=('unknown')
# © Dennaton Interactive Design of the Future ™
arch=('i686' 'x86_64')
groups=("games")
if [ "$CARCH" = "x86_64" ]; then
	depends=('lib32-openal' 'lib32-libvorbis' 'lib32-nvidia-cg-toolkit' 'lib32-libgl' 'lib32-glu' 'fontconfig' 'freetype2')
else
	depends=('openal' 'libvorbis' 'nvidia-cg-toolkit' 'libgl' 'glu' 'qt5-base' 'fontconfig' 'freetype2')
fi
_archivename="${pkgname}_v${pkgver}-Linux_28-05-2013"
source=("hib://${_archivename}.tar.gz"
		"${pkgname}.desktop")
# "${pkgname}.png")
sha256sums=('070300558ba52f75455d1645bba6dd956f460ada0d3839f8b3c3ac712ba957da'
			'c0c3c2f2ed4b199adbac914af0dfb38cd0a0d65516200f1a5da93fd6100459a8')
noextract=("${source[0]:6}")
PKGEXT=".pkg.tar"


# You can download the Humble Indie Bundle file manually, or you can configure
# DLAGENTS in makepkg.conf to auto-download.
#
# For example, to use hib-dlagent to download files set something like this in
# your makepkg.conf (change/add -k and add -u/-p to your needs):
# DLAGENTS=('hib::/usr/bin/hib-dlagent -k 1a2b3c -o %o $(echo %u | cut -c 7-)')
#
# To auto-search through a directory containing Humble Bundle downloads, you
# could set:
# DLAGENTS=('hib::/usr/bin/find /path/to/downloads -name $(echo %u | cut -c 7-) -exec ln -s \{\} %o \; -quit')
DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Download manually to \"$(pwd)\" or setup hib:// DLAGENT in /etc/makepkg.conf."; exit 1')


package()
{
	install -d "${pkgdir}/opt/${pkgname}/"

	#otherwise it extract the files loosely in $srcdir
	tar -xzf  "${srcdir}/${_archivename}.tar.gz" -C "${pkgdir}/opt/${pkgname}/"

	# we don't need these, as we use the shared system libraries
	# we can't go this simple way on x86_64 currently, as arch doesn't provide lib32-qt5 :(
	if [ "$CARCH" = "x86_64" ]; then
		_purgelibs=('libopenal.so.1' 'libCgGL.so' 'libCg.so')
		for i in "${_purgelibs[@]}"; do
			rm "${pkgdir}/opt/${pkgname}/lib/${i}"
		done
		chown root:root "${pkgdir}/opt/${pkgname}/lib/"
	else
		rm -r "${pkgdir}/opt/${pkgname}/lib/"
	fi

	find "${pkgdir}/opt/${pkgname}/" -type f -exec chown root:root "{}" \;

	install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
	# does someone has a suitable one
	# install -D -m644 "${pkgdir}/opt/${pkgname}/${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	
	install -d "${pkgdir}/usr/bin/"

	# we need this as the program expect to be launched from the same directory it resides in
	cat > "${pkgdir}/usr/bin/${pkgname}" <<-EOF
		#!/bin/bash

		cd "/opt/${pkgname}/"
		exec ./hotline_launcher
	EOF

	chmod 0755 "${pkgdir}/usr/bin/${pkgname}"
	}