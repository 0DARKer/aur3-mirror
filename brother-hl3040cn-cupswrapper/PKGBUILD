# Maintainer: Christopher Hall <followingthepath (at) gmail (dot) com>
pkgname=brother-hl3040cn-cupswrapper
_printer=hl3040cn
pkgver=1.1.1_4
pkgrel=1
pkgdesc="LPR-to-CUPS wrapper for Brother HL-3040CN multifunction network printer"
arch=('i686' 'x86_64')
url="http://welcome.solutions.brother.com/bsc/public_s/id/linux/en/index.html"
license=('gpl')
optdepends=('cups: useless otherwise' 'brother-hl3040cn-lpr>=1.0.1_1: needed to print')
makedepends=('binutils')
provides=("$pkgname=${pkgver%_*}")
conflicts=("$pkgname")
replaces=("$pkgname")
source=("http://www.brother.com/pub/bsc/linux/dlf/${_printer}cupswrapper-${pkgver/_/-}.i386.deb")
md5sums=('79e25737e180524262772a9e4e8f4ee4')

build() {
  cd $srcdir
  # Extract the archive
  ar x ${_printer}cupswrapper-${pkgver/_/-}.i386.deb || return 1
  mkdir data
  tar -xzvf data.tar.gz -C data || return 1
  # Fix the directory structure. Moves /usr/local/Brother to /usr/share/brother and fix links
  mkdir -p data/usr/share
  mv data/usr/local/Brother data/usr/share/brother
  rm -r data/usr/local
  sed -i 's|/usr/local/Brother|/usr/share/brother|g' `grep -lr '/usr/local/Brother' data/` || return 1
  sed -i 's|/etc/init.d|/etc/rc.d|' "data/usr/share/brother/Printer/${_printer}/cupswrapper/cupswrapper${_printer}" || return 1
  # copy into place
  cp -r data/usr $pkgdir || return 1
}