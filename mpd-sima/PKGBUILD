# Maintainer: Vincent Berset <msieurhappy@gmail.com>
pkgname=mpd-sima
pkgver=0.7.2
pkgrel=1
pkgdesc="Automagically add title to mpd playlist based on last.fm recomendations"
arch=('any')
url="http://codingteam.net/project/sima"
license=('GPL')
depends=('python2' 'python-mpd')
makedepends=('subversion' 'make')
source=("http://codingteam.net/project/sima/download/file/mpd_sima_$pkgver.tgz")
md5sums=('5973e175a98c504b9e3271851a695fbd')

build() {
  cd "$srcdir/mpd_sima_$pkgver"

  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' \
      src/mpd_sima.py src/simadb_cli
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/mpd_sima_$pkgver"
  make DESTDIR="$pkgdir" PREFIX="/usr" ETC="$pkgdir/etc" install
  make ETC="$pkgdir/etc" bash_completion
}
