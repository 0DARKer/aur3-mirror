# Maintainer:  Majia321 <@GMAIL dot com>
# Contributor: 

pkgname=dnsforwarder-git
_gitname=dnsforwarder
pkgver=e056f08
pkgrel=2
pkgdesc="A dnsforwarder designed for anti-spoofing in China mainland"
url="http://micasmica.blogspot.com/2011/08/dns.html"
arch=('i686' 'x86_64')
license=('GPL')
provides=('dnsforwarder')
conflicts=('dnsforwarder')
depends=('curl' 'openssl')
makedepends=('git')
install=dnsforwarder.install
backup=('etc/dnsforwarder.conf')
# Note: If you want to use the 5.X branch ( which is testing but large queries supported )
# Please modify  the source  lines below.
#source=("$_gitname"::"git+git+https://github.com/holmium/dnsforwarder.git#branch=5" 
source=("$_gitname"::"git+https://github.com/holmium/dnsforwarder.git#branch=master" 
		  'dnsforwarder.service')
md5sums=('SKIP'
         '0f0ba464b105346749b0dd43f2402805')

pkgver() {
  cd "$_gitname"
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$_gitname"
  
  # Fix permission denied uglily
  chmod +x configure
  
  ./configure --prefix=/usr \
  --enable-downloader=libcurl \
  --enable-base64decoder=openssl 
  
  # Convert codeset
  iconv -f gb18030 -t utf8 default.config > dnsforwarder.conf
  
  make 
}

package() {
  cd "$_gitname"

  make  DESTDIR="$pkgdir" install
  
  install -Dm644 "dnsforwarder.conf" "$pkgdir"/etc/dnsforwarder.conf
  install -Dm644 "$srcdir/dnsforwarder.service" "$pkgdir"/usr/lib/systemd/system/dnsforwarder.service
}
