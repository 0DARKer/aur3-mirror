# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sebastian Joergensen <sebjorg.gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas.archlinux.org>

_pkgname=ntfs-3g_ntfsprogs
pkgname=ntfs-3g-ar
pkgver=2011.10.9AR.1
pkgrel=1
pkgdesc="NTFS driver and utilities with support for advanced features"
url="http://b.andre.pagesperso-orange.fr/advanced-ntfs-3g.html"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse' 'util-linux')
makedepends=('pkgconfig')
options=('!libtool')
provides=("ntfs-3g=$pkgver" "ntfsprogs=$pkgver")
conflicts=(ntfs-3g ntfsprogs)
source=("http://pagesperso-orange.fr/b.andre/$_pkgname-$pkgver.tgz"
        25-ntfs-config-write-policy.fdi)
sha1sums=('84d0cf61bac05e4ddd4f4777a6170879e62e5ee0'
          '200029f2999a2c284fd30ae25734abf6459c3501')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ac_cv_path_LDCONFIG=/bin/true ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-fuse=external \
    --disable-static \
    --enable-posix-acls \
    --enable-xattr-mappings

  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  ln -s /bin/ntfs-3g "$pkgdir/sbin/mount.ntfs"
  install -Dm644 "$srcdir/25-ntfs-config-write-policy.fdi" \
    "$pkgdir/usr/share/hal/fdi/policy/10osvendor/25-ntfs-config-write-policy.fdi"
}
