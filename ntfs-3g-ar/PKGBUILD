# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sebastian Joergensen <sebjorg.gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas.archlinux.org>

_pkgname=ntfs-3g_ntfsprogs
pkgname=ntfs-3g-ar
pkgver=2011.4.12AR.3
pkgrel=1
pkgdesc="Read and write NTFS driver, with support for advanced features"
url="http://b.andre.pagesperso-orange.fr/advanced-ntfs-3g.html"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse')
makedepends=('pkgconfig')
options=('!libtool')
provides=("ntfs-3g=$pkgver")
conflicts=(ntfs-3g)
source=("http://pagesperso-orange.fr/b.andre/$_pkgname-$pkgver.tgz"
        25-ntfs-config-write-policy.fdi)
sha1sums=('ce36118ab88301d73b0a61ac3a53d2ea29efe325'
          '200029f2999a2c284fd30ae25734abf6459c3501')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ac_cv_path_LDCONFIG=/bin/true ./configure \
    --prefix=/usr \
    --with-fuse=external \
    --disable-static \
    --enable-posix-acls \
    --enable-xattr-mappings

  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  for _dir in src libntfs-3g include; do (
    cd $_dir
    make DESTDIR="$pkgdir" install
  ) done

  ln -s /bin/ntfs-3g "$pkgdir/sbin/mount.ntfs"
  install -m644 -D "$srcdir/25-ntfs-config-write-policy.fdi" \
    "$pkgdir/usr/share/hal/fdi/policy/10osvendor/25-ntfs-config-write-policy.fdi"
}
