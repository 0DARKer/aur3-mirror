# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sebastian Joergensen <sebjorg.gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas.archlinux.org>

_pkgname=ntfs-3g_ntfsprogs
pkgname=ntfs-3g-ar
pkgver=2012.1.15AR.7
pkgrel=1
pkgdesc="NTFS driver and utilities with support for advanced features"
url="http://jp-andre.pagesperso-orange.fr/advanced-ntfs-3g.html"
arch=(i686 x86_64)
license=(GPL2)
depends=(fuse util-linux)
options=('!libtool' '!emptydirs')
provides=("ntfs-3g=$pkgver" "ntfsprogs=$pkgver")
conflicts=(ntfs-3g ntfsprogs)
source=("http://pagesperso-orange.fr/jp-andre/$_pkgname-$pkgver.tgz")
sha1sums=('e0e19466e06239c8828a83854386409058701d31')

build() {
  cd $_pkgname-$pkgver

  ac_cv_path_LDCONFIG=/bin/true ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-fuse=external \
    --disable-static \
    --enable-posix-acls \
    --enable-xattr-mappings

  make
}

package() {
  cd $_pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/{s,}bin/* "$pkgdir"/usr/bin/
  rmdir "$pkgdir"/{s,}bin

  cd "$pkgdir"/usr/bin
  ln -s ntfs-3g mount.ntfs
  ln -sf ntfs-3g mount.ntfs-3g
  ln -sf lowntfs-3g mount.lowntfs-3g
}
