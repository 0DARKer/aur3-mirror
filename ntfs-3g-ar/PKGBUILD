# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sebastian Joergensen <sebjorg.gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas.archlinux.org>

_pkgname=ntfs-3g
pkgname=$_pkgname-ar
pkgver=2011.1.15AR.4
pkgrel=1
pkgdesc="Read and write NTFS driver, with support for advanced features"
url="http://b.andre.pagesperso-orange.fr/advanced-ntfs-3g.html"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse')
makedepends=('pkgconfig')
options=('!libtool')
provides=("$_pkgname=$pkgver")
conflicts=($_pkgname)
source=("http://pagesperso-orange.fr/b.andre/$_pkgname-$pkgver.tgz"
        25-ntfs-config-write-policy.fdi)
md5sums=('bb50904ace289514d6800f579287ab28'
         '9dca69d78310f8635877984167eb515f')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ac_cv_path_LDCONFIG=/bin/true ./configure \
    --prefix=/usr \
    --with-fuse=external \
    --disable-static \
    --enable-posix-acls \
    --enable-xattr-mappings

  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  ln -s /bin/ntfs-3g "${pkgdir}/sbin/mount.ntfs"

  install -m644 -D "$srcdir/25-ntfs-config-write-policy.fdi" \
    "$pkgdir/usr/share/hal/fdi/policy/10osvendor/25-ntfs-config-write-policy.fdi"
}
