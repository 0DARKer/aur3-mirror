# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Sebastian Joergensen <sebjorg.gmail.com>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas BÃ¤chler <thomas.archlinux.org>

pkgbase=ntfs-3g_ntfsprogs
pkgname=ntfs-3g-ar
pkgver=2013.1.13AR.2
_stablever=${pkgver%AR*}
pkgrel=1
pkgdesc="NTFS filesystem utilities with experimental features"
arch=('i686' 'x86_64')
url="http://jp-andre.pagesperso-orange.fr/advanced-ntfs-3g.html"
license=('GPL2')
depends=('util-linux' 'fuse')
conflicts=('ntfsprogs' 'ntfs-3g')
provides=('ntfsprogs' "ntfs-3g=$_stablever-$pkgrel")
options=('!libtool')
source=(http://tuxera.com/opensource/$pkgbase-$_stablever.tgz
        http://pagesperso-orange.fr/jp-andre/$pkgbase-$pkgver.tgz)
sha1sums=('8c12b7644d90ae9fb8d0aca0d7ebd5f8fac2c818'
          'c7cd78ca3459a2b54f07db927fa73e1e8b4efd98')

prepare() {
  cd $pkgbase-$_stablever
  sed -i 's|$(DESTDIR)/sbin|$(DESTDIR)/usr/bin|' {ntfsprogs,src}/Makefile.in
  cd ../$pkgbase-$pkgver
  sed -i 's|$(DESTDIR)/sbin|$(DESTDIR)/usr/bin|' {ntfsprogs,src}/Makefile.in
}

build() {
  cd $pkgbase-$_stablever
  ./configure --prefix=/usr \
    --with-fuse=external --disable-static \
    --enable-posix-acls --disable-ldconfig \
    --mandir=/usr/share/man --sbin=/usr/bin 
  make -C libntfs-3g libntfs-3g.la

  cd ../$pkgbase-$pkgver
  ./configure --prefix=/usr \
    --with-fuse=external --disable-static \
    --enable-posix-acls --disable-ldconfig \
    --enable-xattr-mappings --enable-extras \
    --mandir=/usr/share/man --sbin=/usr/bin 
  make
}

package_ntfs-3g-ar() {
  cd $pkgbase-$_stablever
  make -C libntfs-3g DESTDIR="$pkgdir" rootlibdir=/usr/lib install-libLTLIBRARIES

  cd ../$pkgbase-$pkgver
  make DESTDIR="$pkgdir" rootbindir=/usr/bin rootsbindir=/usr/bin rootlibdir=/usr/lib install
  ln -s /usr/bin/ntfs-3g "$pkgdir/usr/bin/mount.ntfs"
}
