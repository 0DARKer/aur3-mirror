# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>
# Contributor: Franco Tortoriello

# You may need to uninstall your current firefox installation before building this package!

pkgname=firefox-qt
pkgver=13.0.1
pkgrel=1
pkgdesc="Web browser from mozilla.org, using Qt (experimental)"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('qt' 'mozilla-common' 'libxt' 'startup-notification' 'mime-types'
	 'dbus-glib' 'libnotify' 'libvpx' 'libevent' 'pango' 'nss>=3.13.3'
	 'hunspell')
makedepends=('unzip' 'zip' 'python2' 'wireless_tools' 'yasm' 'mesa'
	     'autoconf2.13' 'libidl2' 'imake' 'upx')
optdepends=('wireless_tools: Location detection via available WiFi networks'
	    'firefox-oxygen-kde')
provides=("firefox=$pkgver")
conflicts=('firefox')
url="http://www.mozilla.org/projects/firefox"
options=(!emptydirs upx)
install=$pkgname.install
source=("https://ftp.mozilla.org/pub/mozilla.org/firefox/releases/$pkgver/source/firefox-$pkgver.source.tar.bz2"
	'mozconfig' 'firefox.desktop' 'firefox-install-dir.patch' 'vendor.js' 'gcc47.patch')
md5sums=('82deadb501c7fc0e9fa6b025f51f05a1'
	 '00f601a040b8db40ddbeae816c925cc5'
	 'bdeb0380c7fae30dd0ead6d2d3bc5873'
	 '594523c536a9be951b52c90dd85aff18'
	 '0d053487907de4376d67d8f499c5502b'
	 '9a0693521d215189cccf2f1013e41c8a')

build() {
  cd "$srcdir/mozilla-release"

  cp ../mozconfig .mozconfig
  patch -Np1 -i ../firefox-install-dir.patch
  patch -Np1 -i ../gcc47.patch

  # Fix PRE_RELEASE_SUFFIX
  sed -i '/^PRE_RELEASE_SUFFIX := ""/s/ ""//' \
    browser/base/Makefile.in

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/firefox"
  export PYTHON="/usr/bin/python2"
  export MOZ_MAKE_FLAGS="$MAKEFLAGS"
  unset MAKEFLAGS

  # Enable PGO
  export MOZ_PGO=1
  export DISPLAY=:99
  Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 $DISPLAY &

  make -f client.mk build
  kill $! || true
}

package() {
  cd "$srcdir/mozilla-release"
  make -j1 -f client.mk DESTDIR="$pkgdir" install

  install -m644 ../vendor.js "$pkgdir/usr/lib/firefox/defaults/pref"

  for i in 16 22 24 32 48 256; do
    install -Dm644 browser/branding/official/default$i.png \
      "$pkgdir/usr/share/icons/hicolor/${i}x${i}/apps/firefox.png"
  done


  install -Dm644 ../firefox.desktop \
    "$pkgdir/usr/share/applications/firefox.desktop"

  # Use system-provided dictionaries
  rm -rf "$pkgdir"/usr/lib/firefox/{dictionaries,hyphenation}
  ln -s /usr/share/hunspell "$pkgdir/usr/lib/firefox/dictionaries"
  ln -s /usr/share/hyphen "$pkgdir/usr/lib/firefox/hyphenation"

  # We don't want the development stuff
  rm -r "$pkgdir"/usr/{include,lib/firefox-devel,share/idl}

  #workaround for now
  #https://bugzilla.mozilla.org/show_bug.cgi?id=658850
  ln -sf firefox "$pkgdir/usr/lib/firefox/firefox-bin"
}
