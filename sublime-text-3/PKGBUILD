# Maintainer: Alban Dericbourg <alban@dericbourg.net>

pkgname=sublime-text-3
pkgver=3_build_3059
pkgrel=2
pkgdesc="sophisticated text editor for code, html and prose"
arch=('i686' 'x86_64')
url="http://www.sublimetext.com/3"
license=('custom')
depends=(libpng gtk2 xdg-utils desktop-file-utils)
install=${pkgname}.install
_arch=' x64'
_archurl='x64'

[[ "${CARCH}" = i686 ]] && _arch='' && _archurl=''

source=(
	"http://c758482.r82.cf2.rackcdn.com/sublime_text_${pkgver}_${_archurl}.tar.bz2"
	sublime-text-3.desktop
	sublime-text-3
)

md5sums=('9fd1828ef4bc71f375b872eb139e9eba'
         'f6d0e7f5da0e8c1d57640f62173295bf'
         '9b9cb496a35929398adbbb02a6c26d8f')


[[ "${CARCH}" = i686 ]] && md5sums[0]='34a60dac42f4ebc41218398fcfa92fe2'

package () {
	cd "${srcdir}"

	install -Dm644 ${pkgname}.desktop \
		"${pkgdir}/usr/share/applications/${pkgname}.desktop"

	install -dm755 "${pkgdir}/opt"
	cp --preserve=mode -r "sublime_text_3" \
		"${pkgdir}/opt/${pkgname}"

	for res in 128x128 16x16 256x256 32x32 48x48; do
		install -dm755 "${pkgdir}/usr/share/icons/hicolor/${res}/apps"
		ln -s /opt/${pkgname}/Icon/${res}/sublime_text.png \
			"${pkgdir}/usr/share/icons/hicolor/${res}/apps/sublime_text.png"
	done

	install -Dm755 sublime-text-3 "${pkgdir}/usr/bin/sublime-text-3"
}

