# Maintainer: sekret, mail=$(echo c2VrcmV0QHBvc3Rlby5zZQo= | base64 -d)
_pkgname=2048.c
pkgname=$_pkgname-git
pkgver=67
pkgrel=1
pkgdesc="Console version of the game '2048' for GNU/Linux"
arch=('i686' 'x86_64' 'armv6h')
url="https://github.com/mevdschee/2048.c"
license=('MIT')
depends=('glibc')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+https://github.com/mevdschee/2048.c.git")
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git rev-list --count master
  #git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"
  make
}

package() {
  cd "$_pkgname"
  install -Dm755 2048 "$pkgdir/usr/bin/$_pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
