# Contributor: Tom < reztho at archlinux dot us >
pkgname=openmsx
pkgver=0.8.2
pkgrel=1
pkgdesc="The MSX emulator that aims for perfection."
arch=('i686' 'x86_64')
url="http://openmsx.sourceforge.net"
license=('custom')
depends=('libxml2' 'sdl' 'sdl_image' 'sdl_ttf' 'tcl' 'libpng' 'zlib')
optdepends=('python2' 'jack-audio-connection-kit' 'glew' 'libgl' 'catalyst-utils' 'nvidia-utils'
'nvidia-96xx-utils' 'nvidia-173xx-utils')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz" 'openmsx_082_zlib.patch')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  # Changing some default configurations...
  sed -i 's@SYMLINK_FOR_BINARY:=true@SYMLINK_FOR_BINARY:=false@' build/custom.mk
  sed -i 's@INSTALL_BASE:=/opt/openMSX@INSTALL_BASE:=/usr/share/openmsx@' build/custom.mk
  echo 'INSTALL_DOC_DIR:=/usr/share/doc/openmsx' >> build/custom.mk
  echo 'INSTALL_SHARE_DIR:=/usr/share/openmsx' >> build/custom.mk
  echo 'INSTALL_BINARY_DIR:=/usr/bin' >> build/custom.mk

  # Patching
  patch -p1 < ${srcdir}/openmsx_082_zlib.patch

  # Compiling
  ./configure
  make
  make DESTDIR=${pkgdir} install

  # Licenses
  mkdir -p ${pkgdir}/usr/share/licenses/openmsx
  install -m 644 ${pkgdir}/usr/share/doc/openmsx/GPL.txt ${pkgdir}/usr/share/licenses/openmsx/openmsx.txt
  install -m 644 ${pkgdir}/usr/share/doc/openmsx/cbios.txt ${pkgdir}/usr/share/licenses/openmsx/
}

md5sums=('42bceabdd89a4986e745f74ac44da631'
         '84ea00e6d64bfa0c7b485eb42a34d387')
