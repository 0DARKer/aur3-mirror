# Maintainer: Thomas S Hatch <thatch45 (at) Gmail (dot) com>

pkgname=febootstrap
pkgver=3.12
pkgrel=1
license=('GPL2')
arch=('i686' 'x86_64')
pkgdesc="Tool used to create libguestfs supermin appliances"
url="http://people.redhat.com/~rjones/febootstrap/"
depends=('wget' 'cpio')
makedepends=('ocaml' 'ocaml-findlib')
source=("http://people.redhat.com/~rjones/${pkgname}/files/${pkgname}-${pkgver}.tar.gz")
sha512sums=('06c2f5a5c7a002f4500fd4011c342f687a1200678e02af13cc27bbff67faac015d9f2dcc781415a33b511a728e0231081e1f2b72fef807e10fe94daf4cc969a5')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make 
}

check() {
  cd "${pkgname}-${pkgver}"
  make -k check
}

package () {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
