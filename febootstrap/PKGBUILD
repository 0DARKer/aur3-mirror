# Maintainer: Thomas S Hatch <thatch45 (at) Gmail (dot) com>

pkgname=febootstrap
pkgver=3.13
pkgrel=1
license=('GPL2')
arch=('i686' 'x86_64')
pkgdesc="Tool used to create libguestfs supermin appliances"
url="http://people.redhat.com/~rjones/febootstrap/"
depends=('wget' 'cpio')
makedepends=('ocaml' 'ocaml-findlib')
source=("http://people.redhat.com/~rjones/${pkgname}/files/${pkgname}-${pkgver}.tar.gz")
sha512sums=('83703d3e4d6fa3a690c7b045851a287535a7329a47e14427a5ceb7953657b240cd9bee3bf5e3231519d8d626b624ed1731bb8c23a778af7c9012a95be3a73660')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make 
}

check() {
  cd "${pkgname}-${pkgver}"
  make -k check
}

package () {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
