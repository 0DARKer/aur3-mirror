# Maintainer: Thomas S Hatch <thatch45 (at) Gmail (dot) com>

pkgname=febootstrap
pkgver=3.14
pkgrel=1
license=('GPL2')
arch=('i686' 'x86_64')
pkgdesc="Tool used to create libguestfs supermin appliances"
url="http://people.redhat.com/~rjones/febootstrap/"
depends=('wget' 'cpio')
makedepends=('ocaml' 'ocaml-findlib')
source=("http://people.redhat.com/~rjones/${pkgname}/files/${pkgname}-${pkgver}.tar.gz")
sha512sums=('89db31a670a98fc620cc63593e9e0b67b4f5911ff6bffb6cbd8220f2eb4f52cabc8f02f9946340637fe180458c95851dce708589af75ef97bfb18503da2dd2b1')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make ${MAKEFLAGS}
}

check() {
  cd "${pkgname}-${pkgver}"
  make -k check
}

package () {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
