# Maintainer: boenki <boenki at gmx dot de>

pkgname=otter-browser
pkgver=0.9.05
pkgrel=2
pkgdesc="Browser aiming to recreate classic Opera (12.x) UI using Qt5."
arch=('i686' 'x86_64')
url="http://$pkgname.org"
license=('GPL3')
depends=('qt5-multimedia' 'qt5-webkit' 'qt5-script' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools')
conflicts=('otter-browser-git')
install=otter.install
source=($pkgname-$pkgver.tar.gz::https://github.com/OtterBrowser/$pkgname/archive/v$pkgver.tar.gz
        $pkgname-fix-open-links.patch::https://github.com/OtterBrowser/otter-browser/commit/931f6ad53468ad5b5d0c4ed7eb12d8d110a67ffe.patch)
md5sums=('e24ed80e04b88811717173de77635094'
         'edd842f69f371bf6d9d124646a9fb84c')

prepare() {
 cd $pkgname-$pkgver
 patch -p1 -i ../$pkgname-fix-open-links.patch
}

build() {
  cd $pkgname-$pkgver
  lrelease resources/translations/*.ts
  cmake -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
