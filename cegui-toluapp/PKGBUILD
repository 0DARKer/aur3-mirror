# $Id$
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>,
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>
# Maintainer : Jesse Jaara <gmail.com: jesse.jaara>

pkgname=cegui-toluapp
pkgver=0.7.5
pkgrel=5
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines"
arch=('i686' 'x86_64')
url="http://crayzedsgui.sourceforge.net"
license=("MIT")
depends=('pcre' 'glew' 'expat' 'freetype2' 'libxml2' 'devil' 'freeglut' 'toluapp' 'silly')
makedepends=('python2' 'doxygen')
source=(http://downloads.sourceforge.net/crayzedsgui/CEGUI-${pkgver}.tar.gz)
md5sums=('38c79d1fdfaaa10f481c99a2ac479516')

build() {
  cd "${srcdir}/CEGUI-${pkgver}"

  sed -i '1i#include <cstddef>' cegui/include/CEGUIString.h
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --disable-xerces-c \
    --enable-null-renderer

  make
}

package() {
  cd "${srcdir}/CEGUI-${pkgver}"

  make DESTDIR="${pkgdir}" install

  #build docs
  cd doc/doxygen && doxygen
  cd .. && make DESTDIR="${pkgdir}" install-html

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

