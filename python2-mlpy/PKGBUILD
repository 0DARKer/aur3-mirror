# Maintainer: Thomas Dziedzic < gostrc at gmail >

pkgname=python2-mlpy
pkgver=3.1
pkgrel=1
pkgdesc='Machine Learning PYthon (mlpy) is a high-performance Python package for predictive modeling.'
arch=('i686' 'x86_64')
url='https://mlpy.fbk.eu/'
license=('GPL3')
depends=('python2-numpy' 'gsl' 'cython')
source=("http://downloads.sourceforge.net/project/mlpy/mlpy%20${pkgver}/mlpy-${pkgver}.tar.gz")
md5sums=('d8ef82b3be6c46b4f5c178baf121a861')

build() {
  cd mlpy-${pkgver}

  # fix missing ptrdiff_t
  sed \
    -e '10i#include <stddef.h>' \
    -i mlpy/fastcluster/fastcluster/src/fastcluster.cpp

  python2 setup.py build
}

package() {
  cd mlpy-${pkgver}

  python2 setup.py install --root=${pkgdir} --optimize=1
}
