# Maintainer: John Williams <jwilliams4200 liamg reverse&remove moc>
pkgname=snapraid
pkgver=2.0
pkgrel=1
pkgdesc="Snapshot RAID, backup storage drives and maintain checksums on all data"
arch=('x86_64' 'i686')
url="http://snapraid.sourceforge.net/"
license=('GPL3')
depends=('openssl')
conflicts=('snapraid-git')
source=("http://sourceforge.net/projects/snapraid/files/snapraid-${pkgver}.tar.gz")
sha256sums=('68a48c961fcc15ffcfb86e4c5ea2371d562252e61f58fe87a529f479e81fb2bb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make 
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install 
  install -D -m644 snapraid.conf.example $pkgdir/usr/share/$pkgname/snapraid.conf.example
  install -D -m644 AUTHORS $pkgdir/usr/share/doc/$pkgname/AUTHORS
  install -D -m644 HISTORY $pkgdir/usr/share/doc/$pkgname/HISTORY
  install -D -m644 README $pkgdir/usr/share/doc/$pkgname/README
  install -D -m644 TODO $pkgdir/usr/share/doc/$pkgname/TODO
}

