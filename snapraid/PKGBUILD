# Maintainer: John Williams <jwilliams4200 liamg reverse&remove moc>
pkgname=snapraid
pkgver=3.0
pkgrel=1
pkgdesc="tool for Snapshot RAID: generate parity files, maintain checksums on data, restore lost data"
arch=('x86_64' 'i686')
url="http://snapraid.sourceforge.net/"
license=('GPL3')
depends=('openssl')
conflicts=('snapraid-git')
source=("http://sourceforge.net/projects/snapraid/files/snapraid-${pkgver}.tar.gz")
sha256sums=('68e610d67087c02715289934472c5fa3d7aa77c8085298ba54f02f26f7d396ad')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make 
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install 
  install -D -m644 snapraid.conf.example $pkgdir/usr/share/$pkgname/snapraid.conf.example
  install -D -m644 AUTHORS $pkgdir/usr/share/doc/$pkgname/AUTHORS
  install -D -m644 HISTORY $pkgdir/usr/share/doc/$pkgname/HISTORY
  install -D -m644 README $pkgdir/usr/share/doc/$pkgname/README
  install -D -m644 TODO $pkgdir/usr/share/doc/$pkgname/TODO
}

