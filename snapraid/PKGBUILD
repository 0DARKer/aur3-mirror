#Maintainer: John Williams <jwilliams4200 gmail>
pkgname=snapraid
pkgver=1.7
pkgrel=3
pkgdesc="Snapshot RAID, backup storage arrays and maintain checksums on all data"
arch=('x86_64' 'i686')
url="http://snapraid.sourceforge.net/"
license=('GPL3')
depends=('openssl')
source=("http://sourceforge.net/projects/snapraid/files/snapraid-${pkgver}.tar.gz"
"${pkgname}_num-content.diff.patch")
md5sums=('f705819e9f9be14fbfcbffdb581f1404'
         '190bbd077f4123fca9be7527bb6561c8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -uN state.c ../${pkgname}_num-content.diff.patch || return 1
  ./configure --prefix=/usr
  make 
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install 
  install -D -m644 snapraid.conf.example $pkgdir/usr/share/$pkgname/snapraid.conf.example
  install -D -m644 AUTHORS $pkgdir/usr/share/$pkgname/AUTHORS
  install -D -m644 HISTORY $pkgdir/usr/share/$pkgname/HISTORY
  install -D -m644 README $pkgdir/usr/share/$pkgname/README
  install -D -m644 TODO $pkgdir/usr/share/$pkgname/TODO
}

