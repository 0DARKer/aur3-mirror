# Maintainer: John Williams <jwilliams4200 liamg reverse&remove moc>
pkgname=snapraid
pkgver=1.10
pkgrel=1
pkgdesc="Snapshot RAID, backup storage drives and maintain checksums on all data"
arch=('x86_64' 'i686')
url="http://snapraid.sourceforge.net/"
license=('GPL3')
depends=('openssl')
conflicts=('snapraid-git')
source=("http://sourceforge.net/projects/snapraid/files/snapraid-${pkgver}.tar.gz")
sha256sums=('5e4eee49748faf966ebcc1cb3b9127ad7047542baf8952c6e0a9cbb32dac4d88')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # patch to allow fewer content files
  sed -i -e 's/if (content_count < state->level+1) {/if (content_count < 1) {/' state.c

  ./configure --prefix=/usr
  make 
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install 
  install -D -m644 snapraid.conf.example $pkgdir/usr/share/$pkgname/snapraid.conf.example
  install -D -m644 AUTHORS $pkgdir/usr/share/doc/$pkgname/AUTHORS
  install -D -m644 HISTORY $pkgdir/usr/share/doc/$pkgname/HISTORY
  install -D -m644 README $pkgdir/usr/share/doc/$pkgname/README
  install -D -m644 TODO $pkgdir/usr/share/doc/$pkgname/TODO
}

