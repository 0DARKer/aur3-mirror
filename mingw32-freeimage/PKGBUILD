# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Mihai Militaru <mihai.militaru@gmx.com>
# Contributor: scippio <scippio@berounet.cz>

pkgname=mingw32-freeimage
pkgver=3.15.3
pkgrel=3
pkgdesc="Library project for developers who would like to support popular graphics image formats (mingw32)."
arch=('any')
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=('mingw32-runtime' 'freeimage')
makedepends=('mingw32-gcc')
options=(!strip !buildflags)
source=("http://downloads.sourceforge.net/sourceforge/freeimage/FreeImage${pkgver//./}.zip"
        'libraw.patch')
md5sums=('6c305b4086500ff8541c596ea6fc7cd7'
         '440a85a8519f8fa5c6ff2eff7dddb78c')

build() {
  cd "${srcdir}/FreeImage"
  
  patch -Np1 < "${srcdir}/libraw.patch"
  unset LDFLAGS
  
  make \
    CC="i486-mingw32-gcc" \
    CXX="i486-mingw32-g++" \
    LD="i486-mingw32-g++" \
    RC="i486-mingw32-windres" \
    AR="i486-mingw32-ar" \
    DLLTOOL="i486-mingw32-dlltool" \
    -f Makefile.mingw
}

package() {
  cd "${srcdir}/FreeImage"

  install -Dm644 Dist/FreeImage.dll "${pkgdir}/usr/i486-mingw32/bin/FreeImage.dll"
  install -Dm644 Dist/FreeImage.lib "${pkgdir}/usr/i486-mingw32/lib/FreeImage.lib"
  install -Dm644 Dist/FreeImage.h   "${pkgdir}/usr/i486-mingw32/include/FreeImage.h"

  i486-mingw32-strip -x -g "${pkgdir}"/usr/i486-mingw32/bin/*.dll
  i486-mingw32-strip -g "${pkgdir}"/usr/i486-mingw32/lib/*.lib
}
