# Maintainer: Davorin Uƒçakar <davorin.ucakar@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Mihai Militaru <mihai.militaru@gmx.com>
# Contributor: scippio <scippio@berounet.cz>

pkgname=mingw32-freeimage
pkgver=3.15.2
pkgrel=1
pkgdesc="Library project for developers who would like to support popular graphics image formats (mingw32)."
arch=(any)
license=('GPL' 'custom:FIPL')
url="http://freeimage.sourceforge.net/"
depends=('mingw32-gcc')
options=(!strip !buildflags)
source=("http://downloads.sourceforge.net/sourceforge/freeimage/FreeImage${pkgver//./}.zip"
        'libraw.patch')
md5sums=('c1f6f404f46f0fb6483c0372d1408833'
         '642007fb9ce70afa96babcf3cea557c4')

build() {
  cd ${srcdir}/FreeImage
  
  patch -Np1 < ${srcdir}/libraw.patch
  
  make \
    CC="i486-mingw32-gcc" \
    CXX="i486-mingw32-g++" \
    LD="i486-mingw32-g++" \
    RC="i486-mingw32-windres" \
    AR="i486-mingw32-ar" \
    DLLTOOL="i486-mingw32-dlltool" \
    -f Makefile.mingw
}

package() {
  cd ${srcdir}/FreeImage

  i486-mingw32-strip Dist/FreeImage.lib
  i486-mingw32-strip Dist/FreeImage.dll

  install -Dm644 Dist/FreeImage.h ${pkgdir}/usr/i486-mingw32/include/FreeImage.h
  install -Dm644 Dist/FreeImage.lib ${pkgdir}/usr/i486-mingw32/lib/libfreeimage.dll.a
  install -Dm644 Dist/FreeImage.dll ${pkgdir}/usr/i486-mingw32/bin/libfreeimage.dll
}
