# Maintainer:  dantob
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Zephyr

pkgname=mupen64plus-z64
pkgver=2.5
pkgrel=1
pkgdesc='Nintendo64 Emulator with z64 plugins'
arch=('i686' 'x86_64')
url='https://github.com/mupen64plus'
license=('GPL')
depends=('sdl' 'libpng' 'freetype2' 'zlib')
makedepends=('mesa')
conflicts=('mupen64plus')
provides=('mupen64plus')
#options=('!strip' 'debug')
source=("https://github.com/mupen64plus/mupen64plus-core/releases/download/${pkgver}/mupen64plus-bundle-src-${pkgver}.tar.gz"
        "https://github.com/mupen64plus/mupen64plus-rsp-z64/releases/download/2.0.0/mupen64plus-rsp-z64-src-2.0.0.tar.gz"
		"https://github.com/mupen64plus/mupen64plus-video-z64/releases/download/2.0.0/mupen64plus-video-z64-src-2.0.0.tar.gz")
md5sums=('e329654f869ced5eaf82a8c2337b28d7'
		 '669e5680dadc38e5d7b580100130de5e'
		 '5a035232847d7fa2fc4f8eea92425e76')

prepare() {
  # move z64 plugins to the bundle directory
  mv mupen64plus-rsp-z64 $srcdir/mupen64plus-bundle-src-${pkgver}/source/
  mv mupen64plus-video-z64 $srcdir/mupen64plus-bundle-src-${pkgver}/source/

  # BUG: video-z64 has a problem finding system glew.h
  cd $srcdir/mupen64plus-bundle-src-${pkgver}/source/mupen64plus-video-z64/src
  sed -i 's:glew.h:GL/glew.h:g' rgl.h
  sed -i 's:glew.h:GL/glew.h:g' glshader.cpp
}

build() {
  # BUG: rice and glide crash on my intel system, so lets disable them
  # BUG: Intel Corporation 82915G/GV/910GL Integrated Graphics Controller (rev 04)
  # BUG: OpenGL renderer string: Mesa DRI Intel(R) 915G 
  # BUG: OpenGL version string: 2.1 Mesa 10.5.5
  # disabled: rsp-hle, video-rice, video-glide64mk2
  cd $srcdir/mupen64plus-bundle-src-${pkgver}
  export M64P_COMPONENTS="core rom ui-console audio-sdl input-sdl rsp-z64 video-z64"
  sh m64p_build.sh
}

package() {
  cd $srcdir/mupen64plus-bundle-src-$pkgver
  
  # set LDCONFIG since we are using fakeroot and scripts run root commands by checking the uid
  ./m64p_install.sh DESTDIR="$pkgdir" PREFIX='/usr' MANDIR='/usr/share/man' LDCONFIG='true'
}

