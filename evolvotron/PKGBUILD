# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>

pkgname=evolvotron
pkgver=0.6.2
pkgrel=1
pkgdesc="An interactive generative art application"
arch=('i686' 'x86_64')
url="http://www.bottlenose.demon.co.uk/share/evolvotron/"
license=('GPL')
depends=('qt' 'boost-libs')
makedepends=('boost')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('f1665deb8520f3de98c3ea473b13b234')

build() {
  cd "$srcdir/$pkgname"

  # use qmake and python2
  sed -i 's|-qt4||;s| \./| python2&|' configure

  QTDIR=/usr/bin ./configure
  make
}

package() {
  cd "$srcdir/$pkgname"

  # bin
  install -d "$pkgdir/usr/bin"
  find . -type f -perm 755 -name "$pkgname*" -exec \
    install -Dm755 {} "$pkgdir/usr/bin" \;

  # man
  install -d "$pkgdir/usr/share/man/man1"
  install -Dm644 man/man1/* \
    "$pkgdir/usr/share/man/man1"

  # doc
  install -Dm644 $pkgname.html \
    "$pkgdir/usr/share/doc/$pkgname/manual.html"
}
# vim:syntax=sh
