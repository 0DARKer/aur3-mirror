# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Reiser <metal@pop3.ru>
# Contributor: Andrew Panchenko <panchenkoac at gmail>

pkgname=qmmp-svn
pkgver=0.8.0.svn.r3972
pkgrel=1
pkgdesc="A Qt4 based audio-player. (SVN Version)"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com"
license=('GPL')
depends=('qt4' 'ffmpeg' 'mplayer' 'wavpack' 'libmpcdec' 'projectm' 'opusfile' 'libmms' 'libgme' 'wildmidi')
makedepends=('subversion' 'cmake')
optdepends=('qmmp-plugin-pack-svn: for mpg123, ffap and qtui plugin')
provides=('qmmp')
conflicts=('qmmp')
install="${pkgname}.install"
source=(svn+http://qmmp.googlecode.com/svn/trunk/qmmp)
md5sums=('SKIP')
_svnname=qmmp

pkgver() {
  cd "${_svnname}"
  echo "$(cat qmmp.pri | grep QMMP_VERSION | cut -d ' ' -f3).svn.r$(svnversion)"
}

build() {
  cd "${_svnname}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DLIB_DIR=lib -DCMAKE_BUILD_TYPE=RELEASE
  make
}

package() {
  cd "${_svnname}"
  make DESTDIR="${pkgdir}" install
}
