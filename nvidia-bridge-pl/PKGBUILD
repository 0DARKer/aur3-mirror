# Contributor: Piotr Gorski <sir_lucjan@birdgelinux.pl>

pkgname=nvidia-bridge-pl
pkgver=337.12
_extramodules=extramodules-3.14-bridge-pl
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=2
_pkgdesc="NVIDIA drivers for linux-bridge-pl"
pkgdesc="$_pkgdesc"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-bridge-pl>=3.14' 'linux-bridge-pl<3.15' "nvidia-utils=${pkgver}")
makedepends=('linux-bridge-pl-headers>=3.14' 'linux-bridge-pl-headers<3.15')
conflicts=('nvidia-96xx-all' 'nvidia-173xx-all' 'nvidia-ck-stable' 'nvidia-beta-ck')
license=('custom')
options=(!strip)
install=nvidia-bridge-pl.install
 
if [ "$CARCH" = "i686" ]; then
        _arch='x86'
        _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
                'linux3.14.patch')
        md5sums=('c610d5bff8f7f758dc25fdd51b1b65eb'
                '92edfe0c11d774f3de2b67e20c3a452e')
elif [ "$CARCH" = "x86_64" ]; then
        _arch='x86_64'
        _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
        source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run"
                'linux3.14.patch')
        md5sums=('4c2aaac75c1ff01a09ac8f4ff84084cd'
                '92edfe0c11d774f3de2b67e20c3a452e')
fi
 
build() {
        cd "${srcdir}"
        sh ${_pkg}.run --extract-only
        cd ${_pkg}
        patch -Np1 -i ../linux3.14.patch
        cd kernel
        make SYSSRC=/lib/modules/"${_kernver}/build" module
}
 
package() {
        install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
        install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
        echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-bridge-pl.conf"
        sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-bridge-pl.install"
        gzip "${pkgdir}/lib/modules/${_extramodules}/nvidia.ko"
}
