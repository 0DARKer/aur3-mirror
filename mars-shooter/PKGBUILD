# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=mars-shooter
pkgver=0.7.2
pkgrel=1
pkgdesc="A ridiculous space shooter with nice graphics"
arch=(i686 x86_64)
url="http://mars-game.sourceforge.net/"
license=('GPL')
depends=('sfml-git' 'taglib' 'fribidi')
makedepends=('cmake')
source=(http://sourceforge.net/projects/mars-game/files/mars_source_${pkgver}.tar.gz)
md5sums=('d8425aaf8edfafe200360aea40b9609f')

build() {
  cd "$srcdir/mars-game"

  [[ -d build ]] && rm -r build
  mkdir build && cd build
  cmake ..
  make
}

package() {
  cd "$srcdir/mars-game"

  mkdir -p $pkgdir/usr/share/marsshooter
  mkdir -p $pkgdir/usr/bin/
  cp -r credits.txt data/* license.txt $pkgdir/usr/share/marsshooter
  cp mars $pkgdir/usr/bin/mars-shooter
  find $pkgdir -name *.svn | xargs rm -rf

  sed -i 's/Exec=.*/Exec=mars-shooter/g' resources/mars.desktop
  install -Dm644 resources/mars.desktop $pkgdir/usr/share/applications/mars.desktop
  install -Dm644 resources/mars.png $pkgdir/usr/share/pixmaps/mars.png
}
