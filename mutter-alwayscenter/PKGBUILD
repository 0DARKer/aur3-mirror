# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>
# Contributor: Felipe Morales <hel.sheep@gmail.com>
# See https://bugzilla.gnome.org/show_bug.cgi?id=646629

pkgname=mutter-alwayscenter
pkgver=3.4.1
pkgrel=4
pkgdesc="A window manager for GNOME, patched to always center new windows"
arch=(i686 x86_64)
license=('GPL')
depends=('clutter' 'dconf' 'gobject-introspection' 'gsettings-desktop-schemas' 'libcanberra' 'startup-notification' 'zenity')
makedepends=('intltool' 'gnome-doc-utils')
provides=('mutter')
conflicts=('mutter')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=mutter.install
source=(http://ftp.gnome.org/pub/gnome/sources/mutter/${pkgver%.*}/mutter-$pkgver.tar.xz
        0001-Always-centernewwindows.patch)
sha256sums=('dbf08b014179980ab3d0cce645c5391c83b0ce070c73504feea8eec0ad000449'
            '7d31419330d008169c3f765303a765e60b333507afca79a6a924e012f6a4cc29')

build() {
  cp 0001-Always-centernewwindows.patch "mutter-$pkgver/"
  cd "mutter-$pkgver"
  patch -p1 < 0001-Always-centernewwindows.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/mutter \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd "mutter-$pkgver"
  make DESTDIR="$pkgdir" install
}
