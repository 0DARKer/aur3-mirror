# Maintainer: Alexander Drozdov <adrozdoff@gmail.com>
pkgname=astxx
pkgver=0.6
pkgrel=3
pkgdesc="A C++ wrapper for Asterisk enabling developers"
arch=(i686 x86_64)
url="http://matt-land.com/astxx/"
license=('GPL2')
groups=()
depends=('boost-libs')
makedepends=('boost' 'doxygen')
install=astxx.install
changelog=ChangeLog

source=(http://matt-land.com/astxx/releases/$pkgname-$pkgver.tar.gz
        qt4-note.txt)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix boost faults: without this steps all application crashed
  # 1. turn off included boost, use system
  (
    for dir in include src
    do
      cd $dir
      mv boost boost.orig
      cd ..
    done

    sed -i 's|\(cxxfile "src/boost/system/error_code.cpp",\)|-- \1|' build/libastxx_manager.pmfile
  )

  ./pm --prefix=\"/usr\"
  ./pm doc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  ./pm --prefix=\"/usr\" --destdir=\"$pkgdir/\" -- install

  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  install -m644 "$srcdir/qt4-note.txt" "$pkgdir/usr/share/doc/$pkgname/qt4-note.txt"
  cp -a "doc/html" "$pkgdir/usr/share/doc/$pkgname/"
}

md5sums=('7276557229a4677bceee38c279772929'
         '07e490ac66049cdbd59a238d16ae91d4')
