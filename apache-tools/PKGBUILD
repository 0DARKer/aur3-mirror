# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Marco A Rojas <marquicus@gmail.com>
# Contributor: Pavel Larev <pavel@larev.ws>

pkgname=apache-tools
pkgver=2.4.4
pkgdesc="Useful Apache tools - htdigest, htpasswd, ab, htdbm"
pkgrel=1
arch=(i686 x86_64)
url=http://httpd.apache.org/
license=(Apache)
depends=(apr-util pcre)
conflicts=(apache)
source=(http://www.apache.org/dist/httpd/httpd-$pkgver.tar.bz2
    $pkgname-Makefile.patch)
sha256sums=('92aabddeca76a4ac7330b143df1407bbf35574c7291c15172238ac598d97655c'
    '9ccfe2af4aa21da14807dab36dd25b09346690cf0741d8165249f5563bfadb4f')
sha512sums=('d68789e1e585b4acf26e4e32d063fa512525f8fcc2077b1dbf573dd9f4b47667772d94bb65fefb354fbfae331e87b3fdea422a732838c86d8887eed4b3a76af0'
    '77ab0dbd5e2d54cbc8deee2b91e8a8057dae59135b6512b93d40dcdb43f46aca72660f14308c1cba1524f1a8bb53bd9ddb4a550a4ccd5fc9a4fe58fb3bed14bb')

build() {
    cd httpd-$pkgver/
    patch -Np1 -i ../$pkgname-Makefile.patch
    ./configure --prefix=/usr --sbindir=/usr/sbin --with-pcre=/usr
    make -C support
}

package() {
    make -C httpd-$pkgver/support DESTDIR="$pkgdir" install
}
