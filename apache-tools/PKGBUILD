# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Marco A Rojas <marquicus@gmail.com>
# Contributor: Pavel Larev <pavel@larev.ws>

pkgname=apache-tools
pkgver=2.4.4
pkgrel=1
pkgdesc="Useful Apache tools - htdigest, htpasswd, ab, htdbm"
arch=(i686 x86_64)
url=http://httpd.apache.org/
license=(Apache)
depends=(apr-util pcre)
conflicts=(apache)
source=(http://www.apache.org/dist/httpd/httpd-$pkgver.tar.bz2
    $pkgname-Makefile.patch)
sha256sums=('92aabddeca76a4ac7330b143df1407bbf35574c7291c15172238ac598d97655c'
    '7018043ee5d8fd909918a87b81e0f3f684b720de0be062b49aafefbbbe586472')
sha512sums=('d68789e1e585b4acf26e4e32d063fa512525f8fcc2077b1dbf573dd9f4b47667772d94bb65fefb354fbfae331e87b3fdea422a732838c86d8887eed4b3a76af0'
    '39f606181e38d27ee8cce35635d668c3596ed47ae69cba241554dca02e3c34f6e8b96f3233e631c586c936a77c1ae950c903eb868fa4bbd9808cdbe0c2266329')

build() {
    cd httpd-$pkgver/
    patch -Np1 -i ../$pkgname-Makefile.patch
    ./configure --prefix=/usr --sbindir=/usr/sbin --with-pcre=/usr
    make -C support
}

package() {
    make -C httpd-$pkgver/support DESTDIR="$pkgdir" install
}
