# Maintainer: qs9rx < that nick at enjoys dot it>
# Contributor: David Manouchehri <d@32t.ca>

pkgname=dump1090-git
_gitname=dump1090
pkgver=0.r59.acecc6d
pkgrel=1
pkgdesc="Dump1090 is a simple Mode S decoder for RTLSDR devices. Git version"
arch=('i686' 'x86_64')
url="https://github.com/antirez/dump1090"
license=('BSD')
depends=('rtl-sdr')
makedepends=('git')
source=('git://github.com/antirez/dump1090.git')
md5sums=('SKIP')
 
pkgver() {
  cd $_gitname
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}
 
build() {
  cd $_gitname
  sed -i 's#gmap\.html#/usr/share/dump1090/gmap\.html#g' dump1090.c
  make
}
 
package() {
  cd $_gitname
  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/dump1090"
  install -m755 dump1090 "$pkgdir/usr/bin"
  install -m644 gmap.html "$pkgdir/usr/share/dump1090"
}
