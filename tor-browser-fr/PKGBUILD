# Maintainer: Max Roder <maxroder@web.de>
# Contributor (French Version): Pierre Schiltz <mysti57155@gmail.com>
# Maintainer (French Version) : Anthony Caccia <acaccia@ulb.ac.be>

# To port this PKGBUILD to another language of tor-browser you 
# have to change $pkgname, $_language, $pkgdesc and $url in PKGBUILD
# AND (!) the first line in the .install file!

pkgname='tor-browser-fr'
pkgver='4.0.7'
_language='fr'
pkgrel='1'
pkgdesc='Surf anonyme avec firefox et tor'
url='https://www.torproject.org/projects/torbrowser.html'
arch=('x86_64' 'i686')
license=('GPL')
depends=('')
optdepends=('zenity: simple dialog boxes'
			      'kdialog: KDE dialog boxes'
			      'notify-send: Gnome dialog boxes')
install="${pkgname}.install"

if [[ "$CARCH" == 'i686' ]]; then
	_architecture=32
else
  _architecture=64
fi

validpgpkeys=('EF6E286DDA85EA2A4BA7DE684E2C6E8793298290')

source=("https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz"
		"https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz.asc"
		"${pkgname}.desktop"
		"${pkgname}.png"
		"${pkgname}.sh")

if [[ "$CARCH" == 'i686' ]]; then
sha256sums=('b8a50fad0bf8ca20ef862a1d1077ebe602fb11f4e965f37ec0028be016f49f29'
            'a7db50380f002108ce25469b5c9a1cfc8fce5301ce016b7d1585a89b38721bff')
else
sha256sums=('087d83c3e06619f796f648c2a16c93ff28e661d4672e5704c2f1b75c9e9e4197'
            '9209a56bb0c1d536b82db5f2b0c9e2125ffb2c6b8c15a05a1e0fcd4a81092b36')
fi
sha256sums+=('2217f011197329019ae3d282d95623e0230f8f7a3a604290744280530cf1698a'
			       '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			       'd5ff47ae1758eeae2fe493978736391c7c24f363f5ab25625a557d9b149eb963')
noextract=("tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz")

package() {
	cd "${srcdir}"

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.sh
	sed -i "s/REPL_VERSION/${pkgver}/g" 	${pkgname}.sh
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.sh
  sed -i "s/REPL_ARCH/${_architecture}/g"   ${pkgname}.sh

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.desktop
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.desktop
	sed -i "s/REPL_COMMENT/${pkgdesc}/g"		${pkgname}.desktop

	install -Dm 644 ${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 ${pkgname}.png		${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 ${pkgname}.sh		${pkgdir}/usr/bin/${pkgname}

	install -Dm 644 tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz \
		${pkgdir}/opt/${pkgname}/tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz
}

# vim:set ts=2 sw=2 et:

