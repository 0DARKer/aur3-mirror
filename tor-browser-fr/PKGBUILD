# Maintainer: Max Roder <maxroder@web.de>
# Contributor (French Version): Pierre Schiltz <mysti57155@gmail.com>
# Maintainer (French Version) : Anthony Caccia <acaccia@ulb.ac.be>

# To port this PKGBUILD to another language of tor-browser you 
# have to change $pkgname, $_language, $pkgdesc and $url in PKGBUILD
# AND (!) the first line in the .install file!

pkgname='tor-browser-fr'
pkgver='4.0.2'
_language='fr'
pkgrel='1'
pkgdesc='Surf anonyme avec firefox et tor'
url='https://www.torproject.org/projects/torbrowser.html'
arch=('x86_64' 'i686')
license=('GPL')
depends=('')
optdepends=('zenity: simple dialog boxes'
			      'kdialog: KDE dialog boxes'
			      'notify-send: Gnome dialog boxes')
install="${pkgname}.install"

if [[ "$CARCH" == 'i686' ]]; then
	_architecture=32
else
  _architecture=64
fi

source=("https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz"
		"https://www.torproject.org/dist/torbrowser/${pkgver}/tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz.asc"
		"${pkgname}.desktop"
		"${pkgname}.png"
		"${pkgname}.sh")

if [[ "$CARCH" == 'i686' ]]; then
sha256sums=('d59369130530bfa1e9b962f593d7042285014644da06e6f6e62018256fb4950c'
            '694d9241a7f28eb4cf71db26472038de75b54b328795dc834bca0358c54397d5')
else
sha256sums=('d957b20b35d969ecb497a5790ef771850c49e01ec48a2a9efadce204c9eea5b5'
            '02444982f19c1f73fcbc7b7477adfa4908965e1b846b54e376ad705c0d5bcda5')
fi
sha256sums+=('2217f011197329019ae3d282d95623e0230f8f7a3a604290744280530cf1698a'
			       '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			       'd5ff47ae1758eeae2fe493978736391c7c24f363f5ab25625a557d9b149eb963')
noextract=("tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz")

package() {
	cd "${srcdir}"

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.sh
	sed -i "s/REPL_VERSION/${pkgver}/g" 	${pkgname}.sh
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.sh
  sed -i "s/REPL_ARCH/${_architecture}/g"   ${pkgname}.sh

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.desktop
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.desktop
	sed -i "s/REPL_COMMENT/${pkgdesc}/g"		${pkgname}.desktop

	install -Dm 644 ${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 ${pkgname}.png		${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 ${pkgname}.sh		${pkgdir}/usr/bin/${pkgname}

	install -Dm 644 tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz \
		${pkgdir}/opt/${pkgname}/tor-browser-linux${_architecture}-${pkgver}_${_language}.tar.xz
}

# vim:set ts=2 sw=2 et:

