# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributer: TryA -  https://bbs.archlinux.org/viewtopic.php?id=114996

pkgname=mprime
pkgver=266
pkgrel=2
pkgdesc="A GIMPS, distributed computing project client, dedicated to finding Mersenne primes."
arch=('i686' 'x86_64')
url="http://www.mersenne.org"
license=("custom")
depends=('gcc-libs' 'curl')
source=("http://www.mersenneforum.org/gimps/source${pkgver}.zip"
        "http://www.mersenneforum.org/gimps/mprime${pkgver}.tar.gz")
sha256sums=('c64ad85fd1dc16c00eade8f2d971cb8a84003cec92f6d51789dee3c1d6b6dc14'
            'e58faf7f2f3c18856cae5d03c5fead133748fd9d4f136c0e847ac6edfb41dc91')

build() {
  # build gwnum.a
  cd ${srcdir}/gwnum
  [ "${CARCH}" == "x86_64" ] && make -f make64
  [ "${CARCH}" == "i686" ]   && make -f makefile
  
  # patch makefile and build mprime
  [ "${CARCH}" == "x86_64" ] && cd ../linux64
  [ "${CARCH}" == "i686" ]   && cd ../linux
  sed -e 's/-Wl,-Bdynamic -ldl//' \
      -e 's/-Wl,-Bstatic $(shell pkg-config --static --libs libcurl)/$(shell pkg-config --libs libcurl)/' \
      -e 's/rm -f $(EXE) $(EXE2) $(LINUXOBJS) $(FACTOROBJ)/rm -f $(EXE) $(EXE2) $(LINUXOBJS)/' \
      -i makefile
  make
}

package() {
  cd ${srcdir}
  
  # executable
  [ "${CARCH}" == "x86_64" ] && install -Dm755 linux64/mprime ${pkgdir}/usr/bin/mprime
  [ "${CARCH}" == "i686" ]   && install -Dm755 linux/mprime ${pkgdir}/usr/bin/mprime

  # license and documentation
  install -Dm644 license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
  install -Dm644 readme.txt ${pkgdir}/usr/share/doc/${pkgname}/readme.txt
  install -Dm644 stress.txt ${pkgdir}/usr/share/doc/${pkgname}/stress.txt
  install -Dm644 undoc.txt ${pkgdir}/usr/share/doc/${pkgname}/undoc.txt
  install -Dm644 whatsnew.txt ${pkgdir}/usr/share/doc/${pkgname}/whatsnew.txt
}
