# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributer: xyne <xyne AT archlinux DOT ca>
# Contributer: TryA -  https://bbs.archlinux.org/viewtopic.php?id=114996

pkgname=mprime
pkgver=279
pkgrel=5
pkgdesc="A GIMPS, distributed computing project client, dedicated to finding Mersenne primes."
arch=('x86_64')
url="http://www.mersenne.org"
license=('custom')
depends=('curl')
source=("http://www.mersenneforum.org/gimps/p95v${pkgver}.source.zip")
#"ftp://mersenne.org/gimps/p95v${pkgver}.linux32.tar.gz")
sha256sums=('ad4ed82d71eb86c4da1341ed908d71728f02ab37dc20780dc96ada4d1c1716bb')

build() {
  cd "$srcdir/gwnum"
  case "$CARCH" in
    x86_64)
      make -f make64
      cd ../linux64
    ;;
    i686)
      make -f makefile
      cd ../linux
    ;;
  esac
  sed -e 's/-Wl,-Bdynamic -ldl//' \
      -e 's/-Wl,-Bstatic $(shell pkg-config --static --libs libcurl)/$(shell pkg-config --libs libcurl)/' \
      -e 's/rm -f $(EXE) $(EXE2) $(LINUXOBJS) $(FACTOROBJ)/rm -f $(EXE) $(EXE2) $(LINUXOBJS)/' \
      -i makefile
  make
}

package() {
  cd "$srcdir"

  # executable
  case "$CARCH" in
    x86_64)
      install -Dm755 linux64/mprime "$pkgdir/usr/bin/mprime"
    ;;
    i686)
      install -Dm755 linux/mprime "$pkgdir/usr/bin/mprime"
    ;;
  esac

  # license and documentation
  # x86_64 tarball for 279 includes none of these
  #install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
  #install -Dm644 readme.txt "$pkgdir/usr/share/doc/$pkgname/readme.txt"
  #install -Dm644 stress.txt "$pkgdir/usr/share/doc/$pkgname/stress.txt"
  #install -Dm644 undoc.txt "$pkgdir/usr/share/doc/$pkgname/undoc.txt"
  #install -Dm644 whatsnew.txt "$pkgdir/usr/share/doc/$pkgname/whatsnew.txt"
}
