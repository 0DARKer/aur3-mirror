# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Ronan Rabouin <darkbaboon@gmail.com>

pkgname=prboom-plus
pkgver=2.5.0.8
pkgrel=1
pkgdesc="An enhanced version of the PrBoom Doom engine port"
arch=('i686' 'x86_64')
url="http://prboom-plus.sourceforge.net/"
license=('GPL')
depends=('libgl' 'mesa' 'sdl_image' 'sdl_mixer' 'sdl_net')
makedepends=('make' 'autoconf' 'automake' 'deutex-devel' 'freedoom' 'unzip')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-src.zip \
	$pkgname.png \
	$pkgname.desktop \
	$pkgname-game-server.png \
	$pkgname-game-server.desktop)
md5sums=('2a7e26666df981477f20a199557cb9c2'
         '4e164aeecda9a6b38d87b594645e42f9'
         '67a2ae75edf575971928d5104aa6da1b'
         'dcc3202ad84bafb97388c7261c041c7b'
         '3137a72a4ed216f862178b434b8e509f')

build() {
  cd $pkgname-$pkgver-src

  tr -d '\r' < bootstrap > bootstrap.fixed
  mv -f bootstrap.fixed bootstrap
  ln -sf /usr/share/doom/doom2.wad data/
  sed 's|/games|/bin|g' -i src/Makefile.am
  sed 's|CFLAGS_OPT=.*|:|g' -i configure.ac

  sh bootstrap
  ./configure	--prefix=/usr \
		--with-waddir=/usr/share/doom \
		--disable-dogs

  make || return 1
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir"/usr/share/{applications,pixmaps}
  install -m644 ../prboom-plus{,-game-server}.png "$pkgdir"/usr/share/pixmaps/
  install -m644 ../prboom-plus{,-game-server}.desktop "$pkgdir"/usr/share/applications/
}
