# Maintainer: bzt <unmacaque at gmail.com>

pkgname=dbgl
pkgver=0.78
pkgrel=1
pkgdesc="An open-source, free, multi-platform frontend for DOSBox (no DOSBox binaries included)"
arch=('any')
url="http://members.quicknet.nl/blankendaalr/dbgl/"
license=('GPL')
depends=('java-runtime' 'gtk2')
optdepends=('dosbox: necessary to run DOS games')
install=dbgl.install
source=("http://members.quicknet.nl/blankendaalr/dbgl/download/${pkgname}078_generic.tar.gz"
        dbgl.desktop
        dbgl.sh)
md5sums=('547e6d2f31f47120a63221db51d1cf9a'
         'aaf6ffc4a18ab0e5444cb37422a4e3ee'
         '897eb9e309e2b30820a2be439292b799')

package() {
  cd "$srcdir"
  
  install -dm755 "$pkgdir/usr/share/java/$pkgname"
  install -m755 dbgl "$pkgdir/usr/share/java/$pkgname"
  install -m644 COPYING \
    dbgl.jar \
    dbgl.png \
    "$pkgdir/usr/share/java/$pkgname"
  
  for dir in captures db dosroot export lib profiles templates xsl; do
    mv "$dir" "$pkgdir/usr/share/java/$pkgname"/
  done
  
  install -dm755 "$pkgdir"/usr/bin
  install -m755 dbgl.sh "$pkgdir"/usr/bin/dbgl
  
  install -dm755 "$pkgdir"/usr/share/icons/hicolor/256x256/apps
  install -m644 dbgl.png "$pkgdir"/usr/share/icons/hicolor/256x256/apps
  
  install -dm755 "$pkgdir"/usr/share/applications
  install -m644 dbgl.desktop "$pkgdir"/usr/share/applications
  
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
