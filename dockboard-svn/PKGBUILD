# Contributor: cdude <cdude996@hotmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

arch=('i686' 'x86_64')
pkgname=dockboard-svn
pkgver=152
pkgrel=1
pkgdesc="Outline editor for authors organizing books, articles, and other published works"
url="http://dockboard.sourceforge.net/"
license=('GPL3')
depends=('libglade' 'enchant' 'aiksaurus')
makedepends=('subversion')
conflicts=('dockboard')
provides=('dockboard')
source=("dockboard::svn://svn.code.sf.net/p/dockboard/code/trunk")
md5sums=('SKIP')
_svnmod=dockboard

pkgver() {
  cd "$srcdir/${_svnmod}"
  local ver="$(svnversion)"
  printf "%s" "${ver//[[:alpha:]]}"
}

build() {
  cd "$srcdir/${_svnmod}"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/${_svnmod}"
  make DESTDIR=$pkgdir install
}
