# Original author: Sven-Hendrik Haase (svenstaro) <sh@lutzhaase.com>
# Maintainer: Zack Buhman (buhman) <zack@buhman.org>
# Contributor: William Giokas (KaiSforza) <1007380@gmail.com>

pkgname=pypy-hg
pkgver=63540
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://codespeak.net/pypy/"
arch=('i686' 'x86_64')
depends=('libffi')
provides=('python2' 'pypy')
options=(!buildflags)
makedepends=('python2' 'mercurial')
conflicts=('pypy')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module')
license=('custom:MIT')

# Why use python2 or a previous version of pypy to do the translation when we
# could just as easily use the upstream binary?
source=('hg+https://bitbucket.org/pypy/pypy' 
        'http://bitbucket.org/pypy/pypy/downloads/pypy-2.0-beta2-linux64-libc2.15.tar.bz2')
md5sums=('SKIP'
         '99f062eb516d8b6b5614f2350a65adab')

pkgver() {
  cd "${srcdir}"/pypy
  echo "$(hg id -n)"
}

build() {
  cd "${srcdir}/pypy/pypy/goal"

  export CFLAGS="-O1"
  export CXXFLAGS="-O1"

  "${srcdir}/pypy-2.0-beta2/bin/pypy" ../../rpython/bin/rpython -Ojit targetpypystandalone
}

package() {
  cd "${srcdir}/pypy/pypy/goal"

  install -Dm755 pypy-c "${pkgdir}/opt/pypy/pypy-c"
  mkdir -p "${pkgdir}/opt/pypy/{lib-python,pypy}"

  cd "${srcdir}/pypy"
  cp -r lib-python/2.7 "${pkgdir}/opt/pypy/lib-python/"
  cp -r lib_pypy "${pkgdir}/opt/pypy/"
  cp -r include "${pkgdir}/opt/pypy/"

  mkdir -p "${pkgdir}/usr/bin"
  ln -s /opt/pypy/pypy-c "${pkgdir}/usr/bin/pypy"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/pypy/LICENSE"
}
# vim: ts=2 sw=2 et:
