# Maintainer: Marti Raudsepp <marti@juffo.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=pypy-hg
pkgver=39523
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://codespeak.net/pypy/"
arch=('i686' 'x86_64')
depends=('python2' 'libffi')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module'
            'pypy: faster build with PyPy itself')
makedepends=('mercurial' 'subversion')
conflicts=('pypy')
provides=('pypy')
license=('MIT')
#install=('')
source=()
md5sums=()

_hgroot="http://bitbucket.org/pypy"
_hgrepo="pypy"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_hgrepo ] ; then
    cd $_hgrepo
    hg pull -u
    msg "The local files are updated."
  else
    hg clone $_hgroot $_hgrepo
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_hgrepo-build"
  hg -R "$srcdir/$_hgrepo" archive "$srcdir/$_hgrepo-build"
  cd "$srcdir/$_hgrepo-build"

  #cf="`pkg-config --cflags libffi`" || return 1
  #sed "s|cflags = \[|cflags = \[\'${cf/  /}\', |g" -i pypy/translator/platform/linux.py || return 1

  msg "Starting make."
  cd pypy/translator/goal
  if which pypy 2>/dev/null; then
    pypy translate.py -Ojit targetpypystandalone.py
  else
    python2 translate.py -Ojit targetpypystandalone.py
  fi

  install -Dm755 pypy-c ${pkgdir}/opt/pypy/pypy-c
  mkdir -p ${pkgdir}/opt/pypy/{lib-python,pypy}
  cp -r ${srcdir}/pypy-build/lib-python/2.5.2 ${pkgdir}/opt/pypy/lib-python/
  cp -r ${srcdir}/pypy-build/lib-python/modified-2.5.2 ${pkgdir}/opt/pypy/lib-python/
  cp -r ${srcdir}/pypy-build/lib_pypy ${pkgdir}/opt/pypy/
  mkdir -p ${pkgdir}/usr/bin/
  ln -s /opt/pypy/pypy-c ${pkgdir}/usr/bin/pypy
}
# vim: ts=2 sw=2 et:
