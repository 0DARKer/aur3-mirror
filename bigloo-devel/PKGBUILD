# Maintainer: Taylor Venable <taylor@metasyntax.net>

_pkgname='bigloo'
pkgname="${_pkgname}-devel"
_pkgver='4.1a-alpha'
_pkgsuffix='05Dec13'
pkgver="${_pkgver/-/_}"
pkgrel=6
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
license=('GPL' 'LGPL')
depends=('openssl' 'sqlite3' 'avahi' 'alsa-lib')
makedepends=('java-environment' 'zip' 'emacs')
optdepends=('emacs: for bee')
provides=('bigloo=4.1a')
conflicts=('bigloo')
options=('!makeflags')
install=bigloo.install
source=("ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${_pkgname}${_pkgver}${_pkgsuffix}.tar.gz"
        'satisfy-ldconfig.sh')
md5sums=('7174eae35e1b161a87d4632224a82551'
         'c253eb5651c81204f6c16b89c3c2cb6a')

build() {
  cd "${srcdir}/${_pkgname}${_pkgver%-*}"
  ./configure --prefix=/usr \
    --enable-ssl \
    --enable-sqlite \
    --disable-gstreamer \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --jvm=yes --bee=full
  make build
}

package() {
  cd "${srcdir}/${_pkgname}${_pkgver%-*}"
  make DESTDIR="${pkgdir}" install
  make -C manuals DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}/usr/lib/${_pkgname}/${_pkgver%-*}"/*.a

  # Slake ldconfig's thirst for symlinks.
  find "${pkgdir}/usr/lib/${_pkgname}/${_pkgver%-*}" -type f -name '*_es-*.so' \
    -exec "${srcdir}/satisfy-ldconfig.sh" '{}' \;
}
