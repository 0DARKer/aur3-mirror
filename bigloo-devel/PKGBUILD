# Contributor: Taylor Venable <taylor@metasyntax.net>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

_pkgname='bigloo'
pkgname="${_pkgname}-devel"
epoch=1
_pkgver='4.2a'
_pkgsuffix='alpha16Jun14'
pkgver="${_pkgver}_${_pkgsuffix}"
pkgrel=1
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
license=('GPL' 'LGPL')
depends=('openssl' 'sqlite3' 'avahi' 'libunistring' 'gmp' 'gstreamer0.10-base' 'mpg123' 'libuv' 'flac')
makedepends=('java-environment' 'zip' 'emacs')
optdepends=('emacs: for bee'
'java-environment: for compiling into jvm')
provides=('bigloo=4.2a')
conflicts=('bigloo')
options=('!makeflags')
install=bigloo.install
source=("ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${_pkgname}${_pkgver}-${_pkgsuffix}.tar.gz"
        'satisfy-ldconfig.sh')
md5sums=('e58085f623519ce7544d09a7d6f8bd94'
         'c253eb5651c81204f6c16b89c3c2cb6a')
prepare() {
  cd "${srcdir}/${_pkgname}${_pkgver}"
  sed -ri 's/ ?-Wl,-rpath=[^"]+"/"/' configure
}

build() {
  cd "${srcdir}/${_pkgname}${_pkgver}"
  ./configure --prefix=/usr \
    --enable-ssl \
    --enable-sqlite \
    --enable-gstreamer \
    --enable-libuv \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --jvm=yes --bee=full --strip=no 

  make build
}

package() {
  cd "${srcdir}/${_pkgname}${_pkgver}"
  make DESTDIR="${pkgdir}" install
  make -C manuals DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}/usr/lib/${_pkgname}/${_pkgver%-*}"/*.a

  # Slake ldconfig's thirst for symlinks.
  find "${pkgdir}/usr/lib/${_pkgname}/${_pkgver%-*}" -type f -name '*_es-*.so' \
    -exec "${srcdir}/satisfy-ldconfig.sh" '{}' \;
}
