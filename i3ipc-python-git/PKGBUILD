pkgname=('i3ipc-python-git')
_srcname='i3ipc-python'
pkgver=r17.e8efde9
pkgrel=1
pkgdesc='An improved Python library to control i3wm'
arch=('i686' 'x86_64')
url='https://github.com/acrisci/i3ipc-python'
license=('GPL3')

depends=('i3ipc-glib-git' 'python-gobject')
makedepends=('python')
provides=('i3ipc-python')
conflicts=('i3ipc-python')

source=("$_srcname::git+https://github.com/acrisci/i3ipc-python.git")
sha256sums=('SKIP')

pkgver() {
	cd "$_srcname"
	printf 'r%s.%s\n' "$(git rev-list HEAD | wc -l)" "$(git describe --always)"
}

build() {
	cd "$_srcname"
	./autogen.sh --prefix=/usr
	make
}

package() {
	cd "$_srcname"
	make DESTDIR="$pkgdir" install
}
