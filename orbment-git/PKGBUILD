pkgname=orbment-git
pkgver=r323.18aa59b
pkgrel=2

pkgdesc='A dynamic window manager and wayland compositor based on the wlc.'
url='https://github.com/Cloudef/loliwm'
arch=('i686' 'x86_64')
license=('GPL')

depends=('wlc')
makedepends=('git' 'cmake')
optdepends=('weston: To test weston clients in loliwm.'
            'bemenu: Dynamic menu similar to dmenu for launching programs.'
            'libpng: PNG screenshot compression.')

provides=('orbment')
conflicts=('orbment')

install='orbment-git.install'

source=('git://github.com/Cloudef/loliwm'
        'git://github.com/Cloudef/chck')

md5sums=('SKIP' 'SKIP')

# Once this software becomes more stable these requirements will be dropped but
# for the time being I recommend leaving them enabled so you may contribute
# useful backtraces to the developer and help remove any errors.
options=('debug' '!strip')

pkgver() {
    cd loliwm
    printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd loliwm
    git submodule init
    git config submodule.chck.url "$srcdir"/chck
    git submodule update lib/chck/src
}

build() {
    cd loliwm
    cmake -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd loliwm
    make DESTDIR="$pkgdir" install
}
