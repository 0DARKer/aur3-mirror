# Maintainer: Tucos <baspape@gmail.com>
# Contributor: Hubert Grzeskowiak <arch at nemesis13 de>

pkgname=panda3d
pkgver=1.7.2
pkgrel=3
pkgdesc="A 3D game engine with Python bindings. SDK package."
url="http://www.panda3d.org"
arch=('i686' 'x86_64')
license=('BSD')
depends=('desktop-file-utils' 'shared-mime-info')
makedepends=('python2')
# NOTICE: please read http://www.panda3d.org/manual/index.php/Dependencies for more information
# optdepends you want your package to support, need to be installed during compiletime!
# you don't need to change anything in the pkgbuild to get support; makepanda automatically detects available dependencies
optdepends=(# Pretty much required
            'xorg-server: X11 support'
            'libgl: OpenGL support for X11'
            # Recommended
            'python2: python bindings'
            'openssl: Provides some networking and encryption support'
            'ffmpeg: Required to load and play video textures'
            'libjpeg: Required to read and write jpeg images'
            'libpng: Required to read and write png images'
            'freetype2: Required to use dynamic fonts (such as TTF fonts)'
            'gtk2: PStats analysis and debugging tool'
            'libtiff: Required to read and write tiff images'
            'nvidia-cg-toolkit: shader support'
            'ode: Support for the physics engine'
            'openal: OpenAL audio'
            'libxxf86dga: Relative mouse mode'
            # Optional
            'fmodex: FMod audio'
            'libsquish: DXT support (AUR)'
            'libxcursor: Custom cursor icons'
            'libxrandr: Resolution switching'
            'fftw: Lossy animation compression in bam files'
            'opencv: alternative to ffmpeg for video texture support'
            'atroolkit: library for augmented reality (unavailable)'
            'fcollada: used for dae2egg and for loading dae files directly into Panda (unavailable)'
            # ARM stuff, not really applicable, stated for completeness
            'libgles: OpenGL ES support'
            'libegl: GLX for OpenGL ES'
            )
install='panda3d.install'
source=("$url/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz" 'v4l.patch' 'panda3d.install')
md5sums=('4278be15f8f0e40ba8a7d55742ef00ab' '598cf383dd9496c6091b64d7831750de' '781da785acb14d547624b505ef01e064')

build() {
  cd $srcdir/$pkgname-$pkgver

  (cd panda/src/vision && patch -N <$srcdir/v4l.patch)

  python2 makepanda/makepanda.py --everything
}

package() {
  cd $srcdir/$pkgname-$pkgver
  python2 makepanda/installpanda.py --prefix=/usr --destdir=$pkgdir
  rm $pkgdir/usr/bin/bin2c # supposedly conflicts with something from collada; not found with pkgfile
  install -D -m644 $srcdir/$pkgname-$pkgver/doc/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  if [ "$CARCH" == "x86_64" ]; then
    mv $pkgdir/usr/lib64/panda3d $pkgdir/usr/lib/
    rmdir $pkgdir/usr/lib64
    sed -i 's/lib64/lib/' $pkgdir/usr/lib/python2.7/site-packages/panda3d.pth
    sed -i 's/lib64/lib/' $pkgdir/etc/ld.so.conf.d/panda3d.conf
  fi
}
