# Maintainer: Tucos <baspape@gmail.com>
# Contributor: Hubert Grzeskowiak <arch at nemesis13 de>

pkgname=panda3d
pkgver=1.7.2
pkgrel=2
pkgdesc="A 3D game engine with Python bindings. SDK package."
arch=('i686' 'x86_64')
url="http://www.panda3d.org"
license=('BSD')
makedepends=('python2')
install='panda3d.install'
depends=('python2' 'nvidia-cg-toolkit' 'ode' 'openal' 'fmodex' 'gtk2' 'libgles' 'libegl' 'libxxf86dga' 'desktop-file-utils' 'ffmpeg')
source=("$url/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz" 'v4l.patch' 'panda3d.install')
md5sums=('4278be15f8f0e40ba8a7d55742ef00ab' '06cda0bc00bc1a07f035216d5bb91528' '781da785acb14d547624b505ef01e064')

#-----------------------------------------------------------------------------
#
# You can safely remove some of the above dependencies if you're sure you won't
# need the features.
#
# WARNING: All make-time dependencies are also expected to be installed
#          afterwards at run-time. This is why it's useless to post-install
#          dependencies and why you can break your panda installation by
#          uninstalling or updating those afterwards.
#
#-----------------------------------------------------------------------------
# DEPENDENCY: FEATURE
#
# python2: needed for building script and python bindings
# xorg-server: basic display stuff. required
# openssl: required
# freetype2: font rendering
# libpng, libjpeg, libtiff: image libs
# ffmpeg: general audio and video
# nvidia-cg-toolkit: shader system
# ode: Open Dynamics Engine - physics
# openal: OpenAL audio
# fmodex: FMod audio
# gtk: PStats analysis and debugging tool
# libsquish: DXT file support
# libgles: Mesa stuff
# libegl: Mesa stuff
#-----------------------------------------------------------------------------


build() {
  cd $srcdir/$pkgname-$pkgver

  (cd panda/src/vision && patch -N <$srcdir/v4l.patch)

  python2 makepanda/makepanda.py --everything
}

package() {
  cd $srcdir/$pkgname-$pkgver
  python2 makepanda/installpanda.py --prefix=/usr --destdir=$pkgdir
  rm $pkgdir/usr/bin/bin2c # supposedly conflicts with something from collada; not found with pkgfile
  install -D -m644 $srcdir/$pkgname-$pkgver/doc/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  if [ "$CARCH" == "x86_64" ]; then
    mv $pkgdir/usr/lib64/panda3d $pkgdir/usr/lib/
    rmdir $pkgdir/usr/lib64
    sed -i 's/lib64/lib/' $pkgdir/usr/lib/python2.7/site-packages/panda3d.pth
    sed -i 's/lib64/lib/' $pkgdir/etc/ld.so.conf.d/panda3d.conf
  fi
}
