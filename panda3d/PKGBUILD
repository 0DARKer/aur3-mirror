# Maintainer: Hubert Grzeskowiak <arch at nemesis13 de>
# Contributor: Tucos <baspape@gmail.com>

pkgname=panda3d
pkgver=1.7.2
pkgrel=5
pkgdesc="A 3D game engine with Python bindings. SDK package."
url="http://www.panda3d.org"
arch=('i686' 'x86_64')
license=('BSD')
depends=('desktop-file-utils' 'shared-mime-info' 'xorg-server' 'libgl' 'libegl' 'libgles' 'python2' 'openssl' 'ffmpeg' 'libjpeg' 'libpng' 'libtiff' 'freetype2' 'gtk2' 'nvidia-cg-toolkit' 'openal' 'libxrandr' 'libxcursor' 'libxxf86dga' 'ode' 'bullet' '')
makedepends=('python2')

# NOTICE: please read http://www.panda3d.org/manual/index.php/Dependencies for more information
# optdepends you want your package to support, need to be installed during compiletime!
# you don't need to change anything in the pkgbuild to get support; makepanda automatically detects available dependencies
optdepends=('fmodex: FMod audio'
            'libsquish: DXT support (AUR)'
            'artoolkit: library for augmented reality (AUR)'
            'opencv: alternative to ffmpeg for video texture support (Does not work, use -cvs)'
            'fftw: Lossy animation compression in bam files (Does not work)'
            'fcollada: used for dae2egg and for loading dae files directly into Panda (unavailable)'
            'vrpn: support for virtual reality trackers (unavailable)'
            )

install='panda3d.install'
source=("$url/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz" 'v4l.patch' 'panda3d.install')
md5sums=('4278be15f8f0e40ba8a7d55742ef00ab' '598cf383dd9496c6091b64d7831750de' '781da785acb14d547624b505ef01e064')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # latest kernel removed v4l; this patch removes v4l1 support from panda
  # the changes in the patch are equal to the relevant changes made in CVS
  (cd panda/src/vision && patch -N <"$srcdir/v4l.patch")

  python2 makepanda/makepanda.py --everything
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 makepanda/installpanda.py --prefix=/usr --destdir="$pkgdir"
  #rm "$pkgdir/usr/bin/bin2c" # uncomment if you get integrity error
  install -D -m644 "$srcdir/$pkgname-$pkgver/doc/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  if [ "$CARCH" == "x86_64" ]; then
    mv "$pkgdir/usr/lib64/panda3d" "$pkgdir/usr/lib/"
    rmdir "$pkgdir/usr/lib64"
    sed -i 's/lib64/lib/' "$pkgdir/usr/lib/python2.7/site-packages/panda3d.pth"
    sed -i 's/lib64/lib/' "$pkgdir/etc/ld.so.conf.d/panda3d.conf"
  fi
}
