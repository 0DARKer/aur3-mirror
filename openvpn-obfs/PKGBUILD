# Maintainer: Sascha Weaver <wzyboies@gmail.com>

pkgname=openvpn-obfs
_pkgname=openvpn
pkgver=2.3.1
pkgrel=1
pkgdesc="OpenVPN binary with obfs patch"
arch=(i686 x86_64)
url="https://www.gsea.com.cn/blog/topic/traffic-obsfucate-patch-for-openvpn/"
depends=('openssl' 'lzo2' 'iproute2' 'openvpn>=2.3.0')
makedepends=('systemd')
license=('custom')
source=(http://swupdate.openvpn.net/community/releases/openvpn-${pkgver}.tar.gz
        OpenVPN-2.3.0-obfs.patch
        openvpn-2.3.0-fix-systemd-ask-password-path.patch)
md5sums=('57a3b64597fc37b7842a3fde354d8bbe'
         '4f57dd4cd140f1cce5fbbe2d92893ca0'
         'e1bd1523e38745e948c710db1a330bb1')
options=(!libtool)

build() {
  cd $srcdir/$_pkgname-$pkgver
  patch -p0 -i $srcdir/OpenVPN-2.3.0-obfs.patch
  patch -p0 -i $srcdir/openvpn-2.3.0-fix-systemd-ask-password-path.patch
  CFLAGS="$CFLAGS -DPLUGIN_LIBDIR=\\\"/usr/lib/openvpn\\\"" ./configure \
    --prefix=/usr \
    --enable-password-save \
    --mandir=/usr/share/man \
    --enable-iproute2 \
    --enable-systemd
  make
}

package() {
  cd $srcdir/$_pkgname-$pkgver
  make DESTDIR=$pkgdir install
  mv $pkgdir/usr/sbin/openvpn{,-obfs}
  rm -r $pkgdir/usr/{share,lib,include}
}
