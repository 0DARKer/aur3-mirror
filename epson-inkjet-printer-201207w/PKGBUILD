# Contributor: Daniel Del Pino <evilside () gmail () com>

pkgname=epson-inkjet-printer-201207w
_pkgname_filter=epson-inkjet-printer-filter
pkgver=1.0.0
pkgrel=1
pkgdesc="Epson printer driver (L110, L210, L300, L350, L355, L550, L555 Series)"
arch=('i686' 'x86_64')
url="http://avasys.jp/eng/linux_driver/download/lsb/epson-inkjet/escp/"
license=('GPL' 'LGPL' 'custom:AVASYS Public Licence')
depends=('cups')
makedepends=('rpmextract' 'libtool' 'make' 'automake' 'autoconf')
source=('http://a1227.g.akamai.net/f/1227/40484/1d/download.ebz.epson.net/dsc/f/01/00/01/87/87/4bb10f016a8e2e3ec39d55ff72b636c0a340365c/epson-inkjet-printer-201207w-1.0.0-1lsb3.2.src.rpm')
build() {
  cd "$startdir/src"
  rpmextract.sh $pkgname-$pkgver-1lsb3.2.src.rpm || return 1
  tar xzf $pkgname-$pkgver.tar.gz || return 1
  FILTER_FILE=`ls $_pkgname_filter*.tar.gz`
  tar xzf $FILTER_FILE || return 1

  cd "${FILTER_FILE%.tar.gz}"
  aclocal
  libtoolize
  chmod +x configure
  # if you have runtime problems: add "--enable-debug" and look into /tmp/epson-inkjet-printer-filter.txt
  ./configure LDFLAGS="$LDFLAGS -Wl,--no-as-needed" --prefix=/opt/$pkgname || return 1
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d "$pkgdir/opt/$pkgname/"
  if [ "$CARCH" = "x86_64" ]; then
     cp -r lib64 "$pkgdir/opt/$pkgname/"
  else
     cp -r lib "$pkgdir/opt/$pkgname/"
  fi
  cp -r resource "$pkgdir/opt/$pkgname/"

  if [ -e "watermark" ]; then
      cp -r watermark "$pkgdir/opt/$pkgname/"
  fi
  install -d "$pkgdir/usr/share/cups/model/$pkgname"
  install -m 644 ppds/* "$pkgdir/usr/share/cups/model/$pkgname"

  cd "$srcdir"
  FILTER_FILE=`ls $_pkgname_filter*.tar.gz`
  cd "${FILTER_FILE%.tar.gz}"
  install -d "$pkgdir/opt/$pkgname/cups/lib/filter/"
  install -m 755 src/epson_inkjet_printer_filter "$pkgdir/opt/$pkgname/cups/lib/filter/epson_inkjet_printer_filter"
}

md5sums=('d4eda3ac0f6e3e96d42492c315a47a6c')
