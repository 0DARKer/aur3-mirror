# $Id: PKGBUILD 117818 2014-08-23 15:48:14Z anatolik $
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

# Build order: avr-binutils -> avr-gcc -> avr-libc

pkgname=avr-gdc
_pkgname=gcc
pkgver=4.9.2
pkgrel=1
pkgdesc="The GNU AVR Compiler Collection"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL' 'custom')
url="http://gcc.gnu.org/"
depends=('avr-binutils>=2.24' 'cloog' 'ppl' 'gcc-libs>=4.8.0' 'libmpc')
optdepends=('avr-libc: Standard C library for Atmel AVR development')
provides=("gcc-avr=$pkgver")
replaces=('gcc-avr')
options=('staticlibs' '!emptydirs' '!strip' 'debug')
source=(
    "ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2"
    "gdc::git://github.com/D-Programming-microD/GDC.git#branch=microD-4.9"
)

_basedir=${_pkgname}-${pkgver}

prepare()
{
    cd $srcdir/gdc
    ./setup-gcc.sh ../gcc-$pkgver
}

build() {
    # default CFLAGS lead to issues later on when configure 
    # calls avr-gcc with -march set.
    export CFLAGS="-O2 -pipe"
    export CXXFLAGS="-O2 -pipe"

    cd ${srcdir}/${_basedir} 

    # Do not install libiberty
    sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

    # https://bugs.archlinux.org/task/34629
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" gcc/configure

    echo ${pkgver} > gcc/BASE-VER

    cd ${srcdir}
    mkdir -p gcc-build
    cd gcc-build

    # --disable-linker-build-id: https://bugs.archlinux.org/task/34902

    ${srcdir}/${_basedir}/configure \
                --disable-cloog-version-check \
                --disable-install-libiberty \
                --disable-libssp \
                --disable-libphobos \
                --disable-libstdcxx-pch \
                --disable-libunwind-exceptions \
                --disable-linker-build-id \
                --disable-nls \
                --disable-werror \
                --enable-__cxa_atexit \
                --enable-checking \
                --enable-clocale=gnu \
                --enable-cloog-backend=isl \
                --enable-gnu-unique-object \
                --enable-gold \
                --enable-languages=c,c++,d \
                --enable-ld=default \
                --enable-lto \
                --enable-plugin \
                --enable-shared \
                --infodir=/usr/share/info \
                --libdir=/usr/lib \
                --libexecdir=/usr/lib \
                --mandir=/usr/share/man \
                --prefix=/usr \
                --target=avr \
                --with-as=/usr/bin/avr-as \
                --with-gnu-as \
                --with-gnu-ld \
                --with-ld=/usr/bin/avr-ld \
                --with-plugin-ld=ld.gold \
                --with-system-zlib

    make
}

package() {
    install -D -m755 $srcdir/gcc-build/gcc/gdc $pkgdir/usr/bin/avr-gdc
    install -D -m755 $srcdir/gcc-build/gcc/cc1d $pkgdir/usr/lib/gcc/avr/${pkgver}/cc1d
}

md5sums=('4df8ee253b7f3863ad0b86359cd39c43'
         'SKIP')
