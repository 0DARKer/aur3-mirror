# Maintainer: Terry Mitchell <tcmdvm@charter.net>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=kompozer
pkgver=0.8b3
pkgrel=4
pkgdesc="A Dreamweaver style WYSIWYG web editor; Nvu unofficial bug-fix release"
arch=('x86_64' 'i686')
license=('GPL')
url="http://www.kompozer.net"
depends=('gtk2' 'glib2' 'pango' 'nss' 'libxt' 'libidl2' 'gnome-vfs')
makedepends=('zip' 'pkgconfig')
options=('!makeflags')
install=(kompozer.install)
source=("http://downloads.sourceforge.net/kompozer/$pkgname-$pkgver-src.tar.bz2"
        "kompozer.desktop"
        "kompozer-png14.patch"
        "mozconfig"
        "https://api.opensuse.org/public/source/mozilla:Factory/kompozer/gcc46_default_ctors.patch")
md5sums=('cd4664ecda908666b19ef0607010c627'
         '783ec7cb74d5154b2f9ca54755c363cb'
         'd5b77df4723cf27131055ed8cecbc4ab'
         '62b8e1d36559c18d638c17c01c4ebde2'
         'ad3ec5cbc6677dd712b140148398f58f')

build() {
  cd "$srcdir/mozilla"
  
  patch -Np0 -i $srcdir/kompozer-png14.patch
  patch -Np0 -i $srcdir/gcc46_default_ctors.patch

  cp ../mozconfig .mozconfig
  cp security/coreconf/Linux2.6.mk security/coreconf/Linux3.0.mk

  make -f client.mk build
}

package() {
  cd "$srcdir/obj-kompozer"

  make DESTDIR=$pkgdir install
  install -m755 -dD $pkgdir/usr/share/applications
  install -m644  $srcdir/kompozer.desktop $pkgdir/usr/share/applications/
}
