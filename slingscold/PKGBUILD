# Maintainer: Zavon <zavon at zavon .org>

pkgname=slingscold
pkgver=1.0.2
pkgrel=2
pkgdesc="A lighter launcher"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/slingscold/"
license=('GPL3')
depends=('libunique' 'libgee06' 'gnome-menus2' 'libwnck' 'gtk2' 'cairo')
makedepends=('cmake' 'vala')
source=("http://ppa.launchpad.net/noobslab/apps/ubuntu/pool/main/s/slingscold-launcher/slingscold-launcher_1.0.2p.orig.tar.gz"
        '0001-Add-missing-lm-dep.patch')
sha256sums=('e3f23ba7ff5a4b06baa419e584729fae001978c89bebbb75767fa5859cf0655a'
         '6b1c97fe7863da5d7be000b3f139cc974ea86654c2b210be6eb1b1ac070a9a7c')

prepare() {
  cd "$srcdir"
  rm -rf build && mkdir build

  cd "$srcdir/slingscold-launcher_1.0.2"
  patch -p1 < "$srcdir"/0001-Add-missing-lm-dep.patch
}

build() {
  cd "$srcdir"/build
  cmake ../"slingscold-launcher_1.0.2" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
}
