#Maintainer: Baptiste Jonglez <zerstorer at free dot fr>
pkgname=lib32-yaml-cpp
pkgver=0.2.7
pkgrel=2
pkgdesc="YAML parser and emitter in C++, written around the YAML 1.2 spec (32-bit library)"
license=('MIT')
arch=('x86_64')
url="http://code.google.com/p/yaml-cpp/"
makedepends=('cmake' 'gcc-multilib')
source=("http://yaml-cpp.googlecode.com/files/yaml-cpp-${pkgver}.tar.gz")
md5sums=('6878e14bad90c69a8f2caca273eb24c2')
options=('!libtool')

build() {

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  cd "${srcdir}/yaml-cpp-${pkgver}"

  cmake -D CMAKE_INSTALL_PREFIX=/usr \
    -D LIB_SUFFIX=32 \
    -D LIB_INSTALL_DIR=/usr/lib32 \
    .
  make

}

package () {  
  cd "${srcdir}/yaml-cpp-${pkgver}"

  make install DESTDIR="${pkgdir}"

  rm -rf "${pkgdir}/usr/include"
}

