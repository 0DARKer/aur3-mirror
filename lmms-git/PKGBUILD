# Maintainer: Xyne
# Contributor: Scott Garrett <Wintervenom@archlinux.us>
# Contributor: Thomas Dziedzic

_pkgname=lmms
pkgname=lmms-git
pkgver=1352672617
pkgrel=1
pkgdesc='The Linux MultiMedia Studio.'
url='http://lmms.sourceforge.net'
arch=('i686' 'x86_64')
license=('GPL')
depends=('qt4' 'sdl_sound' 'jack' 'libsamplerate' 'fluidsynth' 'zynaddsubfx' 'portaudio' 'libxft' 'libxinerama' 'shared-mime-info')
optdepends=('wine: VST support (experimental)'
            'fftw: SpectrumAnalyzer plugin' )
makedepends=('git' 'cmake' 'ladspa' 'raptor' 'rasqal' 'libxft' 'freetype2' 'redland')
provides=('lmms')
conflicts=('lmms')

source=('git://lmms.git.sf.net/gitroot/lmms/lmms'
        'freetype2.patch')
sha512sums=('SKIP'
            'c09943b8c167ee78398966b2cb7f9f49729eb5a66bec0e2ffe30ad8cfe6d5617a967fb5ed91b10e2f5d5be40e74f7ed8c946d49fdf83bee4bf4e3dedd291c449')

install="$pkgname".install

pkgver() {
  cd -- "$srcdir/$_pkgname"
  git log -n1 --pretty=format:%ct
}

prepare() {
  cd -- "$srcdir/$_pkgname"
  patch plugins/zynaddsubfx/fltk/CMakeLists.txt < "$srcdir/freetype2.patch"
}

build() {
  cd -- "$srcdir/$_pkgname"
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make #-j1
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
