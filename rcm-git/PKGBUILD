# Maintainer: Pat Brisbin <pbrisbin@gmail.com>
_gitname='rcm'
pkgname=$_gitname-git
pkgver=0.0.0
pkgrel=3
pkgdesc="rc file (dotfile) management"
arch=('any')
url="http://thoughtbot.github.io/rcm/"
license=('BSD')
conflicts=('rcm')
makedepends=('git' 'ruby-mustache')
source=('git://github.com/thoughtbot/rcm')
md5sums=('SKIP')

pkgver() {
  cd "$_gitname"

  git describe --always | sed 's|-|.|g'
}

prepare() {
  cd "$_gitname"

  ./autogen.sh

  aclocal
  automake --add-missing --copy
  autoconf
}

build() {
  cd "$_gitname"

  ./configure \
     --disable-debug \
     --disable-dependency-tracking \
     --disable-silent-rules \
     --prefix=/usr

  make
}

package() {
  cd "$_gitname"

  make DESTDIR="$pkgdir/" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_gitname/LICENSE"
}

# vim:set ts=2 sw=2 et:
