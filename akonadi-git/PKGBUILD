# Maintainer: Antonio Rojas
# Contributor: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_pkgname=akonadi
pkgname=$_pkgname-git
pkgver=r2761.51ad6cc
pkgrel=1
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('LGPL')
depends=('qt5-base' 'shared-mime-info' 'mariadb')
makedepends=('git' 'extra-cmake-modules' 'postgresql')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=("git://anongit.kde.org/$_pkgname")
install=$_pkgname.install
sha256sums=("SKIP")

pkgver() {
  cd akonadi
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  sed 's|${QT_PLUGINS_DIR}|${CMAKE_INSTALL_PLUGINDIR}|g' -i akonadi/src/qsqlite/CMakeLists.txt
}

build() {
  cd build
  cmake ../akonadi \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DAKONADI_BUILD_TESTS=OFF \
    -DINSTALL_QSQLITE_IN_QT_PREFIX=ON \
    -DCMAKE_INSTALL_PLUGINDIR=lib/qt/plugins
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
