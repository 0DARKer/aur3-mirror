# Maintainer: Jerome Leclanche <jerome.leclanche+arch@gmail.com>

_pkgname=akonadi
pkgname=$_pkgname-git
pkgver=v1.12.1.35.gfccfd1a
pkgrel=1
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=("i686" "x86_64")
url="http://www.kde.org"
license=("LGPL")
depends=("shared-mime-info" "boost-libs" "mariadb" "soprano")
makedepends=("git" "cmake" "automoc4" "boost" "postgresql")
conflicts=("$_pkgname")
provides=("$_pkgname")
# groups=("kde")
source=("git://anongit.kde.org/$_pkgname")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake .. -DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DINSTALL_QSQLITE_IN_QT_PREFIX=TRUE \
		-DQT_QMAKE_EXECUTABLE=qmake-qt4 \
		-DAKONADI_BUILD_TESTS=OFF
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}
