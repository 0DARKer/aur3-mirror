# Contributor: David Spicer <azleifel at googlemail dot com>

_pkgbasename=libjpeg-turbo
pkgname=lib32-libjpeg-turbo
pkgver=1.1.0
pkgrel=2
pkgdesc="A high-speed version of libjpeg for x86 and x86-64 processors which uses SIMD instructions (32-bit)"
arch=('x86_64')
url="http://libjpeg-turbo.virtualgl.org/"
license=('custom:wxWindows Library Licence')
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('nasm')
provides=('lib32-libjpeg=8.3.0')
conflicts=('lib32-libjpeg')
options=('!libtool')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgver/$_pkgbasename-$pkgver.tar.gz)
md5sums=('83e6914a281d649ad289445dc20f9de4')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"

  ./configure --host i686-pc-linux-gnu --prefix=/usr --libdir=/usr/lib32 \
    --mandir=/usr/share/man --disable-static --with-jpeg8 
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

