pkgname=mingw-w64-llvm-libs
pkgver=3.3
pkgrel=3
pkgdesc="Low Level Virtual Machine (mingw-w64)"
arch=(any)
url="http://llvm.org"
license=("custom")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config python2)
depends=(mingw-w64-crt mingw-w64-libffi mingw-w64-libxml2 mingw-w64-zlib)
options=(staticlibs !strip !buildflags)
source=("http://llvm.org/releases/$pkgver/llvm-$pkgver.src.tar.gz"
"http://llvm.org/releases/$pkgver/cfe-$pkgver.src.tar.gz"
"raw_ostream.diff")
sha256sums=('68766b1e70d05a25e2f502e997a3cb3937187a3296595cf6e0977d5cd6727578'
            'b1b55de4ab3a57d3e0331a83e0284610191c77d924e3446498d9113d08dfb996'
            'fdc14dfe6a9473380f08e02854bcf5cd95ea3f7ca84e253e4abea0fa79ad8803')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/llvm-$pkgver.src"
  mv "$srcdir/cfe-$pkgver.src" "tools/clang"
  cd "lib/Support"
  patch -Np0 raw_ostream.cpp < "$srcdir"/raw_ostream.diff
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    export PYTHON="/usr/bin/python2"
    CPPFLAGS+="$(${_arch}-pkg-config --cflags libffi)"
    if [ ${_arch} = "x86_64-w64-mingw32" ]; then
      CPPFLAGS+="-D__USING_SJLJ_EXCEPTIONS__"
    fi
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    CC=${_arch}-gcc CXX=${_arch}-g++ "${srcdir}"/llvm-$pkgver.src/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-cxx11 \
      --enable-optimized \
      --disable-assertions \
      --disable-pthreads \
      --enable-targets=all \
      --enable-bindings=none \
      --enable-libffi \
      --with-c-include-dirs=/usr/${_arch}/include \
      --with-gcc-toolchain=/usr/${_arch} \
      --with-default-sysroot=/usr/${_arch}
    make
  done
}

package() {
	mkdir -p "$pkgdir/usr/bin"
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*gtest*' -o -name 'llvm-lit' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/"{lib/clang,docs,share}
    ln -s "/usr/${_arch}/bin/llvm-config-host" "$pkgdir/usr/bin/${_arch}-llvm-config"
  done
}