pkgname=mingw-w64-llvm-libs
pkgver=3.4.1
pkgrel=3
pkgdesc="Low Level Virtual Machine (mingw-w64)"
arch=(any)
url="http://llvm.org"
license=("custom:University of Illinois/NCSA Open Source License")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config mingw-w64-cmake)
depends=(mingw-w64-crt mingw-w64-libffi mingw-w64-libxml2 mingw-w64-zlib)
options=(staticlibs !strip !buildflags !emptydirs)
source=("http://llvm.org/releases/$pkgver/llvm-$pkgver.src.tar.gz"
"llvm-3.4-provide-cmake-modules.patch")
sha256sums=('7d28bb6eca243a2bb6d65a05743ab915b57958a7770277364e93534b63eef93a'
						'b6bb154d5ec998328e818bb09acfc6229e41367ba45cea7cc5b2dd2a7c835cf5')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
	cd "$srcdir/llvm-$pkgver.src"
	patch -Np0 -i "$srcdir/llvm-3.4-provide-cmake-modules.patch"
}

build() {
	for _arch in ${_architectures}; do
		unset LDFLAGS
		ffiver=`${_arch}-pkg-config --modversion libffi`
		if [ ${_arch} = "x86_64-w64-mingw32" ]; then
			CPPFLAGS+="-D__USING_SJLJ_EXCEPTIONS__"
		fi
		mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		${_arch}-cmake ../llvm-$pkgver.src \
			-DBUILD_SHARED_LIBS=OFF \
			-DCPACK_BINARY_NSIS=OFF \
			-DCPACK_SOURCE_ZIP=OFF \
			-DFFI_INCLUDE_DIR=/usr/${_arch}/lib/libffi-$ffiver/include \
			-DFFI_LIBRARY_DIR=/usr/${_arch}/lib \
			-DLLVM_BUILD_TOOLS=OFF \
			-DLLVM_ENABLE_FFI=ON \
			-DLLVM_INCLUDE_DOCS=OFF \
			-DLLVM_INCLUDE_EXAMPLES=OFF \
			-DLLVM_INCLUDE_TESTS=OFF \
			-DLLVM_INCLUDE_TOOLS=OFF \
			-DLLVM_TABLEGEN=/usr/bin/llvm-tblgen
		make REQUIRES_RTTI=1
	done
}

package() {
	mkdir -p "$pkgdir/usr/bin"
	for _arch in ${_architectures}; do
		cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
		make DESTDIR="$pkgdir" install
		find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*gtest*' -o -name 'llvm-lit' | xargs -rtl1 rm
		find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
		find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
	done
}
