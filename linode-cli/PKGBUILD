# Maintainer: Zack Buhman <zbuhman@linode.com>
# Contributor: Graham Edgecombe <graham@grahamedgecombe.com>
pkgname=linode-cli
pkgver=1.0.0
pkgrel=1
pkgdesc='A simple command-line interface to the Linode platform'
arch=('any')
url='https://github.com/linode/cli'
license=('GPL2')
depends=('perl-try-tiny' 'perl-libwww' 'perl-json' 'perl-crypt-ssleay')
makedepends=('git')
source=("$pkgname::git+https://github.com/linode/cli.git#tag=v1.0.0")
md5sums=('SKIP')
_executables='linode linode-account linode-domain linode-linode linode-stackscript'

pkgver() {
  cd "$pkgname"
  git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//g'
}

build() {
  cd "$pkgname"
  for file in $_executables; do
    in_file=$file
    if [ "$in_file" = "linode" ]; then
      in_file=linode-linode
    fi
    pod2man $in_file | gzip > ${file}.1.gz
  done
}

package() {
  cd "$pkgname"
  for file in $_executables; do
    install -D $file "$pkgdir/usr/bin/$file"
    install -D ${file}.1.gz "$pkgdir/usr/share/man/man1/${file}.1.gz"
  done
  for file in $(find lib -type f -name "*.pm"); do
    install -Dm644 "$file" "$pkgdir/usr/lib/linode-cli/${file/lib\//}"
  done
}
