# Maintainer: Zack Buhman <zbuhman@linode.com>
# Contributor: Graham Edgecombe <graham@grahamedgecombe.com>
pkgname=linode-cli
pkgver=1.0.0
pkgrel=2
pkgdesc='A simple command-line interface to the Linode platform'
arch=('any')
url='https://github.com/linode/cli'
license=('GPL2')
depends=('perl-try-tiny' 'perl-libwww' 'perl-json' 'perl-crypt-ssleay')
makedepends=('git')
source=("$pkgname::git+https://github.com/linode/cli.git#tag=v1.0.0"
        'libdirs.patch')
md5sums=('SKIP'
         '9b748abd483dfbe4e9dfa2d13e1676cd')
pkgver() {
  cd "$pkgname"
  git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
  cd "$pkgname"
  git apply ../libdirs.patch
}

build() {
  cd "$pkgname"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$pkgname"
  make install DESTDIR="$pkgdir"
}
