# Maintainer: Zack Buhman <zbuhman@linode.com>
# Contributor: Graham Edgecombe <graham@grahamedgecombe.com>
pkgname=linode-cli
pkgver=1.3.2
pkgrel=1
pkgdesc='A simple command-line interface to the Linode platform'
url='https://github.com/linode/cli'
license=('GPL2')
arch=('any')
# {ExtUtils::MakeMaker,LWP::UserAgent} are in core_perl
depends=('perl-try-tiny' 'perl-libwww' 'perl-json' 'perl-crypt-ssleay'
         'perl-mozilla-ca' 'perl-webservice-linode')
makedepends=('git')
source=("$pkgname::git+https://github.com/linode/cli.git#tag=v$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$pkgname"
  make install DESTDIR="$pkgdir"
}
