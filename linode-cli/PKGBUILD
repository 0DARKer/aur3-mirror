# Maintainer: Zack Buhman <zbuhman@linode.com>
# Contributor: Graham Edgecombe <graham@grahamedgecombe.com>
pkgname=linode-cli
pkgver=1.1.0
pkgrel=1
pkgdesc='A simple command-line interface to the Linode platform'
arch=('any')
url='https://github.com/linode/cli'
license=('GPL2')
depends=('perl-try-tiny' 'perl-libwww' 'perl-json' 'perl-crypt-ssleay'
         'perl-mozilla-ca' 'perl-webservice-linode>=0.17')
makedepends=('git')
source=("$pkgname::git+https://github.com/linode/cli.git#tag=v$pkgver")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//g'
}

build() {
  cd "$pkgname"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$pkgname"
  make install DESTDIR="$pkgdir"
}
