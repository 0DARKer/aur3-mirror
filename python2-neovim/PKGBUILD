# Maintainer: Patrice Peterson <runiq at archlinux dot us>
# Contributor: lucc <l-m-h@web.de>
# Contributor: wenLiangcan <boxeed at gmail dot com>
# Contributor: astiam <astiamm at gmail dot com>

pkgname=python2-neovim
_pkgname=neovim
pkgver=0.0.34
pkgrel=1
pkgdesc="Python client to neovim"
arch=('any')
url="https://pypi.python.org/pypi/$_pkgname"
license=('Apache')
depends=('python2'
         'python2-trollius'
         'python2-msgpack'
         'python2-greenlet')
makedepends=('python2-setuptools')
optdepends=('python2-click: For GTK GUI'
            'python2-futures: needed for the gtk gui'
            'python2-gobject: needed for the gtk gui')
source=("https://pypi.python.org/packages/source/n/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha256sums=('88d671ea18aa20bdc2858a46219d4d79627751447036a0a985da82410e67613b')

prepare() {
  cd "$_pkgname-$pkgver"
  sed -i 's/pynvim/py2nvim/' setup.py neovim.egg-info/entry_points.txt
}

package() {
  cd "$_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
