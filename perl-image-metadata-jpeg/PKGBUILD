# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Archie <xMickael@ifrance.com>

pkgname=perl-image-metadata-jpeg
_pkgname=Image-MetaData-JPEG
pkgver=0.153
pkgrel=3
pkgdesc="Perl extension for showing/modifying JPEG (meta)data."
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/$_pkgname/"
license=('GPL')
depends=('perl')
source=("http://search.cpan.org/CPAN/authors/id/B/BE/BETTELLI/$_pkgname-$pkgver.tar.gz")
sha512sums=('9d83f3bea6ee1a28320fc924f3742500d2d391b836207b37c08e4063d85c5746b50b2431590b75894e5dfedcc2a149374712db16d0c412ef54e4c1cfe59af96a')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
