# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Archie <xMickael@ifrance.com>

_perlmod=Image-MetaData-JPEG
_modnamespace=Image
pkgname=perl-image-metadata-jpeg
pkgver=0.155
pkgrel=1
pkgdesc="Image::MetaData::JPEG - Perl extension for showing/modifying JPEG (meta)data."
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}/"
license=('GPL2')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('d8c06123a986e9a757d2587a22337658f0fd97605cc8599a44a7baf5141340d167fcc38ca9a7688a018f515c30dbc501cf78843d8fc4ea19c844fe1f0b98345e')

build() {
	cd "${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}/"
}
