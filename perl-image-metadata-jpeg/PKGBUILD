# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Archie <xMickael@ifrance.com>

_perlmod=Image-MetaData-JPEG
_modnamespace=Image
pkgname=perl-image-metadata-jpeg
pkgver=0.156
pkgrel=1
pkgdesc="Image::MetaData::JPEG - Perl extension for showing/modifying JPEG (meta)data."
arch=('any')
url="http://search.cpan.org/dist/${_perlmod}/"
license=('GPL2')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/${_modnamespace}/${_perlmod}-${pkgver}.tar.gz")
sha512sums=('a5903f6e895e09f5b39541900b77d1b040a1746f78f5c3cc4eb8b197c105e5dd662642374e6f3cca57a48f5ee2a3f39b7cbc77349c23e07e996c5fbd6710098d')

build() {
	cd "${_perlmod}-${pkgver}"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "${_perlmod}-${pkgver}"
	make test
}

package() {
	cd "${_perlmod}-${pkgver}"
	make install DESTDIR="${pkgdir}"
}
