# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Archie <xMickael@ifrance.com>

_perlmod=Image-MetaData-JPEG
_modnamespace=Image
pkgname=perl-image-metadata-jpeg
pkgver=0.153
pkgrel=4
pkgdesc="Image::MetaData::JPEG - Perl extension for showing/modifying JPEG (meta)data."
arch=('any')
url="http://search.cpan.org/dist/$_perlmod/"
license=('GPL')
depends=('perl>=5.10.0')
options=('!emptydirs')
source=("http://cpan.perl.org/modules/by-module/$_modnamespace/$_perlmod-$pkgver.tar.gz")
sha512sums=('9d83f3bea6ee1a28320fc924f3742500d2d391b836207b37c08e4063d85c5746b50b2431590b75894e5dfedcc2a149374712db16d0c412ef54e4c1cfe59af96a')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	# Install module in vendor directories.
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

# FIXME: `make test` fails due to errors in a .pod file.
# This is most likely an upstream bug.
# The check() function is commented out so the package can at least be built.
# Use at your own risk, etc.
#
#check() {
#	cd "$srcdir/$_perlmod-$pkgver"
#	make test
#}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	make install DESTDIR="$pkgdir/"
}
