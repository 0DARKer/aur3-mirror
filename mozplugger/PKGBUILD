# Maintainer: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Todd Musall <tmusall@comcast.net>
# Contributor: David Manouchehri <d@32t.ca>

pkgname=mozplugger
pkgver=2.1.4
pkgrel=1
pkgdesc="A Mozilla & Firefox multimedia plugin."
arch=('i686' 'x86_64')
url="http://mozplugger.mozdev.org"
license=('GPL')
depends=('m4' 'libx11')
backup=('etc/mozpluggerrc')
install=mozplugger.install
source=($url/files/mozplugger-$pkgver.tar.gz)
sha256sums=('0aed537ea826bfdd28a5b14e0c45a772df836a939152e13d52e10a1ceb2ce062')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
  #--enable-always-xembed
  # uncomment the preceding line to get mozplugger running in Chromium
  # this will always use Xembed which kills keyboard focus

  make
}

package() {
  cd $pkgname-$pkgver
  # doesn't work anymore
  #make install root="$pkgdir"

  install -Dm644 mozpluggerrc "$pkgdir/etc/mozpluggerrc"
  install -d "$pkgdir/usr/bin/"
  install -m755 mozplugger-{helper,controller,linker,update} "$pkgdir/usr/bin/"
  install -Dm755 mozplugger.so "$pkgdir/usr/lib/mozilla/plugins/mozplugger.so"
  install -Dm644 mozplugger.7 "$pkgdir/usr/share/man/man7/mozplugger.7"
}
