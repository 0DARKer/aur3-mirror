#Maintainer: Raimar Buehmann <raimar _at_ buehmann _dot_ de>

pkgname=eclipse-moreunit
pkgver=3.0.4
pkgrel=1
pkgdesc='JUnit assistent to write more tests in Eclipse'
arch=('any')
url='http://moreunit.sourceforge.net/'
license=('EPL')
depends=('eclipse>=3.4.0')
options=('!strip')
source=('moreunit.zip::https://downloads.sourceforge.net/project/moreunit/moreunit/Version%203.0.4/org.moreunit-3.0.4.zip?r=&ts=1412332245&use_mirror=optimate')
sha256sums=('3f2046a553bb532d44503e56eb92c6da547714a49c78aeb9daed72341031e104')

package() {
  # move everything from first zip folder to current folder
  mv org.${pkgname/eclipse-}-$pkgver/* .
  # remove plug-ins containing sources
  rm plugins/*.source_*
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse
  install -d $_dest
  # extract features (otherwise features are not recognized)
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature} || return 1
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done
  # copy plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}
