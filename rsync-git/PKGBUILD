# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer ([extra] package): Angel Velasquez <angvp@archlinux.org> 
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=rsync-git
pkgver=3.0.3.r532.geec2608
pkgrel=1
pkgdesc="A file transfer program to keep remote files in sync - git checkout"
arch=('i686' 'x86_64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=('acl')
makedepends=('git' 'yodl')
provides=('rsync')
conflicts=('rsync')
backup=('etc/rsyncd.conf' 'etc/xinetd.d/rsync')
source=('git://git.samba.org/rsync.git')

pkgver() {
	cd rsync

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

build() {
	cd rsync

	./prepare-source
	./configure --prefix=/usr \
		--with-included-popt \
		--enable-acl-support \
		--enable-xattr-support
	make
}

package() {
	cd rsync

	make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP')
