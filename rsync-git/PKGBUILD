# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer ([extra] package): Angel Velasquez <angvp@archlinux.org> 
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=rsync-git
pkgver=3.0.3.532.geec2608
pkgrel=1
pkgdesc="A file transfer program to keep remote files in sync - git checkout"
arch=('i686' 'x86_64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=('acl')
makedepends=('git')
provides=('rsync')
conflicts=('rsync')
backup=('etc/rsyncd.conf' 'etc/xinetd.d/rsync')
changelog=ChangeLog
source=('git://git.samba.org/rsync.git'
	'rsyncd.conf' 'rsyncd' 'rsync.xinetd')

pkgver() {
	cd rsync
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd rsync

	./prepare-source
	./configure --prefix=/usr --with-included-popt \
              --enable-acl-support --enable-xattr-support
	make
}

package() {
	cd rsync

	make DESTDIR="${pkgdir}" install
	install -Dm755 ../rsyncd "${pkgdir}/etc/rc.d/rsyncd"
	install -Dm644 ../rsyncd.conf "${pkgdir}/etc/rsyncd.conf"
	install -Dm644 ../rsync.xinetd "${pkgdir}/etc/xinetd.d/rsync"
	install -Dm755 support/rrsync "${pkgdir}/usr/lib/rsync/rrsync"
}

sha256sums=('SKIP'
            '2047fb880ce73ff74a469fa18d8de90661bc8b86a55694ed426c8d3a017a2734'
            'b23c7b8533c86948f6f183d6049c5b487591f34df9c16c44d6e260558b724ccd'
            'da0ec9ce07bf2edafbc8e44020da29a58038b00c3048a22de57017c56318a767')
