# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer ([extra] package): Angel Velasquez <angvp@archlinux.org> 
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=rsync-git
pkgver=20120323
pkgrel=1
pkgdesc="A file transfer program to keep remote files in sync - git checkout"
arch=('i686' 'x86_64')
url="http://samba.anu.edu.au/rsync/"
license=('GPL3')
depends=('acl')
provides=('rsync')
conflicts=('rsync')
backup=('etc/rsyncd.conf' 'etc/xinetd.d/rsync')
changelog=ChangeLog
source=(rsyncd.conf rsyncd rsync.xinetd)

_gitroot="git://git.samba.org/rsync.git"
_gitname="rsync"

build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."

	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "${srcdir}/${_gitname}-build"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

	cd "${srcdir}/${_gitname}-build"

	./prepare-source
	./configure --prefix=/usr --with-included-popt \
              --enable-acl-support --enable-xattr-support
	make
}

package() {
	cd "${srcdir}/${_gitname}-build"
	make DESTDIR="${pkgdir}" install
	install -Dm755 ../rsyncd "${pkgdir}/etc/rc.d/rsyncd"
	install -Dm644 ../rsyncd.conf "${pkgdir}/etc/rsyncd.conf"
	install -Dm644 ../rsync.xinetd "${pkgdir}/etc/xinetd.d/rsync"
	install -Dm755 support/rrsync "${pkgdir}/usr/lib/rsync/rrsync"
}

sha256sums=('2047fb880ce73ff74a469fa18d8de90661bc8b86a55694ed426c8d3a017a2734'
            '08ecba9bb187649d8792214ad3162c361632c6d0d349e8a41a0ad5e6489b28f1'
            'da0ec9ce07bf2edafbc8e44020da29a58038b00c3048a22de57017c56318a767')
