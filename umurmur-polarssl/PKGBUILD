# Maintainer: kleim <kleim8  at yahoo dot fr>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur-polarssl
_pkgsrcname=umurmur
pkgver=0.2.14
pkgrel=1
pkgdesc="A minimalistic Mumble server"
arch=('i686' 'x86_64' 'armv7h')
url="http://code.google.com/p/umurmur/"
license=('custom')
depends=('polarssl' 'libconfig' 'protobuf-c')
conflicts=('umurmur')
source=(
  "http://${_pkgsrcname}.googlecode.com/files/${_pkgsrcname}-${pkgver}.tar.gz"
  "${_pkgsrcname}.service"
)
sha1sums=(
  '7ebaac3b43517662006c7bdc51e44b4f9aecf5cd' 
  '866d8a086bd6920d9be3bad9718aa034fdbbc29d'
)
install="${pkgname}.install"

build() {
  cd "${srcdir}/${_pkgsrcname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --with-ssl=polarssl
  make
}

package() {
  cd "${srcdir}/${_pkgsrcname}-${pkgver}"
  make DESTDIR=${pkgdir} install || return 1
  install -Dm644 'umurmur.conf.example' "${pkgdir}/etc/umurmur/umurmur.conf.example"
  install -Dm644 "${srcdir}/umurmur.service" "${pkgdir}/usr/lib/systemd/system/umurmur.service"
  install -Dm644 'LICENSE' "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
