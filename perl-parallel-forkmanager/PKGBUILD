# Contributor: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-parallel-forkmanager
_prename=Parallel
_pkgname=Parallel-ForkManager
pkgver=0.7.9
pkgrel='1'
pkgdesc="A simple parallel processing fork manager"
arch=(any)
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-extutils-makemaker')
url='http://search.cpan.org/dist/Parallel-ForkManager/'
source=(http://search.cpan.org/CPAN/modules/by-module/${_prename}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('e1bd536dc899807c56ab5777ef2956421206ff4e839ff583ad6358eb1bb4e1b9')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  export PERL_MM_USE_DEFAULT=1

  perl Makefile.PL INSTALLDIRS=vendor
  make
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
