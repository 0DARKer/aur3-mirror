# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-abc
_pkgname=milena_abc
pkgver=0.3.18
pkgrel=1
pkgdesc="Gtk3 gui for audiobook creator from Milena speech synthesizer"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=(milena libwebkit3 gtksourceview3 faac mpg123 libsamplerate curl)
source=(http://tts.polip.com/files/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('49a352b34c8955017b2fe0377f94445b9bd8d95572dfc74da9a1203f4ea99abc')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  ./configure --prefix=/usr

  sed 's|/share/icons/|/share/pixmaps/|g' -i Makefile

  # PS. autorom należy się podziękowanie za poprawiony Makefile (DESTDIR)

  make
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  # :[ To nadal jest wymagane:
  install -dm755 "$pkgdir/usr/"{bin,share/{applications,icons,pixmaps,milena_abc}}

  make DESTDIR="$pkgdir/" install

  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/"{icons,pixmaps}
}

# vim:set ts=2 sw=2 et:
