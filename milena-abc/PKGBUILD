# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-abc
_pkgname=milena_abc
pkgver=0.3.27
pkgrel=1
pkgdesc="Gtk3 gui for audiobook creator from Milena speech synthesizer"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=(milena libwebkit3 gtksourceview3 faac mpg123 libsamplerate curl)
source=(http://tts.polip.com/files/${_pkgname}-${pkgver}.tar.gz
        10-only_glib.h_can_be_included_directly.patch
        20-gtk_deprecated_declarations.patch)
sha256sums=('7c5b512f1305b7946fface5595ace3099ec3b8d2c7cda705274c6d040366de07'
            '714854425892f434444f9812ddd41d8b5c21ba7998661a6b8a1384c5dc277702'
            '7ea58d8580f6257f8f657b02d33601238c87765227b1ac524d071c3d2de98854')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  for (( i=0; i < ${#source[@]}; i++ )); do
    if [ "${source[i]##*.}" = "patch" ]; then
      msg2 "Applying patch: \"${source[i]##*/}\"..."
      patch -Np1 -i "${srcdir}/${source[i]##*/}"
    fi
  done

  msg2 "Configuring make..."
  ./configure --prefix=/usr
  # LDFLAGS w configure nie dziaÅ‚a :P

  sed '
    s|/share/icons/|/share/pixmaps/|g
    s|LDFLAGS = |LDFLAGS = -lm |
  ' -i Makefile

  msg2 "Making..."
  make
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  install -dm755 "$pkgdir/usr/"{bin,share/{applications,icons,pixmaps,milena_abc}}

  make DESTDIR="$pkgdir/" install

  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/"{icons,pixmaps}
}

# vim:set ts=2 sw=2 et:
