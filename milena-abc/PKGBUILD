# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-abc
_pkgname=milena_abc
pkgver=0.3.15
pkgrel=1
pkgdesc="Gtk3 gui for audiobook creator from Milena speech synthesizer"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=(milena libwebkit3 gtksourceview3 faac mpg123 libsamplerate curl)
source=(http://tts.polip.com/files/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('b0cf54b067e08959139ef343e2594f183784bfb0c2802d80ba14b7d27b00944e')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  ./configure --prefix=/usr

  # Autorzy mileny mogli by wresscie zacząc pisac to pod paczki (oby dzialalo)
  # {
    sed 's|^export prefix = |export DESTDIR ?=\nexport prefix = ${DESTDIR}|;
         s|^export htmldir = /usr|export htmldir = ${prefix}|;
         s|/share/icons/|/share/pixmaps/|' \
        -i Makefile
    # prefix powinien mieć wstawiony ${DESTDIR} (standard, make install, paczkowanie)
    # htmldir powinien mieć ${DESTDIR} lub ${prefix} (sam nie wiem co lepsze)
    # prawidłowy katalog ikonek to /usr/share/pixmaps
  # }

  make
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  install -dm755 "$pkgdir/usr/"{bin,share/{applications,icons,pixmaps,milena_abc}}

  make DESTDIR="$pkgdir/" install

  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/"{icons,pixmaps}
}

# vim:set ts=2 sw=2 et:
