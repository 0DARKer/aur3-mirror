# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-abc
_pkgname=milena_abc
pkgver=0.3.35
pkgrel=1
pkgdesc="Gtk3 gui for audiobook creator from Milena speech synthesizer"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=(milena libwebkit3 gtksourceview3 faac mpg123 libsamplerate curl)
source=(http://tts.polip.com/files/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('facef6af440d0f91da84517c68a6be72ff23e4b3cada5604845822f6cf3fe343')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  for (( i=0; i < ${#source[@]}; i++ )); do
    if [ "${source[i]##*.}" = "patch" ]; then
      msg2 "Applying patch: \"${source[i]##*/}\"..."
      patch -Np1 -i "${srcdir}/${source[i]##*/}"
    fi
  done

  msg2 "Configuring make..."
  ./configure --prefix=/usr
  # LDFLAGS w configure nie dziaÅ‚a :P

  sed '
    s|/share/icons/|/share/pixmaps/|g
    s|LDFLAGS = |LDFLAGS = -lm |
  ' -i Makefile

  msg2 "Making..."
  make
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  install -dm755 "$pkgdir/usr/"{bin,share/{applications,icons,pixmaps,milena_abc}}

  make DESTDIR="$pkgdir/" install

  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/"{icons,pixmaps}
}

# vim:set ts=2 sw=2 et:
