# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-abc
_pkgname=milena_abc
pkgver=0.3.21
pkgrel=1
pkgdesc="Gtk3 gui for audiobook creator from Milena speech synthesizer"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=(milena libwebkit3 gtksourceview3 faac mpg123 libsamplerate curl)
source=(http://tts.polip.com/files/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('2f6009dec88abbce3111991a3a940240e281260c3c25fde7963e62b7475a219e')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  ./configure --prefix=/usr
  # LDFLAGS w configure nie dzia≈Ça :P

  sed '
    s|/share/icons/|/share/pixmaps/|g
    s|LDFLAGS = |LDFLAGS = -lm |
  ' -i Makefile

  make
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  install -dm755 "$pkgdir/usr/"{bin,share/{applications,icons,pixmaps,milena_abc}}

  make DESTDIR="$pkgdir/" install

  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/"{icons,pixmaps}
}

# vim:set ts=2 sw=2 et:
