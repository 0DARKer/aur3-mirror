# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-abc
pkgver=0.3.39
pkgrel=1
pkgdesc="Gtk3 gui for audiobook creator from Milena speech synthesizer"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=(milena libwebkit3 gtksourceview3 faac mpg123 libsamplerate curl)
source=(http://tts.polip.com/files/${pkgname//-/_}-${pkgver}.tar.gz)
sha256sums=('6b7b00b7dc5f0ab276f9d1cd5fa50325be847d8ce4d2cec2deb1e164b780e64a')

build() {
  cd "$srcdir/${pkgname//-/_}-${pkgver}"

  ./configure \
    --prefix=/usr \
    LDFLAGS="-lm"

  make
}

package() {
  cd "$srcdir/${pkgname//-/_}-${pkgver}"

  # "make install" powinien tworzyc katalogi:
  install -dm755 "$pkgdir/usr/"{bin,share/{applications,icons,pixmaps,milena_abc}}

  make DESTDIR="$pkgdir/" install

  # Ikonka powinna byc gdzies tu:
  #   * /usr/share/icons/hicolor/YYxZZ/apps/milena_abc.png
  #   * /usr/share/pixmaps/milena_abc.png
  mv "$pkgdir"/usr/share/icons/milena_abc.xpm "$pkgdir"/usr/share/pixmaps/milena_abc.xpm
  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/"{icons,pixmaps}
}

# vim:set ts=2 sw=2 et:
