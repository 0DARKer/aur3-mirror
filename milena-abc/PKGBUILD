# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-abc
_pkgname=milena_abc
pkgver=0.3.13
pkgrel=1
pkgdesc="Gtk3 gui for audiobook creator from Milena speech synthesizer"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
# generated by namcap:
depends=(milena libwebkit3 gtksourceview3 faac mpg123 libsamplerate curl)
source=(http://tts.polip.com/files/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('3099061458e6ccf51669f396900cd214209636497190dc8e81bb60a69ffefca5')

build() {
  cd "$srcdir/${_pkgname}"

  ./configure --prefix=/usr

  # Autorzy mileny mogli by wresscie zaczÄ…c pisac to pod paczki (oby dzialalo)
  # {
    sed -i 's|prefix = /usr|DESTDIR ?=\nprefix = ${DESTDIR}/usr|' Makefile
    sed -i 's|/share/icons/|/share/pixmaps/|' Makefile
  # }
  make
}

package() {
  cd "$srcdir/${_pkgname}"

  install -dm755 "$pkgdir/usr/"{bin,share/applications,share/icons,share/pixmaps,share/milena_abc}

  make DESTDIR="$pkgdir/" install

  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/"{icons,pixmaps}
}

# vim:set ts=2 sw=2 et:
