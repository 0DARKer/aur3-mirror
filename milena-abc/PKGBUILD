# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-abc
_pkgname=milena_abc
pkgver=0.3.28
pkgrel=1
pkgdesc="Gtk3 gui for audiobook creator from Milena speech synthesizer"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=(milena libwebkit3 gtksourceview3 faac mpg123 libsamplerate curl)
source=(http://tts.polip.com/files/${_pkgname}-${pkgver}.tar.gz
        10-only_glib.h_can_be_included_directly.patch)
sha256sums=('4b7c6da550e74ea6e1f088e0df2192978509e3b78d5ba20b0a7489fe289fa337'
            '714854425892f434444f9812ddd41d8b5c21ba7998661a6b8a1384c5dc277702')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  for (( i=0; i < ${#source[@]}; i++ )); do
    if [ "${source[i]##*.}" = "patch" ]; then
      msg2 "Applying patch: \"${source[i]##*/}\"..."
      patch -Np1 -i "${srcdir}/${source[i]##*/}"
    fi
  done

  msg2 "Configuring make..."
  ./configure --prefix=/usr
  # LDFLAGS w configure nie dziaÅ‚a :P

  sed '
    s|/share/icons/|/share/pixmaps/|g
    s|LDFLAGS = |LDFLAGS = -lm |
  ' -i Makefile

  msg2 "Making..."
  make
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"

  install -dm755 "$pkgdir/usr/"{bin,share/{applications,icons,pixmaps,milena_abc}}

  make DESTDIR="$pkgdir/" install

  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/"{icons,pixmaps}
}

# vim:set ts=2 sw=2 et:
