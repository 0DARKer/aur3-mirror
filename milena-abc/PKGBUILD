# Maintainer: 3ED <krzysztof1987 _at_ gmail _dot_ com>

pkgname=milena-abc
_pkgname=milena_abc
pkgver=0.3.8
pkgrel=1
pkgdesc="Gtk2 gui for audiobook creator from Milena speech synthesizer"
arch=('i686' 'x86_64')
url="http://milena.polip.com/"
license=('GPL')
depends=('milena' 'gtksourceview2' 'pcre' 'lame')
optdepends=('faac' 'libsamplerate')
source=(http://tts.polip.com/files/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('a2f18fb78e591cbb9cebb3e6451bc8aa878607a1246a2f34ca080de84266891c')

build() {
  cd "$srcdir/${_pkgname}"

  ./configure --prefix=/usr || return 1

  # Autorzy mileny mogli by wresscie zaczÄ…c pisac to pod paczki (oby dzialalo)
  # {
    sed -i 's|prefix = /usr|DESTDIR ?=\nprefix = ${DESTDIR}/usr|' Makefile
    sed -i 's|/share/icons/|/share/pixmaps/|' Makefile
  # }
  make
}

package() {
  cd "$srcdir/${_pkgname}"

  install -dm755 "$pkgdir/usr/"{bin,share/applications,share/icons,share/pixmaps,share/milena_abc}

  make DESTDIR="$pkgdir/" install

  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/"{icons,pixmaps}
}

# vim:set ts=2 sw=2 et:
