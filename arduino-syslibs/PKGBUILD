# Maintainer: PyroPeter <googlemail.com@abi1789>

pkgname=arduino-syslibs
pkgver=0022
pkgrel=1
pkgdesc="Arduino SDK (depends on external avrdude and librxtx)"
arch=('any')
url="http://arduino.cc/en/Main/Software"
license=('GPL')
depends=('avr-libc' 'java-runtime' 'sh' 'xdg-utils' 'avrdude' rxtx)
provides=('arduino')
conflicts=('arduino')
source=("http://arduino.googlecode.com/files/arduino-$pkgver.tgz"
        'arduino'
        'arduino-syslibs.install'
        'avrdude'
        'arduino.png'
        'arduino.desktop')
md5sums=('2429c118381a4a761d7f5717183ac387' 'e2c80b367bfbd96e56e656c198516288' 'bec2d12f581b2a8904abad00e7653492' 'c4cff287b732fd09d71bb81db3dcb167' '9e36d33891d5e68d38ec55d1494499a5' 'eebc4d6495864bea99ad057af801afb9')

package() {
  cd "$srcdir/arduino-$pkgver"
  mkdir -p "$pkgdir"/usr/{bin,share/{doc,applications}}
  
  # remove architecture dependend stuff
  rm hardware/tools/avrdude
  rm lib/librxtxSerial.so
  
  # copy the whole SDK to /usr/share/arduino/
  cp -r . "$pkgdir/usr/share/arduino"
  
  # install a wrapper for avrdude
  install -m755 "$srcdir/avrdude" "$pkgdir/usr/share/arduino/hardware/tools/"
  
  # at least support the FHS a little bit:
  install -m755 "$srcdir/arduino" "$pkgdir/usr/bin/arduino"
  ln -s /usr/share/arduino/reference "$pkgdir/usr/share/doc/arduino"
  
  # desktop icon
  install -m644 "$srcdir/arduino.desktop" "$pkgdir/usr/share/applications/"
  install -m644 "$srcdir/arduino.png" "$pkgdir/usr/share/arduino/"
}
