pkgname=mingw-w64-speex
pkgver=1.2rc1
pkgrel=3
pkgdesc="A free codec for free speech (mingw-w64)"
arch=(any)
url="http://www.speex.org"
license=("BSD")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt mingw-w64-libogg)
options=(!libtool !strip !buildflags)
source=("http://downloads.us.xiph.org/releases/speex/speex-$pkgver.tar.gz")
md5sums=('c4438b22c08e5811ff10e2b06ee9b9ae')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/speex-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-build-i686-w64-mingw32"
  make DESTDIR="$pkgdir" install
  find "$pkgdir/usr/i686-w64-mingw32" -name '*.exe' | xargs -rtl1 rm
  find "$pkgdir/usr/i686-w64-mingw32" -name '*.dll' | xargs -rtl1 i686-w64-mingw32-strip -x
  find "$pkgdir/usr/i686-w64-mingw32" -name '*.a' -o -name '*.dll' | xargs -rtl1 i686-w64-mingw32-strip -g
  rm -r "$pkgdir/usr/i686-w64-mingw32/share"
  
  cd "${srcdir}/${pkgname}-${pkgver}-build-x86_64-w64-mingw32"
  make DESTDIR="$pkgdir" install
  find "$pkgdir/usr/x86_64-w64-mingw32" -name '*.exe' | xargs -rtl1 rm
  find "$pkgdir/usr/x86_64-w64-mingw32" -name '*.a' | xargs -rtl1 x86_64-w64-mingw32-strip -g
  rm -r "$pkgdir/usr/x86_64-w64-mingw32/share"
}
