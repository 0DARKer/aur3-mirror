# Maintainer: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Mathieu Clabaut <mathieu.clabaut@gmail.com>
pkgname=mwlib.ext
pkgver=0.12.4
pkgrel=4
pkgdesc="external dependencies for mwlib"
arch=(i686 x86_64)
url="http://code.pediapress.com/wiki/wiki"
# ReportLab is BSD, GPL, and LGPL
license=('BSD' 'GPL' 'LGPL')
Changelog=Changelog
depends=('python2' 'freetype2')
makedepends=('python2-distribute' )
source=("http://pypi.python.org/packages/source/m/$pkgname/$pkgname-$pkgver.zip")
md5sums=('5b1c883fe9aed004ea31e8c912add0e1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir"
  find "$pkgdir" -name "*.py" -exec \
    sed -i '1s/python/python2/' {} \;

  local license_target="$pkgdir/usr/share/licenses/$pkgname"
  install -D -m644 upstream-src/LICENSE.txt \
    "$license_target/reportlab-LICENSE.txt"
  install -D -m644 upstream-src/src/reportlab/fonts/bitstream-vera-license.txt \
    "$license_target/bitstream-vera-license.txt"
}

