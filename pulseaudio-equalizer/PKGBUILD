# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=pulseaudio-equalizer
pkgver=2.7.0.1
pkgrel=2
pkgdesc="A 15-band equalizer for PulseAudio"
arch=(i686 x86_64)
url="https://code.launchpad.net/~psyke83/+junk/pulseaudio-equalizer"
license=('GPL3')
depends=('pygtk' 'swh-plugins' 'gnome-icon-theme' 'pulseaudio' 'bc')
source=(https://launchpad.net/~nilarimogard/+archive/webupd8/+files/${pkgname}_$pkgver-1.tar.gz
        pulseaudio-equalizer-2.7-do-not-crash-on-missing-preset.patch
        pulseaudio-equalizer-2.7-force-default-persistence-value.patch
        pulseaudio-equalizer-2.7-remove-preamp.patch
        pulseaudio-equalizer-2.7-window-icon.patch)
md5sums=('684179e10cfd4b44790edf58acb11dee'
         '8c1ac7a94869cc32e66373f36f6a2c33'
         '6ba8603312b45ac61871a85ea89551d8'
         'ce6e9c003b372112d7d26168dae5ad3f'
         '42da378e2d78bf340b027a8d378b064d')

build() {
  cd "$srcdir/$pkgname"

  sed -i 's|gnome-volume-control|multimedia-volume-control|g' usr/share/applications/pulseaudio-equalizer.desktop

  patch -Np1 -i "$srcdir/pulseaudio-equalizer-2.7-force-default-persistence-value.patch"
  patch -Np1 -i "$srcdir/pulseaudio-equalizer-2.7-remove-preamp.patch"
  patch -Np1 -i "$srcdir/pulseaudio-equalizer-2.7-window-icon.patch"
  patch -Np1 -i "$srcdir/pulseaudio-equalizer-2.7-do-not-crash-on-missing-preset.patch"

  sed -i '1s|#!/usr/bin/env python|&2|' usr/share/pulseaudio-equalizer/pulseaudio-equalizer.py
  sed -i 's|^python|&2|' usr/bin/pulseaudio-equalizer-gtk
}

package() {
  cd "$srcdir/$pkgname"
  cp -rfp usr "$pkgdir"
}
