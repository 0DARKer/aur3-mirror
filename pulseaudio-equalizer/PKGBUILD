# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=pulseaudio-equalizer
pkgver=2.7.0.2
pkgrel=1
pkgdesc="A 15-band equalizer for PulseAudio"
arch=(i686 x86_64)
url="http://ubuntuforums.org/showthread.php?t=1308838"
license=('GPL3')
depends=('pygtk' 'swh-plugins' 'gnome-icon-theme' 'pulseaudio' 'bc')
source=(http://ppa.launchpad.net/nilarimogard/webupd8/ubuntu/pool/main/p/$pkgname/${pkgname}_$pkgver.orig.tar.gz
        pulseaudio-equalizer-2.7-do-not-crash-on-missing-preset.patch
        pulseaudio-equalizer-2.7-force-default-persistence-value.patch
        pulseaudio-equalizer-2.7-remove-preamp.patch
        pulseaudio-equalizer-2.7-window-icon.patch)
md5sums=('c3c886a6017cb16408d47d0f5cd8549b'
         '8c1ac7a94869cc32e66373f36f6a2c33'
         '6ba8603312b45ac61871a85ea89551d8'
         'ce6e9c003b372112d7d26168dae5ad3f'
         '42da378e2d78bf340b027a8d378b064d')

build() {
  cd "$srcdir/$pkgname"

  sed -i 's|gnome-volume-control|multimedia-volume-control|g' usr/share/applications/pulseaudio-equalizer.desktop

  patch -Np1 -i "$srcdir/pulseaudio-equalizer-2.7-force-default-persistence-value.patch"
  patch -Np1 -i "$srcdir/pulseaudio-equalizer-2.7-remove-preamp.patch"
  patch -Np1 -i "$srcdir/pulseaudio-equalizer-2.7-window-icon.patch"
  patch -Np1 -i "$srcdir/pulseaudio-equalizer-2.7-do-not-crash-on-missing-preset.patch"

  sed -i '1s|#!/usr/bin/env python|&2|' usr/share/pulseaudio-equalizer/pulseaudio-equalizer.py
  sed -i 's|^python|&2|' usr/bin/pulseaudio-equalizer-gtk
}

package() {
  cd "$srcdir/$pkgname"
  cp -rfp usr "$pkgdir"
}
