# Maintainer: Andrew Nelless <andrew at nelless dot net>
pkgname='avro-cpp'
pkgver=1.7.6
pkgrel=3
arch=('i686' 'x86_64')

pkgdesc="C++ implementation of the Apache Avro data serialization system"
url="https://avro.apache.org/"
license=('Apache')

groups=('avro')
depends=('boost-libs')
makedepends=('boost' 'flex' 'bison' 'cmake' 'python2')
source=("http://www.us.apache.org/dist/avro/avro-$pkgver/cpp/$pkgname-$pkgver.tar.gz"
        $pkgname-python2.patch)
md5sums=('4dbcfc48abf8feab7ea9d7f8ec8766c9'
         '821356ab706366de6c650c3c39506665')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -p1 -i "$srcdir/$pkgname-python2.patch"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    mkdir build
    cd build
    # Package won't pull from the env provided by /etc/makepkg.conf. I've
    # tweaked that here, but kept -DNDEBUG from the CMake default 'Release'
    # profile. With Arch defaults this will weaken optimisation to -O2
    # (from -O3) and enable stack protection.
    #
    # I've also added -flto because it seems to benefit this build.
    cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DCMAKE_CXX_FLAGS_RELEASE="$CXXFLAGS -DNDEBUG -flto" \
        -DCMAKE_C_FLAGS_RELEASE="$CFLAGS -DNDEBUG -flto" \
        -DCMAKE_EXE_LINKER_FLAGS="$LDFLAGS -flto" \
        -DCMAKE_SHARED_LINKER_FLAGS="$LDFLAGS -flto" ..
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver/build"
    make test
}

package() {
    cd "$srcdir/$pkgname-$pkgver/build"
    make DESTDIR="$pkgdir/" install
}
