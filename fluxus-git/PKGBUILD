# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Contributor: hm_b <holger@music-nerds.net>

_name=fluxus
pkgname=$_name-git
pkgver=20120126
pkgrel=1
pkgdesc="A rapid prototyping, livecoding and playing/learning environment for 3D graphics and games."
arch=('i686' 'x86_64')
url="http://www.pawfal.org/fluxus/"
license=('GPL')
depends=('fftw' 'freeglut' 'glew' 'gstreamer0.10-base' 'libunicap'
         'liblo' 'ode' 'openal' 'racket' 'jack' 'ffmpeg')
makedepends=('git' 'scons' 'pth')
provides=("$_name")
conflicts=("$_name")
source=('libffi.patch'
        'racket_5.1.2_segfault.patch::http://lists.pawfal.org/pipermail/fluxus-pawfal.org/attachments/20111006/97b8194a/attachment.txt')
md5sums=('f118c864b0fdfc7d21433cc3998891eb'
         'a8e92341f917747c1a69aa8cc41ba1b8')

_gitroot="http://git.savannah.gnu.org/cgit/$_name.git"
_gitname="$_name"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  # racket 5.1.2+ patch
  patch -p1 -i "$srcdir/racket_5.1.2_segfault.patch"  

  # add libffi to SConstruct
  patch -p1 -i "$srcdir/libffi.patch"

  # PNGLoader fix
  sed -i "176,179d" libfluxus/src/PNGLoader.cpp

  scons install Prefix=/usr \
                RacketPrefix=/usr \
                CCFLAGS="-D__STDC_CONSTANT_MACROS" \
                DESTDIR="$pkgdir/"
} 
