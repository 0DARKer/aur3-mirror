# Maintainer: Mariusz Libera <mariusz.libera@gmail.com>
pkgname=gfio
pkgver=2.1
pkgrel=1
pkgdesc="GTK2 frontend for fio benchmarking tool"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/fio"
license=('GPL2')
depends=("fio=${pkgver}" 'gtk2')
changelog=ChangeLog
source=("http://brick.kernel.dk/snaps/fio-${pkgver}.tar.bz2"
	"gfio.desktop")
sha256sums=('5cab5cf9d1a5ddf60a321143bac5d7babd8ad4788a060ad66422ef04ab163290'
            '92ecdaa3634ae57fd0760e3c7642cb56045863a5a33318d96a093cd512f1000f')

build() {
	cd $srcdir/fio-${pkgver}
	./configure --enable-gfio
	make gfio
}

package() {
	cd $srcdir/fio-${pkgver}

	# binary
	install -Dm755 gfio "${pkgdir}/usr/bin/gfio"

	# desktop file
	install -Dm644 "${srcdir}/gfio.desktop" "${pkgdir}/usr/share/applications/gfio.desktop"

	# documentation
	install -Dm644 GFIO-TODO "${pkgdir}/usr/share/doc/fio/GFIO-TODO"
}

