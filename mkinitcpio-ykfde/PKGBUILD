# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=mkinitcpio-ykfde
pkgver=0.2.0.r0.g8e74f73
pkgrel=1
pkgdesc="Full disk encryption with Yubikey (Yubico key)"
arch=('any')
url="http://www.eworm.de/"
license=('GPL')
depends=('makepasswd' 'ykpers' 'cryptsetup')
makedepends=('git')
install=mkinitcpio-ykfde.install
source=('git://github.com/eworm-de/mkinitcpio-ykfde.git')
    
pkgver() {
	cd mkinitcpio-ykfde/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

package() {
	cd mkinitcpio-ykfde/

	install -D -m644 hook/ykfde ${pkgdir}/usr/lib/initcpio/hooks/ykfde
	install -D -m644 install/ykfde ${pkgdir}/usr/lib/initcpio/install/ykfde
	install -D -m755 bin/ykfde ${pkgdir}/usr/bin/ykfde
}

sha256sums=('SKIP')
