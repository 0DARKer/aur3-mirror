# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=mkinitcpio-ykfde
pkgver=0.2.0.r29.gd9093ea
pkgrel=1
pkgdesc="Full disk encryption with Yubikey (Yubico key)"
arch=('i686' 'x86_64')
url="https://github.com/eworm-de/mkinitcpio-ykfde"
license=('GPL')
depends=('makepasswd' 'ykpers' 'cryptsetup')
makedepends=('git' 'markdown')
install=mkinitcpio-ykfde.install
source=('git://github.com/eworm-de/mkinitcpio-ykfde.git')
backup=('etc/ykfde.conf')

pkgver() {
	cd mkinitcpio-ykfde/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

package() {
	cd mkinitcpio-ykfde/

	make DESTDIR=${pkgdir} install
}

sha256sums=('SKIP')
