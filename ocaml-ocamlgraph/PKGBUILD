# Maintainer: Sergei Lebedev <superbobry@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: Paolo Herms
# Contributor: Magnus Therning <magnus@therning.org>
# Contributor: Jose Neder <jlneder(at)gmail(dot)com>

pkgname=ocaml-ocamlgraph
pkgver=1.8.1
pkgrel=2
pkgdesc="Graph library for OCaml"
url="http://ocamlgraph.lri.fr"
source=("http://ocamlgraph.lri.fr/download/ocamlgraph-$pkgver.tar.gz")
md5sums=('5aa256e9587a6d264d189418230af698')
options=('!strip' '!makeflags')
depends=('ocaml')
makedepends=('ocaml-findlib')
arch=('i686' 'x86_64')
license=('LGPL')

# Note: for graph viewer add 'libgnomecanvas' to depends and
#       'lablgtk2' to makedepends.

build() {
  cd "$srcdir/ocamlgraph-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/ocamlgraph-$pkgver"
  export OCAMLFIND_DESTDIR=$pkgdir$(ocamlfind printconf destdir)
  mkdir -p $OCAMLFIND_DESTDIR
  make DESTDIR=$pkgdir install-findlib
}
