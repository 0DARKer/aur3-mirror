pkgname=dnf
pkgver=1.0.0
_gittag=${pkgname}-${pkgver}-1
pkgrel=1
pkgdesc="Package manager forked from Yum, using libsolv as a dependency resolver"
arch=('any')
url="https://github.com/rpm-software-management/${pkgname}"
license=('GPL2')
depends=('hawkey' 'libcomps' 'librepo' 'rpm-org'
         'python' 'python-iniparse' 'python-pygpgme')
makedepends=('cmake' 'python-sphinx')
checkdepends=('python-nose')
backup=('etc/dnf/automatic.conf'
        'etc/dnf/dnf.conf')
source=("${url}/archive/${_gittag}.tar.gz")
md5sums=('ba5841985a7ff9ca0e9f451ae1350b7f')

build() {
	cd "$pkgname-$_gittag"
	mkdir build
	cd build
	cmake -DCMAKE_BUILD_TYPE='Release'  \
	      -DCMAKE_INSTALL_PREFIX='/usr' \
	      -DPYTHON_DESIRED='3'          \
	      ..
	make
	make doc-man
}

# Tests seem to need a non-empty RPM database installed on the system
#check() {
#	cd "$pkgname-$_gittag"
#	cd build
#	make ARGS="-V" test
#}

package() {
	cd "$pkgname-$_gittag"
	cd build
	make DESTDIR="$pkgdir/" install
	rm "$pkgdir/"usr/bin/yum "$pkgdir/"usr/share/man/man8/yum.8
	ln -s dnf-3 "$pkgdir/"usr/bin/dnf
	ln -s dnf-automatic-3 "$pkgdir/"usr/bin/dnf-automatic
}
