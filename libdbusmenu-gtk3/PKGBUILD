# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgbase=libdbusmenu
pkgname=libdbusmenu-gtk3
pkgver=0.6.1
pkgrel=1
pkgdesc="Small little library that passes a menu structure across DBus (GTK+ 3 library)"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('LGPL')
depends=('gtk3' 'libdbusmenu')
makedepends=('intltool' 'gnome-doc-utils' 'gobject-introspection' 'vala' 'python2')
options=('!libtool' '!emptydirs')
source=(http://launchpad.net/dbusmenu/0.6/$pkgver/+download/$_pkgbase-$pkgver.tar.gz)
md5sums=('820b6999dd1008328bfa442575d859a1')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/dbusmenu-bench

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-static --disable-scrollkeeper --disable-tests
  make
}

package() {
  cd "$srcdir/$_pkgbase-$pkgver"

  make -C libdbusmenu-glib DESTDIR="$pkgdir/" install
  make -j1 -C libdbusmenu-gtk DESTDIR="$pkgdir/" install
  make -C libdbusmenu-glib DESTDIR="$pkgdir/" uninstall
}
