# Maintainer: Frikilinux <frikilinux at frikilinux.com.ar>

pkgname=faenza-icon-theme
pkgver=1.1
pkgrel=1
pkgdesc="Icon theme designed for Equinox GTK theme"
url="http://gnome-look.org/content/show.php/Faenza?content=128143"
license=('GPL3')
arch=('any')
makedepends=()
options=(!strip)
source=("http://${pkgname}.googlecode.com/files/${pkgname}_${pkgver}.tar.gz")
sha256sums=('e0354bbb07368ed871648b3cf6f34a954d8f075da75b8271fa0598b2851a42f7')

package() {
	cd "${srcdir}"
	mkdir -p "${pkgdir}"/usr/share/icons
	cp -rf Faenza{,-Dark,-Darkest,-Darker} "${pkgdir}"/usr/share/icons/

# set the Arch Linux Dristributor and start menu logo
        for size in 22 24 32 48; do
		cd "${pkgdir}"/usr/share/icons/Faenza/places/$size
		ln -sf distributor-logo-archlinux.png distributor-logo.png
		ln -sf start-here-archlinux.png start-here.png
        done;

# set the Arch Linux start menu logo in dark theme
        for size in 22 24 32 48; do
		cd "${pkgdir}"/usr/share/icons/Faenza-Dark/places/$size
		ln -sf start-here-archlinux.png start-here.png
        done;

        cd "${pkgdir}"/usr/share/icons/Faenza/places/scalable
        ln -sf distributor-logo-archlinux.svg distributor-logo.svg
	ln -sf start-here-archlinux.svg start-here.svg
	ln -sf start-here-archlinux-symbolic.svg start-here-symbolic.svg
	cd "${pkgdir}"/usr/share/icons/Faenza-Dark/places/scalable
	ln -sf start-here-archlinux.svg start-here.svg
	ln -sf start-here-archlinux-symbolic.svg start-here-symbolic.svg
}

