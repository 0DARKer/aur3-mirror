# Maintainer: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jeremy Sands <cto@jeremysands.com>

pkgname=python2-parsedatetime
pkgver=1.2
pkgrel=1
pkgdesc="Parse human-readable date/time text"
arch=(any)
url=http://github.com/bear/parsedatetime/
license=(Apache)
depends=(python2)
makedepends=(python2-setuptools)
source=(https://pypi.python.org/packages/source/p/${pkgname#*-}/${pkgname#*-}-$pkgver.tar.gz)
sha256sums=('1b6bc97867e52df7ea95122a25a788cb37daddbc95aba4aa9084deddff0592fd')
sha512sums=('e6e371d82776cb103edecca6feecc384362faca0fd6f2e3337c67e0613cefdd8effb00dbc9af3318faf3f43eddc31919634ea26c24a97fdcee43061abc3a658a')

prepare() {
    find ${pkgname#*-}-$pkgver -name '*.py' -type f -exec sed -ri 's:^#!/usr/bin/(env )?python$:&2:' '{}' \;
}

build() {
    cd ${pkgname#*-}-$pkgver/
    python2 setup.py build
}

package() {
    cd ${pkgname#*-}-$pkgver/
    python2 setup.py install --prefix=/usr --root="$pkgdir"
}
