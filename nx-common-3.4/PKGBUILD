# $Id: PKGBUILD 44734 2009-07-03 19:11:59Z andyrtr $
# Maintainer Tobias Powalowski <tpowa@archlinux.org>
# Contributed: eliott <eliott@solarblue.net>, Andre Naumann <anaumann@SPARCed.org>
_pkgname=nx-common
pkgname=${_pkgname}-3.4
pkgver=3.4.0
_minorver=-1
pkgrel=1
pkgdesc="NoMachine NX common package for client and server"
arch=('i686' 'x86_64')
license=('GPL')
url="http://nomachine.com/"
depends=('libjpeg>=7' 'libpng' 'openssl>=0.9.8g' 'gcc-libs' 'audiofile' 'alsa-lib' 'bash')
conflicts=('nx-common')
source=(\
#Compression libs and proxy sources
http://64.34.161.181/download/$pkgver/sources/nxcomp-$pkgver${_minorver}.tar.gz \
http://64.34.161.181/download/$pkgver/sources/nxssh-$pkgver${_minorver}.tar.gz \
# ESD Support
http://64.34.161.181/download/$pkgver/sources/nxesd-$pkgver${_minorver}.tar.gz
# 64bit fixes
NXproto.h.64bit.diff
nx-gcc44.patch)
options=(!libtool)

build() {
  cd ${srcdir}
  patch -Np0 -i ${srcdir}/nx-gcc44.patch
  if [ "$CARCH" = "x86_64" ]; then
    patch -Np1 -i ../NXproto.h.64bit.diff  || return 1
  fi
 
  mkdir -p ${pkgdir}/opt/NX/bin
  mkdir -p ${pkgdir}/opt/NX/lib 

  cd ${srcdir}/nxcomp
  
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcomp.so.* ${pkgdir}/opt/NX/lib
  cd ${pkgdir}/opt/NX/lib
  ln -s libXcomp.so.1 libXcomp.so

  cd ${srcdir}/nxssh
  ./configure --prefix=/opt/NX
  make || return 1
  install -D -m755 nxssh ${pkgdir}/opt/NX/bin/nxssh
 
  cd ${srcdir}/nxesd
  ./configure --prefix=/opt/NX
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  # fix libxcomp linking
  cd ${pkgdir}/opt/NX/lib
  ln -s libXcomp.so.3 libXcomp.so.1
}
md5sums=('3dc3d0bca883ceb0d74d9774d63668c6'
         '14bd903426c32926e0c213f6bc0aba89'
         '023f5a729cbbc05257eda7d9a73d6082'
         'acae3e9905a0fe5eb1d5958e07a82d28'
         '32600c73e9de8f59ce32388d2137c9ea')
