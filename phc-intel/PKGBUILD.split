# Maintainer: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Maintainer: trapanator <trap[at]trapanator[dot]com>
# Maintainer: tomprogrammer <Thomas-Bahn[at]gmx[dot]net>

pkgname=('phc-intel-modprobe' 'phc-intel-kernel26')
pkgbase='phc-intel'
pkgver=0.3.2
pkgrel=12
_pkgrel=$pkgrel-1
pkgdesc="frequency driver for Intel CPUs with undervolting feature"
arch=('i686' 'x86_64')
url="http://www.linux-phc.org"
license=('GPL')
provides=('linux-phc')
options=(!makeflags)
install=$pkgbase.install
source=($pkgbase-$pkgver-$_pkgrel.tar.gz::$url/forum/download/file.php?id=94
acpi-cpufreq_2.6.{35..36}.c linux-phc-0.3.2_2.6.{35..36}.patch
mperf.h)
md5sums=('0f55f583f9b62b0f6812a3cd2fd24680'
         '6068632173e19a8f10c882ba7da84ac9'
         '3b27dcabe6d1bb0b766d24d89a02d71b'
         '2f15cbdaf6d9ab40f60f97fa221f71a9'
         'e52792d26694ce7e38884dbe66b12bd6'
         'eff9f1e59689cdd1b44d415072c26d95')

build() {
	cd $pkgbase-$pkgver-$_pkgrel
	sed -i 's|^\tinstall -m 644 -o root -g root phc-intel.modprobe|#\tinstall -m 644 -o root -g root phc-intel.modprobe|' Makefile
	sed -i 's|^\tdepmod $(KERNELVERSION) -a|#\tdepmod $(KERNELVERSION) -a|' Makefile

	cp ../mperf.h .
	for i in 2.6.{35..36}; do
		mkdir -p inc/$i
		cp ../acpi-cpufreq_$i.c inc/$i/acpi-cpufreq.c
		cp ../linux-phc-0.3.2_$i.patch inc/$i/linux-phc-0.3.2.patch
	done
	i=2.6.37
	mkdir -p inc/$i
	cp ../acpi-cpufreq_2.6.36.c inc/$i/acpi-cpufreq.c
	cp ../linux-phc-0.3.2_2.6.36.patch inc/$i/linux-phc-0.3.2.patch
}

package_phc-intel-modprobe() {
	pkgdesc="modprobe rule for $pkgbase"
	arch=('any')
	unset provides install
	backup=(etc/modprobe.d/$pkgbase.conf)
	install -Dm644 $pkgbase-$pkgver-$_pkgrel/$pkgbase.modprobe "$pkgdir"/etc/modprobe.d/$pkgbase.conf
}

package_phc-intel-kernel26() {
	# name of the kernel package
	local kernel='kernel26'
	local localversion='-ARCH'

	local kernver=$(pacman -Q $kernel)
	kernver=${kernver:$((${#kernel}+1)):6}
	local kernrel=$kernver$localversion
	local min=${kernver:4}
	depends=($kernel'>=2.6.'$min $kernel'<2.6.'$((min+1)) 'phc-intel-modprobe')
	makedepends=($kernel'-headers>=2.6.'$min $kernel'-headers<2.6.'.$((min+1)))

	sed "s/depmod.*/depmod $kernrel/" -i ../$install
	cd $pkgbase-$pkgver-$_pkgrel
	export KERNELSRC=/lib/modules/$kernrel/build
	make
	make DESTDIR="$pkgdir" install
}

package_phc-intel-kernel26-lts() {
	# name of the kernel package
	local kernel='kernel26-lts'
	local localversion='-lts'

	local kernver=$(pacman -Q $kernel)
	kernver=${kernver:$((${#kernel}+1)):6}
	local kernrel=$kernver$localversion
	local min=${kernver:4}
	depends=($kernel'>=2.6.'$min $kernel'<2.6.'$((min+1)) 'phc-intel-modprobe')
	makedepends=($kernel'-headers>=2.6.'$min $kernel'-headers<2.6.'.$((min+1)))

	sed "s/depmod.*/depmod $kernrel/" -i ../$install
	cd $pkgbase-$pkgver-$_pkgrel
	export KERNELSRC=/lib/modules/$kernrel/build
	make
	make DESTDIR="$pkgdir" install
}
