# Maintainer: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Maintainer: trapanator <trap[at]trapanator[dot]com>
# Maintainer: tomprogrammer <Thomas-Bahn[at]gmx[dot]net>

pkgname=phc-intel
pkgver=0.3.2.12.5
_realver=pack-rev5
pkgrel=1
pkgdesc="frequency driver for Intel CPUs with undervolting feature"
url="http://www.linux-phc.org"
arch=('any')
license=('GPL')
provides=('linux-phc')
backup=(etc/conf.d/phc-intel)
install=phc-intel.install
source=(phc-intel-$_realver.tar.bz2::$url/forum/download/file.php?id=131
        phc-intel.{conf,rc,sleep})
sha256sums=('9555552e07174f5ef34afc801629e1332003649b79a811f356e06b6d82276b4a'
            'ce08a5a4107be1d5723f1f169d515e67b6c77893f3994fc2d0d2ccf611307ed3'
            '8f733378f5e010199eb368604566961ae9b59647cbd4f746da93ed480520bea9'
            '3269294d901844d589e7c95d30baf74bcdf8863124290cca3f040b0ffac91ba5')

build() {
	cd phc-intel-$_realver
	sed -e 's,$(DESTDIR)/lib/modules/$(KERNELRELEASE)/extra$,$(DESTDIR)/lib/modules/$(KERNELRELEASE)/extramodules,' \
		-e 's,^\tinstall -m 644 -o root -g root phc-intel.modprobe,#\tinstall -m 644 -o root -g root phc-intel.modprobe,' -i Makefile
	sed -i 's,/sbin/modprobe phc-intel |,/sbin/modprobe phc-intel \&\& /etc/rc.d/phc-intel set |,' phc-intel.modprobe
}

package() {
	install -Dm644 phc-intel.conf "$pkgdir/etc/conf.d/phc-intel"
	install -Dm755 phc-intel.rc "$pkgdir/etc/rc.d/phc-intel"
	install -Dm755 phc-intel.sleep "$pkgdir/usr/lib/pm-utils/sleep.d/00phc-intel"

	cd phc-intel-$_realver
	install -Dm644 phc-intel.modprobe "$pkgdir/lib/modprobe.d/phc-intel.conf"
	install -d "$pkgdir/usr/src/phc-intel/"
	cp -R inc Makefile "$pkgdir/usr/src/phc-intel/"
}
