# Maintainer: fs4000 <matthias_dienstbier[at]yahoo[dot]de>
# Maintainer: trapanator <trap[at]trapanator[dot]com>
# Maintainer: tomprogrammer <Thomas-Bahn[at]gmx[dot]net>

pkgname=phc-intel
pkgver=0.3.2
pkgrel=12
_pkgrel=$pkgrel-1
pkgdesc="frequency driver for Intel CPUs with undervolting feature"
url="http://www.linux-phc.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kernel26>=2.6.33' 'kernel26<2.6.39')
makedepends=('kernel26-headers>=2.6.33' 'kernel26-headers<2.6.39')
provides=('linux-phc')
[ "$pkgname" = "phc-intel" ] && backup=(etc/modprobe.d/phc-intel.conf)
options=(!makeflags)
install=phc-intel.install
source=(phc-intel-$pkgver-$_pkgrel.tar.gz::$url/forum/download/file.php?id=94
acpi-cpufreq_2.6.{35..36}.c linux-phc-0.3.2_2.6.{35..36}.patch
mperf.h)
md5sums=('0f55f583f9b62b0f6812a3cd2fd24680'
         '6068632173e19a8f10c882ba7da84ac9'
         '3b27dcabe6d1bb0b766d24d89a02d71b'
         '2f15cbdaf6d9ab40f60f97fa221f71a9'
         'e52792d26694ce7e38884dbe66b12bd6'
         'eff9f1e59689cdd1b44d415072c26d95')

build() {
	sed "s/depmod.*/depmod `uname -r`/" -i ../$install
	cd phc-intel-$pkgver-$_pkgrel
	sed -i 's| /etc/modprobe.d/phc-intel.conf| ${DESTDIR}/etc/modprobe.d/phc-intel.conf|' Makefile
	sed -i 's|^\tdepmod $(KERNELVERSION) -a|#\tdepmod $(KERNELVERSION) -a|' Makefile

	cp ../mperf.h .
	local i
	for i in 2.6.{35..36}; do
		mkdir -p inc/$i
		cp ../acpi-cpufreq_$i.c inc/$i/acpi-cpufreq.c
		cp ../linux-phc-0.3.2_$i.patch inc/$i/linux-phc-0.3.2.patch
	done
	for i in 2.6.{37..38}; do
		mkdir -p inc/$i
		cp ../acpi-cpufreq_2.6.36.c inc/$i/acpi-cpufreq.c
		cp ../linux-phc-0.3.2_2.6.36.patch inc/$i/linux-phc-0.3.2.patch
	done

	make
}

package() {
	cd phc-intel-$pkgver-$_pkgrel

	install -d "$pkgdir/etc/modprobe.d"
	make DESTDIR="$pkgdir" install

	# delete the config if we build for another kernel
	[ "$pkgname" = "phc-intel" ] || rm -r "$pkgdir/etc"
}
