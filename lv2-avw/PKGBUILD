# Maintainer: speps <speps at aur dot archlinux dot org>

_name=avw.lv2
pkgname=lv2-avw
pkgver=0.0.8
pkgrel=1
pkgdesc="An LV2 porting of the AMS internal modules"
arch=(i686 x86_64)
url="https://sourceforge.net/projects/avwlv2/"
license=('GPL')
depends=('lv2' 'jack' 'gtkmm')
makedepends=('python2' 'lv2-c++-tools')
provides=("$_name")
conflicts=("$_name")
source=("https://downloads.sourceforge.net/project/${_name/.}/$_name.$pkgver.tar.gz")
md5sums=('95f798eda1b8c80aecdd9ebbcdbbc267')

build() {
  cd "$srcdir/$_name"
  python2 waf configure --prefix=/usr
  python2 waf build
}

package() {
  cd "$srcdir/$_name"
  python2 waf install --destdir="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
