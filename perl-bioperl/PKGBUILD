# Maintainer : Jason St. John <jstjohn .. purdue . edu>

_perlmod=BioPerl
pkgname=perl-bioperl
pkgver=1.6.901
pkgrel=6
pkgdesc="BioPerl - Bioinformatics Toolkit"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl-data-stag>=0.11' 'perl-io-string' 'perl-extutils-manifest>=1.52')
makedepends=('perl-module-build>=0.2805' 'perl-test-harness>=2.62')
optdepends=('perl-array-compare: Needed for Bio::PhyloNetwork'
            'perl-clone: Cloning objects (Bio::Tools::Primer3)'
            'perl-dbi: Needed for various tests for Bio::DB::GFF, MySQL, PostgreSQL, and SQLite'
            'perl-dbd-mysql: MySQL-related tests for Bio::DB::SeqFeature::Store'
            'perl-dbd-pg: PostgreSQL-related tests for Bio::DB::SeqFeature::Store'
            'perl-dbd-sqlite: SQLite-related tests for Bio::DB::SeqFeature::Store'
            'perl-error: OO-based exception handling (Bio::Root::Exception)'
            'perl-gd: Alignment graphic output (Bio::Align::Graphics)'
            'perl-graph: Phylogenetic Networks, ontology engine implementation, contig analysis (Bio::PhyloNetwork, Bio::Ontology::SimpleGOEngine::GraphAdaptor, Bio::Assembly::Tools::ContigSpectrum'
            'perl-graphviz: Phylogenetic Network visulization (Bio::PhyloNetwork::GraphViz)'
            'perl-list-moreutils: Back- or reverse-translation of sequences (Bio::Tools::SeqPattern, Bio::Tools::SeqPattern::BackTranslate)'
            'perl-math-random: Random Phylogenetic Networks (Bio::PhyloNetwork::RandomFactory)'
            'perl-set-scaler: Proper operation (Bio::Tree::Compatible)'
            'perl-soap-lite: Bibliographic queries (Bio::DB::Biblio::soap)'
            'perl-sort-naturally: Sort lexically, but sort numeral parts numerically (Bio::Assembly::IO::ace, Bio::Assembly::IO::tigr)'
            'perl-spreadsheet-parseexcel: Read Microsoft Excel files (Bio::SeqIO::excel)'
            'perl-svg>=2.26: For generating SVG images (Bio::Draw::Pictogram)'
            'perl-xml-dom-xpath>=0.13: parsing interpro features (Bio::FeatureIO::interpro)'
            'perl-xml-parser: Parsing XML (Bio::Biblio::IO::medlinexml)'
            'perl-xml-sax>=0.15: Parsing XML (Bio::SearchIO::blastxml, Bio::SeqIO::tigrxml, Bio::SeqIO::bsml_sax)'
            'perl-xml-sax-writer: Writing XML (Bio::SeqIO::tigrxml)'
            'perl-xml-simple: Reading custom XML (Bio::Tools::EUtilities, Bio::DB::HIV, Bio::DB::Query::HIVQuery)'
            'perl-xml-twig: Parsing XML (Bio::Variation::IO::xml, Bio::DB::Taxonomy::entrez, Bio::DB::Biblio::eutils)'
            'perl-xml-writer>=0.4: Parsing and writing XML (Bio::SeqIO::agave, Bio::SeqIO::game::gameWriter, Bio::SeqIO::chadoxml, Bio::SeqIO::tinyseq, Bio::Variation::IO::xml, Bio::SearchIO::Writer::BSMLResultWriter)'
            'perl-yaml: For using "GenBank->GFF3/bp_genbank2gff3.pl"')
source=("http://cpan.perl.org/modules/by-module/Bio/$_perlmod-${pkgver}.tar.gz")
sha512sums=('227387437c940da1435ed83fad6ec2168ca12a729c90dc557e84750c6474213874c23a8f23e50db4027909469627baee581faa11be6208c8e0a5453a01c7eca4')

build() {
	cd "$srcdir/$_perlmod-$pkgver"

	# Install module in vendor directories.
	perl Build.PL installdirs=vendor destdir="$pkgdir/"
	perl Build
}

check() {
	cd "$srcdir/$_perlmod-$pkgver"
	perl Build test || return 0
}

package() {
	cd "$srcdir/$_perlmod-$pkgver"
	perl Build install
}
