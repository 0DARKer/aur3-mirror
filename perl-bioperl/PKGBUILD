# CPAN Name  : BioPerl
# Maintainer : Jason St. John <jstjohn .. purdue . edu>
# Contributor: Anonymous

pkgname='perl-bioperl'
_pkgname='BioPerl'
pkgver='1.6.901'
pkgrel='2'
pkgdesc="Bioinformatics Toolkit"
arch=('any')
url="http://search.cpan.org/dist/${_pkgname}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.6.1' 'perl-data-stag>=0.11' 'perl-io-string')
source=("http://search.cpan.org/CPAN/authors/id/C/CJ/CJFIELDS/${_pkgname}-$pkgver.tar.gz")
sha512sums=('227387437c940da1435ed83fad6ec2168ca12a729c90dc557e84750c6474213874c23a8f23e50db4027909469627baee581faa11be6208c8e0a5453a01c7eca4')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-$pkgver"
  $PERL Makefile.PL
  make
}

check() {
  cd "${srcdir}/${_pkgname}-$pkgver"
  make test
}

package() {
  cd "${srcdir}/${_pkgname}-$pkgver"
  make DESTDIR="$pkgdir/" install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# vim:set ts=2 sw=2 et:
