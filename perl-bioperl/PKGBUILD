# Maintainer : Jason St. John <jstjohn .. purdue . edu>

_perlmod=BioPerl
pkgname=perl-bioperl
pkgver=1.6.901
pkgrel=5
pkgdesc="BioPerl - Bioinformatics Toolkit"
arch=('any')
url="http://search.cpan.org/dist/$_perlmod"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('perl>=5.10.0' 'perl-data-stag>=0.11' 'perl-io-string' 'perl-extutils-manifest>=1.52')
makedepends=('perl-test-harness>=2.62')
optdepends=('perl-array-compare: Needed for Bio::PhyloNetwork'
            'perl-dbd-mysql: MySQL API for loading and querying of MySQL-based GFF feature and BioSQL databases'
            'perl-math-random: Needed for Bio::PhyloNetwork::RandomFactory'
            'perl-soap-lite: SOAP protocol, XEMBL Services in Bio::Biblio::*'
            'perl-set-scaler: unknown'
            'perl-spreadsheet-parseexcel: Read Microsoft Excel files in Bio::SeqIO::excel and Bio::Microarray::Tools::ReseqChip'
            'perl-spreadsheet-writeexcel: Write Microsoft Excel files in Bio::Microarray::Tools::ReseqChip'
            'perl-svg: For generating SVG documents'
            'perl-uri: For dealing with web resources in numerous modules'
            'perl-xml-dom: XML parser in Bio::SeqIO::bsml and Bio::SeqIO::interpro'
            'perl-xml-parser: Parsing of XML documents in various modules'
            'libxml-perl: Parsing of XML documents in various modules'
            'perl-xml-libxml: Interface to the libxml library'
            'perl-xml-sax: New style SAX parser'
            'perl-xml-sax-base: New style SAX parser'
            'perl-xml-sax-writer: New style SAX parser'
            'perl-xml-simple: Simple XML parser'
            'perl-xml-twig: Parsing of XML documents'
            'perl-xml-writer: Parsing and writing of XML documents')
source=("http://cpan.perl.org/modules/by-module/Bio/$_perlmod-$pkgver.tar.gz")
sha512sums=('227387437c940da1435ed83fad6ec2168ca12a729c90dc557e84750c6474213874c23a8f23e50db4027909469627baee581faa11be6208c8e0a5453a01c7eca4')

build() {
  cd "$srcdir/$_perlmod-$pkgver"

  # Install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_perlmod-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_perlmod-$pkgver"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
