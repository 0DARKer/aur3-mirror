# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Neutral END
# Contributor: Christoph Zeiler <rabyte*gmail>
# Contributor: Adam Griffiths

pkgname=exult
pkgver=1.4.9rc1
pkgrel=2
pkgdesc="A recreation of the Ultima 7 engine"
arch=('i686' 'x86_64')
url="http://exult.sourceforge.net/"
license=('GPL')
depends=('alsa-lib' 'libgl' 'libglade' 'libvorbis' 'sdl')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "http://downloads.sourceforge.net/${pkgname}/${pkgname}_audio.zip"
        "${pkgname}.patch")
md5sums=('c17a48cc0377aa67264aaaf441cb1bb2'
         '1905328f00bbbfee33812c1942bc7880'
         'be55b321987e55142642df8483edfe46')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply diffs from SVN to fix errors
  patch -Np1 -i ../${pkgname}.patch

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --enable-opengl \
    --enable-exult-studio \
    --with-timidity=/etc/timidity++ \
    --with-icondir=/usr/share/pixmaps

  make LDFLAGS=-lX11
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Install audio files
  mkdir -p "${pkgdir}/usr/share/${pkgname}/music"
  install -m644 ../music/*.ogg "${pkgdir}/usr/share/${pkgname}/music"
  install -m644 ../*.flx "${pkgdir}/usr/share/${pkgname}"
  install -m644 ../*.txt "${pkgdir}/usr/share/${pkgname}"

  # Install documentation
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/images docs/*.html "${pkgdir}/usr/share/doc/${pkgname}"
}
