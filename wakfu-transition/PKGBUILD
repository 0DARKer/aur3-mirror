# Contributor: p2k <me@p2k-network.org>
# Contributor: Yaohan Chen <yaohan.chen@gmail.com>
pkgname=wakfu-transition
provides=wakfu
conflicts=wakfu
pkgver=1.33
pkgrel=3
pkgdesc="A turn-based tactical Massively Multiplayer Online Role-playing Game (MMORPG) written in Java/OpenGL."
url="http://www.wakfu.com/"
license="custom"
arch=('i686' 'x86_64')
source=('http://dl.ak.ankama.com/games/installers/wakfu-amd64.tar.gz'
        'http://dl.ak.ankama.com/games/installers/wakfu-x86.tar.gz'
        'http://dl.ak.ankama.com/games/installers/wakfu-setup.exe' # to extract icon
        'wakfu.sh'
        'wakfu.desktop'
        'transition.conf.patch')
md5sums=('8437458fe1b43b6418b5e5e2f41efadc'
         '55df1b950f098069ffc32b459270745d'
         '769d6f722a30205cd352dffcc40d1659'
         '56f3145a28f2a608576d3751de757380'
         '6790dbbfdcb60fe9344cb23bb3dc9ab7'
         'f19c996205eca70b3a2076d855486503')

# Skip this block if PKGBUILD is sourced by updpkgsums (pstree is used for this check,
# and if not available, this check is skipped)
if ! ( command -v pstree >/dev/null 2>&1 && \
       pstree --show-parents $$ | grep -q updpkgsums ) ; then
  # Remove source and md5sum for the wrong arch
  if [ "$CARCH" == "x86_64" ];then
    unset source[1] md5sums[1]
  else
    unset source[0] md5sums[0]
  fi
fi

install='wakfu.install'
depends=('libglapi' 'openal' 'java-environment' 'ankama-transition')
makedepends=('icoutils') # To extract icon

prepare() {
  patch -p0 < transition.conf.patch
}

build() {
  msg2 'Extracting icon...'
  mkdir -p icon
  cd icon
  wrestool -x -t group_icon ../wakfu-setup.exe -o .
  icotool -x -w 48 -h 48 *.ico || true
  mv *.png ../wakfu.png
}

package() {
  install -Dm755 wakfu.sh "$pkgdir/usr/bin/wakfu"
  install -Dm644 wakfu.png "$pkgdir/usr/share/icons/wakfu.png"
  install -Dm644 wakfu.desktop "$pkgdir/usr/share/applications/wakfu.desktop"
  cd wakfu
  _installdir="opt/ankama/wakfu"
  install -d "$pkgdir/$_installdir"
  mv game "$pkgdir/$_installdir"
  install -m644 transition.conf "$pkgdir/$_installdir"
  ln -s /opt/ankama/transition "$pkgdir/$_installdir"
}
# vim:set ts=2 sw=2 et:
