# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=netsurf-git
pkgver=20120904
pkgrel=1
pkgdesc='Lightweight and fast web browser'
arch=('x86_64' 'i686')
url="http://www.netsurf-browser.org/"
license=("GPL")
depends=('libmng' 'libglade' 'lcms' 'librsvg' 'lemon' 'cairo' 'desktop-file-utils')
makedepends=('re2c' 'curl' 'libnsbmp' 'libnsgif' 'libcss' 'libhubbub-git' 'libparserutils' 'libdom-git' 'netsurf-buildsystem-git')
provides=('netsurf')
conflicts=('netsurf')
replaces=('netsurf-svn')
source=("$pkgname.desktop"
        "$pkgname.png::http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png")
sha256sums=('b764966421087ea21211570465ce1a6cfa10423e4f679bade558a362a12879e8'
            'f0dbcc5d80bf03d706aa8b28a322aa7f169a40813848c2d1505691f6e2c7ef00')
install=$pkgname.install
_gitroot='git://git.netsurf-browser.org/netsurf.git'
_gitname='netsurf'

build() {
  cd "$srcdir"

  msg "Connecting to the $_gitname git repository..."
  if [ -d "$srcdir/$_gitname" ] ; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot"
  fi

  msg "GIT checkout done or server timeout"
  cd "$srcdir"
  rm -rf "$srcdir/$_gitname-build"
  git clone "$_gitname" "$_gitname-build"

  ln -sf "$_gitname-build" netsurf # will not compile otherwise
  cd netsurf
  
  # TODO: Report this error upstream instead of commenting out
  msg2 "Patching..."
  sed -i 's:LOG((\"\[://:g' desktop/netsurf.c

  msg2 "Compiling..."
  make NSSHARED='/usr/share/netsurf-buildsystem' PREFIX=/usr

  msg2 "Creating wrapperscript..."
  echo "#!/bin/sh" > netsurf.sh
  echo "NETSURFRES=/usr/share/netsurf/res /usr/bin/nsgtk \$*" >> netsurf.sh
}

package() {
  cd "$srcdir/netsurf"

  msg2 "Packaging executables..."
  mkdir -p "$pkgdir/usr/bin"
  install nsgtk "$pkgdir/usr/bin"
  install -Dm755 netsurf.sh "$pkgdir/usr/bin/netsurf"
  install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/netsurf"

  msg2 "Packaging resources..."
  rm -rf gtk/res/.svn gtk/res/docs/.svn
  cp -RL gtk/res "$pkgdir/usr/share/netsurf"

  msg2 "Packaging application shortcut and icon"
  install -Dm644 "../$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "../$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  msg2 "Cleaning up..."
  find "$pkgdir" -name ".svn" -print0 | xargs -0 rm -rf
}

# vim:set ts=2 sw=2 et:
