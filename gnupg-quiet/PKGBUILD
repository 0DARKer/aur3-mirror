# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=gnupg-quiet
_pkgname=gnupg
_patchversion="1"
pkgver=2.0.19
pkgrel=6
pkgdesc='Complete and free implementation of the OpenPGP standard. Includes patch to sign quietly (no output).'
url='http://www.gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
optdepends=('curl: gpg2keys_curl'
'libldap: gpg2keys_ldap'
'libusb-compat: scdaemon')
makedepends=('curl' 'libldap' 'libusb-compat')
depends=('bzip2' 'libksba' 'libgcrypt' 'pth' 'libassuan' 'readline' 'pinentry' 'dirmngr')
source=("ftp://ftp.gnupg.org/gcrypt/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"{,.sig}
"https://github.com/graysky2/gnupg_quiet_sign/archive/v${_patchversion}.tar.gz"
'valid-keyblock-packet.patch'
'protect-tool-env.patch')
sha256sums=('efa23a8a925adb51c7d3b708c25b6d000300f5ce37de9bdec6453be7b419c622'
'2509e8ce7f23e295cbaf9471a340717baadf270521bf6bbfc5f3be736fcc3445'
'dd8c6fa055dd7116cfe856dc70a569ae3c5c98722f76ff4157ba7f86aff55984'
'f60a9c49d7aec6c98909484606a2f2ad84518681e582e07fbd2d1685870135f7'
'd196591b3777383390374a81ffa0c8ea9ca809ddb465457ab1a7e73d3fd8c1eb')

install=$pkgname.install

conflicts=('gnupg2')
provides=("gnupg2=${pkgver}" "gnupg=${pkgver}")
replaces=('gnupg2')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"

	patch -p1 -i ../protect-tool-env.patch # FS#31900
	patch -p1 -i "${srcdir}/gnupg_quiet_sign-$_patchversion/sign_quietly.patch"
	patch -p1 -i ../valid-keyblock-packet.patch
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/gnupg \
		--enable-maintainer-mode \
		--enable-symcryptrun \
		--enable-gpgtar \

	make
}

check() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -s gpg2 "${pkgdir}"/usr/bin/gpg
	ln -s gpgv2 "${pkgdir}"/usr/bin/gpgv
	ln -s gpg2.1.gz "${pkgdir}"/usr/share/man/man1/gpg.1.gz
	rm "${pkgdir}/usr/share/gnupg/com-certs.pem" # FS#33059
}
