# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=gnupg-quiet
_pkgname=gnupg
_patchversion=2
pkgver=2.0.26
pkgrel=1
pkgdesc='Free implementation of the OpenPGP standard with silent signing.'
url='http://www.gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64')
optdepends=('curl: gpg2keys_curl'
            'libldap: gpg2keys_ldap'
            'libusb-compat: scdaemon')
makedepends=('curl' 'libldap' 'libusb-compat')
depends=('bzip2' 'libksba' 'libgcrypt' 'pth' 'libassuan' 'readline' 'pinentry' 'dirmngr')
source=("ftp://ftp.gnupg.org/gcrypt/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"{,.sig}
				"https://github.com/graysky2/gnupg_quiet_sign/archive/v${_patchversion}.tar.gz")
sha1sums=('3ff5b38152c919724fd09cf2f17df704272ba192'
          'SKIP'
          '2178c2bbd5bac207b681abd7d1b9f58ff27e43bd')

install=install

conflicts=('gnupg2')
provides=("gnupg2=${pkgver}" gnupg=${pkgver})
replaces=('gnupg2')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	patch -p1 -i "${srcdir}/gnupg_quiet_sign-$_patchversion/sign_quietly.patch"
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/gnupg \
		--enable-maintainer-mode \
		--enable-standard-socket \
		--enable-symcryptrun \
		--enable-gpgtar \

	make
}

check() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -s gpg2 "${pkgdir}"/usr/bin/gpg
	ln -s gpgv2 "${pkgdir}"/usr/bin/gpgv
	ln -s gpg2.1.gz "${pkgdir}"/usr/share/man/man1/gpg.1.gz
	rm "${pkgdir}/usr/share/gnupg/com-certs.pem" # FS#33059
}
