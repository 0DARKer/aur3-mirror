# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=rapicorn
pkgver=13.03.0
pkgrel=1
pkgdesc="A toolkit for rapid development of graphical user interfaces using C++ and Python"
arch=(i686 x86_64)
url="http://rapicorn.testbit.eu/"
license=('LGPL')
depends=('python2' 'libpng12' 'pango')
options=('!libtool')
source=("http://dist.testbit.eu/rapicorn/rapicorn-$pkgver.tar.bz2")
md5sums=('f28c91c639f0eb010b4bd075ab476537')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.8 -Werror=format-security + python fix
  # https://bugzilla.redhat.com/show_bug.cgi?id=927358
  # http://bugs.python.org/issue17547
  sed -i "/FLAGS.*Werror/d" configure
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # use python 2.7
  export PYTHON=python2 PYTHON_VERSION=2.7

  # use libpng12
  sed -i "s|png.h|libpng12/&|" ui/pixmap.cc

  # DSO link fix + missing librt
  export LDFLAGS+=" -lcairo -lrt"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # python2 fix
  sed -i "s/env python$/&2/" `grep -rl "env python$" "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
