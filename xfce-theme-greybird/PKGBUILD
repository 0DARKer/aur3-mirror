# Maintainer: Bjoern Franke <bjo at nord-west.org>
# Contributor: flan_suse
#
# This suite contains the following:
#        * xfwm4 theme
#        * xfwm4 compact theme
#        * metacity theme
#        * emerald theme
#        * gtk2 theme
#        * gtk3 theme
#        * unity theme
#        * xfce4-notify theme
#        * lightdm theme
#        * wallpaper (found in the shimmer-wallpapers package; optdepends)

pkgname=xfce-theme-greybird
_pkgname=Greybird
pkgver=1.4
pkgrel=3
pkgdesc="A grey and blue Xfce theme, used by default in Xubuntu 12.04"
arch=(any)
url=http://shimmerproject.org/projects/greybird/
license=(CCPL:by-sa-3.0 GPL)
groups=(xfce-themes-shimmer-collection)
depends=(gtk-engine-murrine)
optdepends=('elementary-xfce-icons: matching icon set; use the dark icon theme'
    'gtk3: required for CSS/GTK3 theme'
    'lightdm-gtk-greeter: required for the LightDM GTK theme'
    'lightdm-unity-greeter: required for the LightDM Unity theme'
    'shimmer-wallpapers: contains the Greybird wallpaper, among others'
    'lib32-gtk-engine-murrine: required for multilib')
conflicts=($pkgname-git)
source=($pkgname-$pkgver.tar.gz::https://github.com/shimmerproject/$_pkgname/archive/v$pkgver.tar.gz
    'gtk-3.14-fix.patch'
    'fix-checkboxes-and-radiosin-gtk-3.14.patch')
sha512sums=('8b414373f01655673e29c7454f88ffffade10b8a0883e777b1b5f41706042253d04ff5f886bb70a0e01a869f4ee5cb2dbe50dde4b48df5e4956d6446aaf16621'
            'c692d921ce27b837177477329799c5191e7c7ca068989cdd74d4c4eb54e4457ba1d9296e944c9e37aea26eac604bf99d8301cac19ca38cc28e1d91ac236e0155'
            '4af34afde65259a9bf26ea3798fbfad5ce0b42a16f5fd0462c29205566739284c3823723eafc3702d8ae8b4ce9e8517ed61fe914a88a55340310421fbe1d852a')

prepare() {
	patch -p1 -d $srcdir/Greybird-1.4 -i $srcdir/gtk-3.14-fix.patch 
	patch -p1 -d $srcdir/Greybird-1.4 -i $srcdir/fix-checkboxes-and-radiosin-gtk-3.14.patch 
}



package() {
    local _themedir="$pkgdir/usr/share/themes"
    install -d "$_themedir/$_pkgname "{Classic,Compact}

    cp -rf $_pkgname-$pkgver/ "$_themedir"/$_pkgname/
    rm "$_themedir"/$_pkgname/.gitignore
    ln -s /usr/share/themes/$_pkgname/xfwm4_compact "$_themedir/$_pkgname Compact/xfwm4"
}
