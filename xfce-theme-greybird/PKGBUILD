# Maintainer: flan_suse

# This suite contains the following:
#        * xfwm4 theme
#        * xfwm4 compact theme
#        * metacity theme
#        * emerald theme
#        * gtk2 theme
#        * gtk2 theme (classic)
#        * gtk3 theme
#        * gtk3 theme (classic)
#        * unity theme
#        * xfce4-notify theme
#        * lightdm theme
#        * wallpaper

# There have been some drastic changes as of July 22nd, 2012!
# Please read through the AUR page comments if you wish to learn more.
# I have modified this PKGBUILD to compensate for the drastic changes.

pkgname=xfce-theme-greybird
_pkgname=Greybird
pkgver=0.9
_pkgverclassic=0.8.2
pkgrel=3
_gitdir=Greybird
_gitbranch=master
_giturl="git://github.com/shimmerproject/$_gitdir.git"
pkgdesc="A grey and blue Xfce theme, introduced in the release of Xubuntu 11.04 and updated for 12.04; includes the classic low saturation theme"
arch=('any')
url="http://shimmerproject.org/projects/greybird/"
license=('GPL3' 'CCPL')
groups=('xfce-themes-shimmer-collection')
depends=('gtk-engine-murrine')
makedepends=('git')
optdepends=('elementary-xfce-icons: matching icon set, use dark icon theme'
            'gtk3: required for current CSS/GTK3 theme'
            'gtk-engine-unico: required for gtk3 support for the classic theme; current theme uses pure CSS'
            'lightdm-gtk-greeter: required for the lightdm theme') 
conflicts=('xfce-theme-greybird-git')

build() {

    # Change into source directory
    cd "${srcdir}"

    # Clone or pull from GIT server, and notify user of the status
    msg "Connecting to GIT server...."
    if [[ -d $_gitdir ]] ; then
      cd "$_gitdir" && git pull origin "$_gitbranch"
      msg "The local files are updated."
    else
      git clone "$_giturl" "$_gitdir"
      cd "$_gitdir"
    fi

    # Checkout current git-tag stable release
    # Comment out the next line if you wish to use the master branch instead; not recommended
    git checkout "v$pkgver"

    # Rename current stable release to Greybird.current
    cd "${srcdir}"
    rm -rf "$_pkgname.current"
    cp -rf "$_pkgname" "$_pkgname.current"

    # Checkout classic git-tag stable release
    cd "$_gitdir"
    git checkout "v$_pkgverclassic"

    # Rename classic stable release to Greybird.classic
    cd "${srcdir}"
    rm -rf "$_pkgname.classic"
    cp -rf "$_pkgname" "$_pkgname.classic"

    # Download the wallpaper from the separate GIT repository
    # The entire Wallpapers repository is 10 MB
    # Therefor, wget'ing a single file is much faster and uses less bandwidth
    # Check the Wallpapers GIT repository if you require validation:
    # https://github.com/shimmerproject/Wallpapers
    wget "https://github.com/shimmerproject/Wallpapers/raw/master/$_pkgname/greybird-wall-1920x1200.png" -O "$_pkgname.png"

    # Notify the user that the build() process is complete
    echo "$_pkgname version $pkgver is ready to be packaged!"

}

package() {

    # Change into source directory
    cd "${srcdir}"

    # Create installation directories
    install -d -m755 "${pkgdir}/usr/share/themes/"
    install -d -m755 "${pkgdir}/usr/share/backgrounds/"
    install -d -m755 "${pkgdir}/usr/share/xfce4/backdrops/"

    # Copy the wallpaper to the correct directory
    cp -f "$_pkgname.png" "${pkgdir}/usr/share/backgrounds/$_pkgname.png"

    # Install the current theme
    cp -rf "$_pkgname.current" "${pkgdir}/usr/share/themes/$_pkgname"

    # Install the classic theme
    mkdir -p "${pkgdir}/usr/share/themes/$_pkgname Classic"
    cp -rf "$_pkgname.classic/gtk-2.0" "${pkgdir}/usr/share/themes/$_pkgname Classic"
    cp -rf "$_pkgname.classic/gtk-3.0" "${pkgdir}/usr/share/themes/$_pkgname Classic"

    # Make a symbolic link for the compact version of the xfwm4 theme
    install -d -m755 "${pkgdir}/usr/share/themes/${_pkgname} Compact"
    cd "${pkgdir}/usr/share/themes/${_pkgname} Compact"
    ln -s "../${_pkgname}/xfwm4_compact" "xfwm4"

    # Make a symbolic link for the LightDM theme
    install -d -m755 "${pkgdir}/usr/share/themes/${_pkgname} LightDM"
    cd "${pkgdir}/usr/share/themes/${_pkgname} LightDM"
    ln -s "../${_pkgname}/lightdm" "gtk-3.0"

    # Make a symbolic link inside of the Xfce backdrops directory, for use with Desktop Settings
    cd "${pkgdir}/usr/share/xfce4/backdrops/"
    ln -s "../../backgrounds/$_pkgname.png" "$_pkgname.png"

    # Remove excess files
    rm -rf "${pkgdir}/usr/share/themes/${_pkgname}/.gitignore"
    rm -rf "${pkgdir}/usr/share/themes/${_pkgname}/.git"

}