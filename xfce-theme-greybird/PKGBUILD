# Maintainer: Bjoern Franke <bjo at nord-west.org>
# Contributor: flan_suse
#
# This suite contains the following:
#        * xfwm4 theme
#        * xfwm4 compact theme
#        * metacity theme
#        * emerald theme
#        * gtk2 theme
#        * gtk2 theme (classic)
#        * gtk3 theme
#        * gtk3 theme (classic)
#        * unity theme
#        * xfce4-notify theme
#        * lightdm theme
#        * wallpaper (found in the shimmer-wallpapers package; optdepends)

pkgname=xfce-theme-greybird
_pkgname=Greybird
pkgver=1.3.4
_pkgverclassic=0.9
pkgrel=2
pkgdesc="A grey and blue Xfce theme, used by default in Xubuntu 12.04; includes the classic low saturation theme"
arch=(any)
url=http://shimmerproject.org/projects/greybird/
license=(CCPL:by-sa-3.0 GPL)
groups=(xfce-themes-shimmer-collection)
depends=(gtk-engine-murrine)
optdepends=('elementary-xfce-icons: matching icon set; use the dark icon theme'
    'gtk-engine-unico: required for gtk3 support for the classic theme; not required for the current theme'
    'gtk3: required for CSS/GTK3 theme'
    'lightdm-gtk-greeter: required for the LightDM GTK theme'
    'lightdm-unity-greeter: required for the LightDM Unity theme'
    'shimmer-wallpapers: contains the Greybird wallpaper, among others')
conflicts=($pkgname-git)
source=($pkgname-$pkgver.tar.gz::https://github.com/shimmerproject/$_pkgname/archive/v$pkgver.tar.gz
    $pkgname-$_pkgverclassic.tar.gz::https://github.com/shimmerproject/$_pkgname/archive/v$_pkgverclassic.tar.gz)
sha256sums=('d4c3ea3445d191f639fd045feea43f7139375da94478c3789f0a319f24c3ff4a'
    '661f81347fe227bc3dc7ae40ee0e9bf1c96b478fab7283416a8569379a0aec4f')
sha512sums=('78bb9372d9c05c7c83ae130a3559f7b1f413bb0519d5f626d477bef917ca35892c7b0235872a084720fdba5aa5b381401fb36f7fa11dea00bc0986c76670c286'
    '599fa02ea6eeb6f805c06f7e57ea86d6b620a28a24c5439b0a826439e04a7ac245a95c86c8ee5a4550c070308174564a427dd80fa8538c90e1439b9f697e17c4')

package() {
    local _themedir="$pkgdir/usr/share/themes"
    install -d "$_themedir/$_pkgname "{Classic,Compact}

    cp -rf $_pkgname-$pkgver/ "$_themedir"/$_pkgname/
    rm "$_themedir"/$_pkgname/.gitignore
    cp -rf $_pkgname-$_pkgverclassic/gtk-{2,3}.0 "$_themedir/$_pkgname Classic"
    ln -s /usr/share/themes/$_pkgname/xfwm4_compact "$_themedir/$_pkgname Compact/xfwm4"
}
