# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=gerbil
pkgver=0.4a
pkgrel=1
pkgdesc="An interactive visualization tool targeted at multispectral and hyperspectral image data"
arch=(i686 x86_64)
url="http://sourceforge.net/p/gerbil/home/"
license=('GPL3')
depends=('boost-libs' 'opencv' 'qt')
makedepends=('boost' 'cmake')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('2dfb6c87536ac60f2f1e304fda1812b4'
         '95c325def4edada78a7841429ec19aa8')

build() {
  cd "$srcdir/$pkgname-${pkgver::-1}"
  ./configure.sh /usr norpath
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver::-1}"

  # cli
  install -Dm755 bin/$pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # qt gui
  install -Dm755 bin/q$pkgname \
    "$pkgdir/usr/bin/q$pkgname"

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 doc/{file_format,introduction}.txt \
    "$pkgdir/usr/share/doc/$pkgname"

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 $pkgname-gui/logo/icon.png \
    "$pkgdir/usr/share/pixmaps/q$pkgname.png"
}
