# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gerbil
pkgver=0.3
pkgrel=1
pkgdesc="An interactive visualization tool targeted at multispectral and hyperspectral image data"
arch=(i686 x86_64)
url="http://sourceforge.net/p/gerbil/home/"
license=('GPL3')
depends=('boost-libs' 'opencv>=2.2' 'qt')
makedepends=('boost' 'cmake')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop" "$pkgname-build.patch")
md5sums=('57a76dee24e3dd9c3a74819cb1bda50f'
         '95c325def4edada78a7841429ec19aa8'
         '88d8646d558e60579e72d38e8bdb950b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # rpath, permissive and DSO link patch
  patch -p1 -i ../$pkgname-build.patch

  ./configure.sh /usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # cli
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # qt gui
  install -Dm755 q$pkgname \
    "$pkgdir/usr/bin/q$pkgname"

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 doc/{file_format,introduction}.txt \
    "$pkgdir/usr/share/doc/$pkgname"

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 $pkgname-gui/logo/icon.png \
    "$pkgdir/usr/share/pixmaps/q$pkgname.png"
}
