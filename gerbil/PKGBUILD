# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gerbil
pkgver=0.2
pkgrel=3
pkgdesc="An interactive visualization tool targeted at multispectral and hyperspectral image data"
arch=(i686 x86_64)
url="http://sourceforge.net/p/gerbil/home/"
license=('GPL3')
depends=('boost-libs' 'opencv>=2.2' 'qt')
makedepends=('boost' 'cmake')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('4979ea8fa8e0427a428697c157fccc66'
         '95c325def4edada78a7841429ec19aa8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # disable rpath
  sed -i '/^.$/i-DCMAKE_SKIP_RPATH=on \\' configure.sh

  ./configure.sh /usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # cli
  install -Dm755 $pkgname \
    "$pkgdir/usr/bin/$pkgname"

  # qt gui
  install -Dm755 q$pkgname \
    "$pkgdir/usr/bin/q$pkgname"

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 doc/{file_format,introduction}.txt \
    "$pkgdir/usr/share/doc/$pkgname"

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 $pkgname-gui/logo/icon.png \
    "$pkgdir/usr/share/pixmaps/q$pkgname.png"
}
