# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libindicate
pkgver=0.5.0
pkgrel=1
pkgdesc="A small library for applications to raise "flags" on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
depends=('libdbusmenu>=0.3.97' 'dbus-glib>=0.76' 'gtk2>=2.12')
makedepends=('gtk-sharp-2>=2.12.1' 'pygtk>=2.14.0' 'gtk-doc>=1.4')
options=(!libtool)
source=(http://launchpad.net/$pkgname/0.5/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('d7e3a2905dac35144547af5b21aab9e9')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/-lpyglib-2\.0-python$PYTHON_VERSION/-lpyglib-2.0-python2/' configure{,.ac}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-introspection \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Remove unneeded examples
  rm -r "$pkgdir/usr/share/doc"
}
