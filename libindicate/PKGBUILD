# Maintainer: György Balló <ballogy@freestart.hu>
pkgname=libindicate
pkgver=0.6.1
pkgrel=4
pkgdesc="A small library for applications to raise 'flags' on DBus for other components of the desktop to pick up and visualize"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
makedepends=('libdbusmenu>=0.3.97' 'dbus-glib>=0.76' 'gobject-introspection>=0.6.7' 'vala' 'gtk-sharp-2' 'gnome-doc-utils>=0.3.2')
options=(!libtool !emptydirs)
source=(http://launchpad.net/$pkgname/0.6/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('dc7ba61069900937868d3e8fddb403f7')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' examples/{im-client,listen-and-print}.py

  # Use different location for header files for GTK+3
  sed -i 's/libindicate-0.6/libindicate3-0.6/' libindicate-gtk/indicate-gtk3-0.6.pc.in
  sed -i 's/libindicate-0.6/libindicate$(VER)-0.6/' libindicate-gtk/Makefile.in

  # Fix pyglib name
  sed -i 's/pyglib-2.0-python$PYTHON_VERSION/pyglib-2.0-python2/' configure

  # Fix mono location
  sed -i 's|indicate-sharp-0.1|mono/indicate-sharp|' bindings/mono/indicate/indicate-sharp-0.1.pc.in
  sed -i 's|indicate-gtk-sharp-0.1|mono/indicate-gtk-sharp|' bindings/mono/indicate-gtk/indicate-gtk-sharp-0.1.pc.in
  sed -i 's|$(libdir)/$(ASSEMBLY_NAME)-sharp-0.1|$(libdir)/mono/$(ASSEMBLY_NAME)-sharp|
          s|-package $(ASSEMBLY_NAME) |-package $(ASSEMBLY_NAME)-sharp |' bindings/mono/indicate{,-gtk}/Makefile.in

  # Disable building tests
  sed -i '/DIST_SUBDIRS/ s/tests//
          /tests/ d' Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-gtk --disable-python \
              PYTHON=python2
  make
}

package() {
  depends=('libdbusmenu>=0.3.97' 'dbus-glib>=0.76')

  cd "$srcdir/$pkgname-$pkgver"

  make -C libindicate DESTDIR="$pkgdir/" install
  make -C bindings/mono/indicate DESTDIR="$pkgdir/" install
  make -C docs DESTDIR="$pkgdir/" install
}
