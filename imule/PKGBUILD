# Maintainer: grufo <grossomalpelo at gmail dot com>

pkgname=imule
pkgver=2.3.2.0
pkgrel=2
pkgdesc="Anonymous P2P file sharing software"
arch=("i686" "x86_64")
url="http://www.imule.i2p.tin0.de"
license=("GPL")
depends=("wxgtk" "crypto++")
conflicts=("amule")
optdepends=("i2p: anonymous routing")
source=("http://aceini.no-ip.info/${pkgname}/${pkgver}/iMule-${pkgver}-src.tbz")
md5sums=("c676f578a828a3ab23cfb5855940d458")

build() {
	cd "${srcdir}/iMule-${pkgver}-src"
	sed "s/wxString::wxString/wxString/g" -i src/PrefsUnifiedDlg.cpp
	./configure --prefix=/usr --disable-profile --disable-debug --enable-optimize \
	--enable-webserver --enable-irouter --enable-imule-gui --enable-imule-daemon \
	--enable-cas --enable-wxcas --enable-alc --enable-alcc --enable-utf8-systray \
	--enable-plasmamule # --enable-imulecmd --enable-fileview
	make
}

check() {
	cd "${srcdir}/iMule-${pkgver}-src"
	make -k check
}

package() {
	cd "${srcdir}/iMule-${pkgver}-src"
	make DESTDIR=${pkgdir} install
}
