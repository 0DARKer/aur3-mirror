# Maintainer: Stefano Gioffr√© <grossomalpelo at gmail dot com>
# Contributor: Cedric Chabanois <cchabanois at gmail dot com>

pkgname=imule
pkgver=2.3.2.0
pkgrel=1
pkgdesc="Anonymous P2P file sharing software"
arch=("i686" "x86_64")
url="http://www.imule.i2p.tin0.de"
license=("GPL")
conflicts=("amule")
depends=("wxgtk" "crypto++")
optdepends=("i2p")
source=("http://aceini.no-ip.info/${pkgname}/${pkgver}/iMule-${pkgver}-src.tbz")
md5sums=("c676f578a828a3ab23cfb5855940d458")

build() {
	cd "${srcdir}/iMule-${pkgver}-src"
	sed "s/wxString::wxString/wxString/g" -i src/PrefsUnifiedDlg.cpp
	./configure --enable-debug=no --enable-optimize=yes --prefix=/usr --disable-profile
	make
}

check() {
	cd "${srcdir}/iMule-${pkgver}-src"
	make -k check
}

package() {
	cd "${srcdir}/iMule-${pkgver}-src"
	make DESTDIR=${pkgdir} install
}
