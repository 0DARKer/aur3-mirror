# Maintainer: Bertrand Bonnefoy-Claudet <bertrandbc@gmail.com>
# Contributor: Brice Waegeneire <brice dot wge at gmail dot com>

_gemname=veewee
pkgname=ruby-$_gemname
pkgver=0.3.12
pkgrel=4
pkgdesc="A tool for building custom Vagrant base boxes and virtual machine images."
arch=(any)
url="https://github.com/jedi4ever/veewee"
license=('MIT')
depends=('ruby' 'ruby-ansi-1.3' 'ruby-childprocess'
         'ruby-fission-0.4.0' 'ruby-fog>=1.8' 'ruby-grit' 'ruby-highline'
         'ruby-i18n' 'ruby-net-ssh>=2.2.0' 'ruby-os>=0.9.6'
         'ruby-popen4>=0.1.2' 'ruby-progressbar' 'ruby-rspec>=2.5'
         'ruby-ruby-vnc-1.0' 'ruby-thor>=0.15')
provides=('veewee')
conflicts=('veewee')
source=(http://rubygems.org/downloads/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
sha512sums=('9fd96282ad39bb036f79a8bb796309c0effffa76cd1746e182a5eaebbf7d0bb79d8149832a7bec97a37203e6882dd1a2f9de0015139b79d599fb4f9af311b6de')

package() {
  cd "$srcdir"
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"
  gem install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
   -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"

  install -Dm644 $(find "${pkgdir}" -name License) \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
