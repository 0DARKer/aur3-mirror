# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: sergeantspoon <sergeantspoon@archlinux.us>
# Contributor: Speedboy <theknight696.linux AT gmail DOT com>
# Maintainer:  Gimmeapill <gimmeapill at gmail DOT com>

pkgname=hdjmod
pkgver=1.28
pkgrel=1
pkgdesc="Hercules DJ kernel module"
arch=('i686' 'x86_64')
url="http://ts.hercules.com/eng/"
license=('GPL2')
optdepends=('hdjcpl')
makedepends=('rpmextract' 'kernel26>=2.6.37' 'kernel26-headers')
install=hdjmod.install

source=('ftp://ftp.hercules.com/pub/webupdate/DJCSeries/Hercules_DJSeries_Linux.tgz'
	'hdjmod.diff'
	'kernel_2.6.31_fix.patch'
	'kernel_2.6.35_fix.diff'
	'dj_console_mp3_e2.patch'
	'kernel_2.6.37_fix.patch')
	
md5sums=('498cf0ff144f20106718932ab22571ce'
         '464f8a8eda9d4c2ec83cf1c43e5dbee3'
         '405a57365935fa003514ac2370325de1'
         '77b9b94f97e99d85f9e9322552f01923'
         'f9e6f12c5f59c7bdb44d813cd42c9b27'
         '132528f078a26775ca8c7c4c3ebfe748')

build() {
	# Obtain source files
	cd $srcdir/kernel\ module
	/usr/bin/rpmextract.sh hdjmod-dkms-1.28-1.noarch.rpm
	
	# Delete the old source folder	
	rm -rf $srcdir/hdjmod-1.28
	
	# Move the source
	mv usr/src/hdjmod-1.28 $srcdir
	cd $srcdir/hdjmod-1.28

	# First patch makes installing easier
	patch -p0 < $srcdir/hdjmod.diff

	# Second patch fixes APIs that were changed in kernel >= 2.6.31
	patch -p0 < $srcdir/kernel_2.6.31_fix.patch

	# Third patch fixes Build errors with a kernel >= 2.6.35 
	patch -p0 < $srcdir/kernel_2.6.35_fix.diff
	
	# Fourth patch adds supports for the DJ Console MP3 e2
	patch -p0 < $srcdir/dj_console_mp3_e2.patch
	
	# Fifth patch fixes build errors with kernel 2.6.37
	patch -p0 < $srcdir/kernel_2.6.37_fix.patch

	# Compile...
	make || return 1
	make DESTDIR="$pkgdir" install || return 1
}
