# Maintainer: Joris Steyn <jorissteyn@gmail.com>
#
# This package has the following benefits over PEAR's pman:
#  * always up to date (pman is not automated, see https://bugs.php.net/bug.php?id=51410)
#  * no ambiguous wrapper, just use man php_{symbol} (this also allows shell auto completion)
#  * no collision with libc symbols (strlen vs PHP's strlen), categorized in man (7)
#
# Info:
#   * https://wiki.php.net/doc/phd/install
#   * the HOWTO.RELEASE file in php-phd-git

pkgname=php-manpages-svn
pkgver=20130407
pkgrel=1
pkgdesc='Manual pages for PHP, generated from latest documentation (alternative to pman)'
arch=('any')
url='https://wiki.php.net/doc'
license=('PHP')
depends=()
conflicts=()
provides=('php-manpages')
makedepends=('svn' 'php' 'php-phd-git')

_svnurl='https://svn.php.net/repository/phpdoc/modules/doc-en'

build() {
    cd "$srcdir"

    # checkout if not already there
    if [ ! -d phpdoc-en ]; then
        msg "Downloading manual sources"
        svn co ${_svnurl} phpdoc-en 
    fi

    # generate manual in raw XML format
    php phpdoc-en/doc-base/configure.php --output=php-manual.xml

    # use PhD_PHP to convert to manpage format 
    php /usr/share/php/phd/render.php -d php-manual.xml -P PHP -f manpage

    # not generation pear package here, use man php_{symbol}
    #php /usr/share/php/phd/package-pman.php

    # clean previously generated manpages
    [ ! -d man7 ] && mkdir man7

    # normalize names (7.gz expected by mandb), prefix with php_
    # so we can have fancy shell autocompletion when invoking man
    cd "$srcdir"/output/php-functions/
    for file in *\.gz; do
        mv "$file" "$srcdir"/man7/php_"${file/3\.gz/7.gz}"
    done
}

package() {
    install -d $pkgdir/usr/share/man/man7/
    install -m 644 $srcdir/man7/php_*.gz $pkgdir/usr/share/man/man7/
}

