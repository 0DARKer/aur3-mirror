# Maintainer: Joris Steyn <jorissteyn@gmail.com>
#
# Example usage:
#  $ man -m php strlen
#  $ man -m php SplFileInfo.getInode
#
# More info:
#  * https://wiki.php.net/doc/phd/install
#  * the HOWTO.RELEASE file in php-phd-git

pkgname=php-manpages-svn
pkgver=330044
pkgrel=2
pkgdesc='Manual pages for PHP, generated from latest documentation (alternative to pman)'
arch=('any')
url='https://wiki.php.net/doc'
license=('PHP')
provides=('php-manpages')
makedepends=('php' 'php-phd-git')
source=('phpdoc-en::svn+https://svn.php.net/repository/phpdoc/modules/doc-en')
md5sums=('SKIP')

pkgver() {
    cd "$SRCDEST"/phpdoc-en
    svnversion | tr -d [A-z]
}

build() {
    cd "$srcdir"

    # generate manual in raw XML format
    php phpdoc-en/doc-base/configure.php --output=php-manual.xml

    # use PhD_PHP to convert to manpage format 
    php /usr/share/php/phd/render.php -d php-manual.xml -P PHP -f manpage

    # not generation pear package here, use man php_{symbol}
    #php /usr/share/php/phd/package-pman.php

    # clean previously generated manpages
    [ ! -d man3 ] && mkdir man3

    # prefix with php_
    cd "$srcdir"/output/php-functions/
    for file in *\.gz; do
        mv "$file" "$srcdir"/man3/"$file"
    done
}

package() {
    install -d "$pkgdir"/usr/share/man/php/man3
    install -m 644 "$srcdir"/man3/*.gz "$pkgdir"/usr/share/man/php/man3/
}

