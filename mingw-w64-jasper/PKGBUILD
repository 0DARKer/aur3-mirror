pkgname=mingw-w64-jasper
pkgver=1.900.1
pkgrel=2
pkgdesc="A software-based implementation of the codec specified in the emerging JPEG-2000 Part-1 standard (mingw-w64)"
arch=(any)
url="http://www.ece.uvic.ca/~mdadams/jasper"
license=("custom:JasPer2.0")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt mingw-w64-libjpeg-turbo)
options=(!libtool !strip !buildflags)
source=("http://www.ece.uvic.ca/~mdadams/jasper/software/jasper-${pkgver}.zip"
"jpc_dec.c.patch"
"patch-libjasper-stepsizes-overflow.diff"
"jasper-1.900.1-CVE-2008-3520.patch"
"jasper-1.900.1-CVE-2008-3522.patch")
md5sums=('a342b2b4495b3e1394e161eb5d85d754'
         '36de7128eea6f701c1e2e13ce5bd8d37'
         '24785d8eb3eea19eec7e77d59f3e6a25'
         '911bb13529483c093d12c15eed4e9243'
         'ed441f30c4231f319d9ff77d86db2ef9')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/jasper-$pkgver"
  patch -p1 -i "${srcdir}/jpc_dec.c.patch"
  patch -p1 -i "${srcdir}/patch-libjasper-stepsizes-overflow.diff"
  patch -p1 -i "${srcdir}/jasper-1.900.1-CVE-2008-3520.patch"
  patch -p1 -i "${srcdir}/jasper-1.900.1-CVE-2008-3522.patch"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-shared \
      --enable-static
    make
    mkdir -p ".dllbuild"
    find "${srcdir}/${pkgname}-${pkgver}-build-${_arch}/src/libjasper" \
      -name '*.o' | xargs -rtl1 -I {} cp {} ".dllbuild"
    ${_arch}-gcc -shared .dllbuild/*.o /usr/${_arch}/lib/libjpeg.dll.a -o libjasper.dll \
      -Xlinker --out-implib -Xlinker libjasper.dll.a
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -m644 "libjasper.dll" "$pkgdir/usr/${_arch}/bin/"
    install -m644 "libjasper.dll.a" "$pkgdir/usr/${_arch}/lib/"
    find "$pkgdir/usr/${_arch}" -name '*.exe' -o -name '*.bat' -o -name '*.def' -o -name '*.exp' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/man"
  done
}