# Contributor: Naoya Inada <inaoya@kushinada.org>
#     Updated: Yuki Chiba <chiba@jaist.ac.jp>

pkgname=smlsharp
pkgver=1.2.0
pkgrel=1
pkgdesc="A new programming language in the Standard ML family"
arch=('i686' 'x86_64')
url="http://www.pllab.riec.tohoku.ac.jp/smlsharp/"
license=('custom')
depends=('gmp')
[ "$CARCH" == "x86_64" ] && depends=('lib32-gmp')
makedepends=("ed" 'cpio' )
source=(http://www.pllab.riec.tohoku.ac.jp/smlsharp/download/$pkgname-$pkgver.tar.gz)
md5sums=('a77a3ed172bca1b382c178a8e199009b')

build() {
  cd $srcdir/$pkgname-$pkgver
  if [ "$CARCH" == "i686" ]; then
    ./configure --prefix=/usr --enable-thread --enable-fast-build
  else
    ./configure CC='gcc -m32' LD='ld -m elf_i386' --prefix=/usr --enable-thread --enable-fast-build
  fi

  make
}

package(){
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  for file in LICENSE SMLNJ_LICENSE; do
    install -D -m 644 $file $pkgdir/usr/share/licenses/$pkgname/$file
  done
}
