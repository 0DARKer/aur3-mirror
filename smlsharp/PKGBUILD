# Contributor: Naoya Inada <inaoya@kushinada.org>
#     Updated: Yuki Chiba <chiba@jaist.ac.jp>

pkgname=smlsharp
pkgver=2.0.0
pkgrel=1
pkgdesc="A new programming language in the Standard ML family"
arch=('i686' 'x86_64')
url="http://www.pllab.riec.tohoku.ac.jp/smlsharp/"
license=('custom')
depends=('gmp')
[ "$CARCH" == "x86_64" ] && depends=('lib32-gmp')
makedepends=("ed" 'cpio' 'llvm' )
[ "$CARCH" == "x86_64" ] && makedepends=("ed" 'cpio' 'lib32-llvm' )
source=(http://www.pllab.riec.tohoku.ac.jp/smlsharp/download/$pkgname-$pkgver.tar.gz)
md5sums=('df71724704c967a51e7c1db77ee5d0b1')
options=(libtool staticlibs)

build() {
  cd $srcdir/$pkgname-$pkgver
  if [ "$CARCH" == "i686" ]; then
    ./configure --prefix=/usr
  else
    ./configure CC='gcc -m32' CXX='g++ -m32' LD='ld -m elf_i386' LLVM_CONFIG=/usr/bin/llvm-config32 --prefix=/usr
  fi

  make
}

package(){
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  for file in LICENSE SMLNJ_LICENSE; do
    install -D -m 644 $file $pkgdir/usr/share/licenses/$pkgname/$file
  done
}
