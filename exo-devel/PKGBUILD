# Maintainer:  cuihao <cuihao dot leo at gmail dot com>

# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be> 
# Contributor: Aurelien Desbrieres <ice.cube@gmx.com>

# Original PKGBUILD:
# $Id: PKGBUILD 141087 2011-10-23 16:12:32Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo-devel
_pkgname=exo
pkgver=0.7.2
pkgrel=1
pkgdesc="Extensions to Xfce by os-cillation (Development Version)"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL')
url="http://www.xfce.org/projects/exo"
depends=("libxfce4util>=4.8" "libxfce4ui-devel>=4.9" 'gtk2' 'hicolor-icon-theme' )
makedepends=('pygtk' 'perl-uri' 'intltool' 'gtk-doc' 'xorg-iceauth')
optdepends=('perl-uri: for mail-compose helper script')
options=('!libtool')
install=${_pkgname}.install
source=(http://archive.xfce.org/src/xfce/${_pkgname}/0.7/${_pkgname}-${pkgver}.tar.bz2)
md5sums=('a4a56730d16f15016ccfa5e7c854feeb')

groups=('xfce4-devel')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname" "$_pkgname-git")

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  # python2 fix
  export PYTHON=python2
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var  \
	--disable-static \
	--enable-gio-unix \
	--enable-gtk-doc \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
