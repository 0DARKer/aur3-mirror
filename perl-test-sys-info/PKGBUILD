# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

pkgname=perl-test-sys-info
_pkgname=Test-Sys-Info
pkgver=0.20
pkgrel=3
pkgdesc="Test::Sys::Info - Centralized test suite for Sys::Info."
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url="http://search.cpan.org/dist/$_pkgname"
source=(http://search.cpan.org/CPAN/authors/id/B/BU/BURAK/$_pkgname-$pkgver.tar.gz)
sha512sums=('d403073fd426ff420cbc8bd8e9846a1b653d2ccb04aa45229a99717a53f0e02279b74d8d0e4c63c70ee2471e24d084d6d9bcecaa5a5be64b1d4e1f7c9a77230a')

build() {
  PERL=/usr/bin/perl
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "$srcdir/${_pkgname}-${pkgver}"
  $PERL Makefile.PL
  make
}

check() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
