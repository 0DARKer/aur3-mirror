# Maintainer: TDY <tdy@gmx.com>

pkgname=antivir-resident
pkgver=2.1.12_19
pkgrel=5
pkgdesc="Avira's Personal UNIX Workstation Antivirus with AvGuard"
arch=('i686' 'x86_64')
url="http://www.free-av.com/"
license=('custom')
depends=('java-runtime>=6' 'kernel26rsbac-daz' 'rsbac-admin')
#_uiflag=cli
[[ $_uiflag == cli ]] && unset depends[0] || unset _uiflag
[[ $CARCH = x86_64 ]] && depends=('lib32-glibc' ${depends[@]})
provides=('antivir')
conflicts=('antivir')
backup=(etc/av{guard,updater}.conf)
options=('!strip')
install=$pkgname.install
source=(http://dlce.antivir.com/down/unix/packages/antivir-workstation-pers.tar.gz
        http://dlce.antivir.com/down/windows/hbedv.key
        $pkgname.desktop avgrd avupd)
md5sums=('c2a5017b69cd959c76135af8fa44fe60'
         '9a4a667fcef5963f330824858f28be44'
         'a29e7d0a5d65d7cba0827c6acdfa16e8'
         'dcd7c6284e6ffaa2c53cda793ebb1f0b'
         '96ac5c9deb8f3d29b832c9d9718f7ea4')

build() {
  cd "$srcdir/antivir-workstation-pers-${pkgver/_/-}"
  install -dm755 "$pkgdir"/usr/{share/doc/antivir,{,s}bin}
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/antivir/LICENSE"
  install -Dm644 doc/* "$pkgdir/usr/share/doc/antivir/"

  msg "Packaging base scanner..."
  install -Dm755 bin/linux_glibc22/antivir "$pkgdir/usr/lib/AntiVir/antivir"
  install -Dm644 vdf/* "$pkgdir/usr/lib/AntiVir/"
  install -Dm644 ../hbedv.key "$pkgdir/usr/lib/AntiVir/hbedv.key"
  install -Dm755 script/configantivir "$pkgdir/usr/lib/AntiVir/configantivir"
  ln -sf /usr/lib/AntiVir/antivir "$pkgdir/usr/bin/antivir"

  msg "Packaging internet updater..."
  install -Dm644 etc/avupdater.conf "$pkgdir/etc/avupdater.conf"
  install -Dm755 script/avupdater "$pkgdir/usr/lib/AntiVir/avupdater"
  install -Dm755 ../avupd "$pkgdir/etc/rc.d/avupd"
  ln -sf /usr/lib/AntiVir/avupdater "$pkgdir/usr/sbin/avupdater"

  msg "Packaging front-end..."
  if [[ $_uiflag == cli ]]; then
    install -Dm644 etc/avguard.conf "$pkgdir/etc/avguard.conf"
    msg2 "CLI done"
  else
    install -Dm644 etc/avguard.conf-gui "$pkgdir/etc/avguard.conf"
    install -Dm755 script/antivir-gui "$pkgdir/usr/lib/AntiVir/antivir-gui"
    install -Dm644 ../$pkgname.desktop \
      "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm644 contrib/applet/data/avira-guard-icon.png \
      "$pkgdir/usr/share/pixmaps/avira.png"
    bsdtar -xf gui/gui_workstation_common.tgz -C "$pkgdir/usr/lib/AntiVir/"
    bsdtar -xf gui/gui_workstation_linux_glibc22.tgz -C \
      "$pkgdir/usr/lib/AntiVir/"
    ln -sf /usr/lib/AntiVir/antivir-gui "$pkgdir/usr/bin/antivir-gui"
    msg2 "GUI done"
  fi

  msg "Packaging resident guard..."
  sed -i 's#^\(IncludePath /\)home#\1#' "$pkgdir/etc/avguard.conf"
  install -Dm755 script/avguard "$pkgdir/usr/lib/AntiVir/avguard"
  install -Dm755 ../avgrd "$pkgdir/etc/rc.d/avgrd"
  ln -sf /usr/lib/AntiVir/avguard "$pkgdir/usr/sbin/avguard"
}
