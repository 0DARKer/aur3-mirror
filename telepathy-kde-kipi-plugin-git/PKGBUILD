# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-kipi-plugin-git
pkgver=34.c7f29e7
pkgrel=1
pkgdesc="A Kipi Plugin to send images through telepathy"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/network/telepathy/ktp-kipi-plugin"
license=('GPL' 'LGPL')
depends=('telepathy-kde-common-internals-git' 'libkipi' 'libkdcraw' 'libkexiv2')
makedepends=('cmake' 'automoc4' 'git')
source=('git://anongit.kde.org/ktp-kipi-plugin')
md5sums=('SKIP')

pkgver() {
  cd ktp-kipi-plugin
  echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
  mkdir -p build
  cd build

  cmake ../ktp-kipi-plugin \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
