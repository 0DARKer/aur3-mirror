# Maintainer: Philip Abernethy <chais.z3r0@gmail.com>

pkgname="symphony"
pkgver=1
pkgrel=1
epoch=1401158846
pkgdesc="A musical top down shooter"
arch=('x86_64' 'i686')
url="http://www.symphonygame.com/"
license=('custom:commercial')
makedepends=('unzip')
depends=('sdl2' 'fmodex')

DLAGENTS+=('hib::/usr/bin/echo "Could not find %u. Manually download it to \"$(pwd)\", or set up a hib:// DLAGENT in /etc/makepkg.conf."; exit 1')

source=("hib://Symphony_Linux_${epoch}.sh")
sha512sums=("ccde087dcd1b0d7a48f86767e013aaefd6143653b75e8a590ade79da5fbbac8f09e83fb55831c112e723ed8e357f8582baba03f4d89cc27f5d7c6fcf71e89755")

package() {
	if [ "$CARCH" = "x86_64" ]; then
		install -Dm 755 "${srcdir}/data/x86_64/Symphony.bin.x86_64" "${pkgdir}/opt/${pkgname}/${pkgname}"
	else
		install -Dm 755 "${srcdir}/data/x86/Symphony.bin.x86" "${pkgdir}/opt/${pkgname}/${pkgname}"
	fi
	mv "${srcdir}"/data/noarch/* ${pkgdir}/opt/${pkgname}/
	rm "${pkgdir}/opt/${pkgname}/README.linux"
	mkdir -p "${pkgdir}"/usr/{lib,bin,share/applications}
	ln -s libfmodex.so ${pkgdir}/usr/lib/libfmodex-4.44.20.so
	ln -s "/opt/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
	echo "[Desktop Entry]
Type=Application
Name=Symphony
GenericName=Symphony
Comment=$pkgdesc
Icon=/opt/${pkgname}/Symphony.png
Exec=/usr/bin/${pkgname}
Categories=Game;
Path=/opt/${pkgname}" > "$pkgdir/usr/share/applications/${pkgname}.desktop"
}
