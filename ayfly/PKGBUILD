# Maintainer: archtux <antonio dot arias99999 at gmail dot com>

pkgname=ayfly
pkgver=0.0.25
pkgrel=1
pkgdesc="Ayfly is a AY-891x emulator and player.(ZX Spectrum chiptunes)"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ayfly/"
license=('GPL2')
depends=('wxgtk2.8')
source=(http://ayfly.googlecode.com/files/$pkgname-$pkgver.tar.bz2
        $pkgname.desktop)
md5sums=('db3fe9e6657217621fcfc5263a6f2bc6'
         '818b2339a2d43ecdc877aabd7afeceb5')

prepare() {
  cd $srcdir/$pkgname-$pkgver  

  # Use wxGTK2.8
  sed -i 's|wx-config|wx-config-2.8|g' configure.ac

  ./autogen.sh
  ./configure --prefix=/usr
}

build() {
  cd $srcdir/$pkgname-$pkgver  
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  
  # Desktop icon
  install -Dm644 ../$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 src/gui/images/icon.xpm $pkgdir/usr/share/pixmaps/$pkgname.xpm
}