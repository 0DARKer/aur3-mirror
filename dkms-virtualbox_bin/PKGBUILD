# Maintainer: Jason Melton <jason.melton@gmail.com>
# This is basically the same as the virtualbox_bin PKGBUILD with some very minor changes for DKMS.

pkgname=dkms-virtualbox_bin
pkgver=4.0.12
_build=72916

pkgrel=1
pkgdesc='DKMS-enabled Oracle VM VirtualBox Binary Edition'
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL2')
depends=('dkms' 'libidl2' 'libxcursor' 'libxinerama' 'libxslt' 'curl' 'python2')
optdepends=('virtualbox-ext-oracle: for Oracle extensions'
            'qt: for GUI support'
            'sdl: for VBoxSDL and GUI support'
            'mesa: for OpenGL support'
            'libgl: for shared OpenGL support'
            'libxt: for shared clipboard support'
            'alsa-lib: for ALSA support'
            'pulseaudio: for PulseAudio support'i)
provides=("virtualbox=${pkgver}")
conflicts=('virtualbox' 'virtualbox-ose' 'virtualbox-modules' 'virtualbox_bin')
backup=('etc/vbox/vbox.cfg' 'etc/conf.d/vboxdrv' 'etc/conf.d/vboxweb')
install='install'

_arch='x86'
[ "${CARCH}" = 'x86_64' ] && _arch='amd64'
source=(
  "VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run::http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run"
  '10-vboxdrv.rules'
  '60-vboxguest.rules'
  'vboxdrv.rc'
  'vboxdrv.conf'
  'vboxweb.rc'
  'vboxweb.conf'
)
md5sums=('d020868f3d255a913eb9c9dbee2b7ff8'
         '98859bfca9ef2ebf2ea43eb9123316fc'
         '13ff08388a54fd48cc04523380f26af6'
         '5eba2081b63d1b1ae0c77c3c94d421f3'
         '2d5a0c5275bf660da9f30f75820b6078'
         '9d5f1eeea43f23d4696e3568671a4c4d'
         '3ac185709bfe688bb753c46e170d0546')

[ "${CARCH}" = 'i686' ] && md5sums[0]='c072af8d97d8bb56fce1e0332eaf94b6'

build() {                                                             
# Check and unpack the run package via sh(1)
  sh "VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run" --check
  echo yes | sh "VirtualBox-${pkgver}-${_build}-Linux_${_arch}.run" --target "${srcdir}" \
    --nox11 --noexec &> /dev/null

  # Unpack bundled files
  install -d "${pkgdir}/opt/VirtualBox"
  cd "${pkgdir}/opt/VirtualBox"
  tar -xjf "${srcdir}/VirtualBox.tar.bz2"

  # Hardened build: Mark binaries suid root, create symlinks for working around
  #                 unsupported $ORIGIN/.. in VBoxC.so and make sure the
  #                 directory is only writable by the user (paranoid).
  chmod 4511 VirtualBox VBox{SDL,Headless,NetDHCP}
  for _lib in VBox{VMM,REM,RT,DDU,XPCOM}.so; do
    ln -sf "/opt/VirtualBox/${_lib}" "components/${_lib}"
  done
  chmod go-w .

  # VBoxNetAdpCtl needs to be suid root in any case
  #chmod 4511 VBoxNetAdpCtl

  # Patch "vboxshell.py" to use Python 2.x instead of Python 3
  sed -i 's#/usr/bin/python#\02#' "${pkgdir}/opt/VirtualBox/vboxshell.py"

  # Install the SDK
  cd "${pkgdir}/opt/VirtualBox/sdk/installer"
  VBOX_INSTALL_PATH="/opt/VirtualBox" python2 vboxapisetup.py install --root "${pkgdir}"
  rm -r -f build
  cd "${pkgdir}/opt/VirtualBox"

  # Update Arch initscripts way of life in VBox.sh
  sed -i -e 's,sudo /etc/init.d/vboxdrv setup,/etc/rc.d/vboxdrv setup,g' \
    "${pkgdir}/opt/VirtualBox/VBox.sh"
  sed -i -e 's,sudo /etc/init.d/vboxdrv restart,/etc/rc.d/vboxdrv restart,g' \
    "${pkgdir}/opt/VirtualBox/VBox.sh"

  # Install vboxdrv initscript
  install -Dm0755 "${srcdir}/vboxdrv.rc" "${pkgdir}/etc/rc.d/vboxdrv"
  install -Dm0644 "${srcdir}/vboxdrv.conf" "${pkgdir}/etc/conf.d/vboxdrv"
  
  # Install vboxweb initscript
  install -Dm0755 "${srcdir}/vboxweb.rc" "${pkgdir}/etc/rc.d/vboxweb"
  install -Dm0644 "${srcdir}/vboxweb.conf" "${pkgdir}/etc/conf.d/vboxweb"

  # Install udev rules
  install -Dm0644 "${srcdir}/10-vboxdrv.rules" "${pkgdir}/lib/udev/rules.d/10-vboxdrv.rules"
  install -Dm0644 "${srcdir}/60-vboxguest.rules" "${pkgdir}/lib/udev/rules.d/60-vboxguest.rules"

  # Symlink the launchers
  install -d "${pkgdir}/usr/bin"
  for _bin in VirtualBox VBox{Headless,Manage,SDL,SVC,Tunctl,NetAdpCtl} rdesktop-vrdp; do
    ln -s "/opt/VirtualBox/${_bin}" "${pkgdir}/usr/bin/${_bin}"
  done

  # Symlink the desktop icon and ".desktop" files
  install -d "${pkgdir}/usr/"{share/applications,share/pixmaps}
  ln -s "/opt/VirtualBox/VBox.png" "${pkgdir}/usr/share/pixmaps/VBox.png"
  ln -s "/opt/VirtualBox/virtualbox.desktop" "${pkgdir}/usr/share/applications/VirtualBox.desktop"

  # Symlink mime info
  install -d "${pkgdir}/usr/share/mime/packages"
  ln -s "/opt/VirtualBox/virtualbox.xml" "${pkgdir}/usr/share/mime/packages/virtualbox.xml"

  # Symlink doc
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  ln -s "/opt/VirtualBox/VirtualBox.chm" "${pkgdir}/usr/share/doc/$pkgname/VirtualBox.chm"

  # Setup default configuration
  install -dm 0755 "${pkgdir}/etc/vbox"
  echo 'INSTALL_DIR="/opt/VirtualBox"' > "${pkgdir}/etc/vbox/vbox.cfg"
  chmod 0644 "${pkgdir}/etc/vbox/vbox.cfg"
  
  # Create the directory below if it doesn't exist
  install -d "${pkgdir}/var/run/VirtualBox"

  # DKMS: Symlink to source 
  mkdir -p "${pkgdir}/usr/src"
  ln -s "/opt/VirtualBox/src/vboxhost" "${pkgdir}/usr/src/vboxhost-${pkgver}"
}


