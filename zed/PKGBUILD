# Maintainer: Jordan Christiansen <xordspar0@gmail.com>
# Based on: https://github.com/jonhoo/zed-PKGBUILD
pkgname=zed
pkgver=0.13
pkgrel=1
pkgdesc="a code editor built using web technologies"
arch=('any')
depends=('node-webkit' 'nodejs')
conflicts=('zed-git')
provides=()
options=(!strip)
license=("MIT")
url="http://zedapp.org/"
source=('https://github.com/zedapp/zed/archive/v0.13.tar.gz' 'zed.sh')
md5sums=('d9cba2a66b0063574acfdbe5aad62e95'
         '78181cc5345b87bcee063c580fbe4596')

build() {
	cp "${srcdir}/${pkgname}-${pkgver}"/Zed.desktop.tmpl zed.desktop
	sed -i "s^Exec=%PREFIX%/lib/zed/zed %U^Exec=zed^" zed.desktop
	sed -i "s^Icon=%PREFIX%/lib/zed/Zed.svg^Icon=/usr/share/icons/zed.svg^" zed.desktop

	cd "${srcdir}/${pkgname}-${pkgver}"
	make app/ace
	make apps-npm
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	install -d "${pkgdir}/usr/share/${pkgname}"

	msg2 "Copy editor files"
	rm -rf "${pkgdir}/usr/share/${pkgname}/app.nw"
	cp -r "${srcdir}/${pkgname}-${pkgver}/app" "${pkgdir}/usr/share/${pkgname}/app.nw"
	install -D "${srcdir}/${pkgname}-${pkgver}"/Zed.svg "${pkgdir}/usr/share/icons"/zed.svg

	# Install Launcher
	msg2 "Install launcher to /usr/bin"
	install -D -m755 "${srcdir}"/zed.sh "${pkgdir}"/usr/bin/zed

	# Install Desktop
	msg2 "Add .desktop file"
	install -D -m644 "${srcdir}"/zed.desktop "${pkgdir}"/usr/share/applications/zed.desktop
}
