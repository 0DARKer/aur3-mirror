# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=makehuman
pkgver=1.0alpha5
_pkgver=1_0_0_alpha5
pkgrel=2
pkgdesc="Parametrical modeling program for creating human bodies"
arch=('i686' 'x86_64')
url="http://www.makehuman.org/"
depends=('python2' 'libgl' 'sdl' 'sdl_image' 'mesa')
optdepends=('aqsis: rendering')
license=('GPL3')
#source=(http://makehuman.googlecode.com/files/makehuman1-0-0_alpha2a_source.zip \
#	makehuman.desktop makehuman.sh makehuman.png)
source=(makehuman.desktop makehuman.sh makehuman.png Makefile.diff)
makedepends=('subversion')
md5sums=('f54fdfbc6c783effc4624808d2547563'
         '31369d91ab9974b6aeff6e0514b6d855'
         '2e7da6fe63519dba19877bd2ecfc9f0c'
         '45e28bbf312c1eaae1c776532b7f1a55')

build() {
  cd "$srcdir"
  svn co http://makehuman.googlecode.com/svn/releases/makehuman_$_pkgver ${pkgname}_${_pkgver}_source

  cd "$srcdir"/${pkgname}_${_pkgver}_source

  patch -Np0 < "$srcdir"/Makefile.diff || true

  make -f Makefile.Linux
}

package() {
  cd "$srcdir"/${pkgname}_${_pkgver}_source

  install -d -m755 "$pkgdir"/opt/makehuman/docs
  cp -a {data,main.py,makehuman,mh_core,mh_icon.bmp,mh_plugins} \
      "$pkgdir"/opt/makehuman/
  install -m644 docs/*.pdf "$pkgdir"/opt/makehuman/docs

  # remove svn files
  find "$pkgdir"/opt/makehuman -type d -name ".svn" -exec rm -rf '{}' '+'
  # fix python location
  find "$pkgdir"/opt/makehuman -type f -name "*.py" -exec sed -i 's|^#!.*|#!/usr/bin/python2|' '{}' '+'

  install -D -m755 "$srcdir"/$pkgname.sh "$pkgdir"/usr/bin/$pkgname
  install -D -m644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -D -m644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
