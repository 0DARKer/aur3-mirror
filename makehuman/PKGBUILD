# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=makehuman
pkgver=1.0alpha7
_pkgver=1_0_0_alpha7
pkgrel=2
pkgdesc="Parametrical modeling program for creating human bodies"
arch=('i686' 'x86_64')
url="http://www.makehuman.org/"
depends=('python2' 'libgl' 'sdl' 'sdl_image' 'mesa' 'glew')
optdepends=('aqsis: rendering' 'povray: rendering' 'mitsuba: rendering' 'python2-numpy: warping')
license=('AGPL3')
makedepends=('subversion')
source=("makehuman_${_pkgver}::svn+http://makehuman.googlecode.com/svn/releases/makehuman_$_pkgver" \
        makehuman.desktop makehuman.sh Makefile.diff)
md5sums=('SKIP'
         'f54fdfbc6c783effc4624808d2547563'
         '31369d91ab9974b6aeff6e0514b6d855'
         '6ad874111511b74d9e2c9a9708a3e533')

build() {
  cd "$srcdir"/${pkgname}_${_pkgver}

  patch -Np0 < "$srcdir"/Makefile.diff || true

  make -f Makefile.Linux
}

package() {
  cd "$srcdir"/${pkgname}_${_pkgver}

  install -d -m755 "$pkgdir"/opt/makehuman/
  # NOTE: the tools subdirectory contains scripts for blender
  cp -a {makehuman,main.py,apps,core,data,docs,plugins,pythonmodules,shared,tools,utils} \
      "$pkgdir"/opt/makehuman/
  install -D -m644 makehuman.png "$pkgdir"/usr/share/pixmaps/makehuman.png

  # fix the python location
  find "$pkgdir"/opt/makehuman -type f -name "*.py" -exec sed -i 's|^#!.*|#!/usr/bin/python2|' '{}' '+'

  install -D -m755 "$srcdir"/$pkgname.sh "$pkgdir"/usr/bin/$pkgname
  install -D -m644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
