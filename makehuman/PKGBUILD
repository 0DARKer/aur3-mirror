# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=makehuman
pkgver=1.0.alpha.8.rc1
pkgrel=1
pkgdesc="Parametrical modeling program for creating human bodies"
arch=('any')
url="http://www.makehuman.org/"
depends=('python2-numpy' 'python2-pyqt4' 'python2-opengl')
license=('AGPL3')
source=("http://download.tuxfamily.org/makehuman/releases/makehuman_$pkgver/makehuman_${pkgver}_source.zip" \
        makehuman.desktop makehuman.sh)
md5sums=('68b9f4a03e587835f8768174039b4142'
         'f54fdfbc6c783effc4624808d2547563'
         '534db191b7c6cd5cf976c9a7089b524c')

build() {
  cd "$srcdir/$pkgname"

  find . -type f -name "*.py" -exec sed -i 's/^#!.*python/&2/' '{}' ';'
  # compile all modules so that they  tracked by pacman
  python2 -m compileall .
  # and also optimize them
  python2 -OO -m compileall .
}

package() {
  install -d -m755 "$pkgdir/opt/"
  cp -a "$srcdir/$pkgname" "$pkgdir/opt/"

  install -D -m644 "$srcdir/$pkgname/icons/makehuman.png" "$pkgdir/usr/share/pixmaps/makehuman.png"
  install -D -m755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -D -m644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
