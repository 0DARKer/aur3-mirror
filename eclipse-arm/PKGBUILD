# Maintainer: Zuyi Hu <hzy068808@gmail.com>
# Contributor: Benjamin Robinben< jarobin@gmail.com>
pkgname=eclipse-arm
pkgver=2.5.1
date=201412191510
pkgrel=1
pkgdesc="GNU ARM Eclipse Plug-in"
arch=('any')
url="http://sourceforge.net/projects/gnuarmeclipse"
install="eclipse-arm.install"
license=("GPL")
options=('!strip')
depends=('eclipse-cdt')
source=("http://sourceforge.net/projects/gnuarmeclipse/files/Current%20Releases/2.x/ilg.gnuarmeclipse.repository-$pkgver-$date.zip")
md5sums=('b8c7324f094fef69df65b4b44cd58519')
 
package() {
  _dest="${pkgdir}/usr/share/eclipse/dropins/arm/eclipse"
 
  cd "${srcdir}"
  mkdir -p "$_dest"
  # Features
  for _f in features/*.jar; do
    _dir="${_dest}/${_f/.jar}"
    mkdir -p "${_dir}"
    bsdtar -xf "${_f}" -C "${_dir}"
  done

  # Plugins
  for _p in plugins/*.jar; do
    install -Dm644 "${_p}" "${_dest}/${_p}"
  done

}
