# Maintainer: Zuyi Hu <hzy068808-at-gmail-dot-com>
pkgname=eclipse-arm
pkgver=20140111
pkgrel=1
pkgdesc="GNU ARM Eclipse Plug-in"
arch=('any')
url="http://sourceforge.net/projects/gnuarmeclipse"
license=("GPL")
depends=('eclipse' 'eclipse-cdt' 'gcc-arm-none-eabi')
source=("http://softlayer-dal.dl.sourceforge.net/project/gnuarmeclipse/Current%20Releases/1.x/ilg.gnuarmeclipse.repository-1.8.1-201401111229.zip")
md5sums=('9c758db7730caac97202507f0ab7fe43')

package() {
  _dest="${pkgdir}/usr/share/eclipse/dropins/arm/eclipse"

  cd "${srcdir}"

  # Features
  for _f in features/*.jar; do
    _dir="${_dest}/${_f/.jar}"
    mkdir -p "${_dir}"
    bsdtar -xf "${_f}" -C "${_dir}"
  done

  # Plugins
  for _p in plugins/*.jar; do
    install -Dm644 "${_p}" "${_dest}/${_p}"
  done

}
