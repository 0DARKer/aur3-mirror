# Maintainer: Zuyi Hu <hzy068808@gmail.com>
# Contributor: Benjamin Robinben< jarobin@gmail.com>
pkgname=eclipse-arm
pkgver=2.1.2
_reldate=201402180751
pkgrel=1
pkgdesc="GNU ARM Eclipse Plug-in"
arch=('any')
url="http://sourceforge.net/projects/gnuarmeclipse"
install="eclipse-arm.install"
license=("GPL")
depends=('eclipse' 'eclipse-cdt')
source=("http://sourceforge.net/projects/gnuarmeclipse/files/Current%20Releases/2.x/ilg.gnuarmeclipse.repository-${pkgver}-${_reldate}.zip")
md5sums=('f7f2360ab3b039a1c3a8d723c5f4549f')
 
package() {
  _dest="${pkgdir}/usr/share/eclipse/dropins/arm/eclipse"
 
  cd "${srcdir}"
 
  # Features
  for _f in features/*.jar; do
    _dir="${_dest}/${_f/.jar}"
    mkdir -p "${_dir}"
    bsdtar -xf "${_f}" -C "${_dir}"
  done
 
  # Plugins
  for _p in plugins/*.jar; do
    install -Dm644 "${_p}" "${_dest}/${_p}"
  done
 
}
