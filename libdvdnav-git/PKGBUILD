# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libdvdnav-git
pkgver=20130206
pkgrel=1
pkgdesc="Library for easy use of DVD navigation (GIT version)"
arch=('i686' 'x86_64')
url="http://dvdnav.mplayerhq.hu/"
license=('GPL2')
depends=('libdvdread' 'bash')
makedepends=('git')
provides=('libdvdnav')
conflicts=('libdvdnav')
options=('!libtool')

_gitroot="git://git.videolan.org/libdvdnav.git"
_gitname="libdvdnav"

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."

  if [ -d "${_gitname}" ] ; then
      cd "${_gitname}" && git pull
  else
      git clone --depth=1 "${_gitroot}" "${_gitname}"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -fr "${srcdir}/${_gitname}-build"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"
  cd  "${srcdir}/${_gitname}-build"

  sed -e 's|INCLUDES|AM_CPPFLAGS|' -e '25a AC_CONFIG_MACRO_DIR([m4])' -i configure.ac
  sed '2a ACLOCAL_AMFLAGS = -I m4' -i Makefile.am

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}-build"
  make DESTDIR="${pkgdir}" install
}
