# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libdvdnav-git
pkgver=5.0.0.10.gaa3659d
pkgrel=1
pkgdesc="Library to navigate DVD disks. (GIT version)"
arch=('i686' 'x86_64')
url="http://dvdnav.mplayerhq.hu"
license=('GPL2')
depends=('libdvdread-git')
makedepends=('git')
provides=('libdvdnav')
conflicts=('libdvdnav')
options=('!libtool')
source=("git://git.videolan.org/libdvdnav.git")
md5sums=('SKIP')
_gitname="libdvdnav"

pkgver() {
  cd "${_gitname}"
  echo "$(git describe --long --tags | tr - .)"
}

build() {
  cd "${_gitname}"
  autoreconf -if
  ./configure --prefix=/usr --enable-static=no
  make
}

package() {
  cd "${_gitname}"
  make DESTDIR="${pkgdir}" install
}
