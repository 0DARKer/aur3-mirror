# Maintainer: josephgbr <rafael.f.f1 at gmail.com>
# Contributor: Themaister <maister at archlinux.us>

# Hint: for a cleaner but less informative usage, replace "Debug"
# with "Release" in the line '-DCMAKE_BUILD_TYPE=' below

pkgname=pcsx2-svn
pkgver=4910
pkgrel=1
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
if [ $CARCH == "x86_64" ]; then
	depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
		 'lib32-glew' 'lib32-portaudio' 'lib32-libjpeg-turbo'
		 'lib32-alsa-lib' 'lib32-soundtouch')	
	makedepends=('cmake' 'subversion' 'google-sparsehash' 'gcc-multilib')
	optdepends=('lib32-gtk-engines: libclearlooks.so library')
else
	depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew'
		 'portaudio' 'alsa-lib' 'libjpeg-turbo' 'soundtouch')
	makedepends=('cmake' 'subversion' 'google-sparsehash')
	optdepends=('gtk-engines: libclearlooks.so library')
fi
provides=('pcsx2')
conflicts=('pcsx2' 'pcsx2-bin')
options=('!emptydirs')
install="$pkgname.install"
changelog="$pkgname.changelog"
source=()
md5sums=()

_svntrunk=http://pcsx2.googlecode.com/svn/trunk
_svnmod=pcsx2

build() {
	cd "$srcdir"
	
	msg "Connecting to SVN server...."
	if [[ -d "$_svnmod/.svn" ]]; then
	  (cd "$_svnmod" && svn up -r "$pkgver")
	else
	  svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
	fi
	rm -rf "$srcdir/$_svnmod-build"
	msg "SVN checkout done or server timeout"

	msg "Applying arch packaging standards..."
	# For Arch64, install plugins in lib32 folder
	plugindir=/usr/lib/pcsx2
	[ $CARCH == x86_64 ] && plugindir=/usr/lib32/pcsx2

	msg "Starting cmake and build..."
	svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
	cd "$srcdir/$_svnmod-build"
	cmake CMakeLists.txt \
		-DCMAKE_BUILD_TYPE="Debug" \
		-DPACKAGE_MODE=TRUE \
		-DFORCE_INTERNAL_SDL=TRUE \
		-DFORCE_INTERNAL_SOUNDTOUCH=FALSE \
		-DCMAKE_INSTALL_PREFIX="/usr" \
		-DPLUGIN_DIR=$plugindir \
		-DGAMEINDEX_DIR="/usr/share/pcsx2"

	make
}

package() {
	cd "$srcdir/$_svnmod-build"
	make DESTDIR=$pkgdir/ install
}
