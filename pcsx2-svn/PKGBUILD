_pkgbasename=pcsx2
pkgname=$_pkgbasename-svn
pkgver=4295
pkgrel=1
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
if [ $CARCH == "x86_64" ]; then
	depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
		 'lib32-alsa-lib' 'lib32-glew' 'lib32-portaudio' 'lib32-sdl'
		 'google-sparsehash' 'lib32-libjpeg' 'lib32-soundtouch>=1.5.0')
	makedepends=('cmake>=2.8' 'subversion' 'gcc-multilib')
else
	depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew' 'portaudio' 
		 'alsa-lib' 'sdl' 'google-sparsehash' 'libjpeg' 'soundtouch>=1.5.0')
	makedepends=('cmake>=2.8' 'subversion')
fi
conflicts=('pcsx2' 'pcsx2-bin' 'pcsx2-svn-bin') # Same destination folders
install="pcsx2.install"
source=('pcsx2.sh')
md5sums=('ba0c99fa926188fcfd9dd65cc4d5f1a9')

_svntrunk=http://$_pkgbasename.googlecode.com/svn/trunk
_svnmod=pcsx2

build() {
	cd "$srcdir"
	if [ -d $_svnmod/.svn ]; then
		(cd $_svnmod && svn up -r $pkgver)
	else
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
	fi	
	rm -rf "$srcdir/$_svnmod-build"
	svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build" #unversioned copy
	msg "SVN checkout done or server timeout"
	msg "Starting make..."
	
	cd "$srcdir/$_svnmod-build"
	cmake CMakeLists.txt \
		-DCMAKE_BUILD_TYPE=Release \
		-DFORCE_INTERNAL_SOUNDTOUCH=FALSE \
		-DPACKAGE_MODE=TRUE
	make
}

package() {
	cd "${srcdir}/${_svnmod}-build/bin"
	mkdir -p ${pkgdir}/opt/pcsx2 ${pkgdir}/usr/share/man/man1
	cp -rf * ${pkgdir}/opt/pcsx2
	# Install man page
   	gzip -9 docs/pcsx2.man
	mv docs/pcsx2.man.gz ${pkgdir}/usr/share/man/man1/pcsx2.1.gz
	# Install script
	install -Dm0755 ${srcdir}/pcsx2.sh ${pkgdir}/usr/bin/pcsx2
}
