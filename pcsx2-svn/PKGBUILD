# Maintainer: josephgbr <rafael.f.f1 at gmail dot com>

pkgname=pcsx2-svn
pkgver=4813
pkgrel=1
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
makedepends=('cmake' 'subversion' 'google-sparsehash')
if [ $CARCH == "x86_64" ]; then
	depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
		 'lib32-glew' 'lib32-portaudio' 'lib32-libjpeg-turbo')	
	makedepends=("${makedepends[@]}" 'gcc-multilib')
else
	depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew'
		 'portaudio' 'alsa-lib' 'libjpeg-turbo')
fi
provides=('pcsx2')
conflicts=('pcsx2' 'pcsx2-bin')
options=('!emptydirs')
install="pcsx2.install"
source=('pcsx2.xpm'
	'pcsx2.desktop')
md5sums=('18cb3c5a243a27363553d3e1008210c0'
         'b055ff7c0f236f33702f4813ae4b980d')

_svntrunk=http://pcsx2.googlecode.com/svn/trunk
_svnmod=pcsx2

build() {
	cd "${srcdir}"
	
	if [ -d $_svnmod/.svn ]; then
		(cd $_svnmod && svn up -r $pkgver)
	else
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
	fi
	rm -rf "$srcdir/$_svnmod-build"
	svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
	msg "SVN checkout done or server timeout"

	msg "Applying arch packaging standards..."
	# For Arch64, install plugins in lib32 folder
	plugindir=/usr/lib/pcsx2
	[ $CARCH == x86_64 ] && plugindir=/usr/lib32/pcsx2 

	msg "Starting cmake and build..."
	cd "$srcdir/$_svnmod-build"
	cmake CMakeLists.txt \
		-DCMAKE_BUILD_TYPE="Debug" \
		-DPACKAGE_MODE=TRUE \
		-DFORCE_INTERNAL_SDL=TRUE \
		-DCMAKE_INSTALL_PREFIX="/usr" \
		-DPLUGIN_DIR=$plugindir \
		-DGAMEINDEX_DIR="/usr/share/pcsx2"

	make
}

package() {
	cd "${srcdir}/${_svnmod}-build"
	make DESTDIR=${pkgdir} install
	install -Dm644 "bin/docs/pcsx2.man"  \
			"${pkgdir}/usr/share/man/man1/pcsx2.1"
	install -Dm644 "bin/docs/PCSX2_FAQ_0.9.8.pdf"  \
			"${pkgdir}/usr/share/docs/pcsx2/PCSX2_FAQ_0.9.8.pdf"
	install -Dm644 "bin/docs/PCSX2_Readme_0.9.8.pdf"  \
			"${pkgdir}/usr/share/docs/pcsx2/PCSX2_Readme_0.9.8.pdf"			
	install -Dm644 "${srcdir}/pcsx2.xpm" \
			"${pkgdir}/usr/share/pixmaps/pcsx2.xpm"
	install -Dm644 "${srcdir}/pcsx2.desktop" \
			"${pkgdir}/usr/share/applications/pcsx2.desktop"
}
