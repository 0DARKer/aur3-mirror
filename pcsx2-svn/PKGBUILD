#PCSX2-svn maintainer: Hans-Kristian Arntzen <maister@archlinux.us>
_pkgbasename=pcsx2
pkgname=$_pkgbasename-svn
pkgver=4276
pkgrel=1
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
#Note2self
# * SoundTouch and Zlib are built-in, unless cmake is set differently
if [ $CARCH == "x86_64" ]; then
   depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
   'lib32-alsa-lib' 'lib32-glew' 'lib32-portaudio-svn' 'lib32-sdl'
   'google-sparsehash' 'lib32-libjpeg')
   makedepends=('cmake' 'svn' 'gcc-multilib' 'w3m')
else
   depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew' 'portaudio' 
   'alsa-lib' 'sdl' 'google-sparsehash' 'libjpeg')
   makedepends=('cmake' 'svn' 'w3m')
fi
conflicts=('pcsx2' 'pcsx2-bin' 'pcsx2-svn-bin') # Same destination folders
install="pcsx2.install"
source=('pcsx2.sh'
'allow64bit.patch')
md5sums=('1e494d99c38a3fa08f2f49e70ec8dfcd'
'3ba263f4bad9d08d3dbf1d89c612fc0b')

_svntrunk=http://$_pkgbasename.googlecode.com/svn/trunk
_svnmod=pcsx2

build() {
   cd "$srcdir"

   if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
   else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
   fi	
   rm -rf "$srcdir/$_svnmod-build"
   svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build" #unversioned copy	
   msg "SVN checkout done or server timeout"
   msg "Starting make..."

   cd "$srcdir/$_svnmod-build"
   # patch CMakeLists to allow build on x86_64
   patch -p0 <../allow64bit.patch

   cmake CMakeLists.txt \
      -DCMAKE_BUILD_TYPE=Release 
   #	-DCMAKE_BUILD_STRIP=FALSE
   #	-DFORCE_INTERNAL_ZLIB=FALSE \
      #	-DFORCE_INTERNAL_SOUNDTOUCH=FALSE
   #	-DUSER_CMAKE_C_FLAGS:STRING="-g -O2 -D_FORTIFY_SOURCE=2" \
      #	-DUSER_CMAKE_CXX_FLAGS:STRING="-g -O2 -D_FORTIFY_SOURCE=2" \
      #	-DUSER_CMAKE_LD_FLAGS:STRING="-Wl,--no-add-needed "
   make
}

package() {
   cd "${srcdir}/${_svnmod}-build/bin"
   mkdir -p ${pkgdir}/opt/pcsx2
   cp -rf * ${pkgdir}/opt/pcsx2

   install -Dm0644 docs/pcsx2.man ${pkgdir}/usr/share/man/man1/pcsx2.1

   install -Dm0755 ${srcdir}/pcsx2.sh ${pkgdir}/usr/bin/pcsx2	

   cat docs/GPL.html | w3m -dump -T Text/html > GPL.txt
   install -Dm0755 GPL.txt ${pkgdir}/usr/share/licenses/$_pkgbasename/GPL

}
