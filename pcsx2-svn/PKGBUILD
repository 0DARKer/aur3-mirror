_pkgbasename=pcsx2
pkgname=$_pkgbasename-svn
pkgver=4389
pkgrel=1
pkgdesc="A PlayStation 2 emulator."
arch=('i686' 'x86_64')
url="http://www.pcsx2.net"
license=('GPL')
if [ $CARCH == "x86_64" ]; then
	depends=('lib32-bzip2' 'lib32-wxgtk' 'lib32-nvidia-cg-toolkit'
		 'lib32-alsa-lib' 'lib32-glew' 'lib32-portaudio' 'lib32-sdl'
		 'google-sparsehash' 'lib32-libjpeg' 'lib32-soundtouch>=1.5.0')
	makedepends=('cmake>=2.8' 'subversion' 'gcc-multilib')
else
	depends=('bzip2' 'wxgtk' 'nvidia-cg-toolkit' 'glew' 'portaudio' 
		 'alsa-lib' 'sdl' 'google-sparsehash' 'libjpeg' 'soundtouch>=1.5.0')
	makedepends=('cmake>=2.8' 'subversion')
fi
conflicts=('pcsx2' 'pcsx2-bin' 'pcsx2-svn-bin')
install="pcsx2.install"
source=('pcsx2.sh' 'pcsx2.xpm' 'pcsx2.desktop')
md5sums=('4bc9fdd49657e1a43433d576eedf7a3a'
         '18cb3c5a243a27363553d3e1008210c0'
         'b055ff7c0f236f33702f4813ae4b980d')

_svntrunk=http://pcsx2.googlecode.com/svn/trunk
_svnmod=pcsx2

build() {
	cd "${srcdir}"
	if [ -d $_svnmod/.svn ]; then
		(cd $_svnmod && svn up -r $pkgver)
	else
		svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
	fi	
	rm -rf "$srcdir/$_svnmod-build"
	svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
	msg "SVN checkout done or server timeout"
	msg "Starting make..."
	
	cd "$srcdir/$_svnmod-build"
	cmake CMakeLists.txt \
		-DCMAKE_BUILD_TYPE="Release" \
		-DFORCE_INTERNAL_SOUNDTOUCH=FALSE \
		-DPACKAGE_MODE=TRUE
	make
}

package() {
	cd "${srcdir}/${_svnmod}-build/bin"
	mkdir -p "${pkgdir}/opt/pcsx2" "${pkgdir}/usr/share/man/man1"
	cp -rPf * "${pkgdir}/opt/pcsx2"
	# Not using it / replaced with pcsx2.sh
	rm ${pkgdir}/opt/pcsx2/launch_pcsx2_linux.sh
	# Install Man page
   	gzip -9 "${pkgdir}/opt/pcsx2/docs/pcsx2.man"
	mv "${pkgdir}/opt/pcsx2/docs/pcsx2.man.gz" "${pkgdir}/usr/share/man/man1/pcsx2.1.gz"
	# Install runnable script, icon and desktop item
	install -Dm755 "${srcdir}/pcsx2.sh" "${pkgdir}/usr/bin/pcsx2"
	install -Dm644 "${srcdir}/pcsx2.xpm" "${pkgdir}/usr/share/pixmaps/pcsx2.xpm"
	install -Dm644 "${srcdir}/pcsx2.desktop" "${pkgdir}/usr/share/applications/pcsx2.desktop"
}
