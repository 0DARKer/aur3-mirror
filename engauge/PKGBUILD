# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=engauge  
pkgver=5_1
pkgrel=3
pkgdesc="A Digitizer for curves and scanned plots"
url="http://digitizer.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('qt' 'fftw')
source=("http://switch.dl.sourceforge.net/project/digitizer/Engauge%20Digitizer/digitizer-5.1/digit-src-5_1.tar.gz" "$pkgname.desktop")
md5sums=('99be0d3853a754339924ad84a20b953c'
         'a7f054aa909e135c262b2e23f4bd7286')
install=engauge.install

build() {
  cd $srcdir/$pkgname
  qmake digitizer.pro
  make 
}
package() {
  cd $srcdir/$pkgname
  install -Dm755 bin/$pkgname $pkgdir/usr/bin/$pkgname
  install -Dm644 usermanual/animation.png \
    $pkgdir/usr/share/icons/$pkgname.png 
  install -d $pkgdir/usr/share/doc/$pkgname
  cp -r samples $pkgdir/usr/share/doc/$pkgname
  cp -r usermanual $pkgdir/usr/share/doc/$pkgname
  install -Dm644 $srcdir/$pkgname.desktop \
    $pkgdir/usr/share/applications/$pkgname.desktop
}
