# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=libkcddb-frameworks-git
pkgver=r650.be53b33
pkgrel=1
pkgdesc="KDE CDDB library. KF5 Frameworks branch (GIT version)"
url='https://projects.kde.org/projects/kde/kdemultimedia/libkcddb'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kio' 'libmusicbrainz5')
makedepends=('extra-cmake-modules' 'kdoctools' 'git' 'cmake')
conflicts=('libkcddb')
provides=('libkcddb')
source=("git://anongit.kde.org/libkcddb#branch=kf5")
sha1sums=('SKIP')

pkgver() {
  cd libkcddb
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../libkcddb \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}