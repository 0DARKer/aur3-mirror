# Maintainer: (epsilom) Xavier Corredor <xavier.corredor.llano (at) gmail.com>
# Contributor: alexwizard

pkgname=rekonq-git
_realname=rekonq
pkgver=20110323
pkgrel=1
pkgdesc="A WebKit based web browser for KDE, with patch for localizations and enable multiple instances"
arch=('i686' 'x86_64')
url="http://rekonq.kde.org"
license=('GPL')
depends=('kdebase-keditbookmarks')
makedepends=('cmake' 'automoc4' 'git' 'docbook-xsl' 'patch' 'bc')
provides=(${_realname})
conflicts=(${_realname})
install=${pkgname}.install
source=(rekonq-git.install patch instances.patch)
md5sums=('c354917df476f6ddfec82d0ccf7dfd85'
         '86515d857298456cb34d60588ae2311c'
         'a83b56b945e8ef1365120274f13214d3')

_gitroot="git://anongit.kde.org/${_realname}"
_gitname=${_realname}
 
build() {
  cd ${srcdir}

  msg "Connecting to GIT server...."

  if [ -d ${_gitname} ]; then
    cd ${_gitname} && git pull origin
    msg "The local files are updated."
  else
    git clone ${_gitroot}
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf ${srcdir}/build
  git clone ${srcdir}/${_gitname} ${srcdir}/build
  cd ${srcdir}/build
 
  # patch and run localization files
  patch -Np1 -i ${srcdir}/patch
  # patch for multiple instances
  patch -Np1 -i ${srcdir}/instances.patch


  export RK_SRCS="${srcdir}/build"
  cd scripts
  chmod +x download_i18n.sh
  ./download_i18n.sh
  cd ..
  # end path and run localization files

  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}