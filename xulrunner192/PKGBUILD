# $Id: PKGBUILD 112304 2011-03-04 19:29:22Z ibiru $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: David Roheim <david dot roheim at gmail dot com>
pkgname=xulrunner192
pkgver=1.9.2.28
_ffoxver=3.6.28
pkgrel=3
pkgdesc="Mozilla Runtime Environment"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2' 'gcc-libs' 'libidl2' 'mozilla-common' 'nss' 'libxt' 'hunspell' 'startup-notification' 'mime-types' 'dbus-glib' 'alsa-lib' 'libevent' 'sqlite>=3.7.4' 'libnotify>=0.4')
provides=('xulrunner-1.9')
conflicts=('xulrunner-1.9-bin')
makedepends=('zip' 'pkg-config' 'diffutils' 'libgnomeui' 'python2' 'wireless_tools' 'autoconf2.13')
url="http://wiki.mozilla.org/XUL:Xul_Runner"

source=(http://ftp.mozilla.org/pub/mozilla.org/xulrunner/releases/${_ffoxver}/source/xulrunner-${_ffoxver}.source.tar.bz2
        mozconfig
        mozilla-pkgconfig.patch
        fix-mozilla-launcher.patch
        xulrunner-version.patch
        xulrunner-png14.patch
        xulrunner-png15.patch
        enable-x86_64-tracemonkey.patch
        offsetof.patch
        python2.7.patch
        file_util.patch
        message_pump_libevent.patch
        file_descriptor_set_posix.patch
        file_util_linux.patch
        time_posix.patch
        i686-fix.patch
        scopefixes.patch)

md5sums=('b80ae2b89f583f3cbaa407ef9e402dd7'
         '0ae778eb9395752f8a4982ad1b4c6f64'
         'd839d1c4ef736e6d89ccf91b23b965a4'
         '63eee2d1da3b43c9d604f2253f242f40'
         '371303c5bdc4fa0d955d14521b93b69d'
         '3bd0566180ad2daa32743b3ce58b2095'
         '65eaf73452b1d974aa1065022d693132'
         'cbd938cd1fb8210cd8a2c41833489af9'
         'c39773f884c79773db10a1216722441e'
         'ab3dc9aecae7f08b9492fb3c00a5fd28'
         '26be7284706ff5f740deb3e83e6cf29c'
         '187ec32c67744df73bb76fd5057a06a6'
         '2352dcccac6c0913d5f68c4e6236af50'
         '642a2fcc49e160385707ff903e17a25a'
         'b3c4fc2396635efc38d4c649ca054ed8'
         'f544cc38768c10def90bcbfd36659eba'
         '59f00e0e6e7454e788c9e19dd9090c4e')

build() {
  cd "${srcdir}/mozilla-1.9.2"
  cp "${srcdir}/mozconfig" .mozconfig

  patch -Np1 -i "${srcdir}/offsetof.patch" || return 1

  # Fix C++ 4.7 compile errors
  patch -Np1 -i "${srcdir}/file_util.patch" || return 1
  patch -Np1 -i "${srcdir}/message_pump_libevent.patch" || return 1
  patch -Np1 -i "${srcdir}/file_descriptor_set_posix.patch" || return 1
  patch -Np1 -i "${srcdir}/file_util_linux.patch" || return 1
  patch -Np1 -i "${srcdir}/time_posix.patch" || return 1
  patch -Np0 -i "${srcdir}/scopefixes.patch" || return 1

  # fix libdir/sdkdir - fedora
  patch -Np1 -i "${srcdir}/mozilla-pkgconfig.patch" || return 1

  # Fix stub launcher - archlinux
  patch -Np0 -i "${srcdir}/fix-mozilla-launcher.patch" || return 1

  # Force installation to the same path for every version
  patch -Np1 -i "${srcdir}/xulrunner-version.patch" || return 1

  # Fix compile with libpng 1.4
  patch -Np0 -i "${srcdir}/xulrunner-png14.patch" || return 1

  # Fix compile with libpng 1.5
  patch -Np0 -i "${srcdir}/xulrunner-png15.patch" || return 1

  # Tracemonkey for x86_64
  patch -Np0 -i "${srcdir}/enable-x86_64-tracemonkey.patch" || return 1

  # python2.7
  patch -Np0 -i "${srcdir}/python2.7.patch" || return 1
  
  # i686 patch
  patch -Np0 -i "${srcdir}/i686-fix.patch" || return 1

  unset CFLAGS
  unset CXXFLAGS

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd "${srcdir}/mozilla-1.9.2"
  _DEST="${pkgdir}/opt/xulrunner192"
  mkdir -p "$_DEST"
  mkdir -p "${pkgdir}/usr/bin"
  make -j1 DESTDIR="$_DEST" install

  ln -sf "${pkgdir}/usr/bin/xulrunner" "${pkgdir}/usr/bin/xulrunner192"
  rm -rf "$DESTDIR/usr/lib/pkgconfig"
  
}
