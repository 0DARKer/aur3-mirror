pkgname=vsftpd-ext
pkgver=2.3.5+ext.1
_pkgver=${pkgver/+/-}
_dirpkgver=${_pkgver/ext./ext}
pkgrel=3
pkgdesc="An extended version of the Very Secure FTP daemon"
arch=('i686' 'x86_64')
url="http://vsftpd.devnet.ru/"
license=('GPL')
depends=('glibc' 'pam' 'openssl' 'libcap>=2.16')
provides=('vsftpd')
conflicts=('vsftpd')
install=vsftpd-ext.install
backup=(etc/vsftpd/vsftpd.conf etc/xinetd.d/vsftpd)
source=(http://vsftpd.devnet.ru/files/2.3.5/ext.1/vsFTPd-${_dirpkgver}.tgz
        vsftpd.d
        vsftpd.xinetd)
md5sums=('77e2bf4991259075e501ab46a6996da8'
         '60060a6c7db3b3b7baa1561a302e6923'
         'b07fd4609c70063c1d6b20142910c1a6')

build() {
  cd "${srcdir}/vsFTPd-${_pkgver}"

  # build-time config
  sed \
    -e 's|^#undef VSF_BUILD_SSL$|#define VSF_BUILD_SSL|' \
    -i builddefs.h

  sed \
    -e 's|-lwrap ||g' \
    -i Makefile

  make CFLAGS="${CFLAGS}"
}

package() {
  cd "${srcdir}/vsFTPd-${_pkgver}"
  install -D -m755 vsftpd "${pkgdir}/usr/sbin/vsftpd"
  install -D -m644 vsftpd.conf "${pkgdir}/etc/vsftpd/vsftpd.conf"
  install -D -m644 vsftpd.8 "${pkgdir}/usr/share/man/man8/vsftpd.8"
  install -D -m644 vsftpd.conf.5 "${pkgdir}/usr/share/man/man5/vsftpd.conf.5"
  install -D -m644 "${srcdir}/vsftpd.xinetd" "${pkgdir}/etc/xinetd.d/vsftpd"
  install -D -m755 "${srcdir}/vsftpd.d" "${pkgdir}/etc/rc.d/vsftpd"

  install -d -m755 "${pkgdir}/usr/share/empty"
}
