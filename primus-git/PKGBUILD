# Maintainer: Alexander Monakov <amonakov@gmail.com>
pkgname=primus-git
pkgver=20120930
pkgrel=1
pkgdesc="Faster OpenGL offloading for Bumblebee (git sources)"
arch=('i686' 'x86_64')
url="https://github.com/amonakov/primus"
license=('custom:ISC')
depends=('bumblebee' 'mesa' 'nvidia-utils-bumblebee')
makedepends=('git')
provides=('primus')
conflicts=('primus')
md5sums=()

_gitroot="git://github.com/amonakov/primus.git"
_gitname="primus"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server..."
  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone --depth=1 $_gitroot $_gitname
    cd $_gitname
  fi
  msg "GIT checkout done or server timeout"

  make
}

package() {
  cd "$srcdir/$_gitname"
  install -D "lib/libGL.so.1" "$pkgdir/usr/lib/primus/libGL.so.1"
  sed -e "s#^PRIMUS_libGL=.*#PRIMUS_libGL='/usr/\$LIB/primus'#" primusrun > primusrun.dist
  install -D "primusrun.dist" "$pkgdir/usr/bin/primusrun"

  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
