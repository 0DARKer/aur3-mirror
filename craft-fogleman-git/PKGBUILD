# Maintainer:  Martin C. Doege <mdoege at compuserve dot com>

pkgname=craft-fogleman-git
_gitname=Craft
pkgver=736.fe3e7b3
pkgrel=1
pkgdesc="A Minecraft clone in C with multiplayer support"
arch=('i686' 'x86_64')
url="http://www.michaelfogleman.com/craft/"
license=('MIT')
depends=('glew' 'curl')
optdepends=('python2-requests: for Python multiplayer server')
makedepends=('git' 'cmake')
source=('git://github.com/fogleman/Craft.git'
        'craft.desktop'
        'craft_icon.png'
        'craft'
        )
md5sums=('SKIP'
         '3f9ad8e597f9654d9db3beeb53051ebd'
         '1829e6bff2265d8c0def57d5ec0440db'
         '5e018d0f73ba1434e7228a0d27acad34')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $_gitname
  cmake .
  make
  
  gcc -std=c99 -O3 -shared -o world -I src -fPIC -I deps/noise deps/noise/noise.c src/world.c # needed for Python server
}

package() {
  cd $_gitname
  install -Dm755 craft "$pkgdir/usr/bin/craft-bin"
  install -Dm755 $srcdir/craft "$pkgdir/usr/bin/craft"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/craft/LICENSE"

  mkdir -p "$pkgdir/usr/share/craft"
  cp -pr shaders textures "$pkgdir/usr/share/craft/"

  mkdir -p $pkgdir/usr/share/icons
  cp $srcdir/../craft_icon.png $pkgdir/usr/share/icons
  mkdir -p $pkgdir/usr/share/applications
  cp $srcdir/../craft.desktop $pkgdir/usr/share/applications
}
