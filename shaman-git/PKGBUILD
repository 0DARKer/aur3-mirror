# Maintainer: Sam Stuewe <halosghost at archlinux dot info>
_name=shaman
pkgname="${_name}-git"
pkgver=243.ff184bc
pkgrel=1
pkgdesc="A small, native C library and utility to fetch weather"
arch=('i686' 'x86_64')
url="https://github.com/HalosGhost/${_name}"
license=('GPL2')
depends=('curl' 'jansson')
makedepends=('git' 'tup' 'clang')
source=("${_name}::git+${url}.git")
sha256sums=('SKIP')

pkgver () {
  cd "${_name}"
  printf '%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD )"
}

build () {
  cd "${_name}"
  tup upd
}

check () {
  cd "${_name}/test"
  LD_LIBRARY_PATH=../src ./suite
}

package () {
  cd "${_name}"
  DESTDIR="${pkgdir}" PREFIX='/usr' ./install.sh
}

# vim:set ts=2 sw=2 et:
