# Maintainer: Sam Stuewe <halosghost at archlinux dot info>
_name=shaman
pkgname="${_name}-git"
pkgver=231.0ed41ce
pkgrel=1
pkgdesc="A small, native C library and utility to fetch weather"
arch=('i686' 'x86_64')
url="https://github.com/HalosGhost/${_name}"
license=('GPL2')
depends=('curl' 'jansson')
makedepends=('git' 'tup')
source=("${_name}::git+${url}.git")
sha256sums=('SKIP')

pkgver () {
  cd "${_name}"
  printf '%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD )"
}

prepare () {
  cd "${_name}"
  tup init
}

build () {
  cd "${_name}"
  tup upd
}

check () {
  cd "${_name}/test"
  LD_LIBRARY_PATH=../src ./suite
}

package () {
  cd "${_name}"
  DESTDIR="${pkgdir}" PREFIX='/usr' ./install.sh
}

# vim:set ts=2 sw=2 et:
