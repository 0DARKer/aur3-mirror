# Maintainer: John Jenkins <twodopeshaggy@gmail.com>

pkgname=drive
pkgver=0.0.4.b
pkgrel=1
pkgdesc="Pull or push Google Drive files"
arch=('x86_64' 'i686')
url="http://github.com/odeke-em/drive"
license=('Apache')
makedepends=('go')
options=('!strip' '!emptydirs')
source=("https://github.com/odeke-em/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('038b26df47e4b51614e9a03452e588c8fbaba85cc19c97b9fb1eda3de8714135')

prepare() {
 mkdir -p "$srcdir/go"
 export GOPATH="$srcdir/go"
 go get github.com/odeke-em/drive
 go get github.com/odeke-em/drive/config
 go get github.com/rakyll/command
}

build() {
  export GOPATH="$srcdir/go"
  cd "$pkgname-$pkgver/cmd/drive"

  msg2 'Building drive'
  go build -o "$pkgname" main.go

}

package() {
  cd "$pkgname-$pkgver/cmd/drive"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"

}
# vim:set ts=2 sw=2 et:
