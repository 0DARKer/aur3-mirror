# Maintainer: Aelius Saionji
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Patches by Nei @ http://anti.teamidiot.de/static/nei/*/Code/Irssi/
# Prettier PKGBUILD with help from Earnestly

pkgname='irssi-256color'
pkgver='0.8.16.rc1'
pkgrel='2'
pkgdesc='Release candidate not in official repo, patched with 256 and true color support'
arch=('i686' 'x86_64')
url='http://irssi.org/'
license=('GPL')
depends=('glib2' 'openssl')
optdepends=('perl-libwww: for the scriptassist script')
conflicts=('irssi')
provides=('irssi')
backup=('etc/irssi.conf')
install='irssi-256color.install'
source=('http://irssi.org/files/irssi-0.8.16-rc1.tar.bz2'
	'alias.diff'
	'05-fix-print_after-scrollback.diff'
	'06-add-print_text_after_time.diff'
	'07-no-split-utf8.diff'
	'256color.diff')
md5sums=('275c926dcbb11059f8685906df7cfe49'
         '1095544905a6860eebd798295147a9b1'
         'b2c13b7635e0109e1b6fc80d62a4c908'
         'f92de48db3c88e997632663f2c9e7c88'
         'a3bcc4ca6961c146408f08a80cafdfc5'
         'a2963ec66acadd258920bba85ffd30ea')

prepare() {
  cd irssi-0.8.16-rc1
  patch -p1 -i "$srcdir"/05-fix-print_after-scrollback.diff
  patch -p1 -i "$srcdir"/06-add-print_text_after_time.diff 
  patch -p1 -i "$srcdir"/07-no-split-utf8.diff
  patch -p1 -i "$srcdir"/256color.diff
}

build() {
  cd irssi-0.8.16-rc1
  aclocal
  automake --add-missing
  autoreconf
  ./configure --prefix=/usr \
              --enable-ipv6 \
	      --with-proxy \
	      --enable-true-color \
	      --sysconfdir=/etc \
	      --with-perl-lib=vendor
  make
}

package() {
  cd irssi-0.8.16-rc1
  make DESTDIR="$pkgdir" install 
}
