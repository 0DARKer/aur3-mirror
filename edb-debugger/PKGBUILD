# Maintainer: revel <revelΘmuub·net>
# Contributor: shild <sxp@bk.ru>

pkgname=edb-debugger
pkgver=0.9.17
pkgrel=1
pkgdesc="EDB (Evan's Debugger) is a QT4 based binary mode debugger with the goal of having usability on par with OllyDbg."
arch=('i686' 'x86_64')
url="http://www.codef00.com/projects.php#Debugger"
license=('GPL2')
depends=('qt>=4.5')
makedepends=('boost>=1.35.0')
install=edb.install
source=(http://www.codef00.com/projects/debugger-$pkgver.tgz
        edb.desktop)

build() {
  cd $srcdir/debugger

  qmake -makefile DEFAULT_PLUGIN_PATH="/usr/lib/edb/"

  # build
  make
}

package() {
  cd $srcdir/debugger
  
  # install to pkg dir
  make INSTALL_ROOT=$pkgdir/usr install
  
  # correct /usr/lib64 -> /usr/lib on x86_64
  [ "$CARCH" = "x86_64" ] && (mv $pkgdir/usr/lib64 $pkgdir/usr/lib)

  # maybe post_install message about this script ?
#  install -m755 edb_make_symbolmap.sh $pkgdir/usr/bin
  
  # icons
  install -D -m 644 src/images/edb48-logo.png $pkgdir/usr/share/pixmaps/edb.png
  install -D -m 644 src/images/edb48-logo.png $pkgdir/usr/share/icons/hicolor/48x48/apps/edb.png
  
  # install desktop file
  cd ..
  install -D -m 644 edb.desktop $pkgdir/usr/share/applications/edb.desktop
}

md5sums=('0349fe842765c177a3d7a1ac43d7462b'
         'e9be4ef10362b0ae251dfb8f7d122395')
sha256sums=('acd33e8babdaf56b1500576adf86c91363e071bc1b19a60037b2de72736ae87e'
            'a489c5df5018e82ca6899cf850ed19602ed12bfad3b62107fdfde85512355d62')
