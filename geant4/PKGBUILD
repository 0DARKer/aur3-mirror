# Maintainer: Luis Sarmiento < lgsarmientop-ala-unal.edu.co >

pkgname=geant4
pkgver=4.9.5
_pkgver=9.5
pkgrel=2
pkgdesc="A simulation toolkit for particle physics interactions."

url="http://geant4.cern.ch/"
arch=('x86_64' 'i686')
license=('custom: http://geant4.cern.ch/license/') 
depends=('cmake' 'xerces-c' 'qt' 'lesstif' 'mesa')
optdepends=('java-environment: for histogram visualizations and analysis'
	    'tcsh: for C Shell support'
	    'python: for G4Python support')
makedepends=('cmake>=2.8')
replaces=('geant4-deb')
install='geant4.install'
options=('!emptydirs')

source=(http://geant4.cern.ch/support/source/geant${pkgver}.tar.gz)
md5sums=('3b4ec11e82b78b25229ef3e626c16400')

build() {
	[ -d ${srcdir}/build ] && rm -rf ${srcdir}/build
	mkdir ${srcdir}/build
	cd ${srcdir}/build
	cmake -DGEANT4_USE_GDML=ON \
              -DGEANT4_USE_G3TOG4=ON \
              -DGEANT4_USE_QT=ON \
	      -DQT_QMAKE_EXECUTABLE=`which qmake` \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DGEANT4_INSTALL_EXAMPLES=ON \
              -DGEANT4_USE_RAYTRACERX=ON \
              -DGEANT4_INSTALL_DATA=ON \
              -DGEANT4_USE_OPENGL_X11=ON \
              -DGEANT4_RAYTRACER_X11=ON \
              -DGEANT4_SYSTEM_CLHEP=OFF \
              -DGEANT4_XM=ON \
              -DCMAKE_INSTALL_LIBDIR=/usr/lib \
              ../${pkgname}.${_pkgver}

	make
}

package() {
	  cd ${srcdir}/build
	  make DESTDIR="${pkgdir}" install

	  echo 'pushd /usr/bin &> /dev/null && source geant4.sh && popd &>/dev/null' > ${srcdir}/geant4.profile
	  install -d ${pkgdir}/etc/profile.d
	  install -m755 ${srcdir}/geant4.profile ${pkgdir}/etc/profile.d/geant4.sh
}
