# Maintainer: Luis Sarmiento < Luis.Sarmiento-ala-nuclear.lu.se >
pkgname='geant4'
pkgver=10.0.2
_pkgver=10.00.p02
pkgrel=1
pkgdesc="A simulation toolkit for particle physics interactions."
depends=('cmake>=2.8.2' 'xerces-c' 'qt4' 'glu' 'soxt' 'mesa')
replaces=('geant4-deb')
optdepends=('java-environment: for histogram visualizations and analysis'
  'tcsh: for C Shell support'
  'python: for G4Python support'
  'geant4-abladata: Data files for nuclear shell effects in INCL/ABLA hadronic mode'
  'geant4-ledata: Data files for low energy electromagnetic processes'
  'geant4-levelgammadata: Data files for photon evaporation'
  'geant4-neutronhpdata: Neutron data files with thermal cross sections'
  'geant4-neutronxsdata: Data files for evaluated neutron cross sections on natural composition of elements'
  'geant4-piidata: Data files for shell ionisation cross sections'
  'geant4-radioactivedata: Data files for radioactive decay hadronic processes'
  'geant4-realsurfacedata: Data files for measured optical surface reflectance'
  'geant4-saiddata: Data files from evaluated cross-sections in SAID data-base')
url="http://geant4.cern.ch/"
arch=('x86_64' 'i686')
license=('custom: http://geant4.cern.ch/license/')
options=('!emptydirs')
install="${pkgname}.install"
source=("http://geant4.cern.ch/support/source/${pkgname}.${_pkgver}.tar.gz"
  "${pkgname}.install")
md5sums=('efe0f7ef99eb9e4a75d397f59adae498'
         'c3652d79011cd3fab2e1081e22f9f513')

build() {

  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build
  cmake -Wno-dev \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGEANT4_BUILD_MULTITHREADED=ON \
    -DGEANT4_USE_GDML=ON \
    -DGEANT4_USE_G3TOG4=ON \
    -DGEANT4_USE_QT=ON \
    -DGEANT4_USE_XM=ON \
    -DGEANT4_USE_OPENGL_X11=ON \
    -DGEANT4_USE_RAYTRACER_X11=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    ../${pkgname}.${_pkgver}

#default    -DCMAKE_BUILD_TYPE=Release \
#default    -DGEANT4_INSTALL_DATA=OFF \
#default    -DGEANT4_USE_SYSTEM_CLHEP=OFF \
#default    -DGEANT4_USE_SYSTEM_EXPAT=ON \
#default    -DGEANT4_USE_SYSTEM_ZLIB=OFF \
#default    -DCMAKE_INSTALL_BINDIR=bin \
#default    -DCMAKE_INSTALL_INCLUDEDIR=include \
#default    -DGEANT4_USE_SYSTEM_CLHEP=OFF \

#  make -j4 || return 1
  make || return 1

##  -DGEANT4_USE_INVENTOR=ON \ this seems to break things because it doesn find -lInventor and -lInventorXt
## How to install OpenScientist?
}

package() {

  #Since the basic package does not include the data files, their
  #configuration should be removed from the configuration file. Data
  #files are also available on the AUR and the environment variables are
  #set automatically for you from the packages.

  msg "Removing wrongly set environment variables"

  variables=("G4LEDATA" \
    "G4LEVELGAMMADATA" \
    "G4NEUTRONHPDATA" \
    "G4NEUTRONXSDATA" \
    "G4PIIDATA" \
    "G4RADIOACTIVEDATA" \
    "G4REALSURFACEDATA" \
    "G4SAIDXSDATA" \
    "G4ABLADATA" )


  for _varname in ${variables[*]}
  do
    sed -i "/${_varname}/d" ${srcdir}/build/InstallTreeFiles/geant4.sh
    sed -i "/${_varname}/d" ${srcdir}/build/InstallTreeFiles/geant4.csh
  done

  cd ${srcdir}/build
  make DESTDIR="${pkgdir}" install

  echo 'pushd /usr/bin &> /dev/null && source geant4.sh  && popd &>/dev/null' > ${srcdir}/geant4.profile.sh
  echo 'pushd /usr/bin &> /dev/null && source geant4.csh && popd &>/dev/null' > ${srcdir}/geant4.profile.csh
  install -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/geant4.profile.sh  ${pkgdir}/etc/profile.d/geant4.sh
  install -m755 ${srcdir}/geant4.profile.csh ${pkgdir}/etc/profile.d/geant4.csh
}
