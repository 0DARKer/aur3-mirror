# Maintainer: Luis Sarmiento < lgsarmientop-ala-unal.edu.co >
pkgname='geant4'
pkgdesc="A simulation toolkit for particle physics interactions."
optdepends=('java-environment: for histogram visualizations and analysis'
	    'tcsh: for C Shell support'
	    'python: for G4Python support'
	    'geant4-abladata: Data files for nuclear shell effects'
            'geant4-ledata: Data files for low energy electromagnetic processes'
            'geant4-levelgammadata: Data files for photon evaporation'
            'geant4-neutronhpdata: Neutron data files with thermal cross sections'
            'geant4-neutronxsdata: Data files for evaluated neutron cross sections on natural composition of elements'
            'geant4-piidata: Data files for shell ionisation cross sections'
	    'geant4-radioactivedata: Data files for radioactive decay hadronic processes'
	    'geant4-realsurfacedata: Data files for measured optical surface reflectance'
)
makedepends=('cmake>=2.6')
depends=('cmake' 'xerces-c' 'qt' 'lesstif' 'mesa')
replaces=('geant4-deb')
pkgver=9.6
_pkgver=9.6
pkgrel=1

url="http://geant4.cern.ch/"
arch=('x86_64' 'i686')
license=('custom: http://geant4.cern.ch/license/')
options=('!emptydirs')

source=("http://geant4.cern.ch/support/source/${pkgname}.${_pkgver}.tar.gz"
  "${pkgname}.install")
md5sums=('5cf83937d4e868167c7e21d70b780bb1'
         '116cde072b1db4c0840974b316f4be5d')

build() {
	[ -d ${srcdir}/build ] && rm -rf ${srcdir}/build
	mkdir ${srcdir}/build
	cd ${srcdir}/build
	cmake -DGEANT4_USE_GDML=ON \
              -DGEANT4_USE_G3TOG4=ON \
              -DGEANT4_USE_QT=ON \
	      -DQT_QMAKE_EXECUTABLE=`which qmake` \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DGEANT4_INSTALL_EXAMPLES=ON \
              -DGEANT4_USE_RAYTRACERX=ON \
              -DGEANT4_INSTALL_DATA=OFF \
              -DGEANT4_USE_OPENGL_X11=ON \
              -DGEANT4_RAYTRACER_X11=ON \
              -DGEANT4_SYSTEM_CLHEP=OFF \
              -DGEANT4_XM=ON \
              -DCMAKE_INSTALL_LIBDIR=/usr/lib \
              ../${pkgname}.${_pkgver}

	make
}

package() {

  cd ${srcdir}/build
  make DESTDIR="${pkgdir}" install

  echo 'pushd /usr/bin &> /dev/null && source geant4.sh && popd &>/dev/null' > ${srcdir}/geant4.profile.sh
  echo 'pushd /usr/bin &> /dev/null && source geant4.csh && popd &>/dev/null' > ${srcdir}/geant4.profile.csh
  install -d ${pkgdir}/etc/profile.d
  install -m755 ${srcdir}/geant4.profile.sh ${pkgdir}/etc/profile.d/geant4.sh
  install -m755 ${srcdir}/geant4.profile.csh ${pkgdir}/etc/profile.d/geant4.csh
}
