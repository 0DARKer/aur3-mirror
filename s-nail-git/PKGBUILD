# Maintainer: Gavin Troy <gavtroy@fastmail.fm>
# Based on heirloom-mailx PKGBUILD

pkgname=s-nail-git
pkgver=latest
pkgrel=5
pkgdesc="Commandline utility for sending and receiving email"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/s-nail/"
license=('custom:BSD')
depends=('libidn' 'openssl')
makedepends=('git')
optdepends=('smtp-forwarder: for sending mail')
provides=('mailx' 'heirloom-mailx')
conflicts=('mailx' 'heirloom-mailx')
backup=('etc/mail.rc')
_gitname='s-nail'
source=("${_gitname}::git://git.code.sf.net/p/s-nail/code")
#source=("${_gitname}::git://git.code.sf.net/p/s-nail/code#branch=next")
sha1sums=('SKIP')

pkgver() {
	cd $_gitname
	# pacman treats "_" like a ".", so use "_x" to seperate the git tag.
	# otherwise pacman may think XX.Y_git to XX.Y.Z_git is a downgrade
	gitver=$(git describe --tags --dirty | cut -c 2-)
	gitver=${gitver//-/_}; echo ${gitver/_/_x}
}

build() {
	cd $_gitname
	make PREFIX=/usr \
	     SYSCONFDIR=/etc \
	     MANDIR=/usr/share/man \
	     SYSCONFRC=/etc/mail.rc \
	     MAILSPOOL=/var/spool/mail \
	     CFLAGS="${CFLAGS} ${CPPFLAGS}" \
	     LDFLAGS="${LDFLAGS}" \
		 _update-version all
}

package() {
	cd $_gitname
	make DESTDIR="${pkgdir}" packager-install

	ln -sf s-nail "${pkgdir}"/usr/bin/mail
	ln -sf s-nail "${pkgdir}"/usr/bin/mailx
	ln -sf s-nail.1.gz "${pkgdir}"/usr/share/man/man1/mail.1.gz
	ln -sf s-nail.1.gz "${pkgdir}"/usr/share/man/man1/mailx.1.gz

	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
