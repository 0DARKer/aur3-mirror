# Maintainer: Gavin Troy <gavtroy@gmail.com>
# Based on heirloom-mailx PKGBUILD

pkgname=s-nail-git
pkgver=latest
pkgrel=1
pkgdesc="A commandline utility for sending and receiving email. Fork of Heirloom mailx"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/s-nail/"
license=('custom')
depends=('openssl' 'krb5')
makedepends=('git')
optdepends=('smtp-forwarder: for sending mail')
provides=('mailx')
conflicts=('mailx')
backup=('etc/mail.rc')
_gitname='s-nail'
source=("${_gitname}::git://git.code.sf.net/p/s-nail/code")
sha1sums=('SKIP')

pkgver() {
	cd "$srcdir/$_gitname"
	#echo "`git describe --tags --abbrev=0 | cut -c 8-`.`git describe --always`"
	echo "`git describe --tags --abbrev=0 | cut -c 8-`_`git shortlog | grep -c '\s\+'`"
}

build() {
	cd "$srcdir/$_gitname"

	sed -i 's|/etc/nail.rc|/etc/mail.rc|g' nail.1

	# For Linux and BSD, this should be set.
	echo "set bsdcompat" >> nail.rc

	sed -i "s/pg/less/" cmd1.c

	echo PREFIX=/usr \
			 MANDIR=/usr/share/man \
			 SYSCONFDIR=/etc \
			 SYSCONFRC=/etc/mail.rc \
			 SENDMAIL=/usr/sbin/sendmail \
			 MAILSPOOL=/var/spool/mail \
			 UCBINSTALL=/usr/bin/install > makeflags

	make `cat makeflags`
}

package() {
	cd "$srcdir/$_gitname"
	make DESTDIR="${pkgdir}" `cat makeflags` install

	ln -sf s-nail "${pkgdir}"/usr/bin/mail
	ln -sf s-nail "${pkgdir}"/usr/bin/mailx
	ln -sf s-nail.1.gz "${pkgdir}"/usr/share/man/man1/mail.1.gz
	ln -sf s-nail.1.gz "${pkgdir}"/usr/share/man/man1/mailx.1.gz

	install -D -m0644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
