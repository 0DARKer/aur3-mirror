# Maintainer: Ivan Puntiy <ivan.puntiy-at-gmail.com>
pkgname=unix-runescape-client
pkgver=3.8.2
pkgrel=1
pkgdesc="Runescape Client for Linux and Unix"
arch=(any)
url="http://hkprojects.weebly.com/runescape-client-for-linux-and-unix.html"
license=(GPL2)
depends=('java-runtime' 'perl' 'xterm' 'p7zip')
optdepends=('wine: for compatibility mode' 'perl-wx: for rsu-settings')
conflicts=('runescape-client-bin')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::http://dl.dropbox.com/u/11631899/opensource/Perl/$pkgname.tar.gz" 'runescape-beta.desktop')
md5sums=('98b4ca7a8486d86d9babb009514314d0' '82fa354ae0e2e1d505453ed5bf3422f5')

package() {
  cd "$srcdir/runescape"

  _instdir="$pkgdir"/opt/runescape
  mkdir -p "$_instdir"/{share,modules,win32} "$pkgdir"/usr/{bin,share/applications}

  # copy the stuff
  cp -t "$_instdir" *.txt runescape update-runescape-client rsu-settings
  cp -R share "$_instdir"/
  cp -R modules "$_instdir"/
  cp -R "$srcdir"/runescape/templates/packaging/usr/share "$pkgdir"/usr/
  cp -R win32/jawt "$_instdir"/win32/
  cp "$srcdir"/runescape-beta.desktop "$pkgdir"/usr/share/applications/
 
  # symlink executables
  ln -s ../../opt/runescape/runescape "$pkgdir"/usr/bin/runescape
  ln -s ../../opt/runescape/update-runescape-client "$pkgdir"/usr/bin/update-runescape-client
  ln -s ../../opt/runescape/rsu-settings "$pkgdir"/usr/bin/rsu-settings
}

# vim:set ts=2 sw=2 et:
