# Maintainer: Jan Pacner
# Contributor: dumblob <dumblob@gmail.com>

_basename=dao
pkgname=${_basename}-lang-git
pkgver=994.436b449
pkgrel=1
pkgdesc="A lightweight, portable, optionally typed programming language and VM written in C featuring blazingly fast real concurrency, deferring, panicking, OOP, LLVM JIT, bytecode generation, syntax customization using BNF, many modules (e.g. graphics) etc."
arch=('i686' 'x86_64')
url="http://www.daovm.net/"
license=('BSD')
source=(
  "$_basename::git+https://github.com/daokoder/dao"
  "$_basename-modules::git+https://github.com/daokoder/dao-modules"
  "DaoGraphics::git+https://github.com/daokoder/DaoGraphics"
  "$_basename-tools::git+https://github.com/daokoder/dao-tools")
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')
depends=('gcc-libs-multilib' 'mesa' 'glu' 'freeglut' 'clang')
makedepends=('patchelf')
optdepends=()
#optdepends=('emscripten-git: support for compilation to JavaScript')
provides=('dao')
conflicts=()

build() {
  mv "$_basename-modules/"* "$_basename/modules/"
  mv "DaoGraphics/"         "$_basename/modules/"
  mv "$_basename-tools/"*   "$_basename/tools/"
  cd "$_basename"

  # FIXME remove after fixing in the Dao upstream
  for f in \
    'modules/DaoCXX/daoCXX.cpp' \
    'modules/DaoJIT/daoJIT.cpp' \
    'modules/DaoJIT/daoJIT.h'; do
    # llvm/LLVMContext.h -> llvm/IR/LLVMContext.h
    sed -i -r -e 's|llvm/(LLVMContext\.h)|llvm/IR/\1|' \
              -e 's|llvm/(Module\.h)|llvm/IR/\1|' \
              -e 's|llvm/(DerivedTypes\.h)|llvm/IR/\1|' \
              -e 's|llvm/(Constants\.h)|llvm/IR/\1|' \
              -e 's|llvm/(Instructions\.h)|llvm/IR/\1|' \
              -e 's|llvm/(DataLayout\.h)|llvm/IR/\1|' \
              -e 's|llvm/(IRBuilder\.h)|llvm/IR/\1|' "$f"
  done

  # the "graph" module is deprecated in favor of the "DaoGraphics" module
  rm -rf tools/graph/

  # FIXME remove because of some bugs
  rm -rf modules/DaoCXX/
  rm -rf tools/clangdao/

  # enable JIT and set CPU count
  sed -i -r -e "s/# *(cpu *=).*/\1$(grep '^processor' /proc/cpuinfo | wc -l)/" \
            -e 's/# *(jit *=).*/\1yes/' dao.conf

  msg "Starting make..."
  _opts="       --option-INSTALL-PATH '$pkgdir/usr/lib/dao/'"
  _opts="$_opts --option-BIN-PATH     '$pkgdir/usr/bin/'"
  _opts="$_opts --option-LIB-PATH     '$pkgdir/usr/lib/dao/'"
  _opts="$_opts --option-INC-PATH     '$pkgdir/usr/include/dao/'"
  _opts="$_opts --option-MOD-PATH     '$pkgdir/usr/lib/dao/modules/'"
  _opts="$_opts --option-DOC-PATH     '$pkgdir/usr/share/doc/dao/'"
  make -f Makefile.daomake linux RESET='--reset' OPTIONS="$_opts"
    #JAVASCRIPT='ON'
}

pkgver() {
  cd "$_basename"
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

package() {
  cd "$_basename"
  make install
  #FIXME try to hack the Dao build system and change rpath of dao_libdao.so
  #  to /usr/lib/
  patchelf --set-rpath /usr/lib/dao/ "$pkgdir/usr/bin/dao"
  patchelf --set-rpath /usr/lib/dao/ "$pkgdir/usr/bin/daomake"
  patchelf --set-rpath /usr/lib/dao/ "$pkgdir/usr/bin/daotest"
  install -D -m644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
