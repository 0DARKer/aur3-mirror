pkgname=winformsui-monoport-hg
pkgver=5
pkgrel=2
pkgdesc="The docking library for .Net Windows Forms development which mimics Visual Studio .Net."
url="https://bitbucket.org/hindlemail"
arch=(i686 x86_64)
license=("MIT")
depends=(mono)
makedepends=(mercurial)
source=("hg+https://bitbucket.org/hindlemail/winformsui-monoport"
"WinFormsUI.snk"
"winforms-docking.pc.in")
md5sums=('SKIP'
         'b9445c254930c4ca5cb78deaf9c95cdb'
         '3a0a520e25c57c73df699e7ae2b00290')

pkgver() {
	cd $srcdir/${pkgname%-*}
	echo $(hg identify -n)
}

build() {
	cd "${srcdir}/${pkgname%-*}"
	xbuild WinFormsUI.Docking.sln
	cd "WinFormsUI/bin/Debug"
	monodis WeifenLuo.WinFormsUI.Docking.dll --output=WeifenLuo.WinFormsUI.Docking.il
	ilasm /dll /key:$srcdir/WinFormsUI.snk WeifenLuo.WinFormsUI.Docking.il
}

package() {
	cd "${srcdir}/${pkgname%-*}/WinFormsUI/bin/Debug"
	install -Dm644 WeifenLuo.WinFormsUI.Docking.dll "$pkgdir/usr/lib/winformsui/WeifenLuo.WinFormsUI.Docking.dll"
	install -Dm644 WeifenLuo.WinFormsUI.Docking.dll.mdb "$pkgdir/usr/lib/winformsui/WeifenLuo.WinFormsUI.Docking.dll.mdb"
	gacutil -i "$pkgdir/usr/lib/winformsui/WeifenLuo.WinFormsUI.Docking.dll" -root "$pkgdir/usr/lib"
	install -Dm644 "${srcdir}/${pkgname%-*}/license.txt" "$pkgdir/usr/share/licenses/winformsui/license.txt"
	#find "$pkgdir/usr/lib/winformsui/" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
	install -Dm644 "$srcdir/winforms-docking.pc.in" "$pkgdir/usr/lib/pkgconfig/winforms-docking.pc"
}
