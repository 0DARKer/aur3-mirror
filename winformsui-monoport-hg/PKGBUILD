pkgname=winformsui-monoport-hg
pkgver=5
pkgrel=3
pkgdesc="The docking library for .Net Windows Forms development which mimics Visual Studio .Net."
url="https://bitbucket.org/hindlemail"
arch=(i686 x86_64)
license=("MIT")
depends=(mono)
makedepends=(mercurial monodevelop)
options=(!makeflags)
source=("hg+https://bitbucket.org/hindlemail/winformsui-monoport"
"WinFormsUI.snk")
md5sums=('SKIP'
         'b9445c254930c4ca5cb78deaf9c95cdb')

pkgver() {
	cd "$srcdir/${pkgname%-*}"
	echo $(hg identify -n)
}

prepare() {
	cd "$srcdir/${pkgname%-*}"
	mdtool generate-makefiles -s -d:Debug WinFormsUI.Docking.sln
}

build() {
	cd "${srcdir}/${pkgname%-*}"
	./configure --prefix=/usr
	make
	cd "WinFormsUI/bin/Debug"
	monodis WeifenLuo.WinFormsUI.Docking.dll --output=WeifenLuo.WinFormsUI.Docking.il
	ilasm /dll /key:"$srcdir/WinFormsUI.snk" WeifenLuo.WinFormsUI.Docking.il
}

package() {
	cd "${srcdir}/${pkgname%-*}"
	make DESTDIR="$pkgdir" install
	find "$pkgdir" -name '*.dll' -exec gacutil -i {} -root "$pkgdir/usr/lib" \;
	mkdir -p "$pkgdir/usr/share/licenses/winformsui-monoport/"
	cd "$pkgdir/usr/share/licenses/winformsui-monoport/"
	ln -s /usr/lib/winformsui.docking/license.txt .
	find "$pkgdir" -name '*.dll' -o -name '*.exe' | xargs -rtl1 mono --aot
}
