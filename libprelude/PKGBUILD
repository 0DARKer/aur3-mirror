# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>

pkgname=libprelude
pkgver=1.0.1
pkgrel=2
pkgdesc="Provides the framework for using the Prelude system"
arch=('i686' 'x86_64')
url="http://www.prelude-ids.org"
options=('!libtool')
license="GPL"
depends=('gnutls' 'python' 'perl')
makedepends=('swig')
source=(https://www.prelude-ids.org/attachments/download/241/$pkgname-$pkgver.tar.gz)

build() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -i -e 's/#undef gets//' -e 's/_GL_WARN_ON_USE (gets.*//' libmissing/stdio.in.h
  sed -i -e '/extern const void/d' src/include/prelude-plugin.h
  sed -i -e '/#include <gnutls\/extra.h>/d' prelude-admin/server.c prelude-admin/prelude-admin.c

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --without-lua
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -name '.packlist' -exec rm {} \;
  rm -r "$pkgdir"/usr/lib/perl5/core_perl
  
  mkdir "$pkgdir"/usr/lib/perl5/vendor_perl
  mv "$pkgdir"/usr/lib/perl5/site_perl/auto/* "$pkgdir"/usr/lib/perl5/vendor_perl
  rm -r "$pkgdir"/usr/lib/perl5/site_perl
}

md5sums=('dce1ea9f82cf436830567894e7ee622f')
