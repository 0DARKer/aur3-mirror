
pkgname=gtkterm-git
pkgver=20120426
pkgrel=1
pkgdesc="A gtk+ based serial port communication program"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/gtkterm/"
license=('GPL')
depends=('gtk3' 'vte3')
makedepends=('git' 'intltool' 'gnome-common')
provides=('gtkterm')
conflicts=('gtkterm')

source=('configure.ac.patch'
        'Makevars')
md5sums=('d1b4c53570ff279b5f60037c820c035e'
          '6034f4ece88754c0668953aae48e7bfd')

_gitroot="http://git.fedorahosted.org/git/gtkterm.git"
_gitname="gtkterm"

build() {
	cd "${srcdir}"
	msg "Connecting to GIT server..."
	
	if [ -d "${_gitname}" ] ; then
		cd "${_gitname}" && git fetch
		msg "The local files are updated."
	else
		git clone --mirror "${_gitroot}" "${_gitname}"
	fi
	
	msg "GIT checkout done or server timeout."

	cd "${srcdir}"

	if [ -d "${_gitname}-build" ]; then
		rm -rf "${_gitname}-build"
	fi

	git clone "${_gitname}" "${_gitname}-build"

	cd "${_gitname}-build"

	patch -p1 -i ../configure.ac.patch
	cp ../Makevars po/

	msg "Starting configure..."
	./autogen.sh --prefix=/usr --mandir=/usr/share/man
	msg "Starting make..."
	make
}

package() {
	cd "${srcdir}/${_gitname}-build"
	make DESTDIR="${pkgdir}" install

	cd "${srcdir}"
	rm -rf "${_gitname}-build"
}
