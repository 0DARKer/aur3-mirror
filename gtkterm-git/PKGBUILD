
pkgname=gtkterm-git
pkgver=20110922
pkgrel=1
pkgdesc="A gtk+ based serial port communication program"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/gtkterm/"
license=('GPL')
depends=('gtk2>=2.6' 'vte>=0.20')
makedepends=('git' 'intltool' 'gnome-common')
provides=('gtkterm')
conflicts=('gtkterm')

source=('configure.ac.patch'
        'Makevars')
md5sums=('d1b4c53570ff279b5f60037c820c035e'
          '6034f4ece88754c0668953aae48e7bfd')

_gitroot="http://git.fedorahosted.org/git/gtkterm.git"
_gitname="gtkterm"

build() {
	cd "${srcdir}" || return 1
	msg "Connecting to GIT server..."
	
	if [ -d "${_gitname}" ] ; then
		cd "${_gitname}" && git pull && cd ..
		msg "The local files are updated."
	else
		git clone "${_gitroot}" "${_gitname}"
	fi
	
	msg "GIT checkout done or server timeout."
	cd "${_gitname}" || return 1

	patch -p1 -i ../configure.ac.patch
	cp ../Makevars po/

	msg "Starting configure..."
	./autogen.sh --prefix=/usr --mandir=/usr/share/man || return 1
	msg "Starting make..."
	make || return 1
}

package() {
	cd "${srcdir}/${_gitname}" || return 1
	make DESTDIR="${pkgdir}" install
}
