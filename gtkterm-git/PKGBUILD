# Maintainer: Benjamin Robin <benjarobin>

pkgname=gtkterm-git
_gitname=gtkterm
pkgver=108.2e16624
pkgrel=1
pkgdesc="A gtk+ based serial port communication program"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/gtkterm/"
license=('GPL')
depends=('gtk3' 'vte3')
makedepends=('git' 'intltool' 'gnome-common')
provides=('gtkterm')
conflicts=('gtkterm')
source=('gtkterm::git+http://git.fedorahosted.org/git/gtkterm.git'
        'gtkterm-tmpfiles.conf' 'lock.patch')

md5sums=('SKIP'
         '995d321212c654d0b4f721ea859f2739'
         'b7883432a5fb01ff3eb436eded332338')

pkgver()
{
    cd "${_gitname}"
    echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build()
{
    cd "${_gitname}"
    rm -f aclocal.m4 configure Makefile.in depcomp
    patch -p1 < "${srcdir}/lock.patch"

    msg "Starting configure..."
    ./autogen.sh --prefix=/usr --mandir=/usr/share/man
    msg "Starting make..."
    make
}

package()
{
    cd "${_gitname}"
    make DESTDIR="${pkgdir}" install

    install -D -m644 "${srcdir}/gtkterm-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/gtkterm.conf"
}
