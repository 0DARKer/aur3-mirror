# Maintainer: Jesus Alvarez <jeezusjr@gmail.com>
# Contributor: sxe <sxxe@gmx.de>
# Based on the wine-git in AUR

pkgname='netflix-desktop'
pkgver=0.6.2
pkgrel=1
pkgdesc="An automated script for viewing Netflix through Firefox and patched WINE"
url="https://aur.archlinux.org/packages/netflix-desktop/"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('wine-silverlight' 'ttf-ms-fonts' 'zenity')

if [[ "$CARCH" == "x86_64" ]]; then
  depends+=(
    'lib32-libsm' 'lib32-alsa-lib' 'lib32-openal' 'lib32-mpg123'
    'lib32-libpng12' 'lib32-libpng' 'lib32-libxcomposite'
  )
  optdepends=(
    'lib32-ati-dri: for open source ATI driver users'
    'lib32-catalyst-utils: for AMD Catalyst users'
    'lib32-intel-dri: for open source Intel driver users'
    'lib32-nouveau-dri: for Nouveau users'
    'lib32-nvidia-utils-bumblebee: for NVIDIA + Bumblebee users'
    'lib32-nvidia-utils: for NVIDIA proprietary blob users'
    'xorg-xset: for screensaver/DPMS toggling'
  )
else
  depends+=('libsm' 'libpng12' 'libpng' 'alsa-lib' 'openal' 'mpg123'
            'libxcomposite')
fi

install=netflix-desktop.install

source=("http://demizerone.com/files/netflix-desktop/netflix-desktop-${pkgver}.tar.xz")

md5sums=('c94e0aa187e75dac21b036f9cfef1edf')

build() {
  cd "${srcdir}/${pkgname}"
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
