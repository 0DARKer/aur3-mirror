# Maintainer: Shanto <shanto@hotmail.com>
# Contributor: Shanto <shanto@hotmail.com>

pkgname=eclipse-aptana
pkgver=3.0.5
pkgdate=201110061700
pkgrel=1
pkgdesc="Aptana Studio plugin for Eclipse (HTML5/CSS3/JavaScript/Ruby/Rails/PHP/Pydev/Django)"
url="http://www.aptana.com/products/studio3"
arch=('any')
license=('EPL')
depends=('eclipse')
makedepends=('unzip')
conflicts=('eclipse-pydev')
provides=('eclipse-pydev')
source=(http://d1iwq2e2xrohf.cloudfront.net/tools/studio/plugin/update/studio3/${pkgver}.${pkgdate}/com.aptana.feature.studio-${pkgver}.${pkgdate}-7E37E7HFEPNEFRHU7T_IyJftqMa9.zip)
# http://download.aptana.com/studio3/plugin/update/index.html
md5sums=('b6ca5498b8023e532f2ea7551a5c5977')
options=('!strip')

build() {
	_dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

	cd ${srcdir}
	
	# Cleanup
	find plugins/*.{win32,macosx}.* -delete

	# Features
	find features -type f | while read _feature ; do
	if [[ ${_feature} =~ (.*\.jar$) ]] ; then
		install -dm755 ${_dest}/${_feature%*.jar}
		cd ${_dest}/${_feature/.jar}
		jar xf ${srcdir}/${_feature}
	else
		install -Dm644 ${srcdir}/${_feature} ${_dest}/${_feature}
	fi
	done

	# Plugins
	#_extract_jar={com.aptana.{git,*libraries,parsing,terminal,swt.webkit}*,{com,org}.python.pydev*}
	_keep_as_jar="(.+/com\.aptana\.scripting_.+)"
	find plugins -type f | while read _plugin ; do
	if [[ ${_plugin} =~ (.*\.jar$) && ! ${_plugin} =~ $_keep_as_jar ]] ; then
		install -dm755 ${_dest}/${_plugin%*.jar}
		cd ${_dest}/${_plugin/.jar}
		jar xf ${srcdir}/${_plugin}
	else
		install -Dm644 ${srcdir}/${_plugin} ${_dest}/${_plugin}
	fi
	done
}
