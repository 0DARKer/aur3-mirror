# Maintainer: hauptmech # Contributor: figo.zhang, chubtuff, lubosz
#
# Matlab bindings are not built by default to reduce dependencies.

pkgname=libflann
pkgver=1.7.1
pkgrel=1
pkgdesc="FLANN is a library for performing fast approximate nearest neighbor searches in high dimensional spaces"
arch=('any')
url='http://www.cs.ubc.ca/~mariusm/index.php/FLANN/FLANN'
license=('BSD')
makedepends=('cmake' 'python2')
optdepends=('hdf5' 'gtest' 'intel-tbb' 'cuda')
conflicts=('flann')
source=("https://people.cs.ubc.ca/~mariusm/uploads/FLANN/flann-${pkgver}-src.zip")
_python2libpath="`python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()" | tr -d '\n'`"

build() {
  cd "${srcdir}/flann-${pkgver}-src"

  sed -i 's/lib64/lib/g' cmake/flann_utils.cmake

  mkdir build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_MATLAB_BINDINGS=OFF \
    -DBUILD_PYTHON_BINDINGS=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/python2|' \
  ../bin/download_checkmd5.py \
  ../bin/run_test.py \
  ../build/src/python/setup.py \
  ../src/python/setup.py \
  ../src/python/setup.py.tpl \
  ../test/test_clustering.py \
  ../test/test_index_save.py \
  ../test/test_nn_autotune.py \
  ../test/test_nn_index.py \
  ../test/test_nn.py

  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' \
  ../test/memusage_clustering.py \
  ../test/memusage_nn.py
}

#check() {
#  cd "${srcdir}/flann-${pkgver}-src/build"
#  # Warning: Downloads big data files.
#  make -k test
#}

package() {
  cd "${srcdir}/flann-${pkgver}-src/build"

  #Grab license from a source file. Delete this when COPYING is finally included in source tarball.
  head -n 28 ../src/cpp/flann/defines.h > ../COPYING
  #install license file
  install -D -m644 ../COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  make DESTDIR="$pkgdir/" install

  # FIXME: awful hack, but I got this error without the fix:
  # running install_lib
  # copying build/lib/pyflann/exceptions.py -> /usr/lib/python2.7/site-packages/pyflann
  # error: /usr/lib/python2.7/site-packages/pyflann/exceptions.py: Permission denied
  mkdir -p "${pkgdir}${_python2libpath}"
  cp -pr "${pkgdir}/usr/share/flann/python/pyflann" "${pkgdir}${_python2libpath}/pyflann"
}

md5sums=('1832b560cce9cde1c18ad1c7e907ae76')
sha256sums=('4defae341a989b061a48d227a2967483ee3088a261f7af85f81ca88b537466e9')
sha512sums=('0637ee35e26fa9f3a2be9f3a5f1ae410b7aeecb6f9d317f24689c7863e7091540d859efc5278432936207036030282907289ce8c7ae32f6a9485b93dd3f51fe9')
