# Maintainer: hauptmech
# Contributor: figo.zhang, chubtuff, lubosz
#
# Matlab bindings are not built by default to reduce dependencies.

pkgname=libflann
pkgver=1.7.0
pkgrel=2
pkgdesc="FLANN is a library for performing fast approximate nearest neighbor searches in high dimensional spaces"
arch=('any')
url='http://www.cs.ubc.ca/~mariusm/index.php/FLANN/FLANN'
# FIXME: License file should be installed.
license=('BSD')
makedepends=('cmake')
optdepends=('python2' 'hdf5' 'gtest' 'intel-tbb' 'cuda')
conflicts=('flann')
source=("https://people.cs.ubc.ca/~mariusm/uploads/FLANN/flann-${pkgver}-src.zip")
md5sums=('d002d1c82a862a8258e826a53b0b1a1e')
sha256sums=('921a6b41098214f681e13dfc3501e43825b554e8116dd0e4cf51a3ca97d09264')
sha512sums=('8bc2ea92de9bcd9f1f2f9d273d51e9c7224f8330f0ddcb37947e060520214b0ef6e0f070dc796f542f7662ac5e017bf192ffd7acef195c6881f1a5880f80a698')
_python2libpath="`python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()" | tr -d '\n'`"

build() {
  cd "${srcdir}/flann-${pkgver}-src"

  sed -i 's/lib64/lib/g' cmake/flann_utils.cmake

  mkdir build
  cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_MATLAB_BINDINGS=OFF \
    -DBUILD_PYTHON_BINDINGS=ON \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/python2|' \
  ../bin/download_checkmd5.py \
  ../bin/run_test.py \
  ../build/src/python/setup.py \
  ../src/python/setup.py \
  ../src/python/setup.py.tpl \
  ../test/test_clustering.py \
  ../test/test_index_save.py \
  ../test/test_nn_autotune.py \
  ../test/test_nn_index.py \
  ../test/test_nn.py

  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' \
  ../test/memusage_clustering.py \
  ../test/memusage_nn.py
}

#check() {
#  cd "${srcdir}/flann-${pkgver}-src/build"
#  # Warning: Downloads big data files.
#  make -k test
#}

package() {
  cd "${srcdir}/flann-${pkgver}-src/build"
  make DESTDIR="$pkgdir/" install

  # FIXME: awful hack, but I got this error without the fix:
  # running install_lib
  # copying build/lib/pyflann/exceptions.py -> /usr/lib/python2.7/site-packages/pyflann
  # error: /usr/lib/python2.7/site-packages/pyflann/exceptions.py: Permission denied
  mkdir -p "${pkgdir}${_python2libpath}"
  cp -pr "${pkgdir}/usr/share/flann/python/pyflann" "${pkgdir}${_python2libpath}/pyflann"
}
