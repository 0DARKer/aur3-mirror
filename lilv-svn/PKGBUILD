# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=lilv-svn
_pkg="${pkgname%-svn}"
pkgver=3512
pkgrel=1
pkgdesc="A library to make the use of LV2 plugins as simple as possible for applications."
arch=("i686" "x86_64")
url="http://drobilla.net/software/${_pkg}/"
license=("custom:ISC")
depends=("lv2core" "sord>=0.5.0" "sord<1.0.0" "python2")
makedepends=("subversion" "python" "swig" "lv2core" "lv2-dyn-manifest")
optdepends=("bash-completion: auto-complete words")
options=()

_svntrunk="http://svn.drobilla.net/lad/trunk/${_pkg}"
_svnmod="$_pkg"

build() {
  cd "$srcdir"

  if [[ -d "${_svnmod}/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}/${_svnmod}-build"
  cp -a "${srcdir}/${_svnmod}" "${srcdir}/${_svnmod}-build"
  cd "${srcdir}/${_svnmod}-build"

  #
  # BUILD
  #

  export PYTHON="/usr/bin/python2"

  # remove ldconfig
  sed -i "/ldconfig/d" wscript

  python ./waf configure --prefix=/usr \
                         --configdir=/etc \
                         --mandir=/usr/share/man \
                         --dyn-manifest \
                         --bindings
  python ./waf build
}

package() {
  local _svndir="${srcdir}/${_svnmod}-build"
  . <(egrep "^${_pkg^^}.*VERSION[ =]*'[.0-9]*'" "${_svndir}/wscript" | tr -d ' ' | sed 's/^/_/')
  provides=("${_pkg}=${_LILV_VERSION}")
  conflicts=("${_pkg}>=${_LILV_MAJOR_VERSION}.0.0")

  cd "$_svndir"
  DESTDIR="$pkgdir" python ./waf install

  # license
  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkg}"
  install -m644 -t "${pkgdir}/usr/share/licenses/${_pkg}" COPYING
}

# vim:set ts=2 sw=2 et:
