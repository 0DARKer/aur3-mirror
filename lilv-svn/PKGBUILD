# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=lilv-svn
pkgver=0.18.1.r5335
pkgrel=1
pkgdesc="A C library interface to the LV2 plug-in standard"
arch=('i686' 'x86_64')
url="http://drobilla.net/software/lilv"
license=("custom:ISC")
depends=('sratom')
makedepends=('subversion' 'python')
optdepends=("bash-completion: auto-complete words")
provides=("lilv")
conflicts=("lilv")

_svnname=lilv
source=("svn+http://svn.drobilla.net/lad/trunk/${_svnname}")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_svnname}"
  local rel=`grep "^LILV_VERSION" wscript | cut -d "'" -f 2`
  local ver="$(svnversion)"
  printf "%s.r%s" "${rel}" "${ver//[[:alpha:]]}"
}

build() {
  cd "${srcdir}/${_svnname}"

  python waf configure --prefix=/usr \
                        --configdir=/etc \
                        --dyn-manifest \
                        --bindings

  python waf
}

package() {
  cd "${srcdir}/${_svnname}"
  python waf install --destdir="${pkgdir}"

  # license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
