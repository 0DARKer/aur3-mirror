# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

_pkgname=pangomm
pkgname=mingw32-${_pkgname}
pkgver=2.26.2
pkgrel=1
arch=(any)
pkgdesc="C++ bindings for pango (mingw32)"
depends=('mingw32-pango>=1.23.0' 'mingw32-glibmm>=2.14.1'
         'mingw32-cairomm>=1.2.2')
makedepends=(mingw32-gcc 'pkgconfig')
license=(LGPL)
options=(!strip)
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgname}/2.26/${_pkgname}-${pkgver}.tar.bz2)
sha256sums=('0d20fd80d35f7df01138e24b2ab4a70974c6652528a905605e3173a6955fa739')

build()
{
  cd ${srcdir}/${_pkgname}-$pkgver

  export CPPFLAGS="$CPPFLAGS -D_REENTRANT"
  export PKG_CONFIG_PATH="/usr/i486-mingw32/lib/pkgconfig"
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-documentation \
    --enable-static

  make
}

package() {
  cd ${srcdir}/${_pkgname}-$pkgver
  make install DESTDIR=${pkgdir}

  cd ${pkgdir}
  find . -name '*.a' -o -name '*.dll' -o -name '*.exe' \
    | xargs -rtl1 i486-mingw32-strip -g
}
