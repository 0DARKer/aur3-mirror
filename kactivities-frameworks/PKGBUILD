# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-frameworks
pkgver=4.96.0
pkgrel=1
pkgdesc="Core components for the KDE's Activities"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
depends=('qt5-declarative' 'boost-libs' 'kf5umbrella' 'kdbusaddons'
         'kwindowsystem' 'kservice')
makedepends=('extra-cmake-modules' 'git' 'boost')
groups=('kf5')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/kactivities-${pkgver}.tar.xz")
md5sums=('b92942110b981d74e73d5ecb944a2f67')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
