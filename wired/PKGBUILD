# Contributor: Ray Rashif <schivmeister@gmail.com>
# Previous/other contributor(s): Prashant Vaibhav, Johannes Sjolund

pkgname=wired
pkgver=0.6
pkgrel=2
pkgdesc="Modern semi-professional Digital Audio Workstation"
url="http://wired.sourceforge.net/"
arch=(i686 x86_64)
license=('GPL2')
options=('!libtool')
depends=('wxgtk-2.6' 'dssi' 'soundtouch' 'portaudio' 'libxml2')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz \
        $pkgname.desktop \
        $pkgname.png)

build() {
  cd $srcdir/$pkgname-$pkgver
  ./autogen.sh
  ./configure	--prefix=/usr \
  		--sysconfdir=/etc \
  		--with-wx-config=/usr/lib/wx/config/gtk2-unicode-release-2.6 \
  		--disable-portaudio

  # Force compile with wxgtk-2.6
  find -name Makefile -exec sed -i 's:wx-config:wx-config-2.6:g' {} \; || return 1
  find -name Makefile -exec sed -i 's:2.8.7:2.6.4:g' {} \; || return 1
  find -name Makefile -exec sed -i 's:2.8:2.6:g' {} \; || return 1

  make || return 1
  make DESTDIR=$pkgdir install

  install -Dm644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
  install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
md5sums=('e3e9caedfced4ec9f287e7776b066614'
         '3fc207496ee5c65f656331319df07477'
         'd744c5a3b7d4d5dfbabf105bd3cb7a1d')
