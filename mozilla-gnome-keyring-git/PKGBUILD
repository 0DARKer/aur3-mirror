# Maintainer: Anshuman Bhaduri <anshuman dot bhaduri0 at gmail dot com>
# Contributor: bzt <unmacaque@gmail.com>

_pkgname=mozilla-gnome-keyring
pkgname=${_pkgname}-git
pkgver=111.32f04a6
pkgrel=1
pkgdesc='Mozilla extension to store passwords and form logins in gnome-keyring.'
arch=('i686' 'x86_64')
url='http://github.com/infinity0/mozilla-gnome-keyring'
license=('GPL')
depends=('gnome-keyring' 'firefox>=21.0')
makedepends=('zip' 'unzip' 'xulrunner>=21.0' 'git' 'libgnome-keyring')
provides=('mozilla-gnome-keyring' 'firefox-gnome-keyring')
conflicts=('mozilla-gnome-keyring' 'firefox-gnome-keyring')
replaces=('firefox-gnome-keyring-git')
source=('git://github.com/infinity0/mozilla-gnome-keyring.git')
sha1sums=('SKIP')

_uuid={6f9d85e0-794d-11dd-ad8b-0800200c9a66}

pkgver() {
  cd "${_pkgname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/${_pkgname}"
  make
}

package() {
  mkdir -p "${pkgdir}/usr/lib/firefox/browser/extensions"
  unzip "${srcdir}/${_pkgname}/mozilla-gnome-keyring-*.xpi" \
    -d "${pkgdir}/usr/lib/firefox/browser/extensions/${_uuid}"
}
