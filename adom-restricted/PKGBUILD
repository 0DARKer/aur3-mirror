# Maintainer: Perttu Luukko <"perttu.luukko" followed by "@iki.fi">
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Sergey Mamonov <mrqwer88 (at) gmail (dot) com>
# Contributor: Feirlane

# This is a PKGBUILD for ADOM prereleases restricted only to Indiegogo campaign donors.
# Please fill in the username and password you received by email for the download link to work.

_username=
_password=

DLAGENTS=("http::/usr/bin/wget --user ${_username} --password ${_password} %u")

pkgname=adom-restricted
pkgver=49
pkgrel=1
pkgdesc="A roguelike game with a quest-centric, plot driven structure"
arch=('i686' 'x86_64' 'armv6h')
conflicts=('adom')
provides=('adom')
depends=('ncurses')
url="http://www.adom.de/"
license=('custom: "adom"')
sha1sums=('' '51d28fe3f0420cd354113fd7ceb2a1a7abf1b069')
if [ "$CARCH" = x86_64 ]
  then
  _arch_link='debian_64'
  sha1sums[0]='956ad0a9703678a4af79325f942682b96ccb497c'
 elif [ "$CARCH" = 'armv6h' ]
  then
  _arch_link='arm'
  sha1sums[0]='9738f46c3a9fc658eb36f251f078ac6a99b186a8'
 else
  _arch_link='debian_32'
  sha1sums[0]='ce1136715d94f79e24b4f7ee8d936d0c39675aba'
fi


source=(http://www.ancardia.com/secure/download/current/adom_linux_${_arch_link}_r${pkgver}.tar.gz LICENSE)

package() {
  cd ${srcdir}/adom

  install -m755 -D adom ${pkgdir}/usr/bin/adom
  install -m644 -D ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/adom-restricted/LICENSE
}
