# Author: Russ Allbery
# Maintainer: <grawity@gmail.com>
pkgname=remctl
pkgver=2.18
pkgrel=3
pkgdesc="client/server for running single commands on a remote host using Kerberos v5 authentication"
arch=("i686" "x86_64")
url="http://www.eyrie.org/~eagle/software/remctl/"
license=("custom")
depends=("krb5")
backup=("etc/remctl/remctl.conf")
source=("http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz"
        "remctl.service"
        "remctl@.service"
        "remctl.socket"
        "remctl.xinetd")
sha1sums=('a704fa15aa4e5efe092d35defccae1f2c3152cf3'
          'b168e4618121eed2c3c99cfe26ac2fc593b50b68'
          'ed9527860d4d82d642da05c48fa4e9bc5fd5b97f'
          'a68d48e508b9037098d76dbd386226343c5d1130'
          '3d8c414091b8bf17fa520871c2290e44f16ce979')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  unset PERL_MM_OPT
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/remctl \
    --enable-perl
    #--enable-python (incompatible with py3)
    #--enable-php (requires phpize)
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  unset PERL_MM_OPT
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE              "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 examples/remctl.conf "$pkgdir/etc/remctl/remctl.conf"

  cd "$srcdir"
  install -Dm0644 remctl.service  "$pkgdir/lib/systemd/system/remctl.service"
  install -Dm0644 remctl@.service "$pkgdir/lib/systemd/system/remctl@.service"
  install -Dm0644 remctl.socket   "$pkgdir/lib/systemd/system/remctl.socket"
  install -Dm0644 remctl.xinetd   "$pkgdir/etc/xinetd.d/remctl.xinetd"
}

# vim: ts=2:sw=2:et:ft=sh
