# Author: Russ Allbery
# Maintainer: <grawity@gmail.com>
pkgname=remctl
pkgver=3.0
pkgrel=2
pkgdesc="client/server for running single commands on a remote host using Kerberos v5 authentication"
arch=("i686" "x86_64")
url="http://www.eyrie.org/~eagle/software/remctl/"
license=("custom")
depends=("krb5")
makedepends=("php")
backup=("etc/remctl/remctl.conf")
source=("http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz"
        "http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz.asc"
        "remctl.service"
        "remctl@.service"
        "remctl.socket"
        "remctl.xinetd")
sha1sums=('46e006a3600933bdd5cdbed84a885db90dc97863'
          'cd772647f883b02143c05b0b566899f0c92a9f3c'
          '0c6904840e4e389e3648adfcebb839da88b0bae3'
          'bcee009ea49767f9a92beaa310ed9469084dce05'
          'a68d48e508b9037098d76dbd386226343c5d1130'
          '3d8c414091b8bf17fa520871c2290e44f16ce979')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=python2
  ./configure \
    --prefix=/usr             \
    --sysconfdir=/etc/remctl  \
    --enable-perl             \
    --enable-php              \
    --enable-python           \
    --enable-ruby             \
    ;
  sed -i 's/python setup\.py/python2 setup.py/g' Makefile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE              "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -Dm0644 examples/remctl.conf "$pkgdir/etc/remctl/remctl.conf"
  mkdir -p "$pkgdir/etc/remctl/conf.d"
  mkdir -p "$pkgdir/etc/php/conf.d"
  echo 'extension=remctl.so' > "$pkgdir/etc/php/conf.d/remctl.ini"

  cd "$srcdir"
  install -Dm0644 remctl.service  "$pkgdir/lib/systemd/system/remctl.service"
  install -Dm0644 remctl@.service "$pkgdir/lib/systemd/system/remctl@.service"
  install -Dm0644 remctl.socket   "$pkgdir/lib/systemd/system/remctl.socket"
  install -Dm0644 remctl.xinetd   "$pkgdir/etc/xinetd.d/remctl"
}

# vim: ts=2:sw=2:et:ft=sh
