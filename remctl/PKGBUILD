# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=remctl
pkgver=3.2
pkgrel=1
pkgdesc="client/server for running single commands on a remote host using Kerberos v5 authentication"
arch=("i686" "x86_64")
url="http://www.eyrie.org/~eagle/software/remctl/"
license=("custom")
depends=("krb5")
makedepends=("php")
backup=("etc/remctl/remctl.conf")
source=("http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz"
        "http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz.asc"
        "remctl.service"
        "remctl@.service"
        "remctl.socket"
        "remctl.xinetd")
sha1sums=('f49c287c29b6b289995b8907edc6ecba4c298c99'
          '4353f8a9101a2c039d6ccba9559fd113da9b3f5c'
          '0c6904840e4e389e3648adfcebb839da88b0bae3'
          'bcee009ea49767f9a92beaa310ed9469084dce05'
          'a68d48e508b9037098d76dbd386226343c5d1130'
          'd94d7dc44dd3b287075afe5e2edfbeeabcaad4cb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=python2
  ./configure \
    --prefix=/usr             \
    --sysconfdir=/etc/remctl  \
    --enable-perl             \
    --enable-php              \
    --enable-python           \
    --enable-ruby             \
    ;
  sed -i 's/python setup\.py/python2 setup.py/g' Makefile
  # parallel builds sometimes fail with missing -lremctl
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE              "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -Dm0644 examples/remctl.conf "$pkgdir/etc/remctl/remctl.conf"
  mkdir -p "$pkgdir/etc/remctl/conf.d"
  mkdir -p "$pkgdir/etc/php/conf.d"
  echo 'extension=remctl.so' > "$pkgdir/etc/php/conf.d/remctl.ini"

  cd "$srcdir"
  install -Dm0644 remctl.service  "$pkgdir/usr/lib/systemd/system/remctl.service"
  install -Dm0644 remctl@.service "$pkgdir/usr/lib/systemd/system/remctl@.service"
  install -Dm0644 remctl.socket   "$pkgdir/usr/lib/systemd/system/remctl.socket"
  install -Dm0644 remctl.xinetd   "$pkgdir/etc/xinetd.d/remctl"
}

# vim: ts=2:sw=2:et:ft=sh
