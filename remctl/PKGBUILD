# Author: Peter Marshall
# Packager: grawity@gmail.com
pkgname=remctl
pkgver=2.16
pkgrel=2
pkgdesc="client/server for running single commands on a remote host using Kerberos v5 authentication"
arch=("i686" "x86_64")
url="http://www.eyrie.org/~eagle/software/remctl/"
license=("custom")
depends=("krb5")
source=("http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz"
	"remctl.service"
	"remctl@.service"
	"remctl.socket"
	"remctl.xinetd")
sha1sums=('b21da39182d306545a441de41903d1a73e738b7c'
          'b168e4618121eed2c3c99cfe26ac2fc593b50b68'
          'ed9527860d4d82d642da05c48fa4e9bc5fd5b97f'
          'a68d48e508b9037098d76dbd386226343c5d1130'
          '3d8c414091b8bf17fa520871c2290e44f16ce979')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --sysconfdir=/etc \
		--enable-perl --enable-python
		# --enable-php (needs phpize 'n' stuff)
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	mkdir -p "$pkgdir/lib/systemd/system" "$pkgdir/etc/xinetd.d"
	install -m0644 "$srcdir/remctl.service" "$pkgdir/lib/systemd/system/"
	install -m0644 "$srcdir/remctl@.service" "$pkgdir/lib/systemd/system/"
	install -m0644 "$srcdir/remctl.socket" "$pkgdir/lib/systemd/system/"
	install -m0644 "$srcdir/remctl.xinetd" "$pkgdir/etc/xinetd.d/"
}
# vim: set ft=sh
