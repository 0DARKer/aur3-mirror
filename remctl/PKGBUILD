# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=remctl
pkgver=3.3
pkgrel=1
pkgdesc="client/server for running single commands on a remote host using Kerberos v5 authentication"
arch=("i686" "x86_64")
url="http://www.eyrie.org/~eagle/software/remctl/"
license=("custom")
depends=("krb5")
makedepends=(
  "perl"
  "php"
  "python2"
  "ruby"
)
backup=("etc/remctl/remctl.conf")
source=("http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz"
        "http://archives.eyrie.org/software/kerberos/$pkgname-$pkgver.tar.gz.asc"
        "remctl.service"
        "remctl@.service"
        "remctl.socket")
sha1sums=('ae330e0532080f8e528d1528fde919782fefed1b'
          '72f6e905dc43fef06b47a7ceab9e47e478b0160c'
          '159660bcf6a8dd13257971605dc6c41c28ee706b'
          '2b5873e22b46b05d13161b1fae4bd140a0af613f'
          'b01a79c8f7dd286fbdeb0238333e44e785b5b3d0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export REMCTL_PYTHON_VERSIONS=python2
  ./configure \
    --prefix=/usr             \
    --sysconfdir=/etc/remctl  \
    --enable-perl             \
    --enable-php              \
    --enable-python           \
    --enable-ruby             \
    ;
  # parallel builds sometimes fail with missing -lremctl
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 LICENSE              "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 examples/remctl.conf "$pkgdir/etc/remctl/remctl.conf"
  mkdir -p "$pkgdir/etc/remctl/conf.d"
  mkdir -p "$pkgdir/etc/php/conf.d"
  echo 'extension=remctl.so' > "$pkgdir/etc/php/conf.d/remctl.ini"

  cd "$srcdir"
  install -Dm0644 remctl.service  "$pkgdir/usr/lib/systemd/system/remctl.service"
  install -Dm0644 remctl@.service "$pkgdir/usr/lib/systemd/system/remctl@.service"
  install -Dm0644 remctl.socket   "$pkgdir/usr/lib/systemd/system/remctl.socket"
}

# vim: ts=2:sw=2:et:ft=sh
