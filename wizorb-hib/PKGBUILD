# Maintainer: Prurigro
# Adapted from the wizorb package contributed by TastyPi
# Humble Bundle agent stuff taken from eetsmunchies-hib package by Sam S.

_pkgname=wizorb
pkgname=${_pkgname}-hib
pkgver=1.1
pkgrel=2
pkgdesc="A retro-RPG themed breakout style game (Humble Bundle version)"
url="http://www.wizorb.com"
license=('custom:commercial')
depends=('mono' 'openal')
makedepends=('hib-dlagent-phantomjs-git')
arch=('i686' 'x86_64')
options=('!strip' '!upx')

source=("hib://${_pkgname}-32bit.tar.gz"
        "${_pkgname}.sh"
        "${_pkgname}.desktop")
sha512sums=("b5997e9575c7339667c756d62893d0f20e264ca2cfda6383a3fa4a9ba2a40bfaaf943db5ebff4e73ae7f21624a83e3cfe7f7557b349e7b502282d00cf6959b91"
            '200722760e1d06c7abd7d46f6f3f57eb1011a96c5a90b397b3384cf7dd21d0129fec02c3d2926125f2c1203058aa80294594091776408310dd9fece6d6b7e41a'
            '654dec6278d19d25440e74e7c850f3ab6f06c9af5ad39a7bf7dd8179234e1a3e763364e84aae7daab118db6b1a11d590bbe2621ff05fc7be40069f94bb97f331')
[[ "$CARCH" = "x86_64" ]] && source[0]="hib://${_pkgname}-64bit.tar.gz" && sha512sums[0]='c2ac71e97361e5b2eeaf547987a6de56df4663caf5b6d510b2db5a881ba5873ecb9ad4ba40c3dd8b91eebde5af5da60cdf16141c62fe009f0f43a88f15af7a05'
DLAGENTS+=("hib::/usr/bin/echo 'Could not find %u. Manually download it or add 'hib::/usr/bin/hib-dlagent -u yourusername -p yourpassword -o %o %u' to 'DLAGENTS' in /etc/makepkg.conf.'; exit 1")

package() {
    install -d "${pkgdir}/opt/${_pkgname}"
    cp -r ${_pkgname}/* "${pkgdir}/opt/${_pkgname}/"
    install -Dm755 ${_pkgname}.sh "${pkgdir}/usr/bin/${_pkgname}"
    install -Dm644 ${_pkgname}.desktop "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
}
