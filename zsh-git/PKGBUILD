# Contributor: Daniel Strandberg <esodax!nospam!@gmail.com>

pkgname=zsh-git
pkgver=20100617
pkgrel=1
pkgdesc="Advanced and programmable command interpreter for UNIX"
arch=('i686' 'x86_64')
url="http://www.zsh.org/"
license=('custom')
depends=('ncurses' 'pcre')
makedepends=('git' 'yodl')
provides=('zsh')
conflicts=('zsh')
install=zsh.install

_gitroot="git://zsh.git.sf.net/gitroot/zsh"
_gitname="zsh"

build() {
  cd $srcdir

  if [ -d $_gitname ]; then
    cd $_gitname && git pull origin || return 1
  else
    git clone $_gitroot || return 1
  fi
  rm -rf $srcdir/$_gitname-build
  git clone $srcdir/$_gitname $srcdir/$_gitname-build

  cd $srcdir/$_gitname-build

  ./Util/preconfig

  ./configure --prefix=/usr \
              --bindir=/bin \
              --enable-etcdir=/etc/zsh \
              --enable-zshenv=/etc/zsh/zshenv \
              --enable-zlogin=/etc/zsh/zlogin \
              --enable-zlogout=/etc/zsh/zlogout \
              --enable-zprofile=/etc/profile \
              --enable-zshrc=/etc/zsh/zshrc \
              --enable-maildir-support \
              --with-term-lib='ncursesw' \
              --enable-multibyte \
              --enable-function-subdirs \
              --enable-zsh-mem \
              --with-tcsetpgrp \
              --enable-pcre \
              --enable-zsh-secure-free \
              --disable-gdbm

  make || return 1
}

package() {
  cd $srcdir/$_gitname-build

  make DESTDIR=$pkgdir install || return 1
  install -Dm644 LICENCE $pkgdir/usr/share/licenses/zsh/LICENSE
}

# vim:ts=2:sw=2:et:
