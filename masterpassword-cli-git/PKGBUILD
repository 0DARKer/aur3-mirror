# Maintainer: Patrick Wozniak <email@patwoz.de>

pkgname=masterpassword-cli-git
_gitname=MasterPassword
pkgver=2.2.r33.g20d1811
pkgrel=1
pkgdesc="CLI version of Master Password. (git-version)"

arch=('any')
url="https://github.com/Lyndir/MasterPassword"
license=('GPL3')

optdepends=(
  'xclip: copy password to clipboard'
)
makedepends=(
  'openssl'
  'gcc'
  'git'
  'subversion'
)
conflicts=(
  'masterpassword-cli'
)

source=("git://github.com/Lyndir/MasterPassword.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_gitname"
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$_gitname/$_gitname/C" # change dir to cli version of masterpassword
  targets="mpw" ./build
}

package() {
  cd "$_gitname"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$_gitname/LICENSE"

  cd "$_gitname/C" # change dir to cli version of masterpassword
  # run ./install
  # mkdir -p "$bindir"
  # echo -e "$bindir\nn" | ./install
  # rm "$bindir/bashlib"

  install -Dm755 "mpw" "$pkgdir/usr/bin/mpw"
}
