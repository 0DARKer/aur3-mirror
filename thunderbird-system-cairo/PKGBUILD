# Maintainer: cantabile <cantabile dot desu at gmail dot com>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Anders Bostrom <anders.bostrom@home.se>

_appname=thunderbird
_suffix=-system-cairo
pkgname=${_appname}${_suffix}
pkgver=3.1.7
pkgrel=4
pkgdesc="Standalone Mail/News reader. Uses system cairo."
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('cairo' 'gtk2' 'gcc-libs' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell' 'sqlite3>=3.7.4')
makedepends=('zip' 'libgnomeui' 'python2' 'libidl2' 'wireless_tools' 'autoconf2.13')
optdepends=('libcanberra: for sound support'
            'hunspell-FOO: spellcheck for language FOO')
provides=("${_appname}=${pkgver}")
conflicts=("${_appname}")
source=(ftp://ftp.mozilla.org/pub/mozilla.org/${_appname}/releases/${pkgver}/source/${_appname}-${pkgver}.source.tar.bz2
        mozconfig
        thunderbird.desktop
        thunderbird-3.0-lang.patch
        thunderbird-appversion.patch
        thunderbird-shared-error.patch
        thunderbird-preferences.patch
        xulrunner-png14.patch
        python2.7.patch)

build() {
  cd "${srcdir}/comm-1.9.2"
  patch -Np1 -i "${srcdir}/thunderbird-3.0-lang.patch"
  patch -Np1 -i "${srcdir}/thunderbird-appversion.patch"
  patch -Np1 -i "${srcdir}/thunderbird-shared-error.patch"
  patch -Np1 -i "${srcdir}/thunderbird-preferences.patch"
  patch -Np0 -i "${srcdir}/xulrunner-png14.patch"
  patch -Np0 -i "${srcdir}/python2.7.patch"

  cp "${srcdir}/mozconfig" .mozconfig
  unset CXXFLAGS
  unset CFLAGS

  export LDFLAGS="-Wl,-rpath,/usr/lib/thunderbird-3.1"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  make -j1 DESTDIR="${pkgdir}" install

  #Remove included dictionaries, add symlink to system myspell path.
  #Note: this will cause file conflicts when users have installed dictionaries in the old location
  rm -rf "${pkgdir}/usr/lib/thunderbird-3.1/dictionaries"
  ln -sf /usr/share/myspell/dicts "${pkgdir}/usr/lib/thunderbird-3.1/dictionaries"

  rm -rf "${pkgdir}/usr/bin/defaults"

  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"

  install -m644 mail/branding/unofficial/mailicon48.png \
  	"${pkgdir}/usr/share/pixmaps/thunderbird.png"

  install -m644 "${srcdir}/thunderbird.desktop" \
      "${pkgdir}/usr/share/applications/"

  rm -f ${pkgdir}/usr/lib/pkgconfig/thunderbird-ns{s,pr}.pc
}
md5sums=('be64630152a2d5a824a76752594e7596'
         'e054ea2351d7d2a7ddb34a1776ec8e6e'
         '7a119d30341dca4eadecedd523404fdb'
         '25b6fe16ac24cd5c852213e5c1adb272'
         '48ffcdb877a69d383b7d354e330f7658'
         '3dca714ee8054634e6dfdb059464dc42'
         'fce17db6057c58a17ca1a02f31adef5a'
         '989b15f6bc9e2e9233fe4c6b23f412b1'
         '78a4d0c920cbb8ec6a9a36974cd775ef')
