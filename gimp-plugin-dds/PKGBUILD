# Maintainer: N30N <archlinux@alunamation.com>
# Contributer: J4913 <J49137@gmail.com>

pkgname=gimp-plugin-dds
pkgver=2.0.9
pkgrel=1
pkgdesc="Allows you to load and save images in DirectDraw Surface (DDS) format."
arch=("i686" "x86_64")
url="http://code.google.com/p/gimp-dds/"
license=("GPL")
depends=("gimp>=2.6" "freeglut" "glew")
makedepends=("pkgconfig")
source=("http://gimp-dds.googlecode.com/files/gimp-dds-${pkgver}.tar.bz2")
md5sums=("8c28f023943012500ce3b9e4eff621ed")

build() {
	cd "gimp-dds-${pkgver}"
	sed -r "/^(LIBS=)/s|$| -lm|" -i Makefile.linux
	make
}

package() {
	install -Dm755 "gimp-dds-${pkgver}/dds" \
		"${pkgdir}/usr/lib/gimp/2.0/plug-ins/dds"
}

# vim: set noet ff=unix:
