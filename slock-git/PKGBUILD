# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sebastian A. Liem <sebastian at liem dot se>

pkgname=slock-git
pkgver=20130405
pkgrel=1
pkgdesc='Simple X display locker'
url='http://tools.suckless.org/slock'
license=('MIT')
depends=('libxext')
arch=('i686' 'x86_64' 'armv7h')
makedepends=('git')
source=('git://git.suckless.org/slock')
sha1sums=('SKIP')

provides=('slock')
conflicts=('slock')
replaces=('slock-hg')

prepare() {
	cd "${srcdir}/slock"
	sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
	sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
	sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk
}

build() {
	cd "${srcdir}/slock"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/slock"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
