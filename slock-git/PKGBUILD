# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sebastian A. Liem <sebastian at liem dot se>

pkgname=slock-git
pkgver=20121130
pkgrel=1
pkgdesc='Simple X display locker'
url='http://tools.suckless.org/slock'
license=('MIT')
depends=('libxext')
arch=('i686' 'x86_64' 'armv7h')
makedepends=('git')

provides=('slock')
conflicts=('slock')
replaces=('slock-hg')

_gitroot='http://git.suckless.org/slock/'
_gitname='slock'

build() {
	cd "${srcdir}"

	( cd "${_gitname}" && git pull origin ) || git clone "${_gitroot}" "${_gitname}"
	rm -fr build; cp -a "${_gitname}" build; cd build

	sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
	sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
	sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk

	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/build"

	make PREFIX=/usr DESTDIR="${pkgdir}" install

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
