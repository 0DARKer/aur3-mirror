# Maintainer: ledti <theprimevaloddity@gmail.com>

pkgname=profanity-git
pkgver=20121127
pkgrel=1
pkgdesc="A console based jabber client inspired by irssi, devel version."
arch=('any')
url="https://github.com/boothj5/profanity"
license=('GPL3')
depends=('libxml2' 'libnotify' 'curl' 'expat')
makedepends=('check' 'libstrophe-git' 'git')
conflicts=('profanity')

_gitroot="git://github.com/boothj5/profanity.git"
_gitname="profanity"
_gitbranch="master"

build() {
  cd "$srcdir"
  msg "Connecting to $_gitroot..."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname"
    git pull origin
    msg "Completed update of local files."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  cd $srcdir/$_gitname

  if [[ "$(git branch)" =~ "$_gitbranch" ]]; then
    git checkout "$_gitbranch"
  else
    git checkout -b "$_gitbranch" "origin/$_gitbranch"
  fi

  msg "GIT checkout done or server timeout."
  msg "Cloning to build directory..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"

  cd "$srcdir/$_gitname-build"
  msg "Starting make..."

  ./bootstrap.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

