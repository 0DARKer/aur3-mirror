# Contributor: [Vitaliy Berdinskikh](mailto:ur6lad@archlinux.org.ua) aka UR6LAD
# Contributor: [Jack Zhang](mailto:moonboots08@gmail.com)

[ $CARCH = "x86_64" ] && _arch=-x86_64
_eclipse_name=indigo
_eclipse_release=SR1
_eclipse_timestamp=201109091335

pkgname=eclipse-java
pkgver=3.7
pkgrel=1
pkgdesc="Eclipse IDE for Java Developers"
arch=('i686' 'x86_64')
url="http://www.eclipse.org"
license=("EPL")
depends=('java-environment>=6' 'gtk2>=2.16.1' 'xulrunner>=1.9.0.10')
makedepends=('unzip' 'patch')
provides=('eclipse')
conflicts=('eclipse')
install=${pkgname}.install
source=(
eclipse.sh
eclipse.desktop
eclipse.ini.patch
eclipse.svg
http://ftp.osuosl.org/pub/eclipse/technology/epp/downloads/release/${_eclipse_name}/${_eclipse_release}/eclipse-java-${_eclipse_name}-${_eclipse_release}-linux-gtk${_arch}.tar.gz
)

changelog=${pkgname}.ChangeLog.markdown


md5sums=('00598e0866353f7a7b1a5ed65dc01610'
         '0537090ceeb11a2af66676481e8cf797'
         '9de5334a779524ff68c6026235872b88'
         '5e9975a49de88815a731cbd4c77a136e'
         '2a0af3038349efa7ddc9af00a4d66dda')
sha256sums=('4cca2873697a3af39a96449021d7fdc2fc2b01abc9f7883946081f6be7a5ed48'
            '4eb2189c96fcfa340886b049b34dc3636d7b2bfa865140dc72edb61455d900c3'
            '363361915eef03091a22b80e17712014679bf4ade7fbb43bdafab058b85cdd9f'
            '6fe3ab198af244f9c8c2463b6837855506e811f61e5fd8ac7c9d5fe348830a14'
            '15e1e9aaa21c61e0dcc8e01b71b368efd070a9a9d6f82e266c8eb4f8573b05e6')

[ "$CARCH" = "x86_64" ] && md5sums[4]='71efb534eeae80644cd421e72a22e7e9'
[ "$CARCH" = "x86_64" ] && sha256sums[4]='fc1b47772e01115ecf933bf4b5b489e4fa57f0131f84ddc14ed1f5d4c03498c7'

package() {
	local _icon_path=/usr/share/eclipse/plugins/org.eclipse.platform_${pkgver}.v${_eclipse_timestamp}

	install -m755 -d $pkgdir/usr/{bin,share/applications}
	install -m755 -d $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48,scalable}/apps

	cd $srcdir

	patch -p1 < eclipse.ini.patch

	mv eclipse $pkgdir/usr/share
	install -D -m 755 eclipse.sh $pkgdir/usr/bin/eclipse
	install -D -m 644 eclipse.desktop $pkgdir/usr/share/applications
	install -D -m 644 eclipse.svg $pkgdir/usr/share/icons/hicolor/scalable/apps/eclipse.svg
	ln -s ${_icon_path}/eclipse.png ${pkgdir}/usr/share/icons/hicolor/16x16/apps/eclipse.png
	ln -s ${_icon_path}/eclipse32.png ${pkgdir}/usr/share/icons/hicolor/32x32/apps/eclipse.png
	ln -s ${_icon_path}/eclipse48.png ${pkgdir}/usr/share/icons/hicolor/48x48/apps/eclipse.png
}
