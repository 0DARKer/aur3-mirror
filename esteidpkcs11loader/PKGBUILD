# Maintainer: kevku <kevku@gmx.com>
pkgname=esteidpkcs11loader
_browser=firefox
pkgver=3.7.0.869
pkgrel=1
pkgdesc="Loads PKCS11 module for web authentication with smart cards (Official version by AS Sertifitseerimiskeskus)"
arch=('any')
url="http://www.id.ee/"
license=('LGPL')
depends=('opensc012' 'esteidcerts' 'ccid')
makedepends=('cmake' 'unzip' 'zip')
conflicts=('esteid-browser-plugin-svn' 'sk-esteidpkcs11loader-svn')
source=("https://installer.id.ee/media/sources/$pkgname-$pkgver.tar.gz"
	"https://installer.id.ee/media/sources/$pkgname-$pkgver.tar.gz.asc")
sha256sums=('6e2e8bb339aac7d097770c3e9d5668d1ab8a3eac1c81bab0b84c96395dcfb83c'
            '2cc5345bbf04b9e8fd1c8a9736b531859523b79f30cb1411f1f63e9876cd4c77')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|(bits == 64) ? "/usr/lib64/" : ||g' chrome/content/pkcs11-loader.js
  #sed -i 's|onepin-opensc-pkcs11|opensc-pkcs11|g' chrome/content/pkcs11-loader.js
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR="lib" -DCMAKE_INSTALL_SYSCONFDIR="/etc"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p "$pkgdir/usr/lib/$_browser/extensions"
  cp -r "{aa84ce40-4253-a00a-8cd6-0800200f9a66}" "$pkgdir/usr/lib/$_browser/extensions/"
}
