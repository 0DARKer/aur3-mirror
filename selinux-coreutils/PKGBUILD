# $Id: PKGBUILD 169689 2012-10-26 01:40:04Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Nicky726 <Nicky726@gmail.com>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=selinux-coreutils
_origname=coreutils
pkgver=8.20
pkgrel=1
pkgdesc="SELinux aware basic file, shell and text manipulation utilities of the GNU operating system"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
groups=('selinux' 'selinux-system-utilities')
depends=('glibc' 'selinux-pam' 'acl' 'gmp' 'libcap' 'selinux-usr-libselinux')
conflicts=("${_origname}")
provides=("${_origname}=${pkgver}-${pkgrel}")
replaces=('mktemp')
backup=('etc/pam.d/su')
install=${_origname}.install
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/$_origname/$_origname-$pkgver.tar.xz{,.sig})
md5sums=('3d69af8f561fce512538a9fe85f147ff'
         '7e6076e4e0961d24c38cf8685efca35b')

build() {
  cd ${srcdir}/${_origname}-${pkgver}

  ./configure --prefix=/usr --libexecdir=/usr/lib \
              --enable-no-install-program=groups,hostname,kill,uptime \
              --enable-pam
  make
}

check() {
  cd ${srcdir}/${_origname}-${pkgver}
  make RUN_EXPENSIVE_TESTS=yes check
}

package() {
  cd ${srcdir}/${_origname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd ${pkgdir}/usr/bin
  install -dm755 ${pkgdir}/bin

  # binaries required by FHS
  _fhs=('cat' 'chgrp' 'chmod' 'chown' 'cp' 'date' 'dd' 'df' 'echo' 'false'
        'ln' 'ls' 'mkdir' 'mknod' 'mv' 'pwd' 'rm' 'rmdir' 'stty' 'sync'
        'true' 'uname')
  for i in ${_fhs[@]}; do
    ln -s ../usr/bin/$i ${pkgdir}/bin/$i
  done
}
