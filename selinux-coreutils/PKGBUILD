# Maintainer: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)
# Contributor: Allan McRae (allan <at> archlinux <dot> org) 
# Contributor: judd (jvinet <at> zeroflux <dot> org)

pkgname=selinux-coreutils
_origname=coreutils
pkgver=8.10
pkgrel=1
pkgdesc="SELinux aware basic file, shell and text manipulation utilities of the GNU operating system"
groups=('selinux' 'selinux-system-utilities')
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/coreutils"
depends=('glibc' 'selinux-shadow' 'selinux-pam' 'acl' 'selinux-usr-libselinux' 'gmp' 'libcap')
conflicts=('coreutils' 'mktemp')
provides=('coreutils' 'mktemp')
backup=('etc/pam.d/su')
install=${pkgname}.install
options=('!emptydirs')
source=(ftp://ftp.gnu.org/gnu/${_origname}/${_origname}-${pkgver}.tar.xz
	${_origname}-uname.patch
	${_origname}-pam.patch
	su)
md5sums=('4bb81c051da6e5436fc1ad9a67ae44fe'
         'c4fcca138b6abf6d443d48a6f0cd8833'
         'aad79a2aa6d566c375d7bdd1b0767278'
         'fa85e5cce5d723275b14365ba71a8aad')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  # added pam patch and i18n patch from fedora cvs
  patch -Np1 -i "${srcdir}/${_origname}-pam.patch"

  # linux specific uname improvement (from gentoo portage)
  patch -Np1 -i "${srcdir}/${_origname}-uname.patch" 

  autoreconf -v
  ./configure --prefix=/usr \
              --enable-install-program=su \
              --enable-no-install-program=groups,hostname,kill,uptime \
              --enable-pam 
	#ac_cv_func_openat=no
  make
  #make RUN_EXPENSIVE_TESTS=yes check
  make check
}

package() {
  cd "${srcdir}/${_origname}-${pkgver}"
  make DESTDIR=${pkgdir} install 

  cd "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/"{bin,usr/sbin}

  # binaries required by FHS
  _fhs="cat chgrp chmod chown cp date dd df echo false ln ls \
        mkdir mknod mv pwd rm rmdir stty su sync true uname"
  mv ${_fhs} "${pkgdir}/bin"

  # binaries required by various scripts
  _bin="cut dir dircolors du install mkfifo readlink shred \
        sleep touch tr vdir"
  mv ${_bin} "${pkgdir}/bin"
  ln -sf /bin/sleep "${pkgdir}/usr/bin/sleep"

  mv chroot "${pkgdir}/usr/sbin"
  install -Dm644 "${srcdir}/su" "${pkgdir}/etc/pam.d/su"
}
