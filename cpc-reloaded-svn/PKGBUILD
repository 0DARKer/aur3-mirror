# Contributor: Calimero <calimeroteknik at free dot fr>

_pkgname=cpc-reloaded
pkgname="${_pkgname}-svn"
pkgver=608
pkgrel=2
pkgdesc='CPC, 464, 664, 6128 Amstrad emulator'
arch=('i686' 'x86_64')
url='http://cpcsdk.googlecode.com/'
license=('GPL')
makedepends=('subversion' 'cmake')
depends=('libstsound2-svn' 'portaudio' 'wxgtk')
source=("${_pkgname}::svn+https://cpcsdk.googlecode.com/svn/trunk/Reloaded")
md5sums=('SKIP')

pkgver() {
  cd "$SRCDEST/${_pkgname}"
  svnversion
}

prepare() {
  sed -i 's#"./logo.png"#"/usr/share/reloaded/logo.png"#' "${srcdir}/${_pkgname}/src/gui/CapriceApp.cpp"
  sed -i 's#"logo.png"#"/usr/share/reloaded/logo.png"#' "${srcdir}/${_pkgname}/src/gui/CapriceUI.cpp"
}

build() {
  cd "${srcdir}/${_pkgname}"

  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release -DCMAKE_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_pkgname}"

  install -D -m 755 build/src/gui/reloaded "${pkgdir}/usr/bin/reloaded"

  mkdir -p "${pkgdir}/usr/share/reloaded"
  cp -r rom Keymap.cfg logo.png "${pkgdir}/usr/share/reloaded/"
  sed 's#@DATA_DIR@#/usr/share/reloaded#' cap32.cfg.tpl > "${pkgdir}/usr/share/reloaded/cap32.cfg"
}
