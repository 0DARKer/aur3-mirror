pkgname=kettle
pkgver=4.4.0
pkgrel=1
pkgdesc="Pentaho Data Integration Community Edition (Kettle) is an ETL tool"
arch=any
url="http://kettle.pentaho.com/"
depends=(java-environment
         swt
         webkitgtk2)
license=("LGPLv2.1")
options=(!strip)
source=("http://downloads.sourceforge.net/project/pentaho/Data%20Integration/${pkgver}-stable/pdi-ce-${pkgver}-stable.tar.gz"
        "spoon-patch.diff"
        "spoon.desktop")
md5sums=('1578169425580b0d4da273d129addbb6'
         'aa2f8ca6e305aaf7897e99e610565b6b'
         'd25747bb2d5b45c3e87b08e4c3102723')

package() {
  cd "$srcdir/data-integration"
  rm -rf docs/{cn_ZN,es_AR,Francais,it_I,ja_JP,Korean,Nederlands,pt_BR}
  rm -rf "Data Integration 32-bit.app" "Data Integration 64-bit.app"
  rm *.bat
  rm README_{OSX,UNIX_AS400}.txt
  rm -rf libswt/{aix,osx,osx64,solaris,win32,win64}
  rm -rf libswt/linux/{motif,ppc}
  rm libswt/linux/{x86,x86_64}/swt.jar
  ln -s /usr/share/java/swt.jar libswt/linux/x86/swt.jar
  ln -s /usr/share/java/swt.jar libswt/linux/x86_64/swt.jar

  patch -p0 < ../spoon-patch.diff

  cd ..
  F='#!/usr/bin/bash\n\n/usr/share/data-integration/%s.sh "$@"\n'
  printf "$F" kitchen > kitchen.sh
  install -D -m755 kitchen.sh $pkgdir/usr/bin/kitchen.sh
  printf "$F" pan > pan.sh
  install -D -m755 pan.sh     $pkgdir/usr/bin/pan.sh
  printf "$F" carte > carte.sh
  install -D -m755 carte.sh   $pkgdir/usr/bin/carte.sh
  printf "$F" import > import.sh
  install -D -m755 import.sh  $pkgdir/usr/bin/import.sh
  #spoon needs to change directory
  echo -e '#!/usr/bin/bash\n\ncd /usr/share/data-integration\n./spoon.sh "$@"\n' > spoon.sh
  install -D -m755 spoon.sh   $pkgdir/usr/bin/spoon.sh
  install -D -m644 spoon.desktop $pkgdir/usr/share/applications/spoon.desktop

  cp -a data-integration "$pkgdir/usr/share/data-integration"
  chown -R root:root $pkgdir/usr/share
}

# vim:set ts=2 sw=2 et:
