# Maintainer: willemw <willemw12@gmail.com>

_pkgname=spyder
pkgname=$_pkgname-hg
pkgver=r3626.5c0af92927a3
pkgrel=1
pkgdesc="Scientific PYthon Development EnviRonment providing MATLAB-like features (formerly pydee)"
arch=('any')
url="https://bitbucket.org/spyder-ide/spyderlib"
license=('MIT')
depends=('python2-pyqt4' 'python2-pyflakes' 'python2-pyzmq' 'python2-pygments'
         'python-pyqt4' 'python-pyflakes' 'python-pyzmq' 'python-pygments'
         'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('python2-sphinx' 'python2-setuptools'
             'python-sphinx' 'python-setuptools' 
             'mercurial')
optdepends=('python2-pylint: powerful code analysis'
            'ipython2: enhanced Python interpreter'
            'python2-rope: editor code completion, calltips and go-to-definition'
            'python2-sphinx: rich text help on the object inspector'
            'python2-numpy: N-dimensional arrays'
            'python2-scipy: signal/image processing'
            'python2-psutil: memory/CPU usage in the status bar'
            'python2-h5py: HDF5 support'
            'python2-matplotlib: interactive 2D/3D data plotting'
            'pep8-python2: real-time code style analysis'
            'python2-sympy: symbolic mathematics for the IPython console'

            'python-pylint: powerful code analysis'
            'ipython: enhanced Python interpreter'
            'python-rope: editor code completion, calltips and go-to-definition'
            'python-sphinx: rich text help on the object inspector'
            'python-numpy: N-dimensional arrays'
            'python-scipy: signal/image processing'
            'python-psutil: memory/CPU usage in the status bar'
            'python-h5py: HDF5 support'
            'python-matplotlib: interactive 2D/3D data plotting'
            'pep8-python: real-time code style analysis'
            'python-sympy: symbolic mathematics for the IPython console')
provides=($_pkgname)
conflicts=($_pkgname)
install=$pkgname.install
source=($pkgname::hg+https://bitbucket.org/spyder-ide/spyderlib)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
} 

build() {
  cd $pkgname
  python2 setup.py build
  python setup.py build
}

package() {
  cd $pkgname

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  rm "$pkgdir/usr/bin/spyder_win_post_install.py"

  install -D -m644 spyderlib/__init__.py "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 img_src/spyder.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/spyder.svg"

  sed -i "s|PYLINT_PATH = programs.find_program('pylint')|PYLINT_PATH = programs.find_program('pylint2')|" \
         "$pkgdir"/usr/lib/python2.*/site-packages/spyderplugins/widgets/pylintgui.py
}

