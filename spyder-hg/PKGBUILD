# Maintainer: TDY <tdy@gmx.com>

pkgname=spyder-hg
pkgver=1059
pkgrel=1
pkgdesc="Scientific PYthon Development EnviRonment providing MATLAB-like features (formerly pydee)"
arch=('i686' 'x86_64')
url="http://spyderlib.googlecode.com/"
license=('MIT')
depends=('python-qscintilla')
makedepends=('mercurial' 'setuptools')
optdepends=('pylint:            code analysis'
            'python-matplotlib: 2D plotting'
            'python-numpy:      N-dimensional arrays'
            'python-scipy:      signal/image processing')
provides=('spyder')
conflicts=('spyder')

_hgroot=https://spyderlib.googlecode.com/hg/
_hgrepo=spyderlib

build() {
  cd "$srcdir"

  if [[ -d $_hgrepo/.hg ]]; then
    cd $_hgrepo && hg pull -u && cd ..
  else
    hg clone $_hgroot/$_hgrepo
  fi

  rm -rf $_hgrepo-build
  hg clone $_hgrepo $_hgrepo-build
  cd $_hgrepo-build

  python2 setup.py build
}

package() {
  cd "$srcdir/$_hgrepo-build"
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 $_hgrepo/__init__.py \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
