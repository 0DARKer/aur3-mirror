# Maintainer: willemw <willemw12@gmail.com>

_pkgname=spyder
pkgname=$_pkgname-hg
pkgver=2909.438976af794c
pkgrel=1
pkgdesc="Scientific PYthon Development EnviRonment providing MATLAB-like features (formerly pydee)"
arch=('any')
url="http://spyderlib.googlecode.com"
license=('MIT')
depends=('desktop-file-utils' 'gtk-update-icon-cache' 'python2-pyflakes' 'python2-pyqt4' 'python2-pyzmq')
makedepends=('mercurial' 'python2-distribute' 'python2-sphinx' )
optdepends=('ipython2: enhanced Python interpreter'
            'python2-matplotlib: 2D/3D plotting'
            'python2-numpy: N-dimensional arrays'
            'python2-pylint: powerful code analysis'
            'python2-rope: editor code completion, calltips and go-to-definition'
            'python2-scipy: signal/image processing'
            "python2-sphinx: object inspector's rich text mode")
#optdepends=('ipython: enhanced Python interpreter'
#            'python-psutil: memory/CPU usage in status bar'
#            'python-pyflakes: real-time code analysis'
#            'python-matplotlib: 2D/3D plotting'
#            'python-numpy: N-dimensional arrays'
#            'python-pylint: powerful code analysis'
#            'python-rope: editor code completion, calltips and go-to-definition'
#            'python-scipy: signal/image processing'
#            "python-sphinx: object inspector's rich text mode")
#makedepends=('mercurial' 'python-distribute' 'python-sphinx')
provides=($_pkgname)
conflicts=($_pkgname)
install=$pkgname.install
source=($pkgname::hg+https://code.google.com/p/spyderlib)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  echo $(hg identify -n).$(hg identify -i)
} 

prepare() {
  cd $pkgname
  sed -i "s|PYLINT_PATH = programs.find_program('pylint')|PYLINT_PATH = programs.find_program('pylint2')|" \
         spyderplugins/widgets/pylintgui.py
}

build() {
  cd $pkgname
  python2 setup.py build
  #python setup.py build
}

package() {
  cd $pkgname
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  #python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  rm "$pkgdir/usr/bin/spyder_win_post_install.py"
  install -D -m644 spyderlib/__init__.py "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 scripts/spyder.desktop "$pkgdir/usr/share/applications/spyder.desktop"
  install -D -m644 img_src/spyder.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/spyder.svg"
}

