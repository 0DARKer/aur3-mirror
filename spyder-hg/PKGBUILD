# Maintainer: TDY <tdy@gmx.com>
# Contributor: willemw <willemw12@gmail.com>

pkgname=spyder-hg
_hgrepo=spyderlib
pkgver=2864.1e89053e238b
pkgrel=1
pkgdesc="Scientific PYthon Development EnviRonment providing MATLAB-like features (formerly pydee)"
arch=('any')
url="http://spyderlib.googlecode.com/"
license=('MIT')
depends=('python2-pyqt4' 'python2-pyflakes' 'desktop-file-utils' 'gtk-update-icon-cache' 'python2-pyzmq')
makedepends=('python2-sphinx' 'python2-distribute')
optdepends=('python2-pylint: powerful code analysis'
            'ipython2: enhanced Python interpreter'
            'python2-rope: editor code completion, calltips and go-to-definition'
            "python2-sphinx: object inspector's rich text mode"
            'python2-numpy: N-dimensional arrays'
            'python2-scipy: signal/image processing'
            'python2-matplotlib: 2D/3D plotting')
provides=('spyder')
install=spyder-hg.install
conflicts=('spyder')
source=("hg+https://code.google.com/p/$_hgrepo/")
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/$_hgrepo"
    echo $(hg identify -n).$(hg identify -i)
} 

build() {
    cd "$srcdir/$_hgrepo"
    python2 setup.py build
}

package() {
    cd "$srcdir/$_hgrepo"
    python2 setup.py install --prefix=/usr --root=$pkgdir --optimize=1
    install -D -m644 spyderlib/__init__.py $pkgdir/usr/share/licenses/spyder/LICENSE
    install -D -m644 $srcdir/spyderlib/scripts/spyder.desktop \
                     $pkgdir/usr/share/applications/spyder.desktop
    install -D -m644 $srcdir/spyderlib/img_src/spyder.svg \
                     $pkgdir/usr/share/icons/hicolor/scalable/apps/spyder.svg
}
