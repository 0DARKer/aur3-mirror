# Maintainer: willemw <willemw12@gmail.com>

_pkgname=spyder
pkgname=$_pkgname-hg
pkgver=r3626.5c0af92927a3
pkgrel=3
pkgdesc="Scientific PYthon Development EnviRonment providing MATLAB-like features (Python 3 version)"
arch=('any')
url="https://bitbucket.org/spyder-ide/spyderlib"
license=('MIT')
depends=('python-pyqt4' 'python-pyflakes' 'python-pyzmq' 'python-pygments'
         'desktop-file-utils')
makedepends=('python-sphinx' 'python-setuptools' 
             'mercurial')
optdepends=('python-pylint: powerful code analysis'
            'ipython: enhanced Python interpreter'
            'python-rope: editor code completion, calltips and go-to-definition'
            'python-sphinx: rich text help on the object inspector'
            'python-numpy: N-dimensional arrays'
            'python-scipy: signal/image processing'
            'python-psutil: memory/CPU usage in the status bar'
            'python-h5py: HDF5 support'
            'python-matplotlib: interactive 2D/3D data plotting'
            'pep8-python: real-time code style analysis'
            'python-sympy: symbolic mathematics for the IPython console')
#provides=($_pkgname)
#conflicts=($_pkgname)
source=($pkgname::hg+https://bitbucket.org/spyder-ide/spyderlib)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  printf "r%s.%s" "$(hg identify -n)" "$(hg identify -i)"
} 

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 spyderlib/__init__.py "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm "$pkgdir/usr/bin/spyder_win_post_install.py"
}

