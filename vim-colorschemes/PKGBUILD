# Maintainer: jsteel <mail at jsteel dot org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: William DÃ­az <wdiaz [at] archlinux [dot] us>
# Contributor: Wolfgang Frisch <xororand@frexx.de>
# Contributor: Leandro Inacio <carvalho.inacio@gmail.com>

pkgname=vim-colorschemes
pkgver=20111107
pkgrel=2
pkgdesc="A collection of color schemes from vim.org"
url="http://www.vim.org"
arch=('any')
conflicts=('vim-colorsamplerpack')
depends=('vim')
license=('vim' 'GPL2' 'public-domain' 'as-is')
groups=('vim-plugins')
source=(http://dev.gentoo.org/~radhermit/distfiles/colorschemes-$pkgver.tar.bz2)
md5sums=('414d9990aa8c48ffe3f8e9686d631eee')

build() {
  cd "$srcdir"/colorschemes-$pkgver

  patch -Np0 -i patches/kib_plastic.patch
  patch -Np0 -i patches/lingodirector.vim.patch
  patch -Np0 -i patches/potts.patch

  # fix line endings
  find . -name '*.vim' -exec sed -i -e 's,\r,\n,g' {} \;
}

package() {
  cd "$srcdir"/colorschemes-$pkgver

  install -dm755 "$pkgdir"/usr/share/vim/vim73/colors
  install -m644 colors/* "$pkgdir"/usr/share/vim/vim73/colors/

  # delete existing colors
  rm -r "$pkgdir"/usr/share/vim/vim73/colors/{blue.vim,delek.vim,desert.vim,torte.vim}
}
