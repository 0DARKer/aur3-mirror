# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=zita-resampler
pkgver=1.1.0
pkgrel=1
pkgdesc="A C++ library for resampling audio signals."
url="http://kokkinizita.linuxaudio.org/linuxaudio/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('libsndfile')
source=("${url}downloads/$pkgname-$pkgver.tar.bz2")
md5sums=('75037d8e9dcf068195ea96be5b86da4d')

build() {
  cd "$srcdir/$pkgname-$pkgver/libs"

  # lib
  make PREFIX=/usr

  # create lib link for building apps
  ln -s lib$pkgname.so.$pkgver lib$pkgname.so

  # apps
  cd ../apps
  CXXFLAGS+=" -I../libs" \
  LDFLAGS+=" -L../libs" \
  make PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver/libs"

  # lib
  install -Dm755 lib$pkgname.so.$pkgver \
    "$pkgdir/usr/lib/lib$pkgname.so.$pkgver"

  # link
  ln -s lib$pkgname.so.$pkgver \
    "$pkgdir/usr/lib/lib$pkgname.so"

  # headers
  install -d "$pkgdir/usr/include/$pkgname"
  install -Dm644 $pkgname/*.h \
    "$pkgdir/usr/include/$pkgname"

  # app
  install -Dm755 ../apps/zresample \
    "$pkgdir/usr/bin/zresample"

  # docs
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 ../docs/* \
    "$pkgdir/usr/share/doc/$pkgname"
}
