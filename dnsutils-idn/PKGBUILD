# $Id: PKGBUILD 75202 2010-04-01 02:17:43Z pierre $
# Maintainer: Igor Nemilentsev <trezorg@gmail.com>
pkgname=dnsutils-idn
# Use a period and not a hyphen before the patch level for proper versioning.
pkgver=9.7.1
_pkgver=9.7.1
pkgrel=3
pkgdesc="Various DNS utilities with IDN support - dig host nslookup nsupdate"
arch=(i686 x86_64)
url="https://www.isc.org/software/bind"
license=('custom:ISC')
depends=('openssl' 'idnkit')
replaces=('bind-tools' 'host' 'dnsutils')
provides=('dnsutils')
conflicts=('dnsutils')
options=('makeflags')
source=(http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz
	bind.so_bsdcompat.diff tools-only.patch)
md5sums=('712c425e59cb4aa9309bc4716283742f'
         '447d58721cfee0e1e377b46f7d50b327'
         'be0558f70ed81d90e68e6b6003ba12ed')

build() {
  cd "${srcdir}/bind-${_pkgver}"
  patch -Np0 -i "${srcdir}/bind.so_bsdcompat.diff" || return 1
  patch -Np1 -i "${srcdir}/tools-only.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--with-openssl=yes --disable-linux-caps --without-libxml2  \
        --with-idn=`pwd`/contrib/idn/idnkit-1.0-src || return 1
  make || return 1
}

package() {
  cd "${srcdir}/bind-${_pkgver}/bin"
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
