# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=qimageblitz-svn
pkgver=r1380455
pkgrel=1
pkgdesc="A graphical effect and filter library for KDE"
arch=('i686' 'x86_64')
url="http://www.kde.org/"
license=('GPL2')
depends=('qt5-base')
makedepends=('cmake' 'subversion')
source=("svn://anonsvn.kde.org/home/kde/trunk/kdesupport/qimageblitz")
md5sums=('SKIP')

pkgver() {
  cd qimageblitz
  local ver="$(svnversion)"
  printf "r%s" "${ver//[[:alpha:]]}"
}

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../qimageblitz \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
