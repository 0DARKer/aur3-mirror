# Maintainer: Scott Smith <jcdenton513 gmail com>
pkgname=veracity
pkgver=1.5.0
_verextra=10647
pkgrel=1
pkgdesc="A new open source distributed version control system."
url="http://www.veracity-scm.com"
arch=('x86_64' 'i686')
license=('APACHE')
depends=('curl' 'icu' 'zlib' 'util-linux' 'nspr')
makedepends=('cmake')
install="veracity.install"
source=("http://download.sourcegear.com/Veracity/release/$pkgver.$_verextra/veracity-source-$pkgver.$_verextra.tar.gz"
        "cmake.patch")
sha256sums=('a3cd8cc9508124eb6b3cd5310cc5d3ebaf13a031fbba17b94825bddefdb520ac'
            '48bc26a67ec75959d5b1dbda60224559b361bcc349b0bb6ec55687f00358d2be')

_thisarch=i686
if test "$CARCH" == x86_64; then
  _thisarch=x86_64
fi

options=(!makeflags)
 
build() {
  # Failure in final linking, undefined symbols.
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  cd ${srcdir}/${pkgname}/thirdparty
  ./build_linux.sh

  cd ${srcdir}/${pkgname}
  # supress -Wunused. causes build to fail
  patch -p1 < ../cmake.patch
  # don't bother with testsuite (it ends up being about 800MB in size)
  sed -i /add_subdirectory\(testsuite\)/d CMakeLists.txt
  cmake -G "Unix Makefiles" \
    -DVVTHIRDPARTY=${srcdir}/veracity/vv-thirdparty/${_thisarch} \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}
 
package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname/NOTICE.txt"
  install -Dm644 TRADEMARKS.txt "$pkgdir/usr/share/licenses/$pkgname/TRADEMARKS.txt"
}
 
# vim:set ts=2 sw=2 et:
