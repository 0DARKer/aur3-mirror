# Maintainer: Scott Smith <jcdenton513 gmail com>
pkgname=veracity
pkgver=1.0.0
_verextra=10517
pkgrel=2
pkgdesc="A new open source distributed version control system."
url="http://www.veracity-scm.com"
arch=('x86_64' 'i686')
license=('APACHE')
depends=('curl' 'icu' 'zlib' 'util-linux' 'nspr')
makedepends=('cmake')
source=("http://download.sourcegear.com/Veracity/release/$pkgver.$_verextra/veracity-source-$pkgver.$_verextra.tar.gz"
        "cmake.patch")
sha256sums=('2f9d1e3108229a85adff01d713abaea9eeaf873de5f651db60271efb7f517321'
            '137da4c9aaa361495301bfd3b1d770a75a034fdab90a28105be395da5935f083')

_thisarch=i686
if test "$CARCH" == x86_64; then
  _thisarch=x86_64
fi

 
build() {
  # Failure in final linking, undefined symbols.
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  cd ${srcdir}/${pkgname}/thirdparty
  ./build_linux.sh

  cd ${srcdir}/${pkgname}
  # supress -Wunused. causes build to fail
  patch -p1 < ../cmake.patch
  # don't bother with testsuite (it ends up being about 800MB in size)
  sed -i /add_subdirectory\(testsuite\)/d CMakeLists.txt
  cmake -G "Unix Makefiles" \
    -DVVTHIRDPARTY=${srcdir}/vv-thirdparty/${_thisarch} \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}
 
package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname/NOTICE.txt"
  install -Dm644 TRADEMARKS.txt "$pkgdir/usr/share/licenses/$pkgname/TRADEMARKS.txt"
}
 
# vim:set ts=2 sw=2 et:
