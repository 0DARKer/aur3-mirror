# Maintainer: Scott Smith <jcdenton513 gmail com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=veracity
pkgver=2.1.0
_verextra=10979
pkgrel=1
pkgdesc="A new open source distributed version control system."
url="http://www.veracity-scm.com"
arch=('x86_64' 'i686')
license=('APACHE')
depends=('curl' 'icu' 'nspr')
optdepends=('libgnome-keyring: for remembering authentication credentials')
makedepends=('cmake')
install="veracity.install"
source=("http://download.sourcegear.com/Veracity/release/$pkgver.$_verextra/veracity-source-$pkgver.$_verextra.tar.gz")
sha256sums=('1476db478f74f00dc1298bfc501987f67290c2c4da46d3e56260a52d3967a397')

options=(!makeflags)
 
build() {
  cd "$srcdir"
  sed -i /add_subdirectory\(testsuite\)/d $pkgname/CMakeLists.txt

  rm -rf build
  mkdir build
  cd build

  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    ../$pkgname
  make
}

package() {
  cd "$srcdir"/build

  make DESTDIR="${pkgdir}" install

  cd "$srcdir"/$pkgname
  install -Dm644 NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname/NOTICE.txt"
  install -Dm644 TRADEMARKS.txt "$pkgdir/usr/share/licenses/$pkgname/TRADEMARKS.txt"
}

