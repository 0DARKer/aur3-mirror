# Maintainer: Scott Smith <jcdenton513 gmail com>
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
pkgname=veracity
pkgver=2.0.0
_verextra=10822
pkgrel=1
pkgdesc="A new open source distributed version control system."
url="http://www.veracity-scm.com"
arch=('x86_64' 'i686')
license=('APACHE')
depends=('curl' 'zlib' 'icu' 'nspr' 'openssl')
optdepends=('libgnome-keyring: for remembering authentication credentials')
makedepends=('cmake')
install="veracity.install"
source=("http://download.sourcegear.com/Veracity/release/$pkgver.$_verextra/veracity-source-$pkgver.$_verextra.tar.gz"
        "cmake.patch")
sha256sums=('080561b7566d0798228d304001a684729434eb7b7d864edd4d3734d7a5cefb7d'
            'f2f8cb7b179d8e906c888f2684f9975b43dee11d8e2744950383bca88bddd54f')

options=(!makeflags)
 
build() {
  cd "$srcdir"
  patch -p0 < cmake.patch
  sed -i /add_subdirectory\(testsuite\)/d $pkgname/CMakeLists.txt

  rm -rf build
  mkdir build
  cd build

  cmake -G "Unix Makefiles" ../$pkgname
  make
}

package() {
  cd "$srcdir"/build

  make DESTDIR="${pkgdir}" install

  cd "$srcdir"/$pkgname
  install -Dm644 NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname/NOTICE.txt"
  install -Dm644 TRADEMARKS.txt "$pkgdir/usr/share/licenses/$pkgname/TRADEMARKS.txt"
}

