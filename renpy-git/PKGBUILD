# Maintainer: Cravix < dr dot neemous at gmail dot com >
# base on renpy and python-renpy PKGBUILD written by AlexanderR <rvacheva at nxt dot ru>
pkgname=renpy-git
_pkgname=renpy
pkgver=6.15.4.2554.81b2829
pkgrel=1
pkgdesc="Visual novel engine Ren'Py along with its platdeps libs (development version)"
arch=('i686' 'x86_64')
license=('MIT')
url='http://www.renpy.org'
depends=('ttf-dejavu' 'ffmpeg' 'fribidi' 'glew' 'python2-pygame')
makedepends=('cython2' 'python2-sphinx')
provides=('renpy' 'python-renpy')
conflicts=('renpy-bin' 'renpy64' 'python-renpy' 'renpy')
replaces=('renpy64')
install=renpy.install

source=("${_pkgname}.desktop"
        "${_pkgname}."{sh,csh}
        'renpy-launcher.sh'
        "git://github.com/renpy/renpy.git")
md5sums=('a9beb35fa6c6d8af7ba5d2a764c33158'
         'd206d24b78e207a2c3b603fef14ac47f'
         '8b9922e26e567248a2a5adc1d0cdfdd4'
         'dfa92cdecc15e5c1ddee387fbbbb2d9c'
         'SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  
  echo $(grep -o "(.*)" ./launcher/game/script_version.rpy | sed 's/[()]//g; s/, /./g' ).$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "$srcdir/$_pkgname"
  #python2 fix
  find . -name "*.py" -exec sed -i 's#python$#python2#' {} \;
  #build docs
  sed -i 's/sphinx-build /sphinx-build2 /' ./sphinx/build.sh
  ./sphinx/build.sh
  
  RENPY_CYTHON=/usr/bin/cython2 python2 module/setup.py build
}

package(){
	#pack datas
  mkdir -p "$pkgdir/"{usr/share/{$_pkgname,doc/$_pkgname},etc/profile.d}

  cd "$srcdir"
  install -m755 ${_pkgname}.{sh,csh} "$pkgdir/etc/profile.d"
  install -D -m755 $_pkgname-launcher.sh "$pkgdir/usr/bin/$_pkgname"
  install -D -m644 ${_pkgname}.desktop "$pkgdir/usr/share/applications/${_pkgname}.desktop"

  cd $srcdir/$_pkgname
  cp -r launcher renpy renpy.py  template the_question tutorial "$pkgdir/usr/share/$_pkgname"
  cp -r doc/* "$pkgdir/usr/share/doc/$_pkgname"
  install -D -m644 launcher/game/logo.png "$pkgdir/usr/share/pixmaps/${_pkgname}.png"
  install -D -m644 sphinx/source/license.rst "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"

  chgrp -R games "$pkgdir"/usr/share/renpy/{the_question,tutorial}
  chmod g+w "$pkgdir"/usr/share/renpy/{the_question,tutorial}
  
  #pack modules
	cd "$srcdir/$_pkgname"
  RENPY_CYTHON=/usr/bin/cython2 python2 module/setup.py install --root="$pkgdir/" --prefix=/usr
}
