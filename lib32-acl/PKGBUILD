_pkgbasename=acl
pkgname=lib32-$_pkgbasename
pkgver=2.2.49
pkgrel=3
pkgdesc="Access control list utilities, libraries and headers (32 bit, library only)"
arch=('x86_64')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
depends=('lib32-attr>=2.4.41' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://mirrors.zerg.biz/nongnu/${_pkgbasename}/${_pkgbasename}-${pkgver}.src.tar.gz)
sha256sums=('b9c7f4752e4ef4930a62fa5aa0d7efe1cba2b5a3a2d6ee2b45c0a70c72b7e5d5')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
  			INSTALL_USER=root INSTALL_GROUP=root CC='gcc -m32'
  make 
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DIST_ROOT="${pkgdir}" install install-lib install-dev
  chmod 0755 ${pkgdir}/usr/lib32/libacl.so.*.*.*
  # Cleanup for a lib32 package
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
