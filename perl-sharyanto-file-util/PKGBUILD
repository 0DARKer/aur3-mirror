# Maintainer: 3ED <krzysztof1987 at gmail dot com>
#
pkgname=perl-sharyanto-file-util
_lastauthor=S/SH/SHARYANTO
_pkgname=SHARYANTO-File-Util
pkgver=0.56
pkgrel=1
pkgdesc='File-related utilities'
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-file-chdir' 'perl-file-slurp')
checkdepends=('perl-test-simple')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('a8b48e74a9a6725ce88ed4bcc67c912d029b48e99729694a2cd9deb95b7ba781')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Makefile.PL
  make
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install
}
