# Maintainer: (epsilom) Xavier Corredor <xavier.corredor.llano (a) gmail.com>
# Old-Mantainer: Gonzalo Seguel <gonzaloseguel@gmail.com>

pkgname=kfilebox
pkgver=0.4.8
pkgrel=1
pkgdesc="KDE4 Dropbox client (with own daemon)"
arch=('i686' 'x86_64')
url="http://kdropbox.deuteros.es"
license=('GPL')
depends=('kdebase-workspace' 'gettext')
provides=('dropbox')
replaces=('kdropbox')
source=(http://downloads.sourceforge.net/project/kdropbox/kfilebox-${pkgver}/Source/${pkgname}-${pkgver}.tar.gz)
install=$pkgname.install
md5sums=('348921712d37ec262eff44065c968d11')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    qmake
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make INSTALL_ROOT=${pkgdir} install
    #install locales
    for language in `ls ${srcdir}/${pkgname}-${pkgver}/locale`
    do
      if [ -e ${srcdir}/${pkgname}-${pkgver}/locale/$language/kfilebox.mo ]; then
	install -m 644 -p -D ${srcdir}/${pkgname}-${pkgver}/locale/$language/kfilebox.mo ${pkgdir}/usr/share/locale/$language/LC_MESSAGES/kfilebox.mo
      fi
    done
}
