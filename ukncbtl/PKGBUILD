# Maintainer: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=ukncbtl
pkgver=beta24
pkgrel=2
pkgdesc="An emulator of the MS-0511 (UKNC) computers"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ukncbtl/"
license=('LGPL')
depends=('qt')
makedepends=('subversion')
source=("http://ukncbtl.googlecode.com/svn/trunk/src/UKNCBTL.Qt/uknc_rom.bin"
        "${pkgname}.png"
        "${pkgname}.desktop")
md5sums=('28d4c0de3f08554ae4f8d54f1aad22f4'
         'e5bdc85b256738707e2f4016cd3f7aa2'
         '75bae0fb25b01c63b3279283a53a54f3')

build() {
  cd "${srcdir}"

  _svntrunk="http://ukncbtl.googlecode.com/svn/trunk/src/UKNCBTL.Qt"
  _svnrev=338
  _svnmod="ukncbtl-read-only"

  # Download source code from SVN
  svn co "${_svntrunk}" -r "${_svnrev}" "${_svnmod}"

  # Set path to ROM file
  cd "${pkgname}-read-only"
  sed -i "s|uknc_rom.bin|/usr/share/${pkgname}/uknc_rom.bin|" Emulator.cpp

  # Compile
  qmake
  make
}

package() {
  cd "${srcdir}/${pkgname}-read-only"

  # Install program files
  install -Dm755 QtUkncBtl "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 ../uknc_rom.bin "${pkgdir}/usr/share/${pkgname}/uknc_rom.bin"

  # Install pixmap and .desktop file
  install -Dm644 "../${pkgname}.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "../${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
