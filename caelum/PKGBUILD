# Contributor: Falcata
# Maintainer: Jose Riha <jose1711 gmail com>

pkgname=caelum
pkgver=0.6.1
pkgrel=1
pkgdesc="library for Ogre targeted to help creating photorealistic atmospheric effects"
arch=('i686' 'x86_64')
url="http://www.ogre3d.org/tikiwiki/Caelum"
depends=('ogre' 'cegui' 'doxygen' 'boost')
makedepends=('cmake')
license=('LGPL')
source=(http://caelum.googlecode.com/files/${pkgname}-${pkgver}.zip caelumplugin.patch)
md5sums=('865e38353e84a7c0f81fcaf365642f0f'
         'b136fa52fa8a941c4db315ba8e0a14c9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p0 < ${srcdir}/caelumplugin.patch main/src/CaelumPlugin.cpp
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -r ${pkgdir}/usr/doc
}
