# Maintainer: shad0w73 <shad0w73@freenet.de>

_basename=gperftools
pkgname=lib32-$_basename
pkgver=2.0
pkgrel=1
pkgdesc="Fast, multi-threaded malloc() and nifty performance analysis tools (32-bit)"
arch=('x86_64')
url="http://code.google.com/p/gperftools/"
license=('BSD')
depends=('gperftools' 'lib32-gcc-libs')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://$_basename.googlecode.com/files/$_basename-$pkgver.tar.gz")
sha256sums=('7de3dd91f018825b1e7d332af1edace15c6211f430186febede1835069861080')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'

  cd "$_basename-$pkgver"

  # fix for glibc 2.16
  sed -i 's:siginfo_t:siginfo:' src/base/linuxthreads.cc

  ./configure --prefix=/usr --libdir=/usr/lib32 --enable-frame-pointers
  make
}

package() {
  cd "$_basename-$pkgver"

  # not needed in 32-bit package
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_basename "$pkgdir/usr/share/licenses/$pkgname"
}
