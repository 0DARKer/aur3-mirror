# Maintainer : Alucryd <alucryd at gmail dot com>

pkgname=nvidia-source-beta
pkgver=313.18
pkgrel=1
pkgdesc="NVIDIA kernel module source (DKMS)" 
arch=('i686' 'x86_64') 
[ "$CARCH" = x86_64 ] && ARCH=x86_64 && _srcname=NVIDIA-Linux-x86_64-${pkgver}-no-compat32
[ "$CARCH" = "i686" ] && ARCH=x86 && _srcname=NVIDIA-Linux-x86-${pkgver}
url="http://www.nvidia.com/"
license=('custom')
depends=('linux>=3.6' 'linux<3.8' "nvidia-utils-beta=${pkgver}" 'dkms')
provides=("nvidia=${pkgver}" "nvidia-source=${pkgver}")
conflicts=('nvidia' 'nvidia-source')
source=("http://us.download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/${_srcname}.run")
install=${pkgname}.install

[ "$CARCH" = "i686" ] && md5sums=('780c37c28a6e06e9571cafe348b7da64')
[ "$CARCH" = "x86_64" ] && md5sums=('fa17a260793a38b4b8ae367db2e03b39')

package() {
  cd "${srcdir}"
  if [ -d ${_srcname} ]; then
    rm -rf ${_srcname}
  fi
  sh ${_srcname}.run --extract-only
  cd ${_srcname}
  mkdir -p "${pkgdir}/usr/src"
  cp -R kernel "${pkgdir}/usr/src/nvidia-${pkgver}"
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s "/usr/share/licenses/nvidia" "${pkgdir}/usr/share/licenses/nvidia-source-beta"
  find "${pkgdir}" -type d | xargs chmod 755
  find "${pkgdir}" -type f | xargs chmod 644
}

# vim: ts=2 sw=2 et:
