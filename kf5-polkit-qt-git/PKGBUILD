# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kf5-polkit-qt-git
pkgver=v4.6.0.384.g2ec59ff
pkgrel=1
pkgdesc='Qt wrapper around polkit-1 client libraries'
arch=('i686' 'x86_64')
url='http://phonon.kde.org/'
license=('LGPL')
depends=('qt5-base-git' 'polkit')
makedepends=('cmake' 'git')
provides=('kf5-polkit-qt')
conflicts=('kf5-polkit-qt')
source=(git://anongit.kde.org/polkit-qt-1.git)
md5sums=('SKIP')

pkgver() {
  cd polkit-qt-1
  git describe --always | sed 's|-|.|g' 
}

prepare() {
  mkdir -p build

  cd polkit-qt-1
  git checkout qt5
}

build() {
  cd build
  cmake ../polkit-qt-1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/opt/kf5/{lib64,lib}
}
