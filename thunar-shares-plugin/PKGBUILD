# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: vinz <vinz@archlinux.us>
# Contributor: Aurélien DESBRIÈRES <ice.cube@gmx.com>

pkgname=thunar-shares-plugin
pkgver=0.2.0
pkgrel=7
pkgdesc="A Thunar file manager extension to share files using Samba. Based on nautilus-shares"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/$pkgname"
license=('GPL2' 'LGPL')
depends=('thunar' 'thunar-vfs' 'samba')
options=('!libtool')
source=("http://archive.xfce.org/src/thunar-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.gz")
md5sums=('80659565406f838585a867443aad4102')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # Patch in order to build against thunarx-2
  sed -i "s|\(thunarx-\)1|\12|g" configure{,.in} thunar-plugin/Makefile.{am,in}
  sed -i "s|get_vfs_info|get_file_info|" libshares/libshares-util.c

  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
