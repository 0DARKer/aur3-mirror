# Maintainer: Florian Walch <florian+aur@fwalch.com>
# Contributor: Patrice Peterson <runiq at archlinux dot us>
# Contributor: lucc <l-m-h@web.de>
# Contributor: wenLiangcan <boxeed at gmail dot com>
# Contributor: astiam <astiamm at gmail dot com>

pkgname=python-neovim
pkgver=0.0.36
pkgrel=1
pkgdesc='Adds support for Python 3 plugins to Neovim.'
arch=('any')
license=('Apache')
url='https://github.com/neovim/python-client'
makedepends=('python-setuptools')
depends=('neovim' 'python' 'python-msgpack' 'python-greenlet')
provides=("python3-neovim=${pkgver}")
conflicts=('python3-neovim')
source=("https://github.com/neovim/python-client/archive/${pkgver}.tar.gz")
sha256sums=('64149b2d7bb2db61e21da22e4df4cba93686688784928021b544d54329187b96')

build() {
  cd "${srcdir}/python-client-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/python-client-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
}

# vim:set sw=2 sts=2 et:
