# Maintainer: Florian Walch <florian+aur@fwalch.com>

pkgname=python-neovim
pkgver=0.0.34
pkgrel=1
pkgdesc='Adds support for Python 3 plugins to Neovim.'
arch=('any')
license=('Apache')
url='https://github.com/neovim/python-client'
makedepends=('python-setuptools')
depends=('neovim' 'python' 'python-msgpack' 'python-greenlet')
optdepends=('python-cairo: for experimental GUI (pynvim)'
            'python-click: for experimental GUI (pynvim)'
            'python-gobject: for experimental GUI (pynvim)')
provides=("python3-neovim=${pkgver}")
conflicts=('python3-neovim')
source=("https://github.com/neovim/python-client/archive/${pkgver}.tar.gz")
sha256sums=('2151827e036815eba9e51d10364506b3eacc647125f6f2cd376d397e6b874e47')

build() {
  cd "${srcdir}/python-client-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/python-client-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
}

# vim:set sw=2 sts=2 et:
