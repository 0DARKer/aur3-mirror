# Contributor: Taylor Hedberg <t@tmh.cc>
# Maintainer: Pablo Lluch <pablo.lluch@gmail.com>

pkgname=ike
pkgver=2.2.0
pkgrel=1
pkgdesc='Shrew Soft VPN client for Linux'
arch=(i686 x86_64)
url='http://www.shrew.net'
license=(BSD)
depends=(openssl qt4)
makedepends=(cmake)
optdepends=(openldap)
backup=(etc/iked.conf)
source=("http://www.shrew.net/download/ike/ike-$pkgver-release.tgz" ike.patch iked.service)
md5sums=(51da0d0170b6436f75ca1afe2ff8b74d 45aff18c3d2e9b7ffb061cbe5ff2e870 7e1316feb38bf2f9655ac9ffaeb36bc6)

build () {
    cd "$srcdir/ike"
    patch -Np1 -i ../ike.patch
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DQTGUI=YES -DETCDIR=/etc -DNATT=YES \
        -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 -DMANDIR=/usr/share/man
    make
}

package () {
    cd "$srcdir/ike"
    make DESTDIR="$pkgdir/" install
    install -Dm 755 $srcdir/iked.service $pkgdir/usr/lib/systemd/system/iked.service
    mv $pkgdir/etc/iked.conf{.sample,}
}
