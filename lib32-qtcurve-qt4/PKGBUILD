# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: xduugu

pkgname=lib32-qtcurve-qt4
pkgver=1.8.14
pkgrel=3
pkgdesc='A configurable set of widget styles for KDE and Gtk. Qt4-only version. Multilib.'
arch=('x86_64')
url="http://craigd.wikispaces.com/"
license=('GPL')
groups=('qtcurve')
depends=('lib32-qt4')
makedepends=('cmake' 'gcc-multilib')
source=("${url}file/view/QtCurve-KDE4-$pkgver.tar.bz2" "qconfig.h")
md5sums=('b4d7924806058f39e842ce7ffe47a4f8'
         '6b89fb32651dd73d1c02185d23b86186')

build() {
  cd "$srcdir/QtCurve-KDE4-$pkgver"

  # export multilib parameters
  # and QT_BUILD_KEY workaround
  export CC="gcc -m32 -I$srcdir"
  export CXX="g++ -m32 -I$srcdir"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  [ -d bld ] || mkdir bld && cd  bld

  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_QT_ONLY=true
  make
}

package() {
  cd "$srcdir/QtCurve-KDE4-$pkgver/bld"

  install -Dm755 style/qtcurve.so \
    "$pkgdir/usr/lib32/qt/plugins/styles/qtcurve.so"
}
