# Maintainer: Renato Garcia <fgarcia.renato@gmail.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=opencv-qt
_realname=OpenCV
pkgver=2.3.1
pkgrel=1
pkgdesc="Open Source Computer Vision Library compiled with QT support"
arch=('i686' 'x86_64')
license=('BSD')
url="http://opencv.willowgarage.com/"
depends=('jasper' 'gstreamer0.10-base' 'openexr' 'ffmpeg>=20111030'
         'gtk2' 'xine-lib' 'libdc1394' 'v4l-utils' 'qt')
makedepends=('pkg-config' 'cmake' 'python2-numpy' 'eigen2')
provides=('opencv=2.3.1')
conflicts=('opencv')
optdepends=('python2-numpy: Python 2.x interface'
            'eigen2')
options=('!libtool')
source=("http://downloads.sourceforge.net/opencvlibrary/${_realname}-${pkgver}a.tar.bz2")
md5sums=('82e4b6bfa349777233eea09b075e931e')


_cmakeopts=('-D CMAKE_BUILD_TYPE=Release'
            '-D CMAKE_INSTALL_PREFIX=/usr'
            '-D CMAKE_SKIP_RPATH=ON'
            '-D BUILD_TESTS=OFF'
            '-D ENABLE_SSE=ON'
            '-D ENABLE_SSE2=ON'
            '-D ENABLE_SSE3=OFF'
            '-D ENABLE_SSSE3=OFF'
            '-D ENABLE_SSE41=OFF'
            '-D ENABLE_SSE42=OFF'
            '-D BUILD_EXAMPLES=OFF'
            '-D INSTALL_PYTHON_EXAMPLES=OFF'
            '-D WITH_XINE=ON'
            '-D WITH_QT=ON'
            '-D WITH_QT_OPENGL=ON'
            '-D WITH_UNICAP=OFF'
            '-D WITH_PVAPI=OFF'
            '-D WITH_OPENNI=OFF'
            '-D WITH_TBB=OFF'
            '-D WITH_IPP=OFF'
            '-D WITH_CUDA=OFF'
            '-D USE_FAST_MATH=ON')

build() {
  cd "$srcdir/$_realname-$pkgver"

  # x64, i.e "Athlon64" and upwards, can use SSE3
  [ $CARCH = x86_64 ] && \
    _cmakeopts=${_cmakeopts[@]/ENABLE_SSE3=OFF/ENABLE_SSE3=ON}

  cmake ${_cmakeopts[@]} .

  make
}

package() {
  install=$pkgname.install

  cd "$srcdir/$_realname-$pkgver"

  make DESTDIR="$pkgdir" install

  # install license file
  install -Dm644 "$srcdir/$_realname-$pkgver/doc/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
