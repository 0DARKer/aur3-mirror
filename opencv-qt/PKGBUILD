# Maintainer: Renato Garcia <fgarcia.renato@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=opencv-qt
_realname=opencv
pkgver=2.4.6.1
pkgrel=1
pkgdesc="Open Source Computer Vision Library compiled with QT support"
arch=('i686' 'x86_64')
license=('BSD')
url="http://opencv.org/"
provides=('opencv=2.4.6.1')
conflicts=('opencv')
depends=('jasper' 'gstreamer0.10-base' 'intel-tbb' 'libcl'
         'xine-lib' 'libdc1394' 'openexr' 'gtkglext' 'qt5-base')
makedepends=('cmake' 'python2-numpy' 'mesa'
             'opencl-headers' 'eigen2')
optdepends=('opencv-samples'
            'eigen2'
            'python2-numpy: Python 2.x interface')
source=("http://downloads.sourceforge.net/opencvlibrary/${_realname}-${pkgver}.tar.gz"
        'pkgconfig.patch'
        'fsh.patch'
        'cldetect.patch')
md5sums=('d756bfa460891697571d5c90050e1cfe'
         'cb916260b5ec594fe7a0cc2e54fc569f'
         '35256e3ccace373feba8131d1540a0de'
         '0dd6572405adc2387ded4c95be0f189c')

_cmakeopts=('-D WITH_OPENCL=ON'
            '-D WITH_OPENGL=ON'
            '-D WITH_TBB=ON'
            '-D WITH_XINE=ON'
            '-D WITH_QT=ON'
            '-D BUILD_WITH_DEBUG_INFO=OFF'
            '-D BUILD_TESTS=OFF'
            '-D BUILD_PERF_TESTS=OFF'
            '-D BUILD_EXAMPLES=OFF'
            '-D INSTALL_C_EXAMPLES=OFF'
            '-D INSTALL_PYTHON_EXAMPLES=OFF'
            '-D CMAKE_BUILD_TYPE=Release'
            '-D CMAKE_INSTALL_PREFIX=/usr'
            '-D CMAKE_SKIP_RPATH=ON')

# SSE only available from Pentium 3 onwards (i686 is way older)
[[ "$CARCH" = 'i686' ]] && \
  _cmakeopts+=('-D ENABLE_SSE=OFF'
               '-D ENABLE_SSE2=OFF'
               '-D ENABLE_SSE3=OFF')

# all x64 CPUs support SSE2 but not SSE3
[[ "$CARCH" = 'x86_64' ]] && _cmakeopts+=('-D ENABLE_SSE3=OFF')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # fix missing opencl module
  # see https://bugs.archlinux.org/task/36076
  # and http://code.opencv.org/issues/3140
  patch -Np1 -i "${srcdir}/cldetect.patch"

  # fix pkg-config mess
  # see https://bugs.archlinux.org/task/32430
  # and http://code.opencv.org/issues/1925
  patch -Np1 -i "${srcdir}/pkgconfig.patch"

  # fix another upstream mess that they won't fix
  # see http://code.opencv.org/issues/2512
  patch -Np1 -i "${srcdir}/fsh.patch"

  # no longer including docs, see https://bugs.archlinux.org/task/34185
  # python2 compatibility for generating (html) docs
  #sed -i 's/sphinx-build/sphinx-build2/' cmake/OpenCVDetectPython.cmake
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  cmake ${_cmakeopts[@]} .

  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install license file
  install -Dm644 "${srcdir}/${_realname}-${pkgver}/doc/license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
