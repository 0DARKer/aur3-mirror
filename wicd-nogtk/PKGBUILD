# Maintainer: cantabile <cantabile dot desu at gmail dot com> 
# Contributor (from extra/wicd): Rashif "Don Ray" Rahman <rayrashif@gmail.com>
# based on extra/wicd

_appname=wicd
_suffix=-nogtk
pkgname=${_appname}${_suffix}
pkgver=1.7.0
pkgrel=4
pkgdesc="New and alternative wireless/wired network management utility - no gtk gui."
arch=('any')
url="http://wicd.sourceforge.net/"
license=('GPL2')
depends=('python2' 'dbus-python' 'dhcpcd' 'wpa_supplicant' 'wireless_tools' 'ethtool' 'python-urwid' 'consolekit' 'pygobject')
optdepends=('python-wpactrl:	needed if you want to use the new experimental ioctrl backend'
            'python-iwscan:	needed if you want to use the new experimental ioctrl backend')
conflicts=('wicd' 'wicd-svn')
provides=('wicd') # or part of it anyway
install=${_appname}.install
source=(http://downloads.sourceforge.net/sourceforge/${_appname}/${_appname}-${pkgver}.tar.gz
        wicd-daemon
        wicd-scripts-execution.patch
        deepcopy+python27-fixes.patch)
options=('emptydirs')
backup=('etc/wicd/encryption/templates/active')
md5sums=('003d2e67240989db55934553437ba32a'
         'f40e5f59998d0829707a7c9976afa8f8'
         'f4c377a25aa077cb76955124adfcc03f'
         '1b7ec95efcb8dc0fe48111da19b395b6')

build() {
  cd $srcdir/$_appname-$pkgver
  
  patch -p0 <$srcdir/wicd-scripts-execution.patch
  patch -p1 -i $srcdir/deepcopy+python27-fixes.patch
  find . -type f -exec sed -i 's@#!/usr.*python@#!/usr/bin/python2@' {} \;
  export PYTHON=python2
  python2 setup.py configure --no-install-init \
                            --resume=/usr/share/wicd/scripts/ \
                            --suspend=/usr/share/wicd/scripts/ \
                            --verbose \
                            --python=/usr/bin/python2 \
                            --no-install-gtk \
                            --no-use-notifications
}

package() {
  cd ${srcdir}/${_appname}-${pkgver}
  python2 setup.py install --root=$pkgdir

  # Add custom rc.d script
  install -Dm755 $srcdir/wicd-daemon $pkgdir/etc/rc.d/wicd

  cd build/lib/wicd
  for i in *.py; do
    install -Dm 755 $i ${pkgdir}/usr/lib/wicd/$i
  done
  
  rm -rf ${pkgdir}/usr/share/autostart
}

