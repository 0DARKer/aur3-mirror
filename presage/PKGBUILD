# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=presage
pkgver=0.8.6
pkgrel=1
pkgdesc="An intelligent predictive text entry platform."
arch=(i686 x86_64)
url="http://presage.sourceforge.net/"
license=('GPL')
depends=('gtk2' 'tinyxml' 'python2')
makedepends=('cmu-sml-toolkit' 'swig' 'cppunit')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('ad51bffb31d65f14b6b537bafe4212b2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  export PYTHON=python2
  sed -i "s|\(^#!.*bin/python\).*|\12|;s|\(^#!.*env python\).*|\12|;" \
    `grep -Erl "(env python|bin/python)" .`

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-static=no \
              --disable-documentation
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
