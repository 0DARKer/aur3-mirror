# Maintainer (Original version): Max Roder <maxroder@web.de>
# Maintainer  (Spanish version): OmeGa <omg@vmail.me>

pkgname='tor-browser-es'
pkgver='2.2.39_5'
_realpkgver='2.2.39-5-dev'
_language='es-ES'
pkgrel='1'
pkgdesc='Navegación anónima utilizando Firefox y Tor.'
url='https://www.torproject.org/projects/torbrowser.html'
arch=('x86_64' 'i686')
license=('GPL')
depends=('java-runtime')
optdepends=('zenity: simple dialog boxes'
			'kdialog: KDE dialog boxes'
			'notify-send: Gnome dialog boxes')
install="${pkgname}.install"
source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz"
		"https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz.asc"
		"${pkgname}.desktop"
		"${pkgname}.png"
		"${pkgname}.sh")
if [[ "$CARCH" == 'x86_64' ]]; then
	sha256sums=('b22864603c7dcdcb6e1db49e6ed53d433ca8c691a9573d25518ca5b5f261ea45'
				'7b3d5c9196cc3af9b732a45e7edb2baa96382e420c1cd7a3e2f4c8c2b9a07791')
else
	sha256sums=('2114cf8f2db96630e89b41d80b0707e92310ef08e1f950288f00ad650e30975d'
				'575fe3504d5680d8e2771c4570a0e3c856f96acb806af75428d289fc560effa1')
fi
sha256sums+=('2217f011197329019ae3d282d95623e0230f8f7a3a604290744280530cf1698a'
			'17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			'7fc947b8ae1483c5abb4545f6e26b315ff1fb16eb7ae8837f7afb10a9277383b')
noextract=("tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz")

package() {
	cd "${srcdir}"

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.sh
	sed -i "s/REPL_VERSION/${_realpkgver}/g"	${pkgname}.sh
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.sh

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.desktop
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.desktop
	sed -i "s/REPL_COMMENT/${pkgdesc}/g"		${pkgname}.desktop

	install -Dm 644 ${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 ${pkgname}.png		${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 ${pkgname}.sh		${pkgdir}/usr/bin/${pkgname}

	install -Dm 644 tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz ${pkgdir}/opt/${pkgname}/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz
}

# vim:set ts=2 sw=2 et:

