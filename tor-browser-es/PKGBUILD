# Maintainer (Spanish version): OmeGa <omega at mailoo dot org>
# Maintainer (English version): Max Roder <maxroder@web.de>

pkgname=tor-browser-es
pkgver=2.3.25_11
_realpkgver=2.3.25-11-dev
_language=es-ES
pkgrel=1
pkgdesc="Navegación anónima utilizando Firefox y Tor"
arch=('i686' 'x86_64')
url="https://www.torproject.org/projects/torbrowser.html"
license=('GPL')
optdepends=('kdebase-kdialog: KDE dialog boxes'
            'libnotify: Gnome dialog boxes'
            'zenity: simple dialog boxes')
install=$pkgname.install
source=(https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz
        https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz.asc
        $pkgname.desktop
        $pkgname.png
        $pkgname.sh)
noextract=(tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz)

if [[ $CARCH == x86_64 ]]; then
  sha256sums=('31940853e66af3074813ce7f2c53ab973afee06f4179a7ce3f55b12a27f56710'
              '72137ef6aebe30fffec78d905a2490f86a6c5ae13468e713b191fc7a9fdaf12c')
else
  sha256sums=('90364261ab27205c5562e0018d12bf5dca6a694b58b053f3a7158d6614509524'
              '34de5f327fa52a9e24b08344cb2f7c5201fc778b128781ab2e5e1c88f500ebaa')
fi
sha256sums+=('e73b5788d195e359d7d3621db402998fb4bfca49caf5fc5814b7c2839f93b5f1'
             '17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
             '7fc947b8ae1483c5abb4545f6e26b315ff1fb16eb7ae8837f7afb10a9277383b')

package() {
  cd "$srcdir"

  sed -i "s/REPL_NAME/$pkgname/g"         $pkgname.sh
  sed -i "s/REPL_LANGUAGE/$_language/g"   $pkgname.sh
  sed -i "s/REPL_VERSION/$_realpkgver/g"  $pkgname.sh

  sed -i "s/REPL_NAME/$pkgname/g"       $pkgname.desktop
  sed -i "s/REPL_COMMENT/$pkgdesc/"     $pkgname.desktop
  sed -i "s/REPL_LANGUAGE/$_language/"  $pkgname.desktop

  install -Dm755 $pkgname.sh      "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.png     "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"

  install -Dm644 tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz \
    "$pkgdir/opt/$pkgname/tor-browser-gnu-linux-$CARCH-$_realpkgver-$_language.tar.gz"
}

# vim:set ts=2 sw=2 et:
