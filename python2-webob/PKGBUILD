# Maintainer: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
pkgname='python2-webob'
pkgver='1.2.3'
pkgrel='1'
pkgdesc="WebOb provides objects for HTTP requests and responses."
arch=('any')
url="http://webob.org/"
license=('MIT')
depends=('python2')
makedepends=('python2-distribute')
optdepends=('python-webob-docs: documentation')
changelog='Changelog'
source=("http://pypi.python.org/packages/source/W/WebOb/WebOb-${pkgver}.tar.gz")
md5sums=('11825b7074ba7043e157805e4e6e0f55')

build() {
  cd "$srcdir/WebOb-${pkgver}"
  python2 setup.py build
}

package() {
  cd "$srcdir/WebOb-${pkgver}"
  python2 setup.py install "--root=$pkgdir" --optimize=1
  install -D -m644 "docs/license.txt" \
                   "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

check() {
  cd "$srcdir/WebOb-${pkgver}"
  python2 setup.py test
}
 
