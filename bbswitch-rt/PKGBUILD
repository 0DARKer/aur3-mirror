# Maintainer: FadeMind <fademind@gmail.com>
# Contributor: Boyan Ding <stu_dby@126.com> - Maintainer of bbswitch-ck

pkgname=bbswitch-rt
pkgver=0.8
_basename=bbswitch
_extramodules=extramodules-3.18-rt # Don't forget to update bbswitch-rt.install
pkgrel=4
pkgdesc="Kernel module allowing to switch dedicated graphics card on Optimus laptops for linux-rt"
arch=('i686' 'x86_64')
url=("http://github.com/Bumblebee-Project/bbswitch")
license=('GPL')
depends=('linux-rt>=3.18.9_rt5' 'linux-rt<3.19')
makedepends=('linux-rt-headers>=3.18.9_rt5' 'linux-rt-headers<3.19')
provides=('bbswitch')
install=${pkgname}.install
source=("https://github.com/Bumblebee-Project/bbswitch/archive/v${pkgver}.tar.gz")
md5sums=('5b116b31ace3604ddf9d1fc1f4bc5807')

build() {
  cd ${srcdir}/${_basename}-${pkgver}

  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

  make KDIR=/lib/modules/${_kernver}/build
}

package() {
  cd ${srcdir}/${_basename}-${pkgver}
  install -Dm644 bbswitch.ko "${pkgdir}"/usr/lib/modules/${_extramodules}/bbswitch.ko
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/bbswitch.ko"
}
