# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
# Contributor: Lex Black <autumn-wind at web dot de>

_hkgname=gitit
pkgname=gitit
pkgver=0.10.0.1
pkgrel=1
pkgdesc="Wiki using happstack, git or darcs, and pandoc."
url="http://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('gmp' 'haskell-base64-bytestring' 'haskell-blaze-html' 'haskell-bytestring' 'haskell-configfile' 'haskell-cgi' 'haskell-containers' 'haskell-directory' 'haskell-feed' 'haskell-filepath' 'haskell-filestore' 'haskell-happstack-server' 'haskell-highlighting-kate' 'haskell-hslogger' 'haskell-hstringtemplate<0.7' 'haskell-http' 'haskell-json' 'haskell-mtl' 'haskell-network' 'haskell-old-locale' 'haskell-old-time'  'haskell-pandoc' 'haskell-pandoc-types' 'haskell-parsec' 'haskell-pretty' 'haskell-process' 'haskell-random' 'haskell-recaptcha' 'haskell-safe' 'haskell-sha' 'haskell-tagsoup' 'haskell-text' 'haskell-time' 'haskell-url' 'haskell-utf8-string' 'haskell-xhtml' 'haskell-xml' 'haskell-xss-sanitize' 'haskell-zlib')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('b1349ad2748329b76ab1ed2c16768981')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
       --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
}
