# Maintainer: Joe Davison <joe@warhaggis.com>

pkgname=residualvm-grim_mouse-git
pkgver=4573.095db0b
pkgrel=1
pkgdesc="A fork of ResidualVM which adds mouse support (point and click) to Grim Fandango"
arch=('i686' 'x86_64')
url="http://www.residualvm.org"
license=(GPL)
depends=('sdl' 'zlib' 'alsa-lib' 'glu' 'libmad' 'mesa' 'libmpeg2' 'freetype2')
makedepends=('git')
conflicts=('residualvm' 'residualvm-git')
provides=('residualvm')
source=(git://github.com/tobiaspfaff/grim_mouse)
install=grim_mouse.install
sha1sums=('SKIP')

pkgver() {
	cd grim_mouse
    echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    cd grim_mouse
    ./configure --prefix=/usr
    make
}

package() {
    cd grim_mouse
    make DESTDIR=${pkgdir} install
}
