# $Id$
# Maintainer: Chuan Ji <jichuan89@gmail.com>

pkgname=w3m-mouse
pkgname_=w3m
pkgver=0.5.3
pkgrel=3
pkgdesc='Text-based Web browser, as well as pager (mouse enabled)'
url='http://w3m.sourceforge.net/'
license=('custom')
arch=('i686' 'x86_64')
makedepends=('imlib2')
optdepends=('imlib2: for graphics support') 
depends=('openssl' 'gc' 'ncurses')
source=("http://downloads.sourceforge.net/sourceforge/w3m/${pkgname_}-${pkgver}.tar.gz"
        'https-segfault.patch')
sha1sums=('444b6c8cf7094ee95f8e9de96b37f814b9d83237'
          '7da91b40194f7b245fddbd2ab7b0d3628d06c1d4')

build() {
	cd "${srcdir}/${pkgname_}-${pkgver}"

	sed 's/file_handle/file_handle_rofl/g' -i istream.*
	patch -p1 -i ../https-segfault.patch

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-image=x11,fb \
		--with-imagelib=imlib2 \
		--with-termlib=ncurses \
		--disable-w3mmailer \

	make
}

package() {
	cd "${srcdir}/${pkgname_}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname_}"
	find doc/* | grep -v CVS | xargs -i install -m644 "{}" "${pkgdir}/usr/share/doc/${pkgname_}"
	ln -s ../../doc/"${pkgname_}"/README "${pkgdir}/usr/share/licenses/${pkgname}"
}
