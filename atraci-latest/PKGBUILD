# Maintainer: David Vasquez <davidjeremias82 AT gmail DOT com>

pkgname=atraci-latest
_shortname=Atraci
pkgver=0.7.0
pkgrel=1
pkgdesc="Free music streaming player"
arch=('i686' 'x86_64')
url="http://getatraci.net/"
license=('GPL3')
depends=('nodejs' 'libnotify' 'libudev0')
makedepends=('git' 'nodejs-grunt-cli' 'ruby-compass-alpha')
conflicts=('atraci-git' 'atraci-bin' 'atraci')
options=('!strip')
[ "$CARCH" = "i686" ]   && _platform=linux32
[ "$CARCH" = "x86_64" ] && _platform=linux64
_datadir=/usr/share

source=(https://github.com/${_shortname}/${_shortname}/archive/$pkgver.tar.gz)

md5sums=('168d94d2f47a768a4ddc14fe69fcd1ad')

prepare() {
  cd ${srcdir}/${_shortname}-$pkgver

sed -i 's|"version": "0.6.5"|"version": "0.7.0"|g' package.json
sed -i 's|download/0.6.5|download/0.7.0|g' package.json
sed -i "s|version: '0.10.1'|version: '0.11.5'|g" Gruntfile.coffee
sed -i 's|class="version">v0.6.5|class="version">v0.7.0|g' splash.html

  # Get dependencies
  npm cache clean
  npm config set registry http://registry.npmjs.org/
  npm install

}

build() {
  cd ${srcdir}/${_shortname}-$pkgver

  # Build
  grunt build

}

package() {
  cd "${srcdir}/${_shortname}-$pkgver/build/${_shortname}/${_platform}/"

  # Program
  msg2 "Installing program to ${_datadir}/${_shortname}"
  install -dm 755 ${pkgdir}${_datadir}/${_shortname}
  install -m644 ${srcdir}/${_shortname}-$pkgver/build/${_shortname}/${_platform}/* ${pkgdir}${_datadir}/${_shortname}
  chmod 755 ${pkgdir}${_datadir}/${_shortname}/${_shortname}

  # Link to program
  msg2 "Symlink /usr/bin/${_shortname} -> ${_datadir}/${_shortname}/${_shortname}"
  install -dm 755 ${pkgdir}/usr/bin
  ln -s ../..${_datadir}/${_shortname}/${_shortname} ${pkgdir}/usr/bin/${_shortname}


# menu-entry
install -dm 755 ${pkgdir}/usr/share/applications
cat > ${pkgdir}/usr/share/applications/${_shortname}.desktop << EOF
[Desktop Entry]
Name=Atraci
Comment=Free music streaming player
Exec=/usr/bin/Atraci
Icon=/usr/share/pixmaps/Atraci.png
Terminal=false
Type=Application
Categories=Player;Video;AudioVideo;Network;
StartupNotify=true
EOF


  # Icon
  install -Dm644 ${srcdir}/${_shortname}-$pkgver/images/icon.png ${pkgdir}/usr/share/pixmaps/${_shortname}.png

}
