# Maintainer: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

BUILD_SELINUX=false

pkgname=389-ds-base
pkgver=1.3.0.6
pkgrel=1
pkgdesc="389 Directory Server (base)"
arch=('i686' 'x86_64')
url="http://port389.org/"
license=('GPL')
depends=('db' 'cyrus-sasl' 'cyrus-sasl-gssapi' 'cyrus-sasl-gssapi' 'icu' 'lm_sensors' 'net-snmp' 'nspr' 'nss' 'openldap' 'openssl' 'pcre' 'perl-mozldap' 'perl-netaddr-ip' 'perl-socket' 'svrcore' 'tcp_wrappers' 'zlib')
if [ "x${BUILD_SELINUX}" = "xtrue" ]; then
  depends+=('selinux-usr-policycoreutils')
fi
backup=('etc/default/dirsrv'
        'etc/default/dirsrv.systemd'
        'etc/dirsrv/config/certmap.conf'
        'etc/dirsrv/config/ldap-agent.conf'
        'etc/dirsrv/config/slapd-collations.conf'
        'etc/dirsrv/config/template-initconfig')
options=('!libtool')
install=${pkgname}.install
source=("http://port389.org/sources/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('cd6746ff3b4e8bc0dd6c9c69226965e878a8f5a6e47647ef72b9b28727057fef8c67bc6c622810627c83c399b749fb7c821f76661426eae0c9921555c0506fbc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  SELINUX=""
  if [ "x${BUILD_SELINUX}" = "xtrue" ]; then
    SELINUX="--with-selinux"
  fi

  if [ "${CARCH}" = "x86_64" ]; then
    export USE_64=1
  fi

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-tmpfiles-d=/usr/lib/tmpfiles.d \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --with-systemdsystemconfdir=/etc/systemd/system \
    --with-systemdgroupname=${pkgname}.target \
    --enable-autobind \
    --with-openldap \
    ${SELINUX} \
    #--disable-static

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}/" install

  install -dm755 "${pkgdir}"/var/log/${pkgname}/ \
                 "${pkgdir}"/var/lib/${pkgname}/ \
                 "${pkgdir}"/var/lock/${pkgname}/ \
                 "${pkgdir}"/etc/systemd/system/${pkgname}.wants

  rm -rv "${pkgdir}/etc/rc.d/"

  find "${pkgdir}" -type f -name '*.a' -delete
}

# vim:set ts=2 sw=2 et:
