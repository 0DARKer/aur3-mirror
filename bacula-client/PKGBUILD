# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Arian <arian.sameni+bacula-client AT gmail DOT com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Matthew <pyther@pyther.net>

_basename=bacula
pkgname=${_basename}-client
pkgver=5.2.13
pkgrel=2
pkgdesc="A network backup tool for Linux, Unix, Mac and Windows. This is the client edition."
conflicts=("${_basename}")
depends=("openssl")
provides=("bacula-fd=${pkgver}") # Also "bconsole", but it may generate conflitcs.
arch=("i686" "x86_64")
url="http://www.${_basename}.org"
license=("GPL2")
backup=("etc/${_basename}/bconsole.conf"
		"etc/${_basename}/${_basename}-fd.conf")
source=("http://downloads.sourceforge.net/project/bacula/bacula/${pkgver}/bacula-${pkgver}.tar.gz"{,.sig}
        'bacula-fd.service')

build() {
	cd ${_basename}-${pkgver}/

	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--enable-client-only \
		--with-openssl \
		--sysconfdir=/etc/${_basename} \
		--with-scriptdir=/etc/${_basename}/scripts

	make
}

package() {
	cd ${_basename}-${pkgver}/

	make DESTDIR=${pkgdir} install

	install -D -m0644 ${srcdir}/bacula-fd.service ${pkgdir}/usr/lib/systemd/system/bacula-fd.service
}

sha256sums=('a4bed458bf001889bd06bf31671b5d9908055a1d1e8113fd750ae4d326607ad8'
            'SKIP'
            'd933abc4c85c79c3e82293ceaccd8ffbc840b692624a21bef2bd825551247599')
