# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jakob Gruber <jakob.gruber@gmail.com>
# Contributor: Marcelo Cavalcante / Kalib <kalib@archlinux-br.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Gimmeapill <gimmeapill@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
pkgname=planeshift
pkgver=0.5.6
pkgrel=1
pkgdesc="Open source and cross-platform 3D Fantasy MMORPG"
url="http://www.planeshift.it/"
arch=('x86_64' 'i686')
license=('GPL' 'custom:PlaneShift Content License')
depends=('libgl' 'libsm' 'libxmu' 'libxpm' 'openal')
makedepends=('aria2')
options=('emptydirs')
install="planeshift.install"
source=("planeshift_x86_64.meta4"
        "planeshift_i686.meta4"
        "planeshift.desktop"
        "planeshiftsetup.desktop"
        "license.txt"
        "scriptfix.patch")
md5sums=('84a638e46daa24d245cccb70095d0e63'
         'fbd8e8645addcb287adbb67f76fd48ab'
         '5be27f6e51c8edff591aa1fdc668b7e9'
         'e75381f677b5d2ae331d0e4e742ee21a'
         'dbac9cf5bfb63463bcb2b5ea09b00586'
         '9f9b8d4033fd1b2c7ef33c5e04207abb')

build() {
  cd "$srcdir"

  msg2 "Downloading PlaneShift $pkgver (may seed for up to a minute)..."
  msg2 "Be patient and don't panic!"
  aria2c -M planeshift_$CARCH.meta4 -V -c --allow-overwrite=true --seed-ratio=5.0 --seed-time=1
}

package() {
  cd "$srcdir"

  msg2 "Extracting the (large) file..."
  chmod +x PlaneShift-*.bin
  ./PlaneShift-*.bin \
    --mode unattended \
    --syswide yes \
    --prefix ${pkgdir}/opt

  msg2 "Setting permissions..."
  find ${pkgdir}/opt/PlaneShift -type f -exec chmod 660 {} +
  find ${pkgdir}/opt/PlaneShift -type d -exec chmod 770 {} +
  cd ${pkgdir}/opt/PlaneShift
  chmod 770 psclient.bin pslaunch.bin psclient pslaunch

  msg2 "Packaging scripts..."
  install -Dm755 psclient ${pkgdir}/usr/bin/${pkgname}
  install -Dm755 pslaunch ${pkgdir}/usr/bin/${pkgname}-setup

  msg2 "Cleaning up..."
  rm -f uninstall unscript.sh planeshift

  msg2 "Patching..."
  cd ${pkgdir}/usr/bin/
  patch -Np0 < ${srcdir}/scriptfix.patch

  msg2 "Packaging shortcuts..."
  mkdir -p ${pkgdir}/usr/share/applications
  install -Dm644 ${srcdir}/*.desktop ${pkgdir}/usr/share/applications/

  msg2 "Packaging license..."
  install -Dm644 ${srcdir}/license.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
