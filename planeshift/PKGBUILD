# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jakob Gruber <jakob.gruber@gmail.com>
# Contributor: Marcelo Cavalcante / Kalib <kalib@archlinux-br.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Gimmeapill <gimmeapill@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>

pkgname=planeshift
pkgver=0.5.9
pkgrel=1
pkgdesc="Open source and cross-platform 3D Fantasy MMORPG"
url="http://www.planeshift.it/"
arch=('x86_64' 'i686')
license=('GPL' 'custom:PlaneShift Content License')
depends=('libgl' 'libsm' 'libxmu' 'libxpm' 'openal')
makedepends=('aria2')
options=('emptydirs')
install=planeshift.install
source=("planeshift_x86_64.meta4"
        "planeshift_i686.meta4"
        "planeshift.desktop"
        "planeshiftsetup.desktop"
        "license.txt"
        "scriptfix.patch")
sha256sums=('8ff7f2c8da92149f0ed44ee1e0d2b29d0ecb0e943a758f6dfdc9ed25206d505d'
            'c05d569c153f12ac4988ac0d5d4a32cd42b74226f02c512bdd2cc450391b3558'
            '5e58cc1d5975015bfe5d91ef2e251f90164fffabcccef155d258baab8b3d2361'
            '51cb76cfa35109412bf12f67ca1d1fde99804226de0fab29ecd882f67e7c336f'
            '51ba4271c77c00efe087f2f456ae777853f85b97767cf085d2fb8f9b82c082fc'
            'e37585331598531d44a2381d8ac96289fd2e6d6ae70321035cf22654f76d2a0a')

build() {
  cd "$srcdir"

  df -h "$srcdir"
  msg2 "Downloading PlaneShift $pkgver (may seed for up to a minute)..."
  msg2 "Be patient and don't panic!"
  aria2c -M planeshift_$CARCH.meta4 -V -c --allow-overwrite=true --seed-ratio=5.0 --seed-time=1
}

package() {
  cd "$srcdir"

  msg2 "Extracting the (large) file..."
  chmod +x PlaneShift-*.run
  ./PlaneShift-*.run \
    --mode unattended \
    --syswide yes \
    --prefix "$pkgdir/opt"

  msg2 "Setting permissions..."
  find "$pkgdir/opt/PlaneShift" -type f -exec chmod 660 {} +
  find "$pkgdir/opt/PlaneShift" -type d -exec chmod 770 {} +
  cd "$pkgdir/opt/PlaneShift"
  chmod 770 psclient.bin pslaunch.bin psclient pslaunch

  msg2 "Packaging scripts..."
  install -Dm755 psclient "$pkgdir/usr/bin/$pkgname"
  install -Dm755 pslaunch "$pkgdir/usr/bin/$pkgname-setup"

  msg2 "Cleaning up..."
  rm -f uninstall unscript.sh planeshift

  msg2 "Patching..."
  cd "$pkgdir/usr/bin/"
  patch -Np0 < "$srcdir/scriptfix.patch"

  msg2 "Packaging shortcuts..."
  mkdir -p "$pkgdir/usr/share/applications"
  install -Dm644 "$srcdir"/*.desktop "$pkgdir/usr/share/applications/"

  msg2 "Packaging license..."
  install -Dm644 "$srcdir/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:


