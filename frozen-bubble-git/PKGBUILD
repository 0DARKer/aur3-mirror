# $Id: PKGBUILD 160465 2012-06-02 10:25:48Z bluewind $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Brandon Niemczyk <maccorin@cfl.rr.com>

pkgname=frozen-bubble-git
pkgver=20120924
pkgrel=1
pkgdesc="A game in which you throw colorful bubbles and build groups to destroy the bubbles"
arch=(i686 x86_64)
url="http://www.frozen-bubble.org"
license=(GPL2)
depends=(perl-sdl perl-ipc-system-simple perl-compress-bzip2 perl-file-which)
makedepends=(perl-file-slurp perl-locale-maketext-lexicon)
options=('!emptydirs')
provides=('frozen-bubble')
conflicts=$provides
source=(frozen-bubble.desktop)

_gitroot='git://github.com/kthakore/frozen-bubble.git'
_gitname='frozen-bubble'

build() {
  cd "$srcdir"
  if [ -d $_gitname ]; then
    msg "Updating git repository..."
    cd "$_gitname"
    git pull origin
  else
    msg "Cloning git repository..."
    git clone "$_gitroot" "$_gitname"
    cd "$_gitname"
  fi

  unset CFLAGS
  # install module in vendor directories
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd "$srcdir/$_gitname"
  perl Build test
}

package() {
  cd "$srcdir/$_gitname"
  perl Build install destdir="$pkgdir"

  install -D -m644 share/icons/frozen-bubble-icon-64x64.png "$pkgdir/usr/share/pixmaps/frozen-bubble.png"
  install -D -m644 "$srcdir/frozen-bubble.desktop" "$pkgdir/usr/share/applications/frozen-bubble.desktop"
}
md5sums=('01b10b77ba6ad856f5c199121c582272')
