# Maintainer: Luis Sarmiento < lgsarmientop-ala-unal.edu.co >
# Contributor: Sebastian Voecking <voeck@web.de>

pkgname=clhep
_pkgname=CLHEP
pkgver=2.1.2.3
pkgrel=1
pkgdesc='A Class library for High Energy Physics'
url="http://proj-clhep.web.cern.ch/"
arch=('x86_64' 'i686')
license=('GPL3')
depends=('bash' 'gcc-libs')
options=('!emptydirs')
makedepends=('cmake'         #for building the package
	     'texlive-core') #for the documentation
source=("http://proj-clhep.web.cern.ch/proj-clhep/DISTRIBUTION/tarFiles/${pkgname}-${pkgver}.tgz")
md5sums=('83737082b511b424bbd03de188f0a797')

build() {
	msg 'Creating building directory'
	[ -d ${srcdir}/build ] && rm -rf ${srcdir}/build
	mkdir ${srcdir}/build
	cd ${srcdir}/build

	msg 'Compiling the package'
  	cmake -DCLHEP_BUILD_DOCS=ON \
	      -DCMAKE_INSTALL_PREFIX=/usr \
	      ${srcdir}/${pkgver}/${_pkgname}
	make
}

package() {
	  msg 'Creating the package'
	  cd ${srcdir}/build
	  make DESTDIR="${pkgdir}" install
	  install -Dm644 ${srcdir}/${pkgver}/${_pkgname}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

	  msg 'Moving documentation to standard location'
	  [ -d ${pkgdir}/usr/share/doc/${pkgname} ] || install -d ${pkgdir}/usr/share/doc/${pkgname}
	  mv ${pkgdir}/usr/doc/* ${pkgdir}/usr/share/doc/${pkgname}/

#thanks to Bastian Beischer
	  msg 'Creating symlinks'
          cd ${pkgdir}/usr/lib
          ln -s libCLHEP-${pkgver}.a libCLHEP.a
          ln -s libCLHEP-${pkgver}.so libCLHEP.so
}
