# Maintainer: Luis Sarmiento < lgsarmientop@unal.edu.co >
# Contributor: Sebastian Voecking <voeck@web.de>

pkgname=clhep
_pkgname=CLHEP
pkgver=2.1.2.2
pkgrel=2
pkgdesc='A Class library for High Energy Physics'
url="http://proj-clhep.web.cern.ch/"
arch=('x86_64' 'i686')
license=('GPL3')
depends=('bash' 'gcc-libs')
optdepends=()
makedepends=('cmake'         #for building the package
	     'texlive-core') #for the documentation
conflicts=()
replaces=()
backup=()
install=''

source=("http://proj-clhep.web.cern.ch/proj-clhep/DISTRIBUTION/tarFiles/${pkgname}-${pkgver}.tgz")
md5sums=('b72115137d9871f1b80aed9da01918b0')

build() {
	[ -d ${srcdir}/build ] && rm -rf ${srcdir}/build
	mkdir ${srcdir}/build
	cd ${srcdir}/build
	
  	cmake -DCLHEP_BUILD_DOCS=ON \
	      -DCMAKE_INSTALL_PREFIX=/usr \
	      ${srcdir}/${pkgver}/${_pkgname}

	make
}

package() {
#install
	  cd ${srcdir}/build
	  make DESTDIR="${pkgdir}" install
	  install -Dm644 ${srcdir}/${pkgver}/${_pkgname}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

#move documentation to standard location
	  [ -d ${pkgdir}/usr/share/doc/${pkgname} ] || install -d ${pkgdir}/usr/share/doc/${pkgname}
	  mv ${pkgdir}/usr/doc/* ${pkgdir}/usr/share/doc/${pkgname}/
	  rmdir ${pkgdir}/usr/doc

#thanks to Bastian Beischer
          cd ${pkgdir}/usr/lib
          ln -s libCLHEP-${pkgver}.a libCLHEP.a
          ln -s libCLHEP-${pkgver}.so libCLHEP.so
}