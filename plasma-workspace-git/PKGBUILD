# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=plasma-workspace-git
pkgver=r6.9a17af5
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('kwin-git' 'libksysguard-git' 'krunner-git' 'kjsembed-git' 'knotifyconfig-git' 'kdesu-git' 'kidletime-git' 'qimageblitz-svn') # 'kf5-akonadi' 'kdepimlibs-frameworks-git' - uncomment when kdepimlibs is ported to KF5
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git' 'kf5umbrella-git') # 'boost'
conflicts=('plasma-workspace' 'kde-workspace<0')
provides=('plasma-workspace')
source=('git://anongit.kde.org/plasma-workspace.git')
md5sums=('SKIP')

pkgver() {
  cd plasma-workspace
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../plasma-workspace/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
