# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgname=plasma-workspace-git
pkgver=r1726.cf2737c
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('qt5-tools' 'kded-git' 'libksysguard-git' 'kjsembed-git' 'knotifyconfig-git' 'ktexteditor-git' 'kde-cli-tools-git' 'khelpcenter-git' 
'kio-extras-git' 'baloo-git' 'milou-git' 'libkscreen-frameworks-git' 'breeze-git' 'kwin-git' 'gpsd' 'libqalculate') # prison-git
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
optdepends=('plasma-workspace-wallpapers: additional wallpapers')
conflicts=('plasma-workspace')
provides=('plasma-workspace')
options=()
source=('git://anongit.kde.org/plasma-workspace.git'
        '10-kf5')
md5sums=('SKIP'
         '162ab0d2d984930321f07c19d3e5cbc9')

pkgver() {
  cd plasma-workspace
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-workspace \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed 's:qtpaths:qtpaths-qt5:' -i "$pkgdir"/opt/kf5/bin/startkde
#  sed 's:/opt/kf5/lib/libexec/kf5/start_kdeinit_wrapper:/usr/lib/libexec/kf5/start_kdeinit_wrapper:' -i "$pkgdir"/opt/kf5/bin/startkde
  sed 's:install_share="/opt/kf5/share":install_share="/usr/share":' -i "$pkgdir"/opt/kf5/bin/startkde

# move session file and SDDM theme to /usr
  install -dm755 "$pkgdir"/usr/share/
  mv "$pkgdir"/opt/kf5/share/xsessions "$pkgdir"/usr/share/
  mv "$pkgdir"/opt/kf5/share/sddm "$pkgdir"/usr/share/

# keep KDM support for now
  install -dm755 "$pkgdir"/usr/share/apps/kdm/sessions/
  ln -sr "$pkgdir/usr/share/xsessions/plasma.desktop" -t "$pkgdir/usr/share/apps/kdm/sessions/"
  
# set env variables at startup
  install -Dm755 "$srcdir/10-kf5" "$pkgdir/etc/X11/xinit/xinitrc.d/10-kf5"
}
