# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgname=plasma-workspace-git
pkgver=r935.fdca63b
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('kidletime-git' 'kded-git' 'kwin-git' 'libksysguard-git' 'krunner-git' 'kjsembed-git' 'knotifyconfig-git' 
'ktexteditor-git' 'kde-cli-tools-git' 'khelpcenter-git' 'kio-extras-git' 'milou-frameworks-git' 'breeze-git' 
'qimageblitz-svn' 'gpsd' 'libqalculate' 'oxygen-icons') # kdepimlibs-frameworks-git
makedepends=('extra-cmake-modules-git' 'git' 'kdoctools-git' 'boost')
conflicts=('plasma-workspace' 'kde-workspace<0')
provides=('plasma-workspace')
options=()
source=('git://anongit.kde.org/plasma-workspace.git'
        'kf5-plasma.desktop'
        '10-kf5')
md5sums=('SKIP'
         '9e8f350ff3aee562848b104b129a6669'
         'd2a7af9c0601ff11525d7b9d0e7f3bed')

pkgver() {
  cd plasma-workspace
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../plasma-workspace \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed s/qtpaths/qtpaths-qt5/g -i "$pkgdir/opt/kf5/bin/startkde"

  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  install -dm755 "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/kf5-plasma.desktop" "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -sr "${pkgdir}/usr/share/apps/kdm/sessions/kf5-plasma.desktop" -t "${pkgdir}/usr/share/xsessions/"
  
  install -Dm755 "${srcdir}/10-kf5" "${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5"
}
