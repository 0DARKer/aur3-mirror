# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgname=plasma-workspace-git
pkgver=r727.96677a9
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('LGPL')
depends=('kidletime-git' 'kded-git' 'kwin-git' 'libksysguard-git' 'krunner-git' 'kjsembed-git' 'knotifyconfig-git' 'kdesu-git' 'ktexteditor-git' 'qimageblitz-svn' 'gpsd') # kdepimlibs-frameworks-git
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git' 'kf5umbrella-git' 'boost')
conflicts=('plasma-workspace' 'kde-workspace<0')
provides=('plasma-workspace')
source=('git://anongit.kde.org/plasma-workspace.git'
        'kf5-plasma.desktop'
        '10-kf5')
md5sums=('SKIP'
         '28548acea99d29f37ee3d18184a661f4'
         'ac17752e928b8d31b3cf780ac7c39317')

pkgver() {
  cd plasma-workspace
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  export XDG_DATA_DIRS="/opt/kf5/share:$XDG_DATA_DIRS"

  cd build
  cmake ../plasma-workspace/${DIR} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed s/qtpaths/qtpaths-qt5/g -i "$pkgdir/opt/kf5/bin/startkde"

  install -dm755 "${pkgdir}/usr/share/apps/kdm/sessions/"
  install -dm755 "${pkgdir}/usr/share/xsessions/"
  install -m644 "${srcdir}/kf5-plasma.desktop" "${pkgdir}/usr/share/apps/kdm/sessions/"
  ln -sr "${pkgdir}/usr/share/apps/kdm/sessions/kf5-plasma.desktop" -t "${pkgdir}/usr/share/xsessions/"
  
  install -Dm755 "${srcdir}/10-kf5" "${pkgdir}/etc/X11/xinit/xinitrc.d/10-kf5"
}
