# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>

pkgname=libretro-4do-git
pkgver=7.4462e4b
pkgrel=1
pkgdesc="libretro implementation of 4DO. (3DO)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/4do-libretro"
license=('unknown')
makedepends=('git')

_gitname=4do-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/4do_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}"
  make
}

package() {
  install -Dm644 "${_gitname}/4do_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-4do.so"
  install -Dm644 "4do_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-4do.info"
}
