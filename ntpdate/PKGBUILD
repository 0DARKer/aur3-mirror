# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
pkgname=ntpdate
pkgver=4.2.6p3
pkgrel=1
pkgdesc='Client for the network time protocol'
arch=('i686' 'x86_64')
makedepends=('perl-html-parser')
conflicts=('ntp' 'ntpdate-dev')
license=('custom')
url='http://www.ntp.org/'
backup=('etc/conf.d/ntpdate')
source=("http://archive.ntp.org/ntp4/ntp-4.2/ntp-${pkgver}.tar.gz"
        'ntpdate.conf'
        'ntpdate.rc')
options=('!emptydirs')
md5sums=('59876a9009b098ff59767ee45a88ebd2'
         'c7f50632b69bd4f32cf052d0b1848463'
         '76ac18cf6f2d8a34d499143931d68ec4')

build() {
  # configure ntp
  cd "ntp-${pkgver}"
  ac_cv_header_dns_sd_h=0 ./configure --prefix=/usr --enable-ignore-dns-errors

  # build ntpdate
  cd "${pkgname}"
  make

  # gen ntpdate man page
  cd "${srcdir}/ntp-${pkgver}/html"
  ../scripts/html2man
}

package() {
  cd "ntp-${pkgver}/${pkgname}"
  make DESTDIR="${pkgdir}" install

  # install man page
  install -D -m644 "${srcdir}/ntp-${pkgver}/html/man/man8/ntpdate.8" "${pkgdir}/usr/share/man/man8/ntpdate.8"
  
  # install rc.d scripts 
  install -D -m755 "${srcdir}/ntpdate.rc" "${pkgdir}/etc/rc.d/${pkgname}"

  # install sample configs
  install -D -m644 "${srcdir}/ntpdate.conf" "${pkgdir}/etc/conf.d/${pkgname}"

  # install copyright
  install -D -m644 "${srcdir}/ntp-${pkgver}/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
}

# vim:set ts=2 sw=2 ft=sh et:
