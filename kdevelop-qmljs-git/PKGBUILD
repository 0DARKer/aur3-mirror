# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-qmljs-git
pkgver=95.ad6a3dc
pkgrel=1
pkgdesc="KDevelop Plugin for QML/JS Language Support"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop-git')
makedepends=('cmake' 'automoc4' 'git')
provides=('kdevelop-qmljs')
conflicts=('kdevelop-qmljs')
source=('git://anongit.kde.org/kdev-qmljs')
md5sums=('SKIP')

pkgver() {
    cd kdev-qmljs
    echo $(git rev-list --count master).$(git rev-parse --short master)
}

build() {
    mkdir -p build
    cd build

    cmake ../kdev-qmljs \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix)
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
