# Maintainer:  Andrzej Giniewicz <gginiu@gmail.com>

pkgname=wacom-mainline-linux-git
pkgver=20120106
pkgrel=1
pkgdesc="Kernel drivers for Wacom tablets, mainline branch"
arch=('i686' 'x86_64')
url="http://linuxwacom.sourceforge.net/"
license=('GPL')
depends=('linux')
makedepends=('linux-headers')
options=('!libtool')
conflicts=('linuxwacom-cvs' 'input-wacom' 'input-wacom-cvs' 'linuxwacom-baboo-cth-ctl'
           'wacom-next-linux-git' 'wacom-linux-git')
install=wacom-mainline-linux-git.install

_gitrepo="linux/kernel/git/torvalds"
_gitname="linux.git"
_gitroot="git://git.kernel.org/pub/scm/$_gitrepo/$_gitname"

function _get_source() {
  _dir=$1
  _file=$2
  msg "Downloading $_dir/$_file"
  wget -q "http://git.kernel.org/?p=$_gitrepo/$_gitname;a=blob_plain;f=drivers/input/$_dir/$_file;hb=HEAD" -O $_file
}

build() {
  cd "$srcdir"
  _get_source "tablet" "wacom.h"
  _get_source "tablet" "wacom_wac.h"
  _get_source "tablet" "wacom_wac.c"
  _get_source "tablet" "wacom_sys.c"
  _get_source "touchscreen" "wacom_w8001.c"

  echo "wacom-objs := wacom_sys.o wacom_wac.o" > Makefile
  echo "obj-m += wacom.o" >> Makefile
  echo "obj-m += wacom_w8001.o" >> Makefile
  make -C /lib/modules/$(uname -r)/build SUBDIRS="$(pwd)" modules
}

package() {
  install -D -m 644 wacom.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom.ko
  install -D -m 644 wacom_w8001.ko "${pkgdir}"/lib/modules/$(uname -r)/updates/wacom_w8001.ko
}

