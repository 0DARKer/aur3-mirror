#! /bin/bash
# Maintainer: Jorge Barroso <jorge.barroso.11 at gmail dot com>
# Contributor: Anton Shestakov <engored*ya.ru>
# Thanks to heftig for his pacman 4.1 compatible PKGBUILD:
#
pkgname=desmume-svn
pkgver=4627
pkgrel=2
pkgdesc='Nintendo DS emulator, svn version'
url='http://desmume.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('agg' 'gtkglext' 'libglade' 'libpcap' 'mesa' 'openal' 'sdl'
         'soundtouch')
makedepends=('autoconf' 'automake' 'intltool' 'pkg-config'
             'subversion')
conflicts=('desmume' 'desmume-cvs')
provides=('desmume')
source=(svn+https://desmume.svn.sourceforge.net/svnroot/desmume/trunk/desmume)
md5sums=('SKIP')

pkgver() {
    cd "$SRCDEST/desmume"
    svnversion | tr -d [A-z]
}
build() {
 cd desmume
 ./autogen.sh
    ./configure \
    --prefix=/usr \
    --enable-wifi \
    --enable-openal \
    --enable-osmesa \
    --enable-hud \
    --enable-glade
    make
}
package() {
 cd desmume
    make DESTDIR="$pkgdir" install
}
