# Contributor: Anton Shestakov <engored*ya.ru>
# Maintainer: Anton Shestakov <engored*ya.ru>

pkgname=desmume-svn
pkgver=4121
pkgrel=1
pkgdesc='Nintendo DS emulator, svn version'
url='http://desmume.org/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('agg' 'gtkglext' 'libglade' 'libpcap' 'mesa' 'openal' 'sdl')
makedepends=('autoconf' 'automake' 'intltool' 'pkg-config' 'subversion')
conflicts=('desmume' 'desmume-cvs')
provides=('desmume')

_svntrunk='https://desmume.svn.sourceforge.net/svnroot/desmume/trunk/desmume'
_svnmod='desmume'

build() {
  cd "$srcdir"

  if [ -d "${_svnmod}/.svn" ]; then
    (cd "$_svnmod" && svn up -r $pkgver)
  else
    svn co "$_svntrunk" --config-dir ./ -r $pkgver $_svnmod
  fi

  msg 'SVN checkout done or server timeout'
  msg 'Starting make...'

  rm -rf "${_svnmod}-build"
  cp -r "$_svnmod" "${_svnmod}-build"
  cd "${_svnmod}-build"

  # missing stdlib.h 
  sed -ie "1 i\
#include <stdlib.h>" src/cheatSystem.h

  ./autogen.sh

  # --enable-hud        Enable HUD rendering, requires libagg
  # --enable-openal     enable experimental OpenAL microphone input
  # --enable-wifi       enable experimental wifi comm support (needs libpcap)
  ./configure --enable-hud --enable-openal --enable-wifi --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${_svnmod}-build"
  make DESTDIR="${pkgdir}" install
}
