# Maintainer: AlexanderR <rvacheva at nxt dot ru>
# Contributor: zhn <zhangn1985 AT gmail com>
pkgname=python-renpy
pkgver=6.16.3
pkgrel=2
pkgdesc="Platform-dependant Ren'Py libraries."
arch=('i686' 'x86_64')
license=('MIT')
url='http://www.renpy.org'
depends=('fribidi' 'glew' 'ffmpeg' 'python2-pygame>=1.9.1' 'freetype2>=2.5.1')
conflicts=("renpy<$pkgver" 'renpy64' 'renpy-bin')
# python-argparse is part of distribution since 2.7
makedepends=('python2>=2.7')
changelog=ChangeLog
source=("http://www.renpy.org/dl/$pkgver/renpy-${pkgver}-source.tar.bz2")

md5sums=('385fd5d2e8540f705d21cbc6d60ecb93')

export CFLAGS="$CFLAGS -Os -I/usr/include/freetype2"

prepare()
{
  sed -i 's|<freetype/|<freetype2/|' $srcdir/renpy-${pkgver}-source/module/ttgsubtable.h
}

build() {
  cd "$srcdir"/renpy-${pkgver}-source

  python2 module/setup.py build
}

package(){
  cd "$srcdir"/renpy-${pkgver}-source

  # install python-renpy
  python2 module/setup.py install --root="$pkgdir/" --prefix=/usr --optimize=1
  install -D -m644 'LICENSE.txt' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
