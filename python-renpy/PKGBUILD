# Maintainer: AlexanderR <rvacheva at nxt dot ru>
# Contributor: zhn <zhangn1985 AT gmail com>

pkgname=python-renpy
pkgver=6.13.8
pkgrel=1
pkgdesc="Ren'Py is a visual novel engine that helps you use words, images, and sounds to tell stories with the computer."
arch=("i686" "x86_64")
license=('MIT')
url="http://www.renpy.org"
# are python-pygame and ttf-dejavu required by renpy or games itself?
depends=('ffmpeg' 'fribidi' 'python2>=2.7' 'glew' 'python-pygame' 'ttf-dejavu')
# python-argparse is part of distribution since 2.7
makedepends=('python2>=2.7')
conflicts=('renpy' 'renpy64')
replaces=('renpy')
provides=("renpy")
source=("http://www.renpy.org/dl/$pkgver/renpy-$pkgver-source.tar.bz2"
	python2.diff)

md5sums=('882a76112608d3e734258100e07fade3'
         '19b861f4d934bfea817c161bf3ea1142')

build() {
  cd "$srcdir/renpy-$pkgver"

  # fonts are provided by ttf-dejavu
  rm launcher/DejaVuSerif.ttf common/DejaVuSans.ttf common/DejaVuSans.txt

  patch -p1 < ../python2.diff
  
  cd module
  ./setup.py build
}

package(){
  cd "$srcdir/renpy-$pkgver"

  # install python-renpy
  module/setup.py install --root="$pkgdir/" --prefix=/usr --optimize=1

  # install renpy base files
  install -dm755 "$pkgdir/usr/share/renpy"
  cp -r launcher renpy common "$pkgdir/usr/share/renpy"

  # install license
  install -D -m644 'LICENSE.txt' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
