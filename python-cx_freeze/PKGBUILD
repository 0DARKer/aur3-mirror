# Maintainer : SpepS <dreamspepser at yahoo dot it>

_pkg=cx_freeze
_tar=${_pkg/f/F}
pkgname=python-$_pkg
pkgver=4.2.2
pkgrel=1
pkgdesc="A set of utilities for freezing Python3 scripts into executables."
arch=('i686' 'x86_64')
url="http://www.python.net/crew/atuining/$_tar/"
license=('custom')
depends=('python')
source=("http://downloads.sourceforge.net/cx-freeze/$_tar-$pkgver.tar.gz")
md5sums=('656ab0ace90b21e0040f27236539bef0')

build() {
  cd "$srcdir/$_tar-$pkgver"

  # python 3.2 py_compile fix
  sed -i "/read_encoding/d;s/, encoding = encoding//" cx_Freeze/finder.py
}

package() {
  cd "$srcdir/$_tar-$pkgver"

  python setup.py install --root "$pkgdir/" --optimize 1

  # install custom license
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # prevent conflicts with cx_freeze
  cd "$pkgdir/usr/bin" && mv cxfreeze cxfreeze3
}
