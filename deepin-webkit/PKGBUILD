# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=deepin-webkit
pkgver=git20131010153235~9ac8cf628b
_realver=1.8.2.1+$pkgver
pkgrel=1
pkgdesc="A fork of webkit-gtk for deepin-desktop-eviroment"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=(LGPL-2+ BSD)
depends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk3'
	 'gstreamer0.10-base-plugins' 'libsecret' 'libwebp' 'harfbuzz-icu')
makedepends=('gtk2' 'gperf' 'gobject-introspection' 'python2' 'mesa' 'gtk-doc' 'gawk'
	     'chrpath' 'bison' 'perl' 'flex' 'which' 'gettext')
optdepends=('gtk2: Netscape plugin support')
provides=("${pkgname%-*}")
options=(!libtool !emptydirs)
source=("http://packages.linuxdeepin.com/deepin/pool/main/d/${pkgname}/${pkgname}_${_realver}.tar.gz"
	"webkit-gtk-1.8.2-bison-2.6.patch"
	"DerivedSources.tar.gz")
sha256sums=('782c4e8c22fbcc2e20338f24aa9f3ffd8929638269edefc107449eadf967c964'
	    '2a9c0c1b9d5259fd1254ebea228fc83313267fbe8cfec1bb43356de1f355b93e'
	    '7a5fbc735dcda90faeafb26778a741ebe1a5fcf0cfb44de5369db42aa60561df')

build() {
  cd ${pkgname}-${_realver}
  
  export CFLAGS="$CFLAGS -Wall -Wl,--as-needed"
  export LDFLAGS="$LDFLAGS -Wl,--no-keep-memory"
    
  patch -p1 -i $srcdir/webkit-gtk-1.8.2-bison-2.6.patch

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
   --disable-silent-rules \
   --with-gtk=3.0 \
   --disable-webgl
  make
}

package() {
  cd ${pkgname}-${_realver}
  
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,include,share}
  mv $pkgdir/usr/lib/pkgconfig/webkitgtk-3.0.pc $pkgdir/usr/lib/pkgconfig/deepin-webkit-3.0.pc
  install -Dm644 Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
