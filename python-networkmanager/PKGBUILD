# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgbase=python-networkmanager
pkgname=('python-networkmanager' 'python2-networkmanager')
pkgver=0.9.12
pkgrel=1
pkgdesc="Easy communication with NetworkManager"
arch=('any')
url="https://github.com/seveas/python-networkmanager"
license=('LGPL3')
makedepends=('python-dbus' 'python2-dbus')
source=("https://github.com/seveas/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('7bfb64d02ed6963f1f89db109e5b67788f42ebea8dfeb0d3ba3eeef5a50a7f4c89396235dfa78310faa842fb21701b0044dfa89514f9310a926a0dd14832eb8e')


package_python-networkmanager() {
  pkgdesc+=" (Python3.x)"
  depends=('python-dbus')

  cd ${srcdir}/${pkgbase}-${pkgver}
  python setup.py install --root="$pkgdir/" --prefix=/usr
}

package_python2-networkmanager() {
  pkgdesc+=" (Python2.x)"
  depends=('python2-dbus')

  cd ${srcdir}/${pkgbase}-${pkgver}
  python2 setup.py install --root="$pkgdir/" --prefix=/usr
  mv $pkgdir/usr/bin/n-m $pkgdir/usr/bin/n-m2
}
