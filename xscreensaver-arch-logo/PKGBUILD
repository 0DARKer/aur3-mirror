# Maintainer: graysky <graysky AT archlinux DOT us>
# Contributor: Thomas Mudrunka <harvie@@email..cz>
# Contributer: Eric Belanger <eric@archlinux.org>

pkgname=xscreensaver-arch-logo
_original_pkgname=xscreensaver
pkgver=5.21
pkgrel=2
pkgdesc="Arch Linux branded screen saver and locker for the X Window System."
arch=('i686' 'x86_64')
url="http://www.jwz.org/xscreensaver/"
license=('BSD')
depends=('libxxf86vm' 'libglade' 'glu' 'xorg-appres' 'libxmu' \
         'perl-libwww' 'perl-http-message')
makedepends=('bc' 'libxpm' 'gdm' 'mesa')
optdepends=('gdm: for login manager support')
conflicts=('xscreensaver')
provides=('xscreensaver')
backup=('etc/pam.d/xscreensaver')
source=(http://www.jwz.org/xscreensaver/${_original_pkgname}-${pkgver}.tar.gz
add-electricsheep.diff
xscreensaver.pam
LICENSE
logo-50.xpm logo-180.xpm logo-50.png logo-180.png)
sha256sums=('90f9a4574880c6e6d24796b5c386405a4bbf27a7d764a00f858d43ce07788868'
            'fa6927429e2eb610fa3a483c87b4bbba82886e922feb900b9d465995cbb23241'
            '0a985e3b699578de3e22b25ee72abe4e24ca8730e4afb37dfeed367823bdb0d5'
            '0d2a17d9645402edf51182609a06f9be3232bba0802397387e64c125ebb8bde0'
            '82366926a2a81bd08459327936ba144e8b3ea5ee4a386c268bd898e1791ab1a0'
            '253f0d5bbdd841f21a7bbdbb0fd7ded21f711751d5cb1b7914952bdd6541b36d'
            '8027bdb2b4328d154a8e517bdb94f5ef4a9eb031e79a285dabedd62acfa77317'
            '8357f9e631b80ae373cb0fc8e27fa96fd032c3d5e3869bd04dde843b79260b19')

build() {
	cd "${_original_pkgname}-${pkgver}"
	install -Dm644 "$srcdir"/logo-180.xpm  "${srcdir}"/${_original_pkgname}-${pkgver}/utils/images/logo-180.xpm
	patch -p0 -i "${srcdir}/add-electricsheep.diff"
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--libexecdir=/usr/lib --with-x-app-defaults=/usr/share/X11/app-defaults \
		--with-pam --with-login-manager --with-gtk --with-gl \
		--without-gle --with-pixbuf --with-jpeg
	make ${MAKEFLAGS}
}

package() {
	cd "${_original_pkgname}-${pkgver}"
	make install_prefix="${pkgdir}" install
	install -D -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${_original_pkgname}/LICENSE"
	install -D -m644 ../xscreensaver.pam "${pkgdir}/etc/pam.d/xscreensaver"
	chmod 755 "${pkgdir}/usr/bin/xscreensaver"
	echo "NotShowIn=KDE;GNOME;" >> "${pkgdir}/usr/share/applications/xscreensaver-properties.desktop"
}
