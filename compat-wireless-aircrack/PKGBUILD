# Maintainer: Max Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=compat-wireless-aircrack
pkgver=2.6.35_1
pkgrel=2
pkgdesc='Compat-wireless modules with aircrack patches (for all supported cards)'
arch=('i686' 'x86_64')
url='http://wireless.kernel.org/'
license=('GPL')
depends=('kernel26')
makedepends=('linux-api-headers' 'kernel26-headers' 'lsb-release')
conflicts=('compat-wireless' 'compat-wireless-all' 'compat-wireless-git-old' 'compat-wireless-patched'
           'compat-wireless-patched-daily')
install=depmod.install
source=("http://orbit-lab.org/kernel/${pkgname/-aircrack}-2.6-stable/v${pkgver%_*}/compat-wireless-${pkgver/_/-}.tar.bz2"
	"http://patches.aircrack-ng.org/mac80211_2.6.32.2-wl_frag+ack_radiotap.patch"
	"http://patches.aircrack-ng.org/channel-negative-one-maxim.patch")
sha1sums=('95a44314284e68ea8902b42bd7a41e0b613efe64'
          '2af535ba85f6d7d5f51601d18e45171d8bb3c9f4'
          'a611acdd7994b07b0b39417ef7a5a6ffc866a733')

build() {
 cd "$srcdir/${pkgname/-aircrack}-${pkgver/_/-}"
 
 patch -p1 -i "$srcdir/mac80211_2.6.32.2-wl_frag+ack_radiotap.patch"
 patch -p1 -i "$srcdir/channel-negative-one-maxim.patch"
 
 make
}

package() {
 cd "$srcdir/${pkgname/-aircrack}-${pkgver/_/-}"
 
 make INSTALL_MOD_PATH="$pkgdir" install-modules
}
