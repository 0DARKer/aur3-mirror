# Maintainer: Neer Sighted (neersighted) <neersighted@myopera.com>

pkgname=subversive-takedown
pkgver=b39
pkgrel=3
pkgdesc='A top-down, tactical voxel shooter, inspired by games like SND Online, Voxatron/Ace of Spades and Wolfenstein: Enemy Territory.'
url='http://mp2.dk/st/'
license=('custom:closed source')
case "${CARCH}" in
  (i686)
    source=('http://mp2.dk/st/builds/linux/st-b39-linux-i686.tar.gz')
    md5sums=('64b41b7e388810ff40b041f5a2446dad')
    sha1sums=('9f270f0bbb44a2d03c46c0635a5966dca8a81bfc')
    sha256sums=('b307b320b8d5234d9aa3d9afc8c7bd50b2355816c04dc4d6f57be378b435bf80')
  ;;
  (x86_64)
    source=('http://mp2.dk/st/builds/linux/st-b39-linux-x86_64.tar.gz')
    md5sums=('3bb8472efd904abf5be481f0d85aa538')
    sha1sums=('356017e6f83f549e6fd0d19d8a2d70cf3aa0ed0a')
    sha256sums=('28d962c98976f48935ce89345c1f1d629546aefbeb4dc0bae31641dae5f2acc2')
  ;;
  (*)
    echo 'Unsupported architecture!'
    exit 1
  ;;
esac
arch=('i686' 'x86_64')
depends=('enet' 'libvorbis' 'openal' 'python2' 'zenity')
backup=('opt/subversive-takedown/config.txt')

prepare() {
  cd "${srcdir}/Subversive Takedown"

  # Patch Python scripts to use Python 2.
  for pythonscript in $(find -name '*.py'); do
    sed -i "s/python/python2/" "${pythonscript}"
  done
}

package() {
  cd "${srcdir}/Subversive Takedown"

  # Prepare the directory structure.
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/etc/subversive-takedown"
  install -dm755 "${pkgdir}/opt"

  # Copy the directory tree over.
  cp -r . "${pkgdir}/opt/subversive-takedown"

  # Install the program.
  ln -s /opt/subversive-takedown/run_client.sh \
        "${pkgdir}/usr/bin/subversive-takedown"
  ln -s /opt/subversive-takedown/run_server.sh \
        "${pkgdir}/usr/bin/subversive-takedown-server"
  # Install the configs.
  ln -s /opt/subversive-takedown/config.txt \
        "${pkgdir}/etc/subversive-takedown/config.txt"
  ln -s /opt/subversive-takedown/config.txt.pacnew \
        "${pkgdir}/etc/subversive-takedown/config.txt.pacnew"

  # Remove useless bundled libraries.
  rm -rf "${pkgdir}/opt/subversive-takedown/lib"
}

# vim:set ts=2 sw=2 et:
