# Maintainer: Neer Sighted (neersighted) <neersighted@myopera.com>

pkgname=subversive-takedown
pkgver=41
pkgrel=1
pkgdesc='A top-down, tactical voxel shooter, inspired by games like SND Online, Voxatron/Ace of Spades and Wolfenstein: Enemy Territory.'
url='http://mp2.dk/st/'
license=('custom:closed source')
case "${CARCH}" in
  (i686)
    source=('http://mp2.dk/st/builds/linux-i686/st-07612501882b2f1249fc2601074cb70da07fffe4-linux-i686.tar.gz')
    md5sums=('759a28336313c0fa571cdee03cfec80d')
    sha1sums=('2c1e79d7b8233264ac04829b645c47e3a5ae9811')
    sha256sums=('66f58884c3c0f767e3b51d8396b39b94444a08177c57f00b9c99450f421470d8')
  ;;
  (x86_64)
    source=('http://mp2.dk/st/builds/linux-x86_64/st-07612501882b2f1249fc2601074cb70da07fffe4-linux-x86_64.tar.gz')
    md5sums=('bc5522ef261931fb17fc641ab6b12630')
    sha1sums=('91f17b0b07196e4bf6cb71a584c002616dfb6821')
    sha256sums=('bc9f7b56676d0ae23bd7d63b135e42bb5ca1f8ab60c3b37754dec9d3c321c15f')
  ;;
  (*)
    echo 'Unsupported architecture!'
    exit 1
  ;;
esac
arch=('i686' 'x86_64')
depends=('enet' 'libvorbis' 'openal' 'python2' 'zenity')
backup=('opt/subversive-takedown/config.txt')

prepare() {
  cd "${srcdir}/Subversive Takedown"

  # Patch Python scripts to use Python 2.
  for pythonscript in $(find -name '*.py'); do
    sed -i "s/python/python2/" "${pythonscript}"
  done
}

package() {
  cd "${srcdir}/Subversive Takedown"

  # Prepare the directory structure.
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/etc/subversive-takedown"
  install -dm755 "${pkgdir}/opt"

  # Copy the directory tree over.
  cp -r . "${pkgdir}/opt/subversive-takedown"

  # Install the program.
  ln -s /opt/subversive-takedown/run_client.sh \
        "${pkgdir}/usr/bin/subversive-takedown"
  ln -s /opt/subversive-takedown/run_server.sh \
        "${pkgdir}/usr/bin/subversive-takedown-server"
  # Install the configs.
  ln -s /opt/subversive-takedown/config.txt \
        "${pkgdir}/etc/subversive-takedown/config.txt"
  ln -s /opt/subversive-takedown/config.txt.pacnew \
        "${pkgdir}/etc/subversive-takedown/config.txt.pacnew"

  # Remove useless bundled libraries.
  rm -rf "${pkgdir}/opt/subversive-takedown/lib"
}

# vim:set ts=2 sw=2 et:
