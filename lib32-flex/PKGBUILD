_pkgbasename=flex
pkgname=lib32-$_pkgbasename
pkgver=2.5.35
pkgrel=4
pkgdesc="A tool for generating text-scanning programs (32 bit, libraries only)"
arch=('x86_64')
url="http://flex.sourceforge.net"
license=('custom')
groups=('base-devel')
depends=('lib32-glibc' 'm4' 'sh')
makedepends=('gcc-multilib')
source=(http://downloads.sourceforge.net/sourceforge/flex/flex-$pkgver.tar.bz2 
        flex-2.5.35-gcc44.patch
        flex-2.5.35-hardening.patch
        flex-2.5.35-missing-prototypes.patch
        flex-2.5.35-sign.patch)
md5sums=('10714e50cea54dc7a227e3eddcd44d57'
         'e4444ef5c07db71a43280be74139bdea'
         'de952b3ed7cc074bc8c3e6ab73634048'
         '6b83f56b1b654c6a321cdc530a3ec68d'
         'd87fd9e9762ba7e230d516bdcf1c8c6f')

build() {
  cd $srcdir/$_pkgbasename-$pkgver
  
  patch -Np1 -i $srcdir/flex-2.5.35-gcc44.patch
  patch -Np1 -i $srcdir/flex-2.5.35-hardening.patch
  patch -Np1 -i $srcdir/flex-2.5.35-missing-prototypes.patch
  patch -Np1 -i $srcdir/flex-2.5.35-sign.patch
  
  ./configure --prefix=/usr --libdir=/usr/lib32 \
  		--mandir=/usr/share/man --infodir=/usr/share/info \
  		CC='gcc -m32' CXX='g++ -m32'
  make
}

package() {
  cd $srcdir/$_pkgbasename-$pkgver
  make DESTDIR=$pkgdir install
  rm -rf ${pkgdir}/usr/{bin,include}
  rm -rf ${pkgdir}/usr/share/{info,locale,man}
}

