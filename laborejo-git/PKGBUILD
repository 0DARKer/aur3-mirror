# Maintainer: speps <speps at aur dot archlinux dot org>

_name=laborejo
pkgname=$_name-git
pkgver=0.7_gitc1c1f78
pkgrel=1
pkgdesc="Music Notation Workshop"
arch=(any)
url="http://www.$_name.org/"
license=('GPL')
depends=('pyqt')
optdepends=('lilypond: lilypond support'
            'python-pysmf: MIDI export'
            'python-pyliblo: session support'
            'calfbox: playback'
            'jack: JACK output')
makedepends=('git')
provides=("$_name")
conflicts=("$_name")
source=("$_name::git+https://github.com/nilsgey/Laborejo.git"
        "$_name.sh")
md5sums=('SKIP' '48017430ff111cc2f89fda26507c62cf')

pkgver() {
  cd "$srcdir/$_name"
  echo `sed -n '/Version/s/=*\(Version \)*//gp' README`_git`git describe --always | sed 's|-|.|g'`
}

package() {
  cd "$srcdir/$_name"

  # data
  install -d "$pkgdir/usr/share/$_name"
  cp -a * "$pkgdir/usr/share/$_name"

  # exec
  install -Dm755 ../$_name.sh "$pkgdir/usr/bin/$_name"
}

# vim:set ts=2 sw=2 et:
