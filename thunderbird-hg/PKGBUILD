# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Want a PGO (Profile-Guided Optimization) build?: http://en.wikipedia.org/wiki/Profile-guided_optimization
_pgo="0"  # "1" to enable

pkgname=thunderbird-hg
pkgver=9294
pkgrel=1
pkgdesc="Standalone Mail/News reader - Mercurial version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss' 'shared-mime-info' 'sqlite3' 'startup-notification')
makedepends=('autoconf2.13' 'libgnomeui' 'libnotify' 'mercurial' 'mesa' 'python2' 'unzip' 'yasm' 'zip')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
install=$pkgname.install
source=(mozconfig
        $pkgname.desktop)
md5sums=('ca98c2bf1017b33e19dae22fdcef2e73'
         '19ea9f30321b65fa8de67f0cf7456be3')

_hgroot="http://hg.mozilla.org/"
_hgrepo="comm-central"

build() {
  cd comm-central
  python2 client.py checkout

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/opt/$pkgname"
  export PYTHON="/usr/bin/python2"

  if [[ "$_pgo" = "1" ]]; then
     LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
     LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
     kill $! || true
  else
     make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  fi
}

package() {
  cd comm-miramar/obj-*
  make -j1 package MOZ_MAKE_FLAGS="$MAKEFLAGS"

  cd mozilla/dist/
  tar -x -f thunderbird-*.tar.bz2
  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r thunderbird/ "$pkgdir/opt/$pkgname/"
  ln -sf /opt/$pkgname/thunderbird "$pkgdir/usr/bin/$pkgname"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 "$srcdir/comm-miramar/other-licenses/branding/thunderbird/mailicon${i/x*/}.png" "$pkgdir/usr/share/icons/hicolor/$i/apps/$pkgname.png"
  done

  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  rm -rf "$pkgdir/opt/$pkgname/dictionaries"
  ln -sf /usr/share/hunspell "$pkgdir/opt/$pkgname/dictionaries"

  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "$pkgdir/opt/$pkgname/defaults/pref/channel-prefs.js"
}