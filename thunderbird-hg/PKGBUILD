# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Want a PGO (Profile-Guided Optimization) build?: http://en.wikipedia.org/wiki/Profile-guided_optimization
_pgo="0"  # "1" to enable

pkgname=thunderbird-hg
pkgver=9335
pkgrel=1
pkgdesc="Standalone Mail/News reader - Mercurial version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/thunderbird"
depends=('alsa-lib' 'cairo' 'dbus-glib' 'desktop-file-utils' 'fontconfig' 'freetype2' 'gtk2' 'hicolor-icon-theme' 'hunspell' 'libevent' 'libjpeg' 'libmng' 'libpng' 'libvpx' 'libxt' 'mozilla-common' 'nspr' 'nss' 'shared-mime-info' 'sqlite3' 'startup-notification')
makedepends=('autoconf2.13' 'libgnomeui' 'libnotify' 'mercurial' 'mesa' 'python2' 'unzip' 'yasm' 'zip')
optdepends=('libcanberra: for sound support')
provides=("thunderbird=$pkgver")
install=$pkgname.install
source=(mozconfig
        $pkgname.desktop
        vendor.js
        libvpx.patch)
md5sums=('42108124b96f9496eece6fe15cf48de0'
         '19ea9f30321b65fa8de67f0cf7456be3'
         '5a53179d14ae9631b7afe5e4d0fc0b25'
         '5b2aaff7dfe7f1f94ad965905b20e782')

__hgroot="http://hg.mozilla.org/comm-central"
__hgrepo="comm-central"

build() {
  [[ ! -d $__hgrepo ]] && hg clone $__hgroot

  cd $__hgrepo
  python2 client.py checkout

  patch -Np1 -i ../libvpx.patch || true

  export LDFLAGS="$LDFLAGS -Wl,-rpath,/opt/$pkgname-r$pkgver"
  export PYTHON="/usr/bin/python2"

  if [[ "$_pgo" = "1" ]]; then
     LD_PRELOAD="" /usr/bin/Xvfb -nolisten tcp -extension GLX -screen 0 1280x1024x24 :99 &
     LD_PRELOAD="" DISPLAY=:99 make -j1 -f client.mk profiledbuild MOZ_MAKE_FLAGS="$MAKEFLAGS"
     kill $! || true
  else
     make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  fi
}

package() {
  cd $__hgrepo/obj-*
  make -j1 package MOZ_MAKE_FLAGS="$MAKEFLAGS"

  cd mozilla/dist/
  tar -x -f thunderbird-*.tar.bz2
  mkdir -p "$pkgdir"/{opt,usr/bin}
  cp -r thunderbird/ "$pkgdir/opt/$pkgname-r$pkgver/"
  ln -sf /opt/$pkgname-r$pkgver/thunderbird "$pkgdir/usr/bin/$pkgname"

  cp "$srcdir"/vendor.js "$pkgdir/opt/$pkgname-r$pkgver/defaults/pref/"

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 "$srcdir/comm-miramar/other-licenses/branding/thunderbird/mailicon${i/x*/}.png" "$pkgdir/usr/share/icons/hicolor/$i/apps/$pkgname.png"
  done

  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  rm -rf "$pkgdir/opt/$pkgname-r$pkgver/dictionaries"
  ln -sf /usr/share/hunspell "$pkgdir/opt/$pkgname-r$pkgver/dictionaries"

  # A fix for the (redundant) "Package contains reference to $srcdir" warning
  sed -i 1d "$pkgdir/opt/$pkgname-r$pkgver/defaults/pref/channel-prefs.js"
}