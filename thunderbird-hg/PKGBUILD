# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Here you choose whether you want a PGO (Profile-Guided Optimization) build
_pgo=0  # Change to "1" to do so

pkgname=thunderbird-hg
pkgver=3.3a3pre
pkgrel=1
pkgdesc="Standalone Mail/News reader - Mercurial version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'gcc-libs' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell' 'sqlite3>=3.7.4' 'fontconfig' 'freetype2')
makedepends=('zip' 'libgnomeui' 'python2' 'libidl2' 'wireless_tools' 'yasm' 'libnotify<0.7' 'mercurial' 'autoconf2.13')
optdepends=('libcanberra: for sound support'
	    'yasm: for WebM support')
provides=("thunderbird=${pkgver}")
source=(mozconfig
	run-thunderbird.sh
        thunderbird-hg.desktop
        thunderbird-3.0-lang.patch
        thunderbird-appversion.patch
        thunderbird-preferences.patch)
md5sums=('e605d5ec8e92ff03720c7f38858c5245'
         'ca53085b81a5534af8fb380b3228f06c'
         'ccfd6ab3dcd3d10c6cd403728234df71'
         '25b6fe16ac24cd5c852213e5c1adb272'
         '48ffcdb877a69d383b7d354e330f7658'
         '8a9737a545f18e75b0da7a80f63a407a')

_hgroot="http://hg.mozilla.org/comm-central/"
_hgname="comm-central"

build() {
  msg "Connecting to Mercurial server..."
  [[ ! -d ${_hgname} ]] && hg clone http://hg.mozilla.org/comm-central/
  python2 comm-central/client.py checkout
  msg "Mercurial checkout done or server timeout"

  rm -rf ${_hgname}-build
  cp -r ${_hgname} ${_hgname}-build
  cd ${_hgname}-build

  patch -Np1 -i ../thunderbird-3.0-lang.patch
  patch -Np1 -i ../thunderbird-appversion.patch
  patch -Np1 -i ../thunderbird-preferences.patch

  cp ../mozconfig .mozconfig
  export LDFLAGS="-Wl,-rpath,/opt/thunderbird-hg"

  if [[ "${_pgo}" = "1" ]]; then
     echo -e "mk_add_options PROFILE_GEN_SCRIPT=../run-thunderbird.sh" >> .mozconfig
     [[ "$CARCH" = "i686" ]] && export CFLAGS="-march=i686 -mtune=generic"
     [[ "$CARCH" = "x86_64" ]] && export CFLAGS="-march=x86-64 -mtune=generic"
     export CXXFLAGS="${CFLAGS}"
     make -f client.mk profiledbuild MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  else
     unset CXXFLAGS
     unset CFLAGS
     make -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  fi
}

package() {
  install -m755 -d "${pkgdir}"{/opt,/usr/{share/{applications,pixmaps},bin}}
  cp -r comm-central-build/obj-*/mozilla/dist/bin/ "${pkgdir}"/opt/thunderbird-hg/

  ln -sf /opt/thunderbird-hg/thunderbird "${pkgdir}"/usr/bin/thunderbird-hg

  install -m644 comm-central/mail/branding/unofficial/mailicon48.png "${pkgdir}"/usr/share/pixmaps/thunderbird-hg.png

  install -m644 thunderbird-hg.desktop "${pkgdir}"/usr/share/applications/
}