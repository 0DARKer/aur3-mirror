# Maintainer: Det <nimetonmaili at gmail a-dot com>
# Based on [extra]'s thunderbird

# NOTE: Here you choose whether you want a PGO (Profile-Guided Optimization) build
_pgo="0"  # Change to "1" to do so - every other number/mark disables PGO

pkgname=thunderbird-hg
pkgver=3.3a4pre
pkgrel=1
pkgdesc="Standalone Mail/News reader - Mercurial version with optional PGO"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2' 'gcc-libs' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell' 'sqlite3>=3.7.4' 'fontconfig' 'freetype2')
makedepends=('zip' 'libgnomeui' 'python2' 'libidl2' 'wireless_tools' 'yasm' 'autoconf2.13' 'mercurial' 'libnotify')
optdepends=('libcanberra: for sound support'
	    'yasm: for WebM support')
provides=("thunderbird=${pkgver}")
source=(mozconfig
        thunderbird-hg.desktop
        thunderbird-3.0-lang.patch
        thunderbird-appversion.patch
        thunderbird-preferences.patch)
md5sums=('3dccde15e97b7ed27b37f70342e0c2d0'
         '5d8a34e30958f3bf47ad49a3038f059f'
         '25b6fe16ac24cd5c852213e5c1adb272'
         '48ffcdb877a69d383b7d354e330f7658'
         '8a9737a545f18e75b0da7a80f63a407a')

_hgroot="http://hg.mozilla.org/comm-central/"
_hgname="comm-central"

build() {
  msg "Connecting to the Mercurial server..."
  [[ ! -d ${_hgname} ]] && hg clone http://hg.mozilla.org/comm-central/
  msg "Updating the tree..."
  cd comm-central && rm -rf obj-* && python2 client.py checkout
  msg "Mercurial checkout done or server timeout"
  patch -Np1 -i ../thunderbird-3.0-lang.patch
  patch -Np1 -i ../thunderbird-appversion.patch
  patch -Np1 -i ../thunderbird-preferences.patch

  cp ../mozconfig .mozconfig
  export LDFLAGS="-Wl,-rpath,/opt/thunderbird-${pkgver}"
  unset CXXFLAGS CFLAGS

  if [[ "${_pgo}" = "1" ]]; then
     [[ "$CARCH" = "i686" ]] && export CFLAGS="-march=i686 -mtune=generic"
     [[ "$CARCH" = "x86_64" ]] && export CFLAGS="-march=x86-64 -mtune=generic"
     export CXXFLAGS="${CFLAGS}"
     make -f client.mk profiledbuild MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  else
     make -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}"
  fi
}

package() {
  cd comm-central/obj-*
  make package

  cd mozilla/dist/
  bsdtar -x -f thunderbird-3.1.en-US.linux-${CARCH}.tar.bz2
  install -m755 -d "${pkgdir}"{/opt,/usr/{share/{applications,pixmaps},bin}}
  cp -r thunderbird/ "${pkgdir}"/opt/thunderbird-${pkgver}/

  ln -sf /opt/thunderbird-${pkgver}/thunderbird "${pkgdir}"/usr/bin/thunderbird-hg
  install -m644 "${srcdir}"/comm-central/mail/branding/unofficial/mailicon48.png "${pkgdir}"/usr/share/pixmaps/thunderbird-hg.png
  install -m644 "${srcdir}"/thunderbird-hg.desktop "${pkgdir}"/usr/share/applications/
}