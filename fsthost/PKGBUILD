# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=fsthost
pkgver=1.1
pkgrel=1
pkgdesc="A linux VST host using winelib, fork of FreeST."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/fsthost/"
license=('GPL')
depends=('libxml2' 'lash' 'gtk2')
[[ "$CARCH" = 'x86_64' ]] &&
depends=(${depends[@]/#/lib32-})
depends+=('wine')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('f8cb6151fdc1579a4883b5ed6f256b1a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # bin
  install -Dm755 fsthost.exe \
    "$pkgdir/usr/bin/fsthost"
  ln -s /usr/lib/libfsthost.so \
    "$pkgdir/usr/bin/fsthost.exe.so"

  # lib
  install -Dm755 fsthost.exe.so \
    "$pkgdir/usr/lib/libfsthost.so"

  # headers
  install -Dm644 fst.h \
    "$pkgdir/usr/include/fsthost.h"
  install -Dm644 jackvst.h \
    "$pkgdir/usr/include/"
  install -Dm644 vestige/aeffectx.h \
    "$pkgdir/usr/include/vestige/aeffectx.h"
}

# vim:set ts=2 sw=2 et:
