# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=fsthost
pkgver=1.5.0
pkgrel=1
pkgdesc="A linux VST host using winelib, fork of FreeST."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/fsthost/"
license=('GPL')
depends=('libxml2' 'lash' 'gtk2')
[[ "$CARCH" = 'x86_64' ]] && _arch=32 && \
depends=(${depends[@]/#/lib32-})
depends+=('wine')
optdepends=('zenity: for fsthost_menu')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('bcfac427a7f3e0132df07bc0f38922f0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # use -fPIC flag for 32bit too
  # in order to avoid text relocations
  # +
  # fix x86_64 lib path
  [[ "$CARCH" = 'x86_64' ]] && \
  sed -e "s/no-pic -fno-//" \
      -e "/fsthost64/s/LIB32/LIB64/" \
      -i Makefile
      
  make LIB32_INST_PATH=/usr/lib$_arch \
       LIB64_INST_PATH=/usr/lib
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make LIB32_INST_PATH=/usr/lib$_arch \
       LIB64_INST_PATH=/usr/lib \
       DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et: