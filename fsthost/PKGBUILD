# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=fsthost
pkgver=1.2
pkgrel=1
pkgdesc="A linux VST host using winelib, fork of FreeST."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/fsthost/"
license=('GPL')
depends=('libxml2' 'lash' 'gtk2')
[[ "$CARCH" = 'x86_64' ]] &&
depends=(${depends[@]/#/lib32-})
depends+=('wine')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.xz"
        "$pkgname-makefile.patch")
md5sums=('9bf111bf959bda745c8c3e712ba50601'
         'e934d6de65a0f0db987c302ec702b297')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # x86_64 lib path suffix
  [[ $CARCH = x86_64 ]] && export _lib=32

  # makefile patch
  patch -p1 -i ../$pkgname-makefile.patch

  make LIB_INST_PATH=/usr/lib$_lib
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make LIB_INST_PATH=/usr/lib$_lib \
       BIN_INST_PATH=/usr/bin \
       DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
