# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=fsthost
pkgver=1.0
pkgrel=1
pkgdesc="A linux VST host using winelib, fork of FreeST."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/fsthost/"
license=('GPL')
depends=('libxml2' 'lash' 'gtk2')
[[ "$CARCH" = 'x86_64' ]] &&
depends=(${depends[@]/#/lib32-})
depends+=('wine')
provides=('fst')
conflicts=('fst')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('39a6e87ab616660fca9e634015444a30')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # bin
  install -d "$pkgdir/usr/bin"
  install -Dm755 fst.exe \
    "$pkgdir/usr/bin/fst"
  ln -s /usr/lib/libfst.so \
    "$pkgdir/usr/bin/fst.exe.so"

  # lib
  install -Dm755 fst.exe.so \
    "$pkgdir/usr/lib/libfst.so"

  # header
  install -d "$pkgdir/usr/include/vestige"
  install -Dm644 *.h \
    "$pkgdir/usr/include"
  install -Dm644 vestige/* \
    "$pkgdir/usr/include/vestige"
}

# vim:set ts=2 sw=2 et:
