# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=fsthost
pkgver=1.5.2
pkgrel=1
pkgdesc="A linux VST host using winelib, fork of FreeST."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/fsthost/"
license=('GPL')
depends=('alsa-lib' 'lash' 'libxml2' 'gtk2')
[[ "$CARCH" = 'x86_64' ]] && _arch=32 && \
depends=(${depends[@]/#/lib32-})
depends+=('wine')
optdepends=('zenity: for fsthost_menu')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('a7d49776ab2b24a4c55797fc0f2b9361')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # use -fPIC flag for 32bit too
  # in order to avoid text relocations
  [[ "$CARCH" = 'x86_64' ]] && \
  sed "s/no-pic -fno-//" -i Makefile
      
  make LIB32_INST_PATH=/usr/lib$_arch \
       LIB64_INST_PATH=/usr/lib
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make LIB32_INST_PATH=/usr/lib$_arch \
       LIB64_INST_PATH=/usr/lib \
       DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
