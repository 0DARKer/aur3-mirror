# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=fsthost
pkgver=1.3
pkgrel=1
pkgdesc="A linux VST host using winelib, fork of FreeST."
arch=(i686 x86_64)
url="http://sourceforge.net/projects/fsthost/"
license=('GPL')
depends=('libxml2' 'lash' 'gtk2')
[[ "$CARCH" = 'x86_64' ]] &&
depends=(${depends[@]/#/lib32-})
depends+=('wine')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('3af8d0ea3e03d7152ec0c2500d0e4223')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # x86_64 lib path suffix
  [[ $CARCH = x86_64 ]] && export _lib=32

  make LIB_INST_PATH=/usr/lib$_lib
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make LIB_INST_PATH=/usr/lib$_lib \
       DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
