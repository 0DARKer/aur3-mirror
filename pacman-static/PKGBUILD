# This is gross, but can totally save you if you need to fix a machine
# on which you can't easily boot install media.

pkgname=pacman-static
pkgver=2013
pkgrel=2
pkgdesc="Statically-compiled pacman (for fixing systems with corrupt libc)"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=()
optdepends=('gnupg: verify package integrity')
makedepends=('devtools')
source=(genpacmanstatic)
sha256sums=('95aee083ec07acdd4d9596941bee33570a80dd688f4e44cb95c1a95ea60ecc16')

build() {
   local arg j=
   for arg in $MAKEFLAGS; do
       case $arg in
	   -j?*)
	       j="$arg"
	       ;;
       esac
   done

   cd "$srcdir"
   echo "* Root privileges are required to build in a chroot environment"
   echo + sudo ./genpacmanstatic $j pacman-static
   sudo ./genpacmanstatic $j pacman-static
}

package() {
  install -sD "$srcdir"/pacman-static "$pkgdir"/usr/bin/pacman-static
}
