# This is gross, but can totally save you if you need to fix a machine
# on which you can't easily boot install media.

pkgname=pacman-static
pkgver=4.0.3
pkgrel=2
acl_ver=2.2.51
assuan_ver=2.0.3
gpgerr_ver=1.11
gpgme_ver=1.3.1
pkgdesc="Statically-compiled pacman (for fixing systems with corrupt libc)"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('glibc>=2.15' 'libarchive>=3.0.2' 'curl>=7.19.4'
		       'pth' 'gnupg>=2')
source=(
  ftp://ftp.archlinux.org/other/pacman/pacman-$pkgver.tar.gz{,.sig}
  http://download.savannah.gnu.org/releases/acl/acl-$acl_ver.src.tar.gz{,.sig}
  ftp://ftp.gnupg.org/gcrypt/libgpg-error/libgpg-error-${gpgerr_ver}.tar.bz2
  ftp://ftp.gnupg.org/gcrypt/libassuan/libassuan-$assuan_ver.tar.bz2
  ftp://ftp.gnupg.org/gcrypt/gpgme/gpgme-${gpgme_ver}.tar.bz2{,.sig}
)
md5sums=('387965c7125e60e5f0b9ff3b427fe0f9'
         '1a70392526c8768470da678b31905a6e'
         '3fc0ce99dc5253bdcce4c9cd437bc267'
         'e0eb18cb5a7371750b5bd1ca3c501dd2'
         'b9fa55b71cae73cb2e44254c2acc4e2c'
         '179d1918325fdb928c7bd90b8a514fc7'
         '90afa8436ce2b2683c001c824bd22601'
         'a032ddc27177ef4ee54e25a231e62da5')


build() {
   cd $srcdir/acl-$acl_ver
   ./configure --prefix=/usr --libdir=/usr/lib --libexecdir=/usr/lib
   make
   make install-dev DIST_ROOT="$srcdir/overlay"

   cd $srcdir/libassuan-$assuan_ver
   ./configure --prefix=/usr --enable-static --disable-shared
   make install DESTDIR="$srcdir/overlay"

   cd $srcdir/libgpg-error-$gpgerr_ver
   ./configure --prefix=/usr --enable-static --disable-shared
   make install DESTDIR="$srcdir/overlay"

   cd $srcdir/gpgme-$gpgme_ver
   ./configure --prefix=/usr --disable-shared
   make install DESTDIR="$srcdir/overlay"
   
   cd $srcdir/pacman-$pkgver
   LDFLAGS="-static -L$srcdir/overlay/usr/lib" \
     LIBS="-lz -ldl -lgpg-error -lassuan" \
     ./configure --disable-shared --disable-doc \
       --with-libcurl="/usr" --with-gpgme \
       --prefix=/usr --sysconfdir=/etc --localstatedir=/var
   make AM_LDFLAGS=-all-static
}

package() {
  install -sD $srcdir/pacman-$pkgver/src/pacman/pacman \
  	  $pkgdir/usr/bin/pacman-static
}
