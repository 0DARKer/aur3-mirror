# This is gross, but can totally save you if you need to fix a machine
# on which you can't easily boot install media.

pkgname=pacman-static
pkgver=4.1.0
pkgrel=2
assuan_ver=2.1.0
gpgerr_ver=1.11
gpgme_ver=1.4.0
pkgdesc="Statically-compiled pacman (for fixing systems with corrupt libc)"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('glibc>=2.17-2' 'acl>=2.2.51-3' 'libarchive>=3.1.2' 'curl>=7.19.4'
		       'pth' 'gnupg>=2')
source=(
  ftp://ftp.archlinux.org/other/pacman/pacman-$pkgver.tar.gz{,.sig}
  ftp://ftp.gnupg.org/gcrypt/libgpg-error/libgpg-error-${gpgerr_ver}.tar.bz2
  ftp://ftp.gnupg.org/gcrypt/libassuan/libassuan-$assuan_ver.tar.bz2
  ftp://ftp.gnupg.org/gcrypt/gpgme/gpgme-${gpgme_ver}.tar.bz2{,.sig}
)
md5sums=('a0f2b3148bee4784f21cf373cf59a0bc'
         'SKIP'
         'b9fa55b71cae73cb2e44254c2acc4e2c'
         'b3231eec8e567f4f9294474a387378f5'
         'a0f93aba6d8a831ba14905085027f2f9'
         'SKIP')


build() {
   cd $srcdir/libassuan-$assuan_ver
   ./configure --prefix=/usr --enable-static --disable-shared
   make install DESTDIR="$srcdir/overlay"

   cd $srcdir/libgpg-error-$gpgerr_ver
   ./configure --prefix=/usr --enable-static --disable-shared
   make install DESTDIR="$srcdir/overlay"

   cd $srcdir/gpgme-$gpgme_ver
   ./configure --prefix=/usr --disable-shared
   make install DESTDIR="$srcdir/overlay"
   
   cd $srcdir/pacman-$pkgver
   LDFLAGS="-static -L$srcdir/overlay/usr/lib" \
     LIBS="-lz -lgpg-error -lassuan -lssl -lcrypto -lssh2 -ldl \
     	       -lstdc++ -lpthread" \
     ./configure --disable-shared --disable-doc \
       --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
       --with-scriptlet-shell=/usr/bin/bash \
       --with-ldconfig=/usr/bin/ldconfig

   make AM_LDFLAGS=-all-static
}

package() {
  install -sD $srcdir/pacman-$pkgver/src/pacman/pacman \
  	  $pkgdir/usr/bin/pacman-static
}
