# Contributor: Rick W. Chen <stuffcorpse@archlinux.us>
pkgname=phonon-vlc-git
pkgver=20130418_v0.5.0_129_g5749d5b
pkgrel=1
pkgdesc="Phonon VLC backend, git version"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kdesupport/phonon/phonon-vlc"
license=('LGPL')
depends=('phonon' 'vlc' 'qt4')
makedepends=('git' 'automoc4' 'cmake' 'phonon')
provides=('phonon-vlc' 'phonon-backend')
conflicts=('phonon-vlc')
options=('!strip')
source=("git://anongit.kde.org/phonon-vlc")
md5sums=('SKIP')

_gitname="phonon-vlc"

pkgver() {
    cd "$srcdir/$_gitname"

    echo "$(LANG=C date '+%Y%m%d')_$(git describe --tags --always | sed 's/-/_/g')"
}

build() {

  cd "$srcdir/$_gitname"

  cmake -DCMAKE_INSTALL_PREFIX=$(kde4-config --prefix) \
        -DCMAKE_BUILD_TYPE=debugfull \
        -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
        .
  make

  rm -rf "$srcdir/$_gitname-build"
} 

package () {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
}

