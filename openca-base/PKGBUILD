# Maintainer: M0Rf30
pkgname=openca-base
pkgver=1.1.1
pkgrel=2
pkgdesc="The OpenCA PKI software provides a complete solution for managing your PKI"
arch=('i686' 'x86_64')
url="http://www.openca.org/projects/openca/"
license=('GPL2')
depends=('openssl' 'openca-tools' 'perl-dbi' 'perl-bit-vector' 'perl-authen-sasl' 'perl-cgi-session' 'perl-convert-asn1' 
'perl-dbd-pg' 'perl-dbd-mysql' 'perl-digest-hmac' 'perl-digest-md5' 'perl-digest-sha1' 'perl-file-temp' 'perl-io-socket-ssl' 'perl-io-stringy'
'perl-mime-base64' 'perl-mime-lite' 'perl-mime-tools' 'perl-mailtools' 'perl-net-ssleay' 'perl-net-server' 'perl-parse-recdescent' 'perl-uri'
'perl-x500-dn' 'perl-xml-parser' 'perl-xml-sax-base' 'perl-xml-twig' 'perl-libintl-perl' 'perl-ldap')
options=('!libtool')
install=$pkgname.install
source=("http://prdownloads.sourceforge.net/project/openca/$pkgname/releases/v$pkgver/sources/$pkgname-$pkgver.tar.gz"
initServer
User.pm
patch)


build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed 's#${openca_prefix}/etc/openca#/etc/openca#g' -i configure
  sed 's#${openca_prefix}/etc/init.d#/etc/rc.d#g' -i configure              
  sed 's#${openca_prefix}/var/openca#/var/openca#g' -i configure              
  cp ../User.pm src/modules/openca-user/
  ./configure --prefix=/usr --disable-static  --with-httpd-fs-prefix=/srv/http --with-httpd-url-prefix=/srv/http \
  --with-cgi-url-prefix=/srv/http/cgi-bin/pki --with-auth-user=openca --with-auth-password=openca \
  --with-db-type=mysql --with-httpd-user=http --with-httpd-group=http
  destdir_var="DESTDIR=\""$pkgdir\"
  sed "s#DESTDIR=\"\"#${destdir_var}#g" -i Makefile.global-vars
  patch -Np1 -i ../patch
  make

}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DEST_DIR="$pkgdir" install-online install-offline

  sed 's#/bin/sh#!/bin/sh#g' -i $pkgdir/etc/rc.d/openca
  chmod 755 -R $pkgdir/var
  mkdir -p $pkgdir/etc/httpd/conf/extra
  ln -s -r $pkgdir/etc/openca/contrib/apache/ $pkgdir/etc/httpd/conf/extra/openca
  cp ../initServer $pkgdir/usr/lib/openca/functions/
  cd $pkgdir/etc/openca/access_control
  sed 's#<protocol>ssl</protocol>#<protocol>.*</protocol>#g' -i *.template
  sed 's#<symmetric_keylength>128</symmetric_keylength>#<symmetric_keylength>0</symmetric_keylength>#g' -i *.template
  sed 's#/dev/fd0#/tmp/openca#g' -i ../config.xml
  chmod 777 -R $pkgdir/var/openca/log/xml/*
}

md5sums=('0bdd34a30fde0424441bd410c29f3837'
         'a9b55b0d758d757094594351580b06eb'
         'a23ba68c71830d9ecc1e6a503fa85769'
         'd63462e9d1565257b835493e8e504ecc')
