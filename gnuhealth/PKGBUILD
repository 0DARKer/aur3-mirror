# Maintainer: Andr√© Silva <emulatorman@parabola.nu>
# Contributor (Arch): SpepS <dreamspepser at yahoo dot it>
# Contributor (Arch): Ander <anderraso@gmail.com>

pkgname=gnuhealth
pkgver=2.6.4
pkgrel=1
pkgdesc="A free Health and Hospital Information System"
arch=any
url=http://health.gnu.org/
license=GPL3
depends=(
          postgresql
          pygtk
          python2-dateutil
          python2-ldap 
          python2-pillow
          python2-pip
          python2-psycopg2
          python2-pytz
          python2-pywebdav
          python2-qrcode 
          python2-vobject
          "trytond>=3.4"
          "trytond-account-invoice>=3.4"
          "trytond-calendar>=3.4"
          "trytond-country>=3.4"
          "trytond-stock-lot>=3.4"
)
source=(
  http://ftp.gnu.org/gnu/health/$pkgname-$pkgver.tar.gz
  http://ftp.gnu.org/gnu/health/$pkgname-$pkgver.tar.gz.sig
)
sha512sums=(
  f76060d334a85a7d726dbe6f2f9bc7206cf4ad7bf5abcdf143bc432411e89701320047c672bebefaeea9f92d5c31a298553f90ad35c9b2e17ae4058482cd067c
  SKIP
)

prepare() {
  # fix python2 problem
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' $(grep -rlI '/usr/bin/env python')
}

package() {
  MODULES=(
           health
           health_archives
           health_calendar
           health_crypto
           health_genetics
           health_gyneco
           health_history
           health_icd10
           health_icd10pcs
           health_icpm
           health_icu
           health_imaging
           health_inpatient
           health_inpatient_calendar
           health_lab
           health_lifestyle
           health_mdg6
           health_ntd
           health_ntd_chagas
           health_ntd_dengue
           health_nursing
           health_pediatrics
           health_pediatrics_growth_charts
           health_pediatrics_growth_charts_who
           health_profile
           health_qrcodes
           health_reporting
           health_services
           health_socioeconomics
           health_stock
           health_surgery
           health_who_essential_medicines
  )

  # build gnuhealth modules
    for module in ${MODULES[@]}
    do
      cd $srcdir/$pkgname-$pkgver/$module
      python2 setup.py install --root=$pkgdir
    done

  # put gnuhealth manual to man folder
    cd "$srcdir/$pkgname-$pkgver"

    install -Dm644 health/man/$pkgname.1 \
      $pkgdir/usr/share/man/man1/$pkgname.1
}
