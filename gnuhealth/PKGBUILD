# Maintainer: SpepS <dreamspepser at yahoo dot it>

pkgname=gnuhealth
pkgver=1.4.5
pkgrel=1
pkgdesc="A free Health and Hospital Information System"
arch=(any)
url="http://health.gnu.org/"
license=('GPL3')
depends=('trytond')
source=("http://ftp.gnu.org/gnu/health/$pkgname-$pkgver.tar.gz")
md5sums=('71f6bbcb9611921c68182ee1afc3f647')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  export PYTHON=python2
  sed -i '/TRYTON_PATH/s/python/&2/' configure

  # makefile fixes
  sed -e '/install:/a\\tinstall -d $(DESTDIR)$(tryton_modules)/' \
      -e 's/$(tryton/$(DESTDIR)&/' -i Makefile.in

  # mv man file
  mv health/man .

  ./configure --prefix=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make MANTARGET="$pkgdir/usr/share/man/man1" \
       DESTDIR="$pkgdir/" install

  # man
  install -Dm644 man/$pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1"

  # python2 fix
  sed -i 's/env python/&2/' \
    `grep -rl 'env python' "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
