# Maintainer: SpepS <dreamspepser at yahoo dot it>
# Maintainer (Parabola): Andr√© Silva <emulatorman@lavabit.com>

pkgname=gnuhealth
pkgver=1.8.1
pkgrel=3
pkgdesc="A free Health and Hospital Information System"
arch=(any)
url="http://health.gnu.org/"
license=('GPL3')
depends=(
          postgresql
          pygtk
          python2-dateutil
          python2-imaging
          python2-ldap 
          python2-pip
          python2-psycopg2
          python2-pytz
          python2-pywebdav
          python2-qrcode 
          python2-vobject
          trytond
          trytond-account-invoice
          trytond-calendar
          trytond-country
          trytond-stock-lot
)
source=("http://ftp.gnu.org/gnu/health/$pkgname-$pkgver.tar.gz")
md5sums=('d3837242011df1d39ce7a0f1d6e6a46a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fixes
  export PYTHON=python2

  # mv man file
  mv health/man .

  ./configure --prefix=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make MANTARGET="$pkgdir/usr/share/man/man1" \
       DESTDIR="$pkgdir/" install

  # man
  install -Dm644 man/$pkgname.1 \
    "$pkgdir/usr/share/man/man1/$pkgname.1"

  # python2 fix
  sed -i 's/env python/&2/' \
    `grep -rl 'env python' "$pkgdir"`
}

# vim:set ts=2 sw=2 et:
