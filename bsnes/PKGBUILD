# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
pkgname='bsnes'
# Fool AUR since it doesn't support split PKGBUILDS (shamelessly stolen from kernel26-git PKGBUILD).
(( 1 )) && pkgname=('bsnes' 'snespurify')
pkgver=0.077
pkgrel=1
pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL2')
depends=('libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt')
makedepends=('pkgconfig' 'mesa')
source=("http://bsnes.googlecode.com/files/${pkgname}_v077-source.tar.bz2")
md5sums=('77cd5f73733060c0c19dfe0f5a33e0b6')

# Build the accuracy profile (you can also choose 'performance' or 'compatibility')
__profile='accuracy'

build() {
  cd "${srcdir}/${pkgname}_v077-source/${pkgname}"

  # Makefile hacks
  # Disable pulseaudio.
  sed -e 's|audio.pulseaudio ||' \
      -e 's|audio.pulseaudiosimple ||' \
      -i "ui/Makefile"
  # Don't copy the cheat file.
  sed '99,101d' -i "ui/Makefile"

  # Compile bsnes
  make compiler=gcc platform=x profile=${__profile}
  
  # Compile snespurify
  cd "${srcdir}/${pkgname}_v077-source/snespurify"
  sed -e 's|g++-4.5|g++|' -i cc-gtk.sh
  ./cc-gtk.sh
}

package_snespurify() {
  pkgver=0.007
  pkgrel=1
  pkgdesc='Utility to clean game files so they are compatible with bsnes.'
  depends=('gtk2')

  cd "${srcdir}/bsnes_v077-source/snespurify"
  install -D --mode 755 snespurify "${pkgdir}/usr/bin/snespurify"
}

package_bsnes() {
  pkgver=0.077
  pkgrel=1
  pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy."
  depends=('libao' 'libgl' 'libxv' 'openal' 'sdl' 'qt')

  cd "${srcdir}/${pkgname}_v077-source/${pkgname}"
  make install profile=${__profile} DESTDIR=${pkgdir} prefix=/usr 
}