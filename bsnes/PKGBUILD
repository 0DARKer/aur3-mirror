# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
pkgname=bsnes
pkgver=0.075
pkgrel=1
pkgdesc="Super Nintendo Entertainment System (SNES) emulator focused on accuracy."
arch=('i686' 'x86_64')
url="http://byuu.org/bsnes/"
license=('GPL2')
depends=('libao' 'libgl' 'libxv' 'gtk2' 'openal' 'sdl')
makedepends=('pkgconfig' 'mesa')
source=("http://bsnes.googlecode.com/files/${pkgname}_v075-source.tar.bz2")
md5sums=('dc98cba4ee50f115cc52efb90b2c5648')

# Build the accuracy profile (you can also choose 'performance' or 'compatibility')
__profile='accuracy'

build() {
  cd "${srcdir}/${pkgname}_v075-source/${pkgname}"

  # Makefile hacks
  # Disable pulseaudio.
  sed -e 's|audio.pulseaudio ||' \
      -e 's|audio.pulseaudiosimple ||' \
      -i "ui/Makefile" || return 1
  # Don't copy the cheat file.
  sed '97,99d' -i "ui/Makefile" || return 1

  make compiler=gcc platform=x profile=${__profile} || return 1
}

package() {
  cd "${srcdir}/${pkgname}_v075-source/${pkgname}"
  make install profile=${__profile} DESTDIR=${pkgdir} prefix=/usr || return 1
}