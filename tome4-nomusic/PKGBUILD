#Maintainer: Joao Cordeiro <jlcordeirolinux at gmail dot com>
pkgname=tome4-nomusic
_pkgname=tome4
pkgver='1.0.0beta35'
pkgrel=1
pkgdesc="An open-source, single-player, role-playing roguelike game set in the world of Eyal."
arch=('i686' 'x86_64')
url="http://tome.te4.org/"
license=('GPL3')
depends=('mesa' 'sdl-hg' 'sdl_image' 'sdl_ttf' 'sdl_mixer' 'openal')
makedepends=('premake4')
options=(!makeflags)
source=("http://te4.org/dl/t-engine/t-engine4-src-${pkgver}-nomusic.tar.bz2"
        'tome4'
        'premake4.patch'
        'te4core.patch'
        'profile.patch'
        'particles.patch'
        'music.patch')
md5sums=('ccc7711ce07c590e35ca3d18ded6688f'
         'b3ca2d9b3db89b00297c5efe565accf2'
         '17ad27a102a798a80701ea5b660825b0'
         '69a3e198f58d7bfd1676a05fee4c43e0'
         '401fd71c880fa279e1e3075922d8ceb1'
         '1a015818ab39974d833eeea6f988ac47'
         'c355aba1c683382a3a94f6855539ef89')

build() {
    cd "${srcdir}/t-engine4-src-$pkgver"

    patch -p2 < "${srcdir}/premake4.patch"  # for sdl_ttf
    patch -p2 < "${srcdir}/te4core.patch"
    patch -p2 < "${srcdir}/profile.patch"
    patch -p2 < "${srcdir}/particles.patch"
    patch -p2 < "${srcdir}/music.patch"

    premake4 gmake
    make
}
package() {
    cd "${srcdir}/t-engine4-src-$pkgver"

    destdir="${pkgdir}/opt/${pkgname}/"

    install -d ${destdir}

    cp -r bootstrap/ game/ CONTRIBUTING COPYING CREDITS bin/Debug/t-engine \
        "${destdir}"

    install -Dm755 "${srcdir}/${_pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
