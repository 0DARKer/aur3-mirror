# Maintainer: Ben Reedy <thebenj88 *AT* gmail *DOT* com>
# Contributor: Clement Guerin <geecko.dev@free.fr>
# Contributor: Thiago Kenji Okada <thiago DOT mast3r AT gmail DOT com>

pkgname=ppsspp-git
pkgver=v0.9.7.2.r244.ca0a8d0
pkgrel=1
pkgdesc="A PSP emulator for Android, Windows, Mac, and Linux, written in C++."
arch=('i686' 'x86_64')
url="http://www.ppsspp.org/"
license=('GPL2')
depends=('zlib' 'sdl')
makedepends=('git' 'cmake' 'gcc')
optdepends=()
provides=('ppsspp')
source=('ppsspp::git://github.com/hrydgard/ppsspp.git#branch=master')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname%-*}"
    printf "%s" "$(git describe --tags | sed 's/-/.r/; s/-g/./')"
}

build() {
	cd $srcdir/ppsspp

	git submodule update --init

	export LDFLAGS="-lavutil -lavformat -lavcodec -lswscale -lswresample"

	mkdir build
	pushd build
	cmake ..
	make
	popd
}

package() {
	cd "$pkgdir"

	mkdir -p usr/bin
	mkdir -p usr/share/ppsspp

	cp "$srcdir/ppsspp/build/PPSSPPSDL" ./usr/share/ppsspp
	cp -r "$srcdir/ppsspp/build/assets/" ./usr/share/ppsspp

	cd ./usr/bin/

    cat >> PPSSPPSDL <<EOF 
#!/bin/bash
cd /usr/share/ppsspp
./PPSSPPSDL
EOF
    
    chmod +x PPSSPPSDL
}
