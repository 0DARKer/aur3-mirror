# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>
# Maintainer: Florian Walch <florian [at] fwalch [dot] com>

pkgname=msgpack-git-neovim
_commit=0335df55e1a408c0d56d43e46253c952fb8a7f04
pkgrel=1
pkgver=0.r1505.0335df5
pkgdesc="An efficient object serialization library; experimental version needed for neovim-git."
arch=('i686' 'x86_64')
url="http://msgpack.org/"
license=('Apache')
depends=('gcc-libs')
conflicts=('msgpack' 'msgpack-git')
provides=('msgpack')
source=("${pkgname}::git://github.com/msgpack/msgpack-c.git#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
  cd "${pkgname}"
  printf "0.r%s.%s" "$(git rev-list --count ${_commit})" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${pkgname}"
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR=${pkgdir} install
}

# vim:set sw=2 sts=2 et:
