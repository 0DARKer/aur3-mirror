# Contributor: Benjamin Wild <benwild@gmx.de>
pkgname=network-manager-applet-gtk2
srcpkgname=network-manager-applet
pkgver=0.8.9997
pkgrel=1
pkgdesc="frontend to NetWorkmanager with minimal GNOME dependencies and compiled for gtk2"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('networkmanager' 'libgnome-keyring' 'polkit-gnome' 'gtk2'
         'notification-daemon' 'libnotify' 'gnome-icon-theme'
         'mobile-broadband-provider-info' 'gconf')
makedepends=('intltool' 'gnome-bluetooth' 'gtk-doc')
optdepends=('gnome-bluetooth: for PAN/DUN support')
options=('!libtool')
provides=('network-manager-applet')
conflicts=('network-manager-applet')
install=network-manager-applet.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$srcpkgname/${pkgver%.*}/$srcpkgname-$pkgver.tar.bz2)
md5sums=('5f21ccf42f723a2ec8e1c83dcd299535')

build() {
  cd "${srcdir}/${srcpkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --with-gtkver=2  \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-static \
      --disable-maintainer-mode
  make
}

package() {
  cd "${srcdir}/${srcpkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${srcpkgname}.schemas" --domain nm-applet ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
