# Maintainer: Aljosha Papsch <papsch.al@gmail.com>
# Contributor: Benjamin Wild <benwild@gmx.de>

pkgname=network-manager-applet-gtk2
_pkgname=network-manager-applet
pkgver=0.9.0
_pkgepoch=${pkgver%.*}
pkgrel=2
pkgdesc="GNOME frontend to NetWorkmanager compiled for gtk2"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('networkmanager' 'gtk2' 'libgnome-keyring'
         'notification-daemon' 'libnotify' 'gnome-icon-theme'
         'mobile-broadband-provider-info' 'gconf')
makedepends=('intltool' 'gtk-doc')
optdepends=('gnome-bluetooth: for PAN/DUN support')
options=('!libtool')
provides=('network-manager-applet')
conflicts=('network-manager-applet')
install=network-manager-applet.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/${_pkgepoch}/$_pkgname-$pkgver.tar.bz2)
sha256sums=('bb34653b7524bf36c79f86545a255d05c076367a2461389a7512b9e0ef0df38f')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --with-gtkver=2  \
      --localstatedir=/var \
      --libexecdir=/usr/lib/networkmanager \
      --disable-static \
      --disable-maintainer-mode
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_pkgname}.schemas" --domain nm-applet ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
