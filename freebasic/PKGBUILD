# Contributor:	rabyte		<gmail.com: rabyte     >
# Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>

pkgname=freebasic
pkgver=1.00.0
pkgrel=1
pkgdesc="A free BASIC compiler"
arch=('i686' 'x86_64')
url="http://www.freebasic.net/"
license=('GPL2' 'LGPL')
depends=('gcc-libs' 'libxpm' 'ncurses')
provides=('fbc')
conflicts=('freebasic-examples')
options=(staticlibs)

if [ "$CARCH" = "x86_64" ]; then
  source=("http://sourceforge.net/projects/fbc/files/Binaries%20-%20Linux/FreeBASIC-${pkgver}-linux-x86_64.tar.xz")
  md5sums=('d388ced8a10bf00bc16ade2c5e99eab0')
  prefix=${CARCH}
else
  source=("http://sourceforge.net/projects/fbc/files/Binaries%20-%20Linux/FreeBASIC-${pkgver}-linux-x86.tar.xz")
  md5sums=('37ccf8d78aa494ef63ee04ff90fa4685')
  prefix="x86"
fi

package() {
  cd "${srcdir}/FreeBASIC-${pkgver}-linux-${prefix}"

  mkdir "${pkgdir}/usr"
  ./install.sh -i "${pkgdir}/usr"

  mv "${pkgdir}"/usr/bin/fbc{,.bin}
  mkdir -p "${pkgdir}/usr/share"
  mv "${pkgdir}/usr/man" "${pkgdir}/usr/share/man"
  echo -e "#!/bin/sh\nexec /usr/bin/fbc.bin -prefix /usr \"\${@}\"" > "${pkgdir}/usr/bin/fbc"
  chmod +x "${pkgdir}/usr/bin/fbc"
}
