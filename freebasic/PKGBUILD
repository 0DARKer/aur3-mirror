# Contributor:	rabyte		<gmail.com: rabyte     >
# Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>

pkgname=freebasic
pkgver=0.22.0
pkgrel=1
pkgdesc="A free BASIC compiler"
arch=('i686' 'x86_64')
url="http://www.freebasic.net/"
license=('GPL2' 'LGPL')
depends=('ncurses')
[ "$CARCH" = "x86_64" ] && depends=('lib32-ncurses')
provides=('fbc')
conflicts=('freebasic-examples')
source=("http://downloads.sourceforge.net/fbc/Binaries - Linux/FreeBASIC-$pkgver-linux.tar.gz")

package() {
	cd "${srcdir}/FreeBASIC-${pkgver}-linux"
	mkdir "${pkgdir}/usr"
	./install.sh -i "${pkgdir}/usr"
	mv "${pkgdir}"/usr/bin/fbc{,.bin}
	echo -e "#!/bin/sh\nexec /usr/bin/fbc.bin -prefix /usr \"\${@}\"" > "${pkgdir}/usr/bin/fbc"
	chmod +x "${pkgdir}/usr/bin/fbc"
}

md5sums=('95ef4df6de56ac4884ae4e06c86cb20b')
