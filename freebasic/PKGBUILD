# Contributor:	rabyte		<gmail.com: rabyte     >
# Maintainer:	Jesse Jaara	<gmail.com: jesse.jaara>

pkgname=freebasic
pkgver=1.01.0
pkgrel=1
pkgdesc="A free BASIC compiler"
arch=('i686' 'x86_64')
url="http://www.freebasic.net/"
license=('GPL2' 'LGPL')
depends=('gcc-libs' 'libxpm' 'libtinfo')
provides=('fbc')
conflicts=('freebasic-examples')
options=(staticlibs)

source_x86_64=("http://downloads.sourceforge.net/fbc/FreeBASIC-${pkgver}-linux-x86_64.tar.xz")
md5sums_x86_64=('b6a07e186864e9ea1878fc503da2672e')

source_i686=("http://downloads.sourceforge.net/fbc/FreeBASIC-${pkgver}-linux-x86.tar.xz")
md5sums_i686=('d0c0c3c6000ca4b9f7f1fc39250134ae')

_prefix="x86_64"
if [ "$CARCH" == "i686" ]; then _prefix="i686"; fi

package() {
  cd "${srcdir}/FreeBASIC-${pkgver}-linux-${_prefix}"

  mkdir "${pkgdir}/usr"
  ./install.sh -i "${pkgdir}/usr"

  mv "${pkgdir}"/usr/bin/fbc{,.bin}
  mkdir -p "${pkgdir}/usr/share"
  mv "${pkgdir}/usr/man" "${pkgdir}/usr/share/man"
  echo -e "#!/bin/sh\nexec /usr/bin/fbc.bin -prefix /usr \"\${@}\"" > "${pkgdir}/usr/bin/fbc"
  chmod +x "${pkgdir}/usr/bin/fbc"
}
