# Maintainer: Vinzenz Vietzke <vinz at vinzv dot de>

# To port this PKGBUILD to another language of tor-browser you 
# have to change $pkgname, $_language, $pkgdesc and $url in PKGBUILD
# AND (!) the first line in the .install file!

pkgname='tor-browser-de'
pkgver='2.3.25_8'
_realpkgver='2.3.25-8'
_language='de'
pkgrel='1'
pkgdesc='Tor Browser Bundle: Anonymous browsing using firefox and tor'
url='https://www.torproject.org/projects/torbrowser.html.en'
arch=('x86_64' 'i686')
license=('GPL')
#depends=('java-runtime')
depends=()
optdepends=('zenity: simple dialog boxes'
			'kdialog: KDE dialog boxes'
			'notify-send: Gnome dialog boxes')
install="${pkgname}.install"
source=("https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-dev-${_language}.tar.gz"
		"https://www.torproject.org/dist/torbrowser/linux/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-dev-${_language}.tar.gz.asc"
		"${pkgname}.desktop"
		"${pkgname}.png"
		"${pkgname}.sh")
if [[ "$CARCH" == 'x86_64' ]]; then
	sha256sums=('bc0b489523f6697a5ff6ed59f9c0e1ed30376694f1a89d7f95c8d35dfea34ee3'
				'a60e68ee4998a694a0b32fc26ec142507b503acaed5e647afb5d26c39caa25c3')
else
	sha256sums=('6b7f50a55e9db0f1072d670ff421c2123471f10a052accb9c5147156759da49d'
				'bf09e61ac0ffa3b0692a41aeb010df28728212115d4960c7b64eeb1f435d737e')
fi
sha256sums+=('c0108e1beb72bd01d82446884dec7b22e47e38ae52930e88f337f97d90ebb226'
			'17fc2f5784d080233aca16e788d62ab6fe3e57cf781b123cfe32767de97d6d3b'
			'7fc947b8ae1483c5abb4545f6e26b315ff1fb16eb7ae8837f7afb10a9277383b')
noextract=("tor-browser-gnu-linux-${CARCH}-${_realpkgver}-dev-${_language}.tar.gz")

package() {
	cd "${srcdir}"

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.sh
	sed -i "s/REPL_VERSION/${_realpkgver}/g"	${pkgname}.sh
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.sh

	sed -i "s/REPL_NAME/${pkgname}/g"			${pkgname}.desktop
	sed -i "s/REPL_LANGUAGE/${_language}/g"		${pkgname}.desktop
	sed -i "s/REPL_COMMENT/${pkgdesc}/g"		${pkgname}.desktop

	install -Dm 644 ${pkgname}.desktop	${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm 644 ${pkgname}.png		${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm 755 ${pkgname}.sh		${pkgdir}/usr/bin/${pkgname}

	install -Dm 644 tor-browser-gnu-linux-${CARCH}-${_realpkgver}-dev-${_language}.tar.gz \
		${pkgdir}/opt/${pkgname}/tor-browser-gnu-linux-${CARCH}-${_realpkgver}-${_language}.tar.gz
}

# vim:set ts=2 sw=2 et:
