# Maintainer: Raziel23 <venom23 at runbox dot com>

pkgname=vcmi
pkgver=0.97
pkgrel=1
pkgdesc="Heroes of Might and Magic 3 game engine"
arch=('i686' 'x86_64')
url="http://forum.vcmi.eu/portal.php"
license=('GPL2')
depends=('boost-libs' 'ffmpeg' 'sdl2_image' 'sdl2_mixer' 'sdl2_ttf' 'qt5-base' 'libxkbcommon-x11'
         'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme' 'minizip')
makedepends=('boost' 'cmake')
optdepends=('innoextract: required by vcmibuilder'
            'unshield: required by vcmibuilder'
            'unzip: required by vcmibuilder')
options=('!makeflags')
install=vcmi.install
source=("https://github.com/vcmi/$pkgname/archive/$pkgver.tar.gz")
md5sums=('11f2000244622706773ce37ca2366ebb')

prepare() {
  cd "$srcdir"

  if [[ -d "$pkgname-$pkgver-build" ]]; then
    msg 'Removing the last build...'
    rm -rf "$pkgname-$pkgver-build"
  fi

  mkdir "$pkgname-$pkgver-build"
}

build() {
  cd "$srcdir/$pkgname-$pkgver-build"

  cmake "../$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_SKIP_RPATH='OFF' \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver-build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
