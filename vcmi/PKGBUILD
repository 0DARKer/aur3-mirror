# Maintainer: Raziel23 <venom23 at runbox dot com>

pkgname=vcmi
pkgver=0.92
pkgrel=1
pkgdesc="Heroes of Might and Magic 3 game engine"
arch=('i686' 'x86_64')
url="http://forum.vcmi.eu/portal.php"
license=('GPL')
depends=('boost-libs' 'ffmpeg' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_ttf' 'zlib')
makedepends=('boost' 'cmake' 'subversion')
optdepends=('innoextract: required by vcmibuilder'
            'unshield: required by vcmibuilder'
            'unzip: required by vcmibuilder'
            'wget: required by vcmibuilder')
install=vcmi.install
source=('http://download.vcmi.eu/vcmi-0.92.tar.gz')
md5sums=('bfb359c75692779d6835d357d4c241ce')

build() {
  cd "${srcdir}"

  if [[ -d "${pkgname}-${pkgver}-build" ]]; then
    msg 'Removing the last build ...'
    rm -rf "${pkgname}-${pkgver}-build"
  fi

  mkdir "${pkgname}-${pkgver}-build" && cd "${pkgname}-${pkgver}-build"
  cmake "../${pkgname}-${pkgver}" \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_SKIP_RPATH='OFF' \
    -DCMAKE_BUILD_TYPE='RelWithDebInfo'
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-build"
  make DESTDIR="${pkgdir}" install
}
