# Maintainer: Andreas Schreiner <andreas.schreiner@sonnenmulde.at>
# Contributor: Ruslan Nabioullin <rnabioullin at gmail dot com>
# Contributor: Gordon JC Pearce <gordon at gjcp dot net>
pkgname=xastir
pkgver=2.0.6
pkgrel=1
pkgdesc="Full featured APRS Tracking/Information Reporting System"
url="http://www.xastir.org/"
license=('GPL' 'LGPL')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        http://downloads.sourceforge.net/$pkgname/$pkgname-sounds.tgz
        $pkgname.desktop
        $pkgname-festival.desktop
        $pkgname-speech
        $pkgname.png)
md5sums=('08a39e442a1117e8f5a72ddea64eedb2'
         '4201e7079cb5b2bd8a7c34dccfa3f2bb'
         'a2b560b97a10c9c132ab0db24435ab3c'
         '38431e4985b2002a68444be502be3973'
         'e56e55a1c43038d4488fbdd429a0a755'
         '0a7269113052d3bd2711b84ff2fda9d6')
sha512sums=('422bfd5c243ce2761279908b2294f94598df4e51e8dc9eea1eede12c460934c97392e8d99d40d67d92b4e31fe09806e9c33190e146696a49d968df6562e858f3'
            'ab0f6cd74cc61f07588c0dcfb34c86537b5007f215bd832165f4fb652c4fa9e7be5ef4529b8deb4af8ab483b7ff9f5c4ac634e15abb049ede18a91ea6d88b602'
            '18d144b0d5601155032d97916bedbe8c3f63b81d9fcdf415e7efed923ebfa3e247f8b0b23e9aa92c5f52f7f3c411cf5496009be3b6527f08e28cab3e815cae88'
            '6a543c9cb6a0119329529c3fe5ace71a4ef5fe8e8deac3c62b5d54e8e2d776479626963a0e621938cdb99be8134f83589e80197d13d2ce505c0ac84def876475'
            'f284ce0544dd6517fbdf1d0f54d7e67f73f6eb0928bf2bd525db2aa0b0f13f61451fe7e669542730a9b77bbc6b2bf679b076fd632584684d433939f21d824ed3'
            'bc4f634825b577baf763f84441f27502480bec7020f84c98c9da1d961014d41d554d1d38d1ac29543ecb71f2f1a88f34f75d895e609bad1ec77a3670f3639874')
arch=('i686' 'x86_64')
depends=('python' 'desktop-file-utils' 'lesstif' 'shapelib' 'curl' 'libax25' 'libxml2' 'libpng' 'gdal'
    'graphicsmagick')
makedepends=('autoconf' 'automake')
conflicts=('xastir-cvs')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  
  sed -i -e s:doc/xastir:xastir/doc: Makefile
  
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  mkdir -p $pkgdir/usr/share/applications
  mkdir -p $pkgdir/usr/share/pixmaps
  cd $srcdir
  install -m644 *.desktop $pkgdir/usr/share/applications/
  install -m644 *.png $pkgdir/usr/share/pixmaps/
  install -m755 $pkgname-speech $pkgdir/usr/bin/
  rm $pkgdir/usr/share/$pkgname/sounds/*
  install -m644 sounds/* $pkgdir/usr/share/$pkgname/sounds/
  
  chmod 4755 $pkgdir/usr/bin/$pkgname
}
