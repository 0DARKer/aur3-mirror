# Maintainer: Ruslan Nabioullin <rnabioullin at gmail dot com>
# Contributor: Gordon JC Pearce <gordon at gjcp dot net>
pkgname=xastir
pkgver=2.0.0
pkgrel=2
pkgdesc="Full featured APRS Tracking/Information Reporting System"
url="http://www.xastir.org/"
license=('GPL' 'LGPL')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        http://downloads.sourceforge.net/$pkgname/$pkgname-sounds.tgz
        $pkgname.desktop
        $pkgname-festival.desktop
        $pkgname-speech
        $pkgname.png
        Makefile.am.patch)
md5sums=('3e660b8168c9037b06e20b0579af3c80'
         '4201e7079cb5b2bd8a7c34dccfa3f2bb'
         'a2b560b97a10c9c132ab0db24435ab3c'
         '38431e4985b2002a68444be502be3973'
         'e56e55a1c43038d4488fbdd429a0a755'
         '0a7269113052d3bd2711b84ff2fda9d6'
         'cb95459f2fd556e76fbaea2314ed0ff3')
arch=('i686' 'x86_64')
depends=('lesstif' 'shapelib' 'curl' 'libax25' 'libxml2' 'libpng' 'gdal'
    'graphicsmagick')
makedepends=('autoconf' 'automake=1.11.1')
conflicts=('xastir-cvs')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p0 < $srcdir/Makefile.am.patch
  ./configure --prefix=/usr
  
  sed -i -e s:doc/xastir:xastir/doc: Makefile
  
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  mkdir -p $pkgdir/usr/share/applications
  mkdir -p $pkgdir/usr/share/pixmaps
  cd $srcdir
  install -m644 *.desktop $pkgdir/usr/share/applications/
  install -m644 *.png $pkgdir/usr/share/pixmaps/
  install -m755 $pkgname-speech $pkgdir/usr/bin/
  rm $pkgdir/usr/share/$pkgname/sounds/*
  install -m644 sounds/* $pkgdir/usr/share/$pkgname/sounds/
  
  chmod 4755 $pkgdir/usr/bin/$pkgname
}
