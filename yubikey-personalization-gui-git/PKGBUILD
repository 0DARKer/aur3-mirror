# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubikey-personalization-gui-git
pkgver=3.1.5.4.g00d86af
pkgrel=1
pkgdesc="Yubico yubikey personalization GUI"
arch=('i686' 'x86_64')
url="https://github.com/Yubico/yubikey-personalization-gui"
license=('BSD')
depends=('libusb' 'yubico-c-client-git' 'yubikey-personalization')
makedepends=('git')
provides=('yubikey-personalization-gui' 'yubikey-personalization-tool')
conflicts=('yubikey-personalization-gui' 'yubikey-personalization-tool')
source=('git://github.com/Yubico/yubikey-personalization-gui.git')

pkgver() {
	cd yubikey-personalization-gui/
	git describe --tags --long | sed 's|^[^0-9]*||;s|-|.|g'
}

build() {
	cd yubikey-personalization-gui/

	qmake "CONFIG += debian"
	make
}

check() {
	cd yubikey-personalization-gui/

	make check
}

package() {
	cd yubikey-personalization-gui/
  
	install -D -m755 build/release/yubikey-personalization-gui "${pkgdir}/usr/bin/yubikey-personalization-gui"
	install -D -m644 resources/lin/yubikey-personalization-gui.1 "${pkgdir}/usr/share/man/man1/yubikey-personalization-gui.1"

	install -D -m644 resources/lin/yubikey-personalization-gui.desktop "${pkgdir}/usr/share/applications/yubikey-personalization-gui.desktop"
	install -D -m644 resources/lin/yubikey-personalization-gui.png "${pkgdir}/usr/share/pixmaps/yubikey-personalization-gui.png"

	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/yubikey-personalization-gui/COPYING"
	install -D -m644 README "${pkgdir}/usr/share/doc/yubikey-personalization-gui/README"
} 

sha256sums=('SKIP')
