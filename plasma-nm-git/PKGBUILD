# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm-git
pkgver=r972.530e3f8
pkgrel=1
pkgdesc='Plasma applet written in QML for managing network connections'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/network/plasma-nm'
license=('GPL2')
depends=('qt5-quick1' 'libnm-qt5-git' 'frameworkintegration-git' 'kidletime-git' 'kitemmodels-git' 'kinit-git' 'plasma-framework-git' 'kjs-git' 'kcmutils-git' 'kemoticons-git' 'kde4support-git' 'libnm-qt')
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git' 'qt5-tools' 'kf5umbrella-git')
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers')
provides=('plasma-nm')
conflicts=('plasma-nm')
source=("git://anongit.kde.org/plasma-nm.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
  cd plasma-nm
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-nm \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
