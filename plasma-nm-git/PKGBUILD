# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-nm-git
pkgver=r1018.ffb10f6
pkgrel=1
pkgdesc='Plasma applet written in QML for managing network connections'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/network/plasma-nm'
license=('GPL2')
depends=('libnm-qt5-git' 'libmm-qt5-git' 'plasma-framework-git' 'kde4support-git')
makedepends=('extra-cmake-modules' 'git' 'kdoctools-git' 'qt5-tools' 'kf5umbrella-git') # 'openconnect'
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers') # 'openconnect: Cisco AnyConnect VPN' 
provides=('plasma-nm')
conflicts=('plasma-nm')
source=("git://anongit.kde.org/plasma-nm.git#branch=frameworks")
sha256sums=('SKIP')

pkgver() {
  cd plasma-nm
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../plasma-nm \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
