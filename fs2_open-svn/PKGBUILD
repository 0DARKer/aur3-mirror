# Maintainer: ZekeSulastin <zekesulastin@gmail.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>

pkgname=fs2_open-svn
pkgver=8843
pkgrel=1
pkgdesc="An enhancement of the original Freespace 2 engine - SVN version"
url="http://scp.indiegames.us"
arch=('i686' 'x86_64')
license=('custom:freespace2')
depends=('libjpeg' 'libpng' 'libtheora' 'libvorbis' 'lua' 'mesa' 'openal' 'sdl')
makedepends=('subversion')
install=fs2_open-svn.install
source=(osapi_unix.patch)
md5sums=('783d5ab68a0ce4d26ee415e8fefbc762')

_svntrunk=svn://svn.icculus.org/fs2open/trunk/fs2_open
_svnmod=fs2_open

build()
{
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"

  patch -Np0 -i $srcdir/osapi_unix.patch

  ./autogen.sh --enable-speech --enable-inferno
  make
  cp code/fs2_open_INF_r "$srcdir"

  # Make the debug binary; make clean doesn't work going back from debug
  #  to release, an issue if repeatedly building from the same src dir

  cd "$srcdir"
  rm -rf "$srcdir/$_svnmod-build"
  cp -r "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"
  cd "$srcdir/$_svnmod-build"
  patch -Np0 -i $srcdir/osapi_unix.patch

  ./autogen.sh --enable-speech --enable-inferno --enable-debug
  make
  cp code/fs2_open_INF_d "$srcdir"
}

package () {
  cd "$srcdir/$_svnmod-build"

  install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/LICENSE

  # This installs to a different binary than the normal fs2_open to allow
  #  versions to coexist (i.e. some mods require different FS versions)

  install -D -m755 "$srcdir/fs2_open_INF_r" "$pkgdir/usr/bin/fs2_open_svn_r"
  install -D -m755 "$srcdir/fs2_open_INF_d" "$pkgdir/usr/bin/fs2_open_svn_d"
  
  ln -s /usr/bin/fs2_open_svn_r "$pkgdir/usr/bin/fs2_open_svn"
}

