# Maintainer: Ondrej Kucera <ondrej.kucera@centrum.cz>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>

pkgname=jdk-docs
pkgver=8.40
_realver=8u40
_buildnum=b25
_javafxver=8.0.40
pkgrel=1
pkgdesc="Java SE Development Kit Documentation 8"
arch=("any")
url="http://www.oracle.com/technetwork/java/index.html"
license=("custom")
options=("!strip" "docs" "libtool" "staticlibs" "!zipman")
source=("http://download.oracle.com/otn-pub/java/jdk/$_realver-$_buildnum/jdk-$_realver-docs-all.zip"
	"http://download.oracle.com/otn-pub/java/javafx/$_javafxver-$_buildnum/javafx-$_realver-apidocs.zip"
	"Java SE Development Kit 8 Documentation License Agreement"
	"Oracle Legal Notices"
       )
sha256sums=('72fa379013ea0139a9840137ae062d2f6bbc4f31914519d2cbabb678929b4d93'
            '26784dc307a67bfc32506cbc1d9088207c0c417d7dee9bb0ad0f98ef0e870b2e'
            '14dc1953902010f7b48891e795183b39c048b19881815eec6a57cf3d62631ab7'
            '63626da8084eec01f7702aab6c62227fb4ddf249a428397da031be39940b689d')
noextract=("jdk-$_realver-docs-all.zip" "javafx-$_realver-apidocs.zip")

DLAGENTS=('http::/usr/bin/curl -LC - -b oraclelicense=a -O')

package() {
  mkdir -p "$pkgdir/usr/share/doc/java"
  bsdtar -xf "$srcdir/jdk-$_realver-docs-all.zip" -C "$pkgdir/usr/share/doc/java"
  mv "$pkgdir/usr/share/doc/java/docs" "$pkgdir/usr/share/doc/java/jdk"
  mkdir -p "$pkgdir/usr/share/doc/java/javafx"
  bsdtar -xf "$srcdir/javafx-$_realver-apidocs.zip" -C "$pkgdir/usr/share/doc/java/javafx"
  install -D -m644 "Java SE Development Kit 8 Documentation License Agreement" "$pkgdir/usr/share/licenses/$pkgname/Java SE Development Kit 8 Documentation License Agreement"
  install -D -m644 "Oracle Legal Notices" "$pkgdir/usr/share/licenses/$pkgname/Oracle Legal Notices"
  chown -R root:root "$pkgdir/usr/share/doc/java"
}
