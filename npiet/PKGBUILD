# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname=npiet
pkgver=1.2b
pkgrel=1
pkgdesc="npiet is an interpreter for piet programs."
arch=('i686' 'x86_64')
url="http://www.bertnase.de/npiet/"
license=('GPL')
depends=('libpng' 'gd' 'giflib')
source=("http://www.bertnase.de/npiet/$pkgname-$pkgver.tar.gz")
md5sums=('4c04f868133df3df8240b06fad8c7549')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make npiet
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # it is broken
  #make DESTDIR="$pkgdir/" install
  install -D -m 755 npiet $pkgdir/usr/bin/npiet
  install -D -m 644 npiet.1 $pkgdir/usr/share/man/man1/npiet.1

  # Provide some examples
  for i in examples/*
  do
    install -D -m 644 $i $pkgdir/usr/share/$pkgname/$i
  done
  rm -rf $pkgdir/usr/share/$pkgname/runtest.sh
}

# vim:set ts=2 sw=2 et:

