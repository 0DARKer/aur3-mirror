# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname=npiet
pkgver=1.3a
pkgrel=2
pkgdesc="An interpreter for piet programs. Also includes npietedit and npiet-foogol"
arch=('i686' 'x86_64')
url="http://www.bertnase.de/npiet/"
license=('GPL')
depends=('libpng' 'gd' 'giflib')
optdepends=('tk: for npietedit')
source=("http://www.bertnase.de/npiet/$pkgname-$pkgver.tar.gz"
  "npiet-1.3a-giflib5.patch"
  "npiet-1.3a-destdir-makefile.patch")
md5sums=('706d70a5de33c148182f6723d003b12d'
         '111497863ef45f4d0971cb28762bc09b'
         'b31edd08527c7a156078aa6676e1919c')
sha1sums=('8d88242dac2c4076ac9ce6809f4e7e94f4dcf0fc'
          '5038bf0527f15b42bf14adf5bfad97ab9f4c4bbe'
          '1dc94dd1dab1f2e93a4d2df2aa570952a51c6746')
sha256sums=('4a5f1261af62c0dd29cbb261e849899668b3c9c501c10d046191187f7c2bfdba'
            'c93c66a1e20967de438fb98aebdd0e51ba7899337e8ce11ea95c4fd58c19b3fe'
            '926e41ab218a7b50134cb805e1e85a4e95a758ef7a86aa234d9064431398babe')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # DESTDIR support for the install target
  patch -p1 -i "$srcdir/npiet-1.3a-destdir-makefile.patch"
  # API change since giflib 5.x
  patch -p1 -i "$srcdir/npiet-1.3a-giflib5.patch"
  ./configure --prefix=/usr
  make npiet npiet-foogol
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # FIXME: broken Makefile
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/man/man1"
  make DESTDIR="$pkgdir" install

  # Provide some examples
  for i in examples/*
  do
    install -D -m 644 $i $pkgdir/usr/share/$pkgname/$i
  done
}

# vim:set ts=2 sw=2 et:

