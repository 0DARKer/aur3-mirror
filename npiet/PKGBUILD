# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname=npiet
pkgver=1.3b
pkgrel=3
pkgdesc="An interpreter for piet programs. Also includes npietedit and npiet-foogol"
arch=('i686' 'x86_64')
url="http://www.bertnase.de/npiet/"
license=('GPL')
depends=('libpng' 'gd' 'giflib')
optdepends=('tk: for npietedit')
source=("http://www.bertnase.de/npiet/$pkgname-$pkgver.tar.gz"
       "giflib-5.1.patch")
md5sums=('0914164175b0e00eefa29416eb818c35'
         '392a8f0926c036ef08b92e0778ad5b55')
sha1sums=('f3db71ff32b580a40b836e2507c8ef73fa797cc0'
          '179745196fd10e22e39b65ed00a47d6fdc901519')
sha256sums=('04805bb5ad185a26e89c860fb6b148ca4a710b98de56cdadcde7b00237e02fae'
            '52561cc33d0dcdbbf110fc529db60d147172e5ae978984cc7281cca45b6ede6d')


build() {
  cd "$srcdir/$pkgname-$pkgver"
  # API change since giflib 5.1
  patch -p1 -i "$srcdir/giflib-5.1.patch"
  ./configure --prefix=/usr
  make npiet npiet-foogol
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  # FIXME: broken Makefile
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/man/man1"
  make DESTDIR="$pkgdir" install

  # Provide some examples
  for i in examples/*
  do
    install -D -m 644 $i $pkgdir/usr/share/$pkgname/$i
  done
}

# vim:set ts=2 sw=2 et:
