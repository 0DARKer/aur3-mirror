pkgname=imprudence-beta-bin
pkgver=1.4.0b1
pkgrel=1
_pkgbasever=1.4.0-beta-1
arch=('i686' 'x86_64')
_arch='x86'
[ "$CARCH" = "x86_64" ] && _arch='x86_64'
pkgdesc="Second Life is a 3-D virtual world entirely built and owned by its residents. Imprudence is an alternative viewer (beta version)."
url="http://www.kokuaviewer.org"
license=('GPL')
depends=('alsa-lib' 'apr-util' 'freealut' 'gnome-vfs' 'gtk2' 'gstreamer0.10' 'libgl' 'libidn' 'libjpeg6' 'libpng12' 'mesa' 'nss' 'sdl')
optdepends=('pulseaudio>=0.9.21')
install=imprudence-beta-bin.install
source=('imprudence-beta-bin.desktop' 'imprudence-beta-bin.launcher' \
"http://imprudence.googlecode.com/files/Imprudence-${_pkgbasever}-Linux-${_arch}.tar.bz2")
sha256sums=('83dfd722dae1ad262f01bb570c542a0dc16ca231e148e266db2af6292a1d8426'
         '9c5a2014b6fc63f104546a012365a94f9633fdec7dd8174305872a8167ee9c17'
         'f8f5408c1dfb3a73fbb34d968d268172239ec12c7b6e28aa754dd90f3d5b1fe9')
[ "$CARCH" = "x86_64" ] && sha256sums=(
         '83dfd722dae1ad262f01bb570c542a0dc16ca231e148e266db2af6292a1d8426'
         '9c5a2014b6fc63f104546a012365a94f9633fdec7dd8174305872a8167ee9c17'
         '7c9bf0a5d067a6138b11ffd28a5c19af0b9de589724d1647f1190fd59e175b9b')
         # Probably a cleaner way to do this

build() {
    cd $srcdir;

    # Rename Data Directory
    mv Imprudence-${_pkgbasever}-Linux-${_arch}/ imprudence-beta-bin

    # Install Desktop File
    install -D -m644 $srcdir/imprudence-beta-bin.desktop \
        $pkgdir/usr/share/applications/imprudence-beta-bin.desktop

    # Install Icon File
    # Changed name to beta so the release version can co-exist
    install -D -m644 $srcdir/imprudence-beta-bin/imprudence_icon.png \
        $pkgdir/usr/share/pixmaps/imprudence_beta_icon.png

    # Install Launcher
    install -D -m755 $srcdir/imprudence-beta-bin.launcher \
        $pkgdir/usr/bin/imprudence-beta-bin

    # Move Data to Destination Directory
    install -d $pkgdir/opt/
    mv imprudence-beta-bin/ $pkgdir/opt/

    # Change Permissions of files to root:games
    chown -R root:games $pkgdir/opt/imprudence-beta-bin
    chmod -R g+r $pkgdir/opt/imprudence-beta-bin

    # Make Binary Group-Executable
    chmod g+x $pkgdir/opt/imprudence-beta-bin/imprudence
}
