# Maintainer: Daniel Micay <danielmicay@gmail.com>

_hkgname=attoparsec
pkgname=haskell-${_hkgname}
pkgver=0.12.1.0
pkgrel=1
pkgdesc="Conversion of LaTeX math formulas to MathML."
url="http://hackage.haskell.org/package/${_hkgname}"
license=("GPL")
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-text' 'haskell-scientific')
source=("http://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
install=$pkgname.install
md5sums=('06607dd8f654cc0e0993ec40f54deba7')
options=("staticlibs")

build() {
  cd "$srcdir/$_hkgname-$pkgver"
  runhaskell Setup configure -O ${PKGBUILD_HASKELL_ENABLE_PROFILING:+-p } --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/$_hkgname-$pkgver"
  install -Dm744 register.sh "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m744 unregister.sh "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
