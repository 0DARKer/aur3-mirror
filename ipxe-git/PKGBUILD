# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=ipxe-git
pkgver=20120413
pkgrel=1
pkgdesc="iPXE open source boot firmware - git checkout"
arch=('i686' 'x86_64')
url="http://www.ipxe.org/"
license=('GPL')
makedepends=('git')
provides=('ipxe')
conflicts=('ipxe')
install=ipxe.install
source=('arch.patch'
	'http://www.eworm.de/download/linux/ipxe_truncate_title_v2.patch'
	'http://www.eworm.de/download/linux/ipxe_datetime.patch'
	'grub'
	'chain-default.ipxe')
backup=('etc/grub.d/20_ipxe')

_gitroot="git://git.ipxe.org/ipxe.git"
_gitname="ipxe"

build() {
	cd "${srcdir}"
	msg "Connecting to GIT server...."

	if [ -d ${_gitname} ] ; then
		cd ${_gitname} && git pull origin
		msg "The local files are updated."
	else
		git clone ${_gitroot} ${_gitname}
	fi

	msg "GIT checkout done or server timeout"
	msg "Starting make..."

	rm -rf "${srcdir}/${_gitname}-build"
	git clone "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-build"

	cd "${srcdir}/${_gitname}-build/src"

	# read and set keymap
	[ -s /etc/rc.conf ] && . /etc/rc.conf
	[ -s /etc/vconsole.conf ] && . /etc/vconsole.conf
	if [ -n "${KEYMAP}" ]; then
		sed -i "/^#define\tKEYBOARD_MAP/s/\s..\$/\t${KEYMAP}/" config/console.h
	fi

	# Submitted to ML, but not yet included
	# truncate menu title if it is too long
	patch -Np2 < ${srcdir}/ipxe_truncate_title_v2.patch

	# Add compile date and time to greeting
	patch -Np2 < ${srcdir}/ipxe_datetime.patch

	# ArchLinux branding
	patch -Np2 < ${srcdir}/arch.patch

	# NO_WERROR=1 is needed to compile with gcc 4.7.0
	NO_WERROR=1 make

	mv bin/undionly.kpxe bin/undionly-dist.kpxe
	make bin/undionly.kpxe EMBED=${srcdir}/chain-default.ipxe
}

package() {
	install -D -m755 ${srcdir}/grub ${pkgdir}/etc/grub.d/20_ipxe

	cd "${srcdir}/${_gitname}-build/"

	install -D -m644 COPYING ${pkgdir}/usr/share/doc/ipxe/COPYING
	install -D -m644 COPYRIGHTS ${pkgdir}/usr/share/doc/ipxe/COPYRIGRIGHTS

	cd "${srcdir}/${_gitname}-build/src"

	install -D -m644 bin/ipxe.dsk ${pkgdir}/usr/share/ipxe/ipxe.dsk
	install -D -m644 bin/ipxe.usb ${pkgdir}/usr/share/ipxe/ipxe.usb
	install -D -m644 bin/ipxe.iso ${pkgdir}/usr/share/ipxe/ipxe.iso

	install -D -m644 bin/undionly-dist.kpxe ${pkgdir}/usr/lib/ipxe/undionly.kpxe

	install -D -m644 bin/ipxe.lkrn ${pkgdir}/boot/ipxe/ipxe.lkrn

	install -D -m644 bin/undionly.kpxe ${pkgdir}/usr/lib/ipxe/undionly-default.kpxe
}

sha256sums=('43ae3512e19421498e090c925f22bbff42724603300472b6d55525ced12de51b'
            'b6f485a4ed43c6c4cdd026020f678299986856d24887fa23a180a1f27a96fc15'
            'a15ce48cc4d27aee650f5cbacc965792ce7132820318de9c508a3580000b6c41'
            'c2fc94baf96dfd180b9359f0eec089a3ac6f78768c95c8e03aaae263420951dc'
            'c1948d2aa6f0a7736a1869eda64db70d48ef5d4d5428e1c8a01d30795d7d287f')
