# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=ipxe-git
pkgver=1.0.0.r1219.ge411b37
pkgrel=1
pkgdesc="iPXE open source boot firmware - git checkout"
arch=('any')
url="http://www.ipxe.org/"
license=('GPL')
makedepends=('git' 'syslinux' 'cdrkit')
provides=('ipxe')
conflicts=('ipxe')
install=ipxe.install
source=('git://git.ipxe.org/ipxe.git'
	'http://www.eworm.de/download/linux/ipxe_asm_v2.patch'
	'http://www.eworm.de/download/linux/ipxe_gitversion.patch'
	'http://www.eworm.de/download/linux/ipxe_arch.patch'
	'grub'
	'chain-default.ipxe')
backup=('etc/grub.d/20_ipxe')

pkgver() {
	cd ipxe/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[_-+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

prepare() {
	cd ipxe/src/

	# fix for GCC 4.8.0
	patch -Np2 < ${srcdir}/ipxe_asm_v2.patch

	# Show git version and compile timestamp
	patch -Np2 < ${srcdir}/ipxe_gitversion.patch

	# ArchLinux branding
	patch -Np2 < ${srcdir}/ipxe_arch.patch

	# read and set keymap
	[ -s /etc/rc.conf ] && . /etc/rc.conf
	[ -s /etc/vconsole.conf ] && . /etc/vconsole.conf
	if [ -n "${KEYMAP}" ]; then
		sed -i "/^#define\tKEYBOARD_MAP/c #define KEYBOARD_MAP ${KEYMAP}" config/console.h
	fi

	# change menu colours
	sed -i "/COLOR_[A-Z]*_BG/s/COLOR_BLUE/COLOR_BLACK/" config/colour.h
}

build() {
	cd ipxe/src/

	make

	mv bin/undionly.kpxe bin/undionly-dist.kpxe
	make bin/undionly.kpxe EMBED=${srcdir}/chain-default.ipxe
}

package() {
	cd ipxe/

	install -D -m755 ${srcdir}/grub ${pkgdir}/etc/grub.d/20_ipxe

	install -D -m644 COPYING ${pkgdir}/usr/share/licenses/ipxe/COPYING
	install -D -m644 COPYRIGHTS ${pkgdir}/usr/share/licenses/ipxe/COPYRIGRIGHTS

	cd src/

	install -D -m644 bin/ipxe.dsk ${pkgdir}/usr/share/ipxe/ipxe.dsk
	install -D -m644 bin/ipxe.usb ${pkgdir}/usr/share/ipxe/ipxe.usb
	install -D -m644 bin/ipxe.iso ${pkgdir}/usr/share/ipxe/ipxe.iso

	install -D -m644 bin/undionly-dist.kpxe ${pkgdir}/usr/lib/ipxe/undionly.kpxe

	install -D -m644 bin/ipxe.lkrn ${pkgdir}/boot/ipxe/ipxe.lkrn

	install -D -m644 bin/undionly.kpxe ${pkgdir}/usr/lib/ipxe/undionly-default.kpxe
}

sha256sums=('SKIP'
            '4784e1ff27ab5987c855a23ecaaf9b236598943549a6f10272d66ef40a8b8db4'
            'cb25a959eb741f0123eeb8f8b061e9a625250a38b3b26cc9495613a02e719e3e'
            '7ee090abf5493bfc18524c7abacae02ccd6395d97f76832862ee29605ffdf2f3'
            'b3c9ee1bac1f85f9e8d9455abd94e4a3a0627d2b54ad030e29e247168db2dc83'
            'c1948d2aa6f0a7736a1869eda64db70d48ef5d4d5428e1c8a01d30795d7d287f')
