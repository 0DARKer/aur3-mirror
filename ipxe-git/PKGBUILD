# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=ipxe-git
pkgver=1.0.0.r1851.gacc27b9
pkgrel=3
pkgdesc="iPXE open source boot firmware - git checkout"
arch=('any')
url="http://www.ipxe.org/"
license=('GPL')
makedepends=('git' 'syslinux' 'cdrkit' 'mtools' 'xz')
provides=('ipxe')
conflicts=('ipxe')
install=ipxe.install
source=('git://git.ipxe.org/ipxe.git'
	'http://www.eworm.de/download/linux/ipxe-0001-git-version.patch'
	'http://www.eworm.de/download/linux/ipxe-0002-banner.patch'
	'grub'
	'chain-default.ipxe'
	'chain-default-3928.ipxe')
backup=('etc/grub.d/20_ipxe')

pkgver() {
	cd ipxe/

	if GITTAG="$(git describe --abbrev=0 --tags 2>/dev/null)"; then
		echo "$(sed -e "s/^${pkgname%%-git}//" -e 's/^[-_/a-zA-Z]\+//' -e 's/[-_+]/./g' <<< ${GITTAG}).r$(git rev-list --count ${GITTAG}..).g$(git log -1 --format="%h")"
	else
		echo "0.r$(git rev-list --count master).g$(git log -1 --format="%h")"
	fi
}

prepare() {
	cd ipxe/src/

	# git version
	patch -Np2 < "${srcdir}/ipxe-0001-git-version.patch"

	# ArchLinux branding
	patch -Np2 < "${srcdir}/ipxe-0002-banner.patch"

	# read and set keymap
	[ -s /etc/rc.conf ] && . /etc/rc.conf
	[ -s /etc/vconsole.conf ] && . /etc/vconsole.conf
	if [ -n "${KEYMAP}" ]; then
		sed -i "/^#define\tKEYBOARD_MAP/c #define KEYBOARD_MAP ${KEYMAP}" config/console.h
	fi

	# change menu colours
	sed -i "/COLOR_[A-Z]*_BG/s/COLOR_BLUE/COLOR_BLACK/" config/colour.h

	# enable neighbour command
	sed -i "/NEIGHBOUR_CMD/c #define NEIGHBOUR_CMD" config/general.h

	# enable ping command
	sed -i "/PING_CMD/c #define PING_CMD" config/general.h

	# enable reboot and poweroff
	sed -i -e "/REBOOT_CMD/c #define REBOOT_CMD" \
		-e "/POWEROFF_CMD/c #define POWEROFF_CMD" config/general.h
}

build() {
	cd ipxe/src/

	mkdir -p finished

	# make default targets (ipxe.{lkrn,dsk,iso,usb}) and ipxe.liso
	make all bin/ipxe.liso

	make bin/undionly.kpxe
	mv bin/undionly.kpxe finished/undionly.kpxe

	make bin/undionly.kpxe EMBED=${srcdir}/chain-default.ipxe
	mv bin/undionly.kpxe finished/undionly-default.kpxe

	make bin/undionly.kpxe EMBED=${srcdir}/chain-default-3928.ipxe
	mv bin/undionly.kpxe finished/undionly-default-3928.kpxe

	make bin/ipxe.pxe
	mv bin/ipxe.pxe finished/ipxe.pxe

	make bin/ipxe.pxe EMBED=${srcdir}/chain-default.ipxe
	mv bin/ipxe.pxe finished/ipxe-default.pxe

	make bin/ipxe.pxe EMBED=${srcdir}/chain-default-3928.ipxe
	mv bin/ipxe.pxe finished/ipxe-default-3928.pxe
}

package() {
	cd ipxe/

	install -D -m0755 ${srcdir}/grub ${pkgdir}/etc/grub.d/20_ipxe

	install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/ipxe/COPYING
	install -D -m0644 COPYING.GPLv2 ${pkgdir}/usr/share/licenses/ipxe/COPYING.GPLv2
	install -D -m0644 COPYING.UBDL ${pkgdir}/usr/share/licenses/ipxe/COPYING.UBDL

	cd src/

	# iPXE kernel bzImage
	install -D -m0644 bin/ipxe.lkrn ${pkgdir}/boot/ipxe/ipxe.lkrn

	# default targets
	install -D -m0644 bin/ipxe.dsk ${pkgdir}/usr/share/ipxe/ipxe.dsk
	install -D -m0644 bin/ipxe.usb ${pkgdir}/usr/share/ipxe/ipxe.usb
	install -D -m0644 bin/ipxe.iso ${pkgdir}/usr/share/ipxe/ipxe.iso
	install -D -m0644 bin/ipxe.liso ${pkgdir}/usr/share/ipxe/ipxe-legacy.iso

	# iPXE undi-only targets
	install -D -m0644 finished/undionly.kpxe ${pkgdir}/usr/lib/ipxe/undionly.kpxe
	install -D -m0644 finished/undionly-default.kpxe ${pkgdir}/usr/lib/ipxe/undionly-default.kpxe
	install -D -m0644 finished/undionly-default-3928.kpxe ${pkgdir}/usr/lib/ipxe/undionly-default-3928.kpxe

	# iPXE targets (with full driver support)
	install -D -m0644 finished/ipxe.pxe ${pkgdir}/usr/lib/ipxe/ipxe.pxe
	install -D -m0644 finished/ipxe-default.pxe ${pkgdir}/usr/lib/ipxe/ipxe-default.pxe
	install -D -m0644 finished/ipxe-default-3928.pxe ${pkgdir}/usr/lib/ipxe/ipxe-default-3928.pxe
}

sha256sums=('SKIP'
            'a72ebfc3ab294cdf0d2a495597e84e87feba0fb48b618b504c1a8fc277b7366d'
            'e60a4c6569e426a6ce7298a17c4f062d73391aa00aff5085694fee98a2618c8f'
            'b3c9ee1bac1f85f9e8d9455abd94e4a3a0627d2b54ad030e29e247168db2dc83'
            'f7ec78e26671f4df90d89440d8b2a69473c15cb6b25dda32c773023378fec42a'
            'e26a54b4e99816b34baebcb7a15d99d57c9395c9689ffbae2329cc675248f9b9')
