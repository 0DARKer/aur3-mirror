pkgname=fusedav-git
pkgver=20130213
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A webdav filesystem based on FUSE"
url="https://github.com/pantheon-systems/fusedav/"
license=('GPL')
depends=('fuse' 'neon' 'leveldb' 'systemd')
makedepends=('git' 'lynx')

_gitroot=git://github.com/pantheon-systems/fusedav.git
_gitname=$pkgname

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  NOCONFIGURE=True ./bootstrap.sh
  LEVELDB_LIBS='-L/usr/lib -lleveldb' LEVELDB_CFLAGS='-I/usr/include' \
     ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

  # seems that 'make install' doesn't install docs and man pages
  # so copy them manually

  mkdir -p "$pkgdir/usr/share/doc/$pkgname-$pkgver"
  cp doc/README doc/README.html doc/style.css "$pkgdir/usr/share/doc/$pkgname-$pkgver/"

  mkdir -p "$pkgdir/usr/share/man/man1"
  cp doc/fusedav.1 "$pkgdir/usr/share/man/man1/"
  gzip "$pkgdir/usr/share/man/man1/fusedav.1"
}

# vim:set ts=2 sw=2 et:
