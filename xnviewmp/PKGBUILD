# Maintainer:  Houlala <houlala 'at' gmail 'dot' com>
# Contributor: Olivier Duclos <olivier.duclos gmail.com>
# Contributor: wido <widomaker2k7@gmail.com>
# Contributor: VuDu <vudu 'dot' curse 'at' gmail 'dot' com>

pkgname=xnviewmp
pkgver=0.50
pkgrel=4
pkgdesc="An efficient multimedia viewer, browser and converter (beta release)."
url="http://newsgroup.xnview.com/viewtopic.php?f=60&t=25892"

arch=('x86_64' 'i686')
license=('custom')
depends=('glib2' 'expat' 'libpng12')

install='xnviewmp.install'

source=("http://download.xnview.com/XnViewMP-linux.tgz")
md5sums=('bc26ba6427fc969c823c28813fe32724')

if [ "$CARCH" = 'x86_64' ]; then
  source=("http://download.xnview.com/XnViewMP-linux-x64.tgz")
  md5sums=('c3271268196486ae4505f362722213dc')
fi


package() {
  install -d -m755 "${pkgdir}/opt/${pkgname}"
  install -d -m755 "${pkgdir}/usr/bin"
  install -d -m755 "${pkgdir}/usr/share/applications"

  cp -R "${srcdir}/XnView"/* "${pkgdir}/opt/${pkgname}"
  ln -s "/opt/${pkgname}/xnview.sh" "${pkgdir}/usr/bin/${pkgname}"

  cat <<-_EOF_ > "${pkgdir}/usr/share/applications/${pkgname}.desktop"
[Desktop Entry]
Encoding=UTF-8
Terminal=0
Exec=${pkgname}
Icon=/opt/${pkgname}/xnview.png
Type=Application
Categories=Graphics;
StartupNotify=true
Name=XnView Multi Platform
GenericName=XnViewMP
MimeType=image/bmp;image/gif;image/x-portable-bitmap;image/x-portable-pixmap;image/x-psd;image/x-tga;image/x-xbitmap;image/tiff;image/jpeg;image/x-psd;image/png;image/x-icon;image/x-xpixmap;image/svg+xml;
_EOF_

  install -D -m644 "${srcdir}/XnView/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
