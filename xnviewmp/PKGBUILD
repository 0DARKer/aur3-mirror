# Maintainer:  Houlala <houlala 'at' gmail 'dot' com>
# Contributor: Olivier Duclos <olivier.duclos gmail.com>
# Contributor: wido <widomaker2k7@gmail.com>
# Contributor: VuDu <vudu 'dot' curse 'at' gmail 'dot' com>

pkgname=xnviewmp
pkgver=0.64
pkgrel=1
pkgdesc="An efficient multimedia viewer, browser and converter."
url="http://www.xnview.com/en/xnviewmp/"

arch=('x86_64' 'i686')
license=('custom')
depends=('glib2' 'expat' 'libpng12' 'gstreamer0.10-base' 'desktop-file-utils')

install='xnviewmp.install'

source=("http://download.xnview.com/XnViewMP-linux.tgz"
        "xnviewmp.desktop")
md5sums=('4b90cb21d5775b0d2785534693ee2bdc'
         '76c157c2644a6ac1ed75be92e2adeb62')

if [ "$CARCH" = 'x86_64' ]; then
  source=("http://download.xnview.com/XnViewMP-linux-x64.tgz"
          "xnviewmp.desktop")
  md5sums=('9038c20e2861f72de6a7698a0e4895b5'
           '76c157c2644a6ac1ed75be92e2adeb62')
fi


package() {
  install -d -m755 "${pkgdir}/opt/${pkgname}"
  install -d -m755 "${pkgdir}/usr/bin"
  install -d -m755 "${pkgdir}/usr/share/applications"

  cp -R "${srcdir}/XnView"/* "${pkgdir}/opt/${pkgname}"
  ln -s "/opt/${pkgname}/xnview.sh" "${pkgdir}/usr/bin/${pkgname}"

  cp "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  install -D -m644 "${srcdir}/XnView/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
