# Maintainer: Iru Cai <mytbk920423\x40gmail 'dot' com>
# Contributor:  Houlala <houlala 'at' gmail 'dot' com>
# Contributor: Olivier Duclos <olivier.duclos gmail.com>
# Contributor: wido <widomaker2k7@gmail.com>
# Contributor: VuDu <vudu 'dot' curse 'at' gmail 'dot' com>

pkgname=xnviewmp
pkgver=0.68
pkgrel=2
pkgdesc="An efficient multimedia viewer, browser and converter."
url="http://www.xnview.com/en/xnviewmp/"

arch=('x86_64' 'i686')
license=('custom')
depends=('glib2' 'expat' 'libpng12' 'gstreamer0.10-base' 'desktop-file-utils')
optdepends=('gvfs: support for moving files to trash')

install='xnviewmp.install'

source=("http://download.xnview.com/XnViewMP-linux.tgz"
        "xnviewmp.desktop")
md5sums=('57cf3b9387f1b8cf6f199d54477fe8d0'
         '76c157c2644a6ac1ed75be92e2adeb62')

if [ "$CARCH" = 'x86_64' ]; then
  source=("http://download.xnview.com/XnViewMP-linux-x64.tgz"
          "xnviewmp.desktop")
  md5sums=('059baf72c2557f7dd4eab8e7d6eeb417'
           '76c157c2644a6ac1ed75be92e2adeb62')
fi


package() {
  install -d -m755 "${pkgdir}/opt/${pkgname}"
  install -d -m755 "${pkgdir}/usr/bin"
  install -d -m755 "${pkgdir}/usr/share/applications"

  cp -R "${srcdir}/XnView"/* "${pkgdir}/opt/${pkgname}"
  ln -s "/opt/${pkgname}/xnview.sh" "${pkgdir}/usr/bin/${pkgname}"

  cp "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  install -D -m644 "${srcdir}/XnView/license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
