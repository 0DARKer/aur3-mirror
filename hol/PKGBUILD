# Maintainer: Nikolaos Bezirgiannis <bezeria@gmail.com>
pkgname=hol
pkgver=kananaskis.9
pkgrel=1
pkgdesc="Interactive proof assistant for higher order logic"
url='http://hol.sourceforge.net/'
arch=('i686' 'x86_64')
license=('BSD')
source=(http://sourceforge.net/projects/hol/files/hol/${pkgver//./-}/${pkgver//./-}.tar.gz)
depends=('mosml')
optdepends=('graphviz')
md5sums=('b1e1f114c64ce32709ccd9da30fc2d7c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver//./-}"
  echo "val mosmldir = \"/usr/bin\";" > config-override
  mosml < tools/smart-configure.sml 
  bin/build
}

package() {
  _oldtop="${srcdir}/${pkgname}-${pkgver//./-}"
  _newtop="/opt/hol"

  cd $_oldtop

  # fix broken symlinks
  for _link in $(find ${_oldtop}/sigobj/ -type l)
  do
    _oldsrc=$(readlink -v $_link)
    ln -sfn ${_oldsrc/$_oldtop/$_newtop} $_link
  done

  # fix references inside text files
  sed -i -e 's,'"$_oldtop"','"$_newtop"',g' bin/hol bin/hol.bare bin/hol.bare.noquote bin/hol.noquote sigobj/SRCFILES

  # install hol programs and libraries under /opt/hol
  install -m755 -d "${pkgdir}/opt/${pkgname}"
  install -m644 std.prelude "${pkgdir}/opt/${pkgname}"
  cp -r src "${pkgdir}/opt/${pkgname}"
  cp -r sigobj "${pkgdir}/opt/${pkgname}"
  cp -r bin "${pkgdir}/opt/${pkgname}"
  cp -r tools "${pkgdir}/opt/${pkgname}"

  # install license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"

  # make symlinks to /usr/bin
  mkdir -p ${pkgdir}/usr/bin
  ln -s /opt/hol/bin/hol ${pkgdir}/usr/bin/hol
  ln -s /opt/hol/bin/hol.noquote ${pkgdir}/usr/bin/hol.noquote
  ln -s /opt/hol/bin/Holmake ${pkgdir}/usr/bin/Holmake

}
