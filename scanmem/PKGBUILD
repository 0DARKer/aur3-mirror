# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org> 
# Contributor: Elmo Todurov <todurov@gmail.com>
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=scanmem
pkgver=0.15
pkgrel=1
pkgdesc="Memory scanner designed to isolate the address of an arbitrary variable in an executing process"
url="https://github.com/scanmem/scanmem"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('pygtk' 'python-gobject' 'polkit')
makedepends=('intltool')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('0038550889fbe023785f70691e89452a1250e56862144d6ab256df221e7a6ac5e85f40cfa7f18a0d7aa775ea5ca2ebf95dcab13c2364233600623c3fbea15673')

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --enable-gui --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
