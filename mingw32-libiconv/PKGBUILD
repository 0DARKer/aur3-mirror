# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-libiconv
pkgver=1.13.1
pkgrel=2
arch=(any)
pkgdesc="libiconv (mingw32)"
depends=(mingw32-runtime)
makedepends=(mingw32-gcc)
options=(!strip !buildflags)
license=(LGPL)
url="http://www.gnu.org/software/libiconv/"
source=(http://ftp.gnu.org/pub/gnu/libiconv/libiconv-$pkgver.tar.gz)
md5sums=('7ab33ebd26687c744a37264a330bbe9a')

build()
{
  cd "$srcdir/libiconv-$pkgver"

  export CFLAGS="-O2 -pipe -march=i686 -mms-bitfields"
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-nls
  make
}

package() {
  cd "$srcdir"/libiconv-$pkgver
  make install DESTDIR="$pkgdir"

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/{share/{doc,man},info,bin/iconv}
  rm usr/i486-mingw32/lib/charset.alias
}
