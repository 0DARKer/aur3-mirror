# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mingw32-libiconv
pkgver=1.14
pkgrel=1
arch=(any)
pkgdesc="libiconv (mingw32)"
depends=(mingw32-runtime)
makedepends=(mingw32-gcc)
options=(!strip !buildflags)
license=(LGPL)
url="http://www.gnu.org/software/libiconv/"
source=(http://ftp.gnu.org/pub/gnu/libiconv/libiconv-$pkgver.tar.gz)
md5sums=('e34509b1623cec449dfeb73d7ce9c6c6')

build() {
  cd "$srcdir/libiconv-$pkgver"

  export CFLAGS="-O2 -pipe -march=i686 -mms-bitfields"
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-nls
  make
}

package() {
  cd "$srcdir"/libiconv-$pkgver
  make install DESTDIR="$pkgdir"

  cd "$pkgdir"
  find . -name '*.a' -o -name '*.dll' | xargs -rtl1 i486-mingw32-strip -g
  rm -rf usr/i486-mingw32/{share/{doc,man},info,bin/iconv}
  rm usr/i486-mingw32/lib/charset.alias
}
