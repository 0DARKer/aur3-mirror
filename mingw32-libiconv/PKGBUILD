pkgname=mingw32-libiconv
pkgver=1.14
pkgrel=3
arch=(any)
pkgdesc="libiconv (mingw32)"
depends=(mingw32-runtime)
makedepends=(mingw32-gcc)
options=(!strip !buildflags !libtool)
conflicts=(mingw32-libiconv-static)
provides=(mingw32-libiconv-static)
license=(LGPL)
url="http://www.gnu.org/software/libiconv/"
source=(http://ftp.gnu.org/pub/gnu/libiconv/libiconv-$pkgver.tar.gz)
md5sums=('e34509b1623cec449dfeb73d7ce9c6c6')

build() {
  cd "$srcdir/libiconv-$pkgver"

  export CFLAGS="-O2 -pipe -march=i686 -mms-bitfields"
  unset LDFLAGS

  ./configure \
    --prefix=/usr/i486-mingw32 \
    --host=i486-mingw32 \
    --build=$CHOST \
    --disable-nls
  make
}

package() {
  cd "$srcdir"/libiconv-$pkgver
  make install DESTDIR="$pkgdir"

  cd "$pkgdir"
  rm -rf usr/i486-mingw32/{share/{doc,man},info}
  rm usr/i486-mingw32/lib/charset.alias
  i486-mingw32-strip $pkgdir/usr/i486-mingw32/bin/*.exe
  i486-mingw32-strip -x $pkgdir/usr/i486-mingw32/bin/*.dll
  i486-mingw32-strip -g $pkgdir/usr/i486-mingw32/lib/*.a
}
