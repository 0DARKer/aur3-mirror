# Maintainer: milomouse <vincent[at]fea.st>
# Contributor: judd <jvinet[at]zeroflux.org>

_basename=util-linux-ng
pkgname=${_basename}-aes
pkgver=2.17.2
pkgrel=2
pkgdesc="Miscellaneous system utilities for Linux, with loop-AES support"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
license=('GPL2')
arch=('i686' 'x86_64')
groups=('base')
depends=('bash' 'ncurses>=5.7' 'zlib')
conflicts=('linux32' 'util-linux' 'util-linux-ng' 'e2fsprogs<1.41.8-2')
replaces=('linux32' 'util-linux' 'util-linux-ng')
provides=('linux32' 'util-linux' "util-linux-ng=$pkgver-$pkgrel")
options=('!libtool')
_loopaesdate=20100314
_loopaespatch=${_basename}-${pkgver}-${pkgrel}-aes-${_loopaesdate}.diff
source=(ftp://ftp.kernel.org/pub/linux/utils/${_basename}/v2.17/${_basename}-${pkgver}.tar.bz2
        ${_loopaespatch})
optdepends=('perl: for chkdupexe support')
install=util-linux-ng-aes.install

build() {
  # have a before and after work directory
  rm -rf "${srcdir}/${_basename}-${pkgver}-build"
  cp -rp "${srcdir}/${_basename}-${pkgver}" "${srcdir}/${_basename}-${pkgver}-build"
  cd "${srcdir}/${_basename}-${pkgver}-build"
  # provide loop-aes
  patch -Np1 -i "${srcdir}/${_loopaespatch}" || return 1
  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i hwclock/hwclock.c || return 1
  mkdir -p "${pkgdir}/var/lib/hwclock" || return 1
  autoreconf || return 1
  automake || return 1
  ./configure --enable-arch --enable-write --enable-raw --disable-wall --enable-rdev --enable-partx || return 1
  make HAVE_SLN=yes ADD_RAW=yes || return 1
  make HAVE_SLN=yes ADD_RAW=yes DESTDIR="${pkgdir}" install || return 1
  # remove files
  rm -f "${pkgdir}/bin/kill"
  rm -f "${pkgdir}/usr/share/man/man1/kill.1"
  rm -f "${pkgdir}/usr/share/man/man5/nfs.5"
  rm -f "${pkgdir}/usr/share/info/dir"
}

md5sums=(
      '4635725a3eef1c57090bac8ea5e082e6'  # util-linux-ng-2.17.2.tar.bz2
      '8575df321099d1d22826407bdf822207') # util-linux-ng-2.17.2-2-aes-20100314.diff
sha256sums=(
      'c9ae801b6a5ab20b7749a278a8bf6830ef53adc5e8b7eb0ac1a9f410c774118f'  # util-linux-ng-2.17.2.tar.bz2
      '4784c2ca503826fb6bcb40e49a2f19647100d587dec9f2584c166832647c5b9d') # util-linux-ng-2.17.2-2-aes-20100314.diff
