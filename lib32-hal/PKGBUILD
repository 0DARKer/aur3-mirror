# Maintainer: josephgbr <rafael.f.f1 at gmail.com>
# Contributor: webmjm <web.jdm at gmail.com>

_pkgbasename=hal
pkgname=lib32-$_pkgbasename
pkgver=0.5.14
pkgrel=7
pkgdesc="Hardware Abstraction Layer (32 Bit)"
arch=('x86_64')
license=('GPL' 'custom:AFL')
url="http://www.freedesktop.org/wiki/Software/hal"
depends=('lib32-dbus-glib' 'lib32-libusb-compat' 'lib32-udev'
	 'lib32-util-linux' $_pkgbasename)
makedepends=('gcc-multilib' 'gperf' 'hal-info' 'eject' 'filesystem' 
	     'usbutils' 'pm-utils' 'pciutils' 'dmidecode' 'consolekit')
options=('!libtool' '!makeflags')
source=(http://hal.freedesktop.org/releases/${_pkgbasename}-${pkgver}.tar.gz
        hal-0.5.9-hide-diagnostic.patch
        hal-remove-dell-killswitch.patch
        hal-KVM-evdev.patch
        hal-HDAPS-blacklist.patch
        hal-xen-unignore-axes.patch
        hal-use-at-console.patch
        fix-libusb-detection.patch
        dbus-error-init.patch
        path-max.patch
        handle-input-touchpad.patch
        macbook-fix-ioperm.patch
        hal-ignore-internal-dm-devices.patch
        dont-build-hald-probe-video4linux.patch)
md5sums=('e9163df591a6f38f59fdbfe33e73bf20'
         '4d4b6801a1cedca22b8bdd9db73b16fb'
         '6d87c3e63184ae3a69caafc846f538a3'
         '6507e5091ee2d11a87ae738a8e2caecb'
         '1171c2d83b76059f2da7a3538e08fa4e'
         '52bd305299aa22ae07f1a862c22d30fa'
         '3f11234fb5e5044fbfc5199ec65b182c'
         'da088f12cfc2d190bbf95b747e19ab9e'
         '634f4ec2203eff7de8fa2ed2c6b9cbe0'
         'f8c9b3a40d03907b498feef571d42466'
         '0575677614db0632b17b1a719798c7e0'
         '46a5db3ff896ee37762aa8d7e70942c4'
         '17bc2b911f245cef4a1d98769d5c4d14'
         '43d8edfdd81a581c53bd4a1cb436b265')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i "${srcdir}/hal-0.5.9-hide-diagnostic.patch"
  patch -Np1 -i "${srcdir}/hal-remove-dell-killswitch.patch"
  patch -Np1 -i "${srcdir}/hal-KVM-evdev.patch"
  patch -Np1 -i "${srcdir}/hal-HDAPS-blacklist.patch"
  patch -Np1 -i "${srcdir}/hal-xen-unignore-axes.patch"
  patch -Np1 -i "${srcdir}/hal-use-at-console.patch"
  patch -Np1 -i "${srcdir}/fix-libusb-detection.patch"
  patch -Np1 -i "${srcdir}/dbus-error-init.patch"
  patch -Np1 -i "${srcdir}/path-max.patch"
  patch -Np1 -i "${srcdir}/handle-input-touchpad.patch"
  patch -Np1 -i "${srcdir}/macbook-fix-ioperm.patch"
  patch -Np1 -i "${srcdir}/hal-ignore-internal-dm-devices.patch"
  patch -Np1 -i "${srcdir}/dont-build-hald-probe-video4linux.patch"
  
  libtoolize --force
  aclocal
  autoconf
  automake

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib32/hal --libdir=/usr/lib32 --with-udev-prefix=/etc \
      --disable-{docbook-docs,gtk-doc,policy-kit,smbios} \
      --disable-{acl-management,acpi-ibm,console-kit} \
      --enable-{umount-helper,static=no} \
      --with-{hal-user,hal-group}=hal \
      --with-pid-file=/var/run/hald.pid \
      CC='gcc -m32'

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,sbin,var,usr/{bin,include,sbin,share}}
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/COPYING"
}
