# Maintainer: Dustin Falgout <dustin@falgout.us>
# Contributor: Gifts <gifts.antichat@gmail.com>
# Contributor: Andrey Vlasovskikh <andrey.vlasovskikh@gmail.com>

pkgname=pycharm-eap
_buildver=138.2401
_pkgver=4.0.0
pkgver=${_pkgver}.${_buildver}
pkgrel=1
pkgdesc="Powerful Python and Django IDE, Early Access Program (EAP) build. Professional edition."
arch=('any')
options=('!strip')
url="http://www.jetbrains.com/pycharm/"
license=("custom")
depends=("java-environment>=6")
changelog="CHANGES"
source=("http://download.jetbrains.com/python/pycharm-professional-${_buildver}.tar.gz")
md5sums=('42af5b8657f247b5e1bb999d9a735b7a')

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}/opt/${pkgname}"
  cp -R "${srcdir}/pycharm-${_buildver}/"* "${pkgdir}/opt/${pkgname}"
  if [[ $CARCH = 'i686' ]]; then
     rm -f "${pkgdir}/opt/${pkgname}/bin/libyjpagent-linux64.so"
     rm -f "${pkgdir}/opt/${pkgname}/bin/fsnotifier64"
  fi
(
cat <<EOF
[Desktop Entry]
Version=$pkgver
Type=Application
Name=PyCharm-EAP
Exec="/opt/$pkgname/bin/pycharm.sh" %f
Icon=/opt/$pkgname/bin/pycharm.png
Comment=$pkgdesc
Categories=Development;IDE;
Terminal=false
StartupNotify=true
StartupWMClass=jetbrains-pycharm
EOF
) > "${srcdir}/${pkgname}.desktop"

  mkdir -p "${pkgdir}/usr/bin/"
  mkdir -p "${pkgdir}/usr/share/applications/"
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/"
  install -Dm644 "${srcdir}/pycharm-${_buildver}/license/PyCharm_license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/PyCharm_license.txt"
  ln -s "/opt/${pkgname}/bin/pycharm.sh" "${pkgdir}/usr/bin/pycharm-eap"
}

