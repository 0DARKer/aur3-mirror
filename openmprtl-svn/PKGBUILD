# Maintainer: Arch User <alaviss0@gmail.com>
pkgname=openmprtl-svn
pkgver=r202590
pkgrel=1
pkgdesc="Intel(R) OpenMP* Runtime Library"
arch=("x86_64" "i686")
url="http://openmp.llvm.org/"
license=("MIT" "custom:UIUC")
depends=('glibc')
makedepends=('gcc>=4.6.2' 'subversion')
provides=('openmprtl')
conflicts=('openmprtl')
source=("openmp::svn+http://llvm.org/svn/llvm-project/openmp/trunk")
md5sums=("SKIP")

_svndir=openmp

pkgver() {
	cd "$srcdir/$_svndir/"
	local ver="$(svnversion)"
	printf "r%s" "${ver//[[:alpha:]]}"
}	

build() {
	cd "$srcdir/$_svndir/runtime"
	make compiler=gcc
}

package() {
	cd "$srcdir/$_svndir/runtime"
	mkdir -p "$pkgdir/usr/"{include,lib}/
	install -m644 "$(find tmp -mindepth 1 -maxdepth 1 -type d)/"*.h "$pkgdir/usr/include/"
	install -m755 "$(find tmp -mindepth 1 -maxdepth 1 -type d)/stripped/libiomp5.so" "$pkgdir/usr/lib"
	install -Dm644 "../LICENSE.txt" "${pkgdir}/usr/share/licenses/openmprtl/LICENSE"
}
