# Maintainer: Filip Brcic <brcha@gna.org>
pkgname=mingw-w64-libxml2
pkgver=2.7.8
pkgrel=1
arch=('any')
pkgdesc="libxml2 (mingw-w64)"
depends=('mingw-w64-crt' 'mingw-w64-gettext' 'mingw-w64-zlib')
makedepends=('mingw-w64-gcc')
options=('!buildflags' '!strip')
license=('LGPL')
url="http://www.xmlsoft.org/"
source=("ftp://xmlsoft.org/libxml/libxml2-${pkgver}.tar.gz")
md5sums=('8127a65e8c3b08856093099b52599c86')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "${srcdir}/libxml2-${pkgver}"

  unset LDFLAGS
  export CFLAGS="${CFLAGS} -mms-bitfields"

  for _arch in ${_architectures}; do
    mkdir build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --build="${CHOST}" \
      --without-python \
      --without-threads
    make
    popd
  done
}

package()
{
  for _arch in ${_architectures}; do
    cd "${srcdir}/libxml2-${pkgver}/build-${_arch}"

    make install DESTDIR="${pkgdir}"

    rm -rf "${pkgdir}/usr/${_arch}"/share/{doc,gtk-doc,man}
    rm -f "${pkgdir}/usr/${_arch}/lib/charset.alias"
  done
}
