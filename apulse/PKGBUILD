# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=apulse
pkgver=0.1.4
pkgrel=1
pkgdesc="PulseAudio emulation for ALSA"
arch=('i686' 'x86_64')
url="https://github.com/i-rinat/apulse"
license=('custom:MIT')
depends=('alsa-lib' 'glib2')
makedepends=('cmake')
install="$pkgname.install"
source=("$url/archive/v$pkgver.tar.gz"
        "asoundrc.sample")
md5sums=('0408eb9c350daa31657c215b8e86a8c4'
         '9d5955f853690cc0685cc3a0364982e2')

prepare() {
  cd apulse-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd apulse-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd apulse-$pkgver/b
  make DESTDIR="$pkgdir/" install

  # sample asoundrc
  install -Dm644 ../../${source[1]} \
    "$pkgdir/usr/share/$pkgname/${source[1]}"

  # license
  install -Dm644 ../LICENSE.MIT \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
