# Maintainer: TDY <tdy@archlinux.info>

pkgname=ttf-beteckna
pkgver=0.4
pkgrel=1
pkgdesc="A geometric sans serif typeface inspired by the popular Futura"
arch=('any')
url="http://gnu.ethz.ch/linuks.mine.nu/beteckna/"
license=('GPL')
depends=('fontconfig' 'xorg-font-utils')
makedepends=('fontforge')
install=ttf.install
source=(http://gnu.ethz.ch/linuks.mine.nu/${pkgname/*-}/${pkgname/*-}-$pkgver.tar.gz)
md5sums=('01c12138e0f689f4acdacb105f4350a7')

build() {
  cd "$srcdir/${pkgname/*-}-$pkgver"
  fontforge -script sfd2ttf.ff *.sfd 2> /dev/null
}

package() {
  cd "$srcdir/${pkgname/*-}-$pkgver"
  install -dm755 "$pkgdir/usr/share/fonts/TTF"
  install -m644 *.ttf "$pkgdir/usr/share/fonts/TTF"
}

# vim:set ts=2 sw=2 et:
