# Maintainer: Zom Aur <zom[at]eevul[dot]org>

pkgname=quassel-indicator
pkgver=0.7.3
pkgrel=2
pkgdesc="Qt4 IRC client with a separate core, with added indicator patches from kubuntu"
url="http://quassel-irc.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kdelibs' 'oxygen-icons' 'hicolor-icon-theme' 'libindicate-qt')
makedepends=('cmake' 'automoc4')
provides=('quassel' 'quasselcore' 'quasselclient')
conflicts=('quassel' 'quasselcore' 'quasselclient')
source=("${url}/pub/quassel-${pkgver}.tar.bz2"
        "kubuntu_02_enable_message_indicator.diff"
        "rc.quassel"
        "quassel.conf"
        "quassel.service")
install=quassel.install
md5sums=('f12b2b09d8ebe533781aa969597d671c'
         'f242253672d03578e623da5b108d429f'
         '91e414cb313430a2360f1a594b1d6ed3'
         '479229556ba0dd94e3ce28e31e5deea9'
         '4200db3bec89cb928907805df56f0d10')

build() {
  cd ${srcdir}/quassel-${pkgver}
  patch -p1 <../kubuntu_02_enable_message_indicator.diff

  cd ${srcdir}
  [[ ! -d build ]] && mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=1 \
    -DWITH_OPENSSL=ON \
    -DWITH_LIBINDICATE=ON \
    -DCMAKE_BUILD_TYPE="Release" \
    ../quassel-${pkgver}/ \
    -Wno-dev

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/rc.quassel \
    ${pkgdir}/etc/rc.d/quassel
  install -D -m644 ${srcdir}/quassel.conf \
    ${pkgdir}/etc/conf.d/quassel
  install -D -m644 ${srcdir}/quassel.service \
    ${pkgdir}/lib/systemd/system/quassel.service
}
