# Maintainer: Mantas M. <grawity@gmail.com>
pkgname=realmd-git
_pkgname=realmd
pkgver=0.15.1.r2.g40f9583
pkgrel=1
pkgdesc="DBus service for configuring kerberos and other online identities."
arch=(i686 x86_64)
url="http://freedesktop.org/software/realmd/"
license=(GPL3)
depends=(dbus krb5 openldap polkit)
makedepends=(docbook-xsl git intltool packagekit python2 xmlto)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git://anongit.freedesktop.org/realmd/$_pkgname")
sha1sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"
  git describe | sed 's/-/.r/; s/-/./g'
}

build() {
  cd "$srcdir/$_pkgname"
  NOCONFIGURE=1 ./autogen.sh
  ./configure \
    --prefix=/usr           \
    --sbindir=/usr/bin      \
    --sysconfdir=/etc       \
    --localstatedir=/var    \
    --with-distro=defaults  ;
  make
}

package() {
  cd "$srcdir/$_pkgname"
  make DESTDIR="$pkgdir" install
}

# vim: ts=2:sw=2:et
