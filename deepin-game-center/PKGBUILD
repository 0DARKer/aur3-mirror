# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=deepin-game-center
pkgver=git20131114141207~54b1664316
_realver=1.0.0+${pkgver}
pkgrel=2
pkgdesc="Game center for Linuxdeepin"
arch=('any')
url="https://github.com/manateelazycat/deepin-software-center"
license=('GPL2')
depends=('deepin-ui' 'flashplugin' 'hicolor-icon-theme' 'python2' 'python2-deepin-storm'
	 'python2-pystorm' 'python2-scipy' 'python2-jswebkit')
install=$pkgname.install
source=("http://packages.linuxdeepin.com/deepin/pool/main/d/$pkgname/${pkgname}_${_realver}.tar.gz")
sha256sums=('df7232dccd8a63c31a169dc88179d823aa00fef32f9c6cd3501bf5fcb8ac3868')

prepare() {
  cd $srcdir/${pkgname}-${_realver}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' \
  {src/*.py,src/_share/*.py,tools/*.py,move.py}
}

package() {
  cd $srcdir/${pkgname}-${_realver}
  
  mkdir -p "${pkgdir}"/usr/{bin,share/{${pkgname},applications,icons/hicolor/48x48/apps}}

  cp -rf {app_theme,image,locale,skin,src,static,weibo_skin,weibo_theme} "$pkgdir/usr/share/${pkgname}"

  install -m 0755 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/"
  install -m 0755 "debian/${pkgname}.png" "${pkgdir}/usr/share/icons/hicolor/48x48/apps/"
  ln -s /usr/share/${pkgname}/src/${pkgname}.py "${pkgdir}/usr/bin/${pkgname}"
}
