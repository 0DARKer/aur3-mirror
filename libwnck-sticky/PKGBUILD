# Contributor: Connor Behan  <connor.behan@gmail.com>
pkgname=libwnck-sticky
pkgver=2.30.7
pkgrel=1
pkgdesc="WNCK that shows sticky windows properly in the pager"
arch=(i686 x86_64)
license=('LGPL')
depends=('gtk2>=2.19.7' 'startup-notification>=0.10' 'libxres')
makedepends=('pkgconfig' 'libxt>=1.0.6' 'intltool')
replaces=("libwnck")
provides=("libwnck=$pkgver")
options=('!libtool')
url="http://www.gnome.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/libwnck/2.30/libwnck-${pkgver}.tar.bz2 sticky.patch)

build() {
  cd "${srcdir}/libwnck-${pkgver}"
  patch -Np1 -i ${srcdir}/sticky.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}
package() {
  cd "${srcdir}/libwnck-${pkgver}"
  make DESTDIR="${pkgdir}" install
}


md5sums=('9bef4b8560acca78cd6f08a95039af9c' '50af9a71f68dd5ade109a30ecbe06c3f')
