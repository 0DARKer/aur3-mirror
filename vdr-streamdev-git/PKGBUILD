# Contributor: David Spicer <azleifel at gmail dot com>

pkgname=vdr-streamdev-git
_pluginname=streamdev
pkgver=20130328
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="A VDR plugin that implements the Video Transfer Protocol (VTP)"
url="http://projects.vdr-developer.org/projects/plg-streamdev"
license=('GPL2')
depends=('vdr>=1.7.42')
makedepends=('git')
provides=('vdr-streamdev')
conflicts=('vdr-streamdev')
backup=('var/lib/vdr/plugins/streamdev-server/streamdevhosts.conf')
install=${pkgname}.install

_gitroot=git://projects.vdr-developer.org/vdr-plugin-streamdev.git
_gitname=vdr-plugin-streamdev.git

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  # Clone source to fresh build directory
  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
  
  make
}

package() {
  cd "${srcdir}/$_gitname-build"

  # VDR directory environment
  _vdrCONFDIR="/var/lib/vdr"
  _vdrINCDIR="/usr/include"
  _vdrLIBDIR="/usr/lib/vdr"
  _vdrLOCDIR="/usr/share/locale"

  make DESTDIR="${pkgdir}" install

  # Install configuration etc. files
  install -d -m755 "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}-server"
  install -m755 ${_pluginname}-server/externremux.sh "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}-server"
  install -m644 ${_pluginname}-server/streamdevhosts.conf "${pkgdir}${_vdrCONFDIR}/plugins/${_pluginname}-server"
  
  # Install documents
  install -d -m755 "${pkgdir}/usr/share/doc/${pkgname}"
  for _file in CONTRIBUTORS HISTORY PROTOCOL README; do
    install -m644 ${_file} "${pkgdir}/usr/share/doc/${pkgname}/${_file}"
  done
}
