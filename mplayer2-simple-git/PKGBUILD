# Maintainer: loonyphoenix < loonyphoenix at gmail >
# This package is based on mplayer2-git, but simplifies its building by linking to the system ffmpeg library

pkgname=mplayer2-simple-git
pkgver=20110405
pkgrel=1
pkgdesc="A media player"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
depends=('ffmpeg' 'fontconfig' 'libass' 'libgl' 'libxss' 'libxxf86dga' 'ttf-dejavu')
makedepends=('git' 'libpulse' 'mesa' 'python2' 'unzip' 'videoproto' 'yasm')
optdepends=('pulseaudio: for pulse output')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer')
conflicts=('mplayer')
source=('mplayer.desktop')
md5sums=('af6ba58cc34322bf12e46e8d1d42d75f')

_gitroot="http://git.mplayer2.org/mplayer2/"
_gitname="mplayer2"

build() {

  # Custom CFLAGS may break the mplayer2 build
  unset CFLAGS
  unset LDFLAGS

  cd "${srcdir}"
  if [[ -d "${_gitname}" ]]; then
    cd "${_gitname}"
    git pull
  else
    git clone "${_gitroot}" "${_gitname}" && cd "${_gitname}"
  fi

  cd "${srcdir}"
  rm -rf "${_gitname}-build"
  cp -r "${_gitname}" "${_gitname}-build"
  cd "${_gitname}-build"

  ./configure --prefix=/usr \
    --language=all \
    --confdir=/etc/mplayer

  msg "Starting make..."

  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  make DESTDIR="${pkgdir}" install

  install -m644 etc/{codecs,input,example}.conf "${pkgdir}/etc/mplayer/"
  install -d -m755 "${pkgdir}/usr/share/mplayer"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "${pkgdir}/usr/share/mplayer/subfont.ttf"

  # there is a desktop file where the xpm is, but we don't want that
  install -Dm644 "${srcdir}/mplayer.desktop" "${pkgdir}/usr/share/applications/mplayer.desktop"
  install -Dm644 "${srcdir}/${_gitname}/etc/mplayer.xpm" "${pkgdir}/usr/share/pixmaps/mplayer.xpm"
}
