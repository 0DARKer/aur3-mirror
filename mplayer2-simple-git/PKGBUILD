# Maintainer: loonyphoenix < loonyphoenix at gmail >
# This package is based on mplayer2-git, but simplifies its building by
# linking to the system ffmpeg/libav libraries

pkgname=mplayer2-simple-git
pkgver=20111023
pkgrel=1
pkgdesc="A media player"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
depends=('ffmpeg' 'fontconfig' 'libass' 'libdvdread' 'libdvdnav' 'libgl'
         'libxss' 'libxxf86dga' 'ttf-dejavu')
makedepends=('git' 'libpulse' 'live-media' 'mesa' 'videoproto' 'yasm')
optdepends=('libdvdcss: for playback of encrypted dvds'
            'unrar: for playback of media files inside rar archives')
# Some additional makedepends can be added if specific features are needed.
# For example, libvdpau, fribidi, libspeex, codecs from AUR for win32 
# codecs. If you don't need pulse output, you might want to remove
# libpulse from makedpends.
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer' 'mplayer2')
conflicts=('mplayer')
source=('mplayer.desktop')
md5sums=('af6ba58cc34322bf12e46e8d1d42d75f')

_gitroot="git://git.mplayer2.org/mplayer2.git"
_gitname="mplayer2"

build() {

  # Custom CFLAGS may break the mplayer2 build
  unset CFLAGS
  unset LDFLAGS

  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./configure --prefix=/usr \
    --language=all \
    --disable-xvid \
    --confdir=/etc/mplayer
  # there's probably no reason left why anyone would want to use xvid's decoder

  make
}

package() {
  cd "${srcdir}/${_gitname}-build"

  make DESTDIR="${pkgdir}" install

  install -m644 etc/{codecs,input,example}.conf "${pkgdir}/etc/mplayer/"
  install -d -m755 "${pkgdir}/usr/share/mplayer"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "${pkgdir}/usr/share/mplayer/subfont.ttf"

  # there is a desktop file where the xpm is, but we don't want that
  install -Dm644 "${srcdir}/mplayer.desktop" "${pkgdir}/usr/share/applications/mplayer.desktop"
  install -Dm644 "${srcdir}/${_gitname}/etc/mplayer.xpm" "${pkgdir}/usr/share/pixmaps/mplayer.xpm"
}
